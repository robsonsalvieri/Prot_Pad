#INCLUDE "TLPP-CORE.TH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TAFA614.CH"

//Define os indices do array que totaliza os tributos por documento
#define TTCODTRI 			01 	//Id do codigo do tributo
#define TTCST    			02	//Id do codigo CST
#define TTCFOP   			03 	//Id do codigo CFOP
#define TTCODSER 			04 	//Id do codigo do servico
#define TTVLOPE  			05 	//Valor da operacao
#define TTBASE   			06 	//Valor base do tributo
#define TTALIQ   			07 	//Aliquota do tributo
#define TTVALOR  			08 	//valor do tributo
#define TTBASEQT 			09 	//valor consolidado da base do tributo em quantidade
#define TTVLRPAU 			10 	//Valor da pauta do tributo ou valor tributavel
#define TTBASENT 			11 	//Valor base não tributavel
#define TTVLNT   			12 	//Valor nao tributavel
#define TTVLISEN 			13 	//Valor isento
#define TTVLOUTR 			14 	//Outros valores
#define TTVLSCRE 			15 	//Valor sem aproveitamento de credito
#define TTICMNDE 			16 	//Valor icms/st anterior
#define IN_PROGRESS_STATUS	"1" //Integração iniciada
#define	SUCCESS_STATUS		"2" //Integração concluída
#define PROCESS_INCLUSION	"1"	//Processo de inclusão

//Define o tamanho do array que totaliza os tributos por documento(Quantidade total dos indices citados acima)
#define TamArrayTrib 16

//A partir dessas constantes vai definir o nome da procedure a ser chamada
#DEFINE PROCEDURE_CODE			"27"
#DEFINE PROCEDURE_NAME			"TAF613"
#DEFINE PROCEDURE_STATUS_ERROR 	"0"

Static aT01Data			:= Nil														as array
Static cProcRealName 	:= xProcedures(GetSPName(PROCEDURE_NAME, PROCEDURE_CODE)) 	as character
Static oError 			:= ErrorClass():New() 										as object
Static __lDtCpIss		:= .F. 														as logical

/*-----------------------------------------------------------
{Protheus.doc} TAFA614
Programa principal para consulta dos dados na base do ERP
@author Carlos Eduardo Nonato
@since 06/06/2023
//---------------------------------------------------------*/
Function TAFA614()
Local cQuery as character
Local cAliasQryPri 	as character
Local cMVEstado   	:= GetNewPar('MV_ESTADO' ,'') as character
Local cUFRESpd    	:= GetNewPar('MV_UFRESPD','') as character
Local cCampSb1 		:= SuperGetMV('MV_TPAPSB1',.F.) as character
Local oDadosIntegra := {"documentosFiscais": array(0)} as json
Local oDadosComplemento := {"informacoesEspecificasComplementares": JsonObject():New()} as json
Local cChvDocFiscal as character
Local cCpoSimpNac as character
Local oStInfCompDocs as object
Local oStDocArrecadRef as object
Local oStDocFiscalRef as object
Local oStCupomFiscalRef as object
Local oStColEnt as object
Local oStFatura as object
Local oStIdentDocumentos as object
Local oStCompComunicacao as object
Local oStConhecimentoCarga as object
Local oStDocsInf as object
Local oStImport as object
Local oStCombus as object
Local oStObserv as object
Local oAgrpCDA := JsonObject():New() as json
Local oStProcess as object
Local oStMedic as object
Local oStGuns  as object
Local oStCar  as object
Local oStIndicativoSuspensaoProcesso as object
Local oStRessarcimentoICMS as object
Local cCodEspecie as character
Local oStArmCombustivel as object
Local oStEnergiaAguaGas as object	
Local aTotalizaSFT := array(4) as array //Declarado com a quantidade de campos que serao totalizados na funcao SomaItensDocumento
Local lGravaD420 as logical
Local lGravaC500 as logical
Local oCodTrib as object						   
Local aTotaisTrib := {} as array
Local lDocErro as logical
Local oStatementSFT	:= Nil as object
Local cChavCda := "" as character
Local cKeyErp as character
Local cIdLogC20 as character
Local cFilialT01 := xFilial('T01')
Local cFilialC20 := xFilial('C20')
Local cEspecie  as character
Local cCodCNO as character 
Local cIdObra as character
Local aArea as Array
Local cUfEst := "" as character
Local cCodUF := "" as character

Private cCodSit	as character
Private nPosDocFiscal as numeric
Private cKeyJson := oDadosIntegra:GetNames()[1] as character
Private lBuild as logical  //Variavel necessária para usar a funcao TMSCodMun()

Private cKeyInfEspecificas := 'informacoesEspecificasComplementares' as character
Private cKeyInfoComp := 'informacoesComplementares' as character
Private cKeyDocArref := 'documentosArrecadacaoReferenciados' as character
Private cKeyNFRef	 := 'documentosFiscaisReferenciados' as character
Private cKeyNF := 'documentosFiscais' as character
Private cKeyCupomRef := 'cupomFiscalReferenciado' as character
Private cKeyColEnt   := 'localColetaEntrega' as character
Private cKeyFatura	 := 'faturas' as character
Private cKeyColEntTransp := 'cargaTransportadaColetaEntrega' as character
Private cKeyComunicacao := 'comunicacaoTelecomunicacao' as character
Private cKeyAquaviario := 'conhecimentoAquaviarioCargas' as character
Private cKeyAereo := 'conhecimentoAereoCargas' as character
Private cKeyCompDocsInf := 'complementoDocumentosInformados' as character
Private cKeyRodoviario 	:= 'conhecimentoRodoviarioMultimodalCargas'  as character
Private cKeyImportacoes	 := 'importacao' as character
Private cKeyCombustiveis := 'combustivel' as character
Private cKeyObservacoes := 'lancamentosFiscais' as character
Private cKeyLancamentos := 'observacoesLancamentosFiscais' as character
Private cKeyProcessos := 'processosReferenciados' as character
Private cKeyCompEnergiaAguaGas := 'energiaEletricaGasAgua'
Private cKeyItens	:= 'itens' as character
Private cKeyTributo := 'tributos' as character
Private cKeyMedicamentos := 'medicamentos' as character
Private cKeyArmas := 'armasFogo' as character
Private cKeyVeiculos  := 'veiculosNovos' as character
Private cKeyTotais := 'totais' as character
Private cKeyValoresTributo := 'valoresTributo' as character
Private cKeyMsgCpl	  := 'mensagemcomplemento' as character
Private cKeyArmCombustivel := 'armazenamentoCombustiveis' as character
Private cKeyRessarICMSST := 'ressarcimentosICMSST' as character
Private cKeyServicoTransp := 'servicoTransporte' as character
Private cKeyInfAjustesIPI := 'infAdicAjustesIpi' as character

Private cSM0CodMun	  := FWSM0Util():GetSM0Data( cEmpAnt , cFilAnt , {'M0_CODMUN'} )[1][2] as character
Private cOperSemFrete := GetNewPar( 'MV_OPSEMF' , '' ) as character
Private nSPDIFC 	  := SuperGetMV( 'MV_SPDIFC',.f.,0) as numeric
Private lIntTMS   	  := SuperGetMv("MV_INTTMS", .F., .F.) as logical
Private aDadosSM0	  := FWSM0Util():GetSM0Data() as array
Private cMVSUBTRIB 	  := SpedPrSeq("MV_SUBTRIB") as character
Private cMV_DifTr 	  := IIf(FindFunction("GETSUBTRIB"),GetSubTrib("",.T.),SuperGetMv("MV_SUBTRIB")) as character
Private lMvItXML 	  := SuperGetMV('MV_CSDXML',.F.,.F.) as logical
Private cMvSPDTC95	  := SuperGetMV('MV_SPDTC95',.F.) as character
Private nDifalDest	:= 01 as numeric
Private nDifalOrig 	:= 02 as numeric
Private nAlqDifalC 	:= 03 as numeric
Private nDifalFecp 	:= 04 as numeric
Private nAlqFecpCm 	:= 05 as numeric
Private nItem 		:= 0 as numeric
Private aInfcpl as array
Private cNotaNf as character
Private cTipoMovimento as character
Private aRegD510 := {} as array
Private aDadosLog	:= {} as array
Private oStPesquisaLog as object

/*Proteção para gravação da data de Competência do ISS*/
__lDtCpIss := C20->(FieldPos("C20_DTCPIS")) > 0



TAFConout("Carregando registros dos documentos fiscais a serem integrados...",,, "INTEGRAÇÃO LIVROS FISCAIS X TAF") // "Carregando registros dos documentos fiscais a serem integrados..." / "INTEGRAÇÃO LIVROS FISCAIS X TAF"

cQuery := " SELECT "
cQuery += " 	C20.C20_FILIAL, "
cQuery += " 	C20.C20_CHVNF, "
cQuery += " 	SFT.FT_FILIAL, "
cQuery += " 	SFT.FT_ESTADO, "
cQuery += " 	SFT.FT_TIPO , " 
cQuery += " 	SFT.FT_TIPOMOV, "
cQuery += " 	SFT.FT_NFISCAL , "  
cQuery += " 	SFT.FT_SERIE, "	
cQuery += " 	SFT.FT_CLIEFOR, "
cQuery += " 	SFT.FT_LOJA, "
cQuery += " 	SFT.FT_ITEM, "
cQuery += " 	SFT.FT_PRODUTO, "
cQuery += " 	SFT.FT_ESPECIE, "
cQuery += " 	SFT.FT_ENTRADA, "
cQuery += " 	SFT.FT_RGESPST, "
cQuery += " 	SFT.FT_NORESP, "
cQuery += " 	SFT.FT_DTCANC, "
cQuery += " 	SFT.FT_CFOP, "
cQuery += " 	SFT.FT_NFORI, "
cQuery += " 	SFT.FT_SERORI, "
cQuery += " 	SFT.FT_CODISS, "
cQuery += " 	SFT.FT_RECISS, "
cQuery += " 	SFT.FT_BASEICM, "
cQuery += " 	SFT.FT_VALICM, "
cQuery += " 	SFT.FT_OUTRICM, "
cQuery += " 	SFT.FT_ISENICM, "
cQuery += " 	SFT.FT_ICMSCOM, "
cQuery += " 	SFT.FT_DIFAL, "
cQuery += " 	SFT.FT_ICMSRET, "
cQuery += " 	SFT.FT_ALIQSOL, "
cQuery += " 	SFT.FT_BASERET, "
cQuery += " 	SFT.FT_CREDST, "
cQuery += " 	SFT.FT_ALIQIPI, "
cQuery += " 	SFT.FT_BASEIPI, "
cQuery += " 	SFT.FT_ISENIPI, "
cQuery += " 	SFT.FT_VALIPI, "
cQuery += " 	SFT.FT_OUTRIPI, "
cQuery += " 	SFT.FT_BASEPIS, "
cQuery += " 	SFT.FT_VALPIS, "
cQuery += " 	SFT.FT_ALIQPIS, "
cQuery += " 	SFT.FT_PAUTPIS, "
cQuery += " 	SFT.FT_BASEPS3, "
cQuery += " 	SFT.FT_CSTPIS, "
cQuery += " 	SFT.FT_BASECOF, "
cQuery += " 	SFT.FT_VALCOF, "
cQuery += " 	SFT.FT_ALIQCOF, "
cQuery += " 	SFT.FT_PAUTCOF, "
cQuery += " 	SFT.FT_BASECF3, "
cQuery += " 	SFT.FT_CSTCOF, "
cQuery += " 	SFT.FT_ALIQPS3, "
cQuery += " 	SFT.FT_ALIQCF3, "
cQuery += " 	SFT.FT_VRETPIS, "
cQuery += " 	SFT.FT_VRETCOF, "
cQuery += " 	SFT.FT_BASEIRR, "
cQuery += " 	SFT.FT_BASEINS, "
cQuery += " 	SFT.FT_BSCP15, "
cQuery += " 	SFT.FT_BSCP20, "
cQuery += " 	SFT.FT_BSCP25, "
cQuery += " 	SFT.FT_ALIQINS, "
cQuery += " 	SFT.FT_VALIRR, "
cQuery += " 	SFT.FT_ALIQIRR, "
cQuery += " 	SFT.FT_VALANTI, "
cQuery += " 	SFT.FT_BRETCSL, "
cQuery += " 	SFT.FT_PDDES, "
cQuery += " 	SFT.FT_VFCPDIF, "
cQuery += " 	SFT.FT_BASECPB, "
cQuery += " 	SFT.FT_ALIQCPB, "
cQuery += " 	SFT.FT_VALCPB, "
cQuery += " 	SFT.FT_BSSENAR, "
cQuery += " 	SFT.FT_ALSENAR, "
cQuery += " 	SFT.FT_VLSENAR, "
cQuery += " 	SFT.FT_CREDST, "
cQuery += " 	SFT.FT_BASEFUN, "
cQuery += " 	SFT.FT_VALFUN, "
cQuery += " 	SFT.FT_ALIQFUN, "
cQuery += " 	SFT.FT_ALIQICM, "
cQuery += " 	SFT.FT_CSTISS, "
cQuery += " 	SFT.FT_MARGEM, "
cQuery += " 	SFT.FT_OBSICM, "
cQuery += " 	SFT.FT_TNATREC, "
cQuery += " 	SFT.FT_CNATREC, "
cQuery += " 	SFT.FT_GRUPONC, "
cQuery += " 	SFT.FT_DTFIMNT, "
cQuery += " 	SFT.FT_VLCP15, "
cQuery += " 	SFT.FT_VLCP20, "
cQuery += " 	SFT.FT_VLCP25, "
cQuery += " 	SFT.FT_ARETCOF, "
cQuery += " 	SFT.FT_BRETCOF, "
cQuery += " 	SFT.FT_ARETPIS, "
cQuery += " 	SFT.FT_BRETPIS, "
cQuery += " 	SFT.FT_VALCF3, "
cQuery += " 	SFT.FT_BASECF3, "
cQuery += " 	SFT.FT_VALPS3, "
cQuery += " 	SFT.FT_ARETCSL, "
cQuery += " 	SFT.FT_VRETCSL, "
cQuery += " 	SFT.FT_BASEDES, "
cQuery += " 	SFT.FT_ESTADO, "
cQuery += " 	SFT.FT_OUTRRET, "
cQuery += " 	SFT.FT_CLASFIS, "
cQuery += " 	SFT.FT_CTIPI, "
cQuery += " 	SFT.FT_ISENRET, "
cQuery += " 	SFT.FT_QUANT, "
cQuery += " 	SFT.FT_VALINS, "
cQuery += " 	SFT.FT_FORMULA, "
cQuery += " 	SFT.FT_FORMUL, "
cQuery += " 	SFT.FT_OBSERV, "
cQuery += " 	SFT.FT_PDV, "
cQuery += " 	SF2.F2_NFCUPOM, "
cQuery += " 	SF2.F2_UFORIG, "
cQuery += " 	SF2.F2_CMUNOR, "
cQuery += " 	SF4.F4_ANTICMS, "
cQuery += " 	SF4.F4_SITTRIB, "
cQuery += " 	SF4.F4_ICM, "
cQuery += " 	SF4.F4_CREDICM, "
cQuery += " 	SF4.F4_LFICM, "
cQuery += " 	SF4.F4_CONSUMO, "
cQuery += " 	SF4.F4_CODOBSE, "
cQuery += " 	SF4.F4_CSTPIS, "
cQuery += " 	SF4.F4_CSTCOF, "
cQuery += " 	GIC.GIC_CODRMD, "
If __lDtCpIss
	cQuery += " 	SF1.F1_DTCPISS, "
Endif
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SD1.D1_CNO "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_CNO "
cQuery += " 	END CODCNO,  
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_PREFIXO "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_PREFIXO "
cQuery += " 	END PREFIXO,   "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_DUPL "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_DUPL "
cQuery += " 	END DUPL,   "
cQuery += " 	SFT.FT_EMISSAO, " 
cQuery += " 	SFT.FT_CHVNFE, "  
cQuery += " 	SFT.FT_ENTRADA, "  
cQuery += " 	SFT.FT_DESCICM + SFT.FT_DESCZFR VL_ABAT_NT,  " 
cQuery += " 	SFT.FT_CONTA, " 
cQuery += " 	SFT.FT_TOTAL, "	
cQuery += " 	SFT.FT_IDTRIB, "
//-- Integração com o SIGATMS
If lIntTMS
	cQuery += " 	coalesce(DT6.R_E_C_N_O_,0) RECDT6, "
	cQuery += " 	coalesce(DT6.DT6_DEVFRE,' ') DT6_DEVFRE, "
	cQuery += " 	coalesce(DT6.DT6_CDRORI,' ') COD_MUN_ORI, "
	cQuery += " 	coalesce(DT6.DT6_CDRCAL,' ') COD_MUN_DEST, "
	cQuery += " 	DT6.DT6_FILDOC, "
	cQuery += " 	DT6.DT6_DOC, "
	cQuery += " 	DT6.DT6_SERIE, "
	cQuery += " 	DT6.DT6_FILDCO, "
	cQuery += " 	DT6.DT6_DOCDCO, "
	cQuery += " 	DT6.DT6_SERDCO, "
	cQuery += " 	DT6.DT6_FILORI, "
	cQuery += " 	DT6.DT6_DOCTMS, "
	cQuery += " 	DT6.DT6_CLIREM, "
	cQuery += " 	DT6.DT6_LOJREM, "
	cQuery += " 	DT6.DT6_CLICAL, "
	cQuery += " 	DT6.DT6_LOJCAL, "
	cQuery += " 	DT6.DT6_CLICON, "
	cQuery += " 	DT6.DT6_LOJCON, "
	cQuery += " 	DT6.DT6_CLIDPC, "
	cQuery += " 	DT6.DT6_LOJDPC, "
	cQuery += " 	DT6.DT6_TIPFRE, "
	cQuery += " 	DT6.DT6_CDRDES, "
	cQuery += " 	coalesce(DT6.DT6_CLIDES,' ') CLIDES, "
	cQuery += " 	coalesce(DT6.DT6_LOJDES,' ') LOJDES, "			
EndIf				  
cQuery += "		C0U.C0U_ID ID_TIPO_DOC, "
cQuery += " 	CASE WHEN SFT.FT_DESPESA - (SFT.FT_SEGURO + SFT.FT_FRETE) > 0 THEN SFT.FT_DESPESA - (SFT.FT_SEGURO + SFT.FT_FRETE) else 0 END VL_OUT_DESP, " 
cQuery += " 	CASE WHEN SFT.FT_TIPO = 'S' AND SFT.FT_CODISS <> '' AND SFT.FT_NFELETR <> '' THEN SFT.FT_NFELETR else SFT.FT_NFISCAL END NUM_DOC, " 
cQuery += " 	CASE WHEN SFT.FT_TIPOMOV = 'E' THEN '0' else '1' END IND_OPER,   "
cQuery += " 	CASE WHEN SFT.FT_FORMUL = 'S' OR ( SFT.FT_FORMUL = ' '	AND SFT.FT_TIPOMOV = 'S') THEN '0' else '1' END IND_EMIT,   " 
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B','D') THEN 'SA2' "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('B','D')		THEN 'SA2' "
cQuery += " 		ELSE 'SA1' "
cQuery += " 	END FOR_CLI, "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_VALBRUT "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_VALBRUT "
cQuery += " 	END VL_DOC,   "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_DESCONT "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_DESCONT "
cQuery += " 	END VL_DESC,   "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_CODISS = '' AND SFT.FT_TIPO <> 'S' THEN SF1.F1_VALMERC "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_CODISS = '' AND SFT.FT_TIPO <> 'S' THEN SF2.F2_VALMERC "
cQuery += " 	END VL_MERC,   "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_CODISS <> '' AND SFT.FT_TIPO = 'S' THEN SF1.F1_VALMERC "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_CODISS <> '' AND SFT.FT_TIPO = 'S' THEN SF2.F2_VALMERC "
cQuery += " 	END VL_SERV, "
cQuery += " 	CASE "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_DESPESA " 
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_DESPESA "
cQuery += " 	END VL_DA, " 
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_SEGURO "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_SEGURO "
cQuery += " 	END VL_SEG, "
cQuery += " 	CASE  "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_FRETE "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_FRETE "
cQuery += " 	END VL_FRT, "
cQuery += " 	CASE "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES IN (' ','1') THEN SA2.A2_COD_MUN "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES = '2' AND SF1.F1_ESTPRES <> ' ' AND SF1.F1_INCISS <> ' ' THEN SF1.F1_INCISS "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES = '2' THEN 'SM0' "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES IN (' ','1') THEN 'SM0' "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES = '2' AND SF2.F2_ESTPRES <> ' ' AND SF2.F2_MUNPRES <> ' ' THEN SA1.A1_COD_MUN "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('B', 'D' ) AND SB1.B1_MEPLES = '2' THEN SF2.F2_MUNPRES "
cQuery += " 	END COD_MUN_SERV, "
cQuery += "		coalesce((SELECT DISTINCT CDT.CDT_INDFRT "
cQuery += "				  FROM " + RetSqlName('CDT') + " CDT "
cQuery += "				  WHERE CDT.CDT_FILIAL = '" + xFilial('CDT') + "'"
cQuery += "					AND CDT.CDT_TPMOV = SFT.FT_TIPOMOV "
cQuery += "					AND CDT.CDT_DOC = SFT.FT_NFISCAL "
cQuery += "					AND CDT.CDT_SERIE = SFT.FT_SERIE "
cQuery += "					AND CDT.CDT_CLIFOR = SFT.FT_CLIEFOR "
cQuery += "					AND CDT.CDT_LOJA = SFT.FT_LOJA "
cQuery += "					AND CDT.D_E_L_E_T_ = ' ') "
cQuery += "		,'X') CDT_INDFRT, "
cQuery += "		CASE "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'E' THEN SF1.F1_MENNOTA "
cQuery += " 		WHEN SFT.FT_TIPOMOV = 'S' THEN SF2.F2_MENNOTA "
cQuery += " 	END MENNOTA, "
cQuery += " 	DHR.DHR_BASPIS, "
cQuery += " 	DHR.DHR_BASCOF, "
cQuery += " 	DHR.DHR_BASCSL, "
cQuery += " 	DHR.DHR_VLRIR, "
cQuery += " 	DHR.DHR_VLRPIS, "
cQuery += " 	DHR.DHR_VLRCOF, "
cQuery += " 	DHR.DHR_VLRCSL, "
cQuery += " 	DHR.DHR_BASEIR, "
cQuery += " 	SD1.D1_ALIQCMP, "
cQuery += " 	SD1.D1_AVLINSS, "
cQuery += " 	SD1.D1_BASEFUN, "
cQuery += " 	SD1.D1_ALIQFUN, "
cQuery += " 	SD1.D1_VALFUN, "
cQuery += " 	SD1.D1_ALFCCMP, "
cQuery += " 	SD2.D2_ALIQCMP, "
cQuery += " 	SD2.D2_ABSCINS, "
cQuery += " 	SD2.D2_BASEFUN, "
cQuery += " 	SD2.D2_ALIQFUN, "
cQuery += " 	SD2.D2_VALFUN, "
cQuery += " 	SD2.D2_ALFCCMP, "
cQuery += "		CASE "
cQuery += " 		WHEN COALESCE(CDNA.CDN_CODLST, ' ') <> ' ' THEN CDNA.CDN_CODLST "
cQuery += " 		WHEN COALESCE(CDNB.CDN_CODLST, ' ') <> ' ' THEN CDNB.CDN_CODLST "
cQuery += "			ELSE ' '
cQuery += " 	END CDN_CODLST, "
cQuery += "		coalesce(CD7.R_E_C_N_O_, 0 ) CD7RECNO  , "
cQuery += " 	coalesce(CD7.CD7_LOTE  , '') CD7_LOTE  , " 
cQuery += " 	coalesce(CD7.CD7_QTDLOT, 0 ) CD7_QTDLOT, "
cQuery += " 	coalesce(CD7.CD7_FABRIC, '') CD7_FABRIC, "
cQuery += " 	coalesce(CD7.CD7_VALID , '') CD7_VALID , "
cQuery += " 	coalesce(CD7.CD7_REFBAS, '') CD7_REFBAS, "
cQuery += " 	coalesce(CD7.CD7_TPPROD, '') CD7_TPPROD, "
cQuery += " 	coalesce(CD7.CD7_PRECO , 0 ) CD7_PRECO , "
cQuery += "		coalesce(F0A.R_E_C_N_O_, 0 ) F0ARECNO  , "
cQuery += "		coalesce(F0A.F0A_LOTE  , '') F0A_LOTE  , "
cQuery += "		coalesce(F0A.F0A_QTDLOT, 0 ) F0A_QTDLOT, "
cQuery += "		coalesce(F0A.F0A_FABRIC, '') F0A_FABRIC, "
cQuery += "		coalesce(F0A.F0A_VALID , '') F0A_VALID , "
cQuery += " 	SA2.A2_CALCIRF, "
cQuery += " 	SA2.A2_TIPO, "
cQuery += " 	SA2.A2_IRPROG, "	
cQuery += "		CASE
cQuery += "			WHEN (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('B', 'D')) OR (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO IN ('B','D' )) THEN SA1.A1_EST
cQuery += "			WHEN (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D')) OR (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('B','D' )) THEN SA2.A2_EST
cQuery += "		END UF_PART,	
cQuery += "		CASE
cQuery += "			WHEN (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ('B', 'D')) OR (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO IN ('B' ,'D' )) THEN SA1.A1_CGC
cQuery += "			WHEN (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D')) OR (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('B' ,'D' )) THEN SA2.A2_CGC
cQuery += "		END CNPJ_PART,
cQuery += "		CD9.R_E_C_N_O_ CD9RECNO,"
cQuery += "		V14.V14_IDC1H COD_PART, "

GetCountCda(@cQuery)
GetCountCdg(@cQuery)
GetSelectItens(@cQuery, cCampSb1)

cQuery += " FROM " + RetSqlName('C20') + " C20 "
cQuery += " 	LEFT JOIN " + RetSqlName('SFT') + " SFT ON "
cQuery += " 				SFT.FT_FILIAL  = '" + xFilial('SFT') + "' AND "
cQuery += " 				SFT.FT_TIPOMOV = (case when C20.C20_INDOPE = '0' then 'E' else 'S' end) AND "
cQuery += " 				C20.C20_NUMDOC = (CASE "
cQuery += "                           WHEN SFT.FT_NFELETR = ' ' THEN SFT.FT_NFISCAL "
cQuery += "                           ELSE SFT.FT_NFELETR "
cQuery += "                       END) AND "
cQuery += " 				SFT.FT_SERIE   = C20.C20_SERIE AND "
cQuery += " 				SFT.FT_CLIEFOR = C20.C20_CLIFOR AND "
cQuery += " 				SFT.FT_LOJA    = C20.C20_LOJA AND "
cQuery += " 				SFT.FT_EMISSAO = C20.C20_DTDOC AND "
cQuery += " 				SFT.D_E_L_E_T_ = ' ' "
cQuery += " 	INNER JOIN " + RetSqlName('SF3') + " SF3 ON "
cQuery += " 				SF3.F3_FILIAL = '" + xFilial('SF3') + "' AND "
cQuery += " 				SF3.F3_SERIE  = SFT.FT_SERIE AND "
cQuery += " 				SF3.F3_NFISCAL = SFT.FT_NFISCAL AND "
cQuery += " 				SF3.F3_CLIEFOR = SFT.FT_CLIEFOR AND "
cQuery += " 				SF3.F3_LOJA    = SFT.FT_LOJA AND "
cQuery += " 				SF3.F3_IDENTFT = SFT.FT_IDENTF3 AND "
cQuery += " 				(SF3.F3_ENTRADA = SFT.FT_ENTRADA OR "
cQuery += " 				SF3.F3_EMISSAO = SFT.FT_EMISSAO) AND "
cQuery += " 				SF3.D_E_L_E_T_ = ' ' "
cQuery += " 	LEFT JOIN " + RetSqlName('SF1') + " SF1 ON  "
cQuery += " 				SFT.FT_TIPOMOV = 'E' AND  "
cQuery += " 				SF1.F1_FILIAL  = '" + xFilial('SF1') + "' AND  "
cQuery += " 				SF1.F1_DOC     = SFT.FT_NFISCAL AND  "
cQuery += " 				SF1.F1_SERIE   = SFT.FT_SERIE AND  "
cQuery += " 				SF1.F1_FORNECE = SFT.FT_CLIEFOR AND  "
cQuery += " 				SF1.F1_LOJA    = SFT.FT_LOJA AND  "
cQuery += " 				SF1.F1_ESPECIE = SFT.FT_ESPECIE AND " 
cQuery += " 				SF1.F1_EMISSAO = SFT.FT_EMISSAO AND " 
cQuery += " 				SF1.D_E_L_E_T_ = ' ' "
cQuery += " 	LEFT JOIN " + RetSqlName('SF2') + " SF2 ON  "
cQuery += " 				SFT.FT_TIPOMOV = 'S' AND  "
cQuery += " 				SF2.F2_FILIAL  = '" + xFilial('SF2') + "' AND "
cQuery += " 				SF2.F2_DOC     = SFT.FT_NFISCAL AND "
cQuery += " 				SF2.F2_SERIE   = SFT.FT_SERIE AND "
cQuery += " 				SF2.F2_CLIENTE = SFT.FT_CLIEFOR AND "
cQuery += " 				SF2.F2_LOJA    = SFT.FT_LOJA AND "
cQuery += " 				SF2.F2_EMISSAO = SFT.FT_EMISSAO AND " 
cQuery += " 				SF2.D_E_L_E_T_ = ' ' "
//-- Cadastro de Clientes
cQuery += " 	LEFT JOIN " + RetSqlName('SA1') + " SA1 ON  "
cQuery += " 				( (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO NOT IN ( 'B', 'D')) OR (SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO IN ( 'B' ,'D' )) ) AND "
cQuery += " 				SA1.A1_FILIAL = '" + xFilial('SA1') + "' AND  "
cQuery += " 				SA1.A1_COD	  = SFT.FT_CLIEFOR AND  "
cQuery += " 				SA1.A1_LOJA   = SFT.FT_LOJA AND "
cQuery += " 				SA1.D_E_L_E_T_ = ' ' "	
//-- Cadastro de Fornecedores
cQuery += " 	LEFT JOIN " + RetSqlName('SA2') + " SA2 ON  "
cQuery += " 				( (SFT.FT_TIPOMOV = 'E'	AND SFT.FT_TIPO NOT IN ('B','D')) OR (SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('B','D')) ) AND  "
cQuery += " 				SA2.A2_FILIAL  = '" + xFilial('SA2') + "' AND  "
cQuery += " 				SA2.A2_COD     = SFT.FT_CLIEFOR AND  "
cQuery += " 				SA2.A2_LOJA    = SFT.FT_LOJA AND  "
cQuery += " 				SA2.D_E_L_E_T_ = ' ' "
//-- Cadastro de Produtos
cQuery += " 	LEFT JOIN " + RetSqlName('SB1') + " SB1 ON "
cQuery += " 				SB1.B1_FILIAL  = '" + xFilial('SB1') + "' AND "
cQuery += " 				SB1.B1_COD     = SFT.FT_PRODUTO AND "
cQuery += " 				SB1.D_E_L_E_T_ = ' ' "
//-- Finalidades Documento Fiscal - TAF
cQuery += " 	LEFT JOIN " + RetSqlName('C0U') + " C0U ON "
cQuery += " 				C0U.C0U_FILIAL  = '" + xFilial('C0U') + "' AND "
cQuery += " 				C0U.C0U_CODIGO  = ( CASE  "
cQuery += "									 		WHEN SFT.FT_TIPO = 'D' THEN '01' "
cQuery += "									 		WHEN SFT.FT_TIPO = 'I' THEN '02' "
cQuery += "									 		WHEN SFT.FT_TIPO = 'P' THEN '03' "
cQuery += "									 		WHEN SFT.FT_TIPO = 'C' THEN '04' "
cQuery += "									 		WHEN SFT.FT_TIPO = 'B' THEN '05' "
cQuery += "									 		WHEN SFT.FT_TIPO = 'S' THEN '06' "
cQuery += "									 		else '00' "
cQuery += "									 	END ) AND "
cQuery += " 				C0U.D_E_L_E_T_ = ' ' "
//-- Tabela de Bilhetes - Gestão de Transportes
cQuery += " 	LEFT JOIN " + RetSqlName('GIC') + " GIC ON "
cQuery += " 		GIC.GIC_FILIAL = '" + xFilial('GIC') + "' AND 
cQuery += " 		GIC.GIC_FILNF  = SFT.FT_FILIAL AND "
cQuery += " 		GIC.GIC_NOTA   = SFT.FT_NFISCAL AND "
cQuery += " 		GIC.GIC_SERINF = SFT.FT_SERIE AND "
cQuery += " 		GIC.GIC_CLIENT = SFT.FT_CLIEFOR AND " 
cQuery += " 		GIC.GIC_LOJA   = SFT.FT_LOJA AND "
cQuery += " 		GIC.D_E_L_E_T_ = ' ' "
//--Resumo de Movimento Diário - Gestão de Transportes
cQuery += " 	LEFT JOIN " + RetSqlName('GZU') + " GZU ON "
cQuery += " 		GZU.GZU_FILIAL = '" + xFilial('GZU') + "' AND "
cQuery += " 		GZU.GZU_DOC    = GIC.GIC_CODRMD AND "
cQuery += " 		GZU.GZU_AGENCI = GIC.GIC_AGENCI AND "
cQuery += " 		GZU.D_E_L_E_T_ = ' ' "
cQuery += " 	LEFT JOIN " + RetSqlName('CD7') + " CD7 ON " //Operacoes com medicamentos
cQuery += "			( TRIM(SFT.FT_ESPECIE) IN ('','NFE','SPED') ) AND " // Modelos 01 ou 55
cQuery += " 		CD7.CD7_FILIAL = '" + xFilial('CD7') + "' AND "
cQuery += " 		CD7.CD7_TPMOV  = SFT.FT_TIPOMOV AND "
cQuery += " 		CD7.CD7_SERIE  = SFT.FT_SERIE AND "
cQuery += " 		CD7.CD7_DOC    = SFT.FT_NFISCAL AND "
cQuery += " 		CD7.CD7_CLIFOR = SFT.FT_CLIEFOR AND "
cQuery += " 		CD7.CD7_LOJA   = SFT.FT_LOJA AND "
cQuery += " 		CD7.CD7_ITEM   = SFT.FT_ITEM AND "
cQuery += " 		CD7.CD7_COD    = SFT.FT_PRODUTO AND"
cQuery += " 		CD7.D_E_L_E_T_ = ' ' "
//-- Tabela de Complemento de Rastreabilidade - Operações com Medicamentos
cQuery += " 	LEFT JOIN " + RetSqlName('F0A') + " F0A ON " 
cQuery += "			( TRIM(SFT.FT_ESPECIE) IN ('','NFE','SPED') ) AND " // Modelos 01 ou 55
cQuery += " 		F0A.F0A_FILIAL = '" + xFilial('F0A') + "' AND " 
cQuery += " 		F0A.F0A_TPMOV  = SFT.FT_TIPOMOV AND "
cQuery += " 		F0A.F0A_SERIE  = SFT.FT_SERIE AND "
cQuery += " 		F0A.F0A_DOC    = SFT.FT_NFISCAL AND "
cQuery += " 		F0A.F0A_CLIFOR = SFT.FT_CLIEFOR AND "
cQuery += " 		F0A.F0A_LOJA   = SFT.FT_LOJA AND "
cQuery += " 		F0A.F0A_ITEM   = SFT.FT_ITEM AND "
cQuery += " 		F0A.F0A_COD    = SFT.FT_PRODUTO AND "
cQuery += " 		F0A.D_E_L_E_T_ = ' ' "
//--CD9 - Complemento veículo automotor
GetLeftJoinCD9(@cQuery)
//--C1L - Identificação do Item - TAF
GetLeftJoinC1L(@cQuery)
//--DHR - NF x Natureza de Rendimento
GetLeftJoinDHR(@cQuery)
//--FKX - Naturezas de Rendimento
GetLeftJoinFKX(@cQuery)
//--F09 - Relacionamento TES x IPM
GetLeftJoinF09(@cQuery)
//--F2Q - Complemento Fiscal - FIS
GetLeftJoinF2Q(@cQuery)
//-- Cod. ISS - TAF
GetLeftJoinCDNA(@cQuery)
//-- Cod. ISS - TAF
GetLeftJoinCDNB(@cQuery)
//--SD1 - Itens das NF de Entrada
GetLeftJoinSD1(@cQuery)
//--SD2 - Itens de Venda da NF
GetLeftJoinSD2(@cQuery)
//--SF4 - Tipos de Entrada e Saida
GetLeftJoinSF4(@cQuery)
//--SB5	- Dados Adicionais do Produto
GetLeftJoinSB5(@cQuery)
//--C0Y	- Cadastro de CFOP - TAF
GetLeftJoinC0Y(@cQuery)
//--C0B	- Codigo Serviço (LCF 116/2003) - TAF
GetLeftJoinC0B(@cQuery)
//--C1N	- Natureza de Operação - TAF
GetLeftJoinC1N(@cQuery)
//--C1C - Plano de Contas Contábeis - TAF
GetLeftJoinC1O(@cQuery)
//--C1P - Centro de Custo - TAF
GetLeftJoinC1P(@cQuery)
//--C8C - Serv. Suj. Ret. Contrib Previd - TAF
GetLeftJoinC8C(@cQuery)
//--LF0 - Itens UF Índice de Part do Mun - TAF
GetLeftJoinLF0(@cQuery)
//--V3O - Natureza de rendimento - TAF
GetLeftJoinV3O(@cQuery)
// Verifica se o parâmetro MV_INTTMS está configurado para integração do TMS.
If lIntTMS 
//--DT6 - Documentos de Transporte - TMS
	GetLeftJoinDT6(@cQuery)
EndIf
//--C1J - Unidade de Medida - TAF
GetInnerJoinC1J(@cQuery)
//--C03 - Origens das Mercadorias - TAF
GetInnerJoinC03(@cQuery)
//--V14 - Integração de Participantes - TAF
GetInnerJoinV14(@cQuery)

cQuery += " WHERE C20.D_E_L_E_T_ = ' ' "
cQuery += " 	AND C20.C20_FILIAL = '" + cFilialC20 + "' "
cQuery += " 	AND C20.C20_STATUS = '" + IN_PROGRESS_STATUS + "' "
cQuery += " 	AND C20.C20_ATUDOC = '" + PROCESS_INCLUSION + "' "
cQuery += " ORDER BY SFT.FT_TIPOMOV, SFT.FT_NFISCAL, SFT.FT_SERIE, SFT.FT_CLIEFOR, SFT.FT_LOJA, SFT.FT_EMISSAO, SFT.FT_ITEM "

cQuery := ChangeQuery(cQuery)

oStatementSFT := FwExecStatement():New(cQuery)	

cAliasQryPri := oStatementSFT:OpenAlias()

If (cAliasQryPri)->(EOF()); TAFConout("Não há registros dos documentos fiscais a serem integrados.",,, "INTEGRAÇÃO LIVROS FISCAIS X TAF"); Else; TAFConout("Gerando JSON com os documentos fiscais a serem integrados...",,, "INTEGRAÇÃO LIVROS FISCAIS X TAF"); EndIf // "Não há registros dos documentos fiscais a serem integrados." / "INTEGRAÇÃO LIVROS FISCAIS X TAF" / "Gerando JSON com os documentos fiscais a serem integrados..." / "INTEGRAÇÃO LIVROS FISCAIS X TAF"

//Seta ordem nas tabelas que serão usadas para consulta antes de integrar os registros no TAF
//Tabelas ERP
SA1->(DbSetOrder(1)) //A1_FILIAL+A1_COD+A1_LOJA                                                                                                                                   
SA2->(DbSetOrder(1)) //A2_FILIAL+A2_COD+A2_LOJA
SFI->(DbSetOrder(1)) //FI_FILIAL+DTOS(FI_DTMOVTO)+FI_PDV+FI_NUMREDZ       
DUD->(DbSetOrder(1)) //DUD_FILIAL+DUD_FILDOC+DUD_DOC+DUD_SERIE+DUD_FILORI+DUD_VIAGEM
DTQ->(DbSetOrder(2)) //DTQ_FILIAL+DTQ_FILORI+DTQ_VIAGEM+DTQ_ROTA  
DTR->(DbSetOrder(1)) //DTR_FILIAL+DTR_FILORI+DTR_VIAGEM+DTR_ITEM          
DUY->(DbSetOrder(1)) //DUY_FILIAL, DUY_GRPVEN                                                                                                                    

//Tabelas TAF
C1H->(DbSetOrder(5)) //C1H_FILIAL+C1H_C1HID
C0Q->(DbSetOrder(1)) //C0Q_FILIAL+C0Q_CODIGO                                                                                                                                           
C1G->(DbSetOrder(1)) //C1G_FILIAL+C1G_NUMPRO+C1G_INDPRO                                                                                                                                      
T01->(DbSetOrder(1)) //T01_FILIAL+T01_CODFIL+T01_ALIAS+T01_REGKEY
V14->(DbSetOrder(1)) //V14_FILIAL+V14_CNPJ+V14_IE+V14_INSCMU+V14_CODMUN+V14_CEP+V14_UF+V14_ORIG+V14_CODIGO+V14_LOJA

while (cAliasQryPri)->(!eof())
	
	aArea:= GetArea() 

	TRY
	if cChvDocFiscal != (cAliasQryPri)->(FT_FILIAL+FT_TIPOMOV+FT_SERIE+FT_NFISCAL+FT_CLIEFOR+FT_LOJA)
		//Zera variavies de totalizadores por documento fiscal
		aRegD510   	:= {}
		aTotaisTrib := {}
		lGravaD420 	:= .F.
		lGravaC500 	:= .F.													
		lDocErro 	:= .F.
		nItem		:= 0
		cIdObra		:= ""
		//Guardo a chave do documento fiscal atual, para ser usado na gravação do log caso ocorra algum erro.
		cKeyErp	:= (cAliasQryPri)->( iif(FT_TIPOMOV == 'E','0','1' ) +'|'+;
				FT_EMISSAO +'|'+;
				FT_SERIE   +'|'+; 
				FT_NFISCAL +'|'+;
				FT_CLIEFOR +'|'+;
				FT_LOJA)			
		
		aFill(aTotalizaSFT, 0)

		//Código do modelo da nota fiscal
		cCodEspecie := AModNot((cAliasQryPri)->FT_ESPECIE)
		//Tratamento para servico
		If AllTrim((cAliasQryPri)->FT_ESPECIE) $ "NFS/RPS/NFSE/NFPS"
			If Empty(cCodEspecie)
				cCodEspecie := "01"
			Endif
		EndIf
		//Estado do emitente do documento fiscal
		cUfEst := (cAliasQryPri)->FT_ESTADO
		//Amarração do documento fiscal com o cadastro de obras
		cCodCNO 	:= (cAliasQryPri)->CODCNO
		//Consulta situacao do documento.
		cCpoSimpNac := iif(alltrim((cAliasQryPri)->FOR_CLI) == 'SA1','SA1->A1_SIMPNAC','SA2->A2_SIMPNAC')
		cCodSit     := SPEDSitDoc(,cAliasQryPri,alltrim((cAliasQryPri)->FOR_CLI),cCpoSimpNac,,,(cMVEstado $ cUFRESpd),.f.,,,'SF4')

		/*Para as notas fiscais de transportes vindas do TMS sempre devera haver um DT6 correspondente, caso nao haja, saltar para a proxima nota.
		Obs.Instrucoes passadas pela equipe do TMS. */		
		if lIntTms .and. (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie $ '07|08|8B|09|10|11|26|27|57' .and. (cAliasQryPri)->RECDT6 = 0 .and. !cCodSit $ '02|03|04|05'
			cChvDocFiscal := (cAliasQryPri)->(FT_FILIAL+FT_TIPOMOV+FT_SERIE+FT_NFISCAL+FT_CLIEFOR+FT_LOJA)
			while (cAliasQryPri)->(!eof()) .and. (cAliasQryPri)->(FT_FILIAL+FT_TIPOMOV+FT_SERIE+FT_NFISCAL+FT_CLIEFOR+FT_LOJA) == cChvDocFiscal 
				(cAliasQryPri)->(DbSkip())
			enddo
			loop	
		endif				
		//Adiciono item no Json a cada nova nota fiscal
		aadd(oDadosIntegra[cKeyJson],JsonObject():New())
		nPosDocFiscal := len(oDadosIntegra[cKeyJson])
		// Verifica se a série da nota fiscal está vinculada a uma espécie válida de acordo com a configuração do MV_ESPECIE
		If ((cAliasQryPri)->FT_TIPOMOV == 'E' .And. SFT->FT_FORMUL == "S" ) .Or. (cAliasQryPri)->FT_TIPOMOV == 'S' 
			cEspecie :=  a460Especie((cAliasQryPri)->FT_SERIE)
			If Empty(cEspecie)
				cMsgLog 	:= STR0010
				aDadosLog 	:= {'C20', (cAliasQryPri)->C20_CHVNF, FwX2Nome('C20'), cMsgLog, .F., 'SFT' }					
				TAFConout(cMsgLog,,, "INTEGRAÇÃO LIVROS FISCAIS X TAF")					
				ErrorHandler(cMsgLog)
			Endif	
		Endif
		//Cadastro de Obras
		If !Empty(cCodCNO)
			GravaDadosT9C(cCodCNO,@cIdObra)
		Endif
		
		//Retorna o codigo da UF conforme a tabela C14
		cCodUF  := RetornaCodUF(cAliasQryPri, cUfEst, cMVEstado)
		//Grava os dados ta tabela C20 - Cabecalho documentos fiscais e retorna o codigo da situacao do documento.
		GravaDadosC20(cAliasQryPri, @oDadosIntegra, cCodEspecie, @cIdObra, cCodUF )

		//Verifica se tem cadastro informcaçções complementares por nota fiscal (T013AA)
		InfComplementaresDoc(cAliasQryPri, @oDadosComplemento, @oStInfCompDocs)

		//Verifica se tem processo referenciado (T013AB)
		If (cAliasQryPri)->QTDPROC > 0
			ProcessosReferenciados(cAliasQryPri, @oDadosComplemento, @oStProcess, cCodEspecie)
		Endif
		
		if cCodEspecie $ ('01|1B|04|55|65') 
			//Verifica se tem complemento de guia de recolhimento (T013AE)
			DocArrecadRef(cAliasQryPri, @oDadosComplemento, @oStDocArrecadRef)

			//Verifica se tem doc. fiscal referenciado (T013AF)
			DocFiscalRef(cAliasQryPri, @oDadosComplemento, @oStDocFiscalRef)

			//Verifica se tem cupom fiscal refenciado (T013AG)
			CupomFiscalRef(cAliasQryPri, @oDadosComplemento, @oStCupomFiscalRef)

			if cCodEspecie != '55'
				//Local de entrega e coleta (T013AH)
				LocaisColetaEntrega(cAliasQryPri, @oDadosComplemento, @oStColEnt)
			endif	
		endif

		if cCodEspecie $ ('01|55')	.and. (cAliasQryPri)->FT_TIPOMOV = 'E'
			//OperaÃ§Ãµes de importação (T013AC)
			OperacoesImportacao(cAliasQryPri, @oDadosComplemento, @oStImport)
		endif 

		if cCodEspecie == '01'
			//Cadastro de faturas e vencimentos das faturas (T013AI e T013AJ)
			CadFaturas(cAliasQryPri, @oDadosIntegra, @oStFatura)

			if (cAliasQryPri)->FT_TIPOMOV == 'S'
				//OPERAÇÕES COM COMBUSTÍVEIS(T013AK)
				OperacoesCombustiveis(cAliasQryPri, @oDadosComplemento, @oStcombus)
			endif
		endif
		
		if lIntTMS .And. (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie $ ('08|09|10|11|26|27') .and. !cCodSit $ '|02|03|04|05|' //Documento cancelado e afins
			
			if cCodEspecie $ '|08|09|10|'
				//Complemento do conhecimento rodoviario, aquaviario ou aereo de cargas (T013AO/T013AU/T013AV - D130/D140/D150 )
				ComplementoRodoAquaAereoCargas(cAliasQryPri, @oDadosComplemento, @oStConhecimentoCarga, cCodEspecie )
			endif	

			//Carga Transportada e local da coleta\entrega(T013AQ - D160 e D161)
			//Identificação dos cosumentos fiscais(T013AW - D162)
			LocaisColetaEntregaTransporte(cAliasQryPri, @oDadosComplemento, @oStIdentDocumentos, cCodEspecie)
		endif

		//Terminal Faturado - Informacao complementares de Seviços de comunicacao e telecomunicacao (T013AR - D530)
		if (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie $ ('21|22')
			ComplementoComunicacaoTelecomunicacao(cAliasQryPri, @oDadosComplemento, @oStCompComunicacao )		
		endif

		//Complemto dos documentos informados (CÓDIGO 13, 14, 15, 16 e 63) (T013AY - D420)
		if (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie $ ('63|13|14|15|16|')
			lGravaD420 := ComplementoDocsInformados(cAliasQryPri, @oDadosComplemento, @oStDocsInf )
		endif	
		
		if cCodEspecie $ ('|06|66|28|29|')
			lGravaC500 := CompEnergiaEletricaAguaGas(cAliasQryPri, @oDadosComplemento, @oStEnergiaAguaGas, cCodEspecie)
		endif	
		If (cAliasQryPri)->QTDLAN > 0
			aAgrupCda := {}
			oAgrpCDA := JsonObject():New()
			AgrupCDA(cAliasQryPri, @oStObserv, @oAgrpCDA, @aAgrupCda)
		Endif									

		if cCodEspecie $ '01|55' .And. oAgrpCDA:HasProperty('IPI')
			//INFORMAÇÕES ADICIONAIS DOS AJUSTES DA APURAÇÃO DO IPI IDENTIFICAÇÃO DOS DOCUMENTOS FISCAIS (E531)
			InfAjustesIPI(cAliasQryPri, @oDadosComplemento, oAgrpCDA['IPI'] )

			//Limpa o json com os ajustes do documento atual
			oAgrpCDA:DelName('IPI')
		endif		

	endif

	if cCodEspecie $ '01|1B|04|55|65' .And. !Empty(oAgrpCDA[(cAliasQryPri)->FT_ITEM])
		//OBSERVAÇÕES DO LANÇAMENTO FISCAL (T013AL e T013AM)
		ObservacoesC195C197(cAliasQryPri, @oDadosComplemento, oAgrpCDA, @cChavCda, @oDadosIntegra)
	endif

	//Caso tenha algum complemento para o documento, incluo no Json
	if oDadosComplemento[cKeyInfEspecificas]:toJson() != '{}'
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas] := oDadosComplemento[cKeyInfEspecificas]
		oDadosComplemento[cKeyInfEspecificas] := JsonObject():New()
	endif

	// Itens do documento fiscal
	SetItens(cAliasQryPri, @oDadosIntegra)

	if cCodEspecie $ ('01|55')
		//OPERAÇÕES COM MEDICAMENTOS (T015AA)
		OperacoesComMedicamentos(cAliasQryPri, @oDadosIntegra, @oStMedic)
		
		//OPERAÇÕES COM VEÍCULOS NOVOS (T015AD)
		If (cAliasQryPri)->CD9RECNO > 0
			OperacoesComVeiculosNovos(cAliasQryPri, @oDadosIntegra, @oStCar)
		Endif	

		if (cAliasQryPri)->FT_TIPOMOV == 'E' .and. !cCodSit $ '|02|03|'
			//Armazenamento de combustíveis
			ArmazenamentoCombustivel(cAliasQryPri, oDadosIntegra, oStArmCombustivel)
		elseif (cAliasQryPri)->FT_TIPOMOV == 'S' .and. !cCodSit $ '|02|03|'
			//Ressarcimento de ICMS-ST
			RessarcimentoICMS(cAliasQryPri, oDadosIntegra, oStRessarcimentoICMS )
		endif

	endif 

	if cCodEspecie == '01' .and. (cAliasQryPri)->FT_TIPOMOV == 'S'
		//OPERAÇÕES COM ARMAS DE FOGO (T015AC)
		OperacoesComArmasDeFogo(cAliasQryPri, @oDadosIntegra, @oStGuns)
	endif 

	if (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie $ ('21|22') .and. !cCodSit $ '|02|03|04|05'
		ComplItensDocComunicaoTelecomunicao(cAliasQryPri, @oDadosIntegra)
	endif	
	
	if lIntTMS .And. (cAliasQryPri)->FT_TIPOMOV == 'S' .and. cCodEspecie == '07' .and. !cCodSit $ '|02|03|04|05'
		ComplItensDocServTransporte(cAliasQryPri, @oDadosIntegra)
	endif	
	
	//Tributos por item 
	TribItem(cAliasQryPri, @oDadosIntegra, @oCodTrib, cCampSb1, cMVEstado, @aTotaisTrib)

	SomaItensDocumento(cAliasQryPri, @aTotalizaSFT)										
	
	//Indicativo de Suspensão por processo judicial/administrativo por item		
	If (cAliasQryPri)->QTDPROC > 0
		SetIndicativoSuspensaoProcesso(cAliasQryPri, @oDadosIntegra, @oStIndicativoSuspensaoProcesso)	
	Endif	
	
	CATCH oError

		//Se der erro na gravação da nota, ela será removida do JSON principal para não ser processada
		oDadosIntegra[cKeyJson][nPosDocFiscal] := Nil
		if len(aDadosLog) = 0; aDadosLog := {'C20', (cAliasQryPri)->C20_CHVNF, '', '', .f., 'SFT'}; endif
		LogIntegra( aDadosLog, cKeyErp )
		TAFConout(oError:Description, 3,, "INTEGRAÇÃO LIVROS FISCAIS X TAF") //"INTEGRAÇÃO LIVROS FISCAIS X TAF"

		lDocErro := .t.

	ENDTRY

	// Independente de dar erro ou não, a nota processada será reservada na variável
	cChvDocFiscal := (cAliasQryPri)->(FT_FILIAL + FT_TIPOMOV + FT_SERIE + FT_NFISCAL + FT_CLIEFOR + FT_LOJA)

	//Guarda o id do documento no TAF caso seja necessário usar para o log de erro
	cIdLogC20 := (cAliasQryPri)->C20_CHVNF
	
	(cAliasQryPri)->(DbSkip())
	
	RestArea(aArea)
	
	//Grava os registros do sped que possuem campos com totalizadores somente para notas que não tiveram erro
	If !lDocErro .And. cChvDocFiscal != (cAliasQryPri)->(FT_FILIAL + FT_TIPOMOV + FT_SERIE + FT_NFISCAL + FT_CLIEFOR + FT_LOJA)
		// Só entra aqui se já possuir nota no JSON principal
		If oDadosIntegra[cKeyJson][nPosDocFiscal] != Nil
			If lGravaD420
				ComplementoDocsInformados(,@oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas],,.t.,aTotalizaSFT)
			EndIf

			If lGravaC500
				CompEnergiaEletricaAguaGas(,@oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas],, cCodEspecie, .t., aTotalizaSFT)
			EndIf
			
			//Valores totais dos tributos agrupados por documento (T013AP)
			TotaisPorDocumento(@oDadosIntegra, aTotaisTrib)
		EndIf

		//Se for integração de documento com correção de algum erro de integração, apaga o registro de log de erro.
		TafCleanLog(cFilialT01, cFilialC20, 'C20', cIdLogC20 )

	EndIf
	//Caso tenha ocorrido erro em algum item, só reinicia o processo de integração quando mudar o documento fiscal
	if lDocErro
		while (cAliasQryPri)->(!eof()) .and. cChvDocFiscal == (cAliasQryPri)->(FT_FILIAL + FT_TIPOMOV + FT_SERIE + FT_NFISCAL + FT_CLIEFOR + FT_LOJA)
			(cAliasQryPri)->(DbSkip())
		enddo
	endif	

enddo

(cAliasQryPri)->(DbCloseArea())

If Empty(oDadosIntegra["documentosFiscais"]); TAFConout("Não há documentos fiscais a serem integrados no JSON.",,, "INTEGRAÇÃO LIVROS FISCAIS X TAF"); EndIf // "Não há documentos fiscais a serem integrados no JSON." / "INTEGRAÇÃO LIVROS FISCAIS X TAF"

TAFFiscalDocumentInsert(oDadosIntegra)

FWFreeObj(oAgrpCDA)
FWFreeObj(oDadosIntegra)

return

/*-----------------------------------------------------------------------
{Protheus.doc} GravaDadosC20
Grava os dados do ERP na tabela C20 do TAF (Cabecalho dos documentos fiscais)
@author Carlos Eduardo Nonato
@since 06/06/2023
//----------------------------------------------------------------------*/
Static Function GravaDadosC20(cAlias as character, oDadosIntegra as json, cCodEspecie as character,;
							  cIdObra as character, cCodUF as character)
Local cIndFrete   	:= GetindTipoFrete(cAlias, cCodSit, cCodEspecie) as character
Local cCodMunServ 	:= GetIdMunServ( iif( alltrim((cAlias)->COD_MUN_SERV) == 'SM0', cSM0CodMun, alltrim((cAlias)->COD_MUN_SERV) )  ) as character
Local cCodModelo  	:= GetTafId('C01',cCodEspecie,1) as character//Id do modelo no TAF
Local cIDCodSit		:= TAFGetIDCodSit(cCodSit) as character
Local lDtContPF     := SuperGetMv("MV_VCTIRPF",.F.,"V") == "C"
Local lDtContPJ     := SuperGetMv("MV_VENCIRF",.F.,"V") == "C" 
Local lPartPF		:= (cAlias)->A2_TIPO == "F" .or. ( (cAlias)->A2_TIPO == "J" .and. (cAlias)->A2_IRPROG == "1" )

oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_FILIAL'] := (cAlias)->C20_FILIAL
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CHVNF' ] := (cAlias)->C20_CHVNF
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_STATUS'] := SUCCESS_STATUS //Integração concluída
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_NUMDOC'] := alltrim((cAlias)->NUM_DOC)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_SERIE' ] := (cAlias)->FT_SERIE
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_INDOPE'] := alltrim((cAlias)->IND_OPER)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_TPDOC' ] := (cAlias)->ID_TIPO_DOC
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_INDEMI'] := alltrim((cAlias)->IND_EMIT)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODPAR'] := alltrim((cAlias)->COD_PART)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_DTDOC' ] := (cAlias)->FT_EMISSAO
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_INDPAG'] := '9'//Sem pagamento - Código fixo
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_INDOCF'] := alltrim((cAlias)->NUM_DOC)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODCTA'] := (cAlias)->FT_CONTA
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_DTES'  ] := (cAlias)->FT_ENTRADA
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CHVELE'] := (cAlias)->FT_CHVNFE
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODORI'] := alltrim((cAlias)->COD_MUN_ORI)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODDES'] := alltrim((cAlias)->COD_MUN_DEST)
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODMOD'] := cCodModelo
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODSIT'] := cIDCodSit
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_CODLOC'] := cCodMunServ 
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_INDFRT'] := cIndFrete
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_IDOBR' ] := cIdObra //Id da obra, caso exista
oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_UF'    ] := cCodUF //Codigo da UF de destino da mercadoria 

If (lPartPF .and. lDtContPF) .or. (!lPartPF .and. lDtContPJ) //tratamento para gravação C20_DTCONT - REINF 2.1.2
	oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_DTCONT'] := (cAlias)->FT_ENTRADA
Endif
If __lDtCpIss //tratamento para integração do campo C20_DTCPIS - GISS Online ABRASF
	oDadosIntegra[cKeyJson][nPosDocFiscal]['C20_DTCPIS'] := (cAlias)->F1_DTCPISS
Endif

//Esses campos da C20 ficam gravados na Aba Totais do Documento Fiscal
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais] := JsonObject():New()
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLMERC'] := (cAlias)->VL_MERC
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLSERV'] := (cAlias)->VL_SERV
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLDESC'] := (cAlias)->VL_DESC
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLABNT'] := (cAlias)->VL_ABAT_NT
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLRDA' ] := (cAlias)->VL_DA
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLRSEG'] := (cAlias)->VL_SEG
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLRFRT'] := (cAlias)->VL_FRT
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLOUDE'] := (cAlias)->VL_OUT_DESP
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLDOC' ] := (cAlias)->VL_DOC
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLABMT'] := 0
oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais]['C20_VLABSU'] := 0

return 


/*-----------------------------------------------------------------------
{Protheus.doc} GravaDadosT9C
Grava os dados do ERP na tabela T9C do TAF (Cadastro de Obra do TAF)
@author Gabriela Luche
@since 28/07/2025
//----------------------------------------------------------------------*/

Static Function GravaDadosT9C(cCodCNO as character, cIdObra as character) 
Local cQuery   		as character
Local cAliasQrySON 	as character
Local cNumCNO 		as character

// Consulta os dados da obra na tabela SON
cQuery += "SELECT "
cQuery += "	SON.ON_FILIAL "    // Filial
cQuery += ",SON.ON_TPINSCR "   // Tipo de Inscricao ( CNPJ ou CNO )
cQuery += ",SON.ON_CNO "       // Numero do Registro ( CNPJ ou CNO )
cQuery += ",SON.ON_CODIGO "    // Codigo da Obra
cQuery += ",SON.ON_DESC "      // Descricao da Obra 
cQuery += ",SON.ON_IDOBRA "    // Indicativo de Prestacao de Serviço
cQuery += ",SON.ON_INDCPRB "   // Contribuicao Previdenciaria 
cQuery += ",CASE WHEN SON.ON_TPOBRA = '2' THEN ? ELSE ? END INDTER " // Indicativo de Terceiro 1= Sim, 2= Não
cQuery += ",SON.ON_CODART "    // codigo ART
cQuery += " FROM " + RetSqlName("SON") + " SON " 
cQuery += " WHERE SON.ON_FILIAL = ? "
cQuery += " AND SON.ON_CODIGO = ? "
cQuery += " AND SON.D_E_L_E_T_ = ? "
cQuery 			:= ChangeQuery(cQuery)
oStatementSON 	:= FwExecStatement():New(cQuery)	

If oStatementSON != Nil
	oStatementSON:SetString(1, '1'					)
	oStatementSON:SetString(2, '2'					)
	oStatementSON:SetString(3, xFilial("SON")		)
	oStatementSON:SetString(4, cCodCNO 				)
	oStatementSON:SetString(5, ' '					)

	cAliasQrySON := oStatementSON:OpenAlias()

	If !(cAliasQrySON)->(EOF())		
		cNumCNO := (cAliasQrySON)->ON_CNO

		DbSelectArea("T9C")
		DbSetOrder(2) // T9C_FILIAL+T9C_NRINSC
		If T9C->(MsSeek(xFilial('T9C')+cNumCNO))
			cIdObra := T9C->T9C_ID	
		Else //Efetua a gravação somente se o registro não existir
			cIdObra := TAFGeraID("TAF") 
			RecLock("T9C", .T.)
			T9C->T9C_FILIAL	:= xFilial("T9C")
			T9C->T9C_ID		:= cIdObra                                                                                                              
			T9C->T9C_TPINSC	:= (cAliasQrySON)->ON_TPINSCR
			T9C->T9C_NRINSC	:= (cAliasQrySON)->ON_CNO
			T9C->T9C_INDOBR	:= (cAliasQrySON)->ON_IDOBRA 
			T9C->T9C_DSCOBR	:= (cAliasQrySON)->ON_DESC
			T9C->T9C_INDTER	:= (cAliasQrySON)->INDTER
			T9C->T9C_CPRB	:= (cAliasQrySON)->ON_INDCPRB
			T9C->T9C_CODART	:= (cAliasQrySON)->ON_CODART 
			T9C->(MsUnlock())
			TAFConout(STR0011,,,STR0012) 
		Endif
	EndIf

	(cAliasQrySON)->(DbCloseArea())

Endif

Return

/*-----------------------------------------------------------------------
{Protheus.doc} InfComplementaresDoc
@description Consulta e carrega o Json com as informacações complementareres por 
				documento (T013AA)
@author Carlos Eduardo Nonato
@since 14/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStInfCompDocs - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function InfComplementaresDoc(cAlias as character, oDadosComplemento as json, oStInfCompDocs as object)
	
	Local aOutputSp 	:= {} 	as array
	Local cQuery 		as character
	Local cAliasCDT 	as character
	Local cProcedure	as character
	Local cDescComp		as character
	Local cKeyReg		as character
	Local cIdC3Q		as character
	Local cMsgLog		as character
	Local iPosInfoComp	as integer
	Local lLogCadastro  as logical

	Default cAlias				:= ""
	Default oDadosComplemento	:= Nil
	Default oStInfCompDocs		:= Nil
	
	If !Empty(cAlias) .And. oDadosComplemento != Nil
		If oStInfCompDocs == Nil
			cQuery := " SELECT "
			cQuery += "		CDT.CDT_IFCOMP, "
			cQuery += "		CDT.CDT_DCCOMP, "
			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID "
			cQuery += " 	FROM " + RetSqlName("CDT") + " CDT "
			cQuery += " 	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
			cQuery += " 		ON C3Q.D_E_L_E_T_ = ' ' "
			cQuery += " 			AND C3Q.C3Q_FILIAL = ? "
			cQuery += " 			AND C3Q.C3Q_CODINF = CDT.CDT_IFCOMP "
			cQuery += " 	WHERE CDT.D_E_L_E_T_   = ? "
			cQuery += " 		AND CDT.CDT_FILIAL = ? " 
			cQuery += " 		AND CDT.CDT_TPMOV  = ? " 
			cQuery += " 		AND CDT.CDT_DOC    = ? " 
			cQuery += " 		AND CDT.CDT_SERIE  = ? " 
			cQuery += " 		AND CDT.CDT_CLIFOR = ? " 
			cQuery += " 		AND CDT.CDT_LOJA   = ? "
			
			cQuery			:= ChangeQuery(cQuery)
			oStInfCompDocs 	:= FwExecStatement():New(cQuery)
		EndIf	

		If oStInfCompDocs != Nil
			oStInfCompDocs:SetString(1, ' ')
			oStInfCompDocs:SetString(2, xFilial("C3Q") 		)
			oStInfCompDocs:SetString(3, xFilial("CDT") 		)
			oStInfCompDocs:SetString(4, (cAlias)->FT_TIPOMOV)
			oStInfCompDocs:SetString(5, (cAlias)->FT_NFISCAL)
			oStInfCompDocs:SetString(6, (cAlias)->FT_SERIE  )
			oStInfCompDocs:SetString(7, (cAlias)->FT_CLIEFOR)
			oStInfCompDocs:SetString(8, (cAlias)->FT_LOJA   )

			cAliasCDT 	:= oStInfCompDocs:OpenAlias()
			cProcedure	:= "TAF613S"

			While (cAliasCDT)->(!EOF())
				if !empty((cAliasCDT)->CDT_IFCOMP)
					cIdC3Q		:= (cAliasCDT)->C3Q_ID
					cDescComp	:= (cAliasCDT)->CDT_DCCOMP
					cKeyReg		:= (cAliasCDT)->CDT_IFCOMP

					If Empty(cIdC3Q)
						cIdC3Q := TAFGeraId("TAF")                                                                                                                
					EndIf

					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
									
					if Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
						cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
						lLogCadastro := .t.
					endif	
				else	
					//Cria a descrição para apresentar no log de falha da integração
					cMsgLog := STR0004 + 'CDT_IFCOMP' //'Obs.: Faltam informações na origem da integração. Campo: '
				endif

				//Caso tenha falha ou erro na integração
				if !empty(cMsgLog)
					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('CDT'), cMsgLog, lLogCadastro, 'SFT' }
					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
				endif
				
				If (cAliasCDT)->(RecNo()) == 1
					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp] := {}
				EndIf	
				
				AAdd(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp], JsonObject():New())

				iPosInfoComp++

				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp]["C21_CODINF"] := alltrim(cIdC3Q)
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp]["C21_DESCRI"] := cDescComp	
				
				(cAliasCDT)->(DbSkip())
			EndDo

			(cAliasCDT)->(DbCloseArea())

			FwFreeArray(aOutputSp)
		EndIf
	EndIf

Return

/*-----------------------------------------------------------------------
{Protheus.doc} DocArrecadRef
@description Consulta e carrega o Json com informações complementares dos documentos 
				de arrecadação referenciados (T013AE)
@author Carlos Eduardo Nonato
@since 15/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStDocArrecadRef - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function DocArrecadRef(cAlias as character, oDadosComplemento as json, oStDocArrecadRef as object)

	Local aOutputSp 	:= {} 	as array
	Local cIdC0R 		as character
	Local cQuery 		as character
	Local cAliasCDC 	as character
	Local cProcedure	as character
	Local cKeyReg		as character
	Local cIdC3Q		as character
	Local cIdC1G		as character
	Local cMsgLog		as character
	Local iPosInfoComp 	as integer
	Local iPosDocArrec 	as integer

	Default cAlias				:= ""
	Default oDadosComplemento	:= Nil
	Default oStDocArrecadRef	:= Nil

	If !Empty(cAlias) .And. oDadosComplemento != Nil
		If oStDocArrecadRef == Nil
			cQuery += " SELECT "
			cQuery += "		CDC.CDC_IFCOMP,
			cQuery += "		SF6.R_E_C_N_O_ RECNO_SF6,
			cQuery += "		COALESCE(CCF.R_E_C_N_O_, 0) RECNO_CCF,
			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID, "
			cQuery += " 	COALESCE(C0R.C0R_ID, ' ') C0R_ID, "
			cQuery += "		COALESCE(C1G.C1G_ID, ' ') C1G_ID "
			cQuery += " FROM " + RetSqlName('CDC') + " CDC "
			cQuery += "	LEFT JOIN " + RetSqlName('SF6') + " SF6 "
		    cQuery += "    ON SF6.D_E_L_E_T_ = ' ' "
		    cQuery += "        	AND SF6.F6_FILIAL = ? "
		    cQuery += "        	AND SF6.F6_NUMERO = CDC.CDC_GUIA "
		    cQuery += "        	AND SF6.F6_EST = CDC.CDC_UF "
			cQuery += "	LEFT JOIN " + RetSqlName('CCF') + " CCF "
		    cQuery += "    ON CCF.D_E_L_E_T_ = ' ' "
		    cQuery += "        	AND CCF.CCF_FILIAL = ? "
		    cQuery += "        	AND CCF.CCF_NUMERO = SF6.F6_NUMPROC "
		    cQuery += "        	AND CCF.CCF_TIPO = SF6.F6_INDPROC "
			cQuery += "	LEFT JOIN " + RetSqlName('C1G') + " C1G "
		    cQuery += "    ON C1G.D_E_L_E_T_ = ' ' "
		    cQuery += "        	AND C1G.C1G_FILIAL = ? "
		    cQuery += "        	AND C1G.C1G_NUMPRO = CCF.CCF_NUMERO "
		    cQuery += "        	AND C1G.C1G_INDPRO = CCF.CCF_TIPO "
			cQuery += "	LEFT JOIN " + RetSqlName('C09') + " C09 "
			cQuery += "		ON C09.C09_FILIAL = ? "
			cQuery += "			AND C09.C09_UF = CDC.CDC_UF "
			cQuery += "			AND C09.D_E_L_E_T_ = ' ' "
			cQuery += "	LEFT JOIN " + RetSqlName('C0R') + " C0R " 
			cQuery += "		ON C0R.C0R_FILIAL = ? " 
			cQuery += "			AND C0R.C0R_NUMDA = CDC.CDC_GUIA "
			cQuery += "			AND C0R.C0R_UF = C09.C09_ID "
			cQuery += "			AND C0R.D_E_L_E_T_ = ' ' "
			cQuery += "	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
			cQuery += "		ON C3Q.D_E_L_E_T_ = ' ' "
			cQuery += "			AND C3Q.C3Q_FILIAL = ? "
			cQuery += "			AND C3Q.C3Q_CODINF = CDC.CDC_IFCOMP "
			cQuery += " WHERE CDC.D_E_L_E_T_ = ' ' "
			cQuery += " 	AND CDC.CDC_FILIAL = ? "
			cQuery += " 	AND CDC.CDC_TPMOV = ? "
			cQuery += " 	AND CDC.CDC_DOC = ? "
			cQuery += " 	AND CDC.CDC_SERIE = ? "
			cQuery += " 	AND CDC.CDC_CLIFOR = ? "
			cQuery += " 	AND CDC.CDC_LOJA = ? "
			cQuery += " 	AND CDC.CDC_IFCOMP <> ' ' "

			cQuery 				:= ChangeQuery(cQuery)
			oStDocArrecadRef 	:= FwExecStatement():New(cQuery)
		EndIf	

		If oStDocArrecadRef != Nil
			oStDocArrecadRef:SetString(1, xFilial("SF6") 		)
			oStDocArrecadRef:SetString(2, xFilial("CCF") 		)
			oStDocArrecadRef:SetString(3, xFilial("C1G") 		)
			oStDocArrecadRef:SetString(4, xFilial("C09") 		)
			oStDocArrecadRef:SetString(5, xFilial("C0R") 		)
			oStDocArrecadRef:SetString(6, xFilial("C3Q") 		)
			oStDocArrecadRef:SetString(7, xFilial("CDC") 		)
			oStDocArrecadRef:SetString(8, (cAlias)->FT_TIPOMOV	)
			oStDocArrecadRef:SetString(9, (cAlias)->FT_NFISCAL	)
			oStDocArrecadRef:SetString(10, (cAlias)->FT_SERIE  	)
			oStDocArrecadRef:SetString(11, (cAlias)->FT_CLIEFOR	)
			oStDocArrecadRef:SetString(12, (cAlias)->FT_LOJA   	)
			
			cAliasCDC := oStDocArrecadRef:OpenAlias()

			While !(cAliasCDC)->(EOF())
				cProcedure 	:= "TAF613S"
				cIdC3Q		:= alltrim((cAliasCDC)->C3Q_ID)
				cKeyReg		:= (cAliasCDC)->CDC_IFCOMP

				If Empty(cIdC3Q)
					cIdC3Q := TAFGeraId("TAF")                                                                                                                
				EndIf

				aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
								
				If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
					cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C3Q'), cMsgLog, .t., 'SFT' }
					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
				EndIf

				cKeyReg		:= cValToChar((cAliasCDC)->RECNO_CCF)
				
				If cKeyReg <> "0"

					cProcedure 	:= "TAF613Q"
					cIdC1G		:= (cAliasCDC)->C1G_ID

					If Empty(cIdC1G)
						cIdC1G := GetSX8Num("C1G", "C1G_ID")                                                                                                             
					EndIf

					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC1G, "")	  
									
					If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
						if __lSx8; C1G->(RollBackSX8('C1G')); endif
						cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
						aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C1G'), cMsgLog, .t., 'SFT' }
						ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
					else
						if __lSx8; C1G->(ConfirmSX8('C1G')); endif
					EndIf
				endif 

				cProcedure 	:= "TAF613R"
				cIdC0R 		:= (cAliasCDC)->C0R_ID
				cKeyReg		:= cValToChar((cAliasCDC)->RECNO_SF6)

				If Empty(cIdC0R)
					cIdC0R := GetSX8Num("C0R", "C0R_ID",, 6)
				EndIf

				aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC0R, "")	  
								
				If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
					if __lSx8; C0R->(RollBackSX8('C0R')); endif
					cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C0R'), cMsgLog, .t., 'SFT' }
					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
				else
					if __lSx8; C0R->(ConfirmSX8('C0R')); endif
				EndIf    

				//Veririco se tem Cadastro de Informações complementares (CDT)
				iPosInfoComp := GetPosJsonInfoComplementar(cAlias, oDadosComplemento, cIdC3Q)

				if !oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp]:HasProperty(cKeyDocArref)
					//Cria o complemento como um Array vazio
					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyDocArref] := {}
				EndIf	

				//Incluo as guias de recolhimento no Json
				AAdd(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyDocArref], JsonObject():New())
				iPosDocArrec := len(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyDocArref])
				
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyDocArref][iPosDocArrec]["C25_CODINF"] := cIdC3Q
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyDocArref][iPosDocArrec]["C25_IDENTI"] := cIdC0R   

				(cAliasCDC)->(DbSkip())

				//Zero a variavel de controle de posicao
				iPosInfoComp := 0			
			EndDo

			(cAliasCDC)->(DbCloseArea())

			FwFreeArray(aOutputSp)
		EndIf
	EndIf

Return

/*-----------------------------------------------------------------------
{Protheus.doc} DocFiscalRef
@description Consulta e carrega o Json com informações complementares dos documentos 
				fiscais referenciados documento (T013AF - SPED C113)
@author Carlos Eduardo Nonato
@since 16/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStDocFiscalRef - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function DocFiscalRef(cAlias as character, oDadosComplemento as json, oStDocFiscalRef as object)

	Local aOutputSp				:= {}	as array
	Local cIdC1H 				:= ""	as character
	Local cQuery 				:= ""	as character
	Local cAliasCDD 			:= ""	as character
	Local cIdModelo				:= ""	as character  
	Local cIndEmit 				:= ""	as character
	Local cIndOper 				:= ""	as character
	Local cProcedure			:= ""	as character
	Local cIdC3Q				:= ""	as character
	Local cKeyReg				:= ""	as character
	Local iPosInfoComp 			:= 0 	as integer
	Local iPosDocFiscalRef		:= 0	as integer
	Local cCliCDD       		:= ""   as character
	Local cLojaCDD      		:= ""   as character
	Local cCliOrFor     		as character

	Default cAlias				:= ""
	Default oDadosComplemento	:= Nil
	Default oStDocFiscalRef		:= Nil

	If !Empty(cAlias) .And. oDadosComplemento != Nil
		If oStDocFiscalRef == Nil
			cQuery += " SELECT "
			cQuery += " 	CDD.CDD_FILIAL, "
			cQuery += " 	CDD.CDD_ENTSAI, "
			cQuery += " 	CDD.CDD_PARREF, "
			cQuery += " 	CDD.CDD_LOJREF, "	
			cQuery += " 	CDD.CDD_DOCREF, "
			cQuery += " 	CDD.CDD_SERREF, "
			cQuery += " 	CDD.CDD_CHVNFE, "
			cQuery += " 	CDD.CDD_MODREF, "
			cQuery += " 	CDD.CDD_IFCOMP, "
			cQuery += " 	CASE "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '1' THEN SF1.F1_TIPO "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '2' THEN SF2.F2_TIPO "
			cQuery += " 	END TIPODOC, "
			cQuery += " 	CASE "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '1' THEN SF1.F1_FORMUL "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '2' THEN SF2.F2_FORMUL "
			cQuery += " 	END FORMULARIO, "
			cQuery += " 	CASE "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '1' THEN SF1.F1_EMISSAO "
			cQuery += " 		WHEN CDD.CDD_ENTSAI = '2' THEN SF2.F2_EMISSAO "
			cQuery += " 	END DT_EMISSAO, "
			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID " 
			cQuery += " FROM " + RetSqlName('CDD') + " CDD "
			cQuery += "	LEFT JOIN " + RetSqlName('SF1') + " SF1 "
			cQuery += "		ON CDD.CDD_ENTSAI = '1' "
			cQuery += " 		AND SF1.F1_FILIAL = ? "
			cQuery += " 		AND SF1.F1_DOC = CDD.CDD_DOCREF "
			cQuery += " 		AND SF1.F1_SERIE = CDD.CDD_SERREF "
			cQuery += " 		AND SF1.F1_FORNECE = CDD.CDD_PARREF "
			cQuery += " 		AND SF1.F1_LOJA = CDD.CDD_LOJREF "
			cQuery += "			AND SF1.D_E_L_E_T_ = ' ' "
			cQuery += " LEFT JOIN " + RetSqlName('SF2') + " SF2 "
			cQuery += "		ON CDD.CDD_ENTSAI = '2' "
			cQuery += "			AND SF2.F2_FILIAL = ? "
			cQuery += " 		AND SF2.F2_DOC = CDD.CDD_DOCREF "
			cQuery += " 		AND SF2.F2_SERIE = CDD.CDD_SERREF "
			cQuery += " 		AND SF2.F2_CLIENTE = CDD.CDD_PARREF "
			cQuery += " 		AND SF2.F2_LOJA = CDD.CDD_LOJREF "
			cQuery += " 		AND SF2.D_E_L_E_T_ = ' ' "
			cQuery += "	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
			cQuery += "		ON C3Q.D_E_L_E_T_ = ' ' "
			cQuery += "			AND C3Q.C3Q_FILIAL = ? "
			cQuery += "			AND C3Q.C3Q_CODINF = CDD.CDD_IFCOMP "
			cQuery += " WHERE CDD.D_E_L_E_T_ = ' ' "
			cQuery += " 	AND CDD.CDD_FILIAL = ? "
			cQuery += " 	AND CDD.CDD_TPMOV = ? "
			cQuery += " 	AND CDD.CDD_DOC = ? "
			cQuery += " 	AND CDD.CDD_SERIE = ? "
			cQuery += " 	AND CDD.CDD_CLIFOR = ? "
			cQuery += " 	AND CDD.CDD_LOJA = ? "
			cQuery += " 	AND CDD.CDD_IFCOMP <> ' ' "

			cQuery := ChangeQuery(cQuery)	

			oStDocFiscalRef := FwExecStatement():New(cQuery)
		EndIf	

		If oStDocFiscalRef != Nil
			oStDocFiscalRef:SetString(1, xFilial("SF1") 		)
			oStDocFiscalRef:SetString(2, xFilial("SF2") 		)
			oStDocFiscalRef:SetString(3, xFilial("C3Q") 		)
			oStDocFiscalRef:SetString(4, xFilial("CDD") 		)
			oStDocFiscalRef:SetString(5, (cAlias)->FT_TIPOMOV	)
			oStDocFiscalRef:SetString(6, (cAlias)->FT_NFISCAL	)
			oStDocFiscalRef:SetString(7, (cAlias)->FT_SERIE  	)
			oStDocFiscalRef:SetString(8, (cAlias)->FT_CLIEFOR	)
			oStDocFiscalRef:SetString(9, (cAlias)->FT_LOJA   	)

			cAliasCDD := oStDocFiscalRef:OpenAlias()

			While !(cAliasCDD)->(EOF())
				cProcedure 	:= "TAF613S"
				cIdC3Q		:= alltrim((cAliasCDD)->C3Q_ID)
				cKeyReg		:= (cAliasCDD)->CDD_IFCOMP

				If Empty(cIdC3Q)
					cIdC3Q := TAFGeraId("TAF")                                                                                                                
				EndIf

				aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
								
				If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C3Q'), '', .t., 'SFT' }
					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
				EndIf

				//Tipo de emissao
				if (cAliasCDD)->CDD_ENTSAI == '1' //Entrada
					cIndEmit := iif( (cAliasCDD)->FORMULARIO == 'S ', '0', '1')
				elseif (cAliasCDD)->CDD_ENTSAI == '2' //Saida
					cIndEmit := iif( (cAliasCDD)->FORMULARIO == 'N ', '1', '0')
				endif	

				//Código do participante
				//Monta o código do participante e pesquisa na tabela do TAF. 
				//Entrada | Saida
				cIndOper := iif((cAliasCDD)->CDD_ENTSAI == '1', '0', '1')
				if (cAliasCDD)->TIPODOC $ 'DB'
					cCliOrFor := iif( cIndOper == '0', 'SA1', 'SA2')
				else
					cCliOrFor := iif( cIndOper == '0', 'SA2', 'SA1')
				endif
				
				cCliCDD       := (cAliasCDD)->CDD_PARREF
				cLojaCDD      := (cAliasCDD)->CDD_LOJREF				

				cIdC1H := ComplementParticipantIntegration(cCliCDD, cLojaCDD, cCliOrFor, 'CDD')

				//Veririco se tem Cadastro de Informações complementares no Json e retorno a posicao(CDT)
				iPosInfoComp := GetPosJsonInfoComplementar(cAlias, oDadosComplemento, cIdC3Q)

				//Cria o complemento como um Array vazio
				If !oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp]:HasProperty(cKeyNFRef)
					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef] := JsonObject():New()
				EndIf
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF] := {}	

				//Busca o id no modelo da nota fiscal no TAF caso esteja preenchida.
				If !Empty((cAliasCDD)->CDD_MODREF)
					cIdModelo := GetTAfId("C01", AModNot((cAliasCDD)->CDD_MODREF), 1)
					cIdModelo := IIf(cIdModelo == "NOTFOUND", "", cIdModelo)
				EndIf	

				//Incluo as guias de recolhimento no Json
				aadd( oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF], JsonObject():New() )
				iPosDocFiscalRef++

				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_CODMOD'] := cIdModelo
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_INDOPE'] := cIndOper
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_INDEMI'] := cIndEmit
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_CODPAR'] := cIdC1H
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_NUMDOC'] := (cAliasCDD)->CDD_DOCREF
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_SERIE' ] := (cAliasCDD)->CDD_SERREF
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_DTDOC' ] := alltrim((cAliasCDD)->DT_EMISSAO)
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_CODINF'] := cIdC3Q
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyNF][iPosDocFiscalRef]['C26_CHVELE'] := (cAliasCDD)->CDD_CHVNFE	

				(cAliasCDD)->(DbSkip())

				//Limpo as variáveis que serão suadas no próximo registro.
				iPosInfoComp := 0
				cIdC1H    	 := ''
				cIdModelo 	 := ''
			enddo

			(cAliasCDD)->(DbCloseArea())

			FwFreeArray(aOutputSp)
		EndIf
	EndIf

Return

/*-----------------------------------------------------------------------
{Protheus.doc} CupomFiscalRef
@description Consulta e carrega o Json com informações complementares os cupons fiscais
				referenciados (T013AG - SPED C114)
@author Carlos Eduardo Nonato
@since 16/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStCupomFiscalRef - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function CupomFiscalRef(cAlias as character, oDadosComplemento as json, oStCupomFiscalRef as object)

 	Local aOutputSp 			:= {} 	as array
 	Local cQuery 				:= "" 	as character
 	Local cAliasCDE 			:= "" 	as character
 	Local cCodEspecie 			:= "" 	as character   
 	Local cIdModelo 			:= "" 	as character 
 	Local cCupomFiscal 			:= "" 	as character 
 	Local cDtEmissao 			:= "" 	as character
 	Local cSerieCupom 			:= "" 	as character
 	Local cPdv 					:= "" 	as character
 	Local cEcfFab 				:= "" 	as character
 	Local cEcfCx 				:= "" 	as character
 	Local cIdCodEcf 			:= "" 	as character							 
 	Local cProcedure 			:= "" 	as character
 	Local cKeyReg 				:= "" 	as character
 	Local cIdC3Q				:= ""	as character
 	Local iPosInfoComp 			:= 0 	as integer
 	Local iPosCupomFiscalRef	:= 0 	as integer

 	Default cAlias				:= ""
 	Default oDadosComplemento	:= Nil
 	Default oStCupomFiscalRef	:= Nil

 	If !Empty(cAlias) .And. oDadosComplemento != Nil
 		If oStCupomFiscalRef == Nil
 			cQuery += " SELECT "
 			cQuery += " 	CDE.CDE_CPREF, "
 			cQuery += " 	CDE.CDE_SERREF, "
 			cQuery += " 	CDE.CDE_ECFFAB, "
 			cQuery += " 	CDE.CDE_ECFCX, "
 			cQuery += "		CDE.CDE_IFCOMP, "
 			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID "
 			cQuery += " 	FROM " + RetSqlName("CDE") + " CDE "
 			cQuery += " 	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
 			cQuery += " 		ON C3Q.D_E_L_E_T_ = ' ' "
 			cQuery += " 			AND C3Q.C3Q_FILIAL = ? "
 			cQuery += " 			AND C3Q.C3Q_CODINF = CDE.CDE_IFCOMP "
 			cQuery += "		WHERE CDE.D_E_L_E_T_ = ' ' "
 			cQuery += " 		AND CDE.CDE_FILIAL = ? "
 			cQuery += " 		AND CDE.CDE_TPMOV = ? "
 			cQuery += " 		AND CDE.CDE_DOC = ? "
 			cQuery += " 		AND CDE.CDE_SERIE = ? "
 			cQuery += " 		AND CDE.CDE_CLIFOR = ? "
 			cQuery += " 		AND CDE.CDE_LOJA = ? "
			cQuery += " 		AND CDE.CDE_IFCOMP <> ' ' "

 			cQuery := ChangeQuery(cQuery)

 			oStCupomFiscalRef := FwExecStatement():New(cQuery)
 		EndIf	

 		If oStCupomFiscalRef != Nil
 			oStCupomFiscalRef:SetString(1, xFilial("C3Q")		)
 			oStCupomFiscalRef:SetString(2, xFilial("CDE")		)
 			oStCupomFiscalRef:SetString(3, (cAlias)->FT_TIPOMOV	)
 			oStCupomFiscalRef:SetString(4, (cAlias)->FT_NFISCAL	)
 			oStCupomFiscalRef:SetString(5, (cAlias)->FT_SERIE  	)
 			oStCupomFiscalRef:SetString(6, (cAlias)->FT_CLIEFOR	)
 			oStCupomFiscalRef:SetString(7, (cAlias)->FT_LOJA   	)

 			cAliasCDE := oStCupomFiscalRef:OpenAlias()

 			While !(cAliasCDE)->(EOF())
				cProcedure 	:= "TAF613S"
				cIdC3Q		:= (cAliasCDE)->C3Q_ID
				cKeyReg		:= (cAliasCDE)->CDE_IFCOMP

				If Empty(cIdC3Q)
					cIdC3Q := TAFGeraId("TAF")                                                                                                                
				EndIf

				aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
									
 				If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
 					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C3Q'), '', .t. }
 					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
 				EndIf

				//Comeco da integracao de cupons fiscais referenciados
 				cCupomFiscal :=	(cAliasCDE)->CDE_CPREF
 				cSerieCupom	 :=	(cAliasCDE)->CDE_SERREF										
				
 				If SF2->(MsSeek(xFilial('SF2')+cCupomFiscal+cSerieCupom))
 					//FT_PDV somente estarah alimentado quando se referir a nota fiscais de saida geradas pelo SIGALOJA.
 					If !Empty(SF2->F2_PDV) .And. alltrim(SF2->F2_ESPECIE) $ 'CF|ECF'
 						cCodEspecie	:=	"2D"
 					Else          
 						cCodEspecie := 	aModNot(SF2->F2_ESPECIE)
 					EndIf
					
 					cDtEmissao 	:= 	SF2->F2_EMISSAO
 					cPdv 		:= 	SF2->F2_PDV
 				EndIf

 				//Se tiver PDV relacionado ao documento fiscal e o mesmo existir na tabela SFI,
 				//utilizo os dados da mesma, caso contrario, utilizo os dados do complemento 
 				If !Empty(cPdv) .And. SFI->(MsSeek(xFilial('SFI')+Dtos(cDtEmissao)+cPdv))
 					cEcfFab     := SFI->FI_SERPDV
 					cEcfCx		:= SFI->FI_PDV	
 				Else
 					cEcfFab     := (cAliasCDE)->CDE_ECFFAB
 					cEcfCx		:= (cAliasCDE)->CDE_ECFCX		
 				EndIf
				
 				//Busca o id nas tabelas do TAF
 				cIdModelo := GetTafId('C01',cCodEspecie,1) //Especie do documento

 				If !Empty(cIdModelo) .And. !Empty(cEcfCx)
 					cProcedure	:= "TAF613P"
 					cIdCodEcf 	:= GetTafId("C0W", cEcfCx, 3) //Cadastro do ECF / SAT-CFe  
 					cKeyReg		:= cEcfCx

 					If Empty(cIdCodEcf) .Or. cIdCodEcf == "NOTFOUND"
 						cIdCodEcf := GetSx8Num("C0W", "C0W_ID")
 					EndIf

 					//Efetua o cadastro do ECF / SAT-CFE caso o mesmo ainda não exista no TAF
 					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdCodEcf, "")	  

 					If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR) //Caso não consiga efetuar o cadastro
 						if __lSx8; C0W->(RollBackSX8('C0W')); endif
 						aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C0W'), '', .t. }
 						ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
 					else
 						if __lSx8; C0W->(ConfirmSX8('C0W')); endif
 					EndIf

 				EndIf	

 				If cCodEspecie != '59'
 					//Veririco se tem Cadastro de Informações complementares no Json e retorno a posicao(CDT)
 					iPosInfoComp := GetPosJsonInfoComplementar(cAlias, oDadosComplemento, cIdC3Q)

 					If !oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp]:HasProperty(cKeyNFRef)
 						oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef] := JsonObject():New()
 					EndIf

 					//Cria o complemento como um Array vazio
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef] 	:= {}

 					//Incluo os cupons fiscais referenciados no json
 					AAdd( oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef], JsonObject():New() )
 					iPosCupomFiscalRef++
					
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_CODECF'] := cIdCodEcf
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_CODMOD'] := cIdModelo
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_NUMDOC'] := cCupomFiscal
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_DTDOC' ] := DToS(cDtEmissao)
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_CHVCFE'] := ' '
 					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyNFRef][cKeyCupomRef][iPosCupomFiscalRef]['C27_CODINF'] := cIdC3Q
 				EndIf

 				(cAliasCDE)->(DBSkip())

 				//Limpo as variaveis que serão usadas no próximo registro.
 				iPosInfoComp 	:= 0
 				cIdCodEcf		:= ''
 			EndDo

 			(cAliasCDE)->(DBCloseArea())

 			FwFreeArray(aOutputSp)
 		EndIf
 	EndIf

Return 

/*-----------------------------------------------------------------------
{Protheus.doc} LocaisColetaEntrega
@description Consulta e carrega o Json com com informações complementares dos locais de 
				coleta e entrega do documento (T013AH)
@author Carlos Eduardo Nonato
@since 21/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStColEnt - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function LocaisColetaEntrega(cAlias as character, oDadosComplemento as json, oStColEnt as object)

	Local aOutputSp     := {} as array
	Local cQuery        as character
	Local cAliasCDF     as character
	Local cCodColeta    as character
	Local cCodEntrega   as character
	Local cIdParColeta  as character
	Local cIdParEntrega as character
	Local cIdC3Q        as character
	Local cProcedure    as character
	Local cKeyReg       as character
	Local iPosColEnt    as integer
	Local iPosInfoComp  as integer
	Local cEntregCDF    as character
	Local cLojEntregCDF as character
	Local cColetaCDF    as character
	Local cLojColetaCDF as character
	Local cMsgLog 		as character
	Local lLogCadastro	as logical
	Local cCliFor := 'SA1' as character
	
	Default cAlias				:= ""
	Default oDadosComplemento	:= Nil
	Default oStColEnt			:= Nil

	If !Empty(cAlias) .And. oDadosComplemento != Nil 
		If oStColEnt == Nil
			cQuery += " SELECT "
			cQuery += "		CDF.CDF_FILIAL, "
			cQuery += "		CDF.CDF_COLETA, "
			cQuery += "		CDF.CDF_LOJCOL, "
			cQuery += "		CDF.CDF_ENTREG, "
			cQuery += "		CDF.CDF_LOJENT, "
			cQuery += "		CDF.CDF_IFCOMP, "
			cQuery += "		CDF.CDF_TPTRAN, "
			cQuery += " 	COALESCE(C16.C16_ID, ' ') C16_ID, "
			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID " 
			cQuery += " FROM " + RetSqlName('CDF') + " CDF "
			cQuery += " LEFT JOIN " + RetSqlName('C16') + " C16 " 
			cQuery += "		ON C16.C16_FILIAL = ? " 
			cQuery += "			AND C16.C16_CODIGO = CDF.CDF_TPTRAN "
			cQuery += "			AND C16.D_E_L_E_T_ = ' ' "
			cQuery += "	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
			cQuery += "		ON C3Q.D_E_L_E_T_ = ' ' "
			cQuery += "			AND C3Q.C3Q_FILIAL = ? "
			cQuery += "			AND C3Q.C3Q_CODINF = CDF.CDF_IFCOMP "
			cQuery += " WHERE CDF.D_E_L_E_T_   = ' ' "
			cQuery += " 	AND CDF.CDF_FILIAL = ? "
			cQuery += " 	AND CDF.CDF_TPMOV  = ? "
			cQuery += " 	AND CDF.CDF_DOC    = ? "
			cQuery += " 	AND CDF.CDF_SERIE  = ? "
			cQuery += " 	AND CDF.CDF_CLIFOR = ? "
			cQuery += " 	AND CDF.CDF_LOJA   = ? "

			cQuery 		:= ChangeQuery(cQuery)
			oStColEnt 	:= FwExecStatement():New(cQuery)
		EndIf	

		If oStColEnt != Nil
			oStColEnt:SetString(1, xFilial("C16") 		)
			oStColEnt:SetString(2, xFilial("C3Q")		)
			oStColEnt:SetString(3, xFilial("CDF") 		)
			oStColEnt:SetString(4, (cAlias)->FT_TIPOMOV	)
			oStColEnt:SetString(5, (cAlias)->FT_NFISCAL	)
			oStColEnt:SetString(6, (cAlias)->FT_SERIE  	)
			oStColEnt:SetString(7, (cAlias)->FT_CLIEFOR	)
			oStColEnt:SetString(8, (cAlias)->FT_LOJA   	)

			cAliasCDF := oStColEnt:OpenAlias()

			While !(cAliasCDF)->(EOF())
				if !empty((cAliasCDF)->CDF_IFCOMP) .and. !empty((cAliasCDF)->CDF_TPTRAN)
					cProcedure 	:= "TAF613S"
					cIdC3Q		:= alltrim((cAliasCDF)->C3Q_ID)
					cKeyReg		:= (cAliasCDF)->CDF_IFCOMP

					If Empty(cIdC3Q)
						cIdC3Q := TAFGeraId("TAF")                                                                                                                
					EndIf

					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
									
					if Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
						cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
						lLogCadastro := .t.
					endif	

				else
					//Cria a descrição para apresentar no log de falha da integração
					cMsgLog := STR0005 //'Obs.: Faltam informações na origem da integração. Campos: CDF_IFCOMP e/ou CDF_TPTRAN '				
				endif

				if !empty(cMsgLog)
					aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('CDF'), cMsgLog, lLogCadastro, 'SFT' }
					ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
				endif

				cColetaCDF    := (cAliasCDF)->CDF_COLETA
				cLojColetaCDF := (cAliasCDF)->CDF_LOJCOL
				cEntregCDF    := (cAliasCDF)->CDF_ENTREG
				cLojEntregCDF := (cAliasCDF)->CDF_LOJENT

				//Conforme a configuração do documento principal altera a varíavel da tabela de cliente para fornecedor
				if ((cAlias)->FT_TIPO $ 'DB' .and. (cAlias)->FT_TIPOMOV != 'E') .or. (!(cAlias)->FT_TIPO $ 'DB' .and. (cAlias)->FT_TIPOMOV == 'E')
					cCliFor := 'SA2'
				endif

				cIdParColeta  := ComplementParticipantIntegration(cColetaCDF, cLojColetaCDF, cCliFor, 'CDF')
				cIdParEntrega := ComplementParticipantIntegration(cEntregCDF, cLojEntregCDF, cCliFor, 'CDF')

				//Veririco se tem Cadastro de Informações complementares no Json e retorno a posicao(CDT)
				iPosInfoComp := GetPosJsonInfoComplementar(cAlias, oDadosComplemento, cIdC3Q)

				if (cAliasCDF)->(RecNo()) == 1
					//Cria o complemento como um Array vazio
					oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt] := {}
				endif

				//Incluo os cupons fiscais referenciados no json
				aadd( oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt], JsonObject():New() )
				iPosColEnt++
				
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt][iPosColEnt]['C28_INDCAR'] := (cAliasCDF)->C16_ID
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt][iPosColEnt]['C28_PARCOL'] := cIdParColeta
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt][iPosColEnt]['C28_PARENT'] := cIdParEntrega
				oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][iPosInfoComp][cKeyColEnt][iPosColEnt]['C28_CODINF'] := cIdC3Q

				(cAliasCDF)->(DbSkip())

				//Limpo as variaveis que serão usadas no próximo registro
				nPosInfoComp  := 0
				cCodColeta    := ''
				cCodEntrega   := ''
				cIdParColeta  := ''
				cIdParEntrega := ''	
			enddo

			(cAliasCDF)->(DbCloseArea())

			FwFreeArray(aOutputSp)
		EndIf
	EndIf

Return

/*----------------------------------------------------------------------
{Protheus.doc} CadFaturas()
Verifica se existe fatutas para o documento fiscal
@author Carlos Eduardo Nonato (T013AI - SPED C140)
@since 26/06/2023
//----------------------------------------------------------------------*/
Static Function CadFaturas( cAlias as character, oDadosIntegra as json, oStFatura as object )
Local cQuery as character
Local cAliasQry := GetNextAlias() as character
Local cDescrTitulo as character
Local cKeyVencimentos := 'vencimentos' as character
Local cPrefixo as character
Local cParDupRef := iif((cAlias)->FT_TIPOMOV == 'E','MV_2DUPREF','MV_1DUPREF') as character
Local cAliasNF := iif((cAlias)->FT_TIPOMOV == 'E','SF1','SF2') as character
Local cAliasTit := iif((cAlias)->FT_TIPOMOV == 'E','SE2','SE1') as character
Local cCampoCliFor := iif((cAlias)->FT_TIPOMOV == 'E','_FORNECE','_CLIENTE') as character 
Local cMV_DUPREF := GetNewPar(cParDupRef,'') as character
Local cCmpTit := cAliasTit + '.' + right(cAliasTit,2) as character // "SE1.E1" ou "SE2.E2"
Local nPosVencimentos as integer

if valtype(oStFatura) != 'O'
	cQuery += " SELECT "
	cQuery += cCmpTit + "_NUM NUM, "
	cQuery += " 	CASE "
	cQuery += " 		WHEN " + cCmpTit + "_TIPO IN ('BOL','DP','NF') THEN '00' "
	cQuery += " 		WHEN " + cCmpTit + "_TIPO = 'CH' THEN '01' "
	cQuery += " 		WHEN " + cCmpTit + "_TIPO = 'NP' THEN '02' "
	cQuery += " 		WHEN " + cCmpTit + "_TIPO = 'RC' THEN '03' "
	cQuery += " 		ELSE '99' "
	cQuery += " 	END TIPO, "
	cQuery += " 	" + cCmpTit + "_HIST HIST, "
	cQuery += " 	" + cCmpTit + "_VENCREA VENCREA, "
	cQuery += " 	" + cCmpTit + "_VLCRUZ VLCRUZ, "
	cQuery += " 	" + cCmpTit + "_VENCTO VENCTO, "
	cQuery += " 	" + cCmpTit + "_DESCONT DESCONT"
	cQuery += " FROM " + RetSqlName(cAliasTit) + ' ' + cAliasTit
	cQuery += " WHERE " + cAliasTit + ".D_E_L_E_T_ = ' ' "
	cQuery += " 	AND " + cCmpTit + "_FILIAL  = ? "
	cQuery += " 	AND " + cCmpTit + cCampoCliFor + " = ? " //E2_FORNECE ou E1_CLIENTE
	cQuery += " 	AND " + cCmpTit + "_LOJA    = ? "
	cQuery += " 	AND " + cCmpTit + "_PREFIXO = ? "
	cQuery += " 	AND " + cCmpTit + "_NUM     = ? "
	cQuery += " ORDER BY " + cCmpTit + "_VENCREA "	

	oStFatura := FWPreparedStatement():New()
	oStFatura:SetQuery( cQuery )
endif

//Tratamento para quando o PREFIXO nao estiver gravado no documento fiscal
if empty((cAlias)->PREFIXO)
	//Se o prefixo nao estiver gravado no documento fiscal, tenho que utilizar o parametro MV_2DUPREF
	//porem o parametro eh uma macro e pode abranger ate um rdmake, para isso, temos que manter um 
	//padrao de chamada, o SF1 deve estar posicionado.                   
	(cAliasNF)->(DbSetOrder(1)) //_FILIAL+_DOC+_SERIE+_FORNECE/_CLIENTE+_LOJA+_TIPO                                                                                                            
	if (cAliasNF)->(MsSeek( xFilial((cAliasNF)) + (cAlias)->(FT_NFISCAL+FT_SERIE+FT_CLIEFOR+FT_LOJA)) )
		cPrefixo	:= 	&(cMV_DUPREF)
	endif	
else
	cPrefixo := alltrim((cAlias)->PREFIXO)
endif

oStFatura:SetString(1, xFilial(cAliasNF) )
oStFatura:SetString(2, (cAlias)->FT_CLIEFOR )
oStFatura:SetString(3, (cAlias)->FT_LOJA    )
oStFatura:SetString(4, cPrefixo )
oStFatura:SetString(5, alltrim((cAlias)->DUPL)	)

dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStFatura:GetFixQuery(),{}), cAliasQry, .f., .t.)

while (cAliasQry)->(!eof())

	if alltrim((cAliasQry)->TIPO) = '99'
		if empty((cAliasQry)->HIST)
			cDescrTitulo := 'Titulo de Nota Fiscal'
		else
			cDescrTitulo := (cAliasQry)->HIST
		endif		
	endif

	if (cAliasQry)->(RecNo()) == 1
		
		//Cria o complemento como um Array vazio
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura] := JsonObject():New() 
		
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_INDEMI'] := alltrim((cAlias)->IND_EMIT)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_INDTIT'] := alltrim((cAliasQry)->TIPO)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_DESCRI'] := cDescrTitulo
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_NUMTIT'] := (cAliasQry)->NUM
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_QTDPAR'] := 0
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRTOT'] := 0
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRDES'] := 0
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRLIQ'] := 0
	
		//Cria o objeto filho como um array vazio								   
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura][cKeyVencimentos] := {}
	endif	

	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_QTDPAR']++
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRTOT'] += (cAliasQry)->VLCRUZ
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRDES'] += (cAliasQry)->DESCONT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura]['C29_VLRLIQ'] += (cAliasQry)->(VLCRUZ - DESCONT)

	aadd(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura][cKeyVencimentos], JsonObject():New() )
	nPosVencimentos++

	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura][cKeyVencimentos][nPosVencimentos]['C2A_NUMPAR'] := strzero(nPosVencimentos,4)
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura][cKeyVencimentos][nPosVencimentos]['C2A_VECTO' ] := (cAliasQry)->VENCREA
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyFatura][cKeyVencimentos][nPosVencimentos]['C2A_VLRPAR'] := (cAliasQry)->VLCRUZ

	(cAliasQry)->(DbSkip())

	//Limpo as variaveis que serão usadas no prócimo registro.
	cDescrTitulo := ''

enddo

(cAliasQry)->(DbCloseArea())

return

/*-----------------------------------------------------------------------
{Protheus.doc} LocaisColetaEntregaTransporte
Consulta e carrega o Json com os locais de coleta e entrega de documentos
fiscais de transporte (T013AQ - SPED D160|D162)
@author Carlos Eduardo Nonato
@since 21/06/2023
//----------------------------------------------------------------------*/
Static Function LocaisColetaEntregaTransporte(cAlias as character, oDadosComplemento as json, oStIdentDocumentos as object, cCodEspecie as character)

Local cQuery                                                 as character
Local cAliasDTC          := GetNextAlias()                   as character
Local cTipoTransporte                                        as character
Local cCodRemetente      := (cAlias)->COD_PART               as character
Local cIdParRemetente                                        as character
Local cCodColeta                                             as character
Local cIdParColeta                                           as character
Local cCodDestinatario   := (cAlias)->COD_PART               as character
Local cIdParDestinaratio                                     as character
Local cCodEntrega                                            as character
Local cIdParEntrega                                          as character
Local cDT6FILDOC                                             as character
Local cDT6DOC                                                as character
Local CDT6SERIE                                              as character
Local cChaveDUL                                              as character
Local cKeyIdentDocs      := 'identificacaoDocumentosFiscais' as character
Local nPosIdentDocs      := 0                                as integer
Local lTabDTC                                                as logical
Local cRemParDT6         := ""                               as character
Local cLojRemDT6         := ""                               as character
Local cCliCal            := ""                               as character
Local cLojCal            := ""                               as character
Local cCliOrFor          := 'SA1'       					 as character
Local cParDestDUL   	 := "" 								 as character
Local cLojDestDUL   	 := "" 								 as character

if valtype(oStIdentDocumentos) != 'O'
	cQuery += " SELECT "
	cQuery += " 	DTC_FILIAL,"
	cQuery += " 	DTC_FILDOC,"
	cQuery += " 	DTC_SERNFC,"
	cQuery += " 	DTC_NUMNFC,"
	cQuery += " 	DTC_SQEDES,"
	cQuery += " 	DTC_EMINFC,"
	cQuery += " 	SUM(DTC_VALOR) DTC_VALOR,	"
	cQuery += " 	SUM(DTC_QTDVOL) DTC_QTDVOL,"
	cQuery += " 	SUM(DTC_PESO) DTC_PESO"
	cQuery += " FROM " + RetSqlName('DTC')
	cQuery += " WHERE D_E_L_E_T_ = ' '"
	cQuery += " 	AND DTC_FILIAL = ? " 
	cQuery += " 	AND DTC_FILDOC = ? "
	cQuery += " 	AND DTC_DOC    = ? "
	cQuery += " 	AND DTC_SERIE  = ? "
	cQuery += " GROUP BY DTC_FILIAL, DTC_FILDOC, DTC_NUMNFC, DTC_SERNFC, DTC_SQEDES, DTC_EMINFC "

	oStIdentDocumentos := FWPreparedStatement():New()
	oStIdentDocumentos:SetQuery( cQuery )
endif	

if cCodEspecie == '08'
	cTipoTransporte := '0'
elseif cCodEspecie == '09'
	cTipoTransporte := 	'3'
elseif cCodEspecie == '10'
	cTipoTransporte := '5'
elseif cCodEspecie == '11'
	cTipoTransporte	:= 	'1'
elseif cCodEspecie == '26'
	cTipoTransporte := 	'9'
endif

//Caso tenha conhecimento de transporte, busco os dados de lá
if (cAlias)->RECDT6 > 0
	if !empty( (cAlias)->(DT6_CLIREM+DT6_LOJREM) )
		cRemParDT6  := (cAlias)->DT6_CLIREM
		cLojRemDT6  := (cAlias)->DT6_LOJREM
		cCodRemetente := ComplementParticipantIntegration(cRemParDT6, cLojRemDT6, cCliOrFor)		
	endif
	if !empty((cAlias)->(DT6_CLICAL+DT6_LOJCAL))
		cCliCal  := (cAlias)->DT6_CLICAL
		cLojCal  := (cAlias)->DT6_LOJCAL
		cCodDestinatario := ComplementParticipantIntegration(cCliCal, cLojCal, cCliOrFor)
	endif	

	//Monto a chave para busca na nas tabelas do TMS DT5(Solicitação de Coleta) e DTC(Doc Cliente para Transporte)
	If val((cAlias)->DT6_DOCTMS) != 8
		cDT6FILDOC := (cAlias)->(DT6_FILDOC) 
		cDT6DOC    := (cAlias)->(DT6_DOC)
		cDT6SERIE  := (cAlias)->(DT6_SERIE)
	Else
		cDT6FILDOC := (cAlias)->(DT6_FILDCO) 
		cDT6DOC    := (cAlias)->(DT6_DOCDCO)
		cDT6SERIE  := (cAlias)->(DT6_SERDCO)
	EndIf	

	//-- Busca endereco de coleta diferente do remetente
	DT5->(DbSetOrder(4)) //DT5_FILIAL+DT5_FILDOC+DT5_DOC+DT5_SERIE	
	if DT5->(MsSeek( xFilial('DT5') + cDT6FILDOC + cDT6DOC + cDT6SERIE))
		DUL->(DbSetOrder(3) ) //DUL_FILIAL+DUL_CODSOL+DUL_SEQEND                                                                                                                                
		if DUL->(MsSeek(xFilial('DUL')+ DT5->(DT5_CODSOL+DT5_SEQEND) ) )
			cColetaDUL    := DUL->DUL_CODCLI
			cLojColetaDUL := DUL->DUL_LOJCLI
			cCodColeta    := ComplementParticipantIntegration(cColetaDUL, cLojColetaDUL, cCliOrFor)
		endif
	endif	

	//-- Busca endereco de entrega diferente do destinatario
	oStIdentDocumentos:SetString(1, xFilial('DTC') )
	oStIdentDocumentos:SetString(2, cDT6FILDOC )
	oStIdentDocumentos:SetString(3, cDT6DOC  )
	oStIdentDocumentos:SetString(4, cDT6SERIE )
	dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStIdentDocumentos:GetFixQuery(),{}), cAliasDTC, .f., .t.)
	if (cAliasDTC)->(!eof()) .and. !empty((cAliasDTC)->DTC_SQEDES)
		cChaveDUL := (cAlias)->(CLIDES+LOJDES) + (cAliasDTC)->DTC_SQEDES
		DUL->(DbSetOrder(2) ) //DUL_FILIAL+DUL_CODCLI+DUL_LOJCLI+DUL_SEQEND                                                                                                                                                                                                                                                    
		if DUL->(MsSeek(xFilial('DUL')+ cChaveDUL ) )
			cParDestDUL   := DUL->DUL_CODCLI
			cLojDestDUL   := DUL->DUL_LOJCLI
			cCodEntrega   := ComplementParticipantIntegration(cParDestDUL, cLojDestDUL, cCliOrFor)
		endif
	endif
	//Verificar onde vai ser o cadastro de coleta!!! Vai comparar por CNPJ como no SPEDFISCAL???---------------------------------------------------------------------------------
endif

//Participante Remetente
cIdParRemetente := cCodRemetente

//Participante Destinatário
cIdParDestinaratio := cCodDestinatario

//Participante Coleta
cIdParColeta  := cCodColeta					

//Participante entrega
cIdParEntrega := cCodEntrega	

lTabDTC := select(cAliasDTC) > 0

//Cria o complemento como um Array vazio
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp] := {}
aadd( oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp], JsonObject():New() )

//Registro D160
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_DESPAC'] := ' ' //Valor padrão
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_CPARRE'] := cIdParRemetente
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_CPARDE'] := cIdParDestinaratio

//Registro D161
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_CPARCO'] := cIdParColeta
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_CPAREN'] := cIdParEntrega

//Registros D160 e D161
oDadosComplemento[cKeyInfEspecificas][cKeyColEntTransp][1]['C3A_TPCARG'] := cTipoTransporte

//Registro D162
If !Empty("VERIFICAR DEPOIS") //Parametro MV_NGD162 - Identifica os CFOPs que nao geram registro D162.
	//Cria o objeto filho como um array vazio
	if !Empty("VERIFICAR DEPOIS")
		oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs] := {}
	endif	

	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs], JsonObject():New() )
	nPosIdentDocs++

	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_CODMOD'] := '000001' //Código 01 - Nota fiscal
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_NUMDOC'] := ""
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_SERIE' ] := ""
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_DTDOC' ] := ""
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_VLDOC' ] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_VLMERC'] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_QTDVOL'] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_PESBRT'] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyIdentDocs][nPosIdentDocs]['C3I_PESLIQ'] := 0

	TAFConOut("VERIFICAR DEPOIS")
EndIf	

//Fecho a tabela caso tenha sido aberta.
if lTabDTC; (cAliasDTC)->(DbCloseArea()); endif

return


/*-----------------------------------------------------------------------
{Protheus.doc} ComplementoComunicacaoTelecomunicacao
Consulta e carrega o Json com as informacações complementareres por 
documento de comunicação e telecomunicação (T013AR - SPED D530 )
@author Carlos Eduardo Nonato
@since 05/07/2023
//----------------------------------------------------------------------*/
Static Function ComplementoComunicacaoTelecomunicacao(cAlias as character, oDadosComplemento as json, oStCompComunicacao as object ) 
Local lRet            := .t.            as logical
Local cQuery                            as character
Local cAliasSFX       := GetNextAlias() as character
Local nPosComunicacao                   as integer
Local cCodPartTaf                       as character
Local cCliSFX         := ""             as character
Local cLojaSFX        := ""             as character
Local cCliOrFor		  := 'SA1' 			as character

if valtype(oStCompComunicacao) != 'O'

	//T013AR
	cQuery += " SELECT  "
	cQuery += " 	SFX.FX_FILIAL, "
	cQuery += " 	SFX.FX_TIPSERV, "
	cQuery += " 	SFX.FX_DTINI, "
	cQuery += " 	SFX.FX_DTFIM, "
	cQuery += " 	SFX.FX_PERFIS, "
	cQuery += " 	SFX.FX_AREATER, "
	cQuery += " 	SFX.FX_TERMINA, "
	cQuery += " 	SFX.FX_TPASSIN, "
	cQuery += " 	SFX.FX_VALTERC, "
	cQuery += " 	coalesce(C10.C10_CODIGO,' ') C10_CODIGO, "	
	cQuery += " 	coalesce(C0M.C0M_ID,' ') C0M_ID, "

	//T015AH - D510
	cQuery += "		SFX.FX_ITEM, "
	cQuery += "		SFX.FX_COD, "
	cQuery += "		SFX.FX_GRPCLAS, "
	cQuery += "		SFX.FX_CLASSIF, "
	cQuery += "		SFX.FX_RECEP, "
	cQuery += "		SFX.FX_LOJAREC, "
	cQuery += "		C0K.C0K_ID "

	cQuery += " FROM " + RetSqlName('SFX') + " SFX "
	cQuery += " 	LEFT JOIN " + RetSqlName('C10') + " C10 ON C10.C10_FILIAL = '" + xFilial('C10') + "' AND C10.C10_CODIGO = SFX.FX_TIPOREC AND C10.D_E_L_E_T_ = ' ' "
	cQuery += " 	LEFT JOIN " + RetSqlName('C0M') + " C0M ON C0M.C0M_FILIAL = '" + xFilial('C0M') + "' AND C0M.C0M_CODIGO = SFX.FX_CLASCON AND C0M.D_E_L_E_T_ = ' ' "
	cQuery += "		LEFT JOIN " + RetSqlName('C0K') + " C0K ON C0K.C0K_FILIAL = '" + xFilial('C0K') + "' AND C0K_CODIGO = SFX.FX_GRPCLAS || SFX.FX_CLASSIF AND C0K.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE SFX.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND SFX.FX_FILIAL  = ? "
	cQuery += " 	AND SFX.FX_TIPOMOV = ? "
	cQuery += " 	AND SFX.FX_DOC     = ? "
	cQuery += " 	AND SFX.FX_SERIE   = ? "
	cQuery += " 	AND SFX.FX_CLIFOR  = ? "
	cQuery += " 	AND SFX.FX_LOJA    = ? "
	cQuery += " ORDER BY SFX.FX_ITEM, SFX.FX_COD"
	cQuery := ChangeQuery(cQuery)

	oStCompComunicacao := FWPreparedStatement():New()
	oStCompComunicacao:SetQuery( cQuery )

endif	

oStCompComunicacao:SetString(1, xFilial('SFX') )
oStCompComunicacao:SetString(2, (cAlias)->FT_TIPOMOV)
oStCompComunicacao:SetString(3, (cAlias)->FT_NFISCAL)
oStCompComunicacao:SetString(4, (cAlias)->FT_SERIE  )
oStCompComunicacao:SetString(5, (cAlias)->FT_CLIEFOR)
oStCompComunicacao:SetString(6, (cAlias)->FT_LOJA   )
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStCompComunicacao:GetFixQuery(),{}), cAliasSFX, .f., .t.)

While (cAliasSFX)->(!eof()) 

	cCliSFX  := (cAliasSFX)->FX_RECEP
	cLojaSFX := (cAliasSFX)->FX_LOJAREC

	//Cria o complemento como um Array vazio
	if (cAliasSFX)->(RecNo()) == 1; oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao] := {}; endif

	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao], JsonObject():New() )
	nPosComunicacao++

	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_INDSER'] := (cAliasSFX)->FX_TIPSERV
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_DTINI' ] := (cAliasSFX)->FX_DTINI
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_DTFIM' ] := (cAliasSFX)->FX_DTFIM
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_PERFIS'] := (cAliasSFX)->FX_PERFIS
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_CODARE'] := (cAliasSFX)->FX_AREATER
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_TERMIN'] := (cAliasSFX)->FX_TERMINA
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_TPASSI'] := (cAliasSFX)->FX_TPASSIN
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_INDREC'] := (cAliasSFX)->C10_CODIGO
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_VLTERC'] := (cAliasSFX)->FX_VALTERC
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_VLSERV'] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_VSERNT'] := 0
	oDadosComplemento[cKeyInfEspecificas][cKeyComunicacao][nPosComunicacao]['C2G_CCONS' ] := (cAliasSFX)->C0M_ID

	//Monta o código do cliente no TAF.
	cCodPartTaf := ComplementParticipantIntegration(cCliSFX, cLojaSFX, cCliOrFor)

	//Guardo as informações no array para posteriormente montar o layout T015AH(D510).
	aadd(aRegD510,{ alltrim((cAlias)->FT_TIPOMOV),;
					alltrim((cAlias)->FT_NFISCAL),;
					alltrim((cAlias)->FT_SERIE  ),;
					alltrim((cAlias)->FT_CLIEFOR),;
					alltrim((cAlias)->FT_LOJA   ),;
					alltrim((cAliasSFX)->FX_ITEM),;
				    alltrim((cAliasSFX)->FX_COD ),;
					alltrim((cAliasSFX)->C0K_ID ),;
					alltrim(cCodPartTaf),;
					alltrim((cAliasSFX)->C10_CODIGO) } ) 

	(cAliasSFX)->(DbSkip())

enddo
(cAliasSFX)->(DbCloseArea())

return lRet

/*-----------------------------------------------------------------------
{Protheus.doc} ComplementoRodoAquaAereoCargas
Consulta e carrega o Json com os locais de coleta e entrega de documentos
fiscais de transporte (T013AO|T013AU|T013AV - SPED D130|D140|D150)
@author Carlos Eduardo Nonato
@since 21/06/2023
//----------------------------------------------------------------------*/
static function ComplementoRodoAquaAereoCargas(cAlias as character, oDadosComplemento as json, oStConhecimentoCarga as object, cEspecie as character)

Local lBuscaRemTMS                                        as logical
Local lBuscaDesTMS                                        as logical
Local cQuery                                              as character
Local cIndRedespacho                                      as character
Local cCodConsignatario                                   as character
Local cIdConsignatario                                    as character
Local cCodDespachante                                     as character
Local cIdDespachante                                      as character
Local cCodRemetente     := alltrim((cAlias)->COD_PART)    as character
Local cCodDestinatario  := alltrim((cAlias)->COD_PART)    as character
Local cNumViagem                                          as character
Local cDT6FILDOC                                          as character
Local cDT6DOC                                             as character
Local cDT6SERIE                                           as character
Local cIdUfOrigem                                         as character
Local cCodUfOrigem                                        as character
Local cIdMunOrigem                                        as character
Local cCodMunOrigem                                       as character
Local cIdUfDestino                                        as character
Local cCodUfDestino                                       as character
Local cIdMunDestino                                       as character
Local cCodMunDestino                                      as character
Local aCompFrete        := &(GetNewPar('MV_COMPFRT', {})) as array
Local cAliasDT8         := GetNextAlias()                 as character
Local nIndVazio                                           as integer
Local cIdVeiculo                                          as character
Local cNewId                                              as character
Local aOutputSp         := {}                             as array
Local cParRem           := ""                             as character
Local cLojRem           := ""                             as character
Local cParDest          := ""                             as character
Local cLojDest          := ""                             as character
Local cParCons          := ""                             as character
Local cLojCons          := ""                             as character
Local cParDesp          := ""                             as character
Local cLojDesp          := ""                             as character
Local cCliOrFor         := 'SA1'     					  as character

if valtype(oStConhecimentoCarga) != 'O'
	//Aquaviario ou aereo
	if cEspecie != '08'
		cQuery += " SELECT "
		cQuery += " 	COALESCE(SUM(CASE WHEN DT8_CODPAS = 'TF'  THEN DT8_VALTOT ELSE 0 END),0) VLR_BRUTO, "
		cQuery += " 	COALESCE(SUM(CASE WHEN DT8_CODPAS <> 'TF' THEN round(DT8_VALTOT,2) ELSE 0 END),0) VLR_OUTRO "
		cQuery += " FROM " + RetSqlName('DT8') + ' DT8 '
	else //Rodoviario	
		cQuery += " SELECT "
		cQuery += "		DT3.DT3_TIPCMP,
		cQuery += "		DT8.DT8_CODPAS,
		cQuery += "		DT8.DT8_VALTOT	
		cQuery += " FROM " + RetSqlName('DT8') + ' DT8 '
		cQuery += " 	LEFT JOIN " + RetSqlName('DT3') + " DT3 ON DT3.DT3_FILIAL = '" + xFilial('DT3') + "' AND DT3.DT3_CODPAS = DT8.DT8_CODPAS AND DT3.D_E_L_E_T_ = ' '
	endif	
									   
	cQuery += " WHERE DT8.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND DT8.DT8_FILIAL = ? "
	cQuery += " 	AND DT8.DT8_FILDOC = ? "
	cQuery += " 	AND DT8.DT8_DOC    = ? "
	cQuery += " 	AND DT8.DT8_SERIE  = ? " 
		
	oStConhecimentoCarga := FWPreparedStatement():New()
	oStConhecimentoCarga:SetQuery( cQuery )
endif	

//Caso tenha conhecimento de transporte, busco os dados de lá
if (cAlias)->RECDT6 > 0

 	//Indice Redespacho
 	cIndRedespacho := iif( (cAlias)->DT6_TIPFRE == '1','0','1')
	
 	//Participante remetente
 	if !empty( (cAlias)->(DT6_CLIREM+DT6_LOJREM) )
 		cParRem    := (cAlias)->DT6_CLIREM
 		cLojRem    := (cAlias)->DT6_LOJREM
 		cCodRemetente := ComplementParticipantIntegration(cParRem, cLojRem, cCliOrFor) //Monto o código para pesquisar no TAF
 		lBuscaRemTMS := cEspecie == '08' //Rodoviario
 	endif

 	//Participante destinatario
 	if !empty((cAlias)->(DT6_CLICAL+DT6_LOJCAL))
 		cParDest    := (cAlias)->DT6_CLICAL
 		cLojDest    := (cAlias)->DT6_LOJCAL
 		cCodDestinatario := ComplementParticipantIntegration(cParDest, cLojDest, cCliOrFor)
 		lBuscaDesTMS := cEspecie == '08' //Rodoviario
 	endif	

 	//Participante consignatario Rodoviario ou aquaviario
 	if cEspecie $ '|08|09|' .and. !empty((cAlias)->(DT6_CLICON+DT6_LOJCON))
 		cParCons    := (cAlias)->DT6_CLICON
 		cLojCons    := (cAlias)->DT6_LOJCON
 		cCodConsignatario := ComplementParticipantIntegration(cParCons, cLojCons, cCliOrFor)
 	endif

 	//Participante despachante rodoviario
 	if cEspecie == '08' .and. !empty((cAlias)->(DT6_CLIDPC+DT6_LOJDPC))
 		cParDesp    := (cAlias)->DT6_CLIDPC
 		cLojDesp    := (cAlias)->DT6_LOJDPC
 		cCodDespachante := ComplementParticipantIntegration(cParDesp, cLojDesp, cCliOrFor)
 	endif

 	//Monto a chave para busca na nas tabelas do TMS DT5(Solicitação de Coleta) e DTC(Doc Cliente para Transporte)
 	If val((cAlias)->DT6_DOCTMS) != 8
 		cDT6FILDOC := (cAlias)->(DT6_FILDOC) 
 		cDT6DOC    := (cAlias)->(DT6_DOC)
 		cDT6SERIE  := (cAlias)->(DT6_SERIE)
 	Else
 		cDT6FILDOC := (cAlias)->(DT6_FILDCO) 
 		cDT6DOC    := (cAlias)->(DT6_DOCDCO)
 		cDT6SERIE  := (cAlias)->(DT6_SERDCO)
 	EndIf			

 	//Movimento de viagem
 	if DUD->( MsSeek( xFilial('DUD') + cDT6FILDOC + cDT6DOC + cDT6SERIE + (cAlias)->DT6_FILORI ))
 		//Viagem
 		if DTQ->( MsSeek( xFilial('DTQ') + DUD->(DUD_FILORI+DUD_VIAGEM) ) )
 			//Veiculo da viagem
 			if DTR->( MsSeek( xFilial('DTR') + DTQ->(DTQ_FILORI+DTQ_VIAGEM) ) )
 				//Veiculo cadastrado no TAF
 				if C0Q->(MsSeek( xFilial('C0Q')+DTR->DTR_CODVEI )) .or. C0Q->(MsSeek( xFilial('C0Q')+DTR->DTR_CODRB1))
 					cIdVeiculo := C0Q->C0Q_ID
 				elseif !Empty(DTR->DTR_CODVEI) .or. !Empty(DTR->DTR_CODRB1)
 					cProcedure 	:= "TAF613N"
 					If !Empty(DTR->DTR_CODVEI)
 						cIdVeiculo :=DTR->DTR_CODVEI        
                     ElseIf  !Empty(DTR->DTR_CODRB1)
 						cIdVeiculo :=DTR->DTR_CODRB1        	
 					endif
	                
 					cNewId := GetSx8Num('C0Q','C0Q_ID',,4)
					
 					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cIdVeiculo, Alltrim(cNewId), "")
			
 					If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)		
 						if __lSx8; RollbackSX8('C0Q'); endif
 						aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C0Q'), '', .t. }
 						ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
 					else
 						if __lSx8; ConfirmSX8('C0Q'); endif
 					EndIf										
 				endif	
 			endif
 		endif
 		cNumViagem := DUD->DUD_VIAGEM
 	endif
endif

//Remetente

C1H->(MsSeek( xFilial('C1H') + cCodRemetente ))

if lBuscaRemTMS
 	//Retorno os códigos do municipio e uf das tabelas autocontidas do TAF
 	cCodMunOrigem := GetIdMunServ(C1H->C1H_CODMUN, @cCodUfOrigem, .f.)
	
 	//Busca municipio de origem conforme regras do TMS
 	cCodMunTms := TMSCodMun((cAlias)->COD_MUN_ORI, .t., , cCodMunOrigem, cCodUfOrigem)
	
 	//Retorno os ids do municipio e uf das tabelas autocontidas do TAF
 	cIdMunOrigem := GetIdMunServ(cCodMunTms, @cIdUfOrigem)	
endif

if !lBuscaRemTMS .or. empty(cIdMunOrigem) .or. alltrim(cIdMunOrigem) == 'NOTFOUND'
	cIdUfOrigem  := C1H->C1H_UF
	cIdMunOrigem := C1H->C1H_CODMUN
endif
	
//Destinatario
C1H->(MsSeek( xFilial('C1H') + cCodDestinatario ))

if lBuscaDesTMS
 	//Retorno os códigos do municipio e uf das tabelas autocontidas do TAF
 	cCodMunDestino := GetIdMunServ(C1H->C1H_CODMUN, @cCodUfDestino, .f.)
	
 	//Busca municipio de origem conforme regras do TMS
 	cCodMunTms := TMSCodMun((cAlias)->COD_MUN_DEST, .t., , cCodMunDestino, cCodUfDestino)
	
 	//Retorno os ids do municipio e uf das tabelas autocontidas do TAF
 	cIdMunDestino := GetIdMunServ(cCodMunTms, @cIdUfDestino)			
endif

if !lBuscaDesTMS .or. empty(cIdMunDestino) .or. alltrim(cIdMunDestino) == 'NOTFOUND'
	cIdUfdestino  := C1H->C1H_UF
	cIdMunDestino := C1H->C1H_CODMUN
endif					
	
cIdConsignatario := cCodConsignatario
 
cIdDespachante := cCodDespachante
							
//Para maior perforfamnce, só executo a query que soma os valores caso os registros forem gravados
oStConhecimentoCarga:SetString(1, xFilial('DT8') )
oStConhecimentoCarga:SetString(2, cDT6FILDOC )
oStConhecimentoCarga:SetString(3, cDT6DOC  )
oStConhecimentoCarga:SetString(4, cDT6SERIE )
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStConhecimentoCarga:GetFixQuery(),{}), cAliasDT8, .f., .t.)

//Monta o json conforme a especie passada por paramentro.
do case
	case cEspecie == '08' //D130 Complemento do conhecimento rodoviario de cargas (Código 08)
		
		//Crio o objeto de complemento como um array vazio
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario] := {}
		aadd( oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario], JsonObject():New() )

		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_UFORIG'] := cIdUfOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_CMUNOR'] := cIdMunOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_UFDEST'] := cIdUfDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_CMUNDE'] := cIdMunDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_CODVEI'] := cIdVeiculo
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_CPARCO'] := cIdConsignatario
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_CPARRE'] := cIdDespachante
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_INDRED'] := cIndRedespacho

		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VLIQFR'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VSECAT'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VDESP '] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VPEDAG'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VOUTRO'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VTOTFR'] := 0

		//Campos usados somente para o modelo 26 (Conhecimento multimodal de cargas)
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_INDNAT'] := '' 
		oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VGRIS' ] := 0  

		//Avalia se todos os itens do array estão vazios
		aEval(aCompFrete, {|x| iif(empty(x), nIndVazio++,) } )

		while (cAliasDT8)->(!eof())

			if len(aCompFrete) = 4 

				//Usa a mesma condicação para as duas situações
				if (cAliasDT8)->DT8_CODPAS == 'TF'
					oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VTOTFR'] += (cAliasDT8)->DT8_VALTOT
				endif	

				if nIndVazio = 4
					do case
						case (cAliasDT8)->DT3_TIPCMP == '1'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VLIQFR'] += (cAliasDT8)->DT8_VALTOT
						
						case (cAliasDT8)->DT3_TIPCMP == '2'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VSECAT'] += (cAliasDT8)->DT8_VALTOT
						
						case (cAliasDT8)->DT3_TIPCMP == '3'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VDESP '] += (cAliasDT8)->DT8_VALTOT

						case (cAliasDT8)->DT3_TIPCMP == '4'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VPEDAG'] += (cAliasDT8)->DT8_VALTOT

						case (cAliasDT8)->DT8_CODPAS != 'TF'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VOUTRO'] += (cAliasDT8)->DT8_VALTOT
					endcase	
				else
					do case
						case (cAliasDT8)->DT8_CODPAS == aCompFrete[1]
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VLIQFR'] += (cAliasDT8)->DT8_VALTOT
						
						case (cAliasDT8)->DT8_CODPAS == aCompFrete[2]
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VSECAT'] += (cAliasDT8)->DT8_VALTOT
						
						case (cAliasDT8)->DT8_CODPAS == aCompFrete[3]
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VDESP '] += (cAliasDT8)->DT8_VALTOT

						case (cAliasDT8)->DT8_CODPAS == aCompFrete[4]
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VPEDAG'] += (cAliasDT8)->DT8_VALTOT

						case (cAliasDT8)->DT8_CODPAS != 'TF'
							oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VOUTRO'] += (cAliasDT8)->DT8_VALTOT
					endcase						
				endif
			else
				if (cAliasDT8)->DT8_CODPAS == 'TF'
					oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VTOTFR'] += (cAliasDT8)->DT8_VALTOT
				else
					oDadosComplemento[cKeyInfEspecificas][cKeyRodoviario][1]['C3F_VOUTRO'] += (cAliasDT8)->DT8_VALTOT	
				endif
			endif
			
			(cAliasDT8)->(DbSkip())
		enddo

	case cEspecie == '09' //D140 Complemento do conhecimento aquaviario de cargas (Código 09)
		//Crio o objeto de complemento como um array vazio
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario] := {}
		aadd( oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario], JsonObject():New() )

		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_UFORIG'] := cIdUfOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_CMUNOR'] := cIdMunOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_UFDEST'] := cIdUfDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_CMUNDE'] := cIdMunDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_CODVEI'] := cIdVeiculo
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_CPARCO'] := cIdConsignatario
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VIAGEM'] := cNumViagem
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VLIQFR'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VDEPOR'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VDESCD'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VADMM '] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VOUTRO'] := (cAliasDT8)->VLR_OUTRO
		oDadosComplemento[cKeyInfEspecificas][cKeyAquaviario][1]['C3G_VBRTFR'] := (cAliasDT8)->VLR_BRUTO

	case cEspecie == '10' //Aéreo D150 Complemento do conhecimento aereo (Código 10)
		//Crio o objeto de complemento como um array vazio
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo] := {}
		aadd( oDadosComplemento[cKeyInfEspecificas][cKeyAereo], JsonObject():New() )

		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_UFORIG'] := cIdUfOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_CMUNOR'] := cIdMunOrigem
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_UFDEST'] := cIdUfDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_CMUNDE'] := cIdMunDestino
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_CODVEI'] := cIdVeiculo
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_VIAGEM'] := cNumViagem
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_INDTAR'] := '0' //Exp
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_VTXTER'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_VTXRED'] := 0
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_VTXAD' ] := 0	
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_VOUTRO'] := (cAliasDT8)->VLR_OUTRO
		oDadosComplemento[cKeyInfEspecificas][cKeyAereo][1]['C3H_PESOTX'] := (cAliasDT8)->VLR_BRUTO
endcase
  
//Fecho a tabela caso tenha sido aberta.
if select(cAliasDT8) > 0; (cAliasDT8)->(DbCloseArea()); endif

return 


/*-----------------------------------------------------------------------
{Protheus.doc} ComplementoDocsInformados
Consulta e carrega o Json com o complemneto dos documentos informados 
(Código 13, 14, 15 e 16) (T013AY)
@author Carlos Eduardo Nonato
@since 14/07/2023
//----------------------------------------------------------------------*/
Static Function ComplementoDocsInformados(cAlias as character, oDadosComplemento as json, oStDocsInf as object, lTotaliza as logical, aTotalizaSFT as array ) as logical
Local lRet as logical
Local cIdUfOrigem  as character
Local cIdMunOrigem as character 
Default lTotaliza := .f.
Default aTotalizaSFT := {}
							   
if !lTotaliza
	cIdMunOrigem := GetIdMunServ((cAlias)->(F2_UFORIG+F2_CMUNOR), @cIdUfOrigem)
	//Crio o objeto de complemento como um array vazio												  
	oDadosComplemento[cKeyInfEspecificas][cKeyCompDocsInf] := {}
	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyCompDocsInf], JsonObject():New() )			
	oDadosComplemento[cKeyInfEspecificas][cKeyCompDocsInf][1]['C7B_UF'    ] := cIdUfOrigem
	oDadosComplemento[cKeyInfEspecificas][cKeyCompDocsInf][1]['C7B_CODMUN'] := cIdMunOrigem
	lRet := .t.
else
	if len(aTotalizaSFT) >= 3
		oDadosComplemento[cKeyCompDocsInf][1]['C7B_VLSERV'] := aTotalizaSFT[1]
		oDadosComplemento[cKeyCompDocsInf][1]['C7B_VLBCIC'] := aTotalizaSFT[2]
		oDadosComplemento[cKeyCompDocsInf][1]['C7B_VLICMS'] := aTotalizaSFT[3]
	endif	
endif
return lRet

/*----------------------------------------------------------------------
{Protheus.doc} ProcessosReferenciados
@description Consulta e carrega o Json com o complemneto dos processos referenciados
				(T013AB - SPED C111)
@author jose.riquelmo
@since 28/06/2023
@param cAlias - Alias da query principal
@param oDadosComplemento - Objeto JSON contendo o complemento do documento fiscal
@param oStProcess - Objeto contendo uma instância da classe FwExecStatement()
@return Nil
//----------------------------------------------------------------------*/
Static Function ProcessosReferenciados(cAlias as character, oDadosComplemento as json, oStProcess as object, cEspecie as character)

	Local aOutputSp				:= {}	as array
	Local cQuery 		as character
	Local cAliasCDG		as character
	Local cIdC1G 		as character
	Local cIdC3Q		as character
	Local cProcedure	as character
	Local cKeyReg		as character
	Local cMsgLog 		as character
	Local iProcesso		as integer
	Local lLogCadastro	as logical
	
	Default cAlias 				:= ""
	Default oDadosComplemento 	:= Nil
	Default oStProcess			:= Nil

	If !Empty(cAlias) .And. oDadosComplemento != Nil
		If oStProcess == Nil
			cQuery := " SELECT "
			cQuery += "		CCF.R_E_C_N_O_ RECNO_CCF, "
			cQuery += "		CDG.CDG_IFCOMP, "
			cQuery += "		COALESCE(C3Q.C3Q_ID, ' ') C3Q_ID, " 
			cQuery += "		COALESCE(C1G.C1G_ID, ' ') C1G_ID "
			cQuery += " FROM " + RetSqlName("CDG") + " CDG "
			cQuery += "		INNER JOIN " + RetSqlName("CCF") + " CCF "
			cQuery += "			ON CCF.D_E_L_E_T_ = ' ' " 
			cQuery += "				AND CCF.CCF_FILIAL = ? "
			cQuery += "				AND CCF.CCF_NUMERO = CDG.CDG_PROCES "
			cQuery += "				AND CCF.CCF_TIPO = CDG.CDG_TPPROC "
			cQuery += "				AND CCF.CCF_IDITEM = CDG.CDG_ITPROC "
			cQuery += "		LEFT JOIN " + RetSqlName("C1G") + " C1G "
			cQuery += "			ON C1G.D_E_L_E_T_ = ' ' " 
			cQuery += "				AND C1G.C1G_FILIAL = ? "
			cQuery += "				AND C1G.C1G_NUMPRO = CDG.CDG_PROCES "
			cQuery += "				AND C1G.C1G_INDPRO = CDG.CDG_TPPROC "
			cQuery += " 	LEFT JOIN " + RetSqlName("C3Q") + " C3Q "
			cQuery += " 		ON C3Q.D_E_L_E_T_ = ' ' "
			cQuery += " 			AND C3Q.C3Q_FILIAL = ? "
			cQuery += " 			AND C3Q.C3Q_CODINF = CDG.CDG_IFCOMP "
			cQuery += " 	WHERE CDG.D_E_L_E_T_ = ' ' "
			cQuery += " 		AND CDG.CDG_FILIAL = ? "
			cQuery += " 		AND CDG.CDG_TPMOV = ? "
			cQuery += " 		AND CDG.CDG_SERIE = ? "
			cQuery += " 		AND CDG.CDG_DOC = ? "
			cQuery += " 		AND CDG.CDG_CLIFOR = ? "
			cQuery += " 		AND CDG.CDG_LOJA = ? "

			cQuery 		:= ChangeQuery(cQuery)
			oStProcess 	:= FwExecStatement():New(cQuery)
		EndIf

		If oStProcess != Nil
			oStProcess:SetString(1, xFilial("CCF")		)
			oStProcess:SetString(2, xFilial("C1G")		)
			oStProcess:SetString(3, xFilial("C3Q")		)
			oStProcess:SetString(4, xFilial("CDG")		)
			oStProcess:SetString(5, (cAlias)->FT_TIPOMOV)
			oStProcess:SetString(6, (cAlias)->FT_SERIE	)
			oStProcess:SetString(7, (cAlias)->FT_NFISCAL) 
			oStProcess:SetString(8, (cAlias)->FT_CLIEFOR) 
			oStProcess:SetString(9, (cAlias)->FT_LOJA   )

			cAliasCDG := oStProcess:OpenAlias()
			
			While !(cAliasCDG)->(EOF())
				
				cKeyReg		:= cValToChar((cAliasCDG)->RECNO_CCF)
				If cKeyReg <> "0"
					
					cProcedure 	:= "TAF613Q"
					cIdC1G		:= (cAliasCDG)->C1G_ID
					
					If Empty(cIdC1G)
						cIdC1G := GetSX8Num("C1G", "C1G_ID") 
					EndIf

					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, Alltrim(cIdC1G), "")
			
					If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)		
						if __lSx8; RollbackSX8('C1G'); endif
						aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C1G'), '', .t., 'SFT' }
						ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
					else
						if __lSx8; ConfirmSX8('C1G'); endif
					endif	
				EndIf  

				If cEspecie $ "08|8B|09|10|11|26|27"
					//Se não for vazio, cadastra o código de informação complementar caso não exista no TAF
					if !empty((cAliasCDG)->CDG_IFCOMP) 

						cProcedure 	:= "TAF613S"
						cIdC3Q		:= alltrim((cAliasCDG)->C3Q_ID)
						cKeyReg		:= (cAliasCDG)->CDG_IFCOMP

						//Gera um novo id caso não exista o registro na C3Q
						if Empty(cIdC3Q)
							cIdC3Q := TAFGeraId("TAF")
						endif

						//Executa a procedure de criação\atualização da tabela de codigo de complementos do TAF
						aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cKeyReg, cIdC3Q, "")	  
										
						//Se apresentou erro na execução da procedure
						if Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
							cMsgLog := STR0003 + cProcedure + CRLF + TCSQLError() //'Erro gerado na execução da procedure: '
							lLogCadastro := .t.
						endif	

						//Verifico pelo id se tem Cadastro de Informações complementares no Json(CDT), caso não faço a inclusão.
						GetPosJsonInfoComplementar(cAlias, oDadosComplemento, cIdC3Q)
					else
						//Cria a descrição para apresentar no log de falha da integração
						cMsgLog := STR0004 + 'CDG_IFCOMP' //'Obs.: Faltam informações na origem da integração. Campo: '
					endif		

					//Se ocorreu erro na execução da procedure ou se não foi preenchido o campo de de código de informação complementar na origem!
					if !empty(cMsgLog)
						aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('CDG'), cMsgLog, lLogCadastro, 'SFT' }
						ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "				
					endif

					If (cAliasCDG)->(RecNo()) == 1
						oDadosComplemento[cKeyInfEspecificas][cKeyProcessos] := {}
					EndIf

					AAdd(oDadosComplemento[cKeyInfEspecificas][cKeyProcessos], JsonObject():New())
					
					iProcesso++

					oDadosComplemento[cKeyInfEspecificas][cKeyProcessos][iProcesso]["C6W_NUMERO"] := cIdC1G
					oDadosComplemento[cKeyInfEspecificas][cKeyProcessos][iProcesso]["C6W_CODINF"] := cIdC3Q 

					//Limpa as variaveis para o próximo item.
					cIdC1G := ''
					cIdC3Q := ''
				endif

				(cAliasCDG)->(DbSkip())

			EndDo
			
			(cAliasCDG)->(DbCloseArea())

			FwFreeArray(aOutputSp)
		EndIf
	EndIf

Return

/*----------------------------------------------------------------------
{Protheus.doc} OperacoesImportacao()
Consulta e carrega o Json com as operações de Importação
(T013AC - SPED C120)
@author jose.riquelmo
@since 28/06/2023
//----------------------------------------------------------------------*/
Static Function OperacoesImportacao(cAlias as character, oDadosComplemento as json, oStImport as object)
Local cQuery as character
Local cSigaEIC := GetNewPar( 'MV_EASY' , '' ) as character
Local cAliasCD5 := GetNextAlias() as character
Local nPosImport as integer

If	cSigaEIC == "S" 
	aAvImport	:=  (cAlias)->(AvGetImpSped(xFilial("SF1"),(cAlias)->FT_NFISCAL,(cAlias)->FT_SERIE,(cAlias)->FT_CLIEFOR,(cAlias)->FT_LOJA))
	If Len(aAvImport)>0

		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes] := {}
		aadd(oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes], JsonObject():New())
	
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][1]['C23_TIPO'  ] := aAvImport[1,2,1,2,1] 
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][1]['C23_NUMDOC'] := aAvImport[1,2,2,2,1]
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][1]['C23_VLRPIS'] := aAvImport[1,2,3,2,1]     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][1]['C23_VLRCOF'] := aAvImport[1,2,4,2,1]
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][1]['C23_NDRAW' ] := ""   
	Endif 
Else
	if valtype(oStImport) != 'O' 

		cQuery += " SELECT " 
		cQuery += "     CD5.CD5_TPIMP , " 
		cQuery += "     CD5.CD5_DOCIMP, " 
		cQuery += "     CD5.CD5_ACDRAW, " 
		cQuery += "     MAX(CD5.CD5_DTPPIS) CD5_DTPPIS, " 
		cQuery += "     MAX(CD5.CD5_DTPCOF) CD5_DTPCOF, " 
		cQuery += "     MAX(CD5.CD5_LOCAL) CD5_LOCAL , " 
		cQuery += "     SUM(CD5.CD5_BSPIS) CD5_BSPIS , " 
		cQuery += " 	SUM(CD5.CD5_VLPIS) CD5_VLPIS , " 
		cQuery += " 	SUM(CD5.CD5_BSCOF) CD5_BSCOF , " 
		cQuery += " 	SUM(CD5.CD5_VLCOF) CD5_VLCOF  " 
		cQuery += " FROM " + RetSqlName('CD5') + " CD5  " 
		cQuery += " WHERE CD5.D_E_L_E_T_ = ' ' " 
		cQuery += " 	AND CD5.CD5_FILIAL = ? "
		cQuery += " 	AND CD5.CD5_DOC    = ? "
		cQuery += " 	AND CD5.CD5_SERIE  = ? "
		cQuery += " 	AND CD5.CD5_FORNEC = ? "
		cQuery += " 	AND CD5.CD5_LOJA   = ? "
		cQuery += " GROUP BY CD5.CD5_TPIMP, CD5.CD5_DOCIMP, CD5.CD5_ACDRAW " 

		oStImport := FWPreparedStatement():New()
		oStImport:SetQuery( cQuery )
	endif

	oStImport:SetString(1, xFilial('CD5') )
	oStImport:SetString(2, (cAlias)->FT_NFISCAL) 
	oStImport:SetString(3, (cAlias)->FT_SERIE  )
	oStImport:SetString(4, (cAlias)->FT_CLIEFOR) 
	oStImport:SetString(5, (cAlias)->FT_LOJA   )
	dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStImport:GetFixQuery(),{}), cAliasCD5, .f., .t.)
							
	if (cAliasCD5)->(!eof())
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes] := {}
	endif	

	While (cAliasCD5)->(!eof())

		aadd( oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes], JsonObject():New() )	
		nPosImport++		

		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_TIPO'  ] := (cAliasCD5)->CD5_TPIMP     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_NUMDOC'] := (cAliasCD5)->CD5_DOCIMP     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_NDRAW' ] := (cAliasCD5)->CD5_ACDRAW     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_PGPIS' ] := alltrim((cAliasCD5)->CD5_DTPPIS)
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_PGCOF' ] := alltrim((cAliasCD5)->CD5_DTPCOF)
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_LES'   ] := alltrim((cAliasCD5)->CD5_LOCAL )
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_BCPIS' ] := (cAliasCD5)->CD5_BSPIS //'151,15'
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_VLRPIS'] := (cAliasCD5)->CD5_VLPIS     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_BCCOF' ] := (cAliasCD5)->CD5_BSCOF     
		oDadosComplemento[cKeyInfEspecificas][cKeyImportacoes][nPosImport]['C23_VLRCOF'] := (cAliasCD5)->CD5_VLCOF     

		(cAliasCD5)->(DbSkip())

	enddo

	(cAliasCD5)->(DbCloseArea())	

endif	

return

/*----------------------------------------------------------------------
{Protheus.doc} OperacoesCombustiveis()
Consulta e carrega o Json com Combustíveis
(T013AK - SPED C165)
@author jose.riquelmo
@since 28/06/2023
//----------------------------------------------------------------------*/
Static Function OperacoesCombustiveis(cAlias as character, oDadosComplemento as json, oStCombus as object)
Local cQuery as character
Local cAliasCD6 := GetNextAlias() as character
Local nPosOpCombustivel as integer
Local cIdParticipante   as character

if valtype(oStCombus) != 'O'
	
	cQuery := " SELECT "
	cQuery += "		COALESCE(C0Q.C0Q_ID ,' ') C0Q_ID,
	cQuery += "		CD6.CD6_SEFAZ, "
	cQuery += "		CD6.CD6_PASSE, "
	cQuery += "		CD6.CD6_HORA, "
	cQuery += "		CD6.CD6_TEMP, "
	cQuery += "		CD6.CD6_VOLUME, "
	cQuery += "		CD6.CD6_PBRUTO, "
	cQuery += "		CD6.CD6_PLIQUI, "
	cQuery += "		CD6.CD6_MOTOR, "
	cQuery += "		CD6.CD6_CPFMOT, "
	cQuery += "		CD6.CD6_PLACA, "
	cQuery += "		CD6.CD6_CODANP, "
	cQuery += "		CD6.CD6_TRANSP "
	cQuery += "  FROM " + RetSqlName('CD6') + " CD6 "
	cQuery += "	 LEFT JOIN " + RetSqlName('C0Q') + " C0Q ON C0Q.C0Q_FILIAL = '" + xFilial('C0Q') + "' AND C0Q.C0Q_PLACA = CD6.CD6_PLACA AND C0Q.D_E_L_E_T_ = ' ' "	
	cQuery += "  WHERE CD6.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND CD6.CD6_FILIAL = ? "
	cQuery += " 	AND CD6.CD6_TPMOV = ? "
	cQuery += " 	AND CD6.CD6_SERIE = ? "
	cQuery += " 	AND CD6.CD6_DOC = ? "
	cQuery += " 	AND CD6.CD6_CLIFOR = ? "
	cQuery += " 	AND CD6.CD6_LOJA = ? "

	oStCombus := FWPreparedStatement():New()
	oStCombus:SetQuery( cQuery )
endif

oStCombus:SetString(1, xFilial('CD6') )
oStCombus:SetString(2, (cAlias)->FT_TIPOMOV)
oStCombus:SetString(3, (cAlias)->FT_SERIE  )
oStCombus:SetString(4, (cAlias)->FT_NFISCAL) 
oStCombus:SetString(5, (cAlias)->FT_CLIEFOR) 
oStCombus:SetString(6, (cAlias)->FT_LOJA   )
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStCombus:GetFixQuery(),{}), cAliasCD6, .f., .t.)

if (cAliasCD6)->(!eof()) 
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis] := {}
endif	

While (cAliasCD6)->(!eof())

	//Integra o transportador ao TAF caso ainda não exista na base.
	cIdParticipante := ComplementParticipantIntegration((cAliasCD6)->CD6_TRANSP, '', 'SA4', 'CD6')

	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis], JsonObject():New() )
	nPosOpCombustivel++

	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_CODPAR'] := cIdParticipante  
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_CODVEI'] := alltrim((cAliasCD6)->C0Q_ID)
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_CODAUT'] := alltrim((cAliasCD6)->CD6_SEFAZ)
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_PASSE' ] := alltrim((cAliasCD6)->CD6_PASSE)  
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_HORA'  ] := (cAliasCD6)->CD6_HORA  
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_TEMPER'] := (cAliasCD6)->CD6_TEMP  
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_QTDVOL'] := (cAliasCD6)->CD6_VOLUME
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_PESBRT'] := (cAliasCD6)->CD6_PBRUTO
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_PESLIQ'] := (cAliasCD6)->CD6_PLIQUI
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_NOMMOT'] := (cAliasCD6)->CD6_MOTOR  
	oDadosComplemento[cKeyInfEspecificas][cKeyCombustiveis][nPosOpCombustivel]['C2B_CPF'   ] := (cAliasCD6)->CD6_CPFMOT

	(cAliasCD6)->(DbSkip())
enddo

(cAliasCD6)->(DbCloseArea())

return

/*----------------------------------------------------------------------
{Protheus.doc} GetCountCda()
Função responsavel por efetuar um Count na tabela CDA para verificar se existe algum código de lançamento nesta nota
para que não seja chamado mais funções de processamento desnecessáriamente
@author adilson.roberto
@since 28/11/2023
//----------------------------------------------------------------------*/
Static Function GetCountCda(cQuery as character)
DEFAULT cQuery := " " 

	cQuery += " (SELECT COUNT(CDA.CDA_CODLAN) QTDLAN "
	cQuery += "	  FROM " + RetSqlName('CDA') + " CDA "
	cQuery += "	  WHERE CDA.CDA_FILIAL = '" + xFilial('CDA') + "'"
	cQuery += "			AND CDA.CDA_TPMOVI = SFT.FT_TIPOMOV "
	cQuery += "			AND CDA_ESPECI     = SFT.FT_ESPECIE "
	cQuery += "			AND CDA.CDA_NUMERO = SFT.FT_NFISCAL "
	cQuery += "			AND CDA.CDA_SERIE  = SFT.FT_SERIE "
	cQuery += "			AND CDA.CDA_CLIFOR = SFT.FT_CLIEFOR "
	cQuery += "			AND CDA.CDA_LOJA   = SFT.FT_LOJA "
	cQuery += "			AND CDA.D_E_L_E_T_ = ' ') "
	cQuery += "	 QTDLAN, "
Return

/*----------------------------------------------------------------------
{Protheus.doc} GetCountCdg()
Função responsavel por efetuar um Count na tabela CDA para verificar se existe algum código de lançamento nesta nota
para que não seja chamado mais funções de processamento desnecessáriamente
@author adilson.roberto
@since 28/11/2023
//----------------------------------------------------------------------*/
Static Function GetCountCdg(cQuery as character)
DEFAULT cQuery := " " 

	cQuery += " (SELECT COUNT(CDG.CDG_PROCES) QTDPROC "
	cQuery += "	  FROM " + RetSqlName('CDG') + " CDG "
	cQuery += "	  WHERE CDG.CDG_FILIAL = '" + xFilial('CDG') + "'"
	cQuery += "			AND CDG.CDG_TPMOV  = SFT.FT_TIPOMOV "
	cQuery += "			AND CDG.CDG_DOC    = SFT.FT_NFISCAL "
	cQuery += "			AND CDG.CDG_SERIE  = SFT.FT_SERIE "
	cQuery += "			AND CDG.CDG_CLIFOR = SFT.FT_CLIEFOR "
	cQuery += "			AND CDG.CDG_LOJA   = SFT.FT_LOJA "
	cQuery += "			AND CDG.D_E_L_E_T_ = ' ') "
	cQuery += "	 QTDPROC, "
Return
/*----------------------------------------------------------------------
{Protheus.doc} ObservacoesC915C197()
OBSERVAÇÕES DO LANÇAMENTO FISCAL (CÓDIGO 01, 1B, 04, 55 E 65) (C195)
OUTRAS OBRIGAÇÕES TRIBUTÁRIAS, AJUSTES E INFORMAÇÕES DE
VALORES PROVENIENTES DE DOCUMENTO FISCAL (C197)
Informações complementares registro 0460
(T013AL/T013AM)
@author adilson.roberto
@since 15/08/2023
//----------------------------------------------------------------------*/
Static Function ObservacoesC195C197(cAlias as character, oDadosComplemento as json, oAgrpCDA as json,cChavCda as character, oDadosIntegra AS json)
Local nContCda         as numeric
Local nCont            as numeric	
local nPass            as numeric
Local nCont197         as numeric	
Local nAgrC197  :=  0  as numeric	
Local cItemCda  := ""  as character
Local aRecCda   := oAgrpCDA[(cAlias)->FT_ITEM] as array
Local nC197Ant  :=  0  as numeric
Local lPassC197 := .F. as logical   
Local lItDif    := .F. as logical 
Local nCodC197  :=  0  as numeric
Local cChavCod	:= ""  as character
Local nContCod	:= 0   as numeric
Local lAgrLan	:= .F. as logical

Default cChavCda := ""

If oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos] == Nil
	oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos] 	 := {}
	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos], JsonObject():New() )
Endif

If oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ] == Nil 
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ] := {}
	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyObservacoes], JsonObject():New() )	
Endif
For nPass := 1 to Len(aRecCda)
	nContCda  := 1
	nAgrC197  += 1
	lPassC197 := .F.
	lItDif    := .F.
	lAgrLan	  := .F.
	If !cChavCda == (cAlias)->(FT_FILIAL+FT_TIPOMOV+FT_ESPECIE+FT_NFISCAL+FT_SERIE+FT_CLIEFOR+FT_LOJA)
		cChavCda := (cAlias)->(FT_FILIAL+FT_TIPOMOV+FT_ESPECIE+FT_NFISCAL+FT_SERIE+FT_CLIEFOR+FT_LOJA)	
		cItemCda := (cAlias)->FT_ITEM
		cChavCod := aRecCda[nPass][3]		
	Else
		If (Alltrim(cItemCda) != Alltrim((cAlias)->FT_ITEM))
			For nCont := 1 to len(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyLancamentos])
				If oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyLancamentos][nCont]['C2C_CODOBS'] == aRecCda[nPass][1]
					nContCda := nCont
					Exit
				Else
					nContCda += 1
				Endif	
			Next
			cChavCod := aRecCda[nPass][3]
			cItemCda := (cAlias)->FT_ITEM
			lItDif   := .T.		 
		Else
			If cChavCod == aRecCda[nPass][3]  // Se for o mesmo item e se for o mesmo código de lançamento verifico se ja tem o mesmo código de observação lançado
				For nContCod := 1 To Len(oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ])
					If oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nContCod]['C2D_CODOBS'] ==  aRecCda[nPass][1]
						nAgrC197 := nContCod
						lAgrLan  := .T.
						Exit
					Endif
				Next	
			Else
				cChavCod := aRecCda[nPass][3]
			Endif
			For nCont := 1 to len(oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos])
				If oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos][nCont]['C2C_CODOBS'] == aRecCda[nPass][1]
					nContCda := nCont
					Exit
				Else
					nContCda += 1
				Endif	
			Next
		Endif
		If !nContCda = nCont	
			aadd( oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos], JsonObject():New() )						
		Endif	

		If !Empty(aRecCda[nPass][14]) .And. !AllTrim(aRecCda[nPass][14]) == "3"
			If nC197Ant < nAgrC197 
				nC197Ant := nAgrC197
			Endif	
			If lItDif
				For nCont197 := 1 to Len(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyObservacoes]) 
					If oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyObservacoes][nCont197]['C2D_CODAJ' ] == aRecCda[nPass][3]
						nAgrC197 := nCont197
						lPassC197 := .T.
						Exit 
					Endif
				Next
				cItemCda := (cAlias)->FT_ITEM
			Else
				For nCont197 := 1 to  Len(oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ])
					If oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nCont197]['C2D_CODAJ' ] ==  aRecCda[nPass][3]
						nAgrC197 := nCont197
						lPassC197 := .T.
						Exit 
					Endif
				Next	
			Endif	
			If !lPassC197
				nAgrC197 := IIf(nC197Ant > nAgrC197, nC197Ant + 1, nAgrC197) 
				If nAgrC197 == nC197Ant + 1; aadd( oDadosComplemento[cKeyInfEspecificas][cKeyObservacoes], JsonObject():New() ); EndIf
			Endif
		Else
			If lItDif
				For nCodC197 := 1 to Len(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyObservacoes])
					If nCodC197 > 1
						aadd( oDadosComplemento[cKeyInfEspecificas][cKeyObservacoes], JsonObject():New() )	
					Endif
					oDadosComplemento[cKeyInfEspecificas][cKeyObservacoes][nCodC197] := oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyInfEspecificas][cKeyObservacoes][nCodC197]
				Next
				nAgrC197 := nCodC197 
			Endif
			If !lAgrLan
				aadd( oDadosComplemento[cKeyInfEspecificas][cKeyObservacoes], JsonObject():New() )	
			Endif	
		Endif
	Endif
	//
	oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos][nContCda]['C2C_CODOBS'] := aRecCda[nPass][1] //C195
	oDadosComplemento[cKeyInfEspecificas][cKeyLancamentos][nContCda]['C2C_DESCRI'] := aRecCda[nPass][2]	
		
	//Grava C197
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_CODOBS'] :=  aRecCda[nPass][1]
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_CODAJ' ] :=  aRecCda[nPass][3]
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_DESCRI'] :=  aRecCda[nPass][4]
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_CODITE'] :=  (cAlias)->COD_ITEM
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_DCODIT'] :=  aRecCda[nPass][5]
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_OPALIQ'] :=  aRecCda[nPass][6]	// 1 Valor, 02 Nulo, 03 Zero
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_OPBASE'] :=  aRecCda[nPass][7]	// 1 Valor, 02 Nulo, 03 Zero
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_OPVAL' ] :=  aRecCda[nPass][8] //1 Leva Valor, 2 preencher campo valor com ""
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_OPVLOU'] :=  aRecCda[nPass][9] //1 Leva Valor, 2 preencher campo valor outros com ""
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_ALQICM'] :=  aRecCda[nPass][10]
	oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_ITEMNF'] :=  (cAlias)->FT_ITEM
	If !Empty(aRecCda[nPass][14]) .And.  !AllTrim(aRecCda[nPass][14]) == "3"
		oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_BSICM' ] :=  aRecCda[nPass][11]
		oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_VLICM' ] :=  aRecCda[nPass][12]
		oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_VLOUTR'] :=  aRecCda[nPass][13]
	Else
		If !lAgrLan
			oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_BSICM' ] :=  aRecCda[nPass][11]
			oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_VLICM' ] :=  aRecCda[nPass][12]
			oDadosComplemento[cKeyInfEspecificas][ cKeyObservacoes ][nAgrC197]['C2D_VLOUTR'] :=  aRecCda[nPass][13]
		//Else
			TAFConOut("VARIFICAR DEPOIS")
			TAFConOut("VARIFICAR DEPOIS")
			TAFConOut("VARIFICAR DEPOIS")
		Endif
	Endif
Next
Return 
/*----------------------------------------------------------------------
{Protheus.doc} AgrupCDA()
Função responsável por coletar informações do código de lançamento na tabela CDA e associadas

@author adilson.roberto
@since 28/11/2023
//----------------------------------------------------------------------*/
Static Function AgrupCDA(cAlias as character, oStObserv as object, oAgrpCDA as json, aAgrupCda as array)
Local lRegValido := .t. as logical
Local cQuery as character															   
Local cAliasCDA  := GetNextAlias() as character
Local cCodObs as character
Local cDesObs as character
Local cDescAj as character
Local cDescObs as character
Local lVL197 as logical
Local nCdaValor as numeric
Local nCdaVlOtr as numeric
Local cChvCda := "" as character
Local aE531 := {} as array
					   
if valtype(oStObserv) != 'O'

	cQuery += " SELECT "
	cQuery += " 	CDA.CDA_TPMOVI, CDA.CDA_ESPECI, CDA.CDA_NUMERO, CDA.CDA_SERIE , CDA.CDA_NUMITE, "
	cQuery += " 	CDA.CDA_CALPRO, CDA.CDA_CODLAN, CDA.CDA_BASE  , CDA.CDA_ALIQ  , CDA.CDA_VALOR , "
	cQuery += " 	CDA.CDA_IFCOMP, CDA.CDA_CLIFOR, CDA.CDA_LOJA  , CCE.CCE_COD   , CCE.CCE_DESCR , "
	cQuery += " 	CDA.CDA_TPLANC, CDA.CDA_ORIGEM, CDA_FORMUL	  , CDA.CDA_VL197 , CDA.CDA_CLANC , "
	cQuery += "     CC6.CC6_REFLEX, CC6.CC6_TPAPUR, CJA.CJA_VLOUTR, CDA.CDA_FILIAL, CC6.CC6_RESPON, "
	cQuery += " 	CC6.CC6_INFLUE, CC6.CC6_ORIGEM, CC6.CC6_CODAJU, CC6.CC6_STUF  , CC6.CC6_CODLAN, "
	cQuery += " 	CC6.CC6_DESCR2, CC6.CC6_DESCR , CC6.CC6_DECLAR, CDA.CDA_VLOUTR, CDA.CDA_REGCAL, " 
	cQuery += " 	CDA.CDA_AGRLAN, CDA.CDA_IDMSG , CDA.CDA_SEQ   , CDA.CDA_CODCPL, CDA.CDA_CODMSG, " 
	cQuery += " 	CDA.CDA_TXTDSC, CDA.CDA_OPBASE, CDA.CDA_OPALIQ, CJA.CJA_VALOR , CDT.CDT_IFCOMP, "
	cQuery += " 	CDT.CDT_DCCOMP, C0J.C0J_ID    , "
	cQuery += "     (ISNULL(CAST(CAST(CJL1.CJL_MENSG AS VARBINARY(8000)) AS VARCHAR(8000)),'')) MSGC195, "
    cQuery += "     (ISNULL(CAST(CAST(CJL2.CJL_MENSG AS VARBINARY(8000)) AS VARCHAR(8000)),'')) MSGC197, "
    cQuery += "     (ISNULL(CAST(CAST(CJL3.CJL_MENSG AS VARBINARY(8000)) AS VARCHAR(8000)),'')) MSG0460, "
	cQuery += " 	C1B.C1B_ID  "

	cQuery += " FROM " + RetSqlName('CDA') + " CDA "
	
	cQuery += " LEFT JOIN " + RetSqlName("CC6") + " CC6 ON CC6.CC6_FILIAL = '" + xFilial( "CC6" ) + "' AND CC6.CC6_CODLAN=CDA.CDA_CODLAN AND CC6.D_E_L_E_T_=' ' "
	cQuery += " LEFT JOIN " + RetSqlName("CCE") + " CCE ON CCE.CCE_FILIAL = '" + xFilial( "CCE" ) + "' AND CCE.CCE_COD = CDA.CDA_IFCOMP AND CCE.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName("CDT") + " CDT ON CDT.CDT_FILIAL = '" + xFilial( "CDT" ) + "' AND CDT.CDT_TPMOV = CDA.CDA_TPMOVI AND CDT.CDT_DOC = CDA.CDA_NUMERO "
	cQuery += " 	AND CDT.CDT_SERIE = CDA.CDA_SERIE AND CDT.CDT_CLIFOR = CDA.CDA_CLIFOR AND CDT.CDT_LOJA = CDA.CDA_LOJA AND CDT.CDT_IFCOMP = CDA.CDA_IFCOMP AND CDT.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName("CJA") + " CJA ON CJA.CJA_FILIAL = '" + xFilial( "CJA" ) + "' AND CJA.CJA_CODLAN = CDA.CDA_CODLAN AND CJA.CJA_REGCAL = CDA.CDA_REGCAL  AND CJA.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName('C0J') + " C0J ON C0J.C0J_FILIAL = '" + xFilial( 'C0J' ) + "' AND C0J.C0J_CODIGO = CDA.CDA_CODLAN AND C0J.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName('CJL') + " CJL1 ON CJL1.CJL_FILIAL = '" + xFilial( 'CJL' ) + "' AND CJL1.CJL_ID = CDA.CDA_IDMSG AND CJL1.CJL_INDICE = '01' AND CJL1.D_E_L_E_T_ = ' ' "
    cQuery += " LEFT JOIN " + RetSqlName('CJL') + " CJL2 ON CJL2.CJL_FILIAL = '" + xFilial( 'CJL' ) + "' AND CJL2.CJL_ID = CDA.CDA_IDMSG AND CJL2.CJL_INDICE = '02' AND CJL2.D_E_L_E_T_ = ' ' "
    cQuery += " LEFT JOIN " + RetSqlName('CJL') + " CJL3 ON CJL3.CJL_FILIAL = '" + xFilial( 'CJL' ) + "' AND CJL3.CJL_ID = CDA.CDA_IDMSG AND CJL3.CJL_INDICE = '03' AND CJL3.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName('CCK') + " CCK ON CCK.CCK_FILIAL = '" + xFilial( 'CCK' ) + "' AND CCK.CCK_CODAJU = CDA.CDA_CODLAN AND CCK.D_E_L_E_T_ = ' ' "
	cQuery += " LEFT JOIN " + RetSqlName('C1B') + " C1B ON C1B.C1B_FILIAL = '" + xFilial( 'C1B' ) + "' AND C1B.C1B_CODIGO = CCK.CCK_CODAJU 	AND C1B.D_E_L_E_T_ = ' ' "

	cQuery += " WHERE CDA.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND CDA.CDA_FILIAL = ? "
	cQuery += " 	AND CDA_TPMOVI     = ? "
	cQuery += " 	AND CDA.CDA_ESPECI = ? "
	cQuery += " 	AND CDA.CDA_SERIE  = ? "
	cQuery += " 	AND CDA.CDA_NUMERO = ? "
	cQuery += " 	AND CDA.CDA_CLIFOR = ? "
	cQuery += " 	AND CDA.CDA_LOJA   = ? "
	cQuery += " ORDER BY CDA.CDA_FILIAL, CDA.CDA_TPMOVI, CDA.CDA_ESPECI, CDA.CDA_FORMUL, CDA.CDA_NUMERO, CDA.CDA_SERIE, CDA.CDA_CLIFOR, CDA.CDA_LOJA, CDA.CDA_NUMITE, C0J.C0J_ID "																									
	oStObserv := FWPreparedStatement():New()
	oStObserv:SetQuery( cQuery )
endif

oStObserv:SetString(1, xFilial('CDA'))
oStObserv:SetString(2, (cAlias)->FT_TIPOMOV)
oStObserv:SetString(3, (cAlias)->FT_ESPECIE)
oStObserv:SetString(4, (cAlias)->FT_SERIE  )
oStObserv:SetString(5, (cAlias)->FT_NFISCAL) 
oStObserv:SetString(6, (cAlias)->FT_CLIEFOR) 
oStObserv:SetString(7, (cAlias)->FT_LOJA   )

dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStObserv:GetFixQuery(),{}), cAliasCDA, .f., .t.)

while (cAliasCDA)->(!eof()) .and. lRegValido

	if (cAliasCDA)->CDA_ORIGEM != '3'
		
		//Configurador
		If !Empty((cAliasCDA)->CDA_REGCAL)
		
		 	If !Empty((cAliasCDA)->CDA_CODCPL) .And. !Empty((cAliasCDA)->MSGC195) // c195
		 		cCodObs := Alltrim((cAliasCDA)->CDA_CODCPL)
		 		cDesObs := (cAliasCDA)->MSGC195
		 	Endif	
		 	If !Empty((cAliasCDA)->CDA_CODMSG) .And. !Empty((cAliasCDA)->MSGC197) //c197
		 		cDescAj := (cAliasCDA)->MSGC197   //Indice 02 é referente ao campo CDA_CODMSG 
		 		cCodObs := Iif(Empty(cCodObs),(cAliasCDA)->CDA_CODMSG,cCodObs)
		 		cDesObs := Iif (Empty(cDesObs),cDescAj,cDesObs)
		 	Endif	
		 	If !Empty((cAliasCDA)->CDA_TXTDSC) .And. !Empty((cAliasCDA)->MSG0460) //0460
		 		cDescObs := (cAliasCDA)->MSG0460
		 		cCodObs  := Iif(Empty(cCodObs),(cAliasCDA)->CDA_TXTDSC,cCodObs)
		 		cDesObs  := Iif(Empty(cDesObs),cDescObs,cDesObs )
		 	Endif

		 	If Empty(cDescAj)
		 		If  AllTrim((cAliasCDA)->CDA_ORIGEM) == '5' .And. !Empty((cAliasCDA)->CDA_CLANC)								
		 			cDescAj:= (cAliasCDA)->CDA_CLANC
		 			//Tratamento para pegar a descrição da tabela 5.2 caso esteja preenchido na F3K a 5.3 x 5.2
		 		ElseIf !Empty((cAliasCDA)->CC6_DECLAR)
		 			cDescAj  := (cAliasCDA)->CC6_DECLAR
		 		Else
		 			cDescAj  := (cAliasCDA)->CC6_DESCR
		 		Endif
		 	Endif		
		 	nCdaValor := (cAliasCDA)->CDA_VALOR
		 	nCdaVlOtr := (cAliasCDA)->CDA_VLOUTR
		Else
			//Legado
			If !Empty((cAliasCDA)->CDA_IFCOMP)	
				cCodObs := (cAliasCDA)->CCE_COD
				cDesObs := (cAliasCDA)->CCE_DESCR
			Else
				cCodObs := (cAliasCDA)->CDT_IFCOMP
				cDesObs := (cAliasCDA)->CDT_DCCOMP
				If Empty(cDesObs)
					cDesObs := (cAliasCDA)->CCE_DESCR
				Endif
			Endif
			If !Empty((cAlias)->F4_CODOBSE) .And. cMvSPDTC95 $ "3" .And. !EMPTY((cAlias)->FT_FORMULA)	
				cDesObs:= (cAlias)->(Formula((cAlias)->FT_FORMULA)+FT_OBSERV)
			Endif
			If AllTrim((cAliasCDA)->CDA_ORIGEM) == '5' .And. !Empty((cAliasCDA)->CDA_CLANC)								
				cDescAj:= (cAliasCDA)->CDA_CLANC
			Else
				cDescAj  := MSMM((cAliasCDA)->CC6_DESCR2)
				cDescAj  := IIF(Empty(cDescAj),(cAliasCDA)->CC6_DESCR,cDescAj)
			Endif
			If Empty(cCodObs) 
				cCodObs := (cAliasCDA)->CCE_COD
				cDesObs := (cAliasCDA)->CCE_DESCR
			Endif
			If AllTrim((cAliasCDA)->CDA_ORIGEM) == '5'
				cDescObs:= cDesObs
			ElseIf !Empty((cAliasCDA)->CDA_CLANC)
				cDescObs:= (cAliasCDA)->CDA_CLANC
			ElseIf Empty((cAliasCDA)->CDA_IFCOMP)
				cDescObs:= cDesObs
			Else	
				cDescObs := cDescAj
			EndIf
			lVL197 := .T.
			If !(cAliasCDA)->CDA_VL197=="1"
				lVL197 := .F.
			Endif
			nCdaValor := Iif(SubStr((cAliasCDA)->CDA_CODLAN,3,1) == "9" .And. !lVL197,0,(cAliasCDA)->CDA_VALOR)
			nCdaVlOtr := Iif(SubStr((cAliasCDA)->CDA_CODLAN,3,1) == "9" .And. !lVL197,(cAliasCDA)->CDA_VALOR,0)
		Endif
		
		lRegValido := RetC3R(cCodObs, cDescObs) //0460
		
		If (lRegValido .And. !empty((cAliasCDA)->CDA_CODLAN) .and. !empty((cAliasCDA)->C0J_ID))
			If !cChvCda == (cAliasCDA)->(CDA_FILIAL+CDA_TPMOVI+CDA_ESPECI+CDA_FORMUL+CDA_NUMERO+CDA_SERIE+CDA_CLIFOR+CDA_LOJA+CDA_NUMITE)
				aAgrupCda := {}
				cChvCda := (cAliasCDA)->(CDA_FILIAL+CDA_TPMOVI+CDA_ESPECI+CDA_FORMUL+CDA_NUMERO+CDA_SERIE+CDA_CLIFOR+CDA_LOJA+CDA_NUMITE)
			Endif	
			Aadd(aAgrupCda,{cCodObs,;
							cDesObs,;
							(cAliasCDA)->C0J_ID,;
							cDescAj,;
							(cAlias)->DESCR_COMPL,;
							(cAliasCDA)->CDA_OPALIQ,;
							(cAliasCDA)->CDA_OPBASE,;
							Iif(!Empty((cAliasCDA)->CJA_VALOR), "1","2"),;
							Iif(!Empty((cAliasCDA)->CJA_VLOUTR), "1","2"),;
							(cAliasCDA)->CDA_ALIQ,;
							(cAliasCDA)->CDA_BASE,;
							nCdaValor,;
							nCdaVlOtr,;
							(cAliasCDA)->CDA_AGRLAN})
			oAgrpCDA[(cAliasCDA)->CDA_NUMITE] := aAgrupCda 	
		Endif

	else //Ajuste de IPI

		aadd(aE531, { strzero(val((cAliasCDA)->CDA_NUMITE),4), (cAliasCDA)->CDA_VALOR, (cAliasCDA)->C1B_ID})

	endif

	(cAliasCDA)->(DbSkip())

enddo

//Grava no Json os ajustes de IPI
if len(aE531) > 0
	oAgrpCDA['IPI'] := aE531
endif	

(cAliasCDA)->(DBCloseArea())

Return

/*-----------------------------------------------------------------------
{Protheus.doc}  CompEnergiaEletricaAguaGas
Consulta e carrega o Json com informações complementares dos documentos 
fiscais de energia eletrica agua e gas (T013AN - SPED C500)
@author Carlos Eduardo Nonato
@since 24/07/2023
//----------------------------------------------------------------------*/
Static Function CompEnergiaEletricaAguaGas(cAlias as character, oDadosComplemento as json, oStEnergiaAguaGas as object, cCodEspecie as character, lTotaliza as logical, aTotalizaSFT as array) as logical

Local lGravaReg                                                                                            as logical
Local cQuery                                                                                               as character
Local cAliasC500       := GetNextAlias()                                                                   as character
Local cTipoJoin        := iif( valtype(cAlias) != 'U' .and. (cAlias)->FT_TIPOMOV == 'E', 'INNER', 'LEFT' ) as character
Local cTipoLigacao                                                                                         as character
Local cIdClasseConsumo                                                                                     as character
Local cIdGrupoTensao                                                                                       as character
Local nVlrTerceiros                                                                                        as numeric
Local nConsumoTotalKwh                                                                                     as numeric
Local nValNaoTrib                                                                                          as numeric
Local nVlrFornCons                                                                                         as numeric
Local lDocumentoAtivo  := !cCodSit $ '|02|04|05|'                                                          as logical
Local cTabQry          := iif(cCodEspecie == '28', ' CD3', ' CD4')                                         as character
Local i                                                                                                    as numeric
Local nPosJson                                                                                             as numeric

Default lTotaliza := .f.
Default aTotalizaSFT := {}

if !lTotaliza
	if valtype(oStEnergiaAguaGas) != 'O'
		if cCodEspecie $ '|06|66|'
			cQuery += " SELECT "
			cQuery += " 	SFU.FU_FILIAL, "
			cQuery += " 	SFU.FU_CLASCON, "
			cQuery += " 	SFU.FU_TIPLIGA, "
			cQuery += " 	SFU.FU_GRUPT, "
			cQuery += " 	SFU.FU_RECEP, "
			cQuery += " 	SFU.FU_LOJAREC, "
			cQuery += "		C0L.C0L_ID, "
			cQuery += "		C0N.C0N_ID, "
			cQuery += " 	SUM(SFU.FU_CONSTOT) FU_CONSTOT, "
			cQuery += " 	SUM(SFU.FU_VALTERC) VLRTERC, "
			cQuery += " 	SUM(SFU.FU_VLFORN) FU_VLFORN "
			cQuery += " FROM " + RetSqlName('SFU') + " SFU "
			cQuery += cTipoJoin + " JOIN " + RetSqlName('C0L') + " C0L ON C0L.C0L_FILIAL = '" + xFilial('C0L') + "' AND C0L.C0L_CODIGO = SFU.FU_CLASCON AND C0L.D_E_L_E_T_ = ' ' "
			cQuery += cTipoJoin + " JOIN " + RetSqlName('C0N') + " C0N ON C0N.C0N_FILIAL = '" + xFilial('C0N') + "' AND C0N.C0N_CODIGO = SFU.FU_GRUPT   AND C0N.D_E_L_E_T_ = ' ' "
			cQuery += " WHERE SFU.D_E_L_E_T_ = ' ' " 
			cQuery += "		AND SFU.FU_FILIAL  = ? "
			cQuery += " 	AND SFU.FU_TIPOMOV = ? "
			cQuery += " 	AND SFU.FU_DOC 	   = ? "
			cQuery += " 	AND SFU.FU_SERIE   = ? "
			cQuery += " 	AND SFU.FU_CLIFOR  = ? "
			cQuery += " 	AND SFU.FU_LOJA    = ? "
			if (cAlias)->FT_TIPOMOV == 'E' //Documento de entrada
				cQuery += " 	AND SFU.FU_CLASCON <> ' ' "
			endif	
			cQuery += "	GROUP BY SFU.FU_FILIAL, SFU.FU_CLASCON, SFU.FU_TIPLIGA, SFU.FU_GRUPT, SFU.FU_RECEP, SFU.FU_LOJAREC, C0L.C0L_ID, C0N.C0N_ID "
		elseif cCodEspecie $ '|28|29|'
			cQuery += " SELECT "
			cQuery += " 	C0L.C0L_ID, "
			cQuery += " 	SUM(" + cTabQry + "." + cTabQry + "_VLTERC) VLRTERC "
			cQuery += " FROM " + RetSqlName(ltrim(cTabQry)) + cTabQry
			cQuery += " 	LEFT JOIN " + RetSqlname('C0L')+ " C0L ON C0L.C0L_FILIAL = '" + xFilial('C0L') + "' AND C0L.C0L_CODIGO = " + cTabQry + "." + cTabQry + "_CLASCO AND C0L.D_E_L_E_T_ = ' ' "
			cQuery += " WHERE " + cTabQry + ".D_E_L_E_T_ = ' ' "
			cQuery += "		AND " + cTabQry + "." + cTabQry + "_FILIAL  = ? "
			cQuery += " 	AND " + cTabQry + "." + cTabQry + "_TPMOV   = ? "
			cQuery += " 	AND " + cTabQry + "." + cTabQry + "_DOC 	= ? "
			cQuery += " 	AND " + cTabQry + "." + cTabQry + "_SERIE   = ? "
			cQuery += " 	AND " + cTabQry + "." + cTabQry + "_CLIFOR  = ? "
			cQuery += " 	AND " + cTabQry + "." + cTabQry + "_LOJA    = ? "
			cQuery += " GROUP BY C0L.C0L_ID "
		endif

		oStEnergiaAguaGas := FWPreparedStatement():New()
		oStEnergiaAguaGas:SetQuery( cQuery )

	endif

	oStEnergiaAguaGas:SetString(1, xFilial('SFU') )
	oStEnergiaAguaGas:SetString(2, (cAlias)->FT_TIPOMOV)
	oStEnergiaAguaGas:SetString(3, (cAlias)->FT_NFISCAL)
	oStEnergiaAguaGas:SetString(4, (cAlias)->FT_SERIE  )
	oStEnergiaAguaGas:SetString(5, (cAlias)->FT_CLIEFOR)
	oStEnergiaAguaGas:SetString(6, (cAlias)->FT_LOJA   )
	dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStEnergiaAguaGas:GetFixQuery(),{}), cAliasC500, .f., .t.)

	while (cAliasC500)->(!eof())

		if (cAliasC500)->(RecNo()) == 1
			oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas] := {}
			lGravaReg := .t.
		endif
		aadd( oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas], JsonObject():New() )
		nPosJson++

		cIdClasseConsumo := (cAliasC500)->C0L_ID //Id da classe de consumo
		nVlrTerceiros 	 := (cAliasC500)->VLRTERC
		if cCodEspecie $ '|06|66|'
			cTipoLigacao 	 := (cAliasC500)->FU_TIPLIGA
			cIdGrupoTensao 	 := (cAliasC500)->C0N_ID //Id do grupo de tensao
			nConsumoTotalKwh := (cAliasC500)->FU_CONSTOT //Vai levar para integração Aparentemente são usados no C600 ???
			nVlrFornCons 	 := (cAliasC500)->FU_VLFORN
		endif	

		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_TPLIG' ] := cTipoLigacao
		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_CODCON'] := cIdClasseConsumo
		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_GRPTEN'] := cIdGrupoTensao
		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_VLTERC'] := nVlrTerceiros
		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_CONS'  ] := nConsumoTotalKwh
		oDadosComplemento[cKeyInfEspecificas][cKeyCompEnergiaAguaGas][nPosJson]['C2E_VLFORN'] := nVlrFornCons	

		(cAliasC500)->(DbSkip())

	enddo
else
	if len(aTotalizaSFT) >= 4 .and. lDocumentoAtivo
		nValNaoTrib := aTotalizaSFT[4]
	endif	
	for i := 1 to len(oDadosComplemento[cKeyCompEnergiaAguaGas])
		oDadosComplemento[cKeyCompEnergiaAguaGas][i]['C2E_VSERNT'] := nValNaoTrib
	next	
endif	

//Fecho a tabela caso exista
if Select(cAliasC500) > 0; (cAliasC500)->(DbCloseArea()); endif

return lGravaReg

//---------------------------------------------------------------------
/*/{Protheus.doc} TAFGetIDCodSit
Retorna o ID do Código de Situação
@type function
@param cCodSituac, character, Código de situação do documento fiscal
@author Melkz Siqueira
@since 19/07/2023
@version 1.0
@return character, ID do Código de Situação
/*/
//---------------------------------------------------------------------
Function TAFGetIDCodSit(cCodSituac as character) as character

	Local cIDCodSit		:= "" as character

	Default cCodSituac 	:= ""

	If !Empty(cCodSituac)
		C02->(DBSetOrder(1))

		If C02->(MsSeek(xFilial("C02") + cCodSituac))
			cIDCodSit := C02->C02_ID
		EndIf
	EndIf

Return cIDCodSit

/*----------------------------------------------------------------------
{Protheus.doc} GetindTipoFrete
Retorna o indicador do tipo de frete conforme regras do TAF.
@author Carlos Eduardo Nonato
@since 09/06/2023
//----------------------------------------------------------------------*/
static function GetindTipoFrete(cAlias as character, cCodSit as character, cEspecie as character) as character
Local cFrete 	:= alltrim((cAlias)->CDT_INDFRT) as character
Local cIndFrete := '9' as character
Local cCpoFrete := iif( (cAlias)->FT_TIPOMOV == 'E', 'F1_FRETE','F2_FRETE') as character
Local cIDFrete	:= "" as character

if lIntTMS .and. (cAlias)->RECDT6 != 0 .and. (cAlias)->FT_TIPOMOV == 'S' .and. cEspecie $ "|07|08|09|10|11|26|27|57"
	if !empty(cOperSemFrete) .And. alltrim((cAlias)->FT_CFOP) $ cOperSemFrete
		cFrete := '9'
	elseif (cAlias)->DT6_DEVFRE == '1'
		cFrete := '0'	// Por conta do emitente = Cif = 1
	elseif (cAlias)->DT6_DEVFRE == '2'
		cFrete := '1' 	// Por conta do destinatario = FOB = 2
	else
		cFrete := '2'	// Apesar do sistema gravar 2=FOB, o devedor do frete pode ser o consignatario, espachante ou outros.
	endif		
else
	if cFrete == 'X' .or. empty(cFrete) //[X]Se não encontrou CDT ou [ ]se encontrou e o campo esta vazio
		if !empty(cOperSemFrete) .And. alltrim((cAlias)->FT_CFOP) $ cOperSemFrete
			cFrete := "9"
		else
			// Utilizo a informacao configurada nos pedidos de venda/campra
			cFrete := SPEDSitFrt(cAlias, IIf((cAlias)->FT_TIPOMOV == 'S', 'SD2', 'SD1'), .T., IIf((cAlias)->FT_TIPOMOV == 'S', 'SF2', 'SF1'), cCpoFrete,, .F.)		
		endif
	endif
	
	if alltrim((cAlias)->CDT_INDFRT) != 'X' //[X]Se encontrou CDT
		if Alltrim(cFrete) == '1'	// 0 - Por conta do emitente
			cFrete := '0'
		elseif Alltrim(cFrete) == '2'	// 1 - Por conta do destinatário/remetente
			cFrete := "1" 
		elseif Alltrim(cFrete) == '0'	// 2 - Por conta de terceiros
			cFrete := '2'
		endif
	endif
endif	
	
// Adaptando o código de frete de acordo com as resgras do TAF
if cCodSit $ '02#03'
	cIndFrete := ''
elseif AllTrim(cFrete) == '0'
	cIndFrete := "1"
elseif AllTrim(cFrete) == '1'
	cIndFrete := "2"
elseif AllTrim(cFrete) == '2'
	cIndFrete := '0'
else
	cIndFrete := cFrete
endif	

C0X->(DBSetOrder(1))

If C0X->(MsSeek(xFilial("C0X") + cIndFrete))
	cIDFrete := C0X->C0X_ID
EndIf

return cIDFrete

/*-----------------------------------------------------------------------
{Protheus.doc} GetIdMunServ
Retorna o Id da tabela autocontida de codigos de municipio do TAF
@author Carlos Eduardo Nonato
@since 12/06/2023
//----------------------------------------------------------------------*/
Static Function GetIdMunServ(cCodMunServ as character, cIdUf as character, lIdTaf as logical) as character
Local cRetId as character
Local cCodMunicipio as character
Local aRetC07 as array
Default cIdUf := ''
Default lIdTaf := .t.

//Se deve retornar oo id da tabela do TAF
if lIdTaf
	if Len(Alltrim(cCodMunServ)) == 7 //Codigo do municipio preenchido com uf 
		cIdUf := xFunGetUf ( left( cCodMunServ, 2 ) ) // Retorna o Id da tabela autocontida de uf's do TAF
		cCodMunicipio := right(cCodMunServ, 5)
		cRetId := GetIdCached('C07', cIdUf + cCodMunicipio, 1 )
	endif	
else
	aRetC07 := GetAdvFval( 'C07', {'C07_UF','C07_CODIGO'}, xFilial( 'C07' ) + cCodMunServ  , 3 ) //Array com [1] Id da UF e [2] código municipio
	if Len(aRetC07) > 0
		//Retorno a UF do estado baseado no id encontrado acima.
		aRetC07[1] := GetAdvFval('C09','C09_UF',xFilial('C09') + aRetC07[1], 3)
		cIdUf  := aRetC07[1]
		cRetId := aRetC07[2]
	endif	
endif	

if cRetId == "NOTFOUND"
	cRetId := ""
EndIf 

return cRetId

/*-----------------------------------------------------------------------
{Protheus.doc} ConsultaJsonInfoComplememtar
Consulta se no Json ja existe o codigo da informacao complementar, se nao
existe inclui no jSon. 
Se faz necessário porque o codigo de informacao complementar não é 
obrigatorio no ERP, porém no TAF é obrigatório e deve ser cadastrado e 
vinculado aos complementos.
@author Carlos Eduardo Nonato
@since 20/06/2023
//----------------------------------------------------------------------*/
static function GetPosJsonInfoComplementar(cAlias as character, oDadosComplemento as json, cIdC3Q as character) as numeric
Local i as numeric
Local nPosInfoComp as numeric
Local cValueCodInf as character

if oDadosComplemento[cKeyInfEspecificas]:HasProperty(cKeyInfoComp)
	//Veririco se tem Cadastro de Informações complementares (CDT)
	for i := 1 to len(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp])
		oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][i]:GetJsonValue('C21_CODINF',@cValueCodInf)
		if cValueCodInf == cIdC3Q
			nPosInfoComp := i
			exit
		endif
	next
else
	//Crio o objeto de complemento como um array vazio												   
	oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp] := {}	
endif	

if nPosInfoComp == 0
	aadd(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp], JsonObject():New())
	nPosInfoComp := len(oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp])	

	oDadosComplemento[cKeyInfEspecificas][cKeyInfoComp][nPosInfoComp]['C21_CODINF'] := cIdC3Q
endif		

return nPosInfoComp

//---------------------------------------------------------------------
/*/{Protheus.doc} GetSelectItens
@type			static function
@description	Retorna os campos dos itens do documento fiscal que compõem o SELECT da query
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna por referência o trecho do SELECT a ser concatenado na query 
@param			cMVTPAPSB1 - Campo da tabela SB1 do parâmetro MV_TPAPSB1
/*/
//---------------------------------------------------------------------
Static Function GetSelectItens(cQuery as character, cMVTPAPSB1 as character) as character

	Default cQuery		:= ""
	Default cMVTPAPSB1 	:= ""

	cQuery += "	ROW_NUMBER() OVER( " // C30_NUMITE
	cQuery += " PARTITION BY SFT.FT_FILIAL, SFT.FT_TIPOMOV, SFT.FT_SERIE, SFT.FT_NFISCAL, SFT.FT_CLIEFOR, SFT.FT_LOJA "
	cQuery += " ORDER BY SFT.FT_FILIAL, SFT.FT_TIPOMOV, SFT.FT_SERIE, SFT.FT_NFISCAL, SFT.FT_CLIEFOR, SFT.FT_LOJA, SFT.FT_ITEM ASC "
	cQuery += " ) NUM_ITEM, "

	cQuery += " C1L.C1L_ID COD_ITEM, " // C30_CODITE
	cQuery += " SB5.B5_CEME DESCR_COMPL, " // C30_DESCRI
	cQuery += " SFT.FT_TOTAL VL_TOT_ITEM, " // C30_TOTAL
	cQuery += " SFT.FT_DESCONT VL_DESC, " // C30_VLDESC

	cQuery += " CASE " // C30_ORICRE
	cQuery += " 	WHEN SUBSTRING(SFT.FT_CFOP, 1, 1) = '3' THEN '1' "
	cQuery += " 	ELSE '0' "
	cQuery += " END IND_ORIG_CRED, "

	cQuery += " C1O.C1O_ID COD_CTA, " // C30_CODCTA
	cQuery += "	C1P.C1P_ID COD_CCUS, " // C30_CCUSTO
	cQuery += " SFT.FT_QUANT QTD, " // C30_QUANT
	cQuery += " C1J.C1J_ID UNID, " // C30_UM
	cQuery += " C0Y.C0Y_ID CFOP, " // C30_CFOP
	cQuery += "	C1N.C1N_ID NAT_OPE, " // C30_NATOPE

	cQuery += " CASE " // C30_CLENQ
	cQuery += " 	WHEN SB1.B1_TAB_IPI = 'M' AND SB1.B1_POSIPI = '24022000' THEN 'III-M' "
	cQuery += " 	WHEN SB1.B1_TAB_IPI = 'R' AND SB1.B1_POSIPI = '24022000' THEN 'III-R' "
	cQuery += " 	ELSE SB1.B1_TAB_IPI "
	cQuery += " END CL_ENQ, "

	cQuery += " CASE " // C30_VLUNID
	cQuery += " 	WHEN SFT.FT_PAUTIPI > 0 THEN SFT.FT_PAUTIPI "
	cQuery += " 	WHEN SB1.B1_TAB_IPI <> ' ' AND SB1.B1_VLR_IPI > 0 THEN SB1.B1_VLR_IPI "
	cQuery += " 	ELSE 0 "
	cQuery += " END VL_UNID, "
	cQuery += " SB1.B1_VLR_COF, "
	cQuery += " SB1.B1_VLR_PIS, "
	cQuery += " SB1.B1_ORIGEM, "
	cQuery += " SB1.B1_IMPORT, "
	
	If !Empty(cMVTPAPSB1) .And. SB1->(ColumnPos(cMVTPAPSB1) > 0)
		cQuery += " SB1." + cMVTPAPSB1 + ", "
	Endif

	cQuery += " SFT.FT_QUANT QUANT_PAD, " // C30_QTDPAD
	cQuery += " SFT.FT_DESPESA VL_OUT, " // C30_VLOUTR
	cQuery += " ' ' IND_NAT_REC, " // C30_NATREC
	cQuery += " 0 ICMS_ST_COMPL, " // C30_VSTCOM
	cQuery += " 0 BC_RET, " // C30_BCRETQ
	cQuery += " 0 ICMS_RET, " // C30_VLRET
	cQuery += " C03.C03_ID ORIGEM, " // C30_ORIGEM

	cQuery += " CASE " // C30_INDMOV
	cQuery += " 	WHEN SF4.F4_MOVFIS = 'S' THEN '0' "
	cQuery += " 	WHEN SF4.F4_MOVFIS = 'N' THEN '1' "
	cQuery += " 	ELSE ' ' "
	cQuery += " END IND_MOV, "

	cQuery += " ' ' ESTOQUE, " // C30_ESTOQ

	cQuery += " CASE " // C30_VLRITE
	cQuery += "		WHEN SFT.FT_TIPO = 'I' OR SFT.FT_TIPO = 'P' OR SFT.FT_TIPO = 'C' OR SFT.FT_PRCUNIT = 0 THEN SFT.FT_TOTAL "
	cQuery += " 	ELSE SFT.FT_PRCUNIT "
	cQuery += " END VL_ITEM, "

	cQuery += "	C0B.C0B_ID COD_LST, " // C30_CODSER
	cQuery += " ' ' CSOSN, " // C30_CSOSN
	cQuery += " ' ' INF_ADIC, " // C30_DESNFE
	cQuery += " SFT.FT_VALCONT VLR_CONTABIL, " // C30_VLOPER
	cQuery += " ' ' CST_COMP, " // C30_CSTCOM
	cQuery += " SFT.FT_CODISS COD_SERV_MUN, " // C30_SRVMUN
	cQuery += " ' ' LICIMP, " // C30_LICIMP

	cQuery += "	CASE " // C30_TPREPA
	cQuery += "		WHEN SFT.FT_TIPOMOV = 'E' THEN TRIM(SD1.D1_TPREPAS) "
	cQuery += " 	WHEN SFT.FT_TIPOMOV = 'S' THEN TRIM(SD2.D2_TPREPAS) "
	cQuery += " 	ELSE ' ' "
	cQuery += " END TPREPASSE, "

	cQuery += "	C8C.C8C_ID TIP_SERV, " // C30_IDTSER
	cQuery += " LF0.LF0_ID COD_DIPAM, " // C30_CODIPA
	cQuery += " SFT.FT_INDISEN IND_ISENCAO_PREVID, " // C30_INDISE
	cQuery += " V3O.V3O_ID NATUREZA_RENDIMENTO, " // C30_NATREN
	cQuery += " FKX.FKX_DECSAL IND_DEC_TERC, " // C30_DECTER
	cQuery += " 0 ICMS_ST_FRETE, " // C30_ICMSFT
	cQuery += " ' ' NR_INSC_FCI_SCP, " // C30_NFCISC
	cQuery += " ' ' COMPFP, " // C30_COMPFP
	cQuery += " SFT.FT_DESPESA VL_DA_ITEM, " // C30_VLRDA
	cQuery += " SFT.FT_SEGURO + SFT.FT_DESPESA + SFT.FT_FRETE VL_ACRE " // C30_VLACRE

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC1L
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C1L - Produtos
@author			Melkz Siqueira
@since			19/09/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC1L(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C1L") + " C1L "
	cQuery += "		ON C1L.C1L_FILIAL = '" + xFilial("C1L") + "' "
	cQuery += " 		AND C1L.C1L_CODIGO = SFT.FT_PRODUTO "
	cQuery += " 		AND C1L.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------


/*/{Protheus.doc} GetLeftJoinCD9
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF CD9
@author			adilson.roberto
@since			28/11/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinCD9(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("CD9") + " CD9 "
	cQuery += "		ON CD9.CD9_FILIAL     = '" + xFilial("CD9") + "' "
	cQuery += " 	   AND CD9.CD9_TPMOV  = SFT.FT_TIPOMOV" 
	cQuery += " 	   AND CD9.CD9_SERIE  = SFT.FT_SERIE" 
	cQuery += " 	   AND CD9.CD9_DOC    = SFT.FT_NFISCAL" 
	cQuery += " 	   AND CD9.CD9_CLIFOR = SFT.FT_CLIEFOR" 
	cQuery += " 	   AND CD9.CD9_LOJA   = SFT.FT_LOJA" 
	cQuery += " 	   AND CD9.CD9_ITEM   = SFT.FT_ITEM" 
	cQuery += " 	   AND CD9.CD9_COD    = SFT.FT_PRODUTO"  
	cQuery += " 	   AND CD9.D_E_L_E_T_=' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinDHR
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal DHR - NF x Natureza de Rendimento
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinDHR(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("DHR") + " DHR "
	cQuery += "		ON DHR.DHR_FILIAL = '" + xFilial("DHR") + "' "
	cQuery += " 		AND DHR.DHR_DOC = SFT.FT_NFISCAL "
	cQuery += " 		AND DHR.DHR_SERIE = SFT.FT_SERIE "
	cQuery += " 		AND DHR.DHR_FORNEC = SFT.FT_CLIEFOR "
	cQuery += " 		AND DHR.DHR_LOJA = SFT.FT_LOJA "
	cQuery += " 		AND DHR.DHR_ITEM = SFT.FT_ITEM "
	cQuery += " 		AND DHR.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinFKX
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal FKX - Naturezas de Rendimento
				Deve ser concatenado depois do LEFT JOIN da tabela DHR
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinFKX(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("FKX") + " FKX "
	cQuery += "		ON FKX.FKX_FILIAL = '" + xFilial("FKX") + "' "
	cQuery += " 		AND FKX.FKX_CODIGO = DHR.DHR_NATREN "
	cQuery += " 		AND FKX.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinF09
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal F09 - Relacionamento TES x IPM
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinF09(cQuery as character)
	
	Local cUF := Space(1) as character
	Local cMVEstado := SuperGetMV("MV_ESTADO", .F., "") as character
	Local cMVUFIPM := SuperGetMV("MV_UFIPM", .F., "") as character

	Default cQuery := ""

	If !Empty(cMVEstado) .And. cMVEstado $ cMVUFIPM
		cUF := cMVEstado
	EndIf

	cQuery += " LEFT JOIN " + RetSQLName("F09") + " F09 "
	cQuery += "		ON F09.F09_FILIAL = '" + xFilial("F09") + "' "
	cQuery += " 		AND F09.F09_TES = SFT.FT_TES "
	cQuery += "			AND F09.F09_UF = '" + cUF + "' "
	cQuery += " 		AND F09.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinF2Q
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal F2Q - Complemento Fiscal
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinF2Q(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("F2Q") + " F2Q "
	cQuery += "		ON F2Q.F2Q_FILIAL = '" + xFilial("F2Q") + "' "
	cQuery += " 		AND F2Q.F2Q_PRODUT = SFT.FT_PRODUTO "
	cQuery += " 		AND F2Q.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinCDNA
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal CDN - Cod. ISS
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinCDNA(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("CDN") + " CDNA "
	cQuery += "		ON CDNA.CDN_FILIAL = '" + xFilial("CDN") + "' "
	cQuery += "			AND CDNA.CDN_CODISS = SFT.FT_CODISS "
	cQuery += "			AND CDNA.CDN_PROD = SFT.FT_PRODUTO "
	cQuery += "			AND CDNA.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinCDNB
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal CDN - Cod. ISS
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinCDNB(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("CDN") + " CDNB "
	cQuery += "		ON CDNB.CDN_FILIAL = '" + xFilial("CDN") + "' "
	cQuery += "			AND CDNB.CDN_CODISS = SFT.FT_CODISS "
	cQuery += "			AND CDNB.CDN_PROD = ' ' "
	cQuery += "			AND CDNB.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinSD1
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal SD1 - Itens das NF de Entrada
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinSD1(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("SD1") + " SD1 "
	cQuery += " 	ON SD1.D1_FILIAL = '" + xFilial("SD1") + "' "
	cQuery += " 		AND SD1.D1_DOC = SFT.FT_NFISCAL "
	cQuery += " 		AND SD1.D1_SERIE = SFT.FT_SERIE "
	cQuery += " 		AND SD1.D1_FORNECE = SFT.FT_CLIEFOR "
	cQuery += " 		AND SD1.D1_LOJA = SFT.FT_LOJA "
	cQuery += " 		AND SD1.D1_COD = SFT.FT_PRODUTO "
	cQuery += " 		AND SD1.D1_ITEM = SFT.FT_ITEM "
	cQuery += " 		AND SD1.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinSD2
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal SD2 - Itens de Venda da NF
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinSD2(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("SD2") + " SD2 "
	cQuery += " 	ON SD2.D2_FILIAL = '" + xFilial("SD2") + "' "
	cQuery += " 		AND SD2.D2_DOC = SFT.FT_NFISCAL "
	cQuery += " 		AND SD2.D2_SERIE = SFT.FT_SERIE "
	cQuery += " 		AND SD2.D2_CLIENTE = SFT.FT_CLIEFOR "
	cQuery += " 		AND SD2.D2_LOJA = SFT.FT_LOJA "
	cQuery += " 		AND SD2.D2_COD = SFT.FT_PRODUTO "
	cQuery += " 		AND SD2.D2_ITEM = SFT.FT_ITEM "
	cQuery += " 		AND SD2.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinSF4
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal SF4 - TES - Tipos de Entrada e Saída
				Deve ser concatenado depois do LEFT JOIN da tabela SB1 pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinSF4(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("SF4") + " SF4 "
	cQuery += " 	ON SF4.F4_FILIAL = '" + xFilial("SF4") + "' "
	cQuery += " 		AND SF4.F4_CODIGO = TRIM( "
	cQuery += " 			CASE "
	cQuery += "					WHEN SFT.FT_TIPOMOV = 'E' THEN SB1.B1_TE "
	cQuery += "					WHEN SFT.FT_TIPOMOV = 'S' THEN SB1.B1_TS "
	cQuery += "					ELSE ' ' "
	cQuery += "				END "
	cQuery += "			) "
	cQuery += " 		AND SF4.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinSB5
@type			static function
@description	Retorna o LEFT JOIN da tabela do Fiscal SB5 - Dados Adicionais do Produto
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinSB5(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("SB5") + " SB5 "
	cQuery += " 	ON SB5.B5_FILIAL = '" + xFilial("SB5") + "' "
	cQuery += " 		AND SB5.B5_COD = SFT.FT_PRODUTO "
	cQuery += " 		AND SB5.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC0Y
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C0Y - Cadastro de CFOP
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC0Y(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C0Y") + " C0Y "
	cQuery += " 	ON C0Y.C0Y_FILIAL = '" + xFilial("C0Y") + "' "
	cQuery += " 		AND C0Y.C0Y_CODIGO = TRIM( "
	cQuery += " 			CASE "

	If TAFGetDB() == "MSSQL"
		cQuery += " 				WHEN LEN(SFT.FT_CFOP) <= 3 AND SFT.FT_TIPOMOV <> ' ' AND SFT.FT_TIPOMOV = 'S' THEN '5949' "
		cQuery += " 				WHEN LEN(SFT.FT_CFOP) <= 3 AND SFT.FT_TIPOMOV <> ' ' AND SFT.FT_TIPOMOV <> 'S' THEN '1949' "
	Else
		cQuery += " 				WHEN LENGTH(SFT.FT_CFOP) <= 3 AND SFT.FT_TIPOMOV <> ' ' AND SFT.FT_TIPOMOV = 'S' THEN '5949' "
		cQuery += " 				WHEN LENGTH(SFT.FT_CFOP) <= 3 AND SFT.FT_TIPOMOV <> ' ' AND SFT.FT_TIPOMOV <> 'S' THEN '1949' "
	EndIf

	cQuery += " 				ELSE SFT.FT_CFOP "
	cQuery += " 			END "
	cQuery += " 		) "
	cQuery += " 		AND C0Y.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC0B
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C0B - Codigo Serviço (LCF 116/2003)
				Deve ser concatenado depois do LEFT JOIN da tabela CDN pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC0B(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C0B") + " C0B "
	cQuery += " 	ON C0B.C0B_FILIAL = '" + xFilial("C0B") + "' "
	cQuery += " 		AND C0B.C0B_CODIGO = TRIM( "
	cQuery += "				REPLACE( "
	cQuery += "					CASE "
	cQuery += "						WHEN CDNA.CDN_CODLST <> ' ' THEN CDNA.CDN_CODLST "
	cQuery += " 					WHEN CDNB.CDN_CODLST <> ' ' THEN CDNB.CDN_CODLST "
	cQuery += " 					ELSE SFT.FT_CODISS "
	cQuery += " 				END, '.', ''"
	cQuery += " 			) "
	cQuery += " 		) "
	cQuery += " 		AND C0B.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC1N
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C1N - Natureza de Operação
				Deve ser concatenado depois do LEFT JOIN da tabela SF4 pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC1N(cQuery as character)

	Local cMVSpedNat := cValToChar(SuperGetMV("MV_SPEDNAT", .F., .F.)) as character
	
	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C1N") + " C1N "
	cQuery += " 	ON C1N.C1N_FILIAL = '" + xFilial("C1N") + "' "
	cQuery += " 		AND C1N.C1N_CODNAT = TRIM( "
	cQuery += "				CASE "
	cQuery += "					WHEN SFT.FT_NATOPER <> ' ' THEN SFT.FT_NATOPER "
	cQuery += " 				WHEN '" + cMVSpedNat + "' = '.T.' THEN SFT.FT_CFOP "
	cQuery += " 				ELSE SF4.F4_CODIGO "
	cQuery += " 			END "
	cQuery += " 		) "
	cQuery += " 		AND C1N.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC1O
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C1O - Plano de Contas Contábeis
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC1O(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C1O") + " C1O "
	cQuery += " 	ON C1O.C1O_FILIAL = '" + xFilial("C1O") + "' "
	cQuery += " 		AND C1O.C1O_CODIGO = SFT.FT_CONTA "
	cQuery += " 		AND C1O.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC1P
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C1P - Centro de Custo
				Deve ser concatenado depois do LEFT JOIN da tabela SD1 pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC1P(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C1P") + " C1P "
	cQuery += " 	ON C1P.C1P_FILIAL = '" + xFilial("C1P") + "' "
	cQuery += " 		AND C1P.C1P_CODCUS = TRIM( "
	cQuery += "				CASE "
	cQuery += "					WHEN SFT.FT_TIPOMOV = 'E' THEN SD1.D1_CC "
	cQuery += " 				WHEN SFT.FT_TIPOMOV = 'S' THEN SD2.D2_CCUSTO "
	cQuery += " 				ELSE ' ' "
	cQuery += " 			END "
	cQuery += " 		) "
	cQuery += " 		AND C1P.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinC8C
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF C8C - Tipo do Serviço
				Deve ser concatenado depois do LEFT JOIN da tabela CDN e F2Q pois depende delas
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinC8C(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C8C") + " C8C "
	cQuery += " 	ON C8C.C8C_FILIAL = '" + xFilial("C8C") + "' "
	cQuery += " 		AND C8C.C8C_CODIGO = TRIM( "
	cQuery += "				CASE "
	cQuery += "					WHEN (CDNA.CDN_CODLST <> ' ') AND (F2Q.F2Q_TPSERV IS NULL OR F2Q.F2Q_TPSERV = '') THEN CDNA.CDN_TPSERV "
	cQuery += " 				WHEN (CDNB.CDN_CODLST <> ' ') AND (F2Q.F2Q_TPSERV IS NULL OR F2Q.F2Q_TPSERV = '') THEN CDNB.CDN_TPSERV "
	cQuery += " 				ELSE F2Q.F2Q_TPSERV "
	cQuery += " 			END "
	cQuery += " 		) "
	cQuery += " 		AND C8C.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinLF0
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF LF0 - Código referente ao DIPAM
				Deve ser concatenado depois do LEFT JOIN da tabela F09 pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinLF0(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("LF0") + " LF0 "
	cQuery += " 	ON LF0.LF0_FILIAL = '" + xFilial("LF0") + "' "
	cQuery += " 		AND LF0.LF0_CODIGO = F09.F09_CODIPM "
	cQuery += " 		AND LF0.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinV3O
@type			static function
@description	Retorna o LEFT JOIN da tabela do TAF V3O - Natureza de Rendimento
				Deve ser concatenado depois do LEFT JOIN da tabela DHR pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetLeftJoinV3O(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("V3O") + " V3O "
	cQuery += " 	ON V3O.V3O_FILIAL = '" + xFilial("V3O") + "' "
	cQuery += " 		AND V3O.V3O_CODIGO = DHR.DHR_NATREN "
	cQuery += " 		AND V3O.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetLeftJoinDT6
@type			static function
@description	Retorna o LEFT JOIN da tabela DT6
@author			Gabriela Luche
@since			21/08/2025
@version		1.0
@param			cQuery - Retorna o trecho do LEFT JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------	
Static Function GetLeftJoinDT6(cQuery as character)
Default cQuery := ""

cQuery += " 	LEFT JOIN " + RetSqlName("DT6") + " DT6 ON "
cQuery += " 				( SFT.FT_TIPOMOV = 'S' AND SFT.FT_ESPECIE IN ('NFST','CTR','CTA','CA','CTF','CTM','CTE') ) AND " //|07|08|09|10|11|26|57
cQuery += " 				DT6.DT6_FILIAL = '" + xFilial('DT6') + "' AND " 
cQuery += " 				DT6.DT6_FILDOC = SFT.FT_FILIAL AND " 
cQuery += " 				DT6.DT6_DOC    = SFT.FT_NFISCAL AND "
cQuery += " 				DT6.DT6_SERIE  = SFT.FT_SERIE AND "
cQuery += " 				DT6.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetInnerJoinC1J
@type			static function
@description	Retorna o INNER JOIN da tabela do TAF C1J - Unidade de Medida
				Deve ser concatenado depois do LEFT JOIN da tabela SB1 pois depende dela
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do INNER JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetInnerJoinC1J(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C1J") + " C1J "
	cQuery += " 	ON C1J.C1J_FILIAL = '" + xFilial("C1J") + "' "
	cQuery += " 		AND C1J.C1J_CODIGO = SB1.B1_UM "
	cQuery += " 		AND C1J.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetInnerJoinC03
@type			static function
@description	Retorna o INNER JOIN da tabela do TAF C03 - Origens das Mercadorias
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param			cQuery - Retorna o trecho do INNER JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetInnerJoinC03(cQuery as character)

	Default cQuery := ""

	cQuery += " LEFT JOIN " + RetSQLName("C03") + " C03 "
	cQuery += " 	ON C03.C03_FILIAL = '" + xFilial("C03") + "' "
	cQuery += " 		AND C03.C03_CODIGO = SFT.FT_CLASFIS "
	cQuery += " 		AND C03.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} SetItens
@type			static function
@description	Inclui o JSON contendo os itens do documento fiscal no JSON do documento fiscal
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param          cAlias - Alias da query que contém os dados dos itens do documento fiscal
@param          oDadosIntegra - JSON do documento fiscal contendo os itens retornado por referência
@return			Nil
/*/
//---------------------------------------------------------------------
Static Function SetItens(cAlias as character, oDadosIntegra as json)

	Local jItens := JsonObject():New() as json
 	
	jItens["C30_NUMITE"] := GetNumeroItem((cAlias)->NUM_ITEM)
	jItens["C30_CODITE"] := (cAlias)->COD_ITEM
	jItens["C30_DESCRI"] := (cAlias)->DESCR_COMPL
	jItens["C30_TOTAL"] := (cAlias)->VL_TOT_ITEM
	jItens["C30_VLDESC"] := (cAlias)->VL_DESC
	jItens["C30_ORICRE"] := (cAlias)->IND_ORIG_CRED
	jItens["C30_CTACTB"] := (cAlias)->COD_CTA
	jItens["C30_CCUSTO"] := (cAlias)->COD_CCUS
	jItens["C30_QUANT"] := (cAlias)->QTD
	jItens["C30_UM"] := (cAlias)->UNID
	jItens["C30_CFOP"] := (cAlias)->CFOP
	jItens["C30_NATOPE"] := (cAlias)->NAT_OPE
	jItens["C30_CLENQ"] := GetCodigoIPI((cAlias)->CL_ENQ)
	jItens["C30_VLUNID"] := (cAlias)->VL_UNID
	jItens["C30_QTDPAD"] := (cAlias)->QUANT_PAD
	jItens["C30_VLOUTR"] := (cAlias)->VL_OUT
	jItens["C30_NATREC"] := (cAlias)->IND_NAT_REC
	jItens["C30_VSTCOM"] := (cAlias)->ICMS_ST_COMPL
	jItens["C30_BCRETQ"] := (cAlias)->BC_RET
	jItens["C30_VLRET"] := (cAlias)->ICMS_RET
	jItens["C30_ORIGEM"] := (cAlias)->ORIGEM
	jItens["C30_INDMOV"] := (cAlias)->IND_MOV
	jItens["C30_ESTOQ"] := (cAlias)->ESTOQUE
	jItens["C30_VLRITE"] := (cAlias)->VL_ITEM
	jItens["C30_CODSER"] := (cAlias)->COD_LST
	jItens["C30_CSOSN"] := (cAlias)->CSOSN
	jItens["C30_DESNFE"] := (cAlias)->INF_ADIC
	jItens["C30_VLOPER"] := (cAlias)->VLR_CONTABIL
	jItens["C30_CSTCOM"] := (cAlias)->CST_COMP
	jItens["C30_SRVMUN"] := (cAlias)->COD_SERV_MUN
	jItens["C30_LICIMP"] := (cAlias)->LICIMP
	jItens["C30_TPREPA"] := (cAlias)->TPREPASSE
	jItens["C30_IDTSER"] := (cAlias)->TIP_SERV
	jItens["C30_CODIPA"] := (cAlias)->COD_DIPAM
	jItens["C30_INDISE"] := (cAlias)->IND_ISENCAO_PREVID
	jItens["C30_CNATRE"] := (cAlias)->NATUREZA_RENDIMENTO
	jItens["C30_DECTER"] := (cAlias)->IND_DEC_TERC
	jItens["C30_ICMSFT"] := (cAlias)->ICMS_ST_FRETE
	jItens["C30_IFCISC"] := (cAlias)->NR_INSC_FCI_SCP
	jItens["C30_COMPFP"] := (cAlias)->COMPFP
	jItens["C30_VLRDA"] := (cAlias)->VL_DA_ITEM
	jItens["C30_VLACRE"] := (cAlias)->VL_ACRE

	If !oDadosIntegra[cKeyJson][nPosDocFiscal]:HasProperty(cKeyItens)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens] := {}
	EndIf

	AAdd(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens], jItens)
	
	nItem++

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} GetNumeroItem
@type			static function
@description	Retorna o número sequencial do item do documento fiscal formatado
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param          iNumeroItem - Número sequencial do item sem formatação
@return			cNumeroItem - Número sequencial do item formatado para o campo C30_NUMITE
/*/
//---------------------------------------------------------------------
Static Function GetNumeroItem(iNumeroItem as integer) as character

	Local cNumeroItem := "" as character
	Local iNumIte := GetSX3Cache("C30_NUMITE", "X3_TAMANHO") as integer

	Default iNumeroItem := 0

	cNumeroItem := StrZero(iNumeroItem, iNumIte)

Return cNumeroItem

//---------------------------------------------------------------------
/*/{Protheus.doc} GetCodigoIPI
@type			static function
@description	Retorna o ID do código IPI cadastrado na tabela C3T do TAF
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param          cCodigo - Código de IPI que será usado para retornar o ID
@return			cID - ID do código IPI
/*/
//---------------------------------------------------------------------
Static Function GetCodigoIPI(cCodigo  as character) as character

	Local cID := "" as character

	Default cCodigo := ""

	If !Empty(cCodigo) 
		If IsDigit(cCodigo)
			cCodigo := "99999"
		EndIf

		C3T->(DBSetOrder(1))

		If C3T->(MsSeek(xFilial("C3T") + cCodigo))
			cID := C3T->C3T_ID
		EndIf
	EndIf

Return cID

/*----------------------------------------------------------------------
{Protheus.doc} SomaItensDocumento
Função para totalizar os campos da SFT por doppcumento fiscal quando 
necessário o uso em algum registro do SPED
@Posição do array em relação aos campos da SFT
aTotalizaSFT[01] - FT_TOTAL - Totaliza o valor total do item
aTotalizaSFT[02] - FT_BASEICM - Totaliza o valor da base de icms
aTotalizaSFT[03] - FT_VALICM - Totaliza o valor do ICMS
aTotalizaSFT[04] - FT_OUTRICM + FT_ISENICM - Totaliza a parte do valor da mercadoria que nao é tributada pelo ICMS

@author Carlos Eduardo Nonato
@since 28/07/2023
//----------------------------------------------------------------------*/
Static Function SomaItensDocumento(cAlias as character, aTotalizaSFT as array)

aTotalizaSFT[01] += (cAlias)->FT_TOTAL 
aTotalizaSFT[02] += (cAlias)->FT_BASEICM
aTotalizaSFT[03] += (cAlias)->FT_VALICM
aTotalizaSFT[04] += (cAlias)->(FT_OUTRICM + FT_ISENICM)

Return

/*-----------------------------------------------------------------------
{Protheus.doc} TribItem
Inclui no json os tributos por item.
@author adilson.roberto
@since 20/06/2023
//----------------------------------------------------------------------*/
Static Function TribItem(cAlias as character, oDadosIntegra as json, oCodTrib as object, cCampSb1 as character, cMVEstado as character, aTotaisTrib as array)
Local jTrib := JsonObject():New() as json
Local cCfop := "1922|2922|5922|6922" as character  //CFOs especificos de lançamentos futuros que possuem ICMS zerados. 
Local cCalcIr := (cAlias)->A2_CALCIRF as character
Local cKeyTrib as character
Local a_ClasFis as array
Local nIcmSenC as numeric
Local nVlPrev as numeric
Local aTPisCof as array
Local nBasGILRAT as numeric
Local nAlqGILRAT as numeric
Local nValGILRAT as numeric
Local a_DIfalFcp as array
Local cUfEst := (cAlias)->FT_ESTADO as character
Local nBaseTributo as numeric
Local nValorTributo as numeric
Local nAliqTributo as numeric
Local nValorTributavel as numeric
Local nVlrNaoTribut as numeric
Local nValorIsento as numeric
Local bAscan as codeblock
Local nPosTrib as numeric
Local nTotVlCp as numeric
Local cIdCST as character 
Local cCodigoServico := strtran( left((cAlias)->CDN_CODLST,4 ), '.', '') as character //Pega os 4 primeiros caracteres do campo e retira o "." do código caso exista
Local cIdCodServ := GetTafId('C0B',cCodigoServico,1) as character
Local cIdCfop := GetTafId('C0Y',(cAlias)->FT_CFOP,1) as character

DEFAULT cCampSb1 := ""

a_ClasFis := SPDRetCCST( cAlias, .T., (cAlias)->FT_ESPECIE, cAlias, cAlias, (cAlias)->UF_PART)

If lIntTMS .And. (cAlias)->FT_TIPOMOV == "S"
	cUfEst := LocEst(cAlias, cMVEstado)
Endif	

// 01=ISSQN - Imposto Sobre Servico
If !Empty((cAlias)->FT_CODISS) .and. (cAlias)->FT_RECISS == '1' 
	
	cKeyTrib := BuscCodTrib(@oCodTrib, '01')
	cIdCST   := ValCodCst( (cAlias)->FT_CSTISS, cKeyTrib ) 

	jTrib := JsonObject():New()	
	jTrib["C35_CODTRI"] := cKeyTrib 
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEICM
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQICM
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALICM
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENICM
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRICM
	jTrib["C35_CST"]	:= cIdCST
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop .and. x[TTCODSER] == cIdCodServ .and. x[TTALIQ] = (cAlias)->FT_ALIQICM }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)		

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQICM
	endif
	aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEICM
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALICM
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENICM
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRICM

Endif

// 02=ICMS - Imposto sobre circulacao de Servicos e Mercadorias
If (Empty((cAlias)->FT_CODISS) .And. ( (cAlias)->FT_BASEICM > 0 .Or. (cAlias)->FT_VALICM > 0 .Or. (cAlias)->FT_OUTRICM > 0;
		 .Or. (cAlias)->FT_ISENICM > 0 .Or. Alltrim((cAlias)->FT_CFOP) $ cCfop ))
	nIcmSenC := FIcmSCred(cAlias,a_ClasFis[1])	 
	cKeyTrib := BuscCodTrib(@oCodTrib, '02')
	cIdCST   := ValCodCst( a_ClasFis[1], cKeyTrib ) 

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib 
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEICM
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQICM
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALICM
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENICM
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRICM
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_VLSCRE"]	:= nIcmSenC
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .And. x[TTCFOP] == cIdCfop .And. x[TTALIQ] = (cAlias)->FT_ALIQICM }
	nPosTrib := aScan(aTotaisTrib, bAscan)
	
	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib 
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQICM
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ 
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEICM
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALICM
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENICM
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRICM
	aTotaisTrib[nPosTrib][TTVLSCRE] += nIcmSenC	

Endif

// 03=ICMS Complementar
If((cAlias)->FT_ICMSCOM > 0 .And. (cAlias)->FT_DIFAL <= 0)

	cKeyTrib 	 := BuscCodTrib(@oCodTrib, '03')
	nAliqTributo := IIf((cAlias)->FT_TIPOMOV == "E",(cAlias)->D1_ALIQCMP,(cAlias)->D2_ALIQCMP)

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_ALIQ"]	:= nAliqTributo
	jTrib["C35_VALOR"]	:= (cAlias)->FT_ICMSCOM	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)	

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := nAliqTributo
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL	
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_ICMSCOM	

Endif
	
// 04=ICMS/ST
If((cAlias)->FT_ICMSRET > 0 .And. (cAlias)->FT_CREDST <> "4")

	cKeyTrib := BuscCodTrib(@oCodTrib, '04')
	cIdCST   := ValCodCst( a_ClasFis[1], cKeyTrib ) 
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASERET
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQSOL
	jTrib["C35_VALOR"]	:= (cAlias)->FT_ICMSRET
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENRET
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRRET
	jTrib["C35_MVA"]	:= (cAlias)->FT_MARGEM	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)	

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQSOL
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASERET
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_ICMSRET
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENRET
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRRET	
	aTotaisTrib[nPosTrib][TTICMNDE] += iif( (cAlias)->FT_CLASFIS == '60', (cAlias)->FT_ICMNDES, 0)  

Endif
	
// 05=IPI - Imposto Sobre Produto Industrializado
If (cAlias)->FT_VALIPI > 0 .Or. (cAlias)->FT_ALIQIPI > 0 .Or. (cAlias)->FT_BASEIPI > 0 .Or. (cAlias)->FT_ISENIPI > 0 .Or. (cAlias)->FT_OUTRIPI > 0

	cKeyTrib := BuscCodTrib(@oCodTrib, '05')
	cIdCST   := ValCodCst( a_ClasFis[2], cKeyTrib )

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib 
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEIPI
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQIPI
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALIPI
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENIPI
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRIPI
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .And. x[TTCFOP] == cIdCfop .And. x[TTALIQ] = (cAlias)->FT_ALIQIPI }
	nPosTrib := AScan(aTotaisTrib, bAscan)
	
	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)	

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib 
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQIPI	
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEIPI
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALIPI
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENIPI
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRIPI	

Endif
	
// 06=PIS
If ((cAlias)->FT_TIPOMOV = 'E' .And. (cAlias)->FT_CSTPIS $ "70|71|72|74|98|99|") .Or. ((cAlias)->FT_TIPOMOV = 'S' .And. (cAlias)->FT_CSTPIS $ "01|07|08|09|49|") .Or.;
	 (cAlias)->FT_BASEPIS > 0  .Or.  (cAlias)->FT_BASEPS3 > 0

	aTPisCof 	  := VerPPisCof("06",cAlias)  //01 PIS , 02 COFINFS
	cKeyTrib 	  := BuscCodTrib(@oCodTrib, '06')
	nAliqTributo  := iif(!empty(aTPisCof[1,1]), aTPisCof[1,1], (cAlias)->FT_ALIQPIS)
	nValorTributo := IIf((cAlias)->FT_CSTPIS $ "07|08|71|74" .And. Empty(aTPisCof[1,1]),0,(cAlias)->FT_VALPIS)
	nValorIsento  := IIf((cAlias)->FT_CSTPIS $ "07|71",(cAlias)->FT_VALPIS,0)
	nVlrNaoTribut := IIf((cAlias)->FT_CSTPIS $ "08|74",(cAlias)->FT_VRETPIS,0)
	cIdCST		  := ValCodCst( a_ClasFis[3], cKeyTrib )
	//Consulta valor tributavel
	if (cAlias)->FT_VALPIS > 0 .or. (cAlias)->FT_BASEPIS > 0 .or. (cAlias)->FT_ALIQPIS > 0
		if (cAlias)->FT_PAUTPIS > 0 .or. (cAlias)->B1_VLR_PIS > 0
			nValorTributavel := (cAlias)->FT_VALPIS
		endif
	endIf	

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"] 	:= (cAlias)->FT_BASEPIS
	jTrib["C35_ALIQ"]	:= nAliqTributo
	jTrib["C35_BASEQT"]	:= aTPisCof[1,2]
	jTrib["C35_ALIQQT"] := aTPisCof[1,1]
	jTrib["C35_VALOR"]	:= nValorTributo
	jTrib["C35_VLISEN"]	:= nValorIsento
	jTrib["C35_VLNT"]	:= nVlrNaoTribut
	jTrib["C35_VLRPAU"] := nValorTributavel
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .and. x[TTCFOP] == cIdCfop .and. x[TTALIQ] = nAliqTributo }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	//Se não encontrou a chave no array
	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := nAliqTributo
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEPIS
	aTotaisTrib[nPosTrib][TTBASEQT] += aTPisCof[1,2]
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo
	aTotaisTrib[nPosTrib][TTVLISEN] += nValorIsento
	aTotaisTrib[nPosTrib][TTVLNT  ] += nVlrNaoTribut
	aTotaisTrib[nPosTrib][TTVLRPAU] += nValorTributavel

EndIf
	
// 07=Cofins
If ((cAlias)->FT_TIPOMOV = 'E' .And. (cAlias)->FT_CSTCOF $ "70|71|72|74|98|99|") .Or. ((cAlias)->FT_TIPOMOV = 'S' .And. (cAlias)->FT_CSTCOF $ "01|07|08|09|49|") .Or. ;
	(cAlias)->FT_BASECOF > 0  .Or.  (cAlias)->FT_BASECF3 > 0

	cKeyTrib 	  := BuscCodTrib(@oCodTrib, '07')
	aTPisCof 	  := VerPPisCof("07",cAlias)
	nAliqTributo  := iif(!Empty(aTPisCof[2,1]),aTPisCof[2,1],(cAlias)->FT_ALIQCOF)
	nValorTributo := IIf((cAlias)->FT_CSTCOF $ "07|08|71|74" .And. Empty(aTPisCof[2,1]),0,(cAlias)->FT_VALCOF)
	nValorIsento  := IIf((cAlias)->FT_CSTCOF $ "07|71",(cAlias)->FT_VALCOF,0)
	nVlrNaoTribut := IIf((cAlias)->FT_CSTCOF $ "08|74",(cAlias)->FT_VRETCOF,0)
	cIdCST   	  := ValCodCst( a_ClasFis[4], cKeyTrib ) 
	//Consulta valor tributavel
	if (cAlias)->FT_VALCOF > 0 .or. (cAlias)->FT_BASECOF > 0 .or. (cAlias)->FT_ALIQCOF > 0
		if (cAlias)->FT_PAUTCOF > 0 .or. (cAlias)->B1_VLR_COF > 0
			nValorTributavel := (cAlias)->FT_VALCOF
		endIf
	endIf	

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"] 	:= (cAlias)->FT_BASECOF
	jTrib["C35_ALIQQT"] := aTPisCof[2,1]
	jTrib["C35_BASEQT"]	:= aTPisCof[2,2]
	jTrib["C35_ALIQ"]	:= nAliqTributo
	jTrib["C35_VALOR"]	:= nValorTributo
	jTrib["C35_VLISEN"]	:= nValorIsento
	jTrib["C35_VLNT"]	:= nVlrNaoTribut
	jTrib["C35_VLRPAU"] := nValorTributavel
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .and. x[TTCFOP] == cIdCfop .and. x[TTALIQ] = nAliqTributo }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib,array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)	

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib		
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := nAliqTributo
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASECOF
	aTotaisTrib[nPosTrib][TTBASEQT] += aTPisCof[2,2]
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo
	aTotaisTrib[nPosTrib][TTVLISEN] += nValorIsento
	aTotaisTrib[nPosTrib][TTVLNT  ] += nVlrNaoTribut
	aTotaisTrib[nPosTrib][TTVLRPAU] += nValorTributavel
	
EndIf
	
// 08=PIS/ST
If (cAlias)->FT_ALIQPS3 > 0
	cKeyTrib := BuscCodTrib(@oCodTrib, '08')
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib	
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEPS3
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQPS3
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALPS3
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .And. x[TTCFOP] == cIdCfop .And. x[TTALIQ] = SFT->FT_ALIQPS3 }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)		

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib		
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQPS3
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEPS3
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALPS3

Endif

// 09=Cofins/ST
If (cAlias)->FT_ALIQCF3 > 0
	cKeyTrib := BuscCodTrib(@oCodTrib, '09')
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASECF3
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQCF3	
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALCF3
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .And. x[TTCFOP] == cIdCfop .And. x[TTALIQ] = SFT->FT_ALIQCF3 }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)		

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQCF3
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASECF3
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALCF3
Endif	

// 10=Pis Retencao
If (cAlias)->FT_VRETPIS > 0 .Or. (cAlias)->DHR_VLRPIS > 0
	cKeyTrib 	  := BuscCodTrib(@oCodTrib, '10')
	nBaseTributo  := Iif((cAlias)->DHR_BASPIS > 0, (cAlias)->DHR_BASPIS , (cAlias)->FT_BRETPIS)
	nValorTributo := Iif((cAlias)->DHR_VLRPIS > 0, (cAlias)->DHR_VLRPIS , (cAlias)->FT_VRETPIS)
	nValorIsento  := IIf((cAlias)->FT_CSTPIS $ "07|71",(cAlias)->FT_VRETPIS,0)
	nVlrNaoTribut := IIf((cAlias)->FT_CSTPIS $ "08|74",(cAlias)->FT_VRETPIS,0)
	cIdCST   	  := ValCodCst( a_ClasFis[3], cKeyTrib ) 

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= nBaseTributo
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ARETPIS
	jTrib["C35_VALOR"]	:= nValorTributo
	jTrib["C35_VLISEN"]	:= nValorIsento
	jTrib["C35_VLNT"]	:= nVlrNaoTribut
	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .and. x[TTCFOP] == cIdCfop .and. x[TTALIQ] = (cAlias)->FT_ARETPIS }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)		

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ARETPIS	
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += nBaseTributo
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo
	aTotaisTrib[nPosTrib][TTVLISEN] += nValorIsento
	aTotaisTrib[nPosTrib][TTVLNT  ] += nVlrNaoTribut
	aTotaisTrib[nPosTrib][TTBASEQT] += (cAlias)->FT_QUANT	

Endif	

// 11=Cofins Retencao
If (cAlias)->FT_VRETCOF > 0 .Or. (cAlias)->DHR_VLRCOF > 0
	cKeyTrib 	  := BuscCodTrib(@oCodTrib, '11')
	nBaseTributo  := Iif((cAlias)->DHR_BASCOF > 0, (cAlias)->DHR_BASCOF , (cAlias)->FT_BRETCOF)
	nValorTributo := Iif((cAlias)->DHR_BASCOF > 0, (cAlias)->DHR_BASCOF , (cAlias)->FT_VRETCOF)
	nValorIsento  := IIf((cAlias)->FT_CSTCOF $ "07|71",(cAlias)->FT_VRETCOF,0)
	nVlrNaoTribut := IIf((cAlias)->FT_CSTCOF $ "08|74",(cAlias)->FT_VRETCOF,0)
	cIdCST   	  := ValCodCst( a_ClasFis[4], cKeyTrib ) 

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= nBaseTributo
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ARETCOF
	jTrib["C35_VALOR"]	:= nValorTributo
	jTrib["C35_VLISEN"]	:= nValorIsento
	jTrib["C35_VLNT"]	:= nVlrNaoTribut
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCST] == cIdCST .and. x[TTCFOP] == cIdCfop .and. x[TTALIQ] = (cAlias)->FT_ARETCOF }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)		

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ARETCOF
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += nBaseTributo
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo
	aTotaisTrib[nPosTrib][TTVLISEN] += nValorIsento
	aTotaisTrib[nPosTrib][TTVLNT  ] += nVlrNaoTribut
	aTotaisTrib[nPosTrib][TTBASEQT] += (cAlias)->FT_QUANT	
Endif
	
// 12=IR EMISSÃO | 28=IR PAGAMENTO
If (cAlias)->FT_BASEIRR > 0 .Or. (cAlias)->DHR_BASEIR > 0
	iF cCalcIr <> '2' 
		cKeyTrib := BuscCodTrib(@oCodTrib, '12')
	elseif cCalcIr == '2' 
		cKeyTrib := BuscCodTrib(@oCodTrib, '28')
	EndIf
	cCalcIr 	  := (cAlias)->A2_CALCIRF
	nBaseTributo  := Iif((cAlias)->DHR_BASEIR > 0, (cAlias)->DHR_BASEIR , (cAlias)->FT_BASEIRR)
	nValorTributo := Iif((cAlias)->DHR_VLRIR > 0, (cAlias)->DHR_VLRIR , (cAlias)->FT_VALIRR)

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib	
	jTrib["C35_BASE"]	:= nBaseTributo
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQIRR
	jTrib["C35_VALOR"]  := nValorTributo
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)			

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib	
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQIRR
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += nBaseTributo
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo	

Endif

// 13=Previdencia
If((cAlias)->FT_BASEINS > 0)
	nVlPrev := IIf((cAlias)->FT_TIPOMOV=="S",(cAlias)->D2_ABSCINS,(cAlias)->D1_AVLINSS)
	cKeyTrib := BuscCodTrib(@oCodTrib, '13')
	nTotVlCp := (cAlias)->FT_VLCP15 + (cAlias)->FT_VLCP20 + (cAlias)->FT_VLCP25

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib	
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEINS
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQINS
	jTrib["C35_VALOR"]  := ((cAlias)->FT_VALINS + nVlPrev - ( nTotVlCp ))
	jTrib["C35_VLSCON"]	:= nVlPrev	

	If(((cAlias)->FT_BSCP15 + (cAlias)->FT_BSCP20 + (cAlias)->FT_BSCP25 ) > 0)
		jTrib["C35_VLCE15"]	:= (cAlias)->FT_BSCP15
		jTrib["C35_VLCE20"]	:= (cAlias)->FT_BSCP20
		jTrib["C35_VLCE25"]	:= (cAlias)->FT_BSCP25

	ElseIf(!Empty(cCampSb1) .And. SB1->(FieldPos(cCampSb1)) > 0)
		if( !Empty((cAlias)->(&(cCampSb1) ) ) )
			If((cAlias)->(&(cCampSb1)) == '1')
				jTrib["C35_VLCE15"]	:= (cAlias)->FT_BASEINS 
			ElseIf((cAlias)->(&(cCampSb1)) == '2')
				jTrib["C35_VLCE20"]	:= (cAlias)->FT_BASEINS 
			ElseIf((cAlias)->(&(cCampSb1)) == '3')
				jTrib["C35_VLCE25"]	:= (cAlias)->FT_BASEINS 
			EndIf
		Endif
	EndIf
	jTrib["C35_VLRADI"]	:= nTotVlCp

	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)			

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib	
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQINS	
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEINS
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALINS		

Endif	
// 14=PIS Importacao
// 15=Cofins Importacao
// 16=ISSQN Retido
If(!Empty((cAlias)->FT_CODISS) .and. (cAlias)->FT_RECISS == '2')
	cKeyTrib := BuscCodTrib(@oCodTrib, '16')
	cIdCST   := ValCodCst( (cAlias)->FT_CSTISS, cKeyTrib )
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASEICM
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQICM
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALICM
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENICM
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRICM
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)			

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib	
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif		
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQICM	
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEICM
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALICM
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENICM
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRICM

Endif	

// 17=ICMS Antecipado
If((cAlias)->F4_ANTICMS == "1" .And. (cAlias)->FT_VALANTI > 0 .And. (cAlias)->F4_SITTRIB $ "10/30/60/70")
	cKeyTrib := BuscCodTrib(@oCodTrib, '17')
	cIdCST   := ValCodCst( (cAlias)->F4_SITTRIB, cKeyTrib ) 
	
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]   := (cAlias)->FT_BASEICM
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQICM	
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALANTI
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENICM
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRICM
	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQICM	
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEICM
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALANTI
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENICM
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRICM	
Endif	

// 18=CSLL
If((cAlias)->FT_BRETCSL > 0 .Or. (cAlias)->DHR_VLRCSL > 0 )
	cKeyTrib 	  := BuscCodTrib(@oCodTrib, '18')
	nBaseTributo  := Iif((cAlias)->DHR_BASCSL > 0, (cAlias)->DHR_BASCSL , (cAlias)->FT_BRETCSL)
	nValorTributo := Iif((cAlias)->DHR_VLRCSL > 0, (cAlias)->DHR_VLRCSL , (cAlias)->FT_VRETCSL)

	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_BASE"]   := nBaseTributo
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ARETCSL
	jTrib["C35_VALOR"]	:= nValorTributo
	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop .and. x[TTALIQ] = (cAlias)->FT_ARETCSL }
	nPosTrib := aScan(aTotaisTrib, bAscan)		

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
		aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ARETCSL
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += nBaseTributo
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo	

Endif	

// 19=IRPJ
// 20=DIfal Origem 
// 21=DIfal Destino
If((cAlias)->FT_PDDES > 0) 
	a_DIfalFcp := fDIfalFcp( cUfEst, cAlias)
	jTrib := JsonObject():New()
	If a_DIfalFcp[nDifalOrig] > 0
		cKeyTrib := BuscCodTrib(@oCodTrib, '20')
		nValorTributo := a_DIfalFcp[nDifalOrig]		
	Endif
 	If a_DIfalFcp[nDifalDest] > 0
		cKeyTrib := BuscCodTrib(@oCodTrib, '21')
		nValorTributo := a_DIfalFcp[nDifalDest]	
	Endif
	jTrib["C35_ALIQ"]	:= a_DIfalFcp[nAlqDifalC]
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_BASE"] 	:= (cAlias)->FT_BASEDES
	jTrib["C35_VALOR"]  := nValorTributo

	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )


	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif	
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := a_DIfalFcp[nAlqDifalC]
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 		
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEDES
	aTotaisTrib[nPosTrib][TTVALOR ] += nValorTributo

Endif	
// 22=FCP Destino
If ((cAlias)->FT_VFCPDIF > 0)
	a_DIfalFcp := fDIfalFcp( cUfEst, cAlias)
	cKeyTrib := BuscCodTrib(@oCodTrib, '22')
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_BASE"] 	:= (cAlias)->FT_BASEDES
	jTrib["C35_VALOR"]	:= a_DIfalFcp[nDifalFecp]
	jTrib["C35_ALIQ"]	:= a_DIfalFcp[nAlqFecpCm]

	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif		
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := a_DIfalFcp[nAlqFecpCm]		
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 		
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASEDES
	aTotaisTrib[nPosTrib][TTVALOR ] += a_DIfalFcp[nDifalFecp]

Endif
	
// 23=CPRB
If((cAlias)->FT_BASECPB > 0)
	cKeyTrib := BuscCodTrib(@oCodTrib, '23')
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib		
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASECPB
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQCPB
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VALCPB	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif		
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQCPB
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASECPB
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VALCPB	
	
Endif	

// 24=GILRAT 
If((cAlias)->FT_BASEFUN > 0)
	If (cAlias)->FT_VALFUN > 0
		nBasGILRAT := (cAlias)->FT_BASEFUN
		nAlqGILRAT := (cAlias)->FT_ALIQFUN
		nValGILRAT := (cAlias)->FT_VALFUN
	Else
		nBasGILRAT := IIf((cAlias)->FT_TIPOMOV == "E",(cAlias)->D1_BASEFUN,(cAlias)->D2_BASEFUN)
		nAlqGILRAT := IIf((cAlias)->FT_TIPOMOV == "E",(cAlias)->D1_ALIQFUN,(cAlias)->D2_ALIQFUN)
		nValGILRAT := IIf((cAlias)->FT_TIPOMOV == "E",(cAlias)->D1_VALFUN,(cAlias)->D2_VALFUN)
	EndIf
	jTrib := JsonObject():New()
	cKeyTrib := BuscCodTrib(@oCodTrib, '24')
	jTrib["C35_CODTRI"] := cKeyTrib		
	jTrib["C35_BASE"]	:= nBasGILRAT
	jTrib["C35_ALIQ"]	:= nAlqGILRAT
	jTrib["C35_VALOR"]	:= nValGILRAT	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif		
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := nAlqGILRAT
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 			
	aTotaisTrib[nPosTrib][TTBASE  ] += nBasGILRAT
	aTotaisTrib[nPosTrib][TTVALOR ] += nValGILRAT

Endif	

// 25=SENAR
If((cAlias)->FT_BSSENAR > 0)
	cKeyTrib := BuscCodTrib(@oCodTrib, '25')
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_BASE"]	:= (cAlias)->FT_BSSENAR
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALSENAR
	jTrib["C35_VALOR"]	:= (cAlias)->FT_VLSENAR
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALSENAR
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BSSENAR
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_VLSENAR	
Endif	

// 26=ICMS/ST - SUBSTITUIDO (IMPOSTO SOBRE CIRCULACAO DE MERCADORIAS E SERVICOS - SUBSTITUIDO ) 
If((cAlias)->FT_ICMSRET > 0 .And. (cAlias)->FT_CREDST == "4")
	cKeyTrib := BuscCodTrib(@oCodTrib, '26')
	cIdCST   := ValCodCst( a_ClasFis[1], cKeyTrib ) 
	jTrib := JsonObject():New()
	jTrib["C35_CODTRI"] := cKeyTrib
	jTrib["C35_CST"]	:= cIdCST
	jTrib["C35_BASE"]	:= (cAlias)->FT_BASERET
	jTrib["C35_ALIQ"]	:= (cAlias)->FT_ALIQSOL
	jTrib["C35_VALOR"]	:= (cAlias)->FT_ICMSRET
	jTrib["C35_VLISEN"]	:= (cAlias)->FT_ISENRET
	jTrib["C35_VLOUTR"]	:= (cAlias)->FT_OUTRRET
	jTrib["C35_MVA"]	:= (cAlias)->FT_MARGEM	
	If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyTributo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo] := {}
	Endif
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyTributo], jTrib )

	//Desse ponto para baixo, grava array com os impostos para gerar T013AP	
	bAscan := {|x| x[TTCODTRI] == cKeyTrib .and. x[TTCFOP] == cIdCfop }
	nPosTrib := aScan(aTotaisTrib, bAscan)	

	if nPosTrib = 0
		aadd(aTotaisTrib, array(TamArrayTrib))
		nPosTrib := len(aTotaisTrib)
		aFill(aTotaisTrib[nPosTrib], 0, 5)

		aTotaisTrib[nPosTrib][TTCODTRI] := cKeyTrib
		aTotaisTrib[nPosTrib][TTCFOP  ] := cIdCfop
	endif		
	aTotaisTrib[nPosTrib][TTCODSER] := cIdCodServ
	aTotaisTrib[nPosTrib][TTCST   ] := cIdCST
	aTotaisTrib[nPosTrib][TTALIQ  ] := (cAlias)->FT_ALIQSOL
	aTotaisTrib[nPosTrib][TTVLOPE ] += (cAlias)->VLR_CONTABIL 	
	aTotaisTrib[nPosTrib][TTBASE  ] += (cAlias)->FT_BASERET
	aTotaisTrib[nPosTrib][TTVALOR ] += (cAlias)->FT_ICMSRET
	aTotaisTrib[nPosTrib][TTVLISEN] += (cAlias)->FT_ISENRET
	aTotaisTrib[nPosTrib][TTVLOUTR] += (cAlias)->FT_OUTRRET	

Endif

Return
//*********************************************************************************/
/*/{Protheus.doc} VerPPisCof
Função responsável por buscar o valor de ICMS sem aproveitamento de crédito
Função extraida do fonte EXT013.PRW função FIcmSemCred
@author Adilson Roberto
@since  31/07/2023
@return variavel com valor de ICMS sem aproveitamento de credito
//*********************************************************************************/
Static Function FIcmSCred(cAliasQ,cClasFis)
Local cMVDAPIC04 := SuperGetMV("MV_DAPIC04", .F., "") as character 
Local nValSCred as numeric 
	
// Somente deve buscar o valor sem crédito para documentos de entrada
If (cAliasQ)->FT_TIPOMOV == "E"
	
	If AllTrim(SubStr(cClasFis,2,2)) == "90"
		
		If "S" $ (cAliasQ)->F4_ICM .And. "N" $ (cAliasQ)->F4_CREDICM
			If AllTrim((cAliasQ)->FT_CFOP) $ cMVDAPIC04 .Or. AllTrim((cAliasQ)->FT_CFOP) $ "1253/1901/1551/1556/2253/2551/2556/1406/2406/1301/1302/1303/1304/1305/1306/1949/2301/2302/2303/2304/2305/2306/2949"	
	  			// Sem aproveitamento do credito coluna outras/isentas
				If  (cAliasQ)->F4_LFICM == "O" .Or. (cAliasQ)->F4_LFICM == "I"				
					// Busco o valor de ICMS sem crédito
					nValSCred := (cAliasQ)->FT_OBSICM
				Else	// Sem aproveitamento do credito
					//Busco o valor de ICMS sem crédito
					nValSCred := (cAliasQ)->FT_VALICM				
				EndIf
				
			ElseIf "S" $ (cAliasQ)->F4_CONSUMO .And. AllTrim((cAliasQ)->FT_CFOP) $ "1251/1252/1253/1254/1255/1256/1257/1407/1556/1557/1653/2407/2556/2557/2653/3556/3653"  	            	 						
				// Busco o valor de ICMS sem crédito
				nValSCred := (cAliasQ)->FT_OBSICM
			EndIf
		Else
		  	If AllTrim((cAliasQ)->FT_CFOP) $ cMVDAPIC04  	   		
				If (cAliasQ)->FT_VALANTI <> 0
					// Busco o valor de ICMS sem crédito
					nValSCred := (cAliasQ)->FT_OUTRICM
				Else			
					// Busco o valor de ICMS sem crédito
					nValSCred := (cAliasQ)->(FT_OUTRICM+FT_ICMSRET)						
		  	   	EndIf
		  	   		
		  		If AllTrim((cAliasQ)->FT_CFOP) $ "1101/1102/1111/1113/1116/1117/1118/1120/1121/1122/1124/1125/1126/1128/1401/1403/1501/1651/1652/1201/1202/1203/1204/1205/1206/1207/1208/1209/1410/1411/1503/1504/1660/1661/1662" .Or.;
		  	   		(AllTrim((cAliasQ)->FT_CFOP) $ "1653" .And. ((cAliasQ)->F4_CREDICM <> "N" .Or. (cAliasQ)->F4_CONSUMO <> "S")) .Or.;
		  	   		(AllTrim((cAliasQ)->FT_CFOP) $ "1414/1415/1451/1452/1901/1902/1903/1904/1905/1906/1907/1908/1909/1910/1911/1912/1913/1914/1915/1916/1917/1918/1919/1920/1921/1922/1923/1924/1925/1926/1933/1663/1664/1949" .And.;
		  	   		(cAliasQ)->F4_LFICM <> "N" ) .And. (cAliasQ)->FT_CREDST $ "1/4"	
		  	   			nValSCred += (cAliasQ)->FT_ICMSRET
		  	   	EndIf   			  	   
		    EndIf    
		EndIf						
	EndIf
EndIf

Return nValSCred

//*********************************************************************************/
/*/{Protheus.doc} VerPPisCof
Função para verIficar a pauta referente ao PIS e COFINS.
Função extraida do fonte EXT013.PRW
@author Adilson Roberto
@since  31/07/2023
@return array multidimensional, contem a pauta e a quantidade do pis e cofins. 
//*********************************************************************************/
Static Function VerPPisCof(cPisCof as character,cAliasPC as character)
Local lTribPis := .F.
Local lTribCof := .F.
Local aPisCofP := {{0,0,0},{0,0,0}}
Local oNotaEfd := Nil 
Local lSecUnid := SuperGetMV( 'MV_PISCOFP',.f.) as logical
	
Default cPisCof := ""
	
If !Empty(cPisCof)
		
	// Se tiver pis
	If cPisCof == "06"
		lTribPis := .F.
		If (cAliasPC)->FT_VALPIS > 0 .Or. (cAliasPC)->FT_BASEPIS > 0 .Or. (cAliasPC)->FT_ALIQPIS > 0
			If (cAliasPC)->FT_PAUTPIS > 0 .Or. (cAliasPC)->B1_VLR_PIS > 0
				lTribPis := .T.
			EndIf
		EndIf
	EndIf
		
	// Se tiver Cofins
	If cPisCof == "07"
		lTribCof := .F.
		If (cAliasPC)->FT_VALCOF > 0 .Or. (cAliasPC)->FT_BASECOF > 0 .Or. (cAliasPC)->FT_ALIQCOF > 0
			If (cAliasPC)->FT_PAUTCOF > 0 .Or. (cAliasPC)->B1_VLR_COF > 0
				lTribCof := .T.
			EndIf
		EndIf
	EndIf
		
	// Se tem pis e cofins
	If lTribPis .Or. lTribCof
		// Guarda a pauta e a quantidade referente ao PIS
		aPisCofP[1,1] := (cAliasPC)->FT_PAUTPIS
		aPisCofP[1,2] := (cAliasPC)->QTD
		aPisCofP[1,3] := (cAliasPC)->FT_VALPIS	
		// Guarda a pauta e a quantidade referente ao COFINS
		aPisCofP[2,1] := (cAliasPC)->FT_PAUTCOF
		aPisCofP[2,2] := (cAliasPC)->QTD
		aPisCofP[2,3] := (cAliasPC)->FT_VALCOF
		// Estancia o objeto
		If lSecUnid
			oNotaEfd := NotaEfd():New()
			oNotaEfd:setTNatRec((cAliasPC)->FT_TNATREC)
			oNotaEfd:setCNatRec((cAliasPC)->FT_CNATREC)
			oNotaEfd:setGNatRec((cAliasPC)->FT_GRUPONC)
			oNotaEfd:setDNatRec((cAliasPC)->FT_DTFIMNT)
			oNotaEfd:setiQtde((cAliasPC)->QTD)
			
			// Pis
			If lTribPis
				//Faz conversão de valores de pauta de PIS
				oNotaEfd:setVlPauta((cAliasPC)->FT_PAUTPIS)
				oNotaEfd:setiBsPaut((cAliasPC)->FT_BASEPIS)
				
				If oNotaEfd:OperPauta("1",.T.)
					aPisCofP[1,1] := oNotaEfd:getAlqReal()
					aPisCofP[1,2] := oNotaEfd:getBaseQtd()
				EndIf
			EndIf
			
			// Cofins
			If lTribCof
				//Faz conversão de valores de pauta de COFINS
				oNotaEfd:setVlPauta((cAliasPC)->FT_PAUTCOF)
				oNotaEfd:setiBsPaut((cAliasPC)->FT_BASECOF)
				
				If oNotaEfd:OperPauta("2",.T.)
					aPisCofP[2,1] := oNotaEfd:getAlqReal()
					aPisCofP[2,2] := oNotaEfd:getBaseQtd()
				EndIf
			EndIf
		Endif	
	EndIf
EndIf
	
Return aPisCofP

//*********************************************************************************/
/*/{Protheus.doc} BuscCodTrib
Função responsável por localizar o código do tributo na tabela C3S
@author Adilson Roberto
@since  31/07/2023
@return código de tributo
//*********************************************************************************/
Static Function BuscCodTrib(oCodTrib as object, cFilTrab as character)
Local cAliasTrb     := GetNextAlias() as character
Local cRet			as character 
Local cQueryC3s		as character

if valtype(oCodTrib) != 'O' 
    cQueryC3s := "SELECT C3S.C3S_ID"
    cQueryC3s += " FROM " + RetSqlName('C3S') + " C3S"
    cQueryC3s += " WHERE C3S.C3S_FILIAL    = ? "
    cQueryC3s += " AND C3S.C3S_CODIGO      = ? "
	cQueryC3s += " AND C3S.D_E_L_E_T_	  = ' ' "

 	oCodTrib := FWPreparedStatement():New()
 	oCodTrib:SetQuery( cQueryC3s )
 endif	

oCodTrib:SetString(1, xFilial('C3S') )
oCodTrib:SetString(2, cFilTrab)

dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oCodTrib:GetFixQuery(),{}), cAliasTrb, .f., .t.)

If (cAliasTrb)->(!eof())  
	cRet := (cAliasTrb)->C3S_ID 
Endif    
(cAliasTrb)->(dbCloseArea())
Return(cRet)

//*********************************************************************************/
/*/{Protheus.doc} fDIfalFcp
Função para retornar as informações do DIfAL e FCP.
Função extraida do fonte EXT013.PRW
@author Adilson Roberto
@since  31/07/2023
@return array multidimensional, contem as informações do dIfal e FCP. 
//*********************************************************************************/
Static Function fDIfalFcp( c_NotaUF as character, cAliasDf as character)
Local aDIfalFcp := {0,0,0,0,0}
	
Default c_NotaUF := ""
Default cMVSUBTRIB := ""
Default cMV_DIfTr	:= ""

aDIfalFcp[nAlqDifalC] := IIf((cAliasDf)->FT_TIPOMOV == "E",(cAliasDf)->D1_ALIQCMP,(cAliasDf)->D2_ALIQCMP)
aDIfalFcp[nAlqFecpCm] := IIf((cAliasDf)->FT_TIPOMOV == "E",(cAliasDf)->D1_ALFCCMP,(cAliasDf)->D2_ALFCCMP)

If (cAliasDf)->FT_PDDES > 0 .Or. (cAliasDf)->FT_VFCPDIF > 0  
	If (cAliasDf)->FT_TIPOMOV == "E" .And. (cAliasDf)->FT_TIPO $ "B/D"
		//Para devolução precisa inverter, o destino é o próprio contribuinte.
		aDIfalFcp[nDifalDest] := (cAliasDf)->FT_ICMSCOM
	
		If c_NotaUF $ cMVSUBTRIB .Or. c_NotaUF $ cMV_DIfTr
			aDIfalFcp[nDifalOrig] := (cAliasDf)->FT_DIFAL		// Deverá ver se é inscrito
			aDIfalFcp[nDifalFecp] := (cAliasDf)->FT_VFCPDIF		// Sempre ve se é inscrito
		EndIf
			
	ElseIf (cAliasDf)->FT_TIPOMOV == "S"	
		aDIfalFcp[nDifalDest] := (cAliasDf)->FT_DIFAL			// Deverá ver se é inscrito
		aDIfalFcp[nDifalOrig] := (cAliasDf)->FT_ICMSCOM		// 
		aDIfalFcp[nDifalFecp] := (cAliasDf)->FT_VFCPDIF		// Sempre ve se é inscrito
				
	EndIf
EndIf

Return aDIfalFcp

//*********************************************************************************/
/*/{Protheus.doc} LocEst
Função retornara o estado do remetente ou do destinatário
Função extraida do fonte EXT013.PRW
@author Adilson Roberto
@since  31/07/2023
@return array multidimensional, contem a pauta e a quantidade do pis e cofins. 
//*********************************************************************************/
/*
Regra1) Necessario considerar a UF do cálculo no TMS.
As regras com os campos de Remetente e Destinatário não definem as UFs que foram pagos o ICMS, 
causando divergência na operação. O TMS possui um campo para definir esta operação 
DT6_CDRCAL ( Codigo Regiao Calculo ). Através deste campo, precisamos fazer um seek na DUY
e pegar o conteúdo do DUY_EST para esta região. Caso o DUY_EST seja igual ao MV_ESTADO, 
iremos fazer a mesma busca, porém com a região informada no DT6_CDRORI ( Codigo Regiao Origem )

Regra2) Tratamento de UF na nota referente ao TMS, o cliente possui um lançamento de nota fiscal de transporte,
onde o transporte eh de responsabilidade do pagador do frete (FOB), mas o tratamento de UF
nao deve levar em consideracao o Estado do pagador do frete e sim o do destinatario da mercadoria.
No Protheus a opcao eh configuravel atraves do parametro MV_TMSUFPG com conteúdo F - Destinatário da Mercadoria.
*/
Static Function LocEst(cAliasEst as character, cMVEstado as character)
	Local lCdOri   := .F. as logical
	Local cEstDUY  as character
	Local cNotaUF  := (cAliasEst)->FT_ESTADO  as character
	Local lTmsUfPg := SuperGetMV('MV_TMSUFPG',.F.,.T.) as logical
	Local aPart as array

	If !Empty((cAliasEst)->COD_MUN_DEST) .Or. !Empty((cAliasEst)->COD_MUN_ORI) //Regra1
		If DUY->(DbSeek(xFilial('DUY') + (cAliasEst)->COD_MUN_DEST))
			If Upper(Alltrim(DUY->DUY_EST)) == Upper(Alltrim(cMVEstado))
				lCdOri := .T.
			Else
				cEstDUY := Upper(Alltrim(DUY->DUY_EST))
			Endif
		Else
			lCdOri := .T.
		Endif

		If lCdOri
			If DUY->(DbSeek(xFilial('DUY') + (cAliasEst)->COD_MUN_ORI))
				If Upper(Alltrim(DUY->DUY_EST)) <> Upper(Alltrim(cMVEstado))
					cEstDUY := Upper(Alltrim(DUY->DUY_EST))
				Endif
			Endif
		Endif
	Endif

	If !Empty( cEstDUY )
		cNotaUF := cEstDUY
	Endif

	If Empty(cEstDUY) .And. !lTmsUfPg //Regra2
		If SubStr(Alltrim(SFT->FT_CFOP),1,1)=="6"
			// Destinatario do conhecimento
			If SA1->(MSSeek(xFilial("SA1")+(cAliasEst)->(CLIDES+LOJDES)))
				aPart := TafPartic("SA1")
				If Valtype( aPart ) == "A" .And. Len( aPart ) >= 2
					cUFDes := aPart[2] //estado
				Endif
				// Remetente do conhecimento
			Endif
			If SA1->(MSSeek(xFilial("SA1")+(cAliasEst)->(DT6_CLIREM+DT6_LOJREM)))
				aPart := TafPartic("SA1")
				If Valtype( aPart ) == "A" .And. Len( aPart ) >= 2
					cUFRem := aPart[2] //estado
				Endif
			Endif
			If Upper(Alltrim(cUFDes)) <> Upper(Alltrim(cMVEstado))
				cNotaUF := cUFDes //Destinatario
			Else
				cNotaUF := cUFRem //Remetente
			Endif
		Endif
	Endif

Return cNotaUF

/*--------------------------------------------------------------------------
{Protheus.doc} SetTributosDocumento
Grava o json com os dados consolidados dos documentos e dos tributos
documento (T013AP)
@author Carlos Eduardo Nonato
@since 03/08/2023
//-------------------------------------------------------------------------*/
Static Function TotaisPorDocumento(oDadosIntegra as json, aTotaisTrib as array)

	Local i := 0 as numeric

	//Inicia a gravação dos valores por tributo do documento, essas informações ficam gravadas na aba Totais do documento fiscal
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo] := {}
	for i := 1 to len(aTotaisTrib)
		aadd(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo], JsonObject():New() )
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_CODTRI'] := aTotaisTrib[i][TTCODTRI]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_CST'   ] := aTotaisTrib[i][TTCST   ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_CFOP'  ] := aTotaisTrib[i][TTCFOP  ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_CODSER'] := aTotaisTrib[i][TTCODSER]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLOPE' ] := aTotaisTrib[i][TTVLOPE ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_BASE'  ] := aTotaisTrib[i][TTBASE  ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_ALIQ'  ] := aTotaisTrib[i][TTALIQ  ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VALOR' ] := aTotaisTrib[i][TTVALOR ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_BASEQT'] := aTotaisTrib[i][TTBASEQT]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLRPAU'] := aTotaisTrib[i][TTVLRPAU]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_BASENT'] := aTotaisTrib[i][TTBASENT]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLNT'  ] := aTotaisTrib[i][TTVLNT  ]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLISEN'] := aTotaisTrib[i][TTVLISEN]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLOUTR'] := aTotaisTrib[i][TTVLOUTR]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_VLSCRE'] := aTotaisTrib[i][TTVLSCRE]
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyTotais][cKeyValoresTributo][i]['C2F_ICMNDE'] := aTotaisTrib[i][TTICMNDE]
	next

return

/*-----------------------------------------------------------------------
{Protheus.doc} ValCodCst()
Ajusta o codigo do cst
@author Carlos Eduardo Boy
@since 11/08/2023
@return Nil, nulo, não tem retorno.
//----------------------------------------------------------------------*/
static function ValCodCst(cCodCst,cIdTrib)
Local cAliasCst  := ''

Default cCodCst  := ''
Default cIdTrib := ''

if !empty(cCodCst)
	if cCodCst == "0  " //mantem o mesmo mecanismo do extrator fiscal
		cCodCst := '00 '  
	endif

	If Len(cCodCst) > 2 
		If Left(cCodCst,1) $ "0|1"
			cCodCst := Right(cCodCst,2)
		Endif
	Endif
	cAliasCst	:= XFUNChgF3(,,,cIdTrib)
	cCodCst := alltrim(Posicione( cAliasCst , 1 , xFilial( cAliasCst ) + cCodCst , cAliasCst+'_ID' ))
	if Empty( cCodCst )
		cCodCst := ''
	endif
endif

return cCodCst

//---------------------------------------------------------------------
/*/{Protheus.doc} SetIndicativoSuspensaoProcesso
@type			static function
@description	Inclui o JSON contendo os indicativos de suspensão por processo 
				para os itens do documento fiscal no JSON do documento fiscal
@author			Melkz Siqueira
@since			19/07/2023
@version		1.0
@param          cAlias - Alias da query que contém os indicativos de suspensão 
				por processo para os itens do documento fiscal
@param          oDadosIntegra - JSON do documento fiscal contendo os indicativos 
				de suspensão por processo para os itens do documento fiscal
@param			oStIndicativoSuspensaoProcesso - Objeto contendo a instância da
				classe FWPreparedStatement() para a query de consulta aos indicativos 
				de suspensão por processo para os itens do documento fiscal
@return			Nil 
/*/
//---------------------------------------------------------------------
Static Function SetIndicativoSuspensaoProcesso(cAlias as character, oDadosIntegra as json, oStIndicativoSuspensaoProcesso as object)

	Local cQuery 							:= "" 	as character
	Local cAliasT9Q							:= "" 	as character
	Local iProcesso							:= 0	as integer

	Default cAlias 							:= ""
	Default oDadosIntegra					:= Nil
	Default oStIndicativoSuspensaoProcesso	:= Nil

	If !Empty(cAlias) .And. oDadosIntegra != Nil
		cAliasT9Q := GetNextAlias()

		If (cAlias)->FT_TIPOMOV != cTipoMovimento .Or. oStIndicativoSuspensaoProcesso == Nil
			cTipoMovimento := (cAlias)->FT_TIPOMOV
			
			cQuery := " SELECT " 
			cQuery += "		CASE "
			cQuery += "			WHEN CCF.CCF_TRIB IN ('1', '2') THEN CCF.CCF_TRIB "			// T9Q_TPPROC
			cQuery += "			ELSE ' ' "
			cQuery += "		END TP_PROC, "
			cQuery += "		C1G.C1G_ID NUM_PROC, " 											// T9Q_NUMPRO
			cQuery += "		C1G.C1G_INDPRO IND_PROC, "

			If TAFGetDB() == "MSSQL"
				cQuery += "		T5L.T5L_ID + T5L.T5L_VERSAO + T5L.T5L_CODSUS COD_SUS, "		// T9Q_IDSUSP
			Else
				cQuery += "		T5L.T5L_ID || T5L.T5L_VERSAO || T5L.T5L_CODSUS COD_SUS, " 	// T9Q_IDSUSP
			EndIf

			cQuery += "		CDG.CDG_VALOR VAL_SUS, " 										// T9Q_VALSUS
			cQuery += "		C3S.C3S_ID COD_TRIB, "											// T9Q_CODTRI
			cQuery += "		0 BASE_SUSPENSA, " 												// T9Q_BSSUSP
			cQuery += "		0 VLR_COMP_ANO_CALENDARIO, " 									// T9Q_VLRANO
			cQuery += "		0 VLR_COMP_ANO_ANT " 											// T9Q_VLRANA
			cQuery += " 	FROM " + RetSqlName("CDG") + " CDG "
			cQuery += " 	INNER JOIN " + RetSqlName("CCF") + " CCF "
			cQuery += " 		ON CCF.D_E_L_E_T_ = ' '	
			cQuery += " 			AND CCF.CCF_FILIAL = ? "
			cQuery += " 			AND CCF.CCF_NUMERO = CDG.CDG_PROCES "		
			cQuery += " 			AND CCF.CCF_TIPO = CDG.CDG_TPPROC "	
			cQuery += " 			AND CCF.CCF_IDITEM = CDG.CDG_ITPROC "	
			cQuery += " 	INNER JOIN " + RetSqlName("C1G") + " C1G "
			cQuery += " 		ON C1G.D_E_L_E_T_ = ' '	
			cQuery += " 			AND C1G.C1G_FILIAL = ? "
			cQuery += " 			AND C1G.C1G_NUMPRO = CCF.CCF_NUMERO "
			cQuery += " 	INNER JOIN " + RetSqlName("T5L") + " T5L "
			cQuery += " 		ON T5L.D_E_L_E_T_ = ' '	
			cQuery += " 			AND T5L.T5L_FILIAL = ? "
			cQuery += " 			AND T5L.T5L_ID = C1G.C1G_ID "
			cQuery += " 			AND T5L.T5L_VERSAO = C1G.C1G_VERSAO "
			cQuery += " 			AND T5L.T5L_CODSUS = CCF.CCF_INDSUS "
			cQuery += " 	INNER JOIN " + RetSqlName("C3S") + " C3S "
			cQuery += " 		ON C3S.D_E_L_E_T_ = ' '	
			cQuery += " 			AND C3S.C3S_FILIAL = ? "
			cQuery += " 			AND C3S.C3S_CODIGO = ( "
			cQuery += "					CASE "
			cQuery += "						WHEN CCF.CCF_TRIB IN ('1', '2') THEN '13' "
			cQuery += "						WHEN CCF.CCF_TRIB = '3' THEN '24' "
			cQuery += "						WHEN CCF.CCF_TRIB = '4' THEN '25' "
			cQuery += "						WHEN CCF.CCF_TRIB = '5' THEN '23' "
			cQuery += "						WHEN CCF.CCF_TRIB = '6' THEN '02' "
			cQuery += "						WHEN CCF.CCF_TRIB = '7' THEN '06' "
			cQuery += "						WHEN CCF.CCF_TRIB = '8' THEN '07' "
			cQuery += "						ELSE ' ' "
			cQuery += "					END "
			cQuery += " 			) "
			cQuery += " 	WHERE CDG.D_E_L_E_T_ = ' ' "
			cQuery += " 		AND CDG.CDG_FILIAL = ? "
			cQuery += " 		AND CDG.CDG_TPMOV = ? "
			cQuery += " 		AND CDG.CDG_SERIE = ? "
			cQuery += " 		AND CDG.CDG_DOC = ? "
			cQuery += " 		AND CDG.CDG_CLIFOR = ? "
			cQuery += " 		AND CDG.CDG_LOJA = ? "
			cQuery += " 		AND CDG.CDG_ITEM = ? "
			
			If (cAlias)->FT_TIPOMOV == "E"
				cQuery += " UNION "

				cQuery += " SELECT " 
				cQuery += "		' ' TP_PROC, "														// T9Q_TPPROC
				cQuery += "		C1G.C1G_ID NUM_PROC, " 												// T9Q_NUMPRO
				cQuery += "		' ' IND_PROC, "

				If TAFGetDB() == "MSSQL"
					cQuery += "		T5L.T5L_ID + T5L.T5L_VERSAO + T5L.T5L_CODSUS COD_SUS, "			// T9Q_IDSUSP
				Else
					cQuery += "		T5L.T5L_ID || T5L.T5L_VERSAO || T5L.T5L_CODSUS COD_SUS, " 		// T9Q_IDSUSP
				EndIf

				cQuery += "		CASE "																// T9Q_VALSUS
				cQuery += "			WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 THEN DHR.DHR_VLRSIR "
				cQuery += "			WHEN DHR.DHR_BSUPIS + DHR.DHR_VLSPIS > 0 THEN DHR.DHR_VLSPIS "
				cQuery += "			WHEN DHR.DHR_BSUCOF + DHR.DHR_VLSCOF > 0 THEN DHR.DHR_VLSCOF "
				cQuery += "			WHEN DHR.DHR_BSUCSL + DHR.DHR_VLSCSL > 0 THEN DHR.DHR_VLSCSL "
				cQuery += "			ELSE 0 "
				cQuery += "		END VAL_SUS, "
				cQuery += "		C3S.C3S_ID COD_TRIB, " 												// T9Q_CODTRI
				cQuery += "		CASE "																// T9Q_BSSUSP
				cQuery += "			WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 THEN DHR.DHR_BASUIR "
				cQuery += "			WHEN DHR.DHR_BSUPIS + DHR.DHR_VLSPIS > 0 THEN DHR.DHR_BSUPIS "
				cQuery += "			WHEN DHR.DHR_BSUCOF + DHR.DHR_VLSCOF > 0 THEN DHR.DHR_BSUCOF "
				cQuery += "			WHEN DHR.DHR_BSUCSL + DHR.DHR_VLSCSL > 0 THEN DHR.DHR_BSUCSL "
				cQuery += "			ELSE 0 "
				cQuery += "		END BASE_SUSPENSA, "																
				cQuery += "		0 VLR_COMP_ANO_CALENDARIO, " 										// T9Q_VLRANO
				cQuery += "		0 VLR_COMP_ANO_ANT " 												// T9Q_VLRANA
				cQuery += " 	FROM " + RetSqlName("DHR") + " DHR "
				cQuery += " 	INNER JOIN " + RetSqlName("C1G") + " C1G "
				cQuery += " 		ON C1G.D_E_L_E_T_ = ' '	
				cQuery += " 			AND C1G.C1G_FILIAL = ? "
				cQuery += " 			AND C1G.C1G_NUMPRO = TRIM( "
				cQuery += "					CASE "
				cQuery += "						WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 THEN DHR.DHR_PSIR "
				cQuery += "						WHEN DHR.DHR_BSUPIS + DHR.DHR_VLSPIS > 0 THEN DHR.DHR_PSPIS "
				cQuery += "						WHEN DHR.DHR_BSUCOF + DHR.DHR_VLSCOF > 0 THEN DHR.DHR_PSCOF "
				cQuery += "						WHEN DHR.DHR_BSUCSL + DHR.DHR_VLSCSL > 0 THEN DHR.DHR_PSCSL "
				cQuery += "						ELSE ' ' "
				cQuery += "					END "
				cQuery += "				) "
				cQuery += " 	INNER JOIN " + RetSqlName("T5L") + " T5L "
				cQuery += " 		ON T5L.D_E_L_E_T_ = ' '	
				cQuery += " 			AND T5L.T5L_FILIAL = ? "
				cQuery += " 			AND T5L.T5L_ID = C1G.C1G_ID "
				cQuery += " 			AND T5L.T5L_VERSAO = C1G.C1G_VERSAO "
				cQuery += " 			AND T5L.T5L_CODSUS = TRIM( "
				cQuery += "					CASE "
				cQuery += "						WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 THEN DHR.DHR_ISIR "
				cQuery += "						WHEN DHR.DHR_BSUPIS + DHR.DHR_VLSPIS > 0 THEN DHR.DHR_ISPIS "
				cQuery += "						WHEN DHR.DHR_BSUCOF + DHR.DHR_VLSCOF > 0 THEN DHR.DHR_ISCOF "
				cQuery += "						WHEN DHR.DHR_BSUCSL + DHR.DHR_VLSCSL > 0 THEN DHR.DHR_ISCSL "
				cQuery += "						ELSE ' ' "
				cQuery += "					END "
				cQuery += "				) "
				cQuery += " 	INNER JOIN " + RetSqlName("SA2") + " SA2 "
				cQuery += " 		ON SA2.D_E_L_E_T_ = ' '	
				cQuery += " 			AND SA2.A2_FILIAL = ? "
				cQuery += " 			AND SA2.A2_COD = DHR.DHR_FORNEC "
				cQuery += " 			AND SA2.A2_LOJA = DHR.DHR_LOJA "
				cQuery += " 	INNER JOIN " + RetSqlName("C3S") + " C3S "
				cQuery += " 		ON C3S.D_E_L_E_T_ = ' '	
				cQuery += " 			AND C3S.C3S_FILIAL = ? "
				cQuery += " 			AND C3S.C3S_CODIGO = ( "
				cQuery += "					CASE "
				cQuery += "						WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 AND SA2.A2_CALCIRF = '2' THEN '28' "
				cQuery += "						WHEN DHR.DHR_BASUIR + DHR.DHR_VLRSIR > 0 AND SA2.A2_CALCIRF <> '2' THEN '12' "
				cQuery += "						WHEN DHR.DHR_BSUPIS + DHR.DHR_VLSPIS > 0 THEN '10' "
				cQuery += "						WHEN DHR.DHR_BSUCOF + DHR.DHR_VLSCOF > 0 THEN '11' "
				cQuery += "						WHEN DHR.DHR_BSUCSL + DHR.DHR_VLSCSL > 0 THEN '18' "
				cQuery += "						ELSE ' ' "
				cQuery += "					END "
				cQuery += " 			) "
				cQuery += " 	WHERE DHR.D_E_L_E_T_ = ' ' "
				cQuery += " 		AND DHR.DHR_FILIAL = ? "
				cQuery += " 		AND DHR.DHR_SERIE = ? "
				cQuery += " 		AND DHR.DHR_DOC = ? "
				cQuery += " 		AND DHR.DHR_FORNEC = ? "
				cQuery += " 		AND DHR.DHR_LOJA = ? "
				cQuery += " 		AND DHR.DHR_ITEM = ? "
			EndIf

			cQuery 							:= ChangeQuery(cQuery)
			oStIndicativoSuspensaoProcesso 	:= FWPreparedStatement():New()
			
			oStIndicativoSuspensaoProcesso:SetQuery(cQuery)
		EndIf

		oStIndicativoSuspensaoProcesso:SetString(1, xFilial("CCF")			)
		oStIndicativoSuspensaoProcesso:SetString(2, xFilial("C1G")			)
		oStIndicativoSuspensaoProcesso:SetString(3, xFilial("T5L")			)
		oStIndicativoSuspensaoProcesso:SetString(4, xFilial("C3S")			)
		oStIndicativoSuspensaoProcesso:SetString(5, xFilial("CDG")			)
		oStIndicativoSuspensaoProcesso:SetString(6, (cAlias)->FT_TIPOMOV	)
		oStIndicativoSuspensaoProcesso:SetString(7, (cAlias)->FT_SERIE		)
		oStIndicativoSuspensaoProcesso:SetString(8, (cAlias)->FT_NFISCAL	) 
		oStIndicativoSuspensaoProcesso:SetString(9, (cAlias)->FT_CLIEFOR	) 
		oStIndicativoSuspensaoProcesso:SetString(10, (cAlias)->FT_LOJA		)
		oStIndicativoSuspensaoProcesso:SetString(11, (cAlias)->FT_ITEM		)

		If (cAlias)->FT_TIPOMOV == "E"
			oStIndicativoSuspensaoProcesso:SetString(12, xFilial("C1G")			)
			oStIndicativoSuspensaoProcesso:SetString(13, xFilial("T5L")			)
			oStIndicativoSuspensaoProcesso:SetString(14, xFilial("SA2")			)
			oStIndicativoSuspensaoProcesso:SetString(15, xFilial("C3S")			)
			oStIndicativoSuspensaoProcesso:SetString(16, xFilial("DHR")			)
			oStIndicativoSuspensaoProcesso:SetString(17, (cAlias)->FT_SERIE		)
			oStIndicativoSuspensaoProcesso:SetString(18, (cAlias)->FT_NFISCAL	) 
			oStIndicativoSuspensaoProcesso:SetString(19, (cAlias)->FT_CLIEFOR	) 
			oStIndicativoSuspensaoProcesso:SetString(20, (cAlias)->FT_LOJA		)
			oStIndicativoSuspensaoProcesso:SetString(21, (cAlias)->FT_ITEM		)
		EndIf

		cQuery := oStIndicativoSuspensaoProcesso:GetFixQuery()

		DBUseArea(.T., "TOPCONN", TCGenQry2(,, cQuery,{}), cAliasT9Q, .F., .T.)

		if (cAliasT9Q)->(!eof())
			If !oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem]:HasProperty(cKeyProcessos)
				oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos] := {}
			EndIf
		endif	
		
		While !(cAliasT9Q)->(EOF())
		 	
			AAdd(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos], JsonObject():New())	
			iProcesso++
			
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_TPPROC"] := (cAliasT9Q)->TP_PROC
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_NUMPRO"] := (cAliasT9Q)->NUM_PROC
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_IDSUSP"] := (cAliasT9Q)->COD_SUS
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_VALSUS"] := (cAliasT9Q)->VAL_SUS
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_CODTRI"] := (cAliasT9Q)->COD_TRIB
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_BSSUSP"] := (cAliasT9Q)->BASE_SUSPENSA
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_VLRANO"] := (cAliasT9Q)->VLR_COMP_ANO_CALENDARIO
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyProcessos][iProcesso]["T9Q_VLRANA"] := (cAliasT9Q)->VLR_COMP_ANO_ANT

			(cAliasT9Q)->(DbSkip())
		EndDo

		(cAliasT9Q)->(DbCloseArea())
	Endif

Return

/*-----------------------------------------------------------------------
{Protheus.doc} RetC3R()
Função responsável pelo preenchimento da Tabela C3R ou registro 0460 so Sped Fiscal
@author Adilson Roberto
@since 15/08/2023
@return Nil, nulo, não tem retorno.
//----------------------------------------------------------------------*/
Static Function RetC3R(cCodObs as character, cDescObs as character) as logical
Local lRet as logical
Local cId as character
Local cProcedure := "TAF613O" as character
Local aOutput := {} as array

Default cCodObs  := " "
Default cDescObs := " "

If C3R->(MsSeek(xFilial('C3R')+cCodObs))
	cId 	:= C3R->C3R_ID
	cDesC3R	:= C3R->C3R_DESCRI
	If !(cDesC3R == cDescObs)
		lProc := .T.
	Endif
Else
	cId := GetSx8Num("C3R","C3R_ID",,3)   
	lProc := .T.                                                                                                 
Endif
If lProc
	aOutput := TCSPExec(cProcRealName, cFilAnt, cProcedure, '', '', '', 0, '', '', '', '', '', '', '', '', '', Alltrim(cCodObs), Alltrim(cId), Alltrim(cDescObs))
Endif
If Empty(aOutput) .Or. Empty(aOutput[1]) .Or. aOutput[1] == '0'			
	lRet := .F.
	RollbackSx8()
	ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
Else
	lRet := .T.										
	ConfirmSx8()
EndIf

Return lRet
/*----------------------------------------------------------------------
{Protheus.doc} OperacoesComMedicamentos()
Consulta e carrega o Json com Medicamentos
(T015AA - SPED C173)
@author jose.riquelmo
@since 28/06/2023
//----------------------------------------------------------------------*/
Static Function OperacoesComMedicamentos(cAlias as character, oDadosIntegra as json, oStMedic as object)
Local cLoteMed as character
Local cDataFab as character
Local cDataVal as character
Local cMsgLog  as character
Local nQuantLote as numeric
Local lRegValido as logical

if (cAlias)->CD7RECNO > 0

	if (cAlias)->F0ARECNO > 0
		cLoteMed   := rtrim((cAlias)->F0A_LOTE  )
		cDataFab   := rtrim((cAlias)->F0A_FABRIC)
		cDataVal   := rtrim((cAlias)->F0A_VALID )
		nQuantLote := (cAlias)->F0A_QTDLOT
	else
		cLoteMed   := rtrim((cAlias)->CD7_LOTE  )
		cDataFab   := rtrim((cAlias)->CD7_FABRIC)
		cDataVal   := rtrim((cAlias)->CD7_VALID )
		nQuantLote := (cAlias)->CD7_QTDLOT
	endif

    //Para ser um registro valido, todos os campos abaixo devem estar preenchidos.
	lRegValido := !( empty(cLoteMed) .or. empty(cDataFab) .or. empty(cDataVal) .or. empty(nQuantLote) .or. empty((cAlias)->CD7_REFBAS) .or. empty((cAlias)->CD7_TPPROD) .or. empty((cAlias)->CD7_PRECO ) )

	if lRegValido
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos] := {}
		aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos], JsonObject():New()  )
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_LOTE'  ] := cLoteMed     
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_DTFAB' ] := cDataFab     
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_DTVAL' ] := cDataVal     
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_QUANT' ] := nQuantLote	
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_INDMED'] := rtrim((cAlias)->CD7_REFBAS)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_TPPROD'] := rtrim((cAlias)->CD7_TPPROD)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyMedicamentos][1]['C31_VLTBMX'] := (cAlias)->CD7_PRECO
	else
		//Gera log de erro na integração.
		cMsgLog := STR0006 //'Obs.: Faltam uma ou mais informações na origem da integração.(Cadastro: "Operações com medicamentos")'
		aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('CDT'), cMsgLog, .t., 'SFT' }
		ErrorHandler(cMsgLog)
	endif	

endif	

return

/*----------------------------------------------------------------------
{Protheus.doc} OperacoesComArmasDeFogo()
Consulta e carrega o Json com Armas de Fogo
(T015AC - SPED C174)
@author jose.riquelmo
@since 28/06/2023
//----------------------------------------------------------------------*/
Static Function OperacoesComArmasDeFogo(cAlias as character, oDadosIntegra as json, oStGuns as object)
Local cQuery as character
Local cAliasCD8 := GetNextAlias() as character
Local nPosArmas as integer

if valtype(oStGuns) != 'O'

	cQuery := "	SELECT " 
	cQuery += "		CD8.CD8_TPARMA,"
	cQuery += "		CD8.CD8_NUMARM ,"
	cQuery += "		CD8.CD8_DESCR "
	cQuery += " FROM " + RetSqlName('CD8') + " CD8 "
	cQuery += " WHERE CD8.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND CD8.CD8_FILIAL = ? " // 1
	cQuery += " 	AND CD8.CD8_TPMOV  = ? " // 2
	cQuery += " 	AND CD8.CD8_DOC    = ? " // 3
	cQuery += " 	AND CD8.CD8_SERIE  = ? " // 4
	cQuery += " 	AND CD8.CD8_CLIFOR = ? " // 5
	cQuery += " 	AND CD8.CD8_LOJA   = ? " // 6
	cQuery += " 	AND CD8.CD8_ITEM   = ? " // 7
	cQuery += " 	AND CD8.CD8_COD    = ? " // 8

	oStGuns := FWPreparedStatement():New()
	oStGuns:SetQuery( cQuery )
endif	

oStGuns:SetString(1, xFilial('CD8') )
oStGuns:SetString(2, (cAlias)->FT_TIPOMOV)
oStGuns:SetString(3, (cAlias)->FT_NFISCAL)
oStGuns:SetString(4, (cAlias)->FT_SERIE  )
oStGuns:SetString(5, (cAlias)->FT_CLIEFOR)
oStGuns:SetString(6, (cAlias)->FT_LOJA   )
oStGuns:SetString(7, (cAlias)->FT_ITEM   )
oStGuns:SetString(8, (cAlias)->FT_PRODUTO)
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStGuns:GetFixQuery(),{}), cAliasCD8, .f., .t.)

if (cAliasCD8)->(!eof()) 
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmas] := {}
endif

while (cAliasCD8)->(!eof()) 
	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmas], JsonObject():New()  )
	nPosArmas++
	
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmas][nPosArmas]['C33_INDARM'] := (cAliasCD8)->CD8_TPARMA
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmas][nPosArmas]['C33_NUMARM'] := (cAliasCD8)->CD8_NUMARM
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmas][nPosArmas]['C33_DESCRI'] := (cAliasCD8)->CD8_DESCR
	
	(cAliasCD8)->(DbSkip())
enddo

(cAliasCD8)->(DBCloseArea())

return

/*----------------------------------------------------------------------
{Protheus.doc} OperacoesComVeiculosNovos()
Consulta e carrega o Json com as operações com veículos novos
(T015AD - SPED C175)
@author jose.riquelmo
@since 13/06/2023
//----------------------------------------------------------------------*/
Static Function OperacoesComVeiculosNovos(cAlias as character, oDadosIntegra as json, oStCar as object) 
Local cQuery as character
Local cAliasCD9  := GetNextAlias() as character
Local cIdUfPart  := "" as character
Local nPosVeiculo as integer

if valtype(oStCar) != 'O'

	cQuery := "  SELECT " 
	cQuery += "    CD9.CD9_TPOPER, "
	cQuery += "    CD9.CD9_CHASSI, "
	cQuery += "    CD9.CD9_CODCOR, "
	cQuery += "    CD9.CD9_POTENC, "
	cQuery += "    CD9.CD9_CM3POT, "
	cQuery += "    CD9.CD9_PESOLI, "
	cQuery += "    CD9.CD9_PESOBR, "
	cQuery += "    CD9.CD9_SERIAL, "
	cQuery += "    CD9.CD9_TPCOMB, "
	cQuery += "    CD9.CD9_NMOTOR, "
	cQuery += "    CD9.CD9_CMKG, "
	cQuery += "    CD9.CD9_DISTEI, "
	cQuery += "    CD9.CD9_RENAVA, "
	cQuery += "    CD9.CD9_ANOMOD, "
	cQuery += "    CD9.CD9_ANOFAB, "
	cQuery += "    CD9.CD9_TPPINT, "
	cQuery += "    CD9.CD9_TPVEIC, "
	cQuery += "    CD9.CD9_ESPVEI, "
	cQuery += "    CD9.CD9_CONVIN, "
	cQuery += "    CD9.CD9_CONVEI, "
	cQuery += "    CD9.CD9_CODMOD, "
	cQuery += "    CD9.CD9_CILIND, "
	cQuery += "    CD9.CD9_TRACAO, "
	cQuery += "    CD9.CD9_LOTAC, "
	cQuery += "    CD9.CD9_CORDE, "
	cQuery += "    CD9.CD9_RESTR, "
	cQuery += "    CD9.CD9_DSCCOR, "
	cQuery += "    CEL.CEL_ID, "
	cQuery += "    CAX.CAX_ID, "
	cQuery += "    CAJ.CAJ_ID "
	cQuery += " FROM " + RetSqlName('CD9') + " CD9 "
	cQuery += " 	LEFT JOIN " + RetSqlname('CEL')+ " CEL ON CEL.CEL_FILIAL = '" + xFilial('CEL') + "' AND CEL.CEL_CODIGO = CD9.CD9_CODCOR  "
	cQuery += " 	LEFT JOIN " + RetSqlname('CAX')+ " CAX ON CAX.CAX_FILIAL = '" + xFilial('CAX') + "' AND CAX.CAX_CODIGO = CD9.CD9_TPCOMB  "
	cQuery += " 	LEFT JOIN " + RetSqlname('CAJ')+ " CAJ ON CAJ.CAJ_FILIAL = '" + xFilial('CAJ') + "' AND CAJ.CAJ_CODIGO = CD9.CD9_TPVEIC  "
	cQuery += " WHERE CD9.D_E_L_E_T_ = ' ' "
	cQuery += " 	AND CD9.CD9_FILIAL = ? " //01
	cQuery += " 	AND CD9.CD9_TPMOV  = ? " //02
	cQuery += " 	AND CD9.CD9_SERIE  = ? " //03
	cQuery += " 	AND CD9.CD9_DOC    = ? " //04
	cQuery += " 	AND CD9.CD9_CLIFOR = ? " //05
	cQuery += " 	AND CD9.CD9_LOJA   = ? " //06
	cQuery += " 	AND CD9.CD9_ITEM   = ? " //07
	cQuery += " 	AND CD9.CD9_COD    = ? " //08	

	oStCar := FWPreparedStatement():New()
	oStCar:SetQuery( cQuery )
endif	

oStCar:SetString(1, xFilial('CD9')      )
oStCar:SetString(2, (cAlias)->FT_TIPOMOV)
oStCar:SetString(3, (cAlias)->FT_SERIE  )
oStCar:SetString(4, (cAlias)->FT_NFISCAL)
oStCar:SetString(5, (cAlias)->FT_CLIEFOR)
oStCar:SetString(6, (cAlias)->FT_LOJA   )
oStCar:SetString(7, (cAlias)->FT_ITEM   )
oStCar:SetString(8, (cAlias)->FT_PRODUTO)
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStCar:GetFixQuery(),{}), cAliasCD9, .f., .t.)

If (cAliasCD9)->(!eof()) 
	
	//Id da uf nas tabelas autocontidas do TAF
	C09->(DbSetOrder(1))
	if C09->(MsSeek( xFilial('C09')+alltrim((cAlias)->UF_PART) ))
		cIdUfPart := C09->C09_ID
	endif	

	//Cria a propriedade como array
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos] := {}
Endif

While (cAliasCD9)->(!eof()) 

	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos], JsonObject():New()  )
	nPosVeiculo++

	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos][nPosVeiculo]['C34_INDVEI']  := (cAliasCD9) ->CD9_TPOPER     
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos][nPosVeiculo]['C34_CHASSI']  := (cAliasCD9) ->CD9_CHASSI     
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos][nPosVeiculo]['C34_CNPJ'	]  := (cAlias)->CNPJ_PART
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyVeiculos][nPosVeiculo]['C34_UF'	]  := cIdUfPart
	
	(cAliasCD9)->(DbSkip())
ENDDO

(cAliasCD9)->(DBCloseArea())

return

/*----------------------------------------------------------------------
{Protheus.doc} ArmazenamentoCombustivel()

(T015AF - SPED C171)
@author Carlos Eduardo Boy
@since 21/08/2023
//----------------------------------------------------------------------*/
Static Function ArmazenamentoCombustivel(cAlias as character, oDadosIntegra as json, oStArmCombustivel as object)
Local cQuery as character
Local cAliasArmComb := GetNextAlias() as character
Local nPosArmComb as numeric

if valtype(oStArmCombustivel) != 'O'
	
	cQuery := " SELECT "
	cQuery += "		CD6.CD6_TANQUE, "
	cQuery += "		CD6.CD6_VOLUME, "
	cQuery += "		CD6.CD6_SEFAZ , "
	cQuery += "		CD6.CD6_QTAMB , "
	cQuery += "		C09.C09_ID    , " //UF CONSUMO
	cQuery += "		CD6.CD6_BCCIDE, "
	cQuery += "		CD6.CD6_VALIQ , "
	cQuery += "		CD6.CD6_VCIDE   "
	cQuery += "  FROM " + RetSqlName('CD6') + ' CD6 ' "
	cQuery += " 	LEFT JOIN " + RetSqlName('C09') + " C09 ON C09.C09_FILIAL = '" + xFilial('C09') + "' AND C09.C09_UF = CD6.CD6_UFCONS AND C09.D_E_L_E_T_ = ' ' "
 	cQuery += "  WHERE CD6.D_E_L_E_T_  = ' ' "
	cQuery += " 	AND CD6.CD6_TPMOV  = 'E' "
	cQuery += " 	AND CD6.CD6_TANQUE <> ' ' "
	cQuery += " 	AND CD6.CD6_FILIAL = ? "
	cQuery += " 	AND CD6.CD6_SERIE  = ? "
	cQuery += " 	AND CD6.CD6_DOC    = ? "
	cQuery += " 	AND CD6.CD6_CLIFOR = ? "
	cQuery += " 	AND CD6.CD6_LOJA   = ? "
	cQuery += " 	AND CD6.CD6_ITEM   = ? "
	cQuery += " 	AND CD6.CD6_COD    = ? "	
	
	oStArmCombustivel := FWPreparedStatement():New()
	oStArmCombustivel:SetQuery( cQuery )
endif

oStArmCombustivel:SetString(1, xFilial('CD6') )
oStArmCombustivel:SetString(2, (cAlias)->FT_SERIE  )
oStArmCombustivel:SetString(3, (cAlias)->FT_NFISCAL) 
oStArmCombustivel:SetString(4, (cAlias)->FT_CLIEFOR) 
oStArmCombustivel:SetString(5, (cAlias)->FT_LOJA   )
oStArmCombustivel:SetString(6, (cAlias)->FT_ITEM   )
oStArmCombustivel:SetString(7, (cAlias)->FT_PRODUTO)
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStArmCombustivel:GetFixQuery(),{}), cAliasArmComb, .f., .t.)


if (cAliasArmComb)->(!eof())  
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel] := {}
endif

While (cAliasArmComb)->(!eof()) 

	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel], JsonObject():New() )	
	nPosArmComb++

	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_NUMTAN'] := (cAliasArmComb)->CD6_TANQUE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_QUANT' ] := (cAliasArmComb)->CD6_VOLUME
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_CODIF' ] := (cAliasArmComb)->CD6_SEFAZ  
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_QTAMB' ] := (cAliasArmComb)->CD6_QTAMB 
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_UFCONS'] := (cAliasArmComb)->C09_ID    
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_BCCIDE'] := (cAliasArmComb)->CD6_BCCIDE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_VALIQ' ] := (cAliasArmComb)->CD6_VALIQ 
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyArmCombustivel][nPosArmComb]['C36_VCIDE' ] := (cAliasArmComb)->CD6_VCIDE 

	(cAliasArmComb)->(DbSkip())

enddo

(cAliasArmComb)->(DbCloseArea())

return

/*----------------------------------------------------------------------
{Protheus.doc} RessarcimentoICMS()
Gera o complemento de ressarcimento de ICMS
(T015AG - SPED C176)
@author Carlos Eduardo Boy
@since 29/08/2023
//----------------------------------------------------------------------*/
Static Function RessarcimentoICMS(cAlias as character, oDadosIntegra as json, oStRessarcimentoICMS as object)

Local cQuery                          as character
Local cAliasCD0     := GetNextAlias() as character
Local nPosRessar                      as numeric
Local cIdPartDocEnt                   as character
Local cIdPartIcmsST                   as character
Local cIdEspecie                      as character
Local cCliCD0       := ""             as character
Local cLojaCD0      := ""             as character
Local cPanFrt       := ""             as character
Local cLjPanf       := ""             as character

if valtype(oStRessarcimentoICMS) != 'O'
	
	cQuery := " SELECT "
	cQuery += " 	CD0_FILIAL, "
	cQuery += " 	CD0_ESPECI, "
	cQuery += " 	CD0_FORNE , "
	cQuery += " 	CD0_LOJENT, "
	cQuery += " 	CD0_DOCENT, "
	cQuery += " 	CD0_SERENT, "
	cQuery += " 	CD0_EMISSA, "
	cQuery += " 	CD0_QUANT , "
	cQuery += " 	CD0_VUNIT , "
	cQuery += " 	CD0_VALBST, "
	cQuery += " 	CD0_CHVNFE, "
	cQuery += " 	CD0_ITENFE, "
	cQuery += " 	CD0_VLUNOP, "
	cQuery += " 	CD0_PICMSE, "
	cQuery += " 	CD0_BSULMT, "
	cQuery += " 	CD0_VLUNCR, "
	cQuery += " 	CD0_ALQSTE, "
	cQuery += " 	CD0_VLUNRE, "
	cQuery += " 	CD0_RESPRE, "
	cQuery += " 	CD0_MOTRES, "
	cQuery += " 	CD0_PANFRT, "
	cQuery += " 	CD0_LJPANF, "
	cQuery += " 	CD0_CHNFRT, "
	cQuery += " 	CD0_SRNFRT, "
	cQuery += " 	CD0_NRNFRT, "
	cQuery += " 	CD0_ITNFRT, "
	cQuery += " 	CD0_CODDA,  "
	cQuery += " 	CD0_NUMDA,  "
	cQuery += " 	CD0_FCPST   "
	cQuery += " FROM " + RetSqlName('CD0') 
	cQuery += " WHERE D_E_L_E_T_ = ' ' "
	cQuery += " 	AND CD0_FILIAL = ? "
	cQuery += "  	AND CD0_TPMOV  = ? "
	cQuery += "  	AND CD0_DOC    = ? "
	cQuery += "  	AND CD0_SERIE  = ? "
	cQuery += "  	AND CD0_CLIFOR = ? "
	cQuery += "  	AND CD0_LOJA   = ? "
	cQuery += "  	AND CD0_ITEM   = ? "

	oStRessarcimentoICMS := FWPreparedStatement():New()
	oStRessarcimentoICMS:SetQuery( cQuery )
endif

oStRessarcimentoICMS:SetString(1, xFilial('CD0') )
oStRessarcimentoICMS:SetString(2, (cAlias)->FT_TIPOMOV)
oStRessarcimentoICMS:SetString(3, (cAlias)->FT_NFISCAL) 
oStRessarcimentoICMS:SetString(4, (cAlias)->FT_SERIE  )
oStRessarcimentoICMS:SetString(5, (cAlias)->FT_CLIEFOR) 
oStRessarcimentoICMS:SetString(6, (cAlias)->FT_LOJA   )
oStRessarcimentoICMS:SetString(7, (cAlias)->FT_ITEM   )
dbUseArea(.t., 'TOPCONN', TCGenQry2(, , oStRessarcimentoICMS:GetFixQuery(),{}), cAliasCD0, .f., .t.)

if (cAliasCD0)->(!eof())  
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST] := {}
endif

While (cAliasCD0)->(!eof())

	cCliCD0  := (cAliasCD0)->CD0_FORNE
	cLojaCD0 := (cAliasCD0)->CD0_LOJENT
	cPanFrt  := (cAliasCD0)->CD0_PANFRT
	cLjPanf := (cAliasCD0)->CD0_LJPANF

	cIdPartDocEnt := ComplementParticipantIntegration(cCliCD0, cLojaCD0, "SA2", 'CD0')		
	cIdPartIcmsST := ComplementParticipantIntegration(cPanFrt, cLjPanf , "SA2", 'CD0')

	aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST], JsonObject():New() )	
	nPosRessar++

	//Id do modelo do documento no TAF
	cIdEspecie := GetTafId('C01',AModNot(CD0->CD0_ESPECI), 1)

	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODMOD'] := cIdEspecie
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_SERIE' ] := (cAliasCD0)->CD0_SERENT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_NUMDOC'] := (cAliasCD0)->CD0_DOCENT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_DTDOC' ] := (cAliasCD0)->CD0_EMISSA
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODPAR'] := cIdPartDocEnt
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_QUANT' ] := (cAliasCD0)->CD0_QUANT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLITEM'] := (cAliasCD0)->CD0_VUNIT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLBCST'] := (cAliasCD0)->CD0_VALBST
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CHVULT'] := (cAliasCD0)->CD0_CHVNFE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_NUMULT'] := (cAliasCD0)->CD0_ITENFE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLBICM'] := (cAliasCD0)->CD0_VALBST
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_ALIQIC'] := (cAliasCD0)->CD0_PICMSE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLLIMI'] := (cAliasCD0)->CD0_BSULMT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLUICM'] := (cAliasCD0)->CD0_VLUNCR
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_ALIQST'] := (cAliasCD0)->CD0_ALQSTE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_VLURES'] := (cAliasCD0)->CD0_VLUNRE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODRES'] := (cAliasCD0)->CD0_RESPRE
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODMOT'] := (cAliasCD0)->CD0_MOTRES
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CHVRET'] := (cAliasCD0)->CD0_CHNFRT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODPRE'] := cIdPartIcmsST
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_SERNFE'] := (cAliasCD0)->CD0_SRNFRT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_NUMNFE'] := (cAliasCD0)->CD0_NRNFRT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_NITEMR'] := (cAliasCD0)->CD0_ITNFRT
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_CODMDA'] := (cAliasCD0)->CD0_CODDA
	oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyRessarICMSST][nPosRessar]['C37_NUMDA' ] := (cAliasCD0)->CD0_NUMDA

	

	//Zera as variaveis para gravação do próximo registro.
	cIdPartDocEnt := ''
	cIdPartIcmsST := ''	

	(cAliasCD0)->(DbSkip())	

enddo

(cAliasCD0)->(DbCloseArea())

return

/*----------------------------------------------------------------------
{Protheus.doc} ComplItensDocComunicaoTelecomunicao()
Gera o complemento dos itens de documento modelos 21(Comunicação) e 
22(telecomunicao)
Obs.: Permite apenas um complemento por item.
(T015AH - SPED D510)
@author Carlos Eduardo Boy
@since 29/08/2023
//----------------------------------------------------------------------*/
Static Function ComplItensDocComunicaoTelecomunicao(cAlias as character, oDadosIntegra as json) as logical 

Local bScanD510 as codeblock
Local nPosItem as integer

bScanD510 := { |x| x[1] == alltrim((cAlias)->FT_TIPOMOV) .and.;
                   x[2] == alltrim((cAlias)->FT_NFISCAL) .and.;
                   x[3] == alltrim((cAlias)->FT_SERIE  ) .and.;
                   x[4] == alltrim((cAlias)->FT_CLIEFOR) .and.;
                   x[5] == alltrim((cAlias)->FT_LOJA   ) .and.;
                   x[6] == alltrim((cAlias)->FT_ITEM   ) .and.;
                   x[7] == alltrim((cAlias)->FT_PRODUTO) }

if len(aRegD510) > 0
	nPosItem := aScan( aRegD510, bScanD510 )
	if nPosItem > 0

			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyComunicacao] := {}
			aadd(oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyComunicacao], JsonObject():New() )
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyComunicacao][1]['C38_CCLASS'] := aRegD510[nPosItem][08] //Id Classificacao do item
		    oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyComunicacao][1]['C38_CODPAR'] := aRegD510[nPosItem][09] //Id Participante que recebera a receita
			oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyComunicacao][1]['C38_INDREC'] := aRegD510[nPosItem][10] //Indide do tipo da receita

	endif
endif

return 


/*------------------------------------------------------------------------
{Protheus.doc} ComplItensDocServTransporte()
Gera o complemento dos itens de documento modelo 07(Serviço de transporte) 

(T015AI - SPED D120)
@author Carlos Eduardo Boy
@since 31/08/2023
//-----------------------------------------------------------------------*/
Static Function ComplItensDocServTransporte(cAlias as character, oDadosIntegra as json)

Local cCodMunTms            as character
Local cCodRemetente         as character
Local cCodUfOrigem          as character
Local cCodMunOrigem         as character
Local cIdUfOrigem           as character
Local cIdMunOrigem          as character
Local cCodDestino           as character
Local cCodUfDestino         as character
Local cCodMunDestino        as character
Local cIdUfDestino          as character
Local cIdMunDestino         as character
Local cIdVeiculo            as character
Local cNewId                as character
Local aOutputSp      := {}  as array
Local cRemDT6        := ""  as character
Local cLjRemDT6      := ""  as character
Local cDestDT6       := ""  as character
Local cLjDestDT6     := ""  as character
Local cCliOrFor      := 'SA1' as character

if (calias)->RECDT6 > 0

	cFilDT6    := ''
	cRemDT6    := (cAlias)->DT6_CLIREM
	cLjRemDT6  := (cAlias)->DT6_LOJREM
	cDestDT6   := (cAlias)->CLIDES
	cLjDestDT6 := (cAlias)->LOJDES

	//Remetente
	cCodRemetente := ComplementParticipantIntegration(cRemDT6, cLjRemDT6, cCliOrFor)
	
	//Retorno os códigos do municipio e uf das tabelas autocontidas do TAF
	cCodMunOrigem := GetIdMunServ(C1H->C1H_CODMUN, @cCodUfOrigem, .f.)
	//Busca municipio de origem conforme regras do TMS
	cCodMunTms    := TMSCodMun((cAlias)->COD_MUN_ORI, , , cCodMunOrigem, cCodUfOrigem )
	//Retorno os ids do municipio e uf das tabelas autocontidas do TAF

	//Destinatario

	cCodDestino := ComplementParticipantIntegration(cDestDT6, cLjDestDT6, cCliOrFor)
	//Retorno os códigos do municipio e uf das tabelas autocontidas do TAF
	cCodMunDestino := GetIdMunServ(C1H->C1H_CODMUN, @cCodUfDestino, .f.)
	//Busca municipio de origem conforme regras do TMS
	if cCodMunDestino == '99999' //Exterior
		cCodMunTms := TMSCodMun((cAlias)->COD_MUN_DEST, , , cCodMunDestino, cCodUfDestino)
	else
		cCodMunTms := TMSCodMun((cAlias)->DT6_CDRDES, , , cCodMunDestino, cCodUfDestino)
	endif		
	//Retorno os ids do municipio e uf das tabelas autocontidas do TAF
	cIdMunDestino  :=  GetIdMunServ(cCodMunTms, @cIdUfDestino)

	//Monto a chave para busca nas tabelas do TMS
	If val((cAlias)->DT6_DOCTMS) != 8
		cDT6FILDOC := (cAlias)->(DT6_FILDOC) 
		cDT6DOC    := (cAlias)->(DT6_DOC)
		cDT6SERIE  := (cAlias)->(DT6_SERIE)
	Else
		cDT6FILDOC := (cAlias)->(DT6_FILDCO) 
		cDT6DOC    := (cAlias)->(DT6_DOCDCO)
		cDT6SERIE  := (cAlias)->(DT6_SERDCO)
	EndIf			

	//Movimento de viagem
	if DUD->( MsSeek( xFilial('DUD') + cDT6FILDOC + cDT6DOC + cDT6SERIE + (cAlias)->DT6_FILORI ))
		//Viagem
		if !empty(DUD->DUD_VIAGEM) .and. DTQ->( MsSeek( xFilial('DTQ') + DUD->(DUD_FILORI+DUD_VIAGEM) ) )
			//Veiculo da viagem
			if DTR->( MsSeek( xFilial('DTR') + DTQ->(DTQ_FILORI+DTQ_VIAGEM) ) )
				//Veiculo cadastrado no TAF
				if C0Q->(MsSeek( xFilial('C0Q')+DTR->DTR_CODRB1)) .or. C0Q->(MsSeek( xFilial('C0Q')+DTR->DTR_CODVEI))
					cIdVeiculo := C0Q->C0Q_ID
				
				elseif !Empty(DTR->DTR_CODVEI) .or. !Empty(DTR->DTR_CODRB1)

					cProcedure 	:= "TAF613N"
					If !Empty(DTR->DTR_CODVEI)
						cIdVeiculo :=DTR->DTR_CODVEI        
					ElseIf  !Empty(DTR->DTR_CODRB1)
						cIdVeiculo :=DTR->DTR_CODRB1        	
					endif
					
					cNewId := GetSx8Num('C0Q','C0Q_ID',,4)
					
					aOutputSp := TCSPExec(cProcRealName, cFilAnt, cProcedure, "", "", "", 0, "", "", "", "", "", "", "", "", "", cIdVeiculo, Alltrim(cNewId), "")
			
					If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)		
					 	if __lSx8; RollbackSX8('C0Q'); endif
					 	aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C0Q'), '', .t. }
					 	ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "
					else
						if __lSx8; ConfirmSX8('C0Q'); endif
					EndIf
																
				endif		
			endif
		endif
	endif
		

	if !empty(cIdUfOrigem + cIdMunOrigem + cIdUfDestino + cIdMunDestino + cIdVeiculo)
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp] := {}
		aadd( oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp], JsonObject():New() )

		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp][1]['C39_UFORIG'] := cIdUfOrigem
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp][1]['C39_CMUNOR'] := cIdMunOrigem
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp][1]['C39_UFDEST'] := cIdUfDestino
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp][1]['C39_CMUNDE'] := cIdMunDestino
		oDadosIntegra[cKeyJson][nPosDocFiscal][cKeyItens][nItem][cKeyServicoTransp][1]['C39_CODVEI'] := cIdVeiculo
	endif	

endif

return 

//---------------------------------------------------------------------
/*/{Protheus.doc} ErrorHandler
@type			static function
@description	Realiza o tratamento dos erros
@author			Melkz Siqueira
@since			12/06/2023
@version		1.0
@return			Nil
/*/
//---------------------------------------------------------------------
Static Function ErrorHandler(cError as character)

	Default cError := ""

	oError:Description := cError

	THROW oError

Return

/*----------------------------------------------------------------------
{Protheus.doc} LogIntegra
Programa principal para consulta dos dados na base do ERP
@author Carlos Eduardo Nonato
@Prametros: aDadosLog
	aDadosLog[01] - alias da tabela do TAF
	aDadosLog[02] - Chave do registro da tabela
	aDadosLog[03] - Nome da tabela que vai compor a mensagem de erro
	aDadosLog[04] - Descrição detalhada do erro
	aDadosLog[05] - Se é cadastro ou não (.t. ou .f.)
	aDadosLog[06] - Alias da tabela de origem (ERP)

@since 06/06/2023
//----------------------------------------------------------------------*/
function LogIntegra( aDadosLog, cKeyErp )

Local cQuery    as character
Local cAliasQry as character
Local cAliasERP as character
Local lNewT01    := .t. as logical
Local lCadastro  := aDadosLog[05] as logical
Local cFilialTaf := xFilial( aDadosLog[1] ) as character
Local cFilialT01 := xFilial( 'T01' )
Local cMsgErro 	:= 'Não foi possivel integrar o documento fiscal ' as character

if !empty(aDadosLog[3])
	cMsgErro += 'por problema no '
	if lCadastro
		cMsgErro += 'cadastro automático do '
	endif
	cMsgErro += ' complemento: '
endif

if len(aDadosLog) >= 6 
	cAliasERP := iif(aDadosLog[6] != nil, aDadosLog[6], ' ' )
endif	
			
if oStPesquisaLog == nil
	cQuery := " SELECT "
	cQuery += "     R_E_C_N_O_ RECNO"
	cQuery += " FROM " + RetSqlName('T01')
	cQuery += " WHERE D_E_L_E_T_ = ' ' "
	cQuery += " 	AND T01_FILIAL = ? " //1
	cQuery += " 	AND T01_CODFIL = ? " //2
	cQuery += " 	AND T01_ALIAS  = ? " //3
	cQuery += " 	AND T01_REGKEY = ? " //4

	oStPesquisaLog := FWPreparedStatement():New()
	oStPesquisaLog:SetQuery(cQuery)
endif

//Passa os parametros para o objeto da query
oStPesquisaLog:SetString( 1, cFilialT01	  )
oStPesquisaLog:SetString( 2, cFilialTaf   )
oStPesquisaLog:SetString( 3, aDadosLog[1] )
oStPesquisaLog:SetString( 4, aDadosLog[2] )

cQuery 	  := oStPesquisaLog:GetFixQuery()
cAliasQry := GetNextAlias()
DBUseArea(.t.,'TOPCONN', TCGenQry(,,cQuery), cAliasQry , .f., .t. )

if (cAliasQry)->(!eof())
	//Se encontrou o registro na T01, posiciono a tabela para alteração
	T01->(DbGoTo( (cAliasQry)->RECNO ))
    lNewT01 := .f.
endif
(cAliasQry)->(DbCloseArea())

RecLock( 'T01', lNewT01  )
if lNewT01 // Caso seja inclusão
	T01->T01_FILIAL := cFilialT01 // Compartilhamento da tabela T01
	T01->T01_CODFIL := cFilialTaf // Filial da tabela passada por parâmetro
	T01->T01_ALIAS  := aDadosLog[1]
	T01->T01_REGKEY := aDadosLog[2]
endif

// Campos abaixo sempre serão atualizados
T01->T01_MSGERR := cMsgErro + aDadosLog[3] + CRLF + aDadosLog[4]
T01->T01_DATA   := dDatabase
T01->T01_HORA   := Time()
T01->T01_ALSERP	:= cAliasERP
T01->T01_ERPKEY := rtrim(cKeyErp)
T01->( MSUnlock() )

Return Nil

//---------------------------------------------------------------------
/*/{Protheus.doc} GetInnerJoinC03
@type			static function
@description	Retorna o INNER JOIN da tabela v14 - Intermediaria da integraçção de participantes.
@author			Carlos Eduardo Boy
@since			05/04/2024
@version		1.0
@param			cQuery - Retorna o trecho do INNER JOIN a ser concatenado na query 
/*/
//---------------------------------------------------------------------
Static Function GetInnerJoinV14(cQuery as character)

Default cQuery := ''

cQuery += " INNER JOIN " + RetSqlname('V14') + " V14 ON "
cQuery += " 	V14.V14_FILIAL = '" + xFilial('V14') + "' AND "
cQuery += " 	V14.V14_ORIG = (CASE "
cQuery += " 						WHEN SFT.FT_TIPOMOV = 'E' AND SFT.FT_TIPO NOT IN ('B', 'D') THEN 'SA2' "
cQuery += " 						WHEN SFT.FT_TIPOMOV = 'S' AND SFT.FT_TIPO IN ('B', 'D') THEN 'SA2' "
cQuery += " 						ELSE 'SA1' "
cQuery += " 					END) AND "
cQuery += "		V14.V14_CNPJ 	= coalesce(SA1.A1_CGC,SA2.A2_CGC) AND "
cQuery += "		V14.V14_IE 		= REPLACE(REPLACE(REPLACE( coalesce(SA1.A1_INSCR, SA2.A2_INSCR) , '-', ''),'.', ''),'/', '') AND "
cQuery += "		V14.V14_INSCMU 	= REPLACE(REPLACE(REPLACE( coalesce(SA1.A1_INSCRM, SA2.A2_INSCRM) , '-', ''),'.', ''),'/', '') AND "
cQuery += "		V14.V14_CODMUN 	= coalesce(SA1.A1_COD_MUN, SA2.A2_COD_MUN) AND "
cQuery += "		V14.V14_CEP 	= coalesce(SA1.A1_CEP, SA2.A2_CEP) AND "
cQuery += "		V14.V14_UF 		= coalesce(SA1.A1_EST, SA2.A2_EST) AND "
cQuery += " 	V14.D_E_L_E_T_ = ' ' "

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} IntegraParticipanteComplemento
@type			static function
@description	Função responsável por verificar se o participante do 
				complemento existe no TAF 
				caso não existá será integrado ao taf
@author			Evandro
@since			06/05/2024
@version		1.0
@param			cFilial - Filial do participante
				cCliFor - Código do participante informado nas tabelas dos complementos
				cCliForLoja - Loja do participante 
				cCliFor   - Informa a tabela a qual o query fala a busca.
/*/
//---------------------------------------------------------------------

Static Function ComplementParticipantIntegration(cCodPart as character, cLojaPar as character, cCliFor as character, cOrigemLog as character) as character
Local aGetArea 		:= (cCliFor)->(GetArea()) as array
Local cProcedure  	:= 'TAF613T' as character
Local cFilialV14   	:= xFilial("V14") as character
Local oJsonFields	:= JsonObject():New() as json
Local aOutputSp   	:= {} as array
Local nTamCnpj		:= GetSx3Cache('V14_CNPJ'	, 'X3_TAMANHO') as integer
Local nTamIe		:= GetSx3Cache('V14_IE'   	, 'X3_TAMANHO') as integer
Local nTamIm		:= GetSx3Cache('V14_INSCMU'	, 'X3_TAMANHO') as integer
Local nTamCm		:= GetSx3Cache('V14_CODMUN'	, 'X3_TAMANHO') as integer
Local nTamCEP		:= GetSx3Cache('V14_CEP'	, 'X3_TAMANHO') as integer
Local nTamUf		:= GetSx3Cache('V14_UF'		, 'X3_TAMANHO') as integer
Local cRetIdC1H  as character
Local cChavePesq as character
Local cPartField as character
Local cNewCodPar as character
Local cMsgLog	 as character

Default cLojaPar := ' '
Default cOrigemLog := ' '

//Posiciona no participante passado por parâmetro
(cCliFor)->(DbSetOrder(1))
if (cCliFor)->(DbSeek(xFilial(cCliFor)+cCodPart+cLojaPar))
	
	cPartField := right(cCliFor,2)
	cFieldIE   := iif(cCliFor $ 'SA1|SA2', '_INSCR', '_INSEST')

	oJsonFields['CNPJ'	] := cPartField + '_CGC'
	oJsonFields['IE'	] := cPartField + cFieldIE
	oJsonFields['IM'	] := cPartField + '_INSCRM'
	oJsonFields['CODMUN'] := cPartField + '_COD_MUN'
	oJsonFields['CEP'	] := cPartField + '_CEP'
	oJsonFields['UF'	] := cPartField + '_EST'

	//Monta chave de consulta na V14
	cChavePesq := padr((cCliFor)->&(oJsonFields['CNPJ'  ]), nTamCnpj)+; 
				  padr((cCliFor)->&(oJsonFields['IE'    ]), nTamIe	)+;
				  padr((cCliFor)->&(oJsonFields['IM'    ]), nTamIm	)+;
				  padr((cCliFor)->&(oJsonFields['CODMUN']), nTamCm	)+;
				  padr((cCliFor)->&(oJsonFields['CEP'	]), nTamCEP	)+;
				  padr((cCliFor)->&(oJsonFields['UF'	]), nTamUf	)

	//Pesquisa chave na tabela intermediária para ver se o participante já foi integrado
	if V14->(MsSeek( cFilialV14 + cChavePesq + cCliFor ))
		cRetIdC1H := V14->V14_IDC1H	
	endif
	
	//Se o participante ainda não foi integrado
	if empty(cRetIdC1H)
		//Se algum participante de origem diferente já foi cadastrado com a mesma chave de consulta anteriormente, usa o mesmo id e código
		if V14->(MsSeek( cFilialV14 + cChavePesq))
			cRetIdC1H := V14->V14_IDC1H
			cNewCodPar:= V14->V14_CODPAR
		else
			cRetIdC1H := FWUUIDv4(.T.)
			cNewCodPar:= RIGHT(GetSX8Num("C1H","C1H_CODPAR"),10)		
		endif
		aOutputSp := TCSPExec( cProcRealName						,; //01 - Grupo de procedures
							   cFilialV14							,; //02 - Filial que ser executada
							   cProcedure							,; //03 - Procedure que será executada
							   (cCliFor)->&(oJsonFields['CODMUN']),; //04 - Código do municipio
							   ''									,; //05 - Não utilizado
							   ''	 								,; //06 - Não utilizado
							   0									,; //07 - Não utilizado
							   (cCliFor)->&(oJsonFields['CNPJ'])	,; //08 - CNPJ 
							   (cCliFor)->&(oJsonFields['IE'])	,; //09 - Inscrição estadual 
							   (cCliFor)->&(oJsonFields['IM'])	,; //10 - Inscrição municipal  
							   (cCliFor)->&(oJsonFields['CEP'])	,; //11 - Cep		
							   cRetIdC1H							,; //12 - Id do participante 
							   cNewCodPar							,; //13 - Código do participante 
							   cCliFor							,; //14 - Origem (SA1, SA2 ou SA4)
							   ''									,; //15 - Nao utilizado
							   ''									,; //16 - Nao utilizado
							   ''									,; //17 - Nao utilizado
							   ''									,; //18 - Nao utilizado
							   (cCliFor)->&(oJsonFields['UF']) )	   //19 - Uf do participante
	
		//Se não conseguiu criar o participante no TAF
		If Empty(aOutputSp) .Or. Empty(aOutputSp[1]) .Or. (aOutputSp[1] == PROCEDURE_STATUS_ERROR)
			if __lSx8; RollBackSX8('C1H'); endif
			cMsgLog := STR0007 + cProcedure + STR0008 + alltrim(FwX2Nome(cOrigemLog)) + CRLF + STR0009 + TCSQLError() //#'Procedure : ' #' - Origem : Complemento ' #'Erro: '
			aDadosLog := {'C20', (cAlias)->(C20_CHVNF), FwX2Nome('C1H'), cMsgLog, .t., 'SFT' }
			ErrorHandler(STR0001 + cProcedure + CRLF + TCSQLError()) // "Falha na integração do complemento. Procedure: "			
		else
			//Se criou o participante TAF
			if __lSx8; ConfirmSX8('C1H'); endif		
			AtuPartic()		
		endif

	endif

	//Posiciona a c1h no participante passado por parâmetro
	C1H->(MsSeek( xFilial('C1H') + cRetIdC1H ))

endif

(cCliFor)->(RestArea(aGetArea))

Return cRetIdC1H

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} AjustesIpiE531
@type			static function
@description	informações adicionais dos ajustes da apuração do IPI - Reg: E531
@author			Carlos Eduardo Boy
@since			14/05/2024
@version		1.0
@param			
/*/
//-------------------------------------------------------------------------------------
Static Function InfAjustesIPI(cAlias as character, oDadosComplemento as json, aAjusteIPI as array)
Local i as integer

//Cria a chave de ajustes no Json de complementos 
oDadosComplemento[cKeyInfEspecificas][cKeyInfAjustesIPI] := {}

for i := 1 to Len(aAjusteIPI)
	aadd( oDadosComplemento[cKeyInfEspecificas][cKeyInfAjustesIPI], JsonObject():New() )
	
	oDadosComplemento[cKeyInfEspecificas][cKeyInfAjustesIPI][i]['CWW_NUMITE'] := aAjusteIPI[i][1]
	oDadosComplemento[cKeyInfEspecificas][cKeyInfAjustesIPI][i]['CWW_VALOR' ] := aAjusteIPI[i][2]
	oDadosComplemento[cKeyInfEspecificas][cKeyInfAjustesIPI][i]['CWW_CODAJU'] := aAjusteIPI[i][3]
next

return

/*----------------------------------------------------------------------
{Protheus.doc} RetornaCodUF
Retorna o código da UF do destinatário
@author Gabriela Luche
@Prametros: cAlias 		- Alias do documento fiscal
			cUfEst 		- Estado do destinatário da mercadoria
			cMVEstado 	- Estado da empresa matriz conforme definido no MV_ESTADO
@since 06/06/2023
//----------------------------------------------------------------------*/
Static Function RetornaCodUF(cAlias as character, cUfEst as character, cMVEstado as character)
	Local aAreaC09	:= {}  as array 
	Local cCodUF	:= ""  as character
	Local cNotaUF	:= ""  as character

	If lIntTMS .And. (cAlias)->FT_TIPOMOV == "S"
		cNotaUF := LocEst(cAlias, cMVEstado)
	Else
		cNotaUF := cUfEst
	EndIf
	//--Localiza o código da UF na tabela C09 a partir da sigla da UF
	If !Empty(cNotaUF)
		aAreaC09 := C09->( GetArea() )
		C09->(DbSetOrder(1))
		if C09->(MsSeek( xFilial('C09')+alltrim(cNotaUF)))
			cCodUF := C09->C09_ID
		endif	
		RestArea(aAreaC09)
	Endif

Return cCodUF

/*-------------------------------------------------------------------------------------
CleanLog
@type			static function
@description	Limpa o log conforme dados passados por parâmetro
@author			Carlos Eduardo Boy
@since			04/02/2025
@version		1.0
@param			
-------------------------------------------------------------------------------------*/
Function TafCleanLog(cFilialT01, cFilialTab, cTabLog, cIdTabLog )

if T01->(DbSeek( cFilialT01 + cFilialTab + cTabLog + cIdTabLog ))
	T01->(RecLock('T01',.f.))
	T01->(DbDelete())
	T01->(DbUnlock())
endif			

return
