#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "protheus.ch"
#INCLUDE "wstaf067.ch"

#DEFINE CONTENT_TYPE        "application/json" 
#DEFINE HEADER_CONTENT_TYPE "Content-Type"
#DEFINE CodeRespBadRequest 400

/*{Protheus.doc} TAFGetIntegrationReinfGov
@type			function
@description	Api para retorno dos dados cadastrados para consulta automática.
@author			Carlos Pister
@since			16/10/2024
@version		1.0
@return			Nil 
*/
@GET(endpoint="/api/taf/fiscal/v1/AutomaticIntegrationReinfGov", description="Realiza a consulta do status e do cadastro realizado")
Function TAFGetIntegrationReinfGov()

	Local jResponse         := Nil as json
    Local aCompany          := {}  as array
    Local cEmpRequest       := ''  as character
    Local cFilRequest       := ''  as character
    Local cMsg              := ''  as character
    Local lAutomato         := .F. as logical
    Local cAutoADVPR        := ''  as character
    Local lExitT8M          := .F. as logical

    lExitT8M := AliasInDic("T8M") 
    cAutoADVPR := oRest:getHeaderRequest()["Content-Advpr"]

    If valtype( cAutoADVPR ) == 'C' .And. !Empty(cAutoADVPR) .And. "WSTAF067" $ Upper(cAutoADVPR)
		lAutomato := .T.
        If Upper(cAutoADVPR) == "WSTAF06723"
            lExitT8M := .F.
        EndIf
	EndIf

    jResponse := JSONObject():New()

    If oRest:getQueryRequest()['companyId'] == Nil
        jResponse['code'] := CodeRespBadRequest
        jResponse['message'] := STR0001 //Empresa|Filial não informado no parâmetro 'companyId'.
        oRest:setStatusCode(CodeRespBadRequest)
    Else
        aCompany := StrTokArr(oRest:getQueryRequest()['companyId'], "|")

        If Len(aCompany) < 2
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0002//Tamanho Empresa|Filial informado no parâmetro 'companyId' menor que o esperado.
            oRest:setStatusCode(CodeRespBadRequest)
		Else
			cEmpRequest := aCompany[1]
			cFilRequest := PADR(alltrim(aCompany[2]),FWSizeFilial())

			If PrepEnv(cEmpRequest, cFilRequest)

                If lExitT8M 

                    cMsg := ValidAutomaticIntegration(lAutomato)

                    If !Empty(cMsg)
                        jResponse['configurated']    := STR0004//Não configurado.
                        jResponse['description']     := cMsg
                        jResponse['dayFrom']         := 0
                        jResponse['dayTo']           := 0
                        jResponse['interval']        := ''
                    Else 
                        DBSelectArea("T8M")
                        T8M->(DBSetOrder(1))
                        If T8M->(DbSeek(cFilRequest))
                            //Caso tenha sido realizado cadastro porém o serviço não estiver habilitado, retorno que não esta configurado
                            jResponse['configurated']    := STR0003//Configurado.
                            jResponse['dayFrom']         := AllTrim(T8M->T8M_DIAINI)
                            jResponse['dayTo']           := AllTrim(T8M->T8M_DIAFIM)
                            jResponse['interval']        := T8M->T8M_RECORR
                            jResponse['description']     := ''
                        Else
                            jResponse['configurated']    := STR0004//Não configurado.
                            jResponse['dayFrom']         := 0
                            jResponse['dayTo']           := 0
                            jResponse['interval']        := ''
                            jResponse['description']     := ''
                        EndIf
                    EndIf
                Else
                    jResponse['configurated']    := STR0004//Não configurado.
                    jResponse['description']     := STR0005//Dicionário não atualizado. Atualizar com a última expedição contínua disponível.
                    jResponse['dayFrom']         := 0
                    jResponse['dayTo']           := 0
                    jResponse['interval']        := ''
                Endif
            Else
                jResponse['code'] := CodeRespBadRequest
                jResponse['message'] := STR0006 + cEmpRequest + STR0007 + cFilRequest //Falha na preparação do ambiente para a Empresa / e Filial
                oRest:setStatusCode(CodeRespBadRequest)
            EndIf
        EndIf
    EndIf

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetResponse(jResponse)

	FWFreeObj(jResponse)

Return Nil

/*{Protheus.doc} TAFSaveIntegrationReinfGov
@type			function
@description	Api para gravar os dados de cadastrado para consulta automática.
@author			Carlos Pister
@since			16/10/2024
@version		1.0
@return			Nil 
*/
@POST(endpoint="/api/taf/fiscal/v1/saveAutomaticIntegrationReinfGov", description="Realiza a gravação")
Function TAFSaveIntegrationReinfGov()

	Local JsBody        := Nil as json
	Local jResponse     := Nil as json
    Local lOK           := .T. as logical
    Local lGrava        := .T. as logical
    Local aCompany      := {}  as array
    Local cEmpRequest   := ''  as character
    Local cFilRequest   := ''  as character
    Local cMsg          := ''  as character
    Local lRet          := .T. as logical
    Local lAutomato     := .F. as logical
    Local cAutoADVPR    := ''  as character
    Local lExitT8M          := .F. as logical

    lExitT8M := AliasInDic("T8M") 
    cAutoADVPR := oRest:getHeaderRequest()["Content-Advpr"]

    If valtype( cAutoADVPR ) == 'C' .And. !Empty(cAutoADVPR) .And. "WSTAF067" $ Upper(cAutoADVPR)
		lAutomato := .T.
        If Upper(cAutoADVPR) == "WSTAF06724"
            lExitT8M := .F.
        EndIf
	EndIf

    JsBody := JSONObject():New()
    JsBody:fromJson( oRest:GetBodyRequest())

    jResponse := JSONObject():New()

    If Empty( oRest:GetBodyRequest() )
        jResponse['code'] := CodeRespBadRequest
        jResponse['message'] := STR0008 //Corpo da requisição não enviado.
        oRest:setStatusCode(CodeRespBadRequest)
    Else
        If JsBody['companyId'] == Nil
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0001 //Empresa|Filial não informado no parâmetro 'companyId'.
            oRest:setStatusCode(CodeRespBadRequest)
        ElseIf JsBody['dayFrom'] == Nil
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0009 //Dia De não informado no parâmetro 'dayFrom'.
            oRest:setStatusCode(CodeRespBadRequest)
        ElseIf JsBody['dayTo'] == Nil
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0010 //Dia Até não informado no parâmetro 'dayTo'.
            oRest:setStatusCode(CodeRespBadRequest)
        ElseIf JsBody['intervalMinutes'] == Nil
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0011 //Intervalo não informado no parâmetro 'intervalMinutes'.
            oRest:setStatusCode(CodeRespBadRequest)
        Else
            aCompany := StrTokArr(JsBody['companyId'], "|")

            If Len(aCompany) < 2
                jResponse['code'] := CodeRespBadRequest
                jResponse['message'] := STR0002 //Tamanho Empresa|Filial informado no parâmetro 'companyId' menor que o esperado.
                oRest:setStatusCode(CodeRespBadRequest)
	    	Else
	    		cEmpRequest := aCompany[1]
	    		cFilRequest := PADR(alltrim(aCompany[2]),FWSizeFilial())

	    		If PrepEnv(cEmpRequest, cFilRequest)
                    If lExitT8M

                        cMsg := ValidAutomaticIntegration(lAutomato)

                        If !Empty(cMsg)
                            lRet := .F.
                        Else
                            DBSelectArea("T8M")
                            T8M->(DBSetOrder(1))
                            If T8M->(DbSeek(cFilRequest))
                                //Verifico se já existe registro com os dados abaixo, caso não exista atualizo
                                If !(AllTrim(T8M->T8M_DIAINI) == cValToChar(JsBody['dayFrom']) .and. AllTrim(T8M->T8M_DIAFIM) == cValToChar(JsBody['dayTo']) .and. T8M->T8M_RECORR == cValToChar(JsBody['intervalMinutes']) .and. T8M->T8M_USUARI == __cUserID)
                                    lOK := .F.
                                Else
                                    lGrava := .F.  
                                    cMsg := STR0012 //Já existe um cadastro com os dados informados.          
                                EndIf                 
                            EndIf 

                            If lGrava
                                RecLock("T8M", lOK)
                                T8M->T8M_FILIAL := cFilRequest
                                T8M->T8M_USUARI := __cUserID
                                T8M->T8M_DIAINI := cValToChar(JsBody['dayFrom'])
                                T8M->T8M_DIAFIM := cValToChar(JsBody['dayTo'])
                                T8M->T8M_RECORR := cValToChar(JsBody['intervalMinutes'])                            
                                T8M->(MsUnlock()) 
                            
                                //Cria o agendamento para o smartSchedule
                                If GrvSchT8M(cFilRequest)
                                    cMsg := STR0013 //Gravação realizada com sucesso.
                                Else
                                    cMsg := STR0018 //"Erro ao gravar o agendamento"
                                EndIf
                            EndIf                    
                        EndIf
                        jResponse['confirmOperation'] := lRet
                        jResponse['messageOperation'] := cMsg
                    Else
                        lRet := .F.
                        jResponse['confirmOperation'] := lRet
                        jResponse['messageOperation'] := STR0005 //Dicionário não atualizado. Atualizar com a última expedição contínua disponível.
                    EndIf  
                Else
                    jResponse['code'] := CodeRespBadRequest
                    jResponse['message'] := STR0006 + cEmpRequest + STR0007 + cFilRequest //Falha na preparação do ambiente para a Empresa / e Filial 
                    oRest:setStatusCode(CodeRespBadRequest)
                EndIf
            EndIf
        EndIf
    EndIf

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetResponse(jResponse)

	FWFreeObj(jResponse)

Return Nil

/*{Protheus.doc} TAFDeleteIntegrationReinfGov
@type			function
@description	Api para excluir os dados de cadastrado para consulta automática.
@author			Carlos Pister
@since			16/10/2024
@version		1.0
@return			Nil 
*/
@DELETE(endpoint="/api/taf/fiscal/v1/deleteAutomaticIntegrationReinfGov", description="Realiza a exclusão")
Function TAFDeleteIntegrationReinfGov()

    Local jResponse         := Nil as json
    Local aCompany          := {}  as array
    Local cEmpRequest       := ''  as character
    Local cFilRequest       := ''  as character
    Local lAutomato         := .F. as logical
    Local cAutoADVPR        := ''  as character

    cAutoADVPR := oRest:getHeaderRequest()["Content-Advpr"]

    If valtype( cAutoADVPR ) == 'C' .And. !Empty(cAutoADVPR) .And. "WSTAF06726" $ Upper(cAutoADVPR)
		lAutomato := .T.
	EndIf
    
    jResponse := JSONObject():New()

    If oRest:getQueryRequest()['companyId'] == Nil
        jResponse['code'] := CodeRespBadRequest
        jResponse['message'] := STR0001//Empresa|Filial não informado no parâmetro 'companyId'.
        oRest:setStatusCode(CodeRespBadRequest)
    Else
        aCompany := StrTokArr(oRest:getQueryRequest()['companyId'], "|")

        If Len(aCompany) < 2
            jResponse['code'] := CodeRespBadRequest
            jResponse['message'] := STR0002//Tamanho Empresa|Filial informado no parâmetro 'companyId' menor que o esperado.
            oRest:setStatusCode(CodeRespBadRequest)
		Else
			cEmpRequest := aCompany[1]
			cFilRequest := PADR(alltrim(aCompany[2]),FWSizeFilial())

			If PrepEnv(cEmpRequest, cFilRequest)                
                DBSelectArea("T8M")
                T8M->(DBSetOrder(1))
                If T8M->(DbSeek(cFilRequest))
                    //Deleta o agendamento
                    If DelSchT8M(cFilRequest, lAutomato)
                        RecLock("T8M",.F.)
                            T8M->( DBDelete() )
                        MsUnLock()
                        jResponse['confirmOperation'] := .T.
                        jResponse['messageOperation'] := STR0014 // Exclusão realizada com sucesso.
                    Else
                        jResponse['confirmOperation'] := .F.
                        jResponse['messageOperation'] := STR0019//'Falha ao realizar a exclusão'
                    EndIf
                Else          
                    jResponse['confirmOperation'] := .F.
                    jResponse['messageOperation'] := STR0015// Registro não localizado.
                EndIf           
            Else
                jResponse['code'] := CodeRespBadRequest
                jResponse['message'] := STR0006 + cEmpRequest + STR0007 + cFilRequest //Falha na preparação do ambiente para a Empresa/e Filial
                oRest:setStatusCode(CodeRespBadRequest)
            EndIf
        EndIf
    EndIf

	oRest:SetKeyHeaderResponse(HEADER_CONTENT_TYPE, CONTENT_TYPE)
	oRest:SetResponse(jResponse)

	FWFreeObj(jResponse)

Return Nil

/*{Protheus.doc} ValidAutomaticIntegration
@type			function
@description	Valida se serviço, função, binário e dicionário existem na base de dados.
@author			Carlos Pister
@since			16/10/2024
@version		1.0
@return			Nil 
*/
Function ValidAutomaticIntegration(lAutomato)

	Local lUtilCreat as logical
    Local lEnableSS  as logical
    Local lRunningSS as logical
    Local cMsg       as character
    Local cEnv       as character

    Default lAutomato := .F.

    lUtilCreat := .T.
    lEnableSS  := .T.
    lRunningSS := .T.
    cMsg       := ""
    cEnv       := TafSchdRun()
	
    If Empty(cEnv)
        lUtilCreat := (FindClass("totvs.framework.schedule.utils.createTask") .Or. FindFunction('totvs.framework.schedule.utils.createTask')) //INSERIR A FUNÇÃO AUTOMATIC
        If !lUtilCreat .or. lAutomato
            cMsg += STR0016// O Repositório de funções está desatualizado.
            cMsg += Chr(13) + Chr(10)
        EndIf
        
        lEnableSS := (FindClass("totvs.framework.smartschedule.startSchedule.smartSchedIsEnabled") .Or. FindFunction('totvs.framework.smartschedule.startSchedule.smartSchedIsEnabled')) .And. totvs.framework.smartschedule.startSchedule.smartSchedIsEnabled()
        lRunningSS := (FindClass("totvs.framework.smartschedule.startSchedule.smartSchedIsRunning") .Or. FindFunction('totvs.framework.smartschedule.startSchedule.smartSchedIsRunning')) .And. totvs.framework.smartschedule.startSchedule.smartSchedIsRunning()
        If !lEnableSS .or. !lRunningSS .or. lAutomato
            cMsg += STR0017// Realizar a configuração de Smart Schedule para habilitar a execução desta rotina.
            cMsg += Chr(13) + Chr(10)
        EndIf
    Endif

    If lAutomato
        cMsg := ""
    EndIf

Return cMsg
