#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TopConn.ch"
#Include "TbiConn.CH"
#Include "Tlpp-Core.th"
#Include "Tlpp-Rest.th"
#Include "rh.taf.ws.fgtsPerWorkerInLaborProcess.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} Totalizador5503
    API do Evento Totalizador S-5503
    @author rodrigo.nicolino/lucas.passos
    @since 21/11/2024
/*/
//---------------------------------------------------------------------
Class Totalizador5503

    public Method New() as object
   
    //Browser
    @Get("/api/rh/esocial/v1/fgtsPerWorkerInLaborProcess")
    public Method fgtsPerWorkerInLaborProcess() 

    @Delete("/api/rh/esocial/v1/fgtsPerWorkerInLaborProcess")
    public Method deleteWorkerInLaborProcess()

    @Get("/api/rh/esocial/v1/fgtsPerWorkerInLaborProcess/xmlFGTSInLaborProcess")
    public Method xmlFGTSPerWorkerInLaborProcess() 

    @Get("/api/rh/esocial/v1/fgtsPerWorkerInLaborProcess/worker/registrationAndCategory")
    public Method registrationAndCategory() 

    @Get("/api/rh/esocial/v1/fgtsPerWorkerInLaborProcess/worker/paymentAndAmounts")
    public Method paymentAndAmounts() 

EndClass

Method New() as object Class Totalizador5503; Return 

//---------------------------------------------------------------------
/*/{Protheus.doc} fgtsPerWorkerInLaborProcess
    Método que carrega registros do evento S-5503, filtrados ou não, 
    ordenados ou não.
    @author  rodrigo.nicolino/lucas.passos
    @since   21/11/2024
    @version 1.0
/*/
//---------------------------------------------------------------------
Method fgtsPerWorkerInLaborProcess() Class Totalizador5503

    Local aCompany    as Array
    Local cAliasProd  as Character
    Local cCompany    as Character
    Local cCompanyId  as Character
    Local cCPF        as Character
    Local cEmpRequest as Character
    Local cFilRequest as Character
    Local cId         as Character
    Local cNome       as Character
    Local cNrProcess  as Character
    Local cPeriod     as Character
    Local cQry        as Character
    Local cSearch     as Character
    Local jQuery      as Json
    Local lHasNext    as Logical
    Local nPage       as Numeric
    Local nPageSize   as Numeric
    Local nRegFim     as Numeric
    Local nRegIni     as Numeric
    Local nTotReg     as Numeric
    Local nX          as Numeric
    Local oError      as Object
    Local oJson       as Object
    Local oJsonAux    as Object

    aCompany    := {}
    cCompany    := ""
    cEmpRequest := ""
    cFilRequest := ""
    jQuery      := oRest:getQueryRequest()
    lHasNext    := .F.
    nTotReg     := 0
    oError      := JSonObject():New()
    oJson       := JsonObject():New()
    oJsonAux    := Nil

    If TAFColumnPos("T8N_NOME")

        If jQuery["companyId"] == Nil

            oRest:setStatusCode(400)
            cRet := STR0001
            oRest:SetResponse(cRet)

        Else

            cCompany    := Iif( Len( StrTokArr( jQuery[ 'companyId' ], ";" ) ) > 1, StrTokArr( jQuery[ 'companyId' ], ";" )[2], "" )

            If !Empty(cCompany)
                aCompany    := StrTokArr( StrTokArr( jQuery[ 'companyId' ], ";" )[1], "|" )
                nPos        := 2
            Else
                aCompany    := StrTokArr( jQuery[ 'companyId' ], "|" )
                nPos        := 3
            EndIf

            aReq        := jQuery:Getnames()

            If Len( aCompany ) < 2

                oRest:setStatusCode(400)
                cRet := STR0001
                oRest:SetResponse(cRet)

            Else

                cEmpRequest := aCompany[1]
                cFilRequest := aCompany[2]

                PrepEnv( cEmpRequest, cFilRequest )

                oJson["items"] := {}
                cCompanyId     := IIF(jQuery["companyId"]       <> Nil, jQuery["companyId"]     , "")
                cNrProcess     := IIF(jQuery["processNumber"]   <> Nil, jQuery["processNumber"] , "")
                cCPF           := IIF(jQuery["cpf"]             <> Nil, jQuery["cpf"]           , "")
                cNome          := IIF(jQuery["name"]            <> Nil, jQuery["name"]          , "")
                cPeriod        := IIF(jQuery["period"]          <> Nil, jQuery["period"]        , "")
                cSearch        := IIF(jQuery["search"]          <> Nil, jQuery["search"]        , "")
                cId            := IIF(jQuery["id"]              <> Nil, jQuery["id"]            , "")
                nPage          := IIF(jQuery["page"]            <> Nil, Val(jQuery["page"])     , 1 )
                nPageSize      := IIF(jQuery["pageSize"]        <> Nil, Val(jQuery["pageSize"]) , 15)

                nRegIni := ( ( nPage - 1 ) * nPageSize ) + 1    
                nRegFim := nPage * nPageSize

                cQry += " SELECT * FROM ( "
                cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY T8N.R_E_C_N_O_ ) LINE_NUMBER, "
                cQry += "   T8N.T8N_FILIAL AS FILIAL, "
                cQry += "   T8N.R_E_C_N_O_ AS RECNO, "
                cQry += "   T8N.T8N_NRPROC AS NRPROC, "
                cQry += "   T8N.T8N_CPF AS CPF, "
                cQry += "   T8N.T8N_PERAPU AS PERAPU, "
                cQry += "   T8N.T8N_NOME AS NOME, "
                cQry += "   T8N.T8N_PROTUL AS PROTUL, "
                cQry += "   (SELECT COUNT(T8NA.R_E_C_N_O_ ) "
                cQry += "   FROM " + RetSqlName("T8N") + " T8NA WHERE T8NA.D_E_L_E_T_ = '') AS TOTAL_LINE "
                cQry += "   FROM " + RetSqlName("T8N") + " T8N "
                cQry += "   WHERE T8N.D_E_L_E_T_ = '' "

                If !Empty(cCompany)
                    cQry += "   AND T8N.T8N_FILIAL = ? "   
                EndIf
                
                If !Empty(cNome) .Or. !Empty(cSearch)
                    cQry += "   AND UPPER(T8N.T8N_NOME) LIKE '%'+upper(?)+'%' "
                EndIf

                If !Empty(cCPF)
                    cQry += "   AND T8N.T8N_CPF = ? "
                EndIf

                If !Empty(cPeriod)
                    cQry += "   AND T8N.T8N_PERAPU = ? "
                EndIf

                If !Empty(cNrProcess)
                    cQry += "   AND T8N.T8N_NRPROC = ? "   
                EndIf

                cQry += "	) TAB  "
                cQry += "        WHERE LINE_NUMBER BETWEEN "+ cValTochar(nRegIni) + " AND " + cValTochar(nRegFim) + " "

                oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

                For nX := 1 To Len(aReq)

                    If !Empty(cCompany) .And. aReq[nX] == "companyId"
                        oStatement:setString( nX, cCompany )
                    EndIf   

                    If ( !Empty(cNome) .And. aReq[nX] == "name" ) .Or. ( !Empty(cSearch) .And. aReq[nX] == "search" )
                        If !Empty(cNome)
                            oStatement:setString( nX - nPos, '%' + cNome + '%' )
                        ElseIf !Empty(cSearch)
                            oStatement:setString( nX - nPos, '%' + cSearch + '%' )
                        EndIf
                    EndIf 

                    If !Empty(cCPF) .And. aReq[nX] == "cpf"
                        oStatement:setString( nX - nPos, cCPF )
                    EndIf

                    If !Empty(cPeriod) .And. aReq[nX] == "period"
                        oStatement:setString( nX - nPos, cPeriod )
                    EndIf 

                    If !Empty(cNrProcess) .And. aReq[nX] == "processNumber"
                        oStatement:setString( nX - nPos, cNrProcess )
                    EndIf     

                Next
            
                cQry := oStatement:getFixQuery()

                cAliasProd := MPSysOpenQuery(cQry)

                While (cAliasProd)->(!EoF())

                    cName := TAFGetNT1U( (cAliasProd)->CPF )
            
                    If Empty(cName)

                        cName := Posicione( "C9V", 3, xFilial("C9V") + (cAliasProd)->CPF + "1", "C9V_NOME" )
                        
                        If Empty(cName)
                            cName := Posicione( "V9U", 5, xFilial("V9U") + (cAliasProd)->CPF + "1", "V9U_NMTRAB" )
                        EndIf
                    
                    EndIf       

                    cName := (cAliasProd)->NOME

                    oJsonAux                    := JsonObject():New()
                    oJsonAux[ "companyId" ]     := cEmpRequest + "|" + (cAliasProd)->FILIAL
                    oJsonAux[ "period" ]        := Transform((cAliasProd)->PERAPU, "@R 9999-99")
                    oJsonAux[ "processNumber" ] := (cAliasProd)->NRPROC
                    oJsonAux[ "cpf" ]           := AllTrim(Transform((cAliasProd)->CPF,"@R 999.999.999-99"))
                    oJsonAux[ "name" ]          := cName
                    oJsonAux[ "id" ]            := (cAliasProd)->RECNO
                    oJsonAux[ "receipt" ]       := (cAliasProd)->PROTUL

                    Aadd(oJson['items'],oJsonAux)
                    FreeObj(oJsonAux)

                    cNome := ""
                    nTotReg := (cAliasProd)->TOTAL_LINE

                    (cAliasProd)->(DbSkip())

                End

                If nTotReg > nRegFim
                    lHasNext := .T.
                EndIf

                oJson["hasNext"] := lHasNext

                (cAliasProd)->(DbCloseArea())
                FreeObj(oStatement)

                oRest:SetResponse(oJson)

            EndIf

        EndIf

    Else

        oError[ "code" ]            := 400
        oError[ "message" ]         := "Ambiente desatualizado."
        oError[ "detailedMessage" ] := "Por favor, atualize o ambiente com o pacote de atualização mensal do TAF, versão de fevereiro de 2025 ou superior."

        oRest:setFault(oError:ToJson())

    EndIf
	
Return 

//---------------------------------------------------------------------
/*/{Protheus.doc} deleteWorkerInLaborProcess
    Método que exclui eventos S-5503 conforme coleção de ID's informados.
    @author rodrigo.nicolino/lucas.passos
    @since 21/11/2024
    @version 1.0
    @return 
/*/
//---------------------------------------------------------------------
Method deleteWorkerInLaborProcess() Class Totalizador5503
    
    Local cEmpRequest as Character
    Local cFilRequest as Character
    Local cQryReg     as Character
    Local cRecno      as Character
    Local jQuery      as Json

    cEmpRequest := ""
    cFilRequest := ""
    cQryReg     := ""
    jQuery      := oRest:getQueryRequest()
    nPage       := IIF( jQuery["page"] <> Nil, Val(jQuery["page"]) , 1 )
    nPageSize   := IIF( jQuery["pageSize"] <> Nil, Val(jQuery["pageSize"]), 15 )

    If jQuery["companyCode"] == Nil .Or. jQuery["branchCode"] == Nil

        oRest:setStatusCode(400)
        cJson := STR0001
        oRest:SetResponse(cJson)
        
    Else 

        cEmpRequest := jQuery["companyCode"]
		cFilRequest := jQuery["branchCode"]
        cRecno      := jQuery["id"]

        PrepEnv( cEmpRequest, cFilRequest )

        If !Empty(cRecno)

            T8N->(DbSetOrder(2))

            nRecno := Val(cRecno)

            T8N->(DbGoTo(nRecno))
            
            cFilReg     := T8N->T8N_FILIAL
            cIdTab      := T8N->T8N_ID
            cVersion    := T8N->T8N_VERSAO
            
            If Empty(cIdTab)

                oRest:setStatusCode(404)
                cJson := STR0002
                oRest:SetResponse(cJson)

            Else

                TafDelTot(,cFilReg, cIdTab, cVersion, {"T8N","T8O","T8P"})

            EndIf 


        Else

            oRest:setStatusCode(400)
            cJson := STR0003
            oRest:SetResponse(cJson)

        EndIf 

    EndIf
    
Return 

//---------------------------------------------------------------------
/*/{Protheus.doc} xmlFGTSPerWorkerInLaborProcess
    Método que retorna XML's do evento S-5503 encodados em base64
    @author rodrigo.nicolino
    @since 22/11/2024
    @version version
/*/
//---------------------------------------------------------------------
Method xmlFGTSPerWorkerInLaborProcess() Class Totalizador5503

    Local aCompany    as Array
    Local aRecnos     as Array
    Local cEmpRequest as Character
    Local cFilRequest as Character
    Local cQryReg     as Character
    Local cRecno      as Character
    Local jQuery      as Json
    Local oJson       as Object

    aCompany    := {}
    aRecnos     := {}
    cEmpRequest := ""
    cFilRequest := ""
    cQryReg     := ""
    jQuery      := oRest:getQueryRequest()
    nPage       := IIF( jQuery["page"] <> Nil, Val(jQuery["page"]) , 1 )
    nPageSize   := IIF( jQuery["pageSize"] <> Nil, Val(jQuery["pageSize"]), 15 )
    oJson       := JsonObject():New()

    If jQuery["companyId"] == Nil

        oRest:setStatusCode(400)
        cJson := STR0001
        oRest:SetResponse(cJson)
        
    Else 

        aCompany    := StrTokArr( jQuery[ 'companyId' ], "|" )

        If Len( aCompany ) < 2

            oRest:setStatusCode(400)
            cRet := STR0001
            oRest:SetResponse(cRet)

		Else

            cEmpRequest := aCompany[1]
            cFilRequest := aCompany[2]
            cRecno      := jQuery["id"]

            PrepEnv( cEmpRequest, cFilRequest )

            If !Empty(cRecno)

                T8N->(DbSetOrder(2))

                nRecno   := Val(cRecno)

                T8N->(DbGoTo(nRecno))
                
                cIdTab      := T8N->T8N_ID
                
                If Empty(cIdTab)

                    oJson["id"]    := cRecno
                    oJson["xml"]   := '[ { "message": "Não foi encontrado registro do evento S-5503 com o ID:"' + cRecno + '" informado"  } ]'

                Else

                    oJson["id"]    := cIdTab
                    oJson["xml"]   := Encode64( T8N->T8N_XMLGRV )

                EndIf 

                oRest:SetResponse( oJson:ToJson() )

            Else

                oRest:setStatusCode(400)
                cJson := STR0003
                oRest:SetResponse(cJson)

            EndIf 

        EndIf

    EndIf

    oJson := Nil
    FreeObj( oJson )

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} registrationAndCategory
    Método que carrega informações relativas a Matrícula e Categoria de 
    trabalhador específico do evento S-5503, ordenadas ou não.
    @author rodrigo.nicolino
    @since 22/11/2024
    @version version
/*/
//---------------------------------------------------------------------
Method registrationAndCategory() Class Totalizador5503

    Local aCompany    as Array
    Local cAliasReg   as Character
    Local cEmpRequest as Character
    Local cFilRequest as Character
    Local cQryReg     as Character
    Local cRecno      as Character
    Local cRet        as Character
    Local cVar        as Character
    Local jQuery      as Json
    Local lHasNext    as Logical
    Local nRegFim     as Numeric
    Local nRegIni     as Numeric
    Local nTotReg     as Numeric
    Local oJson       as Object
    Local oJsonAux    as Object
    Local oJsonMatCat as Object
    Local oStatement  as Object

    aCompany       := {}
    cEmpRequest    := ""
    cFilRequest    := ""
    cQryReg        := ""
    jQuery         := oRest:getQueryRequest()
    lHasNext       := .F.
    nPage          := IIF( jQuery["page"] <> Nil, Val(jQuery["page"]) , 1 )
    nPageSize      := IIF( jQuery["pageSize"] <> Nil, Val(jQuery["pageSize"]), 15 )
    nTotReg        := 0
    oJson          := JsonObject():New()
    oJson["items"] := {}
    oJsonAux       := Nil

    If jQuery["companyId"] == Nil

        oRest:setStatusCode(400)
        cRet := STR0001
        oRest:SetResponse(cRet)

    Else

        If jQuery["id"] == Nil

            oRest:setStatusCode(400)
            cRet := STR0003
            oRest:SetResponse(cRet)

        Else

            aCompany    := StrTokArr( jQuery[ 'companyId' ], "|" )

            If Len( aCompany ) < 2

                oRest:setStatusCode(400)
                cRet := STR0001
                oRest:SetResponse(cRet)

            Else

                cEmpRequest := aCompany[1]
                cFilRequest := aCompany[2]
                cRecno      := jQuery["id"]
                nRegFim     := nPage * nPageSize
                nRegIni     := ( ( nPage - 1 ) * nPageSize ) + 1

                PrepEnv( cEmpRequest, cFilRequest )

                cQryReg += " SELECT * FROM ( "
                cQryReg += "   SELECT ROW_NUMBER() OVER( ORDER BY T8O.R_E_C_N_O_ ) LINE_NUMBER,
                cQryReg += "   T8O.T8O_FILIAL AS FILIAL, "
                cQryReg += "   T8O.T8O_ID AS ID, "
                cQryReg += "   T8O.R_E_C_N_O_ AS RECNO, "
                cQryReg += "   (SELECT COUNT(T8OA.R_E_C_N_O_ ) " 
                cQryReg += "    FROM " + RetSqlName("T8O") + " T8OA " 
                cQryReg += "   WHERE T8OA.T8O_FILIAL = T8N.T8N_FILIAL "  
                cQryReg += "   AND T8OA.T8O_ID = T8N.T8N_ID "  
                cQryReg += "   AND T8OA.T8O_VERSAO = T8N.T8N_VERSAO "
                cQryReg += "   AND T8OA.D_E_L_E_T_ = '') AS TOTAL_LINE  " 
                cQryReg += "   FROM " + RetSqlName("T8N") + " T8N "
                cQryReg += "   INNER JOIN " + RetSqlName("T8O") + " T8O "
                cQryReg += "   ON T8N.T8N_FILIAL = T8O.T8O_FILIAL "
                cQryReg += "   AND T8N.T8N_ID = T8O.T8O_ID "
                cQryReg += "   AND T8N.T8N_VERSAO = T8O.T8O_VERSAO "
                cQryReg += "   WHERE T8N.R_E_C_N_O_ = ? "
                cQryReg += "   AND T8N.D_E_L_E_T_ = '' "
                cQryReg += " ) TAB "
                cQryReg += " WHERE LINE_NUMBER BETWEEN "+ cValTochar(nRegIni) + " AND " + cValTochar(nRegFim) + " "

                oStatement := FWPreparedStatement():New( ChangeQuery(cQryReg) )

                If !Empty(cRecno)
                    oStatement:setString(1, cRecno)
                EndIf

                cQryReg := oStatement:getFixQuery()

                cAliasReg := MPSysOpenQuery(cQryReg)

                While (cAliasReg)->(!EoF())

                    T8O->(DBGoTo((cAliasReg)->RECNO))

                    cVar := T8O->T8O_MATCAT

                    oJsonMatCat := JsonObject():New()
                    oJsonMatCat:FromJson(cVar)

                    oJsonAux                         := JsonObject():New()
                    oJsonAux[ "companyId" ]          := cEmpRequest + "|" + (cAliasReg)->FILIAL
                    oJsonAux[ "id" ]                 := (cAliasReg)->RECNO
                    oJsonAux[ "registration" ]       := oJsonMatCat:GetJsonObject("registration")
                    oJsonAux[ "category" ]           := oJsonMatCat:GetJsonObject("category")
                    oJsonAux[ "originCategory" ]      := oJsonMatCat:GetJsonObject("originCategory")
                    oJsonAux[ "fgtsTot" ]            := oJsonMatCat:GetJsonObject("fgtsTot")
                    oJsonAux[ "typeOfInscription" ]  := typeOfInscription(oJsonMatCat:GetJsonObject("typeOfInscription"))
                    oJsonAux[ "registrationNumber" ] := Iif( oJsonMatCat:GetJsonObject("registrationNumber") != nil, oJsonMatCat:GetJsonObject("registrationNumber"), "" )


                    Aadd(oJson["items"],oJsonAux)
                    FreeObj(oJsonAux)

                    nTotReg := (cAliasReg)->TOTAL_LINE

                    (cAliasReg)->(DbSkip())

                EndDo

                If nTotReg > nRegFim
                    lHasNext := .T.
                EndIf

                oJson["hasNext"] := lHasNext

                (cAliasReg)->(DbCloseArea())
                FreeObj(oStatement)

                oRest:SetResponse(oJson)

            EndIf

        EndIf

    EndIf

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} paymentAndAmounts
    Método que carrega informações relativas a Identificação do 
    estabelecimento responsável pelo pagamento a trabalhador específico 
    dos valores informados do evento S-5503, ordenadas ou não
    @author rodrigo.nicolino
    @since 22/11/2024
    @version version
/*/
//---------------------------------------------------------------------
Method paymentAndAmounts() Class Totalizador5503

    Local aCompany    as Array
    Local cAliasPay   as Character
    Local cEmpRequest as Character
    Local cFilRequest as Character
    Local cQryPay     as Character
    Local cRecno      as Character
    Local cRet        as Character
    Local cVar        as Character
    Local jQuery      as Json
    Local lHasNext    as Logical
    Local nPage       as Numeric
    Local nPageSize   as Numeric
    Local nRegFim     as Numeric
    Local nRegIni     as Numeric
    Local nTotReg     as Numeric
    Local oJson       as Object
    Local oJsonAux    as Object
    Local oJsonPay    as Object
    Local oStatement  as Object

    aCompany       := {}
    cEmpRequest    := ""
    cFilRequest    := ""
    cQryPay        := ""
    jQuery         := oRest:getQueryRequest()
    lHasNext       := .F.
    nPage          := IIF( jQuery["page"] <> Nil, Val(jQuery["page"]) , 1 )
    nPageSize      := IIF( jQuery["pageSize"] <> Nil, Val(jQuery["pageSize"]), 15 )
    nTotReg        := 0
    oJson          := JsonObject():New()
    oJson["items"] := {}
    oJsonAux       := Nil

    If jQuery["companyId"] == Nil

        oRest:setStatusCode(400)
        cRet := STR0001
        oRest:SetResponse(cRet)

    Else

        If jQuery["id"] == Nil

            oRest:setStatusCode(400)
            cRet := STR0003
            oRest:SetResponse(cRet)

        Else

            aCompany    := StrTokArr( jQuery[ 'companyId' ], "|" )

            If Len( aCompany ) < 2

                oRest:setStatusCode(400)
                cRet := STR0001
                oRest:SetResponse(cRet)

            Else
            
                cEmpRequest := aCompany[1]
                cFilRequest := aCompany[2]
                cRecno      := jQuery["id"]
                nRegFim     := nPage * nPageSize
                nRegIni     := ( ( nPage - 1 ) * nPageSize ) + 1

                PrepEnv( cEmpRequest, cFilRequest )

                cQryPay += " SELECT * FROM ( "
                cQryPay += "   SELECT ROW_NUMBER() OVER( ORDER BY T8P.R_E_C_N_O_ ) LINE_NUMBER,
                cQryPay += "   T8P.T8P_ID AS ID, "
                cQryPay += "   T8P.R_E_C_N_O_ AS RECNO, "
                cQryPay += "   (SELECT COUNT(T8PA.R_E_C_N_O_ ) "     
                cQryPay += "    FROM " + RetSqlName("T8P") + " T8PA "  
                cQryPay += "    WHERE T8PA.T8P_FILIAL = T8O.T8O_FILIAL "     
                cQryPay += "    AND T8PA.T8P_ID = T8O.T8O_ID "    
                cQryPay += "    AND T8PA.T8P_VERSAO = T8O.T8O_VERSAO "     
                cQryPay += "    AND T8PA.T8P_CHAVE = T8O.T8O_SEQUEN" 
                cQryPay += "    AND T8O.R_E_C_N_O_ = ? "  
                cQryPay += "    AND T8PA.D_E_L_E_T_ = '') AS TOTAL_LINE "      
                cQryPay += "   FROM " + RetSqlName("T8O") + " T8O "
                cQryPay += "   INNER JOIN " + RetSqlName("T8N") + " T8N "
                cQryPay += "   ON T8N.T8N_FILIAL = T8O.T8O_FILIAL "
                cQryPay += "   AND T8N.T8N_ID = T8O.T8O_ID "
                cQryPay += "   AND T8N.T8N_VERSAO = T8O.T8O_VERSAO "
                cQryPay += "   INNER JOIN " + RetSqlName("T8P") + " T8P "
                cQryPay += "   ON T8N.T8N_FILIAL = T8P.T8P_FILIAL "
                cQryPay += "   AND T8N.T8N_ID = T8P.T8P_ID "
                cQryPay += "   AND T8N.T8N_VERSAO = T8P.T8P_VERSAO "
                cQryPay += "   AND T8O.T8O_SEQUEN = T8P.T8P_CHAVE "
                cQryPay += "   WHERE T8O.R_E_C_N_O_ = ? "
                cQryPay += "   AND T8O.D_E_L_E_T_ = '' "
                cQryPay += " ) TAB "
                cQryPay += " WHERE LINE_NUMBER BETWEEN "+ cValTochar(nRegIni) + " AND " + cValTochar(nRegFim) + " "

                oStatement := FWPreparedStatement():New( ChangeQuery(cQryPay) )

                If !Empty(cRecno)
                    oStatement:setString(1, cRecno)
                    oStatement:setString(2, cRecno)
                EndIf

                cQryPay := oStatement:getFixQuery()

                cAliasPay := MPSysOpenQuery(cQryPay)

                While (cAliasPay)->(!EoF())

                    T8P->(DBGoTo((cAliasPay)->RECNO))

                    cVar := T8P->T8P_STABPA

                    oJsonPay := JsonObject():New()
                    oJsonPay:FromJson(cVar)

                    oJsonAux                              := JsonObject():New()
                    oJsonAux[ "id" ]                      := (cAliasPay)->RECNO
                    oJsonAux[ "referencePeriod" ]         := oJsonPay:GetJsonObject("referencePeriod")
                    oJsonAux[ "category" ]                := oJsonPay:GetJsonObject("category")
                    oJsonAux[ "typeOfLaborProcessValue" ] := oJsonPay:GetJsonObject("typeOfLaborProcessValue")
                    oJsonAux[ "dpsFGTSProcTrab" ]         := Iif( oJsonPay:GetJsonObject("dpsFGTSProcTrab") != nil, oJsonPay:GetJsonObject("dpsFGTSProcTrab"), "" )
                    oJsonAux[ "remFGTSProcTrab" ]         := Iif( oJsonPay:GetJsonObject("remFGTSProcTrab") != nil, oJsonPay:GetJsonObject("remFGTSProcTrab"), "" )
                    oJsonAux[ "remFGTSSefip" ]            := Iif( oJsonPay:GetJsonObject("remFGTSSefip") != nil, oJsonPay:GetJsonObject("remFGTSSefip"), "" )
                    oJsonAux[ "dpsFGTSSefip" ]            := Iif( oJsonPay:GetJsonObject("dpsFGTSSefip") != nil, oJsonPay:GetJsonObject("dpsFGTSSefip"), "" )
                    oJsonAux[ "remFGTSDecAnt" ]           := Iif( oJsonPay:GetJsonObject("remFGTSDecAnt") != nil, oJsonPay:GetJsonObject("remFGTSDecAnt"), "" )
                    oJsonAux[ "dpsFGTSDecAnt" ]           := Iif( oJsonPay:GetJsonObject("dpsFGTSDecAnt") != nil, oJsonPay:GetJsonObject("dpsFGTSDecAnt"), "" )

                    Aadd(oJson["items"],oJsonAux)
                    FreeObj(oJsonAux)

                    nTotReg := (cAliasPay)->TOTAL_LINE

                    nRegIni++

                    (cAliasPay)->(DbSkip())

                EndDo

                If nTotReg > nRegFim
                    lHasNext := .T.
                EndIf

                oJson["hasNext"] := lHasNext

                (cAliasPay)->(DbCloseArea())
                FreeObj(oStatement)

                oRest:SetResponse(oJson)

            EndIf

        EndIf

    EndIf

Return

//---------------------------------------------------------------------
/*/{Protheus.doc} typeOfInscription
    Função para converter o código do tipo de inscrição em nome do tipo
    de inscrição
    @author rodrigo.nicolino
    @since 23/12/2024
    @version version
/*/
//---------------------------------------------------------------------
Static Function typeOfInscription( cType as Character )

    Local cReturn := ""

    Default cType := ""

    If cType == "1"

        cReturn := "CNPJ"

    ElseIf cType == "3"

        cReturn := "CAEPF"

    ElseIf cType == "4"

        cReturn := "CNO"

    EndIf

Return cReturn
