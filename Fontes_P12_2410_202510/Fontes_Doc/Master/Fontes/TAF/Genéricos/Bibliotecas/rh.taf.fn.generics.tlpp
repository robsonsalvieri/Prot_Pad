#Include "PROTHEUS.CH"
#Include "Tlpp-Core.th"
#Include "Tlpp-Rest.th"

Static oReport := Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} TafGeraEvtExc
Função para gerar o evento de exclusão do eSocial (S-3000)

@params oModel  - objeto do modelo de dados
        cAlias  - tabela do evento
        cFonte  - nome do fonte que esta chamando a função
        nInd    - número do índice de ordenação
        cEvento - código do evento de origem

@return 

@author rodrigo.nicolino
@since 04/07/2025
@version 1.0

/*/
//-------------------------------------------------------------------
Function tafGeraEvtExc( oModel as object, cAlias as character, cFonte as character, nInd as numeric, cEvento as character )

    Local aAltRPT    as array
    Local aTafRotn   as array
    Local cCampo     as character
    Local cFunc      as character
    Local cIndApu    as character
    Local cPeriodo   as character
    Local cProtocolo as character
    Local cValor     as character
    Local cVerAnt    as character
    Local nCount     as numeric
    Local oModelOri  as object

    Default oModel  := Nil

    aTafRotn   := TAFRotinas( cEvento ,4,.F.,2)
	cFunc      := (cAlias)->( &( aTafRotn[11]))
    
    FAltRegAnt( cAlias, '2' )

    oModel:SetOperation( 4 )
    oModel:Activate()

    oModelOri := oModel:GetModel( "MODEL_" + cAlias )

    For nCount := 1 To Len(oModel:AALLSUBMODELS[1]:ADATAMODEL[1])

        cCampo := "cCampo" + AllTrim(Str(nCount))
        cValor := "cValor" + AllTrim(Str(nCount))
        &cCampo := oModel:AALLSUBMODELS[1]:ADATAMODEL[1][nCount][1]
        &cValor := oModelOri:GetValue(oModel:AALLSUBMODELS[1]:ADATAMODEL[1][nCount][1])

        If &cCampo $ cAlias + "_INDAPU|" + cAlias + "_PERAPU|"

            cIndApu		:= oModelOri:GetValue( cAlias + "_INDAPU" )
	        cPeriodo	:= oModelOri:GetValue( cAlias + "_PERAPU" )

        EndIf

    Next

    //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
    //³Busco a versao anterior do registro para gravacao do rastro³
    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    cVerAnt		:= oModelOri:GetValue( cAlias + "_VERSAO" )
    cProtocolo	:= oModelOri:GetValue( cAlias + "_PROTUL" )
    
    oModel:DeActivate()
    oModel:SetOperation( 3 )
    oModel:Activate(.T.)

    /*---------------------------------------------------------
    ATENCAO -> A alteracao destes campos deve sempre estar
    abaixo do Loop do For, pois devem substituir as informacoes
    que foram armazenadas no Loop acima
    -----------------------------------------------------------*/
    For nCount := 1 To Len(oModel:AALLSUBMODELS[1]:ADATAMODEL[1]) 

        If oModel:AALLSUBMODELS[1]:ADATAMODEL[1][nCount][1] $ cAlias + "_VERSAO|" + cAlias + "_VERANT|" + cAlias + "_PROTPN|" + cAlias + "_PROTUL|"

            oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_VERSAO", xFunGetVer() )
            oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_VERANT", cVerAnt      )
            oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_PROTPN", cProtocolo   )
            oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_PROTUL", '' 		   )

        Else

            oModel:LoadValue( "MODEL_" + cAlias,  oModel:AALLSUBMODELS[1]:ADATAMODEL[1][nCount][1], &("cValor" + AllTrim(Str(nCount)))    )

        EndIf

    Next

    /*---------------------------------------------------------
    Tratamento para que caso o Evento Anterior fosse de exclusão
    seta-se o novo evento como uma "nova inclusão", caso contrário o
    evento passar a ser uma alteração
    -----------------------------------------------------------*/
    oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_EVENTO", "E" )
    oModel:LoadValue( "MODEL_" + cAlias,  cAlias + "_ATIVO"	, "1" )	

    FwFormCommit(oModel)		
    TAFAltStat(cAlias, "6")

    If cEvento $ "S-1200|"

        aAltRPT := SaveAltRPT( cIndApu, cPeriodo, cFunc, cAlias )
        tafInfoRptJob( aAltRPT, cEvento, cAlias )

    EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} tafInfoRptJob
Função para atualizar a tabela V3N de acordo com os dados passado nos 
parâmetros

@params aAltRPT  - array com os dados do relatório (indApu, perApu, func, alias)
        cEvento  - código do evento de origem
        cAlias   - tabela do evento

@return 

@author rodrigo.nicolino
@since 07/07/2025
@version 1.0

/*/
//-------------------------------------------------------------------
Function tafInfoRptJob(aAltRPT as array, cEvento as character, cAlias as character)

    Local oInfoRPT  as object

    Default aAltRPT := {}

    oInfoRPT   := Nil

    If oReport == Nil
		oReport := TAFSocialReport():New()
	EndIf

    InfoRPTObj( aAltRPT[1], aAltRPT[2], aAltRPT[3], aAltRPT[4],,, @oInfoRPT )
    oReport:UpSert(cEvento, "0", xFilial(cAlias), oInfoRPT, .T.)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} tafDefAAnalitco
Função para definir o tamanho do array analítico utilizado nos relatórios

@params 

@return aAnalitico - array analítico com 25 posições

@author rodrigo.nicolino
@since 07/07/2025
@version 1.0

/*/
//-------------------------------------------------------------------
Function tafDefAAnalitco(aAnalitico)

    Default aAnalitico  := {}

    AAdd(aAnalitico, Array(25))

Return(aAnalitico)
