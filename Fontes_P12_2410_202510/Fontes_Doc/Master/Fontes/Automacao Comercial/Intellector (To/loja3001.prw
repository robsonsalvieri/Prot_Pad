#INCLUDE "MSOBJECT.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "LOJA3001.CH"

#DEFINE APROVADO "APROVA"

Function LOJA3001() ;Return  // "dummy" function - Internal Use   

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ         
ฑฑษออออออออออัออออออออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบClasse    ณLJCYMF			บAutor  ณVendas Clientes     บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณInterface da classe LJCYMF, os metodos precisam ser implementados   บฑฑ
ฑฑบ			 ณna classe LJCYMF.    	    										  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaLoja / FrontLoja                                         		  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Class LJCYMF
    
    Data cUrl												//Url de conexao com Server do YMF
    Data cPrograma                                          //Politica a ser executada
    Data cTipo                                              //Tipo da Politica
    Data cLayout                                            //Layout da Politica
    Data cUsuario                                           //Usuario do Server YMF
    Data cSenha                                             //Senha do Server do YMF
    Data oDadosEnv											//Objeto do Tipo LJCDadosEnvYMF	
                   
	Method New(cPolitica, cTipo, cLayout)                  //Construtor da Classe.
	Method GeraXml()                                       //Metodo que gera o XML que sera enviado.
	Method EnviaXml()                                      //Metodo que envia o XML ao Servico do YMF.
	Method Executar()		                               //Metodo que executa o Servico do YMF.
	Method TrataEnvio(oValor, cTipo)                       //Metodo responsavel em formatar os dados de envio.
	Method TrataRetorno()                                  //Metodo responsavel pelo retorno do Servico do YMF.

End Class

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณNew		บAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo Construtor da Classe                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ			 ณcPolitica - Nome da Politica a ser usada definida no YMF.	  บฑฑ
ฑฑบParametrosณcTipo		- Tipo da Politica usada.						  บฑฑ
ฑฑบ			 ณcLayout	- Layout da Politica.							  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Sigaloja/Fronloja                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method New(cPolitica, cTipo, cLayout) Class LJCYMF 

    Self:cPrograma := ALLTRIM(cPolitica)                                       
    Self:cTipo     := ALLTRIM(cTipo)                                      
    Self:cLayout   := ALLTRIM(cLayout)                                      
    
    Self:cUrl	   := ALLTRIM(SuperGetMv("MV_TOLURL", .F.,""))
    Self:cUsuario  := ALLTRIM(SuperGetMv("MV_TOLUSUA", .F.,""))
    Self:cSenha    := ALLTRIM(SuperGetMv("MV_TOLSENH", .F.,""))                                        
    
    Self:oDadosEnv := LJCDadosEnvYMF():New()									

Return Self

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ GeraXml  บAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Metodo responsavel em gerar o XML que sera enviado ao YMF. บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno	 ณ cXML - String contendo o XML.							  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Sigaloja/Fronloja                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method GeraXml() Class LJCYMF   
                      
	Local cXML 	   := "" 
	Local cCampo   := "" 
	Local nCount   := 1 
	Local oCliente := Self:oDadosEnv:oCliente

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCria a estrutura do XML que sera enviado para o intellector.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cXML 	:= "<?xml version='1.0' encoding='ISO-8859-1'?>"
	
	cXML 	+= "<ANALISE_CREDITO>"
	cXML 	+= "	<programa>" + Self:cPrograma + "</programa>" 	//Nome da politica do intellector
	cXML 	+= "	<tipo>"     + Self:cTipo     + "</tipo>"		//Tipo da politica utilizada no intellector
	cXML 	+= "	<layout>"   + Self:cLayout   + "</layout>"	  	//Layout da politica definida no intellector
	cXML 	+= "	<usuario>"  + Self:cUsuario  + "</usuario>" 	//Usuario do intellector
	cXML 	+= "	<senha>"    + Self:cSenha    + "</senha>"	   	//Senha do intellector 
	
	cXML 	+= "	<VALOR_LIMITE_DE_CREDITO>" + Self:oDadosEnv:cValorLimi + "</VALOR_LIMITE_DE_CREDITO>"
	cXML 	+= "	<DATA_VENDA>"              + Self:oDadosEnv:cDataVenda + "</DATA_VENDA>"
	cXML 	+= "	<DATA_VENCIMENTO_LIMITE>"  + Self:oDadosEnv:cDataVenc  + "</DATA_VENCIMENTO_LIMITE>"
	cXML 	+= "	<TITULOS_EM_ABERTO>"       + Self:oDadosEnv:cTitulosAb + "</TITULOS_EM_ABERTO>"	
	cXML 	+= "	<TOLERANCIA_LIMITE>"       + Self:oDadosEnv:cTolLimite + "</TOLERANCIA_LIMITE>"	
	cXML 	+= "	<VALOR_FINANCIADO>"        + Self:oDadosEnv:cValorFinc + "</VALOR_FINANCIADO>"	
	cXML 	+= "	<VALOR_TITULOS_EM_ATRASO>" + Self:oDadosEnv:cValorTitA + "</VALOR_TITULOS_EM_ATRASO>"	

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณInseri Campos do SA1 no XML.ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
	For nCount := 1 To oCliente:Elements(1):Campos():Count()
		
		cCampo := oCliente:Elements(1):Campos():Elements(nCount):cNome     

		cXML 	+= "<" + cCampo + ">"
		
		cXML 	+= Self:TrataEnvio(oCliente:Elements(1):Campos():Elements(nCount):oValor, oCliente:Elements(1):Campos():Elements(nCount):cTipo)
		
		cXML 	+= "</" + cCampo + ">"

	Next
	
	cXML 	+= "</ANALISE_CREDITO>"

Return cXML

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ EnviaXml บAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Metodo que enviara o XML ao YMF.                           บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno	 ณ oRetorno - Objeto do tipo LJCDadosRetYMF.				  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Sigaloja/Fronloja                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method EnviaXml(cXML) Class LJCYMF   

	Local oWSIntellector :=	Nil  
	Local oRetorno       := -1      

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณChama o WS do intellectorณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oWSIntellector := WSPolicyExecutionService():New()
	
	oWSIntellector:_URL := Self:cUrl   
	
	If oWSIntellector:executePolicy(cXML)
	
   	    oRetorno := ::TrataRetorno(oWSIntellector)
	Else                                                                               
	
		MsgAlert(STR0001) //"Nใo foi Possํve conectar no WebService."
	EndIf
                             
Return oRetorno  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ Executar บAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Metodo que executa o Servico do YMF.                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno	 ณ oRet - Objeto do tipo LJCDadosRetYMF.		        	  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Sigaloja/Fronloja                                          บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method Executar() Class LJCYMF   
	Local oRet := Nil
	Local cXML := ""   
	
	cXml := Self:GeraXml()   

	oRet := Self:EnviaXml(cXml)   

Return oRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTrataEnvio  บAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMetodo responsavel em formatar os dados para o envio ao YMF.  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParam.    ณ oValor - Conteudo a ser formatado                            บฑฑ
ฑฑบ			 ณ cTipo  - Tipo a ser tratado									บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSigaloja / Fronloja                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method TrataEnvio(oValor, cTipo) Class LJCYMF   

	Local oRetorno := Nil
	
	If Empty(oValor)
		oRetorno := ""	
	Else
		Do Case
			Case cTipo == "N"
				oRetorno := CVALTOCHAR(oValor)  
				oRetorno := STRTRAN(oRetorno, ",", ".")
				
			Case cTipo == "D"
				oRetorno := DTOS(oValor)
				
			Otherwise
		EndCase
		
		oRetorno := ALLTRIM(oValor)
	EndIf

Return oRetorno

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTrataRetornoบAutor  ณMicrosiga           บ Data ณ  01/12/09   บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Metodo responsavel pelo retorno                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParam.    ณ oWSIntellector - Retorno do WS do YMF.						บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ oRetorno - Objeto LJCDadosRetYMF Formatado.                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Sigaloja/Fronloja                                            บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Method TrataRetorno(oWSIntellector) Class LJCYMF   

    Local cRetornoXML := ""
    Local oXml        := LJCXml():New()  
    Local oEstrutura  := Nil   
    Local cDelimit    := "_"
    Local oDadosRet   := LJCDadosRetYMF():New()

	cRetornoXML := oWSIntellector:cXmlOutputString
	
	oXml:Criar(cRetornoXML, cDelimit, Nil)
	
	oEstrutura := oXml:oXml  
	                  
	If !Empty(oEstrutura:_RAIZ:_LPT__SMSGERRO:TEXT)
		
	    oDadosRet:nCodErro := VAL(oEstrutura:_RAIZ:_LPT__IRETORNO:TEXT)
	    oDadosRet:cMsgErro := ALLTRIM(oEstrutura:_RAIZ:_LPT__IRETORNO:TEXT)
	    
   		MsgAlert(STR0002 + "-> " + CVALTOCHAR(oDadosRet:nCodErro) + " - " + oDadosRet:cMsgErro) //"Retorno da Consulta
	Else
		oDadosRet:lAprovado  := IIF( ALLTRIM(oEstrutura:_RAIZ:_LPT__sAcao:TEXT) == APROVADO, .T., .F.)
		oDadosRet:cMotBloq	 := ALLTRIM(oEstrutura:_RAIZ:_MOTIVO_BLOQUEIO:TEXT)
		oDadosRet:nValLimite := VAL(oEstrutura:_RAIZ:_LIMITE_CREDITO:TEXT)
		oDadosRet:dDtVctoLim := STOD(oEstrutura:_RAIZ:_DATA_VENCIMENTO_LIMITE:TEXT)
	EndIf

Return oDadosRet   