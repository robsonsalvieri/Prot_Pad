#INCLUDE "PROTHEUS.CH"
#INCLUDE "CRDA035.CH"

Static aList := {} //Para trazer os campos da ListBox da tabela MA6 (Cache)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณCRDA035   บ Autor ณ Fernando Salvatori บ Data ณ  23/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณ Layout para geracao do arquivo de controle grafico de      บฑฑ
ฑฑบ          ณ cartoes.                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ GENERICO                                                   บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.           บฑฑ
ฑฑฬออออออออออออออัออออออออัออออออัออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                   บฑฑ
ฑฑฬออออออออออออออุออออออออุออออออุออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบMONAEL RIBEIROณ30/01/04ณDADALTณInclusใo da Op็ใo Delimitador           บฑฑ
ฑฑบMachima       ณ04/07/05ณ081261ณAcerto no titulo do aHeader para o campoบฑฑ
ฑฑบ              ณ        ณ      ณPosicao. Considerar dois digitos para a บฑฑ
ฑฑบ              ณ        ณ      ณposicao do campo no arquivo texto       บฑฑ
ฑฑบConrado Q.    ณ26/02/07ณ119542ณRetirado ajustes de dicionแrio.         บฑฑ
ฑฑศออออออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CRDA035
Local nOpc 			:= 0
Private cFileGer 	:= ""
Private cFileBk  	:= ""

//"Layout para controle grแfico de cart๕es" ### "Selecione o tipo de arquivo que deseja configurar: " ### "&Enviar" ### "&Receber"
nOpc := Aviso( STR0001, STR0030, { STR0031, STR0032 } )

If nOpc == 1
	cFileGer := "ENVCART.CRD"
	cFileBk  := "ENVCART.OLD"

	If !Pergunte("CRDA31", .T.)
		Return
	EndIf
	
	CA035Man(Iif(mv_par01==1, .T., .F.))
ElseIf nOpc == 2
	cFileGer := "RECCART.CRD"
	cFileBk  := "RECCART.OLD"
	
	If !Pergunte("CRDA32", .T.)
		Return
	EndIf
	
	CA035Man(Iif(mv_par01==1, .T., .F.))
EndIf
                   
Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณChkDelimitบAutor  ณMONAEL P. RIBEIRO   บ Data ณ  12/02/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Checa se o campo relacionamento ้ Delimitador e bloqueia   บฑฑ
ฑฑบ          ณ acesso a quem tiver essa fun็ใo na posi็ใo Valid           บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function ChkDelimit()
Local lRet := .T.
	If aCols[n][04] == "3"
		MsgStop( STR0033, STR0034 )		// "Op็ใo invalida para tipo relat๓rio Delimitador" ### "Op็ใo invalida"
		lRet := .F.
	EndIf
Return lRet
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035Man  บAutor  ณFernando Salvatori  บ Data ณ  25/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Programa de Layout para geracao do Arquivo de Layout de    บฑฑ
ฑฑบ          ณ confeccao de cartao.                                       บฑฑ
ฑฑฬออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ           ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.           บฑฑ
ฑฑฬออออออออออออออัออออออออัออออออัออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                   บฑฑ
ฑฑฬออออออออออออออุออออออออุออออออุออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบMONAEL RIBEIROณ30/01/04ณDADALTณInclusใo da Op็ใo Delimitador           บฑฑ
ฑฑศออออออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035Man(lDelimit)
Local aSize      := MsAdvSize()  		 //Size da tela.
Local aObjects   := {}  				 //Objetos da tela
Local aPosObj    := {}  				 //Posicoes do objeto
Local aInfo      := {}  				 //Posicoes do objeto
Local aHeader    := {}  				 //aHeader da msgetdados
Local aCols      := {}  				 //aCols da Msgetdados
Local nGd_Insert := 1  					 //Insert
Local nGd_UpDate := 2  					 //Update
Local nGd_Delete := 4                    //Delete
Local lR5        := GetRpoRelease("R5")  // Indica se o release e 11.5
Local aButtons 	 := {}

Private cCadastro := STR0001 //"Layout para controle grแfico de cart๕es" //Variavel principal da dialog
Private oDlg      //Objeto da dialog
Private oGetDados //Objeto da msgetdados
Private oBtCan    //Objeto do botao de cancelar
Private oBtSal    //Objeto do botao de salvar
Private oBtCon    //Objeto do botao de consultar

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Montagem do Header.                                                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lDelimit
	AAdd(aHeader,{ STR0039, "nPos"  , "9999", 004, 0 , ".T." , "๛" , "N", "", "", NIL}) 	//"Posi็ใo
Else
	AAdd(aHeader,{ STR0002, "nPosIni", "9999", 004, 0 , ".T." , "๛" , "N", "", "", NIL})  //"Pos. Inicial"
	AAdd(aHeader,{ STR0003, "nTamam" , "9999", 004, 0 , ".T." , "๛" , "N", " ", " ", NIL}) 	//"Tamanho"
EndIf
AAdd(aHeader,{ STR0004, "cRetorno" , "@!"  , 030, 0 , ".T." , "๛" , "C", " ", " ", NIL}) 	//"Retorno"
If lDelimit
	AAdd(aHeader,{STR0005,"cRelac"   ,"@!"  ,001,0 ,".T." ,"๛" ,"C","","", STR0035, "2"})//"Utiliza Relacionamento?" ### "1=Sim;2=Nใo;3=Delimitador"
Else
	AAdd(aHeader,{STR0005,"cRelac"   ,"@!"  ,001,0 ,".T." ,"๛" ,"C","","", STR0036, "2"})//"Utiliza Relacionamento?" ### "1=Sim;2=Nใo"
EndIf
AAdd(aHeader,{STR0006,"cAliasSec","@!"  ,003,0 ,"ChkDelimit() .And. (Vazio() .Or. ExistCpo('SX2'))" ,"๛" ,"C",""," ",NIL})//"Alias"
AAdd(aHeader,{STR0007,"nOrdemSec","99"  ,002,0 ,"ChkDelimit()" ,"๛" ,"N"," "," ",NIL})//"Ordem"
AAdd(aHeader,{STR0008,"cRegra"   ,"@!"  ,150,0 ,"ChkDelimit()" ,"๛" ,"C"," "," ",NIL})//"Regra"   
                                                          
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ  
//ณPosicao dos campos do aCols:                        ณ 
//ณ                                                    ณ 
//ณ1 - Posicao Inicial da geracao do arquivo           ณ 
//ณ2 - Posicao Final da geracao do arquivo             ณ
//ณ3 - Retorno do campo desejado para o arquivo        ณ
//ณ4 - Flag que verifica se deseja fazer relacionamentoณ
//ณ5 - Alias secundario do relacionamento              ณ
//ณ6 - Ordem utilizada no relacionamento               ณ
//ณ7 - Regra de pesquisa ( Seek ) para o relacionamentoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz o preenchimento do aCols.                                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aCols := aClone( CA035Abrir(Nil, lDelimit) )

If valType(aCols) != "U"
	If !Empty(aCols) .And. valType(aCols[1]) == "C" .And. aCols[1] == "FIM"
		Return
	EndIf
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Define as posicoes da GetDados e Paineis.                               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
AAdd( aObjects, {  100, 100, .T., .T. } )      //GetDados
aInfo 	 := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 2, 2 }
aPosObj  := MsObjSize( aInfo, aObjects, .T. , .F. )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Montagem da tela.                                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],00 To aSize[6],aSize[5] OF oMainWnd PIXEL

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ MsGetDados.                                                             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oGetDados := MsNewGetDados():New(aPosObj[1][1],aPosObj[1][2],aPosObj[1][3],aPosObj[1][4],;
GD_INSERT+nGD_DELETE+nGD_UPDATE,"CA035LinOk()",,"",,,120,,,;
,oDlg,aHeader,aCols)
  
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Caso seja Versใo 11.5 exibe a EnchoiceBar, na parte inferior da tela    ณ 
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lR5
	Aadd( aButtons, {"EDIT"	 , {|| CA035Consulta()}, STR0022, STR0022 , {|| .T.}} ) 	//"Consulta de Campos (Ctrl + C)"
	ACTIVATE MSDIALOG oDlg ON INIT (EnchoiceBar(oDlg,{|| IIf(CA035TudoOk(),(CA035Salvar(lDelimit),oDlg:End()) ,NIL)},{||oDlg:End()},,@aButtons))
Else	
	ACTIVATE MSDIALOG oDlg ON INIT ( CA035ChoiceBar(lDelimit),CA035SetKeys(.T.) )
EndIf	

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Limpando Keys para retorno ao menu principal.                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
CA035SetKeys(.F.)
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035TudoOบAutor  ณFernando Salvatori  บ Data ณ  23/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Checa se as informacoes de gravacao esta ok.               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CA035TudoOk()
Local nX := 0 //Contador

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz a consistencia do campo de Retorno.                                 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nX := 1 to Len( oGetDados:aCols )
	If !aTail(oGetDados:aCols[nX]) .And. Empty( oGetDados:aCols[nX][3] )
		MsgStop(STR0009,STR0010  )//"Selecione um campo para o retorno"//"Aten็ใo"
		Return .F.
	EndIf
Next nX

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035SalvaบAutor  ณFernando Salvatori  บ Data ณ  06/23/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Salva as informacoes do aCols para o arquivo CARTAO.CRD    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CA035Salvar(lDelimit)
Local nX := 0                                  //Contador do sistema
Local aBkCols := {}                            //BackUp do aCols para limpeza do mesmo
Local nHdl                                     //Handle do TXT
Local cArq    := "\"+CurDir()+cFileGer         //Arquivo do sistema
Local cArqOld := "\"+CurDir()+cFileBk          //Arquivo Backup
Local cLin    := ""                            //Linha do TXT
Local cConteudo := ""                          //Conteudo do aCols para tratamento antes de grava-lo no txt

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Solicita confirmacao da gravacao.                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !MsgYesNo(STR0011,STR0010)//"Confirma as altera็๕es?"//"Aten็ใo"
	Return .F.
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Seleciona os registros do aCols que estejam disponiveis para geracao.   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nX := 1 to Len( oGetDados:aCols )
	If ! aTail( oGetDados:aCols[nX] )
		AAdd( aBkCols, oGetDados:aCols[nX] )
	EndIf
Next nX

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Se todos os registros estiverem apagados, remover o arquivo CARTAO.CRD  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty( aBkCols )
	AAdd( aBkCols,Array(8) )
	
	If lDelimit
		aBkCols[1][1] := 0000
		aBkCols[1][2] := Space(30)
		aBkCols[1][3] := "2"
		aBkCols[1][4] := Space(3)
		aBkCols[1][5] := 0
		aBkCols[1][6] := Space(100)
		aBkCols[1][7] := .F.
		aBkCols[1][8] := .F.
	Else
		aBkCols[1][1] := 0000
		aBkCols[1][2] := 0000
		aBkCols[1][3] := Space(30)
		aBkCols[1][4] := "2"
		aBkCols[1][5] := Space(3)
		aBkCols[1][6] := 0
		aBkCols[1][7] := Space(100)
		aBkCols[1][8] := .F.
	EndIf
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Atualiza aCols.                                                         ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	oGetDados:aCols := aClone( aBkCols )
	oGetDados:oBrowse:Refresh()
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Caso exista o arquivo, faz um Backup e logo em seguida, apago.          ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If File(cArq)
		If File(cArqOld)
			FErase(cArqOld)
		EndIf
		__CopyFile( cArq, cArqOld )
		FErase(cArq)
	EndIf
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Atualiza aCols.                                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oGetDados:aCols := aClone( aBkCols )
oGetDados:oBrowse:Refresh()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Caso exista o arquivo, faz um Backup e logo em seguida, apago.          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If File(cArq)
	If File(cArqOld)
		FErase(cArqOld)
	EndIf
	__CopyFile( cArq, cArqOld )
	FErase(cArq)
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Crio o arquivo .CRD                                                     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nHdl := FCreate(cArq)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifico sua integridade.                                               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If nHdl == -1
	//"Existem problemas para gera็ใo do arquivo: "
	//" no servidor da aplica็ใo, contate o administrador do sistema !"
	//"Aten็ใo"
	MsgAlert(STR0012 +cArq+ STR0013,STR0010)
	__CopyFile( cArqOld, cArq )
	Return
Endif

If lDelimit
	If valtype(mv_par01) != "U" .And. mv_par01 == 1
	  	If valtype(mv_par02) == "C" .And. valtype(mv_par03) == "C".And. valtype(mv_par04) == "N"
			cLin := "Delimited"+AllTrim(mv_par02)+AllTrim(mv_par03)+AllTrim(Str(mv_par04))+Chr(13)+Chr(10)
		Else
			cLin := "Delimited"+Chr(13)+Chr(10)
		EndIf
	EndIf
	If fWrite(nHdl,cLin,Len(cLin)) != Len(cLin)
		If !MsgYesNo(STR0014,STR0010)//"Aten็ใo"//"Ocorreu um erro na grava็ใo do arquivo. Continua?"
			FClose(nHdl)
			Return .T.
		Endif
	EndIf
Else
	cLin := "Undelimited"+Chr(13)+Chr(10)
	If fWrite(nHdl,cLin,Len(cLin)) != Len(cLin)
		If !MsgYesNo(STR0014,STR0010)//"Aten็ใo"//"Ocorreu um erro na grava็ใo do arquivo. Continua?"
			FClose(nHdl)
			Return .T.
		Endif
	EndIf
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Realizo a gravacao do arquivo TXT.                                      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nX := 1 to Len( oGetDados:aCols )
	
	cLin := ""
	
	If lDelimit
		cConteudo := Alltrim(Str(oGetDados:aCols[nX][1]))
		cLin += PadR( cConteudo ,004 )
	
		cConteudo := oGetDados:aCols[nX][2]
		cLin += PadR( cConteudo ,030 )
	
		cConteudo := oGetDados:aCols[nX][3]
		cLin += PadR( cConteudo ,001 )
	
		cConteudo := oGetDados:aCols[nX][4]
		cLin += PadR( cConteudo ,003 )
	
		cConteudo := AllTrim(Str(oGetDados:aCols[nX][5]))
		cLin += PadR( cConteudo ,002 )
	
		cConteudo := oGetDados:aCols[nX][6]
		cLin += PadR( cConteudo ,150 )
	
	Else
		cConteudo := Alltrim(Str(oGetDados:aCols[nX][1]))
		cLin += PadR( cConteudo ,004 )
	
		cConteudo := Alltrim(Str(oGetDados:aCols[nX][2]))
		cLin += PadR( cConteudo ,004 )
	
		cConteudo := oGetDados:aCols[nX][3]
		cLin += PadR( cConteudo ,030 )
	
		cConteudo := oGetDados:aCols[nX][4]
		cLin += PadR( cConteudo ,001 )
	
		cConteudo := oGetDados:aCols[nX][5]
		cLin += PadR( cConteudo ,003 )
	
		cConteudo := Alltrim(Str(oGetDados:aCols[nX][6]))
		cLin += PadR( cConteudo ,002 )
	
		cConteudo := oGetDados:aCols[nX][7]
		cLin += PadR( cConteudo ,150 )
	EndIf
	
	cLin += Chr(13)+Chr(10)
	
	If fWrite(nHdl,cLin,Len(cLin)) != Len(cLin)
		If !MsgYesNo(STR0014,STR0010)//"Aten็ใo"//"Ocorreu um erro na grava็ใo do arquivo. Continua?"
			Exit
		Endif
	Endif
	
Next nX

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Finalizo a gravacao.                                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
FClose(nHdl)

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035AbrirบAutor  ณFernando Salvatori  บ Data ณ  24/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Faco a abertura do arquivo .CRD                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035Abrir(cFile, lArg, aRecDelimit)   
Local cArq    := "\"+CurDir()               //Arquivo .CRD
Local aRet    := {}                         //Retorno da Funcao
Local cLinha  := ""                         //Linha do arquivo
Local lDelimited := .F.
Local nPosRet := 0
Local cSepara := ""
Local cDelimit := ""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Compatibilidade com outros programas.                                   ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Type("cFileGer") == "C"
	cFile := cFileGer
EndIf                

cArq += cFile

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Se o arquivo nao existe, inicializo um novo registro no aCols.          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !File(cArq)
	Return aRet
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faco a abertura do arquivo TXT.                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
FT_FUSE( cArq )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Avanco para a primeira linha.                                           ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
FT_FGOTOP()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Carrego todos os registros do TXT para o aCols.                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Do While !FT_FEOF()
	cLinha := FT_FREADLN() //Leitura da linha
	If AllTrim(Upper(SubStr(cLinha, 1, 9))) == 'DELIMITED' .OR. AllTrim(Upper(cLinha)) == 'UNDELIMITED'
		If AllTrim(Upper(SubStr(cLinha, 1, 9))) == 'DELIMITED'
			lDelimited := .T.
			cDelimit := SubStr(cLinha, 11, 1)
			cSepara := SubStr(cLinha, 10, 1)
			nPosRet := Val(SubStr(cLinha, 12, 2))
		EndIf
		If FunName() == "CRDA035"
			If lDelimited != lArg
				If MsgYesNo( STR0037 + Chr(13) + STR0038 ) //"O arquivo de layout ้ incompativel com a op็ใo 'Usa Delimitador'." ### "Deseja desenhar um novo lay-out ?"
		 			FT_FUSE()			
					return
				Else
					return ( { "FIM" } )
				EndIf
			EndIf
		EndIf
		FT_FSKIP()
		cLinha := FT_FREADLN() //Leitura da linha
	EndIf
	
	AAdd( aRet,Array(9) )
	
	If lDelimited
		aRet[ Len(aRet) ][1] := Val(SubStr( cLinha,001,004 ))
		If Empty(cLinha)
			aRet[ Len(aRet) ][2] := Space(100)
		Else
			aRet[ Len(aRet) ][2] := SubStr( cLinha,005,030 )
		EndIf
		aRet[ Len(aRet) ][3] := SubStr( cLinha,035,001 )
		aRet[ Len(aRet) ][4] := SubStr( cLinha,036,003 )
		aRet[ Len(aRet) ][5] := Val(SubStr( cLinha,039,002 ))
		aRet[ Len(aRet) ][6] := SubStr( cLinha,041,150 )
		aRet[ Len(aRet) ][8] := lDelimited
		aRet[ Len(aRet) ][9] := .F.
	Else
		aRet[ Len(aRet) ][1] := Val(SubStr( cLinha,001,004 ))
		aRet[ Len(aRet) ][2] := Val(SubStr( cLinha,005,004 ))
		If Empty(cLinha)
			aRet[ Len(aRet) ][3] := Space(100)
		Else
			aRet[ Len(aRet) ][3] := SubStr( cLinha,009,030 )
		EndIf
		aRet[ Len(aRet) ][4] := SubStr( cLinha,039,001 )
		aRet[ Len(aRet) ][5] := SubStr( cLinha,040,003 )
		aRet[ Len(aRet) ][6] := Val(SubStr( cLinha,043,002 ))
		aRet[ Len(aRet) ][7] := SubStr( cLinha,045,150 )
		aRet[ Len(aRet) ][8] := lDelimited
		aRet[ Len(aRet) ][9] := .F.
	EndIf
	
	FT_FSKIP()
EndDo

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faco o fechamento do arquivo em uso.                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
FT_FUSE()
If valtype(aRecDelimited) == "A"
	Aadd(aRecDelimit, lDelimited)
	Aadd(aRecDelimit, cSepara)
	Aadd(aRecDelimit, cDelimit)
	Aadd(aRecDelimit, nPosRet)
EndIf
Return aRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035ChoicบAutor  ณFernando Salvatori  บ Data ณ  06/23/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Monta a enchoice do sistema.                               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035ChoiceBar(lDelimit)
Local oBar   //Objeto do Bar.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz a definicao da EnchoiceBar.                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE BUTTONBAR oBar SIZE 25,25 3D TOP OF oDlg

DEFINE BUTTON RESOURCE "S4WB005N" OF oBar ACTION NaoDisp() TOOLTIP STR0015//"Recortar..."
DEFINE BUTTON RESOURCE "S4WB006N" OF oBar ACTION NaoDisp() TOOLTIP STR0016//"Copiar..."
DEFINE BUTTON RESOURCE "S4WB007N" OF oBar ACTION NaoDisp() TOOLTIP STR0017//"Colar..."
DEFINE BUTTON RESOURCE "S4WB008N" OF oBar GROUP ACTION Calculadora() TOOLTIP STR0018
DEFINE BUTTON RESOURCE "S4WB009N" OF oBar ACTION Agenda() TOOLTIP STR0019//"Agenda..."
DEFINE BUTTON RESOURCE "S4WB010N" OF oBar ACTION OurSpool() TOOLTIP STR0020//"Gerenciador de Impresso..."
DEFINE BUTTON RESOURCE "S4WB016N" OF oBar GROUP  ACTION HelProg() TOOLTIP STR0021//"Help de Programa..."

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Carrego todos os registros do TXT para o aCols.                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE BUTTON oBtCon RESOURCE "EDIT"   OF oBar ACTION ( CA035Consulta() ) TOOLTIP STR0022//"Consulta de Campos (Ctrl + C)"
DEFINE BUTTON oBtSal RESOURCE "SALVAR" OF oBar ACTION ( IIf(CA035TudoOk(),CA035Salvar(lDelimit),NIL) ) TOOLTIP STR0023//"Salvar (Ctrl + G)"
DEFINE BUTTON oBtCan RESOURCE "CANCEL" OF oBar ACTION ( oDlg:End() ) TOOLTIP STR0024//"Sair (Ctrl + S)"

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035LinOkบAutor  ณFernando Salvatori  บ Data ณ  24/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Faz a inconsistencia da linha do aCols.                    บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035LinOk()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica o campo Retorno do sistema.                                    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !aTail( aCols[N] ) .And. Empty( aCols[N][3] )
	MsgStop( STR0009, STR0010 )//"Selecione um campo para o retorno"//"Aten็ใo"
	Return .F.
EndIf                    

Return .T.


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCA035ConsuบAutor  ณFernando Salvatori  บ Data ณ  24/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Efetua a consulta do dicionario de campos do MA6           บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035Consulta()
Local nX := 0    //Contador do sistema
Local oDlgCons   //Dialog da Listbox
Local oListBox   //Objeto da listbox

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Caso a Array estatica aList esteja em branco, faz o preenchimento, esta ณ
//ณ Array tem por objetivo, fazer o <Cache> dos campos da tabela MA6.       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty( aList )
	dbSelectArea( "SX3" )
	dbSetOrder( 1 )
	dbSeek( "MA6" )
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Faz a montagem da estrutura do MA6                                      ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Do While !Eof() .And. X3_ARQUIVO == "MA6"
		If X3USO( X3_USADO )
			AAdd( aList,{X3_TITULO,X3_CAMPO,X3_TAMANHO,X3_DECIMAL} )
		EndIf
		dbSkip()
	EndDo
	
EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Montagem da Tela.                                                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE MSDIALOG oDlgCons TITLE STR0025 FROM 9,0 TO 30,52 OF oMainWnd//"Estrutura do arquivo MA6 - Confec็ใo de Cart๕es"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Botao de saida.                                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DEFINE SBUTTON FROM 004,170 TYPE 2 ACTION (oDlgCons:End()) ENABLE OF oDlgCons

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Listbox.                                                                ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
@ .5,.7 LISTBOX oListBox VAR cListBox Fields HEADER STR0026,STR0027,STR0028,STR0029 SIZE 155,145//"Nome"//"Tํtulo"//"Tamanho"//"Decimais"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz a configuracao da ListBox.                                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
oListBox:SetArray(aList)
oListBox:bLine := { || { aList[oListBox:nAt,1],aList[oListBox:nAt,2],aList[oListBox:nAt,3],aList[oListBox:nAt,4]} }

ACTIVATE MSDIALOG oDlgCons CENTERED

Return
      

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCRDA035   บAutor  ณMicrosiga           บ Data ณ  25/06/2003 บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Ativa-Desativa as SetKeys                                  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CRDA035                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CA035SetKeys(lUsaKeys)
Default lUsaKeys := .F.

If lUsaKeys
	SetKey(3,oBtCon:bAction)
	SetKey(19,oBtCan:bAction)
	SetKey(7,oBtSal:bAction)
Else
	SetKey(3,NIL)
	SetKey(19,NIL)
	SetKey(7,NIL)
EndIf

Return
