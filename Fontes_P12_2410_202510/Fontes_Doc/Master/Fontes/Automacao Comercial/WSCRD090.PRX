#INCLUDE "wscrd090.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
         
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณGetLimcred ณ       Autor ณ   Viviane      ณ Data ณ21/07/2003ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณDefini็ใo de estruturas                                     ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

WSSTRUCT WSLimCred			// Declara็ใo da(s) Estrututra(s) ( opcionais ) 
             
	WSDATA Nome 		AS String 
	WSDATA LimCred		AS Float 
    WSDATA LimUsd		as Float
    WSDATA retorno      as String

ENDWSSTRUCT

WSSERVICE CrdLimCred DESCRIPTION STR0014    //"Servi็o de Consulta do Limite de Cr้dito (<b>Cr้dito</b>)"
	WSDATA UsrSessionID	AS String         
	WSDATA nTipo  		AS Integer
	WSDATA cEntrada    	AS String 
	WSDATA aRetorno     As ARRAY OF WSLimCred

	WSMETHOD GetLimCred
ENDWSSERVICE

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณGetLimCredณ 		 Autor ณ  Viviane       ณ Data ณ21/07/2003ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณRotina WS para consoultar o limite de cr้dito e quanto do   ณฑฑ
ฑฑณcr้dito foi gasto                                                      ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ 
ฑฑบRetorno  ณArray aRetorno                                               บฑฑ
ฑฑ			ณ 1 - Nome			     									  บฑฑ
ฑฑ			ณ 2 - Limite de Cr้dito										  บฑฑ
ฑฑ			ณ 3 - Limite usado											  บฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ 
ฑฑบParametroณEXPN1: Sessใo do usuแrio                                     บฑฑ
ฑฑบ         ณEXPN2: Tipo de Consulta                                      บฑฑ
ฑฑบ         ณEXPC3: Informa็ใo do cliente que o identifique, pode ser     บฑฑ
ฑฑบ         ณ      Cartao Dadalto, CPF,ou c๓digo de cliente               บฑฑ
ฑฑฬอออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSistema de Credito, Venda Assistida e Frontloja             บฑฑ
ฑฑศอออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ

*/
WSMETHOD GetLimCred WSRECEIVE UsrSessionID, nTipo, cEntrada WSSEND aRetorno WSSERVICE CrdLimCred
Local aRet		:= { 0, "", "", {} }
Local aDadosCli	:= {}
Local lRet 		:= .T.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

If !IsSessionVld( ::UsrSessionID )
	lRet := .F.
	Return .F.
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz a pesquisa do Limite e do limite usado do cliente               ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lRet
	aAdd( aDadosCli,{ ::nTipo, ::cEntrada } )
	
	
	aRet := WSCRD090( aDadosCli )
                                  
	If aRet[1] == 0
		::aRetorno:= Array(1)
		::aRetorno[1]				:= WSClassNew("WSLimCred")
		::aRetorno[1]:Retorno   :=aRet[4][1][1]  //retorno 0= ok, 1= nao encontrou registro
		::aRetorno[1]:Nome	    := aRet[4][1][2] //Nome do Cliente
		::aRetorno[1]:LimCred   := aRet[4][1][3] //Limite de cr้dito
		::aRetorno[1]:LimUsd	:= aRet[4][1][4] //Valor das parcelas em aberto/limite utilizado

	Else
		SetSoapFault(aRet[2], aRet[3])
		Return .F.
	Endif
Endif
                   
Return .T.



/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWSCrd090  บAutor  ณViviane Fernandes บ   Data ณ  08/07/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para bloqueio e desbloqueio do cartao                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSintaxe   ณaExp1 := WSCrd090( aExp2 )                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณaExp2 - Array contendo:                                     บฑฑ
ฑฑบ          ณ        [1] - Tipo da Consulta                              บฑฑ
ฑฑบ          ณ              1 = C๓digo + Loja                             บฑฑ
ฑฑบ          ณ              2 = CPF                                       บฑฑ
ฑฑบ          ณ              3 = Cartao Dadalto                            บฑฑ
ฑฑบ          ณ        [3] - Dado a consultar conforme parametro 1         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณaExp1 - Array contendo:                                     บฑฑ
ฑฑบ          ณ        [1] - nRetorno (0 = Ok, Diferente de zero = Erro)   บฑฑ
ฑฑบ          ณ        [2] - Nome do cliente quando [1] = 0                บฑฑ
ฑฑบ          ณ              Msg de Erro quando  [1] = 1                   บฑฑ
ฑฑบ          ณ        [3] - Limite de Cr้dito                             บฑฑ
ฑฑบ          ณ        [4] - Valor do limite usado                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบObservacaoณEssa rotina poderah ser executada via Web Service ou direta-บฑฑ
ฑฑบ          ณmente via Protheus                                          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSistema de Credito, Venda Assistida e Frontloja             บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function WSCrd090( aDadosCli )
Local aRet 		:= { 0, "", "", {} }
Local aRetLim   := {}
Local nTipo     := 0
Local cEntrada  := ""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPesquisa os limite conforme os parametros sugeridosณ
//  1 = codigo + loja
//  3 = CPF
//  5 = Pesquisa por n๚mero do cartใo
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู


nTipo   :=aDadosCli[1][1]
cEntrada:=aDadosCli[1][2]

// O tipo da pesquisa ้ iniciada digitando o n๚mero do cartแo == TIPO = 5
If nTipo == 5
	dbSelectArea("MA6")
	dbSetorder(1)
	If Dbseek(xFilial("MA6")+aDadosCli[1][2])
	   cEntrada := MA6->MA6_CODCLI+MA6->MA6_LOJA
	   nTipo    := 1
    EndIf
EndIf

dbSelectArea("SA1")
dbSetorder(nTipo)  // nTipo pode ser 1 ou 3, ou seja, ordenarแ por codigo ou por cpf
 If DbSeek(xFilial("SA1")+cEntrada)                               
 
 	MA7->( dbSetOrder( 1 ))
 	MA7->( dbSeek( xFilial( "MA7" ) + SA1->A1_COD + SA1->A1_LOJA ))
 	
    nLimUsado := CrdTitAberto(SA1->A1_COD,SA1->A1_LOJA) // calculo o n๚mero de presta็๕es em aberto do Cliente
	aAdd( aRetLim, { "0", SA1->A1_NOME, SA1->A1_LC+MA7->MA7_LC, nLimUsado } ) // adiciono no array de retorno. A primeira 
																  // a primeira posi็ใo sempre indicarแ
																  // 0 - encontrou registro do cliente
																  // 1 - nใo encontrou o cliente
Else
    aAdd( aRetLim, { "1",STR0015, 0, 0 } ) //"Cliente nใo foi encontrado na base."

EndIf	

aRet := { 0, "", "", aRetLim }
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAlimenta a array de retorno                                         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู*/

Return aRet 


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCRDLimCred บAutor  ณViviane Fernandes  บ Data ณ  11/06/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPermite a digitacao dos dados para busca, chama o servi็o   บฑฑ
ฑฑบ			 ณweb service e retorna os limite                             บฑฑ
ฑฑ           ณde cr้dito e o limite que jแ estแ sendo utilizado           บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSintaxe   ณCrdLimCred                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณnEXP1 - Nenhum                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณNenhum                                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGenerico / Integracao com o sistema de Credito              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function CRDLimCred()
Local aRet 			:= {}
Local oDlgCli
Local oDlgRes
Local aDadosCli 	:= {}
Local lCancela 		:= .F.
Local lRet 			:= .T.
Local lWSlimCrd 	:= .T.
Local aLimCred      := {}
Local nX

Local oSvc
Local cSoapFCode 
Local cSoapFDescr

Local aSize    := MsAdvSize( .F., .T., 200 )
Local aObjects := {}
Local aInfo	   := {}
Local aPosObj  := {}                               

Local cSenha		:= "******"

Static cUsrSessionID:=""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณinformacoes do cliente que foram  passados pela tela de digita็ใo          ณ
//ณ                                                      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aDadosCli := TelaLimCrd()
	nTipo     := aDadosCli[1][1]
	cEntrada  := aDadosCli[1][2]	
	

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz o login no Web Service se necessario                                  ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู				   
If !lCancela .And. Empty(cUsrSessionID)
	LJMsgRun(STR0016,, {|| cUsrSessionID := WSCrdLogin( cUserName, cSenha ) } )	// "Aguarde... Efetuando login no servidor ..."
Endif

If !lCancela

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณEstrutura da array aDadosCli                                              ณ
	//ณ[1] - Tipo da solicita็ใo : 1-codigo, 3-CPF , 5- Cartao Dadalto           ณ
	//ณ[2] - C๓digo+Loja  ou CPF ou Cartao Dadalto                               ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณEfetua a transacao WebService para pegar as informacoes do limite do      ณ
	//ณcliente                                                                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณChama a transacao Web Service para o Limite de Cr้dito Faz o tratamen-ณ
		//ณto se a transacao ainda estah ativa, caso contrario, faz novo login e     ณ
		//ณchama o metodo GetLimCred novamente                                       ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		oSvc := WSCRDLIMCRED():New()
		iIf(ExistFunc("LjWsGetAut"),LjWsGetAut(@oSvc),Nil) //Monta o Header de Autentica็ใo do Web Service
		
		lWSLimCrd := .T.
		While lWSLimcrd
     
			lRet := oSvc:GetLimCred( cUSRSESSIONID,nTipo,cEntrada )
			If !lRet
				cSvcError := GetWSCError()
				If Left(cSvcError,9) == "WSCERR048"
				
					cSoapFCode  := Alltrim(Substr(GetWSCError(3),1,At(":",GetWSCError(3))-1))
					cSoapFDescr := Alltrim(Substr(GetWSCError(3),At(":",GetWSCError(3))+1,Len(GetWSCError(3))))
					
					// Se necessario efetua outro login antes de chamar o metodo GetLimCred novamente
					If cSoapFCode $ "-1,-2,-3"
						cUsrSessionID := WSCrdLogin( cUserName, cSenha )
						lWSLimCrd := .T.
					Else
						MsgStop(cSoapFDescr, "Error " + cSoapFCode) 
						lWSLimCrd := .F.	// Nao chama o metodo GetLimCRD novamente 
					Endif
					
				Else
					MsgStop(GetWSCError(), STR0017 )	// "FALHA INTERNA"
    				lWSLimCrd := .F. // Nao chama o metodo GetLimCRD novamente 
				EndIf
			Else
				For nX := 1 to Len(oSvc:oWSGETLIMCREDRESULT:oWSWSLIMCRED)
				    aAdd( aLimCred, oSvc:oWSGETLIMCREDRESULT:oWSWSLIMCRED[nX]:cRetorno)
					aAdd( aLimCred, oSvc:oWSGETLIMCREDRESULT:oWSWSLIMCRED[nX]:cNome )
					aAdd( aLimCred, oSvc:oWSGETLIMCREDRESULT:oWSWSLIMCRED[nX]:nLIMCRED )
					aAdd( aLimCred, oSvc:oWSGETLIMCREDRESULT:oWSWSLIMCRED[nX]:nLIMUSD )
				Next		
				lWSLimCrd := .F.	// Nao chama o metodo GetLimCRD novamente 
			EndIf
		Enddo
		// se o retorno  for 0, o registro de cliente foi encontrado
      If aLimCred[1] = "0"
			Aadd( aObjects, { 100, 100, .T., .T., .F. } )
			aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 0, 0 }
			aPosObj := MsObjSize( aInfo, aObjects)
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณMontagem da Tela                                ณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
				DEFINE MSDIALOG oDlgCli FROM aSize[7],0 TO aSize[3]- 40,aSize[3];
				 TITLE STR0001 PIXEL // dados do cliente

		    @aPosObj[1,1] + 10, aPosObj[1,2] + 10  SAY STR0002	SIZE 50,8 of oDlgCli PIXEL //nome do cliente	
		    @aPosObj[1,1] + 10, aPosObj[1,2] + 60  SAY aLimCred[2] 	SIZE 50,8 of oDlgCli PIXEL 	
			
			
		    @aPosObj[1,1] + 25, aPosObj[1,2] + 10  SAY STR0003	SIZE 50,8 of oDlgCli PIXEL// Limite de cr้dito 	
		    @aPosObj[1,1] + 25, aPosObj[1,2] + 60  SAY aLimCred[3] 	PICTURE "@R 99999999999.99" SIZE 50,8 of oDlgCli PIXEL 		
		    
		    @aPosObj[1,1] + 40, aPosObj[1,2] + 10  SAY STR0004	SIZE 50,8 of oDlgCli PIXEL //Limite de cr้dito utilizado	
		    @aPosObj[1,1] + 40, aPosObj[1,2] + 60  SAY aLimCred[4] 	PICTURE "@R 99999999999.99" SIZE 50,8 of oDlgCli PIXEL 		
		    
	    	DEFINE SBUTTON FROM @aPosObj[1,1] + 55 ,@aPosObj[1,2] + 120 TYPE 01 ACTION (lCancela := .F.,oDlgCli:End()) ENABLE
		    
			ACTIVATE MSDIALOG oDlgCli CENTERED
	
	Else
 	          ApMsgAlert( aLimCred[2], STR0001) // DADOS DO CLIENTE
	
	EndIf
	               

EndIf 

Return .T.


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณTelaLimCrd บAutor  ณViviane Fernandes  บ Data ณ  11/06/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta a tela de digita็ใo de parametros para pesquisar lim. บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณNenhum                                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณaDadosTela                                                  บฑฑ
ฑฑบ          ณ 1 - Tipo de Pesquisa (1 ou 3 ou 5)                         บฑฑ
ฑฑบ          ณ 2 - Dado Digitado                                          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGenerico / Integracao com o sistema de Credito              บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function TelaLimCrd

Local oDlgCli
Local oCombo
Local oMotivo
Local aItems	:= {STR0018,STR0019,STR0020}	// "Codigo/Loja","CPF","cartao"
Local cCombo
Local nTipo   	:= ""  
Local cSolicit 	:=""                  
Local cCartao  	:= ""
// Nesta variแvel serแ armazenado os dados digitados na tela
Local aDadosTela       :={}

Local aSize    := MsAdvSize( .F., .T., 200 )
Local aObjects := {}
Local aInfo	   := {}
Local aPosObj  := {}                               
Local cDado  := space(16)  
Local nPesq  := 1

Aadd( aObjects, { 100, 100, .T., .T., .F. } )
aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 0, 0 }
aPosObj := MsObjSize( aInfo, aObjects)


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณMontagem da Tela                                ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู	
//	DEFINE MSDIALOG oDlgCli FROM 0,0 TO 130,330 TITLE "Dados do cliente" PIXEL
		DEFINE MSDIALOG oDlgCli FROM aSize[7],0 TO aSize[3] - 50,aSize[3];
				 TITLE STR0001 PIXEL

    @aPosObj[1,1] + 10, aPosObj[1,2] + 10  SAY STR0005	SIZE 50,8 of oDlgCli PIXEL // Consulta por	
        oCombo:= tComboBox():New( @aPosObj[1,1] + 10 , aPosObj[1,2] + 60  ,{|u| if( Pcount( )>0, cCombo:= u, cCombo ) },; 
				aItems,45,45,oDlgCli,,{||IIf(cCombo==STR0018,nPesq:=1,IIf(cCombo==STR0019,nPesq:=3,nPesq:=5))},;
			   	,,,.T.,,,,,,,,,"cCombo")   // "Cliente/Loja" ### "CPF"
	
	@aPosObj[1,1] + 25, aPosObj[1,2] + 10 SAY  STR0006 SIZE 55,7 PIXEL OF oDlgCli // Dado a pesquisar
   	@aPosObj[1,1] + 25, aPosObj[1,2] + 60 MSGET oCartao VAR cDado PICTURE "@R 9999999999999999" SIZE 80,9 PIXEL OF oDlgCli		   		

	
	DEFINE SBUTTON FROM @aPosObj[1,1] + 50 ,@aPosObj[1,2] + 100 TYPE 01 ACTION (lCancela := .F.,oDlgCli:End()) ENABLE
	DEFINE SBUTTON FROM @aPosObj[1,1] + 50 ,@aPosObj[1,2] + 130 TYPE 02 ACTION (lCancela := .T.,oDlgCli:End()) ENABLE
	
	ACTIVATE MSDIALOG oDlgCli CENTERED
	               
aAdd(aDadosTela, {nPesq,cDado})	
Return(aDadosTela)

