#INCLUDE "WSCRD110.CH"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "CRDDEF.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณGetStatus  ณ Autor ณ Viviane Fernandes    ณ Data ณ04/02/2004ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณ Servico consulta o status do cliente, refente a sua analiseณฑฑ
ฑฑณ          ณ de bloquei ou desbloqueio de credito						  ณฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณInterfaces de venda	                       		          ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ                Alteracoes desde sua criacao                           ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ   DATA   ณ Programador   ณManutencao Efetuada                         ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ04/07/05  ณMachima        ณDesenvolvimento dos metodos para consultar  ณฑฑ
ฑฑณ          ณBOPS 081261    ณstatus do contrato e pesquisar o numero do  ณฑฑ 
ฑฑณ          ณ               ณcartao Private Label ou CNPJ/CPF do cliente.ณฑฑ 
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

WSSTRUCT WSSTATUS
	WSDATA Situacao		AS String
	WSDATA Mensagem		AS String
ENDWSSTRUCT

WSSTRUCT WSINFCLI
	WSDATA CNPJCPF		AS String OPTIONAL
	WSDATA NumCart		AS String OPTIONAL
ENDWSSTRUCT

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณEstrutura dos dados do computador do caixa requisitanteณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
WSSTRUCT ComputerDataView
	WSDATA NameOrIP			AS String OPTIONAL	//Nome ou IP da maquina do caixa
	WSDATA Port				AS String			//Porta de comunicacao
	WSDATA Environment		AS String			//Environment
ENDWSSTRUCT

WSSERVICE CRDSTATUS DESCRIPTION STR0006   //"Servi็o de Consulta da Anแlise de Cr้dito (<b>Cr้dito</b>)"
	WSDATA UsrSessionID	AS String         
	WSDATA Codcli			As String
	WSDATA Loja				As String 	
	WSDATA Contrato         As String OPTIONAL
	WSDATA StContrato       AS String OPTIONAL
	WSDATA aStatus			AS WSSTATUS
	WSDATA aInfCli			AS WSINFCLI OPTIONAL
	WSDATA CustomerCode			As String		//Codigo do Cliente
	WSDATA UnitCustomerCode		As String		//Loja do Cliente
	WSDATA ComputerData			AS ComputerDataView
	WSDATA WSNULL				AS String

	WSMETHOD GetStatus
	WSMETHOD GetStContr
	WSMETHOD GetInfCli	
	WSMETHOD PutComputerData
ENDWSSERVICE
	
        
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetStatus บAutor  ณ Luiz Felipe Couto  บ Data ณ  15/06/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Este metodo tem por finalidade verificar o status da       บฑฑ
ฑฑบ          ณ analise do credito.                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParam.    ณ ExpC1: Id do caixa                                         บฑฑ
ฑฑบ          ณ ExpC2: Codigo do Cliente                                   บฑฑ
ฑฑบ          ณ ExpC3: Loja do Cliente                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SigaCRD                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	  บฑฑ
ฑฑฬออออออออออุออออออออัออออออุออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD GetStatus WSRECEIVE UsrSessionID, Codcli, Loja WSSEND aStatus WSSERVICE CRDSTATUS

Local aRet	:= { "", "" }					//Retorno da avaliacao do crediario
Local lRet 	:= .T.							//Retorno da funcao

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !IsSessionVld( ::UsrSessionID )
	lRet := .F.
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz a pesquisa do status do cliente			                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lRet
	aRet 				:= WSCRD110( ::CodCli, ::Loja )

	::aStatus			:= WSClassNew( "WSStatus" )
	::aStatus:Situacao	:= aRet[1]
	::aStatus:Mensagem	:= aRet[2]
Endif

Return lRet
                
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWSCRD110  บAutor  ณ Luiz Felipe Couto  บ Data ณ  02/06/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Esta funcao tem por finalidade verificar o status da       บฑฑ
ฑฑบ          ณ avaliacao do crediario.                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParam.    ณ ExpC1: Codigo do Cliente                                   บฑฑ
ฑฑบ          ณ ExpC2: Loja do Cliente                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SigaCRD                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	  บฑฑ
ฑฑฬออออออออออุออออออออัออออออุออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function WSCRD110( cCliente, cLoja )

Local cStatus 	:= "" 					//Status de bloqueio
Local cMsg		:= "" 				 	//Msg a ser exibida ao caixa que esta atendendo
Local aRet		:= { "", "" }			//Retorno da Funcao

DbSelectArea( "MA7" )
DbSetOrder( 1 )

If DbSeek( xFilial( "MA7" ) + cCliente + cLoja )
	Do Case
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCredito Bloqueadoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Case MA7->MA7_BLOQUE == "1"
			cStatus 	:= "1"
			cMsg		:= STR0001 //"Cr้dito bloqueado. Aguarde Libera็ใo..."
			
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCredito Liberadoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Case MA7->MA7_BLOQUE == "2"
			cStatus 	:= "2"
			cMsg		:= STR0002 //"Cr้dito Liberado para efetuar a compra."
			
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCredito Rejeitadoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Case MA7->MA7_BLOQUE == "3"
			cStatus 	:= "3"
			cMsg		:= STR0003 //"Cr้dito Rejeitado para efetuar a compra"
			
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCredito Nao Liberadoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Case MA7->MA7_BLOQUE == "4"
			cStatus 	:= "4"
			cMsg		:= STR0004 //"Cr้dito nใo liberado."+ CHR(13)+ "Encaminhar o Cliente para o setor de Crediแrio"
	EndCase
	
Else
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCliente nao encontradoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cStatus 	:= "5"
	cMsg		:= STR0005 //Cliente nใo encontrado
Endif

aRet[1] := cStatus
aRet[2] := cMsg

Return aRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณGetStContr ณ Autor ณ Fernando Machima     ณ Data ณ04/02/2004ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณPesquisa o status do contrato                               ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD GetStContr WSRECEIVE UsrSessionID, Contrato, CodCli, Loja WSSEND StContrato WSSERVICE CRDSTATUS
Local lRet 		:= .T.              //Controla se prossegue a operacao
Local cStatus   := ""               //Status do contrato

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !IsSessionVld( ::UsrSessionID )
	lRet := .F.
    ::StContrato := ""
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz a pesquisa do status do contrato                                ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lRet	
	cStatus := WSCRD111( ::Contrato, ::CodCli, ::Loja ) 
    ::StContrato := cStatus
Endif

Return (lRet)
                 
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWSCrd111  บAutor  ณViviane Fernandes   บ Data ณ  10/02/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para consulta do status do conttrato                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณStatus do contrato 										  บฑฑ
ฑฑบ          ณ1 - OK                                                      บฑฑ 
ฑฑบ          ณ2 - Pendente        										  บฑฑ
ฑฑบ          ณ3 - Liberado 												  บฑฑ
ฑฑบ          ณ4 - Rejeitado                                               บฑฑ
ฑฑบ          ณ5 - Fila do crediario                                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณSistema de Credito                        				  บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function WSCRD111( cContrato, cCodCli, cLoja )
Local cStatus   :=  ""    //Status do contrato

dbSelectArea("MAH")
dbSetOrder(1)      
If DbSeek(xFilial("MAH")+cContrato)
   If MAH->MAH_CODCLI+MAH->MAH_LOJA == cCodCli+cLoja
      cStatus := MAH->MAH_STATUS 
   EndIf   
EndIf

Return (cStatus)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณMetodo	 ณGetInfCli  ณ Autor ณ Fernando Machima     ณ Data ณ21/06/2005ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescricao ณPesquisa os dados(CNPJ/CPF  e numero do cartao) do cliente  ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD GetInfCli WSRECEIVE UsrSessionID, CodCli, Loja WSSEND aInfCli WSSERVICE CRDSTATUS
Local lRet 		:= .T.
Local aRet      := {"",""}

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !IsSessionVld( ::UsrSessionID )
	lRet := .F.
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFaz a pesquisa do CNPJ/CPF e/ou numero do cartao Private Label      ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lRet	
	aRet := WSCRD112( ::CodCli, ::Loja ) 
	If !Empty(aRet[1]) .OR. !Empty(aRet[2])
		::aInfCli:CNPJCPF	:= aRet[1]
		::aInfCli:NumCart	:= aRet[2]	
	Else	
		::aInfCli:CNPJCPF	:= ""
		::aInfCli:NumCart	:= ""
		lRet  := .F.
	EndIf
Endif

Return (lRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณWSCRD112  บAutor  ณFernando Machima    บ Data ณ  02/06/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณBusca o CNPJ/CPF e numero do cartao do cliente              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSintaxe   ณWSCRD112(ExpC1, ExpC2)                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Codigo do cliente                                   บฑฑ
ฑฑบ          ณExpC2 - Loja do cliente                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณArray contendo                                              บฑฑ
ฑฑบ          ณ 1 - CNPJ/CPF                                               บฑฑ
ฑฑบ          ณ 2 - Numero do cartao ativo                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณInterfaces de Venda                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function WSCRD112( cCodCli, cLoja )

Local aRet       := {"",""}             //Retorno da funcao [1]-CNPJ/CPF [2]-Numero do cartao
Local lFoundMA6  := .F.                 //Controla se achou cartao ativo do cliente

dbSelectArea("SA1")
dbSetOrder(1)
If DbSeek(xFilial("SA1")+cCodCli+cLoja)
   aRet[1]  := SA1->A1_CGC
EndIf   
//Busca cartao ativo do cliente
DbSelectArea("MA6")
DbSetOrder(2)
DbSeek(xFilial("MA6")+cCodCli+cLoja)
While !MA6->(Eof()) .AND. xFilial("MA6")+cCodCli+cLoja == ;
   MA6->MA6_FILIAL+MA6->MA6_CODCLI+MA6->MA6_LOJA .AND. !lFoundMA6
                        
   If MA6->MA6_SITUA <> "1"
      DbSkip()  
      Loop
   EndIf   
                        
   aRet[2]    := MA6->MA6_NUM
   lFoundMA6  := .T.
End 

Return (aRet)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPutComputeบAutor  ณ Luiz Felipe Couto  บ Data ณ  02/06/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Este metodo tem por finalidade enviar os dados do caixa    บฑฑ
ฑฑบ          ณ requisitante para o servidor de avaliadores.               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParam.    ณ ExpC1: Id do caixa                                         บฑฑ
ฑฑบ          ณ ExpC2: Codigo do Cliente                                   บฑฑ
ฑฑบ          ณ ExpC3: Loja do Cliente                                     บฑฑ
ฑฑบ          ณ ExpC4: Dados do Computados do caixa                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ SigaCRD                                                    บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณ Data/Bops/Ver ณManutencao Efetuada                      	  บฑฑ
ฑฑฬออออออออออุออออออออัออออออุออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
WSMETHOD PutComputerData WSRECEIVE UsrSessionID, CustomerCode, UnitCustomerCode, ComputerData WSSEND WSNULL WSSERVICE CrdStatus

Local cCliente	:= ""					//Cliente
Local cLoja		:= ""					//Loja
Local cIPRPC	:= ""					//IP para RPC
Local cPortaRPC	:= ""					//Porta para RPC
Local cEnvRPC	:= ""					//Environment para RPC
Local lRetorno	:= .T.					//Retorno da Funcao
Local aRet		:= {}					//Array de retorno

DEFAULT ::ComputerData:NameOrIP := IIF( Empty( HttpHeadIn->REMOTE_ADDR ), "(unknown)", HttpHeadIn->REMOTE_ADDR )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica a validade e integridade do ID de login do usuario         ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !IsSessionVld( ::UsrSessionID )
	lRetorno := .F.
Endif

If lRetorno
	cCliente 	:= ::CustomerCode
	cLoja 		:= ::UnitCustomerCode
	cIPRPC 		:= ::ComputerData:NameOrIP
	cPortaRPC 	:= ::ComputerData:Port
	cEnvRPC 	:= ::ComputerData:Environment
	
	aRet := WSCRD113( cCliente, cLoja, cIPRPC, cPortaRPC, cEnvRPC )
	lRetorno := (aRet[1]=0)		//O retorno ้ 0 se OK e 1 se Erro em aRet[1]
	If !lRetorno
		SetSoapFault( "PUTCOMPUTERDATA", aRet[3] )
	EndIf
	
Endif

Return lRetorno


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบFuncao    ณWSCRD113  บAutor  ณMarisa V. N. Cruz   บ Data ณ  08/08/19   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณGravo os dados IP/PORTA na tabela MA7                       บฑฑ
ฑฑบ          ณ(Desmembramento do m้todo PutComputerData)                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบSintaxe   ณWSCRD113(ExpC1, ExpC2, ExpC3, ExpC4, ExpC5)                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Codigo do cliente                                  บฑฑ
ฑฑบ          ณExpC2 - Loja do cliente                                    บฑฑ
ฑฑบ          ณExpC3 - Endere็o IP do RP                                  บฑฑ
ฑฑบ          ณExpC4 - Porta do RPC                                       บฑฑ
ฑฑบ          ณExpC5 - Nome do Environment (Ambiente) RPC                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณArray contendo                                              บฑฑ
ฑฑบ          ณ 1 - Retorno (0-OK,1-Erro)                                  บฑฑ
ฑฑบ          ณ 2 - Aviso para Pop-Up                                      บฑฑ
ฑฑบ          ณ 3 - Mensagem de Erro                                       บฑฑ
ฑฑบ          ณ 4 - IP, Porta e Ambiente em Elemento de Array              บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณInterfaces de Venda                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

//-------------------------------------------------------------------
/*/{Protheus.doc} WSCRD113
Gravo os dados IP/PORTA na tabela MA7 (Desmembramento do m้todo PutComputerData)
@type function

@author Marisa V. N. Cruz
@since 08/08/2019
@version P12

@param cCliente, 	caracter, C๓digo do Cliente
@param cLoja, 		caracter, Loja do Cliente
@param [cIPRPC], 	caracter, Endere็o IP do RPC
@param [cPortaRPC], caracter, Porta do RPC
@param [cEnvRPC], 	caracter, Nome do Environment (Ambiente) RPC

@return array {Retorno (0-OK,1-Erro), Aviso para Pop-Up, Mensagem de Erro, {IP, Porta, Ambiente em Elemento de Array}}
/*/
//-------------------------------------------------------------------
Function WSCRD113( cCliente, cLoja, cIPRPC, cPortaRPC, cEnvRPC )

Local aRet       := {1,"","",{"","",""}}             //Retorno da funcao [1]-Retorno [2]-Aviso [3]-Mensagem de Erro [4]-IP,Porta e Ambiente em Elemento de Array
Local aArea := GetArea()

Default cIPRPC    := LjGetStation("RPCSRV")
Default cPortaRPC := LjGetStation("RPCPORT")
Default cEnvRPC   := LjGetStation("RPCENV")

conout( "***** PutComputerData *****" )
conout( "IP: " + cIPRPC )
conout( "Porta: " + cPortaRPC )
conout( "Env: " + cEnvRPC )
conout( "***************************" )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณGrava os dados para RPCณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea( "MA7" )
MA7->( DbSetOrder( 1 ) )

If MA7->( DbSeek( xFilial( "MA7" ) + PadR( cCliente, 6 ) + PadR( cLoja, 2 ) ) )
	If 	MA7->( FieldPos( "MA7_IPRPC" ) ) > 0 .AND. MA7->( FieldPos( "MA7_PORRPC" ) ) > 0 .AND.;
		MA7->( FieldPos( "MA7_ENVRPC" ) ) > 0

		If RecLock( "MA7", .F. )
			MA7->MA7_IPRPC 		:= cIPRPC
			MA7->MA7_PORRPC 	:= cPortaRPC
			MA7->MA7_ENVRPC 	:= cEnvRPC
			MA7->( MsUnlock() )
			aRet := { 0, "", "", {cIPRPC,cPortaRPC,cEnvRPC} }
		Else
			aRet := { 1, STR0007, STR0008, {"","",""}}		//"Erro"###"Falha ao alterar dados na tabela de Cr้dito (MA7)"
		Endif
	Else
		conout( "***** PutComputerData *****" )
		conout( "Favor criar os seguintes campos na base de dados de acesso do crediario:" )
		conout( "------------------------" )
		conout( "MA7_IPRPC : Caracter" )
		conout( "Tipo: Caracter" )
		conout( "Tamanho: 15" )
		conout( "Descricao: IP para RPC" )
		conout( "------------------------" )
		conout( "MA7_PORRPC" )
		conout( "Tipo: Caracter" )
		conout( "Tamanho: 5" )
		conout( "Descricao: Porta para RPC" )
		conout( "------------------------" )
		conout( "MA7_ENVRPC" )
		conout( "Tipo: Caracter" )
		conout( "Tamanho: 100" )
		conout( "Descricao: Environment para RPC" )
		conout( "************************" )
		
		aRet := { 1, STR0009, STR0010, {"","",""}}		//"Atualizar tabelas"###"ษ necessแrio atualiza็ใo da base de dados de acesso ao crediแrio (MA7). Favor contatar o departamento de infra-estrutura."
	Endif
Endif

RestArea(aArea)

Return (aRet)

