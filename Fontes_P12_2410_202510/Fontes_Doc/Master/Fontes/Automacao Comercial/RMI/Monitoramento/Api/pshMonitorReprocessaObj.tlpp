#include "PROTHEUS.CH"
#include "PSHMONITORREPROCESSAOBJ.CH"
#include "tlpp-core.th"

/*/{Protheus.doc} PshMonitorReprocessa
Classe responsavel em reprocessar alterar o status dos registro da tabela MIP para reprocessamento.
@author  Bruno Almeida
@since   11/06/2024
@version 1.0
/*/
Class PshMonitorReprocessa

    Private Data jRegistros as Object

	Public Method New()
    Public Method Reprocessando()

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe. 
@author  Bruno Almeida
@since   11/06/2024
@version 1.0
/*/
Method New(oRest as Object) Class PshMonitorReprocessa

Self:jRegistros := JsonObject():New()
Self:jRegistros:FromJson(oRest:getBodyRequest())

Return Self

/*/{Protheus.doc} Reprocessando
Metodo que faz a consulta com base nos campos Uuid, Loja e Pdv para alterar o status do registro da tabela MIP.
@author  Bruno Almeida
@since   11/06/2024
@version 1.0
/*/
Method Reprocessando() Class PshMonitorReprocessa

Local nI        := 0
Local jRet      := JsonObject():New()
Local nSucesso  := 0
Local cQuery    := ""
Local cAlias    := ""

For nI := 1 To Len(Self:jRegistros)
    cQuery := ""
    cQuery := "SELECT R_E_C_N_O_ REC"
    cQuery += "  FROM " + RetSqlName("MIP")
    cQuery += " WHERE MIP_FILIAL = '" + Self:jRegistros[nI]["loja"] + "'"
    cQuery += "   AND MIP_UUID = '" + Self:jRegistros[nI]["uuid"] + "'"
    cQuery += "   AND MIP_PDV = '" + Self:jRegistros[nI]["pdv"] + "'"

    cAlias := GetNextAlias()
    DbUseArea(.T., "TOPCONN", TcGenQry( , , cQuery), cAlias, .T., .F.)

    If !(cAlias)->( Eof() )
        MIP->(dbGoto((cAlias)->REC))
        If MIP->(RecLock("MIP",.F.,,,IsBlind()))
            MIP->MIP_STATUS := "1"
            MIP->MIP_TENTAT := "0"
            MIP->( MsUnLock() )
            nSucesso++
        EndIf        
    EndIf
Next nI

If nSucesso > 0
    jRet["status"] := STR0001 + cValToChar(nSucesso) //"Resgistro(s) reprocessado(s) com sucesso! Quantidade de registros: "
Else
    jRet["status"] := STR0002 //"Nenhum registro foi reprocessado, por favor, verifique o conteúdo dos campos Uuid, Loja e Pdv."
EndIf

Return jRet:ToJson()

