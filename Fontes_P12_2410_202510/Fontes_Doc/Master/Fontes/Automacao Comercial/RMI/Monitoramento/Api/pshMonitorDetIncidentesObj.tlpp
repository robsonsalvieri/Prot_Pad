#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

class pshMonitorDetIncidentesObj

	Public Method New()                 as Object
	Public Method SetDetIncidentes()    as Variant
    Public Method GetJsonReturn()       as Object

    Private Method GetDetalhes()        as Array

    Public Data oJsonRet        as Object
    Public Data oJQueryRequest  as Object
    Public Data cId             as Character

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author Eduardo Sales
@since  06/06/2024
/*/
Method New(oRest as Object) as Object Class pshMonitorDetIncidentesObj

    Self:oJQueryRequest := JsonObject():New()
    Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())
    Self:cId := AlLTrim(Self:oJQueryRequest['uuid'])
    Self:cId := StrTran(Self:cId, "/undefined", "")

    Self:oJsonRet   := JsonObject():New()
    Self:SetDetIncidentes()

Return Self

/*/{Protheus.doc} SetDetIncidentes
Seta o JSON de retorno
@author Eduardo Sales
@since  06/06/2024
/*/
Method SetDetIncidentes() Class pshMonitorDetIncidentesObj

    Local aDetalhes := Self:GetDetalhes() as Array

    Self:oJsonRet['uuid']       := aDetalhes[1]
    Self:oJsonRet['loja']       := aDetalhes[2]
    Self:oJsonRet['tabela']     := aDetalhes[3]
    Self:oJsonRet['chave']      := aDetalhes[4]
    Self:oJsonRet['desc']       := aDetalhes[5]
    Self:oJsonRet['pdv']        := aDetalhes[6]
    Self:oJsonRet['status']     := aDetalhes[7]
    Self:oJsonRet['dtgeracao']  := aDetalhes[8]
    Self:oJsonRet['hrgeracao']  := aDetalhes[9]
    Self:oJsonRet['dtenvio']    := aDetalhes[10]
    Self:oJsonRet['hrenvio']    := aDetalhes[11]

Return

/*/{Protheus.doc} GetJsonReturn
Retorna o JSON de Retorno da API
@author Eduardo Sales
@since  06/06/2024
/*/
Method GetJsonReturn() as Object Class pshMonitorDetIncidentesObj
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} GetDetalhes
Retorna os detalhes do registro de integração informado
@author Eduardo Sales
@since  06/06/2024
/*/
Method GetDetalhes() as Array Class pshMonitorDetIncidentesObj

    Local cQuery    := ""   as Character
    Local cAliasQry := ""   as Character
    Local aDetalhes := {}   as Array
    Local cDescErro := ""   as Character
    Local cTabela   := ""   as Character
    Local cDatGer   := ""   as Character
    Local cDatPro   := ""   as Character
    Local cHorPro   := ""   as Character

    cQuery += " SELECT "                                                                            + CRLF
    cQuery += "     MIP.MIP_UUID AS UUID "                                                          + CRLF
    cQuery += "     ,MIP.MIP_FILIAL AS FILIAL "                                                     + CRLF
    cQuery += "	    ,MIP.MIP_CPROCE AS TABELA "                                                     + CRLF
    cQuery += "	    ,MIP.MIP_CHVUNI AS CHVUNI "                                                     + CRLF
    cQuery += "     ,MHL.R_E_C_N_O_ AS REC "                                                        + CRLF
    cQuery += "	    ,MIP.MIP_PDV AS PDV "                                                           + CRLF
    cQuery += "	    ,MIP.MIP_STATUS AS DESC_STATUS "                                                + CRLF
    cQuery += "	    ,MHQ.MHQ_DATGER AS DATGER "                                                     + CRLF
    cQuery += "	    ,MHQ.MHQ_HORGER AS HORGER "                                                     + CRLF
    cQuery += "	    ,MIP.MIP_DATPRO AS DATPRO "                                                     + CRLF
    cQuery += "	    ,MIP.MIP_HORPRO AS HORPRO "                                                     + CRLF
    cQuery += " FROM "                                                                              + CRLF
    cQuery += "	    " + RetSqlname("MIP") + " MIP "                                                 + CRLF
    cQuery += "	LEFT JOIN " + RetSqlname("MHL") + " MHL     ON MHL.MHL_FILIAL = MIP.MIP_FILIAL "    + CRLF
    cQuery += "							                    AND MHL.MHL_UIDORI = MIP.MIP_UUID "     + CRLF
    cQuery += " "                                                                                   + CRLF
    cQuery += "	INNER JOIN " + RetSqlname("MHQ") + " MHQ	ON MHQ.MHQ_UUID = MIP.MIP_UIDORI "      + CRLF
    cQuery += " WHERE 1 = 1 "                                                                       + CRLF
    cQuery += "     AND MIP.MIP_UUID = '" + Self:cId + "' "                                         + CRLF

    cAliasQry := MPSysOpenQuery(cQuery)

    If !Empty((cAliasQry)->REC)
        DbSelectArea("MHL")
        MHL->(DbGoTo((cAliasQry)->REC))
        cDescErro := MHL->MHL_ERROR
        MHL->(DbCloseArea())
    EndIf

    cTabela := AllTrim((cAliasQry)->TABELA)
    cTabela := SubStr(cTabela, Len(cTabela)-2, Len(cTabela))

    cDatGer := IIf(Empty((cAliasQry)->DATGER),"", SubStr((cAliasQry)->DATGER, 1, 4) + "-" + SubStr((cAliasQry)->DATGER, 5, 2) + "-" + SubStr((cAliasQry)->DATGER, 7, 2))
    cDatPro := IIf(Empty((cAliasQry)->DATPRO),"", SubStr((cAliasQry)->DATPRO, 1, 4) + "-" + SubStr((cAliasQry)->DATPRO, 5, 2) + "-" + SubStr((cAliasQry)->DATPRO, 7, 2))
    cHorPro := IIf(Empty((cAliasQry)->HORPRO), "", SubStr(AllTrim((cAliasQry)->HORPRO),1,8))

    aAdd(aDetalhes, AllTrim((cAliasQry)->UUID))
    aAdd(aDetalhes, AllTrim((cAliasQry)->FILIAL))
    aAdd(aDetalhes, AllTrim(cTabela))
    aAdd(aDetalhes, AllTrim((cAliasQry)->CHVUNI))
    aAdd(aDetalhes, AllTrim(cDescErro))
    aAdd(aDetalhes, AllTrim((cAliasQry)->PDV))
    aAdd(aDetalhes, AllTrim((cAliasQry)->DESC_STATUS))
    aAdd(aDetalhes, cDatGer)
    aAdd(aDetalhes, AllTrim((cAliasQry)->HORGER))
    aAdd(aDetalhes, cDatPro)
    aAdd(aDetalhes, cHorPro)

Return aDetalhes
