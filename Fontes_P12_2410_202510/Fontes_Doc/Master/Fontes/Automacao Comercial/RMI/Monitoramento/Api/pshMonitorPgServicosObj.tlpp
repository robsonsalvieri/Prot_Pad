
#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'parmtype.ch'

class pshMonitorPgServicos

    Public method New()                     as Object
    Public Method GetJsonReturn()           as Json
    Public Method SetAssinante()            as Variant
    Public Method Executa()                 as Variant
    Public Method SetaParamApi()            as Variant
    Public Method GetStatusServHeader()     as Variant 
    Public Method FechaClasse()             as Variant 

    Private Method GetServicoStatus()       as Character    
    Private Method MontaQuery()             as Variant
    Private Method RetWhere()               as Character
    Private Method MapFields()              as Variant
    Private Method RetOrderBy()             as Character
    Private Method ResultZero()             as Variant
    Private Method GetDataHoraServHeader()  as Array

    Public Data oJsonRet        as Json

    Private Data oJQueryRequest     as Json
    Private Data cAssinante         as Character
    Private Data cQuery             as Character
    Private Data cFilFiltro         as Character
    Private Data cStatus            as Character
    Private Data cServico           as Character
    Private Data cEstacao           as Character
    Private Data cReport            as Character
    Private Data nPage              as Numeric
    Private Data nPageSize          as Numeric
    Private Data cSearch            as Character
    Private Data oQuery             as Object
    Private Data cOrder             as Character

endclass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  14/06/2024
/*/
Method New() as Object Class pshMonitorPgServicos

Self:oJsonRet       := JsonObject():New()
Self:cAssinante     := ""
Self:cQuery         := ""
Self:cFilFiltro     := ""
Self:cStatus        := ""
Self:cServico       := ""
Self:cEstacao       := ""
Self:cReport        := ""
Self:nPage          := 0
Self:nPageSize      := 0
Self:cSearch        := ""
Self:oQuery         := fwAdapterBaseV2():new('GET'     ,  .T.)
Self:cOrder         := ""

Return Self

/*/{Protheus.doc} GetJsonReturn
Retorno da classe
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method GetJsonReturn() as Json Class pshMonitorPgServicos
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} GetJsonReturn
Retorno da classe
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method FechaClasse() Class pshMonitorPgServicos

FwFreeObj(Self:oJsonRet)
Self:oJsonRet := Nil

FwFreeObj(Self:oQuery)
Self:oQuery := Nil

Return

/*/{Protheus.doc} GetJsonReturn
Define o Assinante
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method SetAssinante(cAssinante as Character) Class pshMonitorPgServicos
Self:cAssinante := cAssinante
Return

/*/{Protheus.doc} GetJsonReturn
Retorno da classe
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method GetStatusServHeader() Class pshMonitorPgServicos
Local aAreaMHO          := MHO->(GetArea("MHO"))    as Array
Local aDataHoraServHeader   := {}                   as Array
Local cStatusPublica    := ""                       as Character
Local cStatusEnviaPdv   := ""                       as Character

aDataHoraServHeader := Self:GetDataHoraServHeader()

cStatusPublica  := Self:GetServicoStatus(aDataHoraServHeader[1])
cStatusEnviaPdv := Self:GetServicoStatus(aDataHoraServHeader[2])

Self:oJsonRet['RmiPublica'] := cStatusPublica
Self:oJsonRet['RmiEnviaPdv']   := cStatusEnviaPdv

RestArea(aAreaMHO)

Return

/*/{Protheus.doc} GetJsonReturn
Retorno da classe
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method GetDataHoraServHeader() as Array Class pshMonitorPgServicos
Local aRetDataHora  := {"",""}  as Array
Local cQuery        := ""       as Character
Local cAliasMIW     := ""       as Character

cQuery += " SELECT MIW_SERVIC, MIW_DATA, MIW_HORA "
cQuery += " FROM " + RetSqlName("MIW") + "  "
cQuery += " WHERE SUBSTRING(MIW_SERVIC,1,11) IN ('RMIPUBLICA_','RMIENVIAPDV') "
cQuery += " AND D_E_L_E_T_ = ' ' "

cAliasMIW := MPSysOpenQuery(cQuery)

(cAliasMIW)->(dbGoTop())

While (cAliasMIW)->(!Eof())

    If  "RMIPUBLICA" $ Upper(AllTrim( (cAliasMIW)->MIW_SERVIC ))
        aRetDataHora[1] := (cAliasMIW)->MIW_DATA + " " + (cAliasMIW)->MIW_HORA
    EndIf

    If "RMIENVIAPDV" $ Upper(AllTrim( (cAliasMIW)->MIW_SERVIC ))
        aRetDataHora[2] := (cAliasMIW)->MIW_DATA + " " + (cAliasMIW)->MIW_HORA
    EndIf

    (cAliasMIW)->(dbSkip())

EndDo

(cAliasMIW)->( dbCloseArea() )

Return aRetDataHora

/*/{Protheus.doc} GetServicoStatus
Retorna o stauts do serviço
* Considerando como Inativo o Ponto de Integraçao que esta a mais de 5 minutos sem conectar na Retaguarda
@author joao.marcos
@since  14/06/2024
/*/
Method GetServicoStatus(cDataHora as Character) as Character Class pshMonitorPgServicos
Local cServicoStatus    := "Ativo"  as Character
Local aDataHoraUltConn  := {}       as Array        // Data e hora da ultima execuçao do serviço
Local nDiferencaMin     := 0        as Numeric      // Diferença em minutos
  
aDataHoraUltConn := {SToD( SubStr(cDataHora,1,8) ), SubStr(cDataHora,10,8)}

nDiferencaMin := DataHora2Val( StoD(SubStr(cDataHora,1,8)) , SubStr(cDataHora,10,8) , Date() , Time() ,  )

If aDataHoraUltConn[1] < Date() .OR. ( aDataHoraUltConn[1] == Date() .AND. nDiferencaMin > 5 )
    cServicoStatus := "Inativo"      
EndIf

Return cServicoStatus

/*/{Protheus.doc} SetaParamApi
Seta as parametros enviados pelo cliente da API
@author joao.marcos
@since 18/06/2024
@version 1.0
/*/
Method SetaParamApi() Class pshMonitorPgServicos

Self:oJQueryRequest := JsonObject():New()
Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())

If !Empty( Self:oJQueryRequest['loja'] )
    Self:cFilFiltro   := AllTrim(Self:oJQueryRequest['loja'])
EndIF
If !Empty( Self:oJQueryRequest['status'] )
    Self:cStatus    :=  AllTrim(Self:oJQueryRequest['status'])
EndIf
If !Empty( Self:oJQueryRequest['servico'] )
    Self:cServico    := AllTrim(Self:oJQueryRequest['servico'])
EndIf
If !Empty( Self:oJQueryRequest['estacao'] )
    Self:cEstacao  := AllTrim(Self:oJQueryRequest['estacao'])
EndIf
If !Empty( Self:oJQueryRequest['report'] )
    Self:cReport     := AllTrim(Self:oJQueryRequest['report'])
EndIf
If !Empty( Self:oJQueryRequest['search'] )
    Self:cSearch    := AllTrim(Self:oJQueryRequest['search'])
EndIf
If !Empty( Self:oJQueryRequest['order'] )
    Self:cOrder    := AllTrim(Self:oJQueryRequest['order'])
EndIf
If !Empty( Self:oJQueryRequest['page'] )
    Self:nPage      := Val( AllTrim(Self:oJQueryRequest['page']) )
EndIf
If !Empty( Self:oJQueryRequest['pageSize'] )
    Self:nPageSize  := Val( AllTrim(Self:oJQueryRequest['pageSize']) )
EndIf

If( Empty(Self:nPageSize) .OR. Self:nPageSize == 10 )
    Self:nPageSize := 30
EndIf

Self:nPage      := If(Empty(Self:nPage),1 ,Self:nPage)

Return

/*/{Protheus.doc} Executa
Executa a query
@author joao.marcos
@since  18/06/2024
/*/
Method Executa() Class pshMonitorPgServicos
Local cResult   := ""   as Character

Self:MontaQuery()    
Self:MapFields()

Self:oQuery:setQuery(Self:cQuery)
Self:oQuery:setWhere(Self:RetWhere())
Self:oQuery:setOrder(Self:RetOrderBy())
Self:oQuery:setPageSize(Self:nPageSize)
Self:oQuery:setPage(Self:nPage)

Self:oQuery:execute()
Self:oQuery:fillGetResponse()
cResult := Self:oQuery:getJsonResponse()
Self:oJsonRet:fromJson(cResult)
If Len(Self:oJsonRet["items"]) <= 0
    Self:ResultZero(@cResult)
EndIf        

Return

/*/{Protheus.doc} MontaQuery
Monta a query principal
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method MontaQuery() Class pshMonitorPgServicos
Local cBanco  := AllTrim( Upper( TcGetDB() ) )  as Character
Local cConcat := Iif(cBanco $ "POSTGRES/ORACLE", " || ", " + ") as Character

Self:cQuery := " SELECT #QueryFields# FROM ( "                                                                                                      + CRLF
Self:cQuery += " 	SELECT  MIW_FILPI, MIW_CODPI, MIW_SERVIC, "                                                                                     + CRLF
Self:cQuery += "  	CASE "                                                                                                                          + CRLF
Self:cQuery += "   		WHEN DIFMINUTOS > 5 THEN 'Inativo' "                                                                                        + CRLF
Self:cQuery += "   		ELSE 'Ativo' "                                                                                                              + CRLF
Self:cQuery += "  	END AS MIQ_ATIVO, "                                                                                                             + CRLF

If "ORACLE" $ cBanco
    Self:cQuery += "  	SUBSTR(MIW_DATAHORA,7,2)  +  '/' +  SUBSTR(MIW_DATAHORA,5,2) + '/' + SUBSTR(MIW_DATAHORA,1,4) + ' ' + SUBSTR(MIW_DATAHORA,10,8) AS MIW_HORA  " + CRLF
ElseIf "POSTGRES" $ cBanco
     Self:cQuery += "  	TO_CHAR(TO_TIMESTAMP(MIW_DATAHORA, 'YYYYMMDD HH24MISS'), 'DD/MM/YYYY HH24:MI:SS') AS MIW_HORA  "                                                + CRLF
Else
    // SQL Server
    Self:cQuery += "  	SUBSTRING(MIW_DATAHORA,7,2) + '/' + SUBSTRING(MIW_DATAHORA,5,2) + '/' + SUBSTRING(MIW_DATAHORA,1,4) + ' ' + SUBSTRING(MIW_DATAHORA,10,8) AS MIW_HORA  " + CRLF
EndIf

Self:cQuery += "  	FROM ( "                                                                                                                        + CRLF
Self:cQuery += "  		SELECT MIW_FILPI, MIW_CODPI, MIW_SERVIC, "                                                                                  + CRLF

If "POSTGRES" $ cBanco
    Self:cQuery += "  		EXTRACT(EPOCH FROM (NOW() - TO_TIMESTAMP(MIW_DATAHORA, 'YYYYMMDD HH24MISS'))) / 60 AS DIFMINUTOS, "                      + CRLF
Else
    Self:cQuery += "  		( (DATEDIFF ( DAY , CONVERT(DATE, SUBSTRING(MIW_DATAHORA,1,8), 103) , GETDATE() ) * 24) * 60 * 60 ) + "                 + CRLF
    Self:cQuery += "  		( DATEDIFF( MINUTE , CONVERT(TIME,MIW_DATAHORA), SUBSTRING(CONVERT(VARCHAR(25), GETDATE(), 120),12,8) ) ) AS DIFMINUTOS, "  + CRLF 
EndIf

Self:cQuery += "  		MIW_DATAHORA  "                                                                                                             + CRLF
Self:cQuery += "  		FROM ( "                                                                                                                    + CRLF
Self:cQuery += "  			SELECT MIW_FILIAL, MIW_EMPPI, MIW_FILPI, MIW_CODPI, "                                                                   + CRLF
Self:cQuery += " 			MIW_SERVIC, MAX(MIW_DATA " + cConcat + " ' ' " + cConcat + " MIW_HORA) AS MIW_DATAHORA "                            + CRLF
Self:cQuery += "  			FROM " + RetSqlName("MIW") + " "                                                                                        + CRLF

If Empty(Self:cFilFiltro) .OR. Upper(Self:cFilFiltro) == "TODAS"
    Self:cQuery += "  			WHERE MIW_EMPPI = '" + cEmpAnt + "'"                                                                                + CRLF
Else
    Self:cQuery += "  			WHERE MIW_FILPI = '" + Self:cFilFiltro + "' "                                                                       + CRLF
    Self:cQuery += "  			AND MIW_EMPPI = '" + cEmpAnt + "'"                                                                                  + CRLF
EndIf

If "ORACLE" $ cBanco
    Self:cQuery += "  			AND SUBSTR(MIW_SERVIC,1,11) IN ('RMIPUBLICA_','RMIENVIAPDV','RMIINTEPDV_')  "                                       + CRLF     
Else
    Self:cQuery += "  			AND SUBSTRING(MIW_SERVIC,1,11) IN ('RMIPUBLICA_','RMIENVIAPDV','RMIINTEPDV_') "                                     + CRLF    
EndIf

Self:cQuery += "  			AND D_E_L_E_T_ = ' ' "                                                                                                  + CRLF
Self:cQuery += "  			GROUP BY MIW_FILIAL, MIW_EMPPI, MIW_FILPI, MIW_CODPI, MIW_SERVIC "                                                      + CRLF
Self:cQuery += "  		) AS ULTIMOS "                                                                                                              + CRLF
Self:cQuery += "  	) AS DIFERENCA "                                                                                                                + CRLF
Self:cQuery += " ) AS MIW "                                                                                                                         + CRLF
Self:cQuery += " WHERE #QueryWhere# "                                                                                                               + CRLF

Return

/*/{Protheus.doc} MapFields
Faz o mapeamento dos campos da query com os atributos do json
@author joao.marcos
@since 12/06/2024
@version 1.0
/*/
Method MapFields() Class pshMonitorPgServicos

Self:oQuery:addMapFields('loja'     ,  'MIW_FILPI'  ,.T.,.T.)
Self:oQuery:addMapFields('estacao'  ,  'MIW_CODPI'  ,.T.,.T.)
Self:oQuery:addMapFields('servico'  ,  'MIW_SERVIC' ,.T.,.T.)
Self:oQuery:addMapFields('status'   ,  'MIQ_ATIVO'  ,.T.,.T.)
Self:oQuery:addMapFields('report'   ,  'MIW_HORA'   ,.T.,.T.)

Return

/*/{Protheus.doc} RetWhere
Define o Where da query principal
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method RetWhere() as Character Class pshMonitorPgServicos
Local cWhere        := ""                   as Character

If Empty(Self:cSearch) .AND. !Empty(Self:cFilFiltro)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    Iif(Upper(Self:cFilFiltro) == "TODAS", cWhere += " MIW_FILPI <> '' ", cWhere += " MIW_FILPI = '" + AllTrim(Self:cFilFiltro) + "' ")
EndIf

If Empty(Self:cSearch) .AND. !Empty(Self:cServico)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MIW_SERVIC = '" + AllTrim(Self:cServico) + "' "
EndIf

If Empty(Self:cSearch) .AND. !Empty(Self:cEstacao)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MIW_CODPI = '" + AllTrim(Self:cEstacao) + "' "
EndIf

If Empty(Self:cSearch) .AND. !Empty(Self:cStatus)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MIQ_ATIVO = '" + AllTrim(Self:cStatus) + "' "
EndIf

If Empty(Self:cSearch) .AND. !Empty(Self:cReport)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " MIW_HORA = '" + AllTrim(Self:cReport) + "' "
EndIf

If !Empty(Self:cSearch)

    Iif(!Empty(cWhere), cWhere += " AND " , "" )

    cWhere += " ( MIW_FILPI     LIKE '%" + Self:cSearch + "%' "  + CRLF
    cWhere += " OR MIW_SERVIC   LIKE '%" + Self:cSearch + "%' "  + CRLF
    cWhere += " OR MIW_CODPI    LIKE '%" + Self:cSearch + "%' "  + CRLF
    cWhere += " OR MIQ_ATIVO    LIKE '%" + Self:cSearch + "%' "  + CRLF
    cWhere += " OR MIW_HORA     LIKE '%" + Self:cSearch + "%' )" + CRLF
EndIf

If Empty(cWhere)
     cWhere := " MIW_SERVIC <> '' "
 EndIf

Return cWhere

/*/{Protheus.doc} RetOrderBy
Define o ORDER BY da query principal
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method RetOrderBy() as Character Class pshMonitorPgServicos
Local cOrderBy := "" as Character

Do Case
    Case Empty(Self:cOrder) .OR. Upper( AllTrim(Self:cOrder) ) == "LOJA"
        cOrderBy := " MIW_FILPI "
    Case Upper( AllTrim(Self:cOrder) ) == "SERVICO"
        cOrderBy := " MIW_SERVIC "
    Case Upper( AllTrim(Self:cOrder) ) == "STATUS"
        cOrderBy := " MIQ_ATIVO "
    Case Upper( AllTrim(Self:cOrder) ) == "REPORT"
        cOrderBy := " MIW_HORA "
EndCase

Return cOrderBy

/*/{Protheus.doc} ResultZero
Trata o json para caso a query não retorne valores
@author joao.marcos
@since 14/06/2024
@version 1.0
/*/
Method ResultZero(cResult as Character) Class pshMonitorPgServicos
Local cItensZero := ""                                      as Character
Local nInicio   := AT( "[", cResult)+1                      as Numeric
Local nFim      := AT( "[", cResult)                        as Numeric
Local cResult01 := SubStr( cResult,1, nFim )                as Character
Local cResult02 := SubStr( cResult, nInicio, Len(cResult) ) as Character

cItensZero := '{ "loja": "", "tabela": "", "tempo": "" }'

cResult := cResult01 + cItensZero + cResult02

Self:oJsonRet:fromJson(cResult)

Return


