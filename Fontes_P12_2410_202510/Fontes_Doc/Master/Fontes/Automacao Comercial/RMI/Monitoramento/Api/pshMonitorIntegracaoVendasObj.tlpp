#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include 'parmtype.ch'

class pshMonitorPgIntegracaoVendas

	Public method New()                 as Object

    Private Method SetaParamApi()       as Variant
    Private Method Executa()            as Variant     
    Private Method RetWhere()           as Character
    Private Method RetOrderBy()         as Character
    Private Method RetFilRegiao()       as Character
    Private Method MontaQuery()         as Variant
    Private Method MapFields()          as Variant
    Private Method ResultZero()         as Variant

    Public Method GetJsonReturn()      as Json
    Public Method ErrosPorRegiao()     as Character

    Private Data oJQueryRequest as Json
    Private Data cCodPtoInteg   as Character
    Private Data cEmpPtoInteg   as Character
    Private Data cStatus        as Character
    Private Data cRegiao        as Character
    Private Data cLoja          as Character
    Private Data cDoc           as Character
    Private Data cSerie         as Character
    Private Data cCliente       as Character
    Private Data cLojaCli       as Character
    Private Data cSearch        as Character
    Private Data cOrder         as Character
    Private Data nPage          as Numeric
    Private Data nPageSize      as Numeric
    Private Data oQuery         as Object
    Private Data cQuery         as Character
    Private Data lRegioes       as Logical

    Public Data oJsonRet     as Json

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  16/09/2024
/*/
Method New(oRest as Object, lRegioes as Logical) as Object Class pshMonitorPgIntegracaoVendas

Self:oJQueryRequest := JsonObject():New()
Self:oJQueryRequest:FromJson(oRest:getQueryRequest():ToJson())
Self:lRegioes       := lRegioes
Self:cCodPtoInteg   := ""
Self:cEmpPtoInteg   := cEmpAnt
Self:cStatus        := ""
Self:cRegiao        := ""
Self:cLoja          := ""
Self:cDoc           := ""
Self:cSerie         := ""
Self:cCliente       := ""
Self:cLojaCli       := ""
Self:cSearch        := ""
Self:cOrder         := ""
Self:nPage          := 1
Self:nPageSize      := 0
Self:oJsonRet       := JsonObject():New()
Self:oQuery         := fwAdapterBaseV2():new('GET'     ,  .T.)
Self:cQuery         := ""

Self:SetaParamApi()
If !lRegioes   
    Self:Executa()
EndIf

Return Self


/*/{Protheus.doc} GetJsonReturn
Abre ambiente para uso
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method GetJsonReturn() as Json Class pshMonitorPgIntegracaoVendas
Return Self:oJsonRet:ToJson()

/*/{Protheus.doc} SetaParamApi
Seta as parametros enviados pelo cliente da API
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method SetaParamApi() Class pshMonitorPgIntegracaoVendas

If !Empty( Self:oJQueryRequest['status'] ) .AND. ( Upper( Self:oJQueryRequest['status'] ) <> "TDS" )
    Self:cStatus    :=  AllTrim(Self:oJQueryRequest['status'])
EndIf

If !Empty( Self:oJQueryRequest['regiao'] )
    Self:cRegiao    := AllTrim(Self:oJQueryRequest['regiao'])
EndIf

If !Empty( Self:oJQueryRequest['loja'] ) .AND. ( Upper( Self:oJQueryRequest['loja'] ) <> "TODAS" )
    Self:cLoja    := AllTrim(Self:oJQueryRequest['loja'])
EndIf

// Pesquisa
If !Empty( Self:oJQueryRequest['search'] )
    Self:cSearch    := AllTrim(Self:oJQueryRequest['search'])
EndIf
// Ordenacao
If !Empty( Self:oJQueryRequest['order'] )
    Self:cOrder    := AllTrim(Self:oJQueryRequest['order'])
EndIf
// Definicoes de pagina
If !Empty( Self:oJQueryRequest['page'] )
    Self:nPage      := Val( AllTrim(Self:oJQueryRequest['page']) )
EndIf
If !Empty( Self:oJQueryRequest['pageSize'] )
    Self:nPageSize  := Val( AllTrim(Self:oJQueryRequest['pageSize']) )
EndIf

If( Empty(Self:nPageSize) .OR. Self:nPageSize == 10 )
    Self:nPageSize := 30
EndIf

Self:nPage      := If(Empty(Self:nPage),1 ,Self:nPage)

Return

/*/{Protheus.doc} Executa
Executa a query
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method Executa() Class pshMonitorPgIntegracaoVendas
Local cResult   := ""   as Character
Local nError    := 0    as Numeric
Local cError    := ""   as Character

Self:MontaQuery()    
Self:MapFields()

Self:oQuery:setQuery(Self:cQuery)
Self:oQuery:setWhere(Self:RetWhere())
Self:oQuery:setOrder(Self:RetOrderBy())
Self:oQuery:setPageSize(Self:nPageSize)
Self:oQuery:setPage(Self:nPage)

If Self:oQuery:execute()
    Self:oQuery:fillGetResponse()
    cResult := Self:oQuery:getJsonResponse()
    Self:oJsonRet:fromJson(cResult)
    If Len(Self:oJsonRet["items"]) <= 0
        Self:ResultZero(@cResult)
    EndIf
    
Else
    nError := Self:oQuery:getCode()
    cError := Self:oQuery:getMessage()
    Self:oJsonRet  := jsonObject():new()
    Self:oJsonRet['error'] := cError
    oRest:setFault(Self:oJsonRet:toJson())
EndIf

Return

/*/{Protheus.doc} MontaQuery
Monta a query principal
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method MontaQuery() Class pshMonitorPgIntegracaoVendas

Self:cQuery := " SELECT #QueryFields# FROM ( "              + CRLF
Self:cQuery += " SELECT MI0.MI0_FILIAL, MI0.MI0_CODPTI, "   + CRLF
Self:cQuery += " MI0.MI0_DATA, "                            + CRLF
Self:cQuery += " MI0.MI0_DOC, MI0.MI0_SERIE, "              + CRLF
Self:cQuery += " MI0.MI0_CLIENT, MI0.MI0_LOJACL, "          + CRLF
Self:cQuery += " MI0.MI0_STATUS, L1.L1_SITUA, "             + CRLF
Self:cQuery += " L1.L1_ERGRVBT "                            + CRLF
Self:cQuery += " FROM " + RetSqlName("MI0") + " MI0 "       + CRLF
Self:cQuery += " LEFT JOIN " + RetSqlName("SL1") + " L1 "   + CRLF
Self:cQuery += " ON L1.L1_FILIAL = MI0.MI0_FILIAL "         + CRLF
Self:cQuery += " AND L1.L1_DOC = MI0.MI0_DOC "              + CRLF
Self:cQuery += " AND L1.L1_SERIE = MI0.MI0_SERIE "            + CRLF
Self:cQuery += " AND L1.L1_CLIENTE = MI0.MI0_CLIENT "       + CRLF
Self:cQuery += " AND L1.L1_LOJA = MI0.MI0_LOJACL "          + CRLF

If !Empty(Self:cLoja)
    Self:cQuery += " WHERE MI0.MI0_FILIAL = '" + Self:cLoja + "' "      + CRLF
    Self:cQuery += " AND MI0.MI0_EMPPI = '" + Self:cEmpPtoInteg + "' "  + CRLF
Else
    Self:cQuery += " WHERE MI0.MI0_EMPPI = '" + Self:cEmpPtoInteg + "' "    + CRLF
EndIf

Self:cQuery += " AND MI0_STATUS <> '' "                     + CRLF
Self:cQuery += " AND MI0.D_E_L_E_T_ = ' ' "                 + CRLF
Self:cQuery += " AND ( L1.D_E_L_E_T_ = ' ' OR L1.D_E_L_E_T_ IS NULL  ) ) STATUS "            + CRLF
Self:cQuery += " WHERE #QueryWhere# "                       + CRLF

LjGrvLog(, "pshMonitorPgIntegracaoVendas | MontaQuery | Query ", Self:cQuery )

Return

/*/{Protheus.doc} MapFields
Faz o mapeamento dos campos da query com os atributos do json
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method MapFields() Class pshMonitorPgIntegracaoVendas

Self:oQuery:addMapFields('loja'         ,  'MI0_FILIAL'  ,.T.,.T.) 
Self:oQuery:addMapFields('codPtoInteg'  ,  'MI0_CODPTI'   ,.T.,.T.) 
Self:oQuery:addMapFields('status'       ,  'MI0_STATUS'   ,.T.,.T.) 
Self:oQuery:addMapFields('data'         ,  'MI0_DATA'  ,.T.,.T.) 
Self:oQuery:addMapFields('doc'          ,  'MI0_DOC'     ,.T.,.T.)
Self:oQuery:addMapFields('serie'        ,  'MI0_SERIE'   ,.T.,.T.) 
Self:oQuery:addMapFields('cliente'      ,  'MI0_CLIENT' ,.T.,.T.) 
Self:oQuery:addMapFields('lojacli'      ,  'MI0_LOJACL'    ,.T.,.T.) 

Return

/*/{Protheus.doc} RetWhere
Define o Where da query principal
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method RetWhere() as Character Class pshMonitorPgIntegracaoVendas 
Local cWhere        := ""                   as Character
Local cFiliais      := ""                   as Character

If !Empty(Self:cRegiao)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cFiliais := Self:RetFilRegiao(Self:cRegiao)
    If !Empty(cFiliais)
        // filiais da regiao escolhida
        cWhere += " STATUS.MI0_FILIAL IN(" + cFiliais + ") "
    Else
        // caso nao tenha filiais na regiao escolhida
        cWhere += " STATUS.MI0_FILIAL = '' "
    EndIf
EndIf

If Empty(Self:cRegiao) .AND. !Empty(Self:cLoja)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " STATUS.MI0_FILIAL = '" + AllTrim(Self:cLoja) + "' "
EndIf

If !Empty(Self:cStatus) .AND. Empty(Self:cSearch)
    Iif(!Empty(cWhere), cWhere += " AND " , "" )
    cWhere += " STATUS.MI0_STATUS = '" + AllTrim(Self:cStatus) + "' "
EndIf

If !Empty(Self:cSearch)

    Iif(!Empty(cWhere), cWhere += " AND " , "" )

    cWhere += "(STATUS.MI0_FILIAL          LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR STATUS.MI0_DOC          LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR STATUS.MI0_SERIE        LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR STATUS.MI0_CLIENT       LIKE '%" + Self:cSearch + "%' " + CRLF
    cWhere += " OR STATUS.MI0_LOJACL       LIKE '%" + Self:cSearch + "%')" + CRLF

EndIf

If Empty(cWhere)
    cWhere := " STATUS.MI0_FILIAL <> '' "
EndIf

Return cWhere

/*/{Protheus.doc} RetOrderBy
Define o ORDER BY da query principal
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method RetOrderBy() as Character Class pshMonitorPgIntegracaoVendas
Local cOrderBy := "" as Character

If Empty(Self:cOrder) .OR. Upper( AllTrim(Self:cOrder) ) == "LOJA"
    cOrderBy := " STATUS.MI0_FILIAL, STATUS.MI0_DATA, STATUS.MI0_DOC, STATUS.MI0_SERIE "
EndIf

Return cOrderBy

/*/{Protheus.doc} RetFilRegiao
Retorna as lojas pertencentes a regiao enviada por parametro
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method RetFilRegiao(cRegiao as Character ) As Character Class pshMonitorPgIntegracaoVendas
Local oRegioes  := Nil  as Object
Local oJRegioes := JsonObject():New() as Json
Local cFiliais  := ""   as Character
Local nX        := 0    as Numeric

oRegioes := RmiFiliaisPorRegiao():New(cRegiao)
oJRegioes := oRegioes:GetJsonReturn()

For nX := 1 To Len(oJRegioes['Filiais'])
    If !Empty(oJRegioes['Filiais'][nX]['codFilial'])
        cFiliais := cFiliais + "'" + AllTrim( oJRegioes['Filiais'][nX]['codFilial'] ) + "',"
    EndIf
Next

cFiliais := SubStr(cFiliais,1,Len(cFiliais)-1)

Return cFiliais

/*/{Protheus.doc} ResultZero
Trata o json para caso a query nao retorne valores
@author joao.marcos
@since 16/09/2024
@version 1.0
/*/
Method ResultZero(cResult as Character) Class pshMonitorPgIntegracaoVendas
Local cItensZero := ""                                      as Character
Local nInicio   := AT( "[", cResult)+1                      as Numeric
Local nFim      := AT( "[", cResult)                        as Numeric
Local cResult01 := SubStr( cResult,1, nFim )                as Character
Local cResult02 := SubStr( cResult, nInicio, Len(cResult) ) as Character
Local cQuery    := ""                                       as Character 
Local cAliasMI0 := ""                                       as Character
Local ajStatusVendas := {}                                  as Array
Local nConta    := 0                                        as Numeric

cQuery += " SELECT MI0.MI0_FILIAL, MI0.MI0_CODPTI, "
cQuery += "  MI0.MI0_DATA, "
cQuery += "  MI0.MI0_DOC, MI0.MI0_SERIE, "
cQuery += "  MI0.MI0_CLIENT, MI0.MI0_LOJACL, "
cQuery += "  MI0.MI0_STATUS, L1.L1_SITUA, "
cQuery += "  L1.L1_ERGRVBT "
cQuery += "  FROM " + RetSqlName("MI0") + " MI0 "
cQuery += "  LEFT JOIN " + RetSqlName("SL1") + " L1 "
cQuery += "  ON L1.L1_FILIAL = MI0.MI0_FILIAL "
cQuery += "  AND L1.L1_DOC = MI0.MI0_DOC "
cQuery += "  AND L1.L1_SERIE = MI0.MI0_SERIE "
cQuery += "  AND L1.L1_CLIENTE = MI0.MI0_CLIENT "
cQuery += "  AND L1.L1_LOJA = MI0.MI0_LOJACL "
cQuery += "  WHERE MI0.MI0_EMPPI = '" + Self:cEmpPtoInteg + "' " 
cQuery += "  AND MI0_STATUS <> '' "
cQuery += "  AND MI0.D_E_L_E_T_ = ' '  "
cQuery += "  AND ( L1.D_E_L_E_T_ = ' ' OR L1.D_E_L_E_T_ IS NULL  ) "

cQuery := ChangeQuery(cQuery)
cAliasMI0 := MPSysOpenQuery(cQuery)

While (cAliasMI0)->(!Eof())
    nConta++
    AADD(ajStatusVendas,JsonObject():New())
    ajStatusVendas[nConta]['status']    := (cAliasMI0)->MI0_STATUS
    ajStatusVendas[nConta]['data']      := (cAliasMI0)->MI0_DATA
    ajStatusVendas[nConta]['doc']       := (cAliasMI0)->MI0_DOC
    ajStatusVendas[nConta]['serie']     := (cAliasMI0)->MI0_SERIE
    ajStatusVendas[nConta]['cliente']   := (cAliasMI0)->MI0_CLIENT
    ajStatusVendas[nConta]['loja']      := (cAliasMI0)->MI0_LOJACL    
    
    (cAliasMI0)->(dbSkip())
EndDo

If Len(ajStatusVendas) > 0
    For nConta := 1 To Len(ajStatusVendas)
        cItensZero += ajStatusVendas[nConta]:ToJson() + ','
    Next

    cItensZero := SubStr(cItensZero,1,Len(cItensZero)-1)
Else
    cItensZero := '{ "status": "", "data": "", "doc": "", "serie": "", "cliente": "", "loja": "" }'
EndIf

cResult := cResult01 + cItensZero + cResult02

Self:oJsonRet:fromJson(cResult)

Return

/*/{Protheus.doc} ErrosPorRegiao
Retorna quantidade de erros por regiao
@author joao.marcos
@since 23/09/2024
@version 1.0
/*/
Method ErrosPorRegiao() as Character Class pshMonitorPgIntegracaoVendas
Local cQuery            := "" as Character 
Local cAliasRegioes     := "" as Character
Local oRegioes          :=  RmiFiliaisPorRegiao():New('sudeste')  as Object
Local nErrosNorte       := 0    as Numeric
Local nErrosNordeste    := 0    as Numeric
Local nErrosCentroeste  := 0    as Numeric
Local nErrosSudeste     := 0    as Numeric
Local nErrosSul         := 0    as Numeric
Local oJErrosRegioes := JsonObject():New() as Json

LjGrvLog(, "pshMonitorPgIntegracaoVendas | ErrosPorRegiao | Inicio ", )

cQuery += " SELECT MI0.MI0_FILIAL, MI0.MI0_CODPTI, MI0.MI0_DATA, "  + CRLF
cQuery += " MI0.MI0_DOC, MI0.MI0_SERIE, MI0.MI0_CLIENT, "           + CRLF
cQuery += " MI0.MI0_LOJACL, MI0.MI0_STATUS, L1.L1_SITUA "          + CRLF
cQuery += " FROM " + RetSqlName("MI0") + " MI0 "       + CRLF
cQuery += " LEFT JOIN " + RetSqlName("SL1") + " L1 "   + CRLF
cQuery += " ON L1.L1_FILIAL = MI0.MI0_FILIAL "         + CRLF
cQuery += " AND L1.L1_DOC = MI0.MI0_DOC "              + CRLF
cQuery += " AND L1.L1_SERIE = MI0.MI0_SERIE "           + CRLF
cQuery += " AND L1.L1_CLIENTE = MI0.MI0_CLIENT "       + CRLF
cQuery += " AND L1.L1_LOJA = MI0.MI0_LOJACL "          + CRLF
cQuery += " WHERE MI0.MI0_EMPPI = '" + cEmpAnt + "' "   + CRLF
cQuery += " AND MI0_STATUS <> '' "                     + CRLF
cQuery += " AND MI0.D_E_L_E_T_ = ' ' "                 + CRLF
cQuery += " AND ( L1.D_E_L_E_T_ = ' ' OR L1.D_E_L_E_T_ IS NULL  ) "            + CRLF

cQuery := ChangeQuery(cQuery)
cAliasRegioes := MPSysOpenQuery(cQuery)

If (cAliasRegioes)->(!Eof())
    While (cAliasRegioes)->(!Eof())
        cRegiaoFilial := oRegioes:GetRegiaoFilial( (cAliasRegioes)->MI0_FILIAL )

        Do Case
            Case cRegiaoFilial == 'norte'
                nErrosNorte++
            Case cRegiaoFilial == 'nordeste'
                nErrosNordeste++
            Case cRegiaoFilial == 'centrooeste'
                nErrosCentroeste++
            Case cRegiaoFilial == 'sudeste'
                nErrosSudeste++
            Case cRegiaoFilial == 'sul'
                nErrosSul++
        EndCase

        (cAliasRegioes)->(dbSkip()) 
    EndDo
EndIf

oJErrosRegioes['norte']         := nErrosNorte
oJErrosRegioes['nordeste']      := nErrosNordeste
oJErrosRegioes['centrooeste']   := nErrosCentroeste
oJErrosRegioes['sudeste']       := nErrosSudeste
oJErrosRegioes['sul']           := nErrosSul

(cAliasRegioes)->(DbCloseArea())

LjGrvLog(, "pshMonitorPgIntegracaoVendas | ErrosPorRegiao | Fim | Retorno:", oJErrosRegioes)

Return oJErrosRegioes:ToJson()
