#INCLUDE "PROTHEUS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "SHPWIZ.CH"

using namespace totvs.protheus.retail.rmi.wizard.configuracao.VendaDigital
using namespace totvs.protheus.retail.rmi.wizard.configuracao.PdvSync
using namespace totvs.protheus.retail.rmi.wizard.configuracao.IntegTotvsPdv

Static oStObjWizard := Nil  as Object
Static cStComboProd := ""   as Character
Static cStMensagem  := ""   as Character

//-------------------------------------------------------------------
/*/{Protheus.doc} pshWizCfg
Função da criação do objeto wizard do Smarthub Protheus (Tela de solução)

@type    Function
@author  Evandro Pattaro
@since   05/06/23
@version 12.1.2210
/*/
//-------------------------------------------------------------------
Function pshWizCfg() as Variant
    
    Local oObjPergunte  := Nil as Object
    Local oNewPag       := Nil as Object	//Objeto que adiciona nova pagina no wizard    
    Local aCoords       := {} as Array

    aCoords := FWGetDialogSize()
    oObjPergunte := FWWizardControl():New( , {aCoords[3] , aCoords[4] * 1.0} )// Define o tamanho do wizard  ex: {600,800}
	oObjPergunte:ActiveUISteps()

    // 01 ------------------------------Configuração de Solução  
    oNewPag := oObjPergunte:AddStep("1")//Adiciona a primeira tela do wizard
    oNewPag:SetStepDescription(STR0001) //Altera a descrição do step    //"Configuração de Solução"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               
    oNewPag:SetConstruction({|oPanel|SolucaoWizard(oPanel)}) //Define o bloco de construção
    oNewPag:SetNextAction({|| ValidaSolucaoWizard() })//Define o bloco ao clicar no botão Próximo
    oNewPag:SetCancelAction({|| .T. })//Valida acao cancelar
    oNewPag:SetPrevAction({|| .T. })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar

	//Ativa Wizard
	oObjPergunte:Activate()

    //Desativa Wizard
	oObjPergunte:Destroy()

    If !oStObjWizard == Nil .And. oStObjWizard:lReturn
        oStObjWizard:carga()
        oStObjWizard:ConstroiTelas()
    EndIf

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} SolucaoWizard

Construção de tela para seleção da solução

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function SolucaoWizard(oPanel) as Variant

    Local oCombo1   as Object
    Local oSay1     as Object
    Local oSay2     as Object
    Local oSay3     as Object
    Local oSay4     as Object
    Local oSay5     as Object
    Local oSay6     as Object
    Local oSay7     as Object
    Local oSayMsg   as Object

    Local oTBold    := TFont():New(,,-20,.T.,.T.)   as Object
    Local oTFont    := TFont():New(,,-12,.T.)       as Object
    Local oTSubl	:= TFont():New(,,-12,.T.,,,,,,.T.) as Object

    Local aProdutos     := {'', STR0049, STR0048, "Integração Protheus - TOTVS PDV" /*STR0080*/}	//"TOTVS Venda Digital by Ninegrid", "TOTVS Varejo PDV OMNI", "Integração Protheus - TOTVS PDV"
    Local aLinks        := {'', STR0071, STR0072, STR0081}	//Link documentação Venda Digital, Link documentação Pdv OmniShop, "Link documentação Integração TOTVS PDV"

    Local aCoords01 := {} as Array
    Local   nCord01 :=  0
    Local   nCord02 :=  0
    Local   nCord03 :=  0
    Local   nCord04 :=  0

    aCoords01 := FWGetDialogSize()

    If aCoords01[4] <= 880 //1505(1366x768);876(800x600);1125(1024x768)
        nCord01 :=  95
        nCord02 :=  nCord01 + 55
        nCord03 :=  nCord02 + 10
        nCord04 :=  nCord03 + 10
    else
        nCord01 :=  142
        nCord02 :=  150
        nCord03 :=  160
        nCord04 :=  170
    EndIf

    //TFont():New( [ cName ], [ uPar2 ], [ nHeight ], [ uPar4 ], [ lBold ], [ uPar6 ], [ uPar7 ], [ uPar8 ], [ uPar9 ], [ lUnderline ], [ lItalic ] )
    oSay1:= TSay():New(10,20,{||STR0001},oPanel,,oTBold,,,,.T.,,,200,20) //'Configuração de Solução'
    oSay2:= TSay():New(40,20,{||STR0006},oPanel,,oTFont,,,,.T.,,,200,20)//"Qual produto TOTVS vamos nos conectar?"

    oCombo1 := TComboBox():New(50,20,{|u|if(PCount()>0,cStComboProd:=u,cStComboProd)},aProdutos,200,20,oPanel,,{|| },,,,.T.,,,,,,,,,"cStComboProd")

    oSay3	:= TSay():New(70,20,{|| IIF(!Empty(cStComboProd),I18n(STR0022,{cStComboProd}),"")   },oPanel,,oTFont,,,,.T.,,,200,20) //"Para conhecer mais sobre o #1 , Acesse : "
    oSay4	:= TSay():New(80,20,{||aLinks[AScan(aProdutos,cStComboProd)]},oPanel,,oTSubl,,,,.T.,CLR_CYAN,,200,20)
    oSay4:bLClicked := {|| IIf(!Empty(aLinks[AScan(aProdutos,cStComboProd)]),ShellExecute("open",aLinks[AScan(aProdutos,cStComboProd)],"","",1),.T.) }

    oSayMsg := TSay():New(nCord01,20,{||cStMensagem},oPanel,,oTFont,,,,.T.,CLR_HRED,,400,50)

    oSay5:= TSay():New(nCord02,20,{||STR0007},oPanel,,oTFont,,,,.T.,,,200,20) //"Em caso de dúvidas acesse a documentação:"
    oSay6:= TSay():New(nCord03,20,{||'https://tdn.totvs.com/pages/releaseview.action?pageId=516633428'},oPanel,,oTSubl,,,,.T.,,,200,20)
    oSay6:bLClicked := {|| ShellExecute("open","https://tdn.totvs.com/pages/releaseview.action?pageId=516633428","","",1) }
    oSay7:= TSay():New(nCord04,20,{||STR0089},oPanel,,oTFont,,,,.T.,,,200,20) //"Caso contrário clique no botão avançar" 

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidaSolucaoWizard

Valida a seleção da solução em tela

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function ValidaSolucaoWizard() as Logical
    Local lRet := .T.

    Do Case
        Case Empty(cStComboProd)
            cStMensagem := STR0035   //"Por favor, selecione um produto especificado na lista."
            lRet := .F.

        Case cStComboProd == STR0049	//"TOTVS Venda Digital by Ninegrid"
            oStObjWizard := VendaDigital():New(cStComboProd)
            cStMensagem  := oStObjWizard:oConfigWiz["mensagem"]
            lRet         := oStObjWizard:lReturn
            
        Case cStComboProd == STR0048	//"TOTVS Pdv OmniShop"
            oStObjWizard := PdvSync():New(cStComboProd)
            cStMensagem  := oStObjWizard:oConfigWiz["mensagem"]
            lRet         := oStObjWizard:lReturn
        Case cStComboProd == STR0080 // "Protheus - TOTVS PDV"
             oStObjWizard := IntegTOTVSPdv():New(cStComboProd)
             If oStObjWizard:lReturn
                cStMensagem  := oStObjWizard:oConfigWiz["mensagem"]
             EndIf
             lRet         := oStObjWizard:lReturn
    End Case

Return lRet
