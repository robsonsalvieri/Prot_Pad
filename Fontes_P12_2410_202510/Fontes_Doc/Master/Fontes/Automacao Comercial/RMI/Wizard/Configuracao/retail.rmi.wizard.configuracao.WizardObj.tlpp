#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHPWIZ.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE 'FWMVCDEF.CH'

namespace totvs.protheus.retail.rmi.wizard.configuracao.WizardObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe WizardObj
Classe responsável pela construção das telas de configuração do Wizard
    
/*/
//-------------------------------------------------------------------
Class WizardObj

    Public Data oSHPWiz    As Object       //Objeto que conterá a classe FWWizardControl
    Public Data oConfigWiz As Json          //Objeto que conterá as informações/campos salvos nas telas do wizard
    Public Data cAssinante As Character    //Código do Assinante utilizado
    Public Data cTitulo    As Character    //Título da integração selecionada. Usado nas mensagens exibidas nas telas do Wizard
    Public Data lReturn    As Logical      //Variável de controle do retorno das funções de validação
    Public Data aCoords    As Array        //Array com coordenadas de tamanho da tela
    Public Data aJobs      As Array        //Array com os serviços básicos para configuração do .INI
    Public Data aProcessos As Array        //lista com os processos que serão ajustados conforme a integração selecionada
    
	Public Data oCoordTela As Object       //Define as Coordenadas na tela dos Get
																			  
    Protected Data cIP As Character           //Informação de IP para validação
    Protected Data cPorta As Character        //Informação de porta para validação
    Protected Data cAmb As Character          //Informação de ambiente para validação
    Protected Data lConfLoc As Logical        //Verificador se a configuração dos serviços é feita em ambiente local
    Protected Data lConfMIH As Logical        //Verificador se a configuração dos serviços já está salva na tabela MIH

    
    Public  Method New(cAssin as Character)             as Object       //Método construtor da classe

    Public  Method validaPreReqs()                      as Variant      //Método que efetua a validação dos pré requisitos da integração

    Public  Method Telaservicos(oPanel as Object)       as Variant      //Método construtor da tela de serviços do SmartHub(Comum entre todas as integrações)
    Public  Method Validaservicos()                     as Logical      //Validação da tela de serviços do SmartHub
    Public  Method Telaconfirmacao(oPanel as Object)    as Variant      //Construção da tela de confirmação do Wizard (última tela)
    Public  Method Validaconfirmacao()                  as Logical      //Bloco de validação da tela de confirmação
    Public  Method Voltar()                             as Logical      //Implentações ao clicar no botão voltar
    Public  Method Cancelar()                           as Logical      //Implentações ao clicar no botão cancelar

    Public  Method ValidaStamp(aTabelas as Array)       as Logical      //Validação/criação do campo S_T_A_M_P_ nas tabelas envolvidas na integração
    Public  Method BaixaGit()                           as Variant      //Baixa das configurações padrão da integração selecionada.
    Public  Method AssinantesXProcessos()               as Logical      //Cadastra as configurações de processos pro assinante (MHP)
    Public  Method ConstroiTelas()                      as Variant      //Método que define a construção das telas do Wizard.
    Public  Method CarregaMIH()                         as Variant      //Método que carrega os serviços salvos da tabela MIH
    Public  Method GravaLayoutMIH()                     as Variant      //Método que grava o layout de configuração via Wizard
    Public  Method DeletAppWiz()                        as Variant      //Método que deleta as configurações dos serviços do appserver.ini

    Protected Method carga()                            as Variant      //Método que efetua a carga de dados e preparação do ambiente para integração
    Protected Method WriteIni()                         as Logical      //Método que grava os serviços SmartHub no .ini do appserver

	Protected  Method componentes()                    as Variant																										 
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor da Classe

@param cAssin - Código do assinante que será utilizado

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(cAssin as Character, cTitulo as Character) as Object Class WizardObj

    self:lReturn    := .T.
    self:aCoords    := FWGetDialogSize()
    self:cAssinante := cAssin

    self:oSHPWiz := FWWizardControl():New( , {self:aCoords[3] , self:aCoords[4] * 1.0} )// Define o tamanho do wizard  ex: {600,800}
	self:oSHPWiz:ActiveUISteps()

    self:aProcessos := {}
    self:aJobs      := {'RMIPUBLICA','RMIDISTRIB','RMIENVIA','RMIBUSCA','RMICONTROL'}

    self:oConfigWiz := JsonObject():New()

    self:oConfigWiz['oTBold']	    := TFont():New(,,-20,.T.,.T.)
    self:oConfigWiz['oTFont']	    := TFont():New(,,-12,.T.)
    self:oConfigWiz['oTSubl']   	:= TFont():New(,,-12,.T.,,,,,,.T.)
    self:oConfigWiz["mensagem"]     := ""
    self:oConfigWiz["cComboProd"]   := cTitulo

    self:validaPreReqs()
	self:componentes()      //  Chamada do Metodo componentes														 

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} TelaServicos

Método que constrói a página de serviços do wizard. 
Deverá ser chamado no bloco de código do AddStep do Wizard

@param oPanel - Painel referenciado no bloco de código onde será construida a tela

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method Telaservicos(oPanel as Object) as Variant Class WizardObj

    Local oSay1     as Object
    Local oSay2     as Object
    Local oSay3     as Object
    Local oSay4     as Object
    Local oSay5     as Object
    Local oSay6     as Object
    Local oSay7     as Object
    Local oSay8     as Object
    Local oSay9     as Object
    Local oSay10    as Object
    Local oGet1     as Object
    Local oGet2     as Object
    Local oGet3     as Object
    Local oGet4     as Object
    Local oGet5     as Object
    Local oSayMsg   as Object

    self:CarregaMIH()

    If !self:oConfigWiz:HasProperty("cEmpresa")
        self:oConfigWiz['cEmpresa'] := FWGrpCompany()
    EndIf
    
    If !self:oConfigWiz:HasProperty("cFil")
        self:oConfigWiz['cFil'] := cFilAnt
    EndIf

    oSay1:= TSay():New(10,20,{||STR0004},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,200,20)//'Configuração de Serviços'

    oSay2:= TSay():New(self:oCoordTela['nCord04'],20,{||STR0014},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,300,20)//"Os serviços do Smarthub Protheus serão configurados no ambiente abaixo:"

    //--------------------------------------------------------------------------------------------------------------------------------------------------------

    oSay6:= TSay():New(self:oCoordTela['nCord05'],20,{||STR0018},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Empresa: "
    oGet4:= TGet():New(self:oCoordTela['nCord06'],50,{|u| if( PCount() > 0, self:oConfigWiz['cEmpresa'] := u, self:oConfigWiz['cEmpresa']) } ,oPanel,110,010,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cEmpresa'],,,, )
    oGet4:cF3 := "SM0MRP"

    oSay7:= TSay():New(self:oCoordTela['nCord05'],self:oCoordTela['nColu03'],{||STR0019},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Filial: "
    oGet5:= TGet():New(self:oCoordTela['nCord06'],self:oCoordTela['nColu04'],{|u| if( PCount() > 0, self:oConfigWiz['cFil'] := u, self:oConfigWiz['cFil'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,"self:oConfigWiz['cFil']",,,,)
    oGet5:cF3 := "SM0EMP"

    //oSay3:= TSay():New(self:oCoordTela['nCord11'],self:oCoordTela['nColu01'],{||"Serviço: "},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Serviço: "
    //oGet1:= TGet():New(self:oCoordTela['nCord12'],self:oCoordTela['nColu02'],{|u| if( PCount() > 0, self:oConfigWiz['cServico'] := u,   self:oConfigWiz['cServico'])   } ,oPanel,110,010,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cServico'] )

    oSay3:= TSay():New(self:oCoordTela['nCord11'],20,{||STR0015},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"IP: "
    oGet1:= TGet():New(self:oCoordTela['nCord12'],50,{|u| if( PCount() > 0, self:oConfigWiz['cIP'] := u,   self:oConfigWiz['cIP'])   } ,oPanel,110,010,,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cIP'],,,,,,,,,,,"999.999.999.999" )

    oSay4:= TSay():New(self:oCoordTela['nCord09'],20,{||STR0016},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Porta: "
    oGet2:= TGet():New(self:oCoordTela['nCord10'],50,{|u| if( PCount() > 0, self:oConfigWiz['cPorta'] := u, self:oConfigWiz['cPorta'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cPorta'],,,,)

    oSay5:= TSay():New(self:oCoordTela['nCord13'],self:oCoordTela['nColu01'],{||STR0017},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Ambiente: "
    oGet3:= TGet():New(self:oCoordTela['nCord14'],self:oCoordTela['nColu02'],{|u| if( PCount() > 0, self:oConfigWiz['cAmb'] := u, self:oConfigWiz['cAmb'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cAmb'],,,,) 

    self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(self:oCoordTela['nCord01']-10,20,{||self:oConfigWiz["mensagem"]},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,250,20)

    oSay8:= TSay():New(self:oCoordTela['nCord15'],20,{||STR0007},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//'Em caso de dúvidas acesse a documentação:'
    oSay9:= TSay():New(self:oCoordTela['nCord16'],20,{||STR0059},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,,,200,20)//https://tdn.totvs.com/pages/viewpage.action?pageId=519198290
    oSay9:bLClicked := {|| ShellExecute("open",STR0059,"","",1) }
    oSay10:=TSay():New(self:oCoordTela['nCord17'],20,{||STR0089},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) //"Caso contrário clique no botão avançar"
Return Nil


//-------------------------------------------------------------------
/*/{Protheus.doc} Validaservicos

Validação da tela de serviços do SmartHub

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method Validaservicos() as Logical Class WizardObj
    Local lRet := .T. 

    Processa({|| lRet := self:WriteIni()},STR0045,STR0028)//"Instalando serviços...","Aguarde..."

Return lRet
//-------------------------------------------------------------------
/*/{Protheus.doc} WriteIni

Método que grava os serviços SmartHub no .ini do appserver

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method WriteIni() as Logical Class WizardObj

Local aSessions := GetIniSessions("TOTVSAPPSERVER.INI")
Local nI        := 0
Local cIni      := ""
Local cOnStart  := ""
Local cRate     := ""
Local oRPCSrv   := Nil
Local cJobs     := ''
Local aOnStart  := {}
Local bErrorBlock   := Nil
Local lErrorBlock   := .F.
Local cErrorBlock   := ""

Local lRet      := .T.

If !( (Alltrim(self:cIP) == Alltrim(self:oConfigWiz['cIP'])) .and. ; 
    (Alltrim(self:cPorta) == Alltrim(self:oConfigWiz['cPorta'])) .and. ;
    (Alltrim(self:cAmb) == Alltrim(self:oConfigWiz['cAmb'])) ) .or. ; //Usuário digitou dados de conexão diferentes do pré carregado
    self:lConfLoc .or.; //dados de servidor conectado (usuário não alterou nada) e não tem MIH salvo
    (self:lConfMIH .and. !(Alltrim(self:cIP) == Alltrim(getServerIP()) .and. ;
    Alltrim(self:cPorta) == Alltrim(Str(GetPort(1))) .and. ;
    Alltrim(self:cAmb) == Alltrim(GetEnvServer()))) //Dados de conexão salvas na MIH estão diferentes do servidor conectado

    If !self:lConfLoc 
        If FWAlertYesNo( IIf(self:lConfMIH,STR0076,STR0041), STR0042)//"Você está conectado em ambiente diferente ao configurado! Deseja deletar o antigo e configurar os serviços nesta porta conectada?" //"Configuração de Appserver"
            self:DeletAppWiz()                   
        else
            lRet := .F.
        ENDIF
    EndIF

    IF lRet
        oRpcSrv := TRpc():New( self:oConfigWiz['cAmb'] ) // cria o objeto que realizará a conexão com servidor externo

        bErrorBlock := ErrorBlock( {|oErro| RmiErroBlock(oErro, @lErrorBlock, @cErrorBlock)} )
        Begin Sequence
            If oRpcSrv:Connect(self:oConfigWiz['cIP'],Val(self:oConfigWiz['cPorta'])) //realiza a conexão com o servidor externo
                cIni        := oRpcSrv:CallProc("GetAdv97")
                cOnStart    := oRpcSrv:CallProc("GetPvProfString", "OnStart", "JOBS", "", cIni)
                cRate       := oRpcSrv:CallProc("GetPvProfString", "OnStart", "RefreshRate", "", cIni)
                aSessions   := oRpcSrv:CallProc("GetIniSessions",cIni)

                For nI := 1 To Len(self:aJobs)
                    IncProc("Gravando registros " + cValToChar(nI) + " de " + cValToChar(Len(self:aJobs)) + "...")
                    If AScan(aSessions,{|x| UPPER(x) == self:aJobs[nI]}) == 0 //Se não existir o Job cadastrado no appserver

                        oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Main", self:aJobs[nI], cIni)
                        oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Environment", self:oConfigWiz["cAmb"], cIni)
                        
                        If self:aJobs[nI] == "RMICONTROL"
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "nParms", "3", cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm1", "CUPOM", cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm2", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm3", self:oConfigWiz["cFil"], cIni)
                        ElseIf self:aJobs[nI] == "RMIPUBLICA"                    
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "nParms", "3", cIni)                    
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm1", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm2", self:oConfigWiz["cFil"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm3", "1", cIni)
                        Else
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "nParms", "2", cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm1", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm2", self:oConfigWiz["cFil"], cIni)
                        EndIf
                        If !(self:aJobs[nI] $ cOnStart)
                            cJobs += ","+self:aJobs[nI]
                        EndIf
                    else
                        If !(self:aJobs[nI] $ cOnStart)
                            cJobs += ","+self:aJobs[nI]
                        EndIf                
                    EndIf
                    self:oConfigWiz["cServico"] := self:aJobs[nI]
                    self:GravaLayoutMIH()
                Next nI
                
                If Empty(cOnStart)
                    oRpcSrv:CallProc("WritePProString","OnStart","JOBS",Substr(cJobs,2,Len(cJobs)),cIni)   
                else
                    oRpcSrv:CallProc("WritePProString","OnStart","JOBS",cOnStart+cJobs,cIni)      
                EndIf 
                
                
                If Empty(cRate)
                    oRpcSrv:CallProc("WritePProString","OnStart","RefreshRate","60",cIni)
                EndIf
                oRpcSrv:Disconnect()
                FwFreeObj(oRpcSrv)
                self:oConfigWiz["mensagem"] := ""
            Else
                self:oConfigWiz["mensagem"] := STR0037 //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
                lRet := .F.
            EndIf
        //Se ocorreu erro
        Recover
            self:oConfigWiz["mensagem"] := STR0037 + CRLF + cErrorBlock   //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
            lRet := .F.    
        End Sequence
        ErrorBlock(bErrorBlock)              
    EndIf              
Else //cenario que o ambiente não esta configurado o onstart no appserver
    oRpcSrv := TRpc():New( self:oConfigWiz['cAmb'] ) // cria o objeto que realizará a conexão com servidor externo

    If oRpcSrv:Connect(self:oConfigWiz['cIP'],Val(self:oConfigWiz['cPorta']))
        cIni        := oRpcSrv:CallProc("GetAdv97")
        cOnStart    := oRpcSrv:CallProc("GetPvProfString", "OnStart", "JOBS", "", cIni)
        cRate       := oRpcSrv:CallProc("GetPvProfString", "OnStart", "RefreshRate", "", cIni)
        
        If Empty(cOnStart)
            cOnStart := ArrTokStr(self:aJobs,",")
            oRpcSrv:CallProc("WritePProString","OnStart","JOBS",cOnStart,cIni)   
        else            

            aOnStart := StrTokArr2( cOnStart, ",", .F. )
            If Len(aOnStart) < 5
                For nI := 1 To Len(self:aJobs)
                    If !(self:aJobs[nI] $ cOnStart)
                        cJobs += ","+self:aJobs[nI]
                    EndIf
                NexT nI
                oRpcSrv:CallProc("WritePProString","OnStart","JOBS",cOnStart+cJobs,cIni)
            EndIf
        EndIf 
        
        If Empty(cRate)
            oRpcSrv:CallProc("WritePProString","OnStart","RefreshRate","60",cIni)
        EndIf
        oRpcSrv:Disconnect()
        FwFreeObj(oRpcSrv)
    Else
        self:oConfigWiz["mensagem"] := STR0037 //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
        lRet := .F.
    EndIf
    
EndIf


Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Telaconfirmacao

Método que constrói a página de confirmação do wizard. 
Deverá ser chamado no bloco de código do AddStep do Wizard

@param oPanel - Painel referenciado no bloco de código onde será construida a tela


@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method Telaconfirmacao(oPanel as Object) as Variant Class WizardObj
    Local oSay2
    Local oSay3
    Local oSay4
    Local oSay5

    oSay1:= TSay():New(30,20,{||STR0020},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,200,20)//"Tudo Pronto!!"
    oSay2:= TSay():New(60,20,{||I18n(STR0021,{self:oConfigWiz['cComboProd']})},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,300,20)//"Sua configuração da integração com o #1 está finalizada."

    oSay3:= TSay():New(self:oCoordTela['nCord15'],20,{||STR0007},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//'Em caso de dúvidas acesse a documentação:'
    oSay4:= TSay():New(self:oCoordTela['nCord16'],20,{||STR0073},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,,,200,20)    //"https://tdn.totvs.com/pages/releaseview.action?pageId=791847709"
    oSay4:bLClicked := {|| ShellExecute("open",STR0073,"","",1) }                                       //"https://tdn.totvs.com/pages/releaseview.action?pageId=791847709"
    oSay5:= TSay():New(self:oCoordTela['nCord17'],20,{||STR0090},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) //"Caso contrário clique no botão concluir" 
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Validaconfirmacao
Bloco de validação da tela de confirmação
@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method Validaconfirmacao() as Logical Class WizardObj
    //Implementação esta na classe filha
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidaStamp
Cria o campo S_T_A_M_P_ nas tabelas de integração

@param aTabelas - Array com as tabelas que será gerado o campo S_T_A_M_P.
Caso não seja informado, será gerado o campo S_T_A_M_P_ nas tabelas padrão definidas no método.

@author  Rafael Tenorio da Costa
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method ValidaStamp(aTabelas as Array) as Logical Class WizardObj

    Local lRetorno  := .T.  as Logical
    Local nCont     := 0    as Numeric
    Local cErro     := ""   as Character
    Local cTabelas  := ""   as Character 

    Default aTabelas := StrTokArr("ACU|ACV|CLK|DA0|DA1|F0G|MEN|MEU|MEV|MIH|MIL|SA1|SA2|SA6|SAE|SAH|SB1|SB2|SB5|SB7|SB0|SBV|SD1|SD2|SE5|SF1|SF2|SL1|SL2|SL4|SLK|SYD|SB8|MHL", "|")

    If ExistFunc("FwEnableStamp")
        
        For nCont:=1 To Len(aTabelas)
            IncProc()

            If !FwEnableStamp( aTabelas[nCont] )
                cTabelas += aTabelas[nCont] + ","
            EndIf
        Next nCont

        cTabelas := SubStr(cTabelas, 1, Len(cTabelas) - 1)
        cErro    := IIF( Empty(cTabelas), "",  I18n(STR0030 + CRLF + STR0036, {"S_T_A_M_P_", cTabelas}) )   //"Não foi possível criar o campo #1 nas tabelas: #2."  "Execute a rotina novamente com acesso exclusivo!"
    Else

        cErro := I18n(STR0031, {"FwEnableStamp", "LIB"})    //"Ops, função #1 não existe, efetue a atualização da #2 para prosseguir!"
    EndIf

    If !Empty(cErro)
        lRetorno                    := .F.
        self:oConfigWiz["mensagem"] := cErro
    Else

        //Carrega o campo STAMP quando criado
        StartJob("rmiCarStam", GetEnvServer(), .F./*lEspera*/, cEmpAnt, cFilAnt, aTabelas)
    EndIf

Return lRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} BaixaGit

Baixa das configurações padrão da integração selecionada.

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method BaixaGit(oTstAssi) as Variant Class WizardObj

    Local oGitArq := Nil as Json
    Local lReturn := .T. as Logical

    self:oConfigWiz['arquivosGit'] := RmiGetLayObj():New("https://api.github.com/repos/totvs/protheus-smart-hub-layouts/contents/"+Alltrim(Lower(self:cAssinante)))

    self:oConfigWiz['arquivosGit']:GetArq("configuracao_assinante")
    If self:oConfigWiz['arquivosGit']:lSucesso

        oGitArq := JsonObject():New()
        oGitArq:FromJson(self:oConfigWiz['arquivosGit']:cList)
        oTstAssi:cAssinante     := self:cAssinante 
        oTstAssi:oConfAssin     := JsonObject():New()
        oTstAssi:oConfAssin:FromJson(HttpGet(oGitArq[1]['download_url']))

    Else
        LjGrvLog(" SHPGetLay ","Falha na carga da lista dos arquivos de Layout"+" : "+self:oConfigWiz['arquivosGit']:cRetorno) //"Falha na carga da lista dos arquivos de Layout"
        If self:oConfigWiz['arquivosGit']:ogit:oresponseh:cStatuscode == "403"
            self:oConfigWiz["mensagem"] := I18n(STR0032, {self:cAssinante })    //"Ops! O limite de conexões para obter os arquivos de layout do #1 excederam. Tente novamente após 1 hora"
        Else
            self:oConfigWiz["mensagem"] := STR0033 + CRLF + I18n(STR0034, {"TDN"})   //"Falha na carga da lista dos arquivos de Layout!"     //"Verifique se o Assinante digitado está correto ou na lista de inclusão automática no #1."
        EndIf    
        lReturn := .F.      
    EndIf

Return lReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} AssinantesXProcessos

Cadastra as configurações de processos pro assinante (MHP)

@author  Everson S. P. Junior
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method AssinantesXProcessos() as Logical Class WizardObj

    Local lRet      := .T. As Logical
    Local oModel   	:= FWLoadModel( 'RMICADASSI' ) As Object
    Local oModelMHP := Nil as Object

    SetWizard(.T.)
    oModel:SetOperation( MODEL_OPERATION_INSERT )
    oModel:Activate()

    oModel:SetValue("MHOMASTER","MHO_COD" ,self:cAssinante)
    oModelMHP := oModel:GetModel("MHPDETAIL")

Begin Transaction

    If oModel:VldData()
        oModel:CommitData()
        lRet:= .T.
        self:oConfigWiz['cFil'] := SM0->M0_CODFIL
    Else
        If Alltrim(oModel:GetErrorMessage()[5]) == "JAGRAVADO"
            lRet := .T.
            self:oConfigWiz['cFil'] := SM0->M0_CODFIL
        else
            lRet := .F.
        EndIf
        RollBackSx8()
        self:oConfigWiz["mensagem"] := oModel:GetErrorMessage()[6]
    EndIf

    oModel:DeActivate()
    oModel:Destroy()  

End Transaction

Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} ConstroiTelas

Método que define a construção das telas do Wizard.

@author  Evandro Pattaro
@since   29/05/23
@version 1.0
/*/
//-------------------------------------------------------------------
Method ConstroiTelas() as Variant Class WizardObj
    //Implementação esta na classe filha
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} voltar
Implentações ao clicar no botão voltar

@author  Rafael Tenorio da Costa
@since   30/03/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method Voltar() as Logical Class WizardObj
    //Implementação esta na classe filha 
Return .T. 

//-------------------------------------------------------------------
/*/{Protheus.doc} Cancelar
Implentações ao clicar no botão cancelar

@author  Evandro Pattaro
@since   30/03/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method Cancelar() as Logical Class WizardObj
    //Implementação esta na classe filha 
Return .T. 

//-------------------------------------------------------------------
/*/{Protheus.doc} CarregaMIH
Método que carrega os serviços salvos da tabela MIH

@author  Danilo Rodrigues
@since   27/07/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method CarregaMIH() as Variant Class WizardObj

    Local cJsonMIH  := ""
    Local oJsonMIH  := JsonObject():New()
    Local nPos      := ""

    self:lConfMIH := .F. 

    MIH->(DbSetOrder(1)) //MIH_FILIAL, MIH_TIPCAD, MIH_ID, R_E_C_N_O_, D_E_L_E_T_
    If MIH->(dbSeek(xFilial("MIH") + "CONFIGURACAO"))
        
        cJsonMIH := MIH->MIH_CONFIG

        //Carrega configurações do tipo do cadastro        
        oJsonMIH:FromJson(cJsonMIH)        
    EndIF

    If oJsonMIH:HasProperty("Components")
        self:lConfMIH := .T. 
        If !self:oConfigWiz:HasProperty("cServico") .and. (nPos := aScan( oJsonMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'servico'} ))
            self:oConfigWiz['cServico'] := Alltrim(oJsonMIH["Components"][nPos]["ComponentContent"])
        EndIf        
        If !self:oConfigWiz:HasProperty("cIP") .and. (nPos := aScan( oJsonMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'ip'} ))
            self:cIP := self:oConfigWiz['cIP'] := PADR(oJsonMIH["Components"][nPos]["ComponentContent"],15)
        EndIf
        If !self:oConfigWiz:HasProperty("cPorta") .and. (nPos := aScan( oJsonMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'porta'} ))
            self:cPorta := self:oConfigWiz['cPorta'] := PADR(oJsonMIH["Components"][nPos]["ComponentContent"],10)
        EndIf
        If !self:oConfigWiz:HasProperty("cAmb") .and. (nPos := aScan( oJsonMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'ambiente'} ))
            self:cAmb := self:oConfigWiz['cAmb'] := PADR(oJsonMIH["Components"][nPos]["ComponentContent"],50)
        EndIf
    else
        self:lConfLoc := .T.       
        If !self:oConfigWiz:HasProperty("cServico")
            self:oConfigWiz['cServico'] := "RMIBUSCA"
        EndIf   
        If !self:oConfigWiz:HasProperty("cIP")
            self:cIP := self:oConfigWiz['cIP'] := Padr(getServerIP(),15)
        EndIf
        If !self:oConfigWiz:HasProperty("cPorta")
            self:cPorta := self:oConfigWiz['cPorta'] := Padr(Alltrim(Str(GetPort(1))),10)
        EndIf
        If !self:oConfigWiz:HasProperty("cAmb")
            self:cAmb := self:oConfigWiz['cAmb'] := Padr(GetEnvServer(),50)
        EndIf
    Endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaLayoutMIH
étodo que grava o layout de configuração via Wizard

@author  Danilo Rodrigues
@since   27/07/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method GravaLayoutMIH() as Variant Class WizardObj

    Local aGithub    := LjLayAuxCg()
    Local cLayConf   := ""
    Local oJlayMIH   := JsonObject():New()
    Local nPos       := 0
    Local nPosLay    := 0

    nPos := aScan( aGithub, {|x| UPPER(ALLTRIM(x[1])) == UPPER(ALLTRIM("CONFIGURACAO"))} ) 
    
    If nPos > 0 
        cLayConf := aGithub[nPos][2]
        oJlayMIH:FromJson(cLayConf)
        
        oJlayMIH["Components"][(nPosLay := aScan( oJlayMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'servico'} ))]["ComponentContent"] := self:oConfigWiz['cServico']
        oJlayMIH["Components"][(nPosLay := aScan( oJlayMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'ip'} ))]["ComponentContent"] := self:oConfigWiz['cIP']
        oJlayMIH["Components"][(nPosLay := aScan( oJlayMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'porta'} ))]["ComponentContent"] := self:oConfigWiz['cPorta']
        oJlayMIH["Components"][(nPosLay := aScan( oJlayMIH["Components"], {|x| Alltrim(x["IdComponent"]) == 'ambiente'} ))]["ComponentContent"] := self:oConfigWiz['cAmb']

        Begin Transaction

            RecLock("MIH", .T.)
                MIH->MIH_FILIAL := xFilial("MIH")
                MIH->MIH_TIPCAD := aGithub[nPos][1]
                MIH->MIH_ID     := getSxeNum("MIH", "MIH_ID")
                MIH->MIH_DESC   := self:oConfigWiz['cServico']
                MIH->MIH_ATIVO  := '1'
                MIH->MIH_DATINC := FWTimeStamp(3)
                MIH->MIH_DATALT := FWTimeStamp(3)
                MIH->MIH_CONFIG := oJlayMIH:toJson()   
            MIH->( MsUnLock() )

            ConfirmSx8()

        End Transaction

        self:lConfLoc := .F. //NECESSARIO ALTERAR EM CASOS DO USUARIO APERTAR NO VOLTAR
    Else
        self:oConfigWiz["mensagem"] := STR0044 //"Não foi encontrado o layout de configuração, favor verificar na tela de cadastro de layout ou baixar novamente no github."  
    EndIF

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} DeletAppWiz
Método que deleta as configurações dos serviços do appserver.ini

@author  Danilo Rodrigues
@since   27/07/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method DeletAppWiz() as Variant Class WizardObj

    Local nI        := 0
    Local oRPCSrv   := Nil
    Local aOnStart  := {}

    MIH->(DbSetOrder(1)) //MIH_FILIAL, MIH_TIPCAD, MIH_ID, R_E_C_N_O_, D_E_L_E_T_
    While MIH->(dbSeek(xFilial("MIH") + "CONFIGURACAO"))        
        Reclock("MIH",.F.)
        MIH->(dbDelete())
        MIH->(MsUnLock())

        MIH->(DbSkip())
    EndDo    

    oRpcSrv := TRpc():New( self:cAmb ) // cria o objeto que realizará a conexão com servidor externo

    If oRpcSrv:Connect(Alltrim(self:cIP),Val(self:cPorta)) //realiza a conexão com o servidor externo
        cIni        := oRpcSrv:CallProc("GetAdv97")
        cOnStart    := oRpcSrv:CallProc("GetPvProfString", "OnStart", "jobs", "", cIni)
        cRate       := oRpcSrv:CallProc("GetPvProfString", "OnStart", "RefreshRate", "", cIni)
        aSessions   := oRpcSrv:CallProc("GetIniSessions",cIni)

        For nI := 1 To Len(self:aJobs)
                If (nPos := AScan(aSessions,{|x| UPPER(x) == self:aJobs[nI]})) > 0 
                    oRpcSrv:CallProc("DeleteSectionIni", aSessions[nPos], cIni )
                EndIF
        NexT nI

        aOnStart := StrTokArr2( cOnStart, ",", .F. )        
        For nI := 1 To Len(self:aJobs)
                If (nPos := AScan(aOnStart,{|x| UPPER(x) == self:aJobs[nI]})) > 0 
                    aDel(aOnStart,nPos)
                    aSize( aOnStart, Len( aOnStart ) - 1 )                    
                EndIF
        NexT nI

        cOnStart := ArrTokStr(aOnStart)
        If Empty(cOnStart)
             oRpcSrv:CallProc("DeleteSectionIni", "Onstart", cIni )    
        Else
            oRpcSrv:CallProc("WritePProString","OnStart","JOBS",cOnStart,cIni)
        EndIf
        oRpcSrv:Disconnect()
        FwFreeObj(oRpcSrv)
    Else
        self:oConfigWiz["mensagem"] := STR0037 //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
        Return .F.
    EndIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} validaPreReqs
Método que efetua a validação dos pré requisitos da integração

@author  Rafael Tenorio da Costa
@since   07/09/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method validaPreReqs() as Variant Class WizardObj

    Local aArea     := GetArea()                                        as Array
    Local cIndexMIM := "MIM_FILIAL+MIM_CPROCE+MIM_ETAPA+MIM_FUNCAO"     as Character
    Local cTabela	:= GetNextAlias()                                  as Character
    Local cFiltro   := ""                                               as Character

    If !( cIndexMIM == Alltrim( MIM->( IndexKey(1) ) ) )
        self:lReturn                := .F.
        self:oConfigWiz["mensagem"] := STR0056   //"O índice da tabela MIM está desatualizado, aplique o último pacote acumulado do varejo e realize o procedimento novamente."
    EndIf

    If self:lReturn
        cFiltro := '%' + Alltrim(RMIFiltPro("NCM")) + '%'

	    BeginSql Alias cTabela
            SELECT COUNT(1) AS QTD 
		    FROM %Table:CLK% CLK 
            WHERE %exp:cFiltro%
            AND CLK.%NotDel%
        EndSQl

        If (cTabela)->(Eof()) .Or. (cTabela)->QTD == 0
            self:lReturn                := .F.
            self:oConfigWiz["mensagem"] := STR0075  //"A configuração da integração não poderá ser realizada.  Para que o processo de integração de Produto e NCM ocorra, a tabela CLK deve estar atualizada e dentro do período de vigência (CLK_DTINIV e CLK_DTFIMV).  Realize o processo de atualização, para maiores detalhes acesse, módulo FISCAL: Atualizações \ Cadastros \ Tabela IBPT (FISA092).  Após o processo de atualização, execute novamente o Wizard de configuração."
            LjGrvLog( " PshWizard ", STR0075 ) 
        Endif

        (cTabela)->( DbCloseArea() )
    Endif

    RestArea(aArea)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} carga
Método que efetua a carga de dados e preparação do ambiente para integração
- prepara processos
- cria campos para a integração

@author  Rafael Tenorio da Costa
@since   07/09/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method carga() as Variant Class WizardObj

    If self:lReturn
        //---------------- Carrega processos MHN/MHS e os ajusta conforme integração selecionada
        Processa( {|| RmiCargaPr(), RmiGrvProc(self:aProcessos, .F.)}, STR0027, STR0028)    //"Carregando Processos Padrões", "Aguarde. . ."
        
        //------------------Cria o campo S_T_A_M_P_ nas tabelas de integração
        Processa( {|| self:lReturn := self:ValidaStamp()}, STR0040, STR0028)                //"Criando os campos S_T_A_M_P_ nas tabelas de integração", "Aguarde. . ."
    EndIf

Return Nil

Method componentes() as Variant Class WizardObj

    Local aCoords01 := {} as Array

    aCoords01 := FWGetDialogSize()

	self:oCoordTela := JsonObject():New()

    If aCoords01[4] <= 880 //1505(1366x768);876(800x600);1125(1024x768)
        self:oCoordTela["nCord04"] :=  30
        self:oCoordTela["nCord05"] :=  47
        self:oCoordTela["nCord06"] :=  45

        self:oCoordTela["nCord07"] :=  67
        self:oCoordTela["nCord08"] :=  65
        self:oCoordTela["nCord11"] :=  67
        self:oCoordTela["nCord12"] :=  65

        self:oCoordTela["nCord09"] :=  82
        self:oCoordTela['nCord10'] :=  80
        self:oCoordTela["nCord13"] :=  82
        self:oCoordTela["nCord14"] :=  80
        self:oCoordTela["nCord15"] :=  150
        self:oCoordTela["nCord16"] :=  160  
        self:oCoordTela["nCord17"] :=  170

        self:oCoordTela["nCord01"] :=  103
        self:oCoordTela["nCord02"] :=  self:oCoordTela["nCord01"] + 20
        self:oCoordTela["nCord03"] :=  self:oCoordTela["nCord02"] + 10

        self:oCoordTela["nColu01"] :=  190
        self:oCoordTela["nColu02"] :=  220
        self:oCoordTela["nColu03"] :=  190
        self:oCoordTela["nColu04"] :=  220
    else
        self:oCoordTela["nCord04"] :=  40
        self:oCoordTela["nCord05"] :=  62
        self:oCoordTela["nCord06"] :=  60
        self:oCoordTela["nCord07"] :=  82
        self:oCoordTela["nCord08"] :=  80
        self:oCoordTela["nCord11"] :=  92
        self:oCoordTela["nCord12"] :=  90
        self:oCoordTela["nCord09"] :=  102
        self:oCoordTela["nCord10"] :=  100
        self:oCoordTela["nCord13"] :=  122
        self:oCoordTela["nCord14"] :=  120
        self:oCoordTela["nCord15"] :=  150
        self:oCoordTela["nCord16"] :=  160  
        self:oCoordTela["nCord17"] :=  170
        
        self:oCoordTela["nCord01"] :=  142
        self:oCoordTela["nCord02"] :=  180
        self:oCoordTela["nCord03"] :=  190
        
        self:oCoordTela["nColu01"] :=  20
        self:oCoordTela["nColu02"] :=  50
        self:oCoordTela["nColu03"] :=  200
        self:oCoordTela["nColu04"] :=  220
    EndIf

Return Nil
