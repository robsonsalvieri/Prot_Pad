#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHPWIZ.CH"
#INCLUDE "TLPP-CORE.TH"

namespace totvs.protheus.retail.rmi.wizard.configuracao.Napp
using namespace totvs.protheus.retail.rmi.wizard.configuracao.WizardObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe WizardObj
Classe responsável pela construção das telas de configuração do Wizard
    
/*/
//-------------------------------------------------------------------
Class Napp from WizardObj

    Protected Data lNovo    as  Logical

    Public Method New()                     as Object
    Public Method ConstroiTelas()           as Variant              //Método que define a construção das telas do Wizard.

    Public Method TelaCredenciais(oPanel    as Object) As Variant   //Construção da tela de Credenciais do Venda Digital
    Public Method ValidaCredenciais()       as Logical              //Bloco de validação da tela de Credenciais

    Public  Method Validaconfirmacao()      as Logical              //Bloco de validação da tela de confirmação
    Public  Method Voltar()                 as Logical              //Validação do botão 'Voltar' do Wizard
    Public  Method Cancelar()               as Logical              //Validação do botão 'Cancelar' do Wizard

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Metodo construtor da classe

@type       Method
@author     Eduardo Sales
@since      12/09/2025
@version    P12
@param      cTitulo, caractere, Processo que esta em execução
/*/
//-------------------------------------------------------------------
Method New(cTitulo as Character, cAssinante as Character) as Object Class Napp

    Self:lNovo := .F.
    _Super:New(cAssinante, cTitulo)
    
    Self:oConfigWiz['oTstAssi'] := RmiEnvNappObj():New("WIZARD", cAssinante)
    
    Self:lReturn := Self:BaixaGit(Self:oConfigWiz['oTstAssi'])

Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} ConstroiTelas
Método que define a construção das telas do Wizard.

@type       Method
@author     Eduardo Sales
@since      12/09/2025
@version    P12
/*/
//-------------------------------------------------------------------
Method ConstroiTelas() as Variant Class Napp

    Local oNewPag   := Nil as Object    //Objeto que adiciona nova pagina no wizard

	// 02 ------------------------------Configuração de Credenciais
	oNewPag := Self:oSHPWiz:AddStep("2",{|oPanel|Self:TelaCredenciais(oPanel)})
	oNewPag:SetStepDescription(STR0002)   //"Configuração de Credenciais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               
	oNewPag:SetNextAction({||Self:ValidaCredenciais()})
	oNewPag:SetCancelAction({|| Self:Cancelar() })
    oNewPag:SetPrevAction({|| Self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar

	//Ativa Wizard
	Self:oSHPWiz:Activate()
	//Desativa Wizard
	Self:oSHPWiz:Destroy()

    If Self:lNovo
        
        Self:oConfigWiz['cUser'] := Space(50)
        Self:oConfigWiz['cSenha'] := Space(50) 

        Self:oSHPWiz := FWWizardControl():New( , {Self:aCoords[3] , Self:aCoords[4] * 0.5} )// Define o tamanho do wizard  ex: {600,800}
	    Self:oSHPWiz:ActiveUISteps()    
        Self:ConstroiTelas()
        
    EndIf

Return Nil


//-------------------------------------------------------------------
/*/{Protheus.doc} voltar
Implentações ao clicar no botão voltar

@type       Method
@author     Eduardo Sales
@since      12/09/2025
@version    P12
/*/
//-------------------------------------------------------------------
Method Voltar() as Logical Class Napp
    Self:oConfigWiz["mensagem"] := ""
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} TelaCredenciais
Construtor de tela de Credenciais do Wizard

@type       Method
@author     Eduardo Sales
@since      12/09/2025
@version    P12   
/*/
//-------------------------------------------------------------------
Method TelaCredenciais(oPanel As Object) Class Napp

    Local oSay1     as Object
    Local oSay2     as Object
    Local oSay3     as Object
    Local oSay4     as Object
    Local oGet1     as Object
    Local oGet2     as Object
    Local oSayMsg   as Object

    //"Criando os campos S_T_A_M_P_ nas tabelas de integração", "Aguarde. . ."
    Processa( {|| _Super:ValidaStamp(StrTokArr("SF1|SF2|SF3|SD2|SL1|SL2|SL4|SL1|SLX","|"))}, STR0040, STR0028)

    If !Self:oConfigWiz:HasProperty("cUser")
        Self:oConfigWiz['cUser'] := Space(50)
    EndIf

    If !Self:oConfigWiz:HasProperty("cPassword")
        Self:oConfigWiz['cPassword'] := Space(50)
    EndIf
	
	oSay1:= TSay():New(10,20,{||STR0002},oPanel,,Self:oConfigWiz['oTBold'],,,,.T.,,,200,20)//'Configuração de Credenciais'

    oSay2:= TSay():New(40,20,{||I18n(STR0008,{Self:oConfigWiz['cComboProd']})},oPanel,,Self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Informe suas credenciais do #1 "

    oSay3:= TSay():New(62,20,{||STR0009},oPanel,,Self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Usuário: "
    oGet1:= TGet():New(60,50,{|u| if( PCount() > 0, Self:oConfigWiz['cUser'] := u, Self:oConfigWiz['cUser']) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oConfigWiz['cUser'],,,, )

    oSay4:= TSay():New(82,20,{||STR0010},oPanel,,Self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Senha: "
    oGet2:= TGet():New(80,50,{|u| if( PCount() > 0, Self:oConfigWiz['cPassword'] := u, Self:oConfigWiz['cPassword'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.T.,,Self:oConfigWiz['cPassword'],,,,)

    Self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(142,20,{||Self:oConfigWiz["mensagem"]},oPanel,,Self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,250,20)

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Cancelar
Implentações ao clicar no botão cancelar

@type       Method
@author     Eduardo Sales
@since      12/09/2025
@version    P12
/*/
//-------------------------------------------------------------------
Method Cancelar() as Logical Class Napp
    Self:lNovo := .F.
Return .T. 
