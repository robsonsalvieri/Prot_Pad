#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHPWIZ.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "WIZCFGINTEGTOTVSPDV.CH"

namespace totvs.protheus.retail.rmi.wizard.configuracao.IntegTOTVSPdv
using namespace totvs.protheus.retail.rmi.wizard.configuracao.WizardObj

/*/{Protheus.doc} Classe WizardObj
Classe responsável pela construção das telas de configuração do Wizard de Configuraçao dos Jobs da Integraçao TOTVS PDV     
/*/
Class IntegTOTVSPdv from WizardObj

    Protected Data lNovo    as  Logical    
    
    Private Data aAmbienteSelect    as Array
    Private Data oFontBold16        as Object
    Private Data oFontBold12        as Object
    Private Data lTestConnApi       as Logical
    Private Data aJGruposTabelas    as Array    
    Private Data oDadosAmbiente     as Object
    Private Data cKeyRC4            as Character

    Public Data nRadio              as Numeric
    Public Data aAmbienteDetect     as Array
    Public Data oDadosAmbienteDigit as Object
    Public Data oJAssConfig         as Object
    Public Data cGruposTab          as Character
    Public Data aGruposTabelas      as Array
    Public Data lTelaGruposTab      as Logical
    
    Public Method New()                 as Object
    Public Method ConstroiTelas()       as Variant
    Public Method TelaSeleTipoInteg(oPanel as Object)   as Variant
    Public Method ValidaOpcao()                         as Logical
    Public Method Validaconfirmacao()   as Logical
    //Public Method Voltar()              as Logical
    Public Method Cancelar()            as Logical
    Public Method TelaInfosAmbiente()   as Variant    
    Public Method SeletedGruposTab()    as Variant
    //Public Method GetEndPoint()         as Character
    Public Method SelectGrupo()         as Variant
    //Public Method BtTestConxaoAPI()     as Variant
    Public Method GetConfigAssin()      as Variant
    Public Method BtSelectGruposTab()   as Variant
    Public Method WriteIni()            as Logical
    Public Method ValidaAmbiente()      as Variant

    Private Method ConexaoAPI()         as Logical
    Private Method SetConfigAssin()     as Variant
    Private Method GravaConfigAssin()   as Variant
    Private Method ValidDadosConect()   as Array
    Private Method TelaSeleGruposTab()  as Variant
    Private Method SetGrupoTabelas()    as Variant  
    Private Method DetectaAmbiente()    as Variant
    Private Method GetNomeJob()         as Character

    Protected Method carga()            as Variant

EndClass

/*/{Protheus.doc} New
Método construtor da Classe
@author joao.marcos
@since  17/04/2024
@version 1.0
/*/
Method New(cTitulo as Character)  as Object Class IntegTOTVSPdv

    self:lNovo := .F.
    _Super:New("TOTVS PDV", cTitulo)

    Self:oFontBold16        := TFont():New(,,-16,.T.,.T.)
    Self:oFontBold12        := TFont():New(,,-12,.T.,.T.)
    Self:oDadosAmbiente     := JsonObject():New()
    Self:oDadosAmbienteDigit:= JsonObject():New()
    Self:oJAssConfig        := JsonObject():New()
    Self:nRadio             := 0
    Self:aAmbienteDetect    := {0,""}
    Self:cGruposTab         := Space(100)
    Self:aGruposTabelas     := {}
    Self:lTestConnApi       := .F.
    Self:aJGruposTabelas    := {}
    Self:cKeyRC4            := "0123456789*!@#$%&"
    Self:lTelaGruposTab     := .F.

    // Verifica se existe o assiante TOTVS PDV e Inclui Assinante caso não exista
    If Empty( GetAdvFVal( "MHO", "MHO_COD", xFilial("MHO") + self:cAssinante, 1, "" ) ) 
        RMIInclAss(self:cAssinante)
    EndIf

    Self:lReturn := .T.
    Self:DetectaAmbiente() 
    Self:SetConfigAssin()
    Self:ValidaAmbiente() 
    Self:oConfigWiz["mensagem"] := ""

Return self

/*/{Protheus.doc} SetConfigAssin
Seta os dados do campo MHO_CONFIG 
@author joao.marcos
@since  22/04/2024
@version 1.0
/*/
Method SetConfigAssin() Class IntegTOTVSPdv
Local aAreaMHO := MHO->(GetArea())  as Array

MHO->(dbSetOrder(1)) // MHO_FILIAL+MHO_COD
If MHO->(dbSeek( xFilial("MHO") + self:cAssinante ))
    Self:oJAssConfig:FromJson( MHO->MHO_CONFIG )
    Self:oJAssConfig['CodPontoInt'] := Space(TamSX3("MIQ_COD")[1])
    Self:oJAssConfig['usuario']     := Space(50)
    Self:oJAssConfig['senha']       := Space(50)
    Self:oJAssConfig['endpoint']    :=  Space(100) 
    Self:oJAssConfig['nQtdMxLote']      := "1000"
    Self:oJAssConfig['GruposTabelas']   := ""
EndIf

RestArea(aAreaMHO)

Return

/*/{Protheus.doc} GravaConfigAssin
Grava as configuraçoes do Assinante no campo MHO_CONFIG 
@author joao.marcos
@since  22/04/2024
@version 1.0
/*/
Method GravaConfigAssin() Class IntegTOTVSPdv
Local oJConfig  := JsonObject():New()   as Object
Local aAreaMHO := MHO->(GetArea())  as Array

MHO->(dbSetOrder(1)) // MHO_FILIAL+MHO_COD

MHO->(dbSeek( xFilial("MHO") + self:cAssinante ) )

oJConfig:FromJson(MHO->MHO_CONFIG)
oJConfig["usuario"]         :=  Self:oJAssConfig['usuario']
If !Empty( Self:oJAssConfig['senha'])
    oJConfig["senha"]           :=  AllTrim( Rc4Crypt( Self:oJAssConfig['senha'] ,Self:cKeyRC4, .T.) )
/*    
Else
    oJConfig["senha"]           :=  AllTrim( Self:oJAssConfig['senha'] )
*/    
EndIf
oJConfig["endpoint"]        :=  Self:oJAssConfig['endpoint']
oJConfig["nQtdMxLote"]      :=  Self:oJAssConfig['nQtdMxLote']
oJConfig["GruposTabelas"]   :=  Iif(Empty(oJConfig["GruposTabelas"]), Self:cGruposTab, oJConfig["GruposTabelas"] + "," + Self:cGruposTab)

MHO->(RecLock("MHO",.F.))
MHO->MHO_CONFIG := oJConfig:ToJson()
MsUnLock()

RestArea(aAreaMHO)

Return

/*/{Protheus.doc} DetectaAmbiente
Detecta o ambiente que o usuário esta logado
@type  Method
@author joao.marcos
@since 11/04/2024
@version 1.0
@return lRet, logico, retorno do valid
/*/
Method DetectaAmbiente() Class IntegTOTVSPdv
Local lIsPDV        := AllTrim(GetPvProfString( GetEnvServer()  , "PosLight", "", GetAdv97() ))  == "1"  as Logical
Local lIsCentralPDV := AllTrim(GetPvProfString( "CPDV"          , "ISCPDV"  , "", GetAdv97() ))  == "1"  as Logical

Self:oDadosAmbiente['Grupo']        := FWGrpCompany()
Self:oDadosAmbiente['Filial']       := cFilAnt
Self:oDadosAmbiente['Ambiente']     := GetEnvServer()
Self:oDadosAmbiente['IP']           := GetServerIP()
Self:oDadosAmbiente['Porta']        := AllTrim( GetPvProfString( "TCP" , "Port", "", GetAdv97() ) )

If lIsPDV
    Self:nRadio := 4
    Self:aAmbienteDetect := {4,"TOTVS PDV"}
ElseIf lIsCentralPDV
    Self:nRadio := 2
    Self:aAmbienteDetect := {2,"Central PDV"}
Else
    Self:nRadio := 1
    Self:aAmbienteDetect := {1,"Retaguarda"}
EndIf
 
Return

/*/{Protheus.doc} SetConfigAssin
Seta os dados do campo MHO_CONFIG 
@author joao.marcos
@since  22/04/2024
@version 1.0
/*/
Method ValidaAmbiente() Class IntegTOTVSPdv
//Local aRet      := {.T.,""}     as Array
Local cMsgErro  := ""           as Character

If Self:aAmbienteDetect[1] == 1 // Retaguarda

    If (FWModeAccess("MIR",1) <> "C" .OR. FWModeAccess("MIR",2) <> "C" .OR. FWModeAccess("MIR",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MIR deve ser totalmente compartilhado." + CHR(10)        
    EndIf
    If (FWModeAccess("MHN",1) <> "C" .OR. FWModeAccess("MHN",2) <> "C" .OR. FWModeAccess("MHN",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MHN deve ser totalmente compartilhado." + CHR(10)
    End
    If (FWModeAccess("MHP",1) <> "C" .OR. FWModeAccess("MHP",2) <> "C" .OR. FWModeAccess("MHP",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MHP deve ser totalmente compartilhado." + CHR(10)
    EndIf    
    If (FWModeAccess("MHQ",1) <> "C" .OR. FWModeAccess("MHQ",2) <> "C" .OR. FWModeAccess("MHQ",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MHQ deve ser totalmente compartilhado." + CHR(10)
    EndIf
    If (FWModeAccess("MHR",1) <> "C" .OR. FWModeAccess("MHR",2) <> "C" .OR. FWModeAccess("MHR",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MHR deve ser totalmente compartilhado." + CHR(10)
    EndIf
    If (FWModeAccess("MIN",1) <> "C" .OR. FWModeAccess("MIN",2) <> "C" .OR. FWModeAccess("MIN",3) <> "C")
        cMsgErro += "Compartilhamento da tabela MIN deve ser totalmente compartilhado." + CHR(10)
    EndIf
    If FWModeAccess("MIP",3) <> "E"
        cMsgErro += "Compartilhamento da tabela MIP deve ser exclusivo por filial." + CHR(10)
    EndIf
EndIf

If (FWModeAccess("MHO",1) <> "C" .OR. FWModeAccess("MHO",2) <> "C" .OR. FWModeAccess("MHO",3) <> "C")
    cMsgErro += "Compartilhamento da tabela MHO deve ser totalmente compartilhado." + CHR(10)
EndIf

If !Empty(cMsgErro)
    Self:lReturn := .F.
    MsgInfo(cMsgErro + CHR(13)+CHR(10) +;
    "Faça os ajustes necessários e acesse a rotina novamente.")
EndIf

Return

/*/{Protheus.doc} ConstroiTelas
Método que define a construção das telas do Wizard.
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method ConstroiTelas() as Variant Class IntegTOTVSPdv

    Local oNewPag   := Nil as Object    //Objeto que adiciona nova pagina no wizard

	// 02 ------------------------------ Seleção de Ambiente
	oNewPag := self:oSHPWiz:AddStep("2",{|oPanel|self:TelaSeleTipoInteg(oPanel)})
	oNewPag:SetStepDescription(STR0001)   //"Seleção de Ambiente" 
	oNewPag:SetNextAction({||self:ValidaOpcao()})
	oNewPag:SetCancelAction({|| self:Cancelar() })
    oNewPag:SetPrevWhen({|| .F. })
	
    // 03 ------------------------------ Dados do Ambiente
    oNewPag := self:oSHPWiz:AddStep("3",{|oPanel|self:TelaInfosAmbiente(oPanel)})
    oNewPag:SetStepDescription(STR0002) //"Dados do Ambiente"
    oNewPag:SetNextAction({|oPanel|self:Validaconfirmacao(oPanel)})
    oNewPag:SetCancelAction({|| self:Cancelar() }) 
    oNewPag:SetPrevWhen({|| .F. })

	//Ativa Wizard
	self:oSHPWiz:Activate()
	//Desativa Wizard
	self:oSHPWiz:Destroy()

Return Nil

/*/{Protheus.doc} voltar
Implentações ao clicar no botão voltar
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*//*
Method Voltar() as Logical Class IntegTOTVSPdv
    self:oConfigWiz["mensagem"] := ""
Return .T.
*/

/*/{Protheus.doc} TelaSeleTipoInteg
Tela para selecao do ambiente para configuraçao
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method TelaSeleTipoInteg(oPanel As Object) Class IntegTOTVSPdv
Local aItems    := {STR0003,STR0004,STR0005,STR0006} as Array // 'Retaguarda'## 'Central PDV como Geradora de dados'## 'Central PDV como Receptora de dados' ## 'TOTVS PDV'
Local oRadio    := Nil  as Object
Local nLinha    := 10   as Numeric
Local oSay1     := NIL  as Object
Local oSay2     := NIL  as Object
Local cTexto1   := STR0007 as Character // "O objetivo deste Wizard é efetuar a configuração da Integração de dados entre os ambientes Retaguarda e Central PDV/ TOTVS PDV."
Local cTexto2   := STR0008 as Character // "Selecione abaixo em qual ambiente a configuração será feita:"
Local cTexto3   := STR0009 as Character // "Detecção automática de ambiente: "
Local oGroup1   := Nil  as Object

oSay1  := TSay():New(nLinha,self:aCoords[4]/4,{||STR0010},oPanel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20) // "Bem Vindo!"
nLinha += 20
oSay2  := TSay():New(nLinha,15,{||cTexto1},oPanel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20) // 
nLinha += 25
oSay2  := TSay():New(nLinha,15,{||cTexto2},oPanel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20)

nLinha += 15
oRadio := TRadMenu():New (nLinha,15,aItems,,oPanel,,,,,,,,150,12,,,,.T.)
oRadio:bSetGet := {|u|Iif (PCount()==0,Self:nRadio,Self:nRadio:=u)}

nLinha += 70
oGroup1:= TGroup():New(nLinha,15,nLinha+20, (self:aCoords[4]/2)-10,'',oPanel,,,.T.) // 300
nLinha += 5
oSay2  := TSay():New(nLinha,20,{||cTexto3 + Self:aAmbienteDetect[2] },oGroup1,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20)

Return Nil

/*/{Protheus.doc} ValidaOpcao
Função de validação opção selecionada no Wizard
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method ValidaOpcao() Class IntegTOTVSPdv
Local lRet := .T. as Logical

If lRet
    If Self:nRadio == 1
        Self:aAmbienteSelect := {1,"Retaguarda"}
    ElseIf Self:nRadio == 2
        Self:aAmbienteSelect := {2,"Central PDV"}
    ElseIf Self:nRadio == 3
        Self:aAmbienteSelect := {3,"Central PDV"}
    ElseIf Self:nRadio == 4
        Self:aAmbienteSelect := {4,"TOTVS PDV"}
    EndIf

    If (Self:aAmbienteSelect[1] <> Self:aAmbienteDetect[1]) .AND. !(Self:aAmbienteSelect[1] == 3 .AND. Self:aAmbienteDetect[1] == 2)
        lRet := MSGNoYes(STR0012 + AllTrim(Self:aAmbienteSelect[2] + STR0013 + Self:aAmbienteDetect[2] +;
                STR0014 )) // "Foi selecionado o ambiente " ## " porém o sistema detectou o ambiente " ## ". Deseja realmente seguir a configuração com o ambiente selecionado?"
    EndIf

EndIf

Return lRet

/*/{Protheus.doc} TelaInfosAmbiente
Tela para apresentar e receber informações do ambiente selecionado
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method TelaInfosAmbiente(oPanel) Class IntegTOTVSPdv
Local oGroup1       := Nil as Object
Local oSay1         := NIL as Object
Local oSay2         := NIL as Object
// Grupo
Local oSay3         := NIL as Object 
Local oGet3         := NIL as Object
// Filial
Local oSay4         := NIL as Object 
Local oGet4         := NIL as Object
// Ambiente
Local oSay5         := NIL as Object 
Local oGet5         := NIL as Object
// IP
Local oSay6         := NIL as Object 
Local oGet6         := NIL as Object
// Porta
Local oSay7         := NIL as Object 
Local oGet7         := NIL as Object
// Assinante
Local oSay8         := NIL as Object 
Local oGet8         := NIL as Object
// Conexao PDV
Local oGroup2       := Nil as Object
// Codigo do Ponto de Integração
Local oSay9         := NIL as Object 
Local oGet9         := NIL as Object
// Usuario para acesso a API REST
Local oSay10        := NIL as Object 
Local oGet10        := NIL as Object
// Senha
Local oSay11        := NIL as Object 
Local oGet11        := NIL as Object
// Endpoint
Local oSay12         := NIL as Object 
Local oGet12         := NIL as Object
// Numero maximo para lote de dados
Local oSay13         := NIL as Object 
Local oGet13         := NIL as Object
// Grupo de tabelas
Local oSay15         := Nil as Object
Local oGet15         := Nil as Object
Local oBtBuscaGrupos := Nil as Object
Local bBtBuscaGrupos := {|| Self:BtSelectGruposTab(oPanel, @oBitmpIconAPI,@oSay14), oPanel:Refresh() }
// Rodape
Local oSayRodape    := NIL  as Object 
Local cTxtRodape    := ""   as Character

Local nColAssin     := 0    as Numeric
Local nLinNrMaxLote := 0    as Numeric 
Local nColNrMaxLote := 0    as Numeric

Local oBitmpIconAPI  := Nil  as Object
Local oSay14         := Nil  as Object

Self:oDadosAmbienteDigit['Grupo']   := Self:oDadosAmbiente['Grupo']
Self:oDadosAmbienteDigit['Empresa'] := Self:oDadosAmbiente['Empresa']
Self:oDadosAmbienteDigit['UnidNeg'] := Self:oDadosAmbiente['UnidNeg']
Self:oDadosAmbienteDigit['Filial']  := Self:oDadosAmbiente['Filial']  
Self:oDadosAmbienteDigit['Ambiente']:= Self:oDadosAmbiente['Ambiente']  
Self:oDadosAmbienteDigit['IP']      := Self:oDadosAmbiente['IP']
Self:oDadosAmbienteDigit['Porta']   := Self:oDadosAmbiente['Porta']

Self:GetConfigAssin()

oSay1   := TSay():New(10,20,{||STR0002},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,200,20) // "Dados do Ambiente"
oSay2   := TSay():New(self:oCoordTela['nCord04'],20,{||STR0015},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,300,20) // "Os serviços da Integração TOTVS PDV serão configurados no ambiente abaixo:"

oGroup1 := TGroup():New(self:oCoordTela['nCord05']-10,15,self:oCoordTela['nCord06']+120,(self:aCoords[4]/4)-10,'',oPanel,,,.T.)
oGroup2 := TGroup():New(self:oCoordTela['nCord05']-10,(self:aCoords[4]/4)-10,self:oCoordTela['nCord06']+120,(self:aCoords[4]/2)-10,'',oPanel,,,.T.)

oSay3   := TSay():New(self:oCoordTela['nCord05'],20,{||STR0016},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) // "Grupo: "
oGet3   := TGet():New(self:oCoordTela['nCord06'],50,{|u| if( PCount() > 0, Self:oDadosAmbienteDigit['Grupo'] := u, Self:oDadosAmbienteDigit['Grupo']) } ,oPanel,20,010,,{|| .T.},0,/*10*/,,.F.,,.T.,,.T.,,.F.,.F.,/*20*/,.T.,.F.,,Self:oDadosAmbienteDigit['Grupo'],,,, )

oSay4   := TSay():New(self:oCoordTela['nCord05']+15,20,{||STR0017},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) // "Filial: "
oGet4   := TGet():New(self:oCoordTela['nCord06']+15,50,{|u| if( PCount() > 0, Self:oDadosAmbienteDigit['Filial'] := u, Self:oDadosAmbienteDigit['Filial'] ) } ,oPanel,50,010,,{|| !Empty(Self:oDadosAmbienteDigit['Filial']) },0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oDadosAmbienteDigit['Filial'],,,,)
oGet4:cF3       := "SM0"

oSay5   := TSay():New(self:oCoordTela['nCord05']+30,20,{||STR0018},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Ambiente: "
oGet5   := TGet():New(self:oCoordTela['nCord06']+30,50,{|u| if( PCount() > 0, Self:oDadosAmbienteDigit['Ambiente'] := u, Self:oDadosAmbienteDigit['Ambiente'] ) } ,oPanel,50,010,,{|| !Empty(Self:oDadosAmbienteDigit['Ambiente']) },0,/*10*/,,.F.,,.T.,,.T.,,.F.,.F.,,.F.,.F.,,Self:oDadosAmbienteDigit['Ambiente'],,,,)

oSay6   := TSay():New(self:oCoordTela['nCord05']+45,20,{||STR0019},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "IP: "
oGet6   := TGet():New(self:oCoordTela['nCord06']+45,50,{|u| if( PCount() > 0, Self:oDadosAmbienteDigit['IP'] := u, Self:oDadosAmbienteDigit['IP'] ) } ,oPanel,50,010,,{|| !Empty(Self:oDadosAmbienteDigit['IP'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oDadosAmbienteDigit['IP'],,,,)

oSay7   := TSay():New(self:oCoordTela['nCord05']+60,20,{||STR0020},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Porta: "
oGet7   := TGet():New(self:oCoordTela['nCord06']+60,50,{|u| if( PCount() > 0, Self:oDadosAmbienteDigit['Porta'] := u, Self:oDadosAmbienteDigit['Porta'] ) } ,oPanel,20,010,,{|| !Empty(Self:oDadosAmbienteDigit['Porta'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oDadosAmbienteDigit['Porta'],,,,)

// Lado Direito
If Self:nRadio == 1 .OR. Self:nRadio == 2  
    nColAssin := (self:aCoords[4]/4)+30
    nLinNrMaxLote   := self:oCoordTela['nCord06']
    nColNrMaxLote   := (self:aCoords[4]/4)+150
Else
    nColAssin       := (self:aCoords[4]/4)+90
    nLinNrMaxLote   := self:oCoordTela['nCord06']
    nColNrMaxLote   := (self:aCoords[4]/4)+150
EndIf

If Self:nRadio == 3 .OR. Self:nRadio == 4  
    oSay9   := TSay():New(self:oCoordTela['nCord05'],(self:aCoords[4]/4),{||STR0021},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Codigo do Ponto de Integraçao: "
    oGet9   := TGet():New(self:oCoordTela['nCord06'],(self:aCoords[4]/4)+90,{|u| if( PCount() > 0, Self:oJAssConfig['CodPontoInt'] := u, Self:oJAssConfig['CodPontoInt']) } ,oPanel,20,010,,{|| !Empty(Self:oJAssConfig['CodPontoInt'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oJAssConfig['CodPontInt'],,,,)
    oGet9:cTooltip  := STR0022 // "Informe o código do Ponto de Integração cadastrado na Retaguarda."
    oGet9:lShowHint := .T.    
    oGet9:bHelp     := {||ShowHelpCpo(STR0023, {STR0022}, 2, /*{||}*/, 2)} // "Ponto de Integração" ## "Informe o código do Ponto de Integração cadastrado na Retaguarda."

    oSay10  := TSay():New(self:oCoordTela['nCord05']+15,(self:aCoords[4]/4),{||STR0024},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Usuario para acesso a API REST: "
    oGet10  := TGet():New(self:oCoordTela['nCord06']+15,(self:aCoords[4]/4)+90,{|u| if( PCount() > 0, Self:oJAssConfig['usuario'] := u, Self:oJAssConfig['usuario'] ) } ,oPanel,50,010,,{|| !Empty(Self:oJAssConfig['usuario'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oJAssConfig['usuario'],,,,)
    oGet10:cTooltip     := STR0025 // "Este usuário será utilizado para geração do token de acesso a API."
    oGet10:lShowHint    := .T.
    oGet10:bHelp        := {||ShowHelpCpo(STR0026, { STR0027, STR0028}, 2, /*{||}*/, 2)} // "Usuario acesso REST" ## "Informe um usuário para ser utilizado no geração do token de acesso a API." ## "Este usuário não precisa estar configurado com acesso a nenhuma filial."

    oSay11  := TSay():New(self:oCoordTela['nCord05']+30,(self:aCoords[4]/4),{||STR0029},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Senha: "
    oGet11  := TGet():New(self:oCoordTela['nCord06']+30,(self:aCoords[4]/4)+90,{|u| if( PCount() > 0, Self:oJAssConfig['senha'] := u, Self:oJAssConfig['senha'] ) } ,oPanel,50,010,,{|| !Empty(Self:oJAssConfig['senha'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oJAssConfig['senha'],,,,)
    oGet11:lPassword := .T.

    oSay12  := TSay():New(self:oCoordTela['nCord05']+45,(self:aCoords[4]/4),{||STR0030},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) // "Endpoint do serviço Rest: "
    oGet12  := TGet():New(self:oCoordTela['nCord06']+45,(self:aCoords[4]/4)+90,{|u| if( PCount() > 0, Self:oJAssConfig['endpoint'] := u, Self:oJAssConfig['endpoint'] ) } ,oPanel,100,010,,{|| !Empty(Self:oJAssConfig['endpoint'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oJAssConfig['endpoint'],,,,)
    oGet12:cTooltip     := STR0031 // "Informe o Endpoint do serviço REST ativo no server."
    oGet12:lShowHint    := .T.
    oGet12:bHelp        := {||ShowHelpCpo(STR0032, {STR0031,STR0033,STR0034}, 2, /*{||}*/, 2)} // "EndPoint" ## "Informe o Endpoint do serviço REST ativo no server." ## "O EndPoint é formado pelo IP do server, porta configurada na chave HTTPREST e URL da chave HTTPURI do arquivo appserver.ini" ## "Exemplo: 127.0.0.1:8080/REST"

    oSay15  := TSay():New(self:oCoordTela['nCord05']+60,(self:aCoords[4]/4),{||STR0035},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) // "Grupos de tabelas: "
    oGet15  := TGet():New(self:oCoordTela['nCord06']+60,(self:aCoords[4]/4)+90,{|u| if( PCount() > 0, Self:cGruposTab := u, Self:cGruposTab ) } ,oPanel,70,010,,{|| !Empty(Self:oJAssConfig['endpoint'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:cGruposTab,,,,)
    oGet15:lReadOnly    := .T.
    oGet15:cTooltip     := STR0036 // "Informe os grupos de tabelas que deseja buscar nesta thread."
    oGet15:lShowHint    := .T.
    oGet15:bHelp        := {||ShowHelpCpo(STR0035, {STR0036, STR0037, STR0038}, 2, /*{||}*/, 2)} // "Grupos de Tabelas" ## "Informe os grupos de tabelas que deseja buscar nesta thread." ## "Utilize o botão Selecionar Grupos e selecione os grupos de tabela que deseja incluir." ## "Para criar mais de uma thread refaça a operação informando os outros grupos de tabelas."
    oBtBuscaGrupos      := TButton():New( self:oCoordTela['nCord06']+60, (self:aCoords[4]/4)+165, STR0039,oPanel,bBtBuscaGrupos, 50,13,,,.F.,.T.,.F.,,.F.,,,.F. ) // "Selecionar Grupos"
    /* Nao sera utilizado no momento
    oBtTesteCon     := TButton():New( self:oCoordTela['nCord06']+95, (self:aCoords[4]/4)+100, "Testar conexão com a API",oPanel,bBtTestCon, 75,15,,,.F.,.T.,.F.,,.F.,,,.F. ) // "Testar conexão com a API"
    */
    oBitmpIconAPI   := TBitmap():New(self:oCoordTela['nCord06']+45, (self:aCoords[4]/4)+245, 25, 25, "API_OFF" , /*"C:\Temp\TOTVS.PNG"*/, .T., oPanel, {||}, NIL, .F., .F., NIL, NIL, .F., NIL, .T., NIL, .F.)
    oSay14          := TSay():New(self:oCoordTela['nCord05']+60,(self:aCoords[4]/4)+235,{||STR0040},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Conexão off."
EndIf

oSay13  := TSay():New(self:oCoordTela['nCord05']+75,20,{||STR0042},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)  // "Numero máximo para lote: "
oGet13  := TGet():New(self:oCoordTela['nCord06']+75,95,{|u| if( PCount() > 0, Self:oJAssConfig['nQtdMxLote'] := u, Self:oJAssConfig['nQtdMxLote'] ) } ,oPanel,20,010,,{|| !Empty(Self:oJAssConfig['nQtdMxLote'])},0,/*10*/,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,Self:oJAssConfig['nQtdMxLote'],,,,)
oGet13:cTooltip     := STR0043 // "Informe o numero máximo de registros para o lote de Integração."
oGet13:lShowHint    := .T.
oGet13:bHelp        := {||ShowHelpCpo(STR0044, {STR0043,STR0045}, 2, /*{||}*/, 2)} // "Lote" ## "Informe o numero máximo de registros para o lote de Integração." ## "O numero mínimo é de 500 registros."

If Self:nRadio == 1
    cTxtRodape := STR0046 + CHR(10) + STR0047 // "* O serviço de geração dos registros de Integração será criado no ambiente informado acima," ## "porém independente da filial informada a integração será gerada para todas as filiais do grupo."
ElseIf Self:nRadio == 2
    
ElseIf Self:nRadio == 3
    
ElseIf Self:nRadio == 4
    
EndIf

oSayRodape := TSay():New(self:oCoordTela['nCord05']+95 ,20,{|| cTxtRodape},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,self:aCoords[3],20)

Return

/*/{Protheus.doc} Validaconfirmacao
Função de validação a confirmação do Wizard
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method Validaconfirmacao(oPanel as Object) as Logical Class IntegTOTVSPdv
Local lRet          := .T.                                      as Logical
Local nTamGrupo     := Len(FWGrpCompany())                      as Numeric
Local nTamFilial    := Len(cFilAnt)                             as Numeric
Local lGrpSelec     := Len(AllTrim(Self:cGruposTab)) > 0        as Logical
Local nNumGrpTabSele := Len( STRTOKARR(Self:cGruposTab,",") )   as Numeric

If Self:lTelaGruposTab .AND. (Self:nRadio == 3 .OR. Self:nRadio == 4)
    MsgInfo(STR0062) // "Confirme a tela de seleção de Grupos de Tabelas."
    lRet := .F.
ElseIf !lGrpSelec .AND. (Self:nRadio == 3 .OR. Self:nRadio == 4)
    MsgInfo(STR0063) // "Selecione ao menos um grupo de tabelas."
    lRet := .F.
Else
    self:oConfigWiz['cEmpresa'] :=  PadR(Self:oDadosAmbienteDigit['Grupo'],nTamGrupo," ")
    self:oConfigWiz['cFil']     :=  PadR(Self:oDadosAmbienteDigit['Filial'],nTamFilial," ")

    self:oConfigWiz['cAmb']     := Self:oDadosAmbienteDigit['Ambiente']
    self:cAmb                   := Self:oDadosAmbienteDigit['Ambiente']
    self:oConfigWiz['cIP']      := Self:oDadosAmbienteDigit['IP']
    self:oConfigWiz['cPorta']   := Self:oDadosAmbienteDigit['Porta']
    self:cPorta                 := Self:oDadosAmbienteDigit['Porta']

    If Self:nRadio == 3 .OR. Self:nRadio == 4    
        self:aJobs  := {'RMIINTEPDV'}
    Else
        self:aJobs  := {'RMIPUBLICA','RMIDISTRIB','RMIENVIA'}
    EndIf

    If self:ValidaServicos()
        Self:GravaConfigAssin()
        Self:GetConfigAssin()
        MSGInfo(STR0048) // "Serviços configurados!"

        nNumGrpTabSele := Len( STRTOKARR(Self:oJAssConfig['GruposTabelas'],",") )

        If Len(Self:aJGruposTabelas) > nNumGrpTabSele .AND. MSGYesNo(STR0049,STR0050) // "Deseja configurar outra Thread para outros Grupos de Tabelas?" ## "Grupo de tabelas"
            Self:cGruposTab := Space(100)
            lRet := .F.
        EndIf
    EndIf

    If lRet
        Self:lReturn := lRet
        Self:oConfigWiz["mensagem"] := ""
    EndIf
EndIf

Return lRet

/*/{Protheus.doc} Cancelar
Implentações ao clicar no botão cancelar
@author joao.marcos
@since  17/04/2024
@version 1.0    
/*/
Method Cancelar() as Logical Class IntegTOTVSPdv
self:lNovo := .F.
Return .T. 

/*/{Protheus.doc} ValidDadosConect
Valida se os dados necessario para a conexao a API foram preenchidos
@type  Method
@author joao.marcos
@since 24/04/2024
@version 1.0
/*/
Method ValidDadosConect() as Array Class IntegTOTVSPdv
Local aRet := {.T.,""} as Array
/*
If Empty( Self:oJAssConfig['usuario'] )
    aRet[1] := .F.
    aRet[2] := STR0051 // "Usuário para conexão API não informado."
ElseIf Empty( Self:oJAssConfig['senha'] )
    aRet[1] := .F.
    aRet[2] := STR0052 // "Senha para conexão API não informada."
ElseIf Empty( Self:oJAssConfig['endpoint'] )
    aRet[1] := .F.
    aRet[2] := STR0053 // "EndPoint para conexão API não informado."
EndIf
*/
Return aRet

/*/{Protheus.doc} TelaSeleGruposTab
Apresenta tela para seleçao dos grupos de tabelas
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
/*/
Method TelaSeleGruposTab(oPanel as Object) Class IntegTOTVSPdv
Local nX        := 0    as Numeric 
Local aBranch   := {}   as Array
Local oTCBrwse  := Nil  as Object
Local nLinBrow  := 05 //Self:oCoordTela['nCord05']    as Numeric
Local nColBrow  := 10 //(self:aCoords[4]/4)              as Numeric
Local nLargBrow2    := (self:aCoords[4]/4) + 200
Local nAltBrow2     := 95//Self:oCoordTela['nCord05'] + 180
Local oPanel2       := Nil  as Object
Local oBtConfirma   := Nil as Object
Local bBtConfirma   := {|| Self:SeletedGruposTab(aBranch) , Self:lTelaGruposTab := .F., oTCBrwse:Hide(), oPanel2:Hide() } as Variant
Local oBtCancela    := Nil as Object
Local bBtCancela    := {|| Self:lTelaGruposTab := .F., oPanel2:Hide() } as Variant
Local bLDbClick     := {|| aBranch[oTCBrwse:nAt][1] := !aBranch[oTCBrwse:nAt][1], Self:aGruposTabelas[oTCBrwse:nAt][01] := !Self:aGruposTabelas[oTCBrwse:nAt][01] }
Local bSelectGrupo  := {|| Self:SelectGrupo(@aBranch), oTCBrwse:Refresh()} // Açao Marca Desmarca Todos

Self:lTelaGruposTab := .T.

oPanel2 := TPanel():New(Self:oCoordTela['nCord05']-10,(self:aCoords[4]/4)-10,"",oPanel,/*oTFont*/,.T.,,/*CLR_YELLOW*/,/*CLR_BLUE*/,310,115)

// Seleçao dos Grupos
oTCBrwse := TCBrowse():New( nLinBrow , nColBrow, nLargBrow2, nAltBrow2,, {"",STR0054, STR0055, STR0056},{20,50,50,100}, oPanel2,,,,,bLDbClick,,,,,,/*cMsg*/,.F.,,.T.,,/*.F.*/,,.F., /*.F.*/ ) // "Codigo Grupo", "Nome Grupo", "Tabelas"
oTCBrwse:lVScroll := .T.
oTCBrwse:lHScroll := .F.

For nX := 1 To Len(Self:aGruposTabelas)        
    aAdd(aBranch, { Self:aGruposTabelas[nX][01],;   //01-Seleçao
                    Self:aGruposTabelas[nX][02],;   //02- Codigo Grupo
                    Self:aGruposTabelas[nX][03],;   //03- Nome Grupo
                    Self:aGruposTabelas[nX][04] } ) //07- Tabelas
Next

oTCBrwse:SetArray( aBranch )
oTCBrwse:bLine := {|| { If( aBranch[oTCBrwse:nAt][01], LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" )),; //01 - Seleçao
                            aBranch[oTCBrwse:nAt][02],;  //02 - Codigo Grupo
                            aBranch[oTCBrwse:nAt][03],;  //03 - Nome Grupo
                            aBranch[oTCBrwse:nAt][04]} } //04 - Tabelas

// Marca desmarca todos - Grupos de Tabelas
oTCBrwse:bHeaderClick := bSelectGrupo

oBtConfirma := TButton():New( 100, 215/*(self:aCoords[4]/4)*/, STR0057,oPanel2,bBtConfirma, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) // Confirma
oBtCancela  := TButton():New( 100, 260/*(self:aCoords[4]/4)*/, STR0058,oPanel2,bBtCancela, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) // Cancela

Return 

/*/{Protheus.doc} SetGrupoTabelas
Popula o array aGruposTabelas, com os grupos de tabelas
@type  Method
@author joao.marcos
@since 26/03/2024
@version 1.0
/*/
Method SetGrupoTabelas() Class IntegTOTVSPdv
Local aGrupos   := {}
Local nCont     := 0
Local nGrpSele  := 0

For nCont := 1 To Len(Self:aJGruposTabelas)
    If !(Self:aJGruposTabelas[nCont]['CodigoTabela'] $ Self:oJAssConfig['GruposTabelas']) .AND. (Self:aJGruposTabelas[nCont]['CodigoTabela'] <> "999")// Nao apresenta os grupos ja configurados
        nGrpSele++
        AADD(aGrupos,{.F., Self:aJGruposTabelas[nCont]['CodigoTabela'],;
                        Self:aJGruposTabelas[nCont]['DescricaoTabela'],;
                        Self:aJGruposTabelas[nCont]['TabelasGrupos']})
    EndIf
Next

If nGrpSele >= 1
    Self:aGruposTabelas := aGrupos
EndIf

Return

/*/{Protheus.doc} SelectGrupo
Marca e desmarca todos Grupos de Tabelas
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@param aBranch, array, Grupos de tabelas
/*/
Method SelectGrupo( aBranch as array ) as Variant Class IntegTOTVSPdv
Local nX:= 0 as Numeric

For nX := 1 To Len(Self:aGruposTabelas)
    Self:aGruposTabelas[nX][1] := !Self:aGruposTabelas[nX][1]
    aBranch[nX][1] := !aBranch[nX][1]
Next

Return 

/*/{Protheus.doc} SeletedGruposTab
Grava os grupos selecionados na variavel cGruposTab em formata string, separados por ;
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@param aBranch, array, Grupos de tabelas
/*/
Method SeletedGruposTab( aBranch as array ) as Variant Class IntegTOTVSPdv
Local nX:= 0 as Numeric
Local cGruposTab    := "" as Character

For nX := 1 To Len(aBranch)
    If aBranch[nX][1]
        cGruposTab := cGruposTab + "," + aBranch[nX][2]
    EndIf
Next

cGruposTab := SubStr(cGruposTab,2,Len(cGruposTab))

Self:cGruposTab := cGruposTab

Return 

/*/{Protheus.doc} BtTestConxaoAPI
Testa a conexao com a API de Integraçao PDV
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
/*//* Nao sera utilizado no momento
Method BtTestConxaoAPI(oBitmpIconAPI as Object, oSay14 as Object) Class IntegTOTVSPdv
Local aValidConn := Self:ValidDadosConect() as Array

If aValidConn[1]
    If Self:ConexaoAPI(1)
        oBitmpIconAPI:SetBMP("API_ON")
        oSay14:SetText( "Conexão efetuada!" ) // "Conexão efetuada!" 
    Else
        oBitmpIconAPI:SetBMP("API_OFF")  
        oSay14:SetText( "Conexão off." )  // "Conexão off."
    EndIf
Else
    MSGInfo( aValidConn[2] )
EndIf
Return 
*/

/*/{Protheus.doc} BtSelectGruposTab
Açao do botao Selecionar Grupos, chama a conexao da API para buscar os grupos de tabelas e apresenta tela para seleçao
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
@param oBitmpIconAPI, Object, Objeto com o bitmap do icone utilizado na tela para sinalizar a conexao com a API
/*/
Method BtSelectGruposTab(oPanel as Object, oBitmpIconAPI as Object, oSay14 as Object) Class IntegTOTVSPdv
Local aValidConn := Self:ValidDadosConect() as Array

If aValidConn[1]
    If Self:ConexaoAPI(2)
        oBitmpIconAPI:SetBMP("API_ON")
        oSay14:SetText( STR0041 ) // "Conexão efetuada!" 

        Self:SetGrupoTabelas()
        If Len(Self:aGruposTabelas) > 0
            Self:TelaSeleGruposTab(oPanel)
        Else
            MSGInfo(STR0059) // "Todos os Grupos de Tabelas já foram configurados."
        EndIf
    Else
        oBitmpIconAPI:SetBMP("API_OFF")  
        oSay14:SetText( STR0040 )  // "Conexão off."
    EndIf
EndIf

Return 

/*/{Protheus.doc} ConexaoAPI
Chama a conexao com a API de Integraçao PDV
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
@param nOp, Numeric, opçao de API, 1 - Teste de Conecxao | 2 - Buscar Grupos de Tabelas
@return lRet, logico, retorno do valid
/*/
Method ConexaoAPI(nOp as Numeric) as Logical Class IntegTOTVSPdv
Local lRet          := .F.                  as Logical 
Local oConexaoApi   := Nil                  as Object 
Local oJDadosApi    := JsonObject():New()   as Object
Local aJParametros  := {}                   as Array
Local oJRetornoApi  := JsonObject():New()   as Object
Local nTamEndPoint  := Len(AllTrim(Self:oJAssConfig['endpoint'])) as Numeric 

oJDadosApi['usuario']   := AllTrim(Self:oJAssConfig['usuario'])
oJDadosApi['senha']     := AllTrim( Rc4Crypt( Self:oJAssConfig['senha'] ,Self:cKeyRC4, .T.) )
oJDadosApi['endpoint']  := AllTrim(Self:oJAssConfig['endpoint'])

/* Nao sera utilizado no momento
If nOp == 1 // Teste conexao
    // Formata os parametros da API TestComunicaIntegPdvApi
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[1]['NomeParam'] := "CodPontoInteg"
    aJParametros[1]['Conteudo'] := Self:oJAssConfig['CodPontoInt']
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[2]['NomeParam'] := "EmpresaPC"
    aJParametros[2]['Conteudo'] := Self:oDadosAmbienteDigit['Grupo']
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[3]['NomeParam'] := "FilialPC"
    aJParametros[3]['Conteudo'] := Self:oDadosAmbienteDigit['Filial']
    
    oJDadosApi['enderecoApi']   := "/api/retail/v1/integrapdv/TestComunicaIntegPdvApi/"
    oJDadosApi['tipoMetodoApi'] := "GET" 
    oJDadosApi['parametros']    := aJParametros
Else
*/
If nOp == 2 // Busca Grupos Tabelas

    // Formata os parametros da API TestComunicaIntegPdvApi
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[1]['NomeParam'] := "CodPontoInteg"
    aJParametros[1]['Conteudo'] := Self:oJAssConfig['CodPontoInt']
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[2]['NomeParam'] := "EmpresaPC"
    aJParametros[2]['Conteudo'] := Self:oDadosAmbienteDigit['Grupo']
    Aadd( aJParametros , JsonObject():New() )
    aJParametros[3]['NomeParam'] := "FilialPC"
    aJParametros[3]['Conteudo'] := Self:oDadosAmbienteDigit['Filial']

    oJDadosApi['enderecoApi']   := "/api/retail/v1/integrapdv/RetornaGruposTabelas/"
    oJDadosApi['tipoMetodoApi'] := "GET" 
    oJDadosApi['parametros']    := aJParametros       

EndIf

oConexaoApi := RMIConexaoAPI():New(self:cAssinante,oJDadosApi)

If oConexaoApi:lVldDadosAcesso
    oConexaoApi:ConectaApi()
    If oConexaoApi:GetComunicouApi()
        oJRetornoApi := oConexaoApi:RetornoApi()

        If oJRetornoApi['CodigoRetorno'] == "01"
            lRet := .T.
            Self:lTestConnApi := .T.

            If nOp == 2 // Grupo de Tabelas
                Self:aJGruposTabelas := oJRetornoApi['GrupoTabelas']
            EndIF               
        Else            
            MsgInfo(oJRetornoApi['Erro'])
        EndIf
    Else
        oJRetornoApi := oConexaoApi:RetornoApi()
        MsgInfo(oJRetornoApi['msgErro'])       
    EndIf
Else

EndIf

Return lRet

/*/{Protheus.doc} GetEndPoint
Retorna o EndPoint para acesso a API
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
@return Self:oJAssConfig['endpoint'], Character, EndPoint para acesso a API
/*/
/* Este metodo nao sera utilizado no momento
Method GetEndPoint() as Character Class IntegTOTVSPdv
Return Self:oJAssConfig['endpoint']
*/

/*/{Protheus.doc} GetConfigAssin
Retorna a configuraçao no Json do campo MHO_CONFIG no Json oJAssConfig
@type  Method
@author joao.marcos
@since 23/04/2024
@version 1.0
/*/
Method GetConfigAssin() Class IntegTOTVSPdv
Local oJConfig  := JsonObject():New()   as Object

If MHO->(dbSeek( xFilial("MHO") + self:cAssinante ) )
    oJConfig:FromJson(MHO->MHO_CONFIG)  
    If !Empty(oJConfig["senha"])
        Self:oJAssConfig['senha']           := AllTrim( Rc4Crypt(oJConfig["senha"], Self:cKeyRC4, .F., .T.) )
    EndIf  
    Self:oJAssConfig['usuario']         := PadR(oJConfig["usuario"],50," ")    
    Self:oJAssConfig['senha']           := PadR(Self:oJAssConfig['senha'],50," ")
    Self:oJAssConfig['endpoint']        := PadR(oJConfig["endpoint"],100," ")
    Self:oJAssConfig['nQtdMxLote']      := Iif(Empty(oJConfig["nQtdMxLote"]),"1000",oJConfig["nQtdMxLote"])
    Self:oJAssConfig['GruposTabelas']   := PadR(oJConfig["GruposTabelas"],100," ")
EndIf

Return

/*/{Protheus.doc} WriteIni
Grava os serviços selecionados no .ini do appserver
@author  joao.marcos
@since   25/04/2024
@version 1.0
/*/
Method WriteIni() as Logical Class IntegTOTVSPdv

Local aSessions := GetIniSessions("TOTVSAPPSERVER.INI")
Local nI        := 0        as Numeric
Local cIni      := ""       as Character
Local cOnStart  := ""       as Character
Local cRate     := ""       as Character
Local oRPCSrv   := Nil      as Object
Local cJobs     := ''       as Character
Local aOnStart  := {}       as Array
Local bErrorBlock   := Nil  as Variant
Local lErrorBlock   := .F.  as Logical 
Local cErrorBlock   := ""   as Character
Local cNomeJob      := ""   as Character

Local lRet      := .T.      as Logical

If !( (Alltrim(self:cIP) == Alltrim(self:oConfigWiz['cIP'])) .and. ; 
    (Alltrim(self:cPorta) == Alltrim(self:oConfigWiz['cPorta'])) .and. ;
    (Alltrim(self:cAmb) == Alltrim(self:oConfigWiz['cAmb'])) ) .or. ; //Usuário digitou dados de conexão diferentes do pré carregado
    self:lConfLoc .or.; //dados de servidor conectado (usuário não alterou nada) e não tem MIH salvo
    (self:lConfMIH .and. !(Alltrim(self:cIP) == Alltrim(getServerIP()) .and. ;
    Alltrim(self:cPorta) == Alltrim(Str(GetPort(1))) .and. ;
    Alltrim(self:cAmb) == Alltrim(GetEnvServer()))) //Dados de conexão salvas na MIH estão diferentes do servidor conectado    

    IF lRet
        oRpcSrv := TRpc():New( self:oConfigWiz['cAmb'] ) // cria o objeto que realizará a conexão com servidor externo

        bErrorBlock := ErrorBlock( {|oErro| RmiErroBlock(oErro, @lErrorBlock, @cErrorBlock)} )
        Begin Sequence
            If oRpcSrv:Connect(self:oConfigWiz['cIP'],Val(self:oConfigWiz['cPorta'])) //realiza a conexão com o servidor externo
                cIni        := oRpcSrv:CallProc("GetAdv97")
                cOnStart    := oRpcSrv:CallProc("GetPvProfString", "OnStart", "JOBS", "", cIni)
                cRate       := oRpcSrv:CallProc("GetPvProfString", "OnStart", "RefreshRate", "", cIni)
                aSessions   := oRpcSrv:CallProc("GetIniSessions",cIni)

                For nI := 1 To Len(self:aJobs)
                    IncProc(STR0060 + cValToChar(nI) + " de " + cValToChar(Len(self:aJobs)) + "...") // 

                    If self:aJobs[nI] == "RMIINTEPDV"
                        cNomeJob := Self:GetNomeJob(self:aJobs[nI], aSessions)
                    Else
                        cNomeJob := self:aJobs[nI]
                    EndIf

                    If AScan(aSessions,{|x| UPPER(x) == self:aJobs[nI]}) == 0 //Se não existir o Job cadastrado no appserver
                        
                        oRpcSrv:CallProc("WritePProString",cNomeJob, "Main", self:aJobs[nI], cIni)
                        oRpcSrv:CallProc("WritePProString",cNomeJob, "Environment", self:oConfigWiz["cAmb"], cIni)
                        
                       If self:aJobs[nI] == "RMIPUBLICA"                    
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "nParms", "3", cIni)                    
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm1", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm2", self:oConfigWiz["cFil"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm3", "1", cIni)
                        ElseIf self:aJobs[nI] == "RMIINTEPDV"
                           // self:aJobs[nI] := self:aJobs[nI] + "_" + StrTran( Self:cGruposTab,";", "_" )
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "nParms", "5", cIni)                    
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "Parm1", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "Parm2", self:oConfigWiz["cFil"], cIni)
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "Parm3", Self:oJAssConfig['CodPontoInt'], cIni)
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "Parm4", Self:cGruposTab, cIni) // Ativa o log da integração - 0 desativa ou 1 ativa
                            oRpcSrv:CallProc("WritePProString",cNomeJob, "Parm5", "0", cIni)
                        Else
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "nParms", "2", cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm1", self:oConfigWiz["cEmpresa"], cIni)
                            oRpcSrv:CallProc("WritePProString",self:aJobs[nI], "Parm2", self:oConfigWiz["cFil"], cIni)
                        EndIf

                        If !(cNomeJob $ cOnStart)
                            cJobs += "," + cNomeJob
                        EndIf                    
                    else
                        If !(cNomeJob $ cOnStart)
                            cJobs += "," + cNomeJob
                        EndIf                
                    EndIf
                    self:oConfigWiz["cServico"] := self:aJobs[nI]
                    self:GravaLayoutMIH()
                Next nI
                
                If Empty(cOnStart)
                    oRpcSrv:CallProc("WritePProString","OnStart","JOBS",Substr(cJobs,2,Len(cJobs)),cIni)   
                else
                    oRpcSrv:CallProc("WritePProString","OnStart","JOBS",cOnStart+cJobs,cIni)      
                EndIf 
                
                
                If Empty(cRate)
                    oRpcSrv:CallProc("WritePProString","OnStart","RefreshRate","10",cIni)
                EndIf
                oRpcSrv:Disconnect()
                FwFreeObj(oRpcSrv)
                self:oConfigWiz["mensagem"] := ""
            //Else
                //self:oConfigWiz["mensagem"] := STR0061 //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
                //lRet := .F.
            EndIf
        //Se ocorreu erro
        //Recover
           // self:oConfigWiz["mensagem"] := STR0061 + CRLF + cErrorBlock   //"Não foi possível estabelecer conexão com o ambiente remoto. Verifique as configurações e tente novamente."
           // lRet := .F.    
        End Sequence
        ErrorBlock(bErrorBlock)              
    EndIf
    
EndIf

Return lRet

/*/{Protheus.doc} GetNomeJob
Retorna o nome do JOB pois pode existir mais de um mesmo JOB configurado
@author  joao.marcos
@since   30/04/2024
@version 1.0
@param nNomeServ, Character, Nome do Serviço
        aSessions, Array, Sessoes do arquivo appserver.ini
/*/
Method GetNomeJob(nNomeServ as Character, aSessions as Array) as Character Class IntegTOTVSPdv
Local cNomeJob := ""    as Character
Local nCont := 0        as Numeric
Local nJ    := 0        as Numeric
Local lExiste := .F.    as Logical 

For nCont := 1 To 100
    For nJ := 1 To Len(aSessions)
        If nNomeServ + "_" + AllTrim(Str(nCont)) == aSessions[nJ] // Verifica se nome jah existe
            lExiste := .T.
            Exit
        EndIf
    Next

    If !lExiste
        cNomeJob := nNomeServ + "_" + AllTrim(Str(nCont))
        Exit
    Else
        lExiste := .F.
    EndIf
Next

Return cNomeJOb

/*/{Protheus.doc} carga
@author  joao.marcos
@since   02/05/2024
@version 1.0
/*/
Method carga() as Variant Class IntegTOTVSPdv
Return Nil
