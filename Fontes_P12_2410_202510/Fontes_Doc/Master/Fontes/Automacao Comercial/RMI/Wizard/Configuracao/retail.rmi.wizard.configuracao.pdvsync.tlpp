#INCLUDE "TOTVS.CH"
#INCLUDE "SHPWIZ.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE CRLF CHR(13)+CHR(10)

Static lStFilCmp := existFunc("rmixFilCmp")     //Verifica se existe a função que vai retornar o nome do campo que tem as filiais

namespace totvs.protheus.retail.rmi.wizard.configuracao.PdvSync
using namespace totvs.protheus.retail.rmi.wizard.configuracao.WizardObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe PdvSync
Classe responsável pela construção das telas de configuração do Wizard
    
/*/
//-------------------------------------------------------------------
Class PdvSync from WizardObj

    Protected Data lNovo    as  Logical

    Protected Data aLojas   as  Array

    Public Method new() as Object
    Public Method constroiTelas() as Variant        //Método que define a construção das telas do Wizard.

    Public Method telaCredenciais(oPanel as Object)     As Variant  //Construção da tela de Credenciais
    Public Method validaCredenciais()   				As Logical  //Bloco de validação da tela de Credenciais
    Public Method telaCadAuxLoja(oPanel as Object)     	As Variant  //Construção da tela para cadastro de loja auxiliar.
    Public Method validaCadLoja()   					As Logical	//Bloco de validação da tela de Credenciais

    Public Method voltar()							as Logical	//Validação do botão 'Voltar' do Wizard
    Public Method cancelar()         				as Logical	//Validação do botão 'Cancelar' do Wizard
    Public Method SalvaConfig()		      			as Variant	//Metodo que ira atualizar a a configuração do assinante, via Wizard
    
    Public Method telaInfoPagto(oPanel as Object)	as Variant  //Construção da tela para cadastro de loja auxiliar.
    Public Method CadPagtoPad() 					as Variant 

    Protected Method geraCompartilhamento()         as Logical  //Metodo responsavel por gerar os compartilhamentos
    Protected Method integraCompartilhamentoLoja()  as Logical  //Metodo responsavel por efetuar a integração do compartilhamento e loja
    Protected Method geraLoja()         as Logical  //Metodo responsavel por gerar os compartilhamentos

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method new(cTitulo as Character) as Object Class PdvSync

    Local aTabSecund := {} As Array
    Local aFuncoes   := {} As Array

    self:lNovo  := .T.
    self:aLojas := {}

    _Super:New("PDVSYNC", cTitulo)

    //                  MHS_TABELA  MHS_CHAVE                MHS_FILTRO                     MHS_CONPUB  MHS_TIPO        DELETA REGISTRO
    Aadd(aTabSecund, {  "ACV"       , "ACV_FILIAL+ACV_CODPRO", ""                           , "1"       , ""            , ""})
    Aadd(aTabSecund, {  "MEU"       , "MEU_FILIAL+MEU_CODIGO", ""                           , "1"       , ""            , ""})
    Aadd(aTabSecund, {  "MEV"       , "MEV_FILIAL+MEV_CODKIT", ""                           , "1"       , ""            , ""})
    Aadd(aTabSecund, {  "MIL"       , "MIL_FILIAL+MIL_ENTRAD", "MIL_TIPREL = 'FECP'"        , "1"       , "FECP"        , ""})
    Aadd(aTabSecund, {  "MIL"       , "MIL_FILIAL+MIL_ENTRAD", "MIL_TIPREL = 'ICMS'"        , "1"       , "ICMS"        , ""})
    Aadd(aTabSecund, {  "MIL"       , "MIL_FILIAL+MIL_ENTRAD", "MIL_TIPREL = 'PIS/COFINS'"  , "1"       , "PIS/COFINS"  , ""})
    Aadd(aTabSecund, {  "SB5"       , "B5_FILIAL+B5_COD"     , ""                           , "1"       , ""            , ""})
    
    //                  MIM_ETAPA   MIM_FUNCAO    MIM_DESCRI    MIM_ATIVO   DELETA REGISTRO
    Aadd(aFuncoes  , {  "2"         , "RMIPUBGRAD", STR0093     , "1"       , ""})          //"Publica a variação da grade do produto"
    Aadd(aFuncoes  , {  "1"         , "RMIIMPPRO" , STR0094     , "1"       , ""})          //"Gera impostos na tabela auxiliar"
    Aadd(aFuncoes  , {  "3"         , "RMIMIXPROD", STR0095     , "2"       , ""})          //"Mix de Produto - Motor de promoções"
    Aadd(aFuncoes  , {  "3"         , "RMISITPROD", STR0096     , "2"       , ""})          //"Situação do Produto - Motor de promoções"
    Aadd(aFuncoes  , {  "1"         , "RMIMARCA"  , STR0097     , "2"       , ""})          //"Publicaçao de marca do produto - Venda Digital"

    //                      MHN_COD     MHN_TABELA   MHN_CHAVE            aTabSecund           MHN_FILTRO                                   MHN_SECOBG  aFuncoes
    Aadd(self:aProcessos, { "PRODUTO"   , "SB1"     , "B1_FILIAL+B1_COD", aClone(aTabSecund), "B1_TIPO IN ('PA','ME') AND B1_CODBAR <> ''", ""        , aClone(aFuncoes),"",.F.,"SB1","B1_DESC"})

    aTabSecund  := {}
    aFuncoes    := {}
    Aadd(aTabSecund, {"MEN" , "MEN_FILIAL+MEN_CODADM"})
    Aadd(aFuncoes  , {"3"   , "RMIPUBCNPG", STR0088, "1"})     //"Publica a Condição de Pagamento"
    Aadd(self:aProcessos, {"ADMINISTRADORA", "SAE", "AE_FILIAL+AE_COD", aClone(aTabSecund), "", "", aClone(aFuncoes), ""})

    aTabSecund  := {}
    aFuncoes    := {}
    Aadd(aTabSecund, {"SB8", "B8_FILIAL+B8_LOCAL+B8_PRODUTO", "B8_NUMLOTE = '      '", "1", "", ""})
    Aadd(self:aProcessos, {"SALDO ESTOQUE", "SB2", "B2_FILIAL+B2_LOCAL+B2_COD", aClone(aTabSecund), "", "", aClone(aFuncoes), ""})
    
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} constroiTelas

Método que define a construção das telas do Wizard.

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method constroiTelas() as Variant Class PdvSync

    Local oNewPag   := Nil as Object    //Objeto que adiciona nova pagina no wizard

	// 02 ------------------------------Configuração de Credenciais
	oNewPag := self:oSHPWiz:AddStep("2",{|oPanel|self:telaCredenciais(oPanel)})
	oNewPag:SetStepDescription(STR0002)   //"Configuração de Credenciais"                                                                                                                                                                                                                                                                                                                                                                                                                                                                                               
	oNewPag:SetNextAction({||self:validaCredenciais()})
	oNewPag:SetCancelAction({|| self:cancelar() })
    oNewPag:SetPrevAction({|| self:voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar
	
    // 03 ------------------------------Cadastro de Loja
	oNewPag := self:oSHPWiz:AddStep("3",{|oPanel|self:telaCadAuxLoja(oPanel)})
	oNewPag:SetStepDescription(STR0061) //Cadastro de Loja
	oNewPag:SetNextAction({||IIF(self:validaCadLoja(),Self:CadPagtoPad(),.F.)})
	oNewPag:SetCancelAction({|| self:cancelar() })

    
    // 04 ------------------------------Informações Forma de Pagamento
    oNewPag := self:oSHPWiz:AddStep("4",{|oPanel|self:telaInfoPagto(oPanel)})
    oNewPag:SetStepDescription(STR0062) //Informações Forma de Pagamento
    oNewPag:SetNextAction({||.T.})
    oNewPag:SetCancelAction({|| self:Cancelar() }) 
    oNewPag:SetPrevAction({|| self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar    

    // 05 ------------------------------"Configuração de Serviços"
    oNewPag := self:oSHPWiz:AddStep("5",{|oPanel|self:TelaServicos(oPanel)})
    oNewPag:SetStepDescription(STR0004) //"Configuração de Serviços"
    oNewPag:SetNextAction({||self:Validaservicos()})
    oNewPag:SetCancelAction({|| self:Cancelar() }) 
    oNewPag:SetPrevAction({|| self:Voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar    

    // 06 ------------------------------Tela de Confirmação
	oNewPag := self:oSHPWiz:AddStep("6",{|oPanel|self:Telaconfirmacao(oPanel)})
	oNewPag:SetStepDescription(STR0005) //"Confirmação"
	oNewPag:SetNextAction({||.T.})
	oNewPag:SetCancelAction({|| self:cancelar() })
    oNewPag:SetPrevAction({|| self:voltar() })  //Define o bloco de código que deverá executar ao pressionar o botão Voltar

	//Ativa Wizard
	self:oSHPWiz:Activate()
	
	//Desativa Wizard
	self:oSHPWiz:Destroy()

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} voltar
Implentações ao clicar no botão voltar

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method voltar() as Logical Class PdvSync
    self:oConfigWiz["mensagem"] := ""
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} telaCredenciais
    Construtor de tela de Credenciais do Wizard
@author  Everson S P Junior
@version 1.0    
/*/
//-------------------------------------------------------------------
Method telaCredenciais(oPanel As Object) Class PdvSync
    Local oSay1     as Object
    Local oSay2     as Object
    Local oSay3     as Object
    Local oSay4     as Object
    Local oSay5     as Object
    Local oSay6     as Object
    Local oSay9     as Object
    Local oSay10    as Object
    Local oSay11    as Object
    Local oGet1     as Object
    Local oGet2     as Object
    Local oGet3     as Object
    Local oGet4     as Object
    Local oSayMsg   as Object
    Local oJsonCfg  as Object
    Local cGitPast:= SuperGetMv("MV_PSHGIT", .F., "")   //Adicione o nome da pasta de QA do GitHub exemplo do pdvsync: pdvsync-qa
    
    self:oConfigWiz['cClientId']    := IIF(Empty(cGitPast),"pdvsync_ro_protheus","protheus_pdv_sync")
    self:oConfigWiz['cEnvironment'] := IIF(Empty(cGitPast),"2","3")

    MHO->( DbSetOrder(1) )  //MHO_FILIAL + MHO_COD
    If MHO->( DbSeek( xFilial("MHO") + PadR("PDVSYNC", TamSx3("MHO_COD")[1]) ) ) .AND. !Empty(AllTrim(MHO->MHO_CONFIG))
        oJsonCfg := JsonObject():New()
        oJsonCfg:FromJson( AllTrim(MHO->MHO_CONFIG) )
        self:lNovo := .F.
    EndIf	
    
    If !self:oConfigWiz:HasProperty("cClientSecret")
        self:oConfigWiz['cClientSecret'] := IIF(!self:lNovo .AND. !Empty(Alltrim(oJsonCfg["autenticacao"]["clientSecret"])),oJsonCfg["autenticacao"]["clientSecret"],Alltrim(IIF(Empty(cGitPast),STR0091,STR0092))) //"STR0091" <- produção STR0092 <- QA-dev */
    EndIf
    If !self:oConfigWiz:HasProperty("cUser")
        self:oConfigWiz['cUser'] := IIF(!self:lNovo .AND. !Empty(Alltrim(oJsonCfg["autenticacao"]["user"])),oJsonCfg["autenticacao"]["user"]+Space(30),Space(50))
    EndIf

    If !self:oConfigWiz:HasProperty("cPassword")
        self:oConfigWiz['cPassword'] := IIF(!self:lNovo .AND. !Empty(Alltrim(oJsonCfg["autenticacao"]["password"])) ,oJsonCfg["autenticacao"]["password"]+Space(30),Space(50))
    EndIf
	
	If !self:oConfigWiz:HasProperty("cTenant")
        self:oConfigWiz['cTenant'] := IIF(!self:lNovo .AND. !Empty(Alltrim(oJsonCfg["autenticacao"]["tenent"])),oJsonCfg["autenticacao"]["tenent"]+Space(30),Space(50))
    EndIf

    If !self:oConfigWiz:HasProperty("cInquilino")
        self:oConfigWiz['cInquilino'] := IIF(!self:lNovo .AND. !Empty(Alltrim(oJsonCfg["inquilino"])) ,Alltrim(oJsonCfg["inquilino"])+Space(30),Space(50))
    EndIf
    
	oSay1:= TSay():New(10,20,{||STR0002},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,220,10)//'Configuração de Credenciais'
    oSay2:= TSay():New(self:oCoordTela['nCord04'],20,{||I18n(STR0008,{self:oConfigWiz['cComboProd']})},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,20)//"Informe suas credenciais do #1 "

    oSay3:= TSay():New(self:oCoordTela['nCord05'],20,{||STR0009},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,10)//"Usuário: "
    oGet1:= TGet():New(self:oCoordTela['nCord06'],60,{|u| if( PCount() > 0, self:oConfigWiz['cUser'] := u, self:oConfigWiz['cUser']) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cUser'],,,, )

    oSay4:= TSay():New(self:oCoordTela['nCord07'],20,{||STR0010},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,10)//"Senha: "
    oGet2:= TGet():New(self:oCoordTela['nCord08'],60,{|u| if( PCount() > 0, self:oConfigWiz['cPassword'] := u, self:oConfigWiz['cPassword'] ) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.T.,,self:oConfigWiz['cPassword'],,,,)

    oSay5:= TSay():New(self:oCoordTela['nCord09'],20,{||"Tenant:"},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,10)//"Usuário: "
    oGet3:= TGet():New(self:oCoordTela['nCord10'],60,{|u| if( PCount() > 0, self:oConfigWiz['cTenant'] := u, self:oConfigWiz['cTenant']) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cTenant'],,,, )

    oSay6:= TSay():New(self:oCoordTela['nCord13'],20,{||"Inquilino:"},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,10)//"Usuário: "
    oGet4:= TGet():New(self:oCoordTela['nCord14'],60,{|u| if( PCount() > 0, self:oConfigWiz['cInquilino'] := u, self:oConfigWiz['cInquilino']) } ,oPanel,110,010, ,,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,self:oConfigWiz['cInquilino'],,,, )
    
    self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(self:oCoordTela['nCord01'],20,{||IIF(Empty(self:oConfigWiz["mensagem"]) ,"",MSGINFO(self:oConfigWiz["mensagem"],)),self:oConfigWiz["mensagem"]:=""},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,250,20)

    oSay9 := TSay():New(self:oCoordTela['nCord15'],20,{||STR0057},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Para informações de como obter as credenciais de configuração, acesse: "
    oSay10:= TSay():New(self:oCoordTela['nCord16'],20,{||STR0047},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,,,200,20)//'https://tdn.totvs.com/pages/releaseview.action?pageId=516633428'
    oSay10:bLClicked := {|| ShellExecute("open",STR0047,"","",1) }//"https://tdn.totvs.com/pages/viewpage.action?pageId=725743828"
    oSay11:= TSay():New(self:oCoordTela['nCord17'],20,{||STR0089},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) //"Caso contrário clique no botão avançar" 
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} validaCredenciais
    Função de validação da tela de Credenciais do Wizard
@author  Everson S P Junior
@version 1.0    
/*/
//-------------------------------------------------------------------
Method validaCredenciais() Class PdvSync
    Local lRet      := .T.
    Local cPassword := Alltrim(self:oConfigWiz['cPassword'])
    
    //caso sucesso, informo usuario, senha e token validados na configuração de assinante
    MHO->( DbSetOrder(1) )  //MHO_FILIAL + MHO_COD
    If !MHO->( DbSeek( xFilial("MHO") + PadR("PDVSYNC", TamSx3("MHO_COD")[1]) ) )
        Processa( {|| lRet   := self:AssinantesXProcessos()},STR0029, STR0028) // "Aguarde. . ."  
    EndIf	
    
    If lRet
        self:oConfigWiz['oTstAssi'] := RmiEnvPdvSyncObj():New("WIZARD")
        self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["tenent"]      := Alltrim(self:oConfigWiz['cTenant'])
        self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["user"]        := Alltrim(self:oConfigWiz['cUser'])
        self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["clientId"]    := Alltrim(self:oConfigWiz['cClientId'])
        self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["environment"] := Val(Alltrim(self:oConfigWiz['cEnvironment']))
        self:oConfigWiz['oTstAssi']:oConfAssin["inquilino"]                   := Alltrim(self:oConfigWiz['cInquilino'])
        
        If !self:oConfigWiz['oTstAssi']:oConfAssin:HasProperty("configPSH")
            self:oConfigWiz['oTstAssi']:oConfAssin["configPSH"]                         := JsonObject():New()
        EndIf
        If !self:oConfigWiz['oTstAssi']:oConfAssin["configPSH"]:HasProperty("criptografado")
        self:oConfigWiz['oTstAssi']:oConfAssin["configPSH"]["criptografado"]        := "S"//Retirar a tag criptografado do assinante para recriar.
            self:oConfigWiz['cPassword']  := rc4crypt(Alltrim(self:oConfigWiz['cPassword']),SM0->M0_CGC,.F.)
            self:oConfigWiz['cClientSecret']  := rc4crypt(Alltrim(self:oConfigWiz['cClientSecret']),SM0->M0_CGC,.F.)
        EndIf    
            
        If !(self:lNovo) .AND. (cPassword <> self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["password"])//cClientSecret está criptografado e mudou a senha (Não criptografado)
            self:oConfigWiz['cPassword']  := rc4crypt(Alltrim(self:oConfigWiz['cPassword']),SM0->M0_CGC,.F.)
        EndIf
        
        self:oConfigWiz['oTstAssi']:oPdvSync := totvs.protheus.retail.rmi.classes.pdvsync.PdvSync():New(Alltrim(self:oConfigWiz['cTenant']), Alltrim(self:oConfigWiz['cUser']),;
        Alltrim(self:oConfigWiz['cPassword']), Alltrim(self:oConfigWiz['cClientId']), Alltrim(self:oConfigWiz['cClientSecret']), Val(Alltrim(self:oConfigWiz['cEnvironment'])))       
        
        If (aMensagem := self:oConfigWiz['oTstAssi']:oPdvSync:Token())[1]
  
            self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["password"]      := Alltrim(self:oConfigWiz['cPassword'])
            self:oConfigWiz['oTstAssi']:oConfAssin["autenticacao"]["clientSecret"]  := Alltrim(self:oConfigWiz['cClientSecret'])
            Self:SalvaConfig()
            self:oConfigWiz['oTstAssi']:cAssinante := self:cAssinante
            self:oConfigWiz["mensagem"] := ""
        else
            lRet := aMensagem[1]
            self:oConfigWiz["mensagem"] := aMensagem[2]
        EndIf
    EndIf        
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelar
Implentações ao clicar no botão cancelar

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method cancelar() as Logical Class PdvSync
    self:lNovo := .F.
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} SalvaConfig
Metodo que ira atualizar a configuração do assinante.

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method SalvaConfig() Class PdvSync

    MHO->( DbSetOrder(1) )  //MHO_FILIAL + MHO_COD
    If MHO->( DbSeek( xFilial("MHO") + PadR(Self:cAssinante, TamSx3("MHO_COD")[1]) ) )
        RecLock("MHO", .F.)
            MHO->MHO_CONFIG := self:oConfigWiz['oTstAssi']:oConfAssin:ToJson()
        MHO->( MsUnLock() )
    EndIf
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} telaCadAuxLoja
    Construtor de tela de telaCadAuxLoja do Wizard
@author  Everson S P Junior
@version 1.0    
/*/
//-------------------------------------------------------------------
Method telaCadAuxLoja(oPanel As Object) Class PdvSync
    Local oSay1     as Object
    
    oSay1:= TSay():New(10,20,{||STR0061},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,220,20)//"Cadastro de Loja"
    oSay1:= TSay():New(40,20,{||STR0053},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//"Nesta etapa serão cadastradas as filiais, que possuem lojas integradas com o TOTVS Varejo PDV OMNI."
    oSay1:= TSay():New(50,20,{||STR0054},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,20)//"É preciso ter ao menos uma loja cadastrada, clique abaixo em  "
    oSay1:= TSay():New(50,193,{||STR0055},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,220,20)//"Avancar"
    

    self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(self:oCoordTela['nCord01'],20,{||IIF(Empty(self:oConfigWiz["mensagem"]) ,"",MSGINFO(self:oConfigWiz["mensagem"],)),self:oConfigWiz["mensagem"]:=""},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,250,20)

    oSay9 := TSay():New(self:oCoordTela['nCord15'],20,{||STR0058},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20)//"Saiba como cadastrar uma Loja, acesse:"
    oSay10:= TSay():New(self:oCoordTela['nCord16'],20,{||STR0060},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,,,200,20)//'https://tdn.totvs.com/pages/releaseview.action?pageId=516633428'
    oSay10:bLClicked := {|| ShellExecute("open",STR0060,"","",1) }//"https://tdn.totvs.com/pages/viewpage.action?pageId=725743828"
    oSay11:= TSay():New(self:oCoordTela['nCord17'],20,{||STR0089},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) //"Caso contrário clique no botão avançar 
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} validaCadLoja
    Função de validação da tela de Credenciais do Wizard
@author  Everson S P Junior
@version 1.0    
/*/
//-------------------------------------------------------------------
Method validaCadLoja() Class PdvSync
Local lAtivaEnvio       := .F.
Local cCodMaster        := "" as Character
Local cIdFilialProtheus := "" as Character

MIH->(DbSetOrder(1))
//Pega o modelo fiscal que será usado para criar forma de pagamento.
self:aLojas := {}

If MIH->(dbSeek(xFilial("MIH") + PadR("CADASTRO DE LOJA",TAMSX3("MIH_TIPCAD")[1])  ))
    While MIH->(!Eof()) .AND. Alltrim(MIH->MIH_TIPCAD) == Alltrim("CADASTRO DE LOJA")
        cCodMaster        := LjCAuxRet("CodigoMaster")
        cIdFilialProtheus := LjCAuxRet("IDFilialProtheus")        
        aAdd(self:aLojas, {MIH->MIH_ID, cCodMaster+cIdFilialProtheus})
        MIH->( DbSkip() )
    EndDo
EndIf

//Gera CADASTRO DE LOJAS
Processa( {||self:geraCompartilhamento(),lAtivaEnvio := self:geraLoja()},STR0082, STR0028 ) //"Gerando cadastro das lojas..."   "Aguarde. . ."    

//Efetuar a integração do compartilhamento e loja
If lAtivaEnvio
    Processa( {|| self:integraCompartilhamentoLoja()}, STR0078, STR0028 )   //"Integrando Compartilhamentos\Lojas"  "Aguarde. . ."
EndIf

return self:lReturn //Se der erro self:lReturn determina o retorno.

//-------------------------------------------------------------------
/*/{Protheus.doc} geraCompartilhamento
Metodo responsavel por gerar os compartilhamentos

@author  Rafael Tenorio da Costa
@since   27/09/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method geraCompartilhamento() as Logical Class PdvSync

    Local aArea             := GetArea()    as Array
    Local oModel            := Nil          as Object
    Local nCont             := 1            as Numeric
    Local aNivel            := {"0", "1"}   as Array
    Local cErro             := ""           as Character

    Private INCLUI := .T.       //Necessária dentro do modelo LjCadAux

    LjLayAuxCg()
    
    PshSetTCad("COMPARTILHAMENTOS")

    oModel := FwLoadModel("LjCadAux")

    For nCont := 1 To Len(aNivel)

        If self:lReturn .And. LjAuxValid("COMPARTILHAMENTOS", "nivel", aNivel[nCont])

            oModel:SetOperation(MODEL_OPERATION_INSERT)
            oModel:Activate()
            
            oModel:SetValue( 'MIHMASTER', "MIH_DESC"        , "COMP. NIVEL " + aNivel[nCont]                                              )
            oModel:SetValue( 'MIHDETAIL', "NomeCompartilha" , "COMP. NIVEL " + aNivel[nCont]                                              )
            oModel:SetValue( 'MIHDETAIL', "nivel"           , aNivel[nCont]                                                               )
            
            self:lReturn := oModel:vldData() .And. oModel:commitData()
            cErro        := oModel:GetErrorMessage()[6]

            oModel:DeActivate()
        EndIf
    Next nCont

    MIH->( dbSetOrder(1) )
    MIH->( dbSeek( xFilial("MIH") + PadR("CADASTRO DE LOJA", TamSx3("MIH_TIPCAD")[1]) ) )

    
    oModel:Destroy()

    If !self:lReturn
        self:oConfigWiz["mensagem"] := I18n(STR0074,{"compartilhamento"}) + AllTrim(cErro)         //"Não foi possÍvel gerar o cadastro de Compartilhamento, verifique: "
        LjGrvLog("geraCompartilhamento", self:oConfigWiz["mensagem"])
    EndIf

    RestArea(aArea)

Return self:lReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} telaInfoPagto
    Construtor de tela de telaInfoPagto do Wizard
@author  Everson S P Junior
@version 1.0    
/*/
//-------------------------------------------------------------------
Method telaInfoPagto(oPanel As Object) Class PdvSync
    Local oSay1     as Object
    
    oSay1:= TSay():New(10,20,{||STR0062},oPanel,,self:oConfigWiz['oTBold'],,,,.T.,,,220,20)//"Informações Formas de Pagamento"
    oSay1:= TSay():New(40,20,{||STR0063},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//Foram cadastradas automaticamente as Formas de Pagamento: Dinheiro, Cartão de Crédito(TEF) e Cartão de Débito(TEF).
    oSay1:= TSay():New(50,20,{||STR0064},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//Caso deseja cadastrar outras Formas de Pagamento, poderá ser feito em outro momento, acessando os Cadastros Auxiliares.
    oSay1:= TSay():New(60,20,{||STR0065},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//No link do rodapé você encontrará a documentação de como fazer o cadastro de Forma de Pagamento.
    oSay1:= TSay():New(70,20,{||STR0066+" "},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//Agora clique em Avançar para continuar na trilha de configuração da integração Protheus Smart Hub.

    self:oConfigWiz["mensagem"] := ""
    oSayMsg := TSay():New(self:oCoordTela['nCord01'],20,{||IIF(Empty(self:oConfigWiz["mensagem"]) ,"",MSGINFO(self:oConfigWiz["mensagem"],)),self:oConfigWiz["mensagem"]:=""},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,CLR_HRED,,520,20)

    oSay9 := TSay():New(self:oCoordTela['nCord15'],20,{||STR0067},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,520,20)//"Abaixo encontrará detalhes de como cadastrar outras formas de pagamento através do link:"
    oSay10:= TSay():New(self:oCoordTela['nCord16'],20,{||STR0068},oPanel,,self:oConfigWiz['oTSubl'],,,,.T.,,,200,20)//https://tdn.totvs.com/pages/releaseview.action?pageId=770961862
    oSay10:bLClicked := {|| ShellExecute("open",STR0068,"","",1) }//https://tdn.totvs.com/pages/releaseview.action?pageId=770961862
    oSay11:= TSay():New(self:oCoordTela['nCord17'],20,{||STR0089},oPanel,,self:oConfigWiz['oTFont'],,,,.T.,,,200,20) //"Caso contrário clique no botão avançar 
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} CadPagtoPad
Faz cadastro padrão de de forma de pagamento
@type    function
@author  Everson S P Junior
@since   14/09/23
@version 12.1.33
/*/
//-------------------------------------------------------------------
Method CadPagtoPad() Class PdvSync

	Local oModel    := Nil
	Local aForPag   := {{"R$","DINHEIRO","1 "},{"CC","CARTAO CREDITO","2 "},{"CD","CARTAO DEBITO","3 "}}
	Local nX        := 0
	Local lRet      := .T.
	
	MIH->(DbSetOrder(1))    
	Begin Transaction
	    If !MIH->(dbSeek(xFilial("MIH") + PadR("FORMA DE PAGAMENTO",TAMSX3("MIH_TIPCAD")[1])  ))
	        //"PERFIL DE OPERADOR" 
	        PSHSetTCad("FORMA DE PAGAMENTO" )
	        oModel    := FWLoadModel( 'LjCadAux' )
	        For nX := 1 To Len(aForPag)
	            oModel:SetOperation( MODEL_OPERATION_INSERT )
	            oModel:Activate()
	            oModel:SetValue( 'MIHMASTER', "MIH_DESC", aForPag[nX][2])
	            oModel:SetValue( 'MIHDETAIL', "FORMA", aForPag[nX][1])
	            oModel:SetValue( 'MIHDETAIL', "DESCRICAO", aForPag[nX][2])
	            oModel:SetValue( 'MIHDETAIL', "DESCFISCAL", aForPag[nX][2])
	            oModel:SetValue( 'MIHDETAIL', "VLRMIN", 0.01)
	            oModel:SetValue( 'MIHDETAIL', "TIPO", aForPag[nX][3])
	            oModel:SetValue( 'MIHDETAIL', "MODFISCAL", iif(empty(self:oConfigWiz["modelofiscal"]),"NaoConfigurado",self:oConfigWiz["modelofiscal"]))
	            oModel:SetValue( 'MIHDETAIL', "CONSULCRED", '0')
	            IIF(lRet := oModel:VldData(),oModel:CommitData(),MSGINFO(STR0069+aForPag[nX][1],"Verifique!"))    //"Não foi possivel carregar o Cadastro Padrao de PERFIL DE OPERADOR -> ", "Verifique!"
	            LjGrvLog("LjCadAux", "CadPadrao -> ", IIF(!lRet,oModel:GetErrorMessage(),STR0070+aForPag[nX][1]), /*lCallStack*/) //"Cadastro FORMA DE PAGAMENTO Padrao com Sucesso! "
	            oModel:DeActivate()
	        next
	    EndIf
	
	End Transaction

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} integraCompartilhamentoLoja
Metodo responsavel por efetuar a integração do compartilhamento e loja

@author  Rafael Tenorio da Costa
@since   04/10/2023
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method integraCompartilhamentoLoja() as Logical Class PdvSync

    Local aArea      := GetArea()   as Array
    Local aProcessos := {}          as Array
    Local nCont      := 0           as Numeric
    Local cSql       := ""          as Character
    Local aSql       := {}          as Array
    Local lContinua  := .F.         as Logical
    Local nContErro  := 0           as Numeric
    Local cHoraGer   := time()      as Character
    Local lLojaAtiva := .T.         as Logical
    Local cPdvSync   := padR("PDVSYNC", tamSx3("MHP_CASSIN")[1])    as Character
    Local cTamProc   := tamSx3("MHP_CPROCE")[1]                     as Numeric
    Local cCmpFil    := ""          as Character    //Campo com as filiais utilizadas para processamento

    LjGrvLog("WizardPdvSync", "Inicio do envio de Compartilhamento\Loja")

    //Retorna os processos que serão publicados
    aProcessos := RmiProPub("COMPARTILHAMENT,CADASTRO LOJA")

    //Ativa processo
    MHP->( DbSetOrder(1) )   //MHP_FILIAL, MHP_CASSIN, MHP_CPROCE, MHP_TIPO, R_E_C_N_O_, D_E_L_E_T_
    If MHP->( DbSeek(xFilial("MHP") + cPdvSync ) )
        While !MHP->( Eof() ) .And. MHP->MHP_FILIAL == xFilial("MHP") .And. AllTrim(MHP->MHP_CASSIN) == "PDVSYNC"
            RecLock("MHP", .F.)

                //Compartilhamento e loja sempre ativo e coloca uma filial caso não exista nenhuma loja ativa
                if allTrim(MHP->MHP_CPROCE) $ "CADASTRO LOJA|COMPARTILHAMENT"
                    
                    cCmpFil := iif(lStFilCmp, rmixFilCmp(MHP->MHP_CASSIN, MHP->MHP_CPROCE), "MHP_FILPRO")

                    MHP->MHP_ATIVO := "1"
                    if empty(MHP->&(cCmpFil))
                        MHP->&(cCmpFil) := cFilAnt + ";"
                        lLojaAtiva      := .F.
                    endIf
                endIf

            MHP->( MsUnlock() )

            MHP->( DbSkip() )
        EndDo
    EndIf

    //Publicação
    For nCont := 1 To Len(aProcessos)
        StartJob("SHPPubSel", GetEnvServer(), .T./*lEspera*/, cEmpAnt, cFilAnt, aProcessos[nCont])
    Next nCont

    //Distribuição
    For nCont := 1 To Len(aProcessos)
        StartJob("RmiDistSel", GetEnvServer(), .T./*lEspera*/, cEmpAnt, cFilAnt, aProcessos[nCont][1])
    Next nCont

    //Envio
    For nCont := 1 To Len(aProcessos)
        StartJob("RmiEnvExec", GetEnvServer(), .T./*lEspera*/, cEmpAnt, cFilAnt, "PDVSYNC", aProcessos[nCont][1])
    Next nCont

    //Caso não tenha loja ativa limpa MHP_FILPRO, porque agora ja enviou compartilhamento e loja
    if !lLojaAtiva
        MHP->( dbSetOrder(1) )  //MHP_FILIAL, MHP_CASSIN, MHP_CPROCE, MHP_TIPO, R_E_C_N_O_, D_E_L_E_T_
        if MHP->( dbSeek(xFilial("MHP") + cPdvSync + padR("COMPARTILHAMENT", cTamProc) + "1") )
            cCmpFil := iif(lStFilCmp, rmixFilCmp(MHP->MHP_CASSIN, MHP->MHP_CPROCE), "MHP_FILPRO")

            recLock("MHP", .F.)
                MHP->&(cCmpFil) := ""
            MHP->( msUnlock() )
        endIf

        MHP->( dbSetOrder(1) )  //MHP_FILIAL, MHP_CASSIN, MHP_CPROCE, MHP_TIPO, R_E_C_N_O_, D_E_L_E_T_
        if MHP->( dbSeek(xFilial("MHP") + cPdvSync +padR("CADASTRO LOJA", cTamProc) + "1") )
            cCmpFil := iif(lStFilCmp, rmixFilCmp(MHP->MHP_CASSIN, MHP->MHP_CPROCE), "MHP_FILPRO")

            recLock("MHP", .F.)
                MHP->&(cCmpFil) := ""
            MHP->( msUnlock() )
        endIf
    endIf

    //Valida
    self:lReturn                := .F.
    self:oConfigWiz["mensagem"] := STR0079 + CRLF   //"Não foi possível efetuar o envio do Compartilhamento\Loja, entre em contato com o suporte."

    cSql := " SELECT MHQ_STATUS, MHR_STATUS, CAST( MHL_ERROR AS VARCHAR(4000) ) AS MHL_ERROR"
    cSql += " FROM " + RetSqlName("MHQ") + " MHQ LEFT JOIN " + RetSqlName("MHR") + " MHR"
    cSql +=     " ON MHQ_UUID = MHR_UIDMHQ AND MHR.D_E_L_E_T_ = ' '"
    cSql += " LEFT JOIN " + RetSqlName("MHL") + " MHL"
    cSql +=     " ON MHQ_UUID = MHL_UIDORI AND MHL.D_E_L_E_T_ = ' '"
    cSql += " WHERE MHQ.D_E_L_E_T_ = ' '"
    cSql +=     " AND MHQ_CPROCE IN ('" + aProcessos[1][1] + "', '" + aProcessos[2][1] + "')"
    cSql +=     " AND MHQ_DATGER = '" + DtoS( date() ) + "'"
    cSql +=     " AND MHQ_HORGER >= '" + cHoraGer + "'"

    aSql := RmiXSql(cSql, "*", /*lCommit*/, /*aReplace*/)

    If Len(aSql) > 0

        lContinua := .T.

        For nCont:=1 To Len(aSql)

            If !( aSql[nCont][2] $ "2|6" )
                
                nContErro++

            EndIf
        Next nCont
    EndIf

    If nContErro > 0 
        If nContErro == Len(aSql) //todos os cadastros retornaram erro
            lContinua := .F.
            self:oConfigWiz["mensagem"] += AllTrim( aSql[nContErro][3] )
        Else
            MSGINFO(STR0086+CRLF+STR0087,STR0085) //"Algumas lojas foram cadastradas, porém não obtiveram sucesso no envio para o PDVSync. " / "Por favor, consulte nosso monitoramento do Status da integração (Menu SIGALOJA > Atualizações > Smart Hub > Monitoramento > Status Integração), ou consulte o LogLoja para mais detalhes." / "Atenção!"
        EndIf
    EndIf

    If lContinua
        self:lReturn                := .T.
        self:oConfigWiz["mensagem"] := ""
    EndIf    
    LjGrvLog("WizardPdvSync", "Fim do envio de Compartilhamento\Loja", self:oConfigWiz["mensagem"])

    FwFreeArray(aProcessos)
    FwFreeArray(aSql)

    RestArea(aArea)

Return self:lReturn
//-------------------------------------------------------------------
/*/{Protheus.doc} geraLoja
Metodo responsavel por gerar os compartilhamentos

@author  Everson S P Junior
@since   09/0052024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method geraLoja() as Logical Class PdvSync

    Local aArea             := GetArea()    as Array
    Local oModel            := Nil          as Object
    Local nCont             := 1            as Numeric
    Local cErro             := ""           as Character
    Local aFilial           := FWLoadSM0()
    Local lIncluiFil        := .F.
    
    Local nContFil          := 0
    Local aFilErro          := {}
    Local cFilErro          := ""
    Local lFilOk            := .T.

    Local cCodMaster := ""                  as Character
    Local cIdFilialProtheus := ""           as Character
    
    
    Private INCLUI := .T.       //Necessária dentro do modelo LjCadAux

    PshSetTCad("CADASTRO DE LOJA")

    oModel := FwLoadModel("LjCadAux")

    MIH->( dbSetOrder(1) )
    SM0->(dbSetOrder(1))
    For nCont:=1 To Len(aFilial)
        
        If (!aScan( self:aLojas, {|x| ALLTRIM(x[2]) == Alltrim(aFilial[nCont][1]+aFilial[nCont][2]) } )  > 0) .And. aFilial[nCont][1] == cEmpAnt
            
            nContFil ++

            oModel:SetOperation(MODEL_OPERATION_INSERT)
            oModel:Activate()            
            
            SM0->(DbSeek(aFilial[nCont][1]+aFilial[nCont][2]))
            oModel:SetValue( 'MIHMASTER', "MIH_ATIVO"            , "2")
            oModel:SetValue( 'MIHDETAIL', "CodigoMaster"        , aFilial[nCont][1]      )
            oModel:SetValue( 'MIHDETAIL', "IDFilialProtheus"        , aFilial[nCont][2]      )
            oModel:SetValue( 'MIHDETAIL', "ModeloFiscal"            , "NaoConfigurado")

            lFilOk := oModel:vldData() .And. oModel:commitData()
                         
            lIncluiFil := .T.
            If lFilOk  
            
                self:oConfigWiz["modelofiscal"] := "NaoConfigurado"
                cCodMaster        := LjCAuxRet("CodigoMaster")
                cIdFilialProtheus := LjCAuxRet("IDFilialProtheus")
                aAdd(self:aLojas, {MIH->MIH_ID, cCodMaster+cIdFilialProtheus})
                
                RmiDePaGrv(Alltrim(Self:cAssinante),"SM0","M0_CODFIL",cIdFilialProtheus,cIdFilialProtheus)

            Else
                cErro   := oModel:GetErrorMessage()[6]
                aAdd(aFilErro,aFilial[nCont][1]+aFilial[nCont][2])
                LjGrvLog("Gera Loja", I18n(STR0083,{aFilial[nCont][1]+aFilial[nCont][2]}) + AllTrim(cErro)) //"Erro ao cadastrar a loja #1. Detalhes do erro : "

            EndIf
            oModel:DeActivate()
        EndIf
    Next nCont

    oModel:Destroy()

    If nContFil > 0 .And. nContFil == Len(aFilErro) //Todas as Filiais deram errado 
        self:lReturn := .F.
        self:oConfigWiz["mensagem"] := I18n(STR0074,{"lojas"}) + AllTrim(cErro)         //"Não foi possÍvel gerar o cadastro de lojas, verifique: "
    EndIf

    IF self:lReturn .And. Len(aFilErro) > 0 

        For nCont:=1 To Len(aFilErro)
            cFilErro += aFilErro[nCont] + ", "
        Next nCont

        MSGINFO(I18n(STR0084,{cFilErro}),STR0085) //"As Filiais : #1 não foram cadastradas para a integração. Verifique o LogLoja para mais detalhes." / "Atenção!"
        
    EndIF

    RestArea(aArea)

Return IIF(lIncluiFil,self:lReturn,lIncluiFil)
