#INCLUDE "TOTVS.CH"
#INCLUDE "listaorcamentoOBJ.CH"

namespace totvs.protheus.retail.rmi.api.listaorcamentoobj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe listaorcamentoObj
    Classe para tratamento da API de Orçamentos
/*/
//-------------------------------------------------------------------
Class listaorcamentoObj From LojRestObj20

	Public Method New()                 as Object
    Public Method SetFields()           as Variant
    Public Method Validation()          as Variant
    Public Method GetReturn()           as Character
    Public Method SetCliente()		    as Object
    Public Method SetaId()              as Variant	
    Public Method SetaEmissa()          as Variant	
    Private Method setSelect()          as Variant
    Public Method getCliente()          as Character
    Public Method GetTpEntrega()        as Character

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@param oWsRestObj - Objeto WSRESTFUL da API

@author  Graziella Bianchin
@since   02/08/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(oWsRestObj As Object, jParans as Json, cBody as Character) as Object Class listaorcamentoObj

    _Super:New(oWsRestObj, jParans, cBody)

Return self


//-------------------------------------------------------------------
/*/{Protheus.doc} setFields
Carrega os campos que serão retornados

@author  Graziella Bianchin
@since   02/08/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetFields() as Variant Class listaorcamentoObj

                        //Tag - ID				        Campo                       Expressão que será executada para gerar o retorno   Tag que será utilizada para preencher o objeto de retorno   Tipo do Campo   Expressão de busca
    HmAdd(self:oFields, {"CLIENTE"                  , ""                        , "self:SetCliente()"						        , "cliente"                                                 , "C"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"OFFLINE"			        , ""	                    , ".F."									            , "offline"					                                , "L"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"SITUACAOPREVENDA"		    , ""		                , "self:GetTpEntrega()"						        , "situacaoPreVenda"					                    , "C"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"IDLOJARETAGUARDA"		    , "L1_FILIAL"               , "L1_FILIAL"                                       , "idLojaRetaguarda"				                        , "C"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"IDOPERADORRETAGUARDA"	    , "L1_OPERADO"              , "L1_OPERADO"						                , "idOperadorRetaguarda"				                    , "C"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"TOTALLIQUIDO"		        , "L1_VLRLIQ"			    , "L1_VLRLIQ"							            , "totalLiquido"				                            , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"DESCONTOPERCENTUALTOTAL"	, "L1_TXDESC"	            , "L1_TXDESC"							            , "descontoPercentualTotal"					                , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"DESCONTOTOTAL"	        , "L1_DESCONT"  			, "L1_DESCONT"									    , "descontoTotal"				                            , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"ACRESCIMOPERCENTUALTOTAL" , ""  			            , "0"      									        , "acrescimoPercentualTotal"				                , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"ACRESCIMOTOTAL"	        , "L1_JUROS"	            , "L1_JUROS"									    , "acrescimoTotal"				                            , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"FRETETOTAL"	            , "L1_FRETE"  			    , "L1_FRETE"									    , "freteTotal"				                                , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"TOTALBRUTO"	            , "L1_VALBRUT"  			, "L1_VALBRUT"									    , "totalBruto"				                                , "N"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"MULTIPLOSPEDIDOS"	        , ""  			            , ".T."									            , "multiplosPedidos"				                        , "L"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"DATACADASTRO"	            , ""  			            , "self:SetaEmissa()"						        , "dataCadastro"				                            , "D"           , ""}               , 1, 3)
    HmAdd(self:oFields, {"idRetaguarda"	            , "L1_ID"                   , "L1_ID"               					        , "idRetaguarda"				                                        , "C"           , ""}               , 1, 3)
    
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} setSelect
Carrega a query que será executada

@author  Graziella Bianchin
@since   02/08/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method setSelect(cTable as Character) as Variant Class listaorcamentoObj

    Local cdataInicio   as Character
    Local cdataFim      as Character
    Local cDataBase     := Upper( AllTrim( TcGetDb() ) )

    _Super:setSelect(cTable)
    
    self:cSelect    := "SELECT  L1_FILIAL,L1_CLIENTE, L1_LOJA    , L1_SITUA, L1_VLRLIQ, L1_VALBRUT, L1_DESCONT, L1_TXDESC, L1_JUROS, L1_FRETE, L1_DESPESA,L1_PEDRES," 
    self:cSelect    += IIF(cDataBase != "ORACLE" ," L1_FILIAL+L1_NUM AS L1_ID, ","L1_FILIAL || L1_NUM AS L1_ID")
    self:cSelect    += " L1_EMISSAO , L1_HORA FROM " + RetSqlName("SL1") +" SL1 "
    self:cWhere     += " AND L1_FILIAL = '" + self:jParans['idLojaRetaguarda'] + "' AND L1_DTLIM 	>=	'" + DtoS(dDataBase)+ "' " 
    self:cWhere     += " AND L1_SITUA = '"+Space(TAMSX3('L1_SITUA')[1])+"'" 
    self:cWhere     += " AND L1_DOC   = '"+Space(TAMSX3('L1_DOC')[1])+"'"
    self:cWhere     += " AND EXISTS ("+;
      " SELECT 1"+;
      " FROM " + RetSqlName("SL2")+;
      " WHERE L2_NUM = SL1.L1_NUM"+;
      " AND L2_FILIAL = SL1.L1_FILIAL"+;
      " AND (L2_ENTREGA = '1' OR L2_ENTREGA = '2')"+;
     " ) " 

    If !Empty(self:jParans["cpfCnpj"]) .And. ValType(self:jParans["cpfCnpj"]) == 'C'
        self:cWhere += " AND L1_CLIENTE+L1_LOJA = '" + Self:getCliente() + "'"
    EndIf

    If (!Empty(self:jParans["dataInicio"]) .And. ValType(self:jParans["dataInicio"]) == 'C') .And.;
       (!Empty(self:jParans["dataFim"])    .And. ValType(self:jParans["dataFim"]) == 'C')

        cdataInicio  := SUBSTRING(self:jParans["dataInicio"],1,4)+SUBSTRING(self:jParans["dataInicio"],6,2)+SUBSTRING(self:jParans["dataInicio"],9,2)
        cdataFim     := SUBSTRING(self:jParans["dataFim"],1,4)+SUBSTRING(self:jParans["dataFim"],6,2)+SUBSTRING(self:jParans["dataFim"],9,2)

        self:cWhere += " AND L1_EMISSAO >= '" + cdataInicio + "' "
        self:cWhere += " AND L1_EMISSAO <= '" + cdataFim    + "' "

    EndIf

    self:cGroupBy   := " ORDER BY L1_FILIAL,L1_NUM,L1_CLIENTE,L1_LOJA,L1_EMISSAO "
    
    LjGrvLog("listaorcamentoObj", " lista de orçamentos Query : "+self:cSelect+self:cWhere+self:cGroupBy, self:jParans:toJson())
Return Nil   

//-------------------------------------------------------------------
/*/{Protheus.doc} validation
Metodo para validações executado no momento do get

@author  Graziella Bianchin
@since   02/08/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method validation() as Variant Class listaorcamentoObj

    LjGrvLog("listaorcamentoObj", "Parâmetros de requisição da API lista de orçamentos", self:jParans:toJson())

    _Super:validation()

    If self:lSuccess
        
        Do Case
            Case ValType(self:jParans["idLojaRetaguarda"]) <> "C" .Or. Empty(self:jParans["idLojaRetaguarda"])   //  Id do Dispositivo do PDVOmni
                self:lSuccess   := .F.
                self:cError     := I18n(STR0001, {"idLojaRetaguarda"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
        End Case
        
        If self:lSuccess
            self:cFil := FwXFilial('SB1',self:jParans["idLojaRetaguarda"],'E','E','E')
            self:ChangeBranch()
            //Carrega select
            self:setSelect("SL1")
        Endif

    EndIf
    
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} GetReturn
Retorna json com resultado da consulta

@author  Graziella Souza
@since   02/08/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetReturn() as Character Class listaorcamentoObj

    Local jRetorno          as Object 
    Local cRetorno          as Character
    Local nTotal            as Integer 
    
    jRetorno:= JsonObject():New()
    jRetorno:FromJson( _Super:GetReturn() )
    
    nTotal := len(jRetorno["data"])
    
    jRetorno["errors"]    := nil
    jRetorno["totalTime"] := time()
    jRetorno["numberOfRecords"] := nTotal
   
    cRetorno := jRetorno:toJson()
    FwFreeObj(jRetorno)

Return cRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} SetCliente
Carrega o dado para o json

@author  Graziella Souza
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetCliente() Class listaorcamentoObj
    Local oCliente    := JsonObject():New()
    Local cNome       := ""
    Local cCgc        := ""

    DbSelectArea("SA1")
    SA1->(DbSetOrder(1))

    If SA1->(DbSeek(xFilial("SA1") + PadR((Self:cAliasQuery)->L1_CLIENTE,TamSx3("A1_COD")[1])+PadR((Self:cAliasQuery)->L1_LOJA,TamSx3("A1_LOJA")[1])))
        cNome       := Alltrim(SA1->A1_NOME)
        cCgc        := Alltrim(SA1->A1_CGC)
    EndIF

    oCliente["nome"]    := cNome
    oCliente["cpfCnpj"] := cCgc
    
return oCliente



//-------------------------------------------------------------------
/*/{Protheus.doc} SetaEmissa
Carrega o dado para o json

@author  Graziella Souza
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetaEmissa() Class listaorcamentoObj

    Local cDtEmissa as Character
    //formato 2024-08-01T14:23:55 
    cDtEmissa:= FWTimeStamp(3,STOD((Self:cAliasQuery)->L1_EMISSAO),IIF(!Empty((Self:cAliasQuery)->L1_HORA),Substring((Self:cAliasQuery)->L1_HORA,1,8),"00:00:00"))
    
return cDtEmissa
//-------------------------------------------------------------------
/*/{Protheus.doc} getCliente
Carrega o dado para o json

@author  Graziella Souza
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method getCliente() Class listaorcamentoObj
    Local cCliente    := ""

    DbSelectArea("SA1")
    SA1->(DbSetOrder(3))
    SA1->(DbSeek(xFilial("SA1",self:jParans['idLojaRetaguarda']) + PadR(self:jParans["cpfCnpj"],TamSx3("A1_CGC")[1])))
    cCliente := SA1->A1_COD+SA1->A1_LOJA
return cCliente
//-------------------------------------------------------------------
/*/{Protheus.doc} GetTpEntrega()
Verifica tipo de entrega do pedido, se é recebido ou disponível.
Não será contemplado outros tipo de pedidos Mistos nos itens Exemplo: Entrega, Retira na loja e 
Retira posterior na mesma venda PdvOmni não atende.
@author  Everson S P Junior
@since   29/04/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetTpEntrega() Class listaorcamentoObj
Local cRet  := ''

DbSelectArea("SL2")
SL2->(DbSetOrder(1))
SL2->(DbSeek((Self:cAliasQuery)->L1_ID))
If SL2->(!Eof())
    cRet := IIF(Alltrim(SL2->L2_ENTREGA) == '1','RECEBIDO','DISPONIVEL')
EndIf
Return cRet
