#INCLUDE "PROTHEUS.CH"
#INCLUDE "creditoObj.CH"

Class CreditoObj from LojRestObj20

	Public Method New()           as Object
    
    Protected Method SetFields()  as Variant
    Protected Method Validation() as Variant

    Public  Method SetjBody()     as Variant
    Public  Method SetjParans()   as Variant
    Public  Method getReturn()    as Character

    Private Method SetTipos()     as Variant
    Private Method GetTipos()     as Array

    Private Data aTipos           as Array

EndClass


//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor da Classe

@param oWsRestObj - Objeto WSRESTFUL da API 

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(oWsRestObj As Object, jParans as Json, cBody as Character) As Object Class CreditoObj
        _Super:New(oWsRestObj,jParans,cBody)
        
        Self:SetTipos() 
Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} SetFields
Carrega os campos que serão retornados

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetFields() Class CreditoObj
    Local aAux := {}

    aadd(aAux, "IDRETAGUARDALOJA")  //Tag [1]     
    aadd(aAux, "E1_FILIAL")         //Campo [2]
    aadd(aAux, "E1_FILIAL")         //Expressão que será executada para gerar o retorno[3] 
    aadd(aAux, "IdRetaguardaLoja")  //Tag que será utilizada para preencher o objeto de retorno[4]
    aadd(aAux, "C")                 //Tipo do campo[5]
    aadd(aAux, "xFilial('SE1',&( 'cValue' ))") //Expressão que será executada para composição da query de busca [6] 
    HmAdd(self:oFields,aAux,1,3)

    aAux := {}
    aadd(aAux, "CPFCNPJ")
    aadd(aAux, "E1_CLIENTE")
    aadd(aAux, "GetAdvFVal( 'SA1', 'A1_CGC', xfilial( 'SA1' ) + &( '(self:cAliasQuery)->E1_CLIENTE' ), 1, '')")
    aadd(aAux, "CpfCnpj")
    aadd(aAux, "C")
    aadd(aAux, "GetAdvFVal( 'SA1', 'A1_COD', xfilial( 'SA1' ) + &( 'cValue' ), 3, ' ' )")
    HmAdd(self:oFields,aAux,1,3)

    aAux := {}
    aadd(aAux, "TIPO")
    aadd(aAux, "E1_TIPO")
    aadd(aAux, "E1_TIPO")
    aadd(aAux, "Tipo")
    aadd(aAux, "C")
    aadd(aAux, "")
    HmAdd(self:oFields, aAux , 1, 3)
    
    aAux := {}
    aadd(aAux, "SALDO")
    aadd(aAux, "E1_SALDO")
    aadd(aAux, "E1_SALDO")
    aadd(aAux, "Saldo")
    aadd(aAux, "N")
    aadd(aAux, "")
    HmAdd(self:oFields,aAux, 1, 3)
    
    aAux := {}
     
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Validation
Realiza validações nos parametros de entrada fornecido

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------

Method Validation() as variant Class CreditoObj
    
    Local cMsg  := "" as Character
    Local nTipo  := 1 as Numeric
    
    
    If self:jparans:HasProperty("idRetaguardaLoja") .And. !Empty(self:jparans['idRetaguardaLoja']) 
        
        If Empty(LjAuxPosic("CADASTRO DE LOJA    ","IDFilialProtheus",self:jparans['idRetaguardaLoja'],"IDFilialProtheus"))            
            cMsg += STR0005 //"idRetaguardaLoja informado não localizado no Cadastro de Lojas"
            self:lSuccess    := .F.    
        Else
            self:cFil := self:jparans['idRetaguardaLoja']
            self:ChangeBranch()
        EndIf    
    Else
        cMsg += "Tag idRetaguardaLoja não fornecida, tag obrigatoria" //""
        self:lSuccess    := .F.    
    EndIf
    
    If self:lSuccess .AND. !self:jparans:HasProperty("cpfCnpj") 
        cMsg += STR0001 //"Tag cpfCnpj não fornecida, tag obrigatoria"
        self:lSuccess    := .F.
    EndIf 
  
    If self:lSuccess .AND. Empty(self:jparans['cpfCnpj'])
        cMsg += STR0002 //"Tag cpfCnpj fornecida, porem, sem informação."
        self:lSuccess    := .F.
    EndIf 

    If self:lSuccess .AND. Empty(GetAdvFVal( 'SA1', 'A1_COD', xfilial( 'SA1' ) + PADR(self:jparans['cpfCnpj'],TAMSX3("A1_CGC")[1]), 3,''))
        //cMsg += "Cliente não encontrado com o cpfCnpj = [" + self:jparans['cpfCnpj']  +  "]"
        cMsg += I18n(STR0003, {self:jparans['cpfCnpj']}) + CRLF // "Cliente não encontrado com o cpfCnpj = [#1]"
        self:lSuccess    := .F.
    EndIf 

    If self:lSuccess 
        If self:jparans:HasProperty("tipo") .And. !Empty(self:jparans['tipo'])
            
            cMsg := ""
            
            For nTipo := 1 To Len(self:jparans['tipo'])   
                If AScan(Self:GetTipos(),{|x| x == self:jparans['tipo'][nTipo]}) == 0
                    self:lSuccess := .F.
                    cMsg += I18n(STR0004, {self:jparans['tipo'][nTipo]}) + CRLF //"O tipo:[#1] não é permitido para esta consulta."
                EndIf 
            Next nTipo
        Else  
            Self:SetjParans("tipo",Self:GetTipos())
        EndIf
    EndIf 

    If !self:lSuccess
        self:cError      := cMsg
        self:cDetail     := ""
        self:nStatusCode := 422
    EndIf
return
//-------------------------------------------------------------------
/*/{Protheus.doc} SetTipos
Seta tipos padrões para busca de credito

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetTipos() as variant Class CreditoObj
    Self:aTipos := {}

    Aadd(Self:aTipos,"NCC")
    Aadd(Self:aTipos,"RA")
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetTipos
Retorna tipos padrões para busca de credito

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------

Method GetTipos() as Array Class CreditoObj
Return Self:aTipos

//-------------------------------------------------------------------
/*/{Protheus.doc} SetjBody
Inclui novas prorpiedades no corpo da requisição

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------

Method SetjBody(cProprety as Character, xValue as Variant) as variant class CreditoObj
    Self:jBody[cProprety] := xValue
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} SetjParans
Inclui novas prorpiedades no corpo da requisição

@author  Lucas Novais (lnovais@)
@since   08/08/2022
@version 1.0
/*/
//-------------------------------------------------------------------

Method SetjParans(cProprety as Character, xValue as Variant) as variant class CreditoObj
    Self:jparans[cProprety] := xValue
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getReturn
Retorna json com resultado da consulta

@author  Rafael Tenorio da Costa
@since   08/12/2023
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method getReturn() as Character Class CreditoObj

    Local jRetorno  as Object 
    Local jAux      as Object 
    Local cRetorno  as Character
    Local nData     as Integer

    jRetorno := JsonObject():New()
    jRetorno:FromJson( _Super:GetReturn() )

    If self:lSuccess

        jAux := JsonObject():New()
        jAux["creditos"] := {}

        For nData:=1 To Len(jRetorno["data"])

            aAdd(jAux["creditos"], JsonObject():New() )

            jAux["creditos"][nData]["saldo"]                := jRetorno["data"][nData]["Saldo"]
            jAux["creditos"][nData]["idRetaguardaCredito"]  := ""
        Next nData

        jRetorno["data"] := jAux
    EndIf

    cRetorno := EncodeUtf8( jRetorno:toJson() )

    FwFreeObj(jRetorno)
    FwFreeObj(jAux)

Return cRetorno
