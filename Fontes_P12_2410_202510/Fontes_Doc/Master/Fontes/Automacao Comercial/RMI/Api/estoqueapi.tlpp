#INCLUDE "TLPP-CORE.TH"
#INCLUDE "TLPP-REST.TH"

namespace totvs.protheus.retail.rmi.api.estoqueapi

using namespace totvs.protheus.retail.rmi.api.estoqueobj

//-------------------------------------------------------------------
/*/{Protheus.doc}
    API para consulta de Estoque do Varejo
/*/
//-------------------------------------------------------------------
@Post("api/retail/v1/estoque/lista")
Function lista(jParams as Json,cBody as Character)
    Local cReturn  := ""
    Local oEstoque := Nil
    
    Default jParams := oRest:getQueryRequest()
    Default cBody   := oRest:getBodyRequest()
    
    oEstoque := EstoqueObj():New(oRest, jParams , cBody)

    oEstoque:Get()    
    cReturn := oEstoque:getReturn()    
    LjGrvLog("/api/retail/v1/credito/lista/","Return: ",cReturn)

    IIf(Empty(cReturn),Eval({|| oRest:setStatusCode(500), cReturn := "Ops! A consulta não retornou um json válido. Por favor, verifique os logs do Protheus (Console, LogLoja) para analisar o ocorrido."}),) 
        
    oRest:setResponse(cReturn)
Return


//-------------------------------------------------------------------
/*/{Protheus.doc}
    API para consulta de Estoque do Varejo (QA)
/*/
//-------------------------------------------------------------------
@Get("api/pdvsyncserver/retaguarda/processoOnlineEstoqueSaldo/:inquilino/:idRetaguardaProduto/:idRetaguardaLojaOrigem/:idRetaguardaLojasConsultaEstoque")
Function listaQa()
    Local jParams := oRest:getPathParamsRequest()
    Local aProd   := {}
    Local aLoja   := {}

    aAdd(aProd,jParams['idRetaguardaProduto'])
    aAdd(aLoja,jParams['idRetaguardaLojasConsultaEstoque'])

    jParams['idRetaguardaProduto'] := aProd
    If !Empty(jParams['idRetaguardaLojasConsultaEstoque']) 
        jParams['idRetaguardaLojasConsultaEstoque'] := aLoja  
    EndIf   
    LjGrvLog("Realizada chamada de API de uso interno através do endpoint : api/pdvsyncserver/retaguarda/processoOnlineEstoqueSaldo")
    lista(jParams, "{}")
    FwFreeObj(jParams)
    FwFreeArray(aProd)
    FwFreeArray(aLoja)
Return

//-------------------------------------------------------------------
/*/{Protheus.doc}
    API para consulta de Estoque do Varejo (QA)
/*/
//-------------------------------------------------------------------
@Get("api/pdvsyncserver/retaguarda/processoOnlineEstoqueSaldo/:inquilino/:idRetaguardaProduto/:idRetaguardaLojaOrigem")
Function listaQa2()
    listaQa()
Return