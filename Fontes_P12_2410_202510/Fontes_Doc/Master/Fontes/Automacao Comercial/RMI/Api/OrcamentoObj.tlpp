#INCLUDE "TLPP-CORE.TH"
#INCLUDE "TLPP-REST.TH"

namespace totvs.protheus.retail.rmi.api.OrcamentoObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe identificadorPdv
    Classe para tratamento da API do Varejo Orcamento
/*/
//-------------------------------------------------------------------
Class Orcamento From LojRestObj20

Public Method New()                         as Object
Public Method setFields()                   as Variant
Public Method getReturn()                   as Character
Public Method SetSelect()                   as Variant
Public Method Validation()                  as Variant
Public Method SetCliente()		            as Object	  
Public Method SetMotivoDesconto()	        as Object 
Public Method SetPreVendaItens()	        as Object  
Public Method SetPreVendaPagamentos()       as Object  
Public Method SetVendaPagamentos()          as Object
Public Method GetCgcCli()                     as Character
Public Method GetTpEntrega()                  as Character

Private   Data idInquilino      as Character




EndClass
//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@param oWsRestObj - Objeto WSRESTFUL da API

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(oWsRestObj As Object, jParans as Json) as Object Class Orcamento

    _Super:New(oWsRestObj, jParans, '{}')

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} setFields
Carrega os campos que serão retornados

@author  Everson S P Junior
@version 1.0
/*/
//-------------------------------------------------------------------
Method setFields() as Variant Class Orcamento

                        //Tag - ID                          Campo                   Expressão que será executada para gerar o retorno    Tag que será utilizada para preencher o objeto    Tipo do Campo    Expressão de busca
   HmAdd(self:oFields, {"OFFLINE"                         , ""	                      , ".F."	                        				    , "offline"                                            , "L"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDRETAGUARDA"                    , ""                        , "L1_FILIAL+L1_NUM"					                , "idRetaguarda"                                       , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"CLIENTE"                         , ""                        , "self:SetCliente()"			    	                , "cliente"                                            , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"PREVENDAITENS"                   , ""                        , "self:SetPreVendaItens()"				            , "preVendaItens"                                      , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"PREVENDAPAGAMENTOS"              , ""                        , "self:SetPreVendaPagamentos()"				        , "preVendaPagamentos"                                 , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"SITUACAOPREVENDA"                , ""                        , "Self:GetTpEntrega()"                               , "situacaoPreVenda"                                    , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDLOJARETAGUARDA"                , "L1_FILIAL"	              , "L1_FILIAL"									        , "idLojaRetaguarda"                                   , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDOPERADORRETAGUARDA"            , ""                        , "''"								                , "idOperadorRetaguarda"                               , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"DESCONTOPERCENTUALTOTAL"         , "L1_DESCNF"               , "L1_DESCNF"    				                        , "descontoPercentualTotal"                            , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"DESCONTOTOTAL"                   , "L1_DESCONT"              , "L1_DESCONT"						                , "descontoTotal"                                      , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"ACRESCIMOPERCENTUALTOTAL"        , ""                        , "NoRound((L1_DESPESA / L1_VLRTOT) * 100)"           , "acrescimoPercentualTotal"                           , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"ACRESCIMOTOTAL"                  , "L1_DESPESA"              , "L1_DESPESA"								        , "acrescimoTotal"                                     , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"FRETETOTAL"                      , ""                        , "0"								                    , "freteTotal"                                         , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TOTALBRUTO"                      , ""                        , "L1_VALMERC"					                    , "totalBruto"                                         , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"MULTIPLOSPEDIDOS"                , ""                        , ".T."								                , "multiplosPedidos"                                   , "L"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"DATACADASTRO"                    , ""                        , "FWTimeStamp(3)"					                , "dataCadastro"                                       , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"CODIGOPROMOCIONAL"               , ""                        , "''"	        							        , "codigoPromocional"                                  , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"CPFCONSUMIDOR"                   , ""                        , "self:GetCgcCli()"				                    , "cpfConsumidor"                                      , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"DATA"                            , ""                        , "'2024-06-17T21:15:15.380Z'"	                    , "data"                                               , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDCANALVENDARETAGUARDA"          , ""                        , "''"								                , "idCanalVendaRetaguarda"                             , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDINQUILINO"                     , ""	                      , "''"              	                        		, "idInquilino"                                        , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TOTALLIQUIDO"                    , ""                        , "L1_VLRLIQ"						                    , "totalLiquido"                                       , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"NOMECONSUMIDOR"                  , ""                        , "''"								                , "nomeConsumidor"                                     , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"NUMERO"                          , ""                        , "L1_NUM"							                , "numero"                                             , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"NUMEROAUTORIZACAO"               , ""                        , "''"								                , "numeroAutorizacao"                                  , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"NUMEROVENDA"                     , ""                        , "L1_FILIAL+L1_NUM"					                , "numeroVenda"                                        , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"SERIEVENDA"                      , ""                        , "''"								                , "serieVenda"                                         , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"SINCRONIZADO"                    , ""                        , "0"								                    , "sincronizado"                                       , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"SISTEMAORIGEM"                   , ""                        , "0"								                    , "sistemaOrigem"                                      , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TERMINALORIGEM"                  , ""                        , "''"								                , "terminalOrigem"                                     , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPOCANCELAMENTO"                , ""                        , "0"								                    , "tipoCancelamento"                                   , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPODESCONTOACRESCIMO"           , ""                        , "0"								                    , "tipoDescontoAcrescimo"                              , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPOENTREGA"                     , ""                        , "2"								                    , "tipoEntrega"                                        , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPOPREVENDA"                    , ""                        , "0"								                    , "tipoPreVenda"                                       , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPOVENDA"                       , ""                        , "0"								                    , "tipoVenda"                                          , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"FRETEPERCENTUALTOTAL"            , ""                        , "0"								                    , "fretePercentualTotal"                               , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"TIPOVALORFRETE"                  , ""                        , "0"								                    , "tipoValorFrete"                                     , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"MODALIDADEFRETE"                 , ""                        , "0"								                    , "modalidadeFrete"                                    , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"NUMEROPDV"                       , ""                        , "0"								                    , "numeroPdv"                                          , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"DATAATUALIZACAO"                 , ""                        , "FWTimeStamp(3)"					                , "dataAtualizacao"                                    , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"ID"                              , ""                        , "0"								                    , "id"                                                 , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"SITUACAO"                        , ""                        , "0"								                    , "situacao"                                           , "N"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"MOTIVODESCONTO"                  , ""                        , "self:SetMotivoDesconto()"			                , "motivoDesconto"                                     , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"VENDAPAGAMENTOS"                 , ""                        , "self:SetVendaPagamentos()"				            , "vendaPagamentos"                                    , "C"           , ""}               , 1, 3)


Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Validation
Metodo para validações executado no momento do get
1
@author  Everson S P Junior
@since   14/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method Validation(cOrigem as Character) as Variant Class Orcamento

    _Super:Validation()

    If self:lSuccess

        Do Case

            Case !self:jParans:hasProperty("IdPedidoRetaguarda") .Or. ValType(self:jParans["IdPedidoRetaguarda"]) <> "C" .Or. Empty(self:jParans["IdPedidoRetaguarda"])   //  Id do Dispositivo do PDVOmni
                self:lSuccess   := .F.
                self:cError     := I18n("Ops! #1 nao enviado(s) ou invalido(s).", {"IdPedidoRetaguarda"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
        End Case

        If self:lSuccess
            Self:cFil := FwXFilial('SB1',Substr(self:jParans["IdPedidoRetaguarda"],1,FwSizeFilial()),'E','E','E')
            Self:ChangeBranch()
            self:SetSelect()
        EndIf

    EndIf

Return Nil
//-------------------------------------------------------------------
/*/{Protheus.doc} GetReturn
Retorna json com resultado da consulta

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method getReturn() as Character Class Orcamento

    Local jRetorno          as Object 
    Local cRetorno          as Character
    Local cAuxJson          as Object 

    
    jRetorno:= JsonObject():New()
    jRetorno:FromJson( _Super:GetReturn() )
    
    //Ajuste para o Retorno que é um Objeto e não em lista
    If self:lSuccess
        cAuxJson := jRetorno["data"][1]:ToJson()
    EndIf    
    
    jRetorno["data"] :=JsonObject():New()
    
    If self:lSuccess
        jRetorno["data"]:FromJson(cAuxJson)
    EndIf    
    If Valtype(jRetorno["data"]["vendaPagamentos"]) == "J"
        jRetorno["data"]["vendaPagamentos"] := Nil    
    EndIf   
    jRetorno["data"]["errors"]   := JsonObject():New()
    jRetorno["data"]["errors"]["additionalProp1"]   := IIF( self:lSuccess,"",jRetorno["message"])
    jRetorno["data"]["errors"]["additionalProp2"]   := IIF( self:lSuccess,"",jRetorno["message"])
    jRetorno["data"]["errors"]["additionalProp3"]   := IIF( self:lSuccess,"",jRetorno["message"])
    jRetorno["data"]["totalTime"] := time()
    jRetorno["data"]["numberOfRecords"] := 1

    cRetorno := jRetorno:toJson()
    FwFreeObj(jRetorno)

Return cRetorno
//-------------------------------------------------------------------
/*/{Protheus.doc} SetSelect
Carrega a query que será executada

@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetSelect() Class Orcamento

    Local cSelect       := ""
    Local cWhere        := ""
    Local cGroupBy      := " GROUP BY L1_FILIAL,L1_NUM,L1_CLIENTE,L1_LOJA,L1_VLRLIQ,L1_VALMERC,L1_PARCELA,L1_CONDPG,L1_ESTACAO,L1_FORMPG,L1_DESCNF,L1_DESCONT,L1_DESPESA,L1_VLRTOT, L1_PEDRES "

    cSelect := " SELECT L1_FILIAL,L1_NUM,L1_CLIENTE,L1_LOJA,L1_VLRLIQ,L1_VALMERC,L1_PARCELA,L1_CONDPG,L1_ESTACAO,L1_FORMPG,L1_DESCNF,L1_DESCONT,L1_DESPESA,L1_VLRTOT, L1_PEDRES FROM " + RetSqlName("SL1") + " SL1"    
    cWhere  += " WHERE CONCAT(L1_FILIAL, L1_NUM ) = '" + self:jParans["IdPedidoRetaguarda"] + "'"
    cWhere  += " AND SL1.D_E_L_E_T_ = ' ' "
    cWhere  += " AND  L1_SITUA = '  ' "

    self:cTable     := "SL1"
    self:cSelect    := cSelect
    self:cWhere     := cWhere
    self:cGroupBy   := cGroupBy
    
Return Nil
//-------------------------------------------------------------------
/*/{Protheus.doc} SetCliente
Carrega a query que será executada

@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetCliente() Class Orcamento
    Local oCliente    := JsonObject():New()
    Local oEndereco   := JsonObject():New()

    DbSelectArea("SA1")
    SA1->(DbSetOrder(1))
    SA1->(DbSeek(xFilial("SA1") + PadR((Self:cAliasQuery)->L1_CLIENTE,TamSx3("A1_COD")[1])+PadR((Self:cAliasQuery)->L1_LOJA,TamSx3("A1_LOJA")[1])))
    
    oCliente['nome'] := NOACENTO(UPPER(AllTrim(SA1->A1_NOME)))
    oCliente['endereco'] := NOACENTO(UPPER(AllTrim(SA1->A1_END)))
    oCliente['bairro'] := NOACENTO(UPPER(AllTrim(SA1->A1_BAIRRO)))
    oCliente['cidade'] := AllTrim(SA1->A1_MUN)
    oCliente['estado'] := AllTrim(SA1->A1_EST)
    oCliente['cep'] := AllTrim(SA1->A1_CEP)
    oCliente['telefone'] := AllTrim(SA1->A1_TEL)
    oCliente['celular'] := AllTrim(SA1->A1_TEL)
    oCliente['email'] := AllTrim(A1_EMAIL)
    oCliente['cpfCnpj'] := AllTrim(SA1->A1_CGC)
    oCliente['clienteAlterado'] := .F.
    oCliente['complemento'] := NOACENTO(UPPER(AllTrim(SA1->A1_COMPLEM)))
    oCliente['dataNascimento'] := IIf(!Empty(SA1->A1_DTNASC),FWTimeStamp(3,SA1->A1_DTNASC,"00:00:00"),Nil)
    oCliente['rgInscricao'] :=AllTrim(SA1->A1_RG)
    oCliente['sexo']:= nil
    
    oCliente['enderecos'] := {}
    oEndereco['bairro'] := NOACENTO(UPPER(AllTrim(SA1->A1_BAIRRO)))
    oEndereco['celular'] := ""
    oEndereco['cep'] := AllTrim(SA1->A1_CEP)
    oEndereco['cidade'] := AllTrim(SA1->A1_MUN)
    oEndereco['complemento'] := NOACENTO(UPPER(AllTrim(SA1->A1_COMPLEM)))
    oEndereco['endereco'] := NOACENTO(UPPER(AllTrim(SA1->A1_END)))
    oEndereco['numero'] := ""
    oEndereco['tipo'] := IIF(SA1->A1_PESSOA == 'F', "1", "0")
    oEndereco['tipoId'] := 0
    oEndereco['dataAtualizacao'] := FwTimeStamp(6)
    oEndereco['dataCadastro'] := FwTimeStamp( 6, SA1->A1_DTCAD)
    oEndereco['id'] := 0
    oEndereco['idRetaguarda'] := SA1->A1_COD+SA1->A1_LOJA
    oEndereco['situacao'] := 1
    
    aAdd(oCliente['enderecos'],oEndereco)
    
return oCliente
//-------------------------------------------------------------------
/*/{Protheus.doc} SetPreVendaItens
Carrega a query que será executada

@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetPreVendaItens() Class Orcamento
Local opreVendaItens := {}
Local nX             := 0

    DbSelectArea("SL2")
    SL2->(DbSetOrder(1))
    SL2->(DbSeek(PadR((Self:cAliasQuery)->L1_FILIAL,TamSx3("L2_FILIAL")[1])+PadR((Self:cAliasQuery)->L1_NUM,TamSx3("L2_NUM")[1])))
    
    While SL2->(!Eof()) .AND. SL2->L2_FILIAL + SL2->L2_NUM == (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
        nX++
        Aadd(opreVendaItens,JsonObject():New())
        opreVendaItens[nX]['preco'] := SL2->L2_VRUNIT + (SL2->L2_VALDESC / SL2->L2_QUANT)
        opreVendaItens[nX]['quantidade'] := SL2->L2_QUANT
        opreVendaItens[nX]['descontoPercentual'] := SL2->L2_DESC
        opreVendaItens[nX]['acrescimoPercentual'] := 0
        opreVendaItens[nX]['quantidadeEncomenda'] := SL2->L2_QUANT
        opreVendaItens[nX]['idPreVenda'] := 0
        opreVendaItens[nX]['idProdutoRetaguarda'] := Alltrim(SL2->L2_PRODUTO)
        opreVendaItens[nX]['idProdutoEmbalagemRetaguarda'] := ""
        opreVendaItens[nX]['acrescimo'] := 0
        opreVendaItens[nX]['codigoFilialSaida'] := SL2->L2_FILIAL
        opreVendaItens[nX]['codigoProduto'] := Alltrim(SL2->L2_PRODUTO)
        opreVendaItens[nX]['dataEntrega'] := FwTimeStamp(6) 
        opreVendaItens[nX]['desconto'] := SL2->L2_VALDESC
        opreVendaItens[nX]['idPrecoRetaguarda'] := ""
        opreVendaItens[nX]['idVendedorRetaguarda'] := LjAuxPosic("OPERADOR DE LOJA",'vendedor',SL2->L2_VEND,'MIH_ID')
        opreVendaItens[nX]['dataAtualizacao'] := FwTimeStamp(6)
        opreVendaItens[nX]['dataCadastro'] := FwTimeStamp(6)
        opreVendaItens[nX]['id'] := VAL(SL2->L2_ITEM)
        opreVendaItens[nX]['idRetaguarda'] := AllTrim(SL2->L2_PRODUTO)
        opreVendaItens[nX]['situacao'] := 0
        opreVendaItens[nX]['quantidadeFaturada'] := SL2->L2_QUANT
        opreVendaItens[nX]['frete'] := 0
        opreVendaItens[nX]['fretePercentual'] := 0
        opreVendaItens[nX]['tipoValorFrete'] := 0

        
        opreVendaItens[nX]['entrega']:= JsonObject():New()
        opreVendaItens[nX]['entrega']['dataEntrega'] := FwTimeStamp(6) 
        opreVendaItens[nX]['entrega']['idLojaEntregaRetaguarda'] := SL2->L2_FILIAL 
        opreVendaItens[nX]['entrega']['tipoEntrega'] := '0'
        opreVendaItens[nX]['entrega']['lockerMachineCode'] := ""
        opreVendaItens[nX]['entrega']['lockerParcelCode'] := "" 
        opreVendaItens[nX]['entrega']['fretePercentualTotal'] := 0
        opreVendaItens[nX]['entrega']['freteTotal'] := 0
        opreVendaItens[nX]['entrega']['tipoValorFrete'] := 0

        
        opreVendaItens[nX]['itensKit']:= {}
        Aadd(opreVendaItens[nX]['itensKit'],JsonObject():New())
        opreVendaItens[nX]['itensKit'][1]['idProdutoRetaguarda'] := Alltrim(SL2->L2_PRODUTO)
        opreVendaItens[nX]['itensKit'][1]['sequencial'] := 1
        opreVendaItens[nX]['itensKit'][1]['preco'] := 0
        opreVendaItens[nX]['itensKit'][1]['quantidade'] := 0
        
        
        opreVendaItens[nX]['motivoDesconto'] := Self:SetMotivoDesconto()
        
        
        SL2->( DbSkip() )
    EndDo

Return opreVendaItens
//-------------------------------------------------------------------
/*/{Protheus.doc} SetMotivoDesconto
@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetMotivoDesconto() Class Orcamento
Local oMotivoDesconto := JsonObject():New()

    oMotivoDesconto['descontoMotorPromocaoAplicado'] := .F.
    oMotivoDesconto['idPromocaoMotorPromocaoTerceiro'] := ""
    oMotivoDesconto['tipo'] := 0
    oMotivoDesconto['tipoDescontoAcrescimo'] := 0
    oMotivoDesconto['dataAtualizacao'] := FwTimeStamp(6)
    oMotivoDesconto['dataCadastro'] := FwTimeStamp(6)
    oMotivoDesconto['id'] := 0
    oMotivoDesconto['idRetaguarda'] := ""
    oMotivoDesconto['situacao'] := 0

Return oMotivoDesconto

//-------------------------------------------------------------------
/*/{Protheus.doc} SetPreVendaPagamentos
@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetPreVendaPagamentos() Class Orcamento
    Local oPreVendaPagamentos := {}
    Local nX            := 0
    Local nParcelas     := 0
    Local dDataPagto    := dDataBase
    Local nVlrPag       := 0
    Local cFormaPg      := ""
    Local cAdminis      := ""
    Local cNsu          := ""
    Local cDocTef       := ""
    Local cItem         := ""
    Local nTroco        := 0
    Local idRetFpgo     := ""

    DbSelectArea("SL4")
    SL4->(DbSetOrder(1))
    SL4->(DbSeek(PadR((Self:cAliasQuery)->L1_FILIAL,TamSx3("L4_FILIAL")[1])+PadR((Self:cAliasQuery)->L1_NUM,TamSx3("L4_NUM")[1])))
    
    While SL4->(!Eof()) .AND. SL4->L4_FILIAL + SL4->L4_NUM == (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
        idRetFpgo     := LjAuxPosic("FORMA DE PAGAMENTO",'forma',IiF(Empty(SL4->L4_FORMA),(Self:cAliasQuery)->L1_FORMPG,SL4->L4_FORMA),'MIH_ID')
        cFormaPg    := SL4->L4_FORMA
        cNsu        := SL4->L4_NSUTEF
        dDataPagto  := SL4->L4_DATA
        nVlrPag     := SL4->L4_VALOR
        cAdminis    := SL4->L4_ADMINIS                        
        cDocTef     := SL4->L4_DOCTEF
        cAut        := SL4->L4_AUTORIZ
        nTroco      := SL4->L4_TROCO
        cItem       := SL4->L4_ITEM
        nX++ 
        nParcelas++
        aadd(oPreVendaPagamentos, JsonObject():New())
        oPreVendaPagamentos[nX]['valorPago'] := nVlrPag
        oPreVendaPagamentos[nX]['pagamentoForma'] := JsonObject():New()
        oPreVendaPagamentos[nX]['pagamentoForma']["idRetaguarda"]:= idRetFpgo
        oPreVendaPagamentos[nX]['quantidadeParcelas'] := nParcelas
        oPreVendaPagamentos[nX]['dataAtualizacao'] := FwTimeStamp(6)
        oPreVendaPagamentos[nX]['dataCadastro'] := FwTimeStamp(6)
        oPreVendaPagamentos[nX]['id'] := VAL(cItem)
        oPreVendaPagamentos[nX]['idRetaguarda'] := (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
        oPreVendaPagamentos[nX]['situacao'] := 0
        oPreVendaPagamentos[nX]['idPagamentoCondicaoRetaguarda'] := (Self:cAliasQuery)->L1_CONDPG
        oPreVendaPagamentos[nX]['idPagamentoFormaRetaguarda'] := idRetFpgo
        oPreVendaPagamentos[nX]['idPagamentoOperadoraRetaguarda'] := ""
        oPreVendaPagamentos[nX]['idPreVenda'] := (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
        oPreVendaPagamentos[nX]['valorLiquido'] := nVlrPag
        oPreVendaPagamentos[nX]['valorMinimoAceito'] := 0
        
        oPreVendaPagamentos[nX]['pagamentoCondicao'] := JsonObject():New()
        oPreVendaPagamentos[nX]['pagamentoCondicao']["idRetaguarda"]:= (Self:cAliasQuery)->L1_CONDPG


        oPreVendaPagamentos[nX]['preVendaPagamentoDadosComplementares'] := {}
        oPreVendaPagamentos[nX]['preVendaPagamentoItens'] := {}
        oPreVendaPagamentos[nX]['preVendaPagamentoTefs']   := {}

        
        If AllTrim(cFormaPg) $ "CC"

            aadd(oPreVendaPagamentos[nX]['preVendaPagamentoItens'],JsonObject():New())
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["idPreVendaPagamento"] := (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["numeroParcela"] := (Self:cAliasQuery)->L1_PARCELA
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["valorParcela"] := nVlrPag
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["dataAtualizacao"] :=FwTimeStamp(6)
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["dataCadastro"] := FwTimeStamp(6)
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["dataVencimento"] := FwTimeStamp(6)
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["id"] := 0
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["idRetaguarda"] := (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
            oPreVendaPagamentos[nX]['preVendaPagamentoItens'][1]["situacao"] := 0
        EndIf
        
    
        
        If AllTrim(cFormaPg) $ "CC"
            aadd(oPreVendaPagamentos[nX]['preVendaPagamentoTefs'],JsonObject():New())
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["bandeira"]:= cAdminis
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["codigoRede"]:= Substr(cAdminis,1,3)
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["dataPreDatado"]:= FwTimeStamp(6)
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["diasEntreParcelas"]:= 30
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["idPreVendaPagamento"]:= 0
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["idTransacaoDigital"]:= ""
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["nsuHost"]:= cNsu
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["numeroAutorizacao"]:= cAut
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["numeroBin"]:= cDocTef
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["numeroParcelas"]:= (Self:cAliasQuery)->L1_PARCELA
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["primeiraParcela"]:= ""
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["dataAtualizacao"]:= FwTimeStamp(6)
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["dataCadastro"]:= FwTimeStamp( 6, dDataPagto) 
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["id"]:= 3939
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["idRetaguarda"]:= (Self:cAliasQuery)->L1_FILIAL +(Self:cAliasQuery)->L1_NUM
            oPreVendaPagamentos[nX]['preVendaPagamentoTefs'][1]["situacao"]:= 0
        EndIf
        SL4->(dbSkip())
    EndDo


Return oPreVendaPagamentos

//-------------------------------------------------------------------
/*/{Protheus.doc} SetVendaPagamentos
Carrega a query que será executada

@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method SetVendaPagamentos() Class Orcamento
Local oVendaPagamentos := Nil
   
Return oVendaPagamentos

//-------------------------------------------------------------------
/*/{Protheus.doc} GetCgcCli
retorna CPF ou CNPJ do cliente 

@author  Everson S P Junior
@since   09/08/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetCgcCli() Class Orcamento
Local  cRet := ""
Local aArea := GetArea()    
    
    DbSelectArea("SA1")
    SA1->(DbSetOrder(1))
    SA1->(DbSeek(xFilial("SA1") + PadR((Self:cAliasQuery)->L1_CLIENTE,TamSx3("A1_COD")[1])+PadR((Self:cAliasQuery)->L1_LOJA,TamSx3("A1_LOJA")[1])))
    cRet := Alltrim(SA1->A1_CGC)   

RestArea(aArea)
Return cRet
//-------------------------------------------------------------------
/*/{Protheus.doc} gettpentrega()
Verifica tipo de retira posterior sem pedido.
Não será contemplado outros tipo de pedidos Mistos nos itens
Exemplo: Entrega e Retira na loja, Retira posterior.
@author  Everson S P Junior
@since   29/04/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetTpEntrega() Class Orcamento
Local cRet  := ''

DbSelectArea("SL2")
SL2->(DbSetOrder(1))
SL2->(DbSeek(PadR((Self:cAliasQuery)->L1_FILIAL,TamSx3("L2_FILIAL")[1])+PadR((Self:cAliasQuery)->L1_NUM,TamSx3("L2_NUM")[1])))
If SL2->(!Eof())// Não enviar caso não encontrar item e todos os itens devem ser do mesmo tipo de entrega
    cRet :=  IIF(Alltrim(SL2->L2_ENTREGA) == '1'  ,'RECEBIDO','DISPONIVEL')
EndIf    

Return cRet
