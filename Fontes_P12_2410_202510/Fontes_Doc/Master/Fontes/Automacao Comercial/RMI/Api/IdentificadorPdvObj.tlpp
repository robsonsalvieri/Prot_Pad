#INCLUDE "TOTVS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "TLPP-REST.TH"

namespace totvs.protheus.retail.rmi.api.identificadorPdvObj

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe identificadorPdv
    Classe para tratamento da API de Estacao do Varejo identificadorPdv
/*/
//-------------------------------------------------------------------
Class identificadorPdv From LojRestObj20

	Public Method New()             as Object
    Public Method setFields()       as Variant
    Public Method getReturn()       as Character
    Public Method setDevice(jRetorno As Object)    as Variant
    Public Method getDadosNota()    as Variant
    
    Protected Method Validation()   as Variant
    Private Method Select()         as Variant

    Private   Data aReque         as Array
    Private   Data Inquilino      as Character
    Private   Data serieNota      as Character
    Private   Data modeloFiscal   as Numeric
    Private   Data numeroNota     as Character
    Private   Data ValidaTag      as Logical


EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@param oWsRestObj - Objeto WSRESTFUL da API

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method New(oWsRestObj As Object, jParans as Json) as Object Class identificadorPdv

    _Super:New(oWsRestObj, jParans, '{}')

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} setFields
Carrega os campos que serão retornados

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method setFields() as Variant Class identificadorPdv

                        //Tag - ID           Campo       Expressão que será executada para gerar o retorno        Tag que será utilizada para preencher o objeto de retorno   Tipo do Campo   Expressão de busca
   HmAdd(self:oFields, {"IDINQUILINO"           , ""	                    , "self:Inquilino"					                , "idInquilino"                                        , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDRETAGUARDALOJA"      , "LG_FILIAL"	            , "LG_FILIAL"									    , "IdRetaguardaLoja"                                   , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"IDDISPOSITIVO"         , "LG_NOMCOMP"              , "LG_NOMCOMP"								        , "idDispositivo"                                      , "C"           , ""}               , 1, 3)
   HmAdd(self:oFields, {"PDV"                   , "LG_CODIGO"               , "Val(LG_CODIGO)"								        , "pdv"                                                , "C"           , ""}               , 1, 3)


Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Validation
Metodo para validações executado no momento do get
1
@author  Everson S P Junior
@since   14/09/2022
@version 1.0
/*/
//-------------------------------------------------------------------
Method Validation(cOrigem as Character) as Variant Class identificadorPdv

    _Super:Validation()

    If self:lSuccess

        If cOrigem == "GET"
            self:ValidaTag := .T.
            //Tags obrigatórias
            Do Case

                Case !self:jParans:hasProperty("idInquilino") .Or. ValType(self:jParans["idInquilino"]) <> "C" .Or. Empty(self:jParans["idInquilino"])
                    self:lSuccess   := .F.
                    self:cError     := I18n("Ops! #1 não enviado(s) ou inválido(s).", {"idInquilino"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
                    self:ValidaTag  := .F.  
                Case !self:jParans:hasProperty("idLojaRetaguarda") .Or. ValType(self:jParans["idLojaRetaguarda"]) <> "C" .Or. Empty(self:jParans["idLojaRetaguarda"]) .Or. !FwFilExist(/*cEmpAnt*/, self:jParans["idLojaRetaguarda"])
                    self:lSuccess   := .F.
                    self:cError     := I18n("Ops! #1 não enviado(s) ou inválido(s).", {"idLojaRetaguarda"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
                    self:ValidaTag  := .F.
                Case !self:jParans:hasProperty("idDispositivo") .Or. ValType(self:jParans["idDispositivo"]) <> "C" .Or. Empty(self:jParans["idDispositivo"])   //  Id do Dispositivo do PDVOmni
                    self:lSuccess   := .F.
                    self:cError     := I18n("Ops! #1 não enviado(s) ou inválido(s).", {"idDispositivo"} )  //"Ops! #1 não enviado(s) ou inválido(s)."
                    self:ValidaTag  := .F.
            End Case

            If self:lSuccess
                
                self:Inquilino      := self:jParans["idInquilino"]
                self:serieNota      := ""
                self:modeloFiscal   := 0
                self:numeroNota     := ""
                Self:getDadosNota()
                self:jParans:delName("idInquilino")
            
            EndIf
        EndIf
    EndIf

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} GetReturn
Retorna json com resultado da consulta

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method getReturn() as Character Class identificadorPdv

    Local jRetorno  as Object 
    Local cRetorno  as Character
    Local cAuxJson  as Character

    jRetorno:= JsonObject():New()
    jRetorno:FromJson( _Super:GetReturn() )
    
    If !self:lSuccess .AND. self:ValidaTag
        self:setDevice()
        jRetorno:FromJson( _Super:GetReturn() )        
    EndIf
    
    If self:lSuccess
        cAuxJson := jRetorno["data"][1]:ToJson()
        jRetorno["data"] :=JsonObject():New()
        jRetorno["data"]:FromJson(cAuxJson)
        
        jRetorno["data"]["ipServidor"]   := ""
        jRetorno["data"]["dadosfiscais"] := JsonObject():New()
        jRetorno["data"]["dadosfiscais"]["serieNota"]       := self:serieNota
        jRetorno["data"]["dadosfiscais"]["modeloFiscal"]    := self:modeloFiscal
        jRetorno["data"]["dadosfiscais"]["numeroNota"]      := Val(self:numeroNota) 
    
        jRetorno["data"]["tef"] := JsonObject():New()
        jRetorno["data"]["tef"]["codigoEmpresa"] := ""
        jRetorno["data"]["tef"]["ip"] := ""
        jRetorno["data"]["tef"]["terminal"] := 0 
    EndIf
    
    cRetorno := EncodeUtf8( jRetorno:toJson() )

    LjGrvLog("identificadorPdv", "Retorno da API de Estacao", cRetorno)

    FwFreeObj(jRetorno)

Return cRetorno
//-------------------------------------------------------------------
/*/{Protheus.doc} setDevice
Grava o Device e Retorna os dados de Nota.

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method setDevice() as Variant Class identificadorPdv

    Local nTamCod   := tamSx3("LG_CODIGO")[1]
    Local cCodigo   := "100"
    Local cFilialSlg:= ""
    Local aEstacao	:= {}
    Local aErro		:= {}
    Local cErro		:= ""
    Local nCont     := 0

    Private lMsErroAuto	   := .F.	//Cria a variavel do retorno da rotina automatica

    self:jParans["idLojaRetaguarda"] := padR(self:jParans["idLojaRetaguarda"], tamSx3("LG_FILIAL")[1])
    cFilialSlg                       := xFilial("SLG", self:jParans["idLojaRetaguarda"])

    SLG->( dbSetOrder(1) )  //LG_FILIAL, LG_CODIGO, R_E_C_N_O_, D_E_L_E_T_
    While SLG->( dbSeek(cFilialSlg + padR(cCodigo, nTamCod)) )
        cCodigo := Soma1(cCodigo, nTamCod)
    EndDo

    Aadd( aEstacao, {"LG_FILIAL" , cFilialSlg                                                               , Nil} )
    Aadd( aEstacao, {"LG_NOME"	 , subStr("API-" + self:jParans["idDispositivo"], 1, tamSx3("LG_NOME")[1])  , Nil} ) 
    Aadd( aEstacao, {"LG_NOMCOMP", self:jParans["idDispositivo"]                                            , Nil} ) 
    Aadd( aEstacao, {"LG_CODIGO" , cCodigo                                                                  , Nil} ) 

    MSExecAuto({|a,b,c,d| LOJA121(a,b,c,d)}, /*cFolder*/, /*lCallCenter*/, aEstacao, 3)

    If lMsErroAuto
        aErro := GetAutoGRLog()
        If Empty(aErro)
            aErro:= {"Erro no ExecAuto Loja121 - Para inclusao do Dispositivo "+ self:jParans["idDispositivo"] +" -- "}   
        EndIf
        For nCont := 1 To Len(aErro)
            cErro += aErro[nCont] + CRLF
        Next nCont

        self:lSuccess   := .F.
        self:cError     := cErro
    Else
        self:lSuccess   := .T.
        self:cError := ""
        self:cDetail:= ""
        self:jParans["idInquilino"]:= self:Inquilino
        _Super:GET()
    EndIf

Return nil
//-------------------------------------------------------------------
/*/{Protheus.doc} getDadosNota
Busca Ultima nota caso o Divice já existe na base.

@author  Everson S P Junior
@since   12/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
Method getDadosNota() as Variant Class identificadorPdv
Local aArea := GetArea()
Local cTabela  := GetNextAlias()         

cSelect    := "SELECT MAX(SL1.L1_DOC) as NumeroNota,LG_SERIE,L1_ESPECIE FROM " + RetSqlName("SLG") + " SLG"
cSelect    += " INNER JOIN "+RetSqlName("SL1") +" SL1 ON SL1.L1_FILIAL = SLG.LG_FILIAL AND SL1.D_E_L_E_T_ = ' ' "
cSelect    += " AND SL1.L1_ESTACAO  = SLG.LG_CODIGO "
cSelect    += " WHERE SLG.D_E_L_E_T_ = ' '"
cSelect    += " AND SLG.LG_NOMCOMP  = '" + self:jParans["idDispositivo"] + "'"
cSelect    += " AND SLG.LG_FILIAL  = '" + xFilial("SLG", self:jParans["idLojaRetaguarda"]) + "'"
cSelect += " GROUP BY LG_SERIE,L1_ESPECIE "

DbUseArea(.T., "TOPCONN", TcGenQry( , , cSelect), cTabela, .T., .F.)

If !(cTabela)->(Eof())
    self:serieNota      := (cTabela)->LG_SERIE
    self:modeloFiscal   := IIF( Alltrim((cTabela)->L1_ESPECIE) == 'SATCE',1, IIF(Alltrim((cTabela)->L1_ESPECIE) == 'NFCE',2, 3 )) 
    self:numeroNota     := (cTabela)->NumeroNota
EndIf

(cTabela)->( DbCloseArea() )
RestArea(aArea)
Return nil

//-------------------------------------------------------------------
/*/{Protheus.doc} Select
Valida os campos de pesquisa e ordem da query e executa a consulta

@author  Everson S. P. Junior
@since   16/07/2019
@version 1.0
/*/
//-------------------------------------------------------------------
Method Select() as Variant Class identificadorPdv

If !Empty(self:cSelect)
    (self:cAliasQuery)->( DbCloseArea() )
    self:cAliasQuery := GetNextAlias()      
EndIf

self:setSelect("SLG")
self:cSelect    := "SELECT LG_FILIAL,LG_CODIGO,LG_NOMCOMP FROM " + RetSqlName("SLG") + " SLG"
self:cWhere     := " WHERE SLG.D_E_L_E_T_ = ' '"
self:cWhere     += " AND SLG.LG_NOMCOMP  = '" + self:jParans["idDispositivo"] + "'"
self:cWhere     += " AND SLG.LG_FILIAL  = '" + xFilial("SLG", self:jParans["idLojaRetaguarda"]) + "'"
self:cGroupBy   := "GROUP BY LG_FILIAL,LG_CODIGO,LG_NOMCOMP "

_Super:Select()

Return Nil
