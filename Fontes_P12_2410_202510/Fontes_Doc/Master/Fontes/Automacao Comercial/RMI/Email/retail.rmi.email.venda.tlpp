#INCLUDE "TOTVS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "retail.rmi.email.venda.ch"

namespace totvs.protheus.retail.rmi.email

Static cStFonte := procSource()

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe Venda
Responsável pelo envio do e-mail com as vendas processadas pela integração
    
/*/
//-------------------------------------------------------------------
Class Venda from totvs.protheus.retail.rmi.email.controle

    Public Method new()         as Object

    Protected Method processa()         as Variant
    Protected Method corpoEspecifico()  as Variant

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@author  Rafael Tenorio da Costa
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method new(cAssinante as Character, dDataInicio as Date, dDataFim as Date) as Object Class Venda

    _Super:new(cAssinante, "vendas", dDataInicio, dDataFim)

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} processa
Centraliza o processamento dos dados para o e-mail

@author  Rafael Tenorio da Costa
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method processa() as Variant Class Venda

    Local cArquivo      := ""
    Local aCabecalho    := {}
    Local aDados        := {}
    Local aFiliais      := {}
    Local nCont         := 0
    Local cVenda        := padR("VENDA", tamSx3("MIP_CPROCE")[1])

    aAdd(aCabecalho, {STR0001, 1, .F.}) //"Chave da Integração"
    aAdd(aCabecalho, {STR0002, 1, .F.}) //"Emissão"
    aAdd(aCabecalho, {STR0003, 1, .F.}) //"Loja"
    aAdd(aCabecalho, {STR0004, 1, .F.}) //"Documento"
    aAdd(aCabecalho, {STR0005, 1, .F.}) //"Série"
    aAdd(aCabecalho, {STR0006, 3, .T.}) //"Valor Total Retaguarda"
    aAdd(aCabecalho, {STR0007, 3, .T.}) //"Valor Total Pdv"
    aAdd(aCabecalho, {STR0008, 1, .F.}) //"Situação"
    aAdd(aCabecalho, {STR0009, 1, .F.}) //"Mensagem"

    cSql := " SELECT MIP_FILIAL FROM " + retSqlName("MIP")
    cSql += " WHERE D_E_L_E_T_ = ' '"
    cSql +=     " AND MIP_CPROCE = '" + cVenda + "'"
    cSql +=     " AND MIP_DATGER >= '" + dToS(self:dDataInicio) + "'"
    cSql +=     " AND MIP_DATGER <= '" + dToS(self:dDataFim) + "'"
    cSql += " GROUP BY MIP_FILIAL"

    aFiliais := rmiXSql(cSql, "*", /*lCommit*/, /*aReplace*/)

    for nCont:=1 to len(aFiliais)

        cSql := " SELECT MIP_CHVUNI, MIP_ULTOK, F3_FILIAL, F3_NFISCAL, F3_SERIE, 
        cSql += " CASE"
        cSql +=      " WHEN MIP_EVENTO = '1' THEN F3_VALCONT"
        cSql +=      " ELSE 0"
        cSql += " END AS F3_VALCONT,"
        cSql += " CASE"
        cSql +=      " WHEN MIP_EVENTO = '1' THEN MIP_VALCON"
        cSql +=      " ELSE 0"
        cSql += " END AS MIP_VALCON,"
        cSql += " CASE"
        cSql +=      " WHEN MIP_STATUS IN ('2', 'A') AND MIP_EVENTO = '1'"
        cSql +=          " THEN '" + STR0011 + "'"  //"OK (Processado)"
        cSql +=      " WHEN MIP_STATUS IN ('2', 'A')"
        cSql +=          " THEN '" + STR0015 + "'"  //"OK (Cancelado)"
        cSql +=      " WHEN MIP_STATUS = '3'"
        cSql +=          " THEN '" + STR0012 + "'"  //"Erro"
        cSql +=      " ELSE"
        cSql +=          " '" + STR0013 + "'"       //"Pendente processamento integração"
        cSql += " END AS STATUS,"
        cSql += " CASE"
        cSql +=      " WHEN MIP_STATUS NOT IN ('2', 'A')"
        cSql +=          " THEN '" + STR0014 + "'"  //"Acesse a rotina Status da Integração para obter mais detalhes."
        cSql += " END AS MENSAGEM"

        cSql += " FROM " + retSqlName("MIP") + " MIP"
        cSql +=     " LEFT JOIN " + retSqlName("SL1") + " L1"
        cSql +=         " ON MIP_UIDORI <> '" + space( tamSx3("MIP_UIDORI")[1] ) + "' AND LEFT(MIP_UIDORI, " + cValToChar( tamSx3("L1_UMOV")[1] ) + ") = L1_UMOV AND L1.D_E_L_E_T_ = ' '"

        cSql +=     " LEFT JOIN " + retSqlName("SLX") + " LX"
        cSql +=         " ON MIP_UIDORI <> '" + space( tamSx3("MIP_UIDORI")[1] ) + "' AND LEFT(MIP_UIDORI, " + cValToChar( tamSx3("LX_UUID")[1] ) + ") = LX_UUID AND LX.D_E_L_E_T_ = ' '"

        cSql +=     " LEFT JOIN " + retSqlName("SF3") + " F3"
        cSql +=         " ON ("
        cSql +=                 " (L1_FILIAL = F3_FILIAL AND L1_DOC = F3_NFISCAL   AND L1_SERIE = F3_SERIE) OR"
        cSql +=                 " (LX_FILIAL = F3_FILIAL AND LX_CUPOM = F3_NFISCAL AND LX_SERIE = F3_SERIE)"
        cSql +=             ")"
        cSql +=         " AND F3.D_E_L_E_T_ = ' '"

        cSql += " WHERE MIP.D_E_L_E_T_ = ' '"
        cSql +=     " AND MIP_FILIAL = '" + aFiliais[nCont][1] + "'"    
        cSql +=     " AND MIP_CPROCE = '" + cVenda + "'"
        cSql +=     " AND MIP_DATGER >= '" + dToS(self:dDataInicio) + "'"
        cSql +=     " AND MIP_DATGER <= '" + dToS(self:dDataFim) + "'"

        cSql += " ORDER BY MIP_CHVUNI, MIP_ULTOK"

        ljGrvLog(cStFonte, "Query com as vendas:", cSql)

        aDados := rmiXSql(cSql, "*", /*lCommit*/, /*aReplace*/)

        if len(aDados) > 0
            cArquivo := self:cTipo + "_" + allTrim(aFiliais[nCont][1]) + "_" + dToS(self:dDataInicio) + "_" + dToS(self:dDataFim) + ".xlsx"

            aAdd( self:aArquivos, { cArquivo        ,;  //NOMEARQUIVO 
                                    aCabecalho      ,;  //CABECALHO
                                    aClone(aDados)  } ) //DADOS
        endIf

        fwFreeArray(aDados)
    next aFiliais

    fwFreeArray(aFiliais)

Return nil

//-------------------------------------------------------------------
/*/{Protheus.doc} corpoEspecifico
Retorna o corpo do e-mail, pode ser um html

@author  Rafael Tenorio da Costa
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method corpoEspecifico() as Variant Class Venda

    if len(self:aArquivos) > 0
        self:cCorpo += "<p>" + STR0010 + "</p>" //"Por favor, consulte os arquivos anexos para visualizar as vendas de cada loja."
    endIf            

Return nil