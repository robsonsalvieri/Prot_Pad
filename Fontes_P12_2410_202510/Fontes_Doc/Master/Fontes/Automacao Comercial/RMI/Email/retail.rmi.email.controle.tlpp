#INCLUDE "TOTVS.CH"
#INCLUDE "TLPP-CORE.TH"
#INCLUDE "retail.rmi.email.controle.ch"

namespace totvs.protheus.retail.rmi.email

//Definições do array aArquivos
#DEFINE NOMEARQUIVO 1
#DEFINE CABECALHO   2
#DEFINE DADOS       3

Static cStFonte := procSource()

//-------------------------------------------------------------------
/*/{Protheus.doc} Classe Controle
Responsável pelo controle no envio dos e-mails
    
/*/
//-------------------------------------------------------------------
Class Controle

    Protected Data lSucesso as Logical
    Protected Data cErro    as Character

    Protected Data cCorpo   as Character
    Protected Data cAnexo   as Character
    Protected Data cCaminho as Character
    
    Protected Data aCabecalho   as Array
    Protected Data aArquivos    as Array

    Protected Data cAssinante       as Character 
    Protected Data cTipo            as Character 
    Protected Data dDataInicio      as Date
    Protected Data dDataFim         as Date
    Protected Data oConfiguracao    as Object

    Public Method new()          as Object
    Public Method envia()        as Logical
    Public Method getSucesso()   as Logical
    Public Method getErro()      as Character

    Protected Method processa()     as Variant
    Protected Method corpo()        as Character
    Protected Method anexo()        as Character
    
    Private Method destinatarios()      as Character
    Private Method corpoEspecifico()    as Variant

EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método construtor da Classe

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method new(cAssinante, cTipo, dDataInicio, dDataFim) as Object Class Controle

    self:cAssinante     := cAssinante
    self:cTipo          := lower(cTipo)
    self:dDataInicio    := dDataInicio
    self:dDataFim       := dDataFim
    self:oConfiguracao  := nil
    self:lSucesso       := .T.
    self:cErro          := ""
    self:cCorpo         := ""
    self:cAnexo         := ""
    self:cCaminho       := "\autocom\psh\email\" + self:cTipo + "\"
    self:aArquivos      := {}

    //Cria pasta
    if !existDir(self:cCaminho)
        fwMakeDir(self:cCaminho)
    endIf

    MHO->( DbSetOrder(1) )  //MHO_FILIAL + MHO_COD
    if MHO->( DbSeek( xFilial("MHO") + padR(self:cAssinante, tamSx3("MHO_COD")[1]) ) )

        oAux := JsonObject():New()
        oAux:fromJson( allTrim(MHO->MHO_CONFIG) )

        if oAux:hasProperty("email") .and. oAux["email"]:hasProperty(self:cTipo)
            self:oConfiguracao := oAux["email"][self:cTipo]
        endIf
    endIf

    if self:oConfiguracao == nil
        self:lSucesso   := .F.
        self:cErro     := i18n(STR0001, {"https://tdn.totvs.com/pages/viewpage.action?pageId=833937012"} )  //"Configuração para envio de e-mail incorreta, verifique se as configurações estão de acordo com a documentção #1"
        ljxjMsgErr(self:cErro, /*cSolucao*/, /*cRotina*/, {self:cAssinante, self:cTipo, self:dDataInicio, self:dDataFim})
    endIf

    oAux := nil
    fwFreeObj(oAux)

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} envia
Efetua o envio dos e-mail

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method envia() as Logical Class Controle

    Local cAnexo as Character
    Local cCorpo as Character

    self:processa()

    cAnexo := self:anexo()
    cCorpo := self:corpo()
    cDestinarios := self:destinatarios()
  
    if !JurEnvMail( /*cDe*/, cDestinarios, /*cCc*/, /*cCCO*/, STR0002, cAnexo, @cCorpo, /*cServer*/, /*cEmail*/, /*cPass*/, /*lAuth*/, /*cContAuth*/, /*cPswAuth*/, /*lSSL*/, /*lTLS*/)     //"Protheus Smart Hub - Integrações"
        self:lSucesso   := .F.
        self:cErro     := cCorpo
    endIf

    ljGrvLog(cStFonte, "Processamento do e-mail", {STR0002, self:lSucesso, self:cErro, cDestinarios})     //"Protheus Smart Hub - Integrações"

Return self:lSucesso

//-------------------------------------------------------------------
/*/{Protheus.doc} processa
Efetua os processaamentos para gerar a informações para o corpo

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method processa() as Variant Class Controle
    //Implementação esta na classe filha
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} destinatarios
Retorna os destinatários do e-mail

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method destinatarios() as Character Class Controle
Return self:oConfiguracao["destinatarios"]

//-------------------------------------------------------------------
/*/{Protheus.doc} corpo
Retorna o corpo do e-mail, pode ser um html

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method corpo() as Character Class Controle

    self:cCorpo :=  "<!DOCTYPE html>"
    self:cCorpo +=  "<html>"
    self:cCorpo +=  "<head>"
    self:cCorpo +=      "<meta charset='utf-8'>"
    self:cCorpo +=  "</head>"
    self:cCorpo +=      "<body>"
    self:cCorpo +=          "<h2>" + i18n(STR0003, {self:cTipo, dToC(self:dDataInicio), dToC(self:dDataFim)}) + "</h2>"     //"Integrações de #1 - #2 até #3"
    self:cCorpo +=          "<br>"

    if len(self:aArquivos) == 0
        self:cCorpo +=      "<p><b>" + STR0004 + "</b></p>"     //"Não foram encontradas integrações neste período!"
    endIf

    self:corpoEspecifico()
    
    self:cCorpo +=          "<p><b>" + STR0005 + "</b></p>"     //"Para detalhes sobre os serviços de integração acesse a rotina, Status de Integração."

    self:cCorpo +=          "<p>" + STR0006 + "</p>"    //"TOTVS Varejo Protheus Smart Hub"
    
    self:cCorpo +=    "</body>"
    self:cCorpo += "</html>"

Return self:cCorpo

//-------------------------------------------------------------------
/*/{Protheus.doc} corpoEspecifico
Retorna o corpo do e-mail, pode ser um html

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method corpoEspecifico() as Variant Class Controle
    //Implementação esta na classe filha
Return nil

//-------------------------------------------------------------------
/*/{Protheus.doc} anexo
Gera e retorna o caminho dos anexos

@author  Rafael Tenorio da Costa
@version 1.0
/*/
//-------------------------------------------------------------------
Method anexo() as Character Class Controle

    Local nCont     := 0
    Local cArquivo  := ""

    self:cAnexo := ""

    for nCont:=1 to Len(self:aArquivos)
    
        //Gera arquivos
        cArquivo := self:cCaminho + self:aArquivos[nCont][NOMEARQUIVO]
        fErase(cArquivo)
        //if len( self:aArquivos[nCont][DADOS] ) > 0
            rmiGerExcel(self:aArquivos[nCont][DADOS], cArquivo, self:aArquivos[nCont][CABECALHO], 1)
            self:cAnexo += cArquivo + ";"
        //endIf
    
    next nCont

Return self:cAnexo

//-------------------------------------------------------------------
/*/{Protheus.doc} getSucesso
Retorna se o envio de e-mail foi processado corretamente.

@author  Rafael Tenorio da Costa
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method getSucesso() as Logical Class Controle
Return self:lSucesso

//-------------------------------------------------------------------
/*/{Protheus.doc} getErro
Retorna a descrição do erro

@author  Rafael Tenorio da Costa
@version 12.1.2410
/*/
//-------------------------------------------------------------------
Method getErro() as Character Class Controle
Return self:cErro