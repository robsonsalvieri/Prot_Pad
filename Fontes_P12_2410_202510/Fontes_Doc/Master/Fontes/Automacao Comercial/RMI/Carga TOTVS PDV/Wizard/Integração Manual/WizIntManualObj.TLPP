#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "POSCSS.CH"
#include "tlpp-rest.th"
#Include "msobject.ch"
#Include "WizIntManualObj.ch"

Class WizardIntManualPDV

    Private Data nAlturaWiz     as Numeric
    Private Data nLarguraWiz    as Numeric

    Private Data oStepWiz       as Object
    Private Data o1stPage       as Object
    Private Data o2stPage       as Object
    Private Data o3stPage       as Object

    Private Data cStep1         as Character
    Private Data cStep2         as Character
    Private Data cStep3         as Character

    Private Data oFontBold16    as Object
    Private Data oFontBold12    as Object    

    Private Data nNumFilSele    as Numeric
    
    Public Data nRadio          as Numeric
    Public Data aGruposTabelas  as Array
    Public Data aFiliais        as Array
    Public Data aPontoInteg      as Array
    Public Data aParamInteg     as Array
    Public Data aTabelasSX5     as Array
    Public Data aBranchSX5      as Array
    
    Public Data oJReturn       as Object

    Public Method New()                 as Object
    Public Method GeraWizard()          as Variant
    Public Method WizardPge1()          as Variant
    Public Method WizardPge2()          as Variant
    Public Method WizardPge3()          as Variant
    
    Public Method SelectPtc()           as Variant
    Public Method SelectGrupo()         as Variant
    Public Method SelectTabSX5()        as Variant
    Public  Method ValidStep1()         as Logical
    Public  Method ValidStep2()         as Logical    
    Public  Method SetJSONRet()         as Variant
    Public  Method GetJSONRet()         as Object
    Public Method PesquisaSX5()         as Numeric
    Public Method IncluiParam()         as Variant
    Public Method CancWizard()          as Variant    
    Public Method QuantFilSelect()      as Numeric    
    Public Method SelectFil()           as Variant   

    Private Method SetPontoCg()         as Variant
    Private Method SelectFiliaisInteg() as Variant
    Private Method SelectTabsSX5()      as Variant
    Private Method SetaJFiliais()       as Array  
    Private Method SetaJPontInteg()     as Array    
    Private Method SetFiliais()         as Variant    
    Private Method SetGrupoTabelas()    as Array
    Private Method GetTabelasGrupo()    as character    
    Private Method SelectPtCgGrupo()    as Variant
    Private Method InformaParametros()  as Variant    
    Private Method SetTabelasSX5()      as Variant    

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe
@author joao.marcos
@since  11/03/2024
/*/
Method New() as Object Class WizardIntManualPDV

    Self:oFontBold16  := TFont():New(,,-16,.T.,.T.)
    Self:oFontBold12  := TFont():New(,,-12,.T.,.T.)

    Self:nAlturaWiz        := 486
    Self:nLarguraWiz       := 633

    Self:nRadio            := 0
    Self:aGruposTabelas    := {}
    Self:aFiliais          := {}
    Self:aPontoInteg        := {}
    Self:aParamInteg       := {}

    Self:oStepWiz          := Nil
    Self:o1stPage          := Nil
    Self:o2stPage          := Nil
    Self:o3stPage          := Nil

    Self:cStep1             := STR0001 // "Tipo de Integração"
    Self:cStep2             := STR0002 // "Seleção de Filiais"
    Self:cStep3             := ""      //"Pontos de Integraçao e grupos"
    Self:oJReturn           := JsonObject():New()
    Self:aTabelasSX5        := {}    
    Self:aBranchSX5         := {}
    Self:nNumFilSele        := 0

Return Self

/*/{Protheus.doc} GeraWizard
Cria o Wizard
@type  Method
@author joao.marcos
@since 11/03/2024
@version 1.0
/*/
Method GeraWizard() as Variant Class WizardIntManualPDV

    LjGrvLog(" WizardIntManualPDV ", "Methodo GeraWizard | Inicio ", FWTimeStamp(2) )

    Self:oStepWiz:= FWWizardControl():New(,{Self:nAlturaWiz, Self:nLarguraWiz})   
    Self:oStepWiz:ActiveUISteps()

    Self:o1stPage := Self:oStepWiz:AddStep("1STSTEP",{|Panel| Self:WizardPge1(Panel)})
	Self:o1stPage:SetStepDescription(OemToAnsi(Self:cStep1))
	Self:o1stPage:SetNextTitle(OemToAnsi(STR0003)) // 'Avançar'
	Self:o1stPage:SetNextAction({|| Self:ValidStep1() })
	Self:o1stPage:SetCancelAction({|| Self:CancWizard(), .T.})

	Self:o2stPage := Self:oStepWiz:AddStep("2STSTEP",{|Panel| Self:WizardPge2(Panel)})
	Self:o2stPage:SetStepDescription(OemToAnsi(Self:cStep2))
	Self:o2stPage:SetNextTitle(OemToAnsi(STR0003)) // 'Avançar'
	Self:o2stPage:SetNextAction({|| Self:ValidStep2() })
    Self:o2stPage:SetPrevAction({|Panel| .F.})
    Self:o2stPage:SetPrevWhen({|| .F. })
	Self:o2stPage:SetCancelAction({|| Self:CancWizard(), .T.})

    Self:o3stPage := Self:oStepWiz:AddStep("3STSTEP",{|Panel| Self:WizardPge3(Panel)})
	Self:o3stPage:SetStepDescription(OemToAnsi(Self:cStep3))
	Self:o3stPage:SetNextTitle(OemToAnsi(STR0004)) // 'Finalizar'
	Self:o3stPage:SetNextAction({|| Self:SetJSONRet() })
    Self:o3stPage:SetPrevAction({|Panel| .F.})
    Self:o3stPage:SetPrevWhen({|| .F. })
	Self:o3stPage:SetCancelAction({|| Self:CancWizard(), .T.})

    Self:oStepWiz:Activate()
    Self:oStepWiz:Destroy()

    LjGrvLog(" WizardIntManualPDV ", "Methodo GeraWizard | Fim ", FWTimeStamp(2) )

Return
 
/*/{Protheus.doc} WizardPge1
Aba para seleçao de qual tipo de Integraçao deseja fazer
@type  Method
@author joao.marcos
@since  12/03/2024
@version 1.0
/*/
Method WizardPge1(Panel as Object) as Variant Class WizardIntManualPDV
Local aItems    := {STR0005,STR0006,STR0007/*,STR0008'Senhas'*/} as Array // 'Grupos de Tabelas','Parâmetros','Tabelas SX5','Senhas'
Local oRadio    := Nil  as Object
Local nLinha    := 10   as Numeric
Local oSay1     := NIL  as Object
Local oSay2     := NIL  as Object
Local cTexto1   := STR0009 as Character // "O objetivo deste Wizard é a geração manual de Integração de Grupo de Tabelas (integração da tabela completa), atualização de parâmetros e atualização de senhas."
Local cTexto2   := STR0010 as Character // "Selecione abaixo qual tipo de Integração deseja fazer:"

oSay1  := TSay():New(nLinha,Self:nLarguraWiz/4,{||STR0011},Panel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20) // "Bem Vindo!"
nLinha += 20
oSay2  := TSay():New(nLinha,15,{||cTexto1},Panel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20)
nLinha += 25
oSay2  := TSay():New(nLinha,15,{||cTexto2},Panel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20)
nLinha += 15
oRadio := TRadMenu():New (nLinha,15,aItems,,Panel,,,,,,,,100,12,,,,.T.)
oRadio:bSetGet := {|u|Iif (PCount()==0,Self:nRadio,Self:nRadio:=u)}

Return
 
/*/{Protheus.doc} WizardPge2
Metodo responsavel por criar os elementos da pagina 2 no Wizard
@type  Method
@author joao.marcos
@since  12/03/2024
@version 1.0
/*/
Method WizardPge2(Panel as Object) as Variant Class WizardIntManualPDV

LjGrvLog("WizardIntManualPDV", "Method WizardPge2 | Opçao: ", Self:nRadio )

If Self:nRadio == 1 // Integraçao de Tebalas do Grupo
    Self:SelectFiliaisInteg(@Panel)
ElseIf Self:nRadio == 2 // Integraçao de Parâmetros
     Self:SelectFiliaisInteg(@Panel)    
ElseIf Self:nRadio == 3 // Integraçao de SX5
    Self:SelectFiliaisInteg(@Panel)      
/*    
ElseIf Self:nRadio == 4 // Atualização de Senhas
    Self:SelectFiliaisInteg(@Panel)
*/    
EndIf

Return

/*/{Protheus.doc} WizardPge3
Metodo responsavel por criar os elementos da pagina 3 no Wizard
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
/*/
Method WizardPge3(Panel as Object) as Variant Class WizardIntManualPDV
Local nX := 0               as Numeric
Local nContFilSele  := 0    as Numeric
LjGrvLog("WizardIntManualPDV", "Method WizardPge3 | Opçao: ", Self:nRadio )

For nX := 1 To Len(Self:aFiliais)
    If Self:aFiliais[nX][1]
        nContFilSele++
    EndIf    
Next

If nContFilSele > 1
    Self:nNumFilSele := nContFilSele
EndIf

If Self:nRadio == 1 // Integraçao de Tebalas do Grupo
    Self:SelectPtCgGrupo(@Panel)
ElseIf Self:nRadio == 2 // Integraçao de Parâmetros
    Self:InformaParametros(@Panel)   
ElseIf Self:nRadio == 3 // Atualização de SX5
    Self:SelectTabsSX5(@Panel)
EndIf

Return

/*/{Protheus.doc} SelectFiliaisInteg
Apresenta browse para seleçao das Filiais para Integraçao
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
/*/
Method SelectFiliaisInteg(Panel as Object) as Variant Class WizardIntManualPDV
Local oTCBrwse1         := Nil  as Object
Local nX                := 1
Local aBranch1          := {}
Local nLinha            := 05
Local bLDbClick1        := {|| aBranch1[oTCBrwse1:nAt][1] := !aBranch1[oTCBrwse1:nAt][1], Self:aFiliais[oTCBrwse1:nAt][1] := !Self:aFiliais[oTCBrwse1:nAt][1] }
// Browse 01 - Filiais
Local nLinBrow1         := nLinha + 20
Local nLargBrow1        := Self:nLarguraWiz*0.455
Local nAltBrow1         := Self:nAlturaWiz*0.23
Local bSelectFil        := {||Self:SelectFil(@aBranch1), oTCBrwse1:Refresh()}

Self:aGruposTabelas := Self:SetGrupoTabelas()
Self:SetFiliais()

oSay1  := TSay():New(nLinha,15,{||STR0012},Panel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20) // "Selecione as Filiais que deseja fazer a Integração:"

// Seleçao das Filiais
oTCBrwse1 := TCBrowse():New( nLinBrow1,15 , nLargBrow1, nAltBrow1,, {"   x",STR0013, STR0014, STR0015},{20,50,80,30}, Panel,,,,,bLDbClick1,,,,,,/*19 cMsg*/,.F.,,.T.,,/*.F.*/,,/*.F.*/,/*27 .F.*/ ) // "Codigo Filial", "Nome Filial", "Estado"
oTCBrwse1:lVScroll := .T.
oTCBrwse1:lHScroll := .F.

For nX := 1 To Len(Self:aFiliais)
    aAdd(aBranch1, { Self:aFiliais[nX][01],;    //01-Seleçao
                     Self:aFiliais[nX][02],;    //02- Codigo Filial
                     Self:aFiliais[nX][03],;    //03- Nome Filial
                     Self:aFiliais[nX][04] } )  //07- Estado
Next

oTCBrwse1:SetArray( aBranch1 )
oTCBrwse1:bLine := {|| { If( aBranch1[oTCBrwse1:nAt][01], LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" )),; //01 - Seleçao
                             aBranch1[oTCBrwse1:nAt][02],;    //02 - Codigo Filial
                             aBranch1[oTCBrwse1:nAt][03],;    //03 - Nome Filial
                             aBranch1[oTCBrwse1:nAt][04]} }   //04 - Estado

// Marca desmarca todos - Filiais
oTCBrwse1:bHeaderClick := bSelectFil

Return

/*/{Protheus.doc} CancWizard
Seta Json de retorno indicando cancelamento do Wizard
@author joao.marcos
@since 27/03/2024
@version 1.0
/*/
Method CancWizard() Class WizardIntManualPDV
Self:oJReturn['CodigoRetornoWiz']  := "0"

LjGrvLog("WizardIntManualPDV", "Method CancWizard | Operaçao cancelada ", FWTimeStamp(2) )
Return 

/*/{Protheus.doc} SelectPtCgGrupo
Cria browse para seleçao dos Pontos de Integraçao e dos Grupos para Integraçao
@author joao.marcos
@since 21/03/2024
@version 1.0
@param Panel, objeto, panel na tela
/*/
Method SelectPtCgGrupo(Panel as Object) Class WizardIntManualPDV
Local oTCBrwse1     := Nil  as Object
Local oTCBrwse2     := Nil  as Object
Local nX            := 1    as Numeric 
Local aBranch1      := {}   as Array
Local aBranch2      := {}   as Array

Local bLDbClick1    := {|| Iif(Self:QuantFilSelect() > 1,,aBranch1[oTCBrwse1:nAt][1] := !aBranch1[oTCBrwse1:nAt][1])}

Local bLDbClick2    := {|| aBranch2[oTCBrwse2:nAt][1] := !aBranch2[oTCBrwse2:nAt][1], Self:aGruposTabelas[oTCBrwse2:nAt][01] := !Self:aGruposTabelas[oTCBrwse2:nAt][01] }

Local nLinha        := 15                        as Numeric
// Browse 02 - Pontos de Integraçao
Local nLinBrow1     := nLinha                   as Numeric 
Local nLargBrow1    := Self:nLarguraWiz*0.455   as Numeric
Local nAltBrow1     := Self:nAlturaWiz*0.13     as Numeric

// Browse 03 - Grupos de Tabelas
Local nLinBrow2     := nAltBrow1 + 10           as Numeric
Local nLargBrow2    := nLargBrow1               as Numeric
Local nAltBrow2     := nAltBrow1                as Numeric

Local bSelectPC     := {||Self:SelectPtc(@aBranch1), oTCBrwse1:Refresh()} // Açao Marca Desmarca Todos
Local bSelectGrupo  := {||Self:SelectGrupo(@aBranch2), oTCBrwse2:Refresh()} // Açao Marca Desmarca Todos

Local bDbCliqPCOk   := {|| Self:aPontoInteg[oTCBrwse1:nAt][01] := aBranch1[oTCBrwse1:nAt][01], LoadBitmap( GetResources(), "LBOK" ) }
Local bDbCliqPCNOk  := {|| Self:aPontoInteg[oTCBrwse1:nAt][01] := aBranch1[oTCBrwse1:nAt][01], LoadBitmap( GetResources(), "LBNO" ) }

Local oSay1         := Nil                      as Object
Local cTexto        := STR0038                  as Character // "* Mais de uma filial foi selecionada, com isso será gerada integração para todos os Pontos de Integração que pertencem a essas filiais."

Self:SetPontoCg()
Self:SetGrupoTabelas()

// Seleçao de Pontos e Integraçao
oTCBrwse1 := TCBrowse():New( nLinBrow1 , 15, nLargBrow1, nAltBrow1,, {"   x",STR0016,STR0039,STR0032},{15,100,60,50}, Panel,,,,,bLDbClick1,,,,,,/*cMsg*/,.F.,,.T.,,/*.F.*/,,.F.,/*27 .F.*/ ) // "Codigo Ponto de Integraçao", "É Central PDV?", "Filial"
oTCBrwse1:lVScroll := .T.
oTCBrwse1:lHScroll := .F.

For nX := 1 To Len(Self:aPontoInteg)
    aAdd(aBranch1, { Self:aPontoInteg[nX][01],;     // 01 - Seleçao
                     Self:aPontoInteg[nX][02],;     // 02 - Codigo Ponto de Integraçao
                     Self:aPontoInteg[nX][03],;     // 03 - Filial
                     Self:aPontoInteg[nX][04]} )    // 04 - É Central PDV?
Next

oTCBrwse1:SetArray( aBranch1)
oTCBrwse1:bLine := {|| { If( aBranch1[oTCBrwse1:nAt][01], Eval(bDbCliqPCOk), Eval(bDbCliqPCNOk) ),; // 01 - Seleçao
                            aBranch1[oTCBrwse1:nAt][02],;   // 02 - Codigo Ponto de Integraçao
                            aBranch1[oTCBrwse1:nAt][03],;   // 03 - Filial
                            aBranch1[oTCBrwse1:nAt][04]} }  // 04 - É Central PDV?

// Marca desmarca todos - Pontos de Integraçao
oTCBrwse1:bHeaderClick := bSelectPC

// Seleçao dos Grupos
oTCBrwse2 := TCBrowse():New( nLinBrow2 , 15, nLargBrow2, nAltBrow2,, {"   x",STR0017, STR0018, STR0019},{20,50,50,100}, Panel,,,,,bLDbClick2,,,,,,/*cMsg*/,.F.,,.T.,,/*.F.*/,,.F., /*.F.*/ ) // "Codigo Grupo", "Nome Grupo", "Tabelas"
oTCBrwse2:lVScroll := .T.
oTCBrwse2:lHScroll := .F.

For nX := 1 To Len(Self:aGruposTabelas)        
    aAdd(aBranch2, { Self:aGruposTabelas[nX][01],;   //01-Seleçao
                     Self:aGruposTabelas[nX][02],;   //02- Codigo Grupo
                     Self:aGruposTabelas[nX][03],;   //03- Nome Grupo
                     Self:aGruposTabelas[nX][04] } ) //07- Tabelas
Next

oTCBrwse2:SetArray( aBranch2 )
oTCBrwse2:bLine := {|| { If( aBranch2[oTCBrwse2:nAt][01], LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" )),; //01 - Seleçao
                             aBranch2[oTCBrwse2:nAt][02],;  //02 - Codigo Grupo
                             aBranch2[oTCBrwse2:nAt][03],;  //03 - Nome Grupo
                             aBranch2[oTCBrwse2:nAt][04]} } //04 - Tabelas

// Marca desmarca todos - Grupos de Tabelas
oTCBrwse2:bHeaderClick := bSelectGrupo

If Self:nNumFilSele > 1
    oSay1  := TSay():New(nLinBrow2+nAltBrow2-3,15,{||cTexto},Panel,,/*Self:oFontBold12*/,,,,.T.,CLR_BLACK,CLR_WHITE,280,20)
EndIf

Return

/*/{Protheus.doc} InformaParametros
Apresenta campos para informar os parametros que sera feito a Integraçao
@author joao.marcos
@since 27/03/2024
@version 1.0
@param Panel, objeto, 
/*/
Method InformaParametros(Panel as Object) Class WizardIntManualPDV
Local nLinha        := 15                                as Numeric
// Inclusao de parametros
Local oSay2         := Nil                              as Object
Local cTexto2       := STR0020                          as Character // "Inclua os parâmetros que deseja gerar a Integração:"
Local oTGet1        := Nil                              as Object
Local cTGet1        := "MV_       "                     as Character
Local oTMultiget1   := Nil                              as Object
Local nLinSay1      := nLinha                           as Numeric
Local nLargMGet1    := Self:nLarguraWiz * 0.455         as Numeric
Local nAltMGet1     := Self:nAlturaWiz * 0.15           as Numeric
Local cParamentros  := Space(250)                       as Character
Local oButtonInc    := Nil                              as Object
Local oButtonLimpa  := Nil                              as Object

// Inclusao de Parametros
oSay2  := TSay():New(nLinSay1,15,{||cTexto2},Panel,,Self:oFontBold12,,,,.T.,CLR_BLACK,CLR_WHITE,200,20)

oTGet1      := TGet():New( nLinSay1+10,15,{|u| If(PCount()==0,cTGet1, cTGet1:=u) },Panel,80,10,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cTGet1,,,, )
oButtonInc  := TButton():New( nLinSay1+10, 100 , STR0021,Panel,{|| Self:IncluiParam( cTGet1 , @cParamentros), cTGet1 := "MV_       " , Panel:Refresh()}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // "Incluir"

oButtonLimpa    := TButton():New( nLinSay1+10, 150 , STR0022,Panel,{|| cTGet1 := "MV_       ", cParamentros := Space(256), Self:aParamInteg := {} , Panel:Refresh()}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // "Limpar"
oTMultiget1     := tMultiget():New(nLinSay1+30,15,{| u | If( pCount() > 0, cParamentros := u, cParamentros )},Panel,nLargMGet1,nAltMGet1,,,,,,.T.,,,{|| .F.})

Return

/*/{Protheus.doc} IncluiParam
Apresnta campos para informar mos parametros que sera feito a Integração
@author joao.marcos
@since 27/03/2024
@version 1.0
@param cTGet1       , character, Parametro digitado
        cParamentros, Character, Parametros já digitados
/*/
Method IncluiParam( cTGet1 as Character, cParamentros as Character) Class WizardIntManualPDV

If ExisteSx6(AllTrim(cTGet1)) .AND. !Empty(AllTrim(cTGet1))
    If Empty(cParamentros)
        cParamentros := AllTrim(cParamentros) + AllTrim(cTGet1)
    Else
        cParamentros := AllTrim(cParamentros) + " | " + AllTrim(cTGet1)
    EndIf

    AADD(Self:aParamInteg,AllTrim(cTGet1))
Else
    MSGInfo( STR0023 + AllTrim(cTGet1) + STR0024 ) // "O parâmetro " # " não existe no dicionário de dados."
EndIf

Return

/*/{Protheus.doc} SelectFil
Marca e desmarca todas as Filiais
@type  Method
@author joao.macos
@since 12/03/2024
@version 1.0
@param aBranch1, array, Filiais do Grupo
/*/
Method SelectFil(aBranch1 as array) as Variant Class WizardIntManualPDV
Local nX:= 0 as Numeric

For nX := 1 To Len(Self:aFiliais)
    Self:aFiliais[nX][1] := !Self:aFiliais[nX][1]
    aBranch1[nX][1] :=  !aBranch1[nX][1]
Next

Return

/*/{Protheus.doc} SelectPtc
Marca e desmarca todos Pontos de Integraçao
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@param aBranch1, array, Pontos de Integraçao da FIlial
/*/
Method SelectPtc( aBranch1 as array ) as Variant Class WizardIntManualPDV
Local nX:= 0 as Numeric

For nX := 1 To Len(Self:aPontoInteg)
    Self:aPontoInteg[nX][1] := !Self:aPontoInteg[nX][1]
    aBranch1[nX][1] := !aBranch1[nX][1]
Next

Return 

/*/{Protheus.doc} SelectGrupo
Marca e desmarca todos Grupos de Tabelas
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@param aBranch2, array, Pontos de Integraçao da FIlial
/*/
Method SelectGrupo( aBranch2 as array ) as Variant Class WizardIntManualPDV
Local nX:= 0 as Numeric

For nX := 1 To Len(Self:aGruposTabelas)
    Self:aGruposTabelas[nX][1] := !Self:aGruposTabelas[nX][1]
    aBranch2[nX][1] := !aBranch2[nX][1]
Next

Return 

/*/{Protheus.doc} SetFiliais
Popula array para seleçao de Filiais
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
/*/
Method SetFiliais() as Variant Class WizardIntManualPDV
Local cCompany      := FWGrpCompany()   as Character
Local aTodasFiliais := FWLoadSM0()      as Array
Local nX            := 0                as Numeric
Local cQueryFil     := ""               as Character
Local cAliasFil     := ""               as Character

cQueryFil += " SELECT DISTINCT MIQ_FILPC "
cQueryFil += " FROM " + RetSqlName("MIQ") + " "
cQueryFil += " WHERE MIQ_EMPPC = '" + cCompany + "'
cQueryFil += " AND D_E_L_E_T_ = ' '

cQueryFil := ChangeQuery(cQueryFil)
cAliasFil := MPSysOpenQuery(cQueryFil)

(cAliasFil)->(dbGoTop())

While (cAliasFil)->(!Eof())

    For nX := 1 To Len(aTodasFiliais)
        If aTodasFiliais[nX][1] == cCompany .AND. Upper( AllTrim(aTodasFiliais[nX][2]) ) == Upper( AllTrim((cAliasFil)->MIQ_FILPC) )
            AADD(Self:aFiliais,{.F.,aTodasFiliais[nX][2],aTodasFiliais[nX][7],aTodasFiliais[nX][4]})
            Exit
        EndIf
    Next

    (cAliasFil)->(dbSkip())

EndDo

(cAliasFil)->(dbCloseArea())

Return

/*/{Protheus.doc} SetPontoCg
Seta os Pontos de Integraçao das Filiais Selecionadas
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
/*/
Method SetPontoCg() as Variant Class WizardIntManualPDV
Local cCompany      := FWGrpCompany()       as Character
Local nX            := 0                    as Numeric
Local aAreaMIQ      := MIQ->(GetArea())     as Array
Local cQryPDV       := ""                   as Character
Local oQryPDV       := Nil                  as Object
Local aAliasPDV     := {}                   as Array
Local nContParam    := 0                    as Numeric 
Local cFilPontoInteg    := ""               as Character
Local cCentralPDV   := ""                   as Character

LjGrvLog("WizardIntManualPDV", "Method SetPontoCg | Inicio ", FWTimeStamp(2) )

Self:aPontoInteg := {}

For nX := 1 To Len(Self:aFiliais)
    If Self:aFiliais[nX][01]
        cFilPontoInteg += "'" + Self:aFiliais[nX][2] + "',"
    EndIf
Next

cFilPontoInteg := SubStr(cFilPontoInteg,1,Len(cFilPontoInteg)-1)

cQryPDV += " SELECT MIQ_COD, MIQ_CENTRA, MIQ_FILPC "
cQryPDV += " FROM " +  RetSQLName("MIQ") + " "
cQryPDV += " WHERE MIQ_FILIAL = ? "     // #1
cQryPDV += " AND MIQ_EMPPC = ? "        // #2
cQryPDV += " AND MIQ_FILPC IN(" + cFilPontoInteg + ") "
cQryPDV += " AND MIQ_ATIVO = '1' "
cQryPDV += " AND D_E_L_E_T_ = ' ' "

cQryPDV := ChangeQuery(cQryPDV)
oQryPDV := FwExecStatement():New(cQryPDV)

nContParam++
oQryPDV:SetString(nContParam++, xFilial( "MIQ" ) )  // #1
oQryPDV:SetString(nContParam++, cCompany)           // #2

LjGrvLog(" WizardIntManualPDV ", "Methodo SetPontoCg - Query: ", cQryPDV )
LjGrvLog(" WizardIntManualPDV ", "Methodo SetPontoCg - MIQ_EMPPC: ", cCompany )

aAliasPDV := oQryPDV:OpenAlias()

(aAliasPDV)->(dbGoTop())

If (aAliasPDV)->(EOF())
     AADD(Self:aPontoInteg,{.F.,""})
Else
    While (aAliasPDV)->(!EOF())
        cCentralPDV := Iif((aAliasPDV)->MIQ_CENTRA=="1","Sim","Não")
        IF Self:QuantFilSelect() > 1
            AADD(Self:aPontoInteg,{.T.,(aAliasPDV)->MIQ_COD,(aAliasPDV)->MIQ_FILPC,cCentralPDV})
        Else
            AADD(Self:aPontoInteg,{.F.,(aAliasPDV)->MIQ_COD,(aAliasPDV)->MIQ_FILPC,cCentralPDV})
        EndIf
        (aAliasPDV)->(dbSkip())
    EndDo
EndIf

RestArea(aAreaMIQ)
(aAliasPDV)->(dbCloseArea())

LjGrvLog("WizardIntManualPDV", "Method SetPontoCg | Fim ", FWTimeStamp(2) )

Return

/*/{Protheus.doc} QuantFilSelect
Retorna aquantidade de Filais que foram selecionadas
@type  Method
@author joao.marcos
@since 18/03/2024
@version 1.0
@return nContFil, numeric, numero de filiais selecionadas
/*/
Method QuantFilSelect() as Numeric Class WizardIntManualPDV
Local nX    := 0 as Numeric
Local nContFil  := 0 as Numeric
    
For nX := 1 To Len(Self:aFiliais)
    If Self:aFiliais[nX][1]
        nContFil++
    EndIf
Next

Return nContFil

/*/{Protheus.doc} SetGrupoTabelas
Popula o array aGrupos, com os grupos de tabelas
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@return aGrupos, array, grupos de tabelas existentes
/*/
Method SetGrupoTabelas() as Array Class WizardIntManualPDV
Local aGrupos := {}
Local aAreaMIR := MIR->(GetArea())

MIR->(dbSetOrder(1))
MIR->(dbGoTop())

While MIR->(!EOF())

    If MIR->MIR_ATIVO == "1" .AND. MIR->MIR_COD <> "999"
        AADD(aGrupos,{.F.,MIR->MIR_COD,MIR->MIR_DESCRI,Self:GetTabelasGrupo(MIR->MIR_COD)})
    EndIf

    MIR->(dbSkip())
EndDO

RestArea(aAreaMIR)

Self:aGruposTabelas := aGrupos

Return aGrupos

/*/{Protheus.doc} GetTabelasGrupo
Retorna uma string com as tabelas que pertencem ao grupo de tabelas (MHN)
@type  Method
@author joao.marcos
@since 12/03/2024
@version 1.0
@param cCodGrupo, caracter, codigo do grupo de tabelas
@return cTabelas, caracter, relaçao de tabelas que pertencem ao grupo
/*/
Method GetTabelasGrupo(cCodGrupo as character) as Character Class WizardIntManualPDV
Local cTabelas := "" as Character
Local cQryMHN   := "" as Character
Local cAliasMHN := "" as Character

cQryMHN += " SELECT MHN_CODGRP, MHN_TABELA "
cQryMHN += " FROM " + RetSqlName("MHN") + " "
cQryMHN += " WHERE MHN_FILIAL = '" + xFilial("MHN") + "' "
cQryMHN += " AND MHN_CODGRP = '" + cCodGrupo + "'" 
cQryMHN += " AND D_E_L_E_T_ = ' ' "

cQryMHN := ChangeQuery(cQryMHN)
cAliasMHN := MPSysOpenQuery(cQryMHN)

(cAliasMHN)->(dbGoTop())

While (cAliasMHN)->(!EOF())
    cTabelas += AllTrim((cAliasMHN)->MHN_TABELA) + " | "
    (cAliasMHN)->(dbSkip())
EndDo

cTabelas := SubStr(cTabelas,1,Len(cTabelas)-3)

(cAliasMHN)->(dbCloseArea())

Return cTabelas

/*/{Protheus.doc} ValidStep1
Valida se o usuario selecionou alguma opçao ao avançar apara a pagina 2
@type  Method
@author joao.marcos
@since 14/03/2024
@version 1.0
@return lRet, logico, retorno do valid
/*/
Method ValidStep1() as Logical Class WizardIntManualPDV
Local lRet := .T. as Logical

If Self:nRadio <= 0
    MsgAlert( STR0025 ) // "Selecione uma das opções."
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} ValidStep2
Valida se o usuario selecionou alguma opçao ao avançar para o pagina 3
@type  Method
@author joao.marcos
@since 14/03/2024
@version 1.0
@return lRet, logico, retorno do valid
/*/
Method ValidStep2() as Logical Class WizardIntManualPDV
Local lRet := .T. as Logical

If Self:QuantFilSelect() <= 0 //Self:nRadio == 1 .OR. Self:nRadio == 2 .OR. Self:nRadio == 3    
    MsgAlert( STR0026 ) // "Selecione ao menos uma Filial para Integração."
    lRet := .F.
EndIf

Return lRet

/*/{Protheus.doc} SelectTabsSX5
Apresenta Browse para seleçao da tabela da SX5
@type  Method
@author joao.marcos
@since 26/03/2024
@version 1.0
@param Panel, Object, Panel utilizado
/*/
Method SelectTabsSX5(Panel as Object) Class WizardIntManualPDV
Local oTCBrwse1     := Nil  as Object
Local oTGet1        := Nil  as Object
Local cTGet1        := Space(150)   as Character
Local oTButtonPesq  := Nil  as Object
Local nX            := 1    as Numeric 
Local bLDbClick1    := {|| Self:aBranchSX5[oTCBrwse1:nAt][1] := !Self:aBranchSX5[oTCBrwse1:nAt][1] }
Local nLinha        := 15                         as Numeric
// Browse
Local nLinBrow1     := nLinha + 15                as Numeric 
Local nLargBrow1    := Self:nLarguraWiz * 0.455   as Numeric
Local nAltBrow1     := Self:nAlturaWiz * 0.23     as Numeric

Local cPlaceHold    := STR0027                    as Character // "Pesquise pela Chave ou parte da descrição da tabela."
Local bHelpGet1     := {||ShowHelpCpo(STR0028, {cPlaceHold}, 2, /*{||}*/, 2)} // "Pesquisa SX5"

Local bSelectTabSX5 := {|| Self:SelectTabSX5(), oTCBrwse1:Refresh() }

Self:SetTabelasSX5()

oTGet1 := TGet():New( nLinha,15,{|u| If(PCount()==0,cTGet1, cTGet1:=u) },Panel,150,10,"@!",,0,,,.F.,,.T.,,.F.,,.F.,.F.,/*20*/,.F.,.F.,,cTGet1,,,,,,/*30*/,,,,,cPlaceHold )
oTGet1:bHelp := bHelpGet1
oTGet1:SetFocus()
oTButtonPesq := TButton():New( nLinha, 170 , STR0029,Panel,{|| oTCBrwse1:GoPosition( Self:PesquisaSX5(cTGet1) ) , oTCBrwse1:Refresh()}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) //"Pesquisar"

// Seleçao de Tabes SX5
oTCBrwse1 := TCBrowse():New( nLinBrow1 , 15, nLargBrow1, nAltBrow1,, {"   x",STR0030,STR0031},{20,40,50}, Panel,,,,,bLDbClick1,,,,,,/*cMsg*/,.F.,,.T.,,/*.F.*/,,.F.,/*27 .F.*/ ) // "Chave","Descrição"
oTCBrwse1:lVScroll := .T.
oTCBrwse1:lHScroll := .F.

For nX := 1 To Len(Self:aTabelasSX5)
    aAdd(Self:aBranchSX5, { Self:aTabelasSX5[nX][01],;     //01- Seleçao
                     Self:aTabelasSX5[nX][02],;     //02- Chave
                     Self:aTabelasSX5[nX][03]} )    //03- Descriçao
Next

oTCBrwse1:SetArray( Self:aBranchSX5)
oTCBrwse1:bLine := {|| { If( Self:aBranchSX5[oTCBrwse1:nAt][01], LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" )),; //01 - Seleçao
                            Self:aBranchSX5[oTCBrwse1:nAt][02],;   //02 - Chave
                            Self:aBranchSX5[oTCBrwse1:nAt][03]}}  // 03- Descriçao

// Marca desmarca todas tabelas SX5
oTCBrwse1:bHeaderClick := bSelectTabSX5

Return

/*/{Protheus.doc} SetTabelasSX5
Popula Browse para seleçao da tabela da SX5
@type  Method
@author joao.marcos
@since 26/03/2024
@version 1.0
@param Panel, Object, Panel utilizado
/*/
Method SetTabelasSX5() Class WizardIntManualPDV
Local cQuerySX5 := "" as Character
Local cAliasSX5 := "" as Character

cQuerySX5 += " SELECT X5_CHAVE, X5_DESCRI, X5_CHAVE + X5_DESCRI AS SX5PESQ"
cQuerySX5 += " FROM " + RetSQLName("SX5") + " "
cQuerySX5 += " WHERE X5_FILIAL = '" + xFilial("SX5") + "' "
cQuerySX5 += " AND X5_TABELA = '00' "
cQuerySX5 += " AND D_E_L_E_T_ = ' ' "
cQuerySX5 += " ORDER BY X5_CHAVE "

cQuerySX5 := ChangeQuery(cQuerySX5)
cAliasSX5 := MPSysOpenQuery(cQuerySX5)

(cAliasSX5)->(dbGoTop())

While (cAliasSX5)->(!EOF())
    AADD(Self:aTabelasSX5,{.F., (cAliasSX5)->X5_CHAVE, (cAliasSX5)->X5_DESCRI, (cAliasSX5)->SX5PESQ})
    (cAliasSX5)->(dbSkip())
EndDo

(cAliasSX5)->(dbCloseArea())

Return

/*/{Protheus.doc} PesquisaSX5
Apresenta Browse para seleçao da tabela da SX5
@type  Method
@author joao.marcos
@since 26/03/2024
@version 1.0
@param Panel, Object, Panel utilizado
/*/
Method PesquisaSX5(cPesqDigit) Class WizardIntManualPDV
Local nRet  := 0 as Numeric
Local nX    := 0 as Numeric

For nX := 1 To Len(Self:aTabelasSX5)
    If AllTrim(cPesqDigit) $ AllTrim(Self:aTabelasSX5[nX][4])
        nRet := nX        
        Exit
    EndIf
 Next

Return nRet

/*/{Protheus.doc} SelectTabSX5
Marca Desmarca todas tabelas da SX5
@type  Method
@author joao.marcos
@since 26/03/2024
@version 1.0
/*/
Method SelectTabSX5() Class WizardIntManualPDV
Local nX:= 0 as Numeric

For nX := 1 To Len(Self:aTabelasSX5)
    Self:aTabelasSX5[nX][1] := !Self:aTabelasSX5[nX][1]
    Self:aBranchSX5[nX][1] := !Self:aBranchSX5[nX][1]
    If( Self:aBranchSX5[nX][01], LoadBitmap( GetResources(), "LBOK" ), LoadBitmap( GetResources(), "LBNO" ))
Next

Return

/*/{Protheus.doc} SetJSONRet
Formata o Json de retorno
@type  Method
@author joao.marcos
@since 14/03/2024
@version 1.0
/*/
Method SetJSONRet() as Variant Class WizardIntManualPDV
Local lRet          := .T.  as Logical
Local aJGrupoTabela := {}   as Array
Local aJParamInteg  := {}   as Array
Local aJTabelasSX5  := {}   as Array
Local nX            := 0    as Numeric
Local nY            := 1    as Numeric

LjGrvLog("WizardIntManualPDV", "Method SetJSONRet | Inicio ", FWTimeStamp(2) )

If Self:nRadio == 1 // Integração de Grupos de Tabelas

        // Filiais selecionadas   
    Self:oJReturn['Filiais']    := Self:SetaJFiliais() //aJFiliais

    // Pontos de Integraçao selecionados
    Self:oJReturn['PontoInteg'] := Self:SetaJPontInteg() //aJPontoInteg

    // Grupos de Tabelas selecionados:
    For nX := 1 To Len(Self:aGruposTabelas)
        If Self:aGruposTabelas[nX][1]
            Aadd( aJGrupoTabela, JsonObject():New() )
            aJGrupoTabela[nY] := Self:aGruposTabelas[nX][02]
            nY++
        EndIf
    Next

    If Len(Self:oJReturn['PontoInteg']) < 1 .AND. Empty(aJGrupoTabela)
        Self:oJReturn['CodigoRetornoWiz'] := "0"
        MSGInfo(STR0033) // "Selecione ao menos um Ponto de Integração e um Grupo de Tabelas."
        lRet := .F.
    ElseIf Len(Self:oJReturn['PontoInteg']) < 1
        Self:oJReturn['CodigoRetornoWiz'] := "0"
        MSGInfo(STR0034) // "Selecione ao menos um Ponto de Integração."
        lRet := .F.
    ElseIf Empty(aJGrupoTabela)
        Self:oJReturn['CodigoRetornoWiz'] := "0"
        MSGInfo(STR0035) // "Selecione ao menos um um Grupo de Tabelas."
        lRet := .F.
    Else
        Self:oJReturn['CodigoRetornoWiz']  := AllTrim(STR(Self:nRadio))
        Self:oJReturn['GrupoTabelas']   :=  aJGrupoTabela
    EndIf    

ElseIf Self:nRadio == 2 // Integração Parametros
    
    // Filiais selecionadas   
    Self:oJReturn['Filiais']    := Self:SetaJFiliais()

    // Parametros para Integração
    nY := 1
    For nX := 1 To Len(Self:aParamInteg)
        If !Empty(Self:aParamInteg[nX])
            Aadd( aJParamInteg, JsonObject():New() )
            aJParamInteg[nY] := Self:aParamInteg[nX]
            nY++
        EndIf
    Next

    If Len(aJParamInteg) < 1
        Self:oJReturn['CodigoRetornoWiz']  := "0"
        MsgInfo(STR0036) // "Informe ao menos um parâmetro."
        lRet := .F.
    Else
        Self:oJReturn['CodigoRetornoWiz']  := "2"
        Self:oJReturn['Parametros'] := aJParamInteg
    EndIf    

ElseIf Self:nRadio == 3 // Integração SX5
    
    // Filiais selecionadas   
    Self:oJReturn['Filiais']    := Self:SetaJFiliais()

    // Tabelas da SX5 para Integração
    nY := 1
    For nX := 1 To Len(Self:aBranchSX5) // Self:aTabelasSX5
        If Self:aBranchSX5[nX][1]
            Aadd( aJTabelasSX5, JsonObject():New() )
            aJTabelasSX5[nY] := AllTrim(Self:aBranchSX5[nX][02])
            nY++
        EndIf
    Next

    If Len(aJTabelasSX5) < 1
        Self:oJReturn['CodigoRetornoWiz']  := "0"
        MsgInfo(STR0037) // "Selecione ao menos uma tabela."
        lRet := .F.
    Else
        Self:oJReturn['CodigoRetornoWiz']  := "3"
        Self:oJReturn['TabelasSX5'] := aJTabelasSX5  
    EndIf
    
/*
ElseIf Self:nRadio == 4 // Atualizaçao de Senhas
    Self:oJReturn['CodigoRetornoWiz']  := "4"

    // Filiais selecionadas   
    Self:oJReturn['Filiais']        := Self:SetaJFiliais()
*/    
EndIf

LjGrvLog("WizardIntManualPDV", "Method SetJSONRet | Josn de Retorno: ", Self:oJReturn )

LjGrvLog("WizardIntManualPDV", "Method SetJSONRet | Fim ", FWTimeStamp(2) )

Return lRet

/*/{Protheus.doc} SetaJFiliais
Grava as Filiais selecionadas no array aFiliais
@author joao.marcos
@since 28/03/2024
@version 1.0
@return aJFiliais, array, array com as filais selecionadas
/*/
Method SetaJFiliais() Class WizardIntManualPDV
Local aJFiliais     := {}   as Array
Local nX            := 0    as Numeric
Local nY            := 1    as Numeric

 For nX := 1 To Len(Self:aFiliais)
    If Self:aFiliais[nX][1]
        Aadd( aJFiliais, JsonObject():New() )
        aJFiliais[nY] := Self:aFiliais[nX][02] // ['Filial']
        nY++
    EndIf
Next

Return aJFiliais

/*/{Protheus.doc} SetaJPontInteg
Grava os pontos de Integraçao selecionados no array aJPontoInteg
@author joao.marcos
@since 28/03/2024
@version 1.0
@return aJPontoInteg, array, Pontos de Integraçao selecionados
/*/
Method SetaJPontInteg() Class WizardIntManualPDV
Local aJPontoInteg  := {}   as Array
Local nX            := 0    as Numeric

Local nY            := 1    as Numeric

For nX := 1 To Len(Self:aPontoInteg)
    If Self:aPontoInteg[nX][1]
        Aadd( aJPontoInteg, JsonObject():New() )
        aJPontoInteg[nY] := Self:aPontoInteg[nX][02]
        nY++
    EndIf
Next   
    
Return aJPontoInteg

/*/{Protheus.doc} GetJSONRet
Retorna o JSON oJReturn
@type  Method
@author joao.marcos
@since 14/03/2024
@version 1.0
@return oJReturn, JSON, JSON de retorno do Wizard
/*/
Method GetJSONRet() as Object Class WizardIntManualPDV
Return Self:oJReturn

