#INCLUDE "PROTHEUS.CH"
#INCLUDE "tlpp-core.th"
#INCLUDE "POSCSS.CH"
#INCLUDE "RMIIntManualPDV.CH"

Static cAssinante  := "TOTVS PDV"

/*/{Protheus.doc} RMIIntManualPDV
Chama tela de Wizar da Integraçao Manual
@type  Function
@author joao.marcos
@since 11/03/2024
@version 1.0
/*/
Function RMIIntManualPDV()
Local oWizard           := WizardIntManualPDV():New()   as Object
Local oJRetWiz          := NIL                          as Object

LjGrvLog("RMIIntManualPDV", " Inicio ", FWTimeStamp(2) )

// Apresenta o Wizard para definiçao da Integração
oWizard:GeraWizard()

// Recebe qual Integração será efetuada
oJRetWiz := oWizard:GetJSONRet()

If oJRetWiz['CodigoRetornoWiz'] <> "0" .AND. oJRetWiz['CodigoRetornoWiz'] <> NIL
    MSGInfo(STR0003) // "A geração dos dados de Integração foi iniciada."
    //ProcIntegManual("T1", "D RJ 02", oJRetWiz) // Debug
    StartJob("ProcIntegManual", GetEnvServer(), .F./*lEspera*/,cEmpAnt, cFilAnt, oJRetWiz )
EndIf

LjGrvLog(" RMIIntManualPDV ", " Fim ", FWTimeStamp(2) )

Return

/*/{Protheus.doc} ProcIntegManual
Processa a Integração manual
@type  Static Function
@author joao.marcos
@since 22/03/2024
@version version
@param oJRetWiz, Objeto, Retorno do Wizard
/*/
Function ProcIntegManual(cEmp, cFil, oJRetWiz)
Local oGeraIntegManual  := JsonObject():New()  as Object
Local nX                := 0    as Numeric
Local aProcessos        := {}   as Array

RPCSetType(3)
RpcSetEnv( cEmp, cFil,,,'LOJA')

LjGrvLog(" ProcIntegManual ", " Inicio ", FWTimeStamp(2) )
LjGrvLog(" ProcIntegManual ", " Dados do Wizard ", oJRetWiz)

If oJRetWiz['CodigoRetornoWiz'] == "1" // Integração de Grupo de Tabelas

    LjGrvLog(" ProcIntegManual ", " Inicio Integração de Tabelas", FWTimeStamp(2) )

    For nX := 1 To Len(oJRetWiz['GrupoTabelas'])
        aProcessos := GetProcessos(oJRetWiz['GrupoTabelas'][nX])
        // Abre uma thread para cada grupo
        ProcIntegGrpTab(cEmpAnt, cFilAnt, cAssinante, oJRetWiz['GrupoTabelas'][nX], aProcessos, oJRetWiz) // Debug
        //StartJob("ProcIntegGrpTab", GetEnvServer(), .F./*lEspera*/,cEmpAnt, cFilAnt, cAssinante, oJRetWiz['GrupoTabelas'][nX], aProcessos[nY][1], oJRetWiz )
    Next    

    LjGrvLog(" ProcIntegManual ", " Fim Integração de Tabelas", FWTimeStamp(2) )

ElseIf oJRetWiz['CodigoRetornoWiz'] == "2" // Integração de Parametros

    LjGrvLog(" ProcIntegManual ", " Inicio Integração de Parametros", FWTimeStamp(2) )

    oGeraIntegManual := GeraIntegManualPDV():New(cAssinante, "999SX6", oJRetWiz)
    oGeraIntegManual:IntegraParam()

    LjGrvLog(" ProcIntegManual ", " Fim Integração de Parametros", FWTimeStamp(2) )

ElseIf oJRetWiz['CodigoRetornoWiz'] == "3" // Integração SX5

    LjGrvLog(" ProcIntegManual ", " Inicio Integração SX5", FWTimeStamp(2) )   

    oGeraIntegManual := GeraIntegManualPDV():New(cAssinante, "999SX5", oJRetWiz)
    oGeraIntegManual:IntegraSX5()

    LjGrvLog(" ProcIntegManual ", " Fim Integração SX5", FWTimeStamp(2) )  
EndIf

FwFreeArray(aProcessos)
FwFreeObj(oGeraIntegManual)
oGeraIntegManual:= Nil
RpcClearEnv()

LjGrvLog(" ProcIntegManual ", " Fim ", FWTimeStamp(2) )
    
Return

/*/{Protheus.doc} GetProcessos
Retorna os processos do grupo de tabelas
@type  Static Function
@author joao.marcos
@since 26/03/2024
@version 1.0
@param cCodGrpTabela, character, codigo do grupo de tabelas
@return aRet, array, lista de tabelas do grupo
/*/
Static Function GetProcessos(cCodGrpTabela)
Local aRet      := {}   as Array
Local cQuery    := ""   as Character
Local cAlias    := ""   as Character

cQuery += " SELECT MHN.MHN_COD 
cQuery += " FROM " + RetSqlName("MHN") + " MHN"
cQuery += " INNER JOIN " + RetSqlName("MHP") + " MHP "
cQuery += " ON MHP.MHP_FILIAL = MHN.MHN_FILIAL
cQuery += " AND MHP.MHP_CPROCE = MHN.MHN_COD
cQuery += " WHERE MHN.MHN_FILIAL = '" + xFilial("MHN") + "'
cQuery += " AND MHN.MHN_CODGRP = '" + AllTrim(cCodGrpTabela) + "'
cQuery += " AND MHP.MHP_CASSIN = '" + cAssinante + "'
cQuery += " AND MHP.MHP_ATIVO = '1'
cQuery += " AND MHN.D_E_L_E_T_ = ' '
cQuery += " AND MHP.D_E_L_E_T_ = ' '

cQuery := ChangeQuery(cQuery)
cAlias := MPSysOpenQuery(cQuery)

(cAlias)->(dbGoTop())

While (cAlias)->(!EOF())
    AADD(aRet,{ AllTrim((cAlias)->MHN_COD) })
    (cAlias)->(dbSkip())
EndDo

(cAlias)->(dbCloseArea())

Return aRet

/*/{Protheus.doc} ProcIntegGrpTab
Executa a geraçao da integracao manual por grupo
@type  Function
@author joao.marcos
@since 06/03/2024
@version 1.0
@param  cEmpAnt, Character, Empresa
        cFilAnt, Character, Filial
        cAssinante, Character, codigo do Assinante
        cGrupo, Character, codigo do grupo
        aProcessos, Array, processos do grupo
        oJRetWiz, Object, Objeto com os dados retornados do wizard
/*/
Function ProcIntegGrpTab(cEmp as Character, cFil as Character, cAssinante as Character, cGrupo as Character, aProcessos as Array, oJRetWiz as Json)
Local nX                := 0                    as Numeric
Local oGeraIntegManual  := JsonObject():New()   as Object

RPCSetType(3)
RpcSetEnv( cEmp, cFil,,,'LOJA')

LjGrvLog(" ProcIntegGrpTab ", " Inicio da Integração das tabelas do grupo: " + cGrupo, FWTimeStamp(2) )

For nX := 1 To Len(aProcessos)
    If Len(oJRetWiz['Filiais']) > 0
        oGeraIntegManual := GeraIntegManualPDV():New(cAssinante, aProcessos[nX][1], oJRetWiz)
        oGeraIntegManual:Processa()
        Sleep(500)
    Endif
Next

LjGrvLog(" ProcIntegGrpTab ", " Fim da Integração das tabelas do grupo: " + cGrupo, FWTimeStamp(2) )

RpcClearEnv()

Return
