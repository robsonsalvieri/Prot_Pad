#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.retail.rmi.api.IntegracaoTotvsPdv

/*/{Protheus.doc} IntegracaoTotvsPdvApi
API para disponibilizar os geristros de integraçao do PDV/Central que esta solicitando
@author  joao.marcos
@since   26/02/2024
@version 1.0
@exemple: http://localhost:8090/REST/api/retail/v1/integrapdv/IntegracaoTotvsPdv?CodPontoInteg=001&EmpresaPC=T1&FilialPC=D RJ 02&GruposInteg=001,002,003
/*/
@get("/api/retail/v1/integrapdv/IntegracaoTotvsPdv/") 
Function IntegracaoTotvsPdvApi()
Local oIntegPDV := Nil   as Object

LjGrvLog("IntegracaoTotvsPdvApi ","IntegracaoTotvsPdvApi | Inicio")

oIntegPDV := IngeracaoTotvsPdv():New(oRest,"TOTVS PDV")

If oIntegPDV:GetValidParam()
    oIntegPDV:BuscaDadosAtualizados()
EndIf

oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
oRest:setResponse( oIntegPDV:GetRetornoIntg() ) 

LjGrvLog("IntegracaoTotvsPdvApi ","IntegracaoTotvsPdvApi | Fim.")

oIntegPDV:CloseEnvironment()
 
Return

/*/{Protheus.doc} AtuStatusIntegPDV
Atuaiza os Status dos registros de Integraçao enviados ao Ponto de Integraçao
@type  Function
@author joao.marcos
@since 06/03/2024
@version 1.0
@param param_name, param_type, param_descr
@return return_var, return_type, return_description
@example http://localhost:8090/REST/api/retail/v1/integrapdv/AtuStatusIntegPDV
{
    "EmpresaPC": "T1",
    "FilialPC": "D RJ 02",
    "CodigoPC": "001",
    "RegistrosErro":[
        {
            "UUID": "b3d80ede-fbfc-d0fd-b547-4bb9c62eeca7",
            "Status": "3",
            "Motivo": "xxxxxxxxx" 
        },
        {
            "UUID": "a8968a5c-a496-7f3c-f84a-0991fed8e3f3",
            "Status": "3",
            "Motivo": "xxxxxxxxxx" 
        },
        {
            "UUID": "976e4996-2dba-139a-c67c-e28dd065cdfe",
            "Status": "3",
            "Motivo": "xxxxxxxxxxxx" 
        }        
    ]
}
/*/
@Put("/api/retail/v1/integrapdv/AtuStatusIntegPDV/") 
Function AtuStatusIntegPDV()
Local oStatusInteg     := Nil   as Object

LjGrvLog("AtuStatusIntegPDV ","AtuStatusIntegPDV | Inicio")   

oStatusInteg := AtuStatusInteg():New(oRest,"TOTVS PDV")

If oStatusInteg:GetValidParam()
    oStatusInteg:AtualizaStatusInteg()
EndIf

oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
oRest:setResponse( oStatusInteg:GetRetornoStatus() )   

LjGrvLog("AtuStatusIntegPDV ","AtuStatusIntegPDV | Fim")

oStatusInteg:CloseEnvironment()
   
Return

/*/{Protheus.doc} RetornaGruposTabelas
Retorna os Grupos de Tabelas e valida se o Ponto de Integraçao existe na Retaguarda
@author  joao.marcos
@since   26/02/2024
@version 1.0
@exemple: http://localhost:8090/REST/api/retail/v1/integrapdv/RetornaGruposTabelas?CodPontoInteg=001&EmpresaPC=T1&FilialPC=D RJ 02
/*/
@get("/api/retail/v1/integrapdv/RetornaGruposTabelas/") 
Function RetornaGruposTabelas()
Local oTestIntegPDV     := Nil   as Object

LjGrvLog("RetornaGruposTabelas "," Inicio")

oTestIntegPDV := IngeracaoTotvsPdv():New(oRest,"TOTVS PDV",.T.)

If oTestIntegPDV:GetValidParam()
    oTestIntegPDV:SetGrupoTabelas()
EndIf

oRest:setResponse( oTestIntegPDV:GetRetornoIntg() )    

LjGrvLog("RetornaGruposTabelas "," Fim.")

oTestIntegPDV:CloseEnvironment()

Return
