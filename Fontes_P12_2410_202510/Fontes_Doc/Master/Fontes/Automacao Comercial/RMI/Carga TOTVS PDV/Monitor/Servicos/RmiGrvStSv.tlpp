#include "PROTHEUS.CH"
#include "tlpp-core.th"

/*/{Protheus.doc} RmiGravaStatusServicos
Grava a Data e hora da conexao dos Serviços RMIPublica, RMIEnviaPDV e RMIIntePDV
@type  Function
@author joao.marcos
@since 14/06/2024
@version v1.0
@param  cServico, Character, Servico que esta executando a funçao
        cEmpPtoInteg, character, Empresa do Ponte de Integracao
        cFilPtoInteg, character, Filial do Ponte de Integracao
        cCodPtoInteg, Codigo do Ponto de Integracao 
/*/
Function RmiGrvStSv(cServico as Character, cEmpPtoInteg as character, cFilPtoInteg as character, cCodPtoInteg as Character, cGrupoInteg as Character, cGruposInteg as Character)
 
    Local aAreaMIW      := MIW->(GetArea("MIW"))    as Array
    Local lInclui       := .T.                      as Logical
    Local aGruposInteg  := {}                       as Array
    Local cNomeServ     := ""                       as Character
    Local nX            := 0                        as Numeric
    Local cTempo        := "00:03:00"               as Character

    Default cEmpPtoInteg := ""
    Default cFilPtoInteg := ""
    Default cCodPtoInteg := ""
    Default cGrupoInteg  := ""
    Default cGruposInteg := ""

    If !Empty(cGruposInteg)
        aGruposInteg  := StrTokArr(cGruposInteg, ",")
        cGruposInteg := ""
        For nX := 1 To Len(aGruposInteg)
            cGruposInteg += allTrim(aGruposInteg[nX]) + "_"
        Next

        cNomeServ := cServico + "_" + SubStr(cGruposInteg,1,Len(cGruposInteg)-1)
    EndIf

    LjGrvLog("RmiGrvStSv", " Inicio ", FWTimeStamp(2) )

    MIW->(dbSetOrder(1)) // MIW_FILIAL+MIW_EMPPI+MIW_FILPI+MIW_CODPI+MIW_SERVIC

    lInclui := MIW->(dbSeek( xFilial("MIW") + PadR(cEmpPtoInteg, TamSx3("MIW_EMPPI")[1]) + PadR(cFilPtoInteg, TamSx3("MIW_FILPI")[1]) + PadR(cCodPtoInteg, TamSx3("MIW_CODPI")[1]) + PadR(cNomeServ, TamSx3("MIW_SERVIC")[1])))

    if !lInclui .or. (lInclui .and. elapTime(subStr(MIW->MIW_HORA,1,8), time()) > cTempo)

        if MIW->(RecLock("MIW",!lInclui,,,IsBlind()))

            MIW->MIW_FILIAL := xFilial("MIW")
            MIW->MIW_EMPPI  := cEmpPtoInteg
            MIW->MIW_FILPI  := cFilPtoInteg
            MIW->MIW_CODPI  := cCodPtoInteg
            MIW->MIW_SERVIC := Upper(cNomeServ)
            MIW->MIW_DATA   := Date()
            MIW->MIW_HORA   := TimeFull()  
            MIW->MIW_GRUPO  := cGrupoInteg   
            MIW->(MsUnLock())

        endIf

    endIf

    RestArea(aAreaMIW)

    LjGrvLog("RmiGrvStSv", " Fim ", FWTimeStamp(2) )

Return
