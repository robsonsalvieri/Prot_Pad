#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "msobject.ch"
#include "PROTHEUS.CH"
#include "FWMVCDEF.CH"

namespace totvs.protheus.retail.sv.paramutil

//-------------------------------------------------------------------
/*/{Protheus.doc} RetailSvParamUtil
Classe para tratativas de parâmetros a serem utilizados no SmartView.

@author sigaloja
@since 26/03/2025
@version 1.0
/*/
//-------------------------------------------------------------------
class RetailSvParamUtil

	public method new() as object

	@Get("/api/sigaloja/smartview/v1/options/:modulo/:objeto/:funcao/:opcao")
	public method getLojaComboParam()

	static method GetOpcoes()       as array
	static method processGetCombo() as character

	private data cFuncao as character
	private data cModulo as character
	private data cObjeto as character
	private data cData   as character
	private data cOpcao  as character
	private data jPath   as object
	private data aOptions as array

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Construtor da classe

@author sigaloja
@since  26/03/2025
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class RetailSvParamUtil
	::cFuncao  := ''
	::cModulo  := ''
	::cObjeto  := ''
	::cData    := ''
	::cOpcao   := ''
	::jPath    := Nil
	::aOptions := {}
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} getLojaComboParam
Retorna o combo conforme Path Params passado.

@return oRest:setResponse (com JSON de retorno da api)

@author sigaloja
@since  26/03/2025
@version 1.0
/*/
//-------------------------------------------------------------------
method getLojaComboParam() class RetailSvParamUtil

	::jPath := oRest:getPathParamsRequest()
	If (::jPath <> Nil )
		::cModulo  := ::jPath["modulo"]
		::cObjeto  := ::jPath["objeto"]
		::cfuncao  := ::jPath["funcao"]
		::cOpcao   := ::jPath["opcao" ]  
	EndIf

	::aOptions := ::GetOpcoes(::cModulo , ::cObjeto, ::cfuncao, ::cOpcao)
	::cData    := ::processGetCombo(::aOptions)

	aSize(::aOptions, 0)

Return oRest:setResponse(::cData)

// ------------------------------------------------------------------ //
/*/{Protheus.doc} GetOpcoes
Busca o array com as opções do combo, conforme definido no
no objeto de negócio.

@param  cModulo, Código do módulo (LOJA)
@param  cObjeto, Nome do objeto de negócio.
@param  cFuncao, Função do objeto de negócio.
@param  cOpcao , Parâmetro que será envaido na função.

@return aReturn, Array com as opções do combo.

@author sigaloja
@since  26/03/2025
@version 1.0
/*/
// ------------------------------------------------------------------ //
method GetOpcoes(cModulo as character, cObjeto as character, cFuncao as character, cOpcao as character) as array class RetailSvParamUtil

Local aReturn   := {}  as array
Local lFound    := .F. as logical
Local cFunc     := ""  as character
Local cInName   := "totvs.protheus.retail." as character
Local cFnName   := ".integratedprovider."  as character

	cFunc   := cInName + cObjeto + "." + cFuncao
	lFound  := tlpp.ffunc(cFunc)

	If !lFound
		cFunc   := cInName + cObjeto + cFnName + cFuncao
		lFound  := tlpp.ffunc(cFunc)
	EndIf

	If lFound
		aReturn := tlpp.call(cFunc, cOpcao)
	EndIf

Return aReturn

// ------------------------------------------------------------------ //
/*/{Protheus.doc} processGetCombo
Processa o array formatando o JSON a ser retornado na API, para
os combos.

@param  aOpcoes, Array com as opções que serão retornadas.

@return cReturn, JSON formatado para retornar na API.

@author sigaloja
@since 26/03/2025
@version 1.0
/*/
// ------------------------------------------------------------------ //
method processGetCombo(aOpcoes as array) as character class RetailSvParamUtil

	Local cReturn := "" as character
	Local aReturn := {} as array
	Local jReturn := JsonObject():new() as json
	Local nX,nPos := 0  as numeric

	For nX := 1 to Len(aOpcoes)
		aAdd(aReturn, JsonObject():new())
		nPos := Len(aReturn)

		aReturn[nPos]["key"]    := nX
		aReturn[nPos]["label"]  := Alltrim(aOpcoes[nX])
	Next nX

	jReturn["data"] := aReturn
	cReturn := jReturn:toJson()

Return cReturn
