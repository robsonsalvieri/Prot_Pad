#INCLUDE "protheus.ch"
#INCLUDE "totvs.ch"
#INCLUDE "msobject.ch"
#INCLUDE "totvs.framework.treports.integratedprovider.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "LOJR7030.CH"

namespace totvs.protheus.retail.vendasgerenciais_visao.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,;
	team='SIGALOJA',;
	tables='SL1,SL2,SB1,SBM,ACV,ACU,SA3,SA1',;
	name='Visão de dados de vendas gerenciais',;
	country='ALL',;
	initialRelease='12.1.2210')

//-------------------------------------------------------------------
/*/{Protheus.doc} vendasgerenciais_visaoTReportsBusinessObject
Classe para criação do Objeto de Negócio da visão de dados 
de vendas gerenciais

@author  Jorge Martins
@since   15/05/2025
/*/
//-------------------------------------------------------------------
class vendasgerenciais_visaoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
	public  method new()         as object
	public  method getData()     as object
	public  method getSchema()   as object
	private method getQuery()    as character

	data cCodFil        as character
	data cDescFil       as character
	data lExibeVen      as logical
	data lExibeCli      as logical

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Método de instância da classe

@return  object: self

@author  Jorge Martins
@since   15/05/2025
/*/
//-------------------------------------------------------------------
method new() class vendasgerenciais_visaoTReportsBusinessObject
Local lLGPD := Iif(ExistFunc("FwPDCanUse"), FwPDCanUse(.T.), .F.) as logical // Verifica se a funcionalidade de Dados Protegidos está sendo utilizada no sistema.

	_Super:new()

	// Define a área
	self:appendArea(STR0003) // "Varejo"

	// Define o nome do Objeto de Negócio
	self:setDisplayName(STR0004) // "Visão de vendas gerenciais"

	// Define a descrição do Objeto de Negócio
	self:setDescription(STR0005) // "Objeto de Negócio para a visão de vendas gerenciais"

	Self:lExibeVen := Iif(lLGPD .And. Len(FwProtectedDataUtil():UsrNoAccessFieldsInList({"A3_NOME"})) > 0, .F., .T.) // Indica se deve exibir o nome do vendedor conforme regras de dados protegidos
	Self:lExibeCli := Iif(lLGPD .And. Len(FwProtectedDataUtil():UsrNoAccessFieldsInList({"A1_NOME"})) > 0, .F., .T.) // Indica se deve exibir o nome do cliente conforme regras de dados protegidos

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getData
Retorna os dados do objeto de negocio

@param nPage  , numérico, Indica a página atual do relatorio
@param oFilter, objeto  , Contém o filtro do TReports 

@return object, self:oData 

@author  Jorge Martins
@since   15/05/2025
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class vendasgerenciais_visaoTReportsBusinessObject

Local jParams     := oFilter:getParameters() as Json
Local cQuery      := "" as character
Local cData       := "" as character
Local nTamData    := 0 as numeric
Local cTemp       := GetNextAlias() as character
Local oJsDados    as object

	//Define a quantidade máxima por página (Default 100)
	self:setPageSize(20)

	Self:cCodFil   := ""
	Self:cDescFil  := ""

	cQuery := self:getQuery(jParams)
	MPSysOpenQuery(cQuery, cTemp)

	While !(cTemp)->(Eof())

		If !Empty((cTemp)->L2_FILIAL) .And. Self:cCodFil <> (cTemp)->L2_FILIAL
			Self:cCodFil  := (cTemp)->L2_FILIAL
			Self:cDescFil := FWFilialName( , (cTemp)->L2_FILIAL )
		EndIf

		nTamData := Len(Alltrim((cTemp)->DATAEMISSAO))

		If nTamData == 4
			cData := (cTemp)->DATAEMISSAO
		ElseIf nTamData == 6
			cData := SubStr((cTemp)->DATAEMISSAO, 5, 2) + "/" + SubStr((cTemp)->DATAEMISSAO, 1, 4)
		Else
			cData := SubStr((cTemp)->DATAEMISSAO, 7, 2) + "/" + SubStr((cTemp)->DATAEMISSAO, 5, 2) + "/" + SubStr((cTemp)->DATAEMISSAO, 1, 4)
		EndIf

		oJsDados := JsonObject():new()

		oJsDados["FILIAL"]        := Self:cCodFil
		oJsDados["DESCFILIAL"]    := Self:cDescFil
		oJsDados["PRODUTO"]       := (cTemp)->PRODUTO
		oJsDados["DESCPROD"]      := (cTemp)->DESCPROD
		oJsDados["CODGRUPO"]      := (cTemp)->CODGRUPO
		oJsDados["DESCGRUPO"]     := (cTemp)->DESCGRUPO
		oJsDados["CODCATEGORIA"]  := (cTemp)->CODCATEGORIA
		oJsDados["DESCCATEGORIA"] := (cTemp)->DESCCATEGORIA
		oJsDados["CODVENDEDOR"]   := (cTemp)->CODVENDEDOR
		oJsDados["DESCVENDEDOR"]  := IIf(Self:lExibeVen, (cTemp)->DESCVENDEDOR, Replicate("*", 10))
		oJsDados["CODCLIENTE"]    := (cTemp)->CODCLIENTE
		oJsDados["LOJACLIENTE"]   := (cTemp)->LOJACLIENTE
		oJsDados["DESCCLIENTE"]   := IIf(Self:lExibeCli, (cTemp)->DESCCLIENTE, Replicate("*", 10))
		
		oJsDados["DATAEMISSAO"]   := cData
		oJsDados["QTDITENS"]      := (cTemp)->QTDITENS
		oJsDados["QTDVENDAS"]     := (cTemp)->QTDVENDAS
		oJsDados["VALORBRUTO"]    := (cTemp)->VALORBRUTO
		oJsDados["DESCONTO"]      := (cTemp)->DESCONTO
		oJsDados["ACRESCIMO"]     := (cTemp)->ACRESCIMO
		oJsDados["IMPOSTOS"]      := (cTemp)->IMPOSTOS
		oJsDados["VALORLIQUIDO"]  := (cTemp)->VALORLIQUIDO

		self:oData:appendData(oJsDados)

		(cTemp)->(DBSkip())

	EndDo

	(cTemp)->(DbCloseArea())

Return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author  Jorge Martins
@since   15/05/2025
/*/
//-------------------------------------------------------------------
method getSchema() as object Class vendasgerenciais_visaoTReportsBusinessObject

	self:oSchema:addProperty("FILIAL"       , "FILIAL"       , "string", STR0006 , "FILIAL"       ) // "Código filial"
	self:oSchema:addProperty("DESCFILIAL"   , "DESCFILIAL"   , "string", STR0007 , "DESCFILIAL"   ) // "Descrição filial"
	self:oSchema:addProperty("PRODUTO"      , "PRODUTO"      , "string", STR0008 , "PRODUTO"      ) // "Produto"
	self:oSchema:addProperty("DESCPROD"     , "DESCPROD"     , "string", STR0009 , "DESCPROD"     ) // "Descrição produto"
	self:oSchema:addProperty("CODGRUPO"     , "CODGRUPO"     , "string", STR0010 , "CODGRUPO"     ) // "Grupo de produto"
	self:oSchema:addProperty("DESCGRUPO"    , "DESCGRUPO"    , "string", STR0011 , "DESCGRUPO"    ) // "Descrição grupo"
	self:oSchema:addProperty("CODCATEGORIA" , "CODCATEGORIA" , "string", STR0012 , "CODCATEGORIA" ) // "Categoria"
	self:oSchema:addProperty("DESCCATEGORIA", "DESCCATEGORIA", "string", STR0013 , "DESCCATEGORIA") // "Descrição categoria"
	self:oSchema:addProperty("CODVENDEDOR"  , "CODVENDEDOR"  , "string", STR0014 , "CODVENDEDOR"  ) // "Vendedor"
	self:oSchema:addProperty("DESCVENDEDOR" , "DESCVENDEDOR" , "string", STR0015 , "DESCVENDEDOR" ) // "Nome do vendedor"
	self:oSchema:addProperty("CODCLIENTE"   , "CODCLIENTE"   , "string", STR0016 , "CODCLIENTE"   ) // "Cliente"
	self:oSchema:addProperty("LOJACLIENTE"  , "LOJACLIENTE"  , "string", STR0017 , "LOJACLIENTE"  ) // "Loja cliente"
	self:oSchema:addProperty("DESCCLIENTE"  , "DESCCLIENTE"  , "string", STR0018 , "DESCCLIENTE"  ) // "Nome do cliente"

	self:oSchema:addProperty("DATAEMISSAO"  , "DATAEMISSAO"  , "string", STR0019, "DATAEMISSAO"  ) // "Quantidade de itens"
	self:oSchema:addProperty("QTDITENS"     , "QTDITENS"     , "number", STR0020, "QTDITENS"     ) // "Quantidade de itens"
	self:oSchema:addProperty("QTDVENDAS"    , "QTDVENDAS"    , "number", STR0021, "QTDVENDAS"    ) // "Quantidade de vendas"
	self:oSchema:addProperty("VALORBRUTO"   , "VALORBRUTO"   , "number", STR0022, "VALORBRUTO"   ) // "Valor bruto"
	self:oSchema:addProperty("DESCONTO"     , "DESCONTO"     , "number", STR0023, "DESCONTO"     ) // "Desconto"
	self:oSchema:addProperty("ACRESCIMO"    , "ACRESCIMO"    , "number", STR0024, "ACRESCIMO"    ) // "Acréscimo"
	self:oSchema:addProperty("IMPOSTOS"     , "IMPOSTOS"     , "number", STR0025, "IMPOSTOS"     ) // "Imposto retido"
	self:oSchema:addProperty("VALORLIQUIDO" , "VALORLIQUIDO" , "number", STR0026, "VALORLIQUIDO" ) // "Valor líquido"

	self:oSchema:addParameter("PAR_FILIAL", STR0027, "string", .T.,,   ,,,, STR0033) // "Filial" ## "Filtra as vendas das lojas/filiais selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as lojas/filiais."
	self:oSchema:addParameter("PAR_DTINI" , STR0028, "date"  , .F.,,   ,,,, STR0034, .F., {totvs.framework.treports.date.dateToTimeStamp(Date())}) // "Data inicial" ## "Filtra as vendas com data igual ou superior a data informada. Esse filtro é utilizado em conjunto com a 'data final'."
	self:oSchema:addParameter("PAR_DTFIM" , STR0029, "date"  , .F.,,   ,,,, STR0035, .F., {totvs.framework.treports.date.dateToTimeStamp(Date())}) // "Data final" ## "Filtra as vendas com data igual ou inferior a data informada. Esse filtro é utilizado em conjunto com a 'data inicial'."
	self:oSchema:addParameter("PAR_PROD"  , STR0008, "string", .T.,,   ,,,, STR0036) // "Produto" ## "Filtra as vendas dos produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os produtos."
	self:oSchema:addParameter("PAR_GRUPO" , STR0030, "string", .T.,,   ,,,, STR0037) // "Grupo de produtos" ## "Filtra as vendas dos grupos de produtos selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os grupos."
	self:oSchema:addParameter("PAR_CATEG" , STR0031, "string", .T.,,   ,,,, STR0038) // "Categoria de produtos" ## "Filtra as vendas das categorias de produtos selecionadas. Caso não seja selecionada nenhuma, serão exibidas as vendas de todas as categorias."
	self:oSchema:addParameter("PAR_VEND"  , STR0014, "string", .T.,,   ,,,, STR0039,,,, Self:lExibeVen) // "Vendedor" ## "Filtra as vendas dos vendedores selecionados. Caso não seja selecionado nenhum, serão exibidas as vendas de todos os vendedores."
	self:oSchema:addParameter("PAR_TIPOP" , STR0032, "number", .F.,,.T.,,,, STR0040,, {1}) // "Tipo de período" ## "Indica se os dados serão agrupados por ano, mês ou dia."

	// Seta a consulta padrão nos parâmetros manuais
	self:setCustomURL("PAR_FILIAL", "api/framework/v1/genericLookupService/smartview/SM0", 2)
	self:setCustomURL("PAR_PROD"  , "api/framework/v1/genericLookupService/smartview/SB1", 2)
	self:setCustomURL("PAR_GRUPO" , "api/framework/v1/genericLookupService/smartview/SBM", 2)
	self:setCustomURL("PAR_CATEG" , "api/framework/v1/genericLookupService/smartview/ACU", 2)
	If Self:lExibeVen
		self:setCustomURL("PAR_VEND", "api/framework/v1/genericLookupService/smartview/SA3", 2)
	EndIf
	self:setCustomURL("PAR_TIPOP", "/api/sigaloja/smartview/v1/options/loja/vendasgerenciais_visao/LjVdGerCb/1", 1)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} LjVdGerCb
Retorna as opções disponíveis para parâmetros do tipo combo.

@param cOpcao, Define qual combo será exibido

@return aRet, Array com as opções do combo

@author Jorge Martins
@since  28/03/2025
@version 1.0
*/
//-------------------------------------------------------------------
Function LjVdGerCb(cOpcao)
Local aRet := {} as array
	
	If cOpcao == "1"
		aRet := {STR0041, STR0042, STR0043} // "Anual", "Mensal", "Diário"
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Gera a query para busca dos dados da visão

@param jParams, Parâmetros do smartview

@return cQuery, Query da visão

@author Jorge Martins
@since  20/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getQuery(jParams as Json) as character class vendasgerenciais_visaoTReportsBusinessObject
Local cQuery      := "" as character
Local cJoinFilACV := "" as character
Local cJoinFilSA3 := "" as character
Local cJoinFilSA1 := "" as character
Local cJoinFilSF4 := "" as character
Local cJoinFilSB1 := "" as character
Local cDataIni    := "" as character
Local cDataFim    := "" as character
Local cTipoPer    := "1" as character
Local cTpComACV   := FWModeAccess('ACV', 1, FWGrpCompany()) + FWModeAccess('ACV', 2, FWGrpCompany()) + FWModeAccess('ACV', 3, FWGrpCompany()) as Character
Local cTpComSL2   := FWModeAccess('SL2', 1, FWGrpCompany()) + FWModeAccess('SL2', 2, FWGrpCompany()) + FWModeAccess('SL2', 3, FWGrpCompany()) as Character
Local cTpComSA3   := FWModeAccess('SA3', 1, FWGrpCompany()) + FWModeAccess('SA3', 2, FWGrpCompany()) + FWModeAccess('SA3', 3, FWGrpCompany()) as Character
Local cTpComSA1   := FWModeAccess('SA1', 1, FWGrpCompany()) + FWModeAccess('SA1', 2, FWGrpCompany()) + FWModeAccess('SA1', 3, FWGrpCompany()) as Character
Local cTpComSF4   := FWModeAccess('SF4', 1, FWGrpCompany()) + FWModeAccess('SF4', 2, FWGrpCompany()) + FWModeAccess('SF4', 3, FWGrpCompany()) as Character
Local cTpComSB1   := FWModeAccess('SB1', 1, FWGrpCompany()) + FWModeAccess('SB1', 2, FWGrpCompany()) + FWModeAccess('SB1', 3, FWGrpCompany()) as Character
Local nTamFilACV  := Len(AllTrim(FWxFilial("ACV"))) as numeric
Local nTamFilSA3  := Len(AllTrim(FWxFilial("SA3"))) as numeric
Local nTamFilSA1  := Len(AllTrim(FWxFilial("SA1"))) as numeric
Local nTamFilSF4  := Len(AllTrim(FWxFilial("SF4"))) as numeric
Local nTamFilSB1  := Len(AllTrim(FWxFilial("SB1"))) as numeric
Local nValMinIR   := SuperGetMV("MV_VLRETIR",,0) as numeric // Valor mínimo para retenção de IRRF
Local nValMinPCC  := SuperGetMV("MV_VL13137",,0) as numeric // Valor mínimo para retenção do Pis/Cofins/CSLL
Local aBind       := {} as array
Local aFiliais    := {} as array
Local aProdutos   := {} as array
Local aGrupos     := {} as array
Local aCategorias := {} as array
Local aVendedores := {} as array
Local lFtFilial   := .F. as logical
Local lFtProd     := .F. as logical
Local lFtGrupo    := .F. as logical
Local lFtCateg    := .F. as logical
Local lFtVend     := .F. as logical

	aFiliais    := LJSVMultiSel(jParams['PAR_FILIAL'], "L1_FILIAL")
	aProdutos   := LJSVMultiSel(jParams['PAR_PROD']  , "L2_PRODUTO")
	aGrupos     := LJSVMultiSel(jParams['PAR_GRUPO'] , "BM_GRUPO")
	aCategorias := LJSVMultiSel(jParams['PAR_CATEG'] , "ACU_COD")
	aVendedores := LJSVMultiSel(jParams['PAR_VEND']  , "A3_COD")
	cDataIni    := SubStr(StrTran(jParams['PAR_DTINI'][1],"-",""), 1, 8)
	cDataFim    := SubStr(StrTran(jParams['PAR_DTFIM'][1],"-",""), 1, 8)
	
	If jParams['PAR_TIPOP'][1] <> Nil
		cTipoPer := AllTrim(Str(jParams['PAR_TIPOP'][1]))
	EndIf
	
	lFtFilial   := ValType(aFiliais)    == "A" .And. !Empty(aFiliais[1])    // Indica se deve ser feito o filtro de filiais
	lFtProd     := ValType(aProdutos)   == "A" .And. !Empty(aProdutos[1])   // Indica se deve ser feito o filtro de produtos
	lFtGrupo    := ValType(aGrupos)     == "A" .And. !Empty(aGrupos[1])     // Indica se deve ser feito o filtro de grupos de produtos
	lFtCateg    := ValType(aCategorias) == "A" .And. !Empty(aCategorias[1]) // Indica se deve ser feito o filtro de categorias de produtos
	lFtVend     := ValType(aVendedores) == "A" .And. !Empty(aVendedores[1]) // Indica se deve ser feito o filtro de vendedores

	// Definicao Filial ACV
	If cTpComACV == cTpComSL2
		cJoinFilACV := " SL2.L2_FILIAL "
	ElseIf cTpComACV == "CCC"
		cJoinFilACV :=  "'" + xFilial("ACV") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilACV := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilACV) + ") "
	EndIf

	// Definicao Filial SA3
	If cTpComSA3 == cTpComSL2
		cJoinFilSA3 := " SL2.L2_FILIAL "
	ElseIf cTpComSA3 == "CCC"
		cJoinFilSA3 := "'" + xFilial("SA3") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSA3 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSA3) +")"
	EndIf

	// Definicao Filial SF4
	If cTpComSF4 == cTpComSL2
		cJoinFilSF4 := " SL2.L2_FILIAL "
	ElseIf cTpComSF4 == "CCC"
		cJoinFilSF4 :=  "'" + xFilial("SF4") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSF4 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSF4) + ") "
	EndIf

	// Definicao Filial SB1
	If cTpComSB1 == cTpComSL2
		cJoinFilSB1 := " SL2.L2_FILIAL "
	ElseIf cTpComSB1 == "CCC"
		cJoinFilSB1 :=  "'" + xFilial("SB1") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSB1 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSB1) + ") "
	EndIf

	// Definicao Filial SA1
	If cTpComSA1 == cTpComSL2
		cJoinFilSA1 := " SL2.L2_FILIAL "
	ElseIf cTpComSA1 == "CCC"
		cJoinFilSA1 := "'" + xFilial("SA1") + "'"
	ElseIf cTpComSL2 <> "CCC"
		cJoinFilSA1 := " SUBSTRING(SL2.L2_FILIAL,1," + Str(nTamFilSA1) +")"
	EndIf

	cQuery := " SELECT SL2.L2_FILIAL, "
	cQuery +=        " SL2.L2_PRODUTO PRODUTO, SB1.B1_DESC DESCPROD, "
	cQuery +=        " SB1.B1_GRUPO CODGRUPO, ISNULL(SBM.BM_DESC, ' ') DESCGRUPO, "
	cQuery +=        " ISNULL(ACU.ACU_COD, ' ') CODCATEGORIA, ISNULL(ACU.ACU_DESC, ' ') DESCCATEGORIA, "
	cQuery +=        " SL2.L2_VEND CODVENDEDOR, SA3.A3_NOME DESCVENDEDOR, "
	cQuery +=        " SL1.L1_CLIENTE CODCLIENTE, SL1.L1_LOJA LOJACLIENTE, SA1.A1_NOME DESCCLIENTE, SA1.A1_CGC DOCCLIENTE, "
	
	If cTipoPer == "1" // Somente ano - Ex: 2025
		cQuery +=    " SUBSTRING(SL1.L1_EMISSAO, 1, 4) DATAEMISSAO, "
	ElseIf cTipoPer == "2" // Ano e mês - Ex: 202505
		cQuery +=    " SUBSTRING(SL1.L1_EMISSAO, 1, 6) DATAEMISSAO, "
	ElseIf cTipoPer == "3" // Data completa (ano, mês e dia) - Ex: 20250513
		cQuery +=    " SL1.L1_EMISSAO DATAEMISSAO, "
	EndIf
	cQuery += " SUM(SL2.L2_QUANT) QTDITENS, "
	cQuery += " COUNT(DISTINCT SL1.L1_NUM) QTDVENDAS, "
	cQuery += " SUM(SL2.L2_VLRITEM + SL2.L2_VALIPI) VALORBRUTO, "
	cQuery += " SUM(SL2.L2_VLRITEM + SL2.L2_VALIPI - "
	cQuery +=        " (CASE WHEN SL1.L1_VALIRRF > ? THEN SL2.L2_VALIRRF ELSE 0 END + " // #01
	cQuery +=         " CASE WHEN SL1.L1_ABTOPCC > ? THEN L2_VALPIS + L2_VALCOFI + L2_VALCSLL ELSE 0 END)" // #02
	cQuery +=     ") VALORLIQUIDO, "
	cQuery += " SUM(SL2.L2_VALDESC + SL2.L2_DESCPRO) DESCONTO,"
	cQuery += " SUM(SL2.L2_VALACRS) ACRESCIMO, "
	cQuery += " SUM(CASE WHEN SL1.L1_VALIRRF > ? THEN SL2.L2_VALIRRF ELSE 0 END + " // #03
	cQuery +=     " CASE WHEN SL1.L1_ABTOPCC > ? THEN L2_VALPIS + L2_VALCOFI + L2_VALCSLL ELSE 0 END) IMPOSTOS " // #04

	cQuery +=   " FROM " + RetSqlName("SL2") + " SL2"
	cQuery +=  " INNER JOIN " + RetSqlName("SF4") + " SF4"
	cQuery +=     " ON SF4.F4_FILIAL = ?" // #05
	cQuery +=    " AND SF4.F4_CODIGO = SL2.L2_TES"
	cQuery +=    " AND SF4.F4_DUPLIC = 'S'"
	cQuery +=    " AND SF4.D_E_L_E_T_ = ?" // #06
	cQuery +=  " INNER JOIN " + RetSqlName("SL1") + " SL1"
	cQuery +=     " ON SL1.L1_FILIAL = SL2.L2_FILIAL"
	cQuery +=    " AND SL1.L1_NUM = SL2.L2_NUM"
	cQuery +=    " AND SL1.L1_SITUA IN ('OK','FR')"
	cQuery +=    " AND (SL1.L1_DOCPED <> ' ' OR L1_DOC <> ' ')"
	cQuery +=    " AND SL1.L1_EMISSAO BETWEEN ? AND ?" // #07 #08
	cQuery +=    " AND SL1.D_E_L_E_T_ = ?" // #09
	cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1"
	cQuery +=     " ON SB1.B1_FILIAL = ?" // #10
	cQuery +=    " AND SB1.B1_COD = SL2.L2_PRODUTO"
	cQuery +=    " AND SB1.D_E_L_E_T_ = ?" // #11
	cQuery +=   " LEFT JOIN " + RetSqlName("SBM") + " SBM"
	cQuery +=     " ON SBM.BM_FILIAL = SB1.B1_FILIAL"
	cQuery +=    " AND SBM.BM_GRUPO = SB1.B1_GRUPO"
	cQuery +=    " AND SBM.D_E_L_E_T_ = ?" // #12
	cQuery +=   " LEFT JOIN " + RetSqlName("ACV") + " ACV"
	cQuery +=     " ON ACV.ACV_FILIAL = ?" // #13
	cQuery +=    " AND ACV.ACV_CODPRO = SB1.B1_COD"
	cQuery +=    " AND ACV.D_E_L_E_T_ = ?" // #14
	cQuery +=   " LEFT JOIN " + RetSqlName("ACU") + " ACU"
	cQuery +=     " ON ACU.ACU_FILIAL = ACV.ACV_FILIAL"
	cQuery +=    " AND ACU.ACU_COD = ACV.ACV_CATEGO"
	cQuery +=    " AND ACU.D_E_L_E_T_ = ?" // #15
	cQuery +=  " INNER JOIN " + RetSQLName("SA3") + " SA3 "
	cQuery +=     " ON SA3.A3_FILIAL = ?" // #16
	cQuery +=    " AND SA3.A3_COD = SL2.L2_VEND"
	cQuery +=    " AND SA3.D_E_L_E_T_ = ?" // #17
	cQuery +=  " INNER JOIN " + RetSQLName("SA1") + " SA1 "
	cQuery +=     " ON SA1.A1_FILIAL = ?" // #18
	cQuery +=    " AND SA1.A1_COD = SL1.L1_CLIENTE"
	cQuery +=    " AND SA1.A1_LOJA = SL1.L1_LOJA"
	cQuery +=    " AND SA1.D_E_L_E_T_ = ?" // #19
	cQuery +=  " WHERE SL2.D_E_L_E_T_ = ?" // #20

	If lFtFilial // Indica se deve ser feito o filtro de filiais
		cQuery +=  " AND SL2.L2_FILIAL IN (?)" // #21
	EndIf
	If lFtProd // Indica se deve ser feito o filtro de produtos
		cQuery +=  " AND SL2.L2_PRODUTO IN (?)" // #22
	EndIf
	If lFtGrupo // Indica se deve ser feito o filtro de grupos de produtos
		cQuery +=  " AND SB1.B1_GRUPO IN (?)" // #23
	EndIf
	If lFtCateg // Indica se deve ser feito o filtro de categorias de produtos
		cQuery +=  " AND ACU.ACU_COD IN (?)" // #24
	EndIf
	If lFtVend // Indica se deve ser feito o filtro de vendedores
		cQuery +=  " AND SL2.L2_VEND IN (?)" // #25
	EndIf
	
	cQuery +=  " GROUP BY SL2.L2_FILIAL, "
	cQuery +=           " SL2.L2_PRODUTO, SB1.B1_DESC, "
	cQuery +=           " SB1.B1_GRUPO, ISNULL(SBM.BM_DESC, ' '), "
	cQuery +=           " ISNULL(ACU.ACU_COD, ' '), ISNULL(ACU.ACU_DESC, ' '), "
	cQuery +=           " SL2.L2_VEND, SA3.A3_NOME, "
	cQuery +=           " SL1.L1_CLIENTE, SL1.L1_LOJA, SA1.A1_NOME, SA1.A1_CGC, "

	If cTipoPer == "1"
		cQuery +=    " SUBSTRING(SL1.L1_EMISSAO, 1, 4) "
	ElseIf cTipoPer == "2"
		cQuery +=    " SUBSTRING(SL1.L1_EMISSAO, 1, 6) "
	ElseIf cTipoPer == "3"
		cQuery +=    " SL1.L1_EMISSAO "
	EndIf

	cQuery +=  " ORDER BY DATAEMISSAO, L2_FILIAL, PRODUTO, CODGRUPO, CODCATEGORIA, CODVENDEDOR, CODCLIENTE "

	AAdd(aBind, {nValMinIR, "N"})     // #01
	AAdd(aBind, {nValMinPCC, "N"})    // #02
	AAdd(aBind, {nValMinIR, "N"})     // #03
	AAdd(aBind, {nValMinPCC, "N"})    // #04
	AAdd(aBind, {cJoinFilSF4, "U"})   // #05
	AAdd(aBind, {" ", "S"})           // #06
	AAdd(aBind, {cDataIni, "S"})      // #07
	AAdd(aBind, {cDataFim, "S"})      // #08
	AAdd(aBind, {" ", "S"})           // #09
	AAdd(aBind, {cJoinFilSB1, "U"})   // #10
	AAdd(aBind, {" ", "S"})           // #11
	AAdd(aBind, {" ", "S"})           // #12
	AAdd(aBind, {cJoinFilACV, "U"})   // #13
	AAdd(aBind, {" ", "S"})           // #14
	AAdd(aBind, {" ", "S"})           // #15
	AAdd(aBind, {cJoinFilSA3, "U"})   // #16
	AAdd(aBind, {" ", "S"})           // #17
	AAdd(aBind, {cJoinFilSA1, "U"})   // #18
	AAdd(aBind, {" ", "S"})           // #19
	AAdd(aBind, {" ", "S"})           // #20

	If lFtFilial // Indica se deve ser feito o filtro de filiais
		AAdd(aBind, {aFiliais, "I"}) // #21
	EndIf
	If lFtProd // Indica se deve ser feito o filtro de produtos
		AAdd(aBind, {aProdutos, "I"})  // #22
	EndIf
	If lFtGrupo // Indica se deve ser feito o filtro de grupos de produtos
		AAdd(aBind, {aGrupos, "I"}) // #23
	EndIf
	If lFtCateg // Indica se deve ser feito o filtro de categorias de produtos
		AAdd(aBind, {aCategorias, "I"}) // #24
	EndIf
	If lFtVend // Indica se deve ser feito o filtro de vendedores
		AAdd(aBind, {aVendedores, "I"})  // #25
	EndIf

	cQuery := LJSVBind(cQuery, aBind)

	cQuery := ChangeQuery(cQuery)

Return cQuery
