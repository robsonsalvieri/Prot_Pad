#Include "MATA650.CH"
#include "protheus.ch"

Static __lAutomacao := IsInCallStack("MATA650A_01")
Static lQIPOPEPQP   := FindFunction("QIPOPEPQPK")

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650Alert2³Autor³Rodrigo de A. Sartorio ³ Data ³ 01/10/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Aviso de que a OP a ser incluida deveria ter comecado    ³±±
±±³          ³ antes                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650Alert2(cNumOp,nDias,cProduto)
LOCAL lRet := .F.
LOCAL cLinha1:= STR0041+cNumOp+STR0042	//"A OP "###" poder  gerar OPs Intermedi rias / SCs "
LOCAL cLinha2:= STR0043	//"com datas previstas anteriores a database, j  que o Prazo de "
LOCAL cLinha3:= STR0044+StrZero(nDias,3,0)+STR0045	//"Entrega de seus componentes chega a "###" dias. "
LOCAL cLinha4:= STR0046	//"Confirma a inclus„o da OP ?"
TONE(3500,1)
If Type("l650Auto") == "L" .and. l650Auto
	lRet := .t.
Else
	lRet := (MsgYesNo(OemToAnsi(cLinha1+cLinha2+cLinha3+cLinha4),OemToAnsi(STR0024+" - ")+cProduto))	//"Aten‡„o"
EndIF 
Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A650Quant ³ Autor ³ Marcelo Pimentel      ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina que calcula a 2a unidade medida quanto exitir       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650Quant()
Local nQuant := M->C2_QUANT

If Empty(nQuant)
	Return .T.
Endif

M->C2_QTSEGUM := ConvUm(M->C2_PRODUTO,nQuant,M->C2_QTSEGUM,2)
nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "C2_QTSEGUM" })
If nEndereco > 0
	aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := Transform(M->C2_QTSEGUM,PesqPictQT("C2_QTSEGUM"))
EndIf
Return .T.
/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A650QtSeg ³ Autor ³ Marcelo Pimentel      ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina que calcula a primeira unidade de medida.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650QtSeg()
Local nQtSegUm := M->C2_QTSEGUM
Local cProduto := M->C2_PRODUTO
Local lGrade   := MaGrade()

If Empty(nQtSegUm)
	Return .T.
Endif
If lGrade
   MatGrdPrRf(@cProduto)
EndIF
M->C2_QUANT := ConvUm(cProduto,M->C2_QUANT,nQtSegUm,1)
nEndereco := Ascan(aGets,{ |x| Subs(x,9,8) == "C2_QUANT" } )
If nEndereco > 0
	aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := Transform(M->C2_QUANT,PesqPictQT("C2_QUANT"))
EndIf
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A650LotCTL  ³Autor³Rodrigo de A. Sartorio³ Data ³ 02/12/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Valida o lote de controle digitado pelo usuario.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Mata650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650LotCTL()
Local cVar:=ReadVar()
Local cAlias:=Alias(),nRecno:=Recno(),nOrdem:=IndexOrd()
Local nOldOrdem:=0,nOldRecno:=0
Local lRet:=.T.
Local cCod := aCols[n,nPosCod],cLocal:=aCols[n,nPosLocal],cLote:="",cLoteDigi:="",cSeek:=""
Local nQuant:=0,nSaldo:=0
Local i:=0
Local lConsEst   := (GetMV("MV_CONSEST") == "S")
Local lLotUni    := SuperGetMV('MV_LOTEUNI', .F., .T.)
Local lEmpPrev   := If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local lPesqFull  := .F.
Local cChave     := ""

If FindFunction("a650F4Lote") .And. a650F4Lote()
	lPesqFull := .T.
EndIf

If !Rastro(cCod)
	Help(" ",1,"NAORASTRO")
	lRet:=.F.
EndIf
If aCols[n,nPosQuant] < 0
	Help(" ",1,"A650QTDEMP")
	lRet:=.F.
EndIf

If !__lPyme
	If !Empty(aCols[n,nPosLocLz]) .AND. !A650VldLoclz(lConsEst)
		If ViewLocalz(aCols[n,nPosCod],aCols[n,nPosLocal])
	  		lRet := A650VldLoclz(lConsEst)
		Else
			lRet := .F.
		EndIf           
	EndIf
Endif	

lRet := lRet .And. A650VldConsEst()

If lRet
	dbSelectArea("SB1")
	dbSetOrder(1)
	MsSeek(xFilial()+cCod)
	If cVar == "M->D4_LOTECTL"
		cConteudo:= &(ReadVar())
		dbSelectArea("SB8")
		dbSetOrder(3)
		If lPesqFull .And. lLotUni
			cChave := xFilial()+cCod+cLocal+aCols[n,nPosLotCtl]+aCols[n,nPosLote]+DtoS(aCols[n,nPosdValid])
		Else
			cChave := xFilial()+cCod+cLocal+cConteudo
		EndIf
		If dbSeek(cChave)
			If Rastro(cCod,"S")
				cLote:=SB8->B8_NUMLOTE
				nSaldo:=SB8SALDO(,,,,,lEmpPrev,,,.T.) - (SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+SB8->B8_QACLASS+AvalQtdPre("SB8",1))
				For i:=1 To Len(aCols)
					If aCols[i,nPosLote] == cLote .And. !aCols[i,Len(aCols[i])] .Or. n = i
						nQuant += aCols[i,nPosQuant]
					EndIf
				Next i
			Else
				cLote:=SB8->B8_LOTECTL
				nSaldo:=SaldoLote(cCod,cLocal,cConteudo,,,,,dDataBase)
				For i:=1 To Len(aCols)
					If aCols[i, nPosCod] == cCod .And. aCols[i,nPosLocal] == cLocal .And. aCols[i,nPosLotCtl] == cLote .And. !aCols[i,Len(aCols[i])] .Or. n = i
						nQuant += aCols[i,nPosQuant]
					EndIf
				Next i
			EndIf
			If nSaldo >= nQuant
				If Rastro(cCod,"S")					
					if Vazio(aCols[n,nPosLote])
						M->D4_NUMLOTE	    :=SB8->B8_NUMLOTE
						aCols[n,nPosLote]	:=SB8->B8_NUMLOTE
					else
						M->D4_NUMLOTE       := aCols[n,nPosLote]
					EndIf					
				EndIf
				M->D4_DTVALID			:=SB8->B8_DTVALID
				aCols[n,nPosLotCtl]		:=SB8->B8_LOTECTL
				aCols[n,nPosdValid]		:=SB8->B8_DTVALID
				aCols[n,nPosPotenc]		:=SB8->B8_POTENCI
			Else
				Help(" ",1,"A240LOTENE")
				lRet:=.f.
				M->D4_NUMLOTE		:=CriaVar("D4_NUMLOTE")
				M->D4_LOTECTL		:=CriaVar("D4_LOTECTL")
				M->D4_DTVALID		:=CriaVar("D4_DTVALID")
				M->D4_POTENCI		:=CriaVar("D4_POTENCI")
				aCols[n,nPosLote]	:=M->D4_NUMLOTE
				aCols[n,nPosLotCtl]	:=M->D4_LOTECTL
				aCols[n,nPosdValid]	:=M->D4_DTVALID
				aCols[n,nPosPotenc]	:=M->D4_POTENCI

				aCols[n,nPosLocLz]	:=CriaVar("DC_LOCALIZ")
				aCols[n,nPosnSerie]	:=CriaVar("DC_NUMSERI")

			EndIf
		Else
			If !Empty(cConteudo)
				Help(" ",1,"A240LOTCTL")
				lRet:=.F.
			Else
				M->D4_NUMLOTE		:=CriaVar("D4_NUMLOTE")
				M->D4_LOTECTL		:=CriaVar("D4_LOTECTL")
				M->D4_DTVALID		:=CriaVar("D4_DTVALID")
				M->D4_POTENCI		:=CriaVar("D4_POTENCI")
				aCols[n,nPosLote]	:=M->D4_NUMLOTE
				aCols[n,nPosLotCtl]	:=M->D4_LOTECTL
				aCols[n,nPosdValid]	:=M->D4_DTVALID
				aCols[n,nPosPotenc]	:=M->D4_POTENCI

				aCols[n,nPosLocLz]	:=CriaVar("DC_LOCALIZ")
				aCols[n,nPosnSerie]	:=CriaVar("DC_NUMSERI")
			EndIf
		EndIf
	ElseIf cVar == "M->D4_NUMLOTE" .And. Rastro(cCod,"S")
		cConteudo:=&(ReadVar())
		cLote		:=cConteudo
		dbSelectArea("SB8")
		dbSetOrder(2)
		If dbSeek(xFilial()+cConteudo)
			For i:=1 To Len(aCols)
				If aCols[i, nPosCod] == cCod .And. aCols[i,nPosLocal] == cLocal .And. aCols[i,nPosLote] == cLote .And. !aCols[i,Len(aCols[i])] .Or. n = i
					nQuant += aCols[i,nPosQuant]
				EndIf
			Next i
			If !((SB8SALDO(,,,,,lEmpPrev,,,.T.) - (SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+SB8->B8_QACLASS+AvalQtdPre("SB8",1))) >= nQuant)
				Help(" ",1,"A240LOTENE")
				lRet:=.F.
				M->D4_NUMLOTE		:=CriaVar("D4_NUMLOTE")
				M->D4_LOTECTL		:=CriaVar("D4_LOTECTL")
				M->D4_DTVALID		:=CriaVar("D4_DTVALID")
				M->D4_POTENCI		:=CriaVar("D4_POTENCI")
				aCols[n,nPosLote]	:=M->D4_NUMLOTE
				aCols[n,nPosLotCtl]	:=M->D4_LOTECTL
				aCols[n,nPosdValid]	:=M->D4_DTVALID
				aCols[n,nPosPotenc]	:=M->D4_POTENCI

				aCols[n,nPosLocLz]	:=CriaVar("DC_LOCALIZ")
				aCols[n,nPosnSerie]	:=CriaVar("DC_NUMSERI")

			Else
				M->D4_LOTECTL	:=SB8->B8_LOTECTL
				M->D4_DTVALID	:=SB8->B8_DTVALID
				M->D4_POTENCI	:=SB8->B8_POTENCI
				if Vazio(aCols[n,nPosLotCtl])
					aCols[n,nPosLotCtl] := M->D4_LOTECTL
				else
					M->D4_LOTECTL := aCols[n,nPosLotCtl]
				EndIf
				aCols[n,nPosdValid]	:=M->D4_DTVALID
				aCols[n,nPosPotenc]	:=M->D4_POTENCI
			EndIf
		Else
			M->D4_NUMLOTE		:=CriaVar("D4_NUMLOTE")
			M->D4_LOTECTL		:=CriaVar("D4_LOTECTL")
			M->D4_DTVALID		:=CriaVar("D4_DTVALID")
			M->D4_POTENCI		:=CriaVar("D4_POTENCI")
			aCols[n,nPosLote]	:=M->D4_NUMLOTE
			aCols[n,nPosLotCtl]	:=M->D4_LOTECTL
			aCols[n,nPosdValid]	:=M->D4_DTVALID
			aCols[n,nPosPotenc]	:=M->D4_POTENCI

			aCols[n,nPosLocLz]	:=CriaVar("DC_LOCALIZ")
			aCols[n,nPosnSerie]	:=CriaVar("DC_NUMSERI")
			
		EndIf
	ElseIf cVar == "M->D4_NUMLOTE" .And. Rastro(cCod,"L")
		M->D4_NUMLOTE		:=CriaVar("D4_NUMLOTE")
		aCols[n,nPosLote]	:=M->D4_NUMLOTE
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrdem)
dbGoTo(nRecno)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ A650Seq  ³ Autor ³ Rodrigo de A. Sartorio ³ Data ³ 20/09/96³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Sequencia na digitacao dos empenhos.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650Seq()
Local cSeq:=&(ReadVar()),i,lRet:=.T.,cProd:=aCols[n,1],cLocal:=aCols[n,3],cLote:=aCols[n,6]
For i:=1 To Len(aCols)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tratamento Utilizado para o Siga Pyme ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If i # n .And. aCols[i,4] == cSeq .and. aCols[i,1] == cProd .and. aCols[i,6] == cLote .and. !aCols[i,Len(aCols[i])] .and. aCols[i,3] == cLocal
			Help(" ",1,"A650SEQJA")
			lRet:=.F.
		Elseif( ( i # n ) .AND. (aCols[i,4] == cSeq) .AND. (aCols[i,1] == cProd) .AND. !aCols[i,Len(aCols[i])] .and. ( (aCols[n,2] > 0 .and. aCols[i,2] < 0 ) .OR. (aCols[n,2] < 0 .and. aCols[i,2] > 0 )))
		 	Help(" ",1,"A650SEQJA")
			lRet:=.F.
		EndIf		
Next i
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650VldRot³ Autor ³ Rodrigo de A. Sartorio ³ Data ³ 20/09/96³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida o Roteiro de Operacoes.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650VldRot(cProduto,cRot)
Local aArea    := GetArea()
Local aRetEsp  := {}
Local cQuery   := ""
Local lQIPFRot := SuperGetMV("MV_QIPFROT",.F.,.T.)
Local lQipMat  := .T.

cQuery	:= "SELECT B1_COD, B1_TIPOCQ"
cQuery  += " FROM "+RetSqlName('SB1')+ " SB1 "
cQuery	+= " WHERE "
cQuery	+= " SB1.B1_FILIAL = '"+xFilial("SB1")+"' AND "
cQuery	+= " SB1.B1_COD = '"+AllTrim(cProduto)+"' AND "
cQuery	+= " SB1.D_E_L_E_T_ = ' ' "

cQuery := ChangeQuery(cQuery)
dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),"QRYSB1",.F.,.T.)

lQipMat :=  IIF((!Empty(QRYSB1->B1_COD) .AND. QRYSB1->B1_TIPOCQ <> 'Q' .AND. IsInCallStack("MATA173")),.F.,IntQIP(cProduto,,IIf(cModulo=="QIP","E","T")))

If lQipMat 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se existir a Integracao com o QIP, o Roteiro padrao sera sugerido a ³
	//³ partir da ultima revisao da Especificacao do produto.				³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aRetEsp := QIPRevRot(cProduto)
		
	If !lQIPFRot .And. cRot # aRetEsp[2]
		// Verifica se o roteiro utilizado possui especificacao
		dbSelectArea("QQK")
		dbSetOrder(1)
		If !dbSeek(xFilial("QQK")+cProduto+aRetEsp[1]+cRot) .AND. aRetEsp[2] != "**"
			cRot := aRetEsp[2]
		EndIF
	ElseIf  aRetEsp[2] != "**"
		cRot := aRetEsp[2]
	EndIf
Endif

QRYSB1->(DbCloseArea())

RestArea(aArea)
Return(cRot)

/*/{Protheus.doc} A650Rot()
Valida o Roteiro de Operacoes.
@author Rodrigo de A. Sartorio
@since 20/09/96
@return lRet
/*/
Function A650Rot()
	Local cRoteiro	:= &(ReadVar()),lRet:=.T.,nEndereco
	Local lGrade   	:= MaGrade()
	Local cProduto 	:= M->C2_PRODUTO
	Local aRetorno 	:= {}
	Local cRevisao 	:= " "  
	Local lIntQIP  	:= IntQIP(cProduto,,IIf(cModulo=="QIP","E","T"))
	Local lQIPFRot 	:= SuperGetMv("MV_QIPFROT",.F.,.T.)
	Local lPrototipo	:= IsProdProt(cProduto)

	If Empty(cProduto) .or. Empty(cRoteiro)
		Return .T.
	EndIf

	lret := a630SeekSG2(1,cProduto,xFilial("SG2")+cProduto+cRoteiro,/*04*/,/*05*/,/*06*/,.T.)  

	//Verifica se a grade esta ativa e se o produto digitado eh uma referencia
	If lRet .And. lIntQIP //Define o Roteiro de Operacoes definido na Especificacao
		aRetorno := QIPRevRot(M->C2_PRODUTO)
		cRevisao := aRetorno[1] 
		
		If !lQIPFRot .Or. lPrototipo
			If lQIPOPEPQP
				lRet := !QIPOPEPQPK(M->C2_PRODUTO, cRevisao, cRoteiro, .T.)
			Else
				dbSelectArea("QQK")
				dbSetOrder(1)
				If !dbSeek(xFilial("QQK")+M->C2_PRODUTO+cRevisao+cRoteiro)
					Help(" ",1,"C650NOREV") // "Nao existe especificação para esse Produto/Roteiro."
					lRet := .F.
				EndIF
			EndIf
		ElseIf !Empty(aRetorno[2])
			If M->C2_ROTEIRO # aRetorno[2] .AND. SuperGetMv("MV_QIPOPEP",.F.,"2") != "3"
				Help(" ",1,"C650NOPRI")
				lRet := .F.
			EndIf
		EndIf
	ElseIf lRet .And. lGrade .And. MatGrdPrrf(@cProduto)
		aRotGrade()
	EndIf

	If !RegistroOK("SG2",.F.)
	    Help(" ",1,"_MSBLQL",,STR0212,1,0) //"Roteiro encontra-se bloqueado para uso em seu cadastro."
		lRet := .F.
	EndIf

	If lRet .And. !Empty(M->C2_DATPRF)
		A650DatPrf("M->C2_DATPRF",M->C2_DATPRF,M->C2_PRODUTO,M->C2_QUANT, nil,cRoteiro)
	EndIf 

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650IniPrd³ Autor ³ Rodrigo de A. Sartorio³ Data ³ 29/04/97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina p/ inicializacao de alguns campos a partir do Prod. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650IniPrd()
Local cAlias		:= Alias()
Local nRecno		:= Recno()
Local nOrdem		:= IndexOrd()
Local cLocal		:= ""
Local cDescr		:= ""
Local cLocProc	:= GetMvNNR('MV_LOCPROC','99')
Local cUM			:= ""
Local c2UM			:= ""

dbSelectArea("SB1")
dbSetOrder(1)
If !__lAutomacao
	If MsSeek(xFilial()+&(ReadVar()))
		cLocal:=If(SB1->B1_APROPRI=="I",cLocProc,RetFldProd(SB1->B1_COD,"B1_LOCPAD"))
		cDescr:=SB1->B1_DESC
		cUM:=SB1->B1_UM
		c2UM:=SB1->B1_SEGUM  
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrdem)
dbGoTo(nRecno)
aCols[n,nPosLocal]:=cLocal
aCols[n,nPosDescr]:=cDescr
aCols[n,nPosUM	 ]:=cUM
aCols[n,nPos2UM  ]:=c2UM

If Existblock("A650LOCA")		// permite manipular o local
	cLocal := Execblock("A650LOCA",.F.,.F.)
	aCols[n,nPosLocal]:=cLocal
Endif

Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650LinOk ³ Autor ³ Rodrigo de A. Sartorio ³ Data ³ 20/09/96³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a Linha da Getdados                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650LinOk()
Local aSeq:={},i,lRet:=.T.,nAchoSeq:=0
Local lExe,lMTA650L := (ExistBlock( "MTA650L" ) )
Local cCod:=aCols[n,1]
Local cProcura:=""
Local nSaldoLote:=0,lSaldoEnd:=0,nAcumulado:=0
Local cAlias:=Alias(),nRecno:=Recno(),nOrder:=IndexOrd()
Local lEmpPrev:= If(SuperGetMV("MV_QTDPREV")== "S",.T.,.F.)
Local nBus := 0
Local lWmsNew := SuperGetMV("MV_WMSNEW",.F.,.F.)

If ! A650VldConsEst()
	Return(.F.)
Endif

For i:=1 To Len(aCols)
	If !aCols[i,Len(aCols[i])]
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Tratamento Utilizado para o Siga Pyme ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !__lPyme
			nAchoSeq:=ASCAN(aSeq,aCols[i,nPosTrt]+aCols[i,nPosCod]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocLz]+aCols[i,nPosnserie]+aCols[i,nPosLocal])
		Else
			nAchoSeq:=ASCAN(aSeq,aCols[i,nPosTrt]+aCols[i,nPosCod]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocal])
		EndIf	
		
		If (nAchoSeq = 0) 
		   For nBus:=1 To Len(aCols)
		   
		      If ((i # nBus) .and. !aCols[nBus,Len(aCols[nBus])] .and. (aCols[i,nPosCod]==aCols[nBus,nPosCod]) .and. (aCols[i,nPosTrt]== aCols[nBus,nPosTrt]) .and. ((aCols[i,nPosQuant] > 0 .and. aCols[nBus,nPosQuant] < 0) .or. (aCols[i,nPosQuant] < 0 .and. aCols[nBus,nPosQuant] > 0)))
		         nAchoSeq := nBus
		      Endif      
		   Next
		Endif 
		
		IF nAchoSeq > 0
			Help(" ",1,"A650SEQJA")
			lRet:=.F.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamento Utilizado para o Siga Pyme ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !__lPyme
				AADD(aSeq,aCols[i,nPosTrt]+aCols[i,nPosCod]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocLz]+aCols[i,nPosnSerie]+aCols[i,nPosLocal])
			Else
				AADD(aSeq,aCols[i,nPosTrt]+aCols[i,nPosCod]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocal])
			EndIf
		Endif
	EndIf
Next i

//Valida se o produto a ser empenhado é igual ao produto original - não pode permitir 
//devido Bloco K
IF lRet .And. !Empty(aCols[n,nPosPrdOrg]) .And. (aCols[n,nPosCod] == aCols[n,nPosPrdOrg])
	Help( ,, 'Help',,STR0217, 1, 0 ) //"Produto Empenhado não pode ser o mesmo do Produto Origem."     
	lRet:=.F.
EndIf

If !aCols[n,Len(aCols[n])]
	If Rastro(cCod)
		If Rastro(cCod,"L")
			aCols[n,nPosLote]:=CriaVar("D4_NUMLOTE")
			M->D4_NUMLOTE:=CriaVar("D4_NUMLOTE")
			If !Empty(aCols[n,nPosLotCtl])
			    If !__lPyme
					cProcura:=aCols[n,nPosCod]+aCols[n,nPosLocal]+aCols[n,nPosLotCtl]+aCols[n,nPosLocLz]+aCols[n,nPosnserie]
					nAcumulado:=0
					For i:=1 to Len(aCols)
						If !aCols[i,Len(aCols[i])] .And. cProcura == aCols[i,nPosCod]+aCols[i,nPosLocal]+aCols[i,nPosLotCtl]+aCols[i,nPosLocLz]+aCols[i,nPosnSerie]
							nAcumulado+=aCols[i,nPosQuant]
						EndIf
					Next I
				Else
					cProcura:=aCols[n,nPosCod]+aCols[n,nPosLocal]+aCols[n,nPosLotCtl]
					nAcumulado:=0
					For i:=1 to Len(aCols)
						If !aCols[i,Len(aCols[i])] .And. cProcura == aCols[i,nPosCod]+aCols[i,nPosLocal]+aCols[i,nPosLotCtl]
							nAcumulado+=aCols[i,nPosQuant]
						EndIf
					Next I				
				Endif
				nSaldoLote:=SaldoLote(aCols[n,nPosCod],aCols[n,nPosLocal],aCols[n,nPosLotCtl],,,,,dDataBase)
				If nSaldoLote < nAcumulado
					Help(" ",1,"A240LOTENE")
					lRet:=.F.
				EndIf
			EndIf
		Else
			If !Empty(aCols[n,nPosLote])
				dbSelectArea("SB8")
				dbSetOrder(2)
				If dbSeek(xFilial()+aCols[n,nPosLote]+aCols[n,nPosLotCtl]+aCols[n,nPosCod])
					cProcura:=aCols[n,nPosCod]+aCols[n,nPosLocal]+aCols[n,nPosLote]+aCols[n,nPosLotCtl]+aCols[n,nPosLocLz]+aCols[n,nPosnSerie]
					nAcumulado:=0
					For i:=1 to Len(aCols)
						If !aCols[i,Len(aCols[i])] .And. cProcura == aCols[i,nPosCod]+aCols[i,nPosLocal]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocLz]+aCols[i,nPosnSerie]
							nAcumulado+=aCols[i,nPosQuant]
						EndIf
					Next I
					If (SB8SALDO(,,,,,lEmpPrev,,,.T.) - (SB8SALDO(.T.,,,,,lEmpPrev,,,.T.)+B8_QACLASS+AvalQtdPre("SB8",1))) < nAcumulado
						Help(" ",1,"A240LOTENE")
						lRet:=.F.
					EndIf
				EndIf
			EndIf
		EndIf
	Else

		aCols[n,nPosLote]	:=CriaVar("D4_NUMLOTE")
		aCols[n,nPosLotCtl]	:=CriaVar("D4_LOTECTL")
		aCols[n,nPosdValid]	:=CriaVar("D4_DTVALID")
		M->D4_NUMLOTE:=CriaVar("D4_NUMLOTE")
		M->D4_LOTECTL:=CriaVar("D4_LOTECTL")
		M->D4_DTVALID:=CriaVar("D4_DTVALID")

	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tratamento referente a localizacao fisica  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Localiza(cCod,.T.) .And. lRet
		If lRet .And. (!Empty(aCols[n,nPosLocLz]) .Or. !Empty(aCols[n,nPosnSerie]))
			lRet:=MtAvlNSer(aCols[n,nPosCod],aCols[n,nPosnSerie],aCols[n,nPosQuant],aCols[n,nPosQtSegum])
			If lRet
				cProcura:=aCols[n,nPosCod]+aCols[n,nPosLocal]+aCols[n,nPosLote]+aCols[n,nPosLotCtl]+aCols[n,nPosLocLz]+aCols[n,nPosnSerie]
				nAcumulado:=0
				For i:=1 to Len(aCols)
					If !aCols[i,Len(aCols[i])] .And. cProcura == aCols[i,nPosCod]+aCols[i,nPosLocal]+aCols[i,nPosLote]+aCols[i,nPosLotCtl]+aCols[i,nPosLocLz]+aCols[i,nPosnSerie]
						nAcumulado+=aCols[i,nPosQuant]
					EndIf
				Next I
				If lWmsNew .And. IntWms(cCod)
					If  FindFunction("WMVlOPLtEn")
						If !WMVlOPLtEn(aCols[n,nPosLocal],aCols[n,nPosLocLz],cCod,aCols[n,nPosnSerie],aCols[n,nPosLotCtl],aCols[n,nPosLote],nAcumulado)
							lRet:=.F.
						EndIf
					EndIF 
				Else
					lSaldoEnd := SaldoSBF(aCols[n,nPosLocal],aCols[n,nPosLocLz],cCod,aCols[n,nPosnSerie],aCols[n,nPosLotCtl],aCols[n,nPosLote])
					If nAcumulado > lSaldoEnd
						Help(" ",1,"SALDOLOCLZ")
						lRet:=.F.
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf
If lRet .And. !aCols[n,Len(aCols[n])] .And. Localiza(cCod) .And. !Rastro(cCod)
	For i := 1 To Len(aCols)
		If i != n .And. !aCols[i,Len(aCols[i])]
			If AllTrim(aCols[n,nPosCod])+AllTrim(aCols[n,nPosLocal])+;
				AllTrim(aCols[n,nPosTrt])+AllTrim(aCols[n,nPosLotCtl])+AllTrim(aCols[n,nPosLote])+AllTrim(aCols[n,nPosLocLz])+AllTrim(aCols[n,nPosnserie]) == ;
				AllTrim(aCols[i,nPosCod])+AllTrim(aCols[i,nPosLocal])+;
				AllTrim(aCols[i,nPosTrt])+AllTrim(aCols[i,nPosLotCtl])+AllTrim(aCols[i,nPosLote])+AllTrim(aCols[i,nPosLocLz])+AllTrim(aCols[i,nPosnserie])
				Help(" ",1,"A650SEQJA")
				lRet:=.F.
				Exit
			EndIf
		EndIf
	Next i
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Permite ao usuario validar a linha atraves do ExecBlock MTA650L   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMTA650l
	lExe:=ExecBlock("MTA650L",.F.,.F.,{lRet})
	If ValType(lExe) == "L"
		lRet:=lExe
	EndIf
EndIf
dbSelectArea(cAlias)
dbSetOrder(nOrder)
dbGoto(nRecno)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650DelSD3 ³ Autor ³Rodrigo de A. Sartorio ³ Data ³ 13/11/98³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Acerto na exclusao de OPs e movimentos estornados no SD3    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A650DelSD3(cNumOp,lExcluiSC2,lExcGra)
Local aArea:={Alias(),IndexOrd(),Recno()}
Local nRegSD3 := 0     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se devera' ser excluido ou nao todos os itens da Grade ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lExcGra   := If(lExcGra == NIL, .F. , lExcGra) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se tem movimento                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD3")
dbSetOrder(1)
dbSeek(xFilial()+cNumOp)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se os movimentos existentes foram ³
//³ estornados                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Eof()
	nRegSD3:=Recno()
	lExcluiSC2:= IIf(SD3->D3_ESTORNO == "S",.T.,.F.)
	             
	While !Eof() .And. If(!lExcGra,SD3->D3_OP,left(SD3->D3_OP,len(cNumOp))) == cNumOp .And. lExcluiSC2
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ So nao e'permitida a exclusao quando exis -³
		//³ tir movimento que nao foi estornado        ³
		//³ lExclui = .T. indica que os movimentos des-³
		//³ OP foram estornados                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SD3->D3_ESTORNO != "S"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Existem movimentos que nao foram estornados³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lExcluiSC2:=.F.
		Endif
		dbSkip()
	End
	dbGoto(nRegSD3)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se nao foi apontada producao PCP ou se não há apontamento de perda ³
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
If lExcluiSC2
	dbSelectArea("SH6")
	If dbSeek(xFilial()+cNumOp)
		lExcluiSC2:=.F.
	EndIf
	dbSelectArea("SD3")

	dbSelectArea("SBC")
	If dbSeek(xFilial()+cNumOp)
		lExcluiSC2:=.F.
	EndIf
	dbSelectArea("SD3")
EndIf

If lExcluiSC2 .And. !Eof()
	//-- Na execucao de rotina automatica, sempre deleta as movimentacaoes conforme bops 00000127289                                                                                        
	If Type("l650Auto") == "L" .and. l650Auto
		lExcluiSD3 := .T.
	Else
		cText := STR0021+ cNumOp +STR0022+chr(10) + chr(13)	//"A Ordem de Produ‡„o "###" possui movimenta‡”es estornadas"
		cText += STR0023 + chr(10) + chr(13)	//"Deseja deletar as movimenta‡”es de estorno e a Ordem de Produ‡„o ? "
		lExcluiSD3 := (MsgYesNo(OemToAnsi(cText),OemToAnsi(STR0024)))	//"Aten‡„o"
		lExcluiSC2 := lExcluiSD3
	EndIf
	If lExcluiSD3
		While !Eof() .And. If(!lExcGra,SD3->D3_OP,left(SD3->D3_OP,len(cNumOp))) == cNumOp 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta todas as movimentacoes referente a esta OP   que  ³
			//³ foram estornadas                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock("SD3",.F.,.T.)
			dbDelete()
			MsUnLock()
			dbSkip()
		End
	Endif
Endif
dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³aRotGrade ³ Autor ³Patricia A. Salomao    ³ Data ³30/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Se o produto for Referencia, abre Grade para digitar o      ³±±
±±³          ³Roteiro de Operacoes                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Sempre .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³24/07/2006³Erike Yuri     ³Alteracao total da rotina para uso de Objeto³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function aRotGrade()
Local cProdRef	:= M->C2_PRODUTO
Local lGrade	:= MaGrade() 
Local aColsCop	:= {}
Local lRet		:= .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a grade esta ativa e se ja montou a grade³
//³ de produtos desta OP                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
If ( lGrade ) .And.  MatGrdPrrf(@cProdRef) 
	
	If Empty(oGrade:aColsGrade)
        Aviso(STR0079,STR0084 ,{'Ok'}) 
        M->C2_ROTEIRO := Space(Len(SC2->C2_ROTEIRO))
        lRet := .F.
	Else                                    
		//Guarda ambiente  
		If Type('aCols')=='A'
			aColsCop 		:= AClone(aCols)	
		EndIf
			
		oGrade:cTudoOk 	:= "a650RotaOk()"
	
		lRet := oGrade:Show("C2_ROTEIRO")     
			
		//Restaura tudo Ok
		oGrade:cTudoOk := "AllwaysTrue"
		If Type('aCols')=='A'
			aCols := AClone(aColsCop)
		EndIf
	EndIf
EndIf
M->C2_ROTEIRO := Space(Len(SC2->C2_ROTEIRO))
Return ( lRet )



/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a650RotaOk³ Autor ³Patricia A. Salomao    ³ Data ³30/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida os dados digitados na Grade                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Logico                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³24/07/2006³Erike Yuri     ³Alteracao da rotina para utilizacao de objeto±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a650RotaOk()
Local oDlg
Local nLinha, nColuna 
Local cProd 
Local aLogs	:= {}
Local lRet  := .T.

If !__lAutomacao
	cProd := Substr(M->C2_PRODUTO,1,oGrade:TamRef())              
Else
	cProd := "0000004"
EndIf

For nLinha :=  1 To Len(aCols)
	For nColuna := 2 To Len(aHeader)	
		If !__lAutomacao
			cProdRef := oGrade:GetNameProd(cProd,nLinha,nColuna) 
			nQuant   := oGrade:aColsFieldByName("C2_QUANT",1,nLinha,nColuna)              
		Else
			cProdRef := " "
			nQuant   := 1 
		EndIf
		
		If nQuant <> 0 
			aCols[nLinha][nColuna] := A650VldRot(cProdRef,aCols[nLinha][nColuna])
			dbSelectArea("SG2")
			dbSetOrder(1)
			If !__lAutomacao
				//VERIFICA PRIMEIRO SE TEM ROTEIRO PARA O PRODUTO COMPLETO(GRADE + LINHA + COLUNA)
				If !(dbSeek(xFilial("SG2")+cProdRef+aCols[nLinha][nColuna]))
					dbSetOrder(7)
					//SE NÃO ENCONTRA, PROCURA O ROTEIRO USANDO A REFERENCIA DA GRADE
					If !(dbSeek(xFilial("SG2")+PadR(cProd,Len(G2_REFGRD))+aCols[nLinha][nColuna]))
						//SE NÃO ENCONTRA, GRAVA LOG DE ERRO
						AADD(aLogs,{cProdRef,aCols[nLinha][nColuna]})
					EndIf
				EndIf
			EndIf
		Endif	
	Next nColuna
Next nLinha
	
If Len(aLogs) > 0
    lRet := .F.
	DEFINE MSDIALOG oDlg FROM  190,9 TO 310,450 TITLE STR0079 PIXEL	//"ATENCAO
	@ 20,015 SAY oSay VAR STR0080 SIZE 268, 8 OF oDlg PIXEL //"Foram Informados Roteiros de Operacao Invalidos ..."
	oSay:SetColor(CLR_HRED,GetSysColor(15))
	@ 038,305 BTNBMP oBtn1 RESOURCE "S4WB016N" SIZE 25,25 DESIGN ACTION A650RotInv(aLogs) OF oDlg
	DEFINE SBUTTON FROM 45, 180 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg CENTERED
EndIf    
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a650RotInv³ Autor ³Patricia A. Salomao    ³ Data ³31/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Abre janela contendo os roteiros invalidos que foram infor-³±±
±±³          ³ mados.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function a650RotInv(aLogs)
LOCAL oDlg,oQual,cVarq
DEFINE MSDIALOG oDlg TITLE STR0081 From 130,70 To 350,360 OF oMainWnd PIXEL 
@ 10,13 TO 90,122 LABEL "" OF oDlg  PIXEL
@ 20,18 LISTBOX oQual VAR cVarQ Fields HEADER STR0018,STR0082 SIZE 100,62 NOSCROLL OF oDlg PIXEL //Produto, Recurso
oQual:SetArray(aLogs)
oQual:bLine := { || {aLogs[oQual:nAt,1],aLogs[oQual:nAt,2]}}
DEFINE SBUTTON FROM 95,90 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg PIXEL
ACTIVATE MSDIALOG oDlg
RETURN


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ViewLocalz³ Autor ³ Erike Yuri da Silva   ³ Data ³ 19/04/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Faz a consulta de localizacoes por produto especifico para ³±±
±±³          ³ a getdados da alteracao de empenhos.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cProd = Codigo do produto a ser analisado                  ³±±
±±³          ³ cArm  = Codigo do armazem                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ GENERICO                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ViewLocalz( cProd, cArm )

LOCAL aArrayF4:={},aArrayF4NS:={},nX, cVar
LOCAL cProduto:="" , nPosProd:=0, cLocal:="", nPosLocal:=0, nPosLocaliz:=0, nPosQuant:=0,nPosNumSer:=0
LOCAL nLote     := 0
LOCAL nQuant    := 0
LOCAL nQuantLoc := 0
LOCAL nAcho
LOCAL nEndereco
LOCAL cChave1
LOCAL cChave2
LOCAL cLocaliza  := ""
LOCAL lGetDados := .F.
LOCAL aUsado   := {}
LOCAL nPosNumLote
LOCAL nPosLoteCtl
LOCAL lLote     := .F.
LOCAL cQuant, nOAT 
LOCAL lSaida    := .F.
LOCAL oDlg
Local nOpcA      := 0
Local aPosSBF , aArea:=GetArea()
Local lShowNSeri := .F.
Local cNumSerie  := CriaVar( "BF_NUMSERI", .F. ) 
Local nNumSerie  := 0
Local dDtValid   := CTOD('  /  /  ')
Local aAreaSB8   := SB8->(GetArea())


cProduto := cProd
cLocal   := cArm
cNumLote := ""
cLoteCtl := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Lista apenas se o produto tiver controle de localizacao     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Localiza( cProduto )
	Help( " ", 1, "F4LOCALIZ" )  
	RestArea(aArea)
	Return .F.
EndIf         

If Rastro( cProduto )
	If !Empty( If( Rastro( cProduto, "S" ), cNumLote, cLoteCtl ) )
		lLote := .T.
	EndIf
EndIf
aPosSBF := SBF->(GetArea())
dbSelectArea("SBF")
cChave2 := xFilial( "SBF" ) + cProduto + cLocal
cCompara:= "BF_FILIAL+BF_PRODUTO+BF_LOCAL"
dbSetOrder(2)	
If lLote
	If Rastro(cProduto,"S")
		cCompara+="+BF_LOTECTL+BF_NUMLOTE"
		cChave2 +=cLoteCtl + cNumLote
	Else
		cCompara+="+BF_LOTECTL"
		cChave2 +=cLoteCtl
	EndIf
EndIf		
dbSeek(cChave2)	
While !SBF->( Eof() ) .And. cChave2 == &(cCompara)	
	nSaldoLoc  := SBF->BF_QUANT - (SBF->BF_EMPENHO+AvalQtdPre("SBF",1))
	nSaldoLoc2 := SBF->BF_QTSEGUM - (SBF->BF_EMPEN2+AvalQtdPre("SBF",1,.T.))
	If QtdComp(nSaldoLoc) > QtdComp(0)
		nScan := AScan( aUsado, { |x| x[1] == SBF->BF_LOCALIZ .And. If(lLote,If(Rastro(cProduto,"S"),x[3]==SBF->BF_LOTECTL.And.x[4]==SBF->BF_NUMLOTE,x[3]==SBF->BF_LOTECTL),.T.) .And. x[5] == If(nPosNumSer>0,SBF->BF_NUMSERI,"")} )
		If nScan <> 0
			nSaldoLoc  -= aUsado[ nScan, 2 ]
			nSaldoLoc2 -= ConvUM(cProduto, aUsado[ nScan, 2 ], 0, 2)
		EndIf
	EndIf		
	If nSaldoLoc > 0  
		dDtValid   := CTOD('  /  /  ')
		If Rastro(cProduto)
			dbSelectArea("SB8")
			dbSetOrder(3)                 
			If dbSeek(xFilial("SB8")+cProduto+SBF->BF_LOCAL+SBF->BF_LOTECTL+If(Rastro(cProduto,"S"),SBF->BF_NUMLOTE,""))
				dDtValid:=B8_DTVALID
			EndIf	
		EndIf         
		dbSelectArea("SBF")
		AAdd(aArrayF4NS,{SBF->BF_LOCALIZ,SBF->BF_NUMSERI,TransForm(nSaldoLoc,PesqPict("SBF","BF_QUANT",13)),TransForm(nSaldoLoc2,PesqPict("SBF","BF_QUANT",13)),SBF->BF_LOTECTL,SBF->BF_NUMLOTE,dDtValid})
		AAdd(aArrayF4,{SBF->BF_LOCALIZ,TransForm(nSaldoLoc,PesqPict("SBF","BF_QUANT",13)),TransForm(nSaldoLoc2,PesqPict("SBF","BF_QUANT",13)),SBF->BF_LOTECTL,SBF->BF_NUMLOTE,dDtValid})
		If !Empty(SBF->BF_NUMSERI)
			lShowNSeri := .T.
		EndIf	
	EndIf
	SBF->( dbSkip() )
EndDo			
If lShowNSeri
	aArrayF4:=ACLONE(aArrayF4NS)	
EndIf
If Len( aArrayF4 ) > 0
	nOpcA := 0
	cCadastro := OemToAnsi(STR0109)//"Saldos por Localizacao"
	DEFINE MSDIALOG oDlg TITLE cCadastro From 09,0 To 33,75 OF oMainWnd
	@ 1.1,  .7  Say OemToAnsi(STR0110) //"Produto :"
	@ 1  , 3.8  Get cProduto When .F.
	If lShowNSeri
		@ 2.4,.7 LISTBOX oQual VAR cVar Fields HEADER OemToAnsi(STR0111),OemToAnsi(STR0112),OemToAnsi(STR0113),OemToAnsi(STR0114),RetTitle("BF_LOTECTL"),RetTitle("BF_NUMLOTE"),RetTitle("B8_DTVALID") SIZE 285,140 ON DBLCLICK (nOpca := 1,oDlg:End()) //"Localizacao"###"Numero de Serie"###"Saldo"###"Saldo 2aUM"
	Else
		@ 2.4,.7 LISTBOX oQual VAR cVar Fields HEADER OemToAnsi(STR0111),OemToAnsi(STR0113),OemToAnsi(STR0114),RetTitle("BF_LOTECTL"),RetTitle("BF_NUMLOTE"),RetTitle("B8_DTVALID") SIZE 285,140 ON DBLCLICK (nOpca := 1,oDlg:End())
	EndIf	
	oQual:SetArray(aArrayF4)
	If lShowNSeri
		oQual:bLine:={ ||{aArrayF4[oQual:nAT,1],aArrayF4[oQual:nAT,2],aArrayF4[oQual:nAT,3],aArrayF4[oQual:nAT,4],aArrayF4[oQual:nAT,5],aArrayF4[oQual:nAT,6],aArrayF4[oQual:nAT,7]}}
	Else
		oQual:bLine:={ ||{aArrayF4[oQual:nAT,1],aArrayF4[oQual:nAT,2],aArrayF4[oQual:nAT,3],aArrayF4[oQual:nAT,4],aArrayF4[oQual:nAT,5],aArrayF4[oQual:nAT,6]}}
	EndIf	
	DEFINE SBUTTON FROM 06  ,264  TYPE 1 ACTION (nOpca := 1,oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM 18.5,264  TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg VALID (nOAT := oQual:nAT,.t.) CENTERED
	If nOpca == 1
		If lShowNSeri
			cLocaliza := aArrayF4[ nOAT, 1 ]
			cNumSerie := aArrayF4[ nOAT, 2 ]
			cLoteCtl  := aArrayF4[ nOAT, 5 ]
			cNUMLote  := aArrayF4[ nOAT, 6 ]
			dDtValid  := aArrayF4[ nOAT, 7 ]
			cQuant    := aArrayF4[ nOAT, 3 ]
			cQuant    := StrTran( cQuant, ".", ""  )
			cQuant    := StrTran( cQuant, ",", "." )
			nQuantLoc := Val( cQuant )
		Else
			cLocaliza := aArrayF4[ nOAT, 1 ]
			cLoteCtl  := aArrayF4[ nOAT, 4 ]
			cNUMLote  := aArrayF4[ nOAT, 5 ]
			dDtValid  := aArrayF4[ nOAT, 6 ]
			cQuant    := aArrayF4[ nOAT, 2 ]
			cQuant    := StrTran( cQuant, ".", ""  )
			cQuant    := StrTran	( cQuant, ",", "." )
			nQuantLoc := Val( cQuant )
		EndIf	 
	Else          
		RestArea(aPosSBF)
		RestArea(aAreaSB8)
		Return .F.
	EndIf

	If !Empty(cLocaliza) .Or. !Empty(cNumSerie)
		aCols[n,nPosLocLz] := cLocaliza
	EndIf
	RestArea(aPosSBF)
	RestArea(aAreaSB8)
EndIf
RestArea(aArea)
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A650AETdOk³ Autor ³ Erike Yuri da Silva    ³ Data ³ 19/04/05³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao tudo ok para GetDados da Alteracao do Empenho    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA650                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/       
Function A650AETdOk()
Local nI := 0
Local nX := 0
Local lRet := .T.
If !A650LinOk()    
	Return .F.
EndIf
For nI := 1 To Len(aCols)
	If !aCols[nI,Len(aCols[nI])] .And. Localiza(aCols[nI,nPosCod]) .And. !Rastro(aCols[nI,nPosCod])
		For nX := nI To Len(aCols)
			If nX != nI .And. !aCols[nX,Len(aCols[nX])]
				If AllTrim(aCols[nI,nPosCod])+AllTrim(aCols[nI,nPosLocal])+;
					AllTrim(aCols[nI,nPosTrt])+AllTrim(aCols[nI,nPosLotCtl])+AllTrim(aCols[nI,nPosLote])+AllTrim(aCols[nI,nPosLocLz])+AllTrim(aCols[nI,nPosnserie]) == ;
					AllTrim(aCols[nX,nPosCod])+AllTrim(aCols[nX,nPosLocal])+;
					AllTrim(aCols[nX,nPosTrt])+AllTrim(aCols[nX,nPosLotCtl])+AllTrim(aCols[nX,nPosLote])+AllTrim(aCols[nX,nPosLocLz])+AllTrim(aCols[nX,nPosnserie])
					Help( ,, 'Help',, STR0190 + AlLTrim(aCols[nI,nPosCod]) + STR0191, 1, 0 ) //"Já existe um empenho do material '" XXX "' com a mesma sequência." 
					lRet := .F.
					Exit
				EndIf
			EndIf
		Next nX
		If !lRet
			Exit
		EndIf
	EndIf
Next nI
Return lRet

/*/{Protheus.doc} A650DelOK
Validacao da exclusão de linha no GetDados da Alteracao do Empenho 

@type  Static Function
@author juliana.oliveira
@since 14/05/2024
@version P12
/*/
Function A650DelOK(nOrigem)
Local lRet := .T.

Default nOrigem := 0

If nOrigem == 1
   lRet := .F.
ElseIf nOrigem == 2
    If (aCols[n,Len(aCols[n])]) == .T.
      lRet := ExistCpo("SB1",(aCols[n,nPosCod]),1)
   EndIf
EndIf

Return lRet

