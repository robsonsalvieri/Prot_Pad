#Include "MATA730.CH"
#include "PROTHEUS.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA730  ³ Autor ³ Marcos Bregantin      ³ Data ³          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gera carga Acumulativa                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Edson   M.   ³16/01/98³XXXXXX³Inclusao do campo C2_SLDOP.              ³±±
±±³Edson   M.   ³03/02/98³XXXXXX³Subst. do Campo C2_SLDOP por funcao.     ³±±
±±³Viviani      ³08/01/99³Melhor³Nova criacao de dialogo (Protheus)       ³±±
±±³Cesar Valadao³29/06/99³XXXXXX³Acerto de ZAP Para TopConnect.           ³±±
±±³             ³        ³      ³Acerto de Cond de IndRegua P/ Top (SC2). ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Descri‡…o ³ PLANO DE MELHORIA CONTINUA                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ITEM PMC  ³ Responsavel              ³ Data         |BOPS:		      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³      01  ³                          ³              |                  ³±±
±±³      02  ³Erike Yuri da Silva       ³20/02/2006    |00000091869       ³±±
±±³      03  ³                          ³              |                  ³±±
±±³      04  ³                          ³              |                  ³±±
±±³      05  ³                          ³              |                  ³±±
±±³      06  ³                          ³              |                  ³±±
±±³      07  ³                          ³              |                  ³±±
±±³      08  ³                          ³              |                  ³±±
±±³      09  ³Erike Yuri da Silva       ³20/02/2006    |00000091869       ³±±
±±³      10  ³                          ³              |                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATA730
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL nOpca := 0
LOCAL nSaldo, cMarca
Local aSays:={}, aButtons := {}
LOCAL cCondicao
LOCAL oDlg

PRIVATE lInverte := .T.
PRIVATE cCadastro:= OemToAnsi(STR0001)	//"Carga Acumulativa"
PRIVATE aInd := {}, cIndex                           
PRIVATE aRotina := {	{STR0002,"A730Pesqui", 0 , 1},;	//"Pesquisar"
{STR0003,"A730Calculo", 0 , 0}}	//"Calculo"

Pergunte("MTA730",.T.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01 // data de Calculo inicial                          ³
//³ mv_par02 // data de Calculo Final                            ³
//³ mv_par03 // Data entrega OP de                               ³
//³ mv_par04 // Data entrega OP ate                              ³
//³ mv_par05 // Numero da OP de                                  ³
//³ mv_par06 // Numero da OP ate                                 ³
//³ mv_par07 // Saldo a considerar 1 = OP 2 = Operacao           ³
//³ mv_par08 // Considera informacoes 1 Carga maquina 2 Roteiro  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOpca := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Nova forma de criar dialogos para processos Batch            ³
//³ COMPATIVEL COM PROTHEUS (BOF)                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aSays,OemToAnsi( STR0004 ) )
AADD(aSays,OemToAnsi( STR0005 ) )
AADD(aButtons, { 5,.T.,{|| pergunte("MTA730") } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1, o:oWnd:End() } } )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
FormBatch( cCadastro, aSays, aButtons )
If !ChkFile("SHH",.T.) .or. IsLockSH8(.F.)
	Help(" ",1,"SH8EmUso")
ElseIf nOpca == 1
	A730Limpa()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retira o SHH da variavel cFopened ref. a abertura no MNU     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SHH")
dbCloseArea()
ClosFile("SHH")

dbSelectArea("SC2")
RetIndex("SC2")

dbClearFilter()
dbSetorder(1)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A730pesqui³ Autor ³ Waldemiro L. Lustosa  ³ Data ³ 22.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisa usando indice condicional                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ A730Pesqui                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MAtA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A730Pesqui()
Local cAlias,cCampo,nReg
Local cFil:="",nChave:=1,dCampo
Local cIndex,nIndex,cKey,cOrd
Local oDlg, nOpca
Local ni
PRIVATE aOrd := { }
cAlias  := ALIAS()
nOpt1   := 1
nMaiorStr := 0
PesqOrd(cAlias)
cOrd	  := aOrd[1]
cCampo  := Space(40)
For ni:=1 to Len(aOrd)
	aOrd[nI] := OemToAnsi(aOrd[nI])
Next
If IndexOrd() >= Len(aOrd)
	cOrd := aOrd[Len(aOrd)]
ElseIf IndexOrd() <= 1
	cOrd := aOrd[1]
Else
	cOrd := aOrd[IndexOrd()]
Endif

DEFINE MSDIALOG oDlg FROM 5, 5 TO 14, 50 TITLE OemToAnsi(STR0006)	//"Pesquisar"
@ 0.6,1.3 COMBOBOX oCBX VAR cOrd ITEMS aOrd  SIZE 165,44  ON CHANGE (nOpt1:=oCbx:nAt)  OF oDlg FONT oDlg:oFont
@ 2.1,1.3 MSGET cCampo SIZE 165,10
DEFINE SBUTTON FROM 055,122   TYPE 1 ACTION (nOpca := 1,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 055,149.1 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
ACTIVATE MSDIALOG oDlg CENTERED
If nOpca == 0
	Return 0
Endif
A730MontPes()

nReg := RecNo()
IF LastKey() != 27
	Set Softseek On
	IF SubStr(cAlias,1,3) != "SM2"
		cFil:=xFilial(cAlias)
		nChave:=11
	EndIF
	IF SubStr(IndexKey(nOpt1),nChave,4) == "DTOS"
		dCampo:=Ctod(AllTrim(cCampo))
		dbSeek(cFil+DTOS(dCampo))
	Else
		dbSeek(xFilial(cAlias)+trim(cCampo))
	Endif
	IF SubStr(cAlias,1,3) == "SM2"
		dCampo:=Ctod(AllTrim(cCampo))
		dbSeek(dCampo)
	EndIF
EndIF
IF Eof()
	Go nReg
	Help(" ",1,"PESQ01")
EndIF
Set SoftSeek Off
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A730SelOp ³ Autor ³ Marcos Bregantim      ³ Data ³10/06/94  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica selecao de Op's                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A730SelOp()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A730SelOP()
LOCAL lRet := .f.,cAlias := Alias()

dbSelectArea("SC2")
cCondicao := 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD >= mv_par05 .and. C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <= mv_par06 .and. '
cCondicao += 'C2_DATPRF >= mv_par03 .and. C2_DATPRF <= mv_par04 .and. Empty(C2_DATRF) .and. C2_STATUS != "D" .and. '
cCondicao += 'C2_FILIAL == xFilial("SC2")'
If ExistBlock("A730FSC2")
	cRetBlock := ExecBlock("A730FSC2",.F.,.F.,cCondicao)
	If ValType(cRetBlock) == "C"
		cCondicao := cRetBlock
	EndIf
EndIf
If &(cCondicao)
	lRet := .t.
Endif
dbSelectArea(cAlias)
Return (lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A730QtdOp ³ Autor ³ Marcos Bregantim      ³ Data ³10/06/94  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica saldo da operacao a ser alocado                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A730QtdOp(ExpA2,ExpN1)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA690                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A730QtdOp(cOperac)
LOCAL	nSalvaRec, nQuant:=0, cNumOp := SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD

If mv_par07 == 1
	nQuant := SC2->C2_QUANT - SC2->C2_QUJE - SC2->C2_PERDA
Else
	nSalvaRec := SH6-> ( RecNo() )
	SH6-> ( dbSetOrder(1) )
	SH6-> ( dbSeek(xFilial("SH6")+cNumOp+SC2->C2_PRODUTO+cOperac) )
	While !SH6-> ( Eof() ) .And. SH6->H6_FILIAL+SH6->H6_OP+SH6->H6_PRODUTO+SH6->H6_OPERAC == xFilial("SH6")+cNumOp+SC2->C2_PRODUTO+cOperac
		nQuant += SH6->H6_QTDPROD
		SH6-> ( dbSkip() )
	End
	nQuant := SC2->C2_QUANT - nQuant
	SH6-> ( dbGoto(nSalvaRec) )
Endif
Return nQuant

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A730Uteis ³ Autor ³ Marcos Bregantim      ³ Data ³10/06/94  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a quantidade de horas uteis                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A730Uteis()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A730Uteis()
LOCAL nHoras:=0, cAlias := Alias()
Local ni                           
Local lA730HNUteis	:= ExistBlock("A730HNUT") //PE que indica as horas nao uteis a serem descontadas no dia.
Local nHNUteis		:= 0  // Varivel numerica que indicara o numero de horas a ser descontadas no dia

If mv_par02 >= mv_par01
	For nI:=mv_par01 to mv_par02
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ PE que indica as horas nao uteis a serem descontadas no dia. ³
		//³ Caso o retorno do ponto de entrada seja maior que a hora util³		
		//³ definida no cadastro SHB, o valor da hora inutil sera descon-³		
		//³ ciderado assumindo valor ZERO. Por premissa o retorno do PE  ³		
		//³ deve ser possitivo.(vide documentacao no DEM)                ³		
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lA730HNUteis
			nHNUteis := ExecBlock("A730HNUT",.F.,.F.,{nI})
			If ValType(nHNUteis)#"N" 
				nHNUteis := 0	
			EndIf
		EndIf
		If Dow(nI) == 1
			nHoras += SHB->HB_HRUTDOM-If(nHNUteis > SHB->HB_HRUTDOM,0,nHNUteis)
		ElseIf Dow(nI) == 7
			nHoras += SHB->HB_HRUTSAB-If(nHNUteis > SHB->HB_HRUTSAB,0,nHNUteis)
		Else
			nHoras += SHB->HB_HRUT-If(nHNUteis > SHB->HB_HRUT,0,nHNUteis)
		Endif
	Next
Endif
dbSelectArea(cAlias)
Return (nHoras)
	
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A730Calculo ³ Autor ³ Marcelo Iuspa         ³ Data ³20/09/00³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a funcao A740RunCalc usando regua de processamento   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A730Calculo(cAlias,cCampo,nOpcE,cMarca,lInverte)           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A730Calculo(cAlias,cCampo,nOpcE,cMarca,lInverte)
Processa({|| A730RunCalc(cAlias,cCampo,nOpcE,cMarca,lInverte)})
CloseBrowse()
RETURN

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A730RunCalc ³ Autor ³ Marcos Bregantim      ³ Data ³10/06/94³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica saldo da operacao a ser alocado                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := A730RunCalc(ExpA2,ExpN1)                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A730RunCalc(cAlias,cCampo,nOpcE,cMarca,lInverte)
Local aAreaSC2  := {}
Local aAreaSH8  := {}
Local aFilOper[2]      //-- Variavel utilizada para P.E. A730FILT
Local cAliasTmp := ""
Local cFiltSG2	:= ".T." //-- Variavel utilizada para P.E. A730FILT
Local cFiltSH8	:= ".T." //-- Variavel utilizada para P.E. A730FILT
Local cHrFim	:= ""
Local cHrIni	:= ""
Local cQuery    := ""
Local dDtFim	:= Nil
Local dDtOp		:= Nil
Local lA730FOPE	:= ExistBlock("A730FOPE")
Local lApsInUse := SuperGetMV("MV_APS",.F.,"") == "TOTVS"
Local lInclui	:= .F.                   
Local lLoop		:= .F.
Local nRend		:= 0                      
Local nUso		:= 0

PRIVATE cTipoTemp := GetMV("MV_TPHR")

ProcRegua(SC2->(LastRec()))

SH8->(DbsetOrder(1))
SHH->(DbsetOrder(1))

dbSelectArea("SHB")
dbSeek(xFilial("SHB"))
While !Eof() .and. HB_FILIAL == xFilial("SHB")
	RecLock("SHB",.F.)
	Replace HB_USO   With 0
	Replace HB_UTEIS With A730Uteis()
	MsUnLock()
	dbSkip()
End

dbSelectArea("SC2")
SC2->(DbSetOrder(1))
dbSeek(xFilial("SC2"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ PE utilizado para filtrar as Operacoes que deveram entrarem    ³
//³ no Carga Acumulativa. Podera aplicar-se o filtro na tabela SH8 ³		
//³ e/ou SG2, dependendo da opcao setada no parametro MV_PAR08.    ³		
//³ Devera retorna um Array contendo dois elementos onde:          ³		
//³ Elemento 1 => Expressao (Caracter) para filtro do SH8.         ³
//| Elemento 2 => Expressao (Caracter) para filtro do SG2.         ³		
//³ (Vide documentacao no DEM)                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("A730FILT")
	aFilOper := ExecBlock("A730FILT",.F.,.F.,MV_PAR08)
	If ValType(aFilOper) == "A" .and. Len(aFilOper) == 2
		cFiltSH8:= If(ValType(aFilOper[1]) == "C" .And. !Empty(aFilOper[1]), aFilOper[1], cFiltSH8)
		cFiltSG2:= If(ValType(aFilOper[2]) == "C" .And. !Empty(aFilOper[2]), aFilOper[2], cFiltSG2)
	EndIf
EndIf

If mv_par08 == 1

	cAliasTmp := GetNextAlias()

	cQuery := " SELECT SC2.*, SC2.R_E_C_N_O_ C2REC "
	CQuery += " FROM "+RetSqlName("SC2")+" SC2 "
	CQuery += " WHERE SC2.C2_FILIAL   = '"+xFilial("SC2")+ "' "
	cQuery += " AND SC2.D_E_L_E_T_  = ' ' "

	cQuery    := ChangeQuery(cQuery)

	dbUseArea( .T., 'TOPCONN', TcGenQry(,,cQuery), cAliasTmp, .F., .F. )

	While !(cAliasTmp)->(EOF())
		IncProc()
		nRecSC2:=(cAliasTmp)->C2REC
		DbSelectArea("SC2")
        SC2->(DbGoto(nRecSC2))
		If !SH8->(DbSeek(xFilial("SH8")+(cAliasTmp)->(C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD)))
			(cAliasTmp)->(DbSkip())
			Loop
		EndIf
		IF !IsMark("C2_OK",cMarca,lInverte)
			(cAliasTmp)->(DbSkip())
			Loop
		Endif
		While !SH8->(EOF()) .And. SH8->H8_FILIAL == xFilial("SH8") .And. SH8->H8_OP == (cAliasTmp)->(C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD)
			If ( SH8->H8_DTINI >= mv_par01 .And. SH8->H8_DTINI <= mv_par02 ) .Or. ;
				( SH8->H8_DTFIM >= mv_par01 .And. SH8->H8_DTFIM <= mv_par02 ) .Or. ;
				( SH8->H8_DTINI < mv_par01  .And. SH8->H8_DTFIM >  mv_par02 )
				If &(cFiltSH8) //-- Filtro aplicado pelo P.E. A730FILT 
					For dDtOp := SH8->H8_DTINI To SH8->H8_DTFIM
						If dDtOp == SH8->H8_DTINI
							cHrIni := SH8->H8_HRINI
						Else
							cHrIni := "00:00"
						EndIf
						If dDtOp == SH8->H8_DTFIM
							dDtFim := dDtOp
							cHrFim := SH8->H8_HRFIM
						Else
							dDtFim := dDtOp + 1
							cHrFim := "00:00"
						EndIf
						aAreaSH8 := SH8->(GetArea())
						aAreaSC2 := (cAliasTmp)->(GetArea())
						If lApsInUse
							nUso := A680TimeCale(dDtOp,cHrIni,dDtFim,cHrFim,SH8->H8_RECURSO)
						Else
							nUso := R815Dur(SH8->H8_RECURSO,dDtFim,cHrFim,dDtOp,cHrIni,"C")
						EndIf
						RestArea(aAreaSH8)
						RestArea(aAreaSC2)
						If nUso > 0
							dbSelectArea("SHH")
							lInclui:=.T.
							DbSeek(xFilial("SHH")+DToS(dDtOp)+SH8->H8_CTRAB+SH8->H8_RECURSO+SH8->H8_OP)
							While !Eof() .And. xFilial("SHH")+DToS(dDtOp)+SH8->H8_CTRAB+SH8->H8_RECURSO+SH8->H8_OP == HH_FILIAL+DTOS(HH_DATA)+HH_CTRAB+HH_RECURSO+HH_OP
								// Checa a existencia da mesma operacao
								If HH_OPER == SH8->H8_OPER
									lInclui:=.F.
									Exit
								EndIf
								dbSkip()
							End
							RecLock("SHH",lInclui)
							Replace HH_FILIAL 	With xFilial("SHH")
							Replace HH_DATA 	With dDtOp
							Replace HH_OP 		With SH8->H8_OP
							Replace HH_OPER 	With SH8->H8_OPER
							Replace HH_RECURSO 	With SH8->H8_RECURSO
							Replace HH_FERRAM 	With SH8->H8_FERRAM
							Replace HH_CTRAB 	With SH8->H8_CTRAB
							Replace HH_USO 		With HH_USO+nUso
							MsUnLock()
							dbSelectArea("SHB")
							dbSetOrder(1)
							If dbSeek(xFilial("SHB")+SH8->H8_CTRAB)
								dbSelectArea("SHB")
								RecLock("SHB",.f.)
								Replace HB_USO With HB_USO+nUso
								MsUnLock()
							Endif
						EndIf
					Next
				EndIf
			EndIf
			SH8->(DbSkip())
		EndDo
		(cAliasTmp)->(DbSkip())
	EndDo

	(cAliasTmp)->( DbCloseArea() )
Else
	nOpt1 := 1
	A730MontPes()
	dbSelectArea("SC2")
	dbSeek(xFilial("SC2"))
	While !SC2->(EOF())
		IncProc()
		IF !IsMark("C2_OK",cMarca,lInverte)
			dbSkip()
			Loop
		Endif
		If Empty(cRoteiro := SC2->C2_ROTEIRO)
			dbSelectArea('SB1')
			If dbSeek(xFilial('SB1') + SC2->C2_PRODUTO, .F.)
				If !Empty(SB1->B1_OPERPAD)
					cRoteiro := SB1->B1_OPERPAD
				Else
					cRoteiro:='01'
				EndIf
			Else
				cRoteiro := '01'
			EndIf
		EndIf
		dbSelectArea("SG2")
		dbSetOrder(1)
		dbSeek(xFilial("SG2")+SC2->C2_PRODUTO+cRoteiro,.F.)
		While !Eof() .and. G2_FILIAL == xFilial("SG2") .and. G2_PRODUTO == SC2->C2_PRODUTO .And. SG2->G2_CODIGO == cRoteiro
			If &(cFiltSG2) //-- Filtro aplicado pelo P.E. A730FILT 
				dbSelectArea("SHB")
				dbSetOrder(1)
				dbSeek(xFilial("SHB")+SG2->G2_CTRAB)
				dbSelectArea("SG2")
				If (SHB->(Eof()))
					dbSkip()
					Loop
				Endif
				
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ PE utilizado para filtrar as Operacoes por ordem de producao   ³
				//³ no Carga Acumulativa. (Bops:00000105102)
				//³ (Vide documentacao no DEM)                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lA730FOPE
					lLoop := ExecBlock("A730FOPE",.F.,.F.,{SC2->(C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD),SG2->G2_CODIGO,SG2->G2_PRODUTO,SG2->G2_OPERAC})
					If ValType(lLoop) == "L" .and. lLoop
						dbSkip()
						Loop							
					EndIf
				EndIf
				
				nRend := ( SHB->HB_REND / 100 )
				nRend := 1 / IIF(nRend = 0,1,nRend)
				nSaldo := A730QtdOp(SG2->G2_OPERAC)
				If nSaldo == 0
					dbSkip()
					Loop
				Endif
				If SG2->G2_TPOPER != "2"
					nUso := If(	Empty(SG2->G2_FORMSTP),A690HoraCt(SG2->G2_SETUP),A690HoraCt(Formula(SG2->G2_FORMSTP)))+(nSaldo/If(SG2->G2_LOTEPAD==0,1,SG2->G2_LOTEPAD) * A690HoraCt(SG2->G2_TEMPAD) * nRend)
				Else
					nUso := If(Empty(SG2->G2_FORMSTP),A690HoraCt(SG2->G2_SETUP),A690HoraCt(Formula(SG2->G2_FORMSTP)))+(A690HoraCt(SG2->G2_TEMPAD)/If(SG2->G2_LOTEPAD==0,1,SG2->G2_LOTEPAD) * nRend )
				Endif
				If nUso > 0
					dbSelectArea("SHH")
					lInclui:=.T.
					DbSeek(xFilial("SHH")+DToS(SC2->C2_DATPRF)+SHB->HB_COD+SG2->G2_RECURSO+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD)
					While !Eof() .And. xFilial("SHH")+DToS(SC2->C2_DATPRF)+SHB->HB_COD+SG2->G2_RECURSO+SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD == HH_FILIAL+DTOS(HH_DATA)+HH_CTRAB+HH_RECURSO+HH_OP
						// Checa a existencia da mesma operacao
						If HH_OPER == SG2->G2_OPERAC
							lInclui:=.F.
							Exit
						EndIf
						dbSkip()
					End
					RecLock("SHH", lInclui)
					Replace HH_FILIAL 	With xFilial("SHH")
					Replace HH_DATA 	With SC2->C2_DATPRF
					Replace HH_OP 		With SC2->C2_NUM+SC2->C2_ITEM+SC2->C2_SEQUEN+SC2->C2_ITEMGRD
					Replace HH_OPER 	With SG2->G2_OPERAC
					Replace HH_RECURSO 	With SG2->G2_RECURSO
					Replace HH_FERRAM 	With SG2->G2_FERRAM
					Replace HH_CTRAB 	With SHB->HB_COD
					Replace HH_USO 		With HH_USO+nUso
					MsUnLock()
					dbSelectArea("SHB")
					RecLock("SHB",.F.)
					Replace HB_USO With HB_USO+nUso
					MsUnLock()
				EndIf
			EndIf
			dbSelectArea("SG2")
			dbSkip()
		End
		dbSelectArea("SC2")
		dbSkip()
	End
EndIf
dbSelectArea("SC2")
CloseBrowse()
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A730Limpa  ³ Autor ³ Rodrigo de A. Sartorio³ Data ³28/02/96³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Limpa o arquivo de operacoes alocadas.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A730Limpa()
Local cFile

// Limpa o arquivo de operacoes alocadas
DbSelectArea("SHH")
cFile := RetSqlName("SHH")
TCSQLEXEC("DELETE FROM "+cFIle)
dbSelectArea("SHH")
dbGotop()

cIndex := CriaTrab(NIL,.F.)
AADD(aInd,cIndex)
dbSelectArea("SC2")
dbSetOrder(1)

cCondicao:= 'C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD >= "'+mv_par05+'" .and. C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD <="'
cCondicao+= mv_par06+'" .and. DToS(C2_DATPRF) >="'+DToS(mv_par03)+'".and. DToS(C2_DATPRF) <= "'
cCondicao+= DToS(mv_par04)+'" .and. C2_DATRF == CToD("")'
cCondicao+= ' .and. C2_STATUS <> "D" .and. C2_FILIAL =="'+xFilial("SC2")+'"'
If ExistBlock("A730FSC2")
	cRetBlock := ExecBlock("A730FSC2",.F.,.F.,cCondicao)
	If ValType(cRetBlock) == "C"
		cCondicao := cRetBlock
	EndIf
EndIf
IndRegua("SC2",cIndex,"C2_FILIAL+C2_NUM+C2_ITEM+C2_SEQUEN+C2_ITEMGRD",,cCondicao,STR0007)	//"Gerando Indice Trabalho ..."

dbGoTop()

cCadastro:= OemToAnsi(STR0008)	//"Gera‡Æo da Carga Acumulativa de M quinas"

cMarca := GetMark()
MarkBrow("SC2","C2_OK",,,,cMarca)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A730MontPes³ Autor ³ Rodrigo de A. Sartorio³ Data ³28/02/96³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta os indices condicionais para pesquisa.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA730                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A730MontPes()
DbSetOrder(nOpt1)
cIndex := CriaTrab(nil,.f.)
Aadd(aInd,cIndex)
dbSelectArea("SC2")
cKey   := IndexKey()

// AGUARDANDO SOLUCAO TECNOLOGIA
cCondicao := 'C2_NUM >= "'+mv_par05+'" .and. C2_NUM <="'
cCondicao+= mv_par06+'" .and. DTOS(C2_DATPRF) >="'+DtoS(mv_par03)+'".and. DTOS(C2_DATPRF) <="'
cCondicao+= DtoS(mv_par04)+'" .and. C2_DATRF == CToD("")'
cCondicao+= ' .and. C2_STATUS <> "D" .and. C2_FILIAL =="'+xFilial("SC2")+'"'
If ExistBlock("A730FSC2")
	cRetBlock := ExecBlock("A730FSC2",.F.,.F.,cCondicao)
	If ValType(cRetBlock) == "C"
		cCondicao := cRetBlock
	EndIf
EndIf
IndRegua("SC2",cIndex,cKey,,cCondicao,STR0009)	//"Selecionando Registros..."

dbGoTop()
Return .T. 
