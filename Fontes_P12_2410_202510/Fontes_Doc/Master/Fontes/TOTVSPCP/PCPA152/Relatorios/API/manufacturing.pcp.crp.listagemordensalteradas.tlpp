#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "manufacturing.pcp.crp.listagemordensalteradas.ch"

#DEFINE OP_INICIO_ALTERADO   "02"
#DEFINE OP_ENTREGA_ALTERADA  "03"
#DEFINE COMPRA_DATA_ALTERADA "13"

#DEFINE PARAM_LISTA_ORDENS  1
#DEFINE PARAM_LISTA_COMPRAS 2
#DEFINE PARAM_LISTA_AMBAS   3

#DEFINE PROGRAMACAO_STATUS_EFETIVADA "F"

Static _oQryCotacao := Nil as object

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SMF,T4X,SC2,SB1,SC1,SC7.SC8,SVY", name="Listagem de Ordens Alteradas", country="ALL", initialRelease="12.1.2310", customTables="SC2,SB1")

/*/{Protheus.doc} ListagemOrdensAlteradasTReportsBusinessObject
Classe para criação do Objeto de Negócio para a Listagem de Ordens Alteradas

@author breno.ferreira
@since 09/01/2025
@version 1.0
/*/
Class ListagemOrdensAlteradasTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
	Public Method new() as object
	Public Method getData() as object
	Public Method getSchema() as object
EndClass

/*/{Protheus.doc} new
Método de instância da classe

@return object: self

@author breno.ferreira
@since 09/01/2025
@version 1.0
/*/
Method new() as object Class ListagemOrdensAlteradasTReportsBusinessObject
	_Super:new()
	//Define a Área
	self:appendArea(STR0001)//STR0001 = "Manufatura"

	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0002)//STR0002 - "Listagem de Ordens Alteradas"

	//Define a descrição do Objeto de Negócio
	self:setDescription(STR0003)//STR0003 - "Listagem das ordens que tiverem datas alteradas na efetivação"

	self:setPergunte("PCPA152ALT") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author breno.ferreira
@since 09/01/2025
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object Class ListagemOrdensAlteradasTReportsBusinessObject
	Local aStruct := {}        as array
	Local aTables := {}        as array
	Local cAlias               as character
	Local cBanco  := TCGetDB() as character
	Local cCenttrabAte         as character
	Local cCentTrabDe          as character
	Local cDataFinal           as character
	Local cDataInicial         as character
	Local cDescricao           as character
	Local cEntregaAlterado     as character
	Local cEntregaOriginal     as character
	Local cInicioAlterado      as character
	Local cInicioOriginal      as character
	Local cMsg                 as character
	Local cMsgDesc             as character
	Local cOpAte               as character
	Local cOpDe                as character
	Local cPeriodo             as character
	Local cProg                as character
	Local cQuery               as character
	Local cRecursoAte          as character
	Local cRecursoDe           as character
	Local cStatus              as character
	Local dDataAte             as date
	Local dDataDe              as date
	Local jParams              as json
	Local nLista               as numeric
	Local nPosParam            as numeric
	Local nX                   as numeric
	Local lFiltraData          as logical
	Local oDetCompra           as object
	Local oDetEntrega          as object
	Local oDetInicio           as object
	Local oExec                as object
	Local ojItems              as json

	jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

	cProg          := pcpGetParametroSV(jParams, "MV_PAR01", .F.)
	cRecursoDe     := pcpGetParametroSV(jParams, "MV_PAR02", .F.)
	cRecursoAte    := pcpGetParametroSV(jParams, "MV_PAR03", .T., "MF_RECURSO")
	cCentTrabDe    := pcpGetParametroSV(jParams, "MV_PAR04", .F.)
	cCenttrabAte   := pcpGetParametroSV(jParams, "MV_PAR05", .T., "MF_CTRAB")
	cOpDe          := pcpGetParametroSV(jParams, "MV_PAR06", .F.)
	cOpAte         := pcpGetParametroSV(jParams, "MV_PAR07", .T., "MF_OP")
	nLista         := jParams["MV_PAR16"][1]
	lFiltraData    := !Empty(jParams["MV_PAR14"][1]) .Or. !Empty(jParams["MV_PAR15"][1])
	If lFiltraData
		dDataDe  := PCPConvDat(jParams["MV_PAR14"][1], 1)
		dDataAte := PCPConvDat(jParams["MV_PAR15"][1], 1)
	EndIf

	If !validaParam(jParams, @cMsg, @cMsgDesc, @cDataInicial, @cDataFinal, @cDescricao, @cStatus)
		Self:setErrorStatus(400, cMsg, cMsgDesc)
		Return Self:oData
	EndIf
	cPeriodo := cDataInicial + " - " + cDataFinal

	aAdd(aTables, {"C2_","SC2."})
	aAdd(aTables, {"B1_","SB1."})

	If nLista == PARAM_LISTA_ORDENS .Or. nLista == PARAM_LISTA_AMBAS
		cQuery += " SELECT DISTINCT SMF.MF_OP ordemProd, "
		cQuery +=                 " CASE SC2.C2_STATUS "
		cQuery +=                     " WHEN 'S' THEN '1' "
		cQuery +=                     " ELSE SC2.C2_TPOP "
		cQuery +=                 " END                    situacao, "
		cQuery +=                 " SB1.B1_DESC            descriProd, "
		cQuery +=                 " SC2.C2_QUANT           c2_quantidade, "
		cQuery +=                 " SC2.C2_DATPRI          dataInicio, "
		cQuery +=                 " SC2.C2_DATPRF          dataEntrega, "
		cQuery +=                 " SC2.C2_PRODUTO         produto, "
		cQuery +=                 " dataInicial.R_E_C_N_O_ recnoInicio, "
		cQuery +=                 " dataEntrega.R_E_C_N_O_ recnoEntrega, "
		cQuery +=                 " NULL                   recnoCompra "

		// Adiciona campos customizados
		PcpInCposPerson(aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)

		cQuery +=   " FROM " + RetSqlName("SMF") + " SMF "
		cQuery +=  " INNER JOIN " + RetSqlName("SC2") + " SC2 "
		cQuery +=     " ON SC2.C2_FILIAL  =  ? "
		cQuery +=    " AND " + PCPQrySC2("SC2", "SMF.MF_OP")
		If lFiltraData
			cQuery +=" AND SC2.C2_DATPRF >=  ? "
			cQuery +=" AND SC2.C2_DATPRF <=  ? "
		EndIf
		cQuery +=    " AND SC2.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1 "
		cQuery +=     " ON SB1.B1_FILIAL  =  ? "
		cQuery +=    " AND SB1.B1_COD     = SC2.C2_PRODUTO "
		cQuery +=    " AND SB1.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SVY") + " dataInicial "
		cQuery +=     " ON dataInicial.VY_FILIAL  =  ? "
		cQuery +=    " AND dataInicial.VY_PROG    = SMF.MF_PROG "
		cQuery +=    " AND dataInicial.VY_OP      = SMF.MF_OP "
		cQuery +=    " AND dataInicial.VY_TIPO    =  ? "
		cQuery +=    " AND dataInicial.D_E_L_E_T_ = ' ' "
		cQuery +=   " LEFT JOIN " + RetSqlName("SVY") + " dataEntrega "
		cQuery +=     " ON dataEntrega.VY_FILIAL  =  ? "
		cQuery +=    " AND dataEntrega.VY_PROG    = SMF.MF_PROG "
		cQuery +=    " AND dataEntrega.VY_OP      = SMF.MF_OP "
		cQuery +=    " AND dataEntrega.VY_TIPO    =  ? "
		cQuery +=    " AND dataEntrega.D_E_L_E_T_ = ' ' "
		cQuery +=  " WHERE SMF.MF_FILIAL   =  ? "
		cQuery +=    " AND SMF.MF_PROG     =  ? "
		cQuery +=    " AND SMF.MF_OP      >=  ? "
		cQuery +=    " AND SMF.MF_OP      <=  ? "
		cQuery +=    " AND SMF.MF_RECURSO >=  ? "
		cQuery +=    " AND SMF.MF_RECURSO <=  ? "
		cQuery +=    " AND SMF.MF_CTRAB   >=  ? "
		cQuery +=    " AND SMF.MF_CTRAB   <=  ? "
		cQuery +=    " AND (dataInicial.VY_DETALHE IS NOT NULL OR dataEntrega.VY_DETALHE IS NOT NULL) "
		cQuery +=    " AND SMF.D_E_L_E_T_  = ' ' "
	EndIf

	If nLista == PARAM_LISTA_AMBAS
		cQuery += " UNION "
	EndIf

	If nLista == PARAM_LISTA_COMPRAS .Or. nLista == PARAM_LISTA_AMBAS
		cQuery += " SELECT SVY.VY_OP ordemProd, "
		cQuery +=        " CASE SC2.C2_STATUS "
		cQuery +=            " WHEN 'S' THEN '1' "
		cQuery +=            " ELSE SC2.C2_TPOP "
		cQuery +=        " END            situacao, "
		cQuery +=        " SB1.B1_DESC    descriProd, "
		cQuery +=        " SC2.C2_QUANT   c2_quantidade, "
		cQuery +=        " SC2.C2_DATPRI  dataInicio, "
		cQuery +=        " SC2.C2_DATPRF  dataEntrega, "
		cQuery +=        " SC2.C2_PRODUTO produto, "
		cQuery +=        " NULL           recnoInicio, "
		cQuery +=        " NULL           recnoEntrega, "
		cQuery +=        " SVY.R_E_C_N_O_ recnoCompra "

		// Adiciona campos customizados
		PcpInCposPerson(aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)

		cQuery +=   " FROM " + RetSqlName("SVY") + " SVY "
		cQuery +=  " INNER JOIN " + RetSqlName("SC2") + " SC2 "
		cQuery +=     " ON SC2.C2_FILIAL  =  ? "
		cQuery +=    " AND " + PCPQrySC2("SC2", "SVY.VY_OP")
		If lFiltraData
			cQuery +=" AND SC2.C2_DATPRF >=  ? "
			cQuery +=" AND SC2.C2_DATPRF <=  ? "
		EndIf
		cQuery +=    " AND SC2.D_E_L_E_T_ = ' ' "
		cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1 "
		cQuery +=     " ON SB1.B1_FILIAL  =  ? "
		cQuery +=    " AND SB1.B1_COD     = SC2.C2_PRODUTO "
		cQuery +=    " AND SB1.D_E_L_E_T_ = ' ' "
		cQuery +=  " WHERE SVY.VY_FILIAL  =  ? "
		cQuery +=    " AND SVY.VY_PROG    =  ? "
		cQuery +=    " AND SVY.VY_TIPO    =  ? "
		cQuery +=    " AND SVY.VY_OP     >=  ? "
		cQuery +=    " AND SVY.VY_OP     <=  ? "
		cQuery +=    " AND EXISTS (SELECT 1 "
		cQuery +=                  " FROM " + RetSqlName("SMF") + " SMF "
		cQuery +=                 " WHERE SMF.MF_FILIAL   = ? "
		cQuery +=                   " AND SMF.MF_PROG     = SVY.VY_PROG "
		cQuery +=                   " AND SMF.MF_OP       = SVY.VY_OP "
		cQuery +=                   " AND SMF.MF_RECURSO >=  ? "
		cQuery +=                   " AND SMF.MF_RECURSO <=  ? "
		cQuery +=                   " AND SMF.MF_CTRAB   >=  ? "
		cQuery +=                   " AND SMF.MF_CTRAB   <=  ? "
		cQuery +=                   " AND SMF.D_E_L_E_T_  = ' ') "
		cQuery +=    " AND SVY.D_E_L_E_T_ = ' ' "
	EndIf
	cQuery +=  " ORDER BY ordemProd "

	If "MSSQL" $ cBanco
		//Substitui concatenação || por +
		cQuery := StrTran(cQuery, '||', '+')
	EndIf

	oExec := FwExecStatement():New(cQuery)

	nPosParam := 1
	If nLista == PARAM_LISTA_ORDENS .Or. nLista == PARAM_LISTA_AMBAS
		oExec:setString(nPosParam++, xFilial("SC2")) // C2_FILIAL
		If lFiltraData
			oExec:setDate(nPosParam++, dDataDe ) // C2_DATPRF
			oExec:setDate(nPosParam++, dDataAte) // C2_DATPRF
		EndIf
		oExec:setString(nPosParam++, xFilial("SB1")     ) // B1_FILIAL
		oExec:setString(nPosParam++, xFilial("SVY")     ) // VY_FILIAL
		oExec:setString(nPosParam++, OP_INICIO_ALTERADO ) // VY_TIPO
		oExec:setString(nPosParam++, xFilial("SVY")     ) // VY_FILIAL
		oExec:setString(nPosParam++, OP_ENTREGA_ALTERADA) // VY_TIPO
		oExec:setString(nPosParam++, xFilial("SMF")     ) // MF_FILIAL
		oExec:setString(nPosParam++, cProg              ) // MF_PROG
		oExec:setString(nPosParam++, cOpDe              ) // MF_OP
		oExec:setString(nPosParam++, cOpAte             ) // MF_OP
		oExec:setString(nPosParam++, cRecursoDe         ) // MF_RECURSO
		oExec:setString(nPosParam++, cRecursoAte        ) // MF_RECURSO
		oExec:setString(nPosParam++, cCentTrabDe        ) // MF_CTRAB
		oExec:setString(nPosParam++, cCenttrabAte       ) // MF_CTRAB
	EndIf

	If nLista == PARAM_LISTA_COMPRAS .Or. nLista == PARAM_LISTA_AMBAS
		oExec:setString(nPosParam++, xFilial("SC2")) // C2_FILIAL
		If lFiltraData
			oExec:setDate(nPosParam++, dDataDe ) // C2_DATPRF
			oExec:setDate(nPosParam++, dDataAte) // C2_DATPRF
		EndIf
		oExec:setString(nPosParam++, xFilial("SB1")      ) // B1_FILIAL
		oExec:setString(nPosParam++, xFilial("SVY")      ) // VY_FILIAL
		oExec:setString(nPosParam++, cProg               ) // VY_PROG
		oExec:setString(nPosParam++, COMPRA_DATA_ALTERADA) // VY_TIPO
		oExec:setString(nPosParam++, cOpDe               ) // VY_OP
		oExec:setString(nPosParam++, cOpAte              ) // VY_OP
		oExec:setString(nPosParam++, xFilial("SMF")      ) // MF_FILIAL
		oExec:setString(nPosParam++, cRecursoDe          ) // MF_RECURSO
		oExec:setString(nPosParam++, cRecursoAte         ) // MF_RECURSO
		oExec:setString(nPosParam++, cCentTrabDe         ) // MF_CTRAB
		oExec:setString(nPosParam++, cCenttrabAte        ) // MF_CTRAB
	EndIf

	cAlias := oExec:openAlias()

	While (cAlias)->(!Eof())
		ojItems     := JsonObject():new()
		oDetInicio  := JsonObject():new()
		oDetEntrega := JsonObject():new()

		//Cabeçalho
		ojItems["programacao"] := descricao(RTrim(cProg), RTrim(cDescricao))
		ojItems["status"     ] := PCPA152Process():getDescricaoStatus(cStatus)
		ojItems["periodo"    ] := cPeriodo
		ojItems["registro"   ] := "OP"

		If Empty((cAlias)->recnoCompra)
			If !Empty((cAlias)->recnoInicio)
				SVY->(dbGoTo((cAlias)->recnoInicio))
				oDetInicio:fromJson(SVY->VY_DETALHE)
				cInicioOriginal := oDetInicio["dataOriginal"]
				cInicioAlterado := oDetInicio["dataAlterada"]
			Else
				cInicioOriginal := (cAlias)->dataInicio
				cInicioAlterado := (cAlias)->dataInicio
			EndIf

			If !Empty((cAlias)->recnoEntrega)
				SVY->(dbGoTo((cAlias)->recnoEntrega))
				oDetEntrega:fromJson(SVY->VY_DETALHE)
				cEntregaOriginal := oDetEntrega["dataOriginal"]
				cEntregaAlterado := oDetEntrega["dataAlterada"]
			Else
				cEntregaOriginal := (cAlias)->dataEntrega
				cEntregaAlterado := (cAlias)->dataEntrega
			EndIf

			//Campos OPs
			ojItems["ordemProducao"  ] := RTrim((cAlias)->ordemProd)
			ojItems["situacao"       ] := getSituacaoOP((cAlias)->situacao)
			ojItems["c2_produto"     ] := descricao(AllTrim((cAlias)->produto), (cAlias)->descriProd)
			ojItems["c2_quantidade"  ] := (cAlias)->c2_quantidade
			ojItems["dataOriIniEntC2"] := PCPConvDat(cInicioOriginal, 4) +  " - " + PCPConvDat(cEntregaOriginal, 4)
			ojItems["dataAltIniEntC2"] := PCPConvDat(cInicioAlterado, 4) +  " - " + PCPConvDat(cEntregaAlterado, 4)

			FreeObj(oDetInicio)
			FreeObj(oDetEntrega)
		ElseIf getDetalhes(jParams, (cAlias)->recnoCompra, @oDetCompra)

			// Campos Compra
			ojItems["c1_produto"     ] := descricao(AllTrim(oDetCompra["produto"]), oDetCompra["descriProd"])
			ojItems["c1_quantidade"  ] := oDetCompra["quantidade"  ]
			ojItems["documento"      ] := oDetCompra["documento"   ]
			ojItems["dataOrigIniEnt" ] := oDetCompra["dataOriginal"]
			ojItems["dataAlteIniEnt" ] := oDetCompra["dataAlterada"]
			ojItems["tipo"           ] := oDetCompra["tipo"        ]
			oJItems["registro"       ] := "COMP"

			FreeObj(oDetCompra)
		Else
			// Documento de compra não esta nos filtros
			(cAlias)->(DBSkip())
			Loop
		EndIf

		For nX := 1 To Len(aStruct)
			If aStruct[nX][3] == "date"
				ojItems[aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(aStruct[nX][5]))
			ElseIf aStruct[nX][3] == "boolean"
				ojItems[aStruct[nX][1]] := ((cAlias)->&(aStruct[nX][5]) == "T")
			Else
				ojItems[aStruct[nX][1]] := (cAlias)->&(aStruct[nX][5])
			EndIf
		End

		self:ProcessData()
		self:oData:appendData(ojItems)

		(cAlias)->(DBSkip())
	EndDo
	(cAlias)->(DBCloseArea())

	aSize(aStruct, 0)
	aSize(aTables, 0)
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author breno.ferreira
@since 09/01/2025
@version 1.0
@return object: self:oSchema
/*/
Method getSchema() as object Class ListagemOrdensAlteradasTReportsBusinessObject

    //Campos Auxiliares
    Self:addProperty("programacao"    , "programacao"    , "string", "programacao"    )
    Self:addProperty("status"         , "status"         , "string", "status"         )
    Self:addProperty("periodo"        , "periodo"        , "string", "periodo"        )
    Self:addProperty("ordemProducao"  , "ordemProducao"  , "string", "ordemProducao"  )
    Self:addProperty("situacao"       , "situacao"       , "string", "situacao"       )
    Self:addProperty("c2_produto"     , "c2_produto"     , "string", "c2_produto"     )
    Self:addProperty("c2_quantidade"  , "c2_quantidade"  , "number", "c2_quantidade"  )
    Self:addProperty("c1_produto"     , "c1_produto"     , "string", "c1_produto"     )
    Self:addProperty("c1_quantidade"  , "c1_quantidade"  , "number", "c1_quantidade"  )
    Self:addProperty("dataOriIniEntC2", "dataOriIniEntC2", "string", "dataOriIniEntC2")
    Self:addProperty("dataAltIniEntC2", "dataAltIniEntC2", "string", "dataAltIniEntC2")
    Self:addProperty("documento"      , "documento"      , "string", "documento"      )
    Self:addProperty("dataOrigIniEnt" , "dataOrigIniEnt" , "string", "dataOrigIniEnt" )
    Self:addProperty("dataAlteIniEnt" , "dataAlteIniEnt" , "string", "dataAlteIniEnt" )
    Self:addProperty("tipo"           , "tipo"           , "string", "tipo"           )
    Self:addProperty("registro"       , "registro"       , "string", "registro"       )

    //URL para o lookup dos parâmetros de tela
    self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/alocProgramacao", 2)
    self:setCustomURL("MV_PAR02", "/api/pcp/smartview/v1/alocRecurso",     2)
    self:setCustomURL("MV_PAR03", "/api/pcp/smartview/v1/alocRecurso",     2)
    self:setCustomURL("MV_PAR04", "/api/pcp/smartview/v1/alocCtrab",       2)
    self:setCustomURL("MV_PAR05", "/api/pcp/smartview/v1/alocCtrab",       2)
    self:setCustomURL("MV_PAR06", "/api/pcp/smartview/v1/getOrdens",       2)
    self:setCustomURL("MV_PAR07", "/api/pcp/smartview/v1/getOrdens",       2)
    self:setCustomURL("MV_PAR08", "/api/pcp/smartview/v1/getSoliComp",     2)
    self:setCustomURL("MV_PAR09", "/api/pcp/smartview/v1/getSoliComp",     2)
    self:setCustomURL("MV_PAR10", "/api/pcp/smartview/v1/getAutEnt",       2)
    self:setCustomURL("MV_PAR11", "/api/pcp/smartview/v1/getAutEnt",       2)
    self:setCustomURL("MV_PAR12", "/api/pcp/smartview/v1/getCotacao",      2)
    self:setCustomURL("MV_PAR13", "/api/pcp/smartview/v1/getCotacao",      2)

Return self:oSchema

/*/{Protheus.doc} validaParam
Verifica se os parametros sao validos
@type  Static Function
@author Breno Soares
@since 09/01/2025
@version P12
@param 01 jParams     , Json, Json carregado com os parametros
@param 02 cMsg        , Character, Retorna por referência a mensagem do erro
@param 03 cMsgDesc    , Character, Retorna por referência a descrição do erro
@param 04 cDataInicial, Character, Retorna por referência a data inicial da programação
@param 05 cDataFinal  , Character, Retorna por referência a data final da programação
@param 06 cDescricao  , Character, Retorna por referência a descrição da programação
@param 07 cStatus     , Character, Retorna por referência o status da programação
@return lOk, Logico, Indica que as datas são iguais
/*/
Static Function validaParam(jParams, cMsg, cMsgDesc, cDataInicial, cDataFinal, cDecricao, cStatus)
	Local cProg           as character
	Local dDataFinal      as date
	Local dDataFinalMaior as date
	Local dDataInicial    as date
	Local dDataRealFinal  as date
	Local lOk             as logical
	Local nLista          as numeric

	lOk    := .T.
	cProg  := jParams["MV_PAR01"][1]
	nLista := jParams["MV_PAR16"][1]

	If Empty(cProg) .Or. !T4X->(dbSeek(xFilial("T4X") + cProg))
		lOk      := .F.
		cMsg     := STR0004 //STR0004 - Programação inválida
		cMsgDesc := STR0005 //STR0005 - Obrigatório informar a programação!
	EndIf

	If lOk .And. (nLista == PARAM_LISTA_COMPRAS .Or. nLista == PARAM_LISTA_AMBAS)
		lOk := T4X->T4X_STATUS == PROGRAMACAO_STATUS_EFETIVADA

		If !lOk
			cMsg     := STR0004 // Programação inválida
			cMsgDesc := STR0012 // "A consulta de compras alteradas é permitida apenas para programações efetivadas."
		EndIf
	EndIf

	If lOk
		cDescricao := T4X->T4X_DESCRI
		cStatus    := T4X->T4X_STATUS

		T4Y->(dbSetOrder(2))
		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataInicial"))
		dDataInicial := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)
		cDataInicial := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 3)

		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataFinal"))
		dDataFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)

		T4Y->(dbSeek(xFilial("T4Y") + cProg + "dataRealFim"))
		dDataRealFinal := PCPConvDat(AllTrim(T4Y->T4Y_VALOR), 1)

		dDataFinalMaior := Max(dDataRealFinal, dDataFinal)
		cDataFinal      := DToC(dDataFinalMaior)
	EndIf

Return lOk

/*/{Protheus.doc} descricao
Campo com descrição
@type  Static Function
@author Breno Soares
@since 09/01/2025
@version P12
@param 01 cCampo , Character, Campo do alias
@param 02 cDescri, Character, Descrição do campo
@return cDescricao, Character, Campo com a descrição do campo
/*/
Static Function descricao(cCampo, cDescri)
	Local cDescricao as character

	cDescricao := cCampo
	If !Empty(cDescri)
		cDescricao += " - " + cDescri
	EndIf

Return cDescricao

/*/{Protheus.doc} getDetalhes
Retorna os detalhes do documento de compra e se ele esta nos filtros do relatório.
@type  Static Function
@author Breno Soares
@since 09/01/2025
@version P12
@param jParams   , Json    , Json com os parâmetros do relatório.
@param nRecSVY   , Numérico, Recno da ocorrência na SVY.
@param oDetCompra, Json    , Retorna por referência o objeto com os detalhes do documento de compra.
@return lValido, Lógico, Indica se o documento de compra está nos filtros do relatório.
/*/
Static Function getDetalhes(jParams, nRecSVY, oDetCompra)
	Local cAlias       as character
	Local cAutEntAte   as character
	Local cAutEntDe    as character
	Local cCotacaoAte  as character
	Local cCotacaoDe   as character
	Local cQuery       as character
	Local cSoliCompAte as character
	Local cSoliCompDe  as character
	Local lValido      as logical

	cSoliCompDe  := pcpGetParametroSV(jParams, "MV_PAR08", .F.)
	cSoliCompAte := pcpGetParametroSV(jParams, "MV_PAR09", .T., "C1_NUM")
	cAutEntDe    := pcpGetParametroSV(jParams, "MV_PAR10", .F.)
	cAutEntAte   := pcpGetParametroSV(jParams, "MV_PAR11", .T., "C7_NUM")
	cCotacaoDe   := pcpGetParametroSV(jParams, "MV_PAR12", .F.)
	cCotacaoAte  := pcpGetParametroSV(jParams, "MV_PAR13", .T., "C8_NUM")

	oDetCompra := JsonObject():new()

	SVY->(dbGoTo(nRecSVY))
	oDetCompra:fromJson(SVY->VY_DETALHE)

	If oDetCompra["origem"] == "SC1"
		lValido := oDetCompra["numeroDocumento"] >= RTrim(cSoliCompDe) .And. oDetCompra["numeroDocumento"] <= RTrim(cSoliCompAte)

		If lValido

			SC1->(dbSetOrder(1))
			SC1->(dbSeek(xFilial("SC1") + oDetCompra["numeroDocumento"]))

			oDetCompra["produto"     ] := SC1->C1_PRODUTO
			oDetCompra["descriProd"  ] := SC1->C1_DESCRI
			oDetCompra["quantidade"  ] := SC1->C1_QUANT
			oDetCompra["documento"   ] := oDetCompra["numeroDocumento"]
			oDetCompra["dataOriginal"] := PCPConvDat(oDetCompra["dataOriginalInicial"], 4) + " - " + PCPConvDat(oDetCompra["dataOriginalEntrega"], 4)
			oDetCompra["dataAlterada"] := PCPConvDat(oDetCompra["dataAlteradaInicial"], 4) + " - " + PCPConvDat(oDetCompra["dataAlteradaEntrega"], 4)
			oDetCompra["tipo"        ] := STR0009 // "Solic. Compra"
		EndIf

	ElseIf oDetCompra["origem"] == "SC7"
		lValido := oDetCompra["numeroDocumento"] >= RTrim(cAutEntDe) .And. oDetCompra["numeroDocumento"] <= RTrim(cAutEntAte)

		If lValido

			SC7->(dbSetOrder(1))
			SC7->(dbSeek(xFilial("SC7") + oDetCompra["numeroDocumento"]))

			oDetCompra["produto"     ] := SC7->C7_PRODUTO
			oDetCompra["descriProd"  ] := SC7->C7_DESCRI
			oDetCompra["quantidade"  ] := SC7->C7_QUANT
			oDetCompra["documento"   ] := oDetCompra["numeroDocumento"]
			oDetCompra["dataOriginal"] := PCPConvDat(oDetCompra["dataOriginalInicial"], 4) + " - " + PCPConvDat(oDetCompra["dataOriginalEntrega"], 4)
			oDetCompra["dataAlterada"] := PCPConvDat(oDetCompra["dataAlteradaInicial"], 4) + " - " + PCPConvDat(oDetCompra["dataAlteradaEntrega"], 4)
			oDetCompra["tipo"        ] := STR0010 // "Aut. Entrega"
		EndIf

	ElseIf oDetCompra["origem"] == "SC8"
		lValido := oDetCompra["numeroDocumento"] >= RTrim(cCotacaoDe) .And. oDetCompra["numeroDocumento"] <= RTrim(cCotacaoAte)

		If lValido

			If _oQryCotacao == Nil
				cQuery := " SELECT SC8.C8_PRODUTO, "
				cQuery +=        " SC8.C8_QUANT, "
				cQuery +=        " SB1.B1_DESC "
				cQuery +=   " FROM " + RetSqlName("SC8") + " SC8 "
				cQuery +=  " INNER JOIN " + RetSqlName("SB1") + " SB1 "
				cQuery +=     " ON SB1.B1_FILIAL  =  ? "
				cQuery +=    " AND SB1.B1_COD     = SC8.C8_PRODUTO "
				cQuery +=    " AND SB1.D_E_L_E_T_ = ' ' "
				cQuery +=  " WHERE SC8.C8_FILIAL  =  ? "
				cQuery +=    " AND SC8.C8_NUM     =  ? "
				cQuery +=    " AND SC8.D_E_L_E_T_ = ' ' "

				_oQryCotacao := FwExecStatement():New(cQuery)

				_oQryCotacao:setString(1, xFilial("SB1")) // B1_FILIAL
				_oQryCotacao:setString(2, xFilial("SC8")) // C8_FILIAL
			EndIf

			_oQryCotacao:setString(3, oDetCompra["numeroDocumento"]) // C8_NUM

			cAlias := _oQryCotacao:openAlias()

			oDetCompra["produto"     ] := (cAlias)->C8_PRODUTO
			oDetCompra["descriProd"  ] := (cAlias)->B1_DESC
			oDetCompra["quantidade"  ] := (cAlias)->C8_QUANT
			oDetCompra["documento"   ] := oDetCompra["numeroDocumento"]
			oDetCompra["dataOriginal"] := PCPConvDat(oDetCompra["dataOriginalEntrega"], 4)
			oDetCompra["dataAlterada"] := PCPConvDat(oDetCompra["dataAlteradaEntrega"], 4)
			oDetCompra["tipo"        ] := STR0011 // "Cotação"
		EndIf
	EndIf

Return lValido

/*/{Protheus.doc} getSituacaoOP
Retorna a descrição da situação da OP
@type  Static Function
@author Lucas Fagundes
@since 19/05/2025
@version P12
@param cTipo, Caracter, Tipo da OP.
@return cSituacao, Caracter, Descrição da situação da OP
/*/
Static Function getSituacaoOP(cTipo)
	Local cSituacao as character

	If cTipo == '1'
		cSituacao := STR0006 //STR0006 - 'Efetivada'

	ElseIf cTipo == 'F'
		cSituacao := STR0007 //STR0007 - 'Firme'

	ElseIf cTipo == 'P'
		cSituacao := STR0008 //STR0008 - 'Prevista'

	EndIf

Return cSituacao
