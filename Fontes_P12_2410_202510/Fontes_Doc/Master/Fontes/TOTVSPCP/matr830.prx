#Include "MATR830.CH"
#Include "FIVEWIN.CH"

/*


Ŀ
Funo     MATR830   Autor  Felipe Nunes Toledo    Data  20/07/06 
Ĵ
Descrio  Relacao Do Cadastro De Recursos                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function MATR830()
Local oReport
If  TRepInUse()
	//Ŀ
	//Interface de impressao..                                                
	//
	oReport:= ReportDef()
	oReport:PrintDialog()
Else
	MATR830R3()
EndIf

Return NIL

/*/


Ŀ
Programa  ReportDef  Autor Felipe Nunes Toledo     Data 20/07/06  
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
Ĵ
ParametrosNenhum                                                      
Ĵ
 Uso      MATR830			                                          
ٱ


/*/
Static Function ReportDef()
Local oReport
Local oSection1
Local oSection2
Local oSection3
Local cTitle    := OemToAnsi(STR0001) //"Relacao do Cadastro de Recursos"

//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport:= TReport():New("MATR830",cTitle,"MTR830", {|oReport| ReportPrint(oReport)},OemToAnsi(STR0002)) //##"Este programa ira imprimir a relacao do Cadastro de Recursos"
oReport:SetPortrait() //Define a orientacao de pagina do relatorio como retrato.
oReport:SetEdit(.F.)

//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01        	// Do Recurso                            
// mv_par02        	// Ate o Recurso                         
// mv_par03        	// Data a Considerar                     
//
Pergunte(oReport:GetParam(),.F.)

// Preenche a pergunta MTR830 Data a Considerar? = dDataBase
SetMVValue("MTR830","MV_PAR03",dDataBase) 

//Ŀ
//Criacao das secoes utilizadas pelo relatorio                            
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//

//Ŀ
// oSection1 (Recurso)                                         
//
oSection1 := TRSection():New(oReport,STR0022,{"SH1"},/*Ordem*/) //"Recursos"
oSection1:SetHeaderPage()

TRCell():New(oSection1,'H1_CODIGO'  ,'SH1',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'H1_DESCRI'  ,'SH1',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'H1_CCUSTO'  ,'SH1',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'SABADO'     ,'SH1', STR0013  ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,'DOMINGO'    ,'SH1', STR0014  ,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//Ŀ
// oSection2 (Recurso Alternativo/Secundario)                  
//
oSection2 := TRSection():New(oSection1,STR0023,{"SH1","SH2"},/*Ordem*/) //"Recursos alternativos e secundrios"
oSection2:SetHeaderPage()

TRCell():New(oSection2,'H2_RECALTE','SH2',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,'H2_TIPO'   ,'SH2',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//Ŀ
// oSection3 (Bloqueio)                                        
//
oSection3 := TRSection():New(oSection1,STR0024,{"SH1","SH9"},/*Ordem*/) //"Bloqueios e exees de Calendrio"
oSection3:SetHeaderPage(.F.)
oSection3:SetHeaderSection(.T.)

TRCell():New(oSection3,'BLOQUEIO'   ,'SH9', STR0020  ,/*Picture*/,/*Tamanho*/,/*lPixel*/,  {|| '' }                      )
TRCell():New(oSection3,'H9_DTINI'   ,'SH9',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,'H9_DTFIM'   ,'SH9',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,'H9_HRINI'   ,'SH9',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection3,'H9_HRFIM'   ,'SH9',/*Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

Return(oReport)

/*/


Ŀ
Programa  ReportPrint  Autor Felipe Nunes Toledo   Data 20/07/06  
Ĵ
Descrio A funcao estatica ReportPrint devera ser criada para todos  
          os relatorios que poderao ser agendados pelo usuario.       
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatorio                           
Ĵ
 Uso       MATR830			                                          
ٱ


/*/
Static Function ReportPrint(oReport)
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)
Local oSection3 := oReport:Section(1):Section(2)
Local cFilter
Local aDia[7],aDayWeek[7]
Local nPrecisa  := GetMv("MV_PRECISA")
Local cCaleVige := ""
Local nDia
Local x
Local cSab      := ""
Local cDom      := ""
Local cMaqAnt   := ""
Local cAloc
Private nCntBitMap := 0

dbSelectarea("SH1")
dbSetOrder(1)

//Ŀ
//Transforma parametros Range em expressao ADVPL                          
//
MakeAdvplExpr(oReport:GetParam())
	
//Ŀ
// Condicao de Filtragem do SH1                                 
// 

cFilter := 'H1_FILIAL=="'+xFilial("SH1")+'".And.H1_CODIGO>="'+mv_par01+'".And.'
cFilter+='H1_CODIGO<="'+mv_par02+'"'

oSection1:SetFilter(cFilter,Indexkey())

//Ŀ
//Definindo o titulo do relatorio
//
oReport:SetTitle(STR0001+" - " + Dtoc(MV_PAR03)) //"Relacao do Cadastro de Recursos"

//Ŀ
//             Inicializacao de variaveis                       
//
Afill(aDia,"")

aDayWeek[1] := STR0008	//"Seg"
aDayWeek[2] := STR0009	//"Ter"
aDayWeek[3] := STR0010	//"Qua"
aDayWeek[4] := STR0011	//"Qui"
aDayWeek[5] := STR0012	//"Sex"
aDayWeek[6] := STR0013	//"Sab"
aDayWeek[7] := STR0014	//"Dom"

//Ŀ
//Inicio da impressao do fluxo do relatorio                               
//
oReport:SetMeter(SH1->(LastRec()))
oSection1:Init()

While !oReport:Cancel() .And. !SH1->(Eof())
	cCaleVige := ASHICalen(SH1->H1_CODIGO, MV_PAR03, .T.)
	dbSelectArea("SH7")
	If dbSeek(xFilial("SH7") + cCaleVige)
	
		cAloc    := SH7->H7_ALOC
		nDia := Len(cAloc) / 7
		
		For x := 1 to 7
			aDia[x] := SubStr(cAloc, (nDia * (x - 1) + 1), nDia)
		Next	
		
		dbSelectArea("SH1")
		
		cSab := aDia[6]
		cDom := aDia[7]

		IIF( EMPTY(cSab) , cSab := STR0016 , cSab := STR0017 )	//"N"###"S"
		IIF( EMPTY(cDom) , cDom := STR0016 , cDom := STR0017 )	//"N"###"S"

		oSection1:Cell('SABADO' ):SetValue(cSab)
		oSection1:Cell('DOMINGO'):SetValue(cDom)
		
		oReport:IncMeter()						
		oSection1:PrintLine() // Impressao da secao 1
	
		//Ŀ
		//Fluxo Impressao da secao 2   
		//
		If SH2->(dbSeek(xFilial("SH2")+SH1->H1_CODIGO))
			oReport:SkipLine()
			oSection2:Init()
			While SH2->(!eof()) .and. SH2->(H2_FILIAL+H2_RECPRIN) == xFilial("SH2")+SH1->H1_CODIGO
				oSection2:PrintLine() // Impressao da secao 2
				SH2->(dbSkip())
			EndDo
			oSection2:Finish()
		EndIf    

		If oReport:PageHeight() <= oReport:Row()+ (5*60)
			nCntBitMap := 0
		  	oReport:EndPage() 
			oReport:PrintText('') //Usado para limpar buffer e reinicar posicionamento  	  
		  	oReport:SkipLine()
		Else						
			oReport:SkipLine()
		EndIf
	
		//Ŀ
		//Impressao do Calendario
		//
		If !(A830Graph(oReport, nPrecisa, aDayWeek, aDia))
			Return .f.
		EndIf
    
		//Ŀ
		//Fluxo Impressao da secao 3   
		//
		If SH9->(dbSeek(xFilial("SH9")+"B"+SH1->H1_CCUSTO+SH1->H1_CODIGO))
			oReport:SkipLine()
			oSection3:Init()
			cMaqAnt := xFilial("SH9")+"B"+SH1->H1_CCUSTO+SH1->H1_CODIGO
			While SH9->(!eof()) .and. SH9->(H9_FILIAL+H9_TIPO+H9_CCUSTO+H9_RECURSO) == cMaqAnt
				If SH9->H9_DTINI >= MV_PAR03
					oSection3:PrintLine() // Impressao  da secao 3
				EndIf
				SH9->(dbSkip())
			EndDo
			oSection3:Finish()
		EndIf

	EndIf
	SH1->(dbSkip())
	oReport:SkipLine()
	oReport:ThinLine()
	oReport:SkipLine()
EndDo
oSection1:Finish()
SH1->(DbCloseArea())

Return Nil
/*


Ŀ
Funo     MATR830   Autor  Paulo Boschetti        Data  07.08.92 
Ĵ
Descrio Relacao Do Cadastro De Recursos                             
                                                                      
Ĵ
Sintaxe e  MATR830(void)                                              
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Cesar       31/03/99XXXXXXManutencao na SetPrint()                  
Iuspa       01/08/00005333Tratamento de MV_PRECISA                  
ٱ

*/
Function MATR830R3()
Local titulo := STR0001	//"Relacao do Cadastro de Recursos"
Local cString:="SH1"
Local wnrel:="MATR830"
Local cDesc :=STR0002	//"Este programa ira imprimir a relacao do Cadastro de Recursos"
Local tamanho:="G"

Private aReturn  := {STR0003,1,STR0004, 2, 2, 1, "",1 }	//"Zebrado"###"Administracao"
Private cPerg    :="MTR830"
Private nLastKey := 0

//Ŀ
// Verifica as perguntas selecionadas                           
//
pergunte("MTR830",.F.)
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01        	// Do Recurso                            
// mv_par02        	// Ate o Recurso                         
// mv_par03        	// Data a Considerar?                    
//
//Ŀ
// Envia controle para a funcao SETPRINT                        
//


// Preenche a pergunta MTR830 Data a Considerar? = dDataBase
SetMVValue("MTR830","MV_PAR03",dDataBase) 

wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc,"","",.F.,"",,Tamanho)

If nLastKey = 27
	Set Filter To
   Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
   Set Filter To
   Return
Endif

RptStatus({|lEnd| R830Imp(@lEnd,wnRel,titulo,tamanho)},titulo)

Return NIL

/*


Ŀ
Funo     R830Imp   Autor  Waldemiro L. Lustosa   Data  13.11.95 
Ĵ
Descrio  Chamada do Relatrio                                       
Ĵ
 Uso       MATR830			                                          
ٱ


*/
Static Function R830Imp(lEnd,wnRel,titulo,tamanho)

//Ŀ
// Define Variaveis                                             
//
Local CbCont,cabec1,cabec2
Local nomeprog:="MATR830"
Local nTipo  := IIF(aReturn[4]==2,18,15)
Local cSab := ""
Local cDom := ""
Local cMqAlt , cMqSec 
Local cCalend:= SPACE(84)
Local aDia[7],aDayWeek[7]
Local I := 1
Local lFirst := .T.
Local aBloqueio := {}
Local nPrecisa := GetMv("MV_PRECISA")
Local x, cNewAloc
Local nDia
Local cCaleVige := ""
Local cAloc

//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
cbtxt    := SPACE(10)
cbcont   := 0
li       := 80
m_pag    := 1
//Ŀ
// Monta os Cabecalhos                                          
//

cabec1 := STR0006	//"REC.   DENOMINACAO                      CENTRO  SAB/DOM RECURSOS ALTERNATIVOS / SECUNDARIOS"
cabec2 := STR0007	//"                                       DE CUSTO"
// 			         123456 123456789012345678901234567890 123456789 
//         				0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
//                   			 1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18   

Titulo += " - " + Dtoc(MV_PAR03)

//Ŀ
//             Inicializacao de variaveis                       
//
Afill(aDia,"")

aDayWeek[1] := STR0008	//"Seg"
aDayWeek[2] := STR0009	//"Ter"
aDayWeek[3] := STR0010	//"Qua"
aDayWeek[4] := STR0011	//"Qui"
aDayWeek[5] := STR0012	//"Sex"
aDayWeek[6] := STR0013	//"Sab"
aDayWeek[7] := STR0014	//"Dom"

aEscala := a830Week()

//Ŀ
//   Final da Inicializacao de variaveis                        
//

dbSelectarea("SH1")
dbSetOrder(1)
dbSeek(cFilial+mv_par01,.T.)

SetRegua(LastRec())

While !Eof() .and. H1_FILIAL+H1_CODIGO <= cFilial+mv_par02

	If lEnd
		@Prow()+1,001 PSay STR0015	//"CANCELADO PELO OPERADOR"		
		Exit
	EndIF

	IncRegua()

	cCaleVige := ASHICalen(SH1->H1_CODIGO, MV_PAR03, .T.)

	dbSelectArea("SH7")
	dbSeek(xFilial("SH7") + cCaleVige)

	If Found()

		cAloc    := SH7->H7_ALOC    
		nDia := Len(cAloc) / 7  

		For x := 1 to 7 
			aDia[x] := SubStr(cAloc, (nDia * (x - 1) + 1), nDia)
		Next	

		dbSelectArea("SH1")

		cSab := aDia[6]
		cDom := aDia[7]

		IIF( EMPTY(cSab) , cSab := STR0016 , cSab := STR0017 )	//"N"###"S"
		IIF( EMPTY(cDom) , cDom := STR0016 , cDom := STR0017 )	//"N"###"S"     

		If Li >= 48 
			cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
		EndIF

		cMqAlt    := MAQALT(SH1->H1_CODIGO)   // Devolve String C/Codigo Rec.Alternativos
		cMqSec    := MAQSEC(SH1->H1_CODIGO)   // Devolve String C/Codigo Rec.Secundario
		aBloqueio := BLOQUEIO(SH1->H1_CODIGO,SH1->H1_CCUSTO) // Devolve Array com os Bloqueios

		@Li,00 PSay H1_CODIGO
		@Li,07 PSay H1_DESCRI
		@Li,37 PSay PADL(ALLTRIM(H1_CCUSTO),20)
		@Li,58 PSay cSab+"   "+cDom
		If LEN(cMqAlt) > 56
			@Li,66 PSay STR0018	//"Alternativos:"
			For i:=1 to LEN(cMqAlt) Step 56
				@Li,80 PSay SUBS(cMqAlt,i,56)
				Li++
			Next i
		ElseIf !Empty(cMqAlt) 
			@Li,66 PSay STR0018	//"Alternativos:"
			@Li,80 PSay SUBS(cMqAlt,1,56)
		EndIf

		If !Empty(cMqAlt) .And. !Empty(cMqSec) .And. LEN(cMqAlt) < 56
			Li++
		EndIf

		If LEN(cMqSec) > 56
			@Li,66 PSay STR0019	//"Secundarios:"
			For i:=1 to LEN(cMqSec) Step 56
				@Li,80 PSay SUBS(cMqSec,i,56)
				Li++
			Next i
		ElseIf !Empty(cMqSec) 
			@Li,66 PSay STR0019	//"Secundarios:"
			@Li,80 PSay SUBS(cMqSec,1,56)
		EndIf
		
		If LEN(cMqSec) > 56
			Li++
		Else
			Li+=2
		EndIf

		lFirst := .T.

		FOR I:=1 TO 7

			cCalend := aDia[I]

			IF li > If(nPrecisa = 60, 45, 55)
				cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
			Endif

			If nPrecisa < 4
				cNewAloc := ""
				For x := 1 to Len(cCalend)
					cNewAloc += Replicate(SubStr(cCalend, x, 1), If(nPrecisa = 1, 4, 2))
				Next
				cCalend := cNewAloc
			Endif		
		
			aAloc := A830Divide(cCalend, Len(aEscala[1, 1]) - 2)
			For x := 1 to Len(aEscala)

				If li > 58
					cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)	
				Endif	

				If i = 1 .or. Len(aEscala) > 1
					@li++,00 pSay Space(Len(aDayWeek[i])) + " | " + aEscala[x, 1]
					@li++,00 pSay Space(Len(aDayWeek[i])) + " | " + aEscala[x, 2]
				Endif
				@li++,00 pSay If(x = 1, aDayWeek[i], Space(Len(aDayWeek[i]))) + " |  " + ;
							IIF(Len(aAloc) >= x, aAloc[x], '')
			Next

			cCalend := ""

			If I == 7 
				Li++
				@Li,00 PSay __PrtThinLine()
				Li+=2
			Endif

		NEXT I

		If Len(aBloqueio) > 0	

			IF li > 58
				cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
			Endif

			@Li,00  PSay STR0020	//"Bloqueio(s)"
			Li++
			@Li,00  PSay "-----------"
			Li+=2

			lFirst := .T.		

			FOR I := 1 TO LEN(aBloqueio)

				IF li > 58
					cabec(titulo,cabec1,cabec2,nomeprog,tamanho,nTipo)
				Endif

				If lFirst
					@Li,00 PSay STR0021	//"Data Inicio   Hora Inicio   Data Final   Hora Final"
					Li++
					@Li,00 PSay __PrtThinLine()
					Li++
					lFirst := .F.
				Endif

				@Li,00 PSay aBloqueio[I,1]
				@Li,14 PSay aBloqueio[I,3]
				@Li,28 PSay aBloqueio[I,2]
				@Li,41 PSay aBloqueio[I,4]
				Li++

			NEXT I

			Li++

		Endif

	EndIf

	Afill(aDia,"")

	dbSelectArea("SH1")

	dbSkip()

EndDO

IF  LI != 80
	roda(cbcont,cbtxt, "G")
EndIF

dbSelectarea("SH1")
Set Filter To
dbSetOrder(1)

If aReturn[5] = 1
   Set Printer TO 
   dbCommitall()
   ourspool(wnrel)
Endif

MS_FLUSH()

Return NIL

/*


Ŀ
Funo     MAQALT    Autor  Paulo Boschetti        Data  07.08.92 
Ĵ
Descrio Pesquisa maquinas alternativas                              
                                                                      
Ĵ
Sintaxe e  MAQALT(VOID)                                               
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
STATIC FUNCTION MaqAlt(cMaquina)

Local cMaqAnt := SPACE(8)
Local cMaqAlt := ""

dbSelectarea("SH2")
dbSeek(cFilial+cMaquina)

If EOF()
	dbSelectarea("SH1")
	Return ""
Else
	cMaqAnt := cFilial+cMaquina
	While !eof() .and. H2_FILIAL+H2_RECPRIN == cMaqAnt
		If H2_TIPO == "A"
			cMaqAlt += H2_RECALTE+" "
		EndIf
		dbSkip()
	EndDo
	dbSelectarea("SH1")
	Return cMaqAlt
EndIf

/*


Ŀ
Funo     MAQSEC    Autor  Rodrigo de A. Sartorio Data  17/10/95 
Ĵ
Descrio Pesquisa maquinas secundarias                               
                                                                      
Ĵ
Sintaxe e  MAQSEC(VOID)                                               
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
STATIC FUNCTION MaqSec(cMaquina)
Local cMaqAnt := SPACE(8)
Local cMaqSec := ""
dbSelectarea("SH2")
dbSeek(cFilial+cMaquina)
If EOF()
	dbSelectarea("SH1")
	Return ""
Else
	cMaqAnt := cFilial+cMaquina
	While !eof() .and. H2_FILIAL+H2_RECPRIN == cMaqAnt
		If H2_TIPO == "S"
			cMaqSec += H2_RECALTE+" "
		EndIf
		dbSkip()
	EndDo
	dbSelectarea("SH1")
	Return cMaqSec
EndIf

/*


Ŀ
Funo     BLOQUEIO  Autor  Paulo Boschetti        Data  07.08.92 
Ĵ
Descrio  Monta Array com  bloqueio de recursos                      
                                                                      
Ĵ
Sintaxe e  BLOQUEIO(VOID)                                             
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ

*/
STATIC FUNCTION BLOQUEIO(cMaquina,cCCusto)

Local cMaqAnt := SPACE(8)
Local aBloqueio := {}

dbSelectarea("SH9")
dbSeek(cFilial+"B"+cCCusto+cMaquina)

If EOF()

	dbSelectarea("SH1")

Else

	cMaqAnt := cFilial+"B"+cCCusto+cMaquina

	While !eof() .and. H9_FILIAL+H9_TIPO+H9_CCUSTO+H9_RECURSO == cMaqAnt
		
		If H9_DTINI >= dDatabase

			AADD( aBloqueio , { H9_DTINI , H9_DTFIM , H9_HRINI , H9_HRFIM } )

		Endif

		dbSkip()

	EndDo

	dbSelectarea("SH1")

EndIf

Return aBloqueio


/*


Ŀ
Funao     A830Week  Autor  Marcelo Iuspa          Data  31-07-00 
Ĵ
Descrio  Monta Array com escala de horas de acordo com a precisao   
           (parametro mv_precisa)                                     
Ĵ
Sintaxe    A830Week                                                   
Ĵ
Parametros Nenhum (usa o GetMv("MV_PRECISA"))                         
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function A830Week
Local nPrecisa := GetMV("MV_PRECISA")
Local nLinha   := nPrecisa 
Local aRelacao := {{60, 3}, {12, 12}, {6, 12}, {4, 24}, {2, 24}, {1, 24}}
Local aEscala  := {}
Local cRegua, cEscala
Local x

If nPrecisa < 4	// Para precisao menor que 15 minutos no diminuo a escala
	nPrecisa := 4
	nLinha   := 4
Endif	

nHoraCorte := aScan(aRelacao, {|_1| _1[1] = nPrecisa})
If nHoraCorte = 0
	Return(.F.)
Endif
nHoraCorte := aRelacao[nHoraCorte, 2]

cRegua  := " 0"
cEscala := " |"

For x := 1 to 24
	cRegua  += Padl(Str(x, 2), nLinha)
	cEscala += Replicate("-", nLinha - 1) + "|"
	If x % nHoraCorte = 0	
		Aadd(aEscala, {cRegua, cEscala})
		cRegua  := Str(x, 2)
		cEscala := " |"
	Endif	
Next	
Return(aEscala)


/*


Ŀ
Funao    A830Divide Autor  Marcelo Iuspa          Data  31-07-00 
Ĵ
Descrio  Divide a string que contem a utilizacao do recurso em      
           funcao do calendario usado, para permitir sua impressao    
Ĵ
Sintaxe    A830Divide                                                 
Ĵ
Parametros String contendo a alocacao do recurso                      
Ĵ
 Uso       Generico                                                   
ٱ

*/
Function A830Divide(cAloc, nTamanho)
Local aRet := {}
While Len(cAloc) > 0
	Aadd(aRet, SubStr(cAloc, 1, nTamanho))
	cAloc := SubStr(cAloc, nTamanho + 1)
Enddo	
Return(aRet)

/*


Ŀ
Funao    A830Graph  Autor  Felipe Nunes Toledo    Data  21/07/06 
Ĵ
Descrio  Imprime o Calendario utilizado pelo Recurso em modelo      
           Grafico.                                                   
Ĵ
Sintaxe    A830Graph(ExpO1, ExpN2, ExpA3, ExpA4)                      
Ĵ
Parametros ExpO1: Objeto Report do Relatorio                          
           ExpN2: Precisao utilizada no Calendario (MV_PRECISA)       
           ExpA3: Array contendo os Dias da Semanas                   
                  Ex. {"Seg","Ter","Qua",...}                         
           ExpA4: Array contendo a alocacao dos Dias da Semana        
Ĵ
 Uso       MATR830                                                    
ٱ

*/
Static Function A830Graph(oReport, nPrecisa, aDayWeek, aDia)
Local nPixFull   := 1614   //-- 1614 Pixels = 24 hr
Local nPixel     := ((nPixFull / 24) / nPrecisa)   //-- Tamanho do Pixel conforme a Precisao
Local nPixFat    := 0
Local nPixRegua  := (nPixFull / 96)   //-- Regua com Precisao de 15 Minutos
Local cBmpCalen  := OnePixBmp(ConvRGB(CLR_HBLUE),,"PMSBMP")
Local cBmpRegua  := OnePixBmp(ConvRGB(CLR_BLACK),,"PMSBMP")
Local cCalen     := ""
Local nColIni    := 80 
Local nAltura    := 30   //-- Altura do tracejado da Regua
Local nMult      := 0
Local aMult[24]
Local x,y
Local oFnt11N    := TFont():New("Arial",11,11,,.T., , , , .t., .f.)

nCntBitMap += 97

//-- Tratamento para o espacamento do Pixel de acordo com precisao.
If nPrecisa == 1 
	nPixFat := nPixel*0.10
ElseIf nPrecisa == 2 
	nPixFat := nPixel*0.15
ElseIf nPrecisa == 4
	nPixFat := nPixel*0.20
ElseIf nPrecisa == 6
	nPixFat := nPixel*0.35
ElseIf nPrecisa == 12
	nPixFat := nPixel*0.80
ElseIf nPrecisa == 60
	nPixFat := nPixel*3.5
EndIf
	
For x:=1 To 24 // Adiciona no Array numeros multiplos de 4, para tratamento da Regua
	aMult[x] := x * 4
Next x

//Ŀ
//Impressao dos Numeros superiores da Regua
//
For x := 0 To 24
	oReport:Say(oReport:Row(),oReport:Col()+nColIni+(nPixRegua*x*4),AllTrim(Str(x)))
Next x

oReport:SkipLine() //Pula Linha	

//Ŀ
//Impressao da Regua
//
If !(IncrContad())
	Return .f.
EndIf
oReport:SayBitmap( oReport:Row() ,oReport:Col()+nColIni,cBmpRegua,3,( oReport:Row()+ (nAltura-oReport:Row()) ))
oReport:Say(oReport:Row(), oReport:Col()+nColIni, "-", oFnt11N)
nAltura := 20
For x := 1 To 96
	nMult := aScan(aMult,x)
	If nMult > 0
		nAltura := 30
	EndIf
	If !(IncrContad())
		Return .f.
	EndIf
	oReport:SayBitmap( oReport:Row() ,oReport:Col()+nColIni+(nPixRegua*x),cBmpRegua,3,(oReport:Row()+nAltura-oReport:Row()))	
	nAltura := 20
Next x

oReport:SkipLine() //Pula Linha	
oReport:PrintText("    | ")	

//Ŀ
//Impressao do Calendario
//
For x := 1 To Len(aDayWeek)

	//Ŀ
	//Verifica se cabera na pagina 
	//
    If oReport:PageHeight() <= oReport:Row()+ 100
    	oReport:EndPage()
		nCntBitMap := 0
    EndIf

	oReport:PrintText(aDayWeek[x]+" | ")	
	cCalen := aDia[x]
	For y := 1 To (Len(cCalen))
		If Upper(SubStr(cCalen,y,1))=='X'	
			If !(IncrContad())
				Return .f.
			EndIf
			oReport:SayBitmap( oReport:Row()-25,oReport:Col()+nColIni-nPixel+(nPixel*y),cBmpCalen,nPixel+nPixFat,(oReport:Row()+20-oReport:Row()))
		EndIf		  	 
	Next y            
Next x

Return .t.

/*


Ŀ
Funao    IncrContad   Autor  Marcos Wagner Junior   Data  18/11/20 
Ĵ
Descrio  Verifica a limitacao de tamanho por pagina,                
Ĵ
 Uso       MATR830                                                    
ٱ

*/
Static Function IncrContad()
Default lAutomacao  := .f.

If !lAutomacao
	nCntBitMap++
	If nCntBitMap > 9000
		Help(" ",1,STR0026, ,STR0025,1,0, ,,,,,{STR0027}) // Ateno! No  possvel gerar o relatrio personalizvel devido  limitao de caracteres por pgina! Utilize o relatrio do tipo texto ou ajuste a os parmetros com uma faixa menor para os recursos do...at
		Return .f.
	EndIf
EndIf

Return .t.
