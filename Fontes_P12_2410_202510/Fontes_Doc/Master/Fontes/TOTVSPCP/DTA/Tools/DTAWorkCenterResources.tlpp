#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAWorkCenterResources.CH"

/*/{Protheus.doc} DTAWorkCenterResources
Classe responsável pela ferramenta de busca de recursos do centro de trabalho

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
/*/
Class DTAWorkCenterResources
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@return cErrorMessage, Character, Indica se houve erro na carga da ferramenta
/*/
Method loadTool() as Character Class DTAWorkCenterResources
	Local cDescription  := ""  as Character
	Local cErrorMessage := ""  as Character
	Local cName         := ""  as Character
	Local cVersion      := ""  as Character
	Local oLoadTool     := Nil as Object

	cName    := "get_work_center_resources"
	cVersion := "001"

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 //"Fornece uma lista dos recursos vinculados a um centro de trabalho. Também conhecido como 'CT'."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAWorkCenterResources")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setShortDescription(STR0002) //"Recupera os recursos vinculados à um centro de trabalho"
	oLoadTool:setExample(I18N(STR0003, {"work_center_code"})) //"Me dê informações do CT '#1[work_center_code]#'."
	oLoadTool:setExample(I18N(STR0004, {"work_center_code"})) //"Quais os recursos do centro de trabalho '#1[work_center_code]#'?"
	oLoadTool:setStrict(.T.)
	oLoadTool:setGroup("resource_usage")
	oLoadTool:addParameter("work_center_code", "string", STR0005 + ". \n " + STR0006, .T.) //"Código do centro de trabalho" //"O código do centro de trabalho pode conter somente letras, somente números ou a combinação de caracteres, podendo ou não ter caracteres especiais. Exemplo: 'CT0001', '000001'."
	oLoadTool:addGlossary("work_center_code"    , STR0005) //"Código do centro de trabalho"
	oLoadTool:addGlossary("work_center_name"    , STR0007) //"Nome do centro de trabalho"
	oLoadTool:addGlossary("resources_list"      , STR0008) //"Recursos vinculados ao centro de trabalho"
	oLoadTool:addGlossary("resource_code"       , STR0009) //"Código do recurso"
	oLoadTool:addGlossary("resource_description", STR0010) //"Descrição do recurso"
	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Busca os recursos relacionados a um centro de trabalho. Para a busca, é obrigatório que seja enviado o filtro:
"work_center_code" - código do centro de trabalho;

@author lucas.franca/renan.roeder
@since 21/01/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das informações do centro de trabalho.
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAWorkCenterResources
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cContext     := ""  as Character
	Local jData        := Nil as Json

	If Empty(jFilters["work_center_code"])
		Return DTAUtils():APIErrorMessage(400, STR0011) //"Para realizar a busca dos recursos relacionados ao centro de trabalho, é necessário informar o código do centro de trabalho."
	EndIf
	jFilters["work_center_code"] := Upper(PadR(jFilters["work_center_code"], GetSX3Cache("H1_CTRAB", "X3_TAMANHO")))
	SHB->(dbSetOrder(1))
	If !SHB->(dbSeek(xFilial("SHB") + jFilters["work_center_code"]))
		Return DTAUtils():APIErrorMessage(400, I18N(STR0012, {RTrim(jFilters["work_center_code"])})) //"O centro de trabalho #1[work_center_code]# não existe. Informe um centro de trabalho existente para realizar a pesquisa."
	EndIf
	
	jData := JsonObject():New()
	jData["work_center_code"] := RTrim(jFilters["work_center_code"])
	jData["work_center_name"] := RTrim(SHB->HB_NOME)
	jData["resources_list"  ] := {} 
	SH1->(dbSetOrder(4))
	If SH1->(dbSeek(xFilial("SH1") + jFilters["work_center_code"]))
		While SH1->(!EoF()) .And. SH1->(H1_FILIAL+H1_CTRAB) == xFilial("SH1") + jFilters["work_center_code"]
			aAdd(jData["resources_list"], {"resource_code": RTrim(SH1->H1_CODIGO), "resource_description": RTrim(SH1->H1_DESCRI)})
			SH1->(dbSkip())
		End
	EndIf
	If Len(jData["resources_list"]) > 0
		aAdd(aSuggestions, {"label": STR0013}) //"Quantas horas foram apontadas nesses recursos nos últimos 15 dias?"
		aAdd(aSuggestions, {"label": STR0014}) //"Quais ordens de produção preciso produzir nesses recursos hoje?"
		aAdd(aSuggestions, {"label": STR0015}) //"Algum desses recursos possui uma parada iniciada?"
		aAdd(aSuggestions, {"label": STR0016}) //"Quais os recursos alternativos desses recursos?"
	EndIf
	cContext := I18N(STR0017, {"work_center_code", "work_center_name", "work_center_code", "work_center_name"}) //"Quando apresentar as informações de um centro de trabalho, sempre combine o '#1[work_center_code]#' e o '#2[work_center_name]#' em uma única frase, no seguinte formato: Centro de Trabalho: ['#3[work_center_code]#'] - ['#4[work_center_name]#']."
	cContext += "\n " + I18N(STR0018, {"resource_code", "resource_description", "resource_code", "resource_description"}) //"Quando apresentar as informações de um recurso, sempre combine o '#1[resource_code]#' e a '#2[resource_description]#' em uma única frase, no seguinte formato: Recurso: ['#3[resource_code]#'] - ['#4[resource_description]#']."
	aReturn  := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}

	aSuggestions := Nil
	jData := Nil
Return aReturn
