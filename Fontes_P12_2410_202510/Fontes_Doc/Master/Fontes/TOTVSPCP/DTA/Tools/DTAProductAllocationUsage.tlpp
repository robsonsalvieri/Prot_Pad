#Include "TLPP-CORE.TH"
#Include "TOTVS.CH"
#Include "DTAProductAllocationUsage.CH"

/*/{Protheus.doc} DTAProductAllocationUsage
Classe responsável pela ferramenta de busca de ordens de produção que utilizam determinado empenho

@author lucas.franca/renan.roeder
@since 03/09/2025
@version P12
/*/
Class DTAProductAllocationUsage
	Static Method getData(jFilters as Json) as Array
	Static Method loadTool() as Character
EndClass

/*/{Protheus.doc} loadTool
Método que faz a carga das propriedades das ferramentas disponíveis por esta classe. Alimenta as tabelas HZV/HZW.

@author lucas.franca/renan.roeder
@since 03/09/2025
@version P12
@return cErrorMessage, Character, Indica se houve erro na carga da ferramenta
/*/
Method loadTool() as Character Class DTAProductAllocationUsage
	Local cDescription  := ""  as Character
	Local cErrorMessage := ""  as Character
	Local cName         := ""  as Character
	Local cVersion      := ""  as Character
	Local cRules        := ""  as Character
	Local oLoadTool     := Nil as Object

	cName    := "get_product_allocation_usage"
	cVersion := "003"

	If DTALoad():toolIsUpdated(cName, cVersion)
		Return ""
	EndIf

	cDescription := STR0001 //"A partir de um código de MATÉRIA-PRIMA, COMPONENTE, INSUMO ou EMPENHO, rastreia e lista todas as Ordens de Produção (OPs) que consomem este produto."
	cRules       := "\n***" + STR0008 + "***: " + STR0002 //"IMPORTANTE" //"Nesta ferramenta SOMENTE informações relacionadas à EMPENHOS podem ser obtidas."
	cRules       += "\n\n***" + STR0023 + "***\n" + STR0031 + "\n" + STR0032 //ALERTA DE USO: //"Esta ferramenta opera em UMA ÚNICA DIREÇÃO: 'Componente -> Produto Final'. //"Ela **NÃO SERVE** para listar os materiais necessários para fabricar um produto (operação inversa)."
	oLoadTool := DTALoad():New()
	oLoadTool:setName(cName)
	oLoadTool:setClassName("DTAProductAllocationUsage")
	oLoadTool:setDataMethod("getData")
	oLoadTool:setVersion(cVersion)
	oLoadTool:setDescription(cDescription)
	oLoadTool:setRules(cRules)
	oLoadTool:setShortDescription(STR0003) //"Lista as ordens de produção que utilizam um empenho"
	oLoadTool:setExample(I18N(STR0005, {"product_code"})) //"Quais ordens de produção vão consumir o produto '#1[product_code]#' até o fim desse mês?"
	oLoadTool:setExample(I18N(STR0007, {"product_code"})) //"O produto '#1[product_code]#' está empenhado em quais ordens de produção?"
	oLoadTool:setExample(I18N(STR0006, {"product_code", "end_date_optional"})) //"Qual o saldo total empenhado do produto '#1[product_code]#' até '#2[end_date]#'?"
	oLoadTool:setExample(I18N(STR0026, {"product_code", "start_date_optional"})) //"Em quantas ordens de produção o produto '#1[product_code]#' está empenhado a partir de '#2[start_date]#'?"
	oLoadTool:setExample(I18N(STR0033, {"product_code"})) //"Qual é a previsão de consumo do produto '#1[product_code]#' para o mês de Dezembro?"
	oLoadTool:setStrict(.T.)
	oLoadTool:setGroup("production_order")
	oLoadTool:addParameter("product_code"       , "string" , STR0009, .T.) //"Código da MATÉRIA-PRIMA, COMPONENTE, INSUMO ou EMPENHO que se deseja rastrear. Este código deve ser de um item de nível inferior que é CONSUMIDO para fabricar outra coisa. JAMAIS deve ser o código de um PRODUTO FINAL ou VENDÁVEL."
	oLoadTool:addParameter("start_date_optional", {"string", "null"}, STR0034+"\n"+STR0035+"\n"+STR0036, .F.) //"Data inicial para busca da utilização dos empenhos. //"Utilize este argumento para filtrar a utilização dos empenhos que possuem data igual ou maior à data informada. //"Este argumento é opcional, e quando não informado permite a busca ilimitada dos empenhos."
	oLoadTool:addParameter("end_date_optional"  , {"string", "null"}, STR0037+"\n"+STR0038+"\n"+STR0036, .F.) //"Data final para busca da utilização dos empenhos. //"Utilize este argumento para filtrar a utilização dos empenhos que possuem data igual ou menor à data informada. //"Este argumento é opcional, e quando não informado permite a busca ilimitada dos empenhos."
	oLoadTool:addGlossary("product_code"                         , STR0010) //"Código do produto empenhado"
	oLoadTool:addGlossary("product_description"                  , STR0011) //"Descrição do produto empenhado"
	oLoadTool:addGlossary("product_unit_measure"                 , STR0012) //"Unidade de medida do produto empenhado"
	oLoadTool:addGlossary("production_orders_list"               , STR0013) //"Lista das ordens de produção que possuem o produto empenhado"
	oLoadTool:addGlossary("total_orders"                         , STR0027) //"Quantidade de ordens de produção que possuem o produto no empenho"
	oLoadTool:addGlossary("total_balance"                        , STR0028) //"Saldo total empenhado do produto"
	oLoadTool:addGlossary("production_order_code"                , STR0014) //"Código da Ordem de Produção, também conhecido como OP ou Ordem"
	oLoadTool:addGlossary("production_order_product"             , STR0015) //"Produto da ordem de produção"
	oLoadTool:addGlossary("production_order_product_description" , STR0016) //"Descrição do produto da ordem de produção"
	oLoadTool:addGlossary("production_order_product_unit_measure", STR0017) //"Unidade de medida do produto da ordem de produção"
	oLoadTool:addGlossary("production_order_allocation_balance"  , STR0029) //"Saldo do empenho na ordem de produção"
	oLoadTool:addGlossary("allocation_date_list"                 , STR0030) //"Lista de empenhos da ordem por data"
	oLoadTool:addGlossary("allocation_balance"                   , STR0018) //"Quantidade do empenho a ser consumida"
	oLoadTool:addGlossary("allocation_date"                      , STR0019) //"Data do empenho"

	If !oLoadTool:createTool()
		cErrorMessage := oLoadTool:getErrorMessage()
	EndIf
	oLoadTool:Destroy()
Return cErrorMessage

/*/{Protheus.doc} getData
Método que faz a busca de quais ordens utilizam determinado produto como empenho

@author lucas.franca/renan.roeder
@since 03/09/2025
@version P12
@param 01 jFilters, Json, Filtros que serão aplicados para a busca das ordens com o empenho
@return aReturn, Array, Array com os dados encontrados, sendo:
        aReturn[1] - Logical - Identifica se processou com sucesso a busca.
        aReturn[2] - Json    - Dados obtidos na busca ou mensagem de erro caso não consiga fazer a busca.
        aReturn[3] - Numeric - Status que representa o retorno (200 ok, 400 erro.)
/*/
Method getData(jFilters as Json) as Array Class DTAProductAllocationUsage
	Local aReturn      := {}  as Array
	Local aSuggestions := {}  as Array
	Local cContext     := ""  as Character
	Local jData        := Nil as Json
	Local lAllRows     := .T. as Logical

	If Empty(jFilters["product_code"])
		Return DTAUtils():APIErrorMessage(400, I18N(STR0020, {"product_code"})) //"Para realizar a busca de onde um produto é utilizado como empenho, o parâmetro '#1[PARAMETRO]#' deve ser informado."
	EndIf
	jFilters["product_code"] := Upper(PadR(jFilters["product_code"], GetSX3Cache("B1_COD", "X3_TAMANHO")))
	SB1->(dbSetOrder(1))
	If !SB1->(dbSeek(xFilial("SB1") + jFilters["product_code"]))
		Return DTAUtils():APIErrorMessage(400, I18N(STR0021, {RTrim(jFilters["product_code"])})) //"O produto #1[PRODUTO]# não existe. Informe um produto existente para realizar a pesquisa."
	EndIf
	If !Empty(jFilters["start_date_optional"])
		jFilters["start_date_optional"] := PCPConvDat(jFilters["start_date_optional"], 1)
		If Empty(jFilters["start_date_optional"])
			Return DTAUtils():APIErrorMessage(400, I18N(STR0039, {"start_date_optional"})) //"Argumento '#1[ARGUMENTO]#' não é uma data válida. Informe uma data correta para realizar a pesquisa."
		EndIf
	EndIf
	If !Empty(jFilters["end_date_optional"])
		jFilters["end_date_optional"] := PCPConvDat(jFilters["end_date_optional"], 1)
		If Empty(jFilters["end_date_optional"])
			Return DTAUtils():APIErrorMessage(400, I18N(STR0039, {"end_date_optional"})) //"Argumento '#1[ARGUMENTO]#' não é uma data válida. Informe uma data correta para realizar a pesquisa."
		EndIf
	EndIf
	jData := JsonObject():New()
	jData["product_code"          ] := RTrim(SB1->B1_COD)
	jData["product_description"   ] := RTrim(SB1->B1_DESC)
	jData["product_unit_measure"  ] := RTrim(SB1->B1_UM)
	jData["production_orders_list"] := DTAProductionOrderAllocations():getAllocationsFromProduct(SB1->B1_COD,,, @lAllRows, jFilters["start_date_optional"], jFilters["end_date_optional"])
	If jData["production_orders_list"]["total_orders"] == 0
		Return DTAUtils():APIErrorMessage(400, I18N(STR0022, {RTrim(jFilters["product_code"])})) //"O produto #1[PRODUTO]# não possui empenhos."
	Else
		If !lAllRows
			cContext := I18N(STR0024, {jData["production_orders_list"]["total_orders"], "production_orders_list", "total_balance"}) //"Foram encontrados empenhos deste produto em #1[QTD_ORDENS]# ordens de produção e a '#2[LISTA_OP]#' está limitada às primeiras 20 ordens de produção, não sendo possível obter a lista completa. O '#3[SALDO_TOTAL]#' representa a quantidade total em todas as ordens encontradas. Use essas informações para orientar o usuário sobre o limite dos dados. Pode-se filtrar um período de datas menor para obter a lista completa."
		EndIf
		aAdd(aSuggestions, {"label": STR0025}) //"Quanto tenho em estoque desse produto?"
		aReturn := {.T., {"context": cContext, "data": jData, "suggestions": aSuggestions}, 200}
	EndIf
	jData        := Nil
	aSuggestions := Nil
Return aReturn
