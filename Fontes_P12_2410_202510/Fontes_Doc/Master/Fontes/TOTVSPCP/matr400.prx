#INCLUDE "MATR400.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo     MATR400   Autor  Ricardo Berti          Data  17/05/06 
Ĵ
Descrio  Emite uma relacao informando onde um determinado componente
           e' utilizado.                                         	  
Ĵ
Sintaxe    MATR400(void)                                              
Ĵ
 Uso       Generico                                            		  
ٱ


/*/                   
Function MATR400()
Local cExpData    := ""
Local lRetMsg     := .T.
Local oReport

If GetRpoRelease() >= "12.1.2410"
	cExpData := "20260331"
	lRetMsg  := PCPExpSV(cExpData,"MATR400", "PCPR400")
EndIf

If lRetMsg  
	If TRepInUse()
		//Ŀ
		//Interface de impressao                                                  
		//
		oReport := ReportDef()
		oReport:PrintDialog()
	Else
		MATR400R3()
	EndIf
EndIf

Return


/*/


Ŀ
Programa  ReportDef  Autor  Ricardo Berti          Data 17/05/2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   ExpO1: Objeto do relatrio                                  
Ĵ
ParametrosNenhum                                                      
Ĵ
 Uso       Generico                                            		  
ٱ


/*/
Static Function ReportDef()

Local oReport
Local oSection1
Local oSection2
Local oSection3
Local oCell

//-- Variaveis utilizadas para determinar o tamanho das celulas
Local nB1_COD  := TamSX3("B1_COD")[1]
Local cB1_COD  := PesqPict("SB1","B1_COD") 
Local nB1_DESC := TamSX3("B1_DESC")[1]
Local cB1_DESC := PesqPict("SB1","B1_DESC") 
Local nB1_TIPO := TamSX3("B1_TIPO")[1]
Local cB1_TIPO := PesqPict("SB1","B1_TIPO")
Local nB1_UM   := TamSX3("B1_UM")[1]
Local cB1_UM   := PesqPict("SB1","B1_UM")
Local nB1_GRUPO:= TamSX3("B1_GRUPO")[1]
Local cB1_GRUPO:= PesqPict("SB1","B1_GRUPO") 
Local nG1_NIV  := TamSX3("G1_NIV")[1]
Local cG1_NIV  := PesqPict("SG1","G1_NIV") 
Local nG1_QUANT:= TamSX3("G1_QUANT")[1]
Local cG1_QUANT:= PesqPict("SG1","G1_QUANT")


//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//                                                                        
//
oReport := TReport():New("MATR400",STR0001,"MTR400", {|oReport| ReportPrint(oReport)},STR0002+" "+STR0003) //"Onde e' Usado"##"Emite uma relao informando onde determinados materiais selecionados"##"so utilizados na composio da estrutura."

//Ŀ
// Verifica as perguntas selecionadas                           
//
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01             // Componente de                        
// mv_par02             // Componente ate                       
// mv_par03             // Impr. Comp. Vencido                  
// mv_par04             // Visual. Todos Niveis                 
// mv_par05             // Considera Revisoes na Estrutura ?    
//
Pergunte("MTR400",.F.)

//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da secao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatorio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//
//Ŀ
//Criacao da celulas da secao do relatorio                                
//                                                                        
//TRCell():New                                                            
//ExpO1 : Objeto TSection que a secao pertence                            
//ExpC2 : Nome da celula do relatrio. O SX3 sera consultado              
//ExpC3 : Nome da tabela de referencia da celula                          
//ExpC4 : Titulo da celula                                                
//        Default : X3Titulo()                                            
//ExpC5 : Picture                                                         
//        Default : X3_PICTURE                                            
//ExpC6 : Tamanho                                                         
//        Default : X3_TAMANHO                                            
//ExpL7 : Informe se o tamanho esta em pixel                              
//        Default : False                                                 
//ExpB8 : Bloco de codigo para impressao.                                 
//        Default : ExpC2                                                 
//

//Ŀ
// Section 1                                                    
//
oSection1 := TRSection():New(oReport,STR0009,{"SB1"},/*Array c/Ordens*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Produtos"
oSection1:SetHeaderSection(.F.)

TRCell():New(oSection1,"B1_COD"		,"SB1",STR0012+CRLF+STR0013	,cB1_COD	,nB1_COD+2	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_DESC"	,"SB1",STR0014				,cB1_DESC	,nB1_DESC	,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_TIPO"	,"SB1",STR0015				,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_GRUPO"	,"SB1",STR0016				,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection1,"B1_UM"		,"SB1",STR0017				,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)

//Ŀ
// Section 2                                                    
//
oSection2 := TRSection():New(oSection1,STR0010,{"SB1","SG1"}) //"Estruturas"
oSection2:SetHeaderPage()

TRCell():New(oSection2,"G1_COD"		,"SG1",STR0012+CRLF+STR0013	,""			,nB1_COD+2  ,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"DESC"		,"SB1",STR0014				,cB1_DESC	,nB1_DESC	,/*lPixel*/,{||SB1->B1_DESC})
TRCell():New(oSection2,"TIPO"		,"SB1",STR0015				,cB1_TIPO	,nB1_TIPO	,/*lPixel*/,{||SB1->B1_TIPO})
TRCell():New(oSection2,"GRUPO"		,"SB1",STR0016				,cB1_GRUPO	,nB1_GRUPO	,/*lPixel*/,{||SB1->B1_GRUPO})
TRCell():New(oSection2,"UM"			,"SB1",STR0017				,cB1_UM		,nB1_UM		,/*lPixel*/,{||SB1->B1_UM})
TRCell():New(oSection2,"G1_NIV"		,"SG1",STR0018				,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)
TRCell():New(oSection2,"G1_QUANT"	,"SG1",STR0019+CRLF+STR0020	,/*Picture*/,/*Tamanho*/,/*lPixel*/,{||G1_QUANT})

//Ŀ
// Section 3                                                    
//
oSection3 := TRSection():New(oSection2,STR0011,{"SB1","SG1"},/*Array c/Ordens*/,/*Campos do SX3*/,/*Campos do SIX*/) //"Estruturas (recursividade)"
oSection3:SetHeaderSection(.F.)

TRCell():New(oSection3,"COD3"	,"SB1",STR0012+CRLF+STR0013	,""			,nB1_COD+3	,/*lPixel*/,{||SB1->B1_COD})
TRCell():New(oSection3,"DESC3"	,"SB1",STR0014				,cB1_DESC	,nB1_DESC	,/*lPixel*/,{||SB1->B1_DESC})
TRCell():New(oSection3,"TIPO3"	,"SB1",STR0015				,cB1_TIPO	,nB1_TIPO	,/*lPixel*/,{||SB1->B1_TIPO})
TRCell():New(oSection3,"GRUPO3"	,"SB1",STR0016				,cB1_GRUPO	,nB1_GRUPO	,/*lPixel*/,{||SB1->B1_GRUPO})
TRCell():New(oSection3,"UM3"	,"SB1",STR0017	   			,cB1_UM		,nB1_UM		,/*lPixel*/,{||SB1->B1_UM})
TRCell():New(oSection3,"NIV3"	,"SG1",STR0018				,cG1_NIV	,nG1_NIV	,/*lPixel*/,{||SG1->G1_NIV})
TRCell():New(oSection3,"QUANT3"	,"SG1",STR0019+CRLF+STR0020	,cG1_QUANT  ,nG1_QUANT  ,/*lPixel*/,{||SG1->G1_QUANT})

Return(oReport)

/*/


Ŀ
Programa  ReportPrin Autor  Ricardo Berti          Data 17/05/2006
Ĵ
Descrio A funcao estatica ReportDef devera ser criada para todos os 
          relatorios que poderao ser agendados pelo usuario.          
                                                                      
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpO1: Objeto Report do Relatrio                           
                                                                      
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function ReportPrint(oReport)

Local bWhile1	:= {}
Local bWhile2	:= {}
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(1):Section(1)
Local cAliasQRY := "SB1"
Local cAliasSG1 := "SG1"
Local lFirst	:= .T.
Local lTraco	:= .F.
Local cCompo    := ""
Local nReg
Local cQuery	:= ''
dbSelectArea("SG1")
dbSelectArea("SB1")
dbSetOrder(1)
//Ŀ
//Filtragem do relatorio                                                  
//

//Ŀ
//Transforma parametros Range em expressao SQL                            	
//
MakeSqlExpr(oReport:uParam)
//Ŀ
//Query do relatorio da secao 1                                           
//
oReport:Section(1):BeginQuery()	
cAliasQRY := GetNextAlias()
cAliasSG1 := cAliasQRY

cQuery :="%"
If mv_par03 == 2
	cQuery += " AND G1_INI <= '"+DTOS(dDataBase)+"' AND G1_FIM >= '"+DTOS(dDataBase)+"' "
EndIf

cQuery +="%"

BeginSql Alias cAliasQRY
	SELECT	B1_FILIAL,B1_COD,B1_DESC,B1_TIPO,B1_GRUPO,B1_UM,G1_COD,G1_NIV,G1_QUANT,G1_COMP,G1_REVINI,G1_REVFIM
	
	    FROM %table:SB1% SB1
 	    
 	    JOIN %table:SG1% SG1
    	
    	ON	G1_FILIAL = %xFilial:SG1% AND
			G1_COMP   = B1_COD AND
			SG1.%NotDel%	
			
	WHERE	B1_FILIAL = %xFilial:SB1% AND
			B1_COD   >= %Exp:mv_par01% AND
			B1_COD   <= %Exp:mv_par02% AND
			SB1.%NotDel%	
			%Exp:cQuery%
			
	ORDER BY %Order:SB1%
EndSql 
//Ŀ
//Metodo EndQuery ( Classe TRSection )                                    
//                                                                        
//Prepara o relatorio para executar o Embedded SQL.                       
//                                                                        
//ExpA1 : Array com os parametros do tipo Range                           
//                                                                        
//

oReport:Section(1):EndQuery(/*Array com os parametros do tipo Range*/)
oSection2:SetRelation({|| xFilial("SG1")+(cAliasQRY)->G1_COD},"SG1",1,.T.)


//Ŀ
//Metodo TrPosition()                                                     
//                                                                        
//Posiciona em um registro de uma outra tabela. O posicionamento ser     
//realizado antes da impressao de cada linha do relatorio.                
//                                                                        
//ExpO1 : Objeto Report da Secao                                          
//ExpC2 : Alias da Tabela                                                 
//ExpX3 : Ordem ou NickName de pesquisa                                   
//ExpX4 : String ou Bloco de codigo para pesquisa. A string sera macroexe-
//        cutada.                                                         
//

TrPosition():New(oSection2,"SG1",1,{|| xFilial("SG1")+(cAliasQRY)->G1_COD+(cAliasQRY)->G1_COMP})

//Ŀ
//Inicio da impressao do fluxo do relatorio                               
//

oReport:SetMeter(SB1->(LastRec()))
dbSelectArea(cAliasQRY)

bWhile1 := { || !oReport:Cancel() .And. !(cAliasQRY)->(Eof()) }
bWhile2 := { || !oReport:Cancel() .And. !(cAliasQRY)->(Eof()).And.(cAliasQRY)->B1_FILIAL+(cAliasQRY)->B1_COD == xFilial("SB1")+cCompo }

While Eval(bWhile1)
	If oReport:Cancel() 
		Exit
	EndIf
	oReport:IncMeter()

	cCompo := (cAliasQRY)->B1_COD

	lFirst := .T.

	While Eval(bWhile2)
		If oReport:Cancel()
			Exit
		EndIf
		
		If (mv_par05 == 1) .And. !MR400REV((cAliasSG1)->G1_COD,(cAliasSG1)->G1_REVINI,(cAliasSG1)->G1_REVFIM)
			(cAliasSG1)->( dbSkip() )
			Loop
		EndIf

		
		
		If lFirst
			oSection1:Init()
			oSection1:PrintLine()
			oSection1:Finish()
			lFirst := .F. 
		EndIf


		SB1->(dbSeek(xFilial("SB1")+(cAliasSG1)->G1_COD)) 
		oSection2:Init()
		oSection2:Cell("G1_COD"):SetValue(Space(2)+(cAliasSG1)->G1_COD)
		oSection2:PrintLine()

		If mv_par04 == 1
			//Ŀ
			// Imprime todos os produtos do componente (se o PA for componente)       
			//
			MR400Stru(oReport,(cAliasSG1)->G1_COD)
 		Endif

		oSection2:Finish()
		lTraco := .T.

		dbSkip()
	EndDo

	If lTraco
		oReport:ThinLine()
		oReport:SkipLine(1)
		lTraco := .F.
	EndIf
EndDo
(cAliasQRY)->(DbCloseArea())

Return NIL


/*


Ŀ
Funo     MR400Stru Autor  Ricardo Berti          Data 17/05/2006
Ĵ
Descrio  Explode os niveis da Estrutura de Baixo para Cima          
                                                                 	  
Ĵ
Sintaxe    MR400Stru(ExpO1,ExpC1)	                                  
Ĵ
Parametros ExpO1 = obj report   		                              
           ExpC1 = Cod. do produto 		                              
Ĵ
Retorno    Nenhum				 		                              
Ĵ
 Uso       MATR400.PRX (R4)                                    		  
ٱ

*/
STATIC Function MR400Stru(oReport,cProduto)

Local nReg := 0
Local aAreaAtu := GetArea()
Local aAreaSG1 := SG1->(GetArea())	
Local aAreaSB1 := SB1->(GetArea())	
Local oSection3 := oReport:Section(1):Section(1):Section(1)      
Local cAliasSG1 := "SG1"

dbSelectArea("SG1")
dbSetOrder(2)
dbSeek(xFilial("SG1")+cProduto)

While !Eof() .And. G1_FILIAL+G1_COMP == xFilial("SG1")+cProduto
	nReg := Recno()
	
	If (mv_par05 == 2) .Or. MR400REV(SG1->G1_COD,SG1->G1_REVINI,SG1->G1_REVFIM) 
		If mv_par03 == 1 .Or. (dDataBase >= G1_INI .And. dDataBase <= G1_FIM)
	       
			dbSelectArea("SB1")
			dbSeek(xFilial("SB1")+SG1->G1_COD)
	
			oSection3:Init(.F.)
			oSection3:Cell("COD3"):SetValue(Space(4)+(cAliasSG1)->G1_COD)
			oSection3:PrintLine()
			oSection3:Finish()
	    
			//Ŀ
			// Verifica se existe sub-estrutura                
			//
			dbSelectArea("SG1")
			dbSeek(xFilial("SG1")+G1_COMP+G1_COD)
			IF Found()
				MR400Stru(oReport,G1_COD)
			EndIf

		Endif
	EndIf
	dbGoto(nReg)
	dbSkip()
EndDo

RestArea(aAreaSB1)
RestArea(aAreaSG1)
RestArea(aAreaAtu)

Return Nil



/*/               


Ŀ
Funo    MATR400R3  Autor  Eveli Morasco          Data  16/02/93 
Ĵ
Descrio  Emite uma relacao informando onde um determinado componen- 
           te e' utilizado.                                           
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Cesar       30/03/99XXXXXXManutencao na SetPrint()                  
ٱ


/*/
Function MATR400R3
//Ŀ
// Variaveis tipo Local padrao de todos os relatorios           
//
LOCAL wnrel  :="MATR400"
LOCAL Tamanho:=IIf(TamSX3("B1_COD")[1] > 15, "M", "P")
LOCAL titulo :=STR0001	//"Onde e' Usado"
LOCAL cDesc1 :=STR0002	//"Emite uma relao informando onde determinados materiais selecionados"
LOCAL cDesc2 :=STR0003	//"so utilizados na composio da estrutura."
LOCAL cDesc3 :=""
LOCAL cString:="SB1"

//Ŀ
// Variaveis tipo Private padrao de todos os relatorios         
//
PRIVATE aReturn := { OemToAnsi(STR0004), 1,OemToAnsi(STR0005), 2, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nLastKey := 0 ,cPerg := "MTR400"

//Ŀ
// Verifica as perguntas selecionadas                           
//
//Ŀ
// Variaveis utilizadas para parametros                         
// mv_par01             // Componente de                        
// mv_par02             // Componente ate                       
// mv_par03             // Impr. Comp. Vencido                  
// mv_par04             // Visual. Todos Niveis                 
// mv_par05             // Considera Revisoes na Estrutura ?    
//
pergunte(cPerg,.F.)

//Ŀ
// Envia controle para a funcao SETPRINT                        
//

wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,,,Tamanho)

If nLastKey = 27
	Set Filter to
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	Set Filter to
	Return
Endif

RptStatus({|lEnd| C400Imp(@lEnd,wnRel,titulo,Tamanho)},titulo)

Return NIL

/*/


Ŀ
Funo     C400IMP   Autor  Rodrigo de A. Sartorio Data  13.11.95 
Ĵ
Descrio  Chamada do Relatorio                                       
Ĵ
 Uso       MATR400R3 		                                          
ٱ


/*/
Static Function C400Imp(lEnd,WnRel,titulo,Tamanho)
LOCAL cbCont := 0
LOCAL cbTxt  := Space(10)
Local lFirst := .T.
LOCAL nTipo  := 0
Local nB1_COD:= TamSX3("B1_COD")[1]
LOCAL nCol   := IIF(mv_par04==1,-2,0)

LOCAL nRecComp ,cCodComp ,cDescComp ,cTipoComp ,cGrupoComp ,cUMComp ,lImprime

PRIVATE nInc := IIF(nB1_COD > 15, 15, 0)

//Ŀ
// Variaveis utilizadas para Impressao do Cabecalho e Rodape    
//
li       := 80
m_pag    := 1
imprime  := .T.
nOrdem   := aReturn[8]
nTipo    := IIF(aReturn[4]=1,15,18)

//Ŀ
// Verifica se deve comprimir ou nao                            
//
nTipo  := IIF(aReturn[4]==1,15,18)

//Ŀ
// Monta o cabecalho da Producao                                
//
If nB1_COD > 15
	Cabec1 := STR0021	//"COMPONENTE                        DESCRICAO                      TP GRUPO UM NIV QUANTIDADE"
	Cabec2 := STR0022	//"   PRODUTO                                                                        UTILIZADA"
Else
	Cabec1 := STR0006	//"COMPONENTE         DESCRICAO                      TP GRUPO UM NIV QUANTIDADE"
	Cabec2 := STR0007	//"   PRODUTO                                                         UTILIZADA"
EndIf
//Ŀ
// Inicializa variaveis para controlar cursor de progressao     
//
dbSelectArea("SG1")
dbSetOrder(2)

dbSelectArea("SB1")
SetRegua(RecCount())

dbSeek(xFilial("SB1")+mv_par01,.T.)

While !EOF() .And. B1_FILIAL+B1_COD <= xFilial("SB1")+mv_par02

	If lEnd
		@PROW()+1,001 PSay STR0008		//"CANCELADO PELO OPERADOR"
		Exit
	EndIf

	IncRegua()

	//Ŀ
	// Se houver filtro do usuario, desliga e aplica manualmente, p/ nao afetar niveis de estruturas |
	//	
	If !Empty(aReturn[7])
		If lFirst
			dbClearFilter()
			lFirst := .F.
		ElseIf !(&(aReturn[7]))
			dbSkip()
			Loop
		EndIf
	EndIf

	nRecComp   := RecNo()
	cCodComp   := B1_COD
	cDescComp  := SubStr(B1_DESC,1,30)
	cTipoComp  := B1_TIPO
	cGrupoComp := B1_GRUPO
	cUMComp    := B1_UM

	dbSelectArea("SG1")
	dbSeek(xFilial("SG1")+cCodComp)
	lImprime := .T.

	While !Eof() .And. G1_FILIAL+G1_COMP == xFilial("SG1")+cCodComp

		If li > 60
			Cabec(titulo,cabec1,cabec2,wnrel,Tamanho,nTipo)
			lImprime := .T.
		EndIf

		SB1->(dbSeek(xFilial("SB1")+SG1->G1_COD))

		If ((dDataBase >= SG1->G1_INI .And. dDataBase <= SG1->G1_FIM) .Or. mv_par03 == 1) .And. (mv_par05==2 .Or. MR400REV(SG1->G1_COD,SG1->G1_REVINI,SG1->G1_REVFIM) )

			If lImprime
				@ li,000 PSay cCodComp+"    "+cDescComp+" "+cTipoComp+"  "+cGrupoComp+" "+cUMComp
				Li++
				Li++
				lImprime := .F.
			EndIf

			dbSelectArea("SB1")
			dbSeek(xFilial("SB1")+SG1->G1_COD)
			@ Li,nCol+003 PSay B1_COD+" "+SubStr(B1_DESC,1,30)+" "+B1_TIPO+"  "+B1_GRUPO+" "+B1_UM
			dbSelectArea("SG1")
			@ Li,(nCol+nInc)+063 PSay G1_NIV
			@ Li,(nCol+nInc)+066 PSay G1_QUANT Picture PesqPictQt("G1_QUANT",12)
			Li++

   		   If mv_par04 == 1
		      MR400Estr(SG1->G1_COD,titulo,cabec1,cabec2,wnrel,tamanho,nTipo)
		   Endif
		   
		EndIf

		SG1->(DbSkip())

	EndDo

	If !lImprime
		@ Li,000 PSay __PrtThinLine()
		Li++
	EndIf

	dbSelectArea("SB1")
	Go nRecComp
	DbSkip()

EndDo

dbSelectArea("SB1")
Set Filter To
dbSetOrder(1)
dbSelectArea("SG1")
dbSetOrder(1)

If Li != 80
	Roda(cbcont,cbtxt,Tamanho)
EndIf

If aReturn[5] = 1
	Set Printer To 
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return NIL

/*


ͻ
Programa  MR400ESTR Autor  Marcos V. Ferreira   Data   09/30/04   
͹
Desc.     Explode os niveis da Estrutura de Baixo para Cima           
                                                                      
͹
Uso       MATR400R3	                                                  
ͼ


*/               
STATIC Function MR400Estr(cProduto,titulo,cabec1,cabec2,wnrel,tamanho,nTipo,nOpc)
LOCAL nReg := 0
LOCAL aAreaSG1 := SG1->(GetArea())	
LOCAL aAreaSB1 := SB1->(GetArea())	
Local cProd    := ""
DEFAULT nOpc   := 1

dbSelectArea("SG1")
dbSetOrder(2)
dbSeek(xFilial("SG1")+cProduto)

While !Eof() .And. G1_FILIAL+G1_COMP == xFilial("SG1")+cProduto
	nReg := Recno()
 
 	If (mv_par05 == 2) .Or. MR400REV(SG1->G1_COD,SG1->G1_REVINI,SG1->G1_REVFIM)
		If (dDataBase >= G1_INI .And. dDataBase <= G1_FIM) .Or. mv_par03 == 1
	       
	        dbSelectArea("SB1")
	        dbSeek(xFilial("SB1")+SG1->G1_COD)
	    
	        If nOpc == 1
	        	If li > 60
		        	Cabec(titulo,Cabec1,Cabec2,Wnrel,Tamanho,nTipo)
		      	EndIf
	
				@ Li,003 PSay B1_COD+" "+SubStr(B1_DESC,1,30)+" "+B1_TIPO+"  "+B1_GRUPO+" "+B1_UM
				dbSelectArea("SG1")
				@ Li,nInc+063 PSay G1_NIV
				@ Li,nInc+066 PSay G1_QUANT Picture PesqPictQt("G1_QUANT",12)
				Li++
		 	ElseIf nOpc == 2
	 	      	cProd := B1_COD
		 	Endif

			//Ŀ
			// Verifica se existe sub-estrutura                
			//
			dbSelectArea("SG1")
			dbSeek(xFilial("SG1")+G1_COMP+G1_COD)
			IF Found()
				MR400Estr(G1_COD,titulo,cabec1,cabec2,wnrel,Tamanho,nTipo,nOpc)
			EndIf
		Endif
	EndIf
	dbGoto(nReg)
	dbSkip()
EndDo

RestArea(aAreaSB1)
RestArea(aAreaSG1)

Return IIF(nOpc==2,cProd,Nil)

/*


ͻ
Programa  MR400REV  Autor  Marcos V. Ferreira   Data   21/10/04   
͹
Desc.     Valida a Revisao Atual do Produto                           
                                                                      
͹
Uso       MATR400R3                                                   
ͼ


*/               
Function MR400REV(cCod,cRevIni,cRevFim)
Local lRet     := .T.
Local cAlias   := Alias()
Local aAreaSB1 := SB1->(GetArea())
Local cRevAtu		:= ''
Local lPCPREVATU	:= FindFunction('PCPREVATU')  .AND.  SuperGetMv("MV_REVFIL",.F.,.F.)

//Ŀ
// Verifica Produto PAI do compomente              
//
SB1->( MsSeek(xFilial("SB1")+cCod) )

cRevAtu := IIF(lPCPREVATU , PCPREVATU(SB1->B1_COD), SB1->B1_REVATU )

If !(cRevAtu >= cRevIni .And. cRevAtu <= cRevFim)
	lRet :=.F.
Endif

RestArea(aAreaSB1)
dbSelectArea(cAlias)
Return lRet
