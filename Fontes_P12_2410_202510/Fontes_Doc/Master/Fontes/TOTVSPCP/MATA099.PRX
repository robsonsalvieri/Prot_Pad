#INCLUDE "MATA099.ch"
#include "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MATA099   º Autor ³ Marcelo Iuspa      º Data ³  16/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Cadastro de de Conjuntos de Opcoes                         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA099                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA099

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta um aRotina proprio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()

SBX->(dbSetOrder(1))

mBrowse( 6,1,22,75,"SBW")

Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A099SBXATUºAutor  ³Marcelo Iuspa       º Data ³  16/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Tela de manutencao de Conjuntos de Caracteristicas          º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA099                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A099SBXATU( cAlias,nReg,nOpc, xParam, lCopia)
Local lOk        := .F.
Local cCadastro  := STR0006 //"Conjunto de Opcoes"
Local lPosDel    := Nil
Local bGravaCols := {|| A099GravaCol(nOpc)}
Local aNoFields  := {'BX_CONJUN'}
Local cSeekSBX   := ''
Local cWhile     := ''
Local aColsReg   := {}
Local cQuery 	   := ""
Local nCount     := 0
Local aButtons   := {}
Local nX
Local aStruSBW
Private oItsCjto
Private aHeader  := {}
Private aCols    := {}
Private aColsAux := {}
Private lAltera  := IIF(nOpc==4,.T.,.F.)	

RegToMemory( "SBW", nOpc == 3)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do AHEADER e ACOLS para GetDados  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpc == 3
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Sintaxe da FillGetDados(/*nOpcX*/,/*Alias*/,/*nOrdem*/,/*cSeek*/,/*bSeekWhile*/,/*uSeekFor*/,/*aNoFields*/,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,/*lEmpty*/,/*aHeaderAux*/,/*aColsAux*/,/*bAfterCols*/) |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	FillGetDados(nOpc,'SBX',,,,,aNoFields,,,,,.T.,,aColsAux)
Else
	cSeekSBX := xFilial("SBX") + M->BW_CONJUN
	cWhile   := 'SBX->(BX_FILIAL + BX_CONJUN)'

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Sintaxe da FillGetDados(/*nOpcX*/,/*Alias*/,/*nOrdem*/,/*cSeek*/,/*bSeekWhile*/,/*uSeekFor*/,/*aNoFields*/,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,/*lEmpty*/,/*aHeaderAux*/,/*aColsAux*/,/*bAfterCols*/) |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	FillGetDados(nOpc,'SBX',1,cSeekSBX,{|| &cWhile },,aNoFields,,,,,,,aColsAux,{||Aadd(aColsReg, SBX->(RecNo())),.T.}, )
EndIf

nPosDel := Len(aHeader) + 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o calculo automatico de dimensoes de objetos     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 0050, .T., .F. } )
AAdd( aObjects, { 100, 1000, .T., .T. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )
Aadd(aButtons, {"pmspesq",{ || A099PesqIt()},OemToAnsi(STR0007),OemToAnsi(STR0008)})  //Pesquisa Item

	if nOpc == 2
		cCadastro += " - "+ UPPER(STR0002)
	elseif nOpc == 3
		cCadastro += " - "+ UPPER(STR0003)
	elseIf nOpc == 4
		cCadastro += " - "+ UPPER(STR0004)
	elseIf nOpc == 5
		cCadastro += " - "+ UPPER(STR0005)
	Endif

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	EnChoice( "SBW", nReg, nOpc, , , , , aPosObj[1],,3,,,)
	oItsCjto := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"A099LinOk","A099TudoOk","",nOpc # 2,,1,,15000,"AllWaysTrue()",/*15*/,/*16*/,"A099DelOk")
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||lOk:=.T.,If(oItsCjto:TudoOk(),oDlg:End(),lOk:=.F.)},{||oDlg:End()},,aButtons)

If lOk .and. nOpc <> 2 // Gravar se confirmado e nao for visualizacao
	// Por causa da Integridade Referencial devo respeitar inclusao/exclusao com PAI/FILHO
	If nOpc == 5
		cQuery := "SELECT COUNT(*) AS USA FROM " +RetSQLName("SBQ") +" WHERE "
		cQuery += "BQ_CONJUNT = '" +SBW->BW_CONJUN +"' AND D_E_L_E_T_ = '' "
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"VALDEL",.T.,.T.)
		nCount := VALDEL->USA
		dbCloseArea()
		
		If (nCount==0)
 			AEval(aCols, {|z,w| aCols[w, nPosDel] := .T.})
			Eval(bGravaCols)
			RecLock("SBW", .F.)
			dbDelete()
			MsUnlock()
		Else
			HELP(" ",1,"A093NDEL")		
		EndIf
	Else
		RecLock("SBW", nOpc == 3)
		aStruSBW := FWFormStruct(1,'SBW'):getFields() // Busca os campos usados (X3_USADO) da tabela
		For nX := 1 to Len(aStruSBW)
			If aStruSBW[nX][3] == "BW_FILIAL"
				SBW->BW_FILIAL := xFilial("SBW")
 			ElseIf !aStruSBW[nX][14]
				SBW->(&(aStruSBW[nX][3])) := M->(&(aStruSBW[nX][3]))
			EndIf
		Next nX		
		MsUnlock()
		Eval(bGravaCols)
	EndIf
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A099VldCodOpºAutor  ³Marcelo Iuspa       º Data ³  16/09/03   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida o codgo da opcao do conjunto.                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ Logico                                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ lVldCpo = Verifica se a funcao e executada atraves da valicaoº±±
±±º          ³ do campo.                                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA099                                                      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A099VldCodOp(lVldCpo)
Local cFld     := "BX_CODOP"
Local lRet     := A093ChavLine({cFld})
Local nPos     := GDFieldPos(cFld)
Local lTam     := .T.
Local nTam     := 0
Local nCnt     := 0
Local nPosCols := 0

Default lVldCpo := .T.

For nCnt := 1 To Len(aCols)
	If !GdDeleted(nCnt)
		nTam := Len(Trim(aCols[nCnt, nPos]))
		nPosCols := nCnt
		Exit
	EndIf
Next

If lRet .And. nTam > 0 .And. Len(aCols) > 1
	If !GdDeleted() .And. nPosCols <> n
		lTam := lTam .And. Len(Trim(If(lVldCpo,&(ReadVar()),aCols[n,nPos]))) == nTam
	EndIf
EndIf

If !lTam
	//-- O tamanho do campo esta incorreto.
	//-- Todos os itens de opcoes de conjunto ou de uma caracteristica deverao ter o mesmo tamanho.
	Help(" ",1,"A099TAMOPC")
EndIf
Return(lRet .And. lTam)	

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³08/11/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³    1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
PRIVATE aRotina	:= {	{STR0001 ,"AxPesqui"  ,0,1,0,.F.},;	//"Pesquisar"
						{STR0002 ,"A099SBXATU",0,2,0,nil},;	//"Visualizar"
						{STR0003 ,"A099SBXATU",0,3,0,nil},;	//"Incluir"
						{STR0004 ,"A099SBXATU",0,4,0,nil},;	//"Alterar"
						{STR0005 ,"A099SBXATU",0,5,0,nil} }	//"Excluir"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada utilizado para inserir novas opcoes no array aRotina  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MTA099MNU")
	ExecBlock("MTA099MNU",.F.,.F.)
EndIf
Return(aRotina)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A099LinOk ³ Autor ³ Felipe Nunes de Toledo³ Data ³26/02/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o contexto da linha atual do aCols.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA099                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A099LinOk(o)
Local lRet := .T.
Local cBXCodOP := GDFieldGet("BX_CODOP",, .T.)
Local nPos     := GDFieldPos("BX_CODOP")
Local aArea	 := GetArea()

//-- Nao avalia linhas deletadas
If !GdDeleted() .And. (lRet:=MaCheckCols(aHeader,aCols,n))
	//-- Analisa se ha itens duplicados na GetDados.
	lRet := GDCheckKey({'BX_CODOP'},4)
EndIf

If	lRet .And. Empty(cBXCodOP)
	Help(" ",1,"BX_CODOP")
	lRet := .F.
EndIf

If lRet
	lRet := A099VldCodOp(.F.)
EndIf

If lAltera .And. n <= Len(aColsAux)
	If lRet .And. aCols[n,nPos] <> aColsAux[n,nPos]
		dbSelectArea("SBX")
		If dbSeek(xFilial("SBX")+M->BW_CONJUN+aCols[n,nPos])
			Help(" ",1,"JAGRAVADO")
			lRet := .F.
		EndIf
		
		If !A099ValDel(aColsAux[n,nPos])
			lRet := .F.
		EndIf
	EndIf
EndIf

RestArea(aArea)

Return(lRet)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A099TudoOk³ Autor ³ Felipe Nunes de Toledo³ Data ³26/02/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o contexto geral da MsGetdados(aCols).              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA099                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A099TudoOk(o)
Local lRet  := .T.
Local nAcho := 0
nAcho := Ascan( aCols, {|x| x[Len(aHeader)+1] == .F. })
If nAcho == 0
	lRet := .F.
	Help(" ",1,"A099INTENS")
EndIf
Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³A099PesqIt³ Autor ³ Andre Anjos			³ Data ³27/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta tela de pesquisa de opcoes				              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA099                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A099PesqIt()
Local oPesq, oInd
Local cInd
Local aInd   := {OemToAnsi(STR0012),OemToAnsi(STR0013),OemToAnsi(STR0014)}
Local cChave := Space(TamSX3("BX_CODOP")[1])
Local aArea  := GetArea()
Local lPesq  := .F.
Local nPosCod   := aScan(aHeader,{|x| AllTrim(x[2]) == "BX_CODOP"}) 
Local nPosDesc  := aScan(aHeader,{|x| AllTrim(x[2]) == "BX_DESC"}) 
Local nPosDescp := aScan(aHeader,{|x| AllTrim(x[2]) == "BX_DESCPR"}) 
Local nPos := 0

If !A099LinOk()
	Return
EndIf

DEFINE MSDIALOG oPesq TITLE OemToAnsi(STR0009) From 0,0 to 150,205 of oMainWnd PIXEL
@05,05 SAY OemToAnsi(STR0010) SIZE 60,10 OF oPesq PIXEL
@15,05 COMBOBOX oInd VAR cInd ITEMS aInd SIZE 95,10 ON CHANGE (AjustChave(cInd,aInd,@cChave),oChave:Refresh()) OF oPesq PIXEL

@30,05 SAY OemToAnsi(STR0011) SIZE 95,10 OF oPesq PIXEL
@40,05 MSGET oChave VAR cChave PICTURE "@!" SIZE 95,10 OF oPesq PIXEL

DEFINE SBUTTON FROM 060,020 TYPE 1 ENABLE OF oPesq ACTION (lPesq:=.T.,oPesq:End())
DEFINE SBUTTON FROM 060,060 TYPE 2 ENABLE OF oPesq ACTION (lPesq:=.F.,oPesq:End())

ACTIVATE MSDIALOG oPesq CENTERED

If lPesq                                                 
	nPos := aScan(aCols,{|aX| aX[RetInd(cInd,aInd,nPosCod,nPosDesc,nPosDescp)] == cChave})
	If oItsCjto:lNewLine //se estava em nova linha
		oItsCjto:lNewLine := .F.
		aSize(aCols,Len(aCols)-1)	
	EndIf
	If nPos > 0 //se encontrou posiciona 
		oItsCjto:oBrowse:nAt := nPos
		oItsCjto:oBrowse:nColPos := RetInd(cInd,aInd,nPosCod,nPosDesc,nPosDescp)
		oItsCjto:oBrowse:SetFocus()
		oItsCjto:oBrowse:Refresh()
	EndIf
EndIf

RestArea(aArea)

Return

Static Function AjustChave(cInd,aInd,cChave)
Do Case
Case cInd==aInd[1]
	cChave:=Space(TamSX3("BX_CODOP")[1])
Case cInd==aInd[2]
	cChave:=Space(TamSX3("BX_DESC")[1])
Case cInd==aInd[3]
	cChave:=Space(TamSX3("BX_DESCPR")[1])
EndCase
Return

Static Function RetInd(cInd,aInd,nPosCod,nPosDesc,nPosDescp)
Do Case
Case cInd==aInd[1]
	Return nPosCod
Case cInd==aInd[2]
	Return nPosDesc
Case cInd==aInd[3]
	Return nPosDescp
EndCase
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} A099DelOk()
Função para validação da exclusão dos itens do conjunto
@author Ricardo Prandi
@since 24/06/2014
@version 2.0
/*/
//-------------------------------------------------------------------
Function A099DelOk()
Local lRet 		:= .T.
Static nCnt    	:= 0

nCnt++
If (nCnt%2>0)
	If !A099ValDel()
		lRet := .F.
	EndIf
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A099ValDel()
Função para validação da exclusão dos itens do conjunto
@author Ricardo Prandi
@since 07/04/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function A099ValDel(cCodOp)
Local cQuery		:= ""
Local cAliasTop	:= "VALDELC"
Local lRet 		:= .T.
Local aSvArea  	:= GetArea()

Default cCodOp	:= GDFieldGet("BX_CODOP",, .T.)

lRet := .F.
cQuery := " SELECT DISTINCT SB1.B1_BASE2, " +;
                          " SBQ.BQ_TAMANHO " +;
            " FROM " + RetSqlName("SBQ") + " SBQ, " +;
                       RetSqlName("SB1") + " SB1 " +;
           " WHERE SBQ.BQ_CONJUNT = '" + SBW->BW_CONJUN + "' " +;
             " AND SB1.B1_BASE    = SBQ.BQ_BASE " +;
             " AND SBQ.D_E_L_E_T_ = ' ' " +;
             " AND SB1.D_E_L_E_T_ = ' ' "
cQuery := ChangeQuery(cQuery)

If !lRet
	lRet := .T.

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTop,.T.,.T.)
	
	While !Eof()
		If (AllTrim(SBW->BW_CONJUN)+AllTrim(cCodOp)) $ B1_BASE2
			HELP(" ",1,"A093NDEL")
			lRet := .F.
		EndIf
		
		If !lRet
			Exit
		EndIf
		
		dbSelectArea(cAliasTop)
		dbSkip()
	End
	
	dbSelectArea(cAliasTop)
	dbCloseArea()
EndIf	

RestArea(aSvArea)

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} A099GravaCol()
Função para gravar a aCols
@author Ricardo Prandi
@since 07/01/2015
@version 1.0
/*/
//------------------------------------------------------------------- 
Function A099GravaCol(nOpc)
 
Local nCnt 	:= 0
Local nHeader	:= 0
Local nCountH	:= 0

If nOpc == 5
	dbSelectArea("SBX")
	dbSeek(xFilial("SBX")+M->BW_CONJUN)
	While (!EOF()) .And. (BX_FILIAL+BX_CONJUN == xFilial("SBX")+M->BW_CONJUN)
		RecLock("SBX",.F.)
		dbDelete()
		MsUnlock()
		dbSkip()
	End
ElseIf nOpc == 3
	For nCnt := 1 To Len(aCols)
  		RecLock("SBX",.T.)
  		SBX->BX_FILIAL := xFilial("SBX")
  		SBX->BX_CONJUN := M->BW_CONJUN
  		For nHeader := 1 to Len(aHeader)-2
  			SBX->&(aHeader[nHeader,2]) := aCols[nCnt,nHeader]
  		Next
  		SBX->(MsUnlock())
	Next
ElseIf nOpc == 4
	nCountH := LEN(aHeader)
	For nCnt := 1 To Len(aCols)
		If GdDeleted(nCnt)
			IF aCols[nCnt,nCountH] > 0
				dbSelectArea("SBX")
				dbGoTo(aCols[nCnt,nCountH])
				RecLock("SBX",.F.)
				dbDelete()
				SBX->(MsUnlock())
			EndIf
		Else
			dbSelectArea("SBX")
			If aCols[nCnt,nCountH] == 0
				RecLock("SBX",.T.)
  				SBX->BX_FILIAL := xFilial("SBX")
  				SBX->BX_CONJUN := M->BW_CONJUN
  				For nHeader := 1 to Len(aHeader)-2
  					SBX->&(aHeader[nHeader,2]) := aCols[nCnt,nHeader]
  				Next
  				SBX->(MsUnlock())
			Else
				dbGoTo(aCols[nCnt,nCountH])
				RecLock("SBX",.F.)
				For nHeader := 1 to Len(aHeader)-2
  					SBX->&(aHeader[nHeader,2]) := aCols[nCnt,nHeader]
  				Next
  				SBX->(MsUnlock())
  			EndIf
  		EndIf
	Next
EndIf

Return (.T.)
