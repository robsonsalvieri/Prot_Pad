#INCLUDE "TOTVS.CH"
#INCLUDE "MATA690B.ch"
#include "mproject.ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A690PrjOrd ºAutor  ³Marcelo Iuspa       º Data ³  18/04/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Processa a exportacao dos dados da ultima carga maquina     º±±
±±º          ³alocada para o project                                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A690PrjOrd()
Local oApp, nTask := 1
lOCAL aCalBase := {PJSUNDAY, PJMONDAY, PJTUESDAY, PJWEDNESDAY, PJTHURSDAY, PJFRIDAY, PJSATURDAY}
Local cOpAtual, nIdent := 0
Local nBitIni := 0
Local nPrecisao := 60 / GetMV("MV_PRECISA")
Local cRecurso
Local cCaption
Local x
Local aPred := {}
Local nPos := 1

oApp := MsProject():New()
oApp:Visible := .T.
oApp:Projects:Add()

cCaption := A690Caption(oApp)

oApp:TableEdit( 'Ap5View', .T., .T., .T., ,STR0007     , , '', 6, PJCENTER, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //ID
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0008    , STR0006, 22, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //STR0008 = NAME , STR0006 'Ordem de Producao'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0009   , STR0001, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //TEXT1 //'Operacao'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0012   , STR0002, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Nome do Recurso'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0013   , STR0003     , 10, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Calendario'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0014, , 9, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER )
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0015   , , 12, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Start'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0016   , , 12, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Finish' 

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0010   , STR0004, 12, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Cod. Produto'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0011   , STR0005, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Produto'

oApp:TableApply( 'Ap5View' )

dbSelectArea("SH1")
dbSeek(xFilial("SH1"))
do While ! Eof() .And. H1_FILIAL == xFilial("SH1")
	oApp:Projects(1):Resources:Add( cRecurso := SH1->H1_CODIGO + " - " + H1_DESCRI)
	
	SH7->(dbSeek(xFilial("SH7") + SH1->H1_CALEND))
    aCalend := A690Calend(SH7->H7_CODIGO)    
	For x := 1 to Len(aCalend)
		oApp:ResourceCalendarEditDays(cCaption, cRecurso,,, aCalBase[x], .T., .F., aCalend[x, 2], aCalend[x, 3], aCalend[x, 4], aCalend[x, 5], aCalend[x, 6], aCalend[x, 7], aCalend[x, 8], aCalend[x, 9], aCalend[x, 10], aCalend[x, 11])
	Next
	dbSkip()
Enddo	

dbSelectArea("SH9")
dbSeek(xFilial("SH9"))
do While ! Eof() .And. SH9->H9_FILIAL == xFilial("SH9")
	If SH9->H9_TIPO == "E"
		aArrayH9 := A690Excec(SH9->H9_ALOC)
		If ! Empty(SH9->H9_RECURSO)
			SH1->(dbSeek(xFilial("SH1") + SH9->H9_RECURSO))
			oApp:ResourceCalendarEditDays(cCaption, SH1->H1_CODIGO + " - " + SH1->H1_DESCRI, DTOC(SH9->H9_DTINI), DTOC(SH9->H9_DTFIM),, .T., .F., aArrayH9[1], aArrayH9[2], aArrayH9[3], aArrayH9[4], aArrayH9[5], aArrayH9[6], aArrayH9[7], aArrayH9[8], aArrayH9[9], aArrayH9[10])
		Else
			dbSelectArea("SH1")
			dbSeek(xFilial("SH1"))
			do While ! Eof() .And. xFilial("SH1") == SH1->H1_FILIAL
				If (Empty(SH9->H9_CCUSTO ) .Or. SH9->H9_CCUSTO  == SH1->H1_CCUSTO)
					oApp:ResourceCalendarEditDays(cCaption, SH1->H1_CODIGO + " - " + SH1->H1_DESCRI, DTOC(SH9->H9_DTINI), DTOC(SH9->H9_DTFIM),, .T., .F., aArrayH9[1], aArrayH9[2], aArrayH9[3], aArrayH9[4], aArrayH9[5], aArrayH9[6], aArrayH9[7], aArrayH9[8], aArrayH9[9], aArrayH9[10])	
 				Endif
				dbSkip()
			Enddo
		Endif	
	Endif	
	dbSelectArea("SH9")
	dbSkip()
Enddo		

SC2->(dbSetOrder(1))

oApp:Visible := .T.

dbSelectArea("SH8")
dbSeek(xFilial("SH8"))
cOpAtual := ""
do While ! Eof() .And. SH8->H8_FILIAL == xFilial("SH8")
	If cOpAtual # SH8->H8_OP .Or. Empty(cOpAtual) // Inserir aqui o Indent/OutDent
		If nTask # 1
			oApp:projects(1):Tasks(nBackup):Finish:= Dtoc(dBackup) + " " + cHrbkp
		EndIf
		 
		cOpAtual := SH8->H8_OP
		oApp:projects(1):Tasks:Add(SH8->H8_OP + " " + SH8->H8_OPER + " " + SH8->H8_RECURSO)
		
		oApp:projects(1):Tasks(nTask):Start := Dtoc(SH8->H8_DTINI) + " " + SH8->H8_HRINI		
		nBackup := nTask		
		
		If nIdent == -1
			oApp:Projects(1):Tasks(nTask):OutLineOutdent()
		Endif
		nTask ++
		nIdent   := 1
	Endif
	
	dBackup := SH8->H8_DTFIM
	cHrbkp := SH8->H8_HRFIM
	AADD(aPred,{nTask,SH8->H8_BITFIM})
		
	SC2->(dbSeek(xFilial("SH8")+SH8->H8_OP))
	a630SeekSG2(1,SC2->C2_PRODUTO,xFilial("SG2")+SC2->C2_PRODUTO+SH8->H8_ROTEIRO+SH8->H8_OPER)
	SH1->(dbSeek(xFilial("SH1")+SH8->H8_RECURSO))
	SB1->(dbSeek(xFilial("SB1")+SC2->C2_PRODUTO))

	oApp:projects(1):Tasks:Add(SH8->H8_OP + " " + SH8->H8_OPER + " " + SH8->H8_RECURSO)
 	oApp:projects(1):Tasks(nTask):ResourceNames := SH8->H8_RECURSO + " - " + SH1->H1_DESCRI
	oApp:projects(1):Tasks(nTask):Text1 := SG2->G2_DESCRI
	oApp:projects(1):Tasks(nTask):Text2 := SC2->C2_PRODUTO
	oApp:projects(1):Tasks(nTask):Text3 := SB1->B1_DESC
	oApp:projects(1):Tasks(nTask):Text4 := SH1->H1_DESCRI
	oApp:projects(1):Tasks(nTask):Text5 := SH1->H1_CALEND

	oApp:projects(1):Tasks(nTask):Start := Dtoc(SH8->H8_DTINI) + " " + SH8->H8_HRINI
	oApp:projects(1):Tasks(nTask):Finish:= Dtoc(SH8->H8_DTFIM) + " " + SH8->H8_HRFIM

	If nIdent == 1
		oApp:Projects(1):Tasks(nTask):OutLineIndent()
		nIdent := 0
	Endif	

	If ! FirstOpe()
		nPos := Ascan(aPred,{|x| x[2] == SH8->H8_BITINI})
		
		If nPos > 0
			oApp:projects(1):Tasks(nTask):predecessors := AllTrim(Str(aPred[nPos,1]))
		EndIf
		
		
		If Empty(SG2->G2_TPSOBRE)
 			If nPos > 0
				oApp:LinkTasksEdit(aPred[nPos,1],nTask,,PJFINISHTOSTART,"0h")
			EndIf
		Else
 			oApp:LinkTasksEdit(nTask-1-nIdent,nTask,, PJSTARTTOSTART , AllTrim(Transform(((SH8->H8_BITINI - nBitIni) * nPrecisao)/60, "@E 99999.99")) + " h")
		Endif
	Endif
	nBitIni := SH8->H8_BITINI
	dbSkip()
	If cOpAtual # SH8->H8_OP .Or. Eof() .Or. SH8->H8_FILIAL # xFilial("SH8") // Inserir aqui o Indent/OutDent 
		nIdent := -1
	Endif	
	nTask ++
Enddo 
oApp:Destroy()
Return


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FirstOpe   ºAutor  ³Marcelo Iuspa       º Data ³  30/08/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Identifica se a operacao atual e' a primeira do produto     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FirstOpe
Local aSavArea := {Alias(), SG2->(RecNo()), SG2->(IndexOrd())}
Local cOperAtu := SG2->G2_OPERAC
Local lRet     := .F.
dbSelectArea("SG2") 

If Empty(SG2->G2_REFGRD)
	dbSetOrder(3)
	dbSeek(xFilial("SG2") + SG2->G2_PRODUTO)
Else
	dbSetOrder(8)
	dbSeek(xFilial("SG2") + SG2->G2_REFGRD)
EndIf
lRet := cOperAtu == G2_OPERAC
dbGoto(aSavArea[2])
dbSetOrder(aSavArea[3])
dbSelectArea(aSavArea[1])
Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A690CaptionºAutor  ³Marcelo Iuspa       º Data ³  27/11/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Identifica o Caption no Project                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A690Caption(oApp)
Local cCaption := ""
Local nAt      := 1
Default lAutoMacao := .F.

IF !lAutoMacao
	cCaption := oApp:GetCaption()
	nAt      := At("-", cCaption)
ENDIF

If nAt > 0
	cCaption := AllTrim(SubStr(cCaption, nAt + 1))
Endif
Return(cCaption)	

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A690Sec2TimeºAutor  ³Marcelo Iuspa       º Data ³  27/11/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Transforma de segundos para formato hora HH:MM:SS            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A690Sec2Time( nSeconds )
RETURN StrZero(INT(Mod(nSeconds / 3600, 24)), 4, 0) + ":" +;
	   StrZero(INT(Mod(nSeconds / 60, 60)), 2, 0)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A690DtProj ºAutor  ³Yuri Porto       º Data ³  30/07/2018    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a Data inicial e final do Projeto			           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION A690DtProj()
Local aDatas := {}
Local aSH8   := SH8->(GetArea())
Local aSC2   := SC2->(GetArea())
Local cOp    := MV_PAR09

dbSelectArea("SH8")
dbSetOrder(1)//H8_FILIAL, H8_OP, H8_OPER, H8_DESDOBR, R_E_C_N_O_, D_E_L_E_T_
If !Empty(cOp)
	dbSeek(xFilial("SH8")+cOp)
Else 
	dbSeek(xFilial("SH8"))	
EndIf

dbSelectArea("SC2")
dbSetOrder(1)//C2_FILIAL, C2_NUM, C2_ITEM, C2_SEQUEN, C2_ITEMGRD, R_E_C_N_O_, D_E_L_E_T_
If  dbSeek(xFilial("SC2")+SH8->H8_OP) 
	Aadd(aDatas,{"Data Inicial", SC2->C2_DATAJI})
	Aadd(aDatas,{"Data Final"  , SC2->C2_DATAJF})	
Else
	Help(" ",1,"FWRITERROR",,Str(FError(),2,0),05,38)
EndIf

RestArea(aSH8)
RestArea(aSC2)
RETURN aDatas

//------------------------------------------------------------------
/*/{Protheus.doc} A690Project
Chama a função de exportação dos dados do carga máquina para o
MS Project conforme parametrização.

@author Renan Roeder
@since 04/10/2018
@version P12
/*/
//------------------------------------------------------------------
Function A690Project()
Local nCargaExp := SuperGetMV("MV_CARGEXP",, 0)

If nCargaExp == 1
	A690PrjRec()
Else
	A690PrjOrd()
EndIf	

Return          

//------------------------------------------------------------------
/*/{Protheus.doc} A690PrjRec
Processa a exportação dos dados do carga máquina para o
MS Project agrupado por recurso.

@author Renan Roeder
@since 04/10/2018
@version P12
/*/
//------------------------------------------------------------------          
Function A690PrjRec()
Local oApp, nTask := 1
lOCAL aCalBase := {PJSUNDAY, PJMONDAY, PJTUESDAY, PJWEDNESDAY, PJTHURSDAY, PJFRIDAY, PJSATURDAY}
Local cOpAtual, nIdent := 0
Local nBitIni := 0
Local nPrecisao := 60 / GetMV("MV_PRECISA")
Local cRecurso
Local x
Local aPred := {}
Local nPos := 1

Local cRecAtual

Local cCaption
Local aDatas	:= {}
Default lAutoMacao := .F.

oApp := MsProject():New()
oApp:Visible := .T.
oApp:Projects:Add()

oApp:TableEdit( 'Ap5View', .T., .T., .T., ,STR0007     , , '', 6, PJCENTER, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'ID'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0008   , STR0002, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Name' 'Nome do Recurso'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0009    , STR0006, 22, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Text1' 'Ordem de Producao'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0010   , STR0001, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Text2' 'Operacao'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0011   , STR0003     , 10, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Text3' 'Calendario'

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0014, , 9, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Duration'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0015   , , 12, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Start'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0016   , , 12, PJRIGHT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Finish' 

oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0012   , STR0004, 12, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Text4' 'Cod. Produto'
oApp:TableEdit( 'Ap5View', .T., , .T., , , STR0013   , STR0005, 18, PJLEFT, .T., .T., PJDATEDEFAULT, 1, ,PJCENTER ) //'Text5' 'Produto'

oApp:TableApply( 'Ap5View' )

oApp:Projects(1):Resources:Add("Default")

cCaption := oApp:Projects(1):Resources(1):BaseCalendar

aadd(aDatas,{"Dt. Inicial",MV_PAR15})
aadd(aDatas,{"Dt. Final",MV_PAR15+MV_PAR02})

IF !lAutoMacao
	oApp:BaseCalendarEditDays(cCaption, DToc(aDatas[1,2]) , DToc(aDatas[2,2]) ,,.T.,"00:00","23:59",,,,,.F.,,,,)
ENDIF

SC2->(dbSetOrder(1))

oApp:Visible := .T.

dbSelectArea("SH8")
dbSetOrder(2)
dbSeek(xFilial("SH8"))
cRecAtual := ""
do While ! Eof() .And. SH8->H8_FILIAL == xFilial("SH8")
	If cRecAtual # SH8->H8_RECURSO .Or. Empty(cRecAtual) // Inserir aqui o Indent/OutDent
		If nTask # 1
			oApp:projects(1):Tasks(nBackup):Finish:= Dtoc(dBackup) + " " + cHrbkp
		EndIf	
		cRecAtual := SH8->H8_RECURSO
		SH1->(dbSeek(xFilial("SH1")+SH8->H8_RECURSO))
		oApp:projects(1):Tasks:Add(SH8->H8_RECURSO + " - " + SH1->H1_DESCRI)
		oApp:projects(1):Tasks(nTask):Start := Dtoc(SH8->H8_DTINI) + " " + SH8->H8_HRINI
		nBackup := nTask
		If nIdent == -1
			oApp:Projects(1):Tasks(nTask):OutLineOutdent()
		Endif
		nTask ++
		nIdent := 1
		aPred  := {}
	Endif

	SC2->(dbSeek(xFilial("SH8")+SH8->H8_OP))
	SB1->(dbSeek(xFilial("SB1")+SC2->C2_PRODUTO))
	a630SeekSG2(1,SC2->C2_PRODUTO,xFilial("SG2")+SC2->C2_PRODUTO+SH8->H8_ROTEIRO+SH8->H8_OPER)

	oApp:projects(1):Tasks:Add(SH8->H8_RECURSO + " - " + SH1->H1_DESCRI)
 	oApp:projects(1):Tasks(nTask):ResourceNames := AllTrim(SC2->C2_PRODUTO) + " - " + AllTrim(SB1->B1_DESC) //SH8->H8_OP + " " + SH8->H8_OPER + " " + SH8->H8_RECURSO
	oApp:projects(1):Tasks(nTask):Text1 := SH8->H8_OP
	oApp:projects(1):Tasks(nTask):Text2 := SG2->G2_DESCRI
	oApp:projects(1):Tasks(nTask):Text3 := SH1->H1_CALEND 
	oApp:projects(1):Tasks(nTask):Text4 := SC2->C2_PRODUTO
	oApp:projects(1):Tasks(nTask):Text5 := SB1->B1_DESC

	oApp:projects(1):Tasks(nTask):SetField('PJTASKMANUAL', '0')

	oApp:projects(1):Tasks(nTask):Start  := Dtoc(SH8->H8_DTINI) + " " + SH8->H8_HRINI
	oApp:projects(1):Tasks(nTask):Finish := Dtoc(SH8->H8_DTFIM) + " " + SH8->H8_HRFIM

	If nIdent == 0
		nPos := Ascan(aPred,{|x| x[2] == SH8->H8_BITINI})
		If nPos > 0
			oApp:projects(1):Tasks(nTask):predecessors := AllTrim(Str(aPred[nPos,1]))
		EndIf

		If Empty(SG2->G2_TPSOBRE)
 			If nPos > 0
				oApp:LinkTasksEdit(aPred[nPos,1],nTask,,PJFINISHTOSTART,"0h")
			EndIf
		Else
 			oApp:LinkTasksEdit(nTask-1,nTask,, PJSTARTTOSTART , AllTrim(Transform(((SH8->H8_BITINI - nBitIni) * nPrecisao)/60, "@E 99999.99")) + " h")
		Endif

	EndIf

	If nIdent == 1
		oApp:Projects(1):Tasks(nTask):OutLineIndent()
		nIdent := 0
	Endif

	dBackup := SH8->H8_DTFIM
	cHrbkp := SH8->H8_HRFIM
	AADD(aPred,{nTask,SH8->H8_BITFIM})	

	nBitIni := SH8->H8_BITINI

	dbSkip()
	If cRecAtual # SH8->H8_RECURSO .Or. Eof() .Or. SH8->H8_FILIAL # xFilial("SH8") // Inserir aqui o Indent/OutDent
		oApp:projects(1):Tasks(nBackup):Finish:= Dtoc(dBackup) + " " + cHrbkp
		nIdent := -1
	Endif	
	nTask ++
Enddo 
oApp:Destroy()
Return          
