#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'

//-------------------------------------------------------------------
/*{Protheus.doc} PcpTrGetStruct
Prepara a estrutura dos campos do relatório
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author ana.paula
@since 03/05/2023
@version 1.0
*/
//------------------------------------------------------------------- 
function PcpTrGetStruct(aCpos As Array) As Array
    Local aDeParaCpo    := {{"C", "string"}, {"D", "date"}, {"N", "number"}, {"L", "boolean"}} as array
    Local aCpoTmp       := {} as array
    Local cCampo        := "" as character
    Local cCpoQry       := "" as character
    Local cTipR         := "" as character
    Local nPos          := 0 as numeric
    Local nC            := 0 as numeric
    
    for nC := 1 to Len(aCpos)
        cCpoQry := aCpos[nC]
        nPos    := AT(".", aCpos[nC]) + 1
        
        if nPos > 0
            cCampo := Substr(cCpoQry, nPos)
        //Comentando devido cobertura de testes - essa situação não ocorreu na issue DMANNEWPCP-7624
        //else
            //cCampo := cCpoQry
        endif
        
        cTipo := GetSx3Cache(cCampo, "X3_TIPO")
        
        if (nPos := aScan(aDeParaCpo, {|c| c[01] = cTipo})) > 0
            cTipR := aDeParaCpo[nPos, 02]
        //Comentando devido cobertura de testes - essa situação não ocorreu na issue DMANNEWPCP-7624
        //else
            //cTipR := "string"
        endif
    
        AAdd(aCpoTmp, {strTran(cCampo, "_", ""), FWSX3Util():GetDescription(cCampo), cTipR, cCampo, cCampo})
    next nC
 
return (aCpoTmp)

/*{Protheus.doc} PcpInCposPerson
Inseri no array de dados os campos customizados caso existam.

@param aStruct      array : Array com as estruturas dos campos do relatório.
@param cCposSelect  character : String com os campos do SELET da query príncipal.
@param acCposPerson array : Array com os campos personalizados.
@param lAtuQry      logical : Indica se ira acrescentar na query os campos customizados
@param lAtuAStruct  logical : Indica se ira atualizar o schema do objeto de negocio
@param cPrefixoTabNQry character : Prefixo da tabela que não sera adicionada na query
@param aTables      array : Array com o alias das tabelas

@return: Nill

@author breno.ferreira
@since 08/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function PcpInCposPerson(aStruct as array, cCposSelect as character ,acCposPerson as array , lAtuQry as logical , lAtuAStruct as logical, cPrefixoTabNQry as character, aTables as array )

Local cPrfxCpo      as character
Local nX            := 0    as numeric
Local lCpoIncQry    := .T.  as logical

Default lAtuQry     := .T.
Default lAtuAStruct := .T.
Default cPrefixoTabNQry := ''

If Len(acCposPerson)
    
    For nX := 1 to Len( acCposPerson )
        
        //Valida se o campo não existe no aStruct
        IIF(  lAtuAStruct .And. ( aScan(aStruct, {|x| x[01] == acCposPerson[nx][01]})) == 0 , aadd(aStruct, {acCposPerson[nX][1], acCposPerson[nX][4],  acCposPerson[nX][3],  acCposPerson[nX][4], acCposPerson[nX][1]})   , .T.) 
                
        If aScan(aStruct, {|x| x[01] == acCposPerson[nx][01]}) > 0 .And. lAtuQry
            If SubStr(AllTrim(Upper(acCposPerson[nX][1])), 1, At('_',acCposPerson[nX][1])) $ cPrefixoTabNQry
                lCpoIncQry := .F.
            EndIf

            If lCpoIncQry
                nLinha = aScan(aTables,{|x| x[01] == SubStr(AllTrim(Upper(acCposPerson[nX][1])), 1, At('_',acCposPerson[nX][1]))})
                if nLinha > 0
                    cPrfxCpo = aTables[nlinha][2]
                else
                    cPrfxCpo := ''
                endif

                cCposSelect += "," + cPrfxCpo + acCposPerson[nX][1]  
            EndIf
            lCpoIncQry := .T.
        EndIf

    Next nX

EndIF

Return nil

/*{Protheus.doc} pcpValidParam
Valida se o Parametro é vazio, ajustando para bancos que necessitam de conteudo em branco.

@author breno.ferreira
@since 31/10/2024
@version 1.0
@param cParam, character, parametro enviado do relatório para vereficar se é vazio.
@return cParam, character, retorna o parametro vazio para a API do relatório.
*/
Function pcpValidParam(cParam)
    
    If Empty(cParam)
        cParam := ' '
    EndIf

Return cParam

/*{Protheus.doc} addDec
Retorna uma string com os decimais da quantidade.

@author breno.ferreira
@since 24/07/2025
@version 1.0
@param 01 nValor, numeric, Valor da quantidade a ser colocado os decimais.
@param 02 cCampo, character, Campo para buscar o tamanho e decimais.
@return cQtDec, character, Retorna a quantidade com os decimais em string 
*/
Function addDec(nValor, cCampo)
    Local cQtDec := ""
    Local cStr   := ""
    Local nDec   := 0
    Local nTamC  := 0

    nDec   := GetSx3Cache(cCampo, "X3_DECIMAL")
    nTamC  := GetSx3Cache(cCampo, "X3_TAMANHO")
    cStr   := AllTrim(Str(nValor, nTamC, nDec))
    cQtDec := StrTran(cStr, ".", ",")

Return cQtDec
