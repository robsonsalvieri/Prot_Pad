#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.perda.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SBC", name="Perda por OP", country="ALL", initialRelease="12.1.2210", customTables="SBC")

/*/{Protheus.doc} PerdaTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem da perdas de produção

@author ana.paula
@since 10/05/2023
@version 1.0
/*/
class PerdaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author ana.paula
@since 10/05/2023
@version 1.0
/*/ 
method new() as object class PerdaTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Perda"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relação das Perdas de Produção"

    self:setPergunte("MTR235") //Indica o pergunte que será utilizado no relatório
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author ana.paula
@since 10/05/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class PerdaTReportsBusinessObject
    Local aParam   := Array(10)      as array
    Local aTables  := {}             as Array
    Local cAlias   := ""             as character
    Local cDescEmp := ""             as character
    Local cDescFil := ""             as character
    Local cFields  := ""             as character
    Local cMotivo  := ""             as character
    Local cQuery   := ""             as character
    Local cTipo    := ""             as character
    Local jParams  := Nil            as json
    Local nX       := 0              as numeric
    Local oExec    := Nil            as object
    Local ojItems  := Nil            as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1]  := pcpValidParam(jParams["MV_PAR01"][1]) //De  OP ?
    aParam[2]  := pcpValidParam(jParams["MV_PAR02"][1]) //Ate OP ?
    aParam[3]  := pcpValidParam(jParams["MV_PAR03"][1]) //De  Produto ?
    aParam[4]  := pcpValidParam(jParams["MV_PAR04"][1]) //Ate Produto ?
    aParam[5]  := pcpValidParam(jParams["MV_PAR05"][1]) //De  Recurso ?
    aParam[6]  := pcpValidParam(jParams["MV_PAR06"][1]) //Ate Recurso ?
    aParam[7]  := pcpValidParam(jParams["MV_PAR07"][1]) //De  Motivo ?
    aParam[8]  := pcpValidParam(jParams["MV_PAR08"][1]) //Ate Motivo ?
    aParam[9]  := pcpconvdat(jParams["MV_PAR09"][1],1) //De  Data ?
    aParam[10] := pcpconvdat(jParams["MV_PAR10"][1],1) //Ate  Data ?

    cFields := ArrTokStr(self:aFields,",")
    
    cQuery := "SELECT ? "

    aadd(aTables,{'BC_','SBC.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,,aTables)
    aTables := {}

	cQuery +=    " FROM " + RetSqlName("SBC") + " SBC "
    cQuery +=   " WHERE SBC.BC_FILIAL = ? "
    cQuery +=     " AND BC_OP    	 >= ? "
    cQuery +=     " AND BC_OP        <= ? "
    cQuery +=     " AND BC_PRODUTO   >= ? "
    cQuery +=     " AND BC_PRODUTO   <= ? "
    cQuery +=     " AND BC_RECURSO   >= ? "
    cQuery +=     " AND BC_RECURSO   <= ? "
    cQuery +=     " AND BC_MOTIVO    >= ? "
    cQuery +=     " AND BC_MOTIVO    <= ? "
    cQuery +=     " AND BC_DATA      >= ? "
    cQuery +=     " AND BC_DATA      <= ? "
	cQuery +=     " AND SBC.D_E_L_E_T_ = ' ' "
    cQuery +=   " ORDER BY SBC.BC_FILIAL, SBC.BC_OP, SBC.BC_PRODUTO "

    //Montagem da query para execução
    oExec := FwExecStatement():New(cQuery)

    oExec:SetUnSafe(1, cFields)
    oExec:SetString(2, xFilial("SBC"))
    oExec:SetString(3, aParam[1])
    oExec:SetString(4, aParam[2])
    oExec:SetString(5, aParam[3])
    oExec:SetString(6, aParam[4])
    oExec:SetString(7, aParam[5])
    oExec:SetString(8, aParam[6])
    oExec:SetString(9, aParam[7])
    oExec:SetString(10, aParam[8])
    oExec:SetDate(11, aParam[9])
    oExec:SetDate(12, aParam[10])  

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->BC_FILIAL, 1)
    EndIf
    
    While (cAlias)->(!Eof())

        //Descrição do Motivo
        CYO->(DbSetOrder(1))
        If CYO->(dbSeek(xFilial("CYO")+(cAlias)->BC_MOTIVO))
        	cMotivo := CYO->CYO_DSRF 
        EndIf       

        If (cAlias)->BC_TIPO == "R"
            cTipo := STR0004 //"Refugo"
        ElseIf (cAlias)->BC_TIPO == "S"
            cTipo := STR0005 //"Scrap"
        EndIf

        ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            //Campos auxiliares
            If self:aStruct[nX][1] $ 'DESCTP'
                ojItems[self:aStruct[nX][1]] := cTipo
            ElseIf self:aStruct[nX][1] $ 'DESCMOT'                
                ojItems[self:aStruct[nX][1]] := cMotivo
            ElseIf self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            ElseIf self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            Else 
                //Campos do select principal
                If self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                Else                
                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])  
                EndIf
            EndIf
        Next nX
    
        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())

    EndDo

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)
    
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author ana.paula
@since 10/05/2023
@version 1.0
/*/
Method getSchema() as object class PerdaTReportsBusinessObject
    Local nX        := 0 as numeric

    self:aFields := {"BC_FILIAL","BC_TIPO","BC_OP","BC_PRODUTO","BC_QUANT","BC_LOCORIG","BC_MOTIVO","BC_CODDEST",;
                     "BC_QTDDEST","BC_DATA","BC_RECURSO","BC_OPERAC","BC_OPERADO"}
                     
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCTP" , "DESC_TP" , "string", "DESC_TP" , "DESC_TP" })
    AAdd(self:aStruct , {"DESCMOT", "DESC_MOT", "string", "DESC_MOT", "DESC_MOT"})
    AAdd(self:aStruct , {"DESCEMP", "DESC_EMP", "string", "DESC_EMP", "DESC_EMP"})
    AAdd(self:aStruct , {"DESCFIL", "DESC_FIL", "string", "DESC_FIL", "DESC_FIL"})

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
