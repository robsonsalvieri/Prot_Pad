#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "manufacturing.pcp.resultados.mrp.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAPCP", tables="HWB,HW3", name="Resultados do MRP", country="ALL", initialRelease="12.1.2210", customTables="HWB,HW3")

/*/{Protheus.doc} ResultadosMrpTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem dos registros do Resultado do MRP

@author breno.ferreira
@since 04/08/2023
@version 1.0
/*/
class ResultadosMrpTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 04/08/2023
@version 1.0
/*/ 
Method new() as object class ResultadosMrpTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 - "Manufatura"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Resultados do MRP"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Listagem do Resultado do MRP"

    self:setPergunte("TRMRP") //Indica o pergunte que será utilizado no relatório

Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 04/08/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class ResultadosMrpTReportsBusinessObject
    Local aParam    := Array(6)       as array
    Local aTables   := {}             as Array
    Local cAlias    := ""             as character
    Local cData     := ""             as character
    Local cDescEmp  := ""             as character
    Local cDescFil  := ""             as character
    Local cQueryHWB := ""             as character
    Local jParams   := Nil            as json
    Local lHasNext  := .F.            as logical
    Local lOk       := .T.            as logical
    Local nIndex    := 0              as numeric
    Local nX        := 0              as numeric
    Local oExec     := Nil            as object
    Local ojItems   := Nil            as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := jParams["MV_PAR01"][1] //Ticket ?       
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Produto de ?
    aParam[3] := pcpValidParam(jParams["MV_PAR03"][1]) //Produto até ?
    aParam[4] := pcpconvdat(jParams["MV_PAR04"][1],1) //Data de Processamento de ?
    aParam[5] := pcpconvdat(jParams["MV_PAR05"][1],1) //Data de Processamento até ? 
    aParam[6] := jParams["MV_PAR06"][1] //Tipo do relatório ?

    If empty(aParam[1])
        lOK := .F.
        self:setErrorStatus(400, STR0037, STR0038) //"Ticket inválido", "Obrigatório informar o ticket!"
    EndIf

    If lOk 
        //Sumarizado
        If aParam[6] == 1
            cQueryHWB := " SELECT DISTINCT "
            cQueryHWB +=        " HWB.HWB_FILIAL, "
            cQueryHWB +=        " HWB.HWB_TICKET, "
            cQueryHWB +=        " HWB.HWB_PRODUT, "
            cQueryHWB +=        " HWB.HWB_IDOPC, "
            cQueryHWB +=        " HWB.HWB_DATA, "
            cQueryHWB +=        " HW3.HW3_DTINIC AS HW3_DTINIC, "
            cQueryHWB +=        " HW3.HW3_DTFIM AS HW3_DTFIM, "
            cQueryHWB +=        " HW3.HW3_STATUS AS HW3_STATUS "
            
            aadd(aTables,{'HWB_','HWB.'})
            aadd(aTables,{'HW3_','HW3.'})
            PcpInCposPerson(@self:aStruct, @cQueryHWB, self:getCustomFields(),.T.,.T.,,aTables)
            aTables := {}

            cQueryHWB += "   FROM "  + RetSqlName("HWB") + " HWB "
            cQueryHWB +=  " INNER JOIN " + RetSqlName("HW3") + " HW3 "
            cQueryHWB +=     " ON HW3.HW3_FILIAL = ? "
            cQueryHWB +=     " AND HWB.HWB_TICKET = HW3.HW3_TICKET "
            cQueryHWB +=     " AND HW3.D_E_L_E_T_ = ' ' "
            cQueryHWB += "  WHERE HWB.HWB_FILIAL  = ? "
            cQueryHWB += "    AND HWB.HWB_TICKET  = ? " 
            cQueryHWB += "    AND HWB.HWB_PRODUT >= ? "
            cQueryHWB += "    AND HWB.HWB_PRODUT <= ? "
            cQueryHWB += "    AND HWB.HWB_DATA   >= ? "
            cQueryHWB += "    AND HWB.HWB_DATA   <= ? " 
            cQueryHWB += "    AND HWB.D_E_L_E_T_  = ' ' "

            //Montagem da query para execução
            oExec := FwExecStatement():New(cQueryHWB)

            oExec:SetString(1, xFilial("HW3"))
            oExec:SetString(2, xFilial("HWB"))
            oExec:SetString(3, aParam[1])
            oExec:SetString(4, aParam[2])
            oExec:SetString(5, aParam[3])
            oExec:SetDate(6, aParam[4])
            oExec:SetDate(7, aParam[5])

            cAlias := oExec:OpenAlias()

            If (cAlias)->(!Eof())
                cDescEmp := FWGrpName()
                cDescFil := FWFilialName(, (cAlias)->HWB_FILIAL, 1)
            EndIf

            While (cAlias)->(!Eof())

                cData    := totvs.framework.treports.date.stringToTimeStamp((cAlias)->HWB_DATA)
                lHasNext := .T.

                While lHasNext

                    //Chama a funcao para retornar os dados detalhados
                    aReturn := MrpGetRes( , (cAlias)->HWB_TICKET, (cAlias)->HWB_PRODUT, , , , , , , ,999999 )

                    If aReturn[1] == .T.

                        oRet := JsonObject():New()
                        oRet:FromJson(DecodeUTF8(aReturn[2]))
                        lHasNext := oRet['hasNext']

                        For nIndex := 1 To Len(oRet['items'])
                            ojItems := JsonObject():new()

                            //Sumarizado
                            ojItems[self:aStruct[1][1]] := oRet["items"][nIndex]["stockBalance"]           
                            ojItems[self:aStruct[2][1]] := oRet["items"][nIndex]["inFlows"]        
                            ojItems[self:aStruct[3][1]] := oRet["items"][nIndex]["outFlows"]       
                            ojItems[self:aStruct[4][1]] := oRet["items"][nIndex]["structureOutFlows"]
                            ojItems[self:aStruct[5][1]] := oRet["items"][nIndex]["finalBalance"]            
                            ojItems[self:aStruct[6][1]] := oRet["items"][nIndex]["substitution"]  

                            //Geral                
                            ojItems[self:aStruct[7][1]] := cDescFil          
                            ojItems[self:aStruct[8][1]] := cDescEmp           
                            ojItems[self:aStruct[9][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->HW3_DTINIC)        
                            ojItems[self:aStruct[10][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->HW3_DTFIM)        
                            do case        
                                case (cAlias)->HW3_STATUS = "1"
                                    ojItems[self:aStruct[11][1]] := STR0004 //STR0004 - "Reservado"
                                case (cAlias)->HW3_STATUS = "2" 
                                    ojItems[self:aStruct[11][1]] := STR0005 //STR0005 - "Iniciado"
                                case (cAlias)->HW3_STATUS = "3"
                                    ojItems[self:aStruct[11][1]] := STR0006 //STR0006 - "Finalizado"
                                case (cAlias)->HW3_STATUS = "4"
                                    ojItems[self:aStruct[11][1]] := STR0007 //STR0007 - "Cancelando"
                                case (cAlias)->HW3_STATUS = "5"
                                    ojItems[self:aStruct[11][1]] := STR0008 //STR0008 - "Cancelado"
                                case (cAlias)->HW3_STATUS = "6"
                                    ojItems[self:aStruct[11][1]] := STR0009 //STR0009 - "Documentos gerados"  
                                case (cAlias)->HW3_STATUS = "7"
                                    ojItems[self:aStruct[11][1]] := STR0010 //STR0010 - "Documentos gerados com pendências"
                                case (cAlias)->HW3_STATUS = "8"
                                    ojItems[self:aStruct[11][1]] := STR0011 //STR0011 - "Excluído"
                                case (cAlias)->HW3_STATUS = "9"
                                    ojItems[self:aStruct[11][1]] := STR0012 //STR0012 - "Documentos gerados(Integrado)"
                            endcase
                            ojItems[self:aStruct[12][1]] := oRet["items"][nIndex]["optionalId"]         
                            ojItems[self:aStruct[13][1]] := oRet['items'][nIndex]['product']
                            ojItems[self:aStruct[14][1]] := oRet['items'][nIndex]['ticket']
                            ojItems[self:aStruct[15][1]] := cData     
                            //Sumarizado
                            ojItems[self:aStruct[24][1]] := oRet["items"][nIndex]["necessityQuantity"]

                            For nX := 25 To Len(self:aStruct)
                                //Campos Personalizados
                                If self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                                Else
                                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5]) 
                                EndIf 
                            Next nX

                            self:ProcessData()
                            Self:oData:AppendData(ojItems)

                        Next nIndex 

                    EndIf        
                EndDo

                (cAlias)->(DBSkip())

            EndDo

            (cAlias)->(DBCloseArea())
            FreeObj(oExec)
        EndIf

        //Detalhado
        If aParam[6] == 2
            cQueryHWB := " SELECT DISTINCT "
            cQueryHWB +=        " HWB.HWB_FILIAL, "
            cQueryHWB +=        " HWB.HWB_TICKET, "
            cQueryHWB +=        " HWB.HWB_PRODUT, "
            cQueryHWB +=        " HWB.HWB_IDOPC, "
            cQueryHWB +=        " HW3.HW3_DTINIC AS HW3_DTINIC, "
            cQueryHWB +=        " HW3.HW3_DTFIM AS HW3_DTFIM, "
            cQueryHWB +=        " HW3.HW3_STATUS AS HW3_STATUS "

            aadd(aTables,{'HWB_','HWB.'})
            aadd(aTables,{'HW3_','HW3.'})
            PcpInCposPerson(@self:aStruct, @cQueryHWB, self:getCustomFields(),.T.,.T.,,aTables)
            aTables := {}

            cQueryHWB += "   FROM "  + RetSqlName("HWB") + " HWB "
            cQueryHWB +=  " INNER JOIN " + RetSqlName("HW3") + " HW3 "
            cQueryHWB +=     " ON HW3.HW3_FILIAL = ? "
            cQueryHWB +=     " AND HWB.HWB_TICKET = HW3.HW3_TICKET "
            cQueryHWB +=     " AND HW3.D_E_L_E_T_ = ' ' "
            cQueryHWB += "  WHERE HWB.HWB_FILIAL  = ? "
            cQueryHWB += "    AND HWB.HWB_TICKET  = ? "
            cQueryHWB += "    AND HWB.HWB_PRODUT >= ? "
            cQueryHWB += "    AND HWB.HWB_PRODUT <= ? "
            cQueryHWB += "    AND HWB.HWB_DATA   >= ? "
            cQueryHWB += "    AND HWB.HWB_DATA   <= ? "
            cQueryHWB += "    AND HWB.D_E_L_E_T_  = ' ' "

            //Montagem da query para execução
            oExec := FwExecStatement():New(cQueryHWB)

            oExec:SetString(1, xFilial("HW3"))
            oExec:SetString(2, xFilial("HWB"))
            oExec:SetString(3, aParam[1])
            oExec:SetString(4, aParam[2])
            oExec:SetString(5, aParam[3])
            oExec:SetDate(6, aParam[4])
            oExec:SetDate(7, aParam[5])

            cAlias := oExec:OpenAlias()

            If (cAlias)->(!Eof())
                cDescEmp := FWGrpName()
                cDescFil := FWFilialName(, (cAlias)->HWB_FILIAL, 1)
            EndIf

            While (cAlias)->(!Eof())

                lHasNext := .T.

                While lHasNext

                    //Chama a funcao para retornar os dados detalhados
	                aReturn := getResDet((cAlias)->HWB_TICKET, (cAlias)->HWB_PRODUT, , , , (cAlias)->HWB_IDOPC, , , ,999999 )

                    If aReturn[1] == .T.

                        oRet := JsonObject():New()
                        oRet:FromJson(DecodeUTF8(aReturn[2]))
                        lHasNext := oRet['hasNext']

                        For nIndex := 1 To Len(oRet['items'])
                            ojItems := JsonObject():new()  
                           
                            //Geral
                            ojItems[self:aStruct[7][1]] := cDescFil          
                            ojItems[self:aStruct[8][1]] := cDescEmp           
                            ojItems[self:aStruct[9][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->HW3_DTINIC)        
                            ojItems[self:aStruct[10][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->HW3_DTFIM)
                            do case        
                                case (cAlias)->HW3_STATUS = "1"
                                    ojItems[self:aStruct[11][1]] := STR0004 //STR0004 - "Reservado"
                                case (cAlias)->HW3_STATUS = "2" 
                                    ojItems[self:aStruct[11][1]] := STR0005 //STR0005 - "Iniciado"
                                case (cAlias)->HW3_STATUS = "3"
                                    ojItems[self:aStruct[11][1]] := STR0006 //STR0006 - "Finalizado"
                                case (cAlias)->HW3_STATUS = "4"
                                    ojItems[self:aStruct[11][1]] := STR0007 //STR0007 - "Cancelando"
                                case (cAlias)->HW3_STATUS = "5"
                                    ojItems[self:aStruct[11][1]] := STR0008 //STR0008 - "Cancelado"
                                case (cAlias)->HW3_STATUS = "6"
                                    ojItems[self:aStruct[11][1]] := STR0009 //STR0009 - "Documentos gerados"  
                                case (cAlias)->HW3_STATUS = "7"
                                    ojItems[self:aStruct[11][1]] := STR0010 //STR0010 - "Documentos gerados com pendências"
                                case (cAlias)->HW3_STATUS = "8"
                                    ojItems[self:aStruct[11][1]] := STR0011 //STR0011 - "Excluído"
                                case (cAlias)->HW3_STATUS = "9"
                                    ojItems[self:aStruct[11][1]] := STR0012 //STR0012 - "Documentos gerados(Integrado)" 
                            endcase                               
                            ojItems[self:aStruct[12][1]] := oRet["items"][nIndex]["optionalId"]
                            ojItems[self:aStruct[13][1]] := oRet['items'][nIndex]['product']
                            ojItems[self:aStruct[14][1]] := oRet['items'][nIndex]['ticket']
                            //Detalhado
                            ojItems[self:aStruct[15][1]] := oRet["items"][nIndex]["periodDate"]
                            ojItems[self:aStruct[16][1]] := oRet["items"][nIndex]["documentCode"]
                            ojItems[self:aStruct[17][1]] := oRet["items"][nIndex]["fatherProduct"]
                            ojItems[self:aStruct[18][1]] := oRet["items"][nIndex]["warehouse"]

                            If !EMPTY( oRet['items'][nIndex]['quantity'] )  
                                ojItems[self:aStruct[19][1]] := oRet['items'][nIndex]['quantity']
                            Else
                                ojItems[self:aStruct[19][1]] := 0
                            EndIf

                            do case    
                                case oRet["items"][nIndex]["registerType"] = "1"
                                    ojItems[self:aStruct[20][1]] := STR0013 //STR0013 - "Entrada"
                                case oRet["items"][nIndex]["registerType"] = "2"
                                    ojItems[self:aStruct[20][1]] := STR0014 //STR0014 - "Saída"
                                case oRet["items"][nIndex]["registerType"] = "3"
                                    ojItems[self:aStruct[20][1]] := STR0015 //STR0015 - "Saldo"
                                case oRet["items"][nIndex]["registerType"] = "4"
                                    ojItems[self:aStruct[20][1]] := STR0015 //STR0015 - "Saldo"             
                            endcase

                            do case
                                case oRet['items'][nIndex]['documentType'] = "SI" 
                                    ojItems[self:aStruct[21][1]] := STR0016 //STR0016 - "Saldo Inicial" 
                                case oRet['items'][nIndex]['documentType'] = "SP" 
                                    ojItems[self:aStruct[21][1]] := STR0017 //STR0017 - "Saldo Período"
                                case oRet['items'][nIndex]['documentType'] = "EM" 
                                    ojItems[self:aStruct[21][1]] := STR0018 //STR0018 - "Empenho"
                                case oRet['items'][nIndex]['documentType'] = "DM" 
                                    ojItems[self:aStruct[21][1]] := STR0019 //STR0019 - "Demanda"
                                case oRet['items'][nIndex]['documentType'] = "OP" 
                                    ojItems[self:aStruct[21][1]] := STR0020 //STR0020 - "Ordem de Produção"
                                case oRet['items'][nIndex]['documentType'] = "SU" 
                                    ojItems[self:aStruct[21][1]] := STR0021 //STR0021 - "OP - SubProduto"
                                case oRet['items'][nIndex]['documentType'] = "OT" 
                                    ojItems[self:aStruct[21][1]] := STR0022 //STR0022 - "OP - Transferência"
                                case oRet['items'][nIndex]['documentType'] = "SC" 
                                    ojItems[self:aStruct[21][1]] := STR0023 //STR0023 - "Solicitação de compra"
                                case oRet['items'][nIndex]['documentType'] = "PC" 
                                    ojItems[self:aStruct[21][1]] := STR0024 //STR0024 - "Pedido de compra"
                                case oRet['items'][nIndex]['documentType'] = "AU" 
                                    ojItems[self:aStruct[21][1]] := STR0025 //STR0025 - "Autorização de Entrega"
                                case oRet['items'][nIndex]['documentType'] = "ES" 
                                    ojItems[self:aStruct[21][1]] := STR0026 //STR0026 - "Estoque de segurança"
                                case oRet['items'][nIndex]['documentType'] = "TR" 
                                    ojItems[self:aStruct[21][1]] := STR0027 //STR0027 - "Transferência"
                                case oRet['items'][nIndex]['documentType'] = "LV" 
                                    ojItems[self:aStruct[21][1]] := STR0028 //STR0028 - "Lote Vencido"
                                case oRet['items'][nIndex]['documentType'] = "PM" 
                                    ojItems[self:aStruct[21][1]] := STR0029 //STR0029 - "Plano Mestre"
                                case oRet['items'][nIndex]['documentType'] = "SR" 
                                    ojItems[self:aStruct[21][1]] := STR0030 //STR0030 - "Saldo Rejeitado CQ"
                                case oRet['items'][nIndex]['documentType'] = "DT" 
                                    ojItems[self:aStruct[21][1]] := STR0031 //STR0031 - "De Terceiro"
                                case oRet['items'][nIndex]['documentType'] = "ET" 
                                    ojItems[self:aStruct[21][1]] := STR0032 //STR0032 - "Em Terceiro"
                                case oRet['items'][nIndex]['documentType'] = "SB" 
                                    ojItems[self:aStruct[21][1]] := STR0033 //STR0033 - "Saldo Bloqueado"                                                                                                                                                 
                            endcase

                            do case
                                case oRet["items"][nIndex]["documentStatus"] = "M"
                                    ojItems[self:aStruct[22][1]] := STR0034 //STR0034 - "MRP"
                                case oRet["items"][nIndex]["documentStatus"] = "P"
                                    ojItems[self:aStruct[22][1]] := STR0035 //STR0035 - "Previsto"
                                case oRet["items"][nIndex]["documentStatus"] = "F"
                                    ojItems[self:aStruct[22][1]] := STR0036 //STR0036 - "Firme"      
                            endcase 

                            ojItems[self:aStruct[23][1]] := oRet["items"][nIndex]["balance"]

                            For nX := 25 To Len(self:aStruct)
                                //Campos Personalizados
                                If self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                                Else
                                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5]) 
                                EndIf 
                            Next nX
                            
                            self:ProcessData()
                            Self:oData:AppendData(ojItems)

                        Next nIndex
                    Else 
                        lHasNext := .F.    
                    EndIf	
                EndDo

                (cAlias)->(DBSkip())

            EndDo
            
            (cAlias)->(DBCloseArea())
            FreeObj(oExec)
        EndIf
    EndIf
    
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 04/08/2023
@version 1.0
/*/
Method getSchema() as object class ResultadosMrpTReportsBusinessObject
    Local nX        := 0 as numeric          

    self:aStruct := {}
    
    //SUMARIZADO
    AAdd(self:aStruct , {"STOCKBALANCE"  , "STOCKBALANCE"  , "number", "STOCKBALANCE"  , "STOCKBALANCE"  }) //01
    AAdd(self:aStruct , {"INFLOWS"       , "INFLOWS"       , "number", "INFLOWS"       , "INFLOWS"       }) //02
    AAdd(self:aStruct , {"OUTFLOWS"      , "OUTFLOWS"      , "number", "OUTFLOWS"      , "OUTFLOWS"      }) //03
    AAdd(self:aStruct , {"STRUCTOUTFLOWS", "STRUCTOUTFLOWS", "number", "STRUCTOUTFLOWS", "STRUCTOUTFLOWS"}) //04
    AAdd(self:aStruct , {"FINALBALANCE"  , "FINALBALANCE"  , "number", "FINALBALANCE"  , "FINALBALANCE"  }) //05    
    AAdd(self:aStruct , {"SUBSTITUTION"  , "SUBSTITUTION"  , "number", "SUBSTITUTION"  , "SUBSTITUTION"  }) //06  

    //Geral
    AAdd(self:aStruct , {"FILIAL"           , "FILIAL"           , "string", "FILIAL"           , "FILIAL"           }) //07
    AAdd(self:aStruct , {"DESC_EMP"         , "DESC_EMP"         , "string", "DESC_EMP"         , "DESC_EMP"         }) //08
    AAdd(self:aStruct , {"HW3_DTINIC"       , "HW3_DTINIC"       , "date"  , "HW3_DTINIC"       , "HW3_DTINIC"       }) //09
    AAdd(self:aStruct , {"HW3_DTFIM"        , "HW3_DTFIM"        , "date"  , "HW3_DTFIM"        , "HW3_DTFIM"        }) //10
    AAdd(self:aStruct , {"HW3_STATUS"       , "HW3_STATUS"       , "string", "HW3_STATUS"       , "HW3_STATUS"       }) //11
    AAdd(self:aStruct , {"IDOPC"            , "IDOPC"            , "string", "IDOPC"            , "IDOPC"            }) //12  
    AAdd(self:aStruct , {"PRODUTO_PRINCIPAL", "PRODUTO_PRINCIPAL", "string", "PRODUTO_PRINCIPAL", "PRODUTO_PRINCIPAL"}) //13
    AAdd(self:aStruct , {"TICKET_PRINCIPAL" , "TICKET_PRINCIPAL" , "string", "TICKET_PRINCIPAL" , "TICKET_PRINCIPAL" }) //14
    AAdd(self:aStruct , {"DATA_PRINCIPAL"   , "DATA_PRINCIPAL"   , "date"  , "DATA_PRINCIPAL"   , "DATA_PRINCIPAL"   }) //15

    //Detalhado
    AAdd(self:aStruct , {"DOCUM_PRINCIPAL"     , "DOCUM_PRINCIPAL"     , "string", "DOCUM_PRINCIPAL"     , "DOCUM_PRINCIPAL"     }) //16
    AAdd(self:aStruct , {"PRODPAI_PRINCIPAL"   , "PRODPAI_PRINCIPAL"   , "string", "PRODPAI_PRINCIPAL"   , "PRODPAI_PRINCIPAL"   }) //17
    AAdd(self:aStruct , {"ARMAZEM_PRINCIPAL"   , "ARMAZEM_PRINCIPAL"   , "string", "ARMAZEM_PRINCIPAL"   , "ARMAZEM_PRINCIPAL"   }) //18
    AAdd(self:aStruct , {"QUANTIDADE_PRINCIPAL", "QUANTIDADE_PRINCIPAL", "number", "QUANTIDADE_PRINCIPAL", "QUANTIDADE_PRINCIPAL"}) //19
    AAdd(self:aStruct , {"TYPE"                , "TYPE"                , "string", "TYPE"                , "TYPE"                }) //20
    AAdd(self:aStruct , {"DOCTYPE"             , "DOCTYPE"             , "string", "DOCTYPE"             , "DOCTYPE"             }) //21
    AAdd(self:aStruct , {"DOCSTATUS"           , "DOCSTATUS"           , "string", "DOCSTATUS"           , "DOCSTATUS"           }) //22
    AAdd(self:aStruct , {"SALDO_FINAL"         , "SALDO_FINAL"         , "number", "SALDO_FINAL"         , "SALDO_FINAL"         }) //23

    //Sumarizado 
    AAdd(self:aStruct , {"NECESSIDADE", "NECESSIDADE", "number", "NECESSIDADE", "NECESSIDADE"}) //24
    
    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/ticketsMrp", 2)

Return self:oSchema
