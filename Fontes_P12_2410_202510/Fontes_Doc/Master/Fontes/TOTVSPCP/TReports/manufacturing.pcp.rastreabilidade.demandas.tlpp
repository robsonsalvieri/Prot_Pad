#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "manufacturing.pcp.rastreabilidade.demandas.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAPCP", tables="SMH", name="Rastreabilidade das Demandas", country="ALL", initialRelease="12.1.2210") 

/*/{Protheus.doc} RastreabilidadeDemandasTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem dos registros do Resultado do MRP

@author breno.ferreira
@since 27/09/2023
@version 1.0
/*/
Class RastreabilidadeDemandasPrincipalTReportsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields  as array
    protected data aFields2 as array
    protected data aFields3 as array
    protected data aFields4 as array
    protected data aStruct  as array
    protected data aStruct2 as array
    protected data aStruct3 as array
    protected data aStruct4 as array
EndClass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 27/09/2023
@version 1.0
/*/ 
Method new() as object class RastreabilidadeDemandasPrincipalTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001) //STR0001 - "Manufatura"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) //STR0002 - "Rastreabilidade das Demandas"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003) //STR0003 - "Listagem da Rastreabilidade das Demandas"

    self:setPergunte("TRRAS") //Indica o pergunte que será utilizado no relatório
Return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 27/09/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class RastreabilidadeDemandasPrincipalTReportsBusinessObject
    Local aParam      := Array(13) as array
    Local aTipo       := {}        as array
    Local cAlias      := ""        as character
    Local cDescEmp    := ""        as character
    Local cDescFil    := ""        as character
    Local cOrigem     := ""        as character
    Local cQuerySMH   := ""        as character
    Local cStatus     := ""        as character
    Local cStatusComp := ""        as character
    Local cTipo       := ""        as character
    Local dDataFim                 as date
    Local dValidade                as date
    Local jParams     := Nil       as json
    Local lAnt        := .F.       as logical
    Local lHasNext    := .F.       as logical
    Local linfdem     := .F.       as logical
    Local linfdoc     := .F.       as logical
    Local linfop      := .F.       as logical
    Local linfprod    := .F.       as logical
    Local nIndex      := 0         as numeric
    Local nIndex2     := 0         as numeric
    Local nIndex3     := 0         as numeric
    Local nIndex4     := 0         as numeric
    Local oExec       := Nil       as object
    Local ojItems     := Nil       as json
    Local ojItems2    := Nil       as json
    Local ojItems3    := Nil       as json
    Local ojItems4    := Nil       as json
    Local oRet        := Nil       as object
    Local oRet2       := Nil       as object
    Local oRet3       := Nil       as object
    Local oRet4       := Nil       as object

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1]  := jParams["MV_PAR01"][1] //Demanda de ?        
    aParam[2]  := jParams["MV_PAR02"][1] //Demanda até ? 
    aParam[3]  := jParams["MV_PAR03"][1] //Documento de ? 
    aParam[4]  := jParams["MV_PAR04"][1] //Documento até ?
    aParam[5]  := jParams["MV_PAR05"][1] //OP de ?
    aParam[6]  := jParams["MV_PAR06"][1] //OP até ? 
    aParam[7]  := jParams["MV_PAR07"][1] //Produto de ?
    aParam[8]  := jParams["MV_PAR08"][1] //Produto até ?
    aParam[9]  := jParams["MV_PAR09"][1] //Pedido de venda ? (Sim ou Não)
    aParam[10] := jParams["MV_PAR10"][1] //Previsão de venda ? (Sim ou Não) 
    aParam[11] := jParams["MV_PAR11"][1] //Plano mestre de produção ? (Sim ou Não)
    aParam[12] := jParams["MV_PAR12"][1] //Empenho de projeto ? (Sim ou Não)
    aParam[13] := jParams["MV_PAR13"][1] //Manual ? (Sim ou Não)

    If !EMPTY(mv_par01)  .or. mv_par02 != 'ZZZZZZZZZZZZZZZ'
        linfdem = .T.
    EndIf 

    If !EMPTY(mv_par03)  .or. mv_par04 != 'ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ'
        linfdoc = .T.
    EndIf 

    If !EMPTY(mv_par05)  .or. mv_par06 != 'ZZZZZZZZZZZZZZZZZZZZZZZZZ'
        linfop = .T.
    EndIf 

    If !EMPTY(mv_par07)  .or. mv_par08 != 'ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ'
        linfprod = .T.
    EndIf 

    lAnt := .F.
    cTipo := ""

    If aParam[9] == 1
        Aadd(aTipo, '1') //Pedido de venda
        lAnt := .T.
        cTipo += '1'
    EndIf
    If aParam[10] == 1
        Aadd(aTipo, '2') //Previsão de venda
        If lAnt 
            cTipo += ','
        EndIf
        lAnt := .T.
        cTipo += '2'
    EndIf 
    If aParam[11] == 1
        Aadd(aTipo, '3') //Plano mestre de produção
        If lAnt 
            cTipo += ','
        EndIf
        lAnt := .T.
        cTipo += '3'
    EndIf 
    If aParam[12] == 1
        Aadd(aTipo, '4') //Empenho de projeto
        If lAnt 
            cTipo += ','
        EndIf
        lAnt := .T.
        cTipo += '4'
    EndIf 
    If aParam[13] == 1
        Aadd(aTipo, '9') //Manual
        If lAnt 
            cTipo += ','
        EndIf
        lAnt := .T.
        cTipo += '9'
    EndIf
    
    cQuerySMH := " SELECT DISTINCT "
    cQuerySMH +=        " SMH.MH_FILIAL "
    If linfdem 
        cQuerySMH +=        ", SMH.MH_DEMANDA "
    EndIf    
    If linfdoc 
        cQuerySMH +=        ", SMH.MH_DEMDOC "
    EndIf
    If linfop
        cQuerySMH +=        ", SMH.MH_NMDCENT "
    EndIf
    If linfprod
        cQuerySMH +=        ", SMH.MH_PRODUTO "
    EndIf

    cQuerySMH += "   FROM "  + RetSqlName("SMH") + " SMH "
    cQuerySMH +=  " INNER JOIN " + RetSqlName("SVR") + " SVR "
    cQuerySMH +=     " ON SVR.VR_FILIAL = ? "
    cQuerySMH +=    " AND SVR.VR_CODIGO = SMH.MH_DEMANDA "
    cQuerySMH +=    " AND SVR.VR_PROD   = SMH.MH_PRODUTO "
    cQuerySMH +=    " AND SVR.D_E_L_E_T_ = ' ' "
    cQuerySMH += "  WHERE SMH.MH_FILIAL  = ? "
    cQuerySMH += "    AND SMH.MH_DEMANDA >= ? " 
    cQuerySMH += "    AND SMH.MH_DEMANDA <= ? "
    cQuerySMH += "    AND UPPER(SMH.MH_DEMDOC)  >= UPPER(?) "
    cQuerySMH += "    AND UPPER(SMH.MH_DEMDOC)  <= UPPER(?) "
    cQuerySMH += "    AND UPPER(SMH.MH_NMDCENT) >= UPPER(?) "
    cQuerySMH += "    AND UPPER(SMH.MH_NMDCENT) <= UPPER(?) "
    cQuerySMH += "    AND SMH.MH_PRODUTO >= ? "
    cQuerySMH += "    AND SMH.MH_PRODUTO <= ? "
    If Len(aTipo) != 0
        cQuerySMH += "    AND SVR.VR_TIPO IN (?) "
    EndIf
    cQuerySMH += "    AND SMH.D_E_L_E_T_  = ' ' " 

    //Montagem da query para execução
    oExec := FwExecStatement():New(cQuerySMH)

    oExec:SetString(1, xFilial("SVR"))
    oExec:SetString(2, xFilial("SMH"))
    oExec:SetString(3, trim(aParam[1]))
    oExec:SetString(4, trim(aParam[2]))
    oExec:SetString(5, trim(aParam[3]))
    oExec:SetString(6, trim(aParam[4]))
    oExec:SetString(7, trim(aParam[5]))
    oExec:SetString(8, trim(aParam[6]))
    oExec:SetString(9, trim(aParam[7]))
    oExec:SetString(10, trim(aParam[8]))
    If Len(aTipo) != 0
        oExec:SetIn(11, aTipo)
    EndIf

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->MH_FILIAL, 1)
    EndIf

    While (cAlias)->(!Eof())

        lHasNext := .T.

        While lHasNext

            //Chama a funcao para retornar os dados detalhados - Principal
            aReturn := GetPOrdPeg((cAlias)->MH_FILIAL,;
                                  If(linfop, (cAlias)->MH_NMDCENT, ), cTipo,; 
                                  If(linfdoc, (cAlias)->MH_DEMDOC, ),;
                                  If(linfprod, (cAlias)->MH_PRODUTO, ), , , 999999, nil, , , , ,;
                                  If(linfdem, (cAlias)->MH_DEMANDA, )) 

            ojItems  := JsonObject():new()
            ojItems2 := JsonObject():new()
            ojItems3 := JsonObject():new()
            ojItems4 := JsonObject():new()

            ojItems["Principal"] := {}
            ojItems2["Compras"] := {}
            ojItems3["Opcionais"] := {}
            ojItems4["Empenhos"] := {}

            if aReturn[1] == .T.

                oRet := JsonObject():New()
                oRet:FromJson(DecodeUTF8(aReturn[2]))
                lHasNext := oRet['hasNext']             

                For nIndex := 1 To Len(oRet['items'])

                    if !EMPTY(oRet["items"][nIndex]["endDate"]) 
                        dDataFim := oRet["items"][nIndex]["endDate"] 
                    endif 
                    do case 
                        case oRet["items"][nIndex]["status"] = 1
                            cStatus := STR0011 //STR0011 - "No Prazo"
                        case oRet["items"][nIndex]["status"] = 2
                            cStatus := STR0004 //STR0004 - "Atrasado"
                        case oRet["items"][nIndex]["status"] = 3
                            cStatus := STR0005 //STR0005 - "Saldo Inicial"
                    endcase
                    do case
                        case oRet["items"][nIndex]["documentOrigin"] = "1"
                            cOrigem := STR0006 //STR0006 - "Pedido de venda"
                        case oRet["items"][nIndex]["documentOrigin"] = "2"
                            cOrigem := STR0007 //STR0007 - "Previsão de venda"
                        case oRet["items"][nIndex]["documentOrigin"] = "3"
                            cOrigem := STR0008 //STR0008 - "Plano mestre de produção"
                        case oRet["items"][nIndex]["documentOrigin"] = "4"
                            cOrigem := STR0009 //STR0009 - "Empenho de projeto"    
                        case oRet["items"][nIndex]["documentOrigin"] = "9"
                            cOrigem := STR0010 //STR0010 - "Manual"    
                    endcase 

                    if !empty(oRet["items"][nIndex]["productionOrder"])

                        //Chama a funcao para retornar os dados detalhados - Compras                
                        aReturn2 := GetPurchs((cAlias)->MH_FILIAL,;
                                              oRet["items"][nIndex]["productionOrder"], , 999999,;
                                              If(linfdoc,oRet["items"][nIndex]["documentId"], ), , )

                        if aReturn2[1] == .T.

                            oRet2 := JsonObject():New()
                            oRet2:FromJson(DecodeUTF8(aReturn2[2]))

                            for nIndex2 := 1 To Len(oRet2['items'])

                                if AllTrim(oRet["items"][nIndex]["idReg"]) == AllTrim(oRet2["items"][nIndex2]["idPai"])

                                    do case
                                        case oRet2["items"][nIndex2]["status"] = 1
                                            cStatusComp := STR0011 //STR0006 - "No Prazo" 
                                        case oRet2["items"][nIndex2]["status"] = 2
                                            cStatusComp := STR0004 //STR0004 - "Atrasado"
                                        case oRet2["items"][nIndex2]["status"] = 3
                                            cStatusComp := STR0005 //STR0005 - "Saldo Inicial"
                                    endcase

                                    //Compras
                                    aAdd(ojItems2["Compras"], {"COMPRAS_OP": oRet2["items"][nIndex2]["productionOrder"]  ,;
                                                               "COMPRAS_DOCUMENTO": oRet2["items"][nIndex2]["documentId"] ,;
                                                               "COMPRAS_SC_PC": oRet2["items"][nIndex2]["purchaseId"] ,;
                                                               "COMPRAS_TIPO": UPPER(oRet2["items"][nIndex2]["purchaseType"]) ,;
                                                               "COMPRAS_PRODUTO": oRet2["items"][nIndex2]["product"] ,;
                                                               "COMPRAS_QUANT_SC_PC": oRet2["items"][nIndex2]["quantity"] ,;
                                                               "COMPRAS_QUANT_USAD": oRet2["items"][nIndex2]["usedQuantity"] ,;
                                                               "COMPRAS_ENTREGA": oRet2["items"][nIndex2]["deliveryDate"] ,;
                                                               "COMPRAS_SITUACAO": cStatusComp ,;
                                                               "COMPRAS_ATRASO": oRet2["items"][nIndex2]["delay"] ,;
                                                               "COMPRAS_ARMAZEM": oRet2["items"][nIndex2]["warehouse"] })
                                    self:ProcessData()
                                endif    
                            Next nIndex2
                        endif        
                    endif

                    if !empty(oRet["items"][nIndex]["viewOptional"])

                        //Chama a funcao para retornar os dados detalhados - Opcionais
                        aReturn3 := GetOPCPeg(oRet["items"][nIndex]["PORecno"])

                        if aReturn3[1] == .T.

                            oRet3 := JsonObject():New()
                            oRet3:FromJson(DecodeUTF8(aReturn3[2]))

                            for nIndex3 := 1 To Len(oRet3['items'])

                                //Opcionais
                                aAdd(ojItems3["Opcionais"], {"GRUPO_OPCIONAL": oRet3["items"][nIndex3]["groupOptional"] ,;
                                                             "DESC_GRUPO_OPC": oRet3["items"][nIndex3]["descGroupOptional"] ,;
                                                             "ITEM_OPC": oRet3["items"][nIndex3]["itemOptional"] ,;
                                                             "DESC_ITEM_OPC": oRet3["items"][nIndex3]["descItemOptional"] ,;
                                                             "PRODUTO_OPCIONAL": oRet3["items"][nIndex3]["product"] ,;
                                                             "DESC_PRODUTO": oRet3["items"][nIndex3]["description"] })

                                self:ProcessData()
                            Next nIndex3   
                        endif
                    endif

                    if !empty(oRet["items"][nIndex]["productionOrder"])

                        //Chama a funcao para retornar os dados detalhados - Empenhos
                        aReturn4 := PCPEmpPegg( oRet["items"][nIndex]["productionOrder"], 999999, , , )

                        if aReturn4[1] == .T.

                            oRet4 := JsonObject():New()
                            oRet4:FromJson(DecodeUTF8(aReturn4[2]))

                            for nIndex4 := 1 To Len(oRet4['items'])

                                if !empty(oRet4["items"][nIndex4]["expirationDate"])
                                    dValidade := oRet4["items"][nIndex4]["expirationDate"]
                                endif

                                //Empenhos
                                aAdd(ojItems4["Empenhos"], {"EMPENHO_OP": oRet4["items"][nIndex4]["productionOrder"]  ,;
                                                            "EMPENHO_PRODUTO": oRet4["items"][nIndex4]["product"] ,;
                                                            "EMPENHO_ARMAZEM": oRet4["items"][nIndex4]["warehouse"] ,;
                                                            "EMPENHO_DATA": oRet4["items"][nIndex4]["allocationDate"] ,;
                                                            "EMPENHO_QUANT_TOTAL": oRet4["items"][nIndex4]["quantity"] ,;
                                                            "EMPENHO_LOTE": oRet4["items"][nIndex4]["lot"] ,;
                                                            "EMPENHO_SUB_LOTE": oRet4["items"][nIndex4]["sublot"] ,;
                                                            "EMPENHO_VALIDADE": dValidade ,;
                                                            "EMPENHO_OPERACAO": oRet4["items"][nIndex4]["operation"] ,;
                                                            "EMPENHO_OP_ORIGEM": oRet4["items"][nIndex4]["originalProductionOrder"] })

                                self:ProcessData()
                            Next nIndex4 
                        endif        
                    endif

                    //Principal
                    aAdd(ojItems["Principal"], {"FILIAL": cDescFil ,;
                                                "DESCEMP": cDescEmp ,;
                                                "DEMANDA": oRet["items"][nIndex]["demand"]  ,;
                                                "DOCUMENTO": oRet["items"][nIndex]["documentId"] ,;
                                                "ORDEM_PRODUCAO": oRet["items"][nIndex]["productionOrder"] ,;
                                                "PRODUTO": oRet["items"][nIndex]["product"] ,;
                                                "QUANT_OP": oRet["items"][nIndex]["balance"] ,;
                                                "QUANT_USADA": oRet["items"][nIndex]["usedQuantity"] ,;
                                                "ENTREGA": dDataFim ,;
                                                "DATA_USO": oRet["items"][nIndex]["usedDate"] ,;
                                                "ATRASO": oRet["items"][nIndex]["delay"] ,;
                                                "SITUACAO": cStatus ,;
                                                "ORIGEM": cOrigem ,;
                                                "Compras": ojItems2["Compras"] ,;
                                                "Opcionais": ojItems3["Opcionais"] ,;
                                                "Empenhos": ojItems4["Empenhos"] })

                    self:ProcessData()

                    ojItems2["Compras"] := {}
                    ojItems3["Opcionais"] := {}
                    ojItems4["Empenhos"] := {}

                Next nIndex
            endif 
        EndDo 
                
        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())
    EndDo

    (cAlias)->(dbClosearea())
    oExec:Destroy()
    FreeObj(oExec)
    
Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 27/09/2023
@version 1.0
/*/
Method getSchema() as object class RastreabilidadeDemandasPrincipalTReportsBusinessObject

    self:aStruct  := {}
    self:aStruct2 := {}
    self:aStruct3 := {}
    self:aStruct4 := {}

    //Campos do Select Principal
    AAdd(self:aStruct , {"FILIAL"         , "FILIAL"         , "string" , "FILIAL"         }) //01
    AAdd(self:aStruct , {"DESCEMP"        , "DESCEMP"        , "string" , "DESCEMP"        }) //02
    AAdd(self:aStruct , {"DEMANDA"        , "DEMANDA"        , "string" , "DEMANDA"        }) //03
    AAdd(self:aStruct , {"DOCUMENTO"      , "DOCUMENTO"      , "string" , "DOCUMENTO"      }) //04  
    AAdd(self:aStruct , {"ORDEM_PRODUCAO" , "ORDEM_PRODUCAO" , "string" , "ORDEM_PRODUCAO" }) //05
    AAdd(self:aStruct , {"PRODUTO"        , "PRODUTO"        , "string" , "PRODUTO"        }) //06
    AAdd(self:aStruct , {"QUANT_OP"       , "QUANT_OP"       , "number" , "QUANT_OP"       }) //07
    AAdd(self:aStruct , {"QUANT_USADA"    , "QUANT_USADA"    , "number" , "QUANT_USADA"    }) //08
    AAdd(self:aStruct , {"ENTREGA"        , "ENTREGA"        , "date"   , "ENTREGA"        }) //09
    AAdd(self:aStruct , {"DATA_USO"       , "DATA_USO"       , "date"   , "DATA_USO"       }) //10
    AAdd(self:aStruct , {"ATRASO"         , "ATRASO"         , "number" , "ATRASO"         }) //11
    AAdd(self:aStruct , {"SITUACAO"       , "SITUACAO"       , "string" , "SITUACAO"       }) //12
    AAdd(self:aStruct , {"ORIGEM"         , "ORIGEM"         , "string" , "ORIGEM"         }) //13

    //Aninhados do principal
    AAdd(self:aStruct , {"Compras"        , "Compras"        , "array"  , "Compras"        })
    AAdd(self:aStruct , {"Opcionais"      , "Opcionais"      , "array"  , "Opcionais"      })
    AAdd(self:aStruct , {"Empenhos"       , "Empenhos"       , "array"  , "Empenhos"       })

    self:addNestedProperty("Principal", "Principal", "Principal",, self:aStruct)

    //COMPRAS
    AAdd(self:aStruct2 , {"COMPRAS_OP"          , "COMPRAS_OP"          , "string" , "COMPRAS_OP"          }) //01
    AAdd(self:aStruct2 , {"COMPRAS_DOCUMENTO"   , "COMPRAS_DOCUMENTO"   , "string" , "COMPRAS_DOCUMENTO"   }) //02
    AAdd(self:aStruct2 , {"COMPRAS_SC_PC"       , "COMPRAS_SC_PC"       , "string" , "COMPRAS_SC_PC"       }) //03
    AAdd(self:aStruct2 , {"COMPRAS_TIPO"        , "COMPRAS_TIPO"        , "string" , "COMPRAS_TIPO"        }) //04
    AAdd(self:aStruct2 , {"COMPRAS_PRODUTO"     , "COMPRAS_PRODUTO"     , "string" , "COMPRAS_PRODUTO"     }) //05
    AAdd(self:aStruct2 , {"COMPRAS_QUANT_SC_PC" , "COMPRAS_QUANT_SC_PC" , "number" , "COMPRAS_QUANT_SC_PC" }) //06
    AAdd(self:aStruct2 , {"COMPRAS_QUANT_USAD"  , "COMPRAS_QUANT_USAD"  , "number" , "COMPRAS_QUANT_USAD"  }) //07
    AAdd(self:aStruct2 , {"COMPRAS_ENTREGA"     , "COMPRAS_ENTREGA"     , "date"   , "COMPRAS_ENTREGA"     }) //08
    AAdd(self:aStruct2 , {"COMPRAS_SITUACAO"    , "COMPRAS_SITUACAO"    , "string" , "COMPRAS_SITUACAO"    }) //09
    AAdd(self:aStruct2 , {"COMPRAS_ATRASO"      , "COMPRAS_ATRASO"      , "number" , "COMPRAS_ATRASO"      }) //10
    AAdd(self:aStruct2 , {"COMPRAS_ARMAZEM"     , "COMPRAS_ARMAZEM"     , "string" , "COMPRAS_ARMAZEM"     }) //11
   
    self:transformInNested("Compras",,self:aStruct2)

    //Opcionais
    AAdd(self:aStruct3 , {"GRUPO_OPCIONAL"    , "GRUPO_OPCIONAL"   , "string" , "GRUPO_OPCIONAL"   }) //01
    AAdd(self:aStruct3 , {"DESC_GRUPO_OPC"    , "DESC_GRUPO_OPC"   , "string" , "DESC_GRUPO_OPC"   }) //02
    AAdd(self:aStruct3 , {"ITEM_OPC"          , "ITEM_OPC"         , "string" , "ITEM_OPC"         }) //03
    AAdd(self:aStruct3 , {"DESC_ITEM_OPC"     , "DESC_ITEM_OPC"    , "string" , "DESC_ITEM_OPC"    }) //04
    AAdd(self:aStruct3 , {"PRODUTO_OPCIONAL"  , "PRODUTO_OPCIONAL" , "string" , "PRODUTO_OPCIONAL" }) //05
    AAdd(self:aStruct3 , {"DESC_PRODUTO"      , "DESC_PRODUTO"     , "string" , "DESC_PRODUTO"     }) //06
    

    self:transformInNested("Opcionais",,self:aStruct3)
    
    //Empenhos
    AAdd(self:aStruct4 , {"EMPENHO_OP"          , "EMPENHO_OP"          , "string" , "EMPENHO_OP"          }) //01
    AAdd(self:aStruct4 , {"EMPENHO_PRODUTO"     , "EMPENHO_PRODUTO"     , "string" , "EMPENHO_PRODUTO"     }) //02
    AAdd(self:aStruct4 , {"EMPENHO_ARMAZEM"     , "EMPENHO_ARMAZEM"     , "string" , "EMPENHO_ARMAZEM"     }) //03
    AAdd(self:aStruct4 , {"EMPENHO_DATA"        , "EMPENHO_DATA"        , "date"   , "EMPENHO_DATA"        }) //04
    AAdd(self:aStruct4 , {"EMPENHO_QUANT_TOTAL" , "EMPENHO_QUANT_TOTAL" , "number" , "EMPENHO_QUANT_TOTAL" }) //05
    AAdd(self:aStruct4 , {"EMPENHO_LOTE"        , "EMPENHO_LOTE"        , "string" , "EMPENHO_LOTE"        }) //06
    AAdd(self:aStruct4 , {"EMPENHO_SUB_LOTE"    , "EMPENHO_SUB_LOTE"    , "string" , "EMPENHO_SUB_LOTE"    }) //07
    AAdd(self:aStruct4 , {"EMPENHO_VALIDADE"    , "EMPENHO_VALIDADE"    , "date"   , "EMPENHO_VALIDADE"    }) //08
    AAdd(self:aStruct4 , {"EMPENHO_OPERACAO"    , "EMPENHO_OPERACAO"    , "string" , "EMPENHO_OPERACAO"    }) //09
    AAdd(self:aStruct4 , {"EMPENHO_OP_ORIGEM"   , "EMPENHO_OP_ORIGEM"   , "string" , "EMPENHO_OP_ORIGEM"   }) //10

    self:transformInNested("Empenhos",,self:aStruct4)

    self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/rastreaDemanda", 2)
    self:setCustomURL("MV_PAR02", "/api/pcp/smartview/v1/rastreaDemanda", 2)
    self:setCustomURL("MV_PAR03", "/api/pcp/smartview/v1/rastreaDocumento", 2)
    self:setCustomURL("MV_PAR04", "/api/pcp/smartview/v1/rastreaDocumento", 2)
 
Return self:oSchema
