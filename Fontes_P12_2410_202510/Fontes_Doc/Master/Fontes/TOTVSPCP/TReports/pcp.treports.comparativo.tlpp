#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.comparativo.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SH6,SC2,SB1,SH1,SH4", name="Comparativo Real x Previsto", country="ALL", initialRelease="12.1.2210", customTables="SH6,SC2,SB1,SH1,SH4")

static cTipoTemp  := SuperGetMV("MV_TPHR",.F.,"N") as character

/*/{Protheus.doc} ComparativoTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem do Comparativo Real X Previsto

@author breno.ferreira
@since 24/05/2023
@version 1.0
/*/
class ComparativoTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 24/05/2023
@version 1.0
/*/ 
method new() as object class ComparativoTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Comparativo"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Relátorio Comparativo Real X Previsto"

    self:setPergunte("MTR895") //Indica o pergunte que será utilizado no relatário
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatário
@param oFilter, objeto, contêm o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 24/05/2023
@version 1.0
/*/
method getData(nPage as numeric, oFilter as object) as object class ComparativoTReportsBusinessObject
    Local aAreaSH6 := {}             as array
    Local aParam   := Array(8)       as array
    Local aTables  := {}             as array
    Local cAlias   := ""             as character
    Local cBanco   := TCGetDB()      as character
    Local cDescEmp := ""             as character
    Local cDescFil := ""             as character
    Local cFields  := ""             as character
    Local cHrPrev  := ""             as character
    Local cHrReal  := ""             as character
    Local cQuery   := ""             as character
    Local cRoteiro := ""             as character
    Local jParams  := Nil            as json
    Local lHasNext := .F.            as logical
    Local lUlt     := ""             as character
    Local nX       := 0              as numeric
    Local oExec    := Nil            as object
    Local ojItems  := Nil            as json

	Private l680 := .F., l681 := .F. as logical

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := pcpValidParam(jParams["MV_PAR01"][1]) //De OP ?
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Ate OP ?
    aParam[3] := pcpValidParam(jParams["MV_PAR03"][1]) //De Recurso ?
    aParam[4] := pcpValidParam(jParams["MV_PAR04"][1]) //Ate Recurso ?
    aParam[5] := pcpconvdat(jParams["MV_PAR05"][1],1) //De Data Apontam. ?
    aParam[6] := pcpconvdat(jParams["MV_PAR06"][1],1) //Ate Data Apontam. ?
    aParam[7] := pcpValidParam(jParams["MV_PAR07"][1]) //De Operador ?
    aParam[8] := pcpValidParam(jParams["MV_PAR08"][1]) //Ate Operador ?

    cFields := ArrTokStr(self:aFields,",")
    
    cQuery := "SELECT ? "

    aadd(aTables,{'H6_','SH6.'})
    aadd(aTables,{'C2_','SC2.'})
    aadd(aTables,{'B1_','SB1.'})
    aadd(aTables,{'H1_','SH1.'})
    aadd(aTables,{'H4_','SH4.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(), .T., .T.,,aTables)
    aTables := {} 

	cQuery +=    " FROM " + RetSqlName("SH6") + " SH6 "
    cQuery +=    " LEFT JOIN " + RetSqlName("SC2") + " SC2 "
    cQuery +=        "  ON SC2.C2_FILIAL = ? "
    cQuery +=        " AND " + PCPQrySC2("SC2", "SH6.H6_OP")
    cQuery +=        " AND SC2.D_E_L_E_T_ = ' ' "  
    cQuery +=    " LEFT JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery +=        "  ON SB1.B1_FILIAL = ? "
    cQuery +=        " AND SB1.B1_COD = SH6.H6_PRODUTO "  
    cQuery +=        " AND SB1.D_E_L_E_T_ = ' ' "  
    cQuery +=    " LEFT JOIN " + RetSqlName("SH1") + " SH1 "
    cQuery +=        " ON SH1.H1_FILIAL = ? "
    cQuery +=        " AND SH1.H1_CODIGO = SH6.H6_RECURSO "
    cQuery +=        " AND SH1.D_E_L_E_T_ = ' ' "  
    cQuery +=    " LEFT JOIN " + RetSqlName("SH4") + " SH4 "
    cQuery +=        " ON SH4.H4_FILIAL = ? "
    cQuery +=        " AND SH4.H4_CODIGO = SH6.H6_FERRAM "
    cQuery +=        " AND SH4.D_E_L_E_T_ = ' ' " 
    cQuery +=    " WHERE SH6.H6_FILIAL = ? "
    cQuery +=        " AND SH6.H6_OP      >= ? "
    cQuery +=        " AND SH6.H6_OP      <= ? "
    cQuery +=        " AND SH6.H6_RECURSO >= ? "
    cQuery +=        " AND SH6.H6_RECURSO <= ? "
    cQuery +=        " AND SH6.H6_DTAPONT >= ? "
    cQuery +=        " AND SH6.H6_DTAPONT <= ? "
    cQuery +=        " AND SH6.H6_OPERADO >= ? "
    cQuery +=        " AND SH6.H6_OPERADO <= ? "
    cQuery +=        " AND SH6.H6_TIPO IN ('P', ' ') " 	  
	cQuery +=        " AND SH6.D_E_L_E_T_ = ' ' "
    cQuery +=      " ORDER BY SH6.H6_FILIAL, SH6.H6_OP, SH6.H6_PRODUTO "

    If "ORACLE" $ cBanco
		//Substitui 'SUBSTRING' por 'SUBSTR'
		cQuery := StrTran(cQuery, 'SUBSTRING', 'SUBSTR')
	EndIf

    //Montagem da query para execução
    oExec := FwExecStatement():New(cQuery)  

    oExec:SetUnSafe(1, cFields)
    oExec:SetString(2, xFilial("SC2"))
    oExec:SetString(3, xFilial("SB1"))
    oExec:SetString(4, xFilial("SH1"))
    oExec:SetString(5, xFilial("SH4"))
    oExec:SetString(6, xFilial("SH6"))
    oExec:SetString(7, aParam[1])
    oExec:SetString(8, aParam[2])
    oExec:SetString(9, aParam[3])
    oExec:SetString(10, aParam[4])
    oExec:SetDate(11, aParam[5])
    oExec:SetDate(12, aParam[6])
    oExec:SetString(13, aParam[7])
    oExec:SetString(14, aParam[8])

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->H6_FILIAL, 1)
    EndIf
    
    While (cAlias)->(!Eof())

        If !Empty((cAlias)->C2_ROTEIRO)
			cRoteiro := (cAlias)->C2_ROTEIRO
		Else
			cRoteiro := (cAlias)->B1_OPERPAD
		EndIf

        If Empty(cRoteiro)
			cRoteiro := StrZero(1, Len(SG2->G2_CODIGO))
		Endif

        dbSelectArea("SG2")    
		a630SeekSG2(1,(cAlias)->H6_PRODUTO,xFilial("SG2")+(cAlias)->(H6_PRODUTO+cRoteiro+H6_OPERAC))

        cHrPrev := HRPREVCALC(cAlias)
        cHrReal	:= TimeH6(NIL, NIL, cAlias)
        If cTipoTemp == "C"
            cHrReal := StrTran(cHrReal, ":", ".")
        EndIf

        //Valida se é ultima operação
	    lUlt		:= 'N' //Ultima operação
	    aAreaSH6	:=  SH6->(GetArea())
    
	    DBSELECTAREA("SH6")
	    SH6->(DBSETORDER(1))
	    SH6->(DBSEEK(XFILIAL("SH6")+(cAlias)->H6_OP+(cAlias)->H6_PRODUTO+(cAlias)->H6_OPERAC+(cAlias)->H6_SEQ+(cAlias)->H6_DATAINI+(cAlias)->H6_HORAINI+(cAlias)->H6_DATAFIN+(cAlias)->H6_HORAFIN ))   
        IF A680UltOper(.F.) //função que valida se o registro pertence a ultima operação. 
	    	lUlt := 'S'         
	    ENDIF   

		RestArea(aAreaSH6)

        ojItems := JsonObject():new()

        for nX := 1 To Len(self:aStruct)
            //Campos auxiliares
            if self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            elseif self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            ELSEIF self:aStruct[nX][1] $ 'G2DESCRI'
                ojItems[self:aStruct[nX][1]] := SG2->G2_DESCRI
            elseif self:aStruct[nX][1] $ 'HRREAL'
                ojItems[self:aStruct[nX][1]] := cHrReal
            elseif self:aStruct[nX][1] $ 'HRPREV'
                ojItems[self:aStruct[nX][1]] := cHrPrev  
            elseif self:aStruct[nX][1] $ 'ULT'
                ojItems[self:aStruct[nX][1]] := lUlt       
            Else 
                //Campos do select principal
                if self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                else
                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5]) 
                endif 
            EndIf
        next nX
    
        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())

    enddo 
  
    //Se não for o Último registro indica que terá próxima página
    self:setHasNext( lHasNext )

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
	FreeObj(oExec)
    
return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 24/05/2023
@version 1.0
/*/
method getSchema() as object class ComparativoTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do Select Principal
    self:aFields := {"H6_FILIAL","H6_OP","H6_PRODUTO","H6_OPERAC","H6_RECURSO","H6_FERRAM",; 
                     "H6_OPERADO","H6_QTDPROD","H6_QTDPERD","H6_DTAPONT","H6_TIPO","H6_TEMPO",; 
                     "H6_TIPOTEM","H6_SEQ","H6_DATAINI","H6_HORAINI","H6_DATAFIN","H6_HORAFIN",;
                     "C2_FILIAL","C2_NUM","C2_ITEM","C2_SEQUEN","C2_ITEMGRD","C2_QUANT","C2_ROTEIRO","C2_QUJE",;
                     "B1_FILIAL","B1_COD","B1_OPERPAD",;
                     "H1_FILIAL","H1_CODIGO","H1_DESCRI","H1_MAOOBRA",; 
                     "H4_FILIAL","H4_CODIGO","H4_DESCRI"}
                     
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCEMP" , "DESC_EMP" , "string", "DESC_EMP" , "DESC_EMP" })
    AAdd(self:aStruct , {"DESCFIL" , "DESC_FIL" , "string", "DESC_FIL" , "DESC_FIL" })
    AAdd(self:aStruct , {"HRREAL"  , "HR_REAL"  , "string", "HR_REAL"  , "HR_REAL"  })
    AAdd(self:aStruct , {"HRPREV"  , "HR_PREV"  , "string", "HR_PREV"  , "HR_PREV"  })
    AAdd(self:aStruct , {"ULT"     , "ULT"      , "string", "ULT"      , "ULT"      })
    AAdd(self:aStruct , {"G2DESCRI", "G2_DESCRI", "string", "G2_DESCRI", "G2_DESCRI"})

    for nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    next nX
    
return self:oSchema

Static Function HRPREVCALC(cAlias)
    Local aTotal  := {}                                       as array
    Local cTime   := ""                                       as character
    Local nLotPad := If(SG2->G2_LOTEPAD==0,1,SG2->G2_LOTEPAD) as numeric
    Local nQuant  := 0                                        as numeric
    Local nTemPad := If(SG2->G2_TEMPAD ==0,1,SG2->G2_TEMPAD)  as numeric
    Local nTempo  := 0                                        as numeric

    nQuant  := (cAlias)->(H6_QTDPROD+H6_QTDPERD)

    //|---------------------------------------------------------------------|
    //|  Se MV_TPHR for N (Normal) devo converter G2_TEMPAD para            |
    //|  centesimal para permitir multiplicar pela quantidade produzida     |
    //|---------------------------------------------------------------------|
    If cTipoTemp == "N"
        nTemPad := Int(nTemPad) + (Mod(nTemPad, 1) / 0.6)
    Endif

    // Calcula Tempo de Duração baseado no Tipo de Operacao
    If SG2->G2_TPOPER $ " 1"
        nTempo := nQuant * nTemPad / nLotPad
        dbSelectArea("SH1")
        dbSeek(xFilial("SH1")+(cAlias)->H6_RECURSO)
        If Found() .And. SH1->H1_MAOOBRA # 0
            nTempo :=Round( nTempo / H1_MAOOBRA,5)
        EndIf
        dbSelectArea(cAlias)
    ElseIf SG2->G2_TPOPER == "4"
        nQuantAloc:= nQuant % nLotPad
        nQuantAloc:= Int(nQuant)+If(nQuantAloc>0,nLotPad-nQuantAloc,0)
        nTempo := Round(nQuantAloc * ( nTemPad / nLotPad ),5)
        dbSelectArea("SH1")
        dbSeek(xFilial("SH1")+(cAlias)->H6_RECURSO)
        If Found() .And. SH1->H1_MAOOBRA # 0
            nTempo :=Round( nTempo / H1_MAOOBRA,5)
        EndIf
        dbSelectArea(cAlias)
    ElseIf SG2->G2_TPOPER == "2"
        aTotal := TRFH6Total((cAlias)->C2_NUM+(cAlias)->C2_ITEM+(cAlias)->C2_SEQUEN,(cAlias)->H6_OPERAC)
        If nQuant == 0
            nTemPad := (nTemPad/(aTotal[3] + aTotal[2])) * aTotal[3]
            nTempo := (nTemPad/aTotal[3])* 1
        else
            nTemPad := (nTemPad/(aTotal[2] + aTotal[3]))*aTotal[2]
            nTempo := (nTemPad/aTotal[1])* nQuant
        EndIf

    ElseIf SG2->G2_TPOPER == "3"
        nTempo := nTemPad
    EndIf

    If cTipoTemp == "N"
        cTime := StrZero(Int(nTempo), 3) + ":" + StrZero(Mod(nTempo, 1) * 100, 2)
        cTime := A680ConvHora(cTime, "C", "N")
    else
        cTime := StrZero(Int(nTempo), 3) + "." + StrZero(Mod(nTempo, 1) * 100, 2)
    Endif

Return cTime

/*/{Protheus.doc} FH6Total
Retonar as quantidades de apontamentos
@type  Static Function
@author ana.paula
@since 12/07/2023
@param 01 cOp, character, numero da OP
@param 02 cOperac, character, Operacao
@return array, array, retorna a quantidade de producao, quantidade de apontamentos e a quantidade apontada sem producao.
/*/
Static Function TRFH6Total(cOp,cOperac)
    Local nQtdApont := 0 as numeric
    Local nQtdCount := 0 as numeric
    Local nQtdTotal := 0 as numeric

	cAliasSH6 := GetNextAlias()
	//QUANTIDADE TOTAL DE APONTAMENTOS
	BeginSQL alias cAliasSH6

		SELECT
			SUM(SH6.H6_QTDPROD + SH6.H6_QTDPERD) TOTAL, COUNT(SH6.H6_QTDPROD + SH6.H6_QTDPERD) COUNTTOTAL
		FROM
			%table:SH6% SH6
		WHERE
			SH6.%notDel% AND
			SH6.H6_FILIAL = %xfilial:SH6% AND
			(SH6.H6_QTDPROD != '0' OR SH6.H6_QTDPERD != '0') AND
			SH6.H6_OP = %exp:cOp% AND
			SH6.H6_OPERAC = %exp:cOperac%

	EndSQL

	nQtdTotal := (cAliasSH6)->(TOTAL)
	nQtdCount := (cAliasSH6)->(COUNTTOTAL)
	(cAliasSH6)->(dbCloseArea())

	cAliasSH6 := GetNextAlias()
	//QUANTIDADE TOTAL DE APONTAMENTOS
	BeginSQL alias cAliasSH6

		SELECT
			COUNT(SH6.H6_QTDPROD) TOTAL
		FROM
			%table:SH6% SH6
		WHERE
			SH6.%notDel% AND
			SH6.H6_FILIAL = %xfilial:SH6% AND
			SH6.H6_QTDPROD = '0' AND
			SH6.H6_QTDPERD = '0' AND
			SH6.H6_OP = %exp:cOp% AND
			SH6.H6_OPERAC = %exp:cOperac%
	EndSQL

	nQtdApont := (cAliasSH6)->(TOTAL)
	(cAliasSH6)->(dbCloseArea())

Return {nQtdTotal, nQtdCount, nQtdApont}
