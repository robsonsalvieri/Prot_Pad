#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.demanda.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SVR,SVB,SB1,NNR", name="Demandas do MRP", country="ALL", initialRelease="12.1.2210",customTables="SVR,SVB,SB1,NNR")

/*/{Protheus.doc} DemandaTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem das demandas do MRP

@author breno.ferreira
@since 14/07/2023
@version 1.0
/*/
class DemandaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author breno.ferreira
@since 14/07/2023
@version 1.0
/*/ 
method new() as object class DemandaTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Demandas"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "Demandas do MRP"

    self:setPergunte("TRDEM") //Indica o pergunte que será utilizado no relatório
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author breno.ferreira
@since 14/07/2023
@version 1.0
/*/
method getData(nPage as numeric, oFilter as object) as object class DemandaTReportsBusinessObject
    Local aParam    := Array(15) as array
    Local aTables   := {}        as Array
    Local aTipo     := {}        as array
    Local cAlias    := ""        as character
    Local cDescEmp  := ""        as character
    Local cDescFil  := ""        as character
    Local cFields   := ""        as character
    Local cQuery    := ""        as character
    Local jParams   := Nil       as json
    Local nPosParam := 0         as numeric
    Local nX        := 0         as numeric
    Local oExec     := Nil       as object
    Local ojItems   := Nil       as object

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros 

    aParam[1]  := pcpValidParam(jParams["MV_PAR01"][1]) //Demanda de ?
    aParam[2]  := pcpValidParam(jParams["MV_PAR02"][1]) //Demanda até ?
    aParam[3]  := pcpValidParam(jParams["MV_PAR03"][1]) //Produto de ?
    aParam[4]  := pcpValidParam(jParams["MV_PAR04"][1]) //Produto até ?
    aParam[5]  := pcpconvdat(jParams["MV_PAR05"][1],1) //Data de ?
    aParam[6]  := pcpconvdat(jParams["MV_PAR06"][1],1) //Data até ?
    aParam[7]  := pcpValidParam(jParams["MV_PAR07"][1]) //Local de ?
    aParam[8]  := pcpValidParam(jParams["MV_PAR08"][1]) //Local até ? 
    aParam[9]  := jParams["MV_PAR09"][1] //Integra MRP ?
    aParam[10] := jParams["MV_PAR10"][1] //Tipo ?
    aParam[11] := jParams["MV_PAR11"][1] //Previsão de Vendas ?  
    aParam[12] := jParams["MV_PAR12"][1] //Plano Mestre ?
    aParam[13] := jParams["MV_PAR13"][1] //Empenhos de Projeto ?
    aParam[14] := jParams["MV_PAR14"][1] //Importação CSV ?
    aParam[15] := jParams["MV_PAR15"][1] //Manual ?

    If aParam[10] == 1
        Aadd(aTipo, '1')// 'Pedido de Venda'
    EndIf
    If aParam[11] == 1
        Aadd(aTipo, '2') //'Previsão de Vendas'
    EndIf
    If aParam[12] == 1
        Aadd(aTipo, '3') //'Plano Mestre'
    EndIf
    If aParam[13] == 1
        Aadd(aTipo, '4') //'Empenhos de Projeto'
    EndIf       
    If aParam[14] == 1
        Aadd(aTipo, '5') //'Importação CSV'
    EndIf        
    If aParam[15] == 1
        Aadd(aTipo, '9') //'Manual'
    EndIf

    cFields := ArrTokStr(self:aFields,",")
    
    cQuery := "SELECT ? " 

    aadd(aTables,{'VB_','SVB.'})
    aadd(aTables,{'VR_','SVR.'})
    aadd(aTables,{'B1_','SB1.'})
    aadd(aTables,{'NNR_','NNR.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,,aTables)
    aTables := {} 

	cQuery +=    " FROM " + RetSqlName("SVB") + " SVB "
    cQuery +=     " INNER JOIN " + RetSqlName("SVR") + " SVR "
    cQuery +=        " ON SVR.VR_FILIAL = ? "
    cQuery +=       " AND SVR.VR_CODIGO = SVB.VB_CODIGO "
    cQuery +=       " AND SVR.D_E_L_E_T_ = ' ' "
    cQuery +=     " INNER JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery +=        " ON SB1.B1_FILIAL = ? "
    cQuery +=       " AND SB1.B1_COD = SVR.VR_PROD "
    cQuery +=       " AND SB1.D_E_L_E_T_ = ' ' "
    cQuery +=      " LEFT JOIN " + RetSqlName("NNR") + " NNR "
    cQuery +=        " ON NNR.NNR_FILIAL = ? "
    cQuery +=       " AND NNR.NNR_CODIGO = SVR.VR_LOCAL "
    cQuery +=       " AND NNR.D_E_L_E_T_ = ' ' "
    cQuery +=     " WHERE SVB.VB_FILIAL  = ? "
    cQuery +=       " AND SVB.VB_CODIGO >= ? "
    cQuery +=       " AND SVB.VB_CODIGO <= ? "
    cQuery +=       " AND SVR.VR_PROD   >= ? "
    cQuery +=       " AND SVR.VR_PROD   <= ? "
    cQuery +=       " AND SVR.VR_DATA   >= ? "
    cQuery +=       " AND SVR.VR_DATA   <= ? "
    cQuery +=       " AND SVR.VR_LOCAL  >= ? "
    cQuery +=       " AND SVR.VR_LOCAL  <= ? "
    If aParam[9] == 1 .Or. aParam[9] == 2 .Or. aParam[9] == 3
        cQuery +=       " AND SVR.VR_INTMRP = "
        If aParam[9] == 1
            cQuery +=   "'1'" //'Integrado'
        ElseIf aParam[9] == 2
            cQuery +=   "'2'" //'Pendente'
        ElseIf aParam[9] == 3
            cQuery +=   "'3'" //'Não Integrado'
        EndIf
    EndIf
    If Len(aTipo) != 0
        cQuery +=       " AND SVR.VR_TIPO IN (?) "
    EndIf       
	cQuery +=       " AND SVB.D_E_L_E_T_ = ' ' "
    cQuery +=     " ORDER BY SVB.VB_FILIAL, SVR.VR_DATA, SVB.VB_CODIGO, SVR.VR_PROD " 

    nPosParam := 1

    //Montagem da query para execução
    oExec := FwExecStatement():New(cQuery)

    oExec:SetUnSafe(nPosParam++, cFields)
    oExec:SetString(nPosParam++, xFilial("SVR"))
    oExec:SetString(nPosParam++, xFilial("SB1"))
    oExec:SetString(nPosParam++, xFilial("NNR"))
    oExec:SetString(nPosParam++, xFilial("SVB"))
    oExec:SetString(nPosParam++, aParam[1])
    oExec:SetString(nPosParam++, aParam[2])
    oExec:SetString(nPosParam++, aParam[3])
    oExec:SetString(nPosParam++, aParam[4])
    oExec:SetDate(nPosParam++, aParam[5])
    oExec:SetDate(nPosParam++, aParam[6])
    oExec:SetString(nPosParam++, aParam[7])
    oExec:SetString(nPosParam++, aParam[8])
    If Len(aTipo) != 0
        oExec:SetIn(nPosParam++, aTipo)
    EndIf

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->VB_FILIAL, 1)
    EndIf
    
    While (cAlias)->(!Eof())

        //Mudança dos numerais para as determinadas "frases" do VR_TIPO
        If (cAlias)-> VR_TIPO $ '1' 
            cDescTipo := STR0004 //STR0004 - 'Pedido de Venda'
        ElseIf (cAlias)-> VR_TIPO $ '2'
            cDescTipo := STR0005 //STR0005 - 'Previsão de Vendas'    
        ElseIf (cAlias)-> VR_TIPO $ '3'
            cDescTipo := STR0006 //STR0006 - 'Plano Mestre'
        ElseIf (cAlias)-> VR_TIPO $ '4'
            cDescTipo := STR0007 //STR0007 - 'Empenhos Projeto'    
        ElseIf (cAlias)-> VR_TIPO $ '5'
            cDescTipo := STR0008 //STR0008 - 'Importação CSV'
        ElseIf (cAlias)-> VR_TIPO $ '9'
            cDescTipo := STR0009 //STR0009 - 'Manual'    
        EndIf    
        //---------------------------------------------------------------//
        //Mudança dos numerais para as determinadas "frases" do VR_INTMRP
        If (cAlias)-> VR_INTMRP $ '1'
            cDescIntmrp := STR0010 //STR0010 - 'Integrado'
        ElseIf (cAlias)-> VR_INTMRP $ '2'
            cDescIntmrp := STR0011 //STR0011 - 'Pendente'
        ElseIf (cAlias)-> VR_INTMRP $ '3'
            cDescIntmrp := STR0012 //STR0012 - 'Não Integrado'    
        EndIf    

        ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            //Campos auxiliares
            If self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            ElseIf self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil 
            ElseIf self:aStruct[nX][1] == 'VRTIPO' //Tratamento para imprimir o STR do campo VR_TIPO
                ojItems[self:aStruct[nX][1]] := cDescTipo 
            ElseIf self:aStruct[nX][1] == 'VRINTMRP' //Tratamento para imprimir o STR do campo VR_INTMRP
                ojItems[self:aStruct[nX][1]] := cDescIntmrp       
            Else
                //Campos do select principal
                If self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                Else             
                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
                EndIf 
            EndIf
        Next nX

        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())

    EndDo 

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)
    
return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author breno.ferreira
@since 14/07/2023
@version 1.0
/*/
method getSchema() as object class DemandaTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do Select Principal
    self:aFields := {"VB_FILIAL","VB_CODIGO","VB_DTINI","VB_DTFIM","VR_DATA","VR_TIPO","VR_PROD","VR_QUANT",;
                     "VR_LOCAL","VR_DOC","VR_OPC","VR_NRMRP","VR_INTMRP","B1_DESC","NNR_DESCRI"}
                     
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"DESCEMP", "DESC_EMP", "string", "DESC_EMP", "DESC_EMP"})
    AAdd(self:aStruct , {"DESCFIL", "DESC_FIL", "string", "DESC_FIL", "DESC_FIL"})

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:setCustomURL("MV_PAR01", "/api/pcp/smartview/v1/demanda", 2)
    self:setCustomURL("MV_PAR02", "/api/pcp/smartview/v1/demanda", 2)

return self:oSchema
