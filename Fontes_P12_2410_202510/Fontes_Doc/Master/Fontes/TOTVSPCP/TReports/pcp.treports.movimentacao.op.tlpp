#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "pcp.treports.movimentacao.op.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCP", tables="SD3,SB1", name="Movimentos por Ordem de Produção", country="ALL", initialRelease="12.1.2210", customTables="SD3,SB1")

/*/{Protheus.doc} MovimentacaoOPTReportsBusinessObject
Classe para criação do Objeto de Negócio para listagem das movimentações por OP

@author ana.paula
@since 05/06/2023
@version 1.0
/*/
class MovimentacaoOPTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data aStruct as array
endclass

/*/{Protheus.doc} new
Método de instância da classe
 
@return object: self

@author ana.paula
@since 05/06/2023
@version 1.0
/*/ 
method new() as object class MovimentacaoOPTReportsBusinessObject
    _Super:new()
    //Define a Área
    self:appendArea(STR0001)//STR0001 = "PCP"   
 
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002)//STR0002 - "Relacao por Ordem de Producao"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003)//STR0003 - "O objetivo deste relatório é exibir detalhadamente todas as movimenta-"
                                          //"ções feitas para cada Ordem de Produçäo ,mostrando inclusive os custos."

    self:setPergunte("MTR860") //Indica o pergunte que será utilizado no relatório
return self

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData

@author ana.paula
@since 05/06/2023
@version 1.0
/*/
Method getData(nPage as numeric, oFilter as object) as object class MovimentacaoOPTReportsBusinessObject
    Local aParam     := Array(7)                                               as array
    Local aTables    := {}                                                     as Array
    Local cAlias     := ""                                                     as character
    Local cBanco     := TCGetDB()                                              as character
    Local cDescCusto := ""                                                     as character
    Local cDescEmp   := ""                                                     as character
    Local cDescFil   := ""                                                     as character
    Local cFields    := ""                                                     as character
    Local cQuery     := ""                                                     as character
    Local cSimbolo   := ""                                                     as character
    Local dDataFec   := GETMV("MV_ULMES")                                      as date
    Local jParams    := Nil                                                    as json
    Local lCusRep    := (SuperGetMv("MV_CUSREP",.F.,.F.) .And. (MA330AvRep())) as logical
    Local nCampoCus  := 0                                                      as numeric
    Local nCusUnit   := 0                                                      as numeric
    Local nValFecha  := 0                                                      as numeric
    Local nX         := 0                                                      as numeric
    Local oExec      := Nil                                                    as object
    Local ojItems    := Nil                                                    as json

    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros

    aParam[1] := pcpValidParam(jParams["MV_PAR01"][1]) //Da OP ?
    aParam[2] := pcpValidParam(jParams["MV_PAR02"][1]) //Ate a O.P. ?
    aParam[3] := jParams["MV_PAR03"][1] //Qual a moeda ?
    aParam[4] := pcpconvdat(jParams["MV_PAR04"][1],1) //De  Data Moviment. ?
    aParam[5] := pcpconvdat(jParams["MV_PAR05"][1],1) //Ate Data Moviment. ?
    aParam[6] := jParams["MV_PAR06"][1] //Total.Qtde.por O.P. ?
    aParam[7] := jParams["MV_PAR07"][1] //Qual Custo Imprimir ?

    If aParam[7] == 2 .And. !lCusRep
	    aParam[7] := 1
    EndIf     

    cSimbolo := AllTrim(GetMv("MV_SIMB"+Ltrim(Str(aParam[3]))))
    If lCusRep .And. aParam[7]==2
        cDescCusto := "( " + STR0004 + " )"  //STR0004 -  "( CUSTO REPOSIÇÃO )"
    Else
        cDescCusto := "( " + STR0005 + " )"  //STR0005 -  "( CUSTO MEDIO )"
    EndIf 

    cFields := ArrTokStr(self:aFields,",")                            
    
    cQuery := " SELECT ? "

    aadd(aTables,{'D3_','SD3.'})
    aadd(aTables,{'B1_','SB1.'})
    PcpInCposPerson(@self:aStruct, @cQuery, self:getCustomFields(),.T.,.T.,,aTables)
    aTables := {}

    cQuery +=         ", CASE WHEN SUBSTRING(SB1.B1_COD,1,3) = 'MOD' OR SB1.B1_CCCUSTO <> ' ' THEN 'S' ELSE 'N' END AS ISMOD "
	cQuery +=    " FROM " + RetSqlName("SD3") + " SD3 "
    cQuery +=   " INNER JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery +=      " ON SB1.B1_FILIAL   = ? "
    cQuery +=     " AND SB1.B1_COD      = SD3.D3_COD "
    cQuery +=     " AND SB1.D_E_L_E_T_  = ' ' "    
    cQuery +=   " WHERE SD3.D3_FILIAL  = ? "
    cQuery +=     " AND SD3.D3_OP      >= ? "
    cQuery +=     " AND SD3.D3_OP      <= ? "
    cQuery +=     " AND SD3.D3_EMISSAO >= ? "
    cQuery +=     " AND SD3.D3_EMISSAO <= ? "
    cQuery +=     " AND SD3.D3_OP      <> ' ' "
    cQuery +=     " AND SD3.D3_ESTORNO <> 'S' "
    cQuery +=     " AND SD3.D_E_L_E_T_ = ' ' "
    cQuery +=   " ORDER BY SD3.D3_FILIAL ,SD3.D3_OP, SD3.D3_CHAVE, SD3.D3_NUMSEQ, SD3.D3_COD "

    If "ORACLE" $ cBanco
		//Substitui 'SUBSTRING' por 'SUBSTR'
		cQuery := StrTran(cQuery, 'SUBSTRING', 'SUBSTR')
	EndIf

    oExec := FwExecStatement():New(cQuery)  

    oExec:SetUnSafe(1, cFields)
    oExec:setString(2, xFilial("SB1"))
    oExec:setString(3, xFilial("SD3"))
    oExec:SetString(4, aParam[1])
    oExec:SetString(5, aParam[2])
    oExec:SetDate(6, aParam[4])
    oExec:SetDate(7, aParam[5])

    cAlias := oExec:OpenAlias()

    If (cAlias)->(!Eof())
        cDescEmp := FWGrpName()
        cDescFil := FWFilialName(, (cAlias)->D3_FILIAL, 1)
    EndIf
    
    While (cAlias)->(!Eof())

        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
        //³ Define o campo a ser impresso no valor de acordo com a moeda selecionada ³
        //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
        Do Case
        	Case aParam[3] == 1
        		If lCusRep .And. aParam[7] == 2
        			nCampoCus := (cAlias)->D3_CUSRP1
        		Else
        			nCampoCus := (cAlias)->D3_CUSTO1
        		EndIf	
        	Case aParam[3] == 2
        		If lCusRep .And. aParam[7] == 2
        			nCampoCus := (cAlias)->D3_CUSRP2
        		Else
        			nCampoCus := (cAlias)->D3_CUSTO2
        		EndIf	
        	Case aParam[3] == 3
        		If lCusRep .And. aParam[7] == 2
        			nCampoCus := (cAlias)->D3_CUSRP3
        		Else
        			cCampoCus := (cAlias)->D3_CUSTO3
        		EndIf	
        	Case aParam[3] == 4
        		If lCusRep .And. aParam[7] == 2
        			nCampoCus := (cAlias)->D3_CUSRP4
        		Else
        			nCampoCus := (cAlias)->D3_CUSTO4
        		EndIf	
        	Case aParam[3] == 5
        		If lCusRep .And. aParam[7] == 2
        			nCampoCus := (cAlias)->D3_CUSRP5
        		Else
        			nCampoCus := (cAlias)->D3_CUSTO5
        		EndIf	
        EndCase

        nCusUnit := (nCampoCus)/(cAlias)->D3_QUANT

        //Valor final no último fechamento
        If lCusRep .And. aParam[7] == 2
            nValFecha := Posicione("SC2",1,xFilial("SC2")+(cAlias)->D3_OP,"C2_VFIMRP"+Str(aParam[3],1))
	    Else
            nValFecha := Posicione("SC2",1,xFilial("SC2")+(cAlias)->D3_OP,"C2_VFIM"+Str(aParam[3],1))
	    EndIf	

        ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            //Campos auxiliares
            If self:aStruct[nX][1] $ 'DESCEMP'
                ojItems[self:aStruct[nX][1]] := cDescEmp
            ElseIf self:aStruct[nX][1] $ 'DESCFIL'
                ojItems[self:aStruct[nX][1]] := cDescFil
            ElseIf self:aStruct[nX][1] $ 'CAMPOCUS'
                If SubStr((cAlias)->D3_CF,1,2)=="DE" 
                    ojItems[self:aStruct[nX][1]] := -nCampoCus
                Else
                    ojItems[self:aStruct[nX][1]] := nCampoCus
                EndIf
            ElseIf self:aStruct[nX][1] $ 'CUSUNIT'
                ojItems[self:aStruct[nX][1]] := nCusUnit
            ElseIf self:aStruct[nX][1] $ 'DATAFECHA'
                ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(dDataFec)
            ElseIf self:aStruct[nX][1] $ 'VALFECHA'
                ojItems[self:aStruct[nX][1]] := nValFecha
            ElseIf self:aStruct[nX][1] $ 'C2PRODUTO'
                ojItems[self:aStruct[nX][1]] := Posicione("SC2",1,xFilial("SC2")+(cAlias)->D3_OP,"C2_PRODUTO")
            ElseIf self:aStruct[nX][1] $ 'SIMBOLO'
                ojItems[self:aStruct[nX][1]] := cSimbolo
            ElseIf self:aStruct[nX][1] $ 'DESCCUS'
                ojItems[self:aStruct[nX][1]] := cDescCusto
            Else 
                //Campos do select principal
                If self:aStruct[nX][3] == "date" //Converter data para o formato padrão - 2023-01-27T15:31:43Z
                    ojItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aStruct[nX][5]))
                ElseIf self:aStruct[nX][1] == "D3QUANT" //Fórmula para o campo D3_QUANT
                    If (cAlias)->ISMOD == 'S'
                        If SubStr((cAlias)->D3_CF,1,2)=="DE" 
                            ojItems[self:aStruct[nX][1]] := -R860ToDec((cAlias)->&(self:aStruct[nX][5]))
                        Else
                            ojItems[self:aStruct[nX][1]] := R860ToDec((cAlias)->&(self:aStruct[nX][5]))
                        EndIf
                    Else
                        If SubStr((cAlias)->D3_CF,1,2)=="DE" 
                            ojItems[self:aStruct[nX][1]] := -(cAlias)->&(self:aStruct[nX][5])
                        Else
                            ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
                        EndIf
                    EndIf    
                ElseIf self:aStruct[nX][1] == "B1CUSTD"
                    ojItems[self:aStruct[nX][1]] := RetFldProd((cAlias)->B1_COD,"B1_CUSTD",cAlias)                               
                Else  
                    ojItems[self:aStruct[nX][1]] := (cAlias)->&(self:aStruct[nX][5])
                EndIf
            EndIf
        Next nX

        self:ProcessData()
        self:oData:appendData(ojItems)

        (cAlias)->(DBSkip())

    EndDo 

    (cAlias)->(DBCloseArea())
    oExec:Destroy()
    FreeObj(oExec)

Return self:oData

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema

@author ana.paula
@since 05/06/2023
@version 1.0
/*/
Method getSchema() as object class MovimentacaoOPTReportsBusinessObject
    Local nX        := 0 as numeric

    //Campos do Select Principal
    self:aFields := {"D3_FILIAL","D3_OP","D3_CHAVE","D3_NUMSEQ","D3_COD","D3_CC","D3_CF","D3_QUANT","D3_UM",;
                     "D3_CUSRP1","D3_CUSTO1",;
                     "D3_CUSRP2","D3_CUSTO2",;
                     "D3_CUSRP3","D3_CUSTO3",;
                     "D3_CUSRP4","D3_CUSTO4",;
                     "D3_CUSRP5","D3_CUSTO5",;
                     "D3_DOC","D3_EMISSAO",;
                     "B1_COD","B1_DESC","B1_CUSTD"}
                     
    self:aStruct := PcpTrGetStruct(self:aFields)

    //Campos Auxiliares
    AAdd(self:aStruct , {"C2PRODUTO", "C2_PRODUTO", "string", "C2_PRODUTO", "C2_PRODUTO"})
    AAdd(self:aStruct , {"DESCEMP"  , "DESC_EMP"  , "string", "DESC_EMP"  , "DESC_EMP"  })
    AAdd(self:aStruct , {"DESCFIL"  , "DESC_FIL"  , "string", "DESC_FIL"  , "DESC_FIL"  })
    AAdd(self:aStruct , {"ISMOD"    , "ISMOD"     , "string", "ISMOD"     , "ISMOD"     })
    AAdd(self:aStruct , {"CAMPOCUS" , "CAMPO_CUS" , "number", "CAMPO_CUS" , "CAMPO_CUS" })
    AAdd(self:aStruct , {"CUSUNIT"  , "CUS_UNIT"  , "number", "CUS_UNIT"  , "CUS_UNIT"  })
    AAdd(self:aStruct , {"DATAFECHA", "DATAFECHA" , "date"  , "DATAFECHA" , "DATAFECHA" })
    AAdd(self:aStruct , {"VALFECHA" , "VALFECHA"  , "number", "VALFECHA"  , "VALFECHA"  })
    AAdd(self:aStruct , {"SIMBOLO"  , "SIMBOLO"   , "string", "SIMBOLO"   , "SIMBOLO"   })
    AAdd(self:aStruct , {"DESCCUS"  , "DESC_CUS"  , "string", "DESC_CUS"  , "DESC_CUS"  })

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

Return self:oSchema
