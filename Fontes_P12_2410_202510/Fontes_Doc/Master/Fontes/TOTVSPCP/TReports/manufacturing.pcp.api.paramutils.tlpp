#Include "TLPP-CORE.TH"
#Include "TLPP-REST.TH"
#Include "TOTVS.CH"
#Include "manufacturing.pcp.api.paramutils.ch"

namespace totvs.protheus.sigapcp.treportsintegratedprovider

/*/{Protheus.doc} PCPSmartViewParamUtils
    Definição dos endpoints para parâmetros de tipo LookUp de objetos de negócio do Smart View.
    @type Class
    @version 12.1.2310
    @author ana.paula
    @since 27/11/2023
/*/
Class PCPSmartViewParamUtils
    // Declaração dos métodos públicos
    Public Method new() As Object

    @Get("/api/pcp/smartview/v1/ticketsMrp")
    Public Method ticketsMrp() As Variant

    @Get("/api/pcp/smartview/v1/demanda")
    Public Method demanda() As Variant

    @Get("/api/pcp/smartview/v1/rastreaDocumento")
    Public Method rastreaDocumento() As Variant

    @Get("/api/pcp/smartview/v1/rastreaDemanda")
    Public Method rastreaDemanda() As Variant

    @Get("/api/pcp/smartview/v1/alocProgramacao")
    Public Method alocProgramacao() As Variant

    @Get("/api/pcp/smartview/v1/alocRecurso")
    Public Method alocRecurso() As Variant

    @Get("/api/pcp/smartview/v1/alocCtrab")
    Public Method alocCtrab() As Variant

    @Get("/api/pcp/smartview/v1/getOrdens")
    Public Method getOrdens() As Variant

    @Get("/api/pcp/smartview/v1/getSoliComp")
    Public Method getSoliComp() As Variant

    @Get("/api/pcp/smartview/v1/getAutEnt")
    Public Method getAutEnt() As Variant

    @Get("/api/pcp/smartview/v1/getCotacao")
    Public Method getCotacao() As Variant

    @Get("/api/pcp/smartview/v1/getProdutoPai")
    Public Method getProdPai() As Variant

EndClass

/*/{Protheus.doc} PCPSmartViewParamUtils::new
    Método construtor da classe.
    @type Method
    @version 12.1.2310
    @author ana.paula
    @since 28/11/2023
    @return Object, Instância da classe
/*/
Method new() Class PCPSmartViewParamUtils As Object
Return self

/*/{Protheus.doc} PCPSmartViewParamUtils::ticketsMrp
    Retorna os tickets do MRP.
    @type Method
    @version 12.1.2310
    @author ana.paula
    @since 28/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method ticketsMrp() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(HW3_TICKET) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "HW3_TICKET"
    jResponse["descriptor"]       := {;
        "HW3_TICKET":  EncodeUTF8(STR0001),; // "Ticket"
        "HW3_DTINIC":  EncodeUTF8(STR0002),; // "Data Início"
        "HW3_DTFIM":   EncodeUTF8(STR0003);  // "Data Fim"
    }

    // Busca os tickets na HW3
    BEGINSQL ALIAS cAlias
        SELECT
            HW3_TICKET,
            HW3_DTINIC,
            HW3_DTFIM
        FROM
            %TABLE:HW3%
        WHERE
            %NOTDEL%
            AND HW3_STATUS IN ('3','6','7','9')
            AND (%EXP:cSearch%)
    ENDSQL

    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "HW3_TICKET":  EncodeUTF8(AllTrim((cAlias)->HW3_TICKET)),;
            "HW3_DTINIC": EncodeUTF8(AllTrim((cAlias)->HW3_DTINIC)),;
            "HW3_DTFIM": EncodeUTF8(AllTrim((cAlias)->HW3_DTFIM));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::demanda
    Retorna as Demandas do MRP.
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 29/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method demanda() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(VB_CODIGO) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "VB_CODIGO"
    jResponse["descriptor"]       := {;
        "VB_CODIGO": EncodeUTF8(STR0004),; // "Demanda"
        "VB_DTINI" : EncodeUTF8(STR0005),; // "Data Inícial"
        "VB_DTFIM" : EncodeUTF8(STR0006);  // "Data Final"
    }

    // Busca os demanda na SVB
    BEGINSQL ALIAS cAlias
        SELECT
            VB_CODIGO,
            VB_DTINI,
            VB_DTFIM
        FROM
            %TABLE:SVB%
        WHERE
            %NOTDEL%
            AND (%EXP:cSearch%)
    ENDSQL

    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "VB_CODIGO":  EncodeUTF8(AllTrim((cAlias)->VB_CODIGO)),;
            "VB_DTINI" :  EncodeUTF8(AllTrim((cAlias)->VB_DTINI)),;
            "VB_DTFIM" :  EncodeUTF8(AllTrim((cAlias)->VB_DTFIM));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::rastreaDocumento
    Retorna os documentos da rastreabilidade de demandas.
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 29/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method rastreaDocumento() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(MH_DEMDOC) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "MH_DEMDOC"
    jResponse["descriptor"]       := {;
        "MH_DEMDOC":  EncodeUTF8(STR0007); // "Documento"
    }

    // Busca os documentos na SMH
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT
            MH_DEMDOC
        FROM
            %TABLE:SMH%
        WHERE
            %NOTDEL%
            AND (%EXP:cSearch%)
            AND MH_DEMDOC <> ''
    ENDSQL

    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "MH_DEMDOC":  EncodeUTF8(AllTrim((cAlias)->MH_DEMDOC));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)

Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::rastreaDemanda
    Retorna as demandas da rastreabilidade de demandas.
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 30/11/2023
    @return Variant, Retorno nulo pré-fixado
/*/
Method rastreaDemanda() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(MH_DEMANDA) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "MH_DEMANDA"
    jResponse["descriptor"]       := {;
        "MH_DEMANDA":  EncodeUTF8(STR0004); // "Demanda"
    }

    // Busca os demanda na SMH
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT
            MH_DEMANDA
        FROM
            %TABLE:SMH%
        WHERE
            %NOTDEL%
            AND (%EXP:cSearch%)
            AND MH_DEMANDA <> ''
    ENDSQL

    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "MH_DEMANDA":  EncodeUTF8(AllTrim((cAlias)->MH_DEMANDA));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)

Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::alocProgramacao
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 25/04/2024
    @return Variant, Retorno nulo pré-fixado
/*/
Method alocProgramacao() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(T4X_PROG) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "T4X_PROG"
    jResponse["descriptor"]       := {;
        "T4X_PROG":   EncodeUTF8(STR0008),; // "Programação"
        "T4X_STATUS": EncodeUTF8(STR0009),; // "Status"
        "T4X_USER":   EncodeUTF8(STR0010),; // "Usuário"
        "T4Y_VALOR1": EncodeUTF8(STR0005),; // "Data Inicial"
        "T4Y_VALOR2": EncodeUTF8(STR0006);  // "Data Final"
    }

    // Busca os programação na T4X
    BEGINSQL ALIAS cAlias
        SELECT
            T4X.T4X_PROG programacao,
            T4X.T4X_STATUS idStatus,
            T4X.T4X_USER usuario,
            dataInicial.T4Y_VALOR dataInicial,
            dataFinal.T4Y_VALOR dataFinal
        FROM
            %TABLE:T4X% T4X
        INNER JOIN %TABLE:T4Y% dataInicial
           ON dataInicial.T4Y_PROG   = T4X.T4X_PROG
          AND dataInicial.T4Y_PARAM  = 'dataInicial'
          AND dataInicial.%NotDel%
          AND dataInicial.T4Y_FILIAL = %xFilial:T4Y%
        INNER JOIN %TABLE:T4Y% dataFinal
           ON dataFinal.T4Y_PROG   = T4X.T4X_PROG
          AND dataFinal.T4Y_PARAM  = 'dataFinal'
          AND dataFinal.%NotDel%
          AND dataFinal.T4Y_FILIAL = %xFilial:T4Y%
        WHERE
          T4X.%NotDel%
          AND (%EXP:cSearch%)
          AND T4X.T4X_STATUS IN ('2','6','F')
        ORDER BY T4X.T4X_PROG DESC
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "T4X_PROG":  EncodeUTF8(AllTrim((cAlias)->programacao)),;
            "T4X_STATUS":   EncodeUTF8(PCPA152Process():getDescricaoStatus((cAlias)->idStatus)),;
            "T4X_USER":  EncodeUTF8(AllTrim((cAlias)->usuario)),;
            "T4Y_VALOR1":  EncodeUTF8(PCPConvDat((cAlias)->dataInicial, 3)),;
            "T4Y_VALOR2":  EncodeUTF8(PCPConvDat((cAlias)->dataFinal, 3));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf

    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::alocRecurso
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 25/04/2024
    @return Variant, Retorno nulo pré-fixado
/*/
Method alocRecurso() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais'
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(H1_CODIGO) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "H1_CODIGO"
    jResponse["descriptor"]       := {;
        "H1_CODIGO": EncodeUTF8(STR0011),; // "Recurso"
        "H1_DESCRI": EncodeUTF8(STR0012);  // "Descrição"
    }

    // Busca os recurso na SH1
    BEGINSQL ALIAS cAlias
        SELECT
            SH1.H1_CODIGO,
            SH1.H1_DESCRI
        FROM
            %TABLE:SH1% SH1
        WHERE SH1.%NOTDEL%
            AND SH1.H1_CODIGO IN (SELECT DISTINCT SMR.MR_RECURSO
                                    FROM %TABLE:SMR% SMR, %TABLE:T4X% T4X
                                   WHERE SMR.MR_FILIAL = %xFilial:SMR%
                                     AND SMR.MR_PROG   = T4X.T4X_PROG
                                     AND SMR.%NotDel%)
            AND (%EXP:cSearch%)
        ORDER BY SH1.H1_CODIGO
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "H1_CODIGO":  EncodeUTF8((cAlias)->H1_CODIGO),;
            "H1_DESCRI":  EncodeUTF8(RTrim((cAlias)->H1_DESCRI));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::alocCtrab
    @type Method
    @version 12.1.2310
    @author breno.ferreira
    @since 25/04/2024
    @return Variant, Retorno nulo pré-fixado
/*/
Method alocCtrab() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(HB_COD) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "HB_COD"
    jResponse["descriptor"]       := {;
        "HB_COD":  EncodeUTF8(STR0013),; // "Centro de Trabalho"
        "HB_NOME": EncodeUTF8(STR0012);  // "Descrição"
    }

    // Busca os centro de trabalho na SHB
    BEGINSQL ALIAS cAlias
        SELECT
            SHB.HB_COD,
            SHB.HB_NOME
        FROM
            %TABLE:SHB% SHB
        WHERE
            SHB.%NOTDEL%
            AND SHB.HB_FILIAL = %xFilial:SHB%
            AND (%EXP:cSearch%)
        ORDER BY SHB.HB_COD
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "HB_COD":  EncodeUTF8((cAlias)->HB_COD),;
            "HB_NOME":  EncodeUTF8((cAlias)->HB_NOME);
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::getOrdens
Retorna as ordens de produção que em algum momento foram consideradas pelo CRP.
@type Method
@version 12.1.2310
@author breno.ferreira
@since 22/01/2025
@return Variant, Retorno nulo pré-fixado
/*/
Method getOrdens() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local cJoinSC2     As Character // Join da tabela SC2

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    cJoinSC2 := "%" + PCPQrySC2("SC2", "SMF.MF_OP") + "%"

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(MF_OP) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "MF_OP"
    jResponse["descriptor"]       := {;
        "MF_OP":      EncodeUTF8(STR0014),; // "Ordem de Produção"
        "C2_PRODUTO": EncodeUTF8(STR0015),; // "Produto"
        "C2_QUANT":   EncodeUTF8(STR0016),; // "Quantidade"
        "C2_TPOP":    EncodeUTF8(STR0017),; // "Tipo"
        "C2_DATPRI":  EncodeUTF8(STR0002),; // "Data Início"
        "C2_DATPRF":  EncodeUTF8(STR0003);  // "Data Entrega"
    }

    // Busca as Ordens de Produção SMF
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT SMF.MF_OP,
                        SC2.C2_PRODUTO,
                        SC2.C2_QUANT,
                        SC2.C2_TPOP,
                        SC2.C2_DATPRI,
                        SC2.C2_DATPRF
          FROM %TABLE:SMF% SMF
         INNER JOIN %table:SC2% SC2
            ON SC2.C2_FILIAL = %xFilial:SC2%
           AND %exp:cJoinSC2%
           AND SC2.%NOTDEL%
         WHERE SMF.MF_FILIAL = %xFilial:SMF%
           AND (%EXP:cSearch%)
           AND SMF.%NOTDEL%
         ORDER BY SMF.MF_OP
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "MF_OP":  EncodeUTF8((cAlias)->MF_OP),;
            "C2_PRODUTO": EncodeUTF8((cAlias)->C2_PRODUTO),;
            "C2_QUANT": (cAlias)->C2_QUANT,;
            "C2_TPOP": EncodeUTF8(labelTipoOP((cAlias)->C2_TPOP)),;
            "C2_DATPRI": EncodeUTF8(PCPConvDat((cAlias)->C2_DATPRI, 4)),;
            "C2_DATPRF": EncodeUTF8(PCPConvDat((cAlias)->C2_DATPRF, 4));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::getSoliComp
Retorna as solicitações de compra das ordens de produção que em algum momento foram consideradas pelo CRP.
@type Method
@version 12.1.2310
@author breno.ferreira
@since 22/01/2025
@return Variant, Retorno nulo pré-fixado
/*/
Method getSoliComp() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local cDocumento   As Character // Documento da solicitação de compra

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := " UPPER(SC1.C1_NUM || SC1.C1_ITEM || SC1.C1_ITEMGRD) LIKE '%" + Upper(jQueryParams["q"]) + "%' "
            If "MSSQL" $ TcGetDB()
                cSearch := StrTran(cSearch, '||', '+')
            EndIf
            cSearch := "%" + cSearch + "%"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "documento"
    jResponse["descriptor"]       := {;
        "documento":  EncodeUTF8(STR0019),; // "Solicitação de Compra"
        "C1_PRODUTO": EncodeUTF8(STR0015),; // "Produto"
        "C1_QUANT":   EncodeUTF8(STR0016),; // "Quantidade"
        "C1_DINICOM": EncodeUTF8(STR0002),; // "Data Início"
        "C1_DATPRF":  EncodeUTF8(STR0018);  // "Data Entrega"
    }

    // Busca as Solicitações de compra SC1
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT SC1.C1_NUM,
                        SC1.C1_ITEM,
                        SC1.C1_ITEMGRD,
                        SC1.C1_PRODUTO,
                        SC1.C1_QUANT,
                        SC1.C1_DINICOM,
                        SC1.C1_DATPRF
          FROM %table:SC1% SC1
         INNER JOIN %table:SMF% SMF
            ON SMF.MF_FILIAL = %xFilial:SMF%
           AND SMF.MF_OP = SC1.C1_OP
           AND SC1.%notDel%
         WHERE SC1.C1_FILIAL = %xFilial:SC1%
           AND (%EXP:cSearch%)
           AND SC1.%NOTDEL%
         ORDER BY SC1.C1_NUM, SC1.C1_ITEM, SC1.C1_ITEMGRD
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        cDocumento := RTrim((cAlias)->C1_NUM) + RTrim((cAlias)->C1_ITEM) + RTrim((cAlias)->C1_ITEMGRD)

        jItem := {;
            "documento": EncodeUTF8(cDocumento),;
            "C1_PRODUTO": (cAlias)->C1_PRODUTO,;
            "C1_QUANT": (cAlias)->C1_QUANT,;
            "C1_DINICOM": EncodeUTF8(PCPConvDat((cAlias)->C1_DINICOM, 4)),;
            "C1_DATPRF": EncodeUTF8(PCPConvDat((cAlias)->C1_DATPRF, 4));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::getAutEnt
Retorna as autorizações de entrega das ordens que em algum momento foram consideradas pelo CRP.
@type Method
@version 12.1.2310
@author breno.ferreira
@since 22/01/2025
@return Variant, Retorno nulo pré-fixado
/*/
Method getAutEnt() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local cDocumento   As Character // Documento da autorização de entrada

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := " UPPER(SC7.C7_NUM || SC7.C7_ITEM || SC7.C7_SEQUEN || SC7.C7_ITEMGRD) LIKE '%" + Upper(jQueryParams["q"]) + "%' "
            If "MSSQL" $ TcGetDB()
                cSearch := StrTran(cSearch, '||', '+')
            EndIf
            cSearch := "%" + cSearch + "%"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "documento"
    jResponse["descriptor"]       := {;
        "documento":  EncodeUTF8(STR0020),; // "Autorização de Entrada"
        "C7_PRODUTO": EncodeUTF8(STR0015),; // "Produto"
        "C7_QUANT":   EncodeUTF8(STR0016),; // "Quantidade"
        "C7_DINICOM": EncodeUTF8(STR0002),; // "Data Início"
        "C7_DATPRF":  EncodeUTF8(STR0018);  // "Data Entrega"
    }

    // Busca os Pedidos de Compra SC7
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT SC7.C7_NUM,
                        SC7.C7_ITEM,
                        SC7.C7_SEQUEN,
                        SC7.C7_ITEMGRD,
                        SC7.C7_PRODUTO,
                        SC7.C7_QUANT,
                        SC7.C7_DINICOM,
                        SC7.C7_DATPRF
          FROM %table:SC7% SC7
         INNER JOIN %table:SMF% SMF
            ON SMF.MF_FILIAL = %xFilial:SMF%
           AND SMF.MF_OP = SC7.C7_OP
           AND SC7.%notDel%
         WHERE SC7.C7_FILIAL = %xFilial:SC7%
           AND SC7.C7_TIPO = 2
           AND (%exp:cSearch%)
           AND SC7.%notDel%
         ORDER BY  SC7.C7_NUM, SC7.C7_ITEM, SC7.C7_SEQUEN, SC7.C7_ITEMGRD
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        cDocumento := RTrim((cAlias)->C7_NUM) + RTrim((cAlias)->C7_ITEM) + RTrim((cAlias)->C7_SEQUEN) + RTrim((cAlias)->C7_ITEMGRD)

        jItem := {;
            "documento": EncodeUTF8(cDocumento),;
            "C7_PRODUTO": EncodeUTF8((cAlias)->C7_PRODUTO),;
            "C7_QUANT": (cAlias)->C7_QUANT,;
            "C7_DINICOM": EncodeUTF8(PCPConvDat((cAlias)->C7_DINICOM, 4)),;
            "C7_DATPRF": EncodeUTF8(PCPConvDat((cAlias)->C7_DATPRF, 4));
        }

        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} PCPSmartViewParamUtils::getCotacao
Retorna as cotações das solicitações de compra que possuem ordem de produção considerada pelo CRP.
@type Method
@version 12.1.2310
@author breno.ferreira
@since 22/01/2025
@return Variant, Retorno nulo pré-fixado
/*/
Method getCotacao() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação
    Local cSolicitacao As Character // Solicitação de compra

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := " UPPER(SC8.C8_NUM) LIKE '%" + Upper(jQueryParams["q"]) + "%' "
            cSearch := "%" + cSearch + "%"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "C8_NUM"
    jResponse["descriptor"]       := {;
        "C8_NUM":      EncodeUTF8(STR0021),; // "Cotação"
        "solicitacao": EncodeUTF8(STR0019),; // "Solicitação de Compra"
        "C1_PRODUTO":  EncodeUTF8(STR0015),; // "Produto"
        "C1_QUANT":    EncodeUTF8(STR0016),; // "Quantidade"
        "C1_DINICOM":  EncodeUTF8(STR0002),; // "Data Início"
        "C1_DATPRF":   EncodeUTF8(STR0018);  // "Data Entrega"
    }

    // Busca a Cotação SC8
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT SC8.C8_NUM,
                        SC1.C1_NUM,
                        SC1.C1_ITEM,
                        SC1.C1_ITEMGRD,
                        SC1.C1_PRODUTO,
                        SC1.C1_QUANT,
                        SC1.C1_DINICOM,
                        SC1.C1_DATPRF
          FROM %table:SC8% SC8
         INNER JOIN %table:SC1% SC1
            ON SC1.C1_FILIAL = %xFilial:SC1%
           AND SC8.C8_NUM = SC1.C1_COTACAO
           AND SC1.%notDel%
         INNER JOIN %table:SMF% SMF
            ON SMF.MF_FILIAL = %xFilial:SMF%
           AND SMF.MF_OP = SC1.C1_OP
           AND SMF.%notDel%
         WHERE SC8.C8_FILIAL = %xFilial:SC8%
           AND (%EXP:cSearch%)
           AND SC8.%notDel%
         ORDER BY SC8.C8_NUM
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        cSolicitacao := RTrim((cAlias)->C1_NUM) + RTrim((cAlias)->C1_ITEM) + RTrim((cAlias)->C1_ITEMGRD)

        jItem := {;
            "C8_NUM":  EncodeUTF8((cAlias)->C8_NUM),;
            "solicitacao":  EncodeUTF8(cSolicitacao),;
            "C1_PRODUTO":  EncodeUTF8((cAlias)->C1_PRODUTO),;
            "C1_QUANT":  (cAlias)->C1_QUANT,;
            "C1_DINICOM":  EncodeUTF8(PCPConvDat((cAlias)->C1_DINICOM, 4)),;
            "C1_DATPRF":  EncodeUTF8(PCPConvDat((cAlias)->C1_DATPRF, 4));
        }

        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"]      := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL

/*/{Protheus.doc} labelTipoOP
Retorna o label para o tipo da ordem de produção.
@type  Static Function
@author Lucas Fagundes
@since 22/05/2025
@version P12
@param cTipo, Character, Tipo da ordem de produção
@return cLabel, Character, Label do tipo da ordem de produção.
/*/
Static Function labelTipoOP(cTipo as Character) As Character
    Local cLabel := "" As Character

    If cTipo == "F"
        cLabel := STR0022 // "Firme"

    ElseIf cTipo == "P"
        cLabel := STR0023 // "Prevista"

    EndIf

Return cLabel

/*/{Protheus.doc} pcpGetParametroSV
Retorna o valor de um parâmetro do pergunte.
@type  Function
@author Lucas Fagundes
@since 05/06/2025
@version P12
@param jParams     , json     , Json com os parâmetros do pergunte.
@param cMV         , character, Nome do parâmetro a ser retornado.
@param lTrataBranco, logical  , Indica se deve retornar ZZZ se o parâmetro estiver em branco.
@param cCampo      , character, Cammpo utilizado para retornar o tamanho do ZZZ caso o parâmetro esteja em branco.
@return cParametro, character, Valor do parâmetro
/*/
Function pcpGetParametroSV(jParams as json, cMV as character, lTrataBranco as logical, cCampo as character) as character
	Local cParametro as character
	Local nTamanhoCampo as numeric

	cParametro := jParams[cMV][1]

	If Empty(cParametro) .And. lTrataBranco
		nTamanhoCampo := GetSx3Cache(cCampo, "X3_TAMANHO")
		cParametro := Replicate("z", nTamanhoCampo)
	EndIf

Return cParametro

/*/{Protheus.doc} PCPSmartViewParamUtils::getProdPai
Retorna os proutos para a busca do relatório de rastreabilidade de lote;
@type Method
@version 12.1.2410
@author breno.ferreira
@since 07/07/2025
@return Variant, Retorno nulo pré-fixado
/*/
Method getProdPai() As Variant Class PCPSmartViewParamUtils
    // Declaração das variáveis locais'
    Local jQueryParams As JSON      // Query parameters da requisição
    Local jResponse    As JSON      // Resposta da requisição
    Local jItem        As JSON      // Item do valor do parâmetro
    Local cAlias       As Character // Alias do arquivo temporário
    Local cSearch      As Character // Termo de busca
    Local nPage        As Numeric   // Número da página do controle de paginação
    Local nPageSize    As Numeric   // Tamanho da página do controle de paginação
    Local nCount       As Numeric   // Contador de registros para o controle de paginação

    // Inicialização das variáveis
    jQueryParams := oRest:getQueryRequest()
    jResponse    := JSONObject():New()
    jItem        := NIL
    cAlias       := GetNextAlias()
    cSearch      := "%1 = 1%"
    nPage        := 1
    nPageSize    := 20
    nCount       := 1

    // Captura os query parameters da requisição
    If (jQueryParams != NIL)
        nPage := IIf(jQueryParams:hasProperty("page"), Val(jQueryParams["page"]), nPage)

        // Corrige o número da página, em caso de ser menor ou igual a zero
        nPage := IIf(nPage <= 0, 1, nPage)

        // Monta a expressão SQL de filtro conforme o termo de busca
        If (jQueryParams:hasProperty("q"))
            cSearch := "%UPPER(D5_PRODUTO) LIKE UPPER('%" + jQueryParams["q"] + "%') %"
        EndIf
    EndIf

    // Cria os atributos do JSON de resposta
    jResponse["data"]             := {}
    jResponse["nextPageUrl"]      := NIL
    jResponse["keyProperty"]      := "D5_PRODUTO"
    jResponse["descriptor"]       := {;
        "D5_PRODUTO": EncodeUTF8(STR0015),; // "Produto"
        "B1_DESC": EncodeUTF8(STR0012);  // "Descrição"
    }

    // Busca os recurso na SD5
    BEGINSQL ALIAS cAlias
        SELECT DISTINCT
            SD5.D5_PRODUTO,
            SB1.B1_DESC
        FROM
            %TABLE:SD5% SD5
        INNER JOIN %TABLE:SB1% SB1
           ON SB1.B1_FILIAL = %xFilial:SB1%
          AND SB1.B1_COD = SD5.D5_PRODUTO
          AND SB1.%notDel%
        WHERE SD5.D5_FILIAL = %xFilial:SD5%
          AND (%EXP:cSearch%)
          AND SD5.%notDel%
        ORDER BY SD5.D5_PRODUTO
    ENDSQL

    // Conta a quantidade total de registros da query
    (cAlias)->(DBGoTop())

    // Pula os registros da(s) página(s) anterior(es)
    If (nPage > 1)
        (cAlias)->(DBSkip((nPage - 1) * nPageSize))
    EndIf

    // Percorre os itens adicionando-os no JSON de resposta
    While (!(cAlias)->(EOF()) .and. nCount <= nPageSize)
        jItem := {;
            "D5_PRODUTO":  EncodeUTF8((cAlias)->D5_PRODUTO),;
            "B1_DESC":  EncodeUTF8(RTrim((cAlias)->B1_DESC));
        }
        AAdd(jResponse["data"], jItem)

        // Pula para o próximo registro e soma um no contador
        (cAlias)->(DBSkip())
        nCount++
    End

    // Define a quantidade de registros restantes e a URL da próxima página
    If nCount > nPageSize .and. !(cAlias)->(EOF())
        jResponse["nextPageUrl"] := oRest:getFullURLRequest() + "?page=" + CValToChar(nPage + 1) + IIf(jQueryParams:hasProperty("q"), "&q=" + jQueryParams["q"], "")
    EndIf
    // Define a resposta da requisição
    oRest:setResponse(jResponse)
    oRest:setStatusCode(200)
    oRest:setKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
    oRest:setKeyHeaderResponse("Accept-Encoding", "UTF-8")

    // Fecha o arquivo temporário
    (cAlias)->(DBCloseArea())

    // Libera os objetos da memória
    FwFreeObj(jQueryParams)
    FwFreeObj(jResponse)
    FwFreeObj(jItem)
Return NIL
