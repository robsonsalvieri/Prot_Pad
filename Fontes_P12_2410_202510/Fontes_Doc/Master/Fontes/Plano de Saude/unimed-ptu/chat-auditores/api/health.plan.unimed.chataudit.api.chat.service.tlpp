#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fwmvcdef.ch"
#include "health.plan.unimed.cadbenef.api.risksharing.service.ch"

#define REQUIRED .T.
#define OPTIONAL .F.

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} ChatAuditApiChatService
@type class
@version 12.1.2410  
@author jose.paulo
@since 22/05/2025
/*/
class ChatAuditApiChatService

	protected data jRequestBody as json
	protected data jResponseAnex as json
	protected data jResponseBody as json
	protected data nStatusCode as numeric
	protected data cHealthInsurerCode as character
	protected data lError as logical
	protected data cRequestBody as character

	public method new() constructor
	public method post() as logical
	public method setRequestBody(cRequestBody as character)
	public method setPathParamsRequest(jPath as json)
	public method setQueryParamsRequest(jParams as json)
	public method getResponse() as character
	public method getStatusCode() as numeric
	public method destroy()
	public method downloadZip(url as character) as character

	protected method validRequestData(nOperation as numeric) as logical
	protected method validJsonData() as logical
	protected method checkFields(jData as json) as logical
	protected method formatBodyData()
	method GrvBiuBiv(nOperation as numeric, cTab as character) as logical
	protected method setResponseStatus(cStatus as character, cCode as character, cMessage as character)
	protected method setStatusCode(nStatusCode as numeric)
	protected method PLSUFAPI(cUF as numeric) as character

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author jose.paulo
@since 22/05/2025
@return object, objeto da instância da classe
/*/
method new() class ChatAuditApiChatService

	self:jRequestBody := JsonObject():new()
	self:jResponseAnex := JsonObject():new()
	self:jResponseBody := JsonObject():new()
	self:cHealthInsurerCode := plsIntPad()
	self:lError := .F.
	self:nStatusCode := 0

return self

/*/{Protheus.doc} post
Realiza o processamento da requisição de inclusão (post) do chat.
@type method
@version 12.1.2410  
@author jose.paulo
@since 22/05/2025
@return logical, se o processo de inclusão foi realizado com sucesso
/*/
method post() as logical class ChatAuditApiChatService
	Local Qxx
	local lSuccess  := .F. as logical

	if self:validRequestData(MODEL_OPERATION_INSERT)
		BIV->(dbSetOrder(2))          // BIV_FILIAL+BIV_ORGMCB+BIV_DESTCB+BIV_NRDOC1+BIV_NRDOC2+BIV_LOTPRT+BIV_GUIPRT+BIV_GUIOPE

		cUnimedOrigem := strzero(val(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedOrigemCobranca"]),4)
		cUnimedDestino:= strzero(val(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedDestinoCobranca"]),4)
		cNumeroDoc1   := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc1"],20)
		cNumeroDoc2   := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc2"],20)
		cNumeroLote   := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroLotePrestador"],12)
		cGuiaPrestador:= padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissPrestador"],20)
		cGuiaOperadora:= padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissOperadora"],20)

		//unimed respondendo minha mensagem
		if BIV->(msSeek(xFilial("BIV")+cUnimedOrigem+cUnimedDestino+cNumeroDoc1+cNumeroDoc2+cNumeroLote+cGuiaPrestador+cGuiaOperadora)) .Or. ;
			BIV->(msSeek(xFilial("BIV")+cUnimedDestino+cUnimedOrigem+cNumeroDoc1+cNumeroDoc2+cNumeroLote+cGuiaPrestador+cGuiaOperadora))
			self:GrvBiuBiv(3,"BIU")			
			lSuccess := .T.

		Else //Unimed iniciando uma conversa com minha Operadora
			Qxx := PLSBGCOB(.t.,cUnimedOrigem,cUnimedDestino,cNumeroDoc1,cNumeroDoc2,cNumeroLote,cGuiaPrestador,cGuiaOperadora)

			if !Qxx->(Eof())			
				self:GrvBiuBiv(3,"BIV")
				self:GrvBiuBiv(3,"BIU")		
				lSuccess := .T.
			Else
				self:setResponseStatus("E", "422.023", "Fatura não encontrada.") 
				self:setStatusCode(422)
				lSuccess:= .f.
			Endif
		endif

	endif

return lSuccess

/*/{Protheus.doc} setRequestBody
Defini o body recebido pela solicitação
@type method
@version 12.1.2410 
@author jose.paulo
@since 26/04/2025
@param cRequestBody, character, body da solicitação
/*/
method setRequestBody(cRequestBody as character) class ChatAuditApiChatService	
	Local cVar as character

	self:cRequestBody := cRequestBody

	self:jRequestBody:fromJson(cRequestBody)
	self:jResponseAnex:fromJson(cRequestBody)	
	 
	If self:jRequestBody:hasProperty("Elements")
		If Len(self:jRequestBody["Elements"])>0
			If self:jRequestBody["Elements"][1]:hasProperty("Value")
				cVar:=self:jRequestBody["Elements"][1]["Value"]:toJson()
				self:jRequestBody:fromJson(cVar) 
			EndIf

			If Len(self:jResponseAnex["Elements"])>1 .And. self:jResponseAnex["Elements"][2]:hasProperty("UploadName") .And. self:jResponseAnex["Elements"][2]:hasProperty("UploadDir") 
				self:jRequestBody["Elements"] := self:jResponseAnex["Elements"][2]["UploadDir"] +"*"+ self:jResponseAnex["Elements"][2]["UploadName"]
			EndIf
		EndIf
	EndIf

return

/*/{Protheus.doc} validRequestData
Valida se os dados recebidos pela requisição estão validos para serem processados
@type method
@version 12.1.2410  
@author jose.paulo
@since 26/04/2025
@param nOperation, numeric, operação sendo realizada: Inclusão, atualização ou exclusão
@return logical, se os dados da solicitação estão válidos.
/*/
method validRequestData(nOperation as numeric) as logical class ChatAuditApiChatService

	local lValid := .F. as logical

	if self:validJsonData()
		BA0->(dbSetOrder(1))
		If BA0->(msSeek(xFilial("BA0") + self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"]))
			lValid:= .T.
		Else
			self:setResponseStatus("E", "422.001", "Unimed não cadastrada", self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"]) 
			self:setStatusCode(422)
		EndIf
	endif

return lValid

/*/{Protheus.doc} validJsonData
Valida se os dados do json (body) está preenchidos corretamente. (obrigatóriedade e tipo)
@type method
@version 12.1.2410 
@author jose.paulo
@since 26/04/2025
@return logical, se os dados do body estão validos.
/*/
method validJsonData() as logical class ChatAuditApiChatService

	if self:checkFields({"body": self:jRequestBody, "attribute": "cabecalho", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["cabecalho"], "attribute": "codigoControleTransacao", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["cabecalho"], "attribute": "numeroVersaoPTU", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["cabecalho"], "attribute": "dataGeracao", "type": "C", "required": REQUIRED})
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosGuiaCobrancaUtilizacao", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "unimedOrigemCobranca", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "unimedDestinoCobranca", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "numeroDoc1", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "numeroDoc2", "type": "C", "required": OPTIONAL})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "numeroLotePrestador", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "numeroGuiaTissPrestador", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosGuiaCobrancaUtilizacao"], "attribute": "numeroGuiaTissOperadora", "type": "C", "required": REQUIRED})		
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosUnimedOrigemMensagem", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedOrigemMensagem"], "attribute": "unimedOrigem", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedOrigemMensagem"], "attribute": "tipoAuditor", "type": "N", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedOrigemMensagem"], "attribute": "nomeAuditorOrigem", "type": "C", "required": OPTIONAL})
		self:checkFields({"body": self:jRequestBody["dadosUnimedOrigemMensagem"], "attribute": "numeroConselhoProfissional", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedOrigemMensagem"], "attribute": "ufConselhoProfissional", "type": "N", "required": REQUIRED})
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosUnimedDestinoMensagem", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedDestinoMensagem"], "attribute": "unimedDestino", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedDestinoMensagem"], "attribute": "tipoAuditor", "type": "N", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedDestinoMensagem"], "attribute": "nomeAuditorDestino", "type": "C", "required": OPTIONAL})
		self:checkFields({"body": self:jRequestBody["dadosUnimedDestinoMensagem"], "attribute": "numeroConselhoProfissional", "type": "C", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosUnimedDestinoMensagem"], "attribute": "ufConselhoProfissional", "type": "N", "required": REQUIRED})
	endif

	if self:checkFields({"body": self:jRequestBody, "attribute": "dadosMensagem", "type": "J", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosMensagem"], "attribute": "tipoTransacao", "type": "N", "required": REQUIRED})
		self:checkFields({"body": self:jRequestBody["dadosMensagem"], "attribute": "descricaoMensagem", "type": "C", "required": REQUIRED})
	endif

	if !self:lError
		self:formatBodyData()
	endif

return !self:lError

/*/{Protheus.doc} formatBodyData
Formata os dados recebidos do json para serem tratados
@type method
@version 12.1.2410 
@author jose.paulo
@since 26/04/2025
/*/
method formatBodyData() class ChatAuditApiChatService

	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedOrigemCobranca"]   := strZero(val(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedOrigemCobranca"]) , 4)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedDestinoCobranca"]  := strZero(val(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedDestinoCobranca"]), 4)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc1"]             := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc1"]                   ,20)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc2"]             := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc2"]                   ,20)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroLotePrestador"]    := padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroLotePrestador"]          ,12)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissPrestador"]:= padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissPrestador"]      ,20)
	self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissOperadora"]:= padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissOperadora"]      ,20)

	self:jRequestBody["dadosUnimedOrigemMensagem"]["unimedOrigem"]             := strZero(val(self:jRequestBody["dadosUnimedOrigemMensagem"]["unimedOrigem"])           , 4)
	self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"]           := strZero(val(self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"])         , 4)

	self:jRequestBody["dadosUnimedDestinoMensagem"]["ufConselhoProfissional"]  :=PLSUFAPI(self:jRequestBody["dadosUnimedDestinoMensagem"]["ufConselhoProfissional"])
	self:jRequestBody["dadosUnimedOrigemMensagem"]["ufConselhoProfissional"]   :=PLSUFAPI(self:jRequestBody["dadosUnimedOrigemMensagem"]["ufConselhoProfissional"])
	
return


/*/{Protheus.doc} checkFields
Verifica se o campo do objeto está de acordo com os parâmetros informados (tipo e obrigatóriedade)
@type method
@version 12.1.2410
@author jose.paulo
@since 26/04/2025
@param jData, json, dados do campo a ser verificado (body, attribute, type, required)
@return logical, se o campo está válido
/*/
method checkFields(jData as json) as logical class ChatAuditApiChatService

	local lOk := .T. as logical
	//local dFormatDate as date

	if jData["required"] .and. !jData["body"]:hasProperty(jData["attribute"])
		lOk := .F.
		self:setResponseStatus("E", "400.001", STR0004 + jData["attribute"] + STR0005) // "O campo ";" é obrigatório."
		self:setStatusCode(400)
	/*else
		if jData["body"]:hasProperty(jData["attribute"]) .and. valType(jData["body"][jData["attribute"]]) <> jData["type"]
			if jData["type"] == "D"
				dFormatDate := formatDate(jData["body"][jData["attribute"]])

				if empty(dFormatDate)
					lOk := .F.
					self:setResponseStatus("E", "400.002", STR0006 + jData["attribute"] + STR0007) // "O valor informado para o campo ";" é inválido."
					self:setStatusCode(400)
				else
					jData["body"][jData["attribute"]] := dFormatDate
				endif
			ElseIf jData["type"] == "C" .And. !jData["required"]
				lOk:= .T.
			else
				lOk := .F.
				self:setResponseStatus("E", "400.002", STR0006 + jData["attribute"] + STR0007) // "O valor informado para o campo ";" é inválido."
				self:setStatusCode(400)
			endif
		endif*/
	endif

return lOk


/*/{Protheus.doc} setResponseStatus
Defini o status da resposta da api
@type method
@version 12.1.2410  
@author jose.paulo
@since 26/04/2025
@param cStatus, character, status, sendo: E ou S
@param cCode, character, código do status code
@param cMessage, character, mensagem do status
@param cSubscriberId, character, número de carteirinha do beneficiário
/*/
method setResponseStatus(cStatus as character, cCode as character, cMessage as character) class ChatAuditApiChatService
	local jStatus := JsonObject():new() as object

	jStatus["status"] := cStatus
	jStatus["codigoMensagem"] := cCode
	jStatus["descricaoMensagem"] := cMessage

	self:jResponseBody["cabecalho"] := JsonObject():new()
	self:jResponseBody["cabecalho"]["codigoControleTransacao"] := self:jRequestBody["cabecalho"]["codigoControleTransacao"]
	self:jResponseBody["cabecalho"]["numeroVersaoPTU"]         := self:jRequestBody["cabecalho"]["numeroVersaoPTU"]        
	self:jResponseBody["cabecalho"]["dataGeracao"]             := self:jRequestBody["cabecalho"]["dataGeracao"]            
	self:jResponseBody["dadosMensagem"]                         := JsonObject():new()
	self:jResponseBody["dadosMensagem"]["unimedOrigem"]        := self:jRequestBody["dadosUnimedOrigemMensagem"]["unimedOrigem"] 
	self:jResponseBody["dadosMensagem"]["unimedDestino"]       := self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"]
	self:jResponseBody["status"]                               := JsonObject():new()

	If cStatus == "E"
		self:jResponseBody["status"]["statusProcessamento"] := "E"
		self:jResponseBody["status"]["codigoMensagem"]                       := jStatus["codigoMensagem"]     
		self:jResponseBody["status"]["descricaoMensagem"]                    := jStatus["descricaoMensagem"]  
		self:lError := .T.		
	Else
		self:jResponseBody["status"]["statusProcessamento"] := "S"
		self:lError := .F.			
	endif

return

/*/{Protheus.doc} ChatAuditApiChatService::setStatusCode
Defini o status code do processamento
@type method
@version 12.1.2410  
@author jose.paulo
@since 26/04/2025
@param nStatusCode, numeric, código do status code
/*/
method setStatusCode(nStatusCode as numeric) class ChatAuditApiChatService

	self:nStatusCode := nStatusCode

return

method GrvBiuBiv(nOperation as numeric,cTab as character) as logical class ChatAuditApiChatService

	local lSuccess := .F. as logical
	local cMessage as character
	local cStatusCode as character
	local nStatusCode as numeric
	local cData := ""

	If cTab == "BIV"

		BIV->(RECLOCK("BIV",.T.))
		BIV->BIV_FILIAL:=xFilial("BIV")
		BIV->BIV_IDSALA:=PLSIDSALA()
		BIV->BIV_ORGMCB:=strZero(VAL(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedOrigemCobranca"]),4)
		BIV->BIV_DESTCB:=strZero(VAL(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["unimedDestinoCobranca"]),4)
		BIV->BIV_NRDOC1:=padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc1"],20)
		BIV->BIV_NRDOC2:=padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroDoc2"],20)
		BIV->BIV_LOTPRT:=padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroLotePrestador"],12)
		BIV->BIV_GUIPRT:=padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissPrestador"],20)
		BIV->BIV_GUIOPE:=padr(self:jRequestBody["dadosGuiaCobrancaUtilizacao"]["numeroGuiaTissOperadora"],20)
		BIV->BIV_STAPRO:="A"
		cData := self:jRequestBody["cabecalho"]["dataGeracao"]
		BIV->BIV_DATMSG:= CtoD(SubStr(cData, 9, 2) + "/" + SubStr(cData, 6, 2) + "/" + SubStr(cData, 1, 4))
		BIV->(MsUnlock())

		lSuccess := .t.

	EndIf

	If cTab == "BIU"

		BIU->(RECLOCK("BIU",.T.))
		BIU->BIU_FILIAL:=xFilial("BIU")
		BIU->BIU_IDSALA:=BIV->BIV_IDSALA
		BIU->BIU_IDCHAT:=CVALTOCHAR(PLSIDCHAT(BIV->BIV_IDSALA))
		BIU->BIU_CODCTR:=CVALTOCHAR(self:jRequestBody["cabecalho"]["codigoControleTransacao"])
		BIU->BIU_DATGER:=self:jRequestBody["cabecalho"]["dataGeracao"]
		BIU->BIU_ORGMSG:=self:jRequestBody["dadosUnimedOrigemMensagem"]["unimedOrigem"]
		BIU->BIU_TADTOR:=CVALTOCHAR(self:jRequestBody["dadosUnimedOrigemMensagem"]["tipoAuditor"])
		BIU->BIU_NOMEOR:=self:jRequestBody["dadosUnimedOrigemMensagem"]["nomeAuditorOrigem"]
		BIU->BIU_CONPOR:=self:jRequestBody["dadosUnimedOrigemMensagem"]["numeroConselhoProfissional"]
		BIU->BIU_UFCPOR:=self:jRequestBody["dadosUnimedOrigemMensagem"]["ufConselhoProfissional"]
		BIU->BIU_DESMSG:=self:jRequestBody["dadosUnimedDestinoMensagem"]["unimedDestino"]
		BIU->BIU_TADTDS:=CVALTOCHAR(self:jRequestBody["dadosUnimedDestinoMensagem"]["tipoAuditor"])
		BIU->BIU_NOMEDS:=self:jRequestBody["dadosUnimedDestinoMensagem"]["nomeAuditorDestino"]
		BIU->BIU_CONPDS:=self:jRequestBody["dadosUnimedDestinoMensagem"]["numeroConselhoProfissional"]
		BIU->BIU_UFCPDS:=self:jRequestBody["dadosUnimedDestinoMensagem"]["ufConselhoProfissional"]
		BIU->BIU_TPTRAN:=CVALTOCHAR(self:jRequestBody["dadosMensagem"]["tipoTransacao"])
		BIU->BIU_MSGENV:=self:jRequestBody["dadosMensagem"]["descricaoMensagem"]
		BIU->BIU_ARQANX:= IIF(ValType(self:jRequestBody["Elements"]) == "C",downloadZip(self:jRequestBody["Elements"]),"")
		BIU->BIU_STAPRO:="N"
		BIU->(MsUnlock())

		cMessage := "Chat incluido com sucesso."
		cStatusCode := "200.001"
		nStatusCode := 201

		SELF:nStatusCode := nStatusCode
		
		self:setResponseStatus("S", cStatusCode, cMessage)
		self:setStatusCode(nStatusCode)
		lSuccess := .T.

		Reclock("BIV", .F.)
            BIV->BIV_STAPRO := BIU->BIU_STAPRO
        BIV->(MsUnlock())
	EndIf
return lSuccess

method getResponse() as character class ChatAuditApiChatService

return self:jResponseBody:toJson()

static function PLSUFAPI(nUF) 
	local cCodeUF := ""
	local cSql := "SELECT X5_CHAVE FROM " + RetSqlName("SX5");
	      		+ " WHERE X5_TABELA  = 'AA'";
		  		+ "   and X5_DESCRI = '" + strzero(nUF,3) + "'"

	PlsQuery(cSql,'Qxx')

	cCodeUF := iif(!Qxx->(Eof()), Qxx->X5_CHAVE, "EX")
return cCodeUF

static Function GetLastPart(cTexto)
    Local nPos := Rat("_", cTexto) // Encontra a última ocorrência de "_"
    Local cResultado := ""
	
	cResultado := SubStr(cTexto, nPos + 1) // Pega tudo após o último "_"
Return cResultado

/*/{Protheus.doc} getStatusCode
Método responsavel por retornar o status code de retorno da API

@type method
@author JOSE.PAULO
@since 29/04/25
@version Protheus 12
/*/
method getStatusCode() as numeric class ChatAuditApiChatService
return self:nStatusCode

static function downloadZip(url) 
	local aLink      := STRTOKARR(url, "*")
    local cUrlOrPath := StrTran(aLink[1], "./", "") + aLink[2] // Substitua pelo link ou caminho local
    local cTargetDir := "\cadbenef\" // Diretório de destino no rootpath
    local cFileName := GetLastPart(aLink[2])
    local cFullPath := cTargetDir + cFileName
    local lSuccess := .F.
	local cArquivo := ""

    // Verifica se o diretório existe, caso contrário, cria
    if !ExistDir(cTargetDir)
        mkdir(cTargetDir)
    endif

    // Baixa o arquivo
    lSuccess := iif(substr(cUrlOrPath, 1, 4) $ "http", DownloadFile(cUrlOrPath, cFullPath), .F.)
        
    if !(substr(cUrlOrPath, 1, 4) $ "http")
        cFullPath := StrTran(StrTran(upper(cUrlOrPath),"/","\"),GetSrvProfString("RootPath",""),"")		
		lSuccess := .T.
    endif

    iif(!lSuccess, MsgStop("Erro ao baixar o arquivo: " + cUrlOrPath), "")

    // Insere o conteúdo nas tabelas ACB e AC9
	if lSuccess
    	cArquivo := PLIncluiBase(BIU->BIU_IDSALA + BIU->BIU_IDCHAT, val(BIU->BIU_CODCTR), cFullPath, cFileName)
	endif
return cArquivo


/*method destroy() class ChatAuditApiChatService

	freeObj(self:jRequestBody)
	freeObj(self:jResponseBody)
	freeObj(self:jResponseAnex)

return*/
