#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.unimed.cadbenef.api.contractType.ch"

#define REQUIRED .T.

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} CadBenefApiContractType
API para consulta dos dados do beneficiário através da carteirinha, com o retorno do tipo de contrato vinculado ao beneficiario.
@type class
@version 12.1.2510
@author giovanna.charlo 
@since 11/08/2025
/*/
class CadBenefApiContractType from RestClient                            

	protected data jBody as json
	protected data aParams as array
	protected data jResponse as json
	protected data cMessage as character
	protected data lIsError as logical
	protected data oToken as object
	protected data aSubscriberId as array
	protected data jStatusCodeMessage as json
	protected data aContracts as array

	public method new(cHealthInsurerCode) constructor
	public method addSubscriberId(aSubscriberId as array) as logical
	public method send() as logical
	public method getRequest() as json
	public method getResponse() as json
	public method getMessage() as character
	public method destroy()

	protected method loadSubscriberId()
	protected method loadApiConfig(cHealthInsurerCode as character) as logical
	protected method setError(cCode as character, cMessage as character, cDetailedMessage as character) as json
	protected method setAttribute(jData as json)
    protected method getStatusCodeMessage(cStatusCode as character) as character

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param cHealthInsurerCode, character, código da operadora para buscar as configurações
@return object, objeto da instância da classe
/*/
method new(cHealthInsurerCode) class CadBenefApiContractType

	_Super:new()

	self:oToken := TokenOAuth():new(PLSINTPAD())
	self:jBody := JsonObject():new()
	self:aParams := {}
	self:jResponse := JsonObject():new()
	self:lIsError := .F.

return self

/*/{Protheus.doc} addSubscriberId
Adiciona a carterinha do beneficiário que será enviado para o cadbenef.
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param aSubscriberId, character,  carterinha do beneficiário  a ser adicionado
@return logical, se o contrato foi adicionado com sucesso para ser enviado
/*/
method addSubscriberId(aSubscriberId as array) as logical class CadBenefApiContractType

	local lAdd := .f. as logical

	self:jBody := JsonObject():new()
	self:aSubscriberId := aSubscriberId

	self:loadSubscriberId()
		
	if !self:lIsError
		lAdd := .T.
	endif

return lAdd

/*/{Protheus.doc} send
Envia o  a carterinha do beneficiário  adicionado pelo método addSubscriberId
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@return logical, se o contrato foi validado com sucesso
/*/
method send() as logical class CadBenefApiContractType

	local lSuccess := .F. as logical
	local aHeaders := {} as array

	if self:lRobotTest .or. self:oToken:getToken()
		if self:lRobotTest .or. self:loadApiConfig()

			aAdd(aHeaders, {"key": "Content-Type", "value": "application/json"})
			aAdd(aHeaders, {"key": "Authorization", "value": "Bearer " + self:oToken:getBearer()})
			self:setHeaderParams(aHeaders)

			self:setBody(self:jBody:toJson())

			lSuccess := self:post()
			
			self:jResponse:fromJson(self:getBody())
			self:cMessage := self:getStatusCodeMessage(cValToChar(self:getStatusCode()))
		else
			lSuccess := .F.
		endif
	else
		lSuccess := .F.
		self:cMessage := self:oToken:getMessage()
		self:jResponse := self:oToken:getBody()
	endif

	fwFreeArray(aHeaders)
return lSuccess

/*/{Protheus.doc} getRequest
obter json de envio do contrato
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@return json, objeto json com os dados da solicitação
/*/
method getRequest() as json class CadBenefApiContractType
return self:jBody

/*/{Protheus.doc} getMessage
Obter mensagem de resposta do processamento
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@return character, mensagem de erro ou sucesso
/*/
method getMessage() as character class CadBenefApiContractType

return self:cMessage

/*/{Protheus.doc} getResponse
Obter json de resposta do envio do contrato
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@return json, objeto json com os dados da resposta
/*/
method getResponse() as json class CadBenefApiContractType

return self:jResponse

/*/{Protheus.doc} destroy
Limpa da memória as variáveis (objeto, array e json) da clase
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
/*/
method destroy() class CadBenefApiContractType

	freeObj(self:jBody)
	freeObj(self:jResponse)
	freeObj(self:oToken)
	freeObj(self:jStatusCodeMessage)
	fwFreeArray(self:aParams)

return


/*/{Protheus.doc} loadSubscriberId
Carrega a carterinha do beneficiário na requisição (body) a ser enviado.
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
/*/
method loadSubscriberId() class CadBenefApiContractType

	self:jBody["carteirinhaBeneficiario"] := {}
	self:setAttribute({"body": self:jBody["carteirinhaBeneficiario"], "value": self:aSubscriberId, "required": REQUIRED})
return


/*/{Protheus.doc} loadApiConfig
Carrega as configurações necessárias para comunicar com o CadBenef
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param cHealthInsurerCode, character, código da operadora para buscar as configurações
@return logical, se as configurações foram carregadas com sucesso
/*/
method loadApiConfig() as logical class CadBenefApiContractType
    local lOk := .T. as logical
    local cPassword as character
    local cFileNamePFX as character
    local cCertificateDirectory := superGetMV("MV_PLCBCER", .F., "\cadbenef\certificados\") as character
    local jStatus as json

    BA0->(dbSetOrder(1))
    if BA0->(dbSeek(xFilial("BA0") + PLSINTPAD()))

		if BA0->(FieldPos("BA0_CBTYPE")) > 0 
        	self:setEndpoint(alltrim(BA0->BA0_CBTYPE))
		endif
		
        cFileNamePFX := alltrim(BA0->BA0_CDBPFX)
        cPassword := alltrim(BA0->BA0_CDBPAS)

        if !empty(cFileNamePFX)
            jStatus := self:appendValidCertificate(cCertificateDirectory, cFileNamePFX, cPassword)

            if jStatus["status"]
                lOk := .T.
            else
                lOk := .F.
                self:setError("401", jStatus["message"], jStatus["error"])
            endif
        endif
    endif

    freeObj(jStatus)

return lOk


/*/{Protheus.doc} setAttribute
Defini os campos a ser adicionado no body para comunicar com o CadBenef.
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param jData, json, dados do campo a ser adicionado body da requisição
/*/
method setAttribute(jData as json) class CadBenefApiContractType

	local lEmpty as logical

	lEmpty := empty(jData["value"])

	// Mandatório (Obrigatório envio da informação)
	if jData["required"] .and. lEmpty
		self:setError("400.001", STR0001)  // "Existem campos obrigatórios do beneficiário que não foram preenchidos."   				
	else
		if !lEmpty
			self:jBody["carteirinhaBeneficiario"] := jData["value"]
		endif
	endif
return


/*/{Protheus.doc} getStatusCodeMessage
Obter a mensagem de acordo com o status code
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@return character, mensagem do status code
/*/
method getStatusCodeMessage(cStatusCode as character) as character class CadBenefApiContractType

	local cMessage := "" as character
	local jStatusCodeMessage := JsonObject():new() as json

	jStatusCodeMessage["200"] := STR0002 // "Tipo de Contrato Retornado com sucesso."
	jStatusCodeMessage["400"] := STR0003 // "Matricula Informada Inválida."

	if jStatusCodeMessage:hasProperty(cStatusCode)
		cMessage := jStatusCodeMessage[cStatusCode]
	endif

return cMessage

/*/{Protheus.doc} setError
Defini os dados de erro no json de resposta
@type method
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param cCode, character, código do erro (status code)
@param cMessage, character, mensagem do erro
@param cDetailedMessage, character, detalhes do erro

@return json, objeto json de resposta
/*/
method setError(cCode as character, cMessage as character, cDetailedMessage as character) as json class CadBenefApiContractType

	self:jResponse["code"] := cCode
	self:jResponse["message"] := cMessage
	self:jResponse["detailedMessage"] := cDetailedMessage

	self:lIsError := .T.
	self:cMessage := cMessage

return self:jResponse

/*/{Protheus.doc} getContractType
Retorna em qual cadastro e campo o usuário deverá preencher/alterar o campo criticado
@type function
@version 12.1.2510
@author giovanna.charlo
@since 11/08/2025
@param aSubscriberId, array, campo a ser obtido a soluçaõ de preenchimento
@return array, solução de preenchimento do campo
/*/
function getContractType(aSubscriberId as array) as array

    local oContractType as object
	local aResponse := {} as array 

    default aSubscriberId := {}

    oContractType := CadBenefApiContractType():new(PLSINTPAD())

    oContractType:addSubscriberId(aSubscriberId)

    oContractType:send()

	aResponse := {oContractType:getResponse(), oContractType:getMessage()}

    oContractType:destroy()
    freeObj(oContractType)

return aResponse
