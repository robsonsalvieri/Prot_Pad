#include "tlpp-core.th"
#include "fwmvcdef.ch"
#include "health.plan.unimed.cadbenef.contracts.send.ch"

#define SEND_COMPLETED "1" // Cadastrado com Sucesso 
#define PENDING_SEND "2" // Pendente de envio
#define SEND_ERROR "3" // Erro de envio
#define IN_ANALYSIS "4" // Em análise

#define INCLUSION_TYPE "0" // Inclusão
#define UPDATE_TYPE "2" // Atualização
#define SEARCH_TYPE "3" // Consultar

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} cadBenefSendContracts
Realiza a comunicação com o servidor do CadBenef para envio do contrato
@type function
@version 12.1.2510
@author giovanna.charlo
@since 20/03/2025
@param lShowMessage, logical, exibir mensagem do status do envio
@return json, objeto com os dados do status do envio
/*/
function cadBenefSendContracts(cOperation as character, cAlias as character, cKey as character) as json

    local oCadBenefContract as object
    local jContractData as json
    local jStatus as json
    local aArea := getArea() as array

    jStatus := {"status": "",;
                "sendMessage": "",;
                "responseMessage": "",;
                "showMessage": "",;
                "transactionCode": "",;
                "sendDate": "",;
                "sendTime": "",;
                "protocolNumber": "",;
                "responseTime": "";
               }
    /*
    if cAlias == "BA1"
        BA3->(dbSetOrder(1))
        if BA3->(msSeek(alltrim(xFilial("BA3") + cKey)))
            cAlias := "BA3"
            cKey  := "BA3_CODINT+BA3_CODEMP+BA3_MATRIC"                                                                 
        endif
    endif
    */
 
    (cAlias)->(dbSetOrder(1))

   
    if !(cAlias)->(msSeek(alltrim(xFilial(cAlias) + &(cAlias+"->"+cKey))))
        jStatus["status"] := SEND_ERROR
        jStatus["showMessage"] := STR0005 //"Não foi encontrado nenhum contrato com essa chave."
        return jStatus
    else
        if cAlias == "BA1"
            BA3->(dbSetOrder(1))
            if BA3->(msSeek(alltrim(xFilial("BA3") + BA1->(BA1_CODINT+BA1_CODEMP+BA1_CONEMP+BA1_VERCON+BA1_SUBCON+BA1_VERSUB))))
                cAlias := "BA3"
                cKey := BA3->(BA3_CODINT+BA3_CODEMP)                                                               
            endif
        endif
        jContractData := getContractData(cAlias, cKey)
        jStatus := contractValidation(jContractData, jStatus)

        if jStatus["status"] == SEND_ERROR
            return jStatus
        else    

            oCadBenefContract := CadBenefApiContracts():new(jContractData["healthInsurerCode"])
            jStatus["transactionCode"] := getNewTransactionCode(jContractData)
                                                                                        
            if oCadBenefContract:addContracts(cKey, cOperation, jStatus["transactionCode"], jContractData)
                jStatus["responseTime"] := time()

                if oCadBenefContract:send()
                    jStatus["status"] := SEND_COMPLETED            
                else
                    jStatus["status"] := SEND_ERROR
                endif  

                jStatus["responseTime"] := elapTime(jStatus["responseTime"], time())
            else
                jStatus["status"] := SEND_ERROR 
            endif

            jStatus["showMessage"] := oCadBenefContract:getMessage()
            jStatus["sendMessage"] := oCadBenefContract:getRequest():toJson()
            jStatus["responseMessage"] := oCadBenefContract:getResponse():toJson()
            jStatus["transactionCode"] := jStatus["transactionCode"]
            jStatus["protocolNumber"] := oCadBenefContract:getprotocolNumber()
            jStatus["sendDate"] := dDataBase
            jStatus["sendTime"] := time()    

            RecLock(cAlias,.F.)
            &(cAlias+"->"+cAlias+"_STSVC") := jStatus["status"]
            &(cAlias+"->"+cAlias+"_NPROVC") := jStatus["protocolNumber"]
            &(cAlias+"->"+cAlias+"_CDTRVC") := jStatus["transactionCode"]
            &(cAlias+"->"+cAlias+"_MSGRVC")  := jStatus["responseMessage"]
            &(cAlias+"->( MsUnlock())")

            restArea(aArea)
            oCadBenefContract:destroy()

            freeObj(jContractData)
            freeObj(oCadBenefContract)
            
        endif
    endif

return jStatus

/*/{Protheus.doc} sendContracts
Envia o contrato vinculado ao SubContrato
@type function
@version 12.1.2510
@author giovanna.charlo
@since 20/03/2025
/*/
function sendContracts(cOperation as character, cAlias as character, cKey as character)

    if FWAlertNoYes("Deseja enviar este Contrato para Umined da Brasil?", "") // "Deseja enviar todos os beneficiários do lote para o CadBenef?"

        jStatus := cadBenefSendContracts(cOperation, cAlias, cKey)

        do case
            case jStatus["status"] == SEND_COMPLETED
                fwAlertSuccess(jStatus["showMessage"], "")
            case jStatus["status"] == IN_ANALYSIS
                fwAlertWarning(jStatus["showMessage"], "")
            otherWise
                fwAlertError(jStatus["showMessage"], "")
        endcase
    endif
return

/*/{Protheus.doc} cadBenefUpdateBatchStatus
Atualiza o status do subcontrato
@type function
@version 12.1.2510
@author giovanna.charlo
@since 20/03/2025
@param cStatus, character, status a ser atribuido ao lote posiconado
@return logical, se o status foi atualizado com sucesso
/*/
function searchProtocolNumber(cStatus as character) as logical

    local lOk := .F. as logical

return lOk

/*/{Protheus.doc} getNewTransactionCode
Obter o proximo código de transação a ser utilizado para comunicar com o validador comercial unimed.
@type function
@version 12.1.2510
@author giovanna.charlo
@since 20/03/2025
@param cTransactionCode, character, código de transacao
@return character, código da transação gerado
/*/
static function getNewTransactionCode(jContractData as json) as character

    local cAlias := jContractData["alias"] as character
    local aArea := (cAlias)->(GetArea()) as array
    local nSequen := 1 as numeric
    local cTransactionCode as character
    local cMonth as character
    local cYear as character

    cTransactionCode := &((cAlias)+"->"+ (cAlias + "_CDTRVC"))

    if cAlias == "BA3"
        cMonth := month2Str(&((cAlias)+"->"+ (cAlias + "_DATBAS")))
        cYear := year2Str(&((cAlias)+"->"+ (cAlias + "_DATBAS")))
    else
        cMonth := month2Str(&((cAlias)->(cAlias + "_DATCON")))
        cYear := year2Str(&((cAlias)->(cAlias + "_DATCON")))
    endif

    iif(cAlias == "BA3",(cAlias)->(dbSetOrder(13)), (cAlias)->(dbSetOrder(6)))

    if (cAlias)->(msSeek(xFilial(cAlias) + &((cAlias)+"->"+ (cAlias + "_CDTRVC"))))
        nSequen := val(substr(cTransactionCode, 15)) + 1
    endif

    if cAlias == "BA3"
        cTransactionCode :=  cYear + cMonth +  &((cAlias)+"->"+ (cAlias + "_CODINT")  + (cAlias + "_CODEMP")) + strZero(nSequen, 6)
    else
        cTransactionCode :=  cYear + cMonth + &((cAlias)->((cAlias + "_CODIGO"))) + strZero(nSequen, 6)
    endif

    restArea(aArea)

return cTransactionCode

/*/{Protheus.doc} sendAttachment
Chamada da base de conhecimento para abertura do anexo
@type function
@version 12.1.2510
@author giovanna.charlo
NÃO ESTA SENDO UTILIZADA
@since 02/05/2025
@param cAlias, tabela da rotina sendo utilizada
@param cSearchKey, chave de busca para gravação do arquivo
@return logical, situação do retorno
/*/
function sendAttachment(cAlias as character, cSearchKey as character) as logical

	local aArea	:= getArea()
    local lOk := .F. as logical

    Private aRotina := {}

    default cAlias := ""
	default cSearchKey := ""

	aRotina := {{"Anexar arquivo acordo",'MsDocument', 0/*permite exclusao do registro*/,1/*visualizar arquivo*/},{"Inclusão Rápida",'PLSDOcs',0,3}}

	(cAlias)->(DbSelectArea(cAlias))

    if cAlias == "BQC"
	    (cAlias)->(DbSetOrder(1))	
        if (cAlias)->(MsSeek(xFilial(cAlias) + &((cAlias) + "_CODIGO") + &((cAlias) + "_NUMCON") + &((cAlias) + "_VERCON") + &((cAlias) + "_SUBCON") + &((cAlias) + "_VERSUB")))
		    MsDocument(cAlias, BQC->(Recno()), 4)
            lOk := .t.
        endif

    else
         (cAlias)->(DbSetOrder(7))	
    endif

	if !lOk
		MsgAlert( "Opção não disponível. Na inclusão da mensagem, ao finalizar o cadastro será apresentada a opção de adicionar o anexo", "Atenção")
        lOk := .f.
	endif

	RestArea(aArea)

return lOk

/*/{Protheus.doc} getContractData
Captura os daodos do contrato para envio
@type function
@version 12.1.2510
@author giovanna.charlo
@since 02/06/2025
@param cAlias, tabela posicionada
@param cKey, chave da tabela posicionada
@return json, dados do contrato
/*/
static function getContractData(cAlias as character, cKey as character)

    local jContractData := JsonObject():new() as json

    jContractData["sendContract"] := &((cAlias)+"->"+ (cAlias + "_ENVVC"))
    jContractData["sendingOperation"] := &((cAlias)+"->"+ (cAlias + "_OPEVC"))
    jContractData["beneficiaryQuantity"] := &((cAlias)+"->"+ (cAlias + "_QTBVC"))
    jContractData["agreementFile"] := &((cAlias)+"->"+ (cAlias + "_ACDVC"))
    jContractData["contractStatus"] := &((cAlias)+"->"+ (cAlias + "_STSVC"))
    jContractData["protocolNumber"] := &((cAlias)+"->"+ (cAlias + "_NPROVC"))
    jContractData["healthInsurerCode"] := &((cAlias)+"->"+ (cAlias + "_CODINT"))
    jContractData["alias"] := cAlias
    jContractData["key"] := cKey

return jContractData

/*/{Protheus.doc} contractValidation
Chamada da base de conhecimento para abertura do anexo
@type function
@version 12.1.2510
@author giovanna.charlo
@since 02/05/2025
@param cAlias, tabela da rotina sendo utilizada
@param cSearchKey, chave de busca para gravação do arquivo
@return logical, situação do retorno
/*/
static function contractValidation(jContractData as json, jStatus as json)

    if jContractData["contractStatus"] == SEND_COMPLETED .and. !empty(jContractData["protocolNumber"])
        jStatus["status"] := SEND_ERROR
        do case
            case jContractData["sendingOperation"] == INCLUSION_TYPE
                jStatus["showMessage"] := STR0001 // "O contrato já enviado no VCOM."
            case jContractData["sendingOperation"] == UPDATE_TYPE
                jStatus["showMessage"] := STR0002 // "Cadastro deste contrato já realizado no VCOM"
            case jContractData["sendingOperation"] == SEARCH_TYPE
                jStatus["showMessage"] := STR0002 // "Cadastro deste contrato já realizado no VCOM"
        endcase
    else
        if jContractData["sendContract"] == "1"
            do case
                case jContractData["sendingOperation"]  == "1" .and. empty(jContractData["protocolNumber"])
                    jStatus["status"] := SEND_ERROR 
                    jStatus["showMessage"] := STR0003 //"Não é possível realizar o encerramento, pois esse contrato não foi cadastrado!"
                    
                case jContractData["sendingOperation"]  == "1" .and.  empty(jContractData["beneficiaryQuantity"]) 
                    jStatus["status"] := SEND_ERROR
                    jStatus["showMessage"] := STR0004 // Informe a quantidade de beneficiários para envio esse contrato.
            endcase
        endif
    endif

return jStatus
