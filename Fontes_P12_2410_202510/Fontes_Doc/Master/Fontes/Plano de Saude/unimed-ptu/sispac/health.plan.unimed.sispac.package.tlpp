#include "tlpp-core.th"

namespace totvs.protheus.health.plan.unimed

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} PackageSISPAC
Serviços para integração SISPAC – Sistema de Pacotes

@type method
@version 12.1.2410
@author vinicius.queiros
@since 11/01/2024
/*/
class PackageSISPAC from RestClient

    private data oToken as object
    private data lConfig as logical
    private data jResponse as json
    private data jRequest as json
    private data nPage as numeric
    private data nPageSize as numeric

    public method new(cHealthInsurerCode) constructor
    public method packagesQuery() as logical
    public method destroy()

    public method getJsonResponse() as json
    public method setJsonRequest(cProperty as character, xValue as variant, cType as character) as logical
    public method setPage(nPage as numeric) as logical
    public method setPageSize(nPageSize as numeric) as logical

    private method postPackages() as logical
    private method setConfig(cHealthInsurerCode as character) as logical
    private method setResponse() as logical

endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@version 12.1.2410  
@author vinicius.queiros
@param cHealthInsurerCode, character, codigo da operadora que contém as configurações (BA0)
@since 11/01/2024
/*/
method new(cHealthInsurerCode as character) class PackageSISPAC

    default cHealthInsurerCode := plsIntPad()

    _Super:new()

    self:oToken := TokenGIU():new(cHealthInsurerCode)
    self:setConfig(cHealthInsurerCode)

    self:nPage := 1
    self:nPageSize := 2
    self:jRequest := JsonObject():new()

return self

/*/{Protheus.doc} packagesQuery
Disponibilizar os Pacotes cadastrados no Sistema de Pacotes, tanto os da própria Unimed 
solicitante (todos os status), quanto de outras Unimeds (desde que estejam aptos a 
trafegar no Intercâmbio Nacional).

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
@return logical, se houve sucesso ao consultar os pacotes
/*/
method packagesQuery() as logical class PackageSISPAC   

    local lOk := .f. as logical

    if self:lRobotTest .or. self:oToken:getToken()
        self:jRequest["limit"] := self:nPageSize
        self:jRequest["offset"] := iif(self:nPage == 1, 0, self:nPageSize * (self:nPage - 1))

        lOk := self:postPackages()
    else
        self:cErrorMessage := self:oToken:getErrorMessage()
        lOk := .f.
    endif

return lOk

/*/{Protheus.doc} destroy
Limpa as propriedades para liberar a memoria alocada no Server

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
/*/
method destroy() class PackageSISPAC

    freeObj(self:oToken)
    freeObj(self:jResponse)
    freeObj(self:jRequest)

return

/*/{Protheus.doc} getJsonResponse
Obter json de resposta da consulta dos pacotes

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
@return json, resposta da requisição
/*/
method getJsonResponse() as json class PackageSISPAC

return self:jResponse

/*/{Protheus.doc} getJsonResponse
Defini propriedade no json da solicitação da consulta dos pacotes

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
@param jProperty, json, propriedade para ser adicionada ao json da requisição
@return logical, sucesso ao adicionar propriedade
/*/
method setJsonRequest(cProperty as character, xValue as variant, cType as character) as logical class PackageSISPAC

    local lOk := .f. as logical

    default cType := "C" 

    if !empty(cProperty) .and. !empty(xValue)
        do Case
            case cType == "C" .and. valType(xValue) == "C"
                xValue := alltrim(xValue)

            case cType == "D" .and. valType(xValue) == "D"
                xValue := transform(dtos(xValue), "@R 9999-99-99")
        endcase

        self:jRequest[cProperty] := xValue
        lOk := .t.
    endif

return lOk

/*/{Protheus.doc} setPage
Defini pagina a ser consultada

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/01/2024
@param nPage, numeric, numero da pagina
@return logical, sucesso ao adicionar pagina
/*/
method setPage(nPage as numeric) as logical class PackageSISPAC

    self:nPage := nPage

return .t.

/*/{Protheus.doc} setPageSize
Defini total de registros por pagina

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/01/2024
@param nPageSize, numeric, total de registros por pagina
@return logical, sucesso ao adicionar total de paginas
/*/
method setPageSize(nPageSize as numeric) as logical class PackageSISPAC

    self:nPageSize := nPageSize

return .t.

/*/{Protheus.doc} postPackages
Realiza a comunicação POST da api de pacotes

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
/*/
method postPackages() as logical class PackageSISPAC

    local lSuccess := .f. as logical
    local aHeaderParams := {} as array

    if self:lConfig
        aAdd(aHeaderParams, {"key": "Content-Type", "value": "application/json"})
        aAdd(aHeaderParams, {"key": "Authorization", "value": self:oToken:getBearer()})
        aAdd(aHeaderParams, {"key": "Cookie", "value": self:oToken:getCookie()})

        self:setHeaderParams(aHeaderParams)

        self:setBody(self:jRequest:toJson())

        if self:post()
            lSuccess := self:setResponse()
        else
            self:jResponse := {"code": self:getStatusCode(), "message": self:getErrorMessage()}
        endif
    endif

    fwFreeArray(aHeaderParams)

return lSuccess

/*/{Protheus.doc} setConfig
Defini as configurações do SISPAC no cadastro da operadora de saúde (BA0)

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
@param cHealthInsurerCode, character, codigo da operadora que contém as configurações (BA0)
@return logical, sucesso ao obter as configurações
/*/
method setConfig(cHealthInsurerCode as character) as logical class PackageSISPAC

    BA0->(dbSetOrder(1))
    if BA0->(msSeek(xFilial("BA0")+cHealthInsurerCode))
        if !empty(BA0->BA0_URLPAC) .and. !empty(BA0->BA0_PTHPAC)
            self:setEndpoint(alltrim(BA0->BA0_URLPAC))
            self:setPath(alltrim(BA0->BA0_PTHPAC))

            self:lConfig := .t.
        endif
    endif

return self:lConfig

/*/{Protheus.doc} setResponse
Defini o json de resposta da consulta dos pacotes

@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/01/2024
@return logical, sucesso ao montar json de resposta
/*/
method setResponse() as logical class PackageSISPAC

    local lSuccess := .f. as logical
    local jBody := JsonObject():new() as json
    
    self:jResponse := JsonObject():new()

    jBody:fromJson(self:getBody())

    if valType(jBody["errors"]) == "A" .and. len(jBody["errors"]) > 0
        lSucces := .f.
        self:cStatusCode := 400
        self:jResponse["code"] := jBody["status"]["id_status_execucao"]
        self:jResponse["message"] := decodeUTF8(jBody["status"]["ds_status_execucao"])
    else
        lSuccess := .t.
        self:jResponse["items"] := jBody["rows"]
        self:jResponse["hasNext"] := (self:nPage * self:nPageSize) < jBody["total"]    
        self:jResponse["remainingRecords"] := iif(self:jResponse["hasNext"], jBody["total"] - (self:nPage * self:nPageSize), 0)
    endif

    freeObj(jBody)

return lSuccess
