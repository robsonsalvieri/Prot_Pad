#include "tlpp-core.th"
#include "fwmvcdef.ch"

namespace totvs.protheus.health.plan.integration.monitor.tissonline

function InputIntegration(cSituacao as character, cBodyEnv as character, cBodyRet as character, nOperation as numeric, cId as character)
	Local jParameters := JsonObject():new() as json
	Local lOk := .F. as logical
	Local oXml as object
	Local cAviso := "" as character
    Local cErro := "" as character

	Default cSituacao := "0"                                                                                               
	Default cBodyEnv := ""
	Default cBodyRet := ""
	Default cId := ""

	If nOperation == MODEL_OPERATION_INSERT
		
    	oXML := XmlParser(cBodyEnv, "_", @cAviso, @cErro)

		If Empty(cErro)
			jParameters := GetParameters(cBodyEnv, "", nOperation)
		EndIf

		jParameters["integrationDate"] := dDatabase
		jParameters["integrationHour"] := time()
		jParameters["situationRegistry"] := "0"
		jParameters["shippingBody"] := cBodyEnv

		cId := createReg(jParameters)
	ElseIf nOperation == MODEL_OPERATION_UPDATE .And. !Empty(cId)

		oXML := XmlParser(cBodyRet, "_", @cAviso, @cErro)

		If Empty(cErro)
			jParameters := GetParameters("", cBodyRet, nOperation)
		EndIf

		jParameters["identifierCode"] := cId
		jParameters["returnBody"] := IndentXML(cBodyRet)

		lOk := updateReg(jParameters)

		Return lOk
	EndIf

Return cId

static function GetParameters(cBodyEnv as character, cBodyRet as character, nOperation as numeric) as json
	
	Local jParameters := JsonObject():new() as json
	Local oXML := nil as object
	Local lRet := .F. as logical
	Local cVerArq := "" as character
	Local cSoapXML := "" as character
	Local cBody := "" as character
	Local nPosPrim := 0 as numeric
	Local nPosXmlns := 0 as numeric
	Local cTagTipReq := "" as character
	
	Default cBodyEnv := ""
	Default cBodyRet := ""

	private cNS := "" as character

	cBody := Iif(nOperation == 3, cBodyEnv, cBodyRet)

	HttpCtType( "text/xml; charset="+'UTF-8' )

	cVerArq	:= Substr(cBody,At("Padrao>", cBody) + Len("Padrao>"),7)
	aRetObj := GetDataPrefixNs(cBody)

	If aRetObj[1]

		cSoapXML := aRetObj[3]
		oXML := TXmlManager():New()

		If empty(cNS)
			nPos := At(">",Upper(cSoapXml))
			nPosPrim := At("<",Upper(cSoapXml))
			cSoapPt1 := Substr(cSoapXml,1,nPos-1)
			cSoapPt2 := Substr(cSoapXml,nPos,len(cSoapXml))
			nPosXmlns := At("XMLNS",Upper(cSoapPt1))
			cTagTipReq := Substr(cSoapPt1,nPosPrim,nPosXmlns-nPosPrim-1)
			cSoapXml := "      " + cTagTipReq + cSoapPt2
		Endif

		lRet := oXML:Parse(cSoapXml)

		if lRet
			aNS := oXML:XPathGetRootNsList()
			nPos := ascan(aNS,{|x| upper(alltrim(x[1])) == upper(cNS) })
			If nPos > 0
				oXML:XPathRegisterNs( aNS[ nPos ][ 1 ],aNS[ nPos ][ 2 ] )
			EndIf
		EndIf

		If(nOperation == 3)
			jParameters := GetDataXML(oXml)
		Else
			jParameters := GetNumberAttendance(oXml)
		EndIf

	EndIf

return jParameters

static function GetDataXML(oXml as object) as json

	Local jParameters := JsonObject():new() as json

	Default oXml := nil

	If oXml:XPathHasNode(SetNs("/solicitacaoProcedimentoWS"))

		cPathTag := SetNs("/solicitacaoProcedimentoWS")

		jParameters["codeCreditor"] := GetDataHealthcareProvider(oXml, cPathTag)
		jParameters["transactionType"] := "01"

		Do Case
			Case oXml:XPathHasNode( cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoSP-SADT" ))
				jParameters["subscriberId"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoSP-SADT/dadosBeneficiario/numeroCarteira"))
				jParameters["guideType"] := "01"
			Case oXml:XPathHasNode( cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoInternacao" ))
				jParameters["subscriberId"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoInternacao/dadosBeneficiario/numeroCarteira"))
				jParameters["guideType"] := "02"
			Case oXml:XPathHasNode( cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoProrrogacao" ))
				jParameters["subscriberId"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoProrrogacao/dadosBeneficiario/numeroCarteira"))
				jParameters["guideType"] := "03"
			Case oXml:XPathHasNode( cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoOdontologia" ))
				jParameters["subscriberId"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/solicitacaoProcedimento/solicitacaoOdontologia/numeroCarteira"))
				jParameters["guideType"] := "04"
		EndCase

	EndIf

return jParameters

static function GetNumberAttendance(oXml as object) as json

	Local jParameters := JsonObject():new() as json

	Default oXml := nil

	If oXml:XPathHasNode(SetNs("/autorizacaoProcedimentoWS"))

		cPathTag := SetNs("/autorizacaoProcedimentoWS")

		Do Case
			Case oXml:XPathHasNode( cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoServico/dadosAutorizacao/numeroGuiaOperadora"))
				jParameters["guideNumber"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoServico/dadosAutorizacao/numeroGuiaOperadora"))
                jParameters["situationRegistry"] := "1"
			Case oXml:XPathHasNode( cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoInternacao/autorizacaoDosServicos/dadosAutorizacao/numeroGuiaOperadora"))
				jParameters["guideNumber"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoInternacao/autorizacaoDosServicos/dadosAutorizacao/numeroGuiaOperadora"))
                jParameters["situationRegistry"] := "1"
			Case oXml:XPathHasNode( cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoProrrogacao/autorizacaoDosServicos/dadosAutorizacao/numeroGuiaOperadora"))
				jParameters["guideNumber"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoProrrogacao/autorizacaoDosServicos/dadosAutorizacao/numeroGuiaOperadora"))
                jParameters["situationRegistry"] := "1"
			Case oXml:XPathHasNode( cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoServicoOdonto/dadosAutorizacao/numeroGuiaOperadora"))
				jParameters["guideNumber"] := oXML:XPathGetNodeValue(cPathTag + SetNs("/autorizacaoProcedimento/autorizacaoServicoOdonto/dadosAutorizacao/numeroGuiaOperadora"))
                jParameters["situationRegistry"] := "1"
		EndCase

	EndIf

return jParameters

static function GetDataPrefixNs(cSoap as character,cSchema as character) as array
	local cSoapAux := "" as character
	local cMsg := "" as character
	local cErro := "" as character
	local cNameSpace := "" as character
	local nPos := 0 as numeric
	local nX := 0 as numeric
	local lRet := .T. as logical
	Local nPos2	:= 0 as numeric
	Local cXmlns := "" as character
	Local nPos1	:= 0 as numeric

	nPos := At("BODY",Upper(cSoap))
	cSoapAux := Substr(cSoap,nPos+4,len(cSoap))
	nPos := At(">",Upper(cSoapAux))
	cSoapAux := Substr(cSoapAux,nPos+1,len(cSoapAux))

	nPos := At("BODY",Upper(cSoapAux))
	for nX := 1 to nPos
		if Substr(cSoapAux,nPos-nX,1) == "<"
			cSoapAux := Substr(cSoapAux,1,nPos-(nX+1))
			Exit
		endif
	next

	nPos1 := At("XMLNS",Upper(cSoap))
	If nPos1 > 0
		nPos2 := At(">",Upper(cSoap),nPos1)
		cXmlns := subString(cSoap, nPos1, nPos2 - nPos1)
		nPos1 := At(">",Upper(cSoapAux))
		nPos2 := At("XMLNS",Upper(cSoapAux))
		cSoapPt := Substr(cSoapAux,nPos2,nPos1-1-At("XMLNS",Upper(cSoapAux)))
		cSoapPt1 := Substr(cSoapAux,1,nPos1-1)
		cSoapPt2 := Substr(cSoapAux,nPos1,len(cSoapAux))
		cSoapAux := Iif(cSoapPt $ cXmlns,Substr(cSoapAux,1,nPos2-1)+ " " + cXmlns + cSoapPt2,cSoapPt1 + " " + cXmlns + cSoapPt2)
	endIf

	if nPos == 0 .Or. empty(cSoap)
		cErro := "Erro com o pacote Soap recebido"
	endif

	if !empty(cErro)
		return {.F.,cErro}
	endif

	nPos := At("SOLICITACAOPROCEDIMENTOWS",Upper(cSoapAux))
	If Substr(cSoapAux,nPos-1,1) == ":"
		nPosNamSpc := nPos-2
		For nX := 1 to nPosNamSpc
			If Substr(cSoapAux,nPosNamSpc-nX,1) == "<"
				cNameSpace := Substr(cSoapAux,nPosNamSpc - nX +1,nPosNamSpc - (nPosNamSpc - nX))
				cNS := cNameSpace
				Exit
			EndIf
		next
	EndIf

	nPos := At("AUTORIZACAOPROCEDIMENTOWS",Upper(cSoapAux))
	If Substr(cSoapAux,nPos-1,1) == ":"
		nPosNamSpc := nPos-2
		For nX := 1 to nPosNamSpc
			If Substr(cSoapAux,nPosNamSpc-nX,1) == "<"
				cNameSpace := Substr(cSoapAux,nPosNamSpc - nX +1,nPosNamSpc - (nPosNamSpc - nX))
				cNS := cNameSpace
				Exit
			EndIf
		next
	EndIf

	cSoapXml := EncodeUTF8(cSoapAux)

return {lRet,cMsg,cSoapXml,cNameSpace}

static function SetNs(cTag as character) as character

	if !empty(cNS)
		cTag := strtran(cTag, "/", "/" + cNS + ":")
	endif

return cTag

static function GetDataHealthcareProvider(oXml as object, cPathTag as character) as character

	Local cPrest := "" as character
	Local cPathPres := cPathTag + SetNs("/cabecalho/origem/identificacaoPrestador") as character

	Default oXml := nil
	Default cPathTag := ""

	If oXml:XPathHasNode(cPathPres)
		If oXML:XPathHasNode(cPathPres + SetNs("/CNPJ"))
			cPrest := Posicione("BAU",4,xFilial("BAU")+oXML:XPathGetNodeValue(cPathPres + SetNs("/CNPJ")), "BAU_CODIGO")
		ElseIf oXML:XPathHasNode(cPathPres + SetNs("/CPF"))
			cPrest := Posicione("BAU",4,xFilial("BAU")+oXML:XPathGetNodeValue(cPathPres + SetNs("/CPF")), "BAU_CODIGO")
		ElseIf oXML:XPathHasNode(cPathPres + SetNs("/codigoPrestadorNaOperadora"))
			cPrest := oXML:XPathGetNodeValue(cPathPres + SetNs("/codigoPrestadorNaOperadora"))
		EndIf
	EndIf

return cPrest

static function IndentXML(cTextoOrig as character) as character
    Local aArea := GetArea() as array
    Local aLinhas := {} as array
    Local cEspaco := "" as character
    Local nAbriu := 0 as numeric
    Local nAtual := 0 as numeric
    Local aLinNov := {} as array
    
    If ! Empty(cTextoOrig) .And. '<?xml version=' $ cTextoOrig
        
        aLinhas := StrTokArr(cTextoOrig, Chr(13) + Chr(10))
        
        For nAtual := 1 To Len(aLinhas)
            
            If ! Empty(aLinhas[nAtual])
             
                If nAtual > 1 .And. "<" $ aLinhas[nAtual-1] .And. ! "<?" $ aLinhas[nAtual-1] .And. ! "</" $ aLinhas[nAtual-1] .And. ! "/>" $ aLinhas[nAtual-1]
                    nAbriu += 1
                EndIf
                
                cEspaco := ""
                If nAbriu > 0
                    cEspaco := Replicate('    ', 2 * (nAbriu + Iif(! "<" $ aLinhas[nAtual], 1, 0)) )
                EndIf
                
                aAdd(aLinNov, cEspaco + aLinhas[nAtual])
                
                If nAtual + 1 <= Len(aLinhas) .And. "</" $ aLinhas[nAtual+1] .And. At('<', SubStr(aLinhas[nAtual+1], 2, Len(aLinhas[nAtual+1]))) == 0
                    nAbriu -= 1
                EndIf
            EndIf
        Next
        
        cTextoOrig := ""
        For nAtual := 1 TO Len(aLinNov)
            cTextoOrig += aLinNov[nAtual] + Chr(13) + Chr(10)
        Next
    EndIf
     
    RestArea(aArea)
Return cTextoOrig
