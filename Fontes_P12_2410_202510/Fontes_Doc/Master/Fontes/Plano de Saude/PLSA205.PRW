#INCLUDE "plsa205.ch"

#include "PLSMGER.CH"

STATIC aListaDel   := {"105","106","112","114","115","119","126"}

/*/


Ŀ
Programa  PLSA205   Autor  Tulio Cesar           Data  03.07.2002 
Ĵ
Descrio  Cadastro de Lancamentos de Cobranca.                       
Ĵ
Uso        Advanced Protheus                                          
Ĵ
Parametros Nenhum                                                     
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL           
Ĵ
Programador  Data    BOPS   Motivo da Alterao                     
Ĵ
ٱ


/*/
Function PLSA205
LOCAL nFor
//Ŀ
// Define variaveis PRIVATE...                                              
//
PRIVATE aRotina   := MenuDef()
PRIVATE cCadastro := STR0006 //"Lancamentos de Cobranca"
PRIVATE cAlias    := "BFQ"

If Empty(PlsIntPad())
     Return //"O Usuario '"###"' esta sem parametrizacao para uma ou mais Operadoras. Isso deve ser feito antes de utilizar esta e outras opcoes do sistema"
EndIf

If FindFunction("PLSCRIABFQ")
   PLSCRIABFQ() //para criar BFQ que ainda nao existe... PLSCRIABFQ esta no psla627.prw
Endif   
//Excluir lancamento sem utilizacao quando ainda existitem
For nFor := 1 To Len(aListaDel)
    BFQ->(DbSetOrder(1))
    If BFQ->(DbSeek(xFilial("BFQ")+PLSINTPAD()+aListaDel[nFor]))
       BFQ->(RecLock("BFQ",.F.))
       BFQ->(DbDelete())
       BFQ->(MsUnLock())
    Endif
Next

If BFQ->(FieldPos("BFQ_ATIVO")) > 0
   BFQ->(DbSetOrder(1))
   If BFQ->(DbSeek(xFilial("BFQ")+PLSINTPAD()))
      While ! BFQ->(Eof()) .And. BFQ->(BFQ_FILIAL+BFQ_CODINT) == xFilial("BFQ")+PLSINTPAD()
            If Empty(BFQ->BFQ_ATIVO) 
               BFQ->(RecLock("BFQ",.F.))
               BFQ->BFQ_ATIVO := "1"
               BFQ->(MsUnLock())              
            Endif   
      BFQ->(DbSkip())
      Enddo
   Endif   
Endif

//Ŀ
// Verifica a chamada de acordo com tipo...                            
//
BFQ->(mBrowse(ndLinIni,ndColIni,ndLinFin,ndColFin,cAlias))
BFQ->(DbClearFilter())
//Ŀ
// Fim da Rotina                                                       
//
Return              
/*/


Ŀ
Funcao     PLSA205Num  Autor  Tulio Cesar           Data  15.03.01 
Ĵ
Descricao  Retorna o proximo codigo                                    
ٱ


/*/
Function PLSA205Num(cCodInt,cPropri)
LOCAL nRet    := 0
LOCAL nOrdBFQ := BFQ->(IndexOrd())
LOCAL nTam    := Len(BFQ->BFQ_CODLAN)

BFQ->(DbSetOrder(1))

BFQ->(DbSeek(xFilial("BFQ")+cPropri+cCodInt+Replicate("9",nTam),.T.))
BFQ->(DbSkip(-1))

If BFQ->(BFQ_FILIAL+BFQ_PROPRI+BFQ_CODINT) <> xFilial("BFQ")+cPropri+cCodInt
   nRet := StrZero(1,nTam)
Else
   nRet := Soma1(BFQ->BFQ_CODLAN)
Endif

BFQ->(DbSetOrder(nOrdBFQ))

Return(nRet)
/*/


Ŀ
Programa   PLSA205MOV  Autor  Tulio Cesar        Data  13.07.1999 
Ĵ
Descrio  Movimentacao da TDE.                                       
Ĵ
Uso        PLSA205()                                                  
Ĵ
Parametros Padrao do mBrowse                                          
ٱ


/*/
Function PLSA205MOV(cAlias,nReg,nOpc)
Local I__f := 0
//Ŀ
// Define variaveis LOCAL...                                                
//
LOCAL nOpcA     := 0
LOCAL nRegAnt   := nReg
LOCAL aSize 	:= {}
LOCAL aObjects 	:= {}
LOCAL aInfo 	:= {}
LOCAL aPosObj 	:= {}

//Ŀ
// Define variaveis...                                                      
//
PRIVATE oDlg
PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE aVetTrab := {}
PRIVATE aChave   
PRIVATE oEnchoice
PRIVATE oGet
PRIVATE bOK      := {|| nOpca := 1,If(Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=2),If(nOpca==1,oDlg:End(),.F.) }
PRIVATE bCancel  := {||oDlg:End()}   
PRIVATE n        := 1
//Ŀ
// Testa se pode editar ou excluir...                                       
//
If nOpc == K_Excluir .And. BFQ->BFQ_PROPRI == "1"
	MsgInfo(STR0007) //"Alteracao/Exclusao somente de lancamentos criados pelo cliente"
   	Return
Endif

If nOpc == K_Incluir
   Copy "BFQ" TO Memory Blank
Else
   Copy "BFQ" TO Memory  
EndIf        
BFQ->(DbGoTo(nRegAnt))  

aSize := MsAdvSize() 

aObjects := {}       
AAdd( aObjects, { 100, 200, .T., .T. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )

//Ŀ
// Inicia a construcao do Dialogo com usuario...                            
//
DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL 
//Ŀ
// Monta Enchoice...                                                        
//
oEnchoice := MSMGET():New(cAlias,nReg,nOpc,,,,,aPosObj[1],,,,,,oDlg,,,.F.)
//Ŀ
// Ativa o Dialogo...                                                       
//
ACTIVATE MSDIALOG oDlg ON INIT Eval( { || EnChoiceBar(oDlg,bOK,bCancel,.F.,{}) })
//Ŀ
// Tratamento para atualizacoes...                                          
//
If nOpcA == K_OK
   //Ŀ
   // Trata a exclusao nao podendo haver modificacoes...                       
   //
   If ( nOpc <> K_Visualizar )
      //Ŀ
      // Atualiza Enchoice...                                                     
      //
      BFQ->(DbGoTo(nRegAnt))
      PLUPTENC("BFQ",nOpc)   	
   Endif  
EndIf
//Ŀ
// Fim da Rotina                                                            
//
Return

/*/


Ŀ
Programa  MenuDef    Autor  Darcio R. Sporl        Data 02/01/2007
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Private aRotina := {	{ STR0001	,'AxPesqui'    , 0 , K_Pesquisar  , 0, .F.},; //'Pesquisar'
											{ STR0002	,'PLSA205MOV'  , 0 , K_Visualizar , 0, Nil},; //'Visualizar'
											{ STR0003	,'PLSA205MOV'  , 0 , K_Incluir    , 0, Nil},; //'Incluir'
											{ STR0004	,'PLSA205MOV'  , 0 , K_Alterar    , 0, Nil},; //'Alterar'
											{ STR0005	,'PLSA205MOV'  , 0 , K_Excluir    , 0, Nil} } //'Excluir'
Return(aRotina)