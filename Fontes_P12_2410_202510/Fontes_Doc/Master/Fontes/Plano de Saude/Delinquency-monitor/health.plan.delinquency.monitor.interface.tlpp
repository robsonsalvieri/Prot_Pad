#INCLUDE "totvs.ch"
#INCLUDE "health.plan.delinquency.monitor.interface.ch"
#INCLUDE "fwmvcdef.ch"
#INCLUDE "tlpp-core.th"

namespace totvs.protheus.health.delinquency.monitor

//variavel estatica utilizada para disponibilizar os beneficiarios selecionas nas funcoes criadas nos botoes 
//customizados da tela, essa variavel sera enviada atraves do metodo estatico getSelecBeneficiaries
static oJReturnSelectBeneficiares := JsonObject():New()
static oDlgMain as object

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} DelinquencyInterface
Monitor de inadimplência
@version P12
/*/
Class DelinquencyInterface
	
	public data aBeneficiariesNoInformation       as array
	public data aClientsNoInformation      	      as array
	
	protected data oBeneficiariesInterface 	      as object
	
	private data cHealtOperatorCode     	      as character
	private data cCompanyCode           	      as character
	private data cContractCode          	      as character
	private data cSubcontractCode       	      as character
	private data dDateReturn           	          as date
	private data lPressNewFilter           	      as logical
	private data lPressRefresh          	      as logical
	private data lMarkAll               	      as logical
	private data oJSelecBeneficiaries   	      as json
	private data oMonitorService        		  as object
	private data oInvoicesInterface     		  as object
	private data oReasonInterface        		  as object

	public Method new()  
	public Method showDelinquencyMonitor(lHideQstion as logical)
	public Method markAllBeneficiaries(oMsgRun as Object) 
	public Method DelinquencyNotificationEmail() 
	public Method getClientsNoInformation()       as array
	public Method getBeneficiariesNoInformation() as array
	public Method sendNotification(oFwRun as object) 
	public Method setPressNewFilter(lValue as logical) 
	public Method setPressButtonRefresh(lValRefresh as logical) 

	protected Method createDelinquencyMonitor()
	protected Method showQuestions(lHideQuestion as logical)  as logical  
	protected Method markBeneficiaries() 
	protected Method optionsRN593(cInvoiceDate as character, cRegisBenef as character)
	protected Method filterRN593(nSelFilter as integer)
	protected Method getPressNewFilter()          as logical 
	protected Method getPressButtonRefresh()      as logical 
	protected Method documentation() 

	private Method createGridBeneficiaries(oPanelUp as object, cNameDataTable as character)
	private Method createGridInvoices(oPanLeft as object, cAliasInvoices as character)
	private Method createReasonContact(oPanRight as object)
	private Method createLawers(oDialogMain as object) 
	private Method notificationWithoutEmails() 
	private Method delPosArrayNoMails(cNumRecno as character) 
	private Method cleanFilter() 
	private Method retClientData() as array
	private Method showErrosSendMail(aErrorData as array) 
	private Method validateEmailPattern(cRecKey as character) 
	private Method customizeButtonsBeneficiaries() 
	private Method customizeLegendReason() 
	private Method noEmails(cEmailBeneficiarie  as character, cRegister   as character, cBeneficiarieName as character,;
                            cClientEmail        as character, cClientCode as character, cStore            as character,;
							cClientName         as character, cRecnoLine  as character) as array 
	
	static Method getSelecBeneficiaries() as json
	static Method updateMessage()
EndClass

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} New
Construtor 
@version P12
/*/
Method new() Class DelinquencyInterface
	self:oMonitorService             := totvs.protheus.health.delinquency.monitor.DelinquencyService():New()
	self:oJSelecBeneficiaries        := JsonObject():New()
	self:aBeneficiariesNoInformation := {} 
	self:aClientsNoInformation       := {} 
	self:lMarkAll 					 := .F.
	self:lPressRefresh               := .F.
	self:lPressNewFilter             := .F.
	
	oJReturnSelectBeneficiares       := JsonObject():New()

Return self

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} showQuestions
Pergunte com as opcoes de filtro
@version P12
/*/
Method showQuestions(lHideQuestion as logical) as logical Class DelinquencyInterface
	
	Local lRetQuestion := .F.

	Default lHideQuestion := .T.
	
	If !lHideQuestion
		lRetQuestion := Pergunte("MONINAD")
	Else
		lRetQuestion := .T.
	EndIf

	self:dDateReturn         := MV_PAR01     
	self:cHealtOperatorCode  := MV_PAR02
	self:cCompanyCode		 := MV_PAR03         
	self:cContractCode	     := MV_PAR04
	self:cSubcontractCode	 := MV_PAR05

Return lRetQuestion

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} getPressNewFilter
Indica se o botão de novo filtro foi pressionado para controlar a
exibição do pergunte para realizar novo filtro no metodo 
showDelinquencyMonitor
@version P12
/*/
Method getPressNewFilter() Class DelinquencyInterface
Return self:lPressNewFilter

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} setPressNewFilter
Altera a varavel que indica se o botão de novo filtro foi 
pressionado para controlar a exibição do pergunte para realizar
novo filtro no metodo showDelinquencyMonitor
@version P12
/*/
Method setPressNewFilter(lValue as logical) Class DelinquencyInterface
Return self:lPressNewFilter := lValue

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} getPressButtonRefresh
Indica se o botão de atualização foi pressionado para controlar a
exibição do pergunte para realizar recarregar a tela sem a necessidade
de exibir o pergunte 
@version P12
/*/
Method getPressButtonRefresh() Class DelinquencyInterface
Return self:lPressRefresh

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} setPressNewFilter
Altera a varavel que indica se o botão de atualização foi 
pressionado para controlar a exibição do pergunte para realizar 
recarregar a tela sem a necessidade de exibir o pergunte 
@version P12
/*/
Method setPressButtonRefresh(lValRefresh as logical) Class DelinquencyInterface
Return self:lPressRefresh := lValRefresh

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} showDelinquencyMonitor
Exibe o pergunte e exibe o monitor de inadimplencia
@version P12
/*/
Method showDelinquencyMonitor(lHideQstion as logical) Class DelinquencyInterface
	
	Default lHideQstion := .F.

	If Self:showQuestions(lHideQstion)

		//limpa as principais variaveis de controle
		self:cleanFilter()

		FWMsgRun(, {|| Self:createDelinquencyMonitor() }, STR0001, STR0002) //"Aguarde", "Localizando Beneficiarios Inadimplentes..."

		//abre a rotina novamente iniciada do botao de novo filtro, foi necessario criar um novo metodo para controlar a exibicao do pergunte
		//e fechar a enchoice aberta antes de abrir uma nova, caso contrario ira abrir varias janelas
		If self:getPressNewFilter() .OR. self:getPressButtonRefresh()

			//se o botao de atualizar for pressionado, o sistema ira recarregar a tela sem exibir o pergunte, apenas nessa situacao
			//se o botao de novo filtro for pressionado a variavel lHdQuestion ficara com o valor .F. e ira exibir o pergunte
			/*Foi necessario manipular os dados dessa forma pois mesmo atualizando as tabelas temporarias, o browser nao atualiza com
			os novos dados, sendo necessario fechar a rotina e abrir novamente*/
			lHdQuestion := self:getPressButtonRefresh()

			//o metodo executa ele mesmo para recarregar a tela e reiniciar o processo sem que o usuario tenha que fiscar buscando 
			//a rotina na opcao de menu todas as vezes
			self:showDelinquencyMonitor(lHdQuestion)
		EndIf

		self:oMonitorService:DropTables()
	EndIf
Return 

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} createDelinquencyMonitor
Monta a estrutura da interface do monitor de inadimplencia
@version P12
/*/
Method createDelinquencyMonitor() Class DelinquencyInterface

	Local oJLawers as json
	Local oJResult as json
	Local aCoors   := FWGetDialogSize(oMainWnd)
	
	//altera para false a variavel que indica que o botao de novo filtro foi pressionado
	self:setPressNewFilter(.F.)
	
	//altera para false a variavel que indica que o botao de atualizar dados foi pressionado
	self:setPressButtonRefresh(.F.)
	
	//Gera as tabelas temporarias e os dados correspondetes ao filtro selecionado
	oJResult := self:oMonitorService:loadDataTables(self:dDateReturn , self:cHealtOperatorCode,;
												    self:cCompanyCode, self:cContractCode     ,;
													self:cSubcontractCode)
	If oJResult["dataExists"]
		oDlgMain := MSDialog():New(aCoors[1],aCoors[2],aCoors[3],aCoors[4], STR0003,,,,,,,,,.T.) //"Monitor de Inadimplência"
		odlg := oDlgMain
		
		oJLawers := self:createLawers(oDlgMain)

		//monta o lawer e grid de beneficiarios
		self:createGridBeneficiaries(oJLawers["panelUp"], self:oMonitorService:getAliasDataBenef())
		
		//monta o lawer e grid de titulos em atraso
		self:createGridInvoices(oJLawers["panelLeft"], self:oMonitorService:getAliasInvoices()) 
		
		//monta o lawer e grid de contatos realizados
		self:createReasonContact(oJLawers["panelRight"] )

		oDlgMain:lCentered := .T.
		oDlgMain:Activate()
		
	else
		//se nao encontrou dados, ira exibir novamente o pergunte para realizar um novo filtro
		FWAlertWarning("<big>" + oJResult["message"] + "</big>", STR0058) //"Atenção"
		Self:showDelinquencyMonitor()
	EndIf 

Return oJResult["dataExists"]

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} createLawers
Cria os layers para a tela
@version P12
/*/
Method createLawers(oDialogMain) Class DelinquencyInterface

	Default oDialogMain := NIL

	oFWLayer := FWLayer():New()
	oFWLayer:Init( oDialogMain, .F., .T. )

	oFWLayer:AddLine( "UP", 50, .F. )
	oFWLayer:AddCollumn( "ALL", 100, .T., "UP" )
	oPanelUp := oFWLayer:GetColPanel( "ALL", "UP" )
	
	oFWLayer:AddLine( "DOWN", 50, .F. )
	oFWLayer:AddCollumn( "LEFT" , 50, .T., "DOWN" )
	oFWLayer:AddCollumn( "RIGHT", 50, .T., "DOWN" )

	oPanelLeft := oFWLayer:GetColPanel( "LEFT" , "DOWN" ) 
	oPanelRight := oFWLayer:GetColPanel( "RIGHT", "DOWN" )

Return {"panelUp": oPanelUp, "panelLeft": oPanelLeft, "panelRight":oPanelRight} 

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} createGridBeneficiaries
Cria o grid de beneficiarios
@version P12
/*/
Method createGridBeneficiaries(oPanUp as object, cNameDataTable as character) Class DelinquencyInterface

	Local aSeek      := {}
	Local aAliasLgPd := {"BA1", "SE1"}
	Local oLgpdBenef := CENFUNLGP():New()

	Default oPanUp 		   := NIL
	Default cNameDataTable := NIL
	
	oLgpdBenef:useLogUser()
	
	oLgpdBenef:setAlias(aAliasLgPd)

	self:oBeneficiariesInterface := FWMarkBrowse():New()
	self:oBeneficiariesInterface:SetOwner( oPanUp ) 
	self:oBeneficiariesInterface:SetProfileID( "1" )
	self:oBeneficiariesInterface:SetDescription( STR0004 ) //"Beneficiários Inadimplentes"		
	self:oBeneficiariesInterface:SetAlias( cNameDataTable )
	self:oBeneficiariesInterface:SetTemporary(.T.)
	self:oBeneficiariesInterface:ForceQuitButton()
	self:oBeneficiariesInterface:DisableDetails()
	self:oBeneficiariesInterface:DisableReport()
	self:oBeneficiariesInterface:SetUseFilter(.T.)

	self:oBeneficiariesInterface:SetFieldMark( "MARK" )
	self:oBeneficiariesInterface:SetAfterMark({||self:markBeneficiaries()})
	self:oBeneficiariesInterface:SetAllMark({|| FwMsgRun(,{|oMsgRun| self:markAllBeneficiaries(oMsgRun)}, STR0005, STR0006)}) //"Aguarde, Processo em Andamento..."#"Identificando linhas afetadas..."

	self:oBeneficiariesInterface:SetColumns({{STR0007, {||BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO}, "C",,0, 17 }}) //"Matrícula"
	self:oBeneficiariesInterface:SetColumns({{STR0008, {||oLgpdBenef:verCamNPR("BA1_NOMUSR", BA1_NOMUSR)}, "C",,1, 99}}) //"Nome Beneficiário"
	self:oBeneficiariesInterface:SetColumns({{STR0009, {||BA3_CODCLI}, "C",,0, 6 }}) //"Código Cliente"
	self:oBeneficiariesInterface:SetColumns({{STR0010, {||BA3_LOJA  }, "C",,0, 6 }}) //"Loja Cliente"
	self:oBeneficiariesInterface:SetColumns({{STR0011, {||oLgpdBenef:verCamNPR("E1_NOMCLI" , E1_NOMCLI )}, "C",,1, 99}}) //"Nome Cliente"
	self:oBeneficiariesInterface:SetColumns({{STR0012, {||oLgpdBenef:verCamNPR("BA1_NOMSOC", BA1_NOMSOC)}, "C",,1, 99}}) //"Nome Social"
	self:oBeneficiariesInterface:SetColumns({{STR0013, {||BE4_DTDIGI}, "D",,0, 8 }}) //"Data Internação"
	self:oBeneficiariesInterface:SetColumns({{STR0014, {||BA1_MATANT}, "C",,0, 17}}) //"Matrícula Antiga"
	self:oBeneficiariesInterface:SetColumns({{STR0015, {||BA1_CODINT}, "C",,0, 4 }}) //"Operadora"
	self:oBeneficiariesInterface:SetColumns({{STR0016, {||BA1_CODEMP}, "C",,0, 4 }}) //"Empresa"
	self:oBeneficiariesInterface:SetColumns({{STR0017, {||BA1_CONEMP}, "C",,0, 12}}) //"Contrato"
	self:oBeneficiariesInterface:SetColumns({{STR0018, {||BA1_VERCON}, "C",,0, 3 }}) //"Versão"
	self:oBeneficiariesInterface:SetColumns({{STR0019, {||BA1_SUBCON}, "C",,0, 9 }}) //"Sub-Contrato"
	self:oBeneficiariesInterface:SetColumns({{STR0018, {||BA1_VERSUB}, "C",,0, 3 }}) //"Versão"
	
	AaDd(aSeek, {STR0020, {{"","D", 8 , ,STR0020}}}) //"Data de Vencimento"
	AaDd(aSeek, {STR0021, {{"","C", 17, ,STR0021}}}) //"Matrícula" 
	AaDd(aSeek, {STR0022, {{"","C", 99, ,STR0022}}}) //"Nome"

	Self:oBeneficiariesInterface:SetSeek(.T.,aSeek)

	self:oBeneficiariesInterface:SetFieldFilter({ {"BA1_NOMUSR", STR0008, "C", 99, 0, ""},; //"Nome Beneficiário"
												  {"BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO", STR0021, "C", 17, 0, ""},; //"Matrícula"
												  {"BA3_CODCLI", STR0009, "C", 6 , 0, ""},; //"Código Cliente"
												  {"E1_NOMCLI" , STR0011, "C", 99, 0, ""}; //"Nome Cliente"
												};
											   )
	//cria os botoes do grid
	/*altera para true a variavel lPressNewFilter para indicar que a rotina esta sendo fechada para realizar um novo filtro,
	//onde sera utilizada no metodo getPressNewFilter*/
	self:oBeneficiariesInterface:addButton(STR0023, {||self:setPressNewFilter(.T.), oDlgMain:END()},,1) //"Novo Filtro"
	self:oBeneficiariesInterface:addButton(STR0024, {||self:DelinquencyNotificationEmail()        },,1) //"Enviar Notificação de Inadimplência"

	/*altera para true a variavel lPressRefresh para indicar que a rotina foi chamada pelo botao de atualizacao, dessa forma
	ela ira fechar a tela, recarregar os dados e abrir novamente a rotina sem abrir o pergunte*/
	//variavel utilizada no metodo getPressButtonRefresh
	self:oBeneficiariesInterface:addButton(STR0025, {||self:setPressButtonRefresh(.T.), oDlgMain:END()},,1) //"Atualizar Dados"
	self:oBeneficiariesInterface:addButton(STR0026, {||Self:optionsRN593()                            },,1) //"Filtros RN 593"
	self:customizeButtonsBeneficiaries()
	self:oBeneficiariesInterface:addButton(STR0061, {||Self:documentation()                            },,1) //"Documentação"
	self:oBeneficiariesInterface:Activate()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} customizeButtonsBeneficiaries
Cria os botoes customizados para a rotina
@version P12
/*/
Method customizeButtonsBeneficiaries() Class DelinquencyInterface

	Local aMenuOptions as array
	Local nLenMenuOpt  as integer
	Local nLn		   as integer

	If ExistBlock("PLMON001")
		aMenuOptions := ExecBlock("PLMON001", .F., .F.)

		nLenMenuOpt := Len(aMenuOptions)

		For nLn := 1 To nLenMenuOpt
			self:oBeneficiariesInterface:addButton(aMenuOptions[nLn][1], aMenuOptions[nLn][2],,1)
		Next
	EndIf
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} createGridInvoices
Cria o grid de titulos em atraso
@version P12
/*/
Method createGridInvoices(oPanLeft as object, cAliasInvoices as character) Class DelinquencyInterface

	Local oRelacion        as object
	
	Default oPanLeft 	   := NIL
	Default cAliasInvoices := ""

	self:oInvoicesInterface:= FWMBrowse():New()
	self:oInvoicesInterface:SetOwner( oPanLeft )
	self:oInvoicesInterface:SetProfileID( "2" )
	self:oInvoicesInterface:SetDescription( "Títulos em Atraso" )
	self:oInvoicesInterface:SetAlias( cAliasInvoices )
	self:oInvoicesInterface:SetTemporary(.T.)
	self:oInvoicesInterface:DisableDetails()
	self:oInvoicesInterface:SetUseFilter(.T.)
	self:oInvoicesInterface:SetMenuDef( "" )

	self:oInvoicesInterface:SetColumns({{STR0027, {||E1_PREFIXO}, "C",, 0, 3 }}) //"Prefixo"
	self:oInvoicesInterface:SetColumns({{STR0028, {||E1_NUM    }, "C",, 0, 18}}) //"Número"
	self:oInvoicesInterface:SetColumns({{STR0029, {||E1_VENCTO }, "D",,0, 8  }}) //"Vencimento"
	self:oInvoicesInterface:SetColumns({{STR0030, {||DateDiffDay(E1_VENCTO, dDataBase) }, "N",,0, 4}}) //"Dias em Atraso"
	self:oInvoicesInterface:SetColumns({{STR0031, {||E1_VALOR  }, "N","@E 99,999,999,999,999.99", 0, 17, 2}}) //"Valor"
	self:oInvoicesInterface:SetColumns({{STR0032, {||E1_VENCREA}, "D",, 0, 8 }}) //"Vencimento Real"
	self:oInvoicesInterface:SetColumns({{STR0033, {||E1_EMISSAO}, "D",, 0, 8 }}) //"Emissão"
	self:oInvoicesInterface:SetColumns({{STR0034, {||E1_MESBASE}, "C",, 0, 3 }}) //"Mês Base"
	self:oInvoicesInterface:SetColumns({{STR0035, {||E1_ANOBASE}, "C",, 0, 4 }}) //"Ano Base"	
	self:oInvoicesInterface:SetColumns({{STR0036, {||E1_PARCELA}, "C",, 0, 3 }}) //"Parcela"
	self:oInvoicesInterface:SetColumns({{STR0037, {||E1_TIPO   }, "C",, 0, 3 }}) //"Tipo"
	self:oInvoicesInterface:SetColumns({{STR0038, {||E1_PLNUCOB}, "C",, 0, 12}}) //"Lote Cobrança"
	self:oInvoicesInterface:SetColumns({{STR0017, {||E1_CONEMP }, "C",, 0, 12}}) //"Contrato"	
	self:oInvoicesInterface:SetColumns({{STR0018, {||E1_VERCON }, "C",, 0, 3 }}) //"Versão"
	self:oInvoicesInterface:SetColumns({{STR0019, {||E1_SUBCON }, "C",, 0, 9 }}) //"Sub-Contrato"
	self:oInvoicesInterface:SetColumns({{STR0018, {||E1_VERSUB }, "C",, 0, 3 }}) //"Versão"
	self:oInvoicesInterface:SetColumns({{STR0009, {||E1_CLIENTE}, "C",, 0, 6 }}) //"Código Cliente" 
	self:oInvoicesInterface:SetColumns({{STR0010, {||E1_LOJA   }, "C",, 0, 2 }}) //"Loja Cliente"
	
	self:oInvoicesInterface:SetFieldFilter({ {"E1_PREFIXO", STR0027, "C", 3 , 0, ""},; //"Prefixo"
										     {"E1_NUM"    , STR0028, "C", 18, 0, ""},; //"Número"
										     {"E1_VENCTO" , STR0029, "D", 8 , 0, ""},; //"Vencimento"
										     {"E1_VENCREA", STR0032, "D", 8 , 0, ""},; //"Vencimento Real"
										     {"E1_EMISSAO", STR0033, "D", 8 , 0, ""},; //"Emissão"
										     {"E1_MESBASE", STR0034, "C", 2 , 0, ""},; //"Mês Base"
										     {"E1_ANOBASE", STR0035, "C", 4 , 0, ""},; //"Ano Base"
										     {"E1_VALOR"  , STR0031, "N", 8 , 2, ""},; //"Valor"
										     {"E1_TIPO"   , STR0037, "C", 3 , 0, ""} ; //"Tipo" 
									        };
									      )
	self:oInvoicesInterface:Activate()

	oRelacion:= FWBrwRelation():New()
	oRelacion:AddRelation( self:oBeneficiariesInterface, self:oInvoicesInterface,;
						   {;
						     {"BM1_CODINT", "BA1_CODINT"}, {"BM1_CODEMP", "BA1_CODEMP"},;
						     {"BM1_MATRIC", "BA1_MATRIC"}, {"BM1_TIPREG", "BA1_TIPREG"},;
						     {"BM1_DIGITO", "BA1_DIGITO"};
						   };
						 )
	oRelacion:Activate()
Return

////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} createReasonContact
Cria o grid de contatos realizados entre cliente e operadora
@version P12
/*/
Method createReasonContact(oPanRight as object) Class DelinquencyInterface

	Local oRelacionReason  as object
	
	Default oPanRight := NIL 

	self:oReasonInterface := FWMBrowse():New()
	self:oReasonInterface:AddLegend( "BRZ_CNFCON == '0'", "RED"   , STR0039) //"Recebimento Não Confirmado"
	self:oReasonInterface:AddLegend( "BRZ_CNFCON == '1'", "GREEN" , STR0040) //"Recebimento Confirmado"
	self:customizeLegendReason()
	self:oReasonInterface:SetOwner(oPanRight)
	self:oReasonInterface:SetDescription(STR0041) //"Contatos Realizados"
	self:oReasonInterface:SetMenuDef( "PLREASONCONT" )
	self:oReasonInterface:DisableDetails()
	self:oReasonInterface:DisableReport()
	self:oReasonInterface:SetAlias("BRZ") // a tabela BRZ é a unica tabela fisica
	self:oReasonInterface:SetProfileID("3")
	self:oReasonInterface:SetWalkThru(.F.)
	self:oReasonInterface:SetUseFilter(.T.)
	self:oReasonInterface:SetFieldFilter( {;
											{"BRZ_DATCON", STR0042, "D", 8, 0, ""},; //"Data do Contato"
	                                       	{"BRZ_ORICON", STR0043, "C", 2, 0, ""}; //"Origem do Contato"
										  };
										)
	self:oReasonInterface:Activate()

	oRelacionReason := FWBrwRelation():New()
	oRelacionReason:AddRelation( self:oBeneficiariesInterface, self:oReasonInterface,; 
								 {;
									{"BRZ_FILIAL", "xFilial('BRZ')"},;
									{"BRZ_MATRIC", "BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO"};
								 };
							   )
	oRelacionReason:Activate()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} customizeLegendReason
Permite customizar as legendas do browser de contatos
@version P12
/*/
Method customizeLegendReason() Class DelinquencyInterface
	
	Local aLegends     as array
	Local nLenLegends  as integer
	Local nLnFor	   as integer

	If ExistBlock("PLMON002")
		aLegends := ExecBlock("PLMON002",.F.,.F.)

		nLenLegends := Len(aLegends)

		For nLnFor := 1 To nLenLegends
			self:oReasonInterface:AddLegend( aLegends[nLnFor][1], aLegends[nLnFor][2], aLegends[nLnFor][3] )
		Next
	EndIf
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} optionsRN593
Cria a janela com as opções referente aos filtros da RN593
@version P12
/*/
Method optionsRN593( cInvoiceDate as character, cRegisBenef as character) Class DelinquencyInterface

	Local oRadio as object
	Local oModal as object
	Local nSelecRadio := 1
	Local aItems := {STR0044,; //"Mais de 50 Dias de Atraso Sem Envio de Notificação"
					 STR0045,; //"Mais de 50 Dias de Atraso Com Prazo de Retorno Expirado"
					 STR0046,; //"Mais de 50 Dias de Atraso Dentro do Prazo de Retorno"
					 STR0047,; //"Beneficiários Inadimplentes e Atualmente Internados"
					 STR0048} //"Limpar Filtro"
	
	Default cInvoiceDate := ""
	Default cRegisBenef  := ""

	oModal:= FWDialogModal():New()      
    oModal:SetEscClose(.T.)
    oModal:setTitle(STR0049) //"Resolução Normativa 593 "
    oModal:setSubTitle(STR0050) //"Filtros Para Atender a RN 593"
    oModal:setSize(150, 210)
    oModal:createDialog()
    oModal:addCloseButton(nil, "Fechar")
    oModal:addOkButton({||FWMsgRun(, {|| self:filterRN593(nSelecRadio)},STR0001, STR0051), oModal:OOWNER:END()},"Confirmar") //"Aguarde"#"Atualizando os Dados da Tela..."
    
	oContainer := TPanel():New( ,,, oModal:getPanelMain() )
    oContainer:Align := CONTROL_ALIGN_ALLCLIENT
	
	oRadio := TRadMenu():New (20,20,aItems,{||},oContainer,,{||},,,,,,170,12,,,,)
	oRadio:bSetGet := {|u|Iif (PCount()==0,nSelecRadio,nSelecRadio:=u)}
  	
	oModal:Activate()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} filterRN593
Realiza o filtro no grid de beneficiarios de acordo com a opção 
selecionada nos filtros da RN593 e cria o texto no canto superior esquerdo 
de acordo com o filtro selecionado
@version P12
/*/
Method filterRN593(nSelFilter as integer) Class DelinquencyInterface
	
	Local cFilter      as character
	Default nSelFilter := 0

	self:cleanFilter()

	If     nSelFilter == 1 //Mais de 50 Dias de Atraso Sem Envio de Notificação

		oFont := TFont():New("Arial",,-16,,.T.)
		TSay():New(6,120,{|| "- " + STR0044},oDlgMain,,oFont,,,,.T.,CLR_RED,CLR_WHITE,250,15,,,,,,.T.) //"Mais de 50 Dias de Atraso Sem Envio de Notificação"

		cFilter := 	" QTD_REGTRO > 1                 .AND. DAT_LIMITE <= (dDataBase - 50) .AND. "+;
					" (EMPTY(BRZ_DATCON) .OR. DateDiffDay(BRZ_DATCON, dDataBase) < 0)     .AND. "+;
					" BA3_DATBAS >= STOD('20241201') .AND.  BA3_COBNIV == '1'             .AND. "+;
					" EMPTY(AllTrim(BE4_CODRDA)) "

		self:oBeneficiariesInterface:SetFilterDefault(cFilter)
		self:oBeneficiariesInterface:Refresh(.T.)
	
	ElseIf  nSelFilter == 2 //Mais de 50 Dias de Atraso Com Prazo de Retorno Expirado

		oFont := TFont():New("Arial",,-16,,.T.)
		TSay():New(6,120,{|| "- " + STR0045},oDlgMain,,oFont,,,,.T.,CLR_RED,CLR_WHITE,250,15,,,,,,.T.) //"Mais de 50 Dias de Atraso Com Prazo de Retorno Expirado"

		cFilter := 	" QTD_REGTRO > 1     .AND. DAT_LIMITE <= (dDataBase - 50)           .AND. "+;
					" !EMPTY(BRZ_DATCON) .AND. DateDiffDay(BRZ_DATCON, dDataBase) >= 10 .AND. "+;
					" BA3_COBNIV == '1'  .AND. BA3_DATBAS >= STOD('20241201')           .AND. "+; 
					" EMPTY(AllTrim(BE4_CODRDA)) "	

		self:oBeneficiariesInterface:SetFilterDefault(cFilter)
		self:oBeneficiariesInterface:Refresh(.T.)

	ElseIf  nSelFilter == 3 //Mais de 50 Dias de Atraso Dentro do Prazo de Retorno
		
		oFont := TFont():New("Arial",,-16,,.T.)
		TSay():New(6,120,{|| "- " + STR0046},oDlgMain,,oFont,,,,.T.,CLR_RED,CLR_WHITE,250,15,,,,,,.T.) //"Mais de 50 Dias de Atraso Dentro do Prazo de Retorno"

		cFilter := 	" QTD_REGTRO > 1                 .AND. DAT_LIMITE <= (dDataBase - 50)          .AND. "+;
					" BA3_COBNIV == '1'              .AND. DateDiffDay(BRZ_DATCON, dDataBase) < 10 .AND. "+;
					" BA3_DATBAS >= STOD('20241201') .AND. EMPTY(AllTrim(BE4_CODRDA)) "

		self:oBeneficiariesInterface:SetFilterDefault(cFilter)
		self:oBeneficiariesInterface:Refresh(.T.)

	ElseIf  nSelFilter == 4 //Beneficiários Inadimplentes Internados
		
		oFont := TFont():New("Arial",,-16,,.T.)
		TSay():New(6,120,{|| "- " + STR0047},oDlgMain,,oFont,,,,.T.,CLR_RED,CLR_WHITE,250,15,,,,,,.T.) //"Beneficiários Inadimplentes e Atualmente Internados"

		cFilter := 	" QTD_REGTRO > 1     .AND. DAT_LIMITE <= (dDataBase - 50) .AND. "+;
					" BA3_COBNIV == '1'  .AND. E1_VENCTO <= (dDataBase - 50)  .AND. "+;
					" BA3_DATBAS >= STOD('20241201')       .AND. !EMPTY(AllTrim(BE4_CODRDA)) "

		self:oBeneficiariesInterface:SetFilterDefault(cFilter)
		self:oBeneficiariesInterface:Refresh(.T.)
	
	ElseIf  nSelFilter == 5 //Limpar Filtro
		
		TSay():New(6,120,{|| ""},oDlgMain,,,,,,.T.,CLR_RED,CLR_WHITE,250,15,,,,,,.T.)

		self:oBeneficiariesInterface:SetFilterDefault("@ D_E_L_E_T_ = ' ' ")
		self:oBeneficiariesInterface:Refresh(.T.)
	EndIf 
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} markBeneficiaries
Marca ou desmarca uma linha do grid de beneficiarios
@version P12
/*/
Method markBeneficiaries() Class DelinquencyInterface 

	Local cRecno := cValToChar((self:oMonitorService:getAliasDataBenef())->(Recno())) //recebe o recno da linha selecionada

	//verifica se a linha esta desmarcada
	If self:oBeneficiariesInterface:IsMark(self:oBeneficiariesInterface:Mark())

		//valida e manipula os emails do beneficiario selecionado
		self:validateEmailPattern(cRecno)
	Else
		
		//no caso da desmarcacao de uma linha, o atributo que havia gravado os dados da selecao sera excluido do json de envio de email
		self:oJSelecBeneficiaries:DelName(cRecno)

		oJReturnSelectBeneficiares := self:oJSelecBeneficiaries

		//deleta os dados do json que corresponde aos e-mails que estao fora do padrao
		self:delPosArrayNoMails(cRecno)
	EndIf
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} validateEmailPattern
Verifica se os emails dos beneficiarios selecionados estão dentro do
padrão, se estiver no padrão, irá inserir os dados no objeto 
oJSelecBeneficiaries que posteriormente será utilizado como base 
para o envio dos e-mails. Se estiver fora do padrão, irá inserir os 
dados nos arrays aClientsNoInformation e aBeneficiariesNoInformation 
e posteriormente serao exibidos em um alerta sobre os emails que estão
fora do padrão
@version P12
/*/
Method validateEmailPattern(cRecKey as character) Class DelinquencyInterface 

	Local aClientDt       := {}
	Local aRetVldNoEmails := {}
	Local cEmailBenef  	  := (self:oMonitorService:getAliasDataBenef())->BA1_EMAIL
	Local cBenRegister 	  := (self:oMonitorService:getAliasDataBenef())->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)
	Local cBenName     	  := (self:oMonitorService:getAliasDataBenef())->BA1_NOMUSR
	Local cCliCode     	  := (self:oMonitorService:getAliasDataBenef())->BA3_CODCLI
	Local cCliStore    	  := (self:oMonitorService:getAliasDataBenef())->BA3_LOJA
	
	Default cRecKey       := ""

	//retorna nome e e-mail do cliente
	aClientDt := self:retClientData((self:oMonitorService:getAliasDataBenef())->(BA3_CODCLI + BA3_LOJA))
	
	//verifica se os e-mails esta dentro 
	aRetVldNoEmails := self:noEmails(cEmailBenef, cBenRegister, cBenName,;
								     aClientDt[2]/*e-mail*/, cCliCode, cCliStore,;
									 aClientDt[1]/*nome*/  , cRecKey)

	//popula os dados no json que sera utilizado para enviar as notificacoes
	self:oJSelecBeneficiaries[cRecKey] := JsonObject():New()
	self:oJSelecBeneficiaries[cRecKey]["beneficiarie"] := JsonObject():New()
	self:oJSelecBeneficiaries[cRecKey]["beneficiarie"]["register"] := (self:oMonitorService:getAliasDataBenef())->BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO
	self:oJSelecBeneficiaries[cRecKey]["beneficiarie"]["name"    ] := (self:oMonitorService:getAliasDataBenef())->BA1_NOMUSR
	
	//se o e-mail do beneficiario estiver dentro do padrao grava o json para envio de e-mail posteriormente
	If aRetVldNoEmails[1] //possui email do beneficiario
		self:oJSelecBeneficiaries[cRecKey]["beneficiarie"]["email"] := (self:oMonitorService:getAliasDataBenef())->BA1_EMAIL
	Else
		self:oJSelecBeneficiaries[cRecKey]["beneficiarie"]["email"] := ""
	EndIf
	
	self:oJSelecBeneficiaries[cRecKey]["client"] := JsonObject():New()
	self:oJSelecBeneficiaries[cRecKey]["client"]["code" ] := (self:oMonitorService:getAliasDataBenef())->(BA3_CODCLI + " - " + BA3_LOJA)
	self:oJSelecBeneficiaries[cRecKey]["client"]["name" ] := aClientDt[1] 

	//se o e-mail do cliente estiver dentro do padrao grava o json para envio de e-mail posteriormente
	If aRetVldNoEmails[2] //possui email do cliente
		
		self:oJSelecBeneficiaries[cRecKey]["client"]["email"] := aClientDt[2]
	Else
		self:oJSelecBeneficiaries[cRecKey]["client"]["email"] := ""
	EndIf

	//variavel estatica utilizada para disponibilizar os beneficiarios selecionas nas funcoes criadas nos botoes 
	//customizados da tela, essa variavel sera enviada atraves do metodo estatico getSelecBeneficiaries
	oJReturnSelectBeneficiares := self:oJSelecBeneficiaries

Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} markAllBeneficiaries
Marca ou desmarca todas as linhas do grid de beneficiarios
@version P12
/*/
Method markAllBeneficiaries(oMsgRun as Object) Class DelinquencyInterface 

	Local nCountRegister as integer
	Local cAllRecno      as character

	Default oMsgRun  := Nil
	
	/*variavel utilizada para identificar se todas as linhas foram marcadas ou desmarcadas, com isso ira permitir marcar
	ou desmarcar linhas que foram selecionadas ou desmarcadas isoladamente*/
	If !self:lMarkAll  
		self:lMarkAll := .T.
	Else
		self:lMarkAll := .F.
	EndIf 

	(self:oMonitorService:getAliasDataBenef())->(DbGoTop())

	While !(self:oMonitorService:getAliasDataBenef())->(EOF())

		nCountRegister++
		cAllRecno := cValToChar((self:oMonitorService:getAliasDataBenef())->(Recno()))
		
		If self:lMarkAll  

			If !self:oBeneficiariesInterface:IsMark(self:oBeneficiariesInterface:Mark())
				
				//o metodo MarkRec na marcacao chama automaticamente o metodo markBeneficiaries
				self:oBeneficiariesInterface:MarkRec()
				
				//valida e manipula os emails do beneficiario selecionado
			EndIf
		Else
			If self:oBeneficiariesInterface:IsMark(self:oBeneficiariesInterface:Mark())
				
				//o metodo MarkRec na desmarcacao chama automaticamente o metodo markBeneficiaries
				self:oBeneficiariesInterface:MarkRec()

			EndIf
		EndIf

		oMsgRun:SetText(STR0052 + cValToChar(nCountRegister) + STR0053) //"Até o momento "# " linhas foram afetadas, aguarde..."
		ProcessMessages()

		(self:oMonitorService:getAliasDataBenef())->(dbSkip())
	Enddo

	self:oBeneficiariesInterface:Refresh(.T.)
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} DelinquencyNotificationEmail
Exibe os aviso de inconsistencia nos e-mails dos beneficiarios selecionados
@version P12
/*/
Method DelinquencyNotificationEmail() Class DelinquencyInterface 

	//se existir algum cliente com inconsistencia no e-mail ou algum beneficiario com inconsistencia no e-mail 
	//um aviso sera exibido antes de iniciar o processo de envio dos e-mails
  	If !EMPTY(self:getClientsNoInformation()) .OR. !EMPTY(self:getBeneficiariesNoInformation())
	
		If FWAlertNoYes(STR0054 + Replicate(Chr(13)+Chr(10),2) +; //"Foi identificado que um ou mais endereços de e-mail não existem ou estão fora do padrão. "
	                    STR0055 + Chr(13)+Chr(10) + STR0056 +; //"Clique em <b>Sim</b> para continuar o envio ou <b>Não</b> para "
					    STR0057 , STR0058) //"visualizar a lista de inconformidades."#"Atenção"
			
			FwMsgRun(,{|oFwMsgRun| self:sendNotification(oFwMsgRun)}, STR0005, STR0059) //"Aguarde, Processo em Andamento..."#"Localizando Configuração de E-mail..."
		Else
			self:notificationWithoutEmails()
		EndIf	

	ElseIf LEN(self:oJSelecBeneficiaries:GetNames()) == 0
		FWAlertWarning(STR0060, STR0058) //"<big>Nenhum beneficiário foi selecionado</big>"#"Atenção"

	Else
		//inicia o processo de envio dos e-mails
		FwMsgRun(,{|oFwMsgRun| self:sendNotification(oFwMsgRun)}, STR0005, STR0059) //"Aguarde, Processo em Andamento..."#"Localizando Configuração de E-mail..."
	EndIf
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} sendNotification
Envia os e-mails para os beneficiarios selecionas e gera um registro 
na tabela de contatos realizados
@version P12
/*/
Method sendNotification(oFwRun as object) Class DelinquencyInterface

	Local nSendMail 	 	 as integer
	Local nOptMessage        as integer			
	Local cNameClient 		 as character
	Local cCltCode 			 as character
	Local cPosLine 			 as character
	Local cEmailBeneficiarie as character
	Local cemailClient 		 as character
	Local aErrorSendMail 	 := {} as array
	Local aPosSelection 	 := self:oJSelecBeneficiaries:GetNames()
	Local nLenSelection 	 := LEN(aPosSelection)
	Local cLnSelection  	 := cValToChar(nLenSelection)
	Local oJSendResult 		 := JsonObject():New()
	Local oEmailManager 	 := totvs.protheus.health.plan.manager.EmailManager():new()
	Local lExistClient   	 := ValType(self:oJSelecBeneficiaries[aPosSelection[1]]["client"]) == "J"
	
	Default oFwRun := NIL

	self:oMonitorService:createBulkContact()

	nOptMessage := Aviso( STR0077, STR0078,; //"Enviar E-mail"#"Deseja enviar o e-mail apenas para o titular do contrato ou para o titular e o cliente vinculado?"
							{STR0079, STR0080},; //"Apenas para o titular"#"Para o titular e para o cliente"
							3, "",,, .F., 0 )

	For nSendMail := 1 To nLenSelection

		oFwRun:SetText(STR0062 + cValToChar(nSendMail) + STR0063 + cLnSelection + STR0064) //"Até o momento "# " de " # " e-mails foram enviados, aguarde..."
		ProcessMessages()

		cPosLine := aPosSelection[nSendMail]
		cEmailBeneficiarie := AllTrim(self:oJSelecBeneficiaries[cPosLine]["beneficiarie"]["email"])

		If nOptMessage == 2 //Para o titular e para o cliente
			If lExistClient
				cEmailClient := AllTrim(self:oJSelecBeneficiaries[cPosLine]["client"]["email"])
			EndIf
		EndIf

		If EMPTY(cEmailBeneficiarie) .AND. EMPTY(cEmailClient)
			oJSendResult["sendMail"] := .F.
			
			If nOptMessage == 1 //Apenas para o titular
				oJSendResult["message"] := STR0081//"Beneficiário não possui e-mail cadastrado"

			ElseIf nOptMessage == 2 //Para o titular e para o cliente
				oJSendResult["message"]  := STR0065 //"Beneficiário e Cliente não possuem e-mail cadastrado"
				oJSendResult["sendMail"] := .F.
			EndIf
		
		ElseIf EMPTY(cEmailBeneficiarie)
			oJSendResult["message"] := STR0081//"Beneficiário não possui e-mail cadastrado"
			oJSendResult["sendMail"] := .F.
		Else
			//posiciona no registro do beneficiario na tabela temporaria BENEINAD
			self:oMonitorService:GoToBeneficiarie(cPosLine) 

			//envia o e-mail
			oJSendResult := oEmailManager:SendEmailUsingManager(cRecipient = cEmailClient, cBlindCopying = cEmailBeneficiarie)
		EndIf

		//se ocorreu algum erro no envio, sera gravado em um array para que no final exiba uma lista de erros
		If !oJSendResult["sendMail"] 
			
			If lExistClient
				cCltCode       := self:oJSelecBeneficiaries[cPosLine]["client"]["code"]
				cNameClient    := AllTrim(self:oJSelecBeneficiaries[cPosLine]["client"]["name"])
			EndIf

			cRegisterBenef := self:oJSelecBeneficiaries[cPosLine]["beneficiarie"]["register"]
			cNameBenef     := AllTrim(self:oJSelecBeneficiaries[cPosLine]["beneficiarie"]["name"])
			
			AADD(aErrorSendMail, {cPosLine, oJSendResult["message"], cRegisterBenef, cNameBenef, cEmailBeneficiarie, cCltCode, cNameClient, cEmailClient})
		Else
			//se enviou o e-mail, cria um registro no buffer 
			self:oMonitorService:insertDataBulkContact(self:oJSelecBeneficiaries[cPosLine]["beneficiarie"]["register"])
		EndIf
	Next

	//realizar o insert no banco de dados na tabela BRZ
	self:oMonitorService:flushBulkContact()
	self:oReasonInterface:Refresh(.T.)

	If !EMPTY(aErrorSendMail)
		self:showErrosSendMail(aErrorSendMail)  
		
		//se faz necessario atualizar a tela sempre gera um registro na tabela de contatos realizados
		//e isso só acontece fechando a dialog, abrindo novamente e recarregando os dados
		self:setPressButtonRefresh(.T.)
		oDlgMain:END()
	Else
		//se faz necessario atualizar a tela sempre gera um registro na tabela de contatos realizados
		//e isso só acontece fechando a dialog, abrindo novamente e recarregando os dados
		FWAlertSuccess(STR0067, STR0068) //"<big>Envio de e-mails concluido</big>" # "Sucesso"
		self:setPressButtonRefresh(.T.)
		oDlgMain:END()
	EndIf
Return 

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} showErrosSendMail
Interface que exibe os erros de cada tentativa de envio de e-mail
@version P12
/*/
Method showErrosSendMail(aErrorData as array) Class DelinquencyInterface 

	Local oTcBrowseBenef as object
	Local oTPanel    	 as object
	Local oModDialog 	 as object

	Default aErrorData   := {}

	oModDialog := FWDialogModal():New()  
    oModDialog:SetEscClose(.T.)
    oModDialog:setTitle(STR0070) //"Lista de E-mails Com Falha de Envio"
    oModDialog:setSubTitle(STR0071) //"Ocorreram Erros no Envio de Alguns E-mails, o Restante Foi Enviado Com Sucesso"
    oModDialog:setSize(250, 400)
    oModDialog:createDialog()
    oModDialog:addCloseButton(nil, "Fechar")

    oTPanel := TPanel():New( ,,, oModDialog:getPanelMain() )
    oTPanel:Align := CONTROL_ALIGN_ALLCLIENT

	oTcBrowseBenef := TCBrowse():New( 01 , 01, 395, 183,,; 
									 {"ID","ERRO", "MATRÍCULA BENEF.", "NOME BENEF.", "E-MAIL BENEF.","CÓDIGO CLIENTE",;
									  "NOME CLIENTE", "E-MAIL CLIENTE"},{}, oTPanel,,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
	
	oTcBrowseBenef:SetArray(aErrorData)
	oTcBrowseBenef:bLine := {||{ aErrorData[oTcBrowseBenef:nAt,01],;
								 aErrorData[oTcBrowseBenef:nAt,02],; 
								 aErrorData[oTcBrowseBenef:nAt,03],; 
								 aErrorData[oTcBrowseBenef:nAt,04],; 
								 aErrorData[oTcBrowseBenef:nAt,05],; 
								 aErrorData[oTcBrowseBenef:nAt,06],; 
								 aErrorData[oTcBrowseBenef:nAt,07],;
								 aErrorData[oTcBrowseBenef:nAt,08];
							    };
							}
	oModDialog:Activate()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} notificationWithoutEmails
Interface que exibe os e-mails inconsistentes dos clientes e 
dos beneficiarios
@version P12
/*/
Method notificationWithoutEmails() Class DelinquencyInterface 

	Local oTcBrowseBenef   as object
	Local oTcBrowseClient  as object
	Local oTPanel    	   as object
	Local oTFolder   	   as object
	Local oModDialog 	   as object

	oModDialog := FWDialogModal():New()  
    oModDialog:SetEscClose(.T.)
    oModDialog:setTitle(STR0072) //"Aviso de E-mails Inexistentes ou Fora do Padrão"
    oModDialog:setSize(250, 400)
    oModDialog:createDialog()
    oModDialog:addCloseButton(nil, "Fechar")
    
	oTPanel := TPanel():New( ,,, oModDialog:getPanelMain() )
    oTPanel:Align := CONTROL_ALIGN_ALLCLIENT

	oTFolder := TFolder():New( 0,0,{STR0073, STR0074},,oTPanel,,,,.T.,,398, 204 ) //"Beneficiários" # "Clientes"
	
	If !EMPTY(self:getBeneficiariesNoInformation())
		
		oTcBrowseBenef := TCBrowse():New( 01 , 01, 395, 183,, {"ID", "MATRÍCULA", "NOME", "E-MAIL"},{,,150,}, oTFolder:aDialogs[1],,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
		oTcBrowseBenef:SetArray(self:getBeneficiariesNoInformation())
		
		oTcBrowseBenef:bLine := {||{ self:getBeneficiariesNoInformation()[oTcBrowseBenef:nAt,01],;
								     self:getBeneficiariesNoInformation()[oTcBrowseBenef:nAt,02],;
								     self:getBeneficiariesNoInformation()[oTcBrowseBenef:nAt,03],;
								     self:getBeneficiariesNoInformation()[oTcBrowseBenef:nAt,04]; 
								   };
								}
	Else
		oTFolder:HidePage(1)
	EndIf

	If !EMPTY(self:getClientsNoInformation())
	
		oTcBrowseClient := TCBrowse():New( 01 , 01, 395, 183,, {"ID", "CÓDIGO", "LOJA", "NOME", "E-MAIL"},{,,,150,}, oTFolder:aDialogs[2],,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
		oTcBrowseClient:SetArray(self:getClientsNoInformation())
		
		oTcBrowseClient:bLine := {||{ self:getClientsNoInformation()[oTcBrowseClient:nAt,01],;
									  self:getClientsNoInformation()[oTcBrowseClient:nAt,02],;
									  self:getClientsNoInformation()[oTcBrowseClient:nAt,03],; 
									  self:getClientsNoInformation()[oTcBrowseClient:nAt,04],; 
									  self:getClientsNoInformation()[oTcBrowseClient:nAt,05] ;
									};
								 }
	Else
		oTFolder:HidePage(2)
	EndIf

	oModDialog:Activate()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} noEmails
Cria o grid de contatos realizados entre cliente e operadora
@version P12
/*/
Method noEmails(cBeneficiarieEmail  as character, cRegister    as character,; 
				cBeneficiarieName   as character, cClientEmail as character,;
				cClientCode         as character, cStore       as character,;
				cClientName         as character, cRecnoLine   as character) as array Class DelinquencyInterface 

	Local aRetNoEmails := {.T., .T.}

	Default cBeneficiarieEmail := ""
	Default cRegister 		   := ""
	Default cBeneficiarieName  := ""
	Default cClientEmail 	   := ""
	Default cClientCode 	   := ""
	Default cStore 		   	   := ""
	Default cClientName 	   := ""
	Default cRecnoLine 		   := ""
	
	If !IsEmail(cBeneficiarieEmail)

		//caso o e-mail esteja fora do padrao, ao tentar enviar os e-mails o sistema irá exibir uma tela com os dados
		//deste array para informar os e-mails fora do padrao e que nao poderam ser utilizado como destinatario
		AADD(self:aBeneficiariesNoInformation, {cRecnoLine, cRegister, AllTrim(cBeneficiarieName), AllTrim(cBeneficiarieEmail)})
		aRetNoEmails[1] := .F.
	EndIf
	
	If !IsEmail(cClientEmail)

		//caso o e-mail esteja fora do padrao, ao tentar enviar os e-mails o sistema irá exibir uma tela com os dados
		//deste array para informar os e-mails fora do padrao e que nao poderam ser utilizado como destinatario
		AADD(self:aClientsNoInformation, {cRecnoLine, cClientCode, cStore, AllTrim(cClientName), AllTrim(cClientEmail)})
		aRetNoEmails[2] := .F.
	EndIf

Return aRetNoEmails

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} delPosArrayNoMails
Ao desmarcar uma linha no grid de beneficiarios, os dados contidos nos arrays
aClientsNoInformation e aBeneficiariesNoInformation correspondente a 
linha desmarcada deve ser excluida do array caso exista
@version P12
/*/
Method delPosArrayNoMails(cNumRecno as character)  Class DelinquencyInterface 

	Local nScan as integer 

	Default cNumRecno := ""
	
	nScan := Ascan(self:getClientsNoInformation(),{ |x| x[1] == cNumRecno})

	If nScan > 0

		ADEL(self:getClientsNoInformation(), nScan)
		ASIZE(self:getClientsNoInformation(), LEN(self:getClientsNoInformation()) - 1)
	EndIf
	
	nScan := Ascan(self:getBeneficiariesNoInformation(),{ |x| x[1] == cNumRecno})

	If nScan > 0
		
		ADEL(self:getBeneficiariesNoInformation(), nScan)
		ASIZE(self:getBeneficiariesNoInformation(), LEN(self:getBeneficiariesNoInformation()) - 1)
	EndIf
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} getClientsNoInformation
Retorna o array contendo os e-mails inconsistentes dos clientes
vinculados aos beneficiarios
@version P12
/*/
Method getClientsNoInformation() as array Class DelinquencyInterface
Return self:aClientsNoInformation

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} getBeneficiariesNoInformation
Retorna o array contendo os e-mails inconsistentes beneficiarios
@version P12
/*/
Method getBeneficiariesNoInformation() as array Class DelinquencyInterface
Return self:aBeneficiariesNoInformation

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} retClientData
Busca e retorna o nome e e-mail do cliente
@version P12
/*/
Method retClientData() as array Class DelinquencyInterface 

	aDataSA1    := GetAdvFVal("SA1",{"A1_NOME","A1_EMAIL"}, xFilial("SA1") + ;
							  (self:oMonitorService:getAliasDataBenef())->(BA3_CODCLI + BA3_LOJA),;
							  1, {"", ""}, .T.)

Return {aDataSA1[1], aDataSA1[2]}

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} cleanFilter
Limpa as variaveis da classe
@version P12
/*/
Method cleanFilter() Class DelinquencyInterface
	
	self:oJSelecBeneficiaries        := JsonObject():New()
	self:aBeneficiariesNoInformation := {} 
	self:aClientsNoInformation       := {} 
	self:lMarkAll 					 := .F.
	
	oJReturnSelectBeneficiares       := JsonObject():New()
Return

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} getSelecBeneficiaries
Retorna um json com os dados dos beneficiarios selecionados,
esta funcao sera utilizada no ponto de entrada dos botoes
@version P12
/*/
Method getSelecBeneficiaries() as json Class DelinquencyInterface
Return oJReturnSelectBeneficiares

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} updateMessage
Este método cria uma mensagem no canto superior direito e 
é responsável por indicar ao usuário que após a inclusão
de um registro de contato, é necessário atualizar a tela para que
os filtros da RN 593 possam consultar dados atualizados
@version P12
/*/
Method updateMessage() Class DelinquencyInterface
	oFont := TFont():New("Arial",,-16,,.T.)
	aa := 430
	TSay():New(6,120,{|| STR0075},oDlgMain,,oFont,,,,.T.,CLR_HRED,,400,15,,,,,,.T.) //'- Clique no botão "Atualizar Dados" para manter a integridade na utilização do filtros da RN 593'
return 

//////////////////////////////////////////////////////////////////////
//-------------------------------------------------------------------
/*/{Protheus.doc} documentation
Documentação da rotina
@version P12
/*/
Method documentation() Class DelinquencyInterface
	ShellExecute("Open", STR0076, "", "", 1) //"https://tdn.totvs.com/pages/viewpage.action?pageId=949426525"
return 

