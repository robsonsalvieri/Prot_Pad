#INCLUDE "TOTVS.CH"

#Define DBFIELD 1
#Define JSONFIELD 2

Class HealthApiAlertDao from CenDao

    public method New(aFields) Constructor
    public method get(cCode)
    public method hasNext(nRecno)
    public method update(oRest)
    public method setDefault(cConteudo)

EndClass

Method New(aFields) Class HealthApiAlertDao
	_Super:New(aFields)  
Return self

Method get(cCode) Class HealthApiAlertDao

    Local cQuery  := ""
    LOCAL cDataBase:= AllTrim(TCGetDB())

    cQuery += " SELECT "
    cQuery += " BQ7_CODIGO, BQ7_DESCRI, BQ7_NOTIF, BQ7_TITNOT, BQ7_TIPNOT, BQ7_LAYPAD, "
    cQuery +=  iif(cDataBase $ "ORACLE", "CAST(BQ7_TXTNOT as VARCHAR2(4000)) AS BQ7_TXTNOT ", " CAST(BQ7_TXTNOT as VARCHAR(8000)) AS BQ7_TXTNOT") + ", BQ7_WPP, BQ7_TEMPLA, "
    cQuery += " BQ7_EMAIL, " + iif(cDataBase $ "ORACLE", "CAST(BQ7_TXTEMA as VARCHAR2(4000)) AS BQ7_TXTEMA ", "CAST(BQ7_TXTEMA as VARCHAR(8000)) AS BQ7_TXTEMA ")
    cQuery += " FROM " + RetSqlName('BQ7') + ' BQ7 '
    cQuery += " WHERE BQ7_FILIAL = '" + xFilial('BQ7') + "' "
    cQuery += " AND BQ7.D_E_L_E_T_ = ' ' "
    
    if !empty(cCode)
        cQuery += " AND BQ7_CODIGO = '" + cCode + "' "
    endif
    cQuery += " ORDER BY BQ7_CODIGO "

    self:setQuery(self:queryBuilder(cQuery))
    lFound := self:executaQuery()

return lFound

Method hasNext(nRecno) Class HealthApiAlertDao
    Local lTemProx := .F.
    If self:aliasSelected()
        lTemProx := !(self:getAliasTemp())->(Eof())
    EndIf
return lTemProx

Method update(oRest) class HealthApiAlertDao
    
    Local lSuccess  := .F.
    Local cCode     := oRest:getPathParamsRequest()['code']
    Local oJsonBody := JsonObject():New()

    oJsonBody:fromJson(DecodeUTF8(oRest:GetBodyRequest(), "cp1252"))

    BQ7->(DbSetOrder(1))
	if BQ7->(dBseek(xFilial("BQ7") + cCode))
        lSuccess := .T.
		BQ7->( RecLock("BQ7",.F.) )
			BQ7->BQ7_DESCRI := self:setDefault( oJsonBody['description'] )
			BQ7->BQ7_NOTIF  := self:setDefault( oJsonBody['sendNotification'] )
			BQ7->BQ7_TITNOT := self:setDefault( oJsonBody['titleNotification'] )
			BQ7->BQ7_TXTNOT := self:setDefault( oJsonBody['txtNotification'] )
			BQ7->BQ7_TIPNOT := self:setDefault( oJsonBody['typeNotification'] )
			BQ7->BQ7_WPP    := self:setDefault( oJsonBody['sendWpp'] )
			BQ7->BQ7_TEMPLA := self:setDefault( oJsonBody['codeTemplate'] )
			BQ7->BQ7_EMAIL  := self:setDefault( oJsonBody['sendEmail'] )
			BQ7->BQ7_TXTEMA := self:setDefault( oJsonBody['txtEmail'] )
			BQ7->BQ7_LAYPAD := self:setDefault( oJsonBody['useLayout'] )
		BQ7->( MsUnLock() ) 	
	endif

Return lSuccess

Method setDefault(cConteudo) class HealthApiAlertDao
    Local cRet := ""

    if cConteudo <> nil
        cRet := cConteudo
    endif

Return cRet
