#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

@GET("/totvshealthplans/v1/alerts")
function HealthApiAlertRequest()
    local oRequest  := HealthApiAlertRequest():new(oRest)    
    
    oRequest:initRequest()
    oRequest:buscar()
    oRequest:montaJson()

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

@PUT("/totvshealthplans/v1/alerts/:code")
function HealthApiAlertPut()
    local oRequest  := HealthApiAlertRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:update()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

@GET("/totvshealthplans/v1/alerts/settings")
function HealthApiAlertReqGetMV()
    local oRequest  := HealthApiAlertRequest():new(oRest)    
    
    oRequest:initRequest()
    oRequest:montaJsonMV()

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

@PUT("/totvshealthplans/v1/alerts/settings")
function HealthApiAlertPutMV()
    local oRequest  := HealthApiAlertRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:validaMV()
        oRequest:updateMV()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

/*/{Protheus.doc} @Post("/totvshealthplans/v1/alerts")
Envio de notificações
@author  Lucas Nonato
@version P12
@since   21/10/2024
/*/
@Post("/totvshealthplans/v1/alerts")
function HealthApiAlertPost()
    local oRequest  := HealthApiAlertRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:validaPost()
        oRequest:setParam()
        oRequest:processa()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiAlertRequest from CenRequest

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method buscar()
    public method montaJson()
    public method valida()
    public method update()
    public method montaJsonMV()
    public method validaMV()
    public method updateMV()

    public method validaPost()
    public method validaEspecifico()
    public method setParam()
    public method processa()

    public data cChave 
    public data cParam

EndClass

Method destroy()  Class HealthApiAlertRequest
Return _Super:destroy()

Method New(oRest, cSvcName) Class HealthApiAlertRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oCollection := HealthApiAlertCollection():New()
    self:cChave := ''
    self:cParam := ''
Return self

Method valida() Class HealthApiAlertRequest

    local cFields   := ''

    _Super:checkBody()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest[ 'description' ]))  
            cFields += iif(!empty(cFields),',','')+"'description'"
        endif
        
        if empty(cvaltochar(self:jRequest[ 'sendNotification' ]))  
            cFields += iif(!empty(cFields),',','')+"'sendNotification'"
        endif

        if empty(cvaltochar(self:jRequest[ 'typeNotification' ]))  
            cFields += iif(!empty(cFields),',','')+"'typeNotification'"
        endif

        if empty(cvaltochar(self:jRequest[ 'sendEmail' ]))  
            cFields += iif(!empty(cFields),',','')+"'sendEmail'"
        endif

        if empty(cvaltochar(self:jRequest[ 'sendWpp' ]))  
            cFields += iif(!empty(cFields),',','')+"'sendWpp'"
        endif

        if !empty(cFields)
            self:nFault       := "0001"
            self:nStatus      := 400
            self:cFaultDesc   := "Campos obrigatórios não informados"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    
      
    endIf

Return self:lSuccess

Method buscar() Class HealthApiAlertRequest
    Local oQryParam := oRest:getQueryRequest()
    Local cCode     := iif(oQryParam['code'] <> nil, oQryParam['code'], "")

    If self:lSuccess
        self:oCollection:get(cCode)
    EndIf

Return self:lSuccess

Method montaJson() Class HealthApiAlertRequest

    Local oEntity := nil
    Local oJson := nil

    If self:lSuccess
        self:oRespBody["hasNext"] := .F.
        self:oRespBody["items"] := {}
        If self:oCollection:found()
            While self:oCollection:hasNext()
                oEntity := self:oCollection:getNext()
                oEntity:setHashFields(self:oRespControl:hmFields)
                oJson := self:expand(self:buildBody(oEntity))
                aAdd(self:oRespBody['items'], oJson)
            EndDo
            self:cResponse := self:oRespBody:toJson()
            oEntity:destroy()
        Else
            self:oRespBody["hasNext"] := self:oCollection:hasNext()
            self:cResponse := self:oRespBody:toJson()
        EndIf

    EndIf

    FreeObj(oEntity)
    oEntity := Nil

Return self:lSuccess

Method update() class HealthApiAlertRequest
    Local lSuccess := self:oCollection:update(oRest)

    if !lSuccess
        self:lSuccess     := .F.
        self:nStatus      := 400
        self:nFault       := "0002"
        self:cFaultDesc   := "Erro ao atualizar registro!"
        self:cFaultDetail := "Não foi possível fazer a atualização, tente novamente mais tarde"
    endif
Return 

Method montaJsonMV() Class HealthApiAlertRequest

    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}

    aadd(self:oRespBody["items"], jsonObject():new())
    self:oRespBody["items"][1]['operatorID'] := Alltrim(SuperGetMv("MV_PLTOP", .F., ""))

    self:cResponse := self:oRespBody:toJson()
    
Return 

Method validaMV() Class HealthApiAlertRequest

    local cFields   := ''

    _Super:checkBody()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest[ 'operatorID' ]))  
            cFields += iif(!empty(cFields),',','')+"'operatorID'"
        endif
        

        if !empty(cFields)
            self:nFault       := "0001"
            self:nStatus      := 400
            self:cFaultDesc   := "Campo obrigatório não informado"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    
      
    endIf

Return self:lSuccess

Method updateMV() class HealthApiAlertRequest
    Local aEncoded  := nil 
    Local nCipherID := 2 
    Local cKeyInput := "46b5e59b2fd342bf8fee10c561958725"
    Local cIVInput  := "d89a8633204d02f9"

    aEncoded := AESEncrypt( nCipherID, alltrim(self:jRequest[ 'operatorID' ]), , cKeyInput, cIVInput )
    
    if aEncoded[1] == 0 //Retorno de sucesso
        putMV( "MV_PLTOP", aEncoded[2] )
    endif
Return 

Method validaPost() Class HealthApiAlertRequest

    local cFields   := ''
    self:lSuccess   := self:checkBody()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest[ 'code' ]))  
            cFields += iif(!empty(cFields),',','')+"'code'"
        endif   

        if empty(cvaltochar(self:jRequest[ 'key' ]))  
            cFields += iif(!empty(cFields),',','')+"'key'"
        endIf

        if !empty(cFields)
            self:nFault := 400
            self:cFaultDesc   := "Campos obrigatórios não informados"
            self:cFaultDetail := cFields
            self:lSuccess     := .F.
        endIf    

        if self:lSuccess
            self:validaEspecifico()
        endif
      
    endIf

Return self:lSuccess

Method validaEspecifico() Class HealthApiAlertRequest
    self:cChave := cvaltochar(self:jRequest[ 'key' ])

    BQ7->(dbsetorder(1))
    if !BQ7->(msseek(xfilial("BQ7")+self:jRequest['code']))
        self:lSuccess := .f.
        self:cFaultDetail := "Alerta não encontrado ["+self:jRequest['code']+"]"
    endif

    if self:lSuccess .and. cvaltochar(self:jRequest['code']) == '000003'
        BA1->(dbsetorder(2))
        if !BA1->(msseek(xfilial("BA1")+self:jRequest['key']))
            self:lSuccess := .f.
            self:cFaultDetail := "Matricula não encontrada ["+self:jRequest['key']+"]"
        else
            self:cChave := BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)
        endif
        
    endif

    if !self:lSuccess
        self:nFault         := 400
        self:cFaultDesc     := "Falha na validação"
        self:lSuccess       := .F.
    endIf    

Return self:lSuccess

Method setParam() Class HealthApiAlertRequest
local oObj := nil

if self:lSuccess .and. cvaltochar(self:jRequest['code']) == '000003'
    oObj        := TotpBenSvc():new()
    if oObj:retToken(self:cChave)
        self:cParam := cvaltochar(oObj:oResult['token'])
    endif
endif

return

Method processa() Class HealthApiAlertRequest    

    PLSNotifica(self:jRequest['code'], self:cChave, self:cParam)
    self:oRespBody['notifica']  := iif(BQ7->BQ7_NOTIF=='1',.t.,.f.)
    self:oRespBody['email']     := iif(BQ7->BQ7_EMAIL=='1',.t.,.f.)
    self:oRespBody['whatsapp']  := iif(BQ7->BQ7_WPP=='1',.t.,.f.)
    self:cResponse := self:oRespBody:toJson()

Return self:lSuccess