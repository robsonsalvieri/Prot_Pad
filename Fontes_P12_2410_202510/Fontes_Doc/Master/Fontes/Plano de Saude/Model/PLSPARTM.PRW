#INCLUDE "PLSMODEL.CH"
#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"

//
// Define
//
#DEFINE PLS__ALIAS 	 "B70"
#DEFINE PLS_MN_BANC	 STR0009 //"Banc. Processo"
#DEFINE PLS_MODELO 	 "PLSPARTM"
#DEFINE PLS_MD__ID 	 "PLSPARTMMD"
#DEFINE PLS_VW__ID	 "PLSPARTMVI"
#DEFINE PLS_TITULO 	 STR0001 //"Dados"
#DEFINE PLS_MENUDEF	 "VIEWDEF.PLSPARTM"
#DEFINE PLS_VW_TELA	 STR0002 //"TELA"
#DEFINE PLS_VW_OPE  2
#DEFINE PLS_MD_OPE 	 1
//
// Static
//
STATIC oPADC := PLSPADRC():New(PLS__ALIAS) 
//-------------------------------------------------------------------
/*/{Protheus.doc} PLSPARTM
Classe modelo de dados da rotina participativa (Auditoria)

@author Alexander Santos
@since 16/02/2011
@version P11
/*/
//-------------------------------------------------------------------
Class PLSPARTM From PLSCONTR          

METHOD New() Constructor

EndClass     
//-------------------------------------------------------------------
/*/{Protheus.doc} New
Construtor da Class

@author Alexander Santos
@since 16/02/2011
@version P11
/*/
//-------------------------------------------------------------------
METHOD New() Class PLSPARTM
Return Self
/*/


Ŀ
Funcao     MenuDef  Autor  Totvs                   Data  16.02.11 
Ĵ
Descricao  MenuDef													  				
ٱ


/*/
Static Function MenuDef()
LOCAL o790C		:= PLS790GEC()
PRIVATE aRotina := {}              
//
// Opcoes de menu															 
//
If !o790C:lVisible
	AaDd( aRotina, { STR0003, 	PLS_MENUDEF, 0, MODEL_OPERATION_VIEW } ) //"Visualizar"
	AaDd( aRotina, { STR0006, 	PLS_MENUDEF, 0, MODEL_OPERATION_INSERT} ) //"Incluir"
	AaDd( aRotina, { STR0007, 	PLS_MENUDEF, 0, MODEL_OPERATION_UPDATE} ) //"Alterar"
	AaDd( aRotina, { STR0008, 	PLS_MENUDEF, 0, MODEL_OPERATION_DELETE} ) //"Excluir"
	AaDd( aRotina, { STR0011, 	"PLS790EML", 0, 0} )     				  		//"E-Mail"
Else
	AaDd( aRotina, { STR0003, 	PLS_MENUDEF, 0, MODEL_OPERATION_VIEW } ) //"Visualizar"
EndIf	
//
// Fim da rotina															 
//
Return aRotina                              
/*/


Ŀ
Funcao     ViewDef  Autor  Totvs                   Data  16.02.11 
Ĵ
Descricao  ViewDef													  				
ٱ


/*/               
Static Function ViewDef()             
LOCAL oModel := FWLoadModel( PLS_MODELO )
LOCAL oStruV := FWFormStruct( PLS_VW_OPE, PLS__ALIAS )
LOCAL oView	 := FWFormView():New()
//
// Seta o modelo na visao													 
//
oView:SetModel( oModel )
//
// Adiciona a strutura de campos da tabela na view							 
//
oView:AddField( PLS_VW__ID, oStruV, PLS_MD__ID )
//
// Tela																	 
//
oView:CreateHorizontalBox( PLS_VW_TELA , 100 )
oView:SetOwnerView( PLS_VW__ID, PLS_VW_TELA )
//
// Remover campos da View 																	 
//
oStruV:RemoveField('B70_ALIMOV')
oStruv:RemoveField('B70_RECMOV')
oStruV:RemoveField('B70_OPERAD')
oStruv:RemoveField('B70_DATMOV')
oStruv:RemoveField('B70_NOMOPE')
//
// Fecha a tela
//
oView:SetCloseOnOk( {|oView| oPADC:VWOkCloseScreenVLD(oView) } )
//
// Validacao da view
//
oView:SetVldActivate({|oView| oPADC:VWInitVLD(oView) }  )  
//
// Fim da rotina															 
//
Return oView                     
/*/


Ŀ
Funcao     ModelDef Autor  Totvs                   Data  16.02.11 
Ĵ
Descricao  ModelDef													  
ٱ


/*/
Static Function ModelDef()  
LOCAL oStruM := FWFormStruct( PLS_MD_OPE, PLS__ALIAS )
LOCAL oModel := MPFormModel():New( PLS_MODELO,/*bPreValidacao*/,{ |oModel| oPADC:MDPosVLD(oModel,PLS__ALIAS) },{|oModel| oPADC:MDCommit(oModel,PLS__ALIAS) }, /*bCancel*/ ) /*bPosValidacao*/ /*bCommit*/ 
//
// Defini a descricao da tela												 
//
oModel:SetDescription( PLS_TITULO )
//
// Adiciona a strutura de campos ao modelo									 
//
oModel:AddFields( PLS_MD__ID,,oStruM )
//
// Chave primria da tabela B70								 
//
oModel:SetPrimaryKey({"B70_FILIAL","B70_ALIMOV","B70_RECMOV","B70_SEQUEN"})
//
// Valida o modelo															 
//
oModel:SetVldActivate( { |oModel| oPADC:MDActVLD(oModel) } )
//
// Fim da Rotina															 
//
Return oModel
/*/


Ŀ
Funcao    PLSPARTM   Autor  Totvs				     Data  30/03/10 
Ĵ
Descricao  Somente para compilar a class				  			  
ٱ


/*/
Function PLSPARTM
Return
