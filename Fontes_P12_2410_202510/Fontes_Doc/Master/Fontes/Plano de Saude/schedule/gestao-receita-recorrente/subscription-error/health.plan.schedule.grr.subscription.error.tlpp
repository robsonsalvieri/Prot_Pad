#include "tlpp-core.th"

#define LOG_FILE "schedule.grr.subscription.error.log"

namespace totvs.protheus.health.plan.schedule

using namespace totvs.protheus.health.plan.integration.grr

/*/{Protheus.doc} subscriptionErrorGRR
Reprocessa as subscrições que apresentaram erro na tentativa de cadastro na integração com o GRR
@type function
@version 12.1.2510
@autor vinicius.queiros
@since 04/06/2025
@return logical, indica se o reprocessamento foi concluído com sucesso
/*/
function subscriptionErrorGRR()

return processSubscriptionErrors()

/*/{Protheus.doc} processSubscriptionErrors
Processa as subscrições que apresentaram erro na integração com o GRR, utilizando os parâmetros informados
@type function
@version 12.1.2510
@autor vinicius.queiros
@since 04/06/2025
@return logical, indica se o processamento foi concluído com sucesso
/*/
static function processSubscriptionErrors() as logical

    local cAlias := getNextAlias() as character
    local jStatus as json
    local nQtdSuccess := 0 as numeric
    local nQtdFailed := 0 as numeric
    local cHealthInsurerCode := plsIntPad()

    beginSql alias cAlias
		SELECT
            BA3.BA3_CODINT,
            BA3.BA3_CODEMP,
            BA3.BA3_MATRIC
		FROM
			%table:BA3% BA3		
		WHERE
			BA3.BA3_FILIAL = %xfilial:BA3% AND
			BA3.BA3_CODINT = %exp:cHealthInsurerCode% AND
            BA3.BA3_INTGRR = '1' AND
            BA3.BA3_GRRSIT = '2' AND	
			BA3.%notDel%
	endSql

    BA3->(dbSetOrder(1))

    while !(cAlias)->(eof())
        if BA3->(dbSeek(xFilial("BA3") + (cAlias)->(BA3_CODINT + BA3_CODEMP + BA3_MATRIC)))
            jStatus := createSubscription()

            if jStatus["success"]
                nQtdSuccess++
            else
                nQtdFailed++
            endif
        endif

        (cAlias)->(dbSkip())
    enddo

    plsLogFil("[" + time() + "] " + "Resumo do Processamento de Subscrições do GRR", LOG_FILE)
    plsLogFil(" - Total de subscrições reprocessadas: " + cValToChar(nQtdSuccess + nQtdFailed), LOG_FILE)
    plsLogFil(" - Subscrições cadastradas com sucesso: " + cValToChar(nQtdSuccess), LOG_FILE)
    plsLogFil(" - Subscrições que ainda apresentaram erro: " + cValToChar(nQtdFailed), LOG_FILE)
    plsLogFil("", LOG_FILE)
    
    (cAlias)->(dbCloseArea())

    freeObj(jStatus)

return .T.
