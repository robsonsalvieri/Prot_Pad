#include "totvs.ch"
#include "msobject.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "health.sv.plan.billingbatchconference.bizobj.ch"

namespace totvs.protheus.health.smartview.plan.integratedprovider

/*/{Protheus.doc} BillingBatchConferenceBusinessObject
Objeto de negócio para conferência do lote de cobrança
@type class
@version 12.1.2310
@author vinicius.queiros
@since 06/12/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPLS", tables="BDF,BG9,SE1,BM1,BQC", name="Conferência do Lote de Cobrança", country="ALL", initialRelease="12.1.2310")
class BillingBatchConferenceBusinessObject from totvs.protheus.health.smartview.plan.integratedprovider.HealthIntegratedProvider

	public method new() constructor

	protected method initializeBusinessObject()
	protected method loadStatement()
	protected method loadSchema()
	protected method processAppendData()

endclass

/*/{Protheus.doc} new
Contrutor da classe
@type method
@version 12.1.2310  
@author vinicius.queiros
@since 06/12/2023
@return object, objeto da classe BillingBatchConferenceObject
/*/   
method new() class BillingBatchConferenceBusinessObject

	_Super:new()

	self:initializeBusinessObject()

return self

/*/{Protheus.doc} initializeBusinessObject
Inicializa os dados (nome, descrição e grupo de perguntas) do objeto de negócio
@type method
@version 12.1.2310
@author vinicius.queiros
@since 24/01/2024
/*/
method initializeBusinessObject() class BillingBatchConferenceBusinessObject

	self:setDisplayName(STR0001) // "Conferência do Lote de Cobrança"
	self:setDescription(STR0002) // "Visa emitir resumo demonstrando a composição de um lote de cobrança de forma analítico e sintético."
	self:setPergunte("PLSSV001")
	
return

/*/{Protheus.doc} loadStatement
Carrega o objeto oStatement (FWExecStatement) com a query principal do objeto de negócio
@type method
@version 12.1.2310 
@author vinicius.queiros
@since 26/01/2024
/*/
method loadStatement() class BillingBatchConferenceBusinessObject

	local cQuery as character
	local nOrder := 1 as numeric

	cQuery += "SELECT ? "
	cQuery += " FROM ? BM1 "

	cQuery += " INNER JOIN ? BDF ON "
	cQuery += "		BDF.BDF_FILIAL = ? AND "
	cQuery += "		BDF.BDF_PREFIX = BM1.BM1_PREFIX AND "
	cQuery += "		BDF.BDF_NUMTIT = BM1.BM1_NUMTIT AND "
	cQuery += "		BDF.BDF_PARCEL = BM1.BM1_PARCEL AND "
	cQuery += "		BDF.BDF_TIPTIT = BM1.BM1_TIPTIT AND "
	cQuery += "		BDF.BDF_CODEMP >= ? AND "
	cQuery += "		BDF.BDF_CODEMP <= ? AND "
	cQuery += "		BDF.BDF_NUMCON >= ? AND "
	cQuery += "		BDF.BDF_NUMCON <= ? AND "
	cQuery += "		BDF.BDF_SUBCON >= ? AND "
	cQuery += "		BDF.BDF_SUBCON <= ? AND "
	cQuery += "		BDF.BDF_MATRIC >= ? AND "
	cQuery += "		BDF.BDF_MATRIC <= ? AND "
	cQuery += "		BDF.D_E_L_E_T_ = ? "

	cQuery += " INNER JOIN ? BDC ON "
	cQuery += "		BDC.BDC_FILIAL = ? AND "
	cQuery += "     BDC.BDC_CODOPE = BDF.BDF_CODOPE AND "
	cQuery += "		BDC.BDC_NUMERO = BDF.BDF_NUMERO AND "
	cQuery += "		BDC.BDC_CODOPE >= ? AND "
	cQuery += "		BDC.BDC_NUMERO >= ? AND "
	cQuery += "		BDC.BDC_CODOPE <= ? AND "
	cQuery += "		BDC.BDC_NUMERO <= ? AND "
	cQuery += "		BDC.BDC_INTERC = ? AND "
	cQuery += "		BDC.D_E_L_E_T_ = ? "

	cQuery += " INNER JOIN ? BG9 ON
	cQuery += " 	BG9.BG9_FILIAL = ? AND "
	cQuery += " 	BG9.BG9_CODINT = BDF.BDF_CODOPE AND "
	cQuery += " 	BG9.BG9_CODIGO = BDF.BDF_CODEMP AND "
	cQuery += " 	BG9.D_E_L_E_T_ = ? "

	cQuery += " INNER JOIN ? SE1 ON
	cQuery += " 	SE1.E1_FILIAL = ? AND"
	cQuery += " 	SE1.E1_PREFIXO = BDF.BDF_PREFIX AND "
	cQuery += " 	SE1.E1_NUM = BDF.BDF_NUMTIT AND "
	cQuery += " 	SE1.E1_PARCELA = BDF.BDF_PARCEL AND "
	cQuery += " 	SE1.E1_TIPO = BDF.BDF_TIPTIT AND "
	cQuery += " 	SE1.D_E_L_E_T_ = ? "

	cQuery += " LEFT JOIN ? BQC ON "
	cQuery += " 	BQC.BQC_FILIAL = ? AND "
	cQuery += " 	BQC.BQC_CODINT = BM1.BM1_CODINT AND "
	cQuery += " 	BQC.BQC_CODEMP = BM1.BM1_CODEMP AND "
	cQuery += " 	BQC.BQC_NUMCON = BM1.BM1_CONEMP AND "
	cQuery += " 	BQC.BQC_VERCON = BM1.BM1_VERCON AND "
	cQuery += " 	BQC.BQC_SUBCON = BM1.BM1_SUBCON AND	"
	cQuery += " 	BQC.BQC_VERSUB = BM1.BM1_VERSUB AND "
	cQuery += " 	BQC.D_E_L_E_T_ = ? "

	cQuery += " LEFT JOIN ? BT5 ON "
	cQuery += " 	BT5.BT5_FILIAL = ? AND "
	cQuery += " 	BT5.BT5_CODINT = BM1.BM1_CODINT AND "
	cQuery += " 	BT5.BT5_CODIGO = BM1.BM1_CODEMP AND "
	cQuery += " 	BT5.BT5_NUMCON = BM1.BM1_CONEMP AND "
	cQuery += " 	BT5.BT5_VERSAO = BM1.BM1_VERCON AND "
	cQuery += " 	BT5.D_E_L_E_T_ = ? "

	cQuery += " LEFT JOIN ? BI3 ON "
	cQuery += " 	BI3.BI3_FILIAL = ? AND "
	cQuery += " 	BI3.BI3_CODINT = BM1.BM1_CODINT AND "
	cQuery += " 	BI3.BI3_CODIGO = BM1.BM1_CODEVE AND "
	cQuery += " 	BI3.D_E_L_E_T_ = ? "

	cQuery += " WHERE BM1.BM1_FILIAL = ? AND "
	cQuery += " 	  BM1.D_E_L_E_T_ = ? "

	cQuery += " ? "
	cQuery += " ORDER BY ? "

	cQuery := changeQuery(cQuery)

	self:oStatement := FWExecStatement():new(cQuery)

	self:oStatement:setUnsafe(nOrder++, self:getSQLFields())
	self:oStatement:setUnsafe(nOrder++, retSqlName("BM1"))
	self:oStatement:setUnsafe(nOrder++, retSqlName("BDF"))
	self:oStatement:setString(nOrder++, xFilial("BDF"))
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR05"]) // Empresa De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR06"]) // Empresa Até?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR07"]) // Contrato De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR08"]) // Contrato Até?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR09"]) // Subcontract De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR10"]) // Subcontract Até?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR11"]) // Matricula De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR12"]) // MAtricula Até?
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BDC"))
	self:oStatement:setString(nOrder++, xFilial("BDC"))
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR01"]) // Operadora De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR03"]) // Cobrança De?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR02"]) // Operadora Até?
	self:oStatement:setString(nOrder++, self:jFilterParams["MV_PAR04"]) // Cobrança Até?
	self:oStatement:setString(nOrder++, iif(self:jFilterParams["MV_PAR13"] == 1, "1", "0")) // Intercambio
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BG9"))
	self:oStatement:setString(nOrder++, xFilial("BG9"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("SE1"))
	self:oStatement:setString(nOrder++, xFilial("SE1"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BQC"))
	self:oStatement:setString(nOrder++, xFilial("BQC"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BT5"))
	self:oStatement:setString(nOrder++, xFilial("BT5"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, retSqlName("BI3"))
	self:oStatement:setString(nOrder++, xFilial("BI3"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setString(nOrder++, xFilial("BM1"))
	self:oStatement:setString(nOrder++, " ")
	self:oStatement:setUnsafe(nOrder++, self:getFilterSmartView())
	self:oStatement:setUnsafe(nOrder++, "BDF_CODOPE, BDF_CODEMP, BDF_NUMERO, BDF_PREFIX, BDF_NUMTIT, BM1_CODEVE, BM1_IDAINI, BM1_IDAFIN")
	
return

/*/{Protheus.doc} setSchema
Carrega o esquema do objeto de negócio
@type method
@version 12.1.2310
@author vinicius.queiros
@since 27/01/2024
/*/
method loadSchema() class BillingBatchConferenceBusinessObject

	local aFieldsBDF as array
	local aFieldsBG9 as array
	local aFieldsSE1 as array
	local aFieldsBM1 as array
	local aFieldsBQC as array
	local aFieldsBT5 as array
	local aFieldsBI3 as array

	aFieldsBDF := {"BDF_CODEMP", "BDF_NUMCON", "BDF_VERCON", "BDF_SUBCON", "BDF_VERSUB"}
	aFieldsBG9 := {"BG9_DESCRI", "BG9_EMPANT"}
	aFieldsSE1 := {"E1_CLIENTE", "E1_NOMCLI", "E1_PREFIXO", "E1_NUM", "E1_VENCTO", "E1_ANOBASE", "E1_MESBASE", "E1_VALOR", "E1_PLNUCOB"}
	aFieldsBM1 := {"BM1_TIPO", "BM1_CODTIP", "BM1_DESTIP", "BM1_VALOR", "BM1_IDAINI", "BM1_IDAFIN", "BM1_CONEMP", "BM1_VERCON", "BM1_SUBCON", "BM1_VERSUB", "BM1_CODEVE"}
	aFieldsBQC := {"BQC_DESCRI", "BQC_ANTCON"}
	aFieldsBT5 := {"BT5_ANTCON"}
	aFieldsBI3 := {"BI3_DESCRI"}

	self:aliasToSchema("BDF", aFieldsBDF)
	self:aliasToSchema("BG9", aFieldsBG9)
	self:aliasToSchema("SE1", aFieldsSE1)
	self:aliasToSchema("BM1", aFieldsBM1)
	self:aliasToSchema("BQC", aFieldsBQC)
	self:aliasToSchema("BT5", aFieldsBT5)
	self:aliasToSchema("BI3", aFieldsBI3)

	// Campos customizados
	self:addProperty("COMPANY_GROUP", STR0003, "string", STR0003) // "Dados do Grupo/Empresa"
	self:addProperty("CONTRACT_SYNTHETIC", STR0004, "string", STR0004) // "Dados do Contrato - Sintético"
	self:addProperty("SUBCONTRACT_SYNTHETIC", STR0005, "string", STR0005) // "Dados do Subcontrato - Sintético"
	self:addProperty("CLIENT", STR0006, "string", STR0006) // "Dados do Cliente"
	self:addProperty("TITLE", STR0007, "string", STR0007) // "Dados do Titulo"
	self:addProperty("BILLING_TYPE", STR0008, "string", STR0008) // "Tipo de Cobrança"
	self:addProperty("CONTRACT_ANALYTICAL", STR0009, "string", STR0009) // "Dados do Contrato - Analítico"
	self:addProperty("SUBCONTRACT_ANALYTICAL", STR0010, "string", STR0010) // "Dados do Subcontrato - Analítico"
	self:addProperty("PRODUCT", STR0011, "string", STR0011) // "Dados do Produto"
	self:addProperty("RANGE", STR0012, "string", STR0012) // "Dados das Faixas"
	self:addProperty("BATCH", STR0013, "string", STR0013) // "Dados dos Lote"

	fwFreeArray(aFieldsBDF)
	fwFreeArray(aFieldsBG9)
	fwFreeArray(aFieldsSE1)
	fwFreeArray(aFieldsBM1)
	fwFreeArray(aFieldsBQC)
	fwFreeArray(aFieldsBT5)
	fwFreeArray(aFieldsBI3)

return

/*/{Protheus.doc} processAppendData
Processamento dos dados do JData
@type method
@version 12.1.2310 
@author vinicius.queiros
@since 01/02/2024
/*/
method processAppendData() class BillingBatchConferenceBusinessObject

	self:addDataValue("TITLE", "string", "", STR0014, .T.) // "Titulo: "
	self:addDataValue("TITLE", "string", "E1_PREFIXO", nil, .T.)
	self:addDataValue("TITLE", "string", "E1_NUM", nil, .T.)
	self:addDataValue("TITLE", "string", "", " - ", .T.)
	self:addDataValue("TITLE", "string", "", STR0015, .T.) // "Dt.Vencto: "
	self:addDataValue("TITLE", "string", "E1_VENCTO", dtoc(stod((self:cAlias)->E1_VENCTO)), .T.)
	self:addDataValue("TITLE", "string", "", " - ", .T.)
	self:addDataValue("TITLE", "string", "", STR0016, .T.) // "Competência: "
	self:addDataValue("TITLE", "string", "E1_ANOBASE", nil, .T.)
	self:addDataValue("TITLE", "string", "", "/", .T.)
	self:addDataValue("TITLE", "string", "E1_MESBASE", nil, .T.)
	self:addDataValue("TITLE", "string", "", " - ", .T.)
	self:addDataValue("TITLE", "string", "", STR0017, .T.) // "Valor: "
	self:addDataValue("TITLE", "string", "E1_VALOR", "R$ " + alltrim(transform((self:cAlias)->E1_VALOR, "@E 99,999,999,999.99")), .T.)

	self:addDataValue("CONTRACT_SYNTHETIC", "string", "", STR0018, .T.) // "Contrato: "
	if !empty((self:cAlias)->BDF_NUMCON)	
		self:addDataValue("CONTRACT_SYNTHETIC", "string", "BDF_NUMCON", nil, .T.)
		self:addDataValue("CONTRACT_SYNTHETIC", "string", "", " - ", .T.)
		self:addDataValue("CONTRACT_SYNTHETIC", "string", "", STR0019, .T.) // "Versão: "
		self:addDataValue("CONTRACT_SYNTHETIC", "string", "BDF_VERCON", nil, .T.)
	else
		self:addDataValue("CONTRACT_SYNTHETIC", "string", "", STR0020, .T.) // "Todos"
	endif

	self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "", STR0021, .T.) // "Subcontrato: "
	if !empty((self:cAlias)->BDF_SUBCON)
		self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "BDF_SUBCON", nil, .T.)
		self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "", " - ", .T.)
		self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "", STR0019, .T.) // "Versão: "
		self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "BDF_VERSUB", nil, .T.)
	else	
		self:addDataValue("SUBCONTRACT_SYNTHETIC", "string", "", STR0020, .T.) // "Todos"
	endif

	self:addDataValue("COMPANY_GROUP", "string", "", STR0022, .T.) // "Empresa: "
	self:addDataValue("COMPANY_GROUP", "string", "BDF_CODEMP", nil, .T.)
	self:addDataValue("COMPANY_GROUP", "string", "", " - ", .T.)
	self:addDataValue("COMPANY_GROUP", "string", "BG9_DESCRI", nil, .T.)

	if !empty((self:cAlias)->BG9_EMPANT)
		self:addDataValue("COMPANY_GROUP", "string", "", "(", .T.)
		self:addDataValue("COMPANY_GROUP", "string", "BG9_EMPANT", nil, .T.)
		self:addDataValue("COMPANY_GROUP", "string", "", ")", .T.)
	endif

	self:addDataValue("CLIENT", "string", "", STR0023, .T.) // "Cliente: "
	self:addDataValue("CLIENT", "string", "E1_CLIENTE", nil, .T.)
	self:addDataValue("CLIENT", "string", "", " - ", .T.)
	self:addDataValue("CLIENT", "string", "E1_NOMCLI", nil, .T.)

	if (self:cAlias)->BM1_TIPO == "1"
		self:addDataValue("BILLING_TYPE", "string", "", STR0024, .T.) // "Débitos"
	else
		self:addDataValue("BILLING_TYPE", "string", "", STR0025, .T.) // "Créditos"
	endif

	if (self:cAlias)->BM1_CODTIP == "103"
		self:addDataValue("BI3_DESCRI", "string", "BI3_DESCRI", STR0026) // "TAXA DE INSCRICAO"
	endif

	self:addDataValue("CONTRACT_ANALYTICAL", "string", "", STR0018, .T.) // "Contrato: "
	self:addDataValue("CONTRACT_ANALYTICAL", "string", "BM1_CONEMP", nil, .T.)
	self:addDataValue("CONTRACT_ANALYTICAL", "string", "", " - ", .T.)
	self:addDataValue("CONTRACT_ANALYTICAL", "string", "", STR0019, .T.) // "Versão: "
	self:addDataValue("CONTRACT_ANALYTICAL", "string", "BM1_VERCON", nil, .T.)

	if !empty((self:cAlias)->BT5_ANTCON)
		self:addDataValue("CONTRACT_ANALYTICAL", "string", "", "(", .T.)
		self:addDataValue("CONTRACT_ANALYTICAL", "string", "BT5_ANTCON", nil, .T.)
		self:addDataValue("CONTRACT_ANALYTICAL", "string", "", ")", .T.)
	endif

	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", STR0021, .T.) // "Subcontrato: "
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "BM1_SUBCON", nil, .T.)
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", " - ", .T.)
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", STR0019, .T.) // "Versão: "
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "BM1_VERSUB", nil, .T.)
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", " - ", .T.)
	self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "BQC_DESCRI", nil, .T.)

	if !empty((self:cAlias)->BQC_ANTCON)
		self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", "(", .T.)
		self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "BQC_ANTCON", nil, .T.)
		self:addDataValue("SUBCONTRACT_ANALYTICAL", "string", "", ")", .T.)
	endif

	if (self:cAlias)->BM1_CODTIP <> "103"
		if !empty((self:cAlias)->BI3_DESCRI)
			self:addDataValue("PRODUCT", "string", "", STR0027, .T.) // "Produto: "
			self:addDataValue("PRODUCT", "string", "BI3_DESCRI", nil, .T.)
		endif
	else
		self:addDataValue("PRODUCT", "string", "", STR0027, .T.) // "Produto: "
		self:addDataValue("PRODUCT", "string", "BI3_DESCRI", STR0026, .T.) // "TAXA DE INSCRICAO"
	endif

	if (self:cAlias)->BM1_IDAINI > 0 .or. (self:cAlias)->BM1_IDAFIN > 0
		self:addDataValue("RANGE", "string", "", STR0028, .T.) // "Faixa: "
		self:addDataValue("RANGE", "string", "BM1_IDAINI", strZero((self:cAlias)->BM1_IDAINI, 3), .T.)
		self:addDataValue("RANGE", "string", "", STR0029, .T.) // " a "
		self:addDataValue("RANGE", "string", "BM1_IDAFIN", strZero((self:cAlias)->BM1_IDAFIN, 3), .T.)
	endif

	self:addDataValue("BATCH", "string", "", STR0030, .T.) // "Lote: "
	self:addDataValue("BATCH", "string", "E1_PLNUCOB", substr((self:cAlias)->E1_PLNUCOB, 5, 8), .T.)

return
