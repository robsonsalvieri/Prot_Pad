#include "tlpp-core.th"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.health.smartview.plan.beneficiariesregistration

using namespace totvs.protheus.health.smartview.plan.utils

/*/{Protheus.doc} BusinessObject
Objeto de negócio para o Smart View - Beneficiários

@type class
@version Protheus 12.1.2310
@author jose.paulo
@since 08/11/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BA1", name = "Beneficiários", country="ALL", initialRelease="12.1.2210")
class BeneficiariesregistrationBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data aFields as array
	private data cAliasTemp as character
	private data jFilterParams as json
	private data lExistPergunte as logical

	public method new() as object
	public method getData(nPage as numeric, oFilter as object) as object
	public method getSchema() as object

	private method getX3Usado() as array
	private method setFilterParams(oFilter as object) as logical
	private method validParameters() as logical
	private method getUsedFields() as array
	private method getQueryUsers(oFilter as object) as object
	private method getVirtualFieldsValue(cField as character) as variant
	private method getComboBoxValue(cValue as character, aComboBox as array) as character

endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 08/11/2023
@return object, objeto da classe BusinessObject
/*/
method new() class BeneficiariesregistrationBusinessObject

	_Super:new()

	self:jFilterParams := JsonObject():new()
	self:aFields := self:getUsedFields()

	self:appendArea(UtilsBusinessObject():getAreaName())
	self:setDisplayName("Beneficiários")
	self:setDescription("Relação do Cadastro de Beneficiários.")

	self:lExistPergunte := self:setPergunte("PLR613")

	iif (!self:lExistPergunte,self:setErrorStatus(400, "Pergunte não encontrado", "Verifique o grupo de perguntas PLR613 na base de dados."),"")

return self

/*/{Protheus.doc} getData
Retorna o objeto de dados

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 08/11/2023
@param nPage, numérico, indica a página atual do relação
@param oFilter, objeto, contém o filtro do Smart View
@return object, objeto oData da classe IntegratedProvider
/*/
method getData(nPage as numeric, oFilter as object) as object class BeneficiariesregistrationBusinessObject

	local nX := 0 as numeric
	local jItems as json
	local lVirtual as logical
	local lCombo as logical
	local cComboBox as character
	local aComboBox as array
	local cField as character
	local cFieldId as character
	local xFieldValue as variant
	local aPDFields as array
	local lObfuscated as logical
	local lValueAnonymize as logical
	Local oStatement as object

	if self:lExistPergunte
		self:setFilterParams(oFilter)
		if self:validParameters()
			oStatement := self:getQueryUsers(oFilter)
			self:cAliasTemp := oStatement:openAlias()

			(self:cAliasTemp)->(dbGoTop())

			aPDFields := FwProtectedDataUtil():usrAccessPDField(__cUserID, self:aFields)
			lObfuscated := len(aPDFields) <> len(self:aFields)

			while !(self:cAliasTemp)->(eof())
				jItems := JsonObject():new()

				for nX := 1 to len(self:aFields)
					cField := self:aFields[nX]
					lVirtual := UPPER(alltrim(getSx3Cache(cField, "X3_CONTEXT"))) == "V"
					cComboBox := alltrim(getSx3Cache(cField, "X3_CBOX"))
					lCombo := !empty(cComboBox)
					cFieldId := strTran(cField, "_", "")

					if lVirtual
						xFieldValue := self:getVirtualFieldsValue(cField)
					else
						xFieldValue := (self:cAliasTemp)->&(self:aFields[nX])
					endif

					lValueAnonymize := iif(lObfuscated .and. aScan(aPDFields, cField) == 0, .t., .f.)

					xFieldValue := iif(lValueAnonymize, FwProtectedDataUtil():ValueAsteriskToAnonymize(xFieldValue), xFieldValue)

					if !lVirtual .and. !lValueAnonymize
						if getSx3Cache(cField, "X3_TIPO") == "D"
							if !empty(xFieldValue)
								xFieldValue := totvs.framework.treports.date.stringToTimeStamp(xFieldValue)
							else
								xFieldValue := nil
							endif
						else
							if lCombo
								aComboBox := strTokArr(cComboBox, ";")
								xFieldValue := self:getComboBoxValue(xFieldValue, aComboBox)
							endif
						endif
					endif

					jItems[cFieldId] := xFieldValue
				next nX

				self:oData:appendData(jItems)

				(self:cAliasTemp)->(dbSkip())
			enddo

			self:setHasNext(!(self:cAliasTemp)->(eof()))

			(self:cAliasTemp)->(dbCloseArea())
		endif
	endif

return self:oData


/*/{Protheus.doc} getUsedFields
Retorna os campos utilizados no Objeto de Negocios com base no X3_USADO das tabelas:
@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 25/11/2023
@return array, Array com os campos utilizados
/*/
method getUsedFields() as array class BeneficiariesregistrationBusinessObject

	local aAllFields as array
	local aFields as array
	local aFieldsBA1 as array
	local nX := 0 as numeric
	local nY := 0 as numeric

	aAllFields := {}
	aFields := {}
	aFieldsBA1 := self:getX3Usado('BA1')

	aAllFields := {aFieldsBA1}

	for nX := 1 to len(aAllFields)
		for nY := 1 to len(aAllFields[nX])
			AAdd(aFields, aAllFields[nX][nY])
		next nY
	next nX

return aFields

/*/{Protheus.doc} getSchema
Retorna a Estrutura de dados

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 08/11/2023
@return object, objeto oData da classe IntegratedProvider
/*/
method getSchema() as object class BeneficiariesregistrationBusinessObject

	local nX := 0 as numeric
	local cField as character
	local nPos as numeric
	local cId as character
	local cDescription as character
	local cType as character
	local cDisplayName as character
	local cRealName as character
	local cTypeSX3 as character

	for nX := 1 to len(self:aFields)
		cField := self:aFields[nX]
		nPos := at(".", cField) + 1
		cField := iif(nPos > 0, substr(cField, nPos), cField)

		cId := strTran(cField, "_", "")
		cDescription := alltrim(getSx3Cache(cField, "X3_DESCRIC"))
		cDisplayName := alltrim(getSx3Cache(cField, "X3_TITULO"))
		cRealName := cField
		cType := "string"
		cTypeSX3 := getSx3Cache(cField, "X3_TIPO")

		do case
		case cTypeSX3 == "C"
			cType := "string"
		case cTypeSX3 == "D"
			cType := "date"
		case cTypeSX3 == "N"
			cType := "number"
		endcase

		self:addProperty(cId, cDescription, cType, cDisplayName+" ("+cField+")", cRealName)
	next nX

return self:oSchema


/*/{Protheus.doc} setFilterParams
Defini os parâmetros do pergunte recebido pelo smart view para ser usado
na montagem da query

@type method
@version 12.1.2310
@author jose.paulo
@since 25/11/2023
@param oFilter, object, Objeto de filtro do Smart View
@return logical, se os parâmetros foram definidos com sucesso
/*/
method setFilterParams(oFilter as object) as logical class BeneficiariesregistrationBusinessObject

	local jParams := oFilter:getParameters() as json
	local lOk := .t. as logical

	self:jFilterParams["healthInsurerCodeFrom"] := ""
	self:jFilterParams["companyCodeFrom"]       := ""
	self:jFilterParams["familyCodeFrom"]        := ""
	self:jFilterParams["typeRecordFrom"]        := ""
	self:jFilterParams["healthInsurerCodeAt"]   := ""
	self:jFilterParams["companyCodeAt"]         := ""
	self:jFilterParams["familyCodeAt"]          := ""
	self:jFilterParams["typeRecordAt"]          := ""

	self:jFilterParams["healthInsurerCodeFrom"] := substr(jParams["MV_PAR01"][1], 1, 4)
	self:jFilterParams["companyCodeFrom"]       := substr(jParams["MV_PAR01"][1], 5, 4)
	self:jFilterParams["familyCodeFrom"]        := substr(jParams["MV_PAR01"][1], 9, 6)
	self:jFilterParams["typeRecordFrom"]        := substr(jParams["MV_PAR01"][1], 15, 2)

	self:jFilterParams["healthInsurerCodeAt"]   := substr(jParams["MV_PAR02"][1], 1, 4)
	self:jFilterParams["companyCodeAt"]         := substr(jParams["MV_PAR02"][1], 5, 4)
	self:jFilterParams["familyCodeAt"]          := substr(jParams["MV_PAR02"][1], 9, 6)
	self:jFilterParams["typeRecordAt"]          := substr(jParams["MV_PAR02"][1], 15, 2)

	self:jFilterParams["active"]                := cValToChar(jParams["MV_PAR03"][1]) // Situacao ?
	self:jFilterParams["oldSubscriberIdFrom"]   := alltrim(jParams["MV_PAR05"][1]) // Matr. Antiga de ?
	self:jFilterParams["oldSubscriberIdAt"]     := alltrim(jParams["MV_PAR06"][1]) // Matr. Antiga ate ?

return lOk

/*/{Protheus.doc} getQueryUsers
Retorna a query dos Beneficiários do objeto de negocio

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 08/11/2023
@param oFilter, object, Objeto de filtro do Smart View
@return Character, where da query dos Beneficiários
/*/
method getQueryUsers(oFilter as object) as object class BeneficiariesregistrationBusinessObject

	local cQuery as Character
	local nOrder := 1 as numeric
    Local oStatement as object

	cQuery := "SELECT ? FROM ? BA1 "

	cQuery += " INNER JOIN ? BA3 "
	cQuery += "  ON BA1.BA1_FILIAL = ? "
	cQuery += " AND BA1.BA1_CODINT = ? "
	cQuery += " AND BA1.BA1_CODEMP = ? "
	cQuery += " AND BA1.BA1_MATRIC = ? "

	cQuery += " WHERE BA1.BA1_FILIAL = ? "
	cQuery += "   AND BA1.BA1_CODINT >= ? "
	cQuery += "   AND BA1.BA1_CODEMP >= ? "
	cQuery += "   AND BA1.BA1_MATRIC >= ? "
	cQuery += "   AND BA1.BA1_TIPREG >= ? "

	cQuery += "   AND BA1.BA1_CODINT <= ? "
	cQuery += "   AND BA1.BA1_CODEMP <= ? "
	cQuery += "   AND BA1.BA1_MATRIC <= ? "
	cQuery += "   AND BA1.BA1_TIPREG <= ? "

	if !empty(self:jFilterParams["oldSubscriberIdAt"])
		cQuery += " AND BA1.BA1_MATANT >= ? "
		cQuery += " AND BA1.BA1_MATANT <= ? "
	endif

	do case
	case self:jFilterParams["active"] == "1" // Ativo
		cQuery += " AND BA1.BA1_MOTBLO = ? "

	case self:jFilterParams["active"] == "2" // Bloqueado
		cQuery += " AND BA1.BA1_MOTBLO <> ? "
	endcase

	cQuery += " AND BA1.D_E_L_E_T_ = ? "
	cQuery += " AND BA3.D_E_L_E_T_ = ? "

    if oFilter:hasFilter() // Filtros setados na interface do Smart View
        cQuery += " AND ? "
    endif

	cQuery += " ORDER BY ?"

	cQuery := ChangeQuery(cQuery)

	oStatement := FWExecStatement():new(cQuery)

	oStatement:setUnsafe(nOrder++,"BA1.*,BA3.BA3_CODINT,BA3.BA3_CODPLA,BA3.BA3_VERSAO")
	oStatement:setUnsafe(nOrder++, retSqlName("BA1"))

	oStatement:setUnsafe(nOrder++, retSqlName("BA3"))
	oStatement:setString(nOrder++, xFilial("BA1"))
	oStatement:setUnsafe(nOrder++, "BA3.BA3_CODINT")
	oStatement:setUnsafe(nOrder++, "BA3.BA3_CODEMP")
	oStatement:setUnsafe(nOrder++, "BA3.BA3_MATRIC")

	oStatement:setString(nOrder++, xFilial("BA1"))
	oStatement:setString(nOrder++, self:jFilterParams["healthInsurerCodeFrom"])
	oStatement:setString(nOrder++, self:jFilterParams["companyCodeFrom"])
	oStatement:setString(nOrder++, self:jFilterParams["familyCodeFrom"])
	oStatement:setString(nOrder++, self:jFilterParams["typeRecordFrom"])

	oStatement:setString(nOrder++, self:jFilterParams["healthInsurerCodeAt"])
	oStatement:setString(nOrder++, self:jFilterParams["companyCodeAt"])
	oStatement:setString(nOrder++, self:jFilterParams["familyCodeAt"])
	oStatement:setString(nOrder++, self:jFilterParams["typeRecordAt"])

	if !empty(self:jFilterParams["oldSubscriberIdAt"])
		oStatement:setString(nOrder++, self:jFilterParams["oldSubscriberIdFrom"])
		oStatement:setString(nOrder++, self:jFilterParams["oldSubscriberIdAt"])
	endif

	do case
		case self:jFilterParams["active"] == "1" // Ativo
			oStatement:setString(nOrder++, "")

		case self:jFilterParams["active"] == "2" // Bloqueado
			oStatement:setString(nOrder++, "")
	endcase

	oStatement:setString(nOrder++, "")
	oStatement:setString(nOrder++, "")

	if oFilter:hasFilter() // Filtros setados na interface do Smart View
        oStatement:setUnsafe(nOrder++, oFilter:getSQLExpression())
    endif
	
	oStatement:setUnsafe(nOrder++, "BA1.BA1_CODINT, BA1.BA1_CODEMP, BA1.BA1_MATRIC, BA1.BA1_TIPREG")

return oStatement

/*/{Protheus.doc} validParameters
Realizar a validação dos parametros recebidos pelo smart view

@type method
@version 12.1.2310
@author jose.paulo
@since 10/09/2023
@return logical, se os parametros informados pelo Smart View estõa validos
/*/
method validParameters() as logical class BeneficiariesregistrationBusinessObject

	local aParams := {} as array
	local lSucess := .f. as logical
	local jResponse as json
	local aSituation := {{"value": "1", "description": "Ativos"}, {"value": "2", "description": "Bloqueados"}, {"value": "3", "description": "Todos"}} as array

	aAdd(aParams, {"question": "active", "description": "Situação", "options": aSituation, "required": .t., "size": 1})

	jResponse := UtilsBusinessObject():validQuestParams(aParams, self:jFilterParams)
	lSucess := jResponse["sucess"]

	Iif (!lSucess,self:setErrorStatus(jResponse["codError"], jResponse["message"], jResponse["detailedMsg"]),"")

return lSucess

/*/{Protheus.doc} getVirtualFieldsValue
Retorna o valor (conteudo) do campo virtual

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 09/11/2023
@param jItems, json, dados da família posicionada
@param cField, character, campo virtual para preenchimento
@return logical, se foi adicionado o valor do campo virtual
/*/
method getVirtualFieldsValue(cField as character) as variant class BeneficiariesregistrationBusinessObject

	local xValue as variant

	do case
		// Folder Dados Cadastrais
	case cField == "BA1_IDADE"
		xValue := Calc_Idade(dDataBase,STOD((self:cAliasTemp)->BA1_DATNAS))

	case cField == "BA1_DESUSU"
		xValue := Posicione("BIH",1,xFilial("BIH")+(self:cAliasTemp)->BA1_TIPUSU,"BIH_DESCRI")

	case cField == "BA1_DESCIV"
		xValue := (Posicione("SX5",1,xFilial("SX5")+"33"+(self:cAliasTemp)->BA1_ESTCIV,"X5_DESCRI"))

	case cField == "BA1_DESPRF"
		xValue := Posicione("BCX",1,xFilial("BCX")+(self:cAliasTemp)->BA1_CODPRF,"BCX_DESCRI")

	case cField == "BA1_MATUSU"
		xValue := (self:cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG)

	case cField == "BA1_DESPLA"
		If !Empty((self:cAliasTemp)->(BA1_CODPLA+BA1_VERSAO))
			xValue := posicione("BI3", 1, xFilial("BI3")+(self:cAliasTemp)->(BA1_CODINT+BA1_CODPLA+BA1_VERSAO), "BI3_DESCRI")
		EndIf

	case cField == "BA1_DESBLO"
		If !Empty((self:cAliasTemp)->BA1_DATBLO)
			If (self:cAliasTemp)->BA1_CONSID == "U"
				xValue := Posicione("BG3",1,xFilial("BG3")+(self:cAliasTemp)->BA1_MOTBLO,"BG3_DESBLO")
			Elseif (self:cAliasTemp)->BA1_CONSID == "F"
				xValue := Posicione("BG1",1,xFilial("BG1")+(self:cAliasTemp)->BA1_MOTBLO,"BG1_DESBLO")
			Elseif (self:cAliasTemp)->BA1_CONSID== "S"
				xValue := Posicione("BQU",1,xFilial("BQU")+(self:cAliasTemp)->BA1_MOTBLO,"BQU_DESBLO")
			Endif
		Endif

	case cField == "BA1_DESCFU"
		xValue := Posicione("SRJ",1,xFilial("SRJ")+(self:cAliasTemp)->BA1_CODFUN,"RJ_DESC")

	case cField == "BA1_DESSET"
		xValue := Posicione("BLM",1,xFilial("BLM")+(self:cAliasTemp)->BA1_CODSET,"BLM_DESSET")

	case cField == "BA1_NOMEQ"
		xValue := Posicione("BXL",1,xFilial("BXL")+(self:cAliasTemp)->BA1_EQUIPE,"BXL_DESEQU")

	case cField == "BA1_NOMVEN"
		xValue := Posicione("SA3",1,xFilial("SA3")+(self:cAliasTemp)->BA1_CODVEN,"A3_NOME")

	case cField == "BA1_NOMVE2"
		xValue := Posicione("SA3",1,xFilial("SA3")+(self:cAliasTemp)->BA1_CODVE2,"A3_NOME")

	case cField == "BA1_NOMCLI"
		xValue := posicione("SA1", 1, xFilial("SA1")+(self:cAliasTemp)->(BA1_CODCLI+BA1_LOJA), "A1_NOME")

	case cField == "BA1_NOMFOR"
		xValue := posicione("SA3", 1, xFilial("SA3")+(self:cAliasTemp)->(BA1_CODFOR+BA1_LOJFOR), "A3_NOME")

	case cField == "BA1_DESESC"
		xValue := Posicione("SX5",1,xFilial("SX5")+BA1->BA1_ESCOLA,"X5_DESCRI")

	endcase

return xValue

/*/{Protheus.doc} getComboBoxValue
Retorna o contéudo do campo de acordo com o combo box do X3_CBOX

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 23/05/2023
@param cValue, character, Valor do campo gravada no banco de dados
@param aComboBox, array, array com o combo box do campo (X3_CBOX)
@return character, retornar o de/para do valor do campo
/*/
method getComboBoxValue(cValue as character, aComboBox as array) as character class BeneficiariesregistrationBusinessObject

	local nX := 0 as numeric
	local aItems as array
	local cValueComboBox as character

	for nX := 1 to len(aComboBox)
		aItems := strTokArr(aComboBox[nX], "=")

		if len(aItems) >= 2 .and. alltrim(cValue) == alltrim(aItems[1])
			cValueComboBox := alltrim(aItems[2])
			exit
		endif
	next nX

return cValueComboBox

/*/{Protheus.doc} getX3Usado
Retorna os campos que possuem X3_USADO

@type method
@version Protheus 12.1.2310
@author jose.paulo
@since 25/11/2023
@return array, Array com os campos utilizados
/*/
method getX3Usado(cTable) as array class BeneficiariesregistrationBusinessObject
	local aFieldsX3 := {} as array
	local aFields := {} as array
	local cTable as character
	local nX as numeric

	//Pega todos os campos da Tabela
	aFieldsX3 := FWSX3Util():GetAllFields( cTable )

	//Percorre os campos e verifica se esta sendo Usado. Caso seja memo não será adicionado.
	for nX := 1 To Len(aFieldsX3)
		If (X3Uso(GetSx3Cache(aFieldsX3[nX], "X3_USADO")) .OR. aFieldsX3[nX] == cTable+'_FILIAL') .And. UPPER(getSx3Cache(aFieldsX3[nX], "X3_TIPO")) <> "M"
			aadd(aFields, alltrim(aFieldsX3[nX]))
		EndIf
	next nX

return aFields
