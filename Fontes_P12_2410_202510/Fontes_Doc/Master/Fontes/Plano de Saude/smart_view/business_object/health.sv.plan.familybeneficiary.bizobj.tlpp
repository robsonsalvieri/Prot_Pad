#include "msobject.ch"
#include "protheus.ch"
#include "tlpp-core.th"
#include "totvs.framework.treports.integratedprovider.th"

namespace totvs.protheus.health.smartview.plan.familybeneficiary

using namespace totvs.protheus.health.smartview.plan.utils

/*/{Protheus.doc} FamilyBeneficiariesBusinessObject
Objeto de negócio para o Smart View - Famila / Beneficiarios

@type class
@version 12.1.2310
@author cesarh.almeida
@since 02/10/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPLS", tables="BA1,BG9,BT5,BII,BQC,BIH,BRP", name="Familia/ Beneficiarios", country="ALL", initialRelease="12.1.2310")
class FamilyBeneficiariesBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data jFilterParams as json 
	private data lExistPergunte as logical 

	public method new() as object
	public method getData(nPage as numeric, oFilter as object) as object
	public method getSchema() as object

	private method setFilterParams(oFilter as object) as logical
	private method getQueryFamilyBeneficiaries(oFilter as object) as object
	private method validParameters() as logical
	private method changeDateFormat() as character
	private method formatDateR() as character

endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@return object, objeto da classe FamilyBeneficiariesBusinessObject
/*/   
method new() class FamilyBeneficiariesBusinessObject

	_Super:new()

	self:jFilterParams := JsonObject():new()

	self:appendArea(UtilsBusinessObject():getAreaName())
	self:setDisplayName("Familia/ Beneficiarios")
	self:setDescription("Totais de beneficiarios por familias, por contratos e por sub-contratos.")

	self:lExistPergunte := self:setPergunte("PLR615")
	if !self:lExistPergunte
		self:setErrorStatus(400, "Pergunte não encontrado", "Verifique o grupo de perguntas PLR615 na base de dados.")
	endif

return self

/*/{Protheus.doc} getData
Retorna o objeto de dados

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do Smart View
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getData(nPage as numeric, oFilter as object) as object class FamilyBeneficiariesBusinessObject

	local cAliasTemp as character
	local nCount := 0 as numeric
    local cCompanyGroup as character
    local cDescGroup as character
    local cContract as character
	local cContractPJ as character 
	local cVersionPJ as character
	local cTypeCon as character
	local cSubContractPJ as character
	local cSubVersionPJ as character
    local cSubContract as character
	local cSubscriberID as character 
	local cBenefName as character
	local cDateBirth as character
	local cGender as character
	local cMatiralStatus as character 
	local cUserType as character
	local cKinship as character
	local cDateInc as character
	local nAge as character
	Local oStatement as object

	if self:lExistPergunte
		self:setFilterParams(oFilter)
		if self:validParameters(oFilter)
			oStatement := self:getQueryFamilyBeneficiaries(oFilter)

			cAliasTemp := oStatement:openAlias()

			(cAliasTemp)->(DbGotop())

			while!(cAliasTemp)->(eof())

                Posicione("BG9",1,xFilial("BG9")+(cAliasTemp)->(BA1_CODINT+BA1_CODEMP),"BG9_DESCRI")

                cCompanyGroup  := (cAliasTemp)->(BA1_CODINT+BA1_CODEMP)
                cDescGroup := Substr(BG9->BG9_DESCRI,1,40)
                cContract := (cAliasTemp)->(BA1_CONEMP+BA1_VERCON) 
                cSubContract  := (cAliasTemp)->(BA1_SUBCON+BA1_VERSUB)

				while !(cAliasTemp)->(eof()) .And. (cAliasTemp)->(BA1_CODINT+BA1_CODEMP) == cCompanyGroup				

                    If cValtoChar(self:jFilterParams["contractType"]) $ "23" 
                 
						BT5->(DbSetOrder(1))
						BT5->(DbSeek(xFilial("BT5")+(cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_CONEMP+BA1_VERCON)))

						cContractPJ := (cAliasTemp)->BA1_CONEMP
						cTypeCon := Posicione("BII",1,xFilial("BII")+BT5->BT5_TIPCON,"BII_DESCRI")			
						cVersionPJ := (cAliasTemp)->BA1_VERCON + cTypeCon
						
                    EndIf

                    cCompanyGroup  := (cAliasTemp)->(BA1_CODINT+BA1_CODEMP)
                    cContract  := (cAliasTemp)->(BA1_CONEMP+BA1_VERCON)
                    cSubContract  := (cAliasTemp)->(BA1_SUBCON+BA1_VERSUB)	

                    while !(cAliasTemp)->(Eof()) .And. (cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_CONEMP+BA1_VERCON) == cCompanyGroup+cContract

						If  cValToChar(self:jFilterParams["contractType"]) $ "23" 

							BQC->(DbSetOrder(1))
							BQC->(DbSeek(xFilial("BQC")+(cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_CONEMP+BA1_VERCON+BA1_SUBCON+BA1_VERSUB)))

							cSubContractPJ := (cAliasTemp)->BA1_SUBCON
							cSubVersionPJ := (cAliasTemp)->BA1_VERSUB + "-" + BQC->BQC_DESCRI

						EndIf

						cCompanyGroup  := (cAliasTemp)->(BA1_CODINT+BA1_CODEMP)
						cContract  := (cAliasTemp)->(BA1_CONEMP+BA1_VERCON)
						cSubContract  := (cAliasTemp)->(BA1_SUBCON+BA1_VERSUB)	

						while ! (cAliasTemp)->(Eof()) .And. (cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_CONEMP+BA1_VERCON+BA1_SUBCON+BA1_VERSUB) ==cCompanyGroup+cContract+cSubContract

							cUser := (cAliasTemp)->BA1_CODINT+"."+(cAliasTemp)->BA1_CODEMP+"."+(cAliasTemp)->BA1_MATRIC							

							cSubscriberID := (cAliasTemp)->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG)
							cBenefName := Substr((cAliasTemp)->BA1_NOMUSR,1,38)
							cDateBirth := self:formatDateR(STOD((cAliasTemp)->BA1_DATNAS))
							nAge := Calc_Idade(dDataBase,CTOD(cDateBirth))
							cGender := (cAliasTemp)->BA1_SEXO
							cMatiralStatus := Substr(Posicione("SX5",1,xFilial("SX5")+"33"+(cAliasTemp)->BA1_ESTCIV,"X5_DESCRI"),1,15)
							cUserType := Substr(Posicione("BIH",1,xFilial("BIH")+(cAliasTemp)->BA1_TIPUSU,"BIH_DESCRI"),1,15)
							cKinship := Substr(Posicione("BRP",1,xFilial("BRP")+(cAliasTemp)->BA1_GRAUPA,"BRP_DESCRI"),1,15)
							cDateInc := self:formatDateR(STOD((cAliasTemp)->BA1_DATINC))						

							self:oData:appendData({"GRUPOEMPRESA": cCompanyGroup,;
													"DESCGROUP": cDescGroup,;
													"CONTRATO": cContractPJ,; 
													"VERSAO": cVersionPJ,;
													"SUBCONTR": cSubContractPJ,;
													"VERSAOSUB": cSubVersionPJ,; 
													"MATRICULA": cSubscriberID,;
													"NOME": cBenefName,;
													"DTNASC": cDateBirth,;
													"IDADE": nAge,;
													"SEXO": cGender,;
													"ESTCIVIL": cMatiralStatus,;
													"TIPOUSUARIO": cUserType,;
													"PARENTESCO": cKinship,;
													"DTINCLUSAO": cDateInc})

							(cAliasTemp)->(dbSkip())

						enddo
                    enddo								
                      			
					nCount++

					if nCount == self:getPageSize()
						exit
					endif
					
				enddo
			enddo

			self:setHasNext(!(cAliasTemp)->(eof()))

			(cAliasTemp)->(dbCloseArea())

		endif
	endif

return self:oData

/*/{Protheus.doc} getSchema
Retorna a Estrutura de dados

@type method
@version Protheus 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getSchema() as object class FamilyBeneficiariesBusinessObject

    self:addProperty("GRUPOEMPRESA", "GrupoEmpresa", "string", "GrupoEmpresa", "GRUPOEMPRESA")
	self:addProperty("DESCGROUP", "DescGrpEmp", "string", "DescGrpEmp", "DESCGROUP")
    self:addProperty("CONTRATO", "Contrato", "string", "Contrato", "CONTRATO")
    self:addProperty("VERSAO", "Versao", "string", "Versao", "VERSAO")
    self:addProperty("SUBCONTR", "SubContr", "string", "SubContr", "SUBCONTR")
    self:addProperty("VERSAOSUB", "VersaoSub", "string", "VersaoSub", "VERSAOSUB")
    self:addProperty("MATRICULA", "Matricula", "string", "Matricula", "MATRICULA")
	self:addProperty("NOME", "Nome", "string", "Nome", "Nome")
	self:addProperty("DTNASC", "Dt.Nasc.", "string", "Dt.Nasc.", "DTNASC")
	self:addProperty("IDADE","Idade","number","Idade","IDADE")
	self:addProperty("SEXO", "Sexo", "string", "Sexo", "SEXO")
    self:addProperty("ESTCIVIL", "Est.Civil", "string", "Est.Civil", "ESTCIVIL")
    self:addProperty("TIPOUSUARIO", "TipoUsuario", "string", "TipoUsuario", "TIPOUSUARIO")
    self:addProperty("PARENTESCO", "Parentesco", "string", "Parentesco", "PARENTESCO")
    self:addProperty("DTINCLUSAO", "Dt.Inicio", "string", "Dt.Inicio", "DTINCLUSAO")

return self:oSchema

/*/{Protheus.doc} setFilterParams
Defini os parâmetros do pergunte recebido pelo smart view para ser usado
na montagem da query 

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@param oFilter, object, Objeto de filtro do Smart View
@return logical, se os parâmetros foram definidos com sucesso
/*/
method setFilterParams(oFilter as object) as logical class FamilyBeneficiariesBusinessObject

	local jParams := oFilter:getParameters() as json
	local lOk := .t. as logical

	self:jFilterParams["contractType"] := jParams["MV_PAR01"][1] // Tipo Contrato ?
	self:jFilterParams["reportType"] := jParams["MV_PAR02"][1] // Tipo de Relatorio ?
	self:jFilterParams["considerRecords"] := jParams["MV_PAR03"][1] // Considera Registros ?
	self:jFilterParams["printSummary"] := jParams["MV_PAR04"][1] // Imprime Resumo ?
	self:jFilterParams["healthInsurerCodeTo"] := jParams["MV_PAR05"][1] // Operadora de ?
	self:jFilterParams["healthInsurerCodeFrom"] := jParams["MV_PAR06"][1] // Operadora Ate ?
	self:jFilterParams["companyGroupTo"] := jParams["MV_PAR07"][1] // Grupo/Empresa De ?
	self:jFilterParams["companyGroupFrom"] := jParams["MV_PAR08"][1] // Grupo/Empresa Ate ?
	self:jFilterParams["contractTo"] := jParams["MV_PAR09"][1] // Contrato De ?
	self:jFilterParams["contractFrom"] := jParams["MV_PAR10"][1] // Contrato Ate ?
	self:jFilterParams["subcontractTo"] := jParams["MV_PAR11"][1] // Sub-Contrato De  ?
    self:jFilterParams["subcontractFrom"] := jParams["MV_PAR12"][1] // Sub-Contrato Ate  ?	
    self:jFilterParams["monthYear"] := self:changeDateFormat(jParams["MV_PAR13"][1]) // Data Referencia  ?

return lOk

/*/{Protheus.doc} getQueryFamilyBeneficiaries
Retorna a query para buscar as familias x beneficiarios dos parâmetros informados
para o objeto de negocio

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@param oFilter, object, Objeto de filtro do Smart View
@return character, query completa para buscar as familias x beneficiarios
/*/
method getQueryFamilyBeneficiaries(oFilter as object) as object class FamilyBeneficiariesBusinessObject

	local cQuery as character
	local nOrder := 1 as numeric
	Local oStatement as object

	cQuery += "SELECT ? "
	cQuery += " FROM ? BA1 "

	cQuery += " WHERE BA1.BA1_FILIAL = ? AND "
	cQuery += "		  BA1.BA1_CODINT >= ? AND "
	cQuery += "		  BA1.BA1_CODINT <= ? AND "
	cQuery += "		  BA1.BA1_CODEMP >= ? AND " 
	cQuery += "		  BA1.BA1_CODEMP <= ? AND "   

	If self:jFilterParams["contractType"] == 3 // Ambos os tipos de pessoa

		cQuery += "   BA1.BA1_CONEMP >= ? AND " 
		cQuery += "	  BA1.BA1_CONEMP <= ? AND " 
		cQuery += "	  BA1.BA1_SUBCON >= ? AND " 
		cQuery += "	  BA1.BA1_SUBCON <= ? AND " 
		cQuery += "	  BA1.BA1_CONEMP != ? OR BA1.BA1_CONEMP = ? AND "    
		cQuery += "	  BA1.BA1_SUBCON != ? OR BA1.BA1_SUBCON = ? AND "  
	
	ElseIf self:jFilterParams["contractType"] == 2 // Pessoa Juridica

		cQuery += "	  BA1.BA1_CONEMP >= ? AND " 
		cQuery += "	  BA1.BA1_CONEMP <= ? AND " 
		cQuery += "	  BA1.BA1_SUBCON >= ? AND " 
		cQuery += "	  BA1.BA1_SUBCON <= ? AND " 
		cQuery += "	  BA1.BA1_CONEMP != ? AND "   
		cQuery += "	  BA1.BA1_SUBCON != ? AND " 

	ElseIf self:jFilterParams["contractType"] == 1 // Pessoa Fisica

		cQuery += "	  BA1.BA1_CONEMP = ? AND "   
		cQuery += "	  BA1.BA1_SUBCON = ? AND "   

	EndIf 

	cQuery += "		  BA1.BA1_DATINC <= ? AND "
	
	If  self:jFilterParams["considerRecords"] == 1 //Ativo
		cQuery += "  (BA1.BA1_DATBLO = ? OR BA1.BA1_DATBLO > ? ) AND "
	ElseIf self:jFilterParams["considerRecords"] == 2  //Bloqueado
		cQuery += "  (BA1.BA1_DATBLO <> ? AND BA1.BA1_DATBLO <= ? ) AND "
	EndIf

	cQuery += " 	  BA1.D_E_L_E_T_ = ? "
	cQuery += " ? "

	cQuery += " ORDER BY ? "

	cQuery := changeQuery(cQuery)

	oStatement := FWExecStatement():new(cQuery)

	oStatement:setUnsafe(nOrder++, "BA1_CODINT, BA1_CODEMP, BA1_CONEMP, BA1_SUBCON, BA1_MOTBLO, BA1_VERCON, BA1_VERSUB ,BA1_MATRIC, BA1_TIPREG,"+;
									"BA1_NOMUSR, BA1_DATNAS, BA1_SEXO, BA1_ESTCIV, BA1_TIPUSU, BA1_GRAUPA, BA1_DATINC, BA1_MATANT")

	oStatement:setUnsafe(nOrder++, retSqlName("BA1"))
	oStatement:setString(nOrder++, xFilial("BA1"))
	oStatement:setString(nOrder++,self:jFilterParams["healthInsurerCodeTo"])
	oStatement:setString(nOrder++,self:jFilterParams["healthInsurerCodeFrom"])
	oStatement:setString(nOrder++,self:jFilterParams["companyGroupTo"])
	oStatement:setString(nOrder++,self:jFilterParams["companyGroupFrom"])

	If  self:jFilterParams["contractType"] == 3 .Or. self:jFilterParams["contractType"] == 2

		oStatement:setString(nOrder++,self:jFilterParams["contractTo"])
		oStatement:setString(nOrder++,self:jFilterParams["contractFrom"])
		oStatement:setString(nOrder++,self:jFilterParams["subcontractTo"])
		oStatement:setString(nOrder++,self:jFilterParams["subcontractFrom"])
	Endif

	If self:jFilterParams["contractType"] == 3 // Ambos os tipos de pessoa

		oStatement:setString(nOrder++,"")
		oStatement:setString(nOrder++,"")
		oStatement:setString(nOrder++,"")
		oStatement:setString(nOrder++,"")
	
	ElseIf self:jFilterParams["contractType"] == 2 .Or. self:jFilterParams["contractType"] == 1 // Pessoa Juridica ou Pessoa Fisica

		oStatement:setString(nOrder++,"")
		oStatement:setString(nOrder++,"")

	EndIf
	
	oStatement:setString(nOrder++,self:jFilterParams["monthYear"])

	If self:jFilterParams["considerRecords"] == 1 .Or. self:jFilterParams["considerRecords"] == 2
		oStatement:setString(nOrder++,"")
		oStatement:setString(nOrder++,self:jFilterParams["monthYear"])
	Endif

	oStatement:setString(nOrder++," ")
	oStatement:setUnsafe(nOrder++,IIf(oFilter:hasFilter()," AND "+oFilter:getSQLExpression(),""))
	oStatement:setUnsafe(nOrder++,"BA1_CODINT, BA1_CODEMP, BA1_CONEMP, BA1_VERCON, BA1_SUBCON, BA1_VERSUB, BA1_MATRIC, BA1_TIPREG")

return oStatement

/*/{Protheus.doc} validParameters
Realizar a validação dos parametros recebidos pelo smart view

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@return logical, se os parametros informados pelo Smart View estõa validos
/*/
method validParameters(oFilter) as logical class FamilyBeneficiariesBusinessObject

	local jParams := oFilter:getParameters() as json
	local lSucess := .t. as logical

	If EMPTY(jParams['MV_PAR01'][1]) .OR. EMPTY(jParams['MV_PAR02'][1]) .OR.;
	   EMPTY(jParams['MV_PAR03'][1]) .OR. EMPTY(jParams['MV_PAR04'][1]) .OR.;
	   EMPTY(jParams['MV_PAR06'][1]) .OR. EMPTY(jParams['MV_PAR08'][1]) .OR.;
	   EMPTY(jParams['MV_PAR10'][1]) .OR. EMPTY(jParams['MV_PAR12'][1])

		lSucess := .F.

	Endif
	
return lSucess

/*/{Protheus.doc} changeDateFormat
Altera formato da data

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@return character, data
/*/
method changeDateFormat(cDate) as character class FamilyBeneficiariesBusinessObject

	local adateFormated as array 

	adateFormated := StrTokArr(cDate, "T")
	cDate := StrTran( adateFormated[1], "-", "" )
	
return cDate

/*/{Protheus.doc} formatDate
Altera formato da data

@type method
@version 12.1.2310  
@author cesarh.almeida
@since 02/10/2023
@return character, data
/*/
method formatDateR(cDate) as character class FamilyBeneficiariesBusinessObject

	cDate := FwTimeStamp(2, cDate)
	cDate := SUBSTR(cDate,1,10)
	
return cDate
