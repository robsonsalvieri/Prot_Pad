#include "tlpp-core.th"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
  
namespace totvs.protheus.health.smartview.plan.servicenetwork

using namespace totvs.protheus.health.smartview.plan.utils
 
/*/{Protheus.doc} BusinessObject
Objeto de negócio para o Smart View - Rede de Atendimento

@type class
@version Protheus 12.1.2310
@author guilherme.carreiro
@since 29/04/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BAU,B18,BID,SA2,BAH,SRA", name = "Rede de Atendimento", country="ALL", initialRelease="12.1.2210")
class BAUTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    private data aFields as array
    private data cAliasTemp as character
    private data jFilterParams as json 
	private data lExistPergunte as logical 

    public method new() as object
    public method getData(nPage as numeric, oFilter as object) as object
    public method getSchema() as object

    private method setFilterParams(oFilter as object) as logical
    private method validParameters() as logical
    private method getFieldsBusinessObject() as array 
	private method getQueryServiceNetwork(oFilter as object) as object
    private method getVirtualFieldsValue(cField as character) as variant
    private method getComboBoxValue(cValue as character, aComboBox as array) as character

endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 29/04/2023
@return object, objeto da classe BAUTReportsBusinessObject
/*/ 
method new() class BAUTReportsBusinessObject

    _Super:new()

    self:jFilterParams := JsonObject():new()
    self:aFields := self:getFieldsBusinessObject()

    self:appendArea(UtilsBusinessObject():getAreaName())
    self:setDisplayName("Rede de Atendimento")
    self:setDescription("Relação dos dados cadastrais das Redes de Atendimento.")

    self:lExistPergunte := self:setPergunte("PLR606")
    if !self:lExistPergunte
        self:setErrorStatus(400, "Pergunte não encontrado", "Verifique o grupo de perguntas PLR606 na base de dados.")
    endif
 
return self

/*/{Protheus.doc} getData
Retorna o objeto de dados

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 29/04/2023
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do Smart View
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getData(nPage as numeric, oFilter as object) as object class BAUTReportsBusinessObject

    Local oStatement as object
    local nSkip := 0 as numeric
    local nCount := 0 as numeric
    local nX := 0 as numeric
    local jItems as json
    local lVirtual as logical
    local lCombo as logical
    local cComboBox as character
    local aComboBox as array
    local cField as character
    local cFieldId as character
    local xFieldValue as variant
    local aPDFields as array
    local lObfuscated as logical
    local lValueAnonymize as logical

    if self:lExistPergunte
        self:setFilterParams(oFilter)
		if self:validParameters()

            oStatement := self:getQueryServiceNetwork(oFilter)
            
            self:cAliasTemp := oStatement:openAlias()

            if nPage == 1
                (self:cAliasTemp)->(dbGoTop())
            else
                nSkip := ((nPage - 1) * self:getPageSize())

                (self:cAliasTemp)->(dbSkip(nSkip))
            endif

            aPDFields := FwProtectedDataUtil():usrAccessPDField(__cUserID, self:aFields)
            lObfuscated := len(aPDFields) <> len(self:aFields)

            while !(self:cAliasTemp)->(eof())
                jItems := JsonObject():new()
            
                for nX := 1 to len(self:aFields)
                    cField := self:aFields[nX]
                    lVirtual := upper(alltrim(getSx3Cache(cField, "X3_CONTEXT"))) == "V"
                    cComboBox := alltrim(getSx3Cache(cField, "X3_CBOX"))
                    lCombo := !empty(cComboBox)
                    cFieldId := strTran(cField, "_", "")

                    if lVirtual
                        xFieldValue := self:getVirtualFieldsValue(cField)
                    else
                        xFieldValue := (self:cAliasTemp)->&(self:aFields[nX])
                    endif

                    lValueAnonymize := iif(lObfuscated .and. aScan(aPDFields, cField) == 0, .t., .f.)

                    xFieldValue := iif(lValueAnonymize, FwProtectedDataUtil():ValueAsteriskToAnonymize(xFieldValue), xFieldValue)

                    if !lVirtual .and. !lValueAnonymize
                        if getSx3Cache(cField, "X3_TIPO") == "D"
                            if !empty(xFieldValue)
								xFieldValue := totvs.framework.treports.date.stringToTimeStamp(xFieldValue)
							else
								xFieldValue := nil
							endif
                        else
                            if lCombo
                                aComboBox := strTokArr(cComboBox, ";")
                                xFieldValue := self:getComboBoxValue(xFieldValue, aComboBox)
                            endif
                        endif          
                    endif

                    jItems[cFieldId] := xFieldValue
                next nX

                self:oData:appendData(jItems)

                (self:cAliasTemp)->(dbSkip())
                nCount++

                if nCount == self:getPageSize()
                    exit
                endif
            enddo

            self:setHasNext(!(self:cAliasTemp)->(eof()))

            (self:cAliasTemp)->(dbCloseArea())

        endif
    endif

return self:oData

/*/{Protheus.doc} getSchema
Retorna a Estrutura de dados

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 29/04/2023
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getSchema() as object class BAUTReportsBusinessObject

    local nX := 0 as numeric
    local cField as character
    local nPos as numeric
    local cId as character
    local cDescription as character
    local cType as character
    local cDisplayName as character
    local cRealName as character
    local cTypeSX3 as character

    for nX := 1 to len(self:aFields)
        cField := self:aFields[nX]
        nPos := at(".", cField) + 1
        cField := iif(nPos > 0, substr(cField, nPos), cField)

        cId := strTran(cField, "_", "") 
        cDescription := alltrim(getSx3Cache(cField, "X3_DESCRIC"))
        cDisplayName := alltrim(getSx3Cache(cField, "X3_TITULO"))
        cRealName := cField
        cType := "string"
        cTypeSX3 := getSx3Cache(cField, "X3_TIPO")

        do case
            case cTypeSX3 == "C"
                cType := "string"
            case cTypeSX3 == "D"
                cType := "date"
            case cTypeSX3 == "N"
                cType := "number"
        endcase

        self:addProperty(cId, cDescription, cType, cDisplayName+" ("+cField+")", cRealName)
    next nX

return self:oSchema

/*/{Protheus.doc} getFieldsBusinessObject
Retorna os campos que seram utilizados no objeto de negocio - Rede de Atendimento

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 25/06/2023
@return array, Array com os campos do objeto de negocio Rede de Atendimento
/*/ 
method getFieldsBusinessObject() as array class BAUTReportsBusinessObject

    local aFields as array

    aFields := {;
    "BAU_FILIAL", "BAU_OK", "BAU_ACDTRB", "BAU_TABPRO", "BAU_TPPROD", "BAU_GUIPAP", "BAU_ACEPAR", "BAU_B2B",;
    "BAU_AOINT", "BAU_ACAO", "BAU_DTINT", "BAU_PQUANS", "BAU_FILFU2", "BAU_OBRPTO", "BAU_GRALAU", "BAU_ENVRPS",;
    "BAU_GERRPS", "BAU_INTC", "BAU_TPCALC", "BAU_TISVER", "BAU_DIVANS", "BAU_DIVQUA", "BAU_ENVWSD", "BAU_NRCRNA",;
    "BAU_NRLUNI", "BAU_INSCAC", "BAU_SEQDOC", "BAU_DATATU", "BAU_RDASUB", "BAU_EXCEDI", "BAU_MATFU3", "BAU_FILFU3",;
    "BAU_MATFU4", "BAU_FILFU4", "BAU_HOSINT", "BAU_OBSGUI", "BAU_INATIV", "BAU_MULTTH", "BAU_DIGANE", "BAU_INDDOU",;
    "BAU_IS9001", "BAU_DTINAT", "BAU_ANSOPI", "BAU_RETCON", "BAU_GUICAR", "BAU_CODIGO", "BAU_TIPPE", "BAU_CPFCGC",;
    "BAU_NOME", "BAU_NREDUZ", "BAU_NFANTA", "BAU_RECPRO", "BAU_DTINCL", "BAU_TIPPRE", "BAU_CODOPE", "BAU_COPCRE",;
    "BAU_CATHOS", "BAU_ALTCUS", "BAU_LEGEND", "BAU_FAX", "BAU_TIPLOG", "BAU_CEP", "BAU_END", "BAU_NUMERO",;
    "BAU_COMPL", "BAU_BAIRRO", "BAU_MUN", "BAU_EST", "BAU_TEL", "BAU_SEXO", "BAU_NASFUN", "BAU_ESTCIV",;
    "BAU_ANOFOR", "BAU_CODBB0", "BAU_CODBK6", "BAU_DTEXCL", "BAU_CORRES", "BAU_CODBLO", "BAU_DATBLO",;
    "BAU_EMAIL", "BAU_CONJUG", "BAU_CC", "BAU_ITECTA", "BAU_CLVL", "BAU_NACION", "BAU_WEB", "BAU_GAUSS", ;
    "BAU_CONTRA", "BAU_CLAEST", "BAU_CNES", "BAU_DTINSE", "BAU_DTINCT", "BAU_DIRTEC", "BAU_DIRREG", "BAU_TIPDIS",;
    "BAU_RELAOP", "BAU_PRTACD", "BAU_GUIMED", "BAU_SIGDIR", "BAU_UFCDIR", "BAU_TIPRED", "BAU_INSACR", "BAU_NIVACR",;
    "BAU_QTDUS", "BAU_NTVISA", "BAU_SIGLCR", "BAU_ESTCR", "BAU_CONREG", "BAU_SIGCR2", "BAU_ESTCR2", "BAU_CONRE2",;
    "BAU_INSCR", "BAU_INSCRM", "BAU_RG", "BAU_CBO", "BAU_MATVID", "BAU_TIPDOC", "BAU_ENFER", "BAU_APTO", "BAU_URGEME",;
    "BAU_CODCFG", "BAU_NRLTOT", "BAU_NRLCON", "BAU_NRLPIS", "BAU_NRUTIA", "BAU_NRUTIN", "BAU_NRUTIP", "BAU_PRDATN",;
    "BAU_NRLINT", "BAU_NLTCLI", "BAU_NLTCIR", "BAU_NLTOBS", "BAU_NLTPED", "BAU_NLTPSI", "BAU_NRLDIA", "BAU_CALIMP",;
    "BAU_CALIRF", "BAU_CODRET", "BAU_ISS", "BAU_INSS", "BAU_BASINS", "BAU_CODSA2", "BAU_LOJSA2", "BAU_PRDPAG",;
    "BAU_POLFIN", "BAU_DIAPOL", "BAU_FORPGT", "BAU_DIAPGT", "BAU_PAGPRO", "BAU_MODPAG", "BAU_MATFUN", "BAU_FILFUN",;
    "BAU_GRPPAG", "BAU_AUTGER", "BAU_MATFU2", "BAU_CALPGT", "BAU_TPPAG",;
    "BAU_DESCTL", "BAU_DESMUN", "BAU_NOMSA2", "BAU_DESSIG", "BAU_DESSI2", "BAU_BANCO", "BAU_AGENCI", "BAU_CONTA", "BAU_NATURE", "BAU_NOMFUN";
    }

return aFields

/*/{Protheus.doc} setFilterParams
Defini os parâmetros do pergunte recebido pelo smart view para ser usado
na montagem da query 

@type method
@version 12.1.2310  
@author guilherme.carreiro
@since 23/09/2023
@param oFilter, object, Objeto de filtro do Smart View
@return logical, se os parâmetros foram definidos com sucesso
/*/
method setFilterParams(oFilter as object) as logical class BAUTReportsBusinessObject

	local jParams := oFilter:getParameters() as json
	local lOk := .t. as logical

    self:jFilterParams["cServiceNetworkCodeFrom"] := jParams["MV_PAR01"][1] // Codigo Inicial ?
    self:jFilterParams["cServiceNetworkCodeAt"] := jParams["MV_PAR02"][1] // Codigo Final ?
    self:jFilterParams["cSituationCode"] := cValToChar(jParams["MV_PAR03"][1]) // Situacao?

return lOk


/*/{Protheus.doc} getQueryServiceNetwork
Retorna a query das Redes de Atendimento do objeto de negocio

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 29/04/2023
@param oFilter, object, Objeto de filtro do Smart View
@return Character, where da query das Redes de Atendimento
/*/
method getQueryServiceNetwork(oFilter as object) as object class BAUTReportsBusinessObject

	local cQuery as Character
    local nOrder := 1 as numeric
    Local oStatement as object

    cQuery := "SELECT ? FROM ? BAU"

    cQuery += " LEFT JOIN ? B18"
    cQuery += "  ON B18.B18_FILIAL = ? "
    cQuery += " AND B18.B18_CODIGO = ? "
    cQuery += " AND B18.D_E_L_E_T_ = ? "
    
    cQuery += " LEFT JOIN ? BID"
    cQuery += "  ON BID.BID_FILIAL = ? "
    cQuery += " AND BID.BID_CODMUN = ? "
    cQuery += " AND BID.D_E_L_E_T_ = ? "
    
    cQuery += " LEFT JOIN ? SA2"
    cQuery += "  ON SA2.A2_FILIAL = ? "
    cQuery += " AND SA2.A2_COD = ? "
    cQuery += " AND SA2.A2_LOJA = ? "
    cQuery += " AND SA2.D_E_L_E_T_ = ? "
    
    cQuery += " LEFT JOIN ? BAH"
    cQuery += "  ON BAH.BAH_FILIAL = ? "
    cQuery += " AND BAH.BAH_CODIGO = ? " 
    cQuery += " AND BAH.D_E_L_E_T_ = ? "
    
    cQuery += " LEFT JOIN ? SRA"
    cQuery += "  ON SRA.RA_FILIAL = ? "
    cQuery += " AND SRA.RA_MAT = ? "
    cQuery += " AND SRA.D_E_L_E_T_ = ? "

    cQuery += " WHERE BAU_FILIAL = ? "
    cQuery += " AND BAU.BAU_CODIGO >= ? "
    cQuery += " AND BAU.BAU_CODIGO <= ? "

    do case
        case self:jFilterParams["cSituationCode"] == "1" // Ativo
            cQuery += " AND BAU.BAU_CODBLO = ? "

        case self:jFilterParams["cSituationCode"] == "2" // Bloqueado
            cQuery += " AND BAU.BAU_CODBLO <> ? "
    endcase

    cQuery += " AND BAU.D_E_L_E_T_ = ? "

    if oFilter:hasFilter() // Filtros setados na interface do Smart View
        cQuery += " AND ? "
    endif

    cQuery += " ORDER BY ?"

    cQuery := ChangeQuery(cQuery)

	oStatement := FWExecStatement():new(cQuery)
    
    oStatement:setUnsafe(nOrder++,"BAU.*")
	oStatement:setUnsafe(nOrder++, RetSQLName("BAU"))

	oStatement:setUnsafe(nOrder++, RetSQLName("B18"))
	oStatement:setString(nOrder++, FWxFilial('B18'))
	oStatement:setUnsafe(nOrder++, "BAU.BAU_TIPLOG")
	oStatement:setString(nOrder++, " ")

	oStatement:setUnsafe(nOrder++, RetSQLName("BID"))
	oStatement:setString(nOrder++, FWxFilial('BID'))
	oStatement:setUnsafe(nOrder++, "BAU.BAU_MUN")
	oStatement:setString(nOrder++, " ")

	oStatement:setUnsafe(nOrder++, RetSQLName("SA2"))
	oStatement:setString(nOrder++, FWxFilial('SA2'))
	oStatement:setUnsafe(nOrder++, "BAU.BAU_CODSA2")
	oStatement:setUnsafe(nOrder++, "BAU.BAU_LOJSA2")
	oStatement:setString(nOrder++, " ")

	oStatement:setUnsafe(nOrder++, RetSQLName("BAH"))
	oStatement:setString(nOrder++, FWxFilial('BAH'))
	oStatement:setUnsafe(nOrder++, "BAU.BAU_SIGLCR")
	oStatement:setString(nOrder++, " ")

	oStatement:setUnsafe(nOrder++, RetSQLName("SRA"))
	oStatement:setString(nOrder++, FWxFilial('SRA'))
	oStatement:setUnsafe(nOrder++, "BAU.BAU_MATFUN")
	oStatement:setString(nOrder++, " ")

	oStatement:setString(nOrder++, xFilial("BAU"))
	oStatement:setString(nOrder++, self:jFilterParams["cServiceNetworkCodeFrom"])
	oStatement:setString(nOrder++, self:jFilterParams["cServiceNetworkCodeAt"])

    do case
        case self:jFilterParams["cSituationCode"] == "1" // Ativo
	        oStatement:setString(nOrder++, " ")

        case self:jFilterParams["cSituationCode"] == "2" // Bloqueado
	        oStatement:setString(nOrder++, " ")
    endcase

    oStatement:setString(nOrder++, " ")

    if oFilter:hasFilter() // Filtros setados na interface do Smart View
        oStatement:setUnsafe(nOrder++, oFilter:getSQLExpression())
    endif

	oStatement:setUnsafe(nOrder++, "BAU.BAU_CODIGO, BAU.BAU_NOME")

return oStatement

/*/{Protheus.doc} validParameters
Realizar a validação dos parametros recebidos pelo smart view

@type method
@version 12.1.2310
@author guilherme.carreiro
@since 23/09/2023
@return logical, se os parametros informados pelo Smart View estõa validos
/*/
method validParameters() as logical class BAUTReportsBusinessObject

	local aParams := {} as array
	local lSucess := .f. as logical
	local jResponse as json
	local aSituation := {{"value": "1", "description": "Ativos"}, {"value": "2", "description": "Bloqueados"}, {"value": "3", "description": "Todos"}} as array

	aAdd(aParams, {"question": "cSituationCode", "description": "Situação", "options": aSituation, "required": .t., "size": 1})

	jResponse := UtilsBusinessObject():validQuestParams(aParams, self:jFilterParams)
	lSucess := jResponse["sucess"]
	if !lSucess
		self:setErrorStatus(jResponse["codError"], jResponse["message"], jResponse["detailedMsg"])
	endif

return lSucess

/*/{Protheus.doc} getVirtualFieldsValue
Retorna o valor (conteudo) do campo virtual

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 20/05/2023
@param jItems, json, dados da rede de atendimento posicionada
@param cField, character, campo virtual para preenchimento
@return logical, se foi adicionado o valor do campo virtual
/*/
method getVirtualFieldsValue(cField as character) as variant class BAUTReportsBusinessObject

    local xValue as variant

    do case
        case cField == "BAU_DESCTL"
            xValue := Posicione("B18",1,xFilial("B18")+(self:cAliasTemp)->BAU_TIPLOG,"B18_DESCRI")
        
        case cField == "BAU_DESMUN"
            xValue := Posicione("BID", 1, xFilial("BID")+(self:cAliasTemp)->BAU_MUN, "BID_DESCRI")
        
        case cField == "BAU_NOMSA2"
            xValue := Posicione("SA2",1,xFilial("SA2")+(self:cAliasTemp)->(BAU_CODSA2+BAU_LOJSA2),"A2_NOME")

        case cField == "BAU_DESSIG"
            xValue := Posicione("BAH",1,xFilial("BAH")+(self:cAliasTemp)->BAU_SIGLCR,"BAH_DESCRI")

        case cField == "BAU_DESSI2"
             xValue := Posicione("BAH",1,xFilial("BAH")+(self:cAliasTemp)->BAU_SIGCR2,"BAH_DESCRI")

        case cField == "BAU_BANCO"
            xValue := Posicione("SA2",1,xFilial("SA2")+(self:cAliasTemp)->(BAU_CODSA2+BAU_LOJSA2),"A2_BANCO")

        case cField == "BAU_AGENCI"
            xValue := Posicione("SA2",1,xFilial("SA2")+(self:cAliasTemp)->(BAU_CODSA2+BAU_LOJSA2),"A2_AGENCIA")

        case cField == "BAU_CONTA"
            xValue := Posicione("SA2",1,xFilial("SA2")+(self:cAliasTemp)->(BAU_CODSA2+BAU_LOJSA2),"A2_NUMCON")

        case cField == "BAU_NATURE"
            xValue := Posicione("SA2",1,xFilial("SA2")+(self:cAliasTemp)->(BAU_CODSA2+BAU_LOJSA2),"A2_NATUREZ")

        case cField == "BAU_NOMFUN"
            xValue := Posicione("SRA",1,xFilial("SRA")+(self:cAliasTemp)->(BAU_FILFUN+BAU_MATFUN),"RA_NOME")

    endcase

return xValue

/*/{Protheus.doc} getComboBoxValue
Retorna o contéudo do campo de acordo com o combo box do X3_CBOX

@type method
@version Protheus 12.1.2310  
@author guilherme.carreiro
@since 23/05/2023
@param cValue, character, Valor do campo gravada no banco de dados
@param aComboBox, array, array com o combo box do campo (X3_CBOX)
@return character, retornar o de/para do valor do campo
/*/
method getComboBoxValue(cValue as character, aComboBox as array) as character class BAUTReportsBusinessObject

    local nX := 0 as numeric
    local aItems as array
    local cValueComboBox as character

    for nX := 1 to len(aComboBox)
        aItems := strTokArr(aComboBox[nX], "=")

        if len(aItems) >= 2 .and. alltrim(cValue) == alltrim(aItems[1])
            cValueComboBox := alltrim(aItems[2])
            exit
        endif
    next nX

return cValueComboBox
