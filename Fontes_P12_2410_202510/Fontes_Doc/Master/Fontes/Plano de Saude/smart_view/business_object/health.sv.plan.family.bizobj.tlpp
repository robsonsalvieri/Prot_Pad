#include "tlpp-core.th"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
  
namespace totvs.protheus.health.smartview.plan.family

using namespace totvs.protheus.health.smartview.plan.utils
 
/*/{Protheus.doc} BusinessObject
Objeto de negócio para o Smart View - Familias

@type class
@version Protheus 12.1.2310
@author vinicius.queiros
@since 29/04/2023
/*/
@totvsFrameworkTReportsIntegratedProvider(active = .T., team = "SIGAPLS", tables = "BA3,BA1,BT5", name = "Famílias", country="ALL", initialRelease="12.1.2210")
class FamilyBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    private data aFields as array
    private data cAliasTemp as character
    private data jFilterParams as json 
	private data lExistPergunte as logical 

    public method new() as object
    public method getData(nPage as numeric, oFilter as object) as object
    public method getSchema() as object

    private method setFilterParams(oFilter as object) as logical
    private method validParameters() as logical
    private method getFieldsBusinessObject() as array 
	private method getQueryFamilies(oFilter as object) as object
    private method getVirtualFieldsValue(cField as character) as variant
    private method getComboBoxValue(cValue as character, aComboBox as array) as character

endclass

/*/{Protheus.doc} new
Contrutor da classe

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 29/04/2023
@return object, objeto da classe BusinessObject
/*/ 
method new() class FamilyBusinessObject

    _Super:new()

    self:jFilterParams := JsonObject():new()
    self:aFields := self:getFieldsBusinessObject()

    self:appendArea(UtilsBusinessObject():getAreaName())
    self:setDisplayName("Famílias")
    self:setDescription("Relação dos dados cadastrais das famílias.")

    self:lExistPergunte := self:setPergunte("PLR612")
    if !self:lExistPergunte
        self:setErrorStatus(400, "Pergunte não encontrado", "Verifique o grupo de perguntas PLR612 na base de dados.")
    endif

return self

/*/{Protheus.doc} getData
Retorna o objeto de dados

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 29/04/2023
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do Smart View
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getData(nPage as numeric, oFilter as object) as object class FamilyBusinessObject

    local nX := 0 as numeric
    local jItems as json
    local lVirtual as logical
    local lCombo as logical
    local cComboBox as character
    local aComboBox as array
    local cField as character
    local cFieldId as character
    local xFieldValue as variant
    local aPDFields as array
    local lObfuscated as logical
    local lValueAnonymize as logical
    Local oStatement as object

    if self:lExistPergunte
        self:setFilterParams(oFilter)
		if self:validParameters()
            oStatement := self:getQueryFamilies(oFilter)
            self:cAliasTemp := oStatement:openAlias()

            (self:cAliasTemp)->(dbGoTop())

            aPDFields := FwProtectedDataUtil():usrAccessPDField(__cUserID, self:aFields)
            lObfuscated := len(aPDFields) <> len(self:aFields)

            while !(self:cAliasTemp)->(eof())
                jItems := JsonObject():new()
            
                for nX := 1 to len(self:aFields)
                    cField := self:aFields[nX]
                    lVirtual := upper(alltrim(getSx3Cache(cField, "X3_CONTEXT"))) == "V"
                    cComboBox := alltrim(getSx3Cache(cField, "X3_CBOX"))
                    lCombo := !empty(cComboBox)
                    cFieldId := strTran(cField, "_", "")

                    if lVirtual
                        xFieldValue := self:getVirtualFieldsValue(cField)
                    else
                        xFieldValue := (self:cAliasTemp)->&(self:aFields[nX])
                    endif

                    lValueAnonymize := iif(lObfuscated .and. aScan(aPDFields, cField) == 0, .t., .f.)

                    xFieldValue := iif(lValueAnonymize, FwProtectedDataUtil():ValueAsteriskToAnonymize(xFieldValue), xFieldValue)

                    if !lVirtual .and. !lValueAnonymize
                        if getSx3Cache(cField, "X3_TIPO") == "D"
                            if !empty(xFieldValue)
                                xFieldValue := totvs.framework.treports.date.stringToTimeStamp(xFieldValue)
                            else
                                xFieldValue := nil
                            endif
                        else
                            if lCombo
                                aComboBox := strTokArr(cComboBox, ";")
                                xFieldValue := self:getComboBoxValue(xFieldValue, aComboBox)
                            endif
                        endif          
                    endif

                    jItems[cFieldId] := xFieldValue
                next nX

                self:oData:appendData(jItems)

                (self:cAliasTemp)->(dbSkip())
            enddo

            self:setHasNext(!(self:cAliasTemp)->(eof()))

            (self:cAliasTemp)->(dbCloseArea())
        endif
    endif

return self:oData

/*/{Protheus.doc} getSchema
Retorna a Estrutura de dados

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 29/04/2023
@return object, objeto oData da classe IntegratedProvider 
/*/ 
method getSchema() as object class FamilyBusinessObject

    local nX := 0 as numeric
    local cField as character
    local nPos as numeric
    local cId as character
    local cDescription as character
    local cType as character
    local cDisplayName as character
    local cRealName as character
    local cTypeSX3 as character

    for nX := 1 to len(self:aFields)
        cField := self:aFields[nX]
        nPos := at(".", cField) + 1
        cField := iif(nPos > 0, substr(cField, nPos), cField)

        cId := strTran(cField, "_", "") 
        cDescription := alltrim(getSx3Cache(cField, "X3_DESCRIC"))
        cDisplayName := alltrim(getSx3Cache(cField, "X3_TITULO"))
        cRealName := cField
        cType := "string"
        cTypeSX3 := getSx3Cache(cField, "X3_TIPO")

        do case
            case cTypeSX3 == "C"
                cType := "string"
            case cTypeSX3 == "D"
                cType := "date"
            case cTypeSX3 == "N"
                cType := "number"
        endcase

        self:addProperty(cId, cDescription, cType, cDisplayName+" ("+cField+")", cRealName)
    next nX

return self:oSchema

/*/{Protheus.doc} getFieldsBusinessObject
Retorna os campos que seram utilizados no objeto de negocio - Familias

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 25/06/2023
@return array, Array com os campos do objeto de negocio Familias
/*/ 
method getFieldsBusinessObject() as array class FamilyBusinessObject

    local aFields as array

    aFields := {;
    ; // Folder Dados cadastrais
    "BA3_CODINT","BA3_DESINT","BA3_CODEMP","BA3_DESEMP","BA3_CONEMP","BA3_VERCON","BA3_SUBCON",;
    "BA3_VERSUB","BA3_DESSUB","BA3_NUMCON","BA3_MATRIC","BA3_MATEMP","BA3_MATANT","BA3_HORACN","BA3_DATBAS",;
    "BA3_DATINC","BA3_TIPOUS","BA3_MOTBLO","BA3_DATBLO","BA3_AGMTFU","BA3_AGFTFU","BA3_AGNMFU","BA3_VALSAL",;
    "BA3_ROTSAL","BA3_DESMUN","BA3_RGIMP","BA3_DEMITI","BA3_DATDEM","BA3_MOTDEM","BA3_LIMATE","BA3_VALANT",;
    "BA3_LETANT","BA3_DATALT","BA3_MATFMB","BA3_TRAORI","BA3_TRADES","BA3_VALID","BA3_DESLIG","BA3_DATDES",;
    "BA3_LIMITE","BA3_CONSID","BA3_AGLUT","BA3_TIPPGO","BA3_UNDORG",;
    ; // Folder Dados do Plano
    "BA3_CODPLA","BA3_DESPLA","BA3_VERSAO","BA3_FORPAG","BA3_DESFOR","BA3_FORCTX","BA3_DEFOTX","BA3_TXUSU",;
    "BA3_FORCOP","BA3_DEFOOP","BA3_GRPCOB","BA3_CODTDE","BA3_DESTDE","BA3_COBRAT","BA3_RATMAI","BA3_COBRET",;
    "BA3_DIARET","BA3_ULTCOB","BA3_RATSAI","BA3_NUMCOB","BA3_ULREA","BA3_CARIMP","BA3_ROTINA","BA3_DESMEN",;
    "BA3_PADSAU","BA3_CARVIR","BA3_CODFID",;
    ; // Folder ANS
    "BA3_DATCIV","BA3_MESREA","BA3_INDREA","BA3_TIPCON","BA3_DESTCO","BA3_SEGPLA","BA3_DESSEG","BA3_MODPAG",;
    "BA3_APLEI","BA3_ABRANG","BA3_DESABR","BA3_CODACO","BA3_DESACO","BA3_DATPLA",;
    ; // Folder Cobrança
    "BA3_COBNIV","BA3_VENCTO","BA3_CODCLI","BA3_LOJA","BA3_NOMCLI","BA3_NATURE","BA3_CODFOR","BA3_LOJFOR",;
    "BA3_NOMFOR","BA3_ENDCOB","BA3_CEP","BA3_END","BA3_NUMERO","BA3_COMPLE","BA3_BAIRRO","BA3_CODMUN",;
    "BA3_MUN","BA3_ESTADO","BA3_INFCOB","BA3_INFGCB","BA3_IMPORT","BA3_PERMOV","BA3_OUTLAN","BA3_BLOFAT",;
    "BA3_CODRDA","BA3_CODLAN","BA3_TIPPAG","BA3_BCOCLI","BA3_AGECLI","BA3_CTACLI","BA3_PORTAD","BA3_AGEDEP",;
    "BA3_CTACOR","BA3_PACOOK","BA3_CODTES","BA3_CODSB1","BA3_CODCRE","BA3_DTACLI","BA3_DTCCLI",;
    ; // Dados do Titular
    "BA1_NOMUSR",;
    ; // Dados do Contrato
    "BT5_DESCON";
    }

return aFields

/*/{Protheus.doc} setFilterParams
Defini os parâmetros do pergunte recebido pelo smart view para ser usado
na montagem da query 

@type method
@version 12.1.2310  
@author vinicius.queiros
@since 27/08/2023
@param oFilter, object, Objeto de filtro do Smart View
@return logical, se os parâmetros foram definidos com sucesso
/*/
method setFilterParams(oFilter as object) as logical class FamilyBusinessObject

	local jParams := oFilter:getParameters() as json
	local lOk := .t. as logical

    self:jFilterParams["healthInsurerCodeFrom"] := ""
    self:jFilterParams["companyCodeFrom"] := ""
    self:jFilterParams["familyCodeFrom"] := ""
    self:jFilterParams["healthInsurerCodeAt"] := ""
    self:jFilterParams["companyCodeAt"] := ""
    self:jFilterParams["familyCodeAt"] := ""

    if len(jParams["MV_PAR01"][1]) == 14 // Codigo Inicial ?
        self:jFilterParams["healthInsurerCodeFrom"] := substr(jParams["MV_PAR01"][1], 1, 4)
        self:jFilterParams["companyCodeFrom"] := substr(jParams["MV_PAR01"][1], 5, 4)
        self:jFilterParams["familyCodeFrom"] := substr(jParams["MV_PAR01"][1], 9, 6)
    endif

    if len(jParams["MV_PAR02"][1]) == 14 // Codigo Final ?
        self:jFilterParams["healthInsurerCodeAt"] := substr(jParams["MV_PAR02"][1], 1, 4)
        self:jFilterParams["companyCodeAt"] := substr(jParams["MV_PAR02"][1], 5, 4)
        self:jFilterParams["familyCodeAt"] := substr(jParams["MV_PAR02"][1], 9, 6)
    endif

    self:jFilterParams["active"] := cValToChar(jParams["MV_PAR03"][1]) // Situacao ?

    self:jFilterParams["oldSubscriberIdFrom"] := alltrim(jParams["MV_PAR05"][1]) // Matr. Antiga de ?
    self:jFilterParams["oldSubscriberIdAt"] := alltrim(jParams["MV_PAR06"][1]) // Matr. Antiga ate ?

return lOk

/*/{Protheus.doc} getQueryFamilies
Retorna a query das familias do objeto de negocio

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 29/04/2023
@param oFilter, object, Objeto de filtro do Smart View
@return Character, where da query das familias
/*/
method getQueryFamilies(oFilter as object) as object class FamilyBusinessObject

	local cQuery as Character
    local cHolderType := getNewPar("MV_PLCDTIT", "T")
    local nOrder := 1 as numeric
    Local oStatement as object

    cQuery := "SELECT ? FROM ? BA3 "

    // Títular da familia
    cQuery += " INNER JOIN ? BA1 "
    cQuery += "  ON BA1.BA1_FILIAL = ? "
    cQuery += " AND BA1.BA1_CODINT = ? "
    cQuery += " AND BA1.BA1_CODEMP = ? "
    cQuery += " AND BA1.BA1_MATRIC = ? "
    cQuery += " AND BA1.BA1_TIPUSU = ? "
    cQuery += " AND BA1.D_E_L_E_T_ = ? "

    // Contrato - Grupo Empresa Pessoa Juridica
    cQuery += " LEFT JOIN ? BT5 "
    cQuery += "  ON BT5.BT5_FILIAL = ? "
    cQuery += " AND BT5.BT5_CODINT = ? "
    cQuery += " AND BT5.BT5_CODIGO = ? "
    cQuery += " AND BT5.BT5_NUMCON = ? "
    cQuery += " AND BT5.BT5_VERSAO = ? "
    cQuery += " AND BT5.D_E_L_E_T_ = ? "

 	cQuery += " WHERE BA3_FILIAL = ? "
    cQuery += "   AND BA3.BA3_CODINT >= ? "
    cQuery += "   AND BA3.BA3_CODEMP >= ? "
    cQuery += "   AND BA3.BA3_MATRIC >= ? "
    cQuery += "   AND BA3.BA3_CODINT <= ? "
    cQuery += "   AND BA3.BA3_CODEMP <= ? "
    cQuery += "   AND BA3.BA3_MATRIC <= ? "

    if !empty(self:jFilterParams["oldSubscriberIdAt"])
        cQuery += " AND BA3.BA3_MATANT >= ? "
        cQuery += " AND BA3.BA3_MATANT <= ? "
    endif

    do case
        case self:jFilterParams["active"] == "1" // Ativo
            cQuery += " AND BA3.BA3_MOTBLO = ? "

        case self:jFilterParams["active"] == "2" // Bloqueado
            cQuery += " AND BA3.BA3_MOTBLO <> ? "
    endcase

    cQuery += " AND BA3.D_E_L_E_T_ = ? "

    if oFilter:hasFilter() // Filtros setados na interface do Smart View
        cQuery += " AND ? "
    endif

    cQuery += " ORDER BY ?"

    cQuery := ChangeQuery(cQuery)

    oStatement := FWExecStatement():new(cQuery)

    oStatement:setUnsafe(nOrder++,"BA3.*, BA1.BA1_NOMUSR, BT5.BT5_TIPCON")
    oStatement:setUnsafe(nOrder++, retSqlName("BA3"))

    // Títular da familia
    oStatement:setUnsafe(nOrder++, retSqlName("BA1"))
    oStatement:setString(nOrder++, xFilial("BA1"))
    oStatement:setUnsafe(nOrder++, "BA3.BA3_CODINT")
    oStatement:setUnsafe(nOrder++, "BA3.BA3_CODEMP")
    oStatement:setUnsafe(nOrder++, "BA3.BA3_MATRIC")
    oStatement:setString(nOrder++, cHolderType)
    oStatement:setString(nOrder++, "")

    // Contrato - Grupo Empresa Pessoa Juridica
    oStatement:setUnsafe(nOrder++, retSqlName("BT5"))
    oStatement:setString(nOrder++, xFilial("BT5"))
    oStatement:setUnsafe(nOrder++, "BA3.BA3_CODINT")
    oStatement:setUnsafe(nOrder++, "BA3.BA3_CODEMP")
    oStatement:setUnsafe(nOrder++, "BA3.BA3_CONEMP")
    oStatement:setUnsafe(nOrder++, "BA3.BA3_VERCON")
    oStatement:setString(nOrder++, "")
    
    oStatement:setString(nOrder++, xFilial("BA3"))
    oStatement:setString(nOrder++, self:jFilterParams["healthInsurerCodeFrom"])
    oStatement:setString(nOrder++, self:jFilterParams["companyCodeFrom"])
    oStatement:setString(nOrder++, self:jFilterParams["familyCodeFrom"])
    oStatement:setString(nOrder++, self:jFilterParams["healthInsurerCodeAt"])
    oStatement:setString(nOrder++, self:jFilterParams["companyCodeAt"])
    oStatement:setString(nOrder++, self:jFilterParams["familyCodeAt"])

    if !empty(self:jFilterParams["oldSubscriberIdAt"])
        oStatement:setString(nOrder++, self:jFilterParams["oldSubscriberIdFrom"])
        oStatement:setString(nOrder++, self:jFilterParams["oldSubscriberIdAt"])
    endif

    do case
        case self:jFilterParams["active"] == "1" // Ativo
            oStatement:setString(nOrder++, "")

        case self:jFilterParams["active"] == "2" // Bloqueado
            oStatement:setString(nOrder++, "")
    endcase
            
    oStatement:setString(nOrder++, "")

    if oFilter:hasFilter() // Filtros setados na interface do Smart View
        oStatement:setUnsafe(nOrder++, oFilter:getSQLExpression())
    endif

    oStatement:setUnsafe(nOrder++,"BA3.BA3_CODINT, BA3.BA3_CODEMP, BA3.BA3_MATRIC")

return oStatement

/*/{Protheus.doc} validParameters
Realizar a validação dos parametros recebidos pelo smart view

@type method
@version 12.1.2310  
@author vinicius.queiros
@since 10/09/2023
@return logical, se os parametros informados pelo Smart View estõa validos
/*/
method validParameters() as logical class FamilyBusinessObject

	local aParams := {} as array
	local lSucess := .f. as logical
	local jResponse as json
	local aSituation := {{"value": "1", "description": "Ativos"}, {"value": "2", "description": "Bloqueados"}, {"value": "3", "description": "Todos"}} as array

	aAdd(aParams, {"question": "active", "description": "Situação", "options": aSituation, "required": .t., "size": 1})

	jResponse := UtilsBusinessObject():validQuestParams(aParams, self:jFilterParams)
	lSucess := jResponse["sucess"]
	if !lSucess
		self:setErrorStatus(jResponse["codError"], jResponse["message"], jResponse["detailedMsg"])
	endif

return lSucess

/*/{Protheus.doc} getVirtualFieldsValue
Retorna o valor (conteudo) do campo virtual

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 20/05/2023
@param jItems, json, dados da família posicionada
@param cField, character, campo virtual para preenchimento
@return logical, se foi adicionado o valor do campo virtual
/*/
method getVirtualFieldsValue(cField as character) as variant class FamilyBusinessObject

    local xValue as variant

    do case
        // Folder Dados Cadastrais
        case cField == "BA3_DESINT"
            xValue := posicione("BA0", 1, xFilial("BA0")+(self:cAliasTemp)->BA3_CODINT, "BA0_NOMINT")
        
        case cField == "BA3_DESEMP"
            xValue := posicione("BG9", 1, xFilial("BG9")+(self:cAliasTemp)->(BA3_CODINT+BA3_CODEMP), "BG9_DESCRI")

        case cField == "BA3_DESSUB"
            xValue := posicione("BQC", 1, xFilial("BQC")+(self:cAliasTemp)->(BA3_CODINT+BA3_CODEMP+BA3_CONEMP+BA3_VERCON+BA3_SUBCON+BA3_VERSUB), "BQC_DESCRI")
        
        case cField == "BA3_DATINC"
            if (self:cAliasTemp)->BA3_TIPOUS == "2" // Juridico
                xValue := posicione("BQC", 1, xFilial("BQC")+(self:cAliasTemp)->(BA3_CODINT+BA3_CODEMP+BA3_CONEMP+BA3_VERCON+BA3_SUBCON+BA3_VERSUB), "BQC_DATCON")
                if !empty(xValue)
                    xValue := totvs.framework.treports.date.stringToTimeStamp(dtos(xValue))
                endif
            else
                xValue := Nil
            endif
        
        case cField == "BA3_AGNMFU"
            xValue := posicione("SRA", 1, (self:cAliasTemp)->(BA3_AGFTFU+BA3_AGMTFU), "RA_NOME")
        
        // Folder Dados do Plano
        case cField == "BA3_DESPLA"
            xValue := posicione("BI3", 1, xFilial("BI3")+(self:cAliasTemp)->(BA3_CODINT+BA3_CODPLA+BA3_VERSAO), "BI3_DESCRI")

        case cField == "BA3_DESFOR"                                                	
            xValue := posicione("BJ1", 1, xFilial("BJ1")+(self:cAliasTemp)->BA3_FORPAG, "BJ1_DESCRI")

        case cField == "BA3_DEFOTX"
            xValue := posicione("BJ1", 1, xFilial("BJ1")+(self:cAliasTemp)->BA3_FORCTX, "BJ1_DESCRI")

        case cField == "BA3_DEFOOP"
            xValue := posicione("BJ1", 1, xFilial("BJ1")+(self:cAliasTemp)->BA3_FORCOP, "BJ1_DESCRI")

        case cField == "BA3_DESTDE"
            xValue := posicione("BF8", 1, xFilial("BF8")+(self:cAliasTemp)->(BA3_CODINT+BA3_CODTDE), "BF8_DESCM")
         
        // Folder ANS
        case cField == "BA3_DESTCO"
            xValue := posicione("BII", 1, xFilial("BII")+(self:cAliasTemp)->BA3_TIPCON, "BII_DESCRI")
           
        case cField == "BA3_DESSEG"
            xValue := posicione("BI6", 1, xFilial("BI6")+(self:cAliasTemp)->BA3_SEGPLA, "BI6_DESCRI")
 
        case cField == "BA3_DESABR"
            xValue := posicione("BF7", 1, xFilial("BF7")+(self:cAliasTemp)->BA3_ABRANG, "BF7_DESORI")                                                                 
        
        case cField == "BA3_DESACO"
            xValue := posicione("BI4", 1, xFilial("BI4")+(self:cAliasTemp)->BA3_CODACO, "BI4_DESCRI")                                                                 
                                                          
        // Folder Cobrança
        case cField == "BA3_NOMCLI"                                                     
            xValue := posicione("SA1", 1, xFilial("SA1")+(self:cAliasTemp)->(BA3_CODCLI+BA3_LOJA), "A1_NOME")                                                                 
          
        case cField == "BA3_NOMFOR"                                                            
            xValue := posicione("SA3", 1, xFilial("SA3")+(self:cAliasTemp)->(BA3_CODFOR+BA3_LOJFOR), "A3_NOME")                                                                 
        
        // Contrato
        case cField == "BT5_DESCON"
            xValue := posicione("BII", 1, xFilial("BII")+(self:cAliasTemp)->BT5_TIPCON, "BII_DESCRI")                                                   
                                                                                                    
    endcase

return xValue

/*/{Protheus.doc} getComboBoxValue
Retorna o contéudo do campo de acordo com o combo box do X3_CBOX

@type method
@version Protheus 12.1.2310  
@author vinicius.queiros
@since 23/05/2023
@param cValue, character, Valor do campo gravada no banco de dados
@param aComboBox, array, array com o combo box do campo (X3_CBOX)
@return character, retornar o de/para do valor do campo
/*/
method getComboBoxValue(cValue as character, aComboBox as array) as character class FamilyBusinessObject

    local nX := 0 as numeric
    local aItems as array
    local cValueComboBox as character

    for nX := 1 to len(aComboBox)
        aItems := strTokArr(aComboBox[nX], "=")

        if len(aItems) >= 2 .and. alltrim(cValue) == alltrim(aItems[1])
            cValueComboBox := alltrim(aItems[2])
            exit
        endif
    next nX

return cValueComboBox
