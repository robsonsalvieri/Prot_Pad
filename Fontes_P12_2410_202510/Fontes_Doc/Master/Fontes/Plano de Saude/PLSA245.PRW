#INCLUDE "plsa245.ch"

#Include "PLSMGER.CH"
#include "PROTHEUS.CH"
/*/


Ŀ
Programa   PLSA245  Autor  Tulio Cesar           Data  08.03.2000 
Ĵ
Descrio  Sub-Especialidades                                         
Ĵ
Uso        Advanced Protheus 5.08                                     
Ĵ
Parametros Nenhum                                                     
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL           
Ĵ
Programador  Data    BOPS   Motivo da Alterao                     
Ĵ
ٱ


/*/
Function PLSA245
//Ŀ
// Define opcoes da rotina...                                               
//
PRIVATE aRotina := {	{ STRPL01	,'AxPesqui'   , 0 , K_Pesquisar  , 0, .F.},; //'Pesquisar'
                      { STR0001 ,'PLSA245MOV' , 0 , K_Visualizar , 0, Nil} } //"Sub-Especialidades"

//Ŀ
// Define o cabecalho da tela de atualizacoes                               
//
PRIVATE cAlias    := "BAQ"
PRIVATE cCadastro := Fundesc()
//Ŀ
// Chama mBrowse padrao...                                                  
//
BAQ->(DbSetOrder(2))
BAQ->(DbSeek(xFilial("BAQ")))
BAQ->(mBrowse(06,01,22,75,'BAQ'))
//Ŀ
// Fim da Rotina Principal...                                               
//
Return
/*/


Ŀ
Funcao     PLSA245MOV  Autor  Tulio Cesar         Data  03.06.02 
Ĵ
Descricao  Exibe todas as sub-especilidades...                       
Ĵ
Sintaxe    PLSA245Fil(cAlias,nReg,nOpc)                              
Ĵ
 Uso       PLSA245                                                   
ٱ


/*/              
Function PLSA245MOV(cAlias,nReg,nOpc)
//Ŀ
// Define variaveis LOCAIS...                                               
//
LOCAL aAliasItem  := {{"BFP",STR0002,,}} //"Itens"
LOCAL aKeyItem    := {"BFN->BFN_CODINT+BFN->BFN_CODESP+BFN->BFN_CODSUB","BFP->BFP_CODINT+BFP->BFP_CODESP+BFP->BFP_CODSUB"}
LOCAL aCondItem   := {"BFP->BFP_CODINT+BFP->BFP_CODESP+BFP->BFP_CODSUB","BFN->BFN_CODINT+BFN->BFN_CODESP+BFN->BFN_CODSUB"}
LOCAL aChavCab    := {{{"BFP_CODINT","M->BFN_CODINT"},{"BFP_CODESP","M->BFN_CODESP"},{"BFP_CODSUB","M->BFN_CODSUB"}}}
PRIVATE cTitulo
PRIVATE cCodInt   := BAQ->BAQ_CODINT
PRIVATE cCodEsp   := BAQ->BAQ_CODESP
//Ŀ
// Chama funcao de Browse...                                           
//
PRIVATE cFiltro := "@BFN_FILIAL = '"+xFilial("BFN")+"' AND BFN_CODINT = '"+cCodInt+"'"
        cFiltro += " AND BFN_CODESP = '"+cCodEsp+"' AND D_E_L_E_T_ = ' '"
BFN->(DbSetOrder(1))   
DbSelectArea("BFN")
SET FILTER TO &cFiltro
BFN->(DbSeek(xFilial("BFN")))
cTitulo   := STR0003+" [ "+AllTrim(BAQ->BAQ_DESCRI)+" ] " //"Especialidade "

PlsCabItem("BFN",STR0004,aAliasItem,{1,1},aKeyItem,aCondItem,aChavCab,nil,nil,nil,nil,nil,nil,nil,nil, {|| PLSA245DEL() } ) //"Sub-Especialidade"
//Ŀ
// Fim da Rotina Principal...                                               
//
Return
/*/


Ŀ
Funcao     PLSA245Num  Autor  Tulio Cesar           Data  15.03.01 
Ĵ
Descricao  Retorna o proximo numero do lote a partir de uma operadora  
ٱ


/*/
Function PLSA245Num(cCodInt,cCodEsp)
LOCAL nRet    := 0
LOCAL nOrdBFN := BFN->(IndexOrd())
LOCAL nTam    := Len(BFN->BFN_CODSUB)

BFN->(DbSetOrder(1))

BFN->(DbSeek(xFilial("BFN")+cCodInt+cCodEsp+Replicate("9",nTam),.T.))
BFN->(DbSkip(-1))

If BFN->(BFN_FILIAL+BFN_CODINT+BFN_CODESP) <> xFilial("BFN")+cCodInt+cCodEsp
   nRet := StrZero(1,nTam)
Else
   nRet := StrZero(Val(BFN->BFN_CODSUB)+1,nTam)
Endif

BFN->(DbSetOrder(nOrdBFN))

Return(nRet)
/*/


Ŀ
Funcao     PLSA245DEL  Autor  Tulio Cesar           Data  15.03.01 
Ĵ
Descricao  Retorna se pode ser excluida uma sub-especialidade          
ٱ


/*/
Function PLSA245DEL()
LOCAL cSQL           
LOCAL lRet

cSQL := "SELECT BAX_CODSUB FROM "+BAX->(RetSQLName("BAX"))+" WHERE "
cSQL += "BAX_FILIAL = '"+xFilial("BAX")+"' AND "
cSQL += "BAX_CODINT = '"+BAQ->BAQ_CODINT+"' AND "
cSQL += "BAX_CODESP = '"+BAQ->BAQ_CODESP+"' AND "
cSQL += "BAX_CODSUB = '"+BFN->BFN_CODSUB+"' AND "
cSQL += "D_E_L_E_T_ = ' '"

PLSQuery(cSQL,"PLSTRB245")

If PLSTRB245->(Eof())
   lRet := .T.
Else
   lRet := .F.
   MsgInfo(STR0005) //"Esta Sub-Especialidade esta vinculada a pelo menos uma Rede de Atendimento e nao podera ser excluida"
Endif

PLSTRB245->(DbCloseArea())


Return(lRet)

/*/


Ŀ
Programa  MenuDef    Autor  Darcio R. Sporl        Data 02/01/2007
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()

Local aRotina := PLSMnDefY(, "BFN", , )
AAdd(aRotina,	{ STRPL01	,'AxPesqui'   , 0 , K_Pesquisar  , 0, .F.})//,; //'Pesquisar'
AAdd(aRotina,	{ STR0001 ,'PLSA245MOV' , 0 , K_Visualizar , 0, Nil} ) //"Sub-Especialidades"

Return(aRotina)
