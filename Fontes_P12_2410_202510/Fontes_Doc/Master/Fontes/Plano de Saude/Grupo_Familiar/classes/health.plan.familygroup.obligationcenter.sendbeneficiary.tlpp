#include "protheus.ch"
#include "fwmvcdef.ch"

namespace totvs.protheus.health.plan.familygroup.obligationcenter

/*/{Protheus.doc} SendBeneficiary
Envia os dados/movimentação do beneficiário (SIGAPLS) para a Central
de Obrigações

@type class
@author vinicius.queiros
@since 15/08/2023
@version 12.1.2310
/*/
class SendBeneficiary

    private data lActiveObrigCenter as logical
    private data lUseSIP as logical
    private data lUseSIB as logical
    private data cErroMessage as character
    private data aCritSIB as array
    private data oModel as object
    private data nOperation as numeric
    private data jBeneficiaryData as json
    private data aBeneficiariesData as array
    private data aAreaBA1 as array
	private data aAreaBA3 as array
	private data aAreaBTS as array
    private data aAreaB3K as array

    public method new() constructor 
    public method addBeneficiary(cSubscriberId as character, nOperation as numeric) as logical
    public method addFamily(jFamilyData as json, nOperation as numeric) as logical
    public method commitSend() as logical
    public method restore()
    public method getCritSIB() as array
    public method getBeneficiaryData() as variant
    public method getErroMessage() as character

    private method loadData() as logical
    private method setModel() as logical
    private method commitModel() as logical
    private method getBlockData(cSubscriberId as character) as json

endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 15/08/2023
@version 12.1.2310
@return object, Classe SendBeneficiary
/*/
method new() class SendBeneficiary

    self:lActiveObrigCenter := getNewPar("MV_PLSEXCO", .f.)
    self:lUseSIP := "1" $ getNewPar("MV_PLSTIPO", "")
    self:lUseSIB := "2" $ getNewPar("MV_PLSTIPO", "")
    self:aCritSIB := {}
    self:aBeneficiariesData := {}

    self:aAreaBA1 := BA1->(getArea())
	self:aAreaBA3 := BA3->(getArea())
	self:aAreaBTS := BTS->(getArea())
	self:aAreaB3K := B3K->(GetArea())

return self

/*/{Protheus.doc} addBeneficiary
Adiciona beneficiário para enviar para a central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 16/08/2023
@param cSubscriberId, character, matricula do beneficiário
@param nOperation, numeric, numero da operação que esta sendo executada
@return logical, se o beneficiário foi adicionado com sucesso
/*/
method addBeneficiary(cSubscriberId as character, nOperation as numeric) as logical class SendBeneficiary

    local lAdd := .f. as logical
    
    self:nOperation := nOperation

    if self:lActiveObrigCenter .and. (self:lUseSIP .or. self:lUseSIB)
        BA1->(dbSetOrder(2))
        if BA1->(msSeek(xFilial("BA1")+cSubscriberId))

            BA3->(dbSetOrder(1))
            if BA3->(msSeek(xFilial("BA3")+BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC)))
                if PlSibEnvANS(BA3->BA3_TIPOUS,BA3->BA3_CODINT,BA3->BA3_CODEMP,BA3->BA3_CONEMP,BA3->BA3_VERCON,BA3->BA3_SUBCON,BA3->BA3_VERSUB,BA1->BA1_INFANS)

                    if self:nOperation == MODEL_OPERATION_UPDATE
                        B3K->(dbSetOrder(5))
						if !B3K->(msSeek(xFilial("B3K")+cSubscriberId))
							self:nOperation := MODEL_OPERATION_INSERT
						endif
                    endif

                    BTS->(dbSetOrder(1))
	                if BTS->(msSeek(xFilial("BTS")+BA1->BA1_MATVID))
                        if self:loadData()                    
                            lAdd := .t.
                        endif
                    endif
                else
                    self:cErroMessage := "Beneficiário não pode ser enviado para a ANS SIB ou SIP."
                endif
            endif
        else
            self:cErroMessage := "Beneficiário não encontrado no cadastro (BA1)."
        endif
    else
        self:cErroMessage := "Atualização automática desativada com a Central de Obrigações."
    endif

return lAdd

/*/{Protheus.doc} addFamily
Adiciona beneficiários da família para enviar para a central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 16/08/2023
@param jFamilyData, json, dados da família
@param nOperation, numeric, numero da operação que esta sendo executada
@return logical, se os beneficiários foram adicionados com sucesso
/*/
method addFamily(jFamilyData as json, nOperation as numeric) as logical class SendBeneficiary

    local lAdd := .f. as logical
    
    if self:lActiveObrigCenter .and. (self:lUseSIP .or. self:lUseSIB)
        BA3->(dbSetOrder(1))
        if BA3->(msSeek(xFilial("BA3")+jFamilyData["healthInsurerCode"]+jFamilyData["companyCode"]+jFamilyData["matricCode"]))

            BA1->(dbSetOrder(2))
            if BA1->(msSeek(xFilial("BA1")+BA3->(BA3_CODINT+BA3_CODEMP+BA3_MATRIC)))
                while !BA1->(eof()) .and. BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC) == BA3->(BA3_CODINT+BA3_CODEMP+BA3_MATRIC)
                    self:nOperation := nOperation

                    if PlSibEnvANS(BA3->BA3_TIPOUS,BA3->BA3_CODINT,BA3->BA3_CODEMP,BA3->BA3_CONEMP,BA3->BA3_VERCON,BA3->BA3_SUBCON,BA3->BA3_VERSUB,BA1->BA1_INFANS)

                        if self:nOperation == MODEL_OPERATION_UPDATE
                            B3K->(dbSetOrder(5))
                            if !B3K->(msSeek(xFilial("B3K")+BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)))
                                self:nOperation := MODEL_OPERATION_INSERT
                            endif
                        endif

                        BTS->(dbSetOrder(1))
                        if BTS->(msSeek(xFilial("BTS")+BA1->BA1_MATVID))
                            if self:loadData()
                                aAdd(self:aBeneficiariesData, {"beneficiaryData" : self:jBeneficiaryData, "operation" : self:nOperation})                   
                                lAdd := .t.
                            endif
                        endif
                    endif

                    BA1->(dbSkip())
                enddo
            endif
        else
            self:cErroMessage := "Família não encontrada no cadastro (BA3)."
        endif
    else
        self:cErroMessage := "Atualização automática desativada com a Central de Obrigações."
    endif

return lAdd

/*/{Protheus.doc} commitSend
Confirmar o envio do beneficiário para a Central de Obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 17/08/2023
@return logical, se o beneficiário foi enviado com sucesso
/*/
method commitSend() as logical class SendBeneficiary

    local lSend := .f. as logical
    local nCont := 0 as numeric

    if valType(self:jBeneficiaryData) == "J" .or. len(self:aBeneficiariesData) > 0
        // Envio de beneficiários (Família)
        if len(self:aBeneficiariesData) > 0
            for nCont := 1 to len(self:aBeneficiariesData)
                self:jBeneficiaryData := self:aBeneficiariesData[nCont]["beneficiaryData"]
                self:nOperation := self:aBeneficiariesData[nCont]["operation"]

                if self:setModel() .and. self:commitModel()
                    lSend := .t.
                endif
            next nCont
        else
            if self:setModel() .and. self:commitModel()
                lSend := .t.
            endif
        endif     
    else
        self:cErroMessage := "O beneficiário não foi adicionado a classe para ser enviado."
    endif

return lSend

/*/{Protheus.doc} restore
Restaura as propriedades da classe e as areas utilizadas (tabelas)

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 17/08/2023
/*/
method restore() class SendBeneficiary

    self:cErroMessage := ""
    self:nOperation := 0
    self:aCritSIB := {}
    self:aBeneficiariesData := {}

    freeObj(self:oModel)
    self:oModel := nil

    freeObj(self:jBeneficiaryData)
    self:jBeneficiaryData := nil

    restArea(self:aAreaBTS)
	restArea(self:aAreaBA1)
	restArea(self:aAreaBA3)
    restArea(self:aAreaB3K)

return

/*/{Protheus.doc} getCritSIB
Retorna as criticas do SIB geradas pela central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 17/08/2023
@return array, críticas do beneficiário na central
/*/
method getCritSIB() as array class SendBeneficiary

    local aCritSIB := {} as array

    if (len(self:aBeneficiariesData) == 0 .and. len(self:aCritSIB) > 1) .or. (len(self:aBeneficiariesData) > 0 .and. len(self:aCritSIB) > len(self:aBeneficiariesData))
        aCritSIB := aClone(self:aCritSIB)
    endif

return aCritSIB

/*/{Protheus.doc} getBeneficiaryData
Retorna os dados do(s) beneficiário(s) que serão enviados para
a central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 28/08/2023
@return variant, dados do beneficiário ou da família
/*/
method getBeneficiaryData() as variant class SendBeneficiary

    local xBeneficiaryData as variant

    if len(self:aBeneficiariesData) > 0
        xBeneficiaryData := aClone(self:aBeneficiariesData)
    else
        xBeneficiaryData := self:jBeneficiaryData
    endif

return xBeneficiaryData

/*/{Protheus.doc} getErroMessage
Retorna a mensagem de erro quando houver

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 28/08/2023
@return character, messagem de erro
/*/
method getErroMessage() as character class SendBeneficiary

    local cErroMessage := self:cErroMessage as character

return cErroMessage

/*/{Protheus.doc} loadData
Carrega os dados do beneficiário do Plano de Saúde

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 16/08/2023
@return logical, se os dados foram carregados com sucesso
/*/
method loadData() as logical class SendBeneficiary 

    local lLoad := .f. as logical
    local cPEOldSubscriberId as character
    local nSizeFldNomBen := tamSX3("B3K_NOMBEN")[1] as numeric
    local nSizeFldMae := tamSX3("B3K_NOMMAE")[1] as numeric
    local lResidentAbroad as logical
    local cHolderType := getNewPar("MV_PLCDTIT", "T")
    local lFindPlanCode := .f. as logical
    local jBlockData as json
    local aBlockData := {} as array
    local nLastReg := 0 as numeric

    self:jBeneficiaryData := JsonObject():new()

    self:jBeneficiaryData["healthInsurerCode"] := posicione("BA0", 1, xFilial("BA0")+BA1->BA1_CODINT, "BA0_SUSEP")
    self:jBeneficiaryData["codeCco"] := BA1->BA1_CODCCO
    self:jBeneficiaryData["subscriberId"] := BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)
    self:jBeneficiaryData["birthdate"] := BA1->BA1_DATNAS
    self:jBeneficiaryData["effectiveDate"] := BA1->BA1_DATINC
    self:jBeneficiaryData["stateAbbreviation"] := iif(!empty(BA1->BA1_ESTADO), BA1->BA1_ESTADO, iif(!empty(BA3->BA3_ESTADO), BA3->BA3_ESTADO, ""))
    self:jBeneficiaryData["healthInsuranceCode"] := iif(!empty(BA1->(BA1_CODPLA+BA1_VERSAO)), BA1->(BA1_CODPLA+BA1_VERSAO), iif(!empty(BA3->(BA3_CODPLA+BA3_VERSAO)), BA3->(BA3_CODPLA+BA3_VERSAO), ""))
    self:jBeneficiaryData["statusAns"] := "1" // 1 = Valid Pdte
    self:jBeneficiaryData["gender"] := iif(BA1->BA1_SEXO == "2", "3", BA1->BA1_SEXO)
    self:jBeneficiaryData["name"] := alltrim(subStr(BA1->BA1_NOMUSR + space(nSizeFldNomBen), 1, nSizeFldNomBen))
    self:jBeneficiaryData["oldSubscriberId"] := BA1->BA1_MATANT

    if existBlock("PLRETMAT")
		cPEOldSubscriberId := execBlock("PLRETMAT", .f., .f., {"BA1"})

		if !empty(cPEOldSubscriberId)
			self:jBeneficiaryData["oldSubscriberId"] := cPEOldSubscriberId
		endif
	endif

    self:jBeneficiaryData["pisPasep"] := iif(!empty(BA1->BA1_PISPAS), alltrim(BA1->BA1_PISPAS), alltrim(BTS->BTS_PISPAS))
    self:jBeneficiaryData["mothersName"] := alltrim(substr(BA1->BA1_MAE + space(nSizeFldMae), 1, nSizeFldMae))
    self:jBeneficiaryData["declarationOfLiveBirth"] := iif(BA1->BA1_DATNAS >= stod("20070101") .and. len(alltrim(BTS->BTS_DENAVI)) == 11, alltrim(BTS->BTS_DENAVI), "")
    self:jBeneficiaryData["nationalHealthCard"] := allTrim(BTS->BTS_NRCRNA)
    self:jBeneficiaryData["residentAbroad"] := BA1->BA1_RESEXT

    lResidentAbroad := BA1->BA1_RESEXT == "1"
    self:jBeneficiaryData["address"] := iif(!lResidentAbroad, alltrim(BA1->BA1_ENDERE), "")
    self:jBeneficiaryData["houseNumbering"] := iif(!lResidentAbroad, alltrim(BA1->BA1_NR_END), "")
    self:jBeneficiaryData["addressComplement"] := iif(!lResidentAbroad, alltrim(BA1->BA1_COMEND), "")
    self:jBeneficiaryData["district"] = iif(!lResidentAbroad, alltrim(BA1->BA1_BAIRRO), "")
    self:jBeneficiaryData["cityCode"] := iif(!lResidentAbroad, alltrim(BA1->BA1_CODMUN), "")
    self:jBeneficiaryData["cityCodeResidence"] := iif(!lResidentAbroad, iif(len(alltrim(BA1->BA1_MUNRES)) > 6, substr(BA1->BA1_MUNRES, 1, 6), alltrim(BA1->BA1_MUNRES)), "")
    self:jBeneficiaryData["ZIPCode"] := iif(!lResidentAbroad, alltrim(BA1->BA1_CEPUSR), "")
    self:jBeneficiaryData["typeOfAddress"] := BA1->BA1_TIPEND
    self:jBeneficiaryData["holderRelationship"] := PLSGrauPa(BA1->BA1_GRAUPA, alltrim(BA3->BA3_GRPFAM), self:jBeneficiaryData["subscriberId"])
    
    if empty(BA3->BA3_GRPFAM) .and. BA1->BA1_TIPUSU == cHolderType
		self:jBeneficiaryData["holderSubscriberId"] := ""
	else
		self:jBeneficiaryData["holderSubscriberId"] := PLSMatTit(BA3->(BA3_CODINT+BA3_CODEMP+BA3_MATRIC), cHolderType, allTrim(BA3->BA3_GRPFAM))

		if !(BA1->BA1_TIPUSU == cHolderType) .and. empty(self:jBeneficiaryData["holderSubscriberId"])
			self:jBeneficiaryData["holderSubscriberId"] := "AUTO"
		endif
	endif

    self:jBeneficiaryData["codeSusep"] := ""
    self:jBeneficiaryData["codeSCPA"] := ""

    BI3->(dbSetOrder(1))
    do case
        case BI3->(msSeek(xFilial("BI3")+BA1->(BA1_CODINT+BA1_CODPLA+BA1_VERSAO))) // Nivel Beneficiário
            lFindPlanCode := .t.
        
        case BI3->(MsSeek(xFilial("BI3")+BA3->(BA3_CODINT+BA3_CODPLA+BA3_VERSAO))) // Nivel Família
            lFindPlanCode := .t.
        
        otherwise
            if BA3->BA3_TIPOUS <> "1" // 1 = Pessoa Fisica
                BT6->(dbSetOrder(1))
                if BT6->(MsSeek(xFilial("BT6")+BA3->(BA3_CODINT+BA3_CODEMP+BA3_CONEMP+BA3_VERCON+BA3_SUBCON+BA3_VERSUB))) // Nivel Subcontrato
                    lFindPlanCode := BI3->(MsSeek(xFilial("BI3")+BT6->(BT6_CODINT+BT6_CODPRO+BT6_VERSAO)))
                endif
            endif
    endcase

    if lFindPlanCode
        do case
            case BI3->BI3_APOSRG == "0" // Plano não regulamentado
                self:jBeneficiaryData["codeSCPA"] := alltrim(BI3->BI3_SCPA)

            case BI3->BI3_APOSRG == "1" // Plano regulamentado
                self:jBeneficiaryData["codeSusep"] := alltrim(BI3->BI3_SUSEP)

            case BI3->BI3_APOSRG == "2" .or. empty(BI3->BI3_APOSRG) // Plano adaptado
                if !empty(BI3->BI3_SUSEP)
                    self:jBeneficiaryData["codeSusep"] := alltrim(BI3->BI3_SUSEP)
                else
                    self:jBeneficiaryData["codeSCPA"] := alltrim(BI3->BI3_SCPA)
                endif
        endcase
    else
        self:jBeneficiaryData["codeSusep"] := strZero(0, 9, 0)
        self:jBeneficiaryData["codeSCPA"] := strZero(0, 20, 0)
    endif

    self:jBeneficiaryData["portabilityPlanCode"] := iif(!empty(BA3->BA3_PLPOR) .or. !empty(BA1->BA1_PLPOR), iif(!empty(BA1->BA1_PLPOR), BA1->BA1_PLPOR, BA3->BA3_PLPOR), "")
    self:jBeneficiaryData["partialCoverage"] := PLSIBCPT(dDataBase, self:jBeneficiaryData["subscriberId"])
    self:jBeneficiaryData["excludedItems"] := "0" // Não

    self:jBeneficiaryData["guarantorCNPJ"] := ""
    self:jBeneficiaryData["guarantorCEI"] := ""
    self:jBeneficiaryData["caepf"] := ""

    if BA3->BA3_TIPOUS <> "1" // 1 = Pessoa Fisica
        BQC->(dbSetOrder(1))
		if BQC->(MsSeek(xFilial("BQC")+BA3->(BA3_CODINT+BA3_CODEMP+BA3_CONEMP+BA3_VERCON+BA3_SUBCON+BA3_VERSUB)))
			
            if empty(BQC->BQC_CNPJ) .and. empty(BQC->BQC_CEINSS) .and. empty(BQC->BQC_CAEPF)
				if SA1->(MsSeek(xFilial("SA1")+BQC->(BQC_CODCLI+BQC_LOJA)))
					self:jBeneficiaryData["guarantorCNPJ"] := iif(CNPJ(SA1->A1_CGC), SA1->A1_CGC, "")
                    self:jBeneficiaryData["guarantorCEI"] := SA1->A1_CEINSS
				else
                    if PLSRETNCB(BA1->BA1_CODINT, BA1->BA1_CODEMP, BA1->BA1_MATRIC, BA1->BA1_OPEORI)[1]
                        if SA1->(msSeek(xFilial("SA1")+BA3->(BA3_CODCLI+BA3_LOJA)))
							self:jBeneficiaryData["guarantorCNPJ"] := iif(CNPJ(SA1->A1_CGC), SA1->A1_CGC, "")
                            self:jBeneficiaryData["guarantorCEI"] := SA1->A1_CEINSS
						endif
                    endif
				EndIf
			else
				self:jBeneficiaryData["guarantorCNPJ"] := iif(CNPJ(BQC->BQC_CNPJ), BQC->BQC_CNPJ, "")
                self:jBeneficiaryData["guarantorCEI"] := BQC->BQC_CEINSS
                self:jBeneficiaryData["caepf"] := BQC->BQC_CAEPF
			endif
		endif
    endif

    self:jBeneficiaryData["caepf"] := alltrim(iif(empty(self:jBeneficiaryData["guarantorCNPJ"]), self:jBeneficiaryData["caepf"], ""))
    self:jBeneficiaryData["guarantorCEI"] := alltrim(iif(empty(self:jBeneficiaryData["caepf"]), self:jBeneficiaryData["guarantorCEI"], ""))
    self:jBeneficiaryData["originSubscriberId"] := alltrim(BA1->BA1_TRAORI)
    self:jBeneficiaryData["transferDestination"] := alltrim(BA1->BA1_TRADES)
    self:jBeneficiaryData["holderCPF"] := alltrim(BA1->BA1_CPFUSR)
    self:jBeneficiaryData["motherCPF"] := alltrim(BA1->BA1_CPFMAE)
    self:jBeneficiaryData["sponsorCPF"] := alltrim(BA1->BA1_CPFPRE)

    self:jBeneficiaryData["situationANS"] := ""
    self:jBeneficiaryData["blockDate"] := ctod(" / / ")
    self:jBeneficiaryData["blockingReason"] := ""
    self:jBeneficiaryData["reactivationDate"] := ""

    jBlockData := self:getBlockData(BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG))

    self:jBeneficiaryData["blockDate"] := jBlockData["blockDate"]
    self:jBeneficiaryData["blockingReason"] := substr(jBlockData["blockReason"], 1, 2)
    self:jBeneficiaryData["reactivationDate"] := jBlockData["reactivationDate"]
    aBlockData := jBlockData["blockData"]

    if empty(self:jBeneficiaryData["blockDate"])
        self:jBeneficiaryData["blockDate"] := BA1->BA1_DATBLO
    endif

    if empty(self:jBeneficiaryData["blockingReason"])
        self:jBeneficiaryData["blockingReason"] := substr(PlMotBloANS("U", BA1->BA1_MOTBLO), 1, 2)
    endif

    nLastReg := len(aBlockData)

    if (nLastReg == 0 .and. empty(self:jBeneficiaryData["blockDate"])) .or. (nLastReg > 0 .and. aBlockData[nLastReg]["type"] == "1") // 1 = Desbloqueio
        self:jBeneficiaryData["situationANS"] := "A" // Ativo
    else
        self:jBeneficiaryData["situationANS"] := "I" // Inativo
    endif

    self:jBeneficiaryData["skipRuleName"] := alltrim(BA1->BA1_CRINOM)
    self:jBeneficiaryData["skipRuleMothersName"] := alltrim(BA1->BA1_CRIMAE)

    lLoad := .t.

    if existBlock("PLJCenData")
		self:jBeneficiaryData := execBlock("PLJCenData", .f., .f., {self:jBeneficiaryData})
	endif

return lLoad

/*/{Protheus.doc} setModel
Adiciona os dados do beneficiário (Json) no modelo de dados (ModelDef)
da central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 16/08/2023
@return logical, se os dados foram adicionados ao modelo com sucesso
/*/
method setModel() as logical class SendBeneficiary

    local lOk := .f. as logical

    self:oModel := CenLoadModel():new()
    
    B3K->(dbSetOrder(1))
    do case
        case self:nOperation == MODEL_OPERATION_DELETE
            B3K->(msSeek(xFilial("B3K")+self:jBeneficiaryData["healthInsurerCode"]+self:jBeneficiaryData["subscriberId"]))
        
        case B3K->(msSeek(xFilial("B3K")+self:jBeneficiaryData["healthInsurerCode"]+self:jBeneficiaryData["subscriberId"]))
            self:nOperation := MODEL_OPERATION_UPDATE

        otherwise
            self:nOperation := MODEL_OPERATION_INSERT
    endcase

	self:oModel:setOperation(self:nOperation)
	self:oModel:activate()

    if self:nOperation <> MODEL_OPERATION_DELETE

        self:oModel:setValue("B3KMASTER", "B3K_FILIAL", xFilial("B3K"))
        self:oModel:setValue("B3KMASTER", "B3K_CODOPE", self:jBeneficiaryData["healthInsurerCode"])
		self:oModel:setValue("B3KMASTER", "B3K_CODCCO", self:jBeneficiaryData["codeCco"])
		self:oModel:setValue("B3KMASTER", "B3K_MATRIC", self:jBeneficiaryData["subscriberId"])
		self:oModel:setValue("B3KMASTER", "B3K_DATNAS", self:jBeneficiaryData["birthdate"])
		self:oModel:setValue("B3KMASTER", "B3K_DATINC", self:jBeneficiaryData["effectiveDate"])
		self:oModel:setValue("B3KMASTER", "B3K_UF", self:jBeneficiaryData["stateAbbreviation"])
		self:oModel:setValue("B3KMASTER", "B3K_CODPRO", self:jBeneficiaryData["healthInsuranceCode"])
		self:oModel:setValue("B3KMASTER", "B3K_ATUCAR", "1")
		self:oModel:setValue("B3KMASTER", "B3K_STATUS", "1") // 1 = Valid Pdte
		self:oModel:setValue("B3KMASTER", "B3K_STASIB", self:jBeneficiaryData["statusAns"])
		self:oModel:setValue("B3KMASTER", "B3K_STAESP", "1") // 1 = Valid Pdte
		self:oModel:setValue("B3KMASTER", "B3K_SEXO", self:jBeneficiaryData["gender"])
		self:oModel:setValue("B3KMASTER", "B3K_NOMBEN", self:jBeneficiaryData["name"])
		self:oModel:setValue("B3KMASTER", "B3K_MATANT", self:jBeneficiaryData["oldSubscriberId"])
		self:oModel:setValue("B3KMASTER", "B3K_PISPAS", self:jBeneficiaryData["pisPasep"])
		self:oModel:setValue("B3KMASTER", "B3K_NOMMAE", self:jBeneficiaryData["mothersName"])
		self:oModel:setValue("B3KMASTER", "B3K_DN", self:jBeneficiaryData["declarationOfLiveBirth"])
		self:oModel:setValue("B3KMASTER", "B3K_CNS", self:jBeneficiaryData["nationalHealthCard"])
		self:oModel:setValue("B3KMASTER", "B3K_ENDERE", self:jBeneficiaryData["address"])
		self:oModel:setValue("B3KMASTER", "B3K_NR_END", self:jBeneficiaryData["houseNumbering"])
		self:oModel:setValue("B3KMASTER", "B3K_COMEND", self:jBeneficiaryData["addressComplement"])
		self:oModel:setValue("B3KMASTER", "B3K_BAIRRO", self:jBeneficiaryData["district"])
		self:oModel:setValue("B3KMASTER", "B3K_CODMUN", self:jBeneficiaryData["cityCode"])
		self:oModel:setValue("B3KMASTER", "B3K_MUNICI", self:jBeneficiaryData["cityCodeResidence"])
		self:oModel:setValue("B3KMASTER", "B3K_CEPUSR", self:jBeneficiaryData["ZIPCode"])
		self:oModel:setValue("B3KMASTER", "B3K_TIPEND", self:jBeneficiaryData["typeOfAddress"])
		self:oModel:setValue("B3KMASTER", "B3K_RESEXT", self:jBeneficiaryData["residentAbroad"])
		self:oModel:setValue("B3KMASTER", "B3K_TIPDEP", self:jBeneficiaryData["holderRelationship"])
		self:oModel:setValue("B3KMASTER", "B3K_CODTIT", self:jBeneficiaryData["holderSubscriberId"])
		self:oModel:setValue("B3KMASTER", "B3K_SUSEP", Regex(self:jBeneficiaryData["codeSusep"], {"-","/","\","."}))
		self:oModel:setValue("B3KMASTER", "B3K_SCPA", Regex(self:jBeneficiaryData["codeSCPA"], {"-","/","\","."}))
		self:oModel:setValue("B3KMASTER", "B3K_PLAORI", self:jBeneficiaryData["portabilityPlanCode"])
		self:oModel:setValue("B3KMASTER", "B3K_COBPAR", self:jBeneficiaryData["partialCoverage"])
		self:oModel:setValue("B3KMASTER", "B3K_ITEEXC", self:jBeneficiaryData["excludedItems"])
		self:oModel:setValue("B3KMASTER", "B3K_CNPJCO", Regex(self:jBeneficiaryData["guarantorCNPJ"], {"-","/","\","."}))
		self:oModel:setValue("B3KMASTER", "B3K_CEICON", Regex(self:jBeneficiaryData["guarantorCEI"], {"-","/","\","."}))
		self:oModel:setValue("B3KMASTER", "B3K_CAEPF", Regex(self:jBeneficiaryData["caepf"], {"-","/","\","."}) )
		self:oModel:setValue("B3KMASTER", "B3K_TRAORI", self:jBeneficiaryData["originSubscriberId"])
		self:oModel:setValue("B3KMASTER", "B3K_TRADES", self:jBeneficiaryData["transferDestination"])
		self:oModel:setValue("B3KMASTER", "B3K_CPF", self:jBeneficiaryData["holderCPF"])
		self:oModel:setValue("B3KMASTER", "B3K_CPFMAE", self:jBeneficiaryData["motherCPF"])
		self:oModel:setValue("B3KMASTER", "B3K_CPFPRE", self:jBeneficiaryData["sponsorCPF"])

		if !empty(self:jBeneficiaryData["situationANS"])	
            self:oModel:setValue("B3KMASTER", "B3K_SITANS", self:jBeneficiaryData["situationANS"])
        endif

		if !empty(self:jBeneficiaryData["blockDate"])
            self:oModel:setValue("B3KMASTER", "B3K_MOTBLO", self:jBeneficiaryData["blockingReason"])
        endif

		self:oModel:setValue("B3KMASTER", "B3K_DATBLO", self:jBeneficiaryData["blockDate"])
		self:oModel:setValue("B3KMASTER", "B3K_CRINOM", self:jBeneficiaryData["skipRuleName"])
		self:oModel:setValue("B3KMASTER", "B3K_CRIMAE", self:jBeneficiaryData["skipRuleMothersName"])

		if !empty(self:jBeneficiaryData["reactivationDate"])
		    self:oModel:setValue("B3KMASTER", "B3K_DATREA", self:jBeneficiaryData["reactivationDate"])
		endif

        self:oModel:setValue("B3KMASTER", "B3K_OPESIB", "2") // 2 = Retificar
    endif

    lOk := .t.

return lOk

/*/{Protheus.doc} commitModel
Realiza a confirmação/gravação dos dados do modelo na central de obrigações

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 16/08/2023
@return logical, se os dados foram gravados com sucesso
/*/
method commitModel() as logical class SendBeneficiary

    local lOk := .f. as logical
    local cSubscriberId := self:oModel:getValue("B3KMASTER", "B3K_MATRIC")
    local cBeneficiaryName := self:oModel:getValue("B3KMASTER", "B3K_NOMBEN")

    aAdd(self:aCritSIB, {cSubscriberId, cBeneficiaryName, ""})

    lOk := PLCGrvBen(self:oModel, .F., @self:aCritSIB)

return lOk

/*/{Protheus.doc} getBlockData
Retorna os dados e histórico de bloqueio/desbloqueio do beneficiário

@type method
@version 12.1.2310 
@author vinicius.queiros
@since 23/08/2023
@return json, dados de bloqueio/desbloqueio 
/*/
method getBlockData(cSubscriberId as character) as json class SendBeneficiary

    local jBlockData as json
    local cReason as character
    local aBlockData := {} as array
    local aAreaBCA := BCA->(getArea()) as array

    jBlockData := {"blockDate" : ctod(" / / "), "blockReason" : "", "reactivationDate" : ctod(" / / "), "blockData" : aBlockData}

    BCA->(dbSetOrder(1))
    if BCA->(msSeek(xFilial("BCA")+cSubscriberId))
        while !BCA->(eof()) .and. BCA->(BCA_MATRIC+BCA_TIPREG) == cSubscriberId

            cReason := PlMotBloANS("U", BCA->BCA_MOTBLO)

            if BCA->BCA_TIPO == "0" // 0 = Bloqueio
                if BCA->BCA_DATA >= jBlockData["blockDate"]
                    jBlockData["blockDate"] := BCA->BCA_DATA
                    jBlockData["blockReason"] := cReason
                endif
            else
                if BCA->BCA_DATA >= jBlockData["reactivationDate"]
                    jBlockData["reactivationDate"] := BCA->BCA_DATA
                endif
            endif

            aAdd(jBlockData["blockData"], {"date" : BCA->BCA_DATA, "type" : BCA->BCA_TIPO, "subscriberId" : BCA->BCA_MATRIC, "reason" : cReason, "time" : BCA->BCA_HORLAN})

            BCA->(dbSkip())
        enddo
        // Ordem decrescente por data
        aSort(jBlockData["blockData"], nil, nil, {|x,y| x["date"] < y["date"]})
    endif

    restArea(aAreaBCA)

return jBlockData
