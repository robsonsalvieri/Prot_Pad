#include "tlpp-core.th"
#include "fwmvcdef.ch"

#define STATUS_BATCH_RECEIVED "1"
#define STATUS_BATCH_PROCESSING "2"
#define STATUS_BATCH_IMPORTED_WITH_ERROR "3"
#define STATUS_BATCH_IMPORTED_SUCCESSFULLY "4"
#define STATUS_BATCH_CANCELED "5"

namespace totvs.protheus.health.plan.contract.beneficiary.import

/*{Protheus.doc} processImportBatch
Processa em job o lote de importação de beneficiários, executando a análise e validação dos dados.
@type function
@version 12.1.2510
@author vinicius.queiros
@since 25/05/2025
@return logical, Indica verdadeiro se o processamento foi iniciado com sucesso, falso caso contrário.
*/
function processImportBatch() as logical

    local lSuccess := .F. as logical
    local oModel := fwLoadModel("PLCTO001") as object

	oModel:setOperation(MODEL_OPERATION_UPDATE)
	oModel:activate()

	oModel:setValue("BJ5MASTER", "BJ5_STATUS", STATUS_BATCH_PROCESSING)

	if oModel:vldData()
		oModel:commitData()
		lSuccess := .T.

		startJob("totvs.protheus.health.plan.contract.beneficiary.import.startImportBatchJob", getEnvServer(), .F., cEmpAnt, cFilAnt, BJ5->BJ5_CODLOT)
	endif

	oModel:destroy()
	freeObj(oModel)
  
return lSuccess

/*{Protheus.doc} startImportBatchJob
Inicia o job responsável pelo processamento do lote de importação de beneficiários
@type function
@version 12.1.2510
@author vinicius.queiros
@since 25/05/2025
@param cCompany, caracter, Código da empresa.
@param cBranch, caracter, Código da filial.
@param cBatchCode, caracter, Código identificador do lote de beneficiários a ser processado.
*/
function startImportBatchJob(cCompany as character, cBranch as character, cBatchCode as character)	

	rpcSetType(3)
    rpcSetEnv(cCompany, cBranch, nil, nil, "PLS")

	if readImportBatchFile(cBatchCode)
		createBatchBeneficiariesProtocol(cBatchCode)
	endif
	
return

/*{Protheus.doc} readImportBatchFile
Realiza a leitura do arquivo CSV do lote de importação identificado pelo código do lote.
@type function
@version 12.1.2510
@author vinicius.queiros
@since 25/05/2025
@param cBatchCode, caracter, Código identificador do lote cujo arquivo CSV será lido.
@return logical, Indica verdadeiro se a leitura do arquivo foi bem-sucedida, falso caso contrário.
*/
function readImportBatchFile(cBatchCode as character) as logical

    local lSuccess := .F. as logical
	local oBatchFile as object

	oBatchFile := ImportBatchFile():new()

	if oBatchFile:setImportBatch(cBatchCode)
		lSuccess := oBatchFile:loadBeneficiariesFromFile()
	endif

	oBatchFile:destroy()
	freeObj(oBatchFile)

return lSuccess

/*/{Protheus.doc} createBatchBeneficiariesProtocol
Cria um protocolo de inclusão no cadastro de famílias para os beneficiários do lote informado.
@type function
@version 12.1.2510
@since 27/05/2025
@author vinicius.queiros
@param cBatchCode, character, código do lote que será processado para criação do protocolo
@return logical, verdadeiro se o protocolo for criado com sucesso, falso caso contrário
/*/
function createBatchBeneficiariesProtocol(cBatchCode as character) as logical

	local lSuccess := .F. as logical
	local oBatchProtocol as object

	oBatchProtocol := ImportBatchProtocol():new()

	if oBatchProtocol:setImportBatch(cBatchCode)
		lSuccess := oBatchProtocol:processBeneficiariesFromBatch()
	endif

	oBatchProtocol:destroy()
	freeObj(oBatchProtocol)

return lSuccess
