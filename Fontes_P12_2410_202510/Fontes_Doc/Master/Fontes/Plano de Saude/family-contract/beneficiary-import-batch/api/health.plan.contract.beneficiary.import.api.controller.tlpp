#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.contract.beneficiary.import

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} ImportBatchController
Controlador responsável por gerenciar as requisições relacionadas à importação em lote de beneficiários.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 13/05/2025
/*/
class ImportBatchController from BaseController

	public method new() constructor

	@POST("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches")
	public method postImportBatches()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches")
	public method getImportBatches()

	@POST("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches/start")
	public method postStartImportBatch()

	@POST("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches/reprocess")
	public method postReprocessImportBatch()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches/:batchCode/beneficiaries")
	public method getBatchBeneficiaries()

	@GET("/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches/:batchCode")
	public method getImportBatch()

	private method getFormattedFileJson() as json
	private method isFormData() as logical

endclass

/*/{Protheus.doc} new
Inicializa a classe ImportBatchController, preparando os recursos necessários para o controle da importação em lote de beneficiários.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 13/05/2025
/*/
method new() class ImportBatchController

	_Super:new()

return self

/*/{Protheus.doc} postImportBatches
Processa a requisição de importação em lote de beneficiários a partir de um arquivo enviado via form-data.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 13/05/2025
/*/
method postImportBatches() class ImportBatchController

	local oService as object
	local aRequiredData := {} as array
	local cBody := oRest:getBodyRequest() as character
	local lIsFormData as logical
	local lRequestOk := .F. as logical

	lIsFormData := self:isFormData()

	oService := ImportBatchService():new(lIsFormData)

	if lIsFormData
		if oService:setFormData(cBody)
			aAdd(aRequiredData, {"key": "healthInsurerCode", "type": "text"})
			aAdd(aRequiredData, {"key": "companyCode", "type": "text"})
			aAdd(aRequiredData, {"key": "contractCode", "type": "text"})
			aAdd(aRequiredData, {"key": "contractVersion", "type": "text"})
			aAdd(aRequiredData, {"key": "subcontractCode", "type": "text"})
			aAdd(aRequiredData, {"key": "subcontractVersion", "type": "text"})
			aAdd(aRequiredData, {"key": "loginUser", "type": "text"})
			aAdd(aRequiredData, {"key": "file", "type": "file"})

			if self:validateRequiredFormData(oService:getFormData(), aRequiredData)
				lRequestOk := .T.
			endif
		else
			oRest:setFault(oService:getJsonResult())
			oRest:setStatusCode(oService:getStatusCode())
		endif
	else
		if self:setBodyRequest(oRest:getBodyRequest())
			aAdd(aRequiredData, "healthInsurerCode")
			aAdd(aRequiredData, "companyCode")
			aAdd(aRequiredData, "contractCode")
			aAdd(aRequiredData, "contractVersion")
			aAdd(aRequiredData, "subcontractCode")
			aAdd(aRequiredData, "subcontractVersion")
			aAdd(aRequiredData, "loginUser")
			aAdd(aRequiredData, "fileBase64")
			aAdd(aRequiredData, "fileName")

			if self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
				if oService:setBody(self:getFormattedFileJson())
					lRequestOk := .T.
				endif
			endif
		endif
	endif

	if lRequestOk
		if oService:addImportBatch()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	fwFreeArray(aRequiredData)

return

/*/{Protheus.doc} getImportBatches
Retorna os lotes de importação disponíveis no sistema
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
/*/
method getImportBatches() class ImportBatchController

	local oService := ImportBatchService():new() as object

	oService:setQueryParams(oRest:getQueryRequest())

	if oService:getImportBatches()
		oRest:setResponse(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	_Super:destroy()
	oService:destroy()

	freeObj(oService)

return

/*/{Protheus.doc} postStartImportBatch
Inicia o processamento de um lote de beneficiários previamente importado, alterando seu status para Em processamento.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 25/05/2025
/*/
method postStartImportBatch() class ImportBatchController

	local oService := ImportBatchService():new() as object
	local aCheckFields := {} as array
	local cBody := oRest:getBodyRequest() as character

	aAdd(aCheckFields, {"field" : "batchCode", "required" : .T., "type" : "C", "size": tamSX3("BJ5_CODLOT")[1]})

	if oService:setBodyString(cBody, "E001") .and. oService:checkBodyFields(aCheckFields, "E002")
		if oService:startImportBatch()
			oRest:setKeyHeaderResponse("Location", "/totvsHealthPlans/familyContract/v1/beneficiaries/importBatches/" + oService:getBatchCode())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
	else
		oRest:setFault(oService:getJsonResult())
		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	fwFreeArray(aCheckFields)

return

/*/{Protheus.doc} postReprocessImportBatch
Recebe um novo arquivo CSV com ajustes para reprocessar beneficiários que apresentaram erro em um lote de importação anterior
@type method
@version 12.1.2510
@author vinicius.queiros
@since 13/06/2025
/*/
method postReprocessImportBatch() class ImportBatchController

	local oService as object
	local aRequiredData := {} as array
	local cBody := oRest:getBodyRequest() as character
	local lIsFormData as logical
	local lRequestOk := .F. as logical

	lIsFormData := self:isFormData()

	oService := ImportBatchService():new(lIsFormData)

	if lIsFormData
		if oService:setFormData(cBody)
			aAdd(aRequiredData, {"key": "importBatchCode", "type": "text"})
			aAdd(aRequiredData, {"key": "file", "type": "file"})

			if self:validateRequiredFormData(oService:getFormData(), aRequiredData)
				lRequestOk := .T.
			endif
		else
			oRest:setFault(oService:getJsonResult())
			oRest:setStatusCode(oService:getStatusCode())
		endif
	else
		if self:setBodyRequest(oRest:getBodyRequest())
			aAdd(aRequiredData, "importBatchCode")
			aAdd(aRequiredData, "fileBase64")
			aAdd(aRequiredData, "fileName")

			if self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
				if oService:setBody(self:getFormattedFileJson())
					lRequestOk := .T.
				endif
			endif
		endif
	endif

	if lRequestOk
		if oService:reprocessImportBatch()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
	endif

	oRest:setKeyHeaderResponse("Content-Type", "application/json")

	_Super:destroy()
	oService:destroy()

	freeObj(oService)
	fwFreeArray(aRequiredData)

return

/*/{Protheus.doc} getBatchBeneficiaries
Retorna os beneficiários associados a um lote de importação específico
@type method
@version 12.1.2510
@author vinicius.queiros
@since 07/06/2025
/*/
method getBatchBeneficiaries() class ImportBatchController

	local oService := ImportBatchService():new() as object

	if self:validateRequiredParams(oRest:getPathParamsRequest(), {"batchCode"})
		oService:setPathParams(oRest:getPathParamsRequest())
		oService:setQueryParams(oRest:getQueryRequest())

		if oService:getBatchBeneficiaries()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	_Super:destroy()
	oService:destroy()

	freeObj(oService)

return

/*/{Protheus.doc} getImportBatch
Obtém as informações do lote de importação informado
@type method
@version 12.1.2510
@author vinicius.queiros
@since 13/06/2025
/*/
method getImportBatch() class ImportBatchController

	local oService := ImportBatchService():new() as object

	if self:validateRequiredParams(oRest:getPathParamsRequest(), {"batchCode"})
		oService:setPathParams(oRest:getPathParamsRequest())

		if oService:getImportBatch()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	_Super:destroy()
	oService:destroy()

	freeObj(oService)

return

/*/{Protheus.doc} getFormattedFileJson
Retorna o conteúdo do arquivo de importação formatado de json para o padrão form-data.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 20/06/2025
@return json, conteúdo do arquivo formatado
/*/
method getFormattedFileJson() as json class ImportBatchController

	local jBody := self:getBodyRequest() as json
	local jFile := JsonObject():new() as json

	jFile["fileName"] := jBody["fileName"]
	jFile["uploadName"] := formatDateTime() + jBody["fileName"]
	jFile["uploadDir"] := "/http-root/httprest/httpuri/"

	memoWrite(jFile["uploadDir"] + jFile["uploadName"], decode64(jBody["fileBase64"]))

	jBody["file"] := {jFile}

	freeObj(jFile)
	
return jBody

/*/{Protheus.doc} isFormData
Verifica se os dados estão sendo enviados no formato form-data.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 20/06/2025
@return logical, indica se os dados são do tipo form-data
/*/
method isFormData() as logical class ImportBatchController

	local cContentType as character

	do case 
		case oRest:getHeaderRequest():hasProperty("Content-Type")
			cContentType := oRest:getHeaderRequest()["Content-Type"]
		
		case oRest:getHeaderRequest():hasProperty("content-type")
			cContentType := oRest:getHeaderRequest()["content-type"]
	endcase

return "multipart/form-data" $ lower(cContentType)

/*/{Protheus.doc} formatDateTime
Gera uma string com a data e hora atual no formato DD_MM_AAAA_HH_MM_SS_, útil para composições de nomes de arquivos ou identificadores únicos.
@type function
@version 12.1.2510
@autor vinicius.queiros
@since 20/06/2025
@return character, data e hora atual formatada como DD_MM_AAAA_HH_MM_SS_
/*/
function formatDateTime() as character

	local cDateTime as character
	local cTime := time() as character

	cDateTime := subStr(dtoc(dDataBase), 1, 2) + "_" + ; // Dia
				 subStr(dtoc(dDataBase), 4, 2) + "_" + ; // Mês
				 subStr(dtoc(dDataBase), 7, 4) + "_" + ; // Ano
				 subStr(cTime, 1, 2) + "_" + ; // Hora
				 subStr(cTime, 4, 2) + "_" + ; // Minuto
				 subStr(cTime, 7, 2) + "_" // Segundo

return cDateTime
