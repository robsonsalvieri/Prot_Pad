#include "fileIO.ch"
#include "protheus.ch"
#include "totvs.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} PlsLoteGuiaXml
Classe que determina a montagem do arquivo Tiss Lote Guia

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
class PlsLoteGuiaXml

	public data cXml 		as character 	// String do Arquivo XML
	public data cNameSpace	as character 	// NameSpace do arquivo
	public data nArqFull 	as numeric 	    // Arquivo XML
	public data cFileName   as character 	// Nome do arquivo XML gerado
	public data cFolder     as character 	// Pasta onde sera gravado o arquivo XML
	public data cStrHash    as character	// String que vai ser utilizada para calculo do hash
	public data nArqHash 	as numeric	    // Arquivo do hash
	public data cHashMD5    as character 	// Hash MD5 calculado

	public method new() constructor


	public method consulta()
	public method sadt()
	public method resumoIternacao()
	public method guiaHonorarios()


	public method montaTag()
	public method IniCabecalho()
	public method geraXML()
	public method CalcHash()
	public method geraHash()



endclass


//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
method new() class PlsLoteGuiaXml

	::cXml := ""
	::cNameSpace := "ans"
	::nArqFull	:= 0
	::cFileName := ""
	::cStrHash :=""
	::nArqHash := 0
	::cHashMD5 := ''


return self

//-------------------------------------------------------------------
/*/{Protheus.doc} MontaTag
Formata a TAG XML a ser escrita no arquivo

@author  Lucas Nonato
@version P12
@since   11/10/2019
@param nSpc    = chaveamento
@param cTag    = nome tag
@param cVal    = valor campo
@param lFin    = fechamento da tag
@param lEncode = retira caracteres especiais
/*/
method MontaTag(nSpc,cTag,cVal,lFin,lEncode) CLASS PlsLoteGuiaXml
	local cRetTag   := ""
	local cNameSpace:= ""
	local lIni		:= .f.
	default nSpc    := 0
	default cTag    := ""
	default lFin    := .F.
	default lEncode := .F.

	if cVal == nil
		cVal    := ""
		lIni := !lFin
	endif

	if !Empty(::cNameSpace)
		cNameSpace := ::cNameSpace+":"
	endif

	if !empty(cVal) .or. lIni
		cRetTag += '<' + cNameSpace+ cTag + '>'
		cVal 	:= alltrim(iif(lEncode,PtuStTran(cVal),cVal))
		cRetTag += cVal
	endif
	if !empty(cVal) .or. lFin
		cRetTag += '</' + cNameSpace + cTag + '>'
	endif

	if !empty(cVal) .and. cTag <> 'hash'
		::cStrHash += cVal
	endif
	if !empty(cRetTag)
		::cXml  += Replicate( "	", nSpc ) + cRetTag + CRLF
	endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Inifile
Inicia o arquivo XML

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method IniCabecalho(cXmlAlias, lInterc,cCnpj,cSusep) CLASS PlsLoteGuiaXml

	Local dDatGer
	Local cNumLote
	default lInterc := .F.
	default cCnpj   := ""
	default cSusep  := ""

	dDatGer := Iif(lInterc,BTF->BTF_DATGER, BDC->BDC_DATGER)
	cNumLote := Iif(lInterc, BTF->BTF_NUMERO, BDC->BDC_NUMERO)

	::cXml += '<?xml version="1.0" encoding="UTF-8"?>' + CRLF
	::cXml += '<ans:mensagemTISS xmlns:ans="http://www.ans.gov.br/padroes/tiss/schemas" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:schemaLocation="http://www.ans.gov.br/padroes/tiss/schemas http://www.ans.gov.br/padroes/tiss/schemas/tissV3_04_01.xsd">' + CRLF

	::montaTag( 1,"cabecalho")
	::montaTag( 2,'identificacaoTransacao')
	::montaTag( 3,'tipoTransacao',"ENVIO_LOTE_GUIAS")
	::montaTag( 3,'sequencialTransacao',(cXmlAlias)->(NUMFAT))
	::montaTag( 3,'dataRegistroTransacao',substr(FWTimeStamp( 5 , dDatGer),1,10))
	::montaTag( 3,'horaRegistroTransacao',substr(FWTimeStamp( 5 , dDatGer),12,08))
	::montaTag( 2,'identificacaoTransacao',,.T.)

	::montaTag( 2,'origem')
	::montaTag( 3,'identificacaoPrestador')
	::montaTag( 4,'codigoPrestadorNaOperadora',IIF(!Empty(cCnpj),cCnpj,(cXmlAlias)->(CODRDA)),.T.)
	::montaTag( 3,'identificacaoPrestador',,.T.)
	::montaTag( 2,'origem',,.T.)

	::montaTag( 2,'destino')
	::montaTag( 3,'registroANS',IIF(!Empty(cSusep),cSusep,BA0->BA0_SUSEP),.T.)
	::montaTag( 2,'destino',,.T.)
	::montaTag( 2,"Padrao",BA0->BA0_TISVER,.T.)
	::montaTag( 1,"cabecalho",,.T.)

	::montaTag( 1,'prestadorParaOperadora')
	::montaTag( 2,'loteGuias')
	::montaTag( 3,'numeroLote',cNumLote,.T.)
	::montaTag( 3,'guiasTISS')

return


//-------------------------------------------------------------------
/*/{Protheus.doc} geraXML
Escreve o arquivo XML

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method geraXML(lEnd,cDirTmp) CLASS PlsLoteGuiaXml


	if ::nArqFull == 0
		::nArqFull := fCreate( cDirTmp + ::cFileName + '.xml'  ,FC_NORMAL,,.F.)
	endif

	fWrite( ::nArqFull,::cXml )

	fClose( ::nArqFull )
	::cXml := ""

return


//-------------------------------------------------------------------
/*/{Protheus.doc} consulta
Escreve o arquivo do hash

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method consulta(cXmlAlias) CLASS PlsLoteGuiaXml

	::montaTag( 4,'guiaConsulta')

	::montaTag( 5,'cabecalhoConsulta')
	::montaTag( 6,'registroANS',BA0->BA0_SUSEP,.T.)
	::montaTag( 6,'numeroGuiaPrestador',BD5->BD5_NUMERO,.T.)
	::montaTag( 5,'cabecalhoConsulta',,.T.)

	::montaTag( 5,'numeroGuiaOperadora',BD5->BD5_NUMERO,.T.)

	// DADOS DO BENEFECIARIO
	::montaTag( 5,'dadosBeneficiario')
	::montaTag( 6,'numeroCarteira',BD5->(BD5_CODOPE+BD5_CODEMP+BD5_MATRIC+BD5_TIPREG+BD5_DIGITO),.T.)
	::montaTag( 6,'atendimentoRN',if(BD5->BD5_ATERNA =='1','S','N'),.T.)
	::montaTag( 5,'dadosBeneficiario',,.T.)

	// DADOS DO COMTRATANTE EXECUTANTE
	::montaTag( 5,'contratadoExecutante')
	::montaTag( 6,if(BAU->BAU_TIPPE<>'J','cpfContratado','cnpjContratado'),BAU->BAU_CPFCGC,.T.)
	::montaTag( 6,'CNES',if(empty(BAU->BAU_CNES),'0000000',BAU->BAU_CNES),.T.)
	::montaTag( 5,'contratadoExecutante',,.T.)


	If (BD6->(DbSeek((xFilial("BD6")+BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)))))

		While !BD6->(Eof()) .and. BD6->(BD6_CODOPE+BD6_CODLDP+BD6_CODPEG+BD6_NUMERO+BD6_ORIMOV) == BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)
			//DADOS DO PROFICIONAL EXECUTANTE
			::montaTag( 5,'profissionalExecutante')
			::montaTag( 6,'conselhoProfissional',AllTrim( PLSGETVINC("BTU_CDTERM", "BAH", .F., "26", BD6->BD6_SIGEXE)),.T.)
			::montaTag( 6,'numeroConselhoProfissional',BD6->BD6_REGEXE,.T.)
			::montaTag( 6,'UF',PLSGETVINC("BTU_CDTERM", "   ", .F., "59", BD6->BD6_ESTEXE),.T.)

			cCbos := BAQ->(posicione("BAQ",1,xFilial("BAQ")+BD6->BD6_CODOPE+BD6->BD6_ESPEXE,"BAQ_CBOS"))
			if empty(cCbos)
				cCbos := BAQ->(posicione("BAQ",1,xFilial("BAQ")+BD6->BD6_CODOPE+BD5->BD5_CODESP,"BAQ_CBOS"))
			endif
			::montaTag( 6,'CBOS',alltrim(cCbos),.T.)
			::montaTag( 5,'profissionalExecutante',,.T.)

			::montaTag( 5,'indicacaoAcidente',if(!empty(BD5->BD5_INDACI),BD5->BD5_INDACI,'0'),.T.)

			::montaTag( 5,'dadosAtendimento')
			::montaTag( 6,'regimeAtendimento', if(Empty(BD5->BD5_TMREGA), '0'+BD5->BD5_REGATE,BD5->BD5_TMREGA),.T.)
			::montaTag( 6,'saudeOcupacional',if(empty(BD5->BD5_SAUOCU),'01',BD5->BD5_SAUOCU),.T.)
			::montaTag( 6,'dataAtendimento',substr(FWTimeStamp( 5 , (BD6->BD6_DATPRO)),1,10),.T.)
			::montaTag( 6,'tipoConsulta',BD5->BD5_TIPCON,.T.)
			::montaTag( 6,'procedimento')

			cCodPad := Alltrim(PLSVARVINC('87', 'BR4', BD6->BD6_CODPAD)) // Vinculo Terminologia de TIPO de Atendimento Tabela 57 TISS
			cCodPad := padr(cCodPad,tamsx3("BR8_CODPAD")[1])
			cCodPro := PLSVARVINC(cCodPad, 'BR8', BD6->BD6_CODPRO,cCodPad+ BD6->BD6_CODPRO)

			::montaTag( 7,'codigoTabela',cCodPad,.T.)
			::montaTag( 7,'codigoProcedimento',alltrim(cCodPro),.T.)
			::montaTag( 7,'valorProcedimento',transform(BD6->BD6_VLRPAG,'999999.99'),.T.)
			::montaTag( 6,'procedimento',,.T.)
			::montaTag( 5,'dadosAtendimento',,.T.)
			BD6->(dbSkip())
		endDo
	endIF


	::montaTag( 4,'guiaConsulta',,.T.)


return


//-------------------------------------------------------------------
/*/{Protheus.doc} sadt
gera xml sadt

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method sadt(cXmlAlias,cCnpj) CLASS PlsLoteGuiaXml

	local nVlrPago := 0
	Default cCnpj    := ""

	::montaTag( 4,'guiaSP-SADT')

	::montaTag( 5,'cabecalhoGuia')
	::montaTag( 6,'registroANS',BA0->BA0_SUSEP,.T.)
	::montaTag( 6,'numeroGuiaPrestador',BD5->BD5_NUMERO,.T.)
	::montaTag( 5,'cabecalhoGuia',,.T.)


	// dados do beneficiario
	::montaTag( 5,'dadosBeneficiario')
	::montaTag( 6,'numeroCarteira',BD5->(BD5_CODOPE+BD5_CODEMP+BD5_MATRIC+BD5_TIPREG+BD5_DIGITO),.T.)
	::montaTag( 6,'atendimentoRN',if(BD5->BD5_ATERNA =='1','S','N'),.T.)
	::montaTag( 5,'dadosBeneficiario',,.T.)

	// dados do solicitante
	::montaTag( 5,'dadosSolicitante')
	::montaTag( 6,'contratadoSolicitante')
	::montaTag( 7,'codigoPrestadorNaOperadora',BD5->BD5_REGSOL, .T.)

	if BB0->(DbSeek(xFilial("BB0")+BD5->BD5_REGSOL))
		::montaTag( 6,'contratadoSolicitante',,.T.)
		::montaTag( 6,'nomeContratadoSolicitante',alltrim(BB0->BB0_NOME),.T.)
		::montaTag( 6,'profissionalSolicitante')
		::montaTag( 7,'nomeProfissional',alltrim(BB0->BB0_NOME), .T.)
		::montaTag( 7,'conselhoProfissional',alltrim(PLSGETVINC("BTU_CDTERM", "BAH", .F., "26", BB0->BB0_CODSIG)), .T.)
		::montaTag( 7,'numeroConselhoProfissional',alltrim(BB0->BB0_NUMCR), .T.)
		::montaTag( 7,'UF',PLSGETVINC("BTU_CDTERM", "   ", .F., "59", BB0->BB0_ESTADO), .T.)
	endif



	If (BD6->(DbSeek((xFilial("BD6")+BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)))))

		cCbos := BAQ->(posicione("BAQ",1,xFilial("BAQ")+BD6->BD6_CODOPE+BD6->BD6_ESPEXE,"BAQ_CBOS"))
		if empty(cCbos)
			cCbos := BAQ->(posicione("BAQ",1,xFilial("BAQ")+BD6->BD6_CODOPE+BD5->BD5_CODESP,"BAQ_CBOS"))
		endif
		::montaTag( 7,'CBOS',alltrim(cCbos), .T.)
		::montaTag( 6,'profissionalSolicitante',,.T.)
		::montaTag( 5,'dadosSolicitante',,.T.)


		//dadoa de solicitação
		::montaTag( 5,'dadosSolicitacao')
		cTpAdm 	:=  PLSGETVINC("BTU_CDTERM", "BDR", .F., "23", BD5->BD5_TIPADM, .F.)
		::montaTag( 6,'caraterAtendimento',cTpAdm,.T.)
		If !Empty(BD5->BD5_INDCLI)
			::montaTag( 6, 'indicacaoClinica', BD5->BD5_INDCLI, .T.)
		EndIf
		If !Empty(BD5->BD5_COBESP)
			::montaTag( 6, 'indCobEspecial', BD5->BD5_COBESP, .T.)
		EndIf
		::montaTag( 5,'dadosSolicitacao',,.T.)


		// dados executante
		::montaTag( 5,'dadosExecutante')
		::montaTag( 6,'contratadoExecutante')
		::montaTag( 7,'codigoPrestadorNaOperadora',cCnpj,.T.)
		::montaTag( 6,'contratadoExecutante',,.T.)
		::montaTag( 6,'CNES',if(empty(BAU->BAU_CNES),'0000000',BAU->BAU_CNES),.T.)
		::montaTag( 5,'dadosExecutante',,.T.)

		//dados de atendimento
		::montaTag( 5,'dadosAtendimento')
		::montaTag( 6,'tipoAtendimento',BD5->BD5_TIPATE,.T.)
		::montaTag( 6,'indicacaoAcidente',if(empty(BD5->BD5_INDACI),'2',BD5->BD5_INDACI),.T.)
		::montaTag( 6,'tipoConsulta',BD5->BD5_TIPCON,.T.)
		::montaTag( 6,'regimeAtendimento', if(Empty(BD5->BD5_TMREGA), '0'+BD5->BD5_REGATE,BD5->BD5_TMREGA),.T.)
		If !Empty(BD5->BD5_SAUOCU)
			::montaTag( 6, 'saudeOcupacional', BD5->BD5_SAUOCU, .T.)
		EndIf
		::montaTag( 5,'dadosAtendimento',,.T.)


		While !BD6->(Eof()) .and. BD6->(BD6_CODOPE+BD6_CODLDP+BD6_CODPEG+BD6_NUMERO+BD6_ORIMOV) == BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)
			//procedimentos executados

			nVlrPago+=BD6->BD6_VLRPAG

			BD6->(dbSkip())
			EXIT
		endDo
		::montaTag( 5,'valorTotal')
		::montaTag( 6,'valorTotalGeral',transform(nVlrPago,'999999.99'),.T.)
		::montaTag( 5,'valorTotal',,.T.)

	endIF

	::montaTag( 4,'guiaSP-SADT',,.T.)


return




//-------------------------------------------------------------------
/*/{Protheus.doc} resumoInternacao
gera xml resumointernacao

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method resumoIternacao(cXmlAlias,cCnpj) CLASS PlsLoteGuiaXml

	local adadInter:={}
	local nVlrPago :=0
	default cCnpj := ""

	::montaTag( 4,'guiaResumoInternacao')

	::montaTag( 5,'cabecalhoGuia')
	::montaTag( 6,'registroANS',BA0->BA0_SUSEP,.T.)
	::montaTag( 6,'numeroGuiaPrestador',BE4->BE4_NUMERO,.T.)
	::montaTag( 5,'cabecalhoGuia',,.T.)
	::montaTag( 5,'numeroGuiaSolicitacaoInternacao',substr(BE4->BE4_GUIINT,5,20),.T.)

	//dados da autorização
	::montaTag( 5,'dadosAutorizacao')


	//pesquisando a guia principal de internação
	::montaTag( 6,'numeroGuiaOperadora',substr(BE4->BE4_GUIINT,5,20),.T.)
	::montaTag( 6,'dataAutorizacao',substr(FWTimeStamp( 5 , (BE4->BE4_DATPRO)),1,10),.T.)
	::montaTag( 6,'senha',BE4->BE4_SENHA,.T.)

	aadd(adadInter,BE4->BE4_DATPRO) //data inicio do faturamento
	aadd(adadInter,BE4->BE4_HORPRO) //hora inicio do faturamento
	aadd(adadInter,if(!empty(BE4->BE4_DTALTA),BE4->BE4_DTALTA,msdate())) //data final do faturamento
	aadd(adadInter,if(!empty(BE4->BE4_HRALTA),BE4->BE4_HRALTA,substr(time(),1,2)+substr(time(),4,2)+substr(time(),7,2))) //hora final do faturamento
	aadd(adadInter,BE4->BE4_GRPINT) //tipo de internação
	aadd(adadInter,BE4->BE4_REGINT) //regime de internação

	::montaTag( 5,'dadosAutorizacao',,.T.)

	// dados do beneficiario
	::montaTag( 5,'dadosBeneficiario')
	::montaTag( 6,'numeroCarteira',BE4->(BE4_CODOPE+BE4_CODEMP+BE4_MATRIC+BE4_TIPREG+BE4_DIGITO),.T.)
	::montaTag( 6,'atendimentoRN',if(BE4->BE4_ATERNA =='1','S','N'),.T.)
	::montaTag( 5,'dadosBeneficiario',,.T.)

	// dados executante
	::montaTag( 5,'dadosExecutante')
	::montaTag( 6,'contratadoExecutante')
	::montaTag( 7,'codigoPrestadorNaOperadora',cCnpj,.T.)
	// ::montaTag( 7,if(BAU->BAU_TIPPE<>'J','cpfContratado','cnpjContratado'),BAU->BAU_CPFCGC,.T.)
	::montaTag( 6,'contratadoExecutante',,.T.)
	::montaTag( 6,'CNES',if(empty(BAU->BAU_CNES),'0000000',BAU->BAU_CNES),.T.)
	::montaTag( 5,'dadosExecutante',,.T.)

	//dados de internação
	::montaTag( 5,'dadosInternacao')
	cTpAdm 	:=  PLSGETVINC("BTU_CDTERM", "BDR", .F., "23", BE4->BE4_TIPADM, .F.)
	::montaTag( 6,'caraterAtendimento',cTpAdm,.T.)
	::montaTag( 6,'tipoFaturamento',BE4->BE4_TIPFAT,.T.)

	if len(adadInter) > 0
		::montaTag( 6,'dataInicioFaturamento',substr(FWTimeStamp( 5 , adadInter[1]),1,10),.T.)
		::montaTag( 6,'horaInicioFaturamento',transform(strzero(val(adadInter[2]),6),"@R 99:99:99"),.T.)
		::montaTag( 6,'dataFinalFaturamento',substr(FWTimeStamp( 5 , adadInter[3]),1,10),.T.)
		::montaTag( 6,'horaFinalFaturamento',transform(strzero(val(adadInter[4]),6),"@R 99:99:99"),.T.)
		::montaTag( 6,'tipoInternacao',adadInter[5],.T.)
		::montaTag( 6,'regimeInternacao',adadInter[6],.T.)
	endIf
	::montaTag( 5,'dadosInternacao',,.T.)

	//dados de saida de internação
	::montaTag( 5,'dadosSaidaInternacao')
	::montaTag( 6,'indicadorAcidente',if(empty(BE4->BE4_INDACI),'2',BE4->BE4_INDACI),.T.)
	::montaTag( 6,'motivoEncerramento',PLSGETVINC("BTU_CDTERM", "BIY", .F., "39", BE4->BE4_TIPALT),.T.)
	::montaTag( 5,'dadosSaidaInternacao',,.T.)



	If (BD6->(DbSeek((xFilial("BD6")+BE4->(BE4_CODOPE+BE4_CODLDP+BE4_CODPEG+BE4_NUMERO+BE4_ORIMOV)))))

		While !BD6->(Eof()) .and. BD6->(BD6_CODOPE+BD6_CODLDP+BD6_CODPEG+BD6_NUMERO+BD6_ORIMOV) == BE4->(BE4_CODOPE+BE4_CODLDP+BE4_CODPEG+BE4_NUMERO+BE4_ORIMOV)
			//procedimentos executados
			nVlrPago+=BD6->BD6_VLRPAG

			BD6->(dbSkip())
		endDo

		::montaTag( 5,'valorTotal')
		::montaTag( 6,'valorTotalGeral',transform(nVlrPago,'999999.99'),.T.)
		::montaTag( 5,'valorTotal',,.T.)

	endIF

	::montaTag( 4,'guiaResumoInternacao',,.T.)


return


//-------------------------------------------------------------------
/*/{Protheus.doc} guiaHonorarios
gera xml guiaHonorarios

@author    Robson Nayland
@version   V12
@since     10/01/2023
/*/
//-------------------------------------------------------------------
method guiaHonorarios(cXmlAlias) CLASS PlsLoteGuiaXml

	local aAreaBe4:={}
	local cAliasBD7 := GetNextAlias()
	local nVlrPago :=0
	local agraupart:={}

	::montaTag( 4,'guiaHonorarios')

	::montaTag( 5,'cabecalhoGuia')
	::montaTag( 6,'registroANS',BA0->BA0_SUSEP,.T.)
	::montaTag( 6,'numeroGuiaPrestador',BD5->BD5_NUMERO,.T.)
	::montaTag( 5,'cabecalhoGuia',,.T.)
	::montaTag( 5,'guiaSolicInternacao',substr(BD5->BD5_GUIINT,5,20),.T.)

	// dados do beneficiario
	::montaTag( 5,'beneficiario')
	::montaTag( 6,'numeroCarteira',BD5->(BD5_CODOPE+BD5_CODEMP+BD5_MATRIC+BD5_TIPREG+BD5_DIGITO),.T.)
	::montaTag( 6,'atendimentoRN',if(BD5->BD5_ATERNA =='1','S','N'),.T.)
	::montaTag( 5,'beneficiario',,.T.)

	// dados Contratado
	::montaTag( 5,'localContratado')
	::montaTag( 6,'codigoContratado')
	::montaTag( 7,'codigoNaOperadora',BAU->BAU_CODIGO,.T.)
	//::montaTag( 7,'cnpjLocalExecutante',BAU->BAU_CPFCGC,.T.)
	::montaTag( 6,'codigoContratado',,.T.)
	::montaTag( 6,'nomeContratado',alltrim(BAU->BAU_NOME),.T.)
	::montaTag( 6,'cnes',if(empty(BAU->BAU_CNES),'0000000',BAU->BAU_CNES),.T.)
	::montaTag( 5,'localContratado',,.T.)

	// dados executante
	::montaTag( 5,'dadosContratadoExecutante')
	::montaTag( 6,'codigonaOperadora',BA0->BA0_CGC,.T.)
	::montaTag( 6,'cnesContratadoExecutante',if(empty(BAU->BAU_CNES),'0000000',BAU->BAU_CNES),.T.)
	::montaTag( 5,'dadosContratadoExecutante',,.T.)


	aAreaBe4 := BE4->(getArea())

	//pesquisando a guia principal de internação
	if(BE4->(DbSeek(xFilial("BE4")+BD5->BD5_GUIINT)))
		::montaTag( 5,'dadosInternacao')
		::montaTag( 6,'dataInicioFaturamento',substr(FWTimeStamp( 5 , (BE4->BE4_DATPRO)),1,10),.T.)
		::montaTag( 6,'dataFimFaturamento',substr(FWTimeStamp( 5 , (if(!empty(BE4->BE4_DTALTA),BE4->BE4_DTALTA,msdate()))),1,10),.T.)
		::montaTag( 5,'dadosInternacao','',.T.)
	endIf
	RestArea(aAreaBe4)

	If (BD6->(DbSeek((xFilial("BD6")+BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)))))
		::montaTag( 5,'procedimentosRealizados')

		While !BD6->(Eof()) .and. BD6->(BD6_CODOPE+BD6_CODLDP+BD6_CODPEG+BD6_NUMERO+BD6_ORIMOV) == BD5->(BD5_CODOPE+BD5_CODLDP+BD5_CODPEG+BD5_NUMERO+BD5_ORIMOV)
			//procedimentos executados
			::montaTag( 6,'procedimentoRealizado')
			::montaTag( 7,'sequencialItem',BD6->BD6_SEQUEN,.T.)
			::montaTag( 7,'dataExecucao',substr(FWTimeStamp( 5 , (BD6->BD6_DATPRO)),1,10),.T.)
			::montaTag( 7,'horaInicial',transform(strzero(val(BD6->BD6_HORPRO),4),"@R 99:99")+':00',.T.)
			::montaTag( 7,'horaFinal',transform(strzero(val(BD6->BD6_HORFIM),4),"@R 99:99")+':00',.T.)

			//procedimentos utilziados
			::montaTag( 7,'procedimento')
			cCodPad := Alltrim(PLSVARVINC('87', 'BR4', BD6->BD6_CODPAD)) // Vinculo Terminologia de TIPO de Atendimento Tabela 57 TISS
			cCodPad := padr(cCodPad,tamsx3("BR8_CODPAD")[1])
			cCodPro := PLSVARVINC(cCodPad, 'BR8', BD6->BD6_CODPRO,cCodPad+ BD6->BD6_CODPRO)

			::montaTag( 8,'codigoTabela',cCodPad,.T.)
			::montaTag( 8,'codigoProcedimento',alltrim(cCodPro),.T.)
			::montaTag( 8,'descricaoProcedimento',alltrim(BD6->BD6_DESPRO),.T.)
			::montaTag( 7,'procedimento',,.T.)

			::montaTag( 7,'quantidadeExecutada',alltrim(str(BD6->BD6_QTDPRO)),.T.)
			cFatSer := transform((cvaltochar(BD6->BD6_PERHES+100)), '@R 9.99')
			::montaTag( 7,'reducaoAcrescimo',cFatSer,.T.)
			::montaTag( 7,'valorUnitario',transform((BD6->BD6_VLRPAG/BD6->BD6_QTDPRO),'999999.99'),.T.)
			::montaTag( 7,'valorTotal',transform(BD6->BD6_VLRPAG,'999999.99'),.T.)

			::montaTag( 7,'profissionais')

			cSql := " SELECT BD7.BD7_ESPEXE,BD7.BD7_CODESP,BD7.BD7_CODTPA,BD7.BD7_CODUNM, BD7.BD7_SIGLA,BD7.BD7_REGPRE,BD7.BD7_ESTPRE,BD7.BD7_NLANC,BAU.BAU_CODIGO,BAU.BAU_NOME ,BAU.BAU_CPFCGC ,BAU.BAU_SIGLCR ,BAU.BAU_CONREG ,BAU.BAU_ESTCR "
			cSql += " FROM "+ RetSqlName("BD7")+ " BD7, " + RetSqlName("BAU")+ " BAU "
			cSql += " WHERE BD7.BD7_FILIAL = '"+xFilial("BD7")+"'"
			cSql += " AND BAU.BAU_FILIAL = '" +xFilial("BAU")+"'"
			cSql += " AND BD7.BD7_CODOPE =  '" + BD6->BD6_CODOPE +"'"
			cSql += " AND BD7.BD7_CODLDP =  '" + BD6->BD6_CODLDP +"'"
			cSql += " AND BD7.BD7_CODPEG =  '" + BD6->BD6_CODPEG +"'"
			cSql += " AND BD7.BD7_NUMERO =  '" + BD6->BD6_NUMERO +"'"
			cSql += " AND BD7.BD7_ORIMOV =  '" + BD6->BD6_ORIMOV +"'"
			cSql += " AND BD7.BD7_CODPAD =  '" + BD6->BD6_CODPAD +"'"
			cSql += " AND BD7.BD7_CODPRO =  '" + BD6->BD6_CODPRO +"'"
			cSql += " AND BD7.BD7_SEQUEN =  '" + BD6->BD6_SEQUEN +"'"
			cSql += " AND BD7.D_E_L_E_T_ = ' ' "
			cSql += " AND BAU.BAU_CODIGO = BD7.BD7_CODRDA"
			cSql += " AND BAU.D_E_L_E_T_ = ' ' "

			dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),cAliasBD7,.F.,.T.)

			While (cAliasBD7)->(!Eof())


				cCBOS := AllTrim( Posicione("BAQ",7,xfilial("BAQ")+(cAliasBD7)->BD7_ESPEXE,"BAQ_CBOS") )
				if empty(cCBOS)
					cCBOS := AllTrim( Posicione("BAQ",7,xfilial("BAQ")+(cAliasBD7)->BD7_CODESP,"BAQ_CBOS") )
				endIf

				cGrauPart := ''
				if !empty((cAliasBD7)->BD7_CODTPA)
					cGrauPart := PLSGETVINC("BTU_CDTERM", "BWT", .F., "35", (cAliasBD7)->BD7_CODTPA)
				elseif BD6->BD6_TIPGUI == '06'
					if (cAliasBD7)->BD7_CODUNM == "AUX"
						cGrauPart	:=  (cAliasBD7)->BD7_NLANC
						if(empty(cGrauPart) .or. !(cGrauPart $ '01;02;03;04'),cGrauPart	:=  "01",)
						elseif (cAliasBD7)->BD7_CODUNM $ "PAP,PA,PAR"
							cGrauPart	:=  "06"
						else
							cGrauPa := PLSGrauUM(BD6->BD6_CODPAD,BD6->BD6_CODPRO,(cAliasBD7)->BD7_CODUNM,BD6->BD6_DATPRO,"2")[2]
							if empty(cGrauPa) .and. (cAliasBD7)->BD7_CODUNM $ "PPM,HM,HMR,VMT,VMD,REA,VTX,VDI"
								cGrauPart := "00"
							else
								cGrauPart := PLSGETVINC("BTU_CDTERM", "BWT", .F., "35", cGrauPa)
							endif
						endif
					endIf
					If ascan(agraupart,cGrauPart) = 0
						::montaTag( 8,'grauParticipacao',cGrauPart,.T.)
						::montaTag( 8,'codProfissional')
						::montaTag( 9,'codigoPrestadorNaOperadora',BAU->BAU_CPFCGC,.T.)
						::montaTag( 8,'codProfissional',,.T.)
						::montaTag( 8,'nomeProfissional',alltrim((cAliasBD7)->BAU_NOME),.T.)
						::montaTag( 8,'conselhoProfissional',alltrim(PLSGETVINC("BTU_CDTERM", "BAH", .F., "26", (cAliasBD7)->BAU_SIGLCR)),.T.)
						::montaTag( 8,'numeroConselhoProfissional',alltrim((cAliasBD7)->BAU_CONREG),.T.)

						::montaTag( 8,'UF',PLSGETVINC("BTU_CDTERM", "   ", .F., "59", (cAliasBD7)->BAU_ESTCR),.T.)
						::montaTag( 8,'CBO',cCBOS,.T.)

						aadd(agraupart,cGrauPart)
					endif
					exit
					(cAliasBD7)->(DbSkip())
				Enddo

				(cAliasBD7)->(DbCloseArea())

				::montaTag( 7,'profissionais','',.T.)
				::montaTag( 6,'procedimentoRealizado',,.T.)
				nVlrPago+=BD6->BD6_VLRPAG

				BD6->(dbSkip())
			endDo
			::montaTag( 5,'procedimentosRealizados',,.T.)
			::montaTag( 5,'valorTotalHonorarios',transform(nVlrPago,'999999.99'),.T.)
			::montaTag( 5,'dataEmissaoGuia',substr(FWTimeStamp( 5 , (BD5->BD5_DATPRO)),1,10),.T.)
		endIF

		::montaTag( 4,'guiaHonorarios',,.T.)


		return




//-------------------------------------------------------------------
/*/{Protheus.doc} CalcHash
Calcula o Hash MD5 do arquivo

@author    Robson Nayland
@version   P12
@since     14/01/2023
/*/
//-------------------------------------------------------------------
method CalcHash(cDirTmp) CLASS PlsLoteGuiaXml

	if ::nArqHash > 0
		fClose( ::nArqHash )
		::cHashMD5 := md5File(cDirTmp + ::cFileName + ".txt" )
		fErase( cDirTmp + ::cFileName + ".txt" )
	endif

return



//-------------------------------------------------------------------
/*/{Protheus.doc} CalcHash
Calcula o Hash MD5 do arquivo

@author    Robson Nayland
@version   P12
@since     14/01/2023
/*/
//-------------------------------------------------------------------
method geraHash(cDirTmp) CLASS PlsLoteGuiaXml

	if ::nArqHash == 0
		::nArqHash := fCreate( cDirTmp + ::cFileName + ".txt",FC_NORMAL,,.F.)
	endif

	fWrite( ::nArqHash,::cStrHash )

	::cStrHash := ""

return







