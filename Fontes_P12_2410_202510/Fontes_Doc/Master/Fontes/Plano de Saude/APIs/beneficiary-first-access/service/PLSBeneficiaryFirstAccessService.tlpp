#INCLUDE "plsmfun.ch"
#INCLUDE "PLSMCCR.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "PLSMGER.CH"
#INCLUDE "PLSMCCR.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "AP5MAIL.CH"
#INCLUDE "APWEBEX.CH"
#INCLUDE "fileio.ch"
#INCLUDE "Fwlibversion.ch"
#INCLUDE "TOTVS.CH"
#INCLUDE "PLSBeneficiaryFirstAccessService.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSBeneficiaryFirstAccessService

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
class PLSBeneficiaryFirstAccessService from CenRequest

	public Data oError as Object
	public Data oResult as Object
	public data oRest as object
	public data oJson as object

	public method new() constructor
	public method validBenef()
	public method vldJson()
	public method setError()
	public method getError()
	public method getResult()
	public method setResult()
	public Method getStatusCode()
	public Method verificaCpfMingle()
	public Method GetBeneficiarySQL()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
method new(oRest,jPathParams) class PLSBeneficiaryFirstAccessService

	_Super:New(oRest)
	::oRest := jPathParams
	self:oError    := JsonObject():new()
	self:oResult   := JsonObject():new()
	self:nStatus   := 200

return self

//-------------------------------------------------------------------
/*/{Protheus.doc} metodos get set

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method getError() class PLSBeneficiaryFirstAccessService
return self:oError

method getResult() class PLSBeneficiaryFirstAccessService
return self:oResult:toJson()

method getStatusCode() class PLSBeneficiaryFirstAccessService
return self:nStatus

//-------------------------------------------------------------------
/*/{Protheus.doc} validBenef

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/
method validBenef() class PLSBeneficiaryFirstAccessService
	local cRet 		:= ''

	if(self:lSuccess)
		cRet := self:verificaCpfMingle(self:oRest['cpf'])

		if('criado com sucesso' $ cvaltochar(cRet)) 
			self:setResult(cRet)
		else 
			self:setError(400,'0001',nil,cRet)
		endif 
	endif

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} vldJson

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/


Method vldJson() class PLSBeneficiaryFirstAccessService

	if (::oRest <> nil)
		if empty(::oRest['cpf'])
			self:setError(400,'0001',nil,"Atributo 'cpf' nao informado")
		endif

		if !GetNewPar("MV_PLEMAIL", .F.)
			self:setError(400,'0001',nil,"Parametro MV_PLEMAIL desativado, não será possivel enviar e-mail ")
		endif

		if !FindFunction( "PLSNotifica") .or. !FWAliasInDic("BQ7")
			self:setError(400,'0001',nil,"Sistema desatualizado ou sem a tabela BQ7 criada")
		endif		

		oPLMailMsg := PLMailMessage():New()
        If !oPLMailMsg:Connect()
			self:setError(400,'0001',nil,"Parametrização de envio de e-mail invalida.")
		endif
	else 
		self:setError(400,'0001',nil,"Informe o atributo 'cpf' no corpo da requisicao")
	endif 
	
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setError

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSBeneficiaryFirstAccessService

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:nStatus  := nStatus
    self:lSuccess := .F.

return

//-------------------------------------------------------------------
/*/{Protheus.doc} setResult

@author    Daniel Silva
@version   V12
@since     11/01/2024
/*/

method setResult(cMsg) class PLSBeneficiaryFirstAccessService

    self:oResult['code'] := 200
    self:oResult['message'] := 'success'
	self:oResult['detailedMessage'] := cMsg

return

//-------------------------------------------------------------------
/*/{Protheus.doc} verificaCpfMingle

@author    Guilherme Bonni
@version   12.1.2410
@since     30/05/2025
/*/
method verificaCpfMingle(cCpf) class PLSBeneficiaryFirstAccessService
	Local cRet	:= ""
	Local oJson := JsonObject():New()

	cCpf := strtran( cCpf, ".","" ) // retira os pontos
	cCpf := strtran( cCpf, "-","" ) // retira os traços

	oJson:= self:GetBeneficiarySQL(cCpf)
	
	If oJson["subscriberId"] != ""
		If !Empty(oJson["email"]) .And. IsEMail(AllTrim(oJson["email"]))

			lRet := PIncLogin(oJson["subscriberId"],,,,cCpf,oJson["email"],.T.,.T.)
			
			If ValType(lRet) == 'L'
				cRet := STR0001 //"Login criado com sucesso! As informações de acesso foram enviados para o e-mail cadastrado"
			ElseIf ValType(lRet) == 'C' 		
				cRet := lRet
			EndIf	
		Else
			cRet := STR0002 //"Não existe e-mail cadastrado ou está incorreto no Beneficiário, entre em contato com a operadora"
		EndIf
	Else
		cRet := STR0003 //"Beneficiário não encontrado, entre em contato com a operadora"
	EndIf

Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} GetBeneficiarySQL

@author    Guilherme Bonni
@version   12.1.2410
@since     05/06/2025
/*/
method GetBeneficiarySQL(cCpf) class PLSBeneficiaryFirstAccessService
    Local cSql           := ""
    Local cAlias         := "BENEF"
    Local cSubscriberId  := ""
    Local cEmail         := ""
	Local cTypeUser      := ""
    Local oJson          := JsonObject():New()

    cSql += "SELECT BA1_CODINT, BA1_CODEMP, BA1_MATRIC, BA1_TIPREG, BA1_DIGITO, BA1_EMAIL, BA1_TIPUSU "
    cSql += "FROM " + RetSqlName("BA1") + " "
    cSql += "WHERE "
    cSql += "    BA1_FILIAL = '" + xFilial("BA1") + "' "
    cSql += "AND BA1_CPFUSR = '" + cCpf + "' "
    cSql += "AND BA1_DATBLO = ' ' "
    cSql += "AND D_E_L_E_T_ = ' ' "

    dbUseArea(.T., "TOPCONN", TcGenQry(,,ChangeQuery(cSql)), cAlias, .F., .T.)

    oJson["subscriberId"] := ""
    oJson["email"]        := ""
	oJson["typeUser"]     := ""

    If !(cAlias)->(Eof())
        cSubscriberId := (cAlias)->BA1_CODINT + ;
                         (cAlias)->BA1_CODEMP + ;
                         (cAlias)->BA1_MATRIC + ;
                         (cAlias)->BA1_TIPREG + ;
                         (cAlias)->BA1_DIGITO

        cEmail 	  := AllTrim((cAlias)->BA1_EMAIL)
		cTypeUser := AllTrim((cAlias)->BA1_TIPUSU)

        oJson["subscriberId"] := cSubscriberId
        oJson["email"]        := cEmail
		oJson["typeUser"]     := cTypeUser
    EndIf

    (cAlias)->(DbCloseArea())

Return oJson
