#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiAuthorizationsContingency
.
@author  Sakai
@version P12
@since   12/09/2025
/*/
@Post("/totvshealthplans/v1/authorizationsContingency")
function HealthApiAuthorizationsContingencyRequest()
    
    local oRequest := HealthApiAuthorizationsContingencyRequest():New(oRest)
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:procTISS()
    endif
    
    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiAuthorizationsContingencyRequest from CenRequest

    Data oSvc as Object
    Data oQueryParam as Object
    Data oTissBody as Object

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method procTISS()

EndClass

Method New(oRest, cSvcName) Class HealthApiAuthorizationsContingencyRequest
    
    _Super:New(oRest,cSvcName,.T.)

    self:oSvc := HealthApiAuthorizationsContingencyService():New(oRest)
    self:oQueryParam := JsonObject():New()
    self:oTissBody   := JsonObject():New()

Return self

Method destroy() Class HealthApiAuthorizationsContingencyRequest
Return _Super:destroy()

Method valida() Class HealthApiAuthorizationsContingencyRequest

    local cParseError  := ""
    local cFaultDesc   := ""
    local cFaultDetail := ""
    Local lVirgula     := .F.
    local lRet := .T.

    self:oQueryParam := oRest:getQueryRequest()
    cParseError := self:oTissBody:fromJson(oRest:GetBodyRequest())

    if !empty(cParseError)
        cFaultDesc   := "Não foi possível fazer o parse da mensagem."
        cFaultDetail := cParseError
        lRet := .F.
    else
        if empty(self:oQueryParam['tissTransaction'])
            cFaultDesc   := "Atributos obrigatorios nao informados: "
            cFaultDetail += iif(lVirgula,",","")
            cFaultDetail += " authorizationType "
            lRet     := .F.
            lVirgula := .T.
        endif

        if empty(self:oQueryParam['tissVersion'])
            cFaultDesc   := "Atributos obrigatorios nao informados: "
            cFaultDetail += iif(lVirgula,",","")
            cFaultDetail += " authorizationType "
            lRet     := .F.
            lVirgula := .T.
        endif

        if empty(self:oTissBody['tissBody'])
            cFaultDesc   := "Atributos obrigatorios nao informados: "
            cFaultDetail += iif(lVirgula,",","")
            cFaultDetail += " tissBody "
            lRet     := .F.
            lVirgula := .T.
        endif
    endIf

    if !lRet
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := cFaultDesc
        self:cFaultDetail := cFaultDetail
        self:lSuccess     := .F.
    endIf

Return self:lSuccess

Method procTISS() Class HealthApiAuthorizationsContingencyRequest

    Local oResult := JsonObject():New()
    Local cRet  := ""
    
    self:oSvc:setTissVer(self:oQueryParam['tissVersion'])
    cRet := self:oSvc:procTISS(self:oTissBody['tissBody'],self:oQueryParam['tissTransaction'])

    oResult['tissResponse'] := cRet     
    self:cResponse := oResult:toJson()
    self:nStatus   := 200

Return
