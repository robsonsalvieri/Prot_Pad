#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

Class HealthApiAuthorizationsContingencyService

    Data oRest as Object
    Data cTissVersion as Character

    public method New(oRest,cSvcName) Constructor
    public method setTissVer(cTissVersion)
    public method procTISS()

EndClass

Method New(oRest) Class HealthApiAuthorizationsContingencyService
    self:oRest := oRest
    self:cTissVersion := ""
Return self

method setTissVer(cTissVersion) Class HealthApiAuthorizationsContingencyService
    self:cTissVersion := 'V'+StrTran(cTissVersion,'.','_')
return

method procTISS(cXml,cTissTran) Class HealthApiAuthorizationsContingencyService

    local oTiss := nil
    Local cRet := ""
    local aRetObj := {}

    if Upper(cTissTran) == 'WSSOLICITAPROCEDIMENTO'
        cRet := ProcOnLine("autorizacaoProcedimentoWS","autorizacaoServico",.T.,cXml,'TISSSOLICITACAOPROCEDIMENTO'+self:cTissVersion)
    
    elseIf Upper(cTissTran) == 'WSLOTEANEXO'
        
        oTiss := PLSSvcLoteAnexo():New()
        aRetObj := VldWSLoteG(cXml,"tissWebServices" + self:cTissVersion + ".xsd",'LOTEANEXOWS' )
        
        oTiss:cVersao  := StrTran(self:cTissVersion,'_','.')
        oTiss:lSuccess := aRetObj[1]
        oTiss:cError   := aRetObj[2]
        oTiss:cXml     := aRetObj[3]
        oTiss:cNS      := aRetObj[4]

        if oTiss:lSuccess
            cRet += '<?xml version="1.0" encoding="UTF-8"?>' + Chr(10)
            cRet += '<soap:Envelope xmlns:soap="http://schemas.xmlsoap.org/soap/envelope/" xmlns:ans="http://www.ans.gov.br/padroes/tiss/schemas" >' + Chr(10)
            cRet += '<soap:Body>' + chr(10)
            cRet += '<'+oTiss:cNS+':protocoloRecebimentoAnexoWS xmlns:'+oTiss:cNS+'="http://www.ans.gov.br/padroes/tiss/schemas">' + chr(10)
            cRet += oTiss:processa() + chr(10)
            cRet += '</'+oTiss:cNS+':protocoloRecebimentoAnexoWS>' + chr(10)
            cRet += '</soap:Body>' + chr(10)
            cRet += '</soap:Envelope>'
        endIf
    endif

Return cRet
