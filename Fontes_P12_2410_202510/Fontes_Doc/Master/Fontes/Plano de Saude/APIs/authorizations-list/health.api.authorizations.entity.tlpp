#include "TOTVS.CH"

#define CRLF chr( 13 ) + chr( 10 )

/*/{Protheus.doc} 
    Classe concreta da Entidade HealthApiAuthorizationsEntity - Other RemunerationAPI
@type  Class
    @author FrameworkApi 1.0
    @since 20190211
/*/
Class HealthApiAuthorizationsEntity from CenEntity

    public Method New()

    public Method serialize(oJsonControl)
    public Method destroy()
    public Method getStatus()
    public Method getTipGui()
    public Method getDesTipGui()
    public Method getVinculadas()
    public Method getStatusTAE()
    
EndClass

Method New() Class HealthApiAuthorizationsEntity
	_Super:New()
Return self

Method serialize(oJsonControl) Class HealthApiAuthorizationsEntity

	local oJson         := JsonObject():New()
	local cDtPrvAlt     := ''
	local cStatusTAE    := self:getStatusTAE()
    local lOculSer      := GetNewPar("MV_PLDTSER", .f.)
	default oJsonControl := CenJsonControl():New()

    oJsonControl:setProp(oJson,"authorizationStatus",               self:getStatus(self:getValue("authorizationStatus"),self:getValue("canceled")))
    oJsonControl:setProp(oJson,"idOnHealthInsurer",                 self:getValue("idOnHealthInsurer"))
    oJsonControl:setProp(oJson,"subscriberId",                      self:getValue("subscriberId"))
    oJsonControl:setProp(oJson,"authorizationType",                 self:getTipGui(self:getValue("authorizationType")))
    oJsonControl:setProp(oJson,"authorizationDescription",          self:getDesTipGui(self:getValue("authorizationType"),self:getValue("priorAuthorization")))
    oJsonControl:setProp(oJson,"beneficiaryName",                   self:getValue("beneficiaryName"))
    oJsonControl:setProp(oJson,"beneficiarySocialName",             self:getValue("beneficiarySocialName"))
    oJsonControl:setProp(oJson,"authorizationDate",                 FWDateTo8601(stod(self:getValue("authorizationDate"))))
    oJsonControl:setProp(oJson,"passwordExpireDate",                FWDateTo8601(stod(self:getValue("passwordExpireDate"))))
    oJsonControl:setProp(oJson,"requestDate",                       FWDateTo8601(stod(self:getValue("requestDate"))))
    oJsonControl:setProp(oJson,"priorAuthorization",                iif(self:getValue("priorAuthorization")=='1',.t.,.f.))
    oJsonControl:setProp(oJson,"password",                          self:getValue("password"))
    oJsonControl:setProp(oJson,"hasClinicalAttachment",             self:getVinculadas("B4A"))
    oJsonControl:setProp(oJson,"hasTreatmentExtension",             self:getVinculadas("B4Q"))
    oJsonControl:setProp(oJson,"hasInitialSituation",               self:getVinculadas("BEC"))

    oJsonControl:setProp(oJson,"dailyAuthorizedQuantity",           self:getValue("dailyAuthorizedQuantity"))
    oJsonControl:setProp(oJson,"authorizedHospital",                self:getValue("authorizedHospital"))
    oJsonControl:setProp(oJson,"expectedHospitalizationDate",       FWDateTo8601(stod(self:getValue("expectedHospitalizationDate"))))
    oJsonControl:setProp(oJson,"hospitalizationDate",               FWDateTo8601(stod(self:getValue("hospitalizationDate"))))
    oJsonControl:setProp(oJson,"hospitalizationHour",               self:getValue("hospitalizationHour"))
    oJsonControl:setProp(oJson,"dischargedDate",                    FWDateTo8601(stod(self:getValue("dischargedDate"))))
    oJsonControl:setProp(oJson,"dischargedHour",                    self:getValue("dischargedHour"))
    if self:getValue("authorizationType") == '03'
        oJsonControl:setProp(oJson,"passwordExpireDate",            FWDateTo8601(stod(self:getValue("hospitalizationExpireDate"))))
        oJsonControl:setProp(oJson,"password",                      self:getValue("hospitalizationPassword"))
    endif
    if lOculSer .and. self:getValue("serialTreatment") == 1
        oJsonControl:setProp(oJson,"passwordExpireDate",            '')
    endif
    if !empty(self:getValue("hospitalizationDate"))
        cDtPrvAlt := FWDateTo8601(stod(self:getValue("hospitalizationDate"))+self:getValue("dailyAuthorizedQuantity"))
    endif
    oJsonControl:setProp(oJson,"expectedDischargedDate",            cDtPrvAlt)
    oJsonControl:setProp(oJson,"signature",                         cStatusTAE)
    oJsonControl:setProp(oJson,"batchStatus",                       iif(!empty(self:getValue("batchStatusBD5")), self:getValue("batchStatusBD5"), self:getValue("batchStatusBE4")))

return oJson
//------------------------------------------------------------------------------------------
/*/{Protheus.doc} destroy

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method destroy() Class HealthApiAuthorizationsEntity
	_Super:destroy()
	DelClassIntF()
Return

/*/{Protheus.doc} getStatus

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getStatus(cStatus,cCancel) Class HealthApiAuthorizationsEntity
local cRet := ''

if cCancel == '1'
    cRet := '9'
else
    cRet := cStatus
endif

Return cRet

/*/{Protheus.doc} getTipGui

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getTipGui(cTipGui) Class HealthApiAuthorizationsEntity

if cTipGui == '01'
    cTipGui := '1'
elseif cTipGui == '02'
    cTipGui := '2'
elseif cTipGui == '03'
    cTipGui := '4'
elseif cTipGui == '13'
    cTipGui := '9'
endif

Return cTipGui

/*/{Protheus.doc} getDesTipGui

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getDesTipGui(cTipGui,cLibera) Class HealthApiAuthorizationsEntity
local cJornada := ''

do case
    case cTipGui == '01'
    	cJornada := "Consulta"
    case cTipGui == '13' .and. cLibera == '1'
    	cJornada := "Tratamento odontológico - Liberação"
    case cTipGui == '13' .and. cLibera <> '1'
    	cJornada := "Tratamento odontológico - Execução"
    case cTipGui == '03'
    	cJornada := "Solicitação de Internação"
    case cTipGui == '02' .and. cLibera == '1'
    	cJornada := "Exame - Liberação"
    case cTipGui == '02' .and. cLibera <> '1'
    	cJornada := "Exame - Execução"
end case

Return cJornada

/*/{Protheus.doc} getVinculadas

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getVinculadas(cAlias) Class HealthApiAuthorizationsEntity
local lRet := .f.
//4-B4A_FILIAL+B4A_GUIREF
//4-B4Q_FILIAL+B4Q_GUIREF
//2-BEC_FILIAL+BEC_CODRDA+BEC_GUIPRI

if cAlias == 'BEC'
    (cAlias)->(dbsetorder(2))
    lRet := (cAlias)->(msseek(xfilial(cAlias)+alltrim(self:getValue("healthProviderId"))+alltrim(self:getValue("idOnHealthInsurer"))))
else
    (cAlias)->(dbsetorder(4))
    lRet := (cAlias)->(msseek(xfilial(cAlias)+alltrim(self:getValue("idOnHealthInsurer"))))
endif

return lRet

/*/{Protheus.doc} getStatusTAE

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getStatusTAE() Class HealthApiAuthorizationsEntity
local cGrvTAE   := GetNewPar("MV_PLTPTAE", '')
local cRet      := ''

if !empty(cGrvTAE)
    cRet := PLSA203STA(self:getValue("idOnHealthInsurer"))
endif

return cRet
