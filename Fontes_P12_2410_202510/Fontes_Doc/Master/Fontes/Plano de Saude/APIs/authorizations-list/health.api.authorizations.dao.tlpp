#INCLUDE "TOTVS.CH"

#Define DBFIELD 1
#Define JSONFIELD 2

Class HealthApiAuthorizationsDao from CenDao

    public method New(aFields) Constructor
    public method get(cCodRda)
    public method hasNext(nRecno)
    public method loadOrder()
    public method getWhere()

EndClass

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Construtor

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method New(aFields) Class HealthApiAuthorizationsDao
	_Super:New(aFields)
    self:cfieldOrder := "BEA.R_E_C_N_O_ DESC"
    self:cAlias      := "BEAQRY"  
    self:loadOrder()    
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} get
Query que retorna todos os customers

@author    Lucas Nonato
@version   V12
@since     01/03/2021
/*/
Method get(jRequest) Class HealthApiAuthorizationsDao

local cSql      := ""
local cGrvTAE   := GetNewPar("MV_PLTPTAE", '')
local lOculSer  := GetNewPar("MV_PLDTSER", .f.)
local dDataLim  := Ctod(GetNewPar("MV_PLDTFAT", ""))
local cSubstr   := iif(upper(TCGetDB()) $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING") 
local lPendMsg  := !empty(cvaltochar(jRequest['filterOnlyPendentMessenger']))

if lPendMsg 
    self:cAlias := "B53QRY"
endif 

cSql += self:getRowControl()
                                                              
cSql += " BEA_OPEMOV,BEA_ANOAUT,BEA_MESAUT,BEA_NUMAUT,BEA_OPEUSR,BEA_CODEMP,BEA_MATRIC,BEA_TIPREG,BEA_DIGITO,BEA_DATSOL, "
cSql += " BEA_STATUS,BEA_TIPGUI,BEA_NOMUSR,BEA_NOMSOC,BEA_VALSEN,BEA_DATPRO,BEA_CANCEL,BEA_LIBERA,BEA_CODRDA,BEA_SENHA,  "
cSql += " BE4_DIASIN,BE4_CODRDA,BE4_PRVINT,BE4_DTALTA,BE4_HRALTA,BE4_DATPRO,BE4_HORPRO,BE4_SENHA,BE4_DATVAL,BE4_FASE, "
cSql += " BD5_FASE "
if lOculSer
    cSql += " , CASE "
    cSql += " WHEN EXISTS ( "
    cSql += "     SELECT 1 "
    cSql += "     FROM " + RetSqlName("BE2") + " BE2"
    cSql += "     INNER JOIN " + RetSqlName("BR8") + " BR8"
    cSql += "     ON BR8_FILIAL = '"  + xFilial("BR8") + "' "
    cSql += "     	AND BR8_CODPAD = BE2_CODPAD "
    cSql += "     	AND BR8_CODPSA = BE2_CODPRO "
    cSql += "     	AND BR8.D_E_L_E_T_ = ' ' "
    cSql += "     INNER JOIN " + RetSqlName("BJE") + " BJE"
    cSql += "     ON BJE_FILIAL = '"  + xFilial("BJE") + "' "
    cSql += "     	AND BJE_CODIGO = BR8_CLASSE "
    cSql += "     	AND BJE_TIPO = '2' "
    cSql += "     	AND BJE.D_E_L_E_T_ = ' ' "
    cSql += "     WHERE BE2_FILIAL = '"  + xFilial("BE2")+ "' "
    cSql += "       AND BE2_OPEMOV = BEA_OPEMOV "
    cSql += "       AND BE2_ANOAUT = BEA_ANOAUT "
    cSql += "       AND BE2_MESAUT = BEA_MESAUT "
    cSql += "       AND BE2_NUMAUT = BEA_NUMAUT "
    cSql += "       AND BE2.D_E_L_E_T_ = ' ' "
    cSql += " ) THEN 1 "
    cSql += "   ELSE 0 "
    cSql += " END AS TEMBJE "
else
    cSql += " , 0 AS TEMBJE "
endif

cSql += " FROM " + iif(lPendMsg, RetSqlName('B53') + ' B53 ', RetSqlName('BEA') + ' BEA ')

if lPendMsg 
    cSql += " LEFT JOIN " + RetSqlName("BEA") + " BEA"
    cSql += " ON BEA_FILIAL = '"  + xFilial("BEA")+ "' "
    cSql += " AND " + cSubstr + "(B53_NUMGUI, 1, 4) = BEA_OPEMOV "
    cSql += " AND " + cSubstr + "(B53_NUMGUI, 5, 4) = BEA_ANOAUT "
    cSql += " AND " + cSubstr + "(B53_NUMGUI, 9, 2) = BEA_MESAUT "
    cSql += " AND " + cSubstr + "(B53_NUMGUI, 11, 8) = BEA_NUMAUT "
    cSql += " AND BEA_OPERDA = '" + plsintpad() + "' "
    cSql += " AND BEA_TIPGUI NOT IN ('11') "
    cSql += " AND BEA_CODRDA = '" + jRequest['healthProvider'] + "' "
    cSql += " AND BEA.D_E_L_E_T_ = ' ' "
endif 

cSql += " LEFT JOIN " + RetSqlName("BE4") + " BE4"
cSql += " ON BE4_FILIAL = '"  + xFilial("BE4")+ "' "
cSql += " AND BEA_OPEMOV = BE4_CODOPE "
cSql += " AND BEA_ANOAUT = BE4_ANOINT "
cSql += " AND BEA_MESAUT = BE4_MESINT "
cSql += " AND BEA_NUMAUT = BE4_NUMINT "
cSql += iif(lPendMsg, " AND BE4_CODRDA = '" + jRequest['healthProvider'] + "' ", "")
cSql += " AND BE4.D_E_L_E_T_ = ' ' "
cSql += " LEFT JOIN " + RetSqlName("BCI") + " BCI"
cSql += " ON BCI_FILIAL = '"  + xFilial("BCI") + "' "
cSql += " 	AND BEA.BEA_CODPEG = BCI.BCI_CODPEG "
cSql += " 	AND BCI.D_E_L_E_T_ = ' ' "
cSql += " LEFT JOIN " + RetSqlName("BD5") + " BD5"
cSql += " ON BD5_FILIAL = '"  + xFilial("BD5") + "' "
cSql += " 	AND BEA_OPEMOV = BD5_OPEMOV "
cSql += " 	AND BEA_ANOAUT = BD5_ANOAUT "
cSql += " 	AND BEA_MESAUT = BD5_MESAUT "
cSql += " 	AND BEA_NUMAUT = BD5_NUMAUT "
cSql += " 	AND BD5.D_E_L_E_T_ = ' ' "

if !empty(cvaltochar(jRequest['billingHAT'])) .and. !empty(cGrvTAE)
    cSql += " LEFT JOIN ( "
    cSql += "     SELECT BQ6.BQ6_NUMGUI, BQ6.BQ6_STATUS "
    cSql += "     FROM " + RetSqlName("BQ6") + " BQ6"
    cSql += "     WHERE BQ6.BQ6_FILIAL = '"  + xFilial("BQ6")+ "' "
    cSql += "     AND BQ6.BQ6_TPSIGN = '1' "
    cSql += "     AND BQ6.D_E_L_E_T_ = ' ' "
    cSql += " ) BQ6_FILTERED ON "
    cSql += "     BEA.BEA_OPEMOV = "+cSubstr+"(BQ6_FILTERED.BQ6_NUMGUI, 1, 4) "
    cSql += " AND BEA.BEA_ANOAUT = "+cSubstr+"(BQ6_FILTERED.BQ6_NUMGUI, 5, 4) "
    cSql += " AND BEA.BEA_MESAUT = "+cSubstr+"(BQ6_FILTERED.BQ6_NUMGUI, 9, 2) "
    cSql += " AND BEA.BEA_NUMAUT = "+cSubstr+"(BQ6_FILTERED.BQ6_NUMGUI, 11, 8) "
endif

if lPendMsg    
    cSql += " LEFT JOIN ( "
    cSql += " SELECT GUIAREF FROM ( "
    cSql += " SELECT B4A_GUIREF AS GUIAREF FROM " + RetSqlName("B53") + " B53 "
	cSql += "   INNER JOIN " + RetSqlName("B4A") + " B4A "
	cSql += "       ON B4A_FILIAL = '" + xFilial('B4A') + "' "
	cSql += "       AND B4A_OPEMOV = "+cSubstr+"(B53_NUMGUI, 1, 4) "
    cSql += "       AND B4A_ANOAUT = "+cSubstr+"(B53_NUMGUI, 5, 4) "
    cSql += "       AND B4A_MESAUT = "+cSubstr+"(B53_NUMGUI, 9, 2) "
    cSql += "       AND B4A_NUMAUT = "+cSubstr+"(B53_NUMGUI, 11, 8) "
    cSql += "       AND B4A.D_E_L_E_T_ = ' ' "
	cSql += " 	WHERE B53_FILIAL = '" + xFilial('B53') + "' "
	cSql += " 	    AND B53_ALIMOV NOT IN ('BEA','BE4') "
	cSql += " 	    AND B53_MSGSTA = '2' "
    cSql += " 		AND B53.D_E_L_E_T_ = ' ' "
	
    cSql += " UNION ALL "
    cSql += " SELECT B4Q_GUIREF AS GUIAREF FROM " + RetSqlName("B53") + " B53 "
	cSql += "   INNER JOIN " + RetSqlName("B4Q") + " B4Q "
	cSql += "       ON B4Q_FILIAL = '" + xFilial('B4Q') + "' "
	cSql += "       AND B4Q_OPEMOV = "+cSubstr+"(B53_NUMGUI, 1, 4) "
    cSql += "       AND B4Q_ANOAUT = "+cSubstr+"(B53_NUMGUI, 5, 4) "
    cSql += "       AND B4Q_MESAUT = "+cSubstr+"(B53_NUMGUI, 9, 2) "
    cSql += "       AND B4Q_NUMAUT = "+cSubstr+"(B53_NUMGUI, 11, 8) "
    cSql += "       AND B4Q.D_E_L_E_T_ = ' ' "
	cSql += " 	WHERE B53_FILIAL = '" + xFilial('B53') + "' "
	cSql += " 	    AND B53_ALIMOV NOT IN ('BEA','BE4') "
	cSql += " 	    AND B53_MSGSTA = '2' "
    cSql += " 		AND B53.D_E_L_E_T_ = ' ' "
    cSql += " ) AS QRYAUX "
    cSql += " GROUP BY GUIAREF "

    cSql += " ) AS GUIAREFERENCIA "
    cSql += "   ON BEA_FILIAL = '" + xFilial('BEA') + "' "
    cSql += "   AND BEA_OPEMOV = "+cSubstr+"(GUIAREFERENCIA.GUIAREF, 1, 4) "
    cSql += "   AND BEA_ANOAUT = "+cSubstr+"(GUIAREFERENCIA.GUIAREF, 5, 4) "
    cSql += "   AND BEA_MESAUT = "+cSubstr+"(GUIAREFERENCIA.GUIAREF, 9, 2) "
    cSql += "   AND BEA_NUMAUT = "+cSubstr+"(GUIAREFERENCIA.GUIAREF, 11, 8) "
endIf

if lPendMsg
    cSql += " WHERE B53_FILIAL = '" + xFilial('B53') + "' "
    cSql += " AND B53_MSGSTA = '2' "
    cSql += " AND B53_CODRDA = '" + jRequest['healthProvider'] + "' "
    cSql += " AND B53.D_E_L_E_T_ = ' ' "
else 
    cSql += " WHERE BEA_FILIAL = '" + xFilial('BEA') + "' "
    cSql += " AND BEA_OPERDA = '" + plsintpad() + "' "
    cSql += " AND (BEA_CODRDA = '" + jRequest['healthProvider'] + "' "
    cSql += " OR BE4_RDACON = '" + jRequest['healthProvider'] + "') "
    cSql += " AND BEA_TIPGUI NOT IN ('11') "
    cSql += " AND BEA.D_E_L_E_T_ = ' ' "
endif 

if !empty(cvaltochar(jRequest['billingHAT'])) .and. !empty(cGrvTAE)
    cSql += " AND (BQ6_FILTERED.BQ6_NUMGUI IS NULL OR BQ6_FILTERED.BQ6_STATUS = '2') "
endif
cSql += iif(lPendMsg,'',self:getWhere(jRequest))

if !empty(cvaltochar(jRequest['procPegTransfer'])) .And. cvaltochar(jRequest['procPegTransfer']) == "1"
    cSql += " AND BCI.BCI_LOTHAT = '1' "
elseIf !empty(cvaltochar(jRequest['billingHAT'])) .And. cvaltochar(jRequest['billingHAT']) == "1"
    cSql += " AND BCI.BCI_LOTHAT <> '1' "
endIf

//Criacao do parametro MV_PLDTFAT, para ser a data de inicio de busca de guias no faturamento
if !empty(dDataLim) .AND. !empty(cvaltochar(jRequest['billingHAT'])) .And. cvaltochar(jRequest['billingHAT']) == "1" .AND. empty(cvaltochar(jRequest['batchCode']))
    cSql += " AND BEA_DATPRO >= '" + DTOS(dDataLim) + "' "
endif

cSql += self:getWhereRow()

self:setQuery(self:queryBuilder(cSql))
lFound := self:executaQuery()
return lFound

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} getWhere

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method getWhere(jRequest) Class HealthApiAuthorizationsDao
local cTipGui       := ''
local cStatus       := ''
local cCancel       := ''
local cSql          := ''
local nX            := 1
local aParam        := 1

if !empty(cvaltochar(jRequest['authorizationStatus']))
    aParam := Strtokarr2( jRequest['authorizationStatus'],',')
    for nX:=1 to len(aParam)
        if aParam[nX] == 'C'
            cCancel = '1'
        else
            cStatus += "'"+aParam[nX]+"',"
        endif
    next

    cSql += ' AND ( ' 
    cSql += iif(!empty(cStatus), 'BEA_STATUS IN (' +substr(cStatus,1,len(cStatus)-1)+ ") ", "")
    cSql += iif(!empty(cStatus) .and. !empty(cCancel), ' OR ', '')
    cSql += iif(!empty(cCancel), "BEA_CANCEL = '1' ", "")
    cSql += ' ) '
endif

if !empty(cvaltochar(jRequest['authorizationType']))
    aParam := Strtokarr2( jRequest['authorizationType'],',')
    oObj := PLSProcAuthDao():new('',nil)
    nX := 1 
    for nX:=1 to len(aParam)
        cTipGui += "'"+iif(aParam[nX]=='9','13',oObj:guideType(aParam[nX]))+"',"
    next
     cSql += ' AND BEA_TIPGUI IN (' +substr(cTipGui,1,len(cTipGui)-1)+ ")"
endif

if !empty(cvaltochar(jRequest['idOnHealthInsurer']))
    cSql += " AND BEA_OPEMOV = '"+substr(jRequest['idOnHealthInsurer'],1,4)+"' "
	cSql += " AND BEA_ANOAUT = '"+substr(jRequest['idOnHealthInsurer'],5,4)+"' "
	cSql += " AND BEA_MESAUT = '"+substr(jRequest['idOnHealthInsurer'],9,2)+"' "
	cSql += " AND BEA_NUMAUT = '"+substr(jRequest['idOnHealthInsurer'],11,8)+"' "
endif

if !empty(cvaltochar(jRequest['subscriberId']))
    cSql += "   AND BEA_OPEUSR = '" + substr(jRequest['subscriberId'],1,4 ) + "' "
	cSql += "   AND BEA_CODEMP = '" + substr(jRequest['subscriberId'],5,4 ) + "' "
	cSql += "   AND BEA_MATRIC = '" + substr(jRequest['subscriberId'],9,6 ) + "' "
	cSql += "   AND BEA_TIPREG = '" + substr(jRequest['subscriberId'],15,2) + "' "
	cSql += "   AND BEA_DIGITO = '" + substr(jRequest['subscriberId'],17,1) + "' "
endif

if !empty(cvaltochar(jRequest['dateFrom']))
    cSql += " AND BEA_DATPRO >= '" + strtran(cvaltochar(jRequest['dateFrom']),'-','') + "' "
endif

if !empty(cvaltochar(jRequest['dateTo']))
    cSql += " AND BEA_DATPRO <= '" + strtran(cvaltochar(jRequest['dateTo']),'-','') + "' "
endif

if !empty(cvaltochar(jRequest['locationCode']))
    cSql += " AND BEA_CODLOC = '" + jRequest['locationCode'] + "' "
endif

if !empty(cvaltochar(jRequest['attendanceLocation']))
    cSql += " AND BEA_LOCAL = '" + jRequest['attendanceLocation'] + "' "
endif

if !empty(cvaltochar(jRequest['password']))
    cSql += " AND (BEA_SENHA = '" + jRequest['password'] + "' OR  BE4_SENHA = '" + jRequest['password'] + "') "
endif

if !empty(cvaltochar(jRequest['beneficiaryCPF']))
    cSql += " AND BEA_CPFUSR = '" + strtran(strtran(cvaltochar(jRequest['beneficiaryCPF']),'.',''),'-','') + "' "
endif

if !empty(cvaltochar(jRequest['hospitalizationDate']))
    cSql += " AND BE4_DATPRO = '" + strtran(cvaltochar(jRequest['hospitalizationDate']),'-','') + "' "
endif

if !empty(cvaltochar(jRequest['dischargedDate']))
    cSql += " AND BE4_DTALTA = '" + strtran(cvaltochar(jRequest['dischargedDate']),'-','') + "' "
endif

if !empty(cvaltochar(jRequest['billingHAT']))
    cSql += " AND BEA_CODPEG <> ' ' "
    cSql += " AND BEA_LIBERA <> '1' "
    if !empty(cvaltochar(jRequest['batchCode']))
        cSql += " AND BEA_LOTHAT = '" + jRequest['batchCode'] + "' "
    else 
        cSql += " AND BEA_LOTHAT = ' ' "
    endif
endif

Return cSql


//------------------------------------------------------------------------------------------
/*/{Protheus.doc} hasNext
    hasNext especifico

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method hasNext(nRecno) Class HealthApiAuthorizationsDao
    Local lTemProx := .F.
    If self:aliasSelected()
        lTemProx := !(self:getAliasTemp())->(Eof())
    EndIf
return lTemProx

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} loadOrder
    Adicona campos para ordenacao

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method loadOrder() Class HealthApiAuthorizationsDao

    self:oHashOrder:set("idOnHealthInsurer",   "BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT")

Return
