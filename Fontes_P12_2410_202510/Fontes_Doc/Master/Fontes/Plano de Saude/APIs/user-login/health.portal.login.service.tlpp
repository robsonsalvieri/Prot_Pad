#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"
#include "PLSMGER.CH"
#include "Fileio.ch"
#include 'Protheus.ch'

class HealthPortalLoginService from CenRequest

    Data lPortalOperadora as logical

	Public Method new()
	Public Method valida()
	Public Method getUserLogin()
	Public Method changePassword()
	Public Method checkPassword()
	Public Method checkParameters()
	Public Method getCustomStyle()

	private method setContracts(cUserCode as character)

endClass

Method New(oRest) class HealthPortalLoginService
	_Super:new(oRest,,.T.)
    self:lPortalOperadora := .F.
Return self

//nTp 1-getUserLogin;2-changePassword
Method valida(nTp) class HealthPortalLoginService
	local cFields   := ""
	default nTp     := 1
	self:lSuccess := _Super:checkBody(.t.)
	BSW->(dbsetorder(1))

    self:lPortalOperadora := iif(empty(cvaltochar(self:jRequest['healthInsurerPortal'])) .OR. self:jRequest['healthInsurerPortal'] == "false", .F., .T.) 

	if self:lSuccess .and. empty(cvaltochar(self:jRequest['login']))
		cFields += iif(!empty(cFields),',','')+"'login'"
	endif
	if self:lSuccess .and. nTp == 1 .and. empty(cvaltochar(self:jRequest['password']))
		cFields += iif(!empty(cFields),',','')+"'password'"
	endif
	if self:lSuccess .and. nTp == 2 .and. empty(cvaltochar(self:jRequest['email']))
		cFields += iif(!empty(cFields),',','')+"'email'"
	endif
	if self:lSuccess .and. nTp == 2 .and. empty(cvaltochar(self:jRequest['newpassword']))
		cFields += iif(!empty(cFields),',','')+"'newpassword'"
	endif
	if self:lSuccess .and. BSW->(fieldpos("BSW_PERAC2")) <= 0
		self:lSuccess := .f.
		self:cFaultDesc   := "Campo obrigatório não existe no ambiente BSW_PERAC2"
	endif

	if !empty(cFields)
		self:lSuccess := .f.
		self:cFaultDesc   := "Campos obrigatórios não informados:" + cFields
	endif
Return self:lSuccess

Method getUserLogin() class HealthPortalLoginService
	local cSql      := ""
	local cSubstrOperatorSQL := iif(upper(TCGetDB()) $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING")
	local nX        := 1
	local cValid	:= ""

	cSql := " SELECT BSW_TPPOR,BSW_CODUSR,BSW_LOGUSR,BSW_EMAIL,BSW_NOMUSR,BSW_PERAC2,BSW_PERACE,BSW_SENHA "
	cSql += " FROM " + retsqlname("BSW")
	cSql += " WHERE BSW_FILIAL = '" + xFilial("BSW") + "' "
	cSql += " AND UPPER(BSW_LOGUSR) = '" + upper(alltrim(cvaltochar(self:jRequest['login']))) + "'  "
	cSql += " AND D_E_L_E_T_ = ' ' "
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSql),"TrbBSW",.T.,.F.)

	self:oRespBody :=  jsonObject():new()
	self:oRespBody["loginDetails"]  := jsonObject():new()

	if !TrbBSW->(eof()) .and. self:checkPassword(TrbBSW->BSW_SENHA)

		self:oRespBody["loginDetails"]["code"] := alltrim(TrbBSW->BSW_CODUSR)
		self:oRespBody["loginDetails"]["username"] := alltrim(TrbBSW->BSW_LOGUSR)
		self:oRespBody["loginDetails"]["email"] := alltrim(TrbBSW->BSW_EMAIL)
		self:oRespBody["loginDetails"]["name"] := alltrim(TrbBSW->BSW_NOMUSR)
		self:oRespBody["loginDetails"]["portalType"] := alltrim(TrbBSW->BSW_TPPOR)
		self:oRespBody["loginDetails"]["healthInsurerCode"] := plsIntPad()

		if TrbBSW->BSW_TPPOR == '3'
	        self:oRespBody["beneficiaries"] := {}
			cSql := " SELECT B49_BENEFI,BA1_NOMUSR,BA1_TIPUSU "
			cSql += " FROM " + retsqlname("B49") + " B49 "
			cSql += " INNER JOIN "+RetSqlName("BA1")+" BA1 "
			cSql += "   ON BA1.BA1_FILIAL = '"+xFilial("BA1")+"' "
			cSql += "  AND BA1.BA1_CODINT = "+cSubstrOperatorSQL+"(B49.B49_BENEFI, 1, 4) "
			cSql += "  AND BA1.BA1_CODEMP = "+cSubstrOperatorSQL+"(B49.B49_BENEFI, 5, 4) "
			cSql += "  AND BA1.BA1_MATRIC = "+cSubstrOperatorSQL+"(B49.B49_BENEFI, 9, 6) "
			cSql += "  AND BA1.BA1_TIPREG = "+cSubstrOperatorSQL+"(B49.B49_BENEFI, 15, 2) "
			cSql += "  AND BA1.BA1_DIGITO = "+cSubstrOperatorSQL+"(B49.B49_BENEFI, 17, 1) "
			cSql += "  AND BA1.D_E_L_E_T_ = ' ' "
			cSql += " WHERE B49_FILIAL = '" + xFilial("B49") + "' "
			cSql += " AND B49_CODUSR = '" + upper(alltrim(TrbBSW->BSW_CODUSR)) + "'  "
			cSql += " AND B49.D_E_L_E_T_ = ' ' "
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSql),"TrbB49",.T.,.F.)
			while !TrbB49->(eof())
				aadd(self:oRespBody["beneficiaries"], jsonObject():new())
				self:oRespBody["beneficiaries"][nX]['subscriberId']     := alltrim(TrbB49->B49_BENEFI)
				self:oRespBody["beneficiaries"][nX]['name']             := alltrim(TrbB49->BA1_NOMUSR)
				self:oRespBody["beneficiaries"][nX]['subscriberType']   := alltrim(TrbB49->BA1_TIPUSU)
				TrbB49->(dbSkip())
				nX++
			enddo
			TrbB49->(dbCloseArea())
		elseif TrbBSW->BSW_TPPOR == '2'
			self:setContracts(alltrim(TrbBSW->BSW_CODUSR))
		endif

		cValid := iif(self:lPortalOperadora, !empty(TrbBSW->BSW_PERACE), !empty(TrbBSW->BSW_PERAC2))	

		cSql := " SELECT AI8_CODMNU,AI8_TEXTO,AI8_CODPAI,AI8_ROTINA "
		cSql += " FROM " + retsqlname("AI8") + " AI8 "
		if cValid
			cSql += " INNER JOIN "+RetSqlName("B7J")+" B7J "
			cSql += "  ON  B7J_FILIAL = '"+xFilial("B7J")+"' "
			cSql += "  AND B7J_CODPER = '"+ iif(self:lPortalOperadora, TrbBSW->BSW_PERACE, TrbBSW->BSW_PERAC2) +"' "
			cSql += "  AND B7J_CODMNU = AI8_CODMNU "
			cSql += "  AND B7J_PERACE = '1' "
			cSql += "  AND B7J.D_E_L_E_T_ = ' ' "
		endif
		cSql += " WHERE AI8_FILIAL = '" + xFilial("AI8") + "' "
        cSql += " AND AI8_PORTAL = '" + iif(self:lPortalOperadora, "000014", iif(TrbBSW->BSW_TPPOR == "2", "000015", "000013")) + "'  "
		cSQl += " AND AI8.D_E_L_E_T_ = ' '"
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSql),"TrbAI8",.T.,.F.)
		nX := 1
		self:oRespBody["menus"] := {}
		while !TrbAI8->(eof())
			aadd(self:oRespBody["menus"], jsonObject():new())
			self:oRespBody["menus"][nX]['name']     := alltrim(TrbAI8->AI8_TEXTO)
			self:oRespBody["menus"][nX]['module']   := alltrim(TrbAI8->AI8_ROTINA)
			TrbAI8->(dbSkip())
			nX++
		enddo
		TrbAI8->(dbCloseArea())

		self:cResponse := self:oRespBody:toJson()
	elseif self:lSuccess
		self:cFaultDesc   := "Login não encontrado"
	endif
	TrbBSW->(dbCloseArea())

	if !empty(self:cFaultDesc)
		self:lSuccess       := .f.
		self:nFault         := 400
		self:cFaultDesc     := iif(empty(self:cFaultDesc),"Nenhum registro localizado",self:cFaultDesc)
	endif
Return

Method changePassword() class HealthPortalLoginService
	local cSql        := ""
	local cSenhaCrip  := SHA256(self:jRequest['newpassword'])
	local lNotFound   := .F.
	local lUpdMingle  := iif(self:jRequest['updateMingle'] <> nil .AND. ValType(self:jRequest['updateMingle']) == 'L', self:jRequest['updateMingle'], .T.)
	local lUpdPLS     := .F.

	cSql := " SELECT BSW_CODUSR,BSW_LOGUSR,BSW_EMAIL,BSW_NOMUSR,BSW_SENHA, R_E_C_N_O_ Recno "
	cSql += " FROM " + retsqlname("BSW")
	cSql += " WHERE BSW_FILIAL = '" + xFilial("BSW") + "' "
	cSql += " AND BSW_LOGUSR = '" + upper(alltrim(cvaltochar(self:jRequest['login']))) + "'  "
	cSql += " AND BSW_EMAIL = '" + upper(alltrim(cvaltochar(self:jRequest['email']))) + "'  "
	cSql += " AND D_E_L_E_T_ = ' ' "
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSql),"TrbBSW",.T.,.F.)

	self:oRespBody :=  jsonObject():new()
	self:oRespBody["loginDetails"]  := jsonObject():new()
	if !TrbBSW->(eof())
		if self:checkParameters()
			if lUpdMingle
				if PLMINGCRUD(K_Alterar, Alltrim(TrbBSW->BSW_LOGUSR), cSenhaCrip, "3", .F.) //Sincronizacao da atualizacao de senha no Mingle
					lUpdPLS := .T.
				endif
			else
				lUpdPLS := .T.
			endif

			if lUpdPLS
				BSW->(dbgoto(TrbBSW->Recno))
				BSW->(Reclock("BSW",.F.))
				BSW->BSW_SENHA := cSenhaCrip
				BSW->(msUnlock())
				self:oRespBody["loginDetails"]["code"]      := alltrim(TrbBSW->BSW_CODUSR)
				self:oRespBody["loginDetails"]["username"]  := alltrim(TrbBSW->BSW_LOGUSR)
				self:oRespBody["loginDetails"]["email"]     := alltrim(TrbBSW->BSW_EMAIL)
				self:oRespBody["loginDetails"]["name"]      := alltrim(TrbBSW->BSW_NOMUSR)
				self:cResponse := self:oRespBody:toJson()
			endif
		else
			self:lSuccess       := .F.
			self:nFault         := 400
			self:cFaultDetail   := "Verifique se os parâmetros MV_MINGURL, MV_MINGIUS e MV_MINGTOK estão preenchidos!"
			self:cFaultDesc     := "Parâmetro(s) MV_MINGURL, MV_MINGIUS e/ou MV_MINGTOK não preenchido(s)"
		endif
	else
		lNotFound := .T.
	endif
	TrbBSW->(dbCloseArea())

	if empty(self:oRespBody["loginDetails"]["code"])
		self:lSuccess       := .F.
		self:nFault         := 400
		self:cFaultDetail   := "Não foi possível atualizar a senha. Tente novamente!" //Mensagem para erro no Mingle.
		self:cFaultDesc     := "Erro ao atualizar senha"
	endif

	if lNotFound
		self:lSuccess       := .f.
		self:nFault         := 400
		self:cFaultDetail   := "Nenhum registro localizado" //Mensagem para login nao encontrado
		self:cFaultDesc     := "Login/Email não encontrado"
	endif

Return

method checkPassword(cSenha) class HealthPortalLoginService
	if !(alltrim(cSenha) == alltrim(self:jRequest['password']) .or. alltrim(cSenha) == alltrim(SHA256(self:jRequest['password'])))
		self:lSuccess := .f.
		self:cFaultDesc := "Senha inválida"
	endif
return self:lSuccess

method checkParameters() class HealthPortalLoginService
return !Empty(GETNEWPAR("MV_MINGURL","")) .AND. !Empty(GETNEWPAR("MV_MINGIUS","")) .AND. !Empty(GETNEWPAR("MV_MINGTOK",""))

method getCustomStyle(jQueryParams as json) class HealthPortalLoginService

	local cBlob as character
	local nSize as numeric
	local nHandle as numeric
	local cDiretory := "portais-saude/portal-beneficiario/images/" as character
	local cDiretory2 := "portais-saude\portal-beneficiario\images" as character
	local aFiles as array
	local cFile as character
	local nAux as numeric
	local nLenArray as numeric

	self:oRespBody := JsonObject():new()

	self:oRespBody["theme"] := superGetMV("MV_PLTHEME", .F., "classic")

	if jQueryParams["portalType"] == "2"
		cDiretory := "portais-saude/portal-empresa/images/"
		cDiretory2 := "portais-saude\portal-empresa\images"
	elseif jQueryParams["portalType"] == "3"
	 	cDiretory := "portais-saude/portal-operadora/images/"
		cDiretory2 := "portais-saude\portal-operadora\images"
	endif

	aFiles := directory(cDiretory + "*.*", "D")
	nLenArray := len(aFiles)

	for nAux := 1 To nLenArray
		if alltrim(aFiles[nAux][1]) != '.' .and. alltrim(aFiles[nAux][1]) != '..'
			cFile := aFiles[nAux][1]
			nSize := aFiles[nAux][2]

			nHandle := fopen(cDiretory2 + '\' + upper(cFile) , FO_READWRITE + FO_SHARED )
			cString := ""

			fRead( nHandle, cString, nSize )
			cBlob := encode64(cString)
			cFile := substr(cFile, 1, at(".", cFile) - 1)

			do case
			case upper(cFile) == 'LOGO'
				self:oRespBody["logo"] := cBlob

			case upper(cFile) == 'SHORT-LOGO'
				self:oRespBody["shortLogo"] := cBlob

			case upper(cFile) == 'BANNER-LOGIN'
				self:oRespBody["bannerLogin"] := cBlob
			endcase

			fclose(nHandle)
		endif
	next nAux

	self:cResponse := self:oRespBody:toJson()

	fwFreeArray(aFiles)

return self:lSuccess

/*/{Protheus.doc} setContracts
Define os contratos associados a um usuário no portal da empresa
@type method
@version 12.1.2510
@author vinicius.queiros
@since 04/04/2025
@param cUserCode, character, Código do usuário para o qual os contratos serão definidos.
*/
method setContracts(cUserCode as character) class HealthPortalLoginService

	local cQuery as character
	local oExecStmt as object
	local nOrder := 1 as numeric
	local cAlias as character
	local jCompany as json

	self:oRespBody["contracts"] := {}

	cQuery := " SELECT ? "
	cQuery += " FROM ? B40 "
	cQuery += " INNER JOIN ? BG9 ON "
	cQuery += "		BG9.BG9_FILIAL = ? AND "
	cQuery += "		BG9.BG9_CODINT = B40.B40_CODINT AND "
	cQuery += "		BG9.BG9_CODIGO = B40.B40_CODEMP AND "
	cQuery += "		BG9.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN ? BQC ON "
	cQuery += "		BQC.BQC_FILIAL = ? AND "
	cQuery += "		BQC.BQC_CODIGO = B40.B40_CODINT || B40.B40_CODEMP AND "
	cQuery += "		BQC.BQC_NUMCON = B40.B40_NUMCON AND "
	cQuery += "		BQC.BQC_VERCON = B40.B40_VERCON AND "
	cQuery += "		BQC.BQC_SUBCON = B40.B40_SUBCON AND "
	cQuery += "		BQC.BQC_VERSUB = B40.B40_VERSUB AND "
	cQuery += "		BQC.D_E_L_E_T_ = ? "
	cQuery += " WHERE "
	cQuery += "		B40.B40_FILIAL = ? AND "
	cQuery += "		B40.B40_CODUSR = ? AND "
	cQuery += "		B40.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(changeQuery(cQuery))

	oExecStmt:setUnsafe(nOrder++, "B40.B40_CODINT, B40.B40_CODEMP, B40.B40_NUMCON, B40.B40_VERCON, B40.B40_SUBCON, B40.B40_VERSUB, BQC.BQC_DESCRI, BG9.BG9_DESCRI")
	oExecStmt:setUnsafe(nOrder++, retSqlName("B40"))
	oExecStmt:setUnsafe(nOrder++, retSqlName("BG9"))
	oExecStmt:setString(nOrder++, xFilial("BG9"))
	oExecStmt:setString(nOrder++, " ")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BQC"))
	oExecStmt:setString(nOrder++, xFilial("BQC"))
	oExecStmt:setString(nOrder++, " ")
	oExecStmt:setString(nOrder++, xFilial("B40"))
	oExecStmt:setString(nOrder++, cUserCode)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	while !(cAlias)->(eof())
		jCompany := JsonObject():new()

		jCompany["healthInsurerCode"] := alltrim((cAlias)->B40_CODINT)
		jCompany["companyCode"] := alltrim((cAlias)->B40_CODEMP)
		jCompany["companyDescription"] := alltrim((cAlias)->BG9_DESCRI)
		jCompany["contractCode"] := alltrim((cAlias)->B40_NUMCON)
		jCompany["contractVersion"] := alltrim((cAlias)->B40_VERCON)
		jCompany["subcontractCode"] := alltrim((cAlias)->B40_SUBCON)
		jCompany["subcontractVersion"] := alltrim((cAlias)->B40_VERSUB)
		jCompany["subcontractDescription"] := alltrim((cAlias)->BQC_DESCRI)
		jCompany["contractFullKey"] := (cAlias)->B40_CODINT + "|" + (cAlias)->B40_CODEMP + "|" + (cAlias)->B40_NUMCON + "|" +;
									   (cAlias)->B40_VERCON + "|" + (cAlias)->B40_SUBCON + "|" + (cAlias)->B40_VERSUB

		aAdd(self:oRespBody["contracts"], jCompany)
		(cAlias)->(dbSkip())
	enddo

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)
	freeObj(jCompany)

return
