#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiBeneficiaryTeethRequest
.
@author  Sakai
@version P12
@since   11/09/2024
//-------------------------------------------------------------------
/*/
@Get("/totvshealthplans/v1/beneficiaryTeeth/:id")
function HealthApiBeneficiaryTeethRequest()
    local oRequest := HealthApiBeneficiaryTeethRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:buscar()
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiBeneficiaryTeethRequest from CenRequest

    Data oSvc as Object
    
    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method buscar()

EndClass

Method New(oRest, cSvcName) Class HealthApiBeneficiaryTeethRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oSvc := HealthApiBeneficiaryTeethSvc():new(self:oRest:getPathParamsRequest()['id'])   
Return self

Method destroy() Class HealthApiBeneficiaryTeethRequest
Return _Super:destroy()

Method valida() Class HealthApiBeneficiaryTeethRequest

    Local oErrorAux := nil

    if !self:oSvc:validaBenef()
        oErrorAux   := self:oSvc:getError()
        self:lSuccess     := .F.
        self:nFault       := oErrorAux['status']
        self:nStatus      := oErrorAux['status']
        self:cFaultDesc   := oErrorAux['message']
        self:cFaultDetail := oErrorAux['detailedMessage']
    endIf

Return self:lSuccess

Method buscar() Class HealthApiBeneficiaryTeethRequest

    Local aRet := {}
    Local oErrorAux := nil

    if self:lSuccess
        aRet := self:oSvc:buscar()
        if aRet[1]
            self:cResponse := aRet[2]:toJson()
            self:nStatus   := aRet[3]
        else
            oErrorAux := self:oSvc:getError()
            self:lSuccess     := .F.
            self:nFault       := oErrorAux['status']
            self:nStatus      := oErrorAux['status']
            self:cFaultDesc   := oErrorAux['message']
            self:cFaultDetail := oErrorAux['detailedMessage']
        endIf
    endIf

Return self:lSuccess