#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

Class HealthApiBeneficiaryTeethSvc from CenRequest
	
	Data cMatric as Character
	Data oError as Object

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method validaBenef()
    public method buscar()
	public method getError()

	private method procJson()
	private method retCampos()
	private method setError(nStatus, cCode, cMessage, cDetailedMessage) 

EndClass

Method New(cMatric) Class HealthApiBeneficiaryTeethSvc
	self:oError  := JsonObject():new()
	self:cMatric := cMatric
Return self

Method validaBenef() Class HealthApiBeneficiaryTeethSvc
	
	Local lRet := .T.

	if len(self:cMatric) == 17 
		BA1->(DbSetOrder(2))//BA1_FILIAL+BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO
		if !BA1->(DbSeek(xFilial('BA1')+self:cMatric))
			lRet := .F.
			self:setError(400,;
				'0001',;
				'Matricula informada ' + self:cMatric + ' nao encontrada na tabela BA1.',;
				'Beneficiario nao encontrado')
		endIf
	elseIf len(self:cMatric) == 11
		BA1->(DbSetOrder(4))//BA1_FILIAL+BA1_CPFUSR
		if !BA1->(DbSeek(xFilial('BA1')+self:cMatric))
			lRet := .F.
			self:setError(400,;
				'0001',;
				'CPF informado ' + self:cMatric + ' nao encontrado na tabela BA1.',;
				'Beneficiario nao encontrado')
		endIf
	else
		lRet := .F.
		self:setError(400,;
			'0002',;
			'Tamanho da matricula informada ' + self:cMatric + ' diferente de 11 ou 17 caracteres',;
			'Matricula invalida')
	endIf

Return lRet

Method buscar() Class HealthApiBeneficiaryTeethSvc

	Local aRet := {}
	Local cSql := ''
	Local oJsonRet := nil

	DbSelectArea("BEC")

	cSql += " SELECT TOP 1 "+self:retCampos()+" FROM " + RetSqlName("BEC")+" BEC "
	cSql += " INNER JOIN "+RetSqlName("BEA")+" BEA "
	cSql += "	ON BEA_FILIAL = '"+xFilial("BEA")+"' "
	cSql += "	AND BEA_OPEMOV = SUBSTRING(BEC_GUIPRI,1,4) "
	cSql += "	AND BEA_ANOAUT = SUBSTRING(BEC_GUIPRI,5,4) "
	cSql += "	AND BEA_MESAUT = SUBSTRING(BEC_GUIPRI,9,2) "
	cSql += "	AND BEA_NUMAUT = SUBSTRING(BEC_GUIPRI,11,8) "
	if len(self:cMatric) == 17
		cSql += "   AND BEA_OPEUSR = '"+substr(self:cMatric,1,4)+"' "
		cSql += "   AND BEA_CODEMP = '"+substr(self:cMatric,5,4)+"' "
		cSql += "   AND BEA_MATRIC = '"+substr(self:cMatric,9,6)+"' "
		cSql += "   AND BEA_TIPREG = '"+substr(self:cMatric,15,2)+"' "
	elseIf len(self:cMatric) == 11
		cSql += "   AND BEA_CPFUSR = '"+self:cMatric+"' "
	endIf
	cSql += "	AND BEA.D_E_L_E_T_ = ' ' "
	cSql += " WHERE "
	cSql += "	BEC_FILIAL = '"+xFilial("BEC")+"' "
	cSql += "	AND BEC.D_E_L_E_T_ = ' ' "
	cSql += iif(BEC->(FieldPos("BEC_CANCEL")) > 0, "	AND BEC.BEC_CANCEL <> '1' ", "")
	cSql += "	ORDER BY BEC_DATINC DESC, BEC_SEQUEN DESC "

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSQL),"TRB",.T.,.F.)
	if !TRB->(EOF())
		aRet := {.T.,self:procJson(),200}
	else
		oJsonRet := JsonObject():New()
		oJsonRet['items'] := {}
		oJsonRet['hasNext'] := .F.
		aRet := {.T.,oJsonRet,200}
	endIf

	TRB->(dbCloseArea())
	BEC->(DbCloseArea())

Return aRet

Method procJson() Class HealthApiBeneficiaryTeethSvc

	Local oJson := JsonObject():New()
	Local aDentes := {}
	Local nX := 0

	aDentes := {'BEC_DENT11','BEC_DENT12','BEC_DENT13','BEC_DENT14','BEC_DENT15','BEC_DENT16','BEC_DENT85',;
				'BEC_DENT17','BEC_DENT18','BEC_DENT21','BEC_DENT22','BEC_DENT23','BEC_DENT24','BEC_DENT25',;
				'BEC_DENT26','BEC_DENT27','BEC_DENT28','BEC_DENT31','BEC_DENT32','BEC_DENT33','BEC_DENT34',;
				'BEC_DENT35','BEC_DENT36','BEC_DENT37','BEC_DENT38','BEC_DENT41','BEC_DENT42','BEC_DENT43',;
				'BEC_DENT44','BEC_DENT45','BEC_DENT46','BEC_DENT47','BEC_DENT48','BEC_DENT51','BEC_DENT52',;
				'BEC_DENT53','BEC_DENT54','BEC_DENT55','BEC_DENT61','BEC_DENT62','BEC_DENT63','BEC_DENT64',;
				'BEC_DENT65','BEC_DENT71','BEC_DENT72','BEC_DENT73','BEC_DENT74','BEC_DENT75','BEC_DENT81',;
				'BEC_DENT82','BEC_DENT83','BEC_DENT84'}

	oJson['items'] := {}
	oJson['hasNext'] := .F.

	for nX := 1 to len(aDentes)
		oTeeth := JsonObject():New()
		oTeeth['supranumerary'] := '0'
		oTeeth['subscriberId'] := self:cMatric
		oTeeth['healthProviderId'] := Alltrim(TRB->BEC_CODRDA)
		oTeeth['idOnHealthInsurer'] := Alltrim(TRB->BEC_GUIPRE)
		oTeeth['observation'] := '' //Alltrim(TRB->BEC_OBSERV) //Nos testes do HAT sempre envia vazio
		oTeeth['tooth'] := Substr(aDentes[nX],9,2)
		/*
		A - Ausente
		E - Extração Indicada
		H - Hígido
		C - Cariado
		R - Restaurado
		*/
		oTeeth['toothStatus'] := iif(&(TRB->(aDentes[nX]))=="A",5,1)

		aadd(oJson['items'],oTeeth)
		oTeeth := nil
	next
	
Return oJson

Method retCampos() Class HealthApiBeneficiaryTeethSvc

	Local cCampos := ''

	cCampos += ' BEC_DATINC, BEC_DENT11, BEC_DENT12, BEC_DENT13, BEC_DENT14, BEC_DENT15, BEC_DENT16, '
	cCampos += ' BEC_DENT17, BEC_DENT18, BEC_DENT21, BEC_DENT22, BEC_DENT23, BEC_DENT24, BEC_DENT25, '
	cCampos += ' BEC_DENT26, BEC_DENT27, BEC_DENT28, BEC_DENT31, BEC_DENT32, BEC_DENT33, BEC_DENT34, '
	cCampos += ' BEC_DENT35, BEC_DENT36, BEC_DENT37, BEC_DENT38, BEC_DENT41, BEC_DENT42, BEC_DENT43, '
	cCampos += ' BEC_DENT44, BEC_DENT45, BEC_DENT46, BEC_DENT47, BEC_DENT48, BEC_DENT51, BEC_DENT52, '
	cCampos += ' BEC_DENT53, BEC_DENT54, BEC_DENT55, BEC_DENT61, BEC_DENT62, BEC_DENT63, BEC_DENT64, '
	cCampos += ' BEC_DENT65, BEC_DENT71, BEC_DENT72, BEC_DENT73, BEC_DENT74, BEC_DENT75, BEC_DENT81, '
	cCampos += ' BEC_DENT82, BEC_DENT83, BEC_DENT84, BEC_DENT85, BEC_CODRDA, BEC_GPRIOP, BEC_GUIOPE, '
	cCampos += ' BEC_GUIPRE, BEC_GUIPRI, BEC_OBSERV, BEC_SEQUEN '

Return cCampos

method getError() class HealthApiBeneficiaryTeethSvc
return self:oError

method setError(nStatus, cCode, cMessage, cDetailedMessage) class HealthApiBeneficiaryTeethSvc

    Default nStatus  := 400
    Default cCode    := '0004'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

return
