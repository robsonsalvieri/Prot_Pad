#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

class HealthPortalNotificationSvc from CenRequest

	Public Method new()
	Public Method getNotifications(jPath)
	Public Method readNotifications()

endClass

Method New(oRest) class HealthPortalNotificationSvc
	_Super:new(oRest)
	self:lAnnotation := .t.
Return self

Method getNotifications(jPath) class HealthPortalNotificationSvc

	local cAlias := getNextAlias() as character
	local cCodeUser := jPath["codeUser"] as character
	local cCurrentDate := dtos(dDataBase) as character
	local jNotification as json

	self:oRespBody := JsonObject():new()
	self:oRespBody["notifications"] := {}

	beginSql alias cAlias
		SELECT
			BPL.BPL_CODIGO,
			BPL.BPL_TITULO,
			BJH.BJH_CODNOT,
			BPL.BPL_NOTICI
		FROM
			%table:BPL% BPL
		LEFT JOIN
			%table:B49% B49 ON
			B49.B49_FILIAL = %xfilial:B49% AND
			B49.B49_CODUSR = %exp:cCodeUser% AND
			B49.%notDel%
		LEFT JOIN
			%table:BJH% BJH ON
			BJH.BJH_FILIAL = %xfilial:BJH% AND
			BJH.BJH_CODNOT = BPL.BPL_CODIGO AND
			BJH.BJH_CODUSR = %exp:cCodeUser% AND
			BJH.%notDel%			
		WHERE
			BPL.BPL_FILIAL = %xfilial:BPL% AND
			BPL.BPL_TIPUSU = '2' AND
			BPL.BPL_VIGINI <= %exp:cCurrentDate% AND
			BPL.BPL_VIGFIN >= %exp:cCurrentDate% AND
			(BPL.BPL_CHAVE = '' OR BPL.BPL_CHAVE = B49.B49_BENEFI) AND
			BPL.%notDel%
	endSql

	if !(cAlias)->(eof())
		while !(cAlias)->(eof())
			jNotification := JsonObject():new()

			jNotification["code"] := alltrim((cAlias)->BPL_CODIGO)
			jNotification["title"] := alltrim((cAlias)->BPL_TITULO)
			jNotification["message"] := alltrim((cAlias)->BPL_NOTICI)
			jNotification["visualized"] := iif(!empty((cAlias)->BJH_CODNOT), .T., .F.)

			aAdd(self:oRespBody["notifications"], jNotification)
			(cAlias)->(dbSkip())
		endDo
	else
		self:lSuccess := .F.
		self:nFault := 400
		self:cFaultDesc := "Nenhum registro localizado"
	endif

	self:cResponse := self:oRespBody:toJson()

	(cAlias)->(dbCloseArea())

	freeObj(jNotification)

Return

Method readNotifications() class HealthPortalNotificationSvc
	local dDtAtual  := Date()

	self:lSuccess := _Super:checkBody(.t.)

	if self:lSuccess

		BJH->(recLock("BJH",.T.))
		BJH->BJH_FILIAL := xFilial('BJH')
		BJH->BJH_CODNOT := self:jRequest['codeNotification']
		BJH->BJH_CODUSR := self:jRequest['codeUser']
		BJH->BJH_DTVISU := dDtAtual
		BJH->(msUnLock())

	endif

	iif(self:lSuccess, self:nFault := 200, self:nFault:= 400)

Return

