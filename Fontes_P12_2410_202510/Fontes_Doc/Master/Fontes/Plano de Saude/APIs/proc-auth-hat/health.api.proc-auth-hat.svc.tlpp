#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#include "PLSMGER.CH"

Class ProcAuthHatSvc from PLSSvcAux

    Data oBody as Object
    Data cGuia as Character

    public method New() Constructor
    public method procPut()
    private method validaGuia()
    private method setReponse(nX,lSuccess,cDetail)
    private method insertMedicalTeam()
    
EndClass

Method New(oRest) Class ProcAuthHatSvc

    _Super:New()
    self:cGuia := oRest:getPathParamsRequest()['id']
    self:oBody := JsonObject():New()
    self:oBody:fromJson(DecodeUTF8(oRest:GetBodyRequest(), "cp1252"))
   
Return self

Method procPut() Class ProcAuthHatSvc

    Local nX := 0
    Local cCodPad   := ''
    Local cCodPro   := ''
    private aTabDup := PlsBusTerDup( superGetMv("MV_TISSCAB",.f.,"87") )
    private aErrVarVin  := { .f., "", "", "" }

    if self:validaGuia()
        
        BEGIN TRANSACTION

        self:oResult['procedures'] := {}
 
        for nX := 1 to len(self:oBody["procedures"])

            cCodPad := self:oBody["procedures",nX,"tableCode"]
            cCodPro := self:oBody["procedures",nX,"procedureCode"]

            cCodPad := allTrim( PLSVARVINC('87','BR4', allTrim(self:oBody["procedures",nX,"tableCode"])) )
            cCodPro := allTrim( PLSVARVINC(allTrim(self:oBody["procedures",nX,"tableCode"]), 'BR8', allTrim(self:oBody["procedures",nX,"procedureCode"]),cCodPad + cCodPro,, aTabDup, @cCodPad))

            if ValType(self:oBody["procedures",nX,"medicalTeam"]) == 'A' .And. (len(self:oBody["procedures",nX,"medicalTeam"]) > 0)
                aRet := self:insertMedicalTeam(self:oBody["procedures",nX,"medicalTeam"], cCodPad, cCodPro)
            else
                aRet := {.F.,"Atributo 'medicalTeam' nao informado"}
            endif 

            self:setReponse(nX,aRet[1],aRet[2])
        
        next

       END TRANSACTION

    endIf

Return self:lSuccess

Method validaGuia() Class ProcAuthHatSvc

    Local lRet    := .T.

    BEA->(DbSetOrder(1)) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT
    if !BEA->(DbSeek(xFilial("BEA")+self:cGuia ))
        lRet := .F.
        self:setError(400, 400, 'Nao foi possivel alterar a guia', 'Guia informada nao encontrada no alias BEA')
    endIf

Return lRet

Method setReponse(nX,lSuccess,cDetail) Class ProcAuthHatSvc

    Local oJson := JsonObject():New()
    Default cDetail := ''

    oJson["tableCode"] := self:oBody["procedures",nX,"tableCode"]
    oJson["procedureCode"] := self:oBody["procedures",nX,"procedureCode"]
    oJson['success'] := lSuccess
    oJson['message'] := cDetail

    Aadd(self:oResult['procedures'],oJson)

Return

Method insertMedicalTeam(aMedicalTeam, cCodPad, cCodPro) Class ProcAuthHatSvc

    local nx := 0 
    local lRet := .F.
    local cMessage := ''
    
    BE2->(DbSetOrder(6)) //BE2_FILIAL+BE2_OPEMOV+BE2_ANOAUT+BE2_MESAUT+BE2_NUMAUT+BE2_CODPAD+BE2_CODPRO+BE2_DENREG+BE2_FADENT
    BAQ->(DbSetOrder(6))
    B4B->(DbSetOrder(1))//B4B_FILIAL+B4B_OPEMOV+B4B_ANOAUT+B4B_MESAUT+B4B_NUMAUT+B4B_SEQUEN

    if BE2->(DbSeek(xFilial("BE2")+self:cGuia+cCodPad+cCodPro))
                
        for nX := 1 to len(aMedicalTeam)

            if !B4B->(DbSeek(xFilial("B4B")+BE2->(BE2_OPEMOV+BE2_ANOAUT+BE2_MESAUT+BE2_NUMAUT+BE2_SEQUEN)))                                   
                RecLock("B4B", .T.)
                    B4B->B4B_FILIAL := FWxFilial('B4B')           
                    B4B->B4B_SEQUEN := BE2->BE2_SEQUEN
                    B4B->B4B_OPEMOV := PlsIntPad()
                    B4B->B4B_ANOAUT := substr(self:cguia,5,4)
                    B4B->B4B_MESAUT := substr(self:cguia,9,3)
                    B4B->B4B_NUMAUT := substr(self:cguia,11,9)
                    B4B->B4B_SICONS := aMedicalTeam[nX]['professional']['professionalCouncil']
                    B4B->B4B_NUCONS := aMedicalTeam[nX]['professional']['professionalCouncilNumber']    
                    B4B->B4B_UFCONS := aMedicalTeam[nX]['professional']['stateAbbreviation']    
                    B4B->B4B_CDPFPR := aMedicalTeam[nX]['professional']['idOnHealthInsurer']    
                    B4B->B4B_NOMPRF := aMedicalTeam[nX]['professional']['name'] 
                    if BAQ->(DbSeek(xFilial("BAQ")+aMedicalTeam[nX]['professional']['cbos']['code']))
                        B4B->B4B_CODESP := BAQ->BAQ_CODESP
                    endif
                B4B->(MsUnlock())

                lRet := .T.
                cMessage := 'Registro correspondente atualizado com sucesso'
            else
                lRet := .T.
                cMessage := 'Profissional informado ja existe na tabela B4B'
            endIf
        next
       
    else
        lRet := .F.
        cMessage := 'Registro correspondente nao encontrado na tabela BE2'
    endIf

Return {lRet,cMessage}
