#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSapoInfoSvc

retorna status dos fontes, parametros, campos e arquivos/diretórios 

@author  Daniel Silva
@version P12
@since   13/07/2023
/*/
//------------------------------------------------------------------- 
class PLSapoInfoSvc from CenRequest 

    Data nStatus as Integer
    Data oError as Object
    Data oResult as Object
    Data oObjRequest as Object
    Data aFiles as Array 
    Data aParams as Array 
    Data aDirectoryFiles as Array 
    Data aFields as Array 
    Data aSchedules as Array 
    Data aFilesLinux as Array 
    Data aStamps as Array
    Data lFiles as logical 
    Data lParams as logical 
    Data lDirectoryFiles as logical 
    Data lFields as logical 
    Data lSchedules as logical 
    Data lStamps as logical
    Data lLinux as logical
  

    Public Method New()
    Public Method vldJson()
    Public Method enviromentPost()    
    Public Method setObjRequest(oObjRequest)
    Public Method getError()
    Public Method getResult()
    Public Method getStatusCode()
    Public Method checkFilesLinux()
    Public Method checkDirFile()

    Private Method setError(nStatus, cCode, cMessage, cDetailedMessage)
    
endClass

Method New() class PLSapoInfoSvc

    self:oError := JsonObject():new()
    self:oResult := JsonObject():new()
    self:aFiles := {}
    self:aParams := {}
    self:aDirectoryFiles := {}
    self:aFields := {}
    self:aSchedules := {}
    self:aFilesLinux := {}
    self:aStamps := {}
    self:nStatus := 200
    self:lFiles := .t.
    self:lParams := .t.
    self:lDirectoryFiles := .t.
    self:lFields := .t.
    self:lSchedules := .t.
    self:lStamps := .t.
    self:lLinux := IsSrvUnix()

Return self

Method vldJson() class PLSapoInfoSvc

    local lRet := .T.

    if (Empty(self:oObjRequest['files']))
        self:lFiles := .f.
    endif

    if (Empty(self:oObjRequest['params']))
       self:lParams := .f.
    endif

     if (Empty(self:oObjRequest['directoryFiles']))
        self:lDirectoryFiles := .f.
    endif

     if (Empty(self:oObjRequest['fields']))
        self:lFields := .f.
    endif

    if (Empty(self:oObjRequest['schedules']))
        self:lSchedules := .f.
    endif

     if (Empty(self:oObjRequest['stamps']))
        self:lStamps := .f.
    endif

    if(!self:lFiles .and. !self:lParams .and. !self:lDirectoryFiles .and. !self:lFields .and. !self:lSchedules .and. !self:lStamps )
        self:setError(400,'0001',nil,"Atributos não informados ou vazios (files/params/directoryFiles/fields/schedules/stamps)")
        lret := .f.
    endif


Return lRet

Method enviromentPost() class PLSapoInfoSvc

    local nx := 0
    local cParam := ""
    local cResultParm := ""
    local cDirectoryFile := ""
    local cTable := ""
    local cSchedule := ""
    local cSql := ""
    local cSqlViewName := ""
    Local cViewStamp := ""
    local cFiledValue := ''
    Local cDBType := ""

    if (self:lFiles)
        for nx := 1 to Len(self:oObjRequest['files']) 
            aadd(self:aFiles, getapoinfo( self:oObjRequest['files'][nx]))
            if(empty(self:aFiles[nx]))
                aadd(self:aFiles[nx], self:oObjRequest['files'][nx])
            endif 
        next 
    endif

    if (self:lParams)
        for nx := 1 to Len(self:oObjRequest['params']) 
            cParam := self:oObjRequest['params'][nx]
            cResultParm := getnewpar(cParam)
            aAdd(self:aParams, {cParam, cResultParm} )
        next
    endif

    if (self:lDirectoryFiles)
        for nx := 1 to Len(self:oObjRequest['directoryFiles']) 
            cDirectoryFile := self:oObjRequest['directoryFiles'][nx]
            if(self:lLinux .or. self:oObjRequest['advpr']) //verifica se é linux 
              self:checkFilesLinux(lower(cDirectoryFile))
            else 
                self:checkDirFile(cDirectoryFile)
            endif            
        next
    endif

    if (self:lFields)
        for nx := 1 to Len(self:oObjRequest['fields']) 

            cTable := SubStr(self:oObjRequest['fields'][nx], 1, 3) 
            cTableAux :=  self:oObjRequest['fields'][nx] 
            DbSelectArea(cTable)
            (cTable)->( DbSetOrder(1) ) 

            if (cTable)->(FieldPos(self:oObjRequest['fields'][nx]) ) > 0
                if  (cTable)->(DbSeek(xFilial(cTable)+PlsIntPad()))
                    cFiledValue := (cTable)->&(cTableAux)
                    aAdd(self:aFields, {self:oObjRequest['fields'][nx], cFiledValue, .t.} ) 
                 endif
            else 
                 aAdd(self:aFields, {self:oObjRequest['fields'][nx], "", .f.} ) 
            endif  

        next
        DBCloseArea()          
    endif

    if (self:lSchedules)
        for nx := 1 to Len(self:oObjRequest['schedules']) 
            cSchedule := self:oObjRequest['schedules'][nx]

             cSql := "SELECT * FROM " + RetSqlName("XX1") + " WHERE XX1_ROTINA = '" + cSchedule +"'"
             dbUseArea(.t.,"TOPCONN",tcGenQry(,,cSql),"cAlias",.f.,.t.)

             if !cAlias->(Eof())
                aAdd(self:aSchedules, {self:oObjRequest['schedules'][nx], .t.} ) 
            else 
                aAdd(self:aSchedules, {self:oObjRequest['schedules'][nx], .f.} ) 
            endIf

            cAlias->(dbCloseArea())
        next
    endif

    if(self:lStamps)

        cDBType := TCGetDB()

           for nx := 1 to Len(self:oObjRequest['stamps']) 
            cStamp := self:oObjRequest['stamps'][nx]
            
            if cDBType == 'ORACLE'
                // Busca o nome das views no Oracle
                cSqlViewName := "select view_name AS NOME from sys.all_views WHERE view_name LIKE '" + cStamp + "%' AND rownum = 1"
                dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSqlViewName),"HATVIEWS",.T.,.F.)
                cViewStamp := HATVIEWS->NOME
                HATVIEWS->(dbCloseArea())

                cSql := "SELECT COUNT(*) AS quantidade_campos FROM ALL_TAB_COLUMNS WHERE TABLE_NAME LIKE '" + alltrim(cViewStamp) +"'"
                

            elseif cDBType == "POSTGRES"
                // Busca o nome das views no Postgres
                cSqlViewName := "select viewname as NOME from pg_views where schemaname not in ('pg_catalog', 'information_schema') and viewname like '" + lower(cStamp) + "%' limit 1"
                dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSqlViewName),"HATVIEWS",.T.,.F.)
                cViewStamp := HATVIEWS->NOME
                HATVIEWS->(dbCloseArea())

                cSql := "SELECT COUNT(*) AS quantidade_campos FROM INFORMATION_SCHEMA.columns where table_name like '" + alltrim(lower(cViewStamp)) +"'"

            else
                // Busca o nome das views no SQL Server
                cSqlViewName := "SELECT top 1 name AS NOME FROM sys.objects WHERE [type] IN ('v') AND name LIKE'" + cStamp + "%'"
                dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSqlViewName),"HATVIEWS",.T.,.F.)
                cViewStamp := HATVIEWS->NOME
                HATVIEWS->(dbCloseArea())

                cSql := "SELECT COUNT(*) AS quantidade_campos FROM INFORMATION_SCHEMA.COLUMNS WHERE UPPER(TABLE_NAME) = '" + alltrim(Upper(cViewStamp)) +"'"

            endif
   
            // Retorna a quantidade de campos de cada uma das views
            dbUseArea(.t.,"TOPCONN",tcGenQry(,,cSql),"cAlias",.f.,.t.)
            aAdd(self:aStamps, {self:oObjRequest['stamps'][nx], cAlias->(quantidade_campos)} ) 
            cAlias->(dbCloseArea())

        next
    endif 

Return .t.


method checkDirFile(cDirectoryFile) class PLSapoInfoSvc

    if(File(cDirectoryFile))
        aAdd(self:aDirectoryFiles, {cDirectoryFile, .t.} )
    else
        aAdd(self:aDirectoryFiles, {cDirectoryFile, .f.} )
    endif 

return


method checkFilesLinux(cDirectoryFile) class PLSapoInfoSvc

// nos casos de linux nao podemos ter arquivos no upper case e por isso esse tratamento a mais
// caso o arquivo enviado na api for xxx e o encontrado for XXX, devemos retornar .f. 

ADir(cDirectoryFile, self:aFilesLinux, ,,,,.f.)

    if ( aScan(self:aFilesLinux, {|x| x $ cDirectoryFile}) > 0 ) 
        aAdd(self:aDirectoryFiles, {cDirectoryFile, .t.} )
    else 
        aAdd(self:aDirectoryFiles, {cDirectoryFile, .f.} )
    endif 

return 

method setObjRequest(oObjRequest) class PLSapoInfoSvc
    self:oObjRequest := oObjRequest
return

method getError() class PLSapoInfoSvc
return self:oError

method getStatusCode() class PLSapoInfoSvc
return self:nStatus

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSapoInfoSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:nStatus  := nStatus
    self:lSuccess := .F.

return

method getResult() class PLSapoInfoSvc

    local nX := 0 

    if (self:lFiles)
        self:oResult["files"] := {}
        For nX := 1 to Len(self:aFiles)
            aAdd(self:oResult["files"], JsonObject():new())
            if(len(self:aFiles[nx]) == 5)
                self:oResult['files'][nx]['fileName'] := self:aFiles[nx][1]
                self:oResult['files'][nx]['type'] := self:aFiles[nx][2]
                self:oResult['files'][nx]['build'] := self:aFiles[nx][3]
                self:oResult['files'][nx]['date'] := self:aFiles[nx][4]
                self:oResult['files'][nx]['time'] := self:aFiles[nx][5]
                self:oResult['files'][nx]['result'] := .T.
            else 
                self:oResult['files'][nx]['fileName'] := self:aFiles[nx][1]
                self:oResult['files'][nx]['result'] := .F.
            endif
        next 
    endif 

    if (self:lParams)
        self:oResult["params"] := {}
        For nX := 1 to Len(self:aParams)
            aAdd(self:oResult["params"], JsonObject():new())
            self:oResult['params'][nx]['paramName'] := self:aParams[nx][1]
            self:oResult['params'][nx]['result'] := self:aParams[nx][2]
        next
    endif 

    if(self:lDirectoryFiles)
        self:oResult["directoryFiles"] := {}
        For nX := 1 to Len(self:aDirectoryFiles)
            aAdd(self:oResult["directoryFiles"], JsonObject():new())
            self:oResult['directoryFiles'][nx]['directoryFileName'] := self:aDirectoryFiles[nx][1]
            self:oResult['directoryFiles'][nx]['result'] := self:aDirectoryFiles[nx][2]
        next 
    endif 

    if(self:lFields)
        self:oResult["fields"] := {}
        For nX := 1 to Len(self:aFields)
            aAdd(self:oResult["fields"], JsonObject():new())
            self:oResult['fields'][nx]['fieldName'] := self:aFields[nx][1]
            self:oResult['fields'][nx]['filedValue'] := self:aFields[nx][2]
            self:oResult['fields'][nx]['result'] := self:aFields[nx][3]
        next 
    endif 

    if (self:lSchedules)
        self:oResult["schedules"] := {}
        For nX := 1 to Len(self:aSchedules)
            aAdd(self:oResult["schedules"], JsonObject():new())
            self:oResult['schedules'][nx]['scheduleName'] := self:aSchedules[nx][1]
            self:oResult['schedules'][nx]['result'] := self:aSchedules[nx][2]
        next 
    endif 

      if (self:lStamps)
        self:oResult["stamps"] := {}
        For nX := 1 to Len(self:aStamps)
            aAdd(self:oResult["stamps"], JsonObject():new())
            self:oResult['stamps'][nx]['stampName'] := self:aStamps[nx][1]
            self:oResult['stamps'][nx]['result'] := self:aStamps[nx][2]
        next 
    endif 

return self:oResult:toJson()
