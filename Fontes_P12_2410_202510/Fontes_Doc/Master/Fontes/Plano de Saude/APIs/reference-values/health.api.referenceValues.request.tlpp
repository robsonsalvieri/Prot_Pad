#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

#DEFINE LEVEL_CABEC "1"
#DEFINE LEVEL_ITEMS "2"

//-------------------------------------------------------------------
/*/{Protheus.doc} API referenceValues - GET
.
@author  Sakai
@version P12
@since   26/12/2024
/*/
//-------------------------------------------------------------------
@Get("/totvshealthplans/v1/referenceValues")
function ReferenceValuesRequest()
    local oRequest := ReferenceValuesRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:applyPageSize()
        oRequest:buscar()
        oRequest:procGet(ALL)
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} ReferenceValuesRequest
.
@author  Sakai
@version P12
@since   26/12/2024
/*/
//-------------------------------------------------------------------
Class ReferenceValuesRequest from CenRequest

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method buscar()
    public method applyPageSize()

EndClass

Method destroy()  Class ReferenceValuesRequest
    self:oCollection:destroy()
    _Super:destroy()
Return 

Method New(oRest, cSvcName) Class ReferenceValuesRequest
    
    _Super:New(oRest,cSvcName,.t.)
    self:oCollection := ReferenceValuesCollection():New()

Return self

Method valida() Class ReferenceValuesRequest

    Local cLevel := ''

    self:jRequest := oRest:getQueryRequest()

    if self:lSuccess

        if empty(cvaltochar(self:jRequest['level']))
            self:lSuccess := .F.
            self:nStatus  := 400
            self:nFault   := 400
            self:cFaultDesc   := "Campo obrigatorio nao informado"
            self:cFaultDetail := "level"
        else
            cLevel := cvaltochar(self:jRequest['level'])
        endIf

        if self:lSuccess .And. (cLevel <> '1' .And. cLevel <> '2')
            self:lSuccess := .F.
            self:nStatus  := 400
            self:nFault   := 400
            self:cFaultDesc   := "Campo informado invalido"
            self:cFaultDetail := "O atributo level deve ser 1 ou 2"

        endIf

        if self:lSuccess .And. self:jRequest['level'] == LEVEL_ITEMS .And. ;
            (empty(cvaltochar(self:jRequest['healthProviderId'])) .Or. ;
             empty(cvaltochar(self:jRequest['hospitalCategoryId'])) .Or. ;
             empty(cvaltochar(self:jRequest['accommodationId'])) .Or. ;
             empty(cvaltochar(self:jRequest['hospitalRegimeId'])) .Or. ;
             empty(cvaltochar(self:jRequest['hospitalizationGroupId'])))

            self:lSuccess := .F.
            self:nStatus  := 400
            self:nFault   := 400
            self:cFaultDesc   := "Para level 2 os campos abaixo são obrigatorios: "
            self:cFaultDetail := "healthProviderId, hospitalizationGroupId, accommodationId, hospitalRegimeId e hospitalizationGroupId"
            
        endIf

        if self:lSuccess
            self:oCollection:setMapper(self:jRequest['level'])
        endIf

    endIf

Return self:lSuccess

Method buscar() Class ReferenceValuesRequest

    If self:lSuccess
        self:oCollection:get(self:jRequest)
    EndIf

Return self:lSuccess

Method applyPageSize() Class ReferenceValuesRequest

    if empty(cvaltochar(self:jRequest['page']))
        self:jRequest['page'] := '1'
    endif

    if empty(cvaltochar(self:jRequest['pageSize']))
        self:jRequest['pageSize'] := '20'
    endif

    self:oCollection:applyPageSize(self:jRequest['page'],self:jRequest['pageSize'] )
    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}

Return self:lSuccess
