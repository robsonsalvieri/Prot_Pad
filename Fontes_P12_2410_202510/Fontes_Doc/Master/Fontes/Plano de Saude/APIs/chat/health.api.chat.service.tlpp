#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiChatService
.
@author  Sakai
@version P12
@since   18/06/24
/*/
//-------------------------------------------------------------------
Class HealthApiChatService

    Data cGuia as Character
    Data oRest as Object
    Data oJsonReq as Object
    Data oError as Object

    public method New() Constructor
    public method setGuia(cGuia)
    public method setJsonReq(oJsonReq) 
    public method procAuditingPut()
    public method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method getError()

EndClass

Method New(oRest) Class HealthApiChatService
    
    self:oRest  := oRest
    self:oError := JsonObject():new()
    self:cGuia  := ''

Return self

Method setGuia(cGuia) class HealthApiChatService
    self:cGuia := cGuia
Return

Method setJsonReq(oJsonReq) class HealthApiChatService
    self:oJsonReq := oJsonReq
Return

Method procAuditingPut() Class HealthApiChatService

    Local lRet   := .T.
    Local oAudit := nil

    B53->(DbSetOrder(1)) //B53_FILIAL+B53_NUMGUI+B53_ORIMOV
    if B53->(DbSeek(xFilial("B53")+self:cGuia))

        if ValType(self:oJsonReq['chatStatus']) == "C"
            Do Case
                Case B53->B53_MSGSTA == '3'
                    self:setError(400, 400, 'Falha ao alterar o status da sala', 'A guia informadada ja esta com a sala encerrada')
                    lRet := .F.
                Case !self:oJsonReq['chatStatus'] $ '123'
                    self:setError(400, 400, 'Falha ao alterar o status da sala', 'Status informado e invalido')
                    lRet := .F.
                Otherwise
                    oAudit := PLMensCont():new()
                    oAudit:setStatB53(self:cGuia,self:oJsonReq['chatStatus'])
                    oAudit:destroy()
            EndCase

        endIf
    else
        self:setError(400, 400, 'Guia informada nao encontrada', 'Nao foi encontrada a guia informada no alias B53')
        lRet := .F.
    endIf

Return lRet

Method setError(nStatus, cCode, cMessage, cDetailedMessage) class HealthApiChatService

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

Return

Method getError() class HealthApiChatService
Return self:oError