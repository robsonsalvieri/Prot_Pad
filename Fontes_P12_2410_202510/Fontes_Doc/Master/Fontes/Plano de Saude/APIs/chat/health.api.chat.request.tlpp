#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiChatRequest
.
@author  Sakai
@version P12
@since   18/06/24
/*/
//-------------------------------------------------------------------
@Put("/totvshealthplans/v1/chat/:id/auditing")

function HealthApiChatRequest()
    local oRequest := HealthApiChatRequest():new(oRest)    
    
    oRequest:initRequest()
    oRequest:valida()
    oRequest:procAuditingPut()
    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

Return

Class HealthApiChatRequest from CenRequest

    Data oSvc As Object

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method procAuditingPut()

EndClass

Method New(oRest, cSvcName) Class HealthApiChatRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oSvc := HealthApiChatService():new(oRest)  
Return self

Method destroy() Class HealthApiChatRequest
Return _Super:destroy()

Method valida() class HealthApiChatRequest
    
    self:lSuccess := _Super:checkBody()
    if !self:lSuccess
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := 'Json invalido'
        self:cFaultDetail := 'Foi encontrado um erro na estrutura do Json enviado'
    endIf

Return

Method procAuditingPut() class HealthApiChatRequest

    Local oError := Nil

    if self:lSuccess
        self:oSvc:setGuia(self:oRest:getPathParamsRequest()['id'])
        self:oSvc:setJsonReq(self:jRequest)
        self:lSuccess := self:oSvc:procAuditingPut()
        
        if !self:lSuccess
            oError := self:oSvc:getError()
            self:nFault       := oError['status']
            self:nStatus      := oError['code']
            self:cFaultDesc   := oError['message']
            self:cFaultDetail := oError['detailedMessage']
        endIf

    endIf

Return