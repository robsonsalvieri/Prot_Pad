#include "TOTVS.CH"

#define CRLF chr( 13 ) + chr( 10 )

/*/{Protheus.doc} 
    Classe concreta da Entidade HealthApiProtocolEntity - Other RemunerationAPI
@type  Class
    @author FrameworkApi 1.0
    @since 20190211
/*/
Class HealthApiProtocolEntity from CenEntity

    public Method New()
  
    public Method serialize(oJsonControl)
    public Method destroy()
    
    private method getGuiasPls()
    private method getAuthType()
    private method getIdXml(cTipGui)
    private method getBatchNumber(cTipGui)

EndClass

Method New() Class HealthApiProtocolEntity
    _Super:New()
Return self

Method serialize(oJsonControl) Class HealthApiProtocolEntity

	local oJson := JsonObject():New()
    local cTipGui := self:getAuthType()
	default oJsonControl := CenJsonControl():New()

    oJsonControl:setProp(oJson,"protocol",              self:getValue("protocol"))
    oJsonControl:setProp(oJson,"batchNumber",           self:getBatchNumber(cTipGui))
    oJsonControl:setProp(oJson,"providerNumber",        self:getValue("providerNumber"))
    oJsonControl:setProp(oJson,"status",                self:getValue("status"))
    //Caso ainda não mudou a fase pego o apresentado do XML para a capa do lote
    if empty(self:getValue("givenValue")) .and. !empty(self:getValue("xmlValue"))
        oJsonControl:setProp(oJson,"givenValue",        self:getValue("xmlValue"))
    else
        oJsonControl:setProp(oJson,"givenValue",        self:getValue("givenValue"))
    endif
    oJsonControl:setProp(oJson,"glossValue",            self:getValue("glossValue"))
    oJsonControl:setProp(oJson,"value",                 self:getValue("value"))
    oJsonControl:setProp(oJson,"mounth",                self:getValue("mounth"))
    oJsonControl:setProp(oJson,"year",                  self:getValue("year"))

    //Jornada Faturamento HAT
    oJsonControl:setProp(oJson,"batchId",               self:getValue("batchId"))
    oJsonControl:setProp(oJson,"batchDate",             self:getValue("batchDate"))
    oJsonControl:setProp(oJson,"healthProviderId",      self:getValue("healthProviderId"))
    oJsonControl:setProp(oJson,"authQuantity",          self:getValue("authQuantity"))
    oJsonControl:setProp(oJson,"authType",              cTipGui)
    oJsonControl:setProp(oJson,"batchCover",            self:getValue("batchCover"))
    oJsonControl:setProp(oJson,"batchSend",             self:getValue("batchSend"))
    oJsonControl:setProp(oJson,"healthProviderName",    self:getValue("healthProviderName"))
    oJsonControl:setProp(oJson,"idxml",                 self:getIdXml(cTipGui))
    oJsonControl:setProp(oJson,"origin",                iif(cTipGui=='10' .and. !empty(cvaltochar(oJson['idxml'])), 'X',self:getValue("origin")))
    if cTipGui=='10' .and. oJson['origin'] == 'G'
        oJsonControl:setProp(oJson,"batchNumber",       "")
    endif
    oJsonControl:setProp(oJson,"step",                  self:getValue("step"))    
    oJsonControl:setProp(oJson,"guiasPLS",              self:getGuiasPls())
    oJsonControl:setProp(oJson,"accountReview",         iif(self:getValue("status")=="C","1","0"))
    oJsonControl:setProp(oJson,"reviewPeding",          "0")
    oJsonControl:setProp(oJson,"reviewStatus",          "")
    oJsonControl:setProp(oJson,"chat",                  "")
    oJsonControl:setProp(oJson,"azureStorageFilePath",  "")
    oJsonControl:setProp(oJson,"azureStorageSasToken",  "")
    oJsonControl:setProp(oJson,"fileName",              "")
    oJsonControl:setProp(oJson,"notes",                 "")
    oJsonControl:setProp(oJson,"valueType",             "")
    oJsonControl:setProp(oJson,"trackingStatus",        1)
    oJsonControl:setProp(oJson,"rejectionCount",        0)
    oJsonControl:setProp(oJson,"batchValue",            0)

return oJson
//------------------------------------------------------------------------------------------
/*/{Protheus.doc} destroy

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method destroy() Class HealthApiProtocolEntity
	_Super:destroy()
	DelClassIntF()
Return

Method getGuiasPls() Class HealthApiProtocolEntity

    Local cRet := "0"

    if self:getValue("origin") == "G"

        cSql := " SELECT BEA_LOTHAT FROM "+ RetSqlName('BEA')
        cSql += " WHERE BEA_FILIAL = '" + xFilial('BEA') + "' "
        cSql += " AND BEA_LOTHAT = '"+self:getValue("batchNumber")+"' "
        cSql += " AND D_E_L_E_T_ = ' ' "
        dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSQL),"TRBEA",.T.,.F.)
		if !TRBEA->(EOF())
            cRet := "1" 
		endIf
		TRBEA->(dbCloseArea())
    endIf

Return cRet

Method getAuthType() Class HealthApiProtocolEntity

    Local cAuth := self:getValue("authType")
    Local cRet  := ''

    Do Case
        Case cAuth == '01' //Consulta
            cRet := '1'
        Case cAuth == '02' //SADT
            cRet := '2'
        Case cAuth == '05' //Resumo de Internacao
            cRet := '5'
        Case cAuth == '06' //Honorarios
            cRet := '6'
        Case cAuth == '13' //Odonto
            cRet := '9'
        Case cAuth == '10' //Recurso de Glosa
            cRet := '10'
    EndCase

Return cRet

Method getIdXml(cTipGui) Class HealthApiProtocolEntity

    Local cRet := ""

    if cTipGui == "10" //Recurso de Glosa
        cSql := " SELECT BXX_IDXML FROM "+ RetSqlName('BXX')
        cSql += " WHERE BXX_FILIAL = '" + xFilial('BXX') + "' "
        cSql += " AND BXX_CODINT = '" + PlsIntPad() + "' "
        cSql += " AND BXX_PLSHAT = '"+self:getValue("batchNumber")+"' "
        cSql += " AND D_E_L_E_T_ = ' ' "

        dbUseArea(.T.,"TOPCONN",TcGenQry(,,cSQL)," TRBXX",.T.,.F.)
		if ! TRBXX->(EOF())
            cRet := Alltrim(TRBXX->BXX_IDXML)
		endIf
		TRBXX->(dbCloseArea())
    else
        cRet := self:getValue("idxml")
    endIf

Return cRet

Method getBatchNumber(cTipGui) Class HealthApiProtocolEntity

    Local cRet := self:getValue("batchNumber")
    cRet := iif(cTipGui == "10" .And. len(self:getValue("batchNumber")) == 12,"",cRet)
    
Return cRet
