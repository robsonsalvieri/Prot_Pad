#INCLUDE "TOTVS.CH"

#Define DBFIELD 1
#Define JSONFIELD 2

Class HealthApiProtocolDao from CenDao

    Data cCountSql as Character 

    public method New(aFields) Constructor
    public method get(cCodRda,cAno,cMes, cLoteNF)
    public method hasNext(nRecno)
    public method loadOrder()
    public method getFiltCus()
    public method getCount()

EndClass

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} New
    Construtor

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method New(aFields) Class HealthApiProtocolDao
	_Super:New(aFields)
    self:cfieldOrder := "BCI_CODPEG DESC"
    self:cAlias      := "BCIQRY"  
    self:loadOrder()    
    self:cCountSql   := ""
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} get
Query que retorna todos os customers

@author    Lucas Nonato
@version   V12
@since     01/03/2021
/*/
Method get(cCodRda,cAno,cMes,cLoteNF,cPegs,lHatJourney,cBatchNumber,cProtocol,cTipGui,cStatus,cDateFrom,cDateTo) Class HealthApiProtocolDao

local cSql    := ""
local cQuery  := ""
local nX      := 0
local aTipGui := {}
local aStatus := {}
local cLotesPLS  := GetNewPar("MV_PLPROT1","1")
Default cLoteNF  := ""
//Params Tela HAT
Default lHatJourney  := .F.
Default cBatchNumber := ''
Default cProtocol    := ''
Default cTipGui      := ''
Default cStatus      := ''
Default cDateFrom    := ''
Default cDateTo      := ''

cQuery += " BCI_CODPEG,BCI_LOTGUI,BCI_IDXML,BCI_MES,BCI_ANO,BXX_VLRTOT, "
cQuery += " SUM(BD7_VALORI) AS VALORAPR, SUM(BD7_VLRGLO)+ SUM(BD7_VLRGTX) AS VALORGLO, SUM(BD7_VLRPAG) AS VALOR, "
//Campos Jornada HAT
if lHatJourney
    cQuery += " CASE "
    cQuery += " WHEN (BXX_STATUS IS NULL) "
    cQuery += "     THEN BCI_LOTGUI "
    cQuery += "     ELSE BXX_PLSHAT "
	cQuery += " END AS BATCHNUMBER, "
else
    cQuery += " BCI_LOTGUI AS BATCHNUMBER, "
endIf

cQuery += " BCI.R_E_C_N_O_ AS BCI_RECNO, BCI_DTDIGI, BCI_CODRDA, BCI_QTDDIG, BCI_TIPGUI, BCI_VLRGUI, BCI_NOMRDA, "
cQuery += " BCI_FASE, "
cQuery += " CASE WHEN (BXX_STATUS IS NULL ) THEN 'G' ELSE 'X' END AS ORIGIN, "
cQuery += " CASE WHEN (BCI_STTISS = '2' OR BCI_STTISS = '3' OR BCI_STTISS = '6') THEN '1' ELSE '0' END AS LOTCOVER, "
cQuery += " CASE WHEN BCI_STTISS = '0' THEN '1' ELSE '0' END AS SENDLOT, "
cQuery += iif(lHatJourney," CASE WHEN (BXX_STATUS = '6') THEN 'C' ELSE BCI_STTISS END AS BCI_STTISS "," BCI_STTISS ")

cQuery += " FROM " + RetSqlName('BCI') + ' BCI '
cQuery += iif(lHatJourney," LEFT JOIN "," INNER JOIN ") + RetSqlName('BD7') + " BD7 "
cQuery += " ON BD7_FILIAL = '" + xFilial('BD7') + "' "
cQuery += " AND BD7_CODOPE = BCI_CODOPE"
cQuery += " AND BD7_CODLDP = BCI_CODLDP"
cQuery += " AND BD7_CODPEG = BCI_CODPEG"
cQuery += " AND BD7_CODRDA = BCI_CODRDA"
cQuery += " AND BD7_BLOPAG <> '1'"
cQuery += " AND BD7.D_E_L_E_T_ = ' '"

cQuery += " LEFT JOIN " + RetSqlName('BXX') + " BXX "
cQuery += " ON BXX_FILIAL = '" + xFilial('BXX') + "' "
cQuery += " AND BXX_CODINT = BCI_CODOPE "
cQuery += " AND BXX_CODPEG = BCI_CODPEG "
cQuery += " AND BCI_TIPGUI <> '10' 
cQuery += " AND BXX.D_E_L_E_T_ = ' ' "

cQuery += " WHERE BCI_FILIAL = '" + xFilial('BCI') + "' "
cQuery += " AND BCI_OPERDA = '" + plsintpad() + "' "
cQuery += " AND BCI_CODRDA = '" + cCodRda + "' "

cQuery += iif(!empty(cProtocol), " AND BCI_CODPEG = '" + cProtocol + "' ","") //Protocolo PLS
cQuery += iif(!empty(cBatchNumber)," AND BCI_LOTGUI = '" + cBatchNumber + "' ","") //Lote HAT
cQuery += iif(!empty(cDateFrom)," AND BCI_DTDIGI >= '" + cDateFrom + "' ","")
cQuery += iif(!empty(cDateTo)," AND BCI_DTDIGI <= '" + cDateTo + "' ","")

if !empty(cStatus)
    aStatus := StrTokArr(cStatus,',')
    if len(aStatus) > 0
        cQuery += " AND BCI_STTISS IN ("
        for nX := 1 to len(aStatus)
            cQuery += iif(nX > 1,",","")
            cQuery += "'"+ aStatus[nX] +"'"
        next
        cQuery += ") "
    endIf
endIf

if !empty(cTipGui)
    aTipGui := StrTokArr(cTipGui,',')
    if len(aTipGui) > 0
        cQuery += " AND BCI_TIPGUI IN ("
        for nX := 1 to len(aTipGui)
            cQuery += iif(nX > 1,",","")
            cQuery += "'"+ aTipGui[nX] +"'"
        next
        cQuery += ") "
    endIf
endIf

//Regras especificas para a tela de faturamento do HAT
if lHatJourney
    cQuery += " AND BCI_SITUAC = '1' "
    cQuery += " AND BCI_CODLDP NOT IN ('"+PLSRETLDP(4)+"','"+PLSRETLDP(9)+"') "    
    cQuery += iif(cLotesPLS <> "1"," AND BCI_LOTGUI <> ' ' ","")
    cQuery += iif(empty(cTipGui)," AND BCI_TIPGUI IN ('01','02','05','06','10','13') ","")
endIf

if !empty(cPegs)
    cQuery += " AND BCI_CODPEG IN "+formatIn(cPegs,',')
elseIf !lHatJourney 
    if empty(cLoteNF)       
        cQuery += " AND BCI_ANO = '" + cAno + "' "
        cQuery += " AND BCI_MES = '" + cMes + "' "
        cQuery += " AND BCI_FASE IN ('2','3','4') "
        cQuery += " AND BCI_SITUAC = '1'  "
        cQuery += " AND BCI_CODLDP NOT IN ('"+PLSRETLDP(4)+"','"+PLSRETLDP(9)+"') "
    else 
        cQuery += " AND BCI_LOTENF = '" + cLoteNF + "' "
    endif
endif
cQuery += " AND BCI.D_E_L_E_T_ = ' ' "
cQuery += " GROUP BY BCI_CODPEG,BCI_LOTGUI,BCI_IDXML,BCI_STTISS,BCI_MES,BCI_ANO,BXX_VLRTOT,"
//Campos Jornada HAT
cQuery += " BCI.R_E_C_N_O_, BCI_DTDIGI ,BCI_CODRDA ,BCI_QTDDIG, BCI_TIPGUI ,BCI_VLRGUI ,BCI_ORIGEM, BCI_NOMRDA, BCI_FASE,"
cQuery += " BCI_IDXML, BXX_STATUS "
cQuery += iif(lHatJourney,", BXX_PLSHAT","")

if ExistBlock("PLISTPROT") .and. empty(cPegs) // manipula a exibição dos protocolos -> cpegs apenas enviado na api de atualizar lote não podemos manipular o pe nesse caso
	cQuery := ExecBlock("PLISTPROT",.F.,.F.,{cQuery,cCodRda,cLoteNF,cAno,cMes})
endif

//Query para contador de registros
self:cCountSql := " SELECT COUNT(BCI_CODPEG) TOTAL FROM ( SELECT "
self:cCountSql += cQuery + " ) QRY "

//Query para principal
cSql += self:getRowControl()
cSql += cQuery
cSql += self:getWhereRow()

self:setQuery(self:queryBuilder(cSql))
lFound := self:executaQuery()

return lFound

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} hasNext
    hasNext especifico

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method hasNext(nRecno) Class HealthApiProtocolDao
    Local lTemProx := .F.
    If self:aliasSelected()
        lTemProx := !(self:getAliasTemp())->(Eof())
    EndIf
return lTemProx


//------------------------------------------------------------------------------------------
/*/{Protheus.doc} loadOrder
    Adicona campos para ordenacao

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
//------------------------------------------------------------------------------------------
Method loadOrder() Class HealthApiProtocolDao

    self:oHashOrder:set("protocol",          "BCI_CODPEG")

Return

Method getCount() Class HealthApiProtocolDao
    
    Local nCount := 0
    
    dbUseArea(.T.,"TOPCONN",TcGenQry(,,self:cCountSql),"COUNT",.T.,.F.)
	if !COUNT->(EOF())
        nCount := COUNT->TOTAL
    endIf
    COUNT->(dbCloseArea())

return nCount
