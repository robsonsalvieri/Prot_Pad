#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSCompetenceProtocolsSvc

@author  Nicole Luna
@version P12
@since   11/07/2024
/*/
//------------------------------------------------------------------- 
class PLSCompetenceProtocolsSvc from PLSSvcAux

    Data oError   as Object
    Data oResult  as Object
    Data oJsonReq as Object
    Data oDao     as Object
    Data oEntity  as Object
    Data cStatus  as Character

  
    public method New(oRest,cJsonReq)
    public method setPageData(cNumPage, cPageSize) 
    public method getCompetence(cHealthProvider, cSequential, cMonth, cYear)
    public method getError()
    public method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method updtStatus(cSequential)

endClass

Method New(cJsonReq) class PLSCompetenceProtocolsSvc

    self:oError    := JsonObject():new()
    self:oResult   := JsonObject():new()
    self:oDao      := PLSCompetenceProtocolsDao():new()
    self:oEntity   := PLSCompetenceProtocolsEnt():new()
    self:oJsonReq  := cJsonReq

Return self

Method setPageData(cNumPage, cPageSize) class PLSCompetenceProtocolsSvc

    Default cNumPage    := '1'
    Default cPageSize   := '20'

    self:oDao:setNumPage(cNumPage)
    self:oDao:setPageSize(cPageSize)
        
    self:setPage(cNumPage)
    self:setPageSize(cPageSize)

Return

method getCompetence(cHealthProvider, cSequential, cMonth, cYear) class PLSCompetenceProtocolsSvc

    Local lRet              := .F.
    Local aRet              := {}
    Local nX                := 0
    Local lHasNext          := .F.
    Local oDaoAux           := PLSCompetenceProtocolsDao():new()
    Local lRetGlosas        := .F.
    Local aRetCompetence    := {}
    Default cHealthProvider := ''

    lRet := self:oDao:getCompetence(cHealthProvider, cSequential, cMonth, cYear)

    if lRet
        while !self:oDao:isEof()
            nX++
            if nX <= self:nPageSize
                aRetCompetence := self:oEntity:montaJson(self:oDao,nX)
                lRetGlosas := oDaoAux:getGlosas(cHealthProvider, aRetCompetence)
                if lRetGlosas
                    while !oDaoAux:isEof()
                        self:oEntity:montaGlosas(oDaoAux,nX)
                        oDaoAux:dbSkip()
                    enddo
                endif
                oDaoAux:closeQuery()
            else
                lHasNext := .T.
            endIf

            self:oDao:dbSkip()
        endDo
        self:oEntity:setHasNext(lHasNext)
    endIf
    self:oDao:closeQuery()
    
    if lRet
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Nenhum registro foi localizado!","Nao foi localizado nenhum registro para o prestador informado.")
    endIf
        
    self:oDao := nil

return {lRet,aRet}

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSCompetenceProtocolsSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

return

method getError() class PLSCompetenceProtocolsSvc
return self:oError

method updtStatus(cSequential) class PLSCompetenceProtocolsSvc

    Local lRet              := .F.
    Local aRet              := {}
    Local cStatus           := iif(self:oJsonReq['status'] <> nil, self:oJsonReq['status'], "")
    Default cSequential     := ''

    lRet := self:oDao:updtStatus(cSequential, cStatus)

    if !lRet
       self:setError(400,"0003","Ocorreu um erro ao atualizar o registro!","Nao foi localizado nenhum registro para o sequencial informado.")
    else
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    endIf
        
    self:oDao := nil

return {lRet,aRet}
