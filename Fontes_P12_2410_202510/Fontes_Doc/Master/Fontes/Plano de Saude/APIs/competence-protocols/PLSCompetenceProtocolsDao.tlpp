#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSCompetenceProtocolsDao

@author  Nicole Luna
@version P12
@since   12/07/2024
/*/
//------------------------------------------------------------------- 
class PLSCompetenceProtocolsDao From PLSDaoAux

    Public Method New()
	Public Method getCompetence(cHealthProvider, cSequential, cMonth, cYear)
	Public Method getGlosas(cHealthProvider, aRetCompetence)
	Public Method getFilter(cHealthProvider, lJoinBdx, cYear, cMonth, cSequential)
	Public Method updtStatus(cSequential, cStatus)

endClass

Method New() Class PLSCompetenceProtocolsDao

    _Super:New()

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} getProtocols

@author  Nicole Luna
@version P12
@since   12/07/2024
/*/
//------------------------------------------------------------------- 
Method getCompetence(cHealthProvider, cSequential, cMonth, cYear) Class PLSCompetenceProtocolsDao

	Local lFound := .F.
	Local cQuery := ''

	cQuery += " SELECT  "
    cQuery += "     B0J_LOTENF, B0J_CODRDA, B0J_ANO, B0J_MES, B0J_DATPRO, B0J_VLRAPR, B0J_VLRPAG, "
    cQuery += "     B0J_VLRGLO, B0J_STATUS "
	cQuery += " FROM " + RetSqlName("B0J") + " B0J  "
    cQuery +=   self:getFilter(cHealthProvider, .F., cYear, cMonth, cSequential)
	cQuery += " ORDER BY B0J.B0J_ANO DESC, B0J.B0J_MES DESC "

	cQuery += self:pageQuery()

	self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getGlosas(cHealthProvider, aRetCompetence) Class PLSCompetenceProtocolsDao

	Local lFound := .F.
	Local cQuery := ""
    Local cYear  := ""
    Local cMonth := ""
    Default cHealthProvider := ""
    Default aRetCompetence := {}

    if Len(aRetCompetence) > 0
        cYear  := aRetCompetence[1]
        cMonth := aRetCompetence[2]
        cQuery += " SELECT  BDX_CODOPE, BDX_CODGLO, BDX_DESGLO, COUNT(*) AS quantity "
        cQuery += " FROM " + RetSqlName("B0J") + " B0J  "
        cQuery += " LEFT JOIN " + RetSqlName("BCI") + " BCI "
	    cQuery += "     ON BCI_FILIAL = '"  + xFilial("BCI")+ "' "
	    cQuery += "     AND B0J.B0J_LOTENF = BCI.BCI_LOTENF " 
        cQuery += " LEFT JOIN " + RetSqlName("BDX") + " BDX "
	    cQuery += "     ON BDX_FILIAL = '"  + xFilial("BDX")+ "' "
	    cQuery += "     AND BCI.BCI_CODPEG = BDX.BDX_CODPEG "
        cQuery +=   self:getFilter(cHealthProvider, .T., cYear, cMonth, "")
        cQuery += " GROUP BY BDX_CODGLO, BDX_DESGLO, BDX_CODOPE  "

	    self:setQuery(cQuery)
        lFound := self:executaQuery()
    endif

Return lFound

Method getFilter(cHealthProvider, lJoinBdx, cYear, cMonth, cSequential) Class PLSCompetenceProtocolsDao
    Local cFilter           := ""
    Default cHealthProvider := ""
    Default lJoinBdx        := .F.
    Default cYear           := ""
    Default cMonth          := ""
    Default cSequential     := ""

    cFilter += " WHERE 1=1 " 
    cFilter += "     AND B0J_FILIAL = '" + xFilial("B0J") + "' " 
    cFilter += "     AND B0J_CODRDA = '" + cHealthProvider +  "'"
    cFilter += "     AND B0J.D_E_L_E_T_ = ' ' "
    cFilter += iif(!empty(cYear),       "     AND B0J_ANO = '" + cYear  + "' ", "")
    cFilter += iif(!empty(cMonth),      "     AND B0J_MES = '" + cMonth + "' ", "")
    cFilter += iif(!empty(cSequential), "     AND B0J_LOTENF = '" + cSequential + "' ", "")
    if lJoinBdx
        cFilter += "     AND BDX.D_E_L_E_T_ = ' ' "
        cFilter += "     AND BCI.D_E_L_E_T_ = ' ' "
        cFilter += "     AND BDX_TIPREG = '1'  "
        cFilter += "     AND BDX_ACAO = '1'   "
    endif

Return cFilter

Method updtStatus(cSequential, cStatus) class PLSCompetenceProtocolsDao
    
    Local   lSuccess := .F.

    B0J->(DbSetOrder(1))
	if B0J->(dBseek(xFilial("B0J") + cSequential))
        lSuccess := .T.
		B0J->( RecLock("B0J",.F.) )
			B0J->B0J_STATUS := cStatus
		B0J->( MsUnLock() ) 	
	endif

Return lSuccess
