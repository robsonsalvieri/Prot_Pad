#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSCompetenceProtocolsEnt

@author  Nicole Luna
@version P12
@since   12/07/2024
/*/
//------------------------------------------------------------------- 
class PLSCompetenceProtocolsEnt from PLSEntAux
    
	Data cYearCompetence  As Character
	Data cMonthCompetence As Character

    public method new()
    public method montaJson(oDao,nX)
    public method montaGlosas(oDao, nX)

endClass

method new() class PLSCompetenceProtocolsEnt
    _Super:New()
    self:cYearCompetence  := ""
    self:cMonthCompetence := ""
    self:oResult["items"] := {}
Return self

method montaJson(oDao, nX) class PLSCompetenceProtocolsEnt

    Local cAli        := oDao:getAliasTemp()

    self:cYearCompetence  := self:maskField((cAli)->B0J_ANO)
    self:cMonthCompetence := self:maskField((cAli)->B0J_MES)

    aadd(self:oResult["items"], JsonObject():new())
    self:oResult["items",nX,"sequential"]       := self:maskField((cAli)->B0J_LOTENF)
    self:oResult["items",nX,"healthProviderId"] := self:maskField((cAli)->B0J_CODRDA)
    self:oResult["items",nX,"year"]             := self:maskField((cAli)->B0J_ANO)
    self:oResult["items",nX,"month"]            := self:maskField((cAli)->B0J_MES)
    self:oResult["items",nX,"processingDate"]   := self:maskField((cAli)->B0J_DATPRO)
    self:oResult["items",nX,"valueApr"]         := self:maskField((cAli)->B0J_VLRAPR)
    self:oResult["items",nX,"valuePag"]         := self:maskField((cAli)->B0J_VLRPAG)
    self:oResult["items",nX,"valueGlo"]         := self:maskField((cAli)->B0J_VLRGLO)
    self:oResult["items",nX,"status"]           := self:maskField((cAli)->B0J_STATUS)
    self:oResult["items",nX,"glosas"]           := {}

return { self:cYearCompetence, self:cMonthCompetence }

method montaGlosas(oDao, nX) class PLSCompetenceProtocolsEnt

    Local cAli        := oDao:getAliasTemp()
    Local oGlosa      := JsonObject():new()
    Local cCodeGlo    := ""

    cCodeGlo := deParaSimpl("38", (cAli)->BDX_CODGLO, "BCT")
    if(empty(allTrim(cCodeGlo)))
        cCodeGlo := retCodGlo((cAli)->BDX_CODOPE, (cAli)->BDX_CODGLO)
    endif

    oGlosa["codeGlosa"]        := self:maskField(cCodeGlo)
    oGlosa["descriptionGlosa"] := self:maskField((cAli)->BDX_DESGLO)
    oGlosa["quantityGlosa"]    := self:maskField((cAli)->quantity)
    aadd(self:oResult["items",nX,"glosas"], oGlosa)

return

