#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityLib
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityLib

    Data cGuia as Character
    Data oDaoCab as Object
    Data oDaoEve as Object
    Data oDaoCri as Object
    Data oDaoBen as Object
    Data oResult as Object
    Data nStatus as Integer
    Data aTabDup as Array
    Data cExpand as Character
    Public Data lChamadaRecursiva
    Data cRecno as Character
    Data lShowValues as Logical 
    Data lValOptions as Character
    Data lImpNAut as Logical 

    Data nPosCri as Integer
    Data tagProcedures as Character
    Data tagRejection as Character
    Data lBCTMotNeg as Logical 
    Data lB7NExist as Logical

    public method new()
    public method setExpand(cExpand)
    public method getDao(cAlias)
    public method maskField(xValue,lData)
    public method getAliasTemp(cAlias)
    public method getResult()
    public method getStatusCode()
    public method existExpand(cExpand)
    public method montaBenef(cMatric)
    public method montaAnexos()
    public method getVinculadas()
    public method getTipGui()
    public method montaBB0()
    public method montaRDA()
    public method getStatusGui()
    public method quebraLinha()
    public method getObservacao(RecnoB72, cAliasCab, cCodPro, cSequen)
    public method owner(aRetFun)
    public method temSeriado()
    public method resetPosCri()
    public method setCriticas(nItem,cSequen,cCodCri,cReject,cDesc)
    public method getMotivoNegativa(nItem,cSequen,cCodCri)
    public method getAttNote(cMsg,cGuia,cAlias)

endClass

method new(cGuia, lShowDeniedProc) class PLSAuthorizationsEntityLib
    self:cGuia              := cGuia
    self:oResult            := JsonObject():new()
    self:nStatus            := 200
    self:aTabDup            := PlsBusTerDup(SuperGetMv("MV_TISSCAB", .F. ,"87"))
    self:cExpand            := ""
    self:lChamadaRecursiva  := .f.
    self:cRecno             := ""
    self:lShowValues        := GetNewPar("MV_VLTISS","1") == "1"
    self:lValOptions        := GetNewPar("MV_PLSVHAT", "1")
    self:lImpNAut           := iif(GetNewPar("MV_PLNAUT",0) == 0 .AND. !lShowDeniedProc, .F., .T.)
    self:tagProcedures      := _tgProc
    self:tagRejection       := _tgRejec
    self:nPosCri            := 0
    self:lBCTMotNeg         := BCT->(FieldPos("BCT_MOTNEG")) > 0
    self:lB7NExist          := FWAliasInDic("B7N")
Return self

method setExpand(cExpand) class PLSAuthorizationsEntityLib
    self:cExpand := cExpand
return
 
method getDao(cAlias) class PLSAuthorizationsEntityLib
    default cAlias := ''
    do case
        case cAlias == "CAB"
            return self:oDaoCab
        case cAlias == "EVE"
            return self:oDaoEve
        case cAlias == "CRI"
            return self:oDaoCri
        case cAlias == "BEN"
            return self:oDaoBen
    endCase
return nil

method maskField(xValue,lData) class PLSAuthorizationsEntityLib

    local xRet := nil
    default lData := .F.
    
    if lData

        if (!empty(xValue))
            xRet := xValue
            cAno := SubStr(xRet,1,4)
            cMes := SubStr(xRet,5,2)
            cDia := SubStr(xRet,7,2)
            xRet := cAno + "-" + cMes + "-" + cDia
        else
            xRet := ""
        endIf

    elseIf ValType(xValue) == "C"
        xRet := Alltrim(xValue)
    
    elseIf ValType(xValue) == "N"
        xRet := xValue
    endif

Return xRet

method getAliasTemp(cAlias) class PLSAuthorizationsEntityLib
Return (self:getDao(cAlias):getAliasTemp())

method getResult() class PLSAuthorizationsEntityLib
return self:oResult

method getStatusCode() class PLSAuthorizationsEntityLib
return self:nStatus

method existExpand(cExpand) class PLSAuthorizationsEntityLib
return cExpand $ self:cExpand

method montaBenef(cMatric) class PLSAuthorizationsEntityLib

    Local oBenef := JsonObject():new()
    Local cAliTrb := ''

    if self:existExpand('beneficiary')
        
        if self:getDao('BEN'):buscaBeneficiario(cMatric)

            cAliTrb := self:getAliasTemp('BEN')        
            if !self:getDao('BEN'):isEof()
                
                oBenef['subscriberId'] := cMatric
                oBenef['name'] := self:maskField( (cAliTrb)->BA1_NOMUSR )
                oBenef['socialName'] := self:maskField( (cAliTrb)->BA1_NOMSOC )
                oBenef['holderCPF'] := self:maskField( (cAliTrb)->BA1_CPFUSR )
                oBenef['birthdate'] := self:maskField( (cAliTrb)->BA1_DATNAS,.T. )
                oBenef['weight'] := (val((cAliTrb)->BTS_PESO) / 1000) 
                oBenef['height'] := val((cAliTrb)->BTS_ALTURA) 
                oBenef['oldSubscriberId'] := self:maskField( (cAliTrb)->BA1_MATANT )
                oBenef['phoneNumber'] := self:maskField( (cAliTrb)->BA1_TELEFO )
                oBenef['gender'] := self:maskField( (cAliTrb)->BA1_SEXO )
                oBenef['cardExpiration'] := self:maskField( (cAliTrb)->BA1_DTVLCR,.T. )
                oBenef['holderRelationship'] := self:maskField( (cAliTrb)->BA1_TIPUSU )
                oBenef['CNS'] := self:maskField( (cAliTrb)->BTS_NRCRNA )
                oBenef['isOwner'] := (cAliTrb)->BA1_TIPUSU == "T"
                oBenef['age'] := DateDiffYear(date(),stod((cAliTrb)->BA1_DATNAS))

                aRetFun := PLSDADUSR(cMatric,"1",.F.,dDataBase,nil,nil,nil)
                if aRetFun[1]
                    oBenef['healthInsurance'] := JsonObject():new()
                    oBenef['healthInsurance']['code']         := aRetFun[11]
                    oBenef['healthInsurance']['roomType']     := aRetFun[17]
                    oBenef['healthInsurance']['roomDescription'] := aRetFun[21]
                    oBenef['healthInsurance']['description']  := self:maskField( aRetFun[14] )
                    oBenef['companyName'] := iif(aRetFun[8] == "2" .And. len(aRetFun) >= 98,aRetFun[98],'')

                    //Envio do atributo do titular
                    oBenef['owner'] := self:owner(aRetFun)    
                endif

            endIf
        endIf
        self:getDao('BEN'):closeQuery()
    endIf

return oBenef

method montaRDA(cCodRda,cExpand) class PLSAuthorizationsEntityLib
    local oJson     := JsonObject():new()
    local cSql      := ''
    Default cExpand := 'healthProvider'

    if self:existExpand(cExpand)
        cSql := " SELECT BAU_MUN,BAU_CODIGO,BAU_TIPPRE,BAU_DATBLO,BAU_TIPPE,BAU_NFANTA,BAU_CPFCGC,BAU_NOME,BAU_EMAIL,BAU_CONREG,BAU_ESTCR"
        cSql += " FROM " + retsqlname("BAU")
        cSql += " WHERE BAU_FILIAL = '" + xfilial("BAU") + "' "
        cSql += " AND BAU_CODIGO = '" + cCodRda + "' "
        cSql += " AND D_E_L_E_T_ = ' ' "
        dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),"TMPBAU",.F.,.T.)

        if !TMPBAU->(eof())
            oJson['cityCode']               := allTrim(TMPBAU->BAU_MUN)
            oJson['attendanceLocations']    := JsonObject():new()
            oJson['healthProviderId']       := AllTrim(TMPBAU->BAU_CODIGO)
            oJson['healthInsurerType']      := allTrim(TMPBAU->BAU_TIPPRE)
            oJson['blockDate']              := StoD(alltrim(TMPBAU->BAU_DATBLO))
            oJson['isBlocked']              := iif(!empty(oJson['blockDate']) .and. date() > oJson['blockDate'],.t.,.f.)
            oJson['billingType']            := "1"
            oJson['type']                   := allTrim(TMPBAU->BAU_TIPPE)
            oJson['trade']                  := alltrim(TMPBAU->BAU_NFANTA)
            oJson['specialities']           := JsonObject():new()
            oJson['officialRecord']         := allTrim(TMPBAU->BAU_CPFCGC)
            oJson['name']                   := PTURemChr(allTrim(TMPBAU->BAU_NOME))
            oJson['email']                  := alltrim(TMPBAU->BAU_EMAIL)
            oJson['councilNumber']          := alltrim(TMPBAU->BAU_CONREG)
            oJson['stateAbbreviation']      := alltrim(TMPBAU->BAU_ESTCR)
        endIf
        TMPBAU->(dbclosearea())
    endIf

return oJson

method montaBB0(cCodigo,cCodEsp) class PLSAuthorizationsEntityLib
    local oJson := JsonObject():new()

    if self:existExpand('professional')        
        cSql := " SELECT BB0_CODIGO,BB0_TEL,BB0_CODSIG,BB0_CGC,BB0_NUMCR,BB0_NOME,BB0_EMAIL,BB0_ESTADO "
        cSql += " FROM " + retsqlname("BB0")
        cSql += " WHERE BB0_FILIAL = '" + xfilial("BB0") + "' "
        cSql += " AND BB0_CODIGO = '" + cCodigo + "' "
        cSql += " AND D_E_L_E_T_ = ' ' "
        dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),"TMPBB0",.F.,.T.)

        if !TMPBB0->(eof())
            oJson['idOnHealthInsurer']              := alltrim(TMPBB0->BB0_CODIGO)
            oJson['phoneNumber']                    := alltrim(TMPBB0->BB0_TEL)
            oJson['professionalCouncil']            := alltrim(TMPBB0->BB0_CODSIG)
            oJson['professionalIdentifier']         := alltrim(TMPBB0->BB0_CGC)
            oJson['professionalCouncilNumber']      := alltrim(TMPBB0->BB0_NUMCR)
            oJson['name']                           := alltrim(TMPBB0->BB0_NOME)            
            oJson['email']                          := alltrim(TMPBB0->BB0_EMAIL)
            oJson['stateAbbreviation']              := alltrim(TMPBB0->BB0_ESTADO)
            oJson['cbos']                           := jsonObject():new()
            cDesEsp := alltrim(BAQ->( posicione("BAQ",1, xFilial("BAQ") + plsintpad()+cCodEsp,"BAQ_DESCRI")))
            oJson['cbos']['code']                   := alltrim(BAQ->( posicione("BAQ",1, xFilial("BAQ") + plsintpad()+cCodEsp,"BAQ_CBOS")))
            oJson['cbos']['specialtyDescription']   := cDesEsp
            oJson['cbos']['specialtyCode']          := cCodEsp
            oJson['cbos']['description']            := cDesEsp
        endIf
        TMPBB0->(dbclosearea())
    endIf


return oJson

method getVinculadas(cAlias,cGuia,cCodRda) class PLSAuthorizationsEntityLib
local lRet      := .f.
default cCodRda := ''
//4-B4A_FILIAL+B4A_GUIREF
//4-B4Q_FILIAL+B4Q_GUIREF
//2-BEC_FILIAL+BEC_CODRDA+BEC_GUIPRI

if cAlias == 'BEC'
    (cAlias)->(dbsetorder(2))
    lRet := (cAlias)->(msseek(xfilial(cAlias)+cCodRda+cGuia))
else
    (cAlias)->(dbsetorder(4))
    lRet := (cAlias)->(msseek(xfilial(cAlias)+cGuia))
endif

return lRet

Method getTipGui(cTipGui) Class PLSAuthorizationsEntityLib

if cTipGui == '01'
    cTipGui := '1'
elseif cTipGui == '02'
    cTipGui := '2'
elseif cTipGui == '03'
    cTipGui := '4'
elseif cTipGui == '13'
    cTipGui := '9'
endif

Return cTipGui

method montaAnexos() class PLSAuthorizationsEntityLib
return JsonObject():new()

Method getStatusGui(cStatus, cCancel) Class PLSAuthorizationsEntityLib
local cRet := ''

if cCancel == '1'
    cRet := '9'
else
    cRet := cStatus
endif

Return cRet

method quebraLinha(cString, nTamanhoMax) class PLSAuthorizationsEntityLib
local cRet          := ""
local nInicio       := 1
local nAt           := 0
default nTamanhoMax := 180 

cString := alltrim(cString)
nLenString := Len(cString)

Do While nLenString > 0 

    if (nAt := At(chr(13)+chr(10),cString)) > 0
        cRet    += SubStr(cString, nInicio, nAt - 1) + "<br>"
        cString := Substr(cString, nAt + 2, len(cString))
    else
        cRet    += SubStr(cString, nInicio, nTamanhoMax) + "<br>"
        cString := Substr(cString, nTamanhoMax + iif(len(cString) < nTamanhoMax,0,1) , len(cString))
    endIf
    nLenString := len(cString)

EndDo

Return cRet

method getObservacao(cRecno, cAliasCab, cCodPro, cSequen) class PLSAuthorizationsEntityLib

    local cObservacao := ''
    local cQuery      := ''
    default cRecno    := ''
    default cAliasCab := ''
    default cCodPro   := ''
    default cSequen   := ''
    
    cQuery := " SELECT "
    cQuery += iif(!empty(cRecno) .And. !empty(cAliasCab),"B72.R_E_C_N_O_ AS RecnoB72 ","' ' AS RecnoB72 ")
    cQuery += "  FROM " + RetSqlName("B72")+" B72 "
    cQuery += "  WHERE B72_FILIAL = '" + xFilial("B72") +"'  "
    cQuery += "    AND B72_ALIMOV = '" + cAliasCab + "' "
	cQuery += "    AND B72_RECMOV = '" + cRecno + "' "
	cQuery += "    AND B72_CODPRO = '" + cCodPro + "' "
    cQuery += "    AND B72_SEQPRO = '" + cSequen + "' "
    cQuery += "    AND B72.D_E_L_E_T_ = ' ' "

    dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQuery),"TRB72",.F.,.T.)

    while !TRB72->(eof())
        B72->(dbsetorder(1))
        B72->(dbgoto(TRB72->RecnoB72))
        cObservacao := B72->B72_OBSANA
        TRB72->(dbSkip())
    endDo

    TRB72->(dbCloseArea())

Return cObservacao

method owner(aRetFun) class PLSAuthorizationsEntityLib

    local oOwner  := jsonObject():New()

    oOwner['subscriberId']                        := aRetFun[37] + aRetFun[38]
    oOwner['name']                                := Alltrim(aRetFun[40])
    oOwner['healthInsurance']                     := JsonObject():new()
    oOwner['healthInsurance']['code']             := aRetFun[97]
    oOwner['healthInsurance']['description']      := aRetFun[73]

return(oOwner)

method temSeriado(cGuia) class PLSAuthorizationsEntityLib

    Local cSql      := ""
    Local lSeriado  := .F.
    Local cAlias    := GetNextAlias()

    cSql := " SELECT 1 FROM " + RetSqlName("BE2") + " BE2 "
    cSql += " INNER JOIN " + RetSqlName("BR8") + " BR8"
    cSql += "   ON BR8_FILIAL = '"  + xFilial("BR8") + "' "
    cSql += "   AND BR8_CODPAD = BE2_CODPAD "
    cSql += "   AND BR8_CODPSA = BE2_CODPRO "
    cSql += "   AND BR8.D_E_L_E_T_ = ' ' "
    cSql += " INNER JOIN " + RetSqlName("BJE") + " BJE"
    cSql += "   ON BJE_FILIAL = '"  + xFilial("BJE") + "' "
    cSql += "   AND BJE_CODIGO = BR8_CLASSE "
    cSql += "   AND BJE_TIPO = '2' "
    cSql += "   AND BJE.D_E_L_E_T_ = ' ' "
    cSql += " WHERE BE2_FILIAL = '" + xFilial("BE2") + "' "
    cSql += "   AND BE2_OPEMOV = '"+substr(cGuia,1,4)+"' "
	cSql += "   AND BE2_ANOAUT = '"+substr(cGuia,5,4)+"' "
	cSql += "   AND BE2_MESAUT = '"+substr(cGuia,9,2)+"' "
	cSql += "   AND BE2_NUMAUT = '"+substr(cGuia,11,8)+"' "
    cSql += "   AND BE2.D_E_L_E_T_ = ' ' "

    dbUseArea(.t.,"TOPCONN",tcGenQry(,,cSql),cAlias,.F.,.T.)

    if !(cAlias)->(eof())
        lSeriado := .t.
    endif

    (cAlias)->(dbclosearea())
    
return lSeriado

method resetPosCri() class PLSAuthorizationsEntityLib
    self:nPosCri := 0
return

method setCriticas(nItem,cSequen,cCodCri,cReject,cDesc) class PLSAuthorizationsEntityLib

    self:nPosCri ++ 
    aAdd(self:oResult[self:tagProcedures,nItem,self:tagRejection], JsonObject():new())

    self:oResult[self:tagProcedures,nItem,self:tagRejection,self:nPosCri,'procedureSequence'] := self:maskField(cSequen)
    self:oResult[self:tagProcedures,nItem,self:tagRejection,self:nPosCri,'code']              := self:maskField(cCodCri)
    self:oResult[self:tagProcedures,nItem,self:tagRejection,self:nPosCri,'rejectionSequence'] := self:maskField(cReject)
    self:oResult[self:tagProcedures,nItem,self:tagRejection,self:nPosCri,'description']       := Upper(self:maskField(cDesc))

return

method getMotivoNegativa(nItem,cSequen,cCodCri) class PLSAuthorizationsEntityLib

    if self:lBCTMotNeg .And. !Empty(cCodCri)
        BCT->(DbSetOrder(1)) //BCT_FILIAL+BCT_CODOPE+BCT_PROPRI+BCT_CODGLO
        if BCT->(DbSeek(xFilial("BCT")+PlsIntPad()+cCodCri)) .And. !Empty(BCT->BCT_MOTNEG)
            self:setCriticas(nItem,cSequen,'','NEG',BCT->BCT_MOTNEG)
        endIf
    endIf

return

method getAttNote(cGuia,cAlias) class PLSAuthorizationsEntityLib

    local cRet := ""

    if self:lB7NExist
        B7N->(DbSetOrder(2)) //B7N_FILIAL+B7N_OPEMOV+B7N_ANOAUT+B7N_MESAUT+B7N_NUMAUT+B7N_ALIAS+B7N_TIPGUI
        if B7N->(MsSeek(xFilial("B7N")+cGuia+cAlias))
            cRet += iif(B7N->B7N_TOKEN  == "1","T","")
            cRet += iif(B7N->B7N_BIOMET == "1","B","")
            cRet += iif(B7N->B7N_RECFAC == "1","F","")
        endIf
    endIf

return cRet
