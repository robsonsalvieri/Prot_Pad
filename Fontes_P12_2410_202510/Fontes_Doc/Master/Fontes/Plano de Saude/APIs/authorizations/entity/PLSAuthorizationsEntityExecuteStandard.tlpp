#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'
#DEFINE _tgBenef 'beneficiary'
#DEFINE _tgProf 'professional'
#DEFINE _tgRda 'healthProvider'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityExecuteStandard
.
@author  sakai
@version P12
@since   18/07/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityExecuteStandard from PLSAuthorizationsEntityLib

    data aProcExec as Array
    data aDadUsr as Array

    public method new()
    public method setProcExec()
    public method geraJson()
    private method geraBeneficiario()
    private method geraEventos()
    private method geraProfessional()
    private method geraRda()
    private method geraCriticas(nX,cCodPad,cCodPro)
    private method calcCopart(nX,cCodPad,cCodPro)
    private method isSeriado()
    private method obrigaAnex(cCodPad,cCodPro)

endClass

method new(cGuia) class PLSAuthorizationsEntityExecuteStandard
    
    _Super:New(cGuia)
    self:cGuia := cGuia
    self:aProcExec := {}
    self:aDadUsr := {}
  
Return self

method setProcExec(aProcExec) class PLSAuthorizationsEntityExecuteStandard
    self:aProcExec := aProcExec
return

method geraJson() class PLSAuthorizationsEntityExecuteStandard
    local lOculSer  := GetNewPar("MV_PLDTSER", .f.) 
    BEA->( DbSetOrder(1) )//BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT
    BEA->( MsSeek( xFilial("BEA")+self:cGuia ) )

    BCT->(DbSetOrder(1)) //BCT_FILIAL+BCT_CODOPE+BCT_PROPRI+BCT_CODGLO+BCT_DESCRI+BCT_GLTISS

    self:oResult["idOnHealthInsurer"] := self:cGuia
    self:oResult["idOnHealthProvider"] := self:cGuia
    self:oResult["password"] := self:maskField( self:aProcExec[3,18] )
    self:oResult["passwordExpireDate"] := self:maskField( Dtos(self:aProcExec[3,19]) )
    if lOculSer .and. self:temSeriado(self:cGuia)
        self:oResult["passwordExpireDate"] := ''
    endif

    //Especifico HAT
    self:oResult["journey"] := "2"
    self:oResult["authorizationDescription"] := "Exame - Liberação"
    self:oResult['locationCode']       := self:maskField( BEA->BEA_CODLOC )
    self:oResult['consultationType']   := self:maskField( BEA->BEA_TIPCON )
    self:oResult['requestDate']        := self:maskField( Dtos(BEA->BEA_DATSOL),.T. )
    self:oResult['attendanceNote']     := self:maskField( Alltrim(BEA->BEA_MSG01)+Alltrim(BEA->BEA_MSG02)+Alltrim(BEA->BEA_MSG03) )     
    self:oResult['newbornAttendance']  := iif(BEA->BEA_ATERNA=="1",.T.,.F.)
    self:oResult['priorAuthorization'] := iif(BEA->BEA_LIBERA=="1",.T.,.F.)
    self:oResult['attendanceLocation'] := self:maskField( BEA->BEA_LOCAL )
    self:oResult['authorizationType']  := self:maskField( self:getTipGui(BEA->BEA_TIPGUI) )
    self:oResult['guideType']          := self:maskField( BEA->BEA_TIPGUI) 
    self:oResult['missingValidationCode'] := self:maskField( BEA->BEA_AUSVLD )
    self:oResult['attendanceModel']    := self:maskField( BEA->BEA_TIPADM )
    self:oResult['attendanceToken']    := self:maskField( BEA->BEA_TOKEDI )
    self:oResult['attendanceType'] := self:maskField( BEA->BEA_TIPATE )
    self:oResult['idAuthOnHealthProvider'] := self:maskField( BEA->BEA_GUIPRE )
    self:oResult['specialCoverage'] := self:maskField( BEA->BEA_COBESP )
    self:oResult['accidentIndication'] := self:maskField( BEA->BEA_INDACI )
    self:oResult['attendanceProtocol'] := self:maskField( BEA->BEA_PROATE )
    self:oResult['authorizationStatus'] := self:maskField( BEA->BEA_STATUS )
    self:oResult['primaryICD'] := self:maskField( BEA->BEA_CID )
    self:oResult['authorizationDate'] := self:maskField( Dtos(BEA->BEA_DATPRO),.T. )
    self:oResult['mainAuthorizationCode'] := self:maskField( ifPls(BEA->BEA_GUIPRI,BEA->BEA_NRLBOR) )
    self:oResult['clinicalCondition'] := self:maskField( BEA->BEA_INDCLI )
    self:oResult['closingReason'] := self:maskField( BEA->BEA_TIPSAI )
    self:oResult['occupationalHealth'] := self:maskField( BEA->BEA_SAUOCU )
    self:oResult['attendanceScheme'] := self:maskField( BEA->BEA_TMREGA )
  
    self:geraBeneficiario()
    self:geraProfessional()
    self:geraRda()
    self:geraEventos()

return

method geraBeneficiario() class PLSAuthorizationsEntityExecuteStandard

    Local oBenef  := JsonObject():new()
    Local oBI3    := NIL
    local lFind   := .f. 
    
    BA1->( DbSetOrder(2) ) 
	if BA1->( MsSeek( xFilial("BA1")+BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO) ) )
        lFind := .t.
    endif

    if lFind
        oBenef['isInterchange'] := .F.
        oBenef['name']       := self:maskField( BA1->BA1_NOMUSR )
        oBenef['socialName'] := self:maskField( BA1->BA1_NOMSOC )
        oBenef['holderCPF']  := self:maskField( BA1->BA1_CPFUSR )
        oBenef['birthdate']  := self:maskField( Dtos(BA1->BA1_DATNAS),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )       
        oBenef['gender']  := self:maskField( BA1->BA1_SEXO )
        oBenef['cardExpiration']  := self:maskField( Dtos(BA1->BA1_DTVLCR),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )
        oBenef['oldSubscriberId'] := self:maskField( BA1->BA1_MATANT )
        oBenef['weight'] := Val( BA1->BA1_PESO )
        oBenef['height']  := Val( BA1->BA1_ALTURA )
        oBenef['subscriberId'] := BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)

        //Dados Plano
        self:aDadUsr := PLSDADUSR(BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO),"1",.F.,dDataBase,nil,nil,nil)
        if self:aDadUsr[1]
            oBI3 := JsonObject():new()
            oBI3['code'] := self:aDadUsr[11]
            oBI3['roomType'] := self:aDadUsr[17]
            oBI3['roomDescription'] := self:aDadUsr[21]
            oBI3['description'] := self:maskField( self:aDadUsr[14] )
            oBenef['healthInsurance'] := oBI3
        endif

        self:oResult[_tgBenef] := oBenef

    endif


return

method geraProfessional() class PLSAuthorizationsEntityExecuteStandard

    Local oProf := JsonObject():new()
    Local oCbos := JsonObject():new()

    oProf['idOnHealthInsurer'] := self:maskField( BEA->BEA_CDPFSO )
    oProf['professionalCouncil'] := self:maskField( BEA->BEA_SIGLA )
    oProf['professionalCouncilNumber'] := self:maskField( BEA->BEA_REGSOL )
    oProf['name'] := self:maskField( BEA->BEA_NOMSOL )
    oProf['stateAbbreviation'] := self:maskField( BEA->BEA_ESTSOL )

    BB0->(DbSetOrder(1)) //BB0_FILIAL+BB0_CODIGO
    if BB0->(DbSeek(xFilial("BB0")+BEA->BEA_CDPFSO))
        oProf['phoneNumber'] := self:maskField( BB0->BB0_TEL )
        oProf['email'] := self:maskField( BB0->BB0_EMAIL )
    endIf

    //Atributo cbos
    BAQ->(DbSetOrder(1)) //BAQ_FILIAL+BAQ_CODINT+BAQ_CODESP
    if BAQ->(DbSeek(xFilial("BAQ")+PlsIntPad()+BEA->BEA_ESPSOL)) .Or. BAQ->(DbSeek(xFilial("BAQ")+PlsIntPad()+BEA->BEA_CODESP))
        oProf['cbosCode'] := self:maskField( BAQ->BAQ_CBOS )
        oCbos['code'] := self:maskField( BAQ->BAQ_CBOS )
        oCbos['specialtyCode'] := iif(!Empty(BEA->BEA_ESPSOL),BEA->BEA_ESPSOL,BEA->BEA_CODESP)
        oCbos['specialtyDescription'] := self:maskField( BAQ->BAQ_DESCRI )
        B0X->(DbSetOrder(1)) //B0X_FILIAL+B0X_CODCBO
        if B0X->(DbSeek(xFilial("B0X")+BAQ->BAQ_CBOS))
            oCbos['description'] := self:maskField( B0X->B0X_DESCBO )
        endIf
        oProf['cbos'] := oCbos
        self:oResult["cbosSol"] := self:maskField( BAQ->BAQ_CBOS )
    endIf
    self:oResult[_tgProf] := oProf

return

method geraRda() class PLSAuthorizationsEntityExecuteStandard

    Local oRda := JsonObject():new()

    BAU->(DbSetOrder(1)) //BAU_FILIAL+BAU_CODIGO
    if BAU->(DbSeek(xFilial("BAU")+BEA->BEA_CODRDA))
        oRda['name'] := PTURemChr(self:maskField( BAU->BAU_NOME ))
        oRda['cityCode'] := self:maskField( BAU->BAU_MUN )
        oRda['type'] := self:maskField( BAU->BAU_TIPPE )
        oRda['healthProviderId'] := self:maskField( BAU->BAU_CODIGO)
        oRda['officialRecord'] := self:maskField( BAU->BAU_CPFCGC)
        oRda['healthInsurerType'] := self:maskField( BAU->BAU_TIPPRE )
        oRda['blockDate'] := self:maskField( Dtos(BAU->BAU_DATBLO),.T. )
        oRda['email'] := self:maskField( BAU->BAU_EMAIL )        
    endIf
    self:oResult[_tgRda] := oRda
 
return

method geraEventos() class PLSAuthorizationsEntityExecuteStandard

    local nX            := 0
    local cCodPad       := ''
    local cCodPro       := ''
    local cCodTabela    := ''
    local cCodProc      := ''
    local lRecFacial    := .f.
    local lMV_PLRECFA   := getNewPar("MV_PLRECFA", .f.)
  
    DbSelectArea("BR8")

    self:oResult[_tgProc] := {}
    
    if ValType(self:aProcExec[4]) == 'A' .And. Len(self:aProcExec[4]) > 0

        for nX := 1 to len(self:aProcExec[4])
            BR8->(msseek(xFilial("BR8")+self:aProcExec[4,nX,3]+self:aProcExec[4,nX,4]))
            aAdd(self:oResult[_tgProc], JsonObject():new())            
            //De-Para TISS Online
            cCodPad     := self:aProcExec[4,nX,3]
            cCodPro     := self:aProcExec[4,nX,4]
            cCodTabela 	:= PLSGETVINC("BTU_CDTERM", "BR4", .F., "87",  cCodPad,.T.)
            cCodProc 	:= PLSGETVINC("BTU_CDTERM", "BR8", .F., cCodPad, cCodPad+cCodPro, .F. ,self:aTabDup, @cCodTabela, .t.)
            
            self:oResult[_tgProc,nX,'tableCode'] := self:maskField( cCodTabela )
            self:oResult[_tgProc,nX,'procedureCode'] := self:maskField( cCodProc )
            self:oResult[_tgProc,nX,'requestedQuantity'] := Val(self:aProcExec[4,nX,6])
            self:oResult[_tgProc,nX,'authorizedQuantity'] := Val(self:aProcExec[4,nX,7])
            self:oResult[_tgProc,nX,'procedureDescription'] := self:maskField( PTURemChr(self:aProcExec[4,nX,5]) )
            self:oResult[_tgProc,nX,'balance'] := self:maskField( self:aProcExec[4,nX,13] )
            self:oResult[_tgProc,nX,'inSerie'] := self:isSeriado( cCodPad,cCodPro )

            if BR8->(FieldPos("BR8_ANEHAT")) > 0
                self:oResult[_tgProc,nX,'mandatoryAttachment'] := self:obrigaAnex( cCodPad,cCodPro )
            endif
          
            if BAU->(fieldpos("BAU_RECFAC")) > 0 .and. BR8->(fieldpos("BR8_RECFAC")) > 0
                if ',3,' $ ','+alltrim(BAU->BAU_RECFAC)+','
                    if lMV_PLRECFA .or. ',3,' $ ','+alltrim(BR8->BR8_RECFAC)+','
                        lRecFacial := .t.
                    endif
                endif
            endif

            self:oResult[_tgProc,nX,'mandatoryFacial']  := lRecFacial
            self:oResult[_tgProc,nX,'facialType']       := iif(BAU->(fieldpos("BAU_DIMENS")) > 0,BAU->BAU_DIMENS,'')
            
            //Campos BE2
            BE2->(DbSetOrder(1)) //BE2_FILIAL+BE2_OPEMOV+BE2_ANOAUT+BE2_MESAUT+BE2_NUMAUT+BE2_SEQUEN
            if BE2->(DbSeek(xFilial("BE2")+self:cGuia+self:aProcExec[4,nX,2]))
                self:oResult[_tgProc,nX,'status'] := iif(BE2->BE2_AUDITO == '1',2,Val(BE2->BE2_STATUS))            
                self:oResult[_tgProc,nX,'auditing'] := iif(BE2->BE2_AUDITO == '1',.T.,.F.)
                self:oResult[_tgProc,nX,'procedureType'] := self:maskField( BE2->BE2_TPPROC )
                self:oResult[_tgProc,nX,'authLevelKey'] := self:maskField( BE2->BE2_CHVNIV )
                self:oResult[_tgProc,nX,'authLevel'] := 'PLS'
            endIf

            //Dados de Coparticipacao
            if BE2->BE2_STATUS == "1" .And. Val(self:aProcExec[4,nX,7]) > 0 .And. self:aProcExec[4,nX,13] > 0
                self:calcCopart(nX,cCodPad,cCodPro)
            endIf

            //Criticas
            self:oResult[_tgProc,nX,_tgRejec] := {}
            self:geraCriticas(nX,cCodPad,cCodPro)
        next
    endif

    BR8->(DbCloseArea())

return

method geraCriticas(nX,cCodPad,cCodPro) class PLSAuthorizationsEntityExecuteStandard

    Local lWhile := .T.
    Local aCriticas := {}
    Local cCodCri := ''
    Local cDesCri := ''
    Local nPos := 0
    Local nY := 0

    while lWhile
        if (nPos := Ascan(self:aProcExec[5] ,{|x| !Empty(x[1]) .And. Alltrim(x[6]) == cCodPad .And. Alltrim(x[7]) == cCodPro }) ) > 0

            cCodCri := self:aProcExec[5,nPos,1]
            cDesCri := Alltrim(self:aProcExec[5,nPos,2])
            self:aProcExec[5,nPos,1] := ""

            if aScan(aCriticas,{|x| x[1] == cCodCri .And. x[2] == cCodPad .And. x[3] == cCodPro}) == 0
                aadd(aCriticas,{cCodCri,cCodPad,cCodPro,cDesCri})
            endIf
        else
            lWhile := .F.
        endIf
    endDo

    for nY := 1 to len(aCriticas)
        
        //Marca evento como nao autorizado
        self:oResult[_tgProc,nX,'authorizedQuantity'] := 0
        self:oResult[_tgProc,nX,'status'] := iif(BE2->BE2_AUDITO == '1',2,0)

        aAdd(self:oResult[_tgProc,nX,_tgRejec], JsonObject():new())
        self:oResult[_tgProc,nX,_tgRejec,nY,'authorizationNumber'] := self:cGuia
        self:oResult[_tgProc,nX,_tgRejec,nY,'code'] := aCriticas[nY,1]
        self:oResult[_tgProc,nX,_tgRejec,nY,'description'] := aCriticas[nY,4]
        self:oResult[_tgProc,nX,_tgRejec,nY,'procedureSequence'] := self:aProcExec[4,nX,2]
        if BCT->( MsSeek( xFilial("BCT")+PlsIntPad()+aCriticas[nY,1] ) ) .And. !Empty(BCT->BCT_GLTISS)
            self:oResult[_tgProc,nX,_tgRejec,nY,'idTiss'] := Alltrim(BCT->BCT_GLTISS)
        endIf

    next

return

method calcCopart(nX,cCodPad,cCodPro) class PLSAuthorizationsEntityExecuteStandard
        
    Local lPagtoRDA := .F.
    Local cTipoGuia := "02"
    Local aValor := {}

    if len(self:aDadUsr) > 0 
        aValor := PLSCALCCOP(cCodPad, cCodPro, Substr(Dtos(dDataBase),5,2), Substr(Dtos(dDataBase),1,4), ;
                            self:aProcExec[3,1], BEA->BEA_CODESP,/*cSubEsp*/, BEA->BEA_CODLOC, Val(self:aProcExec[4,nX,7]), ;
                            dDatabase, .T.,/*cObsoleto*/,/*nVlrEve*/,/*cGrpInt*/,self:aDadUsr, ;
                            /*cPadInt*/, /*cPadCon*/, /*aQtdPer*/, iif(len(self:aDadUsr)>91,self:aDadUsr[92],"") ,/*nVlrApr*/, .T., ;
                            /*lCompra*/, /*cHorPro*/,/*aRdas*/, /*cOpeRda*/, /*cTipPrefor*/, /*cProRel*/,/*nPrPrRl*/,;
                            /*aValAcu*/, /*cNivAut*/, /*cChvNiv*/, /*dDatCir*/, /*cHorCir*/,/*cCid*/,;
                            /*aUnidsBlo*/,cTipoGuia, /*aCobAcu*/, /*nVlrAprPag*/,/*aVlBloq*/, /*cModCob*/,;
                            /*nVlrPagBru*/, /*nRegBD6*/, /*lCirurgico*/, /*nPerVia*/, /*cRegPag*/,/*cRegCob*/,;
                            /*nNOTUSED1*/, /*nNOTUSED2*/, /*aPacote*/,/*cChaveGui*/,/*cSequen*/, /*aRetCom*/,;
                            /*cRegInt*/,/*cFinAte*/, /*aVetPag*/, /*cChaveLib*/,/*lAuditoria*/,/*cDente*/,/*cFaces*/,;
                            .F.,/*cHorPro6C*/,/*lAneste*/, /*nVlrPagLiq*/,/*cTipAdm*/)
    
        lPagtoRDA := Len(aValor) >= 18 .and. ValType(aValor[18]) == "C" .And. aValor[18] == "1" 
    endIf

    self:oResult[_tgProc,nX,'paymentOnHealthProvider'] := lPagtoRDA

Return

method isSeriado(cCodPad,cCodPro) class PLSAuthorizationsEntityExecuteStandard
    Local cQuery := ""
    Local lSeriado := .F.
    Default cCodPro := ""

    cQuery := " SELECT BJE_TIPO AS TIPO FROM " + RetSqlName("BR8") + " BR8 "
    cQuery += " LEFT JOIN " + RetSqlName("BJE") + " BJE "
    cQuery += " ON BJE_FILIAL = '" + xFilial("BJE") + "' "
    cQuery += " AND BJE.BJE_CODIGO = BR8.BR8_CLASSE " 
    cQuery += " AND BJE.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE BR8_FILIAL = '" + xFilial("BR8") + "' "
    cQuery += " AND BR8_CODPAD = '" + cCodPad +  "'"
    cQuery += " AND BR8_CODPSA = '" + cCodPro +  "'"
    cQuery += " AND BR8.D_E_L_E_T_ = ' ' "

    dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQuery),"TRB",.F.,.T.)

    if !TRB->(eof())
        lSeriado := TRB->TIPO == '2'
    endif

    TRB->(dbclosearea())
    
return lSeriado

method obrigaAnex(cCodPad,cCodPro) class PLSAuthorizationsEntityExecuteStandard
    Local cQuery := ""
    Local cObrigaAne := .F.
    Default cCodPro := ""
    Default cCodPad := ""

    cQuery := " SELECT BR8_ANEHAT FROM " + RetSqlName("BR8") + " BR8 "
    cQuery += " WHERE BR8_FILIAL = '" + xFilial("BR8") + "' "
    cQuery += " AND BR8_CODPAD = '" + cCodPad +  "'"
    cQuery += " AND BR8_CODPSA = '" + cCodPro +  "'"
    cQuery += " AND BR8.D_E_L_E_T_ = ' ' "

    dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQuery),"TRB",.F.,.T.)

    if !TRB->(eof())
        cObrigaAne := Alltrim(TRB->BR8_ANEHAT)
    endif

    TRB->(dbclosearea())
    
return cObrigaAne
