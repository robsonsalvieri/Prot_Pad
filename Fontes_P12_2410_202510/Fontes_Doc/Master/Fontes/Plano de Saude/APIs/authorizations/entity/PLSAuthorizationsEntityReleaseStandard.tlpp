#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'
#DEFINE _tgBenef 'beneficiary'
#DEFINE _tgProf 'professional'
#DEFINE _tgRda 'healthProvider'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityReleaseStandard
.
@author  sakai
@version P12
@since   18/07/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityReleaseStandard from PLSAuthorizationsEntityLib


    public method new()
    public method geraJson()
    private method geraBeneficiario()
    private method geraRda()

endClass

method new(cGuia) class PLSAuthorizationsEntityReleaseStandard
    
    _Super:New(cGuia)
    self:cGuia := cGuia

Return self


method geraJson(lRet,cMsg) class PLSAuthorizationsEntityReleaseStandard

    BEA->( DbSetOrder(1) )//BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT
    BEA->( MsSeek( xFilial("BEA")+self:cGuia ) )

    self:oResult["actionReturn"]  := lRet
    self:oResult["actionMessage"] := cMsg
    self:oResult["idOnHealthProvider"] := self:cGuia
    self:oResult["authorizationDate"] := self:maskField( Dtos(BEA->BEA_DATPRO),.T. )

    self:geraBeneficiario()
    self:geraRda()

return

method geraBeneficiario() class PLSAuthorizationsEntityReleaseStandard

    Local oBenef := JsonObject():new()
    Local aRetFun := {}
    Local oBI3    := NIL
    
    BA1->( DbSetOrder(2) ) //BA1_FILIAL + BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO
	if BA1->( MsSeek( xFilial("BA1")+BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO) ) )

        oBenef['isInterchange'] := .F.
        oBenef['name']       := self:maskField( BA1->BA1_NOMUSR )
        oBenef['socialName'] := self:maskField( BA1->BA1_NOMSOC )
        oBenef['holderCPF']  := self:maskField( BA1->BA1_CPFUSR )
        oBenef['birthdate']  := self:maskField( Dtos(BA1->BA1_DATNAS),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )       
        oBenef['gender']  := self:maskField( BA1->BA1_SEXO )
        oBenef['cardExpiration']  := self:maskField( Dtos(BA1->BA1_DTVLCR),.T. )
        oBenef['holderRelationship'] := self:maskField( BA1->BA1_TIPUSU )
        oBenef['oldSubscriberId'] := self:maskField( BA1->BA1_MATANT )
        oBenef['weight'] := Val( BA1->BA1_PESO )
        oBenef['height']  := Val( BA1->BA1_ALTURA )
        oBenef['subscriberId'] := BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO)
        oBenef['phoneNumber'] := self:maskField( BA1->BA1_TELEFO )
		oBenef['blockedDate'] := self:maskField( Dtos(BA1->BA1_DATBLO),.T. )
		oBenef['originalHealthInsurerCode'] := self:maskField( BA1->BA1_OPEORI )

        //Dados Plano
        aRetFun := PLSDADUSR(BA1->(BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO),"1",.F.,dDataBase,nil,nil,nil)
        if aRetFun[1]
            oBI3 := JsonObject():new()
            oBI3['code'] := aRetFun[11]
            oBI3['roomType'] := aRetFun[17]
            oBI3['description'] := self:maskField( aRetFun[14] )
            oBenef['healthInsurance'] := oBI3
        endIf

        self:oResult[_tgBenef] := oBenef
    endIf

return

method geraRda() class PLSAuthorizationsEntityReleaseStandard

    Local oRda := JsonObject():new()

    BAU->(DbSetOrder(1)) //BAU_FILIAL+BAU_CODIGO
    if BAU->(DbSeek(xFilial("BAU")+BEA->BEA_CODRDA))
        oRda['name'] := self:maskField( BAU->BAU_NOME )
        oRda['cityCode'] := self:maskField( BAU->BAU_MUN )
        oRda['type'] := self:maskField( BAU->BAU_TIPPE )
        oRda['healthProviderId'] := self:maskField( BAU->BAU_CODIGO)
        oRda['officialRecord'] := self:maskField( BAU->BAU_CPFCGC)
        oRda['healthInsurerType'] := self:maskField( BAU->BAU_TIPPRE )
        oRda['blockDate'] := self:maskField( Dtos(BAU->BAU_DATBLO),.T. )
        oRda['email'] := self:maskField( BAU->BAU_EMAIL )        
    endIf
    self:oResult[_tgRda] := oRda
  
return