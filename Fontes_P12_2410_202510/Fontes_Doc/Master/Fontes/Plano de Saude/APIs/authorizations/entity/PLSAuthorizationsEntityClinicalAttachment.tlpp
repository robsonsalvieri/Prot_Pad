#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#DEFINE _tgProc 'procedures'
#DEFINE _tgRejec 'rejectionCauses'
#DEFINE _tgBenef 'beneficiary'
#DEFINE _tgAttac 'attachments'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsEntityClinicalAttachment
.
@author  Lucas Nonato
@version P12
@since   29/05/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsEntityClinicalAttachment from PLSAuthorizationsEntityLib

    public method new()
    public method montaCab()
    public method montaEve()
    public method montaCri()
    public method getStatusTAE()

endClass

method new(cGuia, lShowDeniedProc) class PLSAuthorizationsEntityClinicalAttachment
    
    _Super:New(cGuia, lShowDeniedProc)
    self:cGuia   := cGuia
    self:oDaoCab := PLSAuthorizationsDaoB4A():new(self:cGuia)
    self:oDaoEve := PLSAuthorizationsDaoB4C():new(self:cGuia)
    self:oDaoCri := PLSAuthorizationsDaoBEG():new(self:cGuia)
    self:oDaoBen := PLSAuthorizationsDaoBA1():new(self:cGuia)

Return self

method montaCab() class PLSAuthorizationsEntityClinicalAttachment
    
    Local cAliTrb       := ''
    Local cMatric       := ''
    Local cStatus       := ''
    
    self:getDao('CAB'):buscaGuia()
    cAliTrb := self:getAliasTemp('CAB')
    
    cStatus := self:getStatusGui((cAliTrb)->B4A_STATUS, (cAliTrb)->B4A_CANCEL)

    B4A->(dbsetorder(1))
    B4A->(dbgoto((cAliTrb)->RecnoB4A))
    //Campos B4A
    self:oResult["attendanceProtocol"]          := self:maskField( (cAliTrb)->B4A_PROATE )
    self:oResult["attachNumber"]                := self:maskField( (cAliTrb)->(B4A_OPEMOV+B4A_ANOAUT+B4A_MESAUT+B4A_NUMAUT))
    self:oResult["mainAuthorizationCode"]       := self:maskField( (cAliTrb)->B4A_GUIREF )
    self:oResult["password"]                    := self:maskField( (cAliTrb)->B4A_SENHA )
    self:oResult["authorizationStatus"]         := self:maskField( cStatus )
    self:oResult["passwordExpireDate"]          := ""
    self:oResult["requestedDate"]               := self:maskField( (cAliTrb)->B4A_DATSOL,.t. )
    self:oResult["authorizedDate"]              := self:maskField( (cAliTrb)->B4A_DATPRO,.t. )
    self:oResult["healthProviderId"]            := self:maskField( cvaltochar((cAliTrb)->BEA_CODRDA) )
    self:oResult["subscriberId"]                := self:maskField( (cAliTrb)->(B4A_OPEUSR+B4A_CODEMP+B4A_MATRIC+B4A_TIPREG+B4A_DIGITO) )
    self:oResult["phoneNumber"]                 := self:maskField( (cAliTrb)->B4A_TELSOL )
    self:oResult["email"]                       := self:maskField( (cAliTrb)->B4A_EMASOL )
    self:oResult["technicalJustification"]      := self:quebraLinha( B4A->B4A_JUSTTE )
    self:oResult["drugAdministrationStartDate"] := self:maskField( (cAliTrb)->B4A_DATPRE )
    self:oResult["totalCycleDosage"]            := self:maskField( (cAliTrb)->B4A_DOSTOT )
    self:oResult["attachType"]                  := self:maskField( iIf((cAliTrb)->B4A_TIPANE=="1","14",iIf((cAliTrb)->B4A_TIPANE=="2","13","12"))) 
    self:oResult["tissSequentialTransaction"]   := self:maskField( (cAliTrb)->B4A_LOTGUI )    
    self:oResult["beneficiaryWeight"]           := self:maskField( (cAliTrb)->B4A_PESO )
    self:oResult["beneficiaryHeight"]           := self:maskField( (cAliTrb)->B4A_ALTURA )
    self:oResult["chemotherapyType"]            := self:maskField( (cAliTrb)->B4A_TIPQUI )
    self:oResult["tumor"]                       := self:maskField( (cAliTrb)->B4A_TUMOR )
    self:oResult["nodule"]                      := self:maskField( (cAliTrb)->B4A_NODULO )
    self:oResult["metastasis"]                  := self:maskField( (cAliTrb)->B4A_METAST )
    self:oResult["therapeuticPlan"]             := self:quebraLinha( B4A->B4A_PLATER,85 )
    self:oResult["irradiatedArea"]              := self:quebraLinha( B4A->B4A_AREA )
    self:oResult["radioApplicationDate"]        := self:maskField( (cAliTrb)->B4A_DATIRR )
    self:oResult["expectedCyclesNumber"]        := self:maskField( (cAliTrb)->B4A_NROCIC )
    self:oResult["currentCycle"]                := self:maskField( (cAliTrb)->B4A_CICATU )
    self:oResult["intervalBetweenCycles"]       := self:maskField( (cAliTrb)->B4A_INTCIC )
    self:oResult["diagnosisDate"]               := self:maskField( (cAliTrb)->B4A_DATDIA,.t. )
    self:oResult["primaryICD"]                  := self:maskField( (cAliTrb)->B4A_CIDPRI )
    self:oResult["secondaryICD"]                := self:maskField( (cAliTrb)->B4A_CIDSEC )
    self:oResult["terciaryICD"]                 := self:maskField( (cAliTrb)->B4A_CIDTER )
    self:oResult["quaternaryICD"]               := self:maskField( (cAliTrb)->B4A_CIDQUA )
    self:oResult["imageDiagnosis"]              := self:maskField( (cAliTrb)->B4A_DIAIMG )
    self:oResult["staging"]                     := self:maskField( (cAliTrb)->B4A_ESTADI )
    self:oResult["ecog"]                        := self:maskField( (cAliTrb)->B4A_ECOG )
    self:oResult["purpose"]                     := self:maskField( (cAliTrb)->B4A_FINALI )
    self:oResult["histopathologicalDiagnosis"]  := self:quebraLinha( B4A->B4A_DIAGCH, 105 )
    self:oResult["relevantInformations"]        := self:quebraLinha( B4A->B4A_INFREL, 100 )
    self:oResult["surgery"]                     := self:quebraLinha( B4A->B4A_CIRURG )
    self:oResult["surgeryDate"]                 := self:maskField( (cAliTrb)->B4A_DATCIR,.t. )
    self:oResult["chemotherapy"]                := self:quebraLinha( B4A->B4A_QUIMIO )
    self:oResult["chemoApplicationDate"]        := self:maskField( (cAliTrb)->B4A_DATQUI,.t. )
    self:oResult["radiationFieldsNumber"]       := self:maskField( (cAliTrb)->B4A_NROCAM )
    self:oResult["dailyDose"]                   := self:maskField( (cAliTrb)->B4A_DOSDIA )
    self:oResult["totalDosage"]                 := self:maskField( (cAliTrb)->B4A_DOSTOT )
    self:oResult["numberOfDays"]                := self:maskField( (cAliTrb)->B4A_NRODIA )
    self:oResult["daysFromCurrentCycle"]        := self:maskField( (cAliTrb)->B4A_DCICAT )
    self:oResult["attendanceNote"]              := self:quebraLinha( B4A->B4A_OBSERV )
    self:oResult["journey"]                     := self:oResult["attachType"]
    self:oResult["materialSpec"]                := self:quebraLinha( B4A->B4A_ESPMAT)
    self:oResult["signature"]                   := self:getStatusTAE() 
    self:oResult["authorizationMode"]           := self:getAttNote(self:cGuia,'B4A')

    cMatric := (cAliTrb)->(B4A_OPEUSR+B4A_CODEMP+B4A_MATRIC+B4A_TIPREG+B4A_DIGITO)    
    self:cRecno := cValToChar((cAliTrb)->RecnoB4A)
    
    if self:existExpand('beneficiary')
        self:oResult[_tgBenef] := self:montaBenef(cMatric)
    endIf

    if self:existExpand('healthProvider')
        self:oResult['healthProvider'] := self:montaRDA((cAliTrb)->BEA_CODRDA)
    endIf

    if self:existExpand('professional')
        self:oResult['professional']            := self:montaBB0((cAliTrb)->BEA_CDPFSO,ifPls((cAliTrb)->BEA_ESPSOL,(cAliTrb)->BEA_CODESP))
        self:oResult['professional']['name']    := alltrim((cAliTrb)->B4A_NOMSOL)
    endIf

    if self:existExpand('attachments')
        self:oResult[_tgAttac] := self:montaAnexos()
    endIf

    self:getDao('CAB'):closeQuery()
    
return

method montaEve() class PLSAuthorizationsEntityClinicalAttachment

    local nX := 0
    local cAliTrb := ''

    if self:existExpand('procedures')

        self:oResult[_tgProc] := {}
        if self:getDao('EVE'):buscaEve()

            cAliTrb := self:getAliasTemp('EVE')        
            while !self:getDao('EVE'):isEof()
                if self:lImpNAut .OR. (cAliTrb)->B4C_STATUS <> '0'

                    nX ++
                    aAdd(self:oResult[_tgProc], JsonObject():new())

                    self:oResult[_tgProc,nX,'sequence']                     := self:maskField( (cAliTrb)->B4C_SEQUEN )
                    self:oResult[_tgProc,nX,'authLevel']                    := self:maskField( (cAliTrb)->B4C_NIVAUT )
                    self:oResult[_tgProc,nX,'authLevelKey']                 := self:maskField( (cAliTrb)->B4C_CHVNIV )
                    self:oResult[_tgProc,nX,'status']                       := self:maskField( (cAliTrb)->B4C_STATUS )
                    self:oResult[_tgProc,nX,'authorizedQuantity']           := iif((cAliTrb)->B4C_STATUS=='0',0, (cAliTrb)->B4C_QTDPRO )
                    self:oResult[_tgProc,nX,'auditing']                     := self:maskField( (cAliTrb)->B4C_AUDITO )
                    self:oResult[_tgProc,nX,"optionOrder"]                  := self:maskField( (cAliTrb)->B4C_OPCAO )
                    self:oResult[_tgProc,nX,"requestedQuantity"]            := self:maskField( (cAliTrb)->B4C_QTDSOL )
                    self:oResult[_tgProc,nX,"unitaryWorth"]                 := self:maskField( (cAliTrb)->B4C_VLRUNT )
                    self:oResult[_tgProc,nX,"unitaryWorthAuthorized"]       := self:maskField( (cAliTrb)->B4C_VLRUNA )
                    self:oResult[_tgProc,nX,"anvisaId"]                     := self:maskField( (cAliTrb)->B4C_REGANV )
                    self:oResult[_tgProc,nX,"manufacturer"]                 := self:maskField( (cAliTrb)->B4C_OPCAO )
                    self:oResult[_tgProc,nX,"manufacturerReference"]        := self:maskField( (cAliTrb)->B4C_REFMAF )
                    self:oResult[_tgProc,nX,"operationAuthorizationNumber"] := self:maskField( (cAliTrb)->B4C_AUTFUN )
                    self:oResult[_tgProc,nX,"unitOfMeasurement"]            := self:maskField( (cAliTrb)->B4C_UNMED )
                    self:oResult[_tgProc,nX,"accessWay"]                    := self:maskField( (cAliTrb)->B4C_VIAADM )
                    self:oResult[_tgProc,nX,"frequency"]                    := self:maskField( (cAliTrb)->B4C_FREQUE )                
                    self:oResult[_tgProc,nX,"drugAdministrationStartDate"]  := self:maskField( (cAliTrb)->B4C_DATPRO,.t. )
                    self:oResult[_tgProc,nX,'auditObservation']             := self:getObservacao( self:cRecno, 'B4A', Alltrim((cAliTrb)->B4C_CODPRO), Alltrim((cAliTrb)->B4C_SEQUEN) )

                    if self:oResult["attachType"] == '13' .and. (cAliTrb)->B4C_STATUS=='0'
                        self:oResult[_tgProc,nX,"requestedQuantity"] := 0
                    endif
                    cCodPad     := Alltrim((cAliTrb)->B4C_CODPAD)
                    cCodPro     := Alltrim((cAliTrb)->B4C_CODPRO)
                    cCodTabela 	:= PLSGETVINC("BTU_CDTERM", "BR4", .F., "87",  cCodPad,.T.)
                    cCodProc 	:= PLSGETVINC("BTU_CDTERM", "BR8", .F., cCodPad, cCodPad+cCodPro, .F. ,self:aTabDup, @cCodTabela, .t.)

                    self:oResult[_tgProc,nX,'tableCode'] := self:maskField( cCodTabela )
                    self:oResult[_tgProc,nX,'procedureCode'] := self:maskField( cCodProc )
                    self:oResult[_tgProc,nX,"procedureDescription"] := self:maskField( PTURemChr((cAliTrb)->B4C_DESPRO) )
                    self:oResult[_tgProc,nX,"procedureType"] := Posicione("BR8",1,xFilial("BR8") + cCodPad + cCodPro, "BR8_TPPROC")

                    self:montaCri(nX,(cAliTrb)->B4C_SEQUEN)         
                endif
                self:getDao('EVE'):dbSkip()
            endDo

        endIf
        self:getDao('EVE'):closeQuery()
    endIf

return


method montaCri(nItem,cSequen) class PLSAuthorizationsEntityClinicalAttachment

    local cAliTrb := ''
    local cSeqCri := ''
    local cCodGloAtu := ''
    local cCodGloOld := ''

    if self:existExpand('rejectionCauses')

        self:oResult[_tgProc,nItem,_tgRejec] := {}

        if self:getDao('CRI'):buscaCrit(cSequen)
            
            self:resetPosCri()
            cAliTrb := self:getAliasTemp('CRI')
            while !self:getDao('CRI'):isEof()

                cCodGloAtu := (cAliTrb)->CODGLO
                cSeqCri    := (cAliTrb)->SEQUEN

                //Motivo de Negativa RN 623
                if cCodGloOld <> cCodGloAtu .And. !Empty(cCodGloAtu)
                    if !Empty(cCodGloOld)
                        self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
                    endIf
                    cCodGloOld := cCodGloAtu
                endIf

                //Critica Atual
                self:setCriticas(nItem,(cAliTrb)->SEQUEN,(cAliTrb)->CODGLO,(cAliTrb)->SEQCRI,(cAliTrb)->DESGLO)
                self:getDao('CRI'):dbSkip()
            endDo

            //Chamo novamente para pegar o ultimo registro apos o EOF
            self:getMotivoNegativa(nItem,cSeqCri,cCodGloOld)
        endIf
        self:getDao('CRI'):closeQuery()
    endIf

return

/*/{Protheus.doc} getStatusTAE

@type  Class
@author Lucas Nonato
@since 01/03/2020
/*/
Method getStatusTAE() Class PLSAuthorizationsEntityClinicalAttachment
local cGrvTAE   := GetNewPar("MV_PLTPTAE", '')
local cRet      := ''

if !empty(cGrvTAE)
    cRet := PLSA203STA(self:oResult["attachNumber"])
endif

return cRet
