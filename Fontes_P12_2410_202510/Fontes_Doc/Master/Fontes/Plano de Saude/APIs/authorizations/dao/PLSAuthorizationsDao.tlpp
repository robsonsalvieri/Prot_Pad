#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsDao

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsDao
    
    Data cGuia as Character
    Data cAliasTemp As Character
    Data cQuery as Character
    Data aGuiasPrest as Array
    Data nPage as Integer
    Data nPageSize as Integer
    Data cDb as Character
    Data cSubstrChar as Character

    Public Method New()
    Public Method getAliasTemp()
    Public Method closeQuery()
    Public Method dbSkip()
    Public Method isEof()
    Public Method getGuiasPrest()
    Public Method getQryPage()
    Public Method setPage(cPage)
    Public Method setPageSize(cPageSize)
    Method executaQuery()
    Method aliasSelected()
    Method setQuery(cQuery)
    Method getQuery()

EndClass


Method New(cGuia) class PLSAuthorizationsDao
    
    self:cGuia      := cGuia
    self:cAliasTemp := ""
    self:cQuery     := ""
    self:aGuiasPrest := {}
    self:nPage      := 1
    self:nPageSize  := 10
    self:cDB 		:= TcGetDB()
    self:cSubstrChar := iif(self:cDB $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING")

Return self

Method getAliasTemp() Class PLSAuthorizationsDao
	if empty(self:cAliasTemp)
		self:cAliasTemp := getNextAlias()
	endif
Return self:cAliasTemp

Method setQuery(cQuery) Class PLSAuthorizationsDao
	self:cQuery := cQuery
Return

Method getQuery() Class PLSAuthorizationsDao
Return self:cQuery

Method executaQuery() Class PLSAuthorizationsDao
	Local lFound := .F.

	self:closeQuery()
	self:setQuery(self:getQuery())
	dbUseArea(.T.,"TOPCONN",TCGENQRY(,,self:getQuery()),self:getAliasTemp(),.F.,.T.)

	lFound := (self:getAliasTemp())->(!Eof())
	If !lFound
		self:closeQuery()
	EndIf

Return lFound

Method closeQuery() Class PLSAuthorizationsDao
	if self:aliasSelected()
		(self:getAliasTemp())->(dbCloseArea())
	endIf
Return

Method aliasSelected() Class PLSAuthorizationsDao
Return Select(self:getAliasTemp()) > 0

Method dbSkip() Class PLSAuthorizationsDao
    (self:getAliasTemp())->(DbSkip())
Return

Method isEof() Class PLSAuthorizationsDao
Return (self:getAliasTemp())->(Eof())

Method getGuiasPrest() class PLSAuthorizationsDao
Return self:aGuiasPrest

Method getQryPage() Class PLSAuthorizationsDao

	Local cQuery 	:= ""
	Local cNumPage  := ""

	if self:nPage > 0 .And. self:nPageSize > 0
		cNumPage := alltrim(str((self:nPage - 1) * self:nPageSize))
		cQuery += " OFFSET " + cNumPage + " ROWS FETCH NEXT " + SOMA1(cValToChar(self:nPageSize)) + " ROWS ONLY "
	endif

Return cQuery

Method setPage(cPage) Class PLSAuthorizationsDao
    self:nPage := Val(cPage)
Return

Method setPageSize(cPageSize) Class PLSAuthorizationsDao
    self:nPageSize := Val(cPageSize)
Return