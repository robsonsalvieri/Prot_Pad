#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsDaoB4A
.
@author  Lucas Nonato
@version P12
@since   30/05/2023
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsDaoB4A from PLSAuthorizationsDao
    
    Public Method New()
    Private Method getCampos()
    Public Method buscaGuia()
    Public Method bscGuiaPrestadorReenvio(cMatric, cCodRda, cLoteTiss)
    Public Method vldReenvioHAT(cMatric, cCodRda)
    Public Method buscaGuiasRelacionadas()

EndClass


Method New(cGuia) class PLSAuthorizationsDaoB4A    
    _Super:New(cGuia)
Return self

Method getCampos() class PLSAuthorizationsDaoB4A  

    Local cCampos := ""

    cCampos += 'B4A_PROATE,B4A_OPEMOV,B4A_ANOAUT,B4A_MESAUT,B4A_NUMAUT,B4A_GUIREF,B4A_SENHA,B4A_DATPRO,B4A_DATSOL,B4A_DATPRO,B4A_OPEUSR,'
    cCampos += 'B4A_CODEMP,B4A_MATRIC,B4A_TIPREG,B4A_DIGITO,B4A_TELSOL,B4A_EMASOL,B4A_DATIRR,B4A_DOSTOT,B4A_TIPANE,B4A_LOTGUI,B4A_STATUS,'
    cCampos += 'B4A_PESO,B4A_ALTURA,B4A_TIPQUI,B4A_TUMOR,B4A_NODULO,B4A_METAST,B4A_DATIRR,B4A_NROCIC,B4A_CICATU,B4A_INTCIC,B4A_DATPRE,'
    cCampos += 'B4A_DATDIA,B4A_CIDSEC,B4A_CIDTER,B4A_CIDQUA,B4A_DIAIMG,B4A_ESTADI,B4A_ECOG,B4A_FINALI,B4A_CIDPRI,B4A_NOMSOL,BEA_CODESP,'
    cCampos += 'B4A_DATCIR,B4A_QUIMIO,B4A_DATQUI,B4A_NROCAM,B4A_DOSDIA,B4A_DOSTOT,B4A_NRODIA,B4A_DCICAT,BEA_CODRDA,BEA_CDPFSO,BEA_ESPSOL,B4A_CANCEL,'
    cCampos += 'B4A.R_E_C_N_O_ as RecnoB4A '

Return cCampos


Method buscaGuia() class PLSAuthorizationsDaoB4A

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4A")+" B4A "
    cQuery += " LEFT JOIN " + RetSqlName("BEA") + " BEA"
	cQuery += "   ON BEA_FILIAL = '"  + xFilial("BEA")+ "' "
	cQuery += "   AND BEA_OPEMOV = "+self:cSubstrChar+"(B4A_GUIREF,01,04) "
	cQuery += "   AND BEA_ANOAUT = "+self:cSubstrChar+"(B4A_GUIREF,05,04) "
	cQuery += "   AND BEA_MESAUT = "+self:cSubstrChar+"(B4A_GUIREF,09,02) "
	cQuery += "   AND BEA_NUMAUT = "+self:cSubstrChar+"(B4A_GUIREF,11,08) "
	cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE B4A_FILIAL = '" + xFilial("B4A") +"' "	
    cQuery += "   AND B4A_OPEMOV = '" + Substr(self:cGuia,01,04) + "' "
    cQuery += "   AND B4A_ANOAUT = '" + Substr(self:cGuia,05,04) + "' "
    cQuery += "   AND B4A_MESAUT = '" + Substr(self:cGuia,09,02) + "' "
    cQuery += "   AND B4A_NUMAUT = '" + Substr(self:cGuia,11,08) + "' "
    cQuery += "   AND B4A.D_E_L_E_T_ = ' ' "
   
    self:setQuery(PLSConSQL(cQuery))
	lFound := self:executaQuery()
    
Return lFound

Method bscGuiaPrestadorReenvio(cMatric, cCodRda, cLoteTiss) class PLSAuthorizationsDaoB4A

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4A")+" B4A "
    cQuery += " LEFT JOIN " + RetSqlName("BEA") + " BEA"
	cQuery += "   ON BEA_FILIAL = '"  + xFilial("BEA")+ "' "
	cQuery += "   AND BEA_OPEMOV = "+self:cSubstrChar+"(B4A_GUIREF,01,04) "
	cQuery += "   AND BEA_ANOAUT = "+self:cSubstrChar+"(B4A_GUIREF,05,04) "
	cQuery += "   AND BEA_MESAUT = "+self:cSubstrChar+"(B4A_GUIREF,09,02) "
	cQuery += "   AND BEA_NUMAUT = "+self:cSubstrChar+"(B4A_GUIREF,11,08) "
	cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE B4A_FILIAL = '" + xFilial("B4A") +"' "	
    cQuery += "   AND B4A_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND B4A_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND B4A_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND B4A_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND B4A_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND B4A_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND B4A_LOTGUI = '" + cLoteTiss + "' "
    cQuery += "   AND B4A.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()
    
Return lFound

Method vldReenvioHAT(cMatric, cCodRda) class PLSAuthorizationsDaoB4A

    Local cQuery := ""
    Local lFound := .F.
    Local lRet   := .T.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("B4A")+" B4A "
    cQuery += " LEFT JOIN " + RetSqlName("BEA") + " BEA"
	cQuery += "   ON BEA_FILIAL = '"  + xFilial("BEA")+ "' "
	cQuery += "   AND BEA_OPEMOV = "+self:cSubstrChar+"(B4A_GUIREF,01,04) "
	cQuery += "   AND BEA_ANOAUT = "+self:cSubstrChar+"(B4A_GUIREF,05,04) "
	cQuery += "   AND BEA_MESAUT = "+self:cSubstrChar+"(B4A_GUIREF,09,02) "
	cQuery += "   AND BEA_NUMAUT = "+self:cSubstrChar+"(B4A_GUIREF,11,08) "
	cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE B4A_FILIAL = '" + xFilial("B4A") +"' "	
    cQuery += "   AND B4A_OPEUSR = '" + Substr(cMatric,01,4) + "' "
    cQuery += "   AND B4A_CODEMP = '" + Substr(cMatric,05,4) + "' "
    cQuery += "   AND B4A_MATRIC = '" + Substr(cMatric,09,6) + "' "
    cQuery += "   AND B4A_TIPREG = '" + Substr(cMatric,15,2) + "' "
    cQuery += "   AND B4A_DIGITO = '" + Substr(cMatric,17,1) + "' "
    cQuery += "   AND B4A_DATPRO = '" + Dtos(dDataBase)  + "' "
    cQuery += "   AND B4A.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()
    
   //Se achou a guia, retorna falso pois nao podera realizar o reenvio
    if lFound
        lRet := .F.
        
        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(self:aGuiasPrest,(cAliTrb)->(B4A_OPEMOV+B4A_ANOAUT+B4A_MESAUT+B4A_NUMAUT))
            self:dbSkip()
        endDo

    endIf

Return lRet

Method buscaGuiasRelacionadas() class PLSAuthorizationsDaoB4A

    Local cQuery  := ""
    Local cAliTrb := ""
    Local lFound  := .F.

    cQuery += " SELECT "
    cQuery += " B4A_OPEMOV, B4A_ANOAUT, B4A_MESAUT, B4A_NUMAUT "

    cQuery += " FROM " + RetSqlName("B4A")+" B4A "
    cQuery += " LEFT JOIN " + RetSqlName("BEA") + " BEA"
	cQuery += "   ON BEA_FILIAL = '"  + xFilial("BEA")+ "' "
	cQuery += "   AND BEA_OPEMOV = "+self:cSubstrChar+"(B4A_GUIREF,01,04) "
	cQuery += "   AND BEA_ANOAUT = "+self:cSubstrChar+"(B4A_GUIREF,05,04) "
	cQuery += "   AND BEA_MESAUT = "+self:cSubstrChar+"(B4A_GUIREF,09,02) "
	cQuery += "   AND BEA_NUMAUT = "+self:cSubstrChar+"(B4A_GUIREF,11,08) "
	cQuery += "   AND BEA.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE B4A_FILIAL = '" + xFilial("B4A") +"' "
    cQuery += "   AND B4A_GUIREF = '" + self:cGuia + "' "
    cQuery += "   AND B4A.D_E_L_E_T_ = ' ' "
    
    cQuery += " ORDER BY "
    cQuery += "    B4A_OPEMOV, "
    cQuery += "    B4A_ANOAUT, "
    cQuery += "    B4A_MESAUT, "
    cQuery += "    B4A_NUMAUT "
    cQuery += self:getQryPage()

    self:setQuery(cQuery)
	lFound := self:executaQuery()

    if lFound

        cAliTrb := self:getAliasTemp()
        while !self:isEof()
            aadd(self:aGuiasPrest,(cAliTrb)->(B4A_OPEMOV+B4A_ANOAUT+B4A_MESAUT+B4A_NUMAUT))
            self:dbSkip()
        endDo

    endIf

Return lFound
