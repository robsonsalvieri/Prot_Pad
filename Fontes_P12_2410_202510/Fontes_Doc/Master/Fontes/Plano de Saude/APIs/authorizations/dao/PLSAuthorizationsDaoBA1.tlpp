#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsDaoBA1
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsDaoBA1 from PLSAuthorizationsDao
    
    Public Method New()
    Private Method getCampos()
    Public Method buscaBeneficiario(cBenef)
   
EndClass


Method New(cGuia) class PLSAuthorizationsDaoBA1  
    _Super:New(cGuia)
Return self

Method getCampos() class PLSAuthorizationsDaoBA1  

    Local cCampos := ""

    //Campos BA1
    cCampos += " BA1_NOMUSR, BA1_NOMSOC, BA1_CPFUSR, BA1_DATNAS, BA1_MATANT, BA1_TELEFO, BA1_SEXO, BA1_TIPUSU, BA1_DTVLCR, "
    
    //Campos BTS
    cCampos += " BTS_PESO, BTS_ALTURA, BTS_NRCRNA "

Return cCampos

Method buscaBeneficiario(cBenef) class PLSAuthorizationsDaoBA1

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT "
    cQuery += self:getCampos()

    cQuery += " FROM " + RetSqlName("BA1")+" BA1 "
    
    cQuery += " INNER JOIN " + RetSqlName("BTS") + " BTS"
	cQuery += "   ON BTS_FILIAL = '"  + xFilial("BTS")+ "' "
	cQuery += "   AND BTS.BTS_MATVID = BA1.BA1_MATVID "
	cQuery += "   AND BTS.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE BA1_FILIAL = '" + xFilial("BA1") +"' "	
    cQuery += "   AND BA1_CODINT = '" + Substr(cBenef,01,04) + "' "
    cQuery += "   AND BA1_CODEMP = '" + Substr(cBenef,05,04) + "' "
    cQuery += "   AND BA1_MATRIC = '" + Substr(cBenef,09,06) + "' "
    cQuery += "   AND BA1_TIPREG = '" + Substr(cBenef,15,02) + "' "
    cQuery += "   AND BA1_DIGITO = '" + Substr(cBenef,17,01) + "' "
    cQuery += "   AND BA1.D_E_L_E_T_ = ' ' "
   
    self:setQuery(cQuery)
	lFound := self:executaQuery()
    
Return lFound