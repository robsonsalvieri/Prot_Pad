#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsRequest
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsRequest from CenRequest 

    Data oRest as Object
    Data oService as Object
    
    Public Method New()
    Public Method valida(cTipGui)
    Public Method getError()
    Public Method procReenvioHAT(cCodRda, cTipGui)
    Public Method procExecuteAuthorization()
    Public Method authorization()
    Public Method execution()
    Public Method validaLiberacao()
    Public Method checkBodyResend()
    Public Method checkBodyExecute()
    Public method checkBodyCancel()
    Public Method faultPostOption()
    Public Method executions()
    Public Method validaGuiasRelacionadas(cAlias)
    Public Method geraJsonGuiasRelacionadas(cAlias)
    Public Method vldBenef()
    Public Method procTratSeri()
    Public Method cancelaGuia()
    Public Method customPrint()

endClass

Method New(oRest) class PLSAuthorizationsRequest

    _Super:New(oRest)
    self:oService  := PLSAuthorizationsSvc():New(oRest:idHealthIns)

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} Valida se pode gerar o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method valida(cTipGui, lInitialSituation) class PLSAuthorizationsRequest
    
    self:lSuccess := self:oService:vldExiGuia(cTipGui, lInitialSituation)
    
    if !self:lSuccess
        self:getError()
    endIf

Return self:lSuccess

Method getError() class PLSAuthorizationsRequest
    
    Local oErrorAux   := self:oService:getError()

    self:lSuccess     := .F.
    self:nFault       := oErrorAux['status']
    self:nStatus      := oErrorAux['status']
    self:cFaultDesc   := oErrorAux['message']
    self:cFaultDetail := oErrorAux['detailedMessage']

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} Valida se pode realizar o 
    reenvio de guia TISS pelo HAT

@author  sakai
@version P12
@since   02/05/2023
/*/
//------------------------------------------------------------------- 
Method procReenvioHAT() class PLSAuthorizationsRequest
    
    Local aRet := {}
    Local oResponse := nil

    if self:lSuccess

        oResponse := JsonObject():new()
        self:oService:setJsonReq(self:jRequest)        
        self:oService:setAliCab(self:jRequest["authorizationType"])

        //Primeiro valida se ja existe a guia para rodar a contigencia
        if self:oService:bscGuiaReenvio()
            aRet := self:oService:getJsonGuia()
            oResponse["code"] := "0001"
            oResponse["message"] := "Guia informada encontrada, apta para realizar a contingencia"
            oResponse["idOnHealthInsurer"] := self:oService:getGuia()
            oResponse["authorization"] := aRet[1]
        else
            if self:oService:vldReenvioHAT()
                oResponse["code"] := "0002"
                oResponse["message"] := "Nao foi encontrada guia, e possivel realizar o reenvio"
            else
                self:getError()
            endIf
        endIf

        if self:lSuccess
            self:cResponse := oResponse:toJson()
        endIf
    endif

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Processa uma liberacao

@author  sakai
@version P12
@since   17/07/2023
/*/
//------------------------------------------------------------------- 
Method procExecuteAuthorization() class PLSAuthorizationsRequest

    if self:lSuccess
        self:oService:setJsonReq(self:jRequest)
        self:oService:setGuia(self:jRequest["idOnHealthInsurer"])
        self:lSuccess := self:oService:procExecuteAuthorization()   

        if !self:lSuccess
            self:getError()
        endIf
    endIf

Return self:lSuccess


//-------------------------------------------------------------------
/*/{Protheus.doc} Monta json com a guia

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method authorization() class PLSAuthorizationsRequest

    Local aRet := nil 
    
    self:oService:setShowDeniedPro(self:oRest:showDeniedProc)
    aRet := self:oService:getJsonGuia(self:oRest:expand)
   
    if ExistBlock("PLAPIAUT")
	    aRet := ExecBlock("PLAPIAUT",.F.,.F.,{aRet,self:oRest:expand})
    endif

    self:cResponse := aRet[1]:toJson()
    self:nStatus   := aRet[2]

Return

Method execution() class PLSAuthorizationsRequest

    Local aRet := self:oService:getJsonExecute()
   
    self:cResponse := aRet[1]:toJson()
    self:nStatus   := aRet[2]

Return

Method validaLiberacao() class PLSAuthorizationsRequest
    
    self:lSuccess := self:oService:vldLiberacao()
    
    if self:lSuccess
        aRet := self:oService:getJsonLiberacao(self:oRest:healthProviderCode,self:oRest:locationCode)
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    else
        self:getError()
    endIf

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} checkBody

@author    Sakai
@version   V12
@since     03/05/2023
/*/
//------------------------------------------------------------------- 
method checkBodyResend() class PLSAuthorizationsRequest
    
    local lRet := _Super:checkBody()
    Local lVirgula := .F.
    Local cMsg := ""

    if empty(self:jRequest['tissBatch'])
        cMsg := " tissBatch "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['subscriberId'])
        cMsg += iif(lVirgula,",","")
        cMsg += " subscriberId "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['healthProviderCode'])
        cMsg += iif(lVirgula,",","")
        cMsg += " healthProviderCode "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['authorizationType'])
        cMsg += iif(lVirgula,",","")
        cMsg += " authorizationType "
        lRet := .F.
        lVirgula := .T.
    endif

    if !lRet 
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Atributos obrigatorios nao informados"
        self:cFaultDetail := cMsg
    endif

return self:lSuccess

method checkBodyExecute() class PLSAuthorizationsRequest

    Local lVirgula := .F.
    Local cMsg := ""

    self:lSuccess := _Super:checkBody()

    if self:lSuccess
        if empty(self:jRequest['idOnHealthInsurer'])
            self:lSuccess := .F.
            cMsg += iif(lVirgula,",","")
            cMsg += " idOnHealthInsurer "
            lVirgula := .T.
        endif

        if empty(self:jRequest['healthProviderCode'])
            self:lSuccess := .F.
            cMsg += iif(lVirgula,",","")
            cMsg += " healthProviderCode "
            lVirgula := .T.
        endif

        if empty(self:jRequest['locationCode'])
            self:lSuccess := .F.
            cMsg += iif(lVirgula,",","")
            cMsg += " locationCode "
            lVirgula := .T.
        endif

        if !self:lSuccess
            self:nFault       := 400
            self:nStatus      := 400
            self:cFaultDesc   := "Atributos obrigatorios nao informados"
            self:cFaultDetail := cMsg
        endif
    else
        self:nFault  := 400
        self:nStatus := 400
    endIf

return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} checkBody

@author    Sakai
@version   V12
@since     15/08/2025
/*/
//------------------------------------------------------------------- 
method checkBodyCancel() class PLSAuthorizationsRequest
    
    local lRet := _Super:checkBody()
    Local lVirgula := .F.
    Local cMsg := ""

    if empty(self:jRequest['susepCode'])
        cMsg += "susepCode, "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['healthProviderId'])
        cMsg += "healthProviderId, "
        lRet := .F.
        lVirgula := .T.
    endif

    if empty(self:jRequest['message'])
        cMsg += "message, "
        lRet := .F.
        lVirgula := .T.
    endif

    if !lRet 
        self:lSuccess     := .F.
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Atributos obrigatorios nao informados"
        self:cFaultDetail := Substr(cMsg,1,len(cMsg)-2)
    else

    endif

return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} faultPostOption
Marca falha no POST por nao ter acao selecionada

@author    Sakai
@version   V12
@since     03/05/2023
/*/
//------------------------------------------------------------------- 
method faultPostOption() class PLSAuthorizationsRequest

    self:lSuccess     := .F.
    self:nFault       := 400
    self:nStatus      := 400
    self:cFaultDesc   := "Acao de Post nao informada"
    self:cFaultDetail := "E necessario passar um Queryparam com a acao desejada"

return

//-------------------------------------------------------------------
/*/{Protheus.doc} validaGuiasRelacionadas
Valida a existencia de anexos/prorrogacoes relacionadas

@author  sakai
@version P12
@since   11/12/2023
/*/
//------------------------------------------------------------------- 
Method validaGuiasRelacionadas(cAlias) class PLSAuthorizationsRequest
    
    self:oService:setExpand(self:oRest:expand)
    self:lSuccess := self:oService:vldExiGuiasRelac(cAlias,self:oRest:page,self:oRest:pageSize)
    
    if !self:lSuccess
        self:getError()
    endIf

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} geraJsonGuiasRelacionadas
Gera o json de anexos/prorrogacoes relacionadas

@author  sakai
@version P12
@since   11/12/2023
/*/
//------------------------------------------------------------------- 
Method geraJsonGuiasRelacionadas(cAlias) class PLSAuthorizationsRequest

    Local aRet := self:oService:getJsonGuiasRelac(cAlias,self:oRest:pageSize)
   
    self:cResponse := aRet[1]:toJson()
    self:nStatus   := aRet[2]

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} executions

@author  Lucas Nonato
@version P12
@since   04/04/2024
/*/
//------------------------------------------------------------------- 
Method executions() class PLSAuthorizationsRequest

    Local aRet := {}    
    
    aRet := self:oService:executions()

    self:cResponse := aRet[1]:toJson()
    self:nStatus   := aRet[2]
Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} vldBenef

@author  Nicole Luna
@version P12
@since   08/10/2024
/*/
//------------------------------------------------------------------- 
Method vldBenef() class PLSAuthorizationsRequest
    
    Local aRet := {}  

    self:oService:setExpand(self:oRest:expand)  
    self:lSuccess := self:oService:vldBenef(self:oRest:beneficiaryId)
    
    if self:lSuccess
        aRet := self:oService:getJsonOdonto(self:oRest:beneficiaryId, self:oRest:healthProviderCode, self:oRest:page, self:oRest:pageSize)
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    else
        self:getError()
    endIf 

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} procTratSeri

@author  Daniel silva
@version P12
@since   08/11/2024
/*/
//------------------------------------------------------------------- 

Method procTratSeri() class PLSAuthorizationsRequest
    
    Local aRet := {}  

    self:oService:setExpand(self:oRest:expand)  
    self:lSuccess := self:oService:vldBenef(self:oRest:beneficiaryId)
    
    if self:lSuccess
        aRet := self:oService:getJsonTratamentoSeriado(self:oRest:beneficiaryId, self:oRest:healthProviderCode, self:oRest:page, self:oRest:pageSize)
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    else
        self:getError()
    endIf 

Return self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} cancelaGuia

@author  sakai
@version P12
@since   15/08/2025
/*/
//------------------------------------------------------------------- 
Method cancelaGuia() class PLSAuthorizationsRequest

    local aRet := {}


    if self:lSuccess
        aRet := self:oService:cancelaGuia(self:jRequest['healthProviderId'], self:jRequest['susepCode'], self:jRequest['message'])
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    endIf
    
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} customPrint

@author  sakai
@version P12
@since   15/08/2025
/*/
//------------------------------------------------------------------- 
Method customPrint() class PLSAuthorizationsRequest

    Local oResult := JsonObject():New()
    Local lHasError := .T.
    Local nCode := 404
    Local cBlob := ""
    Local aRetPE := {}
        
    aadd(aRetPE,lHasError) //Posicao 1 do PE - Preenche hasError
    aadd(aRetPE,nCode) //Posicao 1 do PE - Codigo HTTP
    aadd(aRetPE,"") //Posicao 3 do PE - Blob do arquivo

    if ExistBlock("PAUTAP01")
        aRetPE := Execblock("PAUTAP01",.F.,.F.,{self:oRest:idHealthIns,self:oRest:healthProviderCode,self:oRest:locationCode})
        if ValType(aRetPE) == "A" .and. Len(aRetPE) > 0
            lHasError := aRetPE[1]
            nCode := aRetPE[2]
            cBlob := aRetPE[3]
        endIf
    endIf

    oResult['hasError'] := lHasError
    oResult['file'] := cBlob

    self:cResponse := oResult:toJson()
    self:nStatus   := nCode

Return
