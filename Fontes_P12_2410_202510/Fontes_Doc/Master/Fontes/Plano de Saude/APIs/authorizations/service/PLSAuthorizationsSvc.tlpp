#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsSvc
.
@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsSvc

    Data oError as Object
    Data cGuia as Character
    Data cCabAlias as Character
    Data oJsonReq as Object
    Data aProcExec as Array
    Data aGuiasRelac as Array
    Data cExpand as Character
    Data lShowDeniedProc as Logical
    Public Data lChamadaRecursiva
	public data lInitialSituation as Logical

    public method new()

    public method setJsonReq(oJsonReq)
    public method setExpand(cExpand) 
    public method vldExiGuia(cTipGui)
    public method vldLiberacao()
    public method getJsonLiberacao(cCodRda,cCodLoc)
    public method bscGuiaReenvio()
    public method vldReenvioHAT()
    public method getError()
    public method getJsonGuia(cExpand, lShowDeniedProc)
    public method getJsonExecute()
    public method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method setGuia(cGuia)
    public method setShowDeniedPro(lShowDeniedProc)
    public method getGuia()
    public method setAliCab(cTipGuiHAT)
    public method procExecuteAuthorization()
    private method getGuiasDia(oDao,lRet)
    private method setCriLib(aVldLib,cMsg)
    public method vldExiGuiasRelac(cAlias,cPage,cPageSize)
    public method getJsonGuiasRelac()
    public method executions()
    public method valida09S()
    public method getJsonOdonto(cSubscriberId, cHealthProviderId, cPageSize)
    public method vldBenef(cSubscriberId)
    public method getJsonTratamentoSeriado(cSubscriberId, cHealthProviderId, cPageSize)
    public method cancelaGuia(cCodRda,cSusep,cMotivo)

endClass

method new(cGuia) class PLSAuthorizationsSvc

    self:oError             := JsonObject():new()
    self:cGuia              := cGuia
    self:aProcExec          := {}
    self:aGuiasRelac        := {}
    self:cExpand            := ''
    self:lChamadaRecursiva  := .f.
	self:lInitialSituation  := .F.

Return self

method setJsonReq(oJsonReq) class PLSAuthorizationsSvc
    self:oJsonReq := oJsonReq
return

method setExpand(cExpand) class PLSAuthorizationsSvc
    self:cExpand := cExpand
return

method vldExiGuia(cTipGui, lInitialSituation) class PLSAuthorizationsSvc

    Local cMsg := "Guia informada nao encontrada"
    Local cDetail := "Entre em contato com o Suporte"
    Local lRet := .F.
    Local oBEA := NIL
    Local oB4Q := NIL
    Local oBEC := NIL
    Local oB4A := NIL

    Default lInitialSituation := .F.
    self:lInitialSituation := lInitialSituation

    self:setAliCab(cTipGui)

    Do Case
        Case self:cCabAlias == "BEA" //Pesquisa guia de SADT/Consulta/Internacao    
            oBEA := PLSAuthorizationsDaoBEA():new(self:cGuia)
            lRet := oBEA:buscaGuia()
            oBEA:closeQuery()
            if lRet
                lRet := oBEA:validaCodPegNumGui()
                if !lRet
                    cMsg := 'Guia informada ainda esta em processamento'
                    cDetail := 'Aguarde 1 minuto e tente novamente'
                endIf

                if self:lInitialSituation .AND. self:vldExiGuiasRelac("BEC")
                        lRet := .F.
                        cMsg := 'A guia informada já possui um anexo de situação inicial vinculado'
                        cDetail := 'Para mais informações verifique as guias relacionadas na rotina de consultar guias'
                endif
            endif
            oBEA := NIL

        Case self:cCabAlias == "B4Q" //Pesquisa guia de Prorrogacao na B4Q
            oB4Q := PLSAuthorizationsDaoB4Q():new(self:cGuia)
            lRet := oB4Q:buscaGuia()
            oB4Q:closeQuery()
            oB4Q := NIL
        
        Case self:cCabAlias == "BEC" //Pesquisa guia de Situacao Inicial
            oBEC := PLSAuthorizationsDaoBEC():new(self:cGuia)
            lRet := oBEC:buscaGuia()
            oBEC:closeQuery()
            oBEC := NIL

        Case self:cCabAlias == "B4A" //Pesquisa guia de anexos clinicos
            oB4A := PLSAuthorizationsDaoB4A():new(self:cGuia)
            lRet := oB4A:buscaGuia()
            oB4A:closeQuery()
            oB4A := NIL
    EndCase

    if !lRet
        self:setError(400,"0003",cMsg,cDetail)
    endIf

Return lRet

method vldLiberacao() class PLSAuthorizationsSvc

    Local lRet := .F.
    
    //Busca por numero da liberacao
    BEA->(dbsetorder(1)) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT+DTOS(BEA_DATPRO)+BEA_HORPRO
	if BEA->(MsSeek(xFilial("BEA")+self:cGuia))
        lRet := .T.
    else
        //Busca pela senha
        BEA->(dbsetorder(14)) //BEA_FILIAL+BEA_SENHA
        if BEA->(MsSeek(xFilial("BEA")+self:cGuia))
            lRet := .T.
            self:cGuia := BEA->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT)
        endif
    endIf
    
    if !lRet
        self:setError(400,"0004","Guia informada nao encontrada","Entre em contato com o Suporte")
    endIf

return lRet

method getJsonLiberacao(cCodRda,cCodLoc) class PLSAuthorizationsSvc
    
    Local oEntity := PLSAuthorizationsEntityReleaseStandard():new(self:cGuia)
    Local aVldLib := {.T.,""}
    Local aRet := {}
    Local cTipo    := '2' //Indica execucao
    Local cTipoAut := 'L' //Indica execucao
    Local cMatric  := ''
    Local lPlsCTEs := GetNewPar('MV_PLSCTES','0') == '1'

    Default cCodRda  := ''
    Default cCodLoc  := ''

    BEA->(dbsetorder(1)) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT+DTOS(BEA_DATPRO)+BEA_HORPRO
    BEA->(MsSeek(xFilial("BEA")+self:cGuia))
    
    cMatric  := BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO)
    iif(BEA->BEA_TIPGUI == '01',self:setCriLib(aVldLib,"A guia digitada é de consulta, não sendo possível execução ou vinculação"),nil)
    iif(BEA->BEA_LIBERA <> '1' ,self:setCriLib(aVldLib,"A guia digitada não é uma solicitação"),nil)
    iif(BEA->BEA_TIPGUI == '03',self:setCriLib(aVldLib,"A guia digitada é de internação, não sendo possível execução ou vinculação"),nil)
    iif(BEA->BEA_TIPGUI == '13',self:setCriLib(aVldLib,"A guia digitada é uma liberação odonto, por favor utilize a jornada de tratamento odontológico"),nil)
    iif(BEA->BEA_CANCEL == '1',self:setCriLib(aVldLib,"A guia digitada esta cancelada"),nil)
    iif(BEA->BEA_STATUS == '3' ,self:setCriLib(aVldLib,"A guia digitada não foi autorizada"),nil)
    iif(BEA->BEA_STATUS == '5' ,self:setCriLib(aVldLib,"A guia digitada não foi autorizada"),nil)
    iif(BEA->BEA_AUDITO == '1' .Or. BEA->BEA_STATUS == '6',self:setCriLib(aVldLib,"A guia digitada esta em auditoria"),nil)
    iif(BEA->BEA_STALIB != '1' ,self:setCriLib(aVldLib,"A guia digitada não tem mais saldo para execução"),nil)
    iif(self:valida09S(),self:setCriLib(aVldLib,"Quantidade de dias permitido para execucao da guia foi ultrapassado"),nil)
    
    //Valida liberacao
    if aVldLib[1] .And. !Empty(cCodRda) .And. !Empty(cCodLoc)
        cCodEsp  := PlRtEspPre(PlsIntPad(),cCodRda,'',Date(),cCodLoc,lPlsCTEs,.T.)
        aRetorno := PLSRETAULI(cMatric,self:cGuia,cTipoAut,cCodRda,cCodLoc,cCodEsp,'',,,,,cTipo,,,'','' )
        aVldLib[1] := aRetorno[1] .Or. (len(aRetorno[4]) > 0 .And. Empty(aRetorno[2]))
        aVldLib[2] := aRetorno[2]
    endIf

    oEntity:geraJson(aVldLib[1],aVldLib[2])
    aadd(aRet,oEntity:getResult())
    aadd(aRet,oEntity:getStatusCode())

    oEntity := NIL

return aRet

method bscGuiaReenvio() class PLSAuthorizationsSvc

    Local lRet   := .F.
    Local oBEA   := NIL
    Local oB4Q   := NIL
    Local cMatric := self:oJsonReq["subscriberId"]
    Local cCodRda := self:oJsonReq["healthProviderCode"]
    Local cLotGui := self:oJsonReq["tissBatch"]

    Do Case
        Case self:cCabAlias == "BEA" //Pesquisa guia de SADT/Consulta/Internacao    
            oBEA := PLSAuthorizationsDaoBEA():new()
            if oBEA:bscGuiaPrestadorReenvio(cMatric, cCodRda, cLotGui)
                self:cGuia := (oBEA:getAliasTemp())->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT)
                lRet := .T.
            endIf
            oBEA:closeQuery()
            oBEA := NIL

        Case self:cCabAlias == "B4Q" //Pesquisa guia de Prorrogacao na B4Q
            oB4Q := PLSAuthorizationsDaoB4Q():new()
            if oB4Q:bscGuiaPrestadorReenvio(cMatric, cCodRda, cLotGui)
                self:cGuia := (oB4Q:getAliasTemp())->(B4Q_OPEMOV+B4Q_ANOAUT+B4Q_MESAUT+B4Q_NUMAUT)
                lRet := .T.
            endIf
            oB4Q:closeQuery()
            oB4Q := NIL
        
        Case self:cCabAlias == "B4A" //Pesquisa guia de Anexos
            oB4A := PLSAuthorizationsDaoB4A():new()
            if oB4A:bscGuiaPrestadorReenvio(cMatric, cCodRda, cLotGui)
                self:cGuia := (oB4A:getAliasTemp())->(B4A_OPEMOV+B4A_ANOAUT+B4A_MESAUT+B4A_NUMAUT)
                lRet := .T.
            endIf
            oB4A:closeQuery()
            oB4A := NIL
    EndCase

Return lRet

method vldReenvioHAT() class PLSAuthorizationsSvc

    Local cGuias := ""
    Local lRet   := .F.
    Local oBEA   := NIL
    Local oB4Q   := NIL
    Local cMatric := self:oJsonReq["subscriberId"]
    Local cCodRda := self:oJsonReq["healthProviderCode"]

    Do Case
        Case self:cCabAlias == "BEA" //Pesquisa guia de SADT/Consulta/Internacao    
            oBEA := PLSAuthorizationsDaoBEA():new()
            lRet := oBEA:vldReenvioHAT(cMatric, cCodRda)
            cGuias := self:getGuiasDia(oBEA,lRet)
            oBEA:closeQuery()
            oBEA := NIL

        Case self:cCabAlias == "B4Q" //Pesquisa guia de Prorrogacao na B4Q
            oB4Q := PLSAuthorizationsDaoB4Q():new()
            lRet := oB4Q:vldReenvioHAT(cMatric, cCodRda)
            cGuias := self:getGuiasDia(oB4Q,lRet)
            oB4Q:closeQuery()
            oB4Q := NIL
        
        Case self:cCabAlias == "B4A" //Pesquisa guia de Anexos
            oB4A := PLSAuthorizationsDaoB4A():new()
            lRet := oB4A:vldReenvioHAT(cMatric, cCodRda)
            cGuias := self:getGuiasDia(oB4A,lRet)
            oB4A:closeQuery()
            oB4A := NIL

    EndCase

    if !lRet
         self:setError(400,"0003","Nao e possivel realizar o reenvio","Ja foi encontrada uma guia no seu sistema de gestao para o beneficiario e prestador na data de hoje. Guia(s) encontrada(s): "+cGuias)
    endIf

Return lRet

method getGuiasDia(oDao,lRet) class PLSAuthorizationsSvc

    local cRet := ''
    local nX   := 0
    local lVirgula := .F.
    local aGuias := oDao:getGuiasPrest()

    if !lRet
        for nX := 1 to len(aGuias)
            cRet += iif(lVirgula,", ","")
            cRet += aGuias[nX]
            lVirgula := .T.
        next
    endIf

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos auxiliares

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSAuthorizationsSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

return

method getError() class PLSAuthorizationsSvc
return self:oError


//-------------------------------------------------------------------
 /*/  Padrao da API e usar BEA, adicionar novos alias conforme necessidade 

    Tipos do HAT:
    ATENDIMENTO 0
    CONSULTA 1
    EXAME 2
    EXEC 3
    INTERNACAO 4
    RESUMO_INTERNACAO 5
    HONORARIO 6
    PRORROGACAO 8
    TRATODONTO 9
    PRONTOSOCORRO 10
    TRATSERIADO 11
    OPME 12
    QUIMIOTERAPIA 13
    RADIOTERAPIA 14
    PTU_00600 15
    SITUACAO INICIAL 17
/*/
//------------------------------------------------------------------- 
method setAliCab(cTipGuiHAT) class PLSAuthorizationsSvc

    Do Case
        Case cTipGuiHAT == '8'
            self:cCabAlias := 'B4Q'
        Case cTipGuiHAT == '17'
            self:cCabAlias := 'BEC'
        Case cTipGuiHAT == '12' .or. cTipGuiHAT == '13' .or. cTipGuiHAT == '14'
            self:cCabAlias := 'B4A'
        Otherwise
            self:cCabAlias := 'BEA'
    EndCase

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos que montam o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method getJsonGuia(cExpand) class PLSAuthorizationsSvc
    
    Local oEntity := NIL
    Local aRet := {}
    Default cExpand := ''

    Do Case
        Case self:cCabAlias == "BEA"
            oEntity := PLSAuthorizationsEntityStandard():new(self:cGuia, self:lShowDeniedProc)
            self:cExpand := iif(Empty(cExpand),'procedures,rejectionCauses',cExpand)
        
        Case self:cCabAlias == "B4Q"
            oEntity := PLSAuthorizationsEntityTreatment():new(self:cGuia, self:lShowDeniedProc)
            self:cExpand := iif(Empty(cExpand),'procedures,rejectionCauses',cExpand)

        Case self:cCabAlias == "BEC"
            oEntity := PLSAuthorizationsEntityInitialSituation():new(self:cGuia)
            self:cExpand := iif(Empty(cExpand),'teeth',cExpand)
        
        Case self:cCabAlias == "B4A"
            oEntity := PLSAuthorizationsEntityClinicalAttachment():new(self:cGuia, self:lShowDeniedProc)
            self:cExpand := iif(Empty(cExpand),'procedures,rejectionCauses',cExpand)
    EndCase
    oEntity:lChamadaRecursiva := self:lChamadaRecursiva
    oEntity:setExpand(self:cExpand)
    oEntity:montaCab()
    oEntity:montaEve()

    aadd(aRet,oEntity:getResult())
    aadd(aRet,oEntity:getStatusCode())

    oEntity := NIL

return aRet

method getJsonExecute() class PLSAuthorizationsSvc

    Local oEntity := NIL
    Local aRet := {}

    oEntity := PLSAuthorizationsEntityExecuteStandard():new(self:cGuia)
    oEntity:setProcExec(self:aProcExec)
    oEntity:geraJson()

    aadd(aRet,oEntity:getResult())
    aadd(aRet,oEntity:getStatusCode())

    oEntity := NIL

return aRet

method procExecuteAuthorization() class PLSAuthorizationsSvc

    Local cNumAut  := self:oJsonReq["idOnHealthInsurer"]
    Local cCodRda  := self:oJsonReq["healthProviderCode"]
    Local cCodLoc  := self:oJsonReq["locationCode"]
    Local cCodEsp  := ''
    Local cMatric  := ''
    Local cCodProf := '' //Nao tenho profissional selecionado no momento
    Local aRetorno := {}
    Local cTipo    := '2' //Indica execucao
    Local cTipoAut := 'L' //Indica execucao
    Local cAltera  := ''
    Local cDatAtd  := ''
    Local lSuccess := .T.
    Local lPlsCTEs := GetNewPar('MV_PLSCTES','0') == '1'
    Local aVldLib  := {.T.,""}

    BEA->(dbsetorder(1))
	if BEA->(MsSeek(xFilial("BEA")+cNumAut))
        
        cMatric := BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO)
        if Valtype(self:oJsonReq["professional"]) == "J" .And. Valtype(self:oJsonReq["professional","idOnHealthInsurer"])=="C"
            cCodProf := self:oJsonReq["professional","idOnHealthInsurer"]
        endIf
                
        if ValType(self:oJsonReq["specialtyCode"]) == "C"
            cCodEsp := self:oJsonReq["specialtyCode"]
        else //Procura na RDA selecionada, as especialidades disponiveis 
            cCodEsp := PlRtEspPre(PlsIntPad(),cCodRda,'',Date(),cCodLoc,lPlsCTEs,.T.)
        endIf
        
        iif(BEA->BEA_LIBERA == "0",self:setCriLib(aVldLib,"O número digitado não é de uma liberação."),nil)
        iif(BEA->BEA_CANCEL == '1',self:setCriLib(aVldLib,"A guia digitada está cancelada"),nil)
        iif(BEA->BEA_STALIB == "2",self:setCriLib(aVldLib,"Liberação não possui saldo para execução"),nil)
        iif(BEA->BEA_STATUS == '3' .And. !empty(cUserCardNumber),self:setCriLib(aVldLib,"A guia digitada não foi autorizada"),nil)
        lSuccess  := aVldLib[1]
        cErrorMsg := aVldLib[2]

    else 
        lSuccess := .F.
        cErrorMsg := "Liberação informada nao encontrada"
    endIf

    if lSuccess
        aRetorno  := PLSRETAULI(cMatric,cNumAut,cTipoAut,cCodRda,cCodLoc,cCodEsp,cCodProf,,,,,cTipo,,,cAltera,cDatAtd )
        lSuccess  := aRetorno[1] .Or. (len(aRetorno[4]) > 0 .And. Empty(aRetorno[2]))
        cErrorMsg := aRetorno[2]
    endIf

    if lSuccess
        self:cCabAlias := "BEA"
        self:aProcExec := aRetorno
    else
        self:setError(400,"0004",cErrorMsg,"Entre em contato com o Suporte")
    endif

return lSuccess

method setGuia(cGuia) class PLSAuthorizationsSvc
    self:cGuia := cGuia
return

method setShowDeniedPro(lShowDeniedProc) class PLSAuthorizationsSvc
    self:lShowDeniedProc := iif(lShowDeniedProc <> nil, lShowDeniedProc, .T.)
return

method getGuia() class PLSAuthorizationsSvc
return self:cGuia

method setCriLib(aVldLib,cMsg) class PLSAuthorizationsSvc

    if aVldLib[1]
        aVldLib[1] := .F.
        aVldLib[2] := cMsg
    endIf

return

method vldExiGuiasRelac(cAlias,cPage,cPageSize) class PLSAuthorizationsSvc

    Local oDao := nil
    Local lRet := .F.
    Default cPage := '1'
    Default cPageSize := '10'
    
    Do Case
        Case cAlias == 'B4A'
            oDao := PLSAuthorizationsDaoB4A():new(self:cGuia)
        Case cAlias == 'B4Q'
            oDao := PLSAuthorizationsDaoB4Q():new(self:cGuia)
        Case cAlias == 'BEC'
            oDao := PLSAuthorizationsDaoBEC():new(self:cGuia, self:lInitialSituation)
    EndCase

    oDao:setPage(cPage)
    oDao:setPageSize(cPageSize)

    lRet := oDao:buscaGuiasRelacionadas()
    if lRet
        self:aGuiasRelac := oDao:getGuiasPrest()
    else
        self:setError(400,"0004","Nao foram encontradas guias relacionadas","Entre em contato com o Suporte")
    endIf
    oDao:closeQuery()
    oDao := NIL

return lRet

method getJsonGuiasRelac(cAlias,cPageSize) class PLSAuthorizationsSvc
    
    Local oEntity := NIL
    Local oResult := JsonObject():new()
    Local aRet := {}
    Local nX := 0
    Local nCode := 0
    Local lHasNext := .F.
    Default cPageSize := '10'

    oResult['items'] := {}
    for nX := 1 to len(self:aGuiasRelac)
        if nX <= Val(cPageSize)
            Do Case
                Case cAlias == 'B4A'
                    oEntity := PLSAuthorizationsEntityClinicalAttachment():new(self:aGuiasRelac[nX])
                Case cAlias == 'B4Q'
                    oEntity := PLSAuthorizationsEntityTreatment():new(self:aGuiasRelac[nX])
                Case cAlias == 'BEC'
                    oEntity := PLSAuthorizationsEntityInitialSituation():new(self:aGuiasRelac[nX])                    
            EndCase
            oEntity:setExpand(self:cExpand)
            oEntity:montaCab()
            oEntity:montaEve()
            Aadd(oResult['items'],oEntity:getResult())

            nCode := oEntity:getStatusCode()
            oEntity := NIL
        else
            lHasNext := .T.
        endIf
    next
    oResult['hasNext'] := lHasNext
    aadd(aRet,oResult)
    aadd(aRet,nCode)

return aRet

method executions() class PLSAuthorizationsSvc    
    local oEntity := NIL
    local oResult := JsonObject():new()
    local aRet := {}
    local nCode := 404
    Default cPageSize := '10'

    oBEA    := PLSAuthorizationsDaoBEA():new(self:cGuia)
    lFound  := oBEA:buscaGuia(.t.)
    
    oResult['items']    := {}
    oResult['hasNext']  := .f.
    if lFound
        cAlias := oBEA:getAliasTemp()
        
        while !(cAlias)->(eof())
            oEntity := PLSAuthorizationsEntityStandard():new((cAlias)->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT))
            oEntity:setExpand('procedures,medicalTeam,professional,cbos')
            oEntity:montaCab()
            oEntity:montaEve()
            aadd(oResult['items'],oEntity:getResult())
            (cAlias)->(dbSkip())
            nCode := oEntity:getStatusCode()
            oEntity := NIL
        enddo
    endif

    oBEA:closeQuery()
    oBEA := NIL
    aadd(aRet,oResult)
    aadd(aRet,nCode)

return aRet

method valida09S() class PLSAuthorizationsSvc    
    local lCri09S   := Date() > BEA->BEA_VALSEN
    local cAlias    := GetNextAlias()
    local cSql      := ''
    if lCri09S 
        //Valida procedimento seriado, se tiver um na guia ignora a regra.
        cSql := " SELECT 1 FROM " + RetSqlName("BE2") + " BE2 "
        cSql += " INNER JOIN " + RetSqlName("BR8") + " BR8 "
        cSql += " ON BR8_FILIAL = '" + xfilial("BR8") + "' "
        cSql += " AND BR8_CODPAD = BE2_CODPAD "
        cSql += " AND BR8_CODPSA = BE2_CODPRO "
        cSql += " AND BR8_CLASSE <> ' ' "
        cSql += " AND BR8.D_E_L_E_T_ = ' ' "
        cSql += " INNER JOIN " + RetSqlName("BJE") + " BJE "
        cSql += " ON BJE_FILIAL = '" + xfilial("BJE") + "' "
        cSql += " AND BJE_CODIGO = BR8_CLASSE "
        cSql += " AND BJE_TIPO   = '2' "
        cSql += " AND BJE.D_E_L_E_T_ = ' ' "
        cSql += " WHERE BE2_FILIAL = '" + xfilial("BE2") + "' "
        cSql += " AND BE2_OPEMOV = '" + BEA->BEA_OPEMOV + "' "
        cSql += " AND BE2_ANOAUT = '" + BEA->BEA_ANOAUT + "' "
        cSql += " AND BE2_MESAUT = '" + BEA->BEA_MESAUT + "' "
        cSql += " AND BE2_NUMAUT = '" + BEA->BEA_NUMAUT + "' "
        cSql += " AND BE2.D_E_L_E_T_ = ' ' "

        dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),cAlias,.F.,.T.)
        if !(cAlias)->(eof())
            lCri09S := .f.
        endif

        (cAlias)->(dbCloseArea())
    endif

return lCri09S

method vldBenef(cSubscriberId) class PLSAuthorizationsSvc

    Local lRet := .T.
    //Busca pela matricula do beneficiario
    BA1->(DbSetOrder(2)) //BA1_FILIAL+BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO
	if !BA1->(MsSeek(xFilial("BA1")+cSubscriberId))
        lRet := .F.
        self:setError(400,"0004","Beneficiario informado nao encontrado","Entre em contato com o Suporte")
    endIf
 
return lRet

method getJsonOdonto(cSubscriberId, cHealthProviderId, cPage, cPageSize) class PLSAuthorizationsSvc
    
    Local oEntity     := NIL
    Local oResult     := JsonObject():new()
    Local aRet        := {}
    Local nCode       := 404
    Local lHasNext    := .F.
    Default nX        := 1
    Default cPage     := '1'
    Default cPageSize := '5'

    oBEA := PLSAuthorizationsDaoBEA():new("")
    oBEA:setPage(cPage)
    oBEA:setPageSize(cPageSize)
    lFound := oBEA:bscLiberacoesOdonto(cSubscriberId, cHealthProviderId)
    
    oResult['items']    := {}
    oResult['hasNext']  := .f.
    if lFound
        cAlias := oBEA:getAliasTemp()
        
        while !(cAlias)->(eof())
            if nX <= Val(cPageSize)
                oEntity := PLSAuthorizationsEntityStandard():new((cAlias)->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT))
                oEntity:setExpand(self:cExpand)
                oEntity:montaCab()
                oEntity:montaEve()
                aadd(oResult['items'],oEntity:getResult())
                (cAlias)->(dbSkip())
                nCode := oEntity:getStatusCode()
                oEntity := NIL
                nX++
            else
                lHasNext := .T.
                exit
            endif
        enddo
    endif

    oResult['hasNext'] := lHasNext

    oBEA:closeQuery()
    oBEA := NIL

    aadd(aRet,oResult)
    aadd(aRet,nCode)

return aRet

method getJsonTratamentoSeriado(cSubscriberId, cHealthProviderId, cPage, cPageSize) class PLSAuthorizationsSvc
    
    Local oEntity     := NIL
    Local oResult     := JsonObject():new()
    Local aRet        := {}
    Local nCode       := 404
    Local lHasNext    := .F.
    Default nX        := 1
    Default cPage     := '1'
    Default cPageSize := '5'

    oBEA := PLSAuthorizationsDaoBEA():new("")
    oBEA:setPage(cPage)
    oBEA:setPageSize(cPageSize)
    lFound := oBEA:bscTratamentoSeriado(cSubscriberId, cHealthProviderId)
    
    oResult['items']    := {}
    oResult['hasNext']  := .f.
    if lFound
        cAlias := oBEA:getAliasTemp()
        
        while !(cAlias)->(eof())
            if nX <= Val(cPageSize)
                oEntity := PLSAuthorizationsEntityStandard():new((cAlias)->(BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT))
                oEntity:setExpand(self:cExpand)
                oEntity:montaCab()
                oEntity:montaEve()
                aadd(oResult['items'],oEntity:getResult())
                (cAlias)->(dbSkip())
                nCode := oEntity:getStatusCode()
                oEntity := NIL
                nX++
            else
                lHasNext := .T.
                exit
            endif
        enddo
    endif

    oResult['hasNext'] := lHasNext

    oBEA:closeQuery()
    oBEA := NIL

    aadd(aRet,oResult)
    aadd(aRet,nCode)

return aRet

method cancelaGuia(cCodRda,cSusep,cMotivo) class PLSAuthorizationsSvc

    local oResult := JsonObject():new()
    local nStatusTiss := 0
    local cCodOpe := ''
    local nCode := 404
    local cMsg := ''
    local aRet := {}

    default cCodRda := ''
	default cSusep := ''
    default cMotivo := ''

    BA0->(dbSetOrder(5))//BA0_FILIAL+BA0_SUSEP
	if BA0->(MsSeek(xfilial("BA0") + alltrim(cSusep)))
		cCodOpe := BA0->BA0_CODIDE + BA0->BA0_CODINT
	EndIf

    //Verificamos se a guia ja foi enviada para o faturamento
    BEA->(DbSetOrder(1))
	if BEA->( MsSeek( xFilial("BEA") + self:cGuia ))
		//Guia transferido pelo PEG Transfer
		if !empty(BEA->BEA_LOTHAT) 
			nStatusTiss := 3
            nCode := 202
			cMsg := "A guia " + self:cGuia + " ja foi enviada ao faturamento."
		endif 
	
		//Guia executada diretamente
		BD5->(DbSetOrder(6)) //BD5_FILIAL+BD5_NUMIMP
		if BD5->(MsSeek(xFilial("BD5") + self:cGuia)) .And. BD5->BD5_CODLDP <> PLSRETLDP(9)
			nStatusTiss := 3
            nCode := 202
			cMsg := "A guia " + self:cGuia + " ja foi enviada ao faturamento. Lote Operadora: " + BD5->BD5_CODPEG
		endIf
	endif

	//Posiciona na BEA e pega os dados da Guia, se não houver resultado vamos posicionar na B4A, B4Q ou BEC
	BEA->(DbSetOrder(1)) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT+DTOS(BEA_DATPRO)+BEA_HORPRO
	B4A->(DbSetOrder(1)) //B4A_FILIAL+B4A_OPEMOV+B4A_ANOAUT+B4A_MESAUT+B4A_NUMAUT
	BEC->(DbSetOrder(1)) //BEC_FILIAL+BEC_SEQUEN

    if nStatusTiss <> 3 
        Do Case

            Case BEA->( MsSeek( xFilial("BEA")+self:cGuia) )
                //Vamos comparar os dados da Guia (BEA) com os dados da Rda e operadora que vieram do XML.
                if (cCodRda != BEA->BEA_CODRDA .Or. cCodOpe != BEA->BEA_OPERDA)
                    cMsg := 'Codigo ANS ou de RDA informado nao encontrado'
                    nStatusTiss := 3
                else
                    cMsg := PLSA090CAN(.T.,cMotivo, BEA->(RecNo()), .F., @nStatusTiss)
                    cMsg := iif(nStatusTiss == 1 .And. Empty(cMsg),'Guia cancelada com sucesso!',cMsg)
                    nCode := 202

                    if BEA->BEA_TIPGUI == '11' //Guia de prorrogacao
                        B4Q->(DbSetOrder(1)) //B4Q_FILIAL+B4Q_OPEMOV+B4Q_ANOAUT+B4Q_MESAUT+B4Q_NUMAUT
                        if B4Q->( MsSeek( xFilial("B4Q")+self:cGuia) )
                            PLSA09PCAN(.T.,.F.,nil,nil,nil,nil,{cMotivo},.T.,@nStatusTiss)
                        endIf
                    endIf
                endIf

            Case B4A->( MsSeek( xFilial("B4A")+self:cGuia) )
                PLSA09ACAN(.T.,.F.,nil,nil,nil,{cMotivo},.T., @nStatusTiss)
                cMsg := iif(nStatusTiss == 1,'Guia cancelada com sucesso!',cMsg)
                nCode := 202

            Case BEC->( MsSeek( xFilial("BEC")+self:cGuia) ) .And. BEC->(FieldPos("BEC_CANCEL")) > 0
                PLSCanSit(@nStatusTiss)
                cMsg := iif(nStatusTiss == 1,'Guia cancelada com sucesso!',cMsg)
                nCode := 202

            Otherwise
                cMsg := 'Guia informada nao encontrada'
                nStatusTiss := 3
        EndCase
    endIf

    oResult['status'] := iif(nStatusTiss == 1,.T.,.F.)
    oResult['description'] := cMsg

    aadd(aRet,oResult)
    aadd(aRet,nCode)

return aRet
