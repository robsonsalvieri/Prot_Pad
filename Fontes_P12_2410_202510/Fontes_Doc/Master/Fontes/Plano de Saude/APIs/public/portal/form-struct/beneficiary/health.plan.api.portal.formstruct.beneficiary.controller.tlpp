#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.portal.formstruct

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} BeneficiaryController
Controlador para gerenciar operações relacionadas a beneficiários, incluindo chamadas à API externa.
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
*/
class BeneficiaryController from BaseController

    public method new() constructor

    @GET("/totvsHealthPlans/portal/v1/formStruct/beneficiaries")
    public method apiGetBeneficiaries()

    private method validateBeneficiaryAccess() as logical 
    
endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryController.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return object, instância da classe BeneficiaryController
*/
method new() class BeneficiaryController

return self

/*/{Protheus.doc} apiGetBeneficiaries
Realiza a consulta dos beneficiários e define a resposta da API, incluindo status e dados.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
*/
method apiGetBeneficiaries() class BeneficiaryController

    local oBeneficiaryService as object

    if !self:validateBeneficiaryAccess()
        oRest:setStatusCode(self:getStatusCode())
		self:destroy()
		return
    endif

    oBeneficiaryService := BeneficiaryService():new()
    oBeneficiaryService:setQueryParams(oRest:getQueryRequest())

    if oBeneficiaryService:getBeneficiaries()
        oRest:setResponse(oBeneficiaryService:getJsonResult())
        oRest:setStatusCode(oBeneficiaryService:getStatusCode())
    else
        oRest:setFault(oBeneficiaryService:getJsonResult())
        oRest:setStatusCode(oBeneficiaryService:getStatusCode())
    endif

    // Define cabeçalho padrão da resposta
    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oBeneficiaryService:destroy() 
    freeObj(oBeneficiaryService)

return

/*/{Protheus.doc} validateBeneficiaryAccess
Valida o acesso do usuário ao beneficiário.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 04/04/2025
@return logical, Indica se o acesso ao beneficiário é válido.
*/
method validateBeneficiaryAccess() as logical class BeneficiaryController

    local aRequiredParams as array
    local lHasAccess := .F. as logical
    local lAccessBeneficiary := oRest:getQueryRequest():hasProperty("subscriberId")

    if lAccessBeneficiary
        aRequiredParams := {"subscriberId"}
    else
        aRequiredParams := {"healthInsurerCode", "companyCode", "contractCode", "contractVersion", "subcontractCode", "subcontractVersion"}
    endif

    if self:validatePortalUserAccess() .and. self:validateRequiredParams(oRest:getQueryRequest(), aRequiredParams)
        if lAccessBeneficiary
            lHasAccess := self:hasAccessToBeneficiary(oRest:getQueryRequest()["subscriberId"])
        else
            lHasAccess := self:hasAccessToContract(oRest:getQueryRequest())
        endif
    endif

    fwFreeArray(aRequiredParams)

return lHasAccess
