#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.api.portal.beneficiary.import.controller.ch"

#define STATUS_CODE_NOT_FOUND 404

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} BeneficiaryImportController
Classe responsável por gerenciar os acessos do lote de importação de beneficiários
@type class
@version 12.1.2510  
@author vinicius.queiros
@since 18/06/2025
*/
class BeneficiaryImportController from BaseController

	private data lIsFormData as logical
	private data jContractData as json

	public method new() constructor
	public method destroy()

	@POST("/totvsHealthPlans/portal/v1/beneficiaries/importBatches")
	public method postImportBatches()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/importBatches")
	public method getImportBatches()

	@POST("/totvsHealthPlans/portal/v1/beneficiaries/importBatches/start")
	public method postStartImportBatch()

	@POST("/totvsHealthPlans/portal/v1/beneficiaries/importBatches/reprocess")
	public method postReprocessImportBatch()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/importBatches/:batchCode/beneficiaries")
	public method getBatchBeneficiaries()

	@GET("/totvsHealthPlans/portal/v1/beneficiaries/importBatches/:batchCode")
	public method getImportBatch()

	private method validateBodyRequest(aRequiredParams as array) as logical
	private method getValue(cProperty as character) as variant
	private method getImportBatchContract(cBatchCode as character) as json

endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryImportController.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/06/2025
@return self, instancia da classe BeneficiaryImportController.
*/
method new() class BeneficiaryImportController

return self

/*/{Protheus.doc} destroy
Libera os recursos utilizados pela classe BeneficiaryImportController.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
*/
method destroy() class BeneficiaryImportController

	freeObj(self:jContractData)

	_Super:destroy()

return

/*/{Protheus.doc} postImportBatches
Processa a requisição de importação em lote de beneficiários a partir de um arquivo enviado via form-data ou body.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 28/07/2025
/*/
method postImportBatches() class BeneficiaryImportController

	local oController as object
	local aRequiredData as array

	aRequiredData := {"healthInsurerCode", "companyCode", "contractCode", "contractVersion", "subcontractCode", "subcontractVersion"}

	if self:validateBodyRequest(aRequiredData)
		self:jContractData := JsonObject():new()

		self:jContractData["healthInsurerCode"] := self:getValue("healthInsurerCode")
		self:jContractData["companyCode"] := self:getValue("companyCode")
		self:jContractData["contractCode"] := self:getValue("contractCode")
		self:jContractData["contractVersion"] := self:getValue("contractVersion")
		self:jContractData["subcontractCode"] := self:getValue("subcontractCode")
		self:jContractData["subcontractVersion"] := self:getValue("subcontractVersion")

		if self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jContractData)
			oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
			oController:postImportBatches()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getImportBatches
Retorna os lotes de importação disponíveis para o usuário logado
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
/*/
method getImportBatches() class BeneficiaryImportController

	local oController as object

	if self:validatePortalUserAccess()
		oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
		oController:getImportBatches()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} postStartImportBatch
Inicia o processamento de um lote de beneficiários previamente importado, alterando seu status para Em processamento.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 25/05/2025
/*/
method postStartImportBatch() class BeneficiaryImportController

	local oController as object
	local aRequiredData as array

	aRequiredData := {"batchCode"}

	if self:validateBodyRequest(aRequiredData)
		self:jContractData := self:getImportBatchContract(self:getValue("batchCode"))

		if valType(self:jContractData) == "J" .and. self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jContractData)
			oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
			oController:postStartImportBatch()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} postReprocessImportBatch
Recebe um novo arquivo CSV com ajustes para reprocessar beneficiários que apresentaram erro em um lote de importação anterior
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
/*/
method postReprocessImportBatch() class BeneficiaryImportController

	local oController as object
	local aRequiredData as array

	aRequiredData := {"importBatchCode"}

	if self:validateBodyRequest(aRequiredData)
		self:jContractData := self:getImportBatchContract(self:getValue("importBatchCode"))

		if valType(self:jContractData) == "J" .and. self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jContractData)
			oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
			oController:postReprocessImportBatch()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getBatchBeneficiaries
Retorna os beneficiários associados a um lote de importação específico
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
/*/
method getBatchBeneficiaries() class BeneficiaryImportController

	local oController as object

	if self:validateRequiredParams(oRest:getPathParamsRequest(), "batchCode")
		self:jContractData := self:getImportBatchContract(oRest:getPathParamsRequest()["batchCode"])

		if valType(self:jContractData) == "J" .and. self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jContractData)
			oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
			oController:getBatchBeneficiaries()
		endif
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getImportBatch
Obtém as informações do lote de importação informado
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
/*/
method getImportBatch() class BeneficiaryImportController

	local oController as object

	if self:validateRequiredParams(oRest:getPathParamsRequest(), "batchCode")
		self:jContractData := self:getImportBatchContract(oRest:getPathParamsRequest()["batchCode"])

		if valType(self:jContractData) == "J" .and. self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jContractData)
			oController := totvs.protheus.health.plan.contract.beneficiary.import.ImportBatchController():new()
			oController:getImportBatch()
		endif
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} validateBodyRequest
Valida os dados do corpo da requisição (JSON ou FormData), conforme os parâmetros obrigatórios esperados.
A validação verifica se o conteúdo está presente e corretamente estruturado antes de seguir com a lógica da aplicação.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
@param aRequiredParams, array, Lista de campos obrigatórios a serem validados no corpo da requisição
@return logical, Indica se a validação dos parâmetros foi bem-sucedida
/*/
method validateBodyRequest(aRequiredParams as array) as logical class BeneficiaryImportController

	local aRequiredData := {} as array
	local cBody := oRest:getBodyRequest() as character
	local lValidateRequired := .F. as logical
	local nCount as numeric
	local nSizeArray as numeric

	self:lIsFormData := self:isFormData()

	if self:lIsFormData
		if self:setFormData(cBody)
			nSizeArray := len(aRequiredParams)

			for nCount := 1 to nSizeArray
				aAdd(aRequiredData, {"key": aRequiredParams[nCount], "type": "text"})
			next nCount

			if self:validateRequiredFormData(self:getFormData(), aRequiredData)
				lValidateRequired := .T.
			endif
		endif
	else
		if self:setBodyRequest(cBody)
			aRequiredData := aClone(aRequiredParams)

			if self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
				lValidateRequired := .T.
			endif
		endif
	endif

	fwFreeArray(aRequiredData)
	fwFreeArray(aRequiredParams)

return lValidateRequired

/*/{Protheus.doc} getValue
Retorna o valor de uma propriedade armazenada no form-data ou no body da classe com base no nome informado.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 28/07/2025
@param cProperty, character, nome da propriedade cujo valor será retornado
@return variant, valor da propriedade informada
/*/
method getValue(cProperty as character) as variant class BeneficiaryImportController

	local xValue as variant

	if self:lIsFormData
		xValue := self:jFormData[cProperty]["value"]
	else
		xValue := self:jBodyRequest[cProperty]
	endif

return xValue

/*/{Protheus.doc} getImportBatchContract
Obtém as informações de contrato do lote de importação
@type method
@version 12.1.2510
@author vinicius.queiros
@since 30/07/2025
@param cBatchCode, character, código do lote de importação
@return json, dados do contrato associado ao lote.
*/
method getImportBatchContract(cBatchCode as character) as json class BeneficiaryImportController

	local jContractData as json
	local cQuery as character
	local nOrder := 1 as numeric
	local oExecStmt as object
	local cAlias as character

	cQuery += "SELECT ? "
	cQuery += " FROM ? B5J "
	cQuery += " WHERE "
	cQuery += "		B5J.B5J_FILIAL = ? AND "
	cQuery += "		B5J.BJ5_CODLOT = ? AND "
	cQuery += "		B5J.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BJ5_CODOPE, BJ5_CODEMP, BJ5_CONEMP, BJ5_VERCON, BJ5_SUBCON, BJ5_VERSUB")
	oExecStmt:setUnsafe(nOrder++, retSqlName("B5J"))
	oExecStmt:setString(nOrder++, xFilial("B5J"))
	oExecStmt:setString(nOrder++, cBatchCode)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		jContractData := JsonObject():new()
		jContractData["healthInsurerCode"] := (cAlias)->BJ5_CODOPE
		jContractData["companyCode"] := (cAlias)->BJ5_CODEMP
		jContractData["contractCode"] := (cAlias)->BJ5_CONEMP
		jContractData["contractVersion"] := (cAlias)->BJ5_VERCON
		jContractData["subcontractCode"] := (cAlias)->BJ5_SUBCON
		jContractData["subcontractVersion"] := (cAlias)->BJ5_VERSUB
	else
		self:nStatusCode := STATUS_CODE_NOT_FOUND
		self:jResult["code"] := self:nStatusCode
		self:jResult["message"] := STR0001 // "Lote de importação não encontrado"
		self:jResult["detailedMessage"] := STR0002 // "O lote de importação informado não existe na base de dados (Tabela B5J). Verifique se o código está correto."

		oRest:setFault(self:jResult:toJson())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jContractData
