#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.api.portal.beneficiary.card.controller.ch"

#define STATUS_CODE_NOT_FOUND 404

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} BeneficiaryCardController
Classe responsável por gerenciar os acessos relacionadas à carteirinha dos beneficiários no portal
@type class
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
class BeneficiaryCardController from BaseController

	public method new() constructor
	public method destroy()

	@POST("/totvsHealthPlans/portal/v1/cardBatches")
	public method postCardBatches()

	@GET("/totvsHealthPlans/portal/v1/cardBatches")
    public method getCardBatches()

    @GET("/totvsHealthPlans/portal/v1/cardBatches/:batchCode/beneficiaries")
    public method getCardBatchBeneficiaries()

	private method getCardBatchContract(cBatchCode as character) as json

endclass

/*/{Protheus.doc} new
Construtor da classe BeneficiaryCardController
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
method new() class BeneficiaryCardController

	_Super:new()

return self

/*/{Protheus.doc} destroy
Libera os recursos utilizados pela classe BeneficiaryCardController
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
method destroy() class BeneficiaryCardController

	_Super:destroy()

return

/*/{Protheus.doc} postCardBatches
Realiza a criação de um novo lote de cartão com base nos parâmetros informados.
@type method
@version 12.1.2510
@autor vinicius.queiros
@since 01/08/2025
/*/
method postCardBatches() class BeneficiaryCardController

	local oController as object
	local aRequiredData as array
    local cBody := oRest:getBodyRequest() as character

	aRequiredData := {"healthInsurerCode", "companyCode", "contractCode", "contractVersion", "subcontractCode", "subcontractVersion"}

	if self:setBodyRequest(cBody) .and. self:validateRequiredParams(self:getBodyRequest(), aRequiredData)
		if self:validatePortalUserAccess() .and. self:hasAccessToContract(self:jBodyRequest)
			oController := totvs.protheus.health.plan.identificationcard.batch.CardBatchController():new()
			oController:postCardBatches()
		endif
	endif

	fwFreeArray(aRequiredData)
	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getCardBatches
Obtém os lotes de cartões cadastrados no sistema pela operadora de saúde
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
method getCardBatches() class BeneficiaryCardController

	local oController as object

	if self:validatePortalUserAccess()
		oController := totvs.protheus.health.plan.identificationcard.batch.CardBatchController():new()
		oController:getCardBatches()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} getCardBatchBeneficiaries
Obtém os beneficiários associados a um lote específico de cartões
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
method getCardBatchBeneficiaries() class BeneficiaryCardController

	local oController as object
	local jContractData as json

	if self:validateRequiredParams(oRest:getPathParamsRequest(), {"batchCode"})
		jContractData := self:getCardBatchContract(oRest:getPathParamsRequest()["batchCode"])

		if valType(jContractData) == "J" .and. self:validatePortalUserAccess() .and. self:hasAccessToContract(jContractData)
			oController := totvs.protheus.health.plan.identificationcard.batch.CardBatchController():new()
			oController:getCardBatchBeneficiaries()
		endif
	endif

	freeObj(oController)
	freeObj(jContractData)
	self:destroy()

return

/*/{Protheus.doc} getCardBatchContract
Obtém as informações de contrato, subcontrato e empresa associadas ao lote de cartões
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@param cBatchCode, character, código do lote de cartão
@return json, dados do contrato, subcontrato e empresa vinculados ao lote de cartões
/*/
method getCardBatchContract(cBatchCode as character) as json class BeneficiaryCardController

	local jContractData as json
	local cQuery as character
	local nOrder := 1 as numeric
	local oExecStmt as object
	local cAlias as character
	local cContractVersion as character
	local cSubcontractVersion as character

	cQuery += "SELECT ? "
	cQuery += " FROM ? BDE "
	cQuery += " WHERE "
	cQuery += "		BDE.BDE_FILIAL = ? AND "
	cQuery += "		BDE.BDE_CODIGO = ? AND "
	cQuery += "		BDE.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BDE_CODINT, BDE_CODIGO, BDE_EMPDE, BDE_CONDE, BDE_SUBDE")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BDE"))
	oExecStmt:setString(nOrder++, xFilial("BDE"))
	oExecStmt:setString(nOrder++, cBatchCode)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		cContractVersion := posicione("BT5", 1, xFilial("BT5") + (cAlias)->(BDE_CODINT + BDE_EMPDE + BDE_CONDE), "BT5_VERSAO")
		cSubcontractVersion := posicione("BQC", 1, xFilial("BQC") + (cAlias)->(BDE_CODINT + BDE_EMPDE + BDE_CONDE) + cContractVersion + (cAlias)->BDE_SUBDE, "BQC_VERSUB")

		jContractData := JsonObject():new()
		jContractData["healthInsurerCode"] := (cAlias)->BDE_CODINT
		jContractData["companyCode"] := (cAlias)->BDE_EMPDE
		jContractData["contractCode"] := (cAlias)->BDE_CONDE
		jContractData["contractVersion"] := cContractVersion
		jContractData["subcontractCode"] := (cAlias)->BDE_SUBDE
		jContractData["subcontractVersion"] := cSubcontractVersion
	else
		self:nStatusCode := STATUS_CODE_NOT_FOUND
		self:jResult["code"] := self:nStatusCode
		self:jResult["message"] := STR0001 // "Lote de cartão não encontrado"
		self:jResult["detailedMessage"] := STR0002 // "O lote de cartão informado não existe na base de dados (Tabela BDE). Verifique se o código está correto."

		oRest:setFault(self:jResult:toJson())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
	endif

	(cAlias)->(dbCloseArea())

	freeObj(oExecStmt)

return jContractData
