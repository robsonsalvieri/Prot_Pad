#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "health.plan.api.portal.refund.controller.ch"

#define STATUS_CODE_NOT_FOUND 404

namespace totvs.protheus.health.plan.api.portal.access

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} RefundController
Controlador responsável por gerenciar as requisições do portal relacionadas a reembolsos.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
/*/
class RefundController from BaseController

	public method new() constructor

	@GET("/totvsHealthPlans/portal/v1/refunds")
	public method apiGetRefunds()

	@POST("/totvsHealthPlans/portal/v1/refunds")
	public method apiPostRefunds()

	@DELETE("/totvsHealthPlans/portal/v1/refunds/:protocol")
    public method apiDeleteRefunds()

	@GET("/totvsHealthPlans/portal/v1/refunds/:protocol")
	public method apiGetRefundId()

	@GET("/totvsHealthPlans/portal/v1/refunds/:protocol/protocol/base64")
	public method apiGetProtocolBase64()

	@GET("/totvsHealthPlans/portal/v1/refunds/:protocol/expenses")
	public method apiGetExpenses()

	@GET("/totvsHealthPlans/portal/v1/refunds/:protocol/expenses/:sequential/reasonDenied")
	public method apiGetReasonDenied()

	@GET("/totvsHealthPlans/portal/v1/refunds/:protocol/attachments")
	public method apiGetAttachments()

	@POST("/totvsHealthPlans/portal/v1/refunds/attachments")
	public method apiPostAttachments()

	private method validateRefundAccess(jRequestData as json, aRequiredParams as array) as logical
	private method getRefundSubscriberId(cProtocol as character) as character

endclass

/*/{Protheus.doc} new
Construtor da classe RefundController. Inicializa os atributos e configurações necessárias para o controle de reembolsos.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
/*/
method new() class RefundController

	_Super:new()

return self

/*/{Protheus.doc} apiGetRefunds
Realiza a consulta dos reembolsos cadastrados do beneficiário logado.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
/*/
method apiGetRefunds() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getQueryRequest(), {"subscriberId"})
		oController := totvs.protheus.health.plans.api.attendance.refund.RefundController():new()
		oController:apiGetRefunds()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiPostRefunds
Recebe os dados de solicitação de reembolso e os envia para processamento e armazenamento no sistema.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/05/2025
/*/
method apiPostRefunds() class RefundController

	local oController as object

	if self:setBodyRequest(oRest:getBodyRequest()) .and. self:validateRefundAccess(self:getBodyRequest(), {"subscriberId"})
		oController := totvs.protheus.health.plans.api.attendance.refund.RefundController():new()
		oController:apiPostRefunds()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiDeleteRefunds
Realiza a exclusão de reembolsos com base nos parâmetros fornecidos via requisição
@type method
@version 12.1.2510
@author vinicius.queiros
@since 23/05/2025
/*/
method apiDeleteRefunds() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.RefundController():new()
		oController:apiDeleteRefunds()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetRefundId
Retorna os dados do protocolo de reembolso com base nos parâmetro recebido
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
/*/
method apiGetRefundId() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.RefundController():new()
		oController:apiGetRefundId()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetProtocolBase64
Retorna o conteúdo do protocolo em formato Base64 para visualização ou download
@type method
@version 12.1.2510
@author vinicius.queiros
@since 16/05/2025
/*/
method apiGetProtocolBase64() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.RefundController():new()
		oController:apiGetProtocolBase64()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetExpenses
Retorna a lista de despesas registradas para reembolso, conforme o protocolo informados na requisição da API.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 17/05/2025
/*/
method apiGetExpenses() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.expense.ExpenseController():new()
		oController:apiGetExpenses()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetReasonDenied
Retorna os motivos de negativa da despesa do reembolso
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/05/2025
/*/
method apiGetReasonDenied() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol", "sequential"})
		oController := totvs.protheus.health.plans.api.attendance.refund.expense.ExpenseController():new()
		oController:apiGetReasonDenied()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiGetAttachments
Retorna os anexos vinculados a uma solicitação de reembolso, conforme o protocolo da requisição.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 17/05/2025
/*/
method apiGetAttachments() class RefundController

	local oController as object

	if self:validateRefundAccess(oRest:getPathParamsRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.attachment.AttachmentController():new()
		oController:apiGetAttachments()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} apiPostAttachments
Recebe e processa arquivos enviados via API para anexos relacionados à solicitação de reembolso.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/05/2025
/*/
method apiPostAttachments() class RefundController

	local oController as object

	if self:setBodyRequest(oRest:getBodyRequest()) .and. self:validateRefundAccess(self:getBodyRequest(), {"protocol"})
		oController := totvs.protheus.health.plans.api.attendance.refund.attachment.AttachmentController():new()
		oController:apiPostAttachments()
	endif

	freeObj(oController)
	self:destroy()

return

/*/{Protheus.doc} validateRefundAccess
Valida se os dados da requisição contêm as informações necessárias para acessar a funcionalidade de reembolso.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/05/2025
@param jRequestData, json, dados da requisição recebidos pela API
@param aRequiredParams, array, lista de parâmetros obrigatórios que devem estar presentes
@return logical, indica se os dados são válidos para acesso ao reembolso
/*/
method validateRefundAccess(jRequestData as json, aRequiredParams as array) as logical class RefundController

	local lHasAccess := .F. as logical
	local cSubscriberId as character

	if self:validateRequiredParams(jRequestData, aRequiredParams)
		if jRequestData:hasProperty("subscriberId")
			cSubscriberId := jRequestData["subscriberId"]
		else
			cSubscriberId := self:getRefundSubscriberId(jRequestData["protocol"])
		endif

		if !empty(cSubscriberId) .and. self:validatePortalUserAccess()
			lHasAccess := self:hasAccessToBeneficiary(cSubscriberId)
		endif
	endif

return lHasAccess

/*/{Protheus.doc} getRefundSubscriberId
Retorna o identificador do beneficiário associado ao protocolo de reembolso informado.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 14/05/2025
@param cProtocol, character, código do protocolo de reembolso
@return character, identificador do beneficiário vinculado ao protocolo
/*/
method getRefundSubscriberId(cProtocol as character) as character class RefundController

	local cSubscriberId as character
	local cQuery as character
	local nOrder := 1 as numeric
	local oExecStmt as object
	local cAlias as character

	cQuery += "SELECT ? "
	cQuery += " FROM ? BOW "
	cQuery += " WHERE BOW.BOW_FILIAL = ? AND "
	cQuery += "		BOW.BOW_PROTOC = ? AND "
	cQuery += "		BOW.D_E_L_E_T_ = ? "

	oExecStmt := FwExecStatement():new(cQuery)

	oExecStmt:setUnsafe(nOrder++, "BOW.BOW_USUARI")
	oExecStmt:setUnsafe(nOrder++, retSqlName("BOW"))
	oExecStmt:setString(nOrder++, xFilial("BOW"))
	oExecStmt:setString(nOrder++, cProtocol)
	oExecStmt:setString(nOrder++, " ")

	cAlias := oExecStmt:openAlias()

	if !(cAlias)->(eof())
		cSubscriberId := alltrim((cAlias)->BOW_USUARI)
	else
		self:nStatusCode := STATUS_CODE_NOT_FOUND
		self:jResult["code"] := self:nStatusCode
		self:jResult["message"] := STR0001 // "Não foi possível localizar o protocolo de reembolso."
		self:jResult["detailedMessage"] := STR0002 // "Nenhum protocolo foi localizado na tabela BOW. Verifique se o número do protocolo está correto e se ele foi previamente registrado."

		self:setResponseData()
	endif

	(cAlias)->(dbCloseArea())

	oExecStmt:destroy()
	freeObj(oExecStmt)

return cSubscriberId
