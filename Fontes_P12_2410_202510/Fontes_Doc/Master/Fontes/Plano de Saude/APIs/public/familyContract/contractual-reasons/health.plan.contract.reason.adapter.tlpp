#include "tlpp-core.th"

namespace totvs.protheus.health.plan.contract.reason

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ContractualReasonAdapter
Classe responsável por adaptar os dados dos motivos contratuais entre o sistema e o esperado pela API.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
class ContractualReasonAdapter from BaseAdapter

    public method new() constructor
    public method getReasons() as logical
    public method getReasonDocuments() as logical

    protected method mapReasonFields() as logical
    protected method getReasonQuery() as character
    protected method getReasonWhere() as character
    protected method getReasonOrder() as character

    protected method mapDocumentsFields() as logical
    protected method getDocumentsQuery() as character
    protected method getDocumentsWhere() as character
    protected method getDocumentsOrder() as character

endclass

/*/{Protheus.doc} new
Construtor da classe ContractualReasonAdapter, inicializa a instância e prepara os recursos necessários para adaptação dos dados.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method new() class ContractualReasonAdapter

	_Super:new()

return self

/*/{Protheus.doc} getReasons
Realiza a obtenção e adaptação dos motivos contratuais do sistema.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
@return logical, indica se a operação de obtenção e adaptação dos motivos foi realizada com sucesso
/*/
method getReasons() as logical class ContractualReasonAdapter

    local lSuccess := .F. as logical
    local cQuery := self:getReasonQuery() as character
    local cWhere := self:getReasonWhere() as character
    local cOrder := self:getReasonOrder() as character

    self:mapReasonFields()

    lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} getReasonDocuments
Obtém os documentos relacionados aos motivos contratuais
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return logical, indica se os documentos foram obtidos com sucesso
/*/
method getReasonDocuments() as logical class ContractualReasonAdapter

    local lSuccess := .F. as logical
    local cQuery := self:getDocumentsQuery() as character
    local cWhere := self:getDocumentsWhere() as character
    local cOrder := self:getDocumentsOrder() as character

    self:mapDocumentsFields()

    lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} mapReasonFields
Realiza o mapeamento dos campos dos motivos contratuais do sistema para o formato utilizado pela API.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method mapReasonFields() as logical class ContractualReasonAdapter  

    self:oAdapterBase:addMapFields("code", "B9G_COD", .T., .F., {"B9G_COD", "C", tamSX3("B9G_COD")[1], 0})
    self:oAdapterBase:addMapFields("description", "B9G_MOTIVO", .T., .F., {"B9G_MOTIVO", "C", tamSX3("B9G_MOTIVO")[1], 0})
    self:oAdapterBase:addMapFields("type", "B9G_TIPMOT", .T., .F., {"B9G_TIPMOT", "C", tamSX3("B9G_TIPMOT")[1], 0})
    self:oAdapterBase:addMapFields("healthInsurerCode", "B9G_CODINT", .T., .F., {"B9G_CODINT", "C", tamSX3("B9G_CODINT")[1], 0})
    self:oAdapterBase:addMapFields("blockCode", "B9G_MOTBG3", .T., .F., {"B9G_MOTBG3", "C", tamSX3("B9G_MOTBG3")[1], 0})
    self:oAdapterBase:addMapFields("blockDescription", "BG3_DESBLO", .T., .F., {"BG3_DESBLO", "C", tamSX3("BG3_DESBLO")[1], 0})
    self:oAdapterBase:addMapFields("familyBlock", "BG3_BLQFAM", .T., .F., {"BG3_BLQFAM", "C", tamSX3("BG3_BLQFAM")[1], 0})

return .T.

/*/{Protheus.doc} getReasonQuery
Retorna a query base para consulta dos motivos contratuais no sistema, sem cláusula WHERE ou ORDER, servindo como base para construções dinâmicas de consulta.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
@return character, string contendo a query base dos motivos contratuais
/*/
method getReasonQuery() as character class ContractualReasonAdapter

    local cQuery as character
    local aBindValues := {} as array
    
    cQuery := " SELECT #QueryFields# "
	cQuery += " FROM ? B9G "
    cQuery += " LEFT JOIN ? BG3 ON "
    cQuery += "     BG3.BG3_FILIAL = ? AND "
    cQuery += "     BG3.BG3_CODBLO = B9G.B9G_MOTBG3 AND "
    cQuery += "     BG3.D_E_L_E_T_ = ? "
    cQuery += " WHERE #QueryWhere# "

    aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B9G")})
    aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BG3")})
    aAdd(aBindValues, {"set": "string", "value": xFilial("BG3")})
    aAdd(aBindValues, {"set": "string", "value": " "})

    cQuery := self:bindValuesToQuery(cQuery, aBindValues)

    fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getReasonWhere
Retorna a cláusula WHERE para a consulta dos motivos contratuais, permitindo filtrar os registros conforme query params informados.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
@return character, string contendo a cláusula WHERE da query de motivos contratuais
/*/
method getReasonWhere() as character class ContractualReasonAdapter

    local cWhere as character
    local aBindValues := {} as array

    cWhere := " B9G.B9G_FILIAL = ? AND "
    aAdd(aBindValues, {"set": "string", "value": xFilial("B9G")})

    if self:hasValidParam("healthInsurerCode")
        cWhere += " B9G.B9G_CODINT = ? AND "
        aAdd(aBindValues, {"set": "string", "value": self:jParams["healthInsurerCode"]})
    endif

    if self:hasValidParam("code")
        cWhere += " B9G.B9G_COD = ? AND "
        aAdd(aBindValues, {"set": "string", "value": self:jParams["code"]})
    endif

    if self:hasValidParam("description")
        cWhere += " B9G.B9G_MOTIVO LIKE '?%' AND "
        aAdd(aBindValues, {"set": "unsafe", "value": upper(self:jParams["description"])})
    endif

    if self:hasValidParam("type")
        cWhere += " B9G.B9G_TIPMOT = ? AND "
        aAdd(aBindValues, {"set": "string", "value": self:jParams["type"]})
    endif

    cWhere += " B9G.D_E_L_E_T_ = ? "
    aAdd(aBindValues, {"set": "string", "value": " "})

    cWhere := self:bindValuesToQuery(cWhere, aBindValues)

    fwFreeArray(aBindValues)

return cWhere

/*/{Protheus.doc} getReasonOrder
Retorna a cláusula ORDER BY para a consulta dos motivos contratuais, definindo a ordenação dos registros.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 18/08/2025
@return character, string contendo a cláusula ORDER BY da query de motivos contratuais
/*/
method getReasonOrder() as character class ContractualReasonAdapter

    local cOrder as character

    cOrder := "B9G.B9G_CODINT, B9G.B9G_MOTIVO"

return cOrder

/*/{Protheus.doc} mapDocumentsFields
Mapeia os campos relacionados aos documentos dos motivos contratuais
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method mapDocumentsFields() as logical class ContractualReasonAdapter

    self:oAdapterBase:addMapFields("sequential", "B9X_SEQMOT", .T., .F., {"B9X_SEQMOT", "C", tamSX3("B9X_SEQMOT")[1], 0})
    self:oAdapterBase:addMapFields("code", "B9X_CODDOC", .T., .F., {"B9X_CODDOC", "C", tamSX3("B9X_CODDOC")[1], 0})
    self:oAdapterBase:addMapFields("description", "BD2_DESCRI", .T., .F., {"BD2_DESCRI", "C", tamSX3("BD2_DESCRI")[1], 0})
    self:oAdapterBase:addMapFields("required", "B9X_OBRIG", .T., .F., {"B9X_OBRIG", "C", tamSX3("B9X_OBRIG")[1], 0})

return .T.

/*/{Protheus.doc} getDocumentsQuery
Monta a query utilizada para consultar os documentos vinculados aos motivos contratuais
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return character, instrução SQL para consulta dos documentos
/*/
method getDocumentsQuery() as character class ContractualReasonAdapter

    local cQuery as character
    local aBindValues := {} as array

    cQuery := " SELECT #QueryFields# "
    cQuery += " FROM ? B9X "
    cQuery += " LEFT JOIN ? BD2 ON "
    cQuery += "     BD2.BD2_FILIAL = ? AND "
    cQuery += "     BD2.BD2_CODDOC = B9X.B9X_CODDOC AND "
    cQuery += "     BD2.D_E_L_E_T_ = ? "
    cQuery += " WHERE #QueryWhere# "

    aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B9X")})
    aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BD2")})
    aAdd(aBindValues, {"set": "string", "value": xFilial("BD2")})
    aAdd(aBindValues, {"set": "string", "value": " "})

    cQuery := self:bindValuesToQuery(cQuery, aBindValues)

    fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getDocumentsWhere
Monta a cláusula WHERE da consulta de documentos vinculados aos motivos contratuais
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return character, cláusula WHERE da consulta de documentos
/*/
method getDocumentsWhere() as character class ContractualReasonAdapter

    local cWhere as character
    local aBindValues := {} as array

    cWhere := " B9X.B9X_FILIAL = ? AND "
    cWhere += " B9X.B9X_CODINT = ? AND "
    cWhere += " B9X.B9X_CODMOT = ? AND "
    cWhere += " B9X.D_E_L_E_T_ = ? "

    aAdd(aBindValues, {"set": "string", "value": xFilial("B9X")})
    aAdd(aBindValues, {"set": "string", "value": self:jParams["healthInsurerCode"]})
    aAdd(aBindValues, {"set": "string", "value": self:jPath["code"]})
    aAdd(aBindValues, {"set": "string", "value": " "})

    cWhere := self:bindValuesToQuery(cWhere, aBindValues)

    fwFreeArray(aBindValues)

return cWhere

/*/{Protheus.doc} getDocumentsOrder
Monta a cláusula ORDER BY da consulta de documentos vinculados aos motivos contratuais
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return character, cláusula ORDER BY da consulta de documentos
/*/
method getDocumentsOrder() as character class ContractualReasonAdapter

    local cOrder as character

    cOrder := "B9X.B9X_CODINT, B9X.B9X_CODMOT, B9X.B9X_SEQMOT"

return cOrder
