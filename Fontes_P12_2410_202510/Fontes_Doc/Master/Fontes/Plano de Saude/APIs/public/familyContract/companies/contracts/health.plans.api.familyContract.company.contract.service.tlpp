#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.familyContract.company.contract

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ContractService
Classe de serviço de Contratos - Regra de Negócio

@type class
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Class ContractService From BaseService

    Private Data oAdapter As Object

  	Public Method new() Constructor
    Public Method getContracts() As Logical

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method new() Class ContractService

    _Super:new()

Return Self

/*/{Protheus.doc} getContracts
Método responsavel por retornar as paginas de contratos da empresa

@type method
@author Vinicius Queiros Teixeira
@since 08/02/2023
@version Protheus 12
/*/
Method getContracts() As Logical Class ContractService

    Local lSucess := .F. As Logical
    Local aKeys := {} As Array

    // Chaves do path param e do query param obrigatórios na api
    aAdd(aKeys, {"type" : "pathParams", "key" : "companyCode"})
    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})

    If Self:checkRequiredKeys(aKeys, "E001")
        BG9->(DbSetOrder(1))
        If BG9->(MsSeek(xFilial("BG9")+Padr(Self:jParams["healthInsurerCode"], TamSX3("BG9_CODINT")[1])+Padr(Self:jPath["companyCode"], TamSX3("BG9_CODIGO")[1])))
            Self:oAdapter := ContractAdapter():New()
            Self:oAdapter:setQueryParams(Self:jParams)
            Self:oAdapter:setPathParams(Self:jPath)
            Self:oAdapter:setLinks({"rel" : "subcontracts",;
                                    "url" : "/totvsHealthPlans/familyContract/v1/companies",;
                                    "paths" : {{"path" : {"company_code"}, "resource" : "contracts"},;
                                               {"path" : {"contract_code", "contract_version_code"}, "resource" : "subcontracts"}},;
                                    "queryParams" : {{"key" : "healthInsurerCode", "value" : self:jParams["healthInsurerCode"]}};
                                    })

            If Self:oAdapter:getPageContracts()
                lSucess := .T.
                Self:nCodeStatus := 200
                Self:jResult := Self:oAdapter:getJsonResult()
            EndIf
        Else
            Self:SetError("E002", "Empresa não encontrada na operadora.",;
                                  "Não foi encontrada na tabela BG9 a empresa de código "+Self:jPath["companyCode"]+" (BG9_CODIGO).",;
                                  404) // Not found
        EndIf
    EndIf
   
Return lSucess
