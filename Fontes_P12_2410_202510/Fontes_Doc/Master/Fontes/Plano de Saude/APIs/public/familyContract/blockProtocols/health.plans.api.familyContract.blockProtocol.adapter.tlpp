#include "tlpp-core.th"

namespace totvs.protheus.health.plans.api.familyContract.blockProtocol

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BlockProtocolsAdapter
Adapter das apis da solicitação de cancelamento de plano
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 18/06/2024
/*/
class BlockProtocolsAdapter from BaseAdapter

	public method new() constructor
	public method getBlockProtocolsPages() as logical
	public method getBeneficiariesPages() as logical

	protected method mapProtocolsFields() as logical
	protected method getProtocolsQuery() as character
	protected method getProtocolsWhere() as character
	protected method getProtocolsOrder() as character

	protected method mapBeneficiariesFields() as logical
	protected method getBeneficiariesQuery() as character
	protected method getBeneficiariesWhere() as character
	protected method getBeneficiariesOrder() as character

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 18/06/2024
@return object, objeto da instância da classe
/*/
method new() class BlockProtocolsAdapter

	_Super:new()

return self

/*/{Protheus.doc} getBlockProtocolsPages
Retorna as paginas das solicitações de cancelamento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
@return logical, se as paginas foram retornadas com sucesso
/*/
method getBlockProtocolsPages() as logical class BlockProtocolsAdapter

	local lSuccess := .F. as logical
    local cQuery := self:getProtocolsQuery() as character
    local cWhere := self:getProtocolsWhere() as character
    local cOrder := self:getProtocolsOrder() as character
	
	self:mapProtocolsFields()

	if self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSuccess := .T.
	endif

return lSuccess

/*/{Protheus.doc} getBeneficiariesPages
Retorna as paginas dos beneficiarios do protocolo
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
@return logical, se as paginas foram retornadas com sucesso
/*/
method getBeneficiariesPages() as logical class BlockProtocolsAdapter

	local lSuccess := .F. as logical
    local cQuery := self:getBeneficiariesQuery() as character
    local cWhere := self:getBeneficiariesWhere() as character
    local cOrder := self:getBeneficiariesOrder() as character
	
	self:mapBeneficiariesFields()

	if self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSuccess := .T.
	endif

return lSuccess

/*/{Protheus.doc} mapProtocolsFields
Mapea os atributos do json com os campos do dicionário de dados (SX3)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
/*/
method mapProtocolsFields() as logical class BlockProtocolsAdapter

	self:oAdapterBase:addMapFields("subscriber_id", "B5J_MATSOL", .T., .F., {"B5J_MATSOL", "C", tamSX3("B5J_MATSOL")[1], 0})
	self:oAdapterBase:addMapFields("name", "BA1_NOMUSR", .T., .F., {"BA1_NOMUSR", "C", tamSX3("BA1_NOMUSR")[1], 0})
	self:oAdapterBase:addMapFields("protocol", "B5J_PROTOC", .T., .F., {"B5J_PROTOC", "C", tamSX3("B5J_PROTOC")[1], 0})
	self:oAdapterBase:addMapFields("status", "B5J_STATUS", .T., .F., {"B5J_STATUS", "C", tamSX3("B5J_STATUS")[1], 0})
	self:oAdapterBase:addMapFields("request_date", "B5J_DATSOL", .T., .F., {"B5J_DATSOL", "C", tamSX3("B5J_DATSOL")[1], 0}, self:getFormatDate("B5J_DATSOL"))
	self:oAdapterBase:addMapFields("request_time", "B5J_HORSOL", .T., .F., {"B5J_HORSOL", "C", tamSX3("B5J_HORSOL")[1], 0})
	self:oAdapterBase:addMapFields("block_date", "B5J_DATBLO", .T., .F., {"B5J_DATBLO", "C", tamSX3("B5J_DATBLO")[1], 0}, self:getFormatDate("B5J_DATBLO"))
	self:oAdapterBase:addMapFields("block_time", "B5J_HORBLO", .T., .F., {"B5J_HORBLO", "C", tamSX3("B5J_HORBLO")[1], 0})
	self:oAdapterBase:addMapFields("email", "B5J_EMAIL", .T., .F., {"B5J_EMAIL", "C", tamSX3("B5J_EMAIL")[1], 0})
	self:oAdapterBase:addMapFields("observation", "B5J_OBSERV", .T., .F., {"B5J_OBSERV", "M", tamSX3("B5J_OBSERV")[1], 0})
	self:oAdapterBase:addMapFields("origin", "B5J_ORISOL", .T., .F., {"B5J_ORISOL", "C", tamSX3("B5J_ORISOL")[1], 0})
	
return

/*/{Protheus.doc} getProtocolsQuery
Retorna o corpo da query que busca as solicitações de cancelamento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
@return character, corpo da query
/*/
method getProtocolsQuery() as character class BlockProtocolsAdapter

	local cQuery as character
	local cSubstrOperatorSQL := iif(upper(TCGetDB()) $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING")

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + retSqlName("B5J") + " B5J "

	cQuery += " INNER JOIN " + retSqlName("BA1") + " BA1 "
	cQuery += "   ON BA1.BA1_FILIAL = '" + xFilial("BA1") + "' "
	cQuery += "  AND BA1.BA1_CODINT = " + cSubstrOperatorSQL + "(B5J.B5J_MATSOL, 1, 4) "
    cQuery += "  AND BA1.BA1_CODEMP = " + cSubstrOperatorSQL + "(B5J.B5J_MATSOL, 5, 4) "
    cQuery += "  AND BA1.BA1_MATRIC = " + cSubstrOperatorSQL + "(B5J.B5J_MATSOL, 9, 6) "
    cQuery += "  AND BA1.BA1_TIPREG = " + cSubstrOperatorSQL + "(B5J.B5J_MATSOL, 15, 2) "
    cQuery += "  AND BA1.BA1_DIGITO = " + cSubstrOperatorSQL + "(B5J.B5J_MATSOL, 17, 1) "
	cQuery += "  AND BA1.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE #QueryWhere# "
 
return cQuery

/*/{Protheus.doc} getProtocolsWhere
Retorna as condições da query que busca as solicitações de cancelamento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
@return character, where da query
/*/
method getProtocolsWhere() as character class BlockProtocolsAdapter

	local cQuery as character
	local cHealthInsurerCode as character
	local cCompanyCode as character
	local cFamilyCode as character
	local nSizeFldHealthInsurerCode := tamSX3("B5J_OPESOL")[1] as numeric
	local nSizeFldCompanyCode := tamSX3("B5J_CODEMP")[1] as numeric
	local nSizeFldFamilyCode := tamSX3("B5J_MATRIC")[1] as numeric
	
	cQuery := " B5J.B5J_FILIAL = '" + xFilial("B5J") + "' AND "

	if self:jParams:hasProperty("subscriberId")
		if self:jParams:hasProperty("allFamily") .and. self:jParams["allFamily"] == "true" .and.;
		   len(self:jParams["subscriberId"]) >= nSizeFldHealthInsurerCode + nSizeFldCompanyCode + nSizeFldFamilyCode

			cHealthInsurerCode := substr(self:jParams["subscriberId"], 1, nSizeFldHealthInsurerCode)
			cCompanyCode := substr(self:jParams["subscriberId"], nSizeFldHealthInsurerCode + 1, nSizeFldCompanyCode)
			cFamilyCode := substr(self:jParams["subscriberId"], nSizeFldHealthInsurerCode + nSizeFldCompanyCode + 1, nSizeFldFamilyCode)

			cQuery += " B5J.B5J_OPESOL = '" + cHealthInsurerCode + "' AND "
			cQuery += " B5J.B5J_CODEMP = '" + cCompanyCode + "' AND "
			cQuery += " B5J.B5J_MATRIC = '" + cFamilyCode + "' AND "
		else
			cQuery += " B5J.B5J_MATSOL = '" + self:jParams["subscriberId"] + "' AND "
		endif
	endif

	cQuery += " B5J.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getProtocolsOrder
Retorna a ordem da query que busca as solicitações de cancelamento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 18/06/2024
@return character, order da query
/*/
method getProtocolsOrder() as character class BlockProtocolsAdapter

	local cOrder as character
	
	cOrder := "B5J.B5J_PROTOC"

return cOrder

/*/{Protheus.doc} mapBeneficiariesFields
Mapea os atributos do json com os campos do dicionário de dados (SX3)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
/*/
method mapBeneficiariesFields() as logical class BlockProtocolsAdapter

	self:oAdapterBase:addMapFields("subscriber_id", "B5K_MATUSU", .T., .F., {"B5K_MATUSU", "C", tamSX3("B5K_MATUSU")[1], 0})
	self:oAdapterBase:addMapFields("name", "BA1_NOMUSR", .T., .F., {"BA1_NOMUSR", "C", tamSX3("BA1_NOMUSR")[1], 0})
	self:oAdapterBase:addMapFields("type", "BA1_TIPUSU", .T., .F., {"BA1_TIPUSU", "C", tamSX3("BA1_TIPUSU")[1], 0})
	self:oAdapterBase:addMapFields("cpf", "BA1_CPFUSR", .T., .F., {"BA1_CPFUSR", "C", tamSX3("BA1_CPFUSR")[1], 0})
	self:oAdapterBase:addMapFields("effective_date", "BA1_DATINC", .T., .F., {"BA1_DATINC", "C", tamSX3("BA1_DATINC")[1], 0}, self:getFormatDate("BA1_DATINC"))

return

/*/{Protheus.doc} getBeneficiariesQuery
Retorna o corpo da query que busca os beneficiarios do protocolo
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
@return character, corpo da query
/*/
method getBeneficiariesQuery() as character class BlockProtocolsAdapter

	local cQuery as character
	local cSubstrOperatorSQL := iif(upper(TCGetDB()) $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING")

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + retSqlName("B5K") + " B5K "

	cQuery += " INNER JOIN " + retSqlName("B5J") + " B5J "
	cQuery += "   ON B5J.B5J_FILIAL = '" + xFilial("B5J") + "' "
	cQuery += "  AND B5J.B5J_CODIGO = B5K.B5K_CODIGO "
	cQuery += "  AND B5J.B5J_PROTOC = '" + self:jPath["protocol"] + "' "
	cQuery += "  AND B5J.D_E_L_E_T_ = ' ' "

	cQuery += " INNER JOIN " + retSqlName("BA1") + " BA1 "
	cQuery += "   ON BA1.BA1_FILIAL = '" + xFilial("BA1") + "' "
	cQuery += "  AND BA1.BA1_CODINT = " + cSubstrOperatorSQL + "(B5K.B5K_MATUSU, 1, 4) "
    cQuery += "  AND BA1.BA1_CODEMP = " + cSubstrOperatorSQL + "(B5K.B5K_MATUSU, 5, 4) "
    cQuery += "  AND BA1.BA1_MATRIC = " + cSubstrOperatorSQL + "(B5K.B5K_MATUSU, 9, 6) "
    cQuery += "  AND BA1.BA1_TIPREG = " + cSubstrOperatorSQL + "(B5K.B5K_MATUSU, 15, 2) "
    cQuery += "  AND BA1.BA1_DIGITO = " + cSubstrOperatorSQL + "(B5K.B5K_MATUSU, 17, 1) "
	cQuery += "  AND BA1.D_E_L_E_T_ = ' ' "

    cQuery += " WHERE #QueryWhere# "
 
return cQuery

/*/{Protheus.doc} getBeneficiariesWhere
Retorna as condições da query que busca os beneficiarios do protocolo
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
@return character, where da query
/*/
method getBeneficiariesWhere() as character class BlockProtocolsAdapter

	local cQuery as character

	cQuery := " B5K.B5K_FILIAL = '" + xFilial("B5K") + "' AND "
	cQuery += " B5K.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getBeneficiariesOrder
Retorna a ordem da query que busca dos beneficiários do protocolo
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 08/07/2024
@return character, order da query
/*/
method getBeneficiariesOrder() as character class BlockProtocolsAdapter

	local cOrder as character
	
	cOrder := "B5K_MATUSU"

return cOrder
