#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.familyContract.company

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CompanyAdapter
Classe adaptadora de collenction de Empresas - Base de dados

@type class
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Class CompanyAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageCompanies() As Logical

	Private Method mapFieldsCompanies() As Logical
	Private Method getQueryCompanies() As Character
	Private Method getWhereCompanies() As Character
	Private Method getOrderCompanies() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method new() class CompanyAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageCompanies
Método responsavel por retornar a pagina de empresas da operadora

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getPageCompanies() As Logical Class CompanyAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryCompanies() As Character
    Local cWhere := Self:getWhereCompanies() As Character
    Local cOrder := Self:getOrderCompanies() As Character
	
	Self:mapFieldsCompanies()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsCompanies
Método responsavel por mapear os atributos do json com os campos da tabela de empresas

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method mapFieldsCompanies() As Logical Class CompanyAdapter

    Self:oAdapterBase:addMapFields("company_code", "BG9_CODIGO", .T., .F., {"BG9_CODIGO", "C", TamSX3("BG9_CODIGO")[1], 0})
    Self:oAdapterBase:addMapFields("description", "BG9_DESCRI", .T., .F., {"BG9_DESCRI", "C", TamSX3("BG9_DESCRI")[1], 0})
	Self:oAdapterBase:addMapFields("group_type", "BG9_TIPO", .T., .F., {"BG9_TIPO", "C", TamSX3("BG9_TIPO")[1], 0})
	Self:oAdapterBase:addMapFields("use", "BG9_USO", .T., .F., {"BG9_USO", "C", TamSX3("BG9_USO")[1], 0})
	
Return .T.

/*/{Protheus.doc} getQueryCompanies
Método responsavel por retornar o corpo da query que busca as Empresas

@type Method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getQueryCompanies() As Character Class CompanyAdapter

	Local cQuery As Character
	
	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BG9")+" BG9 "

	If Self:jParams:hasProperty("loginUser")
		// Usuario Portal x Empresa
		cQuery += " INNER JOIN "+RetSqlName("B40")+" B40 "
		cQuery += "   ON B40.B40_FILIAL = '"+xFilial("B40")+"' "
		cQuery += "  AND B40.B40_CODINT = BG9.BG9_CODINT "
		cQuery += "  AND B40.B40_CODEMP = BG9.BG9_CODIGO "
		cQuery += "  AND B40.D_E_L_E_T_ = ' ' "
		// Usuarios do Portal
		cQuery += " INNER JOIN "+RetSqlName("BSW")+" BSW "
		cQuery += "   ON BSW.BSW_FILIAL = '"+xFilial("BSW")+"' "
		cQuery += "  AND BSW.BSW_CODUSR = B40.B40_CODUSR "
		cQuery += "  AND BSW.D_E_L_E_T_ = ' ' "
	EndIf

  	cQuery += " WHERE #QueryWhere#"

Return cQuery

/*/{Protheus.doc} getWhereCompanies
Método responsavel por retornar o WHERE da query que busca as Empresas

@type Method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getWhereCompanies() As Character Class CompanyAdapter

	Local cQuery As Character
	
	cQuery := " BG9.BG9_FILIAL = '"+xFilial("BF3")+"' AND "
    cQuery += " BG9.BG9_CODINT = '"+Self:jParams["healthInsurerCode"]+"' AND "

	If Self:jParams:hasProperty("loginUser")
		cQuery += " UPPER(BSW.BSW_LOGUSR) = '"+Upper(Self:jParams["loginUser"])+"' AND "
	EndIf

	If Self:jParams:hasProperty("groupType")
		cQuery += " BG9.BG9_TIPO = '"+Self:jParams["groupType"]+"' AND "
	EndIf

	cQuery += " BG9.D_E_L_E_T_ = ' '"

Return cQuery

/*/{Protheus.doc} getOrderCompanies
Método responsavel por retornar a ORDEM da query que busca as Empresas

@type Method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getOrderCompanies() As Character Class CompanyAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := "BG9.BG9_CODIGO"

Return cOrdemQuery
