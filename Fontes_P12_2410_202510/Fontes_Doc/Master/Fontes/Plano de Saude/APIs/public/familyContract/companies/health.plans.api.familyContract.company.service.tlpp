#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.familyContract.company

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CompanyService
Classe de serviço de Empresas - Regra de Negócio

@type class
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Class CompanyService From BaseService

    Private Data oAdapter As Object

  	Public Method new() Constructor
    Public Method getCompanies() As Logical

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method new() Class CompanyService

    _Super:new()

Return Self

/*/{Protheus.doc} getCompanies
Método responsavel por retornar as paginas de empresas da operadora

@type method
@author Vinicius Queiros Teixeira
@since 07/02/2023
@version Protheus 12
/*/
Method getCompanies() As Logical Class CompanyService

    Local lSucess := .F. As Logical
    Local aKeys := {} As Array

    // Chaves do query param obrigatórios na api
    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})

    If Self:checkRequiredKeys(aKeys, "E001")
        Self:oAdapter := CompanyAdapter():New()
        Self:oAdapter:setQueryParams(Self:jParams)
        Self:oAdapter:setLinks({"rel" : "contracts",;
                                "url" : "/totvsHealthPlans/familyContract/v1/companies",;
                                "paths" : {{"path" : {"company_code"}, "resource" : "contracts"}},;
                                "conditions" : {{"field" : "group_type", "operation" : "==", "value" : "'2'"}},;
                                "queryParams" : {{"key" : "healthInsurerCode", "value" : self:jParams["healthInsurerCode"]}};
                                }) 
        
        If Self:oAdapter:getPageCompanies()
            lSucess := .T.
            Self:nCodeStatus := 200
            Self:jResult := Self:oAdapter:getJsonResult()
        EndIf
    EndIf
   
Return lSucess
