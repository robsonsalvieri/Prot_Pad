#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.familyContract.beneficiary

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} BeneficiaryAdapter
Classe adaptadora de collenction de Beneficiarios - Base de dados

@type class
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Class BeneficiaryAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageBeneficiaries() As Logical

	Private Method mapFieldsBeneficiaries() As Logical
	Private Method getQueryBeneficiaries() As Character
	Private Method getWhereBeneficiaries() As Character
	Private Method getOrderBeneficiaries() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method new() class BeneficiaryAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageBeneficiaries
Método responsavel por retornar a pagina de beneficiarios

@type method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method getPageBeneficiaries() As Logical Class BeneficiaryAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryBeneficiaries() As Character
    Local cWhere := Self:getWhereBeneficiaries() As Character
    Local cOrder := Self:getOrderBeneficiaries() As Character
	
	Self:mapFieldsBeneficiaries()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsBeneficiaries
Método responsavel por mapear os atributos do json com os campos da tabela de beneficiarios

@type method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method mapFieldsBeneficiaries() As Logical Class BeneficiaryAdapter

	Self:oAdapterBase:addMapFields("company_code", "BA1_CODEMP", .T., .F., {"BA1_CODEMP", "C", TamSX3("BA1_CODEMP")[1], 0})
	Self:oAdapterBase:addMapFields("contract_code", "BA1_CONEMP", .T., .F., {"BA1_CONEMP", "C", TamSX3("BA1_CONEMP")[1], 0})
	Self:oAdapterBase:addMapFields("contract_version_code", "BA1_VERCON", .T., .F., {"BA1_VERCON", "C", TamSX3("BA1_VERCON")[1], 0})
	Self:oAdapterBase:addMapFields("subcontract_code", "BA1_SUBCON", .T., .F., {"BA1_SUBCON", "C", TamSX3("BA1_SUBCON")[1], 0})
	Self:oAdapterBase:addMapFields("subcontract_version_code", "BA1_VERSUB", .T., .F., {"BA1_VERSUB", "C", TamSX3("BA1_VERSUB")[1], 0})
	Self:oAdapterBase:addMapFields("family_code", "BA1_MATRIC", .T., .F., {"BA1_MATRIC", "C", TamSX3("BA1_MATRIC")[1], 0})
	Self:oAdapterBase:AddMapFields("subscriber_id", "BA1KEY", .T., .F., {"BA1KEY", "C",;
								   TamSX3("BA1_CODINT")[1]+TamSX3("BA1_CODEMP")[1]+TamSX3("BA1_MATRIC")[1]+TamSX3("BA1_TIPREG")[1]+TamSX3("BA1_DIGITO")[1], 0},;
								   "BA1_CODINT "+self:concatOperator()+"BA1_CODEMP "+self:concatOperator()+"BA1_MATRIC "+self:concatOperator()+"BA1_TIPREG "+self:concatOperator()+"BA1_DIGITO")
    Self:oAdapterBase:addMapFields("name", "BA1_NOMUSR", .T., .F., {"BA1_NOMUSR", "C", TamSX3("BA1_NOMUSR")[1], 0})
	Self:oAdapterBase:addMapFields("social_name", "BA1_NOMSOC", .T., .F., {"BA1_NOMSOC", "C", TamSX3("BA1_NOMSOC")[1], 0})
	Self:oAdapterBase:addMapFields("birth_date", "BA1_DATNAS", .T., .F., {"BA1_DATNAS", "C", TamSX3("BA1_DATNAS")[1], 0}, self:getFormatDate("BA1_DATNAS"))
	Self:oAdapterBase:addMapFields("effective_date", "BA1_DATINC", .T., .F., {"BA1_DATINC", "C", TamSX3("BA1_DATINC")[1], 0}, self:getFormatDate("BA1_DATINC"))
	Self:oAdapterBase:addMapFields("beneficiary_type", "BA1_TIPUSU", .T., .F., {"BA1_TIPUSU", "C", TamSX3("BA1_TIPUSU")[1], 0})
	Self:oAdapterBase:AddMapFields("situation", "SITUACAO", .T., .F., {"SITUACAO", "C", 1, 0},;
                      			   " CASE WHEN BA1_DATINC > '"+DToS(dDataBase)+"' THEN '2'"+; // Inclusão Futura
								   " WHEN BA1_DATBLO = '' THEN '1'"+; // Ativo
								   " WHEN BA1_DATBLO > '"+DToS(dDataBase)+"' THEN '3'"+; // Bloqueio Futuro
								   " ELSE '4' END") // Bloqueado
	Self:oAdapterBase:addMapFields("block_date", "BA1_DATBLO", .T., .F., {"BA1_DATBLO", "C", TamSX3("BA1_DATBLO")[1], 0}, self:getFormatDate("BA1_DATBLO"))
	Self:oAdapterBase:addMapFields("beneficiary_cpf", "BA1_CPFUSR", .T., .F., {"BA1_CPFUSR", "C", TamSX3("BA1_CPFUSR")[1], 0})
	Self:oAdapterBase:addMapFields("email", "BA1_EMAIL", .T., .F., {"BA1_EMAIL", "C", TamSX3("BA1_EMAIL")[1], 0})
	Self:oAdapterBase:addMapFields("card_validity", "BA1_DTVLCR", .T., .F., {"BA1_DTVLCR", "C", TamSX3("BA1_DTVLCR")[1], 0}, self:getFormatDate("BA1_DTVLCR"))
	Self:oAdapterBase:addMapFields("card_copy", "BA1_VIACAR", .T., .F., {"BA1_VIACAR", "N", TamSX3("BA1_VIACAR")[1], 0})

Return .T.

/*/{Protheus.doc} getQueryBeneficiaries
Método responsavel por retornar o corpo da query que busca os beneficiarios

@type Method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method getQueryBeneficiaries() As Character Class BeneficiaryAdapter

	local cQuery as character
	local cTypePortal as character
	local aBindValues := {} as array
	
	cQuery := "SELECT #QueryFields# FROM ? BA1 "

	aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BA1")})

	If Self:jParams:hasProperty("loginUser")

		cTypePortal := Posicione("BSW", 1, xFilial("BSW")+Upper(Self:jParams["loginUser"]), "BSW_TPPOR")

		If cTypePortal == "2" // 2 = Empresa
			// Usuario Portal x Empresa
			cQuery += " INNER JOIN ? B40 "
			cQuery += "   ON B40.B40_FILIAL = ? "
			cQuery += "  AND B40.B40_CODINT = BA1.BA1_CODINT "
			cQuery += "  AND B40.B40_CODEMP = BA1.BA1_CODEMP "
			cQuery += "  AND B40.B40_NUMCON = BA1.BA1_CONEMP "
			cQuery += "  AND B40.B40_VERCON = BA1.BA1_VERCON "
			cQuery += "  AND B40.B40_SUBCON = BA1.BA1_SUBCON "
			cQuery += "  AND B40.B40_VERSUB = BA1.BA1_VERSUB "
			cQuery += "  AND B40.D_E_L_E_T_ = ? "

			aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B40")})
			aAdd(aBindValues, {"set": "string", "value": xFilial("B40")})
			aAdd(aBindValues, {"set": "string", "value": " "})
		Else // 3 = Beneficiario  
			// Usuário Portal x Beneficiário
			cQuery += " INNER JOIN ? B49 "
			cQuery += "   ON B49.B49_FILIAL = ? "

			If Self:jParams:hasProperty("allFamily") .And. Self:jParams["allFamily"] == "1" // 1 = Sim
				cQuery += "  AND "+self:substrOperator()+"(B49_BENEFI, 1, 14) = BA1.BA1_CODINT "+self:concatOperator()+" BA1.BA1_CODEMP "+self:concatOperator()+" BA1.BA1_MATRIC"
			Else
				cQuery += "  AND "+self:substrOperator()+"(B49_BENEFI, 1, 17) = BA1.BA1_CODINT "+self:concatOperator()+" BA1.BA1_CODEMP "+self:concatOperator()+" BA1.BA1_MATRIC "+self:concatOperator()+" BA1.BA1_TIPREG "+self:concatOperator()+" BA1.BA1_DIGITO"
			EndIf

			cQuery += "  AND B49.D_E_L_E_T_ = ? "

			aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B49")})
			aAdd(aBindValues, {"set": "string", "value": xFilial("B49")})
			aAdd(aBindValues, {"set": "string", "value": " "})
		EndIf

		// Usuarios do Portal
		cQuery += " INNER JOIN ? BSW "
		cQuery += "   ON BSW.BSW_FILIAL = ? "
		cQuery += "  AND BSW.BSW_CODUSR = ? "
		cQuery += "  AND BSW.D_E_L_E_T_ = ? "

		aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BSW")})
		aAdd(aBindValues, {"set": "string", "value": xFilial("BSW")})
		aAdd(aBindValues, {"set": "unsafe", "value": iif(cTypePortal == "2", "B40.B40_CODUSR", "B49.B49_CODUSR")})
		aAdd(aBindValues, {"set": "string", "value": " "})
	EndIf	

  	cQuery += " WHERE #QueryWhere#"

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

Return cQuery

/*/{Protheus.doc} getWhereBeneficiaries
Método responsavel por retornar o WHERE da query que busca os beneficiarios

@type Method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method getWhereBeneficiaries() As Character Class BeneficiaryAdapter

	local cQuery as Character
	local jSubscriberIdSplit as Json
	local nSizeSubscriberId := tamSX3("BA1_CODINT")[1]+tamSX3("BA1_CODEMP")[1]+tamSX3("BA1_MATRIC")[1]+tamSX3("BA1_TIPREG")[1]+tamSX3("BA1_DIGITO")[1] as Numeric
	local aBindValues := {} as array

	cQuery := " BA1.BA1_FILIAL = ? AND "
    cQuery += " BA1.BA1_CODINT = ? AND "

	aAdd(aBindValues, {"set": "string", "value": xFilial("BA1")})
    aAdd(aBindValues, {"set": "string", "value": self:jParams["healthInsurerCode"]})

	// Filtros simples da api (propriedade=valor)
	If Self:jParams:hasProperty("companyCode")
		cQuery += " BA1.BA1_CODEMP = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["companyCode"]})
	EndIf

	If Self:jParams:hasProperty("contractCode")
		cQuery += " BA1.BA1_CONEMP = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["contractCode"]})
	EndIf

	If Self:jParams:hasProperty("contractVersionCode")
		cQuery += " BA1.BA1_VERCON = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["contractVersionCode"]})
	EndIf

	If Self:jParams:hasProperty("subcontractCode")
		cQuery += " BA1.BA1_SUBCON = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["subcontractCode"]})
	EndIf

	If Self:jParams:hasProperty("subcontractVersionCode")
		cQuery += " BA1.BA1_VERSUB = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["subcontractVersionCode"]})
	EndIf

	If Self:jParams:hasProperty("familyCode")
		cQuery += " BA1.BA1_MATRIC = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["familyCode"]})
	EndIf

	If Self:jParams:hasProperty("subscriberId")
		jSubscriberIdSplit := self:getSplitSubscriberId(self:jParams["subscriberId"])

		cQuery += " BA1.BA1_CODINT = ? AND "
		cQuery += " BA1.BA1_CODEMP = ? AND "
		cQuery += " BA1.BA1_MATRIC = ? AND "

		aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["codInt"]})
		aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["codEmp"]})
		aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["matric"]})
		
		if !(self:jParams:hasProperty("allFamily") .and. self:jParams["allFamily"] == "1") // 1 = Sim
			cQuery += " BA1.BA1_TIPREG = ? AND "
			cQuery += " BA1.BA1_DIGITO = ? AND "

			aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["tipReg"]})
			aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["digito"]})
		endif
	Endif

	If Self:jParams:hasProperty("name")
		cQuery += " UPPER(BA1.BA1_NOMUSR) LIKE '?%' AND "
		aAdd(aBindValues, {"set": "unsafe", "value": upper(Self:jParams["name"])})
	EndIf

	If Self:jParams:hasProperty("socialName")
		cQuery += " UPPER(BA1.BA1_NOMSOC) LIKE '?%' AND "
		aAdd(aBindValues, {"set": "unsafe", "value": upper(Self:jParams["socialName"])})
	EndIf

	If Self:jParams:hasProperty("beneficiaryType")
		cQuery += " BA1.BA1_TIPUSU = ? AND "
		aAdd(aBindValues, {"set": "string", "value": self:jParams["beneficiaryType"]})
	EndIf

	If self:jParams:hasProperty("subscriberIdOrCpf")
		If Len(self:jParams["subscriberIdOrCpf"]) == nSizeSubscriberId
			jSubscriberIdSplit := self:getSplitSubscriberId(self:jParams["subscriberIdOrCpf"])

			cQuery += " BA1.BA1_CODINT = ? AND "
			cQuery += " BA1.BA1_CODEMP = ? AND "
			cQuery += " BA1.BA1_MATRIC = ? AND "

			aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["codInt"]})
			aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["codEmp"]})
			aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["matric"]})

			if !(self:jParams:hasProperty("allFamily") .and. self:jParams["allFamily"] == "1") // 1 = Sim
				cQuery += " BA1.BA1_TIPREG = ? AND "
				cQuery += " BA1.BA1_DIGITO = ? AND "

				aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["tipReg"]})
				aAdd(aBindValues, {"set": "string", "value": jSubscriberIdSplit["digito"]})
			endif
		Else
			cQuery += " BA1.BA1_CPFUSR = ? AND "
			aAdd(aBindValues, {"set": "string", "value": self:jParams["subscriberIdOrCpf"]})
		EndIf
	Endif

	If Self:jParams:hasProperty("situation")
		Do Case
			Case Self:jParams["situation"] == "1" // Ativo
				cQuery += " BA1.BA1_DATBLO = ? AND BA1.BA1_DATINC <= ? AND "
				aAdd(aBindValues, {"set": "string", "value": " "})
				aAdd(aBindValues, {"set": "date", "value": dDataBase})

			Case Self:jParams["situation"] == "2" // Inclusão Futura
				cQuery += " BA1.BA1_DATINC > ? AND "
				aAdd(aBindValues, {"set": "date", "value": dDataBase})

			Case Self:jParams["situation"] == "3" // Bloqueio Futuro
				cQuery += " BA1.BA1_DATBLO > ? AND "
				aAdd(aBindValues, {"set": "date", "value": dDataBase})

			Case Self:jParams["situation"] == "4" // Bloqueado
				cQuery += " BA1.BA1_DATBLO <> ? AND BA1.BA1_DATBLO <= ? AND "
				aAdd(aBindValues, {"set": "string", "value": " "})
				aAdd(aBindValues, {"set": "date", "value": dDataBase})
		EndCase		
	EndIf

	If Self:jParams:hasProperty("loginUser")
		cQuery += " UPPER(BSW.BSW_LOGUSR) = ? AND "

		// Verifica se os beneficiários bloqueados permitem o login através do motivo de bloqueio (XXX_LOGIN igual a 1)
		cQuery += " ( "
		cQuery += " BA1.BA1_DATBLO = ? " // Beneficiário ativo
		cQuery += " OR (BA1.BA1_DATBLO <> ? AND BA1.BA1_DATBLO <= ? AND " // Beneficiarios Bloqueados
		cQuery += "      (" // Se o bloqueio foi realizado no Familia verifica a tabela BG1
		cQuery += " 	  (BA1.BA1_CONSID = ? AND EXISTS(SELECT BG1.BG1_CODBLO FROM ? BG1 "+; 
															" WHERE BG1_FILIAL = ? "+;
															"   AND BG1.BG1_CODBLO = BA1.BA1_MOTBLO "+;
															"   AND BG1.BG1_LOGIN = ? "+;
		 													"   AND BG1.D_E_L_E_T_ = ?)) "
		cQuery += " 	   OR " // ou se foi realizado no usuário (beneficiário) verifica a tabela BG3
		cQuery += " 	  (BA1.BA1_CONSID = ? AND EXISTS(SELECT BG3.BG3_CODBLO FROM ? BG3 "+;
															" WHERE BG3_FILIAL = ? "+;
															"   AND BG3.BG3_CODBLO = BA1.BA1_MOTBLO "+;
															"   AND BG3.BG3_LOGIN = ? "+;
															"   AND BG3.D_E_L_E_T_ = ?)) "
		cQuery += "      ) "
		cQuery += "    ) "
  		cQuery += " ) AND "

		aAdd(aBindValues, {"set": "string", "value": upper(Self:jParams["loginUser"])})
		aAdd(aBindValues, {"set": "string", "value": " "})
		aAdd(aBindValues, {"set": "string", "value": " "})
		aAdd(aBindValues, {"set": "date", "value": dDataBase})
		aAdd(aBindValues, {"set": "string", "value": "F"})
		aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BG1")})
		aAdd(aBindValues, {"set": "string", "value": xFilial("BG1")})
		aAdd(aBindValues, {"set": "string", "value": "1"})
		aAdd(aBindValues, {"set": "string", "value": " "})
		aAdd(aBindValues, {"set": "string", "value": "U"})
		aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BG3")})
		aAdd(aBindValues, {"set": "string", "value": xFilial("BG3")})
		aAdd(aBindValues, {"set": "string", "value": "1"})
		aAdd(aBindValues, {"set": "string", "value": " "})
	EndIf

	if self:hasValidParam("cardEmissionReason")
		if self:jParams["cardEmissionReason"] == getNewPar("MV_PLSMP1V", "4")
			cQuery += " BA1.BA1_VIACAR = ? AND "
		else
			cQuery += " BA1.BA1_VIACAR <> ? AND "
		endif

		aAdd(aBindValues, {"set": "numeric", "value": 0})

		BPX->(dbSetOrder(1))
		if BPX->(msSeek(xFilial("BPX") + self:jParams["healthInsurerCode"] + self:jParams["cardEmissionReason"])) .and. BPX->BPX_TIPEMI == "2"
			cQuery += " (BA1.BA1_EMICAR = ? OR BA1.BA1_EMICAR = ?) AND "
			aAdd(aBindValues, {"set": "string", "value": "1"})
			aAdd(aBindValues, {"set": "string", "value": "2"})
		else
			cQuery += " BA1.BA1_EMICAR <> ? AND "
			aAdd(aBindValues, {"set": "string", "value": "0"})
		endif
	endif

	if self:hasValidParam("cardValidityStart")
		cQuery += " BA1.BA1_DTVLCR >= ? AND "
		aAdd(aBindValues, {"set": "date", "value": stod(self:convertDateFormat(self:jParams["cardValidityStart"], "S"))})
	endif

	if self:hasValidParam("cardValidityEnd")
		cQuery += " BA1.BA1_DTVLCR <= ? AND "
		aAdd(aBindValues, {"set": "date", "value": stod(self:convertDateFormat(self:jParams["cardValidityEnd"], "S"))})
	endif

	cQuery += " BA1.D_E_L_E_T_ = ? "
	aAdd(aBindValues, {"set": "string", "value": " "})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

Return cQuery

/*/{Protheus.doc} getOrderBeneficiaries
Método responsavel por retornar a ORDEM da query que busca os beneficiarios

@type Method
@author Vinicius Queiros Teixeira
@since 09/02/2023
@version Protheus 12
/*/
Method getOrderBeneficiaries() As Character Class BeneficiaryAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := "BA1.BA1_CODINT, BA1.BA1_CODEMP, BA1.BA1_MATRIC, BA1.BA1_TIPREG, BA1.BA1_DIGITO"

Return cOrdemQuery
