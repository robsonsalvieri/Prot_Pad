#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.contract.reason

using namespace totvs.protheus.health.plan.api.util

/*/{Protheus.doc} ContractualReasonController
Classe responsável por gerenciaras as apis dos motivos contratuais do contrato familiar.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
class ContractualReasonController from BaseController

    public method new() constructor

    @GET("/totvsHealthPlans/familyContract/v1/reasons")
    public method getReasons()

    @GET("/totvsHealthPlans/familyContract/v1/reasons/:code/documents")
    public method getReasonDocuments()

endclass

/*/{Protheus.doc} new
Construtor da classe ContractualReasonController, inicializa a instância da classe pai.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method new() class ContractualReasonController

    _Super:new()

return self

/*/{Protheus.doc} getReasons
Retorna a lista de motivos de solicitações contratuais cadastrados (B9G).
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method getReasons() class ContractualReasonController

    local oService := ContractualReasonService():new() as object
    local jParams := oRest:getQueryRequest() as json

    oService:setQueryParams(jParams)

    if oService:getReasons()
        oRest:setResponse(oService:getJsonResult())
    endif

    oRest:setStatusCode(oService:getStatusCode())
    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    freeObj(oService)
    freeObj(jParams)

    _Super:destroy()

return

/*/{Protheus.doc} getReasonDocuments
Obtém a lista de documentos associados ao motivo contratuais.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method getReasonDocuments() class ContractualReasonController

    local oService := ContractualReasonService():new() as object
    local jPath := oRest:getPathParamsRequest() as json
    local jParams := oRest:getQueryRequest() as json

	if self:validateRequiredParams(jPath, {"code"}) .and. self:validateRequiredParams(jParams, {"healthInsurerCode"})
        oService:setPathParams(jPath)
		oService:setQueryParams(jParams)

		if oService:getReasonDocuments()
			oRest:setResponse(oService:getJsonResult())
		else
			oRest:setFault(oService:getJsonResult())
		endif

		oRest:setStatusCode(oService:getStatusCode())
		oRest:setKeyHeaderResponse("Content-Type", "application/json")
    endif

    oService:destroy()
    freeObj(oService)
    freeObj(jParams)
    freeObj(jPath)

    _Super:destroy()

return
