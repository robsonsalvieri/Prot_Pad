#include "tlpp-core.th"
#include "health.plan.contract.reason.service.ch"

#define STATUS_CODE_OK 200
#define STATUS_CODE_NOT_FOUND 404

namespace totvs.protheus.health.plan.contract.reason

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ContractualReasonService
Classe de serviço responsável por gerenciar os motivos contratuais, incluindo consultas
e regras de negócio relacionadas.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
class ContractualReasonService from BaseService

    public method new() constructor
    public method destroy()

    public method getReasons() as logical
    public method getReasonDocuments() as logical

endclass

/*/{Protheus.doc} new
Construtor da classe ContractualReasonService, inicializa os recursos e dependências necessárias.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method new() class ContractualReasonService

	_Super:new()

return self

/*/{Protheus.doc} destroy
Finaliza a instância da classe ContractualReasonService, liberando os recursos utilizados.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
/*/
method destroy() class ContractualReasonService

	_Super:destroy()

return

/*/{Protheus.doc} getReasons
Obtém a lista de motivos contratuais disponíveis no sistema.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 19/08/2025
@return logical, indica se a operação de obtenção dos motivos foi realizada com sucesso
/*/
method getReasons() as logical class ContractualReasonService   

    local lSuccess := .F. as logical
    local oAdapter := ContractualReasonAdapter():new() as object

    oAdapter:setQueryParams(self:jParams)

    lSuccess := oAdapter:getReasons()

    if lSuccess
        self:nCodeStatus := STATUS_CODE_OK
        self:jResult := oAdapter:getJsonResult()
    endif
 
    oAdapter:destroy()
    freeObj(oAdapter)

return lSuccess

/*/{Protheus.doc} getReasonDocuments
Obtém os documentos relacionados ao motivo contratual informado
@type method
@version 12.1.2510
@author vinicius.queiros
@since 20/08/2025
@return logical, indica se os documentos foram obtidos com sucesso
/*/
method getReasonDocuments() as logical class ContractualReasonService

    local lSuccess := .F. as logical
    local oAdapter := ContractualReasonAdapter():new() as object

    B9G->(dbSetOrder(1))
    if B9G->(msSeek(xFilial("B9G") + self:jParams["healthInsurerCode"] + self:jPath["code"]))
        oAdapter:setQueryParams(self:jParams)
        oAdapter:setPathParams(self:jPath)

        lSuccess := oAdapter:getReasonDocuments()

        if lSuccess
            self:nCodeStatus := STATUS_CODE_OK
            self:jResult := oAdapter:getJsonResult()
        endif
    else
        // "O motivo da solicitação contratual informado não foi encontrado. Verifique o código digitado e tente novamente."
		// "Motivo da solicitação contratual e operadora não localizado na tabela B9G (chaves: B9G_CODINT + B9G_COD)."
		self:setError("", STR0001, STR0002, STATUS_CODE_NOT_FOUND)
    endif

    oAdapter:destroy()
    freeObj(oAdapter)

return lSuccess
