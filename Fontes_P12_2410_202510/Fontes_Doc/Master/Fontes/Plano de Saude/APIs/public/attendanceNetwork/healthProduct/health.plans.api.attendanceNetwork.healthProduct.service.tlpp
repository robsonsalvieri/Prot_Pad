#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.attendanceNetwork.healthProduct 

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} healthProductsService
Classe de serviço Plano de Saúde - Regra de Negócio

@type class
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/ 
Class healthProductsService From BaseService

    Private Data oAdapter As Object

    Public Method new() Constructor
    Public Method getHealthProducts() As Logical
    Public Method getHealthProductsBeneficiary() As Logical
    Public Method getReferencedNetwork() As Logical

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method new() Class healthProductsService

    _Super:new()

Return Self

/*/{Protheus.doc} getHealthProducts
Método responsavel por retornar as paginas dos planos de saúde cadastrados

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getHealthProducts() As Logical Class healthProductsService

    Local lSucess := .F. As Logical
    Local aKeys := {} As Array

    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})

    If Self:checkRequiredKeys(aKeys, "E001")

        Self:oAdapter := healthProductsAdapter():New()
        Self:oAdapter:setQueryParams(Self:jParams)

        If Self:oAdapter:getPageHealthProducts()
            lSucess := .T.
            Self:nCodeStatus := 200
            Self:jResult := Self:oAdapter:getJsonResult()
        EndIf
    EndIf

Return lSucess

/*/{Protheus.doc} getHealthProductsBeneficiary
Método responsavel por retornar o plano de saúde do beneficiário

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getHealthProductsBeneficiary() As Logical Class healthProductsService

    Local lSucess := .F. As Logical 
    Local aFields := {} As Array
	Local cHealthInsurance As Character
    Local cSubscriberId As Character

	aAdd(aFields, {"field" : "subscriberId", "required" : .t., "type" : "C", "size" : tamSX3("BA1_CODINT")[1]+tamSX3("BA1_CODEMP")[1]+tamSX3("BA1_MATRIC")[1]+tamSX3("BA1_TIPREG")[1]+tamSX3("BA1_DIGITO")[1]})

    If self:checkBodyFields(aFields, "E001", self:jPath)

        BA1->(DbSetOrder(2))

        cSubscriberId := self:jPath["subscriberId"]

        If !BA1->(MsSeek(xFilial("BA1")+cSubscriberId))

            self:setError("E002", "Não encontrado o(a) Beneficário(a) informado(a).",;
                                  "Não foi encontrado o(a) beneficiario(a) com a matricula "+cSubscriberId+" .",;  
                                  404) 

        Else 
            cHealthInsurance := BA1->BA1_CODINT +BA1->BA1_CODPLA + BA1->BA1_VERSAO
            
            If Empty(BA1->BA1_CODPLA)

                BA3->(DbSetOrder(1))

                cSubscriberId := SUBSTR(self:jPath["subscriberId"], 1, 14)

                If BA3->(MsSeek(xFilial("BA3")+cSubscriberId))

                    cHealthInsurance := BA3->BA3_CODINT +BA3->BA3_CODPLA + BA3->BA3_VERSAO

                EndIf

            EndIf

            BI3->(DbSetOrder(1))

            BI3->(MsSeek(xFilial("BI3")+cHealthInsurance))

            If BI3->BI3_STATUS != "1"                

                self:setError("E003", "Plano encontrado,porém, verificar se o mesmo está habilitado/ ativo.",;
                                "Verificar campo BI3_STATUS.",;
                                404)             
                
            Else 

                self:setAttributeJson({"attribute" : "productId", "value" : BI3->BI3_CODIGO, "type" : "C"})
                self:setAttributeJson({"attribute" : "version", "value" : BI3->BI3_VERSAO, "type" : "C"})
                self:setAttributeJson({"attribute" : "description", "value" :BI3->BI3_DESCRI, "type" : "C"})
                self:setAttributeJson({"attribute" : "coverage", "value" : BI3->BI3_ABRANG, "type" : "C"})
                self:setAttributeJson({"attribute" : "susep", "value" : BI3->BI3_SUSEP, "type" : "C"})
                self:setAttributeJson({"attribute" : "segmentation", "value" :BI3->BI3_CODSEG , "type" : "C"})
                self:setAttributeJson({"attribute" : "scpaCode", "value" :BI3->BI3_SCPA , "type" : "C"})

                self:nCodeStatus := 200
                lSucess := .T.
                
            Endif

        EndIf

    EndIf 

Return lSucess

/*/{Protheus.doc} getreferencedNetwork
Método responsavel por retornar as paginas dos planos de saúde cadastrados

@type method
@author Pls Team
@since 20/02/2024
@version Protheus 12
/*/
Method getReferencedNetwork() As Logical Class healthProductsService

    Local lSucess := .F. As Logical
    Local aKeys := {} As Array
    Local aFields := {} As Array

    aAdd(aKeys, {"type" : "queryParams", "key" : "healthInsurerCode"})

	aAdd(aFields, {"field" : "healthProducId", "required" : .t., "type" : "C", "size" : tamSX3("BI3_CODIGO")[1]+tamSX3("BI3_VERSAO")[1]})
    aAdd(aFields, {"field" : "specialtieCode", "required" : .t., "type" : "C", "size" : tamSX3("BAX_CODESP")[1]})

    If Self:checkRequiredKeys(aKeys, "E001") .AND. self:checkBodyFields(aFields, "E002", self:jPath)

        Self:oAdapter := healthProductsAdapter():New()
        Self:oAdapter:setPathParams(Self:jPath)
        Self:oAdapter:setQueryParams(Self:jParams)   

        If Self:oAdapter:getPagereferencedNetwork() 
            lSucess := .T.
            Self:nCodeStatus := 200
            Self:jResult := Self:oAdapter:getJsonResult()
        EndIf

    EndIf

Return lSucess
 
