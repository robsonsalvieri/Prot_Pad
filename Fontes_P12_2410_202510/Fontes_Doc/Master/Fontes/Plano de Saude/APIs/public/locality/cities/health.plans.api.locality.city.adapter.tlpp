#Include "tlpp-core.th"

Namespace totvs.protheus.health.plans.api.locality.city

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CityAdapter
Classe adaptadora de collenction cidades - Base de dados

@type class
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Class CityAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageCities() As Logical

	Private Method mapFieldsCities() As Logical
	Private Method getQueryCities() As Character
	Private Method getWhereCities() As Character
	Private Method getOrderCities() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method new() class CityAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageCities
Método responsavel por retornar a pagina de Cidades

@type method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method getPageCities() As Logical Class CityAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryCities() As Character
    Local cWhere := Self:getWhereCities() As Character
    Local cOrder := Self:getOrderCities() As Character
	
	Self:mapFieldsCities()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsCities
Método responsavel por mapear os atributos do json com os campos das cidades

@type method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method mapFieldsCities() As Logical Class CityAdapter

	Self:oAdapterBase:addMapFields("city_code", "BID_CODMUN", .T., .F., {"BID_CODMUN", "C", TamSX3("BID_CODMUN")[1], 0})
	Self:oAdapterBase:addMapFields("city_description", "BID_DESCRI", .T., .F., {"BID_DESCRI", "C", TamSX3("BID_DESCRI")[1], 0})
	Self:oAdapterBase:addMapFields("state_abbreviation", "BID_EST", .T., .F., {"BID_EST", "C", TamSX3("BID_EST")[1], 0})

Return .T.

/*/{Protheus.doc} getQueryCities
Método responsavel por retornar o corpo da query que busca as cidades

@type Method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method getQueryCities() As Character Class CityAdapter

	Local cQuery As Character

	cQuery := "SELECT #QueryFields# FROM "+RetSqlName("BID")+" BID "
    cQuery += " WHERE #QueryWhere#"
 
Return cQuery

/*/{Protheus.doc} getWhereCities
Método responsavel por retornar o WHERE da query que busca as cidades

@type Method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method getWhereCities() As Character Class CityAdapter

	Local cQuery As Character
	
	cQuery := " BID.BID_FILIAL = '"+xFilial("BID")+"' AND "

    // Filtros simples da api (propriedade=valor)
	If Self:jParams:hasProperty("cityCode")
		cQuery += " BID.BID_CODMUN = '"+Self:jParams["cityCode"]+"' AND "
	EndIf

    If Self:jParams:hasProperty("cityDescription")
		cQuery += " UPPER(BID.BID_DESCRI) LIKE '%"+Upper(Self:jParams["cityDescription"])+"%' AND "
	EndIf

    If Self:jParams:hasProperty("stateAbbreviation")
		cQuery += " BID.BID_EST = '"+Self:jParams["stateAbbreviation"]+"' AND "
	EndIf

	cQuery += " BID.D_E_L_E_T_ = ' ' "

Return cQuery

/*/{Protheus.doc} getOrderCities
Método responsavel por retornar a ORDEM da query que busca as cidades

@type Method
@author Vinicius Queiros Teixeira
@since 14/04/2023
@version Protheus 12
/*/
Method getOrderCities() As Character Class CityAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := "BID.BID_EST, BID.BID_CODMUN"

Return cOrdemQuery
