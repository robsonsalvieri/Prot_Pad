#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.locality

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ZipCodeAdapter
Classe responsável por adaptar e processar consultas de códigos postais, herdando funcionalidades de BaseAdapter.
@type class
@version 12.1.2510
@author vinicius.queiros
@since 31/01/2025
/*/
class ZipCodeAdapter from BaseAdapter

	public method new() constructor
	public method getZipCodes() as logical

	protected method mapZipCodeFields() as logical
	protected method getZipCodeQuery() as character
	protected method getZipCodeWhere() as character
	protected method getZipCodeOrder() as character

endclass

/*/{Protheus.doc} new
Método construtor da classe ZipCodeAdapter.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return object, instância da classe ZipCodeAdapter
*/
method new() class ZipCodeAdapter

	_Super:new()

return self

/*/{Protheus.doc} getZipCodes
Método responsável por executar a consulta e obter os códigos postais.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return logical, indica se a operação foi bem-sucedida
*/
method getZipCodes() as logical class ZipCodeAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getZipCodeQuery() as character
	local cWhere := self:getZipCodeWhere() as character
	local cOrder := self:getZipCodeOrder() as character

	self:mapZipCodeFields()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} mapZipCodeFields
Método responsável por mapear os campos de código postal para os campos correspondentes na base de dados.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return logical, sempre retorna .T. indicando que o mapeamento foi realizado com sucesso
*/
method mapZipCodeFields() as logical class ZipCodeAdapter

	self:oAdapterBase:addMapFields("zipCode", "BC9_CEP", .T., .F., {"BC9_CEP", "C", tamSX3("BC9_CEP")[1], 0})
    self:oAdapterBase:addMapFields("address", "BC9_END", .T., .F., {"BC9_END", "C", tamSX3("BC9_END")[1], 0})
    self:oAdapterBase:addMapFields("cityCode", "BC9_CODMUN", .T., .F., {"BC9_CODMUN", "C", tamSX3("BC9_CODMUN")[1], 0})
    self:oAdapterBase:addMapFields("cityDescription", "BC9_MUN", .T., .F., {"BC9_MUN", "C", tamSX3("BC9_MUN")[1], 0})
    self:oAdapterBase:addMapFields("district", "BC9_BAIRRO", .T., .F., {"BC9_BAIRRO", "C", tamSX3("BC9_BAIRRO")[1], 0})
    self:oAdapterBase:addMapFields("state", "BC9_EST", .T., .F., {"BC9_EST", "C", tamSX3("BC9_EST")[1], 0})

return .T.

/*/{Protheus.doc} getZipCodeQuery
Método responsável por gerar a consulta SQL para obter os dados dos códigos postais.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return character, a consulta SQL gerada
*/
method getZipCodeQuery() as character class ZipCodeAdapter

	local cQuery as character

	cQuery := " SELECT #QueryFields# " +;
			  " FROM " + retSqlName("BC9") + " BC9 " +;
			  " WHERE #QueryWhere# "

return cQuery

/*/{Protheus.doc} getZipCodeWhere
Método responsável por construir a cláusula WHERE da consulta SQL, com base nos parâmetros de entrada.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return character, a cláusula WHERE gerada para a consulta SQL
*/
method getZipCodeWhere() as character class ZipCodeAdapter

	local cQuery as character

	cQuery := " BC9.BC9_FILIAL = '" + xFilial("BC9") + "' "

	if self:hasValidParam("zipCode")
		cQuery += " AND BC9.BC9_CEP = '" + self:jParams["zipCode"] + "' "
	endif

	if self:hasValidParam("address")
        cQuery += " AND UPPER(BC9.BC9_END) LIKE '%" + upper(self:jParams["address"]) + "%' "
	endif

	cQuery += " AND BC9.D_E_L_E_T_ = ' ' "

return cQuery

/*/{Protheus.doc} getZipCodeOrder
Método responsável por definir a cláusula ORDER BY da consulta SQL, especificando a ordem dos resultados.
@type method
@version 12.1.2510  
@author vinicius.queiros
@since 03/02/2025 
@return character, a cláusula ORDER BY gerada para a consulta SQL
*/
method getZipCodeOrder() as character class ZipCodeAdapter

	local cOrder as character

	cOrder := "BC9.BC9_CEP"

return cOrder
