#include "tlpp-core.th"
#include "health.plan.api.callcenter.protocol.service.ch"

namespace totvs.protheus.health.plan.api.callcenter

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} AttendanceProtocolService
Serviços das apis do protocolo de atendimento via call center
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 11/06/2024
/*/
class AttendanceProtocolService from BaseService

  	public method new() constructor
    public method postProtocols() as logical
    public method postCloseProtocol() as logical
    public method postRdStationReference() as logical
    public method destroy()
    
    protected method newProtocol() as logical
    protected method closeProtocol() as logical
    protected method RdStationReference() as logical
    protected method setProtocolResponse()
    
endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 11/06/2024
@return object, objeto da instância da classe
/*/
method new() class AttendanceProtocolService

	_Super:new()

return self

/*/{Protheus.doc} postProtocols
Realiza a geração do protocolo de atendimento para o call center
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 11/06/2024
@return logical, se o protocolo foi gerado com sucesso
/*/
method postProtocols() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical
    local aJsonFields := {} as array
    local lFoundBeneficiary := .F. as logical

    aAdd(aJsonFields, {"field" : "subscriberId", "required" : .T., "type" : "C"})
    aAdd(aJsonFields, {"field" : "closeProtocol", "required" : .F., "type" : "L"})
    aAdd(aJsonFields, {"field" : "serviceCode", "required" : .F., "type" : "C", "maxSize" : tamSX3("B00_CODATE")[1]})
    aAdd(aJsonFields, {"field" : "occurrence", "required" : .F., "type" : "C"})
    aAdd(aJsonFields, {"field" : "solution", "required" : .F., "type" : "C"})

    if self:checkBodyFields(aJsonFields, "E002")
        BA1->(dbSetOrder(2))
        if BA1->(msSeek(xFilial("BA1") + self:jBody["subscriberId"]))
            lFoundBeneficiary := .T.
        else
            BA1->(dbSetOrder(5))
            if BA1->(msSeek(xFilial("BA1") + self:jBody["subscriberId"]))
                lFoundBeneficiary := .T.
                self:jBody["subscriberId"] := BA1->(BA1_CODINT + BA1_CODEMP + BA1_MATRIC + BA1_TIPREG + BA1_DIGITO)
            endif
        endif

        if lFoundBeneficiary
            lSuccess := self:newProtocol()
        else
            self:setError("E003",;
                          STR0001,; // "O beneficiário informado não foi encontrado na Operadora."
                          STR0002,; // "Através da carteirinha informada não foi encontrado nenhum beneficiário na tabela BA1, através da chave: BA1_CODINT+BA1_CODEMP+BA1_MATRIC+BA1_TIPREG+BA1_DIGITO ou BA1_MATANT."
                          404) // Not found
        endif
    endif

    fwFreeArray(aJsonFields)
           
return lSuccess

/*/{Protheus.doc} postCloseProtocol
Realiza o encerramento do protocolo de atendimento
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/06/2024
@return logical, se o protocolo foi encerrado
/*/
method postCloseProtocol() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical
    local aJsonFields := {} as array

    aAdd(aJsonFields, {"field" : "protocol", "required" : .T., "type" : "C", "size" : tamSX3("B00_COD")[1]})
    aAdd(aJsonFields, {"field" : "serviceCode", "required" : .F., "type" : "C", "maxSize" : tamSX3("B00_CODATE")[1]})
    aAdd(aJsonFields, {"field" : "occurrence", "required" : .F., "type" : "C"})
    aAdd(aJsonFields, {"field" : "solution", "required" : .F., "type" : "C"})

    if self:checkBodyFields(aJsonFields, "E002")
        B00->(dbSetOrder(1))
        if B00->(msSeek(xFilial("B00") + self:jBody["protocol"]))
            if self:closeProtocol()
                self:setProtocolResponse()

                self:nCodeStatus := 201 // Create
                lSuccess := .T.
            endif
        else
            self:setError("E003",;
                          STR0003,; // "Protocolo informado não encontrado na Operadora."
                          STR0004,; // "Não encontrado nenhum protocolo com esse código na tabela B00, através do campo B00_COD."
                          404) // Not found
        endif
    endif

    fwFreeArray(aJsonFields)

return lSuccess

/*/{Protheus.doc} postRdStationReference
Realiza a integração com o Rd Conversas
@type method
@version 12.1.2510  
@author giovanna.charlo
@since 31/07/2025
@return logical, se a integração foi feita
/*/
method postRdStationReference() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical
    local aJsonFields := {} as array

    aAdd(aJsonFields, {"field" : "protocolRD", "required" : .T., "type" : "C"})
    aAdd(aJsonFields, {"field" : "protocolANS", "required" : .T., "type" : "C"})
  
    if self:checkBodyFields(aJsonFields, "E002") 
        B00->(dbSetOrder(1))
        if B00->(msSeek(xFilial("B00") + self:jBody["protocolANS"]))
            if self:RdStationReference()
                self:setProtocolResponse()
                self:nCodeStatus := 201 // Create
                lSuccess := .T.
            endif
        else
            self:setError("E003",;
                          STR0003,; // "Protocolo informado não encontrado na Operadora."
                          STR0004,; // "Não encontrado nenhum protocolo com esse código na tabela B00, através do campo B00_COD."
                          404) // Not found
        endif
    endif

    fwFreeArray(aJsonFields)

return lSuccess

/*/{Protheus.doc} destroy
Limpa da memória as variáveis (objeto, array e json) da clase
@type method
@version 12.1.2410
@author vinicius.queiros
@since 11/06/2024
/*/
method destroy() class AttendanceProtocolService

    //_Super:destroy()

return

/*/{Protheus.doc} newProtocol
Gera um novo protocolo de atendimento na tabela B00 de origem Call Center
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/06/2024
@return logical, se o novo protocolo foi criado
/*/
method newProtocol() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical
    local cProtocol := "" as character
    local lIsCallCenter := .T. as logical
    
    gerRegB00(@cProtocol, nil, "", .T., lIsCallCenter, self:jBody["subscriberId"])

    if !empty(cProtocol)
        B00->(dbSetOrder(1))
        if B00->(msSeek(xFilial("B00") + cProtocol))
            if self:jBody:hasProperty("closeProtocol")
                self:jBody["protocol"] := B00->B00_COD

                lSuccess := self:closeProtocol()
            else
                lSuccess := .T.      
            endif
            
            if lSuccess
                self:setProtocolResponse()
                self:nCodeStatus := 201 // Create
            endif
        endif
    endif

return lSuccess

/*/{Protheus.doc} closeProtocol
Encerra o protocolo de atendimento de origem do call center
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/06/2024
@return logical, se o protocolo foi encerrado
/*/
method closeProtocol() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical

    if B00->B00_STATUS <> "2"
        B00->(recLock("B00", .F.))
            B00->B00_STATUS := "2"
            B00->B00_DTENCE := dDataBase
			B00->B00_HRENCE := strTran(time(),":","")

             if self:jBody:hasProperty("serviceCode") .and. !empty(self:jBody["serviceCode"])
                B00->B00_CODATE := self:jBody["serviceCode"]
            endif

            if self:jBody:hasProperty("occurrence") .and. !empty(self:jBody["occurrence"])
                B00->B00_OCOTMK := self:jBody["occurrence"]
            endif

            if self:jBody:hasProperty("solution") .and. !empty(self:jBody["solution"])
                B00->B00_SOLTMK := self:jBody["solution"]
            endif
        B00->(msUnLock())

        lSuccess := .T.
    else
        self:setError("E004",;
                      STR0005,; // "O protocolo informado já foi encerrado.",
                      STR0006,; // "Não é possivel encerrar um protocolo de atendimento com o status (B00_STATUS) diferente de 2."
                      400) // Bad request
    endif

return lSuccess

/*/{Protheus.doc} RdStationReference
Gravação do Campo Nº Protocolo RD Conversas (B00_NRPRDC)
@type method
@version 12.1.2510  
@author giovanna.charlo
@since 31/07/2025
@return lSuccess, se o campo foi gravado
/*/
method RdStationReference() as logical class AttendanceProtocolService

    local lSuccess := .F. as logical

     if B00->(FieldPos("B00_NRPRDC")) > 0 .and. self:jBody:hasProperty("protocolRD") .and. !empty(self:jBody["protocolRD"])

        B00->(recLock("B00", .F.))
            B00->B00_NRPRDC := self:jBody["protocolRD"]
        B00->(msUnLock())

        lSuccess := .T.
    endif

return lSuccess

/*/{Protheus.doc} newProtocol
Defini os dados de resposta do protocolo posicionado (B00)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 12/06/2024
/*/
method setProtocolResponse() class AttendanceProtocolService

    self:setAttributeJson({"attribute": "protocolCode", "value": B00->B00_COD, "type": "C"})
    self:setAttributeJson({"attribute": "protocolDate", "value": B00->B00_DTPROT, "type": "D"})
    self:setAttributeJson({"attribute": "protocolHour", "value": transform(B00->B00_HRPROT, "@R !!:!!"), "type": "C"})
    self:setAttributeJson({"attribute": "sendingType", "value": B00->B00_TPENVI, "type": "C"})
    self:setAttributeJson({"attribute": "status", "value": B00->B00_STATUS, "type": "C"})
    self:setAttributeJson({"attribute": "receiverEmail", "value": B00->B00_EMAILD, "type": "C"})
    self:setAttributeJson({"attribute": "closingDate", "value": B00->B00_DTENCE, "type": "D"})
    self:setAttributeJson({"attribute": "closingHour", "value": iif(!empty(B00->B00_HRENCE), transform(B00->B00_HRENCE, "@R !!:!!"), ""), "type": "C"})
    self:setAttributeJson({"attribute": "subscriberId", "value": B00->B00_MATRIC, "type": "C"})
    self:setAttributeJson({"attribute": "beneficiaryName", "value": B00->B00_NOMUSR, "type": "C"})
    self:setAttributeJson({"attribute": "beneficiarySocialName", "value": B00->B00_NOMSOC, "type": "C"})
    self:setAttributeJson({"attribute": "serviceCode", "value": B00->B00_CODATE, "type": "C"})
    self:setAttributeJson({"attribute": "occurrence", "value": B00->B00_OCOTMK, "type": "C"})
    self:setAttributeJson({"attribute": "solution", "value": B00->B00_SOLTMK, "type": "C"})

    if B00->(FieldPos("B00_NRPRDC")) > 0 
        self:setAttributeJson({"attribute": "protocolRD", "value": B00->B00_NRPRDC, "type": "C"})
    endif
return

