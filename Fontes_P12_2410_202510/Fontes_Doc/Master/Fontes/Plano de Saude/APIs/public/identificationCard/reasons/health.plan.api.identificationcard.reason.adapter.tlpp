#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.identificationcard

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CardReasonController
Adapter das apis dos motivos de emissão do cartão do beneficiário
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 03/06/2024
/*/
class CardReasonAdapter from BaseAdapter

	public method new() constructor
	public method getReasonPages() as logical

	protected method mapFields() as logical
	protected method getQuery() as character
	protected method getWhere() as character
	protected method getOrder() as character

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 03/06/2024
@return object, objeto da instância da classe
/*/
method new() class CardReasonAdapter

	_Super:new()

return self

/*/{Protheus.doc} getReasonPages
Retorna as paginas dos motivos de emissão do cartão do beneficiário
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
@return logical, se as paginas foram retornadas com sucesso
/*/
method getReasonPages() as logical class CardReasonAdapter

	local lSuccess := .F. as logical
    local cQuery := self:getQuery() as character
    local cWhere := self:getWhere() as character
    local cOrder := self:getOrder() as character
	
	self:mapFields()

	if self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSuccess := .T.
	endif

return lSuccess

/*/{Protheus.doc} mapFields
Mapea os atributos do json com os campos do dicionário de dados (SX3)
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
/*/
method mapFields() as logical class CardReasonAdapter

	self:oAdapterBase:addMapFields("code", "BPX_MOTIVO", .T., .F., {"BPX_MOTIVO", "C", tamSX3("BPX_MOTIVO")[1], 0})
	self:oAdapterBase:addMapFields("description", "BPX_DESCRI", .T., .F., {"BPX_DESCRI", "C", tamSX3("BPX_DESCRI")[1], 0})

return

/*/{Protheus.doc} getQuery
Retorna o corpo da query que busca os motivos de emissão do cartão
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
@return character, corpo da query
/*/
method getQuery() as character class CardReasonAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " SELECT #QueryFields# "
    cQuery += " FROM ? BPX "
    cQuery += " WHERE #QueryWhere# "

	aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BPX")})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)
 
return cQuery

/*/{Protheus.doc} getWhere
Retorna as condições da query que busca os motivos de emissão do cartão
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
@return character, where da query
/*/
method getWhere() as character class CardReasonAdapter

	local cQuery as character
    local cPortalReasons as character
	local aBindValues := {} as array
	
	cQuery := " BPX.BPX_FILIAL = ? AND "
    cQuery += " BPX.BPX_CODINT = ? AND "

	aAdd(aBindValues, {"set": "string", "value": xFilial("BPX")})
	aAdd(aBindValues, {"set": "string", "value": self:jParams["healthInsurerCode"]})

    if self:jParams:hasProperty("isPortal") .and. self:jParams["isPortal"] == "true"
        cPortalReasons := superGetMV("MV_PLSPERO", .F., "")

        cQuery += " BPX.BPX_MOTIVO IN (?) AND "
		aAdd(aBindValues, {"set": "in", "value": strToKarr(cPortalReasons, "/")})
    endif

	if self:hasValidParam("description")
		cQuery += " BPX.BPX_DESCRI LIKE '?%' AND "
		aAdd(aBindValues, {"set": "unsafe", "value": upper(self:jParams["description"])})
	endif

	cQuery += " BPX.D_E_L_E_T_ = ? "
	aAdd(aBindValues, {"set": "string", "value": " "})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

    fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getOrder
Retorna a ordem da query que busca os motivos de emissão do cartão
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
@return character, order da query
/*/
method getOrder() as character class CardReasonAdapter

	local cOrder as character
	
	cOrder := "BPX.BPX_CODINT, BPX.BPX_MOTIVO"

return cOrder
