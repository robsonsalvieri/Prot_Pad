#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plan.api.identificationcard

/*/{Protheus.doc} CardReasonController
API motivos de emissão do cartão de identificação do beneficiário 
@type class
@version 12.1.2410 
@author vinicius.queiros
@since 03/06/2024
/*/
class CardReasonController

    public method new() constructor

    @GET("/totvsHealthPlans/identificationCard/v1/reasons")
    public method apiGetReasons()
    
endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author vinicius.queiros
@since 03/06/2024
@return object, objeto da instância da classe
/*/
method new() class CardReasonController

return self

/*/{Protheus.doc} apiGetReasons
Recurso responsável por retornar os motivos de emissão do cartão do beneficiário
@type method
@version 12.1.2410  
@author vinicius.queiros
@since 03/06/2024
/*/
method apiGetReasons() class CardReasonController

    local jParams := oRest:getQueryRequest() as Json 
    local oService as object

    oService := CardReasonService():new()
    oService:setQueryParams(jParams)

    if oService:getReasons()
        oRest:setResponse(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oService:destroy()
    
    freeObj(oService)
    freeObj(jParams)

return
