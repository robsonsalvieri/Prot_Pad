#include "tlpp-core.th"

namespace totvs.protheus.health.plan.identificationcard.batch

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} CardBatchAdapter
Classe utilizada para adaptar e manipular dados relacionados ao lote de cartões dos beneficiários
@type class
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
class CardBatchAdapter from BaseAdapter

	public method new() constructor
	public method getCardBatches() as logical
	public method getBatchBeneficiaries() as logical

	protected method cardBatchesFieldsMap() as logical
	protected method getCardBatchesQuery() as character
	protected method getCardBatchesWhere() as character
	protected method getCardBatchesOrder() as character

	protected method batchBeneficiariesFieldsMap() as logical
	protected method getBatchBeneficiariesQuery() as character
	protected method getBatchBeneficiariesWhere() as character
	protected method getBatchBeneficiariesOrder() as character

endclass

/*/{Protheus.doc} new
Inicializa a classe CardBatchAdapter, configurando seus atributos e preparando-a para uso.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
/*/
method new() class CardBatchAdapter

	_Super:new()

return self

/*/{Protheus.doc} getCardBatches
Obtém os lotes de cartões gerados, realizando consulta ao banco de dados.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return logical, indica se os dados foram recuperados com sucesso
/*/
method getCardBatches() as logical class CardBatchAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getCardBatchesQuery() as character
	local cWhere := self:getCardBatchesWhere() as character
	local cOrder := self:getCardBatchesOrder() as character

	self:cardBatchesFieldsMap()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} getBatchBeneficiaries
Obtém a lista de beneficiários vinculados a um determinado lote de cartões, realizando consulta ao banco de dados.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return logical, indica se os dados dos beneficiários foram obtidos com sucesso
/*/
method getBatchBeneficiaries() as logical class CardBatchAdapter

	local lSuccess := .F. as logical
	local cQuery := self:getBatchBeneficiariesQuery() as character
	local cWhere := self:getBatchBeneficiariesWhere() as character
	local cOrder := self:getBatchBeneficiariesOrder() as character

	self:batchBeneficiariesFieldsMap()

	lSuccess := self:executeQueryAdapter(cQuery, cWhere, cOrder)

return lSuccess

/*/{Protheus.doc} cardBatchesFieldsMap
Mapeia e estrutura os campos utilizados no retorno da api do lote de cartões
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method cardBatchesFieldsMap() as logical class CardBatchAdapter

	self:oAdapterBase:addMapFields("batchCode", "BDE_CODIGO", .T., .F., {"BDE_CODIGO", "C", tamSX3("BDE_CODIGO")[1], 0})
	self:oAdapterBase:addMapFields("companyCode", "BDE_EMPDE", .T., .F., {"BDE_EMPDE", "C", tamSX3("BDE_EMPDE")[1], 0})
	self:oAdapterBase:addMapFields("contractCode", "BDE_CONDE", .T., .F., {"BDE_CONDE", "C", tamSX3("BDE_CONDE")[1], 0})
	self:oAdapterBase:addMapFields("subcontractCode", "BDE_SUBDE", .T., .F., {"BDE_SUBDE", "C", tamSX3("BDE_SUBDE")[1], 0})
	self:oAdapterBase:addMapFields("beneficiaryCount", "BDE_QTD", .T., .F., {"BDE_QTD", "N", tamSX3("BDE_QTD")[1], 0})
	self:oAdapterBase:addMapFields("reason", "BDE_MOTIVO", .T., .F., {"BDE_MOTIVO", "C", tamSX3("BDE_MOTIVO")[1], 0})
	self:oAdapterBase:addMapFields("status", "BDE_STACAR", .T., .F., {"BDE_STACAR", "C", tamSX3("BDE_STACAR")[1], 0})
	self:oAdapterBase:addMapFields("requesterName", "BDE_EMPRES", .T., .F., {"BDE_EMPRES", "C", tamSX3("BDE_EMPRES")[1], 0})

	if BDE->(fieldPos("BDE_DTEMIS")) > 0 .and. BDE->(fieldPos("BDE_HREMIS")) > 0 .and. BDE->(fieldPos("BDE_QTCRIT")) > 0
		self:oAdapterBase:addMapFields("date", "BDE_DTEMIS", .T., .F., {"BDE_DTEMIS", "C", TamSX3("BDE_DTEMIS")[1], 0}, self:getFormatDate("BDE_DTEMIS"))
		self:oAdapterBase:addMapFields("time", "BDE_HREMIS", .T., .F., {"BDE_HREMIS", "C", tamSX3("BDE_HREMIS")[1], 0})
		self:oAdapterBase:addMapFields("failedCount", "BDE_QTCRIT", .T., .F., {"BDE_QTCRIT", "N", tamSX3("BDE_QTCRIT")[1], 0})
	endif

return .T.

/*/{Protheus.doc} getCardBatchesQuery
Retorna a query utilizada para buscar os dados dos lotes de cartões
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, instrução SQL para consulta dos lotes de cartões
/*/
method getCardBatchesQuery() as character class CardBatchAdapter

	local cQuery as character
	local cLoginCode as character
	local aBindValues := {} as array

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM ? BDE "
	aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BDE")})

	if self:hasValidParam("loginUser")
		cLoginCode := posicione("BSW", 1, xFilial("BSW") + upper(self:jParams["loginUser"]), "BSW_CODUSR")

		cQuery += " INNER JOIN ? B40 ON"
		cQuery += "		B40.B40_FILIAL = ? AND "
		cQuery += "		B40.B40_CODINT = BDE.BDE_CODINT AND "
		cQuery += "		B40.B40_CODEMP = BDE.BDE_EMPDE AND "
        cQuery += "		B40.B40_CODEMP = BDE.BDE_EMPATE AND "
		cQuery += "		B40.B40_NUMCON = BDE.BDE_CONDE AND "
        cQuery += "		B40.B40_NUMCON = BDE.BDE_CONATE AND "
		cQuery += "		B40.B40_SUBCON = BDE.BDE_SUBDE AND "
        cQuery += "		B40.B40_SUBCON = BDE.BDE_SUBATE AND "
		cQuery += "		B40.B40_CODUSR = ? AND "
		cQuery += "		B40.D_E_L_E_T_ = ? "

		aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("B40")})
		aAdd(aBindValues, {"set": "string", "value": xFilial("B40")})
		aAdd(aBindValues, {"set": "string", "value": cLoginCode})
		aAdd(aBindValues, {"set": "string", "value": " "})
	endif

	cQuery += " WHERE #QueryWhere# "

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getCardBatchesWhere
Retorna a cláusula WHERE da query utilizada na busca dos lotes de cartões, com os filtros aplicáveis conforme os parâmetros recebidos.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, cláusula WHERE para filtragem da consulta de lotes de cartões
/*/
method getCardBatchesWhere() as character class CardBatchAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " BDE.BDE_FILIAL = ? AND "
    cQuery += " BDE.BDE_CODINT = ? AND "

	aAdd(aBindValues, {"set": "string", "value": xFilial("BDE")})
	aAdd(aBindValues, {"set": "string", "value": self:jParams["healthInsurerCode"]})

	if self:hasValidParam("batchCode")
		cQuery += " BDE.BDE_CODIGO LIKE '%?' AND "
		aAdd(aBindValues, {"set": "unsafe", "value": self:jParams["batchCode"]})
	endif

	if self:hasValidParam("status")
		cQuery += " BDE.BDE_STACAR IN (?) AND "
		aAdd(aBindValues, {"set": "in", "value": strToKarr(self:jParams["status"], ",")})
	endif

	cQuery += " BDE.D_E_L_E_T_ = ? "
	aAdd(aBindValues, {"set": "string", "value": " "})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getCardBatchesOrder
Retorna a cláusula ORDER BY utilizada na query de consulta dos lotes de cartões, definindo a ordenação dos registros.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, cláusula ORDER BY para ordenação da consulta de lotes de cartões
/*/
method getCardBatchesOrder() as character class CardBatchAdapter

	local cOrder as character

	cOrder := "BDE.BDE_CODIGO DESC"

return cOrder

/*/{Protheus.doc} batchBeneficiariesFieldsMap
Mapeia e estrutura os campos utilizados no retorno da api dos beneficiários do lote de cartão
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return logical, indica se o mapeamento dos campos foi realizado com sucesso
/*/
method batchBeneficiariesFieldsMap() as logical class CardBatchAdapter

	local nIdFieldSizes := tamSX3("BA1_CODINT")[1] + tamSX3("BA1_CODEMP")[1] + tamSX3("BA1_MATRIC")[1] + tamSX3("BA1_TIPREG")[1] + tamSX3("BA1_DIGITO")[1] as numeric
	local cIdFields as character

	cIdFields := "BED_CODINT " + self:concatOperator() +;
				 "BED_CODEMP " + self:concatOperator() +;
				 "BED_MATRIC " + self:concatOperator() +;
				 "BED_TIPREG " + self:concatOperator() +;
				 "BED_DIGITO"

	self:oAdapterBase:addMapFields("subscriberId", "BA1KEY", .T., .F., {"BA1KEY", "C", nIdFieldSizes, 0}, cIdFields)
	self:oAdapterBase:addMapFields("cardCopy", "BED_VIACAR", .T., .F., {"BED_VIACAR", "N", tamSX3("BED_VIACAR")[1], 0})
	self:oAdapterBase:addMapFields("name", "BED_NOMUSR", .T., .F., {"BED_NOMUSR", "C", tamSX3("BED_NOMUSR")[1], 0})
	self:oAdapterBase:addMapFields("requestDate", "BED_DTSOLI", .T., .F., {"BED_DTSOLI", "C", TamSX3("BED_DTSOLI")[1], 0}, self:getFormatDate("BED_DTSOLI"))
	self:oAdapterBase:addMapFields("status", "BED_STACAR", .T., .F., {"BED_STACAR", "C", tamSX3("BED_STACAR")[1], 0})

return .T.

/*/{Protheus.doc} getBatchBeneficiariesQuery
Retorna a query utilizada para buscar os dados dos beneficiários vinculados aos lotes de cartões.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, instrução SQL para consulta dos beneficiários do lote de cartões
/*/
method getBatchBeneficiariesQuery() as character class CardBatchAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM ? BED "
	cQuery += " WHERE #QueryWhere# "

	aAdd(aBindValues, {"set": "unsafe", "value": retSqlName("BED")})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getBatchBeneficiariesWhere
Retorna a cláusula WHERE da query para filtragem dos beneficiários vinculados aos lotes de cartões.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, cláusula WHERE para filtragem da consulta dos beneficiários do lote de cartões
/*/
method getBatchBeneficiariesWhere() as character class CardBatchAdapter

	local cQuery as character
	local aBindValues := {} as array

	cQuery := " BED.BED_FILIAL = ? AND "
	cQuery += " BED.BED_CDIDEN = ? AND "
	cQuery += " BED.D_E_L_E_T_ = ? "

	aAdd(aBindValues, {"set": "string", "value": xFilial("BED")})
	aAdd(aBindValues, {"set": "string", "value": self:jPath["batchCode"]})
	aAdd(aBindValues, {"set": "string", "value": " "})

	cQuery := self:bindValuesToQuery(cQuery, aBindValues)

	fwFreeArray(aBindValues)

return cQuery

/*/{Protheus.doc} getBatchBeneficiariesOrder
Retorna a cláusula ORDER BY utilizada na query para ordenar os beneficiários vinculados aos lotes de cartões.
@type method
@version 12.1.2510
@author vinicius.queiros
@since 05/08/2025
@return character, cláusula ORDER BY para ordenação da consulta dos beneficiários do lote de cartões
/*/
method getBatchBeneficiariesOrder() as character class CardBatchAdapter

	local cOrder as character

	cOrder := "BED_CODINT, BED_CODEMP, BED_MATRIC, BED_TIPREG, BED_DIGITO"

return cOrder
