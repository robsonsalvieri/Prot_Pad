#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.health.plans.api.identificationCard.card

/*/{Protheus.doc} CardController
API Cartão de identificação do beneficiário - Carteirinha 
@type class
@author José Paulo
@since 01/03/2023
/*/
class CardController

    public method new() constructor

    @POST("/totvsHealthPlans/identificationCard/v1/cards")
    public method apiPostCard()

endclass

/*/{Protheus.doc} new
Método construtor da classe
@type method
@version 12.1.2410
@author José Paulo
@since 01/03/2023
@return object, objeto da instância da classe
/*/
method new() class CardController

return self

/*/{Protheus.doc} apiPostCard
Recurso responsável por solicitar uma via de cartão para o beneficiário
@type Method
@author José Paulo
@since 01/03/2023
@version 12.1.2410
/*/
method apiPostCard() class CardController

    local cBody := oRest:getBodyRequest() as character
    local oService := CardService():new()

    if oService:setBodyString(cBody, "E001")
        if oService:postCards()
            oRest:setResponse(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        else
            oRest:setFault(oService:getJsonResult())
            oRest:setStatusCode(oService:getStatusCode())
        endif
    else
        oRest:setFault(oService:getJsonResult())
        oRest:setStatusCode(oService:getStatusCode())
    endif

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    freeObj(oService)

return
