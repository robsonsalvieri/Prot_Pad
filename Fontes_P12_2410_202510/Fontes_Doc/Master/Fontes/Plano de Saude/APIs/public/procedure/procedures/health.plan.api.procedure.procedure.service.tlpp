#include "tlpp-core.th"

namespace totvs.protheus.health.plan.api.procedures

using namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} ProcedureService
Classe de serviço de procedimentos

@type class
@author vinicius.queiros
@since 13/10/2023
@version 12.1.2410
/*/
class ProcedureService from BaseService

  	public method new() constructor
    public method getProcedureId() as logical
    
endclass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author vinicius.queiros
@since 13/10/2023
@version 12.1.2410
/*/
method new() class ProcedureService

    _Super:new()

return self

/*/{Protheus.doc} getProcedureId
Método responsavel por retornar os dados do procedimento

@type method
@author vincius.queiros
@since 13/10/2023
@version 12.1.2410
/*/
method getProcedureId() as logical class ProcedureService

    local lSucess := .f. as logical
    local aKeys := {} as array
    local cProcedureId := "" as character

    aAdd(aKeys, {"type" : "pathParams", "key" : "procedureId"})

    if self:checkRequiredKeys(aKeys, "E001") // Not Acceptable
        cProcedureId := padr(self:jPath["procedureId"], tamSX3("BR8_CODPAD")[1]+tamSX3("BR8_CODPSA")[1])

        BR8->(dbSetOrder(1))
        if BR8->(msSeek(xFilial("BR8")+cProcedureId))
            self:setAttributeJson({"attribute" : "tableCode", "value" : BR8->BR8_CODPAD, "type" : "C"})
            self:setAttributeJson({"attribute" : "procedureCode", "value" : BR8->BR8_CODPSA, "type" : "C"})
            self:setAttributeJson({"attribute" : "procedureDescription", "value" : BR8->BR8_DESCRI, "type" : "C"})
            self:setAttributeJson({"attribute" : "active", "value" : BR8->BR8_BENUTL, "type" : "C"})
            self:setAttributeJson({"attribute" : "procedureId", "value" : BR8->(BR8_CODPAD+BR8_CODPSA), "type" : "C"})

            lSucess := .t.
            self:nCodeStatus := 200 // Ok
        else
            self:setError("E002", "Nenhum procedimento foi encontrada",;
                                  "Não foi encontrado nenhum procedimento para o identificar "+cProcedureId+" na tabela BR8 (BR8_CODPAD+BR8_CODPSA).",;
                                  404) // Not found
        endif
    endif
           
return lSucess
