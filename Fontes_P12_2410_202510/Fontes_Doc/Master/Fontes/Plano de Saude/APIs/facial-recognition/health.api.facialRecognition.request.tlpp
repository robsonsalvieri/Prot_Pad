#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

@Get("/totvshealthplans/v1/facialRecognition/:id")
function healthGetFacialRecognition()
    local oRequest  := HealthFacialRecognitionRequest():new(oRest,'GET')
    local lResult   := .f.
    local jPath :=  oRest:getPathParamsRequest()
    
    lResult := oRequest:get(jPath)

    oRequest:endRequest()
return

@Post("/totvshealthplans/v1/facialRecognition")
function healthPostFacialRecognition()
    local oRequest  := HealthFacialRecognitionRequest():new(oRest,'POST')
    local lResult   := .f.

    if oRequest:valida()
        lResult := oRequest:post()
    endif
    oRequest:endRequest()
return


class HealthFacialRecognitionRequest from CenRequest 

    Public Method new()
    Public Method get(jPath)    
    Public Method post()
    Public Method setRest()
    Public Method valida()

    Public data oFWRest
    Public data aHeader
    
endClass

Method New(oRest) class HealthFacialRecognitionRequest
    _Super:new(oRest,'FacialRecognition',.t.)
    self:oFWRest := nil
Return self

Method get(jPath) class HealthFacialRecognitionRequest
    local aRet := {}
    self:setRest('/result/'+jPath['id'])

    if existBlock( "PLSRFACG" ) 
		aRet := execBlock( "PLSRFACG",.F.,.F.,{ jPath } )
        self:cResponse  := aRet[1]
        self:nStatus    := aRet[2]
	else
	    lSuccess            := self:oFWRest:get(self:aHeader)
        self:cResponse      := self:oFWRest:getResult()
        self:nStatus        := val(self:oFWRest:getHttpCode())
    endif
Return

Method post() class HealthFacialRecognitionRequest
    local oJson     := jsonObject():new()
    local oJsonRet  := jsonObject():new()
    local aRet      := {}
    local lMVOk     := .t.
    local cBrand    := GetNewPar("MV_PLDBRND",'')//3f6efcd5c3eae601b2da0705109ca27ca080cf7e1207123580af7b4de4fa6eec

    if empty(cBrand)
        lMVOk := .f.
        self:cFaultDesc := 'Parametro MV_PLDBRND não preenchido, verifique a documentação.'
    endif

    if self:lSuccess .and. lMVOk
        oJson['cpf']           := BA1->BA1_CPFUSR
        oJson['phone']         := ifPls(iif(BA1->(fieldpos("BA1_WPP")>0),PTURemChr(BA1->BA1_WPP),""),'11999999999')
        oJson['name']          := alltrim(BA1->BA1_NOMUSR)
        oJson['mother_name']   := alltrim(BA1->BA1_MAE)
        oJson['birth_date']    := Substr(Dtos(BA1->BA1_DATNAS),7,2)+"/"+Substr(Dtos(BA1->BA1_DATNAS),5,2)+"/"+Substr(Dtos(BA1->BA1_DATNAS),1,4)
        oJson['email']         := alltrim(BA1->BA1_EMAIL)
        oJson['brand']         := cBrand
        oJson['return_link']   := .t.
        lMVOk := self:setRest('/start')
        if lMVOk
            self:oFWRest:setPostParams(oJson:toJson())
		    lSuccess            := self:oFWRest:post(self:aHeader)
            self:cResponse      := self:oFWRest:getResult()
            self:nStatus        := val(self:oFWRest:getHttpCode())        

            if cvaltochar(self:jRequest['alert'] ) == 'true'
                oJsonRet:fromJSON(self:cResponse)
                if !empty(cvaltochar(self:jRequest['healthProviderId']))
                    BAU->(dbsetorder(1))
                    BAU->(msseek(xfilial('BAU')+self:jRequest['healthProviderId']))
                    PLSNotifica('000007',self:jRequest['healthProviderId'],cvaltochar(oJsonRet['link']))
                    oJsonRet['message'] := 'Notificação enviada para o prestador ' 
                else
                    PLSNotifica('000008',self:jRequest['subscriberId'],cvaltochar(oJsonRet['link']))
                    oJsonRet['message'] := 'Notificação enviada para o beneficiario ' 
                endif
                self:cResponse      := oJsonRet:toJson()
            endif
        endif
    endif

    if self:lSuccess .and. existBlock( "PLSRFACP" ) 
		aRet := execBlock( "PLSRFACP",.F.,.F.,{ self:jRequest } )
        self:cResponse  := aRet[1]
        self:nStatus    := aRet[2]
    elseif !lMVOk
        self:lSuccess := .f.
    endif
    
Return 

Method valida() class HealthFacialRecognitionRequest
    local cFields   := ""
    self:lSuccess := _Super:checkBody(.t.)
    BA1->(dbsetorder(2))

    if self:lSuccess .and. empty(cvaltochar(self:jRequest['subscriberId']))  
        cFields += iif(!empty(cFields),',','')+"'subscriberId'"
        self:lSuccess := .f.
    endif
    if self:lSuccess .and. !BA1->(msseek(xfilial("BA1")+self:jRequest['subscriberId']))
        self:cFaultDesc += 'Beneficiario ['+self:jRequest['subscriberId']+'] não localizado'
        self:lSuccess := .f.
    endif
    
    if !empty(cFields)
        self:cFaultDesc   := "Campos obrigatórios não informados:" + cFields
    endif
Return self:lSuccess

Method setRest(cApi) class HealthFacialRecognitionRequest
    local cUrl      := GetNewPar("MV_PLDIURL",'https://poc-valid.apps.cli2.inova1.space')
    local cToken    := GetNewPar("MV_PLDITOK",'')//Mkw5ajs9cAB45q6wtm5fbzDgsEAK3Xk724FgYT3g
    local lMVOk  := .t.
    
    self:aHeader   := {}
    aadd(self:aHeader, "x-api-key: "+cToken)
    aadd(self:aHeader, "Content-Type: application/json")

    self:oFWRest := FWRest():New(cUrl + cApi)
	self:oFWRest:setPath('')

    if empty(cUrl) .or. empty(cToken)
        lMVOk := .f.
        self:cFaultDesc := 'Parametros MV_PLDIURL ou MV_PLDITOK não preenchidos, verifique a documentação.'
    endif
    
Return lMVOk