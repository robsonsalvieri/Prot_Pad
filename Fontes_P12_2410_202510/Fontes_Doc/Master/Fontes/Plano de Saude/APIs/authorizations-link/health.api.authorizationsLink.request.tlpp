#Include "PROTHEUS.CH"
#Include "TOTVS.CH"
#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "FWLIBVERSION.CH"

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

//-------------------------------------------------------------------
/*/{Protheus.doc} HealthApiAuthorizationsLinkRequest
.
@author  Lucas Nonato
@version P12
@since   31/01/2024
/*/
@Get("/totvshealthplans/v1/authorizationsLink/:idOnHealthInsurer")
function HealthApiAuthorizationsLinkRequest()
    local oRequest  := HealthApiAuthorizationsLinkRequest():new(oRest)    
    
    oRequest:initRequest()
    if oRequest:valida()
        oRequest:applyPageSize()
        oRequest:buscar()
        oRequest:procGet(ALL)
    endif

    oRequest:endRequest()
    oRequest:destroy()

    FreeObj(oRequest)
    oRequest := Nil

    delClassIntf()

return

Class HealthApiAuthorizationsLinkRequest from CenRequest

    public method New(oRest,cSvcName) Constructor
    public method destroy()
    public method valida()
    public method buscar()
    public method applyPageSize()

EndClass

Method destroy()  Class HealthApiAuthorizationsLinkRequest
    self:oCollection:destroy()
    _Super:destroy()
Return 

Method New(oRest, cSvcName) Class HealthApiAuthorizationsLinkRequest
    _Super:New(oRest,cSvcName,.t.)
    self:oCollection := HealthApiAuthorizationsLinkCollection():New()
    self:cPropLote   := "BEAQRY"

Return self

Method valida() Class HealthApiAuthorizationsLinkRequest
    self:jRequest   := oRest:getQueryRequest()
Return self:lSuccess

Method buscar() Class HealthApiAuthorizationsLinkRequest

    If self:lSuccess
        self:oCollection:get(self:oRest:getPathParamsRequest()['idOnHealthInsurer'])
    EndIf

Return self:lSuccess

Method applyPageSize() Class HealthApiAuthorizationsLinkRequest

    if empty(cvaltochar(self:jRequest['page']))
        self:jRequest['page'] := '1'
    endif

    if empty(cvaltochar(self:jRequest['pageSize']))
        self:jRequest['pageSize'] := '99'
    endif

    self:oCollection:applyPageSize(self:jRequest['page'],self:jRequest['pageSize'] )
    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}

Return self:lSuccess
