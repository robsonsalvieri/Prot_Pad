#Include "tlpp-core.th"

Using Namespace totvs.protheus.health.plans.api.util

/*/{Protheus.doc} PLSBeneficiarySearchAdapter

@type class
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Class PLSBeneficiarySearchAdapter From BaseAdapter

	Public Method new() Constructor
	Public Method getPageBeneficiary() As Logical

	Private Method mapFieldsBeneficiary() As Logical
	Private Method getQueryBeneficiary() As Character
	Private Method getWhereBeneficiary() As Character
	Private Method getOrderBeneficiary() As Character

EndClass

/*/{Protheus.doc} new
Método construtor da classe

@type method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method new() class PLSBeneficiarySearchAdapter

	_Super:new()

Return Self

/*/{Protheus.doc} getPageBeneficiary

@type method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method getPageBeneficiary() As Logical Class PLSBeneficiarySearchAdapter

	Local lSucess := .F. As Logical
    Local cQuery := Self:getQueryBeneficiary() As Character
    Local cWhere := Self:getWhereBeneficiary() As Character
    Local cOrder := Self:getOrderBeneficiary() As Character
	
	Self:mapFieldsBeneficiary()

	If Self:executeQueryAdapter(cQuery, cWhere, cOrder)
		lSucess := .T.
	EndIf

Return lSucess

/*/{Protheus.doc} mapFieldsBeneficiary

@type method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method mapFieldsBeneficiary() As Logical Class PLSBeneficiarySearchAdapter

	local cConcat := Iif(AllTrim(TcGetDB()) $ "DB2/ORACLE/POSTGRES","||","+")

	Self:oAdapterBase:AddMapFields( 'gender' , 'BA1_SEXO' , .T., .F., { 'BA1_SEXO', 'C', TamSX3( 'BA1_SEXO' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'name'   , 'BA1_NOMUSR' , .T., .F., { 'BA1_NOMUSR', 'C', TamSX3( 'BA1_NOMUSR' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'phoneNumber'  , 'BA1_TELEFO' , .T., .F., { 'BA1_TELEFO', 'C', TamSX3( 'BA1_TELEFO' )[1], 0 } )
	Self:oAdapterBase:AddMapFields( 'subscriberId' , 'subscriberId' , .T., .F., { 'subscriberId', 'C', 17 , 0 }, 'BA1_CODINT ' + cConcat + ' BA1_CODEMP ' + cConcat + ' BA1_MATRIC ' + cConcat + ' BA1_TIPREG ' + cConcat + ' BA1_DIGITO' )
    Self:oAdapterBase:AddMapFields( 'cardExpiration' , 'BA1_DTVLCR' , .T., .F., { 'BA1_DTVLCR', 'D', TamSX3( 'BA1_DTVLCR' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'holderRelationship' , 'BA1_TIPUSU' , .T., .F., { 'BA1_TIPUSU', 'C', TamSX3( 'BA1_TIPUSU' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'socialName' , 'BA1_NOMSOC' , .T., .F., { 'BA1_NOMSOC', 'C', TamSX3( 'BA1_NOMSOC' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'holderCPF' , 'BA1_CPFUSR' , .T., .F., { 'BA1_CPFUSR', 'C', TamSX3( 'BA1_CPFUSR' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'birthDate' , 'BA1_DATNAS' , .T., .F., { 'BA1_DATNAS', 'C', TamSX3( 'BA1_DATNAS' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'oldSubscriberId' , 'BA1_MATANT' , .T., .F., { 'BA1_MATANT', 'C', TamSX3( 'BA1_MATANT' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'weight' , 'BA1_PESO' , .T., .F., { 'BA1_PESO', 'C', TamSX3( 'BA1_PESO' )[1], 0 } )
    Self:oAdapterBase:AddMapFields( 'height', 'BA1_ALTURA' , .T., .F., { 'BA1_ALTURA', 'C', TamSX3( 'BA1_ALTURA' )[1], 0 } )

Return .T.

/*/{Protheus.doc} getQueryBeneficiary

@type Method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method getQueryBeneficiary() As Character Class PLSBeneficiarySearchAdapter

	Local cQuery As Character

	cQuery := " SELECT #QueryFields# "
    cQuery +=   " FROM " + RetSqlName( 'BA1' ) + " BA1 "
    cQuery += " WHERE #QueryWhere# "

Return cQuery

/*/{Protheus.doc} getWhereBeneficiary

@type Method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method getWhereBeneficiary() As Character Class PLSBeneficiarySearchAdapter

	Local cQuery As Character
	
	cQuery :=  "BA1_FILIAL = '"+ FWxFilial('BA1') +"' AND D_E_L_E_T_ = ' ' AND BA1_CODINT = '"+ PLSINTPAD() +"'"

	If Self:jParams:hasProperty("benefName")
		cQuery += " AND UPPER(BA1_NOMUSR) LIKE '%"+Upper(Self:jParams["benefName"])+"%'"
	EndIf

	If Self:jParams:hasProperty("birthdate")
		cQuery += " AND BA1_DATNAS = '"+Self:jParams["birthdate"]+"'"
	EndIf

Return cQuery

/*/{Protheus.doc} getOrderBeneficiary

@type Method
@author Daniel Silva Dias
@since 13/03/2024
@version Protheus 12
/*/
Method getOrderBeneficiary() As Character Class PLSBeneficiarySearchAdapter

	Local cOrdemQuery As Character
	
	cOrdemQuery := ""

Return cOrdemQuery
