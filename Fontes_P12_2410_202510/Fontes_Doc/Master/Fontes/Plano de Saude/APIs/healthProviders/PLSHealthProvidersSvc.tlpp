#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#define COLLECTION 1
#define SINGLE_ITEM 2

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSHealthProvidersSvc

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSHealthProvidersSvc From PLSSvcAux

    Data oError as Object
    Data oDao as Object
    Data oDaoBBF as Object
    Data oEntity as Object
    
    public method new()
    public method setCollectionParams(nTypeSearch,cIdSearch,cNumPage,cPageSize)

    public method vldExiRda()    
    public method getError()
    public method getJsonRda()
    public method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method procCollection()
    public method applyFilter()
    public method getSpecialty(cCodLoc, cFilterCbos)

endClass

method new(cCodRda) class PLSHealthProvidersSvc

     _Super:New()
    self:oError  := JsonObject():new()
    self:oDao    := PLSHealthProvidersDao():new(cCodRda)
    self:oEntity := PLSHealthProvidersEntity():new()

Return self

method applyFilter(oRest) class PLSHealthProvidersSvc
    self:setValueData( 'expand',    oRest:expand)
    self:setValueData( 'elapTime',  oRest:elapTime)
    self:setValueData( 'filter',    oRest:filter)
Return self

Method setCollectionParams(cIdSearch,cNumPage,cPageSize) class PLSHealthProvidersSvc

    Default cIdSearch   := ''
    Default cNumPage    := '1'
    Default cPageSize   := '10'

    self:oDao:setTypeSearch(COLLECTION)
    self:oDao:setNumPage(cNumPage)
    self:oDao:setPageSize(cPageSize)
    self:oDao:setValue("idSearch",cIdSearch)
    
    self:setPage(cNumPage)
    self:setPageSize(cPageSize)

Return

method vldExiRda() class PLSHealthProvidersSvc

    Local lRet    := self:oDao:getRda()
    
    self:oDao:closeQuery()
    if !lRet
        self:setError(400,"0003","Prestador informado nao encontrado","Entre em contato com o Suporte")
    endIf
        
Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos auxiliares

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSHealthProvidersSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

return

method getError() class PLSHealthProvidersSvc
return self:oError


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos que montam o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method getJsonRda() class PLSHealthProvidersSvc

    Local aRet := {}

    //Monta raiz do Json
    self:oDao:getRda()
    self:oEntity:montaRda(self:oDao)
    self:oDao:closeQuery()

    //Monta Locais e Especialidades
    self:oDao:getLocEsp()
    self:oEntity:montaLocEsp(self:oDao)
    self:oDao:closeQuery()

    //Encerra Json
    aadd(aRet,self:oEntity:getResult())
    aadd(aRet,self:oEntity:getStatusCode())

return aRet


method procCollection() class PLSHealthProvidersSvc

    Local lRet    := self:oDao:getRda()
    Local aRet    := {}
    Local nX      := 0
    Local lHasNext := .F.

    if lRet
        while !self:oDao:isEof()
            nX++
            if nX <= self:nPageSize
                //Monta dados BAU
                self:oEntity:montaRdaCollection(self:oDao,nX)
            else
                lHasNext := .T.
            endIf
            self:oDao:dbSkip()
        endDo
        self:oDao:closeQuery()
        self:oEntity:setHasNext(lHasNext)
        
        //Monta dados BBF
        if 'specialities' $ self:oDao:getValue('expand') 
            self:oEntity:montaEspecCol(self:oDao)
        endif

        //Monta dados BB8
        if 'attendanceLocations' $ self:oDao:getValue('expand') 
            self:oEntity:montaLocaisCol(self:oDao)
        endif

    else
        self:oDao:closeQuery()
    endIf
    
    if lRet
        //Encerra Json
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Prestador informado nao encontrado","Entre em contato com o Suporte")
    endIf
        
    self:oDao := nil

Return {lRet,aRet}

/*-------------------------------------------------------------------
{Protheus.doc} getSpecialty
---------------------------------------------------------------------*/ 
method getSpecialty(cCodLoc, lBuscaCbs, cFilterCbos) class PLSHealthProvidersSvc

    Local lRet     := .F.
    Local aRet     := {}
    Local nX       := 0
    Local lHasNext := .F.
    Default cCodLoc := ''
    Default lBuscaCbs := .F.
    Default cFilterCbos := ''

    lRet := self:oDao:getSpecialty(cCodLoc, lBuscaCbs, cFilterCbos)

    if lRet
        while !self:oDao:isEof()
            nX++
            if nX <= self:nPageSize
                self:oEntity:montaEspecRDA(self:oDao,nX, lBuscaCbs)
            else
                lHasNext := .T.
            endIf
            self:oDao:dbSkip()
        endDo
        self:oEntity:setHasNext(lHasNext)
    endIf
    self:oDao:closeQuery()
    
    if lRet
        //Encerra Json
        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Nenhuma especialidade encontrada para os filtros informados.","Entre em contato com o Suporte")
    endIf
        
    self:oDao := nil

return {lRet,aRet}
