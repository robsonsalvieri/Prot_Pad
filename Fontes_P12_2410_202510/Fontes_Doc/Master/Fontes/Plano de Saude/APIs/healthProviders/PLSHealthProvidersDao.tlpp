#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#define COLLECTION 1
#define SINGLE_ITEM 2

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSHealthProvidersDao

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSHealthProvidersDao From PLSDaoAux
    
    Data cCodRda as Character
    Data cSubstrChar as Character

    Public Method New()

    //Metodos de busca de dados
    Public Method getRda()
    Public Method getLocEsp()
    Public Method getBBFCollection(cCodBAU)
    Public Method getBB8Collection(cCodBAU) 
    Public Method getSpecialty(cCodLoc, lBuscaCbos, cFilterCbos)
    Public Method getFilter()

EndClass

Method New(cCodRda) class PLSHealthProvidersDao
    
    _Super:New()
    self:cCodRda := cCodRda
    self:cSubstrChar := iif(self:cDB $ "ORACLE|DB2|POSTGRES|INFORMIX", "SUBSTR", "SUBSTRING")

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos de busca de dados

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method getRda() class PLSHealthProvidersDao

    Local cQuery := ""
    Local lFound := .F.

    self:setAlias("BAUQRY")

    cQuery += self:getRowControl("BAU_CODIGO")
    cQuery += " BAU_CODIGO, BAU_NOME, BAU_EMAIL, BAU_TIPPRE, BAU_TIPPE, BAU_CPFCGC, BAU_MUN, BAU_DATBLO, BAU_NFANTA "
    cQuery += " FROM " + RetSqlName("BAU") + " BAU "
    cQuery += " WHERE BAU_FILIAL = '"+xFilial("BAU")+"' "
    if self:nTypeSearch == SINGLE_ITEM
        cQuery += " AND BAU_CODIGO = '"+self:cCodRda+"' "
    elseIf self:nTypeSearch == COLLECTION .and. !empty(cvaltochar(self:getValue("idSearch")))
        cQuery += " AND (BAU_CODIGO LIKE '%"+self:getValue("idSearch")+"%' OR UPPER(BAU_NOME) LIKE UPPER('%"+self:getValue("idSearch")+"%')) "
    endif
    cQuery += " AND D_E_L_E_T_ = ' ' "
    cQuery += self:getFilter()
    cQuery += self:getWhereRow()

    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getLocEsp() class PLSHealthProvidersDao

    Local cQuery := ""
    Local lFound := .F.

    cQuery += " SELECT BB8_CODINT, BB8_CODIGO, BB8_CODLOC, BB8_LOCAL, BB8_END , BAX_CODESP, BAQ_DESCRI, BB8_DESLOC "
    cQuery += " FROM "+RetSqlName("BB8")+" BB8 "

	cQuery += " LEFT JOIN "+RetSqlName("BAX")+" BAX "
	cQuery += "     ON BAX_FILIAL = '"+xFilial("BAX")+"' "
	cQuery += "     AND BAX_CODIGO = BB8_CODIGO "
	cQuery += "     AND BAX_CODINT = BB8_CODINT "
	cQuery += "     AND BAX_CODLOC = BB8_CODLOC "
    cQuery += "     AND (BAX.BAX_DATBLO = ' ' OR BAX.BAX_DATBLO > '"+Dtos(dDataBase)+"')  "
	cQuery += "     AND BAX.D_E_L_E_T_  =  '  '	 "

    cQuery += " LEFT JOIN "+RetSqlName("BAQ")+" BAQ  "
	cQuery += "     ON BAQ_FILIAL = '"+xFilial("BAQ")+"' "
	cQuery += "     AND BAQ_CODINT = '"+PlsIntPad()+"' "
	cQuery += "     AND BAQ_CODESP = BAX_CODESP "
	cQuery += "     AND BAQ.D_E_L_E_T_ = ' ' 	 "

    cQuery += " WHERE BB8_FILIAL = '"+xFilial("BB8")+"' "
    cQuery += "     AND BB8_CODINT = '"+PlsIntPad()+"' "
    cQuery += "     AND BB8.BB8_CODIGO = '"+self:cCodRda+"' "
    cQuery += "     AND (BB8.BB8_DATBLO = ' ' OR BB8.BB8_DATBLO > '"+Dtos(dDataBase)+"')  "
    cQuery += "     AND BB8.D_E_L_E_T_ = ' '  "
    cQuery += "     ORDER BY BB8_CODINT, BB8_CODIGO, BB8_CODLOC, BB8_LOCAL "
    
    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getBBFCollection(cCodBAU) class PLSHealthProvidersDao

    Local cQuery := ""
    Local lFound := .F.
    Default cCodBAU := ''

    cQuery += " SELECT BAQ_CODESP, BAQ_DESCRI "
    cQuery += " FROM "+RetSqlName("BBF")+" BBF "
    
    cQuery += " INNER JOIN "+RetSqlName("BAQ")+" BAQ "
	cQuery += "     ON BAQ_FILIAL = '"+xFilial("BAQ")+"' "
	cQuery += "     AND BAQ_CODINT = "+self:cSubstrChar+"(BBF_CODESP,01,04) "
	cQuery += "     AND BAQ_CODESP = "+self:cSubstrChar+"(BBF_CODESP,05,03) "
	cQuery += "     AND BAQ.D_E_L_E_T_  =  '  '	

    cQuery += " WHERE BBF_FILIAL = '"+xFilial("BBF")+"' "
    cQuery += "     AND BBF_CODIGO = '"+cCodBAU+"' "
    cQuery += "     AND BBF.D_E_L_E_T_ = ' ' "
    cQuery += "     ORDER BY BBF_CODIGO, BBF_CODESP "

    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getBB8Collection(cCodBAU) class PLSHealthProvidersDao

    Local cQuery := ""
    Local lFound := .F.
    Default cCodBAU := ''

    cQuery += " SELECT BB8_END, BB8_NR_END, BB8_CNES, BB8_CODLOC, BB8_LOCAL, BB8_CEP, BB8_EST, BB8_DESLOC, BB8_BAIRRO, BB8_COMEND, BB8_MUN, "
    cQuery += " BB8_CODINT, BB8_DATBLO, BB8_REGMUN "
    cQuery += " FROM "+RetSqlName("BB8")+" BB8 "
    cQuery += " WHERE BB8_FILIAL = '"+xFilial("BB8")+"' "
    cQuery += "     AND BB8_CODINT = '"+PlsIntPad()+"' "
    cQuery += "     AND BB8_CODIGO = '"+cCodBAU+"' "
    cQuery += "     AND BB8.D_E_L_E_T_ = ' ' "
    cQuery += "     ORDER BY BB8_CODLOC, BB8_LOCAL "

    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

/*-------------------------------------------------------------------
{Protheus.doc} getSpecialty
---------------------------------------------------------------------*/ 
Method getSpecialty(cCodLoc, lBuscaCbos, cFilterCbos) class PLSHealthProvidersDao

	Local cQuery := ""
    local cDB := TCGetDB()

    self:setAlias("SPECQRY")

    if lBuscaCbos
        cQuery += self:getRowControl("BTQ_CDTERM")
        cQuery += "BTQ_CDTERM, BTQ_DESTER"
    else 
        cQuery += self:getRowControl("BAX_CODESP")
	    cQuery += " BTQ_CDTERM, BTQ_DESTER, BAX_CODESP, BAQ_DESCRI "
    endif

	cQuery += " FROM "+RetSqlName("BAX")+iif(cDB == "POSTGRES", " BAX ", " BAX ")

	cQuery += " INNER JOIN "+RetSqlName("BTU")+iif(cDB == "POSTGRES"," BTU "," BTU ")
	cQuery += " 	ON BTU_FILIAL = '"+xFilial("BTU")+"' "
	cQuery += "  	AND BTU_CODTAB = '24' "
	cQuery += " 	AND BTU_ALIAS = 'BAQ' "
	cQuery += " 	AND BTU_VLRBUS = BAX_CODESP "
    cQuery += "     AND BTU.D_E_L_E_T_ = ' ' "

	cQuery += " INNER JOIN "+RetSqlName("BTQ")+iif(cDB == "POSTGRES"," BTQ", " BTQ ")
	cQuery += " 	ON BTQ_FILIAL = '"+xFilial("BTQ")+"' "
	cQuery += "  	AND BTQ_CODTAB = '24' "
	cQuery += "  	AND BTQ_CDTERM = BTU_CDTERM "
    cQuery += "     AND BTQ.D_E_L_E_T_ = ' ' "

	cQuery += " INNER JOIN "+RetSqlName("BAQ")+iif(cDB == "POSTGRES"," BAQ", " BAQ ")
	cQuery += "  	ON BAQ_FILIAL = '"+xFilial("BAQ")+"' "
    cQuery += "     AND BAQ_CODESP = BAX_CODESP "
	cQuery += "  	AND BAQ_CODINT = BAX_CODINT "
    cQuery += "     AND BAQ.D_E_L_E_T_ = ' ' "
	
	cQuery += " WHERE 1=1 "
    cQuery += "     AND BAX_FILIAL = '"+xFilial("BAQ")+"' "
    cQuery += "     AND BAX_CODIGO = '"+self:cCodRda+"' "
    cQuery += "     AND BAX_CODLOC = '"+cCodLoc+"' "
    cQuery += "     AND (BAX_DATBLO = ' ' OR BAX_DATBLO >= '" + dtos(Date()) + "') "
    cQuery += "     AND BAX.D_E_L_E_T_ = ' ' "

    if lBuscaCbos
        if !empty(cFilterCbos)
            cQuery += " AND ( UPPER(BTQ_DESTER) LIKE UPPER('%" + cFilterCbos + "%') "
            cQuery += "           OR BTQ_CDTERM LIKE '%" + cFilterCbos + "%') "
        endif
        cQuery += " UNION ALL "
        if cDB == "POSTGRES"
            cQuery += " SELECT ROW_NUMBER() OVER(ORDER BY BTQ_CDTERM) AS ROW,BTQ_CDTERM, BTQ_DESTER "
        else    
            cQuery += " SELECT ROW_NUMBER() OVER(ORDER BY BTQ_CDTERM) AS ROW#,BTQ_CDTERM, BTQ_DESTER "
        endif
        
        cQuery += " FROM "+RetSqlName("BTQ")+iif(cDB == "POSTGRES"," BTQ "," BTQ ")
        cQuery += " INNER JOIN "+RetSqlName("BTU")+iif(cDB == "POSTGRES"," BTU ", " BTU ")
        cQuery += "     ON BTU_FILIAL = '"+xFilial("BTU")+"' "
        cQuery += "     AND BTU_CODTAB = '24' "
	    cQuery += "     AND BTU_ALIAS = 'BAQ' "
	    cQuery += "     AND BTU_CDTERM = BTQ_CDTERM "
	    cQuery += "     AND BTU.D_E_L_E_T_ = ' ' "
	    cQuery += " WHERE 1=1"
	    cQuery += "     AND BTQ_FILIAL = '"+xFilial("BTQ")+"' "
	    cQuery += "     AND BTQ_CODTAB = '24' "
	    cQuery += "     AND ( BTQ.D_E_L_E_T_ = ' ' OR BTQ.D_E_L_E_T_ = ' ' ) "
        if !empty(cFilterCbos)
	        cQuery += "     AND ( UPPER(BTQ_DESTER) LIKE UPPER('%" + cFilterCbos + "%') 
            cQuery += "           OR BTQ_CDTERM LIKE '%" + cFilterCbos + "%')"
        endif
    else
        cQuery += " GROUP BY BTQ_CDTERM, BTQ_DESTER, BAX_CODESP, BAQ_DESCRI "
    endif

	cQuery += self:getWhereRow()

    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getFilter() class PLSHealthProvidersDao
	Local cQuery := ""

    if !empty(cvaltochar(self:getValue("filter")))
        cQuery += " AND BAU_CODIGO IN "+formatIn(self:getValue("filter"),',')
    endif

    if !empty(cvaltochar(self:getValue("elapTime")))
        cQuery += ' AND ( '
        cQuery += '     BAU.S_T_A_M_P_ > ' + getStamp(self:getValue("elapTime"))
        cQuery += '     OR BAU.BAU_CODIGO IN ( '
        cQuery += '         SELECT BB8.BB8_CODIGO  '
        cQuery += '         FROM '+RetSqlName("BB8")+ ' BB8 '
        cQuery += '         WHERE BB8.S_T_A_M_P_ > ' + getStamp(self:getValue("elapTime")) '
        cQuery += '     ) '
        cQuery += ' ) '
    endif

Return cQuery

static function getStamp(cHoras)
local cDBType := TCGetDB()

cSql := iif(cDBType $ "ORACLE|DB2", " (SYS_EXTRACT_UTC(systimestamp)  - INTERVAL '"+cHoras+"' HOUR)  ",iif(cDBType == "POSTGRES"," (current_timestamp - interval'"+cHoras+" hours') ", " (DATEADD(HOUR, -"+cHoras+", GETUTCDATE())) ")) 

return cSql
