#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSClinicalStaffRequest

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSClinicalStaffRequest from CenRequest 

    Data oRest as Object
    Data oService as Object
    
    Public Method New()
    Public Method validaGet()
    Public Method validaPost()
    Public Method getError()
    Public Method getClinicalStaff()
    Public Method postClinicalStaff()
    Public Method block()

endClass

Method New(oRest) class PLSClinicalStaffRequest

    _Super:New(oRest)
    self:oService  := PLSClinicalStaffSvc():New(oRest,self:jRequest)

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} Valida se pode gerar o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method validaGet() class PLSClinicalStaffRequest
    
    Local cRet      := ''
    Local lVirgula  := .F.
    
    if empty(self:oRest:healthProviderCode)
        cRet += iif(lVirgula,", ","")
        cRet += 'healthProviderCode'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf
    
    if empty(self:oRest:locationCode)
        cRet += iif(lVirgula,", ","")
        cRet += 'locationCode'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf
    
    if empty(self:oRest:specialtyCode)
        cRet += iif(lVirgula,", ","")
        cRet += 'specialtyCode'
        lVirgula  := .T.
        self:lSuccess := .F.
    endIf

    if !self:lSuccess
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := 'QueryParams obrigatorios nao informados'
        self:cFaultDetail := cRet
    endIf

Return self:lSuccess


Method validaPost() class PLSClinicalStaffRequest

    Local lVirgula  := .F.
    Local cRet      := ''

    _Super:checkBody()
    if self:lSuccess
        if empty(self:jRequest['professionalCouncil'])
            cRet += iif(lVirgula,", ","")
            cRet += 'professionalCouncil'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif

        if empty(self:jRequest['professionalCouncilNumber'])
            cRet += iif(lVirgula,", ","")
            cRet += 'professionalCouncilNumber'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif

        if empty(self:jRequest['name'])
            cRet += iif(lVirgula,", ","")
            cRet += 'name'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif

        if empty(self:jRequest['stateAbbreviation'])
            cRet += iif(lVirgula,", ","")
            cRet += 'stateAbbreviation'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif

        if empty(self:jRequest['locationCode'])
            cRet += iif(lVirgula,", ","")
            cRet += 'locationCode'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif

        if empty(self:jRequest['healthProviderCode'])
            cRet += iif(lVirgula,", ","")
            cRet += 'healthProviderCode'
            lVirgula  := .T.
            self:lSuccess := .F.
        endif
    endif

    if !self:lSuccess
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := 'Atributos obrigatorios nao informados'
        self:cFaultDetail := cRet
    endIf

Return self:lSuccess

Method getError() class PLSClinicalStaffRequest
    
    Local oErrorAux   := self:oService:getError()

    self:lSuccess     := .F.
    self:nFault       := oErrorAux['status']
    self:nStatus      := oErrorAux['status']
    self:cFaultDesc   := oErrorAux['message']
    self:cFaultDetail := oErrorAux['detailedMessage']

Return self:lSuccess


//-------------------------------------------------------------------
/*/{Protheus.doc} Monta json com a guia

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method getClinicalStaff() class PLSClinicalStaffRequest

    Local aRet := self:oService:getClinicalStaff()
    
    if self:oService:getSuccess()
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    else
        self:getError()
    endIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Adiciona novo profissional no corpo clinico

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method postClinicalStaff() class PLSClinicalStaffRequest

    Local aRet := self:oService:postClinicalStaff()

    if self:oService:getSuccess()
        self:cResponse := aRet[1]:toJson()
        self:nStatus   := aRet[2]
    else
        self:getError()
    endIf

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} Bloqueia o profissional

@author  Lucas Nonato
@version P12
@since   24/05/2023
/*/
Method block() class PLSClinicalStaffRequest
    
    self:oService:block(self:oRest:id)
    if !self:oService:getSuccess()
        self:getError()
    endif

Return
