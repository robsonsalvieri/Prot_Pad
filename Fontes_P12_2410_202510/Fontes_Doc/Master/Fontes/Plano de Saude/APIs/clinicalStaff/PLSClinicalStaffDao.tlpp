#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSClinicalStaffDao

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSClinicalStaffDao from PLSDaoAux
    
    Data oTmpTable as Object
    Data nSequen as Numeric

    Public Method New()
    Public Method block()
    Public Method gerAliTemp()
    Public Method insertTemp(cCodRda,cCodLoc,cCodEsp,cCodRda,cSiglCr,cNumCr,cEstCr,cNomPrf,nRecno,cAlias)
    Public Method getDadTemp()
    Public Method closeTemp() 
    

    //Metodos de busca de dados
    Public Method getClinical(cCodRda,cCodLoc,cCodEsp)
    Public Method getSolInc(cCodRdaKey,cCodLocKey,cCodEspKey)

    Private Method rmvChar(cAux)
    
EndClass

Method New() class PLSClinicalStaffDao
    _Super:New()
    
    self:oTmpTable := NIL
    self:nSequen   := 0

Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos de busca de dados

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
Method getClinical(cCodRda,cCodLoc,cCodEsp) class PLSClinicalStaffDao
    
    Local cQuery := ""
    Local cAli   := ""
    Local lFound := .F.
    
    self:gerAliTemp() //Inicia tabela temporaria
    self:setAlias("BC1QRY")

    cQuery += " SELECT BC1_CODIGO, BC1_NUMCR AS NUMCR, BC1_CODRDA AS CODRDA, BC1_SIGLCR AS SIGLCR, BC1_DATBLO AS DATBLO, "
    cQuery += " BC1_ESTCR AS ESTCR, BC1_NOMPRF AS NOMPRF, R_E_C_N_O_ AS RECNO "
    cQuery += " FROM " + RetSqlName("BC1")
    cQuery += " WHERE BC1_FILIAL = '"+xFilial("BC1")+"' "
    cQuery += " AND BC1_CODIGO = '"+cCodRda+"' "
    cQuery += " AND BC1_CODLOC = '"+cCodLoc+"' "
    cQuery += " AND BC1_CODESP = '"+cCodEsp+"' "
    cQuery += " AND D_E_L_E_T_ = ' ' "
    
    self:setQuery(cQuery)
    lFound := self:executaQuery()
    
    //Monta alias temporario
    if lFound
        cAli := self:getAliasTemp()
        while !self:isEof()
            self:insertTemp(cCodRda, cCodLoc, cCodEsp, (cAli)->CODRDA, (cAli)->SIGLCR, ;
                            (cAli)->NUMCR, (cAli)->ESTCR, (cAli)->NOMPRF, (cAli)->DATBLO, (cAli)->RECNO, 'BC1')
            self:dbSkip()
        endDo
    endif
    self:closeQuery()

Return lFound


Method getSolInc(cCodRdaKey,cCodLocKey,cCodEspKey) class PLSClinicalStaffDao

    Local cQuery := ""
    Local lFound := .F.
    Local cB7L_CHAVE := ''
    Local aCampos := {}
    Local aDados  := {}
    Local nX := 0
    Local nCont := 0

    self:setAlias("B7L")

    cQuery += " SELECT B7L_CHAVE, B7L_CAMPO, B7L_VLPOS FROM "+RetSqlName("B7L")+" B7L "
    cQuery += " INNER JOIN "+RetSqlName("B98")+" B98 "
    cQuery += "   ON B98_FILIAL = '"+xFilial("B98")+"' "
    cQuery += "   AND B98_CODSEQ = B7L_CHAVE "
    cQuery += "   AND B98_STATUS IN ('1','2') "
    cQuery += "   AND B98.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE B7L_FILIAL = '"+xFilial("B7L")+"' "
    cQuery += "   AND B7L.B7L_ALIACH = 'B98' "
    cQuery += "   AND B7L.D_E_L_E_T_ = ' ' "
    cQuery += " ORDER BY B7L_CHAVE "

    self:setQuery(cQuery)
    
    if self:executaQuery()
        cAli := self:getAliasTemp()

        aadd(aCampos,'BC1_CODIGO')
        aadd(aCampos,'BC1_CODLOC')
        aadd(aCampos,'BC1_CODESP')
        aadd(aCampos,'BC1_CODRDA')
        aadd(aCampos,'BC1_SIGLCR')
        aadd(aCampos,'BC1_NUMCR')
        aadd(aCampos,'BC1_ESTCR')
        aadd(aCampos,'BC1_NOMPRF')
         
        while !self:isEof()

            if nCont == 0
                aDados := {'','','','','','','','','',0}
                cB7L_CHAVE := Alltrim((cAli)->B7L_CHAVE)
            endIf
            
            //Mudou a chave, verifico se realizo a inclusao na tabela temporaria
            if Alltrim((cAli)->B7L_CHAVE) <> cB7L_CHAVE .And. nCont > 0
                if cCodRdaKey == aDados[1] .And. cCodLocKey == aDados[2] .And. cCodEspKey == aDados[3]
                    self:insertTemp(aDados[1], aDados[2] ,aDados[3] ,aDados[4], aDados[5], aDados[6], aDados[7] ,aDados[8], aDados[9], aDados[10], 'B98')
                    lFound := .T.
                endIf
                cB7L_CHAVE := Alltrim((cAli)->B7L_CHAVE)
                aDados := {'','','','','','','','','',0}
            endIf

            for nX := 1 to len(aCampos)
                if Alltrim((cAli)->B7L_CAMPO) == aCampos[nX]
                    aDados[nX] := Alltrim((cAli)->B7L_VLPOS)
                    exit
                endIf
            next

            nCont ++
            self:dbSkip()
        endDo
        //Inclui o ultimo registro
        if cCodRdaKey == aDados[1] .And. cCodLocKey == aDados[2] .And. cCodEspKey == aDados[3]
            self:insertTemp(aDados[1], aDados[2] ,aDados[3] ,aDados[4], aDados[5], aDados[6], aDados[7] ,aDados[8], aDados[9], aDados[10], 'B98')
            lFound := .T.
        endIf
    endif
    self:closeQuery()

Return lFound


Method gerAliTemp() class PLSClinicalStaffDao

    Local aColumns := {}

    aAdd( aColumns, { "SEQUEN" ,"C",03,00 })
    aAdd( aColumns, { "CODIGO" ,"C",06,00 })
    aAdd( aColumns, { "CODLOC" ,"C",03,00 })
    aAdd( aColumns, { "CODESP" ,"C",03,00 })
    aAdd( aColumns, { "CODRDA" ,"C",06,00 })
    aAdd( aColumns, { "SIGLCR" ,"C",07,00 })
    aAdd( aColumns, { "NUMCR"  ,"C",15,00 })
    aAdd( aColumns, { "ESTCR"  ,"C",02,00 })
    aAdd( aColumns, { "NOMPRF" ,"C",40,00 })
    aAdd( aColumns, { "DATBLO" ,"C",08,00 })
    aAdd( aColumns, { "ALIAS"  ,"C",03,00 })
    aAdd( aColumns, { "RECNO"  ,"N",20,00 })

    self:oTmpTable := FWTemporaryTable():New( getNextAlias() )
    self:oTmpTable:SetFields( aColumns )
    self:oTmpTable:Create()
    
Return

Method insertTemp(cCodRda,cCodLoc,cCodEsp,cCodRda,cSiglCr,cNumCr,cEstCr,cNomPrf,cDatBlo,nRecno,cAlias) class PLSClinicalStaffDao

    Local cSql := ''

    cSql := " INSERT INTO " + self:oTmpTable:GetRealName() + " (SEQUEN, CODIGO, CODLOC, CODESP, CODRDA, SIGLCR, NUMCR, ESTCR, NOMPRF, DATBLO, RECNO, ALIAS) "
    cSql += " VALUES (
    cSql += "'" + Strzero(self:nSequen,3) + "',"
    cSql += "'" + self:rmvChar(cCodRda) + "',"
    cSql += "'" + self:rmvChar(cCodLoc) + "',"
    cSql += "'" + self:rmvChar(cCodEsp) + "',"
    cSql += "'" + self:rmvChar(cCodRda) + "',"
    cSql += "'" + self:rmvChar(cSiglCr) + "',"
    cSql += "'" + self:rmvChar(cNumCr)  + "',"
    cSql += "'" + self:rmvChar(cEstCr)  + "',"
    cSql += "'" + self:rmvChar(cNomPrf) + "',"
    cSql += "'" + self:rmvChar(cDatBlo) + "',"
    cSql += cValtoChar(nRecno) + ","
    cSql += "'" + cAlias  + "' "
    cSql += ")"
    
    PLSCOMMIT(cSql)
    self:nSequen ++

Return

Method rmvChar(cAux) class PLSClinicalStaffDao

    cAux := StrTran(cAux, "'", "")

Return cAux

Method getDadTemp() class PLSClinicalStaffDao

    Local cQuery := ""

    cQuery += self:getRowControl("SEQUEN")
    cQuery += " SEQUEN, CODIGO, CODLOC, CODESP, CODRDA, SIGLCR, NUMCR, ESTCR, NOMPRF, RECNO, DATBLO, ALIAS "
    cQuery += " FROM " + self:oTmpTable:GetRealName()
    
    cQuery += self:getWhereRow()
    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method closeTemp() class PLSClinicalStaffDao
    self:closeQuery()
    self:oTmpTable:Delete()
    freeObj(self:oTmpTable)               
    self:oTmpTable := nil
Return    

//-------------------------------------------------------------------
/*/{Protheus.doc} Bloqueia o profissional

@author  Lucas Nonato
@version P12
@since   24/05/2023
/*/
Method block(nRecno) class PLSClinicalStaffDao
    
    Local lFound := .F.

    BC1->(dbGoTo(nRecno))
    if !BC1->(Eof())
        RecLock('BC1', .F.)
        BC1_CODBLO := getNewPar("MV_PLBLQCC", "   ")
        BC1_MOTBLO := Posicione("BAP", 1, xFilial("BAP") + BC1->BC1_CODBLO, "BAP_DESCRI")
        BC1_DATBLO := date()
        BC1_OBSERV := "Bloqueio via Portal"
        BC1->(msUnlock())
        lFound := .T.
    endIf

Return lFound
