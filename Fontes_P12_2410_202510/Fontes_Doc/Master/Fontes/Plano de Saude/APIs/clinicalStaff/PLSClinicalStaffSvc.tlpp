#include 'protheus.ch'
#include 'FWMVCDEF.CH' 
#include 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSClinicalStaffSvc

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSClinicalStaffSvc

    Data lAuto as Logical
    Data oError as Object
    Data oJsonReq as Object
    Data oDao as Object
    Data oEntity as Object
    Data lSuccess as logical
    Data lAutoInc as logical
    //Atributos pesquisa
    Data cCodRda as Character
    Data cCodLoc as Character
    Data cCodEsp as Character

    public method new()

    public method getSuccess()
    public method getError()
    public method getClinicalStaff()
    public method block()
    public method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method postClinicalStaff()
    public method saveAttachment()
    public method bscACBNum(cObj) 

endClass

method new(oRest,cJsonReq,lAuto) class PLSClinicalStaffSvc

    Default lAuto := .F.
    
    self:lAuto    := lAuto
    self:oError   := JsonObject():new()
    self:oDao     := PLSClinicalStaffDao():new()
    self:oEntity  := PLSClinicalStaffEntity():new()
    self:lSuccess := .T.
    self:lAutoInc := GetNewPar("MV_PLINBC1", .f.)
    if !self:lAuto
        self:cCodRda  := oRest:healthProviderCode
        self:cCodLoc  := oRest:locationCode
        self:cCodEsp  := oRest:specialtyCode
        self:oJsonReq := cJsonReq
        self:oDao:setPageSize(oRest:pageSize)
        self:oDao:setNumPage(oRest:page)
        self:oEntity:setPageSize(oRest:pageSize)
    endIf

Return self


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos auxiliares

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method getSuccess() class PLSClinicalStaffSvc
return self:lSuccess

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSClinicalStaffSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:lSuccess := .F.

return

method getError() class PLSClinicalStaffSvc
return self:oError


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos que montam o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method getClinicalStaff() class PLSClinicalStaffSvc

    Local aRet := {}
    Local lRetClin := .F.
    Local lRetInc  := .F.
    Local lGerJson := .F.

    lRetClin := self:oDao:getClinical(self:cCodRda,self:cCodLoc,self:cCodEsp)
    lRetInc  := self:oDao:getSolInc(self:cCodRda,self:cCodLoc,self:cCodEsp)

    if lRetClin .Or. lRetInc
        lGerJson := self:oDao:getDadTemp()        
    endIf

    if lGerJson
        self:oEntity:montaJson(self:oDao)
        self:oDao:closeTemp()

        aadd(aRet,self:oEntity:getResult())
        aadd(aRet,self:oEntity:getStatusCode())
    else
        self:setError(400,"0003","Registros nao encontrados","Nao foi encontrado o corpo clinico para os parametros informados")
    endIf

return aRet


//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos que montam o json de resposta

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
method postClinicalStaff() class PLSClinicalStaffSvc

    local oResult   := JsonObject():new()
    local cDados    := ''
    local cCodSeq   := ''
    local aRet      := {}
    local aRetBB0   := {}
    local cSql      := ''
    local cSpec     := ''

    if (self:oJsonReq['specialtyCode'] <> nil .and. self:oJsonReq['specialtyCode'] <> '')
        cSpec := self:oJsonReq['specialtyCode'] 
    else 
        cSql := " SELECT BAX_CODESP FROM " + retsqlname("BAX") + " BAX "
        cSQL += " WHERE BAX_FILIAL = '" + xFilial('BAX') +"' "
        cSQL += " AND BAX_CODIGO = '" + self:oJsonReq['healthProviderCode'] +"' "
        cSQL += " AND BAX_CODINT = '" +  plsintpad() +"' "
        cSQL += " AND BAX_CODLOC = '" + self:oJsonReq['locationCode'] +"' "
        cSQL += " AND BAX_DATBLO = ' ' "
        cSQL += " AND D_E_L_E_T_ = ' ' "
        cSQL += " ORDER BY BAX_ESPPRI DESC "

        dbUseArea(.t.,"TOPCONN",tcGenQry(,,cSql),"TRB",.f.,.t.)

        if !TRB->(Eof())
            cSpec := alltrim(TRB->BAX_CODESP)
        endif
        
        TRB->(DbCloseArea()) 

     endif 

    if !self:lAutoInc
        cDados += self:oJsonReq['healthProviderCode']        + ";BC1_CODIGO~" 
        cDados += self:oJsonReq['locationCode']              + ";BC1_CODLOC~"
        cDados += cSpec                                      + ";BC1_CODESP~"
        cDados += self:oJsonReq['name']                      + ";BC1_NOMPRF~"
        cDados += self:oJsonReq['professionalCouncilNumber'] + ";BC1_NUMCR~"
        cDados += self:oJsonReq['stateAbbreviation']         + ";BC1_ESTCR~"
        cDados += self:oJsonReq['officialRecord']            + ";BB0_CGC~"
        cDados += self:oJsonReq['professionalCouncil']       + ";BC1_SIGLCR"

        cCodSeq := PLS814LOG(cDados, 'hat')
    else
        aRetBB0 := PlSveProfAll(self:oJsonReq['name'], self:oJsonReq['professionalCouncil'], self:oJsonReq['stateAbbreviation'], self:oJsonReq['professionalCouncilNumber'], plsintpad(), self:oJsonReq['officialRecord'], '2', '', {})
        cCodSeq := aRetBB0[2]
        if aRetBB0[1]  
            BC1->(DbSetOrder(1))//BC1_FILIAL + BC1_CODIGO + BC1_CODLOC + BC1_CODESP + BC1_CODPRF
            If !BC1->( MsSeek(xFilial("BC1") + self:oJsonReq['healthProviderCode'] + self:oJsonReq['locationCode'] + cSpec + cCodSeq ) )   

                BC1->(Reclock("BC1",.T.))
                BC1->BC1_FILIAL := xFilial("BC1")
                BC1->BC1_CODINT := plsintpad()
                BC1->BC1_CODIGO := self:oJsonReq['healthProviderCode']
                BC1->BC1_CODLOC := self:oJsonReq['locationCode']
                BC1->BC1_CODESP := cSpec
                BC1->BC1_ESTCR  := self:oJsonReq['stateAbbreviation']  
                BC1->BC1_NUMCR  := self:oJsonReq['professionalCouncilNumber']
                BC1->BC1_SIGLCR := self:oJsonReq['professionalCouncil']
                BC1->BC1_CODPRF := cCodSeq
                BC1->BC1_NOMPRF := self:oJsonReq['name']
                BC1->(MsUnlock())
            endif

            BQ1->(DbSetOrder(1))//BQ1_FILIAL+BQ1_CODIGO+BQ1_CODESP
            If !BQ1->( MsSeek(xFilial("BQ1") + cCodSeq + cSpec  ) )
                BQ1->(Reclock("BQ1",.T.))
                BQ1->BQ1_FILIAL := xFilial("BQ1")
                BQ1->BQ1_CODINT := BC1->BC1_CODINT
                BQ1->BQ1_CODIGO := cCodSeq 
                BQ1->BQ1_CODESP := cSpec
                BQ1->BQ1_DESCRI := BAQ->(Posicione("BAQ",1,xFilial("BAQ")+BC1->BC1_CODINT+cSpec,"BAQ_DESCRI"))
                BQ1->(MsUnlock())
            endif
        endif
    endif
    
    if Empty(cCodSeq)
        self:setError(400,"0003","Falha ao criar pedido","Nao foi possivel gerar o pedido de inclusao de Profissional no Corpo Clinico")
    else
        self:saveAttachment(cCodSeq)
        oResult["code"]    := "0001"
        oResult["message"] := iif(self:lAutoInc,"Profissional incluido","Pedido de inclusao gerado")+ ' com sucesso: ' + cCodSeq

        aadd(aRet,oResult)
        aadd(aRet,200)
    endIf

return aRet


method saveAttachment(cCodSeq) class PLSClinicalStaffSvc

    local cDirDocs  := ""
    local cDescri  	:= ""
    local cExtArq  	:= ""
    local cFileName := self:oJsonReq['filename']
    local nH  		:= 0
    local cChave    := iif(self:lAutoInc,self:oJsonReq['healthProviderCode'],cCodSeq)
    local cAlias    := iif(self:lAutoInc,'BAU','B98')
    if !empty(cChave) .And. !empty(self:oJsonReq['filename']) .And. !empty(self:oJsonReq['base64file'])
           
        //Pega a extensão do arquivo
        cExtArq	:= subStr(cFileName, RAT(".", cFileName)+1)

        //Monta nome
        cDescri 	:= cFileName
        cFileName 	:= upper(left(substr(cFileName, 1, RAT(".", cFileName)-1) + strtran(Time(), ":", "_"), len( ACB->ACB_OBJETO )-5)  + "." + cExtArq)

        ACB->(dbSetOrder(1))
        AC9->(dbSetOrder(1))

        cDirDocs := iif(MsMultDir(),MsRetPath(),MsDocPath())

        nH := fCreate(cDirDocs + "\" + cFileName)
        fWrite(nH, decode64(self:oJsonReq['base64file']))
        fClose(nH)

        cObj := ACB->(getSXENum( "ACB", "ACB_CODOBJ" ))
        ACB->(confirmSX8())

        self:bscACBNum(cObj)
   
        ACB->( RecLock( "ACB", .T. ) )
        ACB->ACB_FILIAL  := xFilial( "ACB" )
        ACB->ACB_CODOBJ := cObj
        ACB->ACB_OBJETO := cFileName
        ACB->ACB_DESCRI := cDescri
        ACB->( MsUnlock() )

        AC9->(RecLock( "AC9", .T. ))
        AC9->AC9_FILIAL := xFilial( "AC9" )
        AC9->AC9_FILENT := xFilial( cAlias )
        AC9->AC9_ENTIDA := cAlias
        AC9->AC9_CODENT := iif(cAlias=='BAU','',xFilial( cAlias )) + cChave
        AC9->AC9_CODOBJ := cObj
        AC9->( MsUnlock() )
    
    endIf

return
//-------------------------------------------------------------------
/*/{Protheus.doc} Bloqueia o profissional

@author  Lucas Nonato
@version P12
@since   24/05/2023
/*/
Method block(nRecno) class PLSClinicalStaffSvc
    if !self:oDao:block(nRecno)
        self:setError(400,"0003","Registros nao encontrados","Nao foi encontrado corpo clinico com o id informado")
    endIf
Return

Method bscACBNum(cObj) class PLSClinicalStaffSvc
    
    Local cAutoObj := Replicate("9",tamSX3("ACB_CODOBJ")[1])
  
    while ACB->( msSeek( xFilial("ACB") + cObj ) )
        cObj := iif(!self:lAuto, ACB->(getSXENum( "ACB", "ACB_CODOBJ" )), cAutoObj) 
        iif(!self:lAuto, ACB->(confirmSX8()), NIL)
    endDo

Return cObj
