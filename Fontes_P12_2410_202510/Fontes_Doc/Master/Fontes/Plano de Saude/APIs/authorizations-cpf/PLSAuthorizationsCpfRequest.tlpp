#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsCpfRequest

@author  sakai
@version P12
@since   23/01/2024

IMPORTANTE: Annotation com a Request no fonte: health.api.authorizations.request.tlpp
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsCpfRequest from CenRequest 

    Data oRest as Object
    Data oService as Object
    Data jPath as Object
    
    Public Method New()
    Public Method getErrorSvc()
    Public Method setError(nFault, nStatus, cFaultDesc, cFaultDetail)
    Public Method validaQueryParam()
    Public Method validaLiberacaoCpf()
    
endClass

Method New(oRest,cSvcName) class PLSAuthorizationsCpfRequest

    _Super:New(oRest,cSvcName,.T.)
    self:oService := PLSAuthorizationsCpfSvc():New()
    self:jPath    := oRest:getQueryRequest()

Return self

Method getErrorSvc() class PLSAuthorizationsCpfRequest
    
    Local oErrorAux   := self:oService:getError()

    self:lSuccess     := .F.
    self:nFault       := oErrorAux['status']
    self:nStatus      := oErrorAux['status']
    self:cFaultDesc   := oErrorAux['message']
    self:cFaultDetail := oErrorAux['detailedMessage']

Return self:lSuccess

Method setError(nFault, nStatus, cFaultDesc, cFaultDetail) class PLSAuthorizationsCpfRequest
    
    self:lSuccess     := .F.
    self:nFault       := nFault
    self:nStatus      := nStatus
    self:cFaultDesc   := cFaultDesc
    self:cFaultDetail := cFaultDetail

Return

Method validaQueryParam() class PLSAuthorizationsCpfRequest

    Local cRet := ""
    Local lQueryParam := .F.
    
    if ValType(self:jPath['healthProviderCode']) <> "C"
        cRet += "healthProviderCode "
        lQueryParam := .T.
    endIf
    if ValType(self:jPath['locationCode']) <> "C"
        cRet += "locationCode "
        lQueryParam := .T.
    endIf

    if lQueryParam
        self:setError(400, 400, "QueryParams obrigatórios não informados", cRet)
    endIf

Return

Method validaLiberacaoCpf() class PLSAuthorizationsCpfRequest

    Local cCpf    := ''
    Local cCodRda := ''
    Local cCodLoc := ''
    Local cPage   := ''
    Local cPageSize := ''
    Local lValidaRda := .F.
    
    if self:lSuccess

        cCpf       := self:oRest:getPathParamsRequest()['cpf']
        cCodRda    := self:jPath['healthProviderCode']
        cCodLoc    := self:jPath['locationCode']
        cPage      := self:jPath['page']
        cPageSize  := self:jPath['pageSize']
        lValidaRda := self:jPath['validateProvider'] == 'true'

        self:oService:setExpand(self:jPath['expand'])
        self:lSuccess := self:oService:vldLiberacao(cCpf,cCodRda,cCodLoc,cPage,cPageSize,lValidaRda)
        
        if self:lSuccess
            aRet := self:oService:getJsonLiberacao(cCodRda,cCodLoc,cPageSize)
            self:cResponse := aRet[1]:toJson()
            self:nStatus   := aRet[2]
        else
            self:getErrorSvc()
        endIf
    endIf

Return self:lSuccess
