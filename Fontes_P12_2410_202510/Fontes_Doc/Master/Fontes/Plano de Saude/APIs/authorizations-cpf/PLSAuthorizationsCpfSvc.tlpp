#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationsCpfSvc

@author  sakai
@version P12
@since   06/09/22
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationsCpfSvc

    Data oError as Object
    Data cCpf as Character
    Data aGuiasLib as Array
    Data cExpand as Character

    public method new()
    public method vldLiberacao(cCpf,cCodRda,cCodLoc,cPage,cPageSize)
    public method getJsonLiberacao(cCodRda,cCodLoc,cPageSize)
    private method setError(nStatus, cCode, cMessage, cDetailedMessage)
    public method getError()
    public method setExpand(cExpand)

endClass

method new() class PLSAuthorizationsCpfSvc

    self:oError := JsonObject():new()
    self:aGuiasLib := {}
    self:cExpand   := ""

Return self

method vldLiberacao(cCpf,cCodRda,cCodLoc,cPage,cPageSize,lValidaRda) class PLSAuthorizationsCpfSvc

    Local aRet         := {}
    Local oDao         := PLSAuthorizationsCpfDao():New()
    Default cPage      := '1'
    Default cPageSize  := '10'
    Default lValidaRda := .F.

    oDao:setPage(cPage)
    oDao:setPageSize(cPageSize)
    
    aRet := oDao:buscaGuias(cCpf,lValidaRda,cCodRda)
    if aRet[1]
        self:aGuiasLib := aRet[2]
    else
        self:setError(400,"0004","Guia informada nao encontrada","Entre em contato com o Suporte")
    endIf

return aRet[1]

method getJsonLiberacao(cCodRda,cCodLoc,cPageSize) class PLSAuthorizationsCpfSvc
    
    Local oEntity := PLSAuthorizationsCpfEntity():new()
    Local nX      := 0
    Local aVldLib := {.T.,""}
    Local aRet    := {}
    Local cTipo    := '2' //Indica execucao
    Local cTipoAut := 'L' //Indica execucao
    Local cCpf  := ''
    Local lPlsCTEs := GetNewPar('MV_PLSCTES','0') == '1'
    Local lHasNext := .F.
    Local aRetAutLi := {}

    Default cCodRda  := ''
    Default cCodLoc  := ''
    Default cPageSize := '10'

    BEA->(dbsetorder(1)) //BEA_FILIAL+BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT+DTOS(BEA_DATPRO)+BEA_HORPRO
    for nX := 1 to len(self:aGuiasLib)
        
        if nX <= Val(cPageSize)
            BEA->(MsSeek(xFilial("BEA")+self:aGuiasLib[nX]))

            cCpf  := BEA->(BEA_OPEUSR+BEA_CODEMP+BEA_MATRIC+BEA_TIPREG+BEA_DIGITO)
            cCodEsp  := PlRtEspPre(PlsIntPad(),cCodRda,'',Date(),cCodLoc,lPlsCTEs,.T.)
            aRetAutLi := PLSRETAULI(cCpf,self:aGuiasLib[nX],cTipoAut,cCodRda,cCodLoc,cCodEsp,'',,,,,cTipo,,,'','' )
            aVldLib[1] := aRetAutLi[1] .Or. (len(aRetAutLi[4]) > 0 .And. Empty(aRetAutLi[2]))
            aVldLib[2] := aRetAutLi[2]

            oEntity:setExpand(self:cExpand)
            oEntity:setGuia(self:aGuiasLib[nX])
            oEntity:geraJson(nX,aVldLib[1],aVldLib[2],aRetAutLi)
        else
            lHasNext := .T.
        endIf
    next

    oEntity:setHasNext(lHasNext)
    aadd(aRet,oEntity:getResult())
    aadd(aRet,oEntity:getStatusCode())

    oEntity := NIL

return aRet

method setError(nStatus, cCode, cMessage, cDetailedMessage) class PLSAuthorizationsCpfSvc

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage

return

method getError() class PLSAuthorizationsCpfSvc
return self:oError

method setExpand(cExpand) class PLSAuthorizationsCpfSvc
    Default cExpand := ''
    self:cExpand := cExpand
return
