#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSAuthorizationBatchSvc

@author  Nicole Luna
@version P12
@since   29/01/2024
/*/
//------------------------------------------------------------------- 
class PLSAuthorizationBatchSvc

    Data oError as Object
  
    public method New()
    public method alteraLote(cLoteHat, cGuias, cContent)
    public method buscarGuias(cBatchCode)

endClass

method New() class PLSAuthorizationBatchSvc
Return self

//-------------------------------------------------------------------
/*/{Protheus.doc} alteraLote

@author    Nicole Luna
@version   V12
@since     29/01/2024
/*/
method alteraLote(cLoteHat, cGuias, cContent) class PLSAuthorizationBatchSvc

    local cQueryBEA     := ""
    local cQueryBase    := ""
    local cQueryUpdate  := ""
    local cWhere        := ""
    local lSuccess      := .T.
    default cLoteHat    := ""
    default cGuias      := ""
    default cContent    := ""

    if !empty(cGuias)
        cWhere += PLSConSQL(" AND BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT IN (" +  cGuias + ") " )
    elseif !empty(cLoteHat)
        cWhere += " AND BEA_LOTHAT = '" +  cLoteHat + "'"
    endif

	cQueryBase := " SELECT BEA_LOTHAT FROM " + RetSqlName("BEA") + " 
    cQueryBase += " WHERE 1=1 " 
    cQueryBase += cWhere

    cQueryBEA := cQueryBase + " AND D_E_L_E_T_ = ' ' "
    
	dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQueryBEA),"cAliasBEA",.f.,.t.)

	if !cAliasBEA->(Eof()) 

		begin transaction

			cQueryUpdate := " UPDATE " + RetsqlName("BEA") 
			cQueryUpdate += " SET BEA_LOTHAT = '" + cContent + "'"
			cQueryUpdate += " WHERE BEA_FILIAL = '" + xfilial("BEA") + "' "
            cQueryUpdate += cWhere
			cQueryUpdate += " AND D_E_L_E_T_ = ' ' "

			tcSqlExec( cQueryUpdate )

		end transaction

    else 
        //Se encontrar alguma guia, mas estiver deletada, retorna sucesso na API 

        dbUseArea(.t.,"TOPCONN",tcGenQry(,,cQueryBase),"cAliasAuxBEA",.f.,.t.)
        if cAliasAuxBEA->(Eof()) 
            lSuccess := .F.
        endif

        cAliasAuxBEA->(dbCloseArea())
    endIf 

	cAliasBEA->(dbCloseArea())

return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} buscarGuias

@author    Nicole Luna
@version   V12
@since     01/02/2024
/*/

Method buscarGuias(cBatchCode) class PLSAuthorizationBatchSvc

    Local cQuery := ""
    Default cBatchCode := ""

    cQuery := " SELECT BEA_OPEMOV+BEA_ANOAUT+BEA_MESAUT+BEA_NUMAUT AS NUMEROGUIA FROM " + RetSqlName("BEA") + " 
    cQuery += " WHERE 1=1 " 
    cQuery += " AND BEA_LOTHAT = '" + cBatchCode +  "'"
    cQuery += " AND D_E_L_E_T_ = ' ' "
	cQuery := PLSConSQL(cQuery)

Return cQuery
