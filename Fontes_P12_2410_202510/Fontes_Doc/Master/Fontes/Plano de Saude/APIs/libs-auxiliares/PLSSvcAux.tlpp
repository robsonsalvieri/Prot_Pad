#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'
#define TOOTH_REGION 1
#define SURFACES 2

//-------------------------------------------------------------------
/*/{Protheus.doc} PLSSvcAux
Classe auxilio para classes de Servicos API

@author  sakai
@version P12
@since   15/09/2023
/*/
//------------------------------------------------------------------- 
Class PLSSvcAux

    Data oResult as Object
    Data oError as Object
    Data oRest as Object
    Data lSuccess as Logical
    Data nStatus as Integer    
    Data lDBisSQL as Logical
    Data nPage as Integer
    Data nPageSize as Integer

    Public Method New()
    Public Method getError()
    Public Method getResult()
    Public Method getStatusCode()
    Public Method setError(nStatus, cCode, cMessage, cDetailedMessage)
    Public Method getQryPage()
    Public Method setPage(cPage)
    Public Method setPageSize(cPageSize)
    Public Method setValueData(cProperty, xValue)

endClass

Method New(oRest) Class PLSSvcAux

    self:oResult   := JsonObject():new()
    self:oError    := JsonObject():new()
    self:oRest     := oRest
    self:lSuccess  := .T.
    self:nStatus   := 200
    self:lDBisSQL  := iif(Upper(TCGetDB()) $ "ORACLE|DB2|POSTGRES|INFORMIX", .F., .T.)
    self:nPage     := 1
    self:nPageSize := 10

Return self

Method getError() Class PLSSvcAux
Return self:oError

Method getResult() Class PLSSvcAux
Return self:oResult:toJson()

Method getStatusCode() Class PLSSvcAux
Return self:nStatus

Method setError(nStatus, cCode, cMessage, cDetailedMessage) Class PLSSvcAux

    Default nStatus  := 400
    Default cCode    := '0002'
    Default cMessage := 'Bad Request'
    Default cDetailedMessage := 'O servidor nao foi capaz de entender a solicitacao'

    self:oError['status'] := nStatus
    self:oError['code'] := cCode
    self:oError['message'] := cMessage
    self:oError['detailedMessage'] := cDetailedMessage
    self:nStatus  := nStatus
    self:lSuccess := .F.

Return

Method getQryPage() Class PLSSvcAux

	Local cQuery 	:= ""
	Local cNumPage  := ""

	if self:nPage > 0 .And. self:nPageSize > 0
		cNumPage := alltrim(str((self:nPage - 1) * self:nPageSize))
		cQuery += " OFFSET " + cNumPage + " ROWS FETCH NEXT " + SOMA1(cValToChar(self:nPageSize)) + " ROWS ONLY "
	endif

Return cQuery

Method setPage(cPage) Class PLSSvcAux
    self:nPage := Val(cPage)
Return

Method setPageSize(cPageSize) Class PLSSvcAux
    self:nPageSize := Val(cPageSize)
Return

method setValueData(cProperty, xValue) Class PLSSvcAux
    self:oDao:setValue(cProperty, xValue)
return