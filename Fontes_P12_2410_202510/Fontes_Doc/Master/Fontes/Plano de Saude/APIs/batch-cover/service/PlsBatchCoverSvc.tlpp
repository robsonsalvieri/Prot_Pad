#include "TOTVS.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} PlsBatchCoverSvc
    Classe para validação de tokens TOTP e processamento de dados de prestadores de saúde e operadoras.

@author  Daniel Silva
@version P12
@since   01/04/2025
@see     https://tdn.totvs.com/
/*/
//------------------------------------------------------------------- 
Class PlsBatchCoverSvc

    Public Data oError 
    Public Data oResult 
    Public Data lSuccess As Logical
    Public Data nStatus As Numeric
    
    Private Data cHealthProviderCode As Character
    Private Data cProtocol As Character
    Private Data cCalcType As Character
    Private Data cExpand As Character

    Public Method New(oRest)
    Public Method SetError(nStatus, cCode, cMessage, cDetailedMessage)
    Public Method GetError()
    Public Method GetResult()
    Public Method GetStatusCode()
    Public Method Process()
    Public Method Validate() 
    Public Method ProcessData() 
    Public Method ProcessHealthProvider() 
    Public Method ProcessProtocol() 
    Public Method ProcessProcedures() 
    Public Method existExpand()
    
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
    Método Construtor

@param   oRest - Objeto contendo os parâmetros da requisição
@return  Self - Referência para o objeto criado
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method New(oRest) Class PlsBatchCoverSvc 

    Self:oError              := JsonObject():New()
    Self:oResult             := JsonObject():New()
    Self:nStatus             := 200
    Self:lSuccess            := .T.

    If ValType(oRest) == "O"
        Self:cHealthProviderCode := oRest:healthProviderCode
        Self:cProtocol           := oRest:protocol
        Self:cCalcType           := oRest:calcType
        self:cExpand             := oRest:expand
    EndIf
    
Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} Process
    Método principal para processamento da requisição

@return  Logical - Status de sucesso do processamento
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method Process() Class PlsBatchCoverSvc

    If Self:Validate()
        Self:ProcessData()
    EndIf

Return Self:lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} Validate
    Método para validação dos dados de entrada

@return  Logical - Status de validação
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method Validate() Class PlsBatchCoverSvc
    Local lValid := .T.
    
    If lValid .And. Empty(cValToChar(Self:cHealthProviderCode))
        lValid := .F.
        Self:SetError(400, "0002", "Atributo healthProviderCode não informado", "Entre em contato com o Suporte")
    EndIf
    
    If lValid .And. Empty(cValToChar(Self:cProtocol))
        lValid := .F.
        Self:SetError(400, "0003", "Atributo protocol não informado", "Entre em contato com o Suporte")
    EndIf

Return lValid

//-------------------------------------------------------------------
/*/{Protheus.doc} ProcessData
    Método para processamento dos dados do prestador de saúde e operadora

@return  Logical - Status do processamento
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method ProcessData() Class PlsBatchCoverSvc
    Local lSuccess := .T.
    
    lSuccess := Self:ProcessHealthProvider()
    
    If lSuccess
        lSuccess := Self:ProcessProtocol()
    EndIf

    if lSuccess 
        lSuccess := Self:ProcessProcedures()
    endif 
    
Return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} ProcessHealthProvider
    Método para processar os dados do prestador de saúde

@return  Logical - Status do processamento
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method ProcessHealthProvider() Class PlsBatchCoverSvc
    Local lSuccess  := .T.
    Local oProvider := Nil
    
    if(self:existExpand("healthProvider"))
        Self:oResult["healthProvider"] := {}
        
        BAU->(DbSetOrder(1))
        If BAU->(MsSeek(xFilial("BAU") + AllTrim(Self:cHealthProviderCode)))
            oProvider := JsonObject():New()
            
            oProvider['name']          := AllTrim(BAU->BAU_NOME)
            oProvider['documentNumber']:= AllTrim(BAU->BAU_CPFCGC)
            oProvider['code']          := AllTrim(BAU->BAU_CODIGO)
            oProvider['fantasyName']   := AllTrim(BAU->BAU_NFANTA)
            oProvider['zipCode']       := AllTrim(BAU->BAU_CEP)
            oProvider['address']       := AllTrim(BAU->BAU_END)
            oProvider['addressNumber'] := AllTrim(BAU->BAU_NUMERO)
            oProvider['neighborhood']  := AllTrim(BAU->BAU_BAIRRO)
            oProvider['state']         := AllTrim(BAU->BAU_EST)
            
            BID->(DbSetOrder(1))
            If BID->(MsSeek(xFilial("BID") + AllTrim(BAU->BAU_MUN)))
                oProvider['city'] := AllTrim(BID->BID_DESCRI)
            EndIf
            
            aAdd(Self:oResult["healthProvider"], oProvider)
        Else 
            lSuccess := .F.
            Self:SetError(400, "0004", "HealthProviderCode não encontrado", "Entre em contato com o Suporte")
        EndIf
    endif
    
Return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} ProcessProtocol
    Método para processar dados do protocolo

@return  Logical - Status do processamento
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method ProcessProtocol() Class PlsBatchCoverSvc
    Local lSuccess := .T.
    Local oProtocol := Nil
    
    if(self:existExpand("protocol"))
        Self:oResult["protocol"] := {}
        
        BCI->(DbSetOrder(14))
        If BCI->(MsSeek(xFilial("BCI") + AllTrim(Self:cProtocol)))
            oProtocol := JsonObject():New()
            
            oProtocol['competence']  := AllTrim(BCI->BCI_MES)+"/"+AllTrim(BCI->BCI_ANO)
            oProtocol['creationDate']:= FormataData(BCI->BCI_DTDIGI, 4)
            oProtocol['protocol']    := AllTrim(BCI->BCI_CODPEG)
            oProtocol['type']        := AllTrim(BCI->BCI_TIPGUI)
            oProtocol['amount']      := BCI->BCI_QTDDIG
            oProtocol['state']       := AllTrim(BA0->BA0_EST)

            if alltrim(Self:cCalcType) == "1" //prioriza apresentado
                iif(BCI->BCI_VALORI > 0, oProtocol['totalValue']  := BCI->BCI_VALORI, oProtocol['totalValue']  := BCI->BCI_VLRGUI )
            elseif alltrim(Self:cCalcType) == "2" // valor calculado
                oProtocol['totalValue']  := BCI->BCI_VLRGUI
            elseif alltrim(Self:cCalcType) == "3" // valor apresentado
                oProtocol['totalValue']  := BCI->BCI_VALORI
            endif
            
            aAdd(Self:oResult["protocol"], oProtocol)
        Else 
            lSuccess := .F.
            Self:SetError(400, "0005", "protocol não encontrado", "Entre em contato com o Suporte")
        EndIf
    endif
    
Return lSuccess

//-------------------------------------------------------------------
/*/{Protheus.doc} SetError
    Método para montar guias e procedimento vinculados a guia 

@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method ProcessProcedures() Class PlsBatchCoverSvc
    Local cSql := ""
    Local oProcedure := nil
    Local oAuth := nil
    Local cNumGui := ""
    Local cCurrentNumGui := ""
    
    if(self:existExpand("protocol"))
        Self:oResult["protocol"][1]["authorization"] := {}
        
        cSql := " SELECT BD6_VLRAPR, BD6_VLRPAG, BD6_CODPEG+ BD6_NUMERO AS NUMEROGUIA, "
        cSql += " BD6_OPEUSR + BD6_CODEMP + BD6_MATRIC + BD6_TIPREG + BD6_DIGITO AS MATRICULA, "
        cSql += " BD6_DTDIGI, BD6_NOMUSR, BD6_NOMSOL, BD6_CODPAD, BD6_CODPRO, BD6_DESPRO, BD6_QTDPRO, BD6_TIPGUI, "

        cSql += " CASE WHEN BD6_TIPGUI = '05' THEN BE4.BE4_NUMIMP ELSE BD5.BD5_NUMIMP END AS NUMIMP "

        cSql += " FROM " + RetSQLName("BD6") + " BD6 "

        cSql += " LEFT JOIN " + RetSQLName("BD5") + " BD5 ON "
        cSql += " BD5.BD5_FILIAL = '" + xFilial("BD5") + "' "
        cSql += " AND BD5.BD5_CODRDA = BD6.BD6_CODRDA " 
        cSql += " AND BD5.BD5_CODPEG = BD6.BD6_CODPEG "
        cSql += " AND BD5.BD5_NUMERO = BD6.BD6_NUMERO "
        cSql += " AND BD5.D_E_L_E_T_ = ' ' "
        cSql += " AND BD6.BD6_TIPGUI <> '05' "

        cSql += " LEFT JOIN " + RetSQLName("BE4") + " BE4 ON "
        cSql += " BE4.BE4_FILIAL = '" + xFilial("BE4") + "' "
        cSql += " AND BE4.BE4_CODRDA = BD6.BD6_CODRDA "
        cSql += " AND BE4.BE4_CODPEG = BD6.BD6_CODPEG "
        cSql += " AND BE4.BE4_NUMERO = BD6.BD6_NUMERO "
        cSql += " AND BE4.D_E_L_E_T_ = ' ' "
        cSql += " AND BD6.BD6_TIPGUI = '05' "

        cSql += " WHERE BD6.BD6_FILIAL = '" + xFilial("BD6") + "' "
        cSql += " AND BD6.BD6_CODRDA = '" + Self:cHealthProviderCode + "' "
        cSql += " AND BD6.BD6_CODPEG = '" + Self:cProtocol + "' "
        cSql += " AND BD6.D_E_L_E_T_ = ' ' "
        cSql += " ORDER BY NUMEROGUIA "
        
        cSql := ChangeQuery(cSql)
        
        dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSql),"TRB",.F.,.T.)
        
        if !TRB->( Eof() )
            cCurrentNumGui := alltrim(TRB->NUMEROGUIA)
            
            oAuth := JsonObject():New()
            oAuth['idOnHealthInsurer'] := iif(!empty(AllTrim(TRB->NUMIMP)),AllTrim(TRB->NUMIMP),AllTrim(TRB->NUMEROGUIA))
            oAuth['authorizationDate'] := SubStr(TRB->BD6_DTDIGI, 7, 2) + "/" + SubStr(TRB->BD6_DTDIGI, 5, 2) + "/" + SubStr(TRB->BD6_DTDIGI, 1, 4)
            oAuth['beneficiaryName']   := AllTrim(TRB->BD6_NOMUSR)
            oAuth['subscriberId']      := AllTrim(TRB->MATRICULA)
            oAuth['professional']      := AllTrim(TRB->BD6_NOMSOL)
            oAuth['procedures']        := {}
            
            Do While !TRB->( Eof() )
                cNumGui := alltrim(TRB->NUMEROGUIA)
                
                if cNumGui != cCurrentNumGui
                    aAdd(Self:oResult["protocol"][1]["authorization"], oAuth)
                    
                    oAuth := JsonObject():New()
                    oAuth['idOnHealthInsurer'] := iif(!empty(AllTrim(TRB->NUMIMP)),AllTrim(TRB->NUMIMP),AllTrim(TRB->NUMEROGUIA))
                    oAuth['authorizationDate'] := SubStr(TRB->BD6_DTDIGI, 7, 2) + "/" + SubStr(TRB->BD6_DTDIGI, 5, 2) + "/" + SubStr(TRB->BD6_DTDIGI, 1, 4)
                    oAuth['beneficiaryName']   := AllTrim(TRB->BD6_NOMUSR)
                    oAuth['subscriberId']      := AllTrim(TRB->MATRICULA)
                    oAuth['professional']      := AllTrim(TRB->BD6_NOMSOL)
                    oAuth['procedures']        := {}
                    
                    cCurrentNumGui := cNumGui
                endif
                
                oProcedure := JsonObject():New()
                oProcedure['tableCode'] := AllTrim(TRB->BD6_CODPAD)
                oProcedure['procedureCode'] := AllTrim(TRB->BD6_CODPRO)
                oProcedure['procedureDescription'] := AllTrim(TRB->BD6_DESPRO)
                oProcedure['authorizedQuantity'] := TRB->BD6_QTDPRO
                
                aAdd(oAuth['procedures'], oProcedure)
                
                TRB->( DbSkip() )
            EndDo
            
            aAdd(Self:oResult["protocol"][1]["authorization"], oAuth)
        endif
        
        TRB->(DbCloseArea())
    endif 
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} SetError
    Método para configurar uma mensagem de erro

@param   nStatus - Código de status HTTP
@param   cCode - Código interno de erro
@param   cMessage - Mensagem de erro
@param   cDetailedMessage - Mensagem detalhada do erro
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method SetError(nStatus, cCode, cMessage, cDetailedMessage) Class PlsBatchCoverSvc

    Default nStatus          := 400
    Default cCode            := '0000'
    Default cMessage         := 'Bad Request'
    Default cDetailedMessage := 'O servidor não foi capaz de entender a solicitação'

    Self:oError['status']          := nStatus
    Self:oError['code']            := cCode
    Self:oError['message']         := cMessage
    Self:oError['detailedMessage'] := cDetailedMessage
    
    Self:nStatus  := nStatus
    Self:lSuccess := .F.

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetError
    Método para obter o objeto de erro

@return  Object - Objeto JSON com informações do erro
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method GetError() Class PlsBatchCoverSvc
Return Self:oError

//-------------------------------------------------------------------
/*/{Protheus.doc} GetResult
    Método para obter o resultado em formato JSON

@return  Character - String JSON com o resultado
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method GetResult() Class PlsBatchCoverSvc
Return Self:oResult:ToJson()

//-------------------------------------------------------------------
/*/{Protheus.doc} GetStatusCode
    Método para obter o código de status HTTP

@return  Numeric - Código de status HTTP
@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 
Method GetStatusCode() Class PlsBatchCoverSvc
Return Self:nStatus

//-------------------------------------------------------------------
/*/{Protheus.doc} GetStatusCode
    Método para verificar expandables

@author  Daniel Silva
@version P12
@since   01/04/2025
/*/
//------------------------------------------------------------------- 

method existExpand(cExpand) class PlsBatchCoverSvc
return cExpand $ self:cExpand

     
