#INCLUDE 'protheus.ch'
#INCLUDE 'restful.ch'

//-------------------------------------------------------------------
/*/{Protheus.doc} PLProfessionalsDao

@author  sakai
@version P12
@since   04/03/2024
/*/
//------------------------------------------------------------------- 
class PLProfessionalsDao From PLSDaoAux

	Data cOrder As Character
	Data cAction As Character
    Data cTpProf As Character
	Data cTpPessoa As Character
	Data cCodRda As Character
	Data cCodLocal As Character
	Data cDB As Character

    Public Method New()
    Public Method setTpProf(cTpProf)
	Public Method setTpPessoa(cTpPessoa)
	Public Method setAction(cAction)
    Public Method setOrder(cOrder)
	Public Method getOrder()
	
	Public Method buscarBB0()
    Public Method getFilterBB0()
	Public Method unionCpfBB0()
	Public Method getFieldsBB0()

	Public Method buscarBC1()
	Public Method buscarPorCpfBC1()
	Public Method getFieldsBC1()
	Public Method getFilterBC1()
	Public Method getSpecialty(cCodProf, lBuscaCbos, cFilterCbos)

endClass

Method New() Class PLProfessionalsDao

    _Super:New()
	self:cOrder := ''
	self:cAction := ''
	self:cTpProf := ''
	self:cTpPessoa := '' 
	self:cDB 		:= TcGetDB()
	self:getValue("healthProviderId") := ''
	self:getValue("attendanceLocation") := '' 

Return self

Method setTpProf(cTpProf) Class PLProfessionalsDao
	self:cTpProf := cTpProf
Return

Method setTpPessoa(cTpPessoa) Class PLProfessionalsDao
	self:cTpPessoa := cTpPessoa
Return

Method setAction(cAction) Class PLProfessionalsDao
	self:cAction := cAction
Return

Method setOrder(cOrder) Class PLProfessionalsDao
    self:cOrder := cOrder
return

Method getOrder() Class PLProfessionalsDao

	if empty(self:cOrder)
		self:cOrder := " ORDER BY BB0_NOME "
	endif

Return self:cOrder

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos BB0

@author  sakai
@version P12
@since   04/03/2024
/*/
//------------------------------------------------------------------- 
Method buscarBB0() Class PLProfessionalsDao

	Local lFound := .F.
	Local cQuery := ""

	if !empty(self:getValue("professionalIdentifier"))
		cQuery += self:unionCpfBB0()
	endif

	cQuery += " SELECT "
	if !empty(self:getValue("professionalIdentifier"))
		cQuery += " 0 as DUMB_SORT, "
	endif
	cQuery += self:getFieldsBB0()
	cQuery += " FROM " + RetSqlName("BB0") + " BB0  "

	if (self:cTpProf == 'E' .AND. self:cTpPessoa == 'F')
		cQuery += " INNER JOIN "+RetSqlName("BAU")+" BAU  "
		cQuery += " 	ON(BB0_CODIGO = BAU_CODBB0) "
	endIf

	cQuery += " WHERE "
	cQuery += " 	BB0_FILIAL = '" + xFilial("BB0") + "' "
	cQuery += " 	AND (BB0_DATBLO = ' ' OR BB0_DATBLO > '" + dtos(Date()) + "') "

	if (self:cTpProf == 'E' .AND. self:cTpPessoa == 'F')
		cQuery += "  AND BAU_FILIAL = '" + xFilial("BAU") + "' "
		cQuery += "  AND BAU_CODIGO = '" + self:getValue("healthProviderId") + "' "
		cQuery += "  AND BAU.D_E_L_E_T_ = ' ' "
	endIf

	cQuery += self:getFilterBB0()
	cQuery += iif(!empty(self:getValue("professionalIdentifier"))," AND BB0.BB0_CGC <> '" + self:getValue("professionalIdentifier") + "' ",'')
	cQuery += " AND BB0.D_E_L_E_T_ = ' ' "
	cQuery += " AND BB0_NUMCR <> ' ' "
	cQuery += " AND BB0_ESTADO <> ' ' "
	cQuery += " AND BB0_NOME <> ' ' "
	cQuery += " AND BB0_CODSIG <> ' ' "

	cQuery += self:getOrder()
	cQuery += self:pageQuery()

	self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method unionCpfBB0() Class PLProfessionalsDao

	local cQuery := ""

	cQuery += " SELECT "
	cQuery += " 0 as DUMB_SORT, "
	cQuery += self:getFieldsBB0()
	cQuery += " FROM " + RetSqlName("BB0") + " BB0  "
	cQuery += " WHERE "
	cQuery += " 	BB0_FILIAL = '" + xFilial("BB0") + "' "
	cQuery += " 	AND (BB0_DATBLO = ' ' OR BB0_DATBLO > '" + dtos(Date()) + "') "
	cQuery += " AND BB0.BB0_CGC = '" + self:getValue("professionalIdentifier") + "' "
	if(self:getValue("isOdonto"))
		cQuery += " AND BB0_CODSIG = 'CRO' "
	endif 
	cQuery += " AND BB0.D_E_L_E_T_ = ' ' "
	cQuery += " UNION ALL "

	self:setOrder(" ORDER BY DUMB_SORT ")

Return cQuery


Method getFilterBB0() Class PLProfessionalsDao

	Local cFilter := ""
	local cOper := "AND"

	if !empty(self:cAction)
		if self:cAction == "typeAhead"
			cOper := "OR"
		endif
	endif

	cFilter += iif(!empty(self:getValue("idOnHealthInsurer")),cOper + " BB0_CODIGO = '" + self:getValue("idOnHealthInsurer") + "' ",'')
	cFilter += iif(!empty(self:getValue("name")),cOper + " UPPER(BB0_NOME) LIKE '%" + UPPER(self:getValue("name")) + "%' ",'')
	cFilter += iif(!empty(self:getValue("stateAbbreviation")),cOper + " UPPER(BB0_ESTADO) = '" + UPPER(self:getValue("stateAbbreviation")) + "' ",'')

	if !empty(self:getValue("professionalCouncilNumber"))
		if self:cAction == "typeAhead"
			cFilter += cOper + " BB0_NUMCR LIKE '" + self:getValue("professionalCouncilNumber") + "%' "
		else
			cFilter += cOper + " BB0_NUMCR = '" + self:getValue("professionalCouncilNumber") + "' "
		endif
	endif

	cFilter += iif(!empty(self:getValue("professionalCouncil")),cOper + " UPPER(BB0_CODSIG) = '" + UPPER(self:getValue("professionalCouncil")) + "' ",'')

	if !empty(self:cAction)
		if self:cAction == "typeAhead" .and. cFilter != ""
			cFilter :=  subStr(cFilter,3)
			cFilter := " AND (" + cFilter + ")"
		endif
	endif

	cFilter += iif(!empty(self:getValue("email"))," 	AND BB0_EMAIL = '" + self:getValue("email") + "' ",'')
	cFilter += iif(!empty(self:getValue("phoneNumber"))," 	AND BB0_TEL = '" + self:getValue("phoneNumber") + "' ",'')

	if(self:getValue("isOdonto"))
		cFilter += " AND BB0_CODSIG = 'CRO' "
	endif 

Return cFilter

Method getFieldsBB0() Class PLProfessionalsDao

	Local cFields := ""

	cFields += " BB0_CODIGO, "
	cFields += " BB0_CODSIG, "
	cFields += " BB0_NUMCR, "
	cFields += " BB0_ESTADO, "
	cFields += " BB0_NOME, "
	cFields += " BB0_CGC, "
	cFields += " BB0_EMAIL, "
	cFields += " BB0_TEL, "
	cFields += " BB0_VINC, "
	cFields += " BB0.R_E_C_N_O_  "

Return cFields

//-------------------------------------------------------------------
/*/{Protheus.doc} Metodos BC1

@author  sakai
@version P12
@since   04/03/2024
/*/
//------------------------------------------------------------------- 
Method buscarBC1() Class PLProfessionalsDao

	Local lFound := .F.
	Local cQuery := ''

	cQuery := " SELECT "
	cQuery += self:getFieldsBC1()
	cQuery += " FROM " + RetSqlName("BC1") + " BC1  "
	cQuery += " INNER JOIN " + RetSqlName("BB0") + " BB0   ON "
	cQuery += "     BB0_FILIAL = BC1_FILIAL "
	cQuery += "     AND BB0_CODIGO = BC1_CODPRF "
	cQuery += " WHERE "
	cQuery += " 	BC1_FILIAL = '" + xFilial("BC1") + "' "
	cQuery += " 	AND (BB0.BB0_DATBLO = ' ' OR BB0.BB0_DATBLO > '" + dtos(Date()) + "') "

	if(self:getValue("isOdonto"))
		cQuery += " AND BB0.BB0_CODSIG = 'CRO' "
	endif 
	
	cQuery += self:getFilterBC1()

	cQuery += self:getOrder()
	cQuery += self:pageQuery()

	self:setQuery(cQuery)
    lFound := self:executaQuery()

	if !lFound .and. !empty(self:getValue("professionalIdentifier"))
		lFound := self:buscarPorCpfBC1()
	endif

Return lFound

Method buscarPorCpfBC1() Class PLProfessionalsDao

	Local lFound := .F.
	Local cQuery := ''

	cQuery := " SELECT "
	cQuery += self:getFieldsBC1()
	cQuery += " FROM " + RetSqlName("BC1") + " BC1  "
	cQuery += " INNER JOIN " + RetSqlName("BB0") + " BB0  ON "
	cQuery += "     BB0_FILIAL = BC1_FILIAL "
	cQuery += "     AND BB0_CODIGO = BC1_CODPRF "
	cQuery += " WHERE "
	cQuery += " 	BB0_FILIAL = '" + xFilial("BB0") + "' "
	cQuery += " AND BB0.BB0_CGC = '" + self:getValue("professionalIdentifier") + "' "
	cQuery += " AND (BB0.BB0_DATBLO = ' ' OR BB0.BB0_DATBLO > '" + dtos(Date()) + "') "
	cQuery += " AND BC1.D_E_L_E_T_ = ' ' "
	cQuery += " AND BB0.D_E_L_E_T_ = ' ' "

	if(self:getValue("isOdonto"))
		cQuery += " AND BB0.BB0_CODSIG = 'CRO' "
	endif 
	
	self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound

Method getFieldsBC1() Class PLProfessionalsDao

	Local cFields := ""

	cFields += " BC1_CODIGO, "
	cFields += " BB0_ESTADO, "
	cFields += " BB0_NUMCR, "
	cFields += " BC1_CODLOC, "
	cFields += " BC1_CODESP, "
	cFields += " BB0_CODSIG, "
	cFields += " BB0_CODIGO, "
	cFields += " BC1_NOMPRF, "
	cFields += " BC1_CODINT, "
	cFields += " BB0_EMAIL, "
	cFields += " BB0_TEL, "
	cFields += " BB0_CGC, "
	cFields += " BC1.R_E_C_N_O_ "

Return cFields

Method getFilterBC1() Class PLProfessionalsDao

	Local cQuery := ""
	local cOper := "AND"
 
	if !empty(self:cAction)
		if self:cAction == "typeAhead"
			cOper := "OR"
		endif
	endif

	/* Mantive estre trecho pq existe no HAT caso precise implementar futuramente
	cQuery += iif(!empty(self:cCodEsp),cOper + " BC1_CODESP = '" + self:cCodEsp + "' ",'') */

	cQuery += iif(!empty(self:getValue("idOnHealthInsurer")),cOper + " BC1_CODPRF = '" + self:getValue("idOnHealthInsurer") + "' ",'')
	cQuery += iif(!empty(self:getValue("stateAbbreviation")),cOper + " UPPER(BC1_ESTCR) = '" + UPPER(self:getValue("stateAbbreviation")) + "' ",'')

	If !empty(self:getValue("professionalCouncilNumber"))
		if self:cAction == "typeAhead"
			cQuery += cOper + " BC1_NUMCR LIKE '" + self:getValue("professionalCouncilNumber") + "%' "
		else
			cQuery += cOper + " BC1_NUMCR = '" + self:getValue("professionalCouncilNumber") + "' "
		endif
	EndIf

	cQuery += iif(!empty(self:getValue("professionalCouncil")),cOper + " UPPER(BC1_SIGLCR) = '" + UPPER(self:getValue("professionalCouncil")) + "' ",'')
	cQuery += iif(!empty(self:getValue("name")),cOper + " UPPER(BC1_NOMPRF)   LIKE '%" + UPPER(self:getValue("name")) + "%' ",'')

	if !empty(self:cAction)
		if self:cAction == "typeAhead" .and. cQuery != ""
			cQuery :=  subStr(cQuery,3)
			cQuery := " AND (" + cQuery + ")"
		endif
	endif

	cQuery += iif(!empty(self:getValue("healthProviderId"))," AND BC1_CODIGO = '" + self:getValue("healthProviderId") + "' ",'')
	cQuery += iif(!empty(self:getValue("attendanceLocation"))," AND BC1_CODLOC = '" + self:getValue("attendanceLocation") + "' ",'')

	cQuery += " AND BC1.D_E_L_E_T_ = ' ' "
	cQuery += " AND BB0.D_E_L_E_T_ = ' ' "

Return cQuery

Method getSpecialty(cCodProf, lBuscaCbos, cFilterCbos) class PLProfessionalsDao

	Local cQuery := ""
	Local cOpePadr	:= PlsIntPad()

    self:setAlias("SPECQRY")

    if lBuscaCbos
        cQuery += self:getRowControl("BTQ_CDTERM")
        cQuery += " BTQ_CDTERM, BTQ_DESTER "
    else 
        cQuery += self:getRowControl("BQ1_CODESP")
	    cQuery += " BTQ_CDTERM, BTQ_DESTER, BQ1_CODESP, BAQ_DESCRI "
    endif

	cQuery += " FROM "+RetSqlName("BAQ")+" BAQ  "

	cQuery += " LEFT JOIN "+RetSqlName("BQ1")+" BQ1  "
    cQuery += "     ON BQ1_CODESP = BAQ_CODESP "
    cQuery += "     AND BQ1.D_E_L_E_T_ = ' ' "

	cQuery += " LEFT JOIN "+RetSqlName("BTU")+" BTU  "
	cQuery += " 	ON BTU_FILIAL = '"+xFilial("BTU")+"' "
	cQuery += "  	AND BTU_CODTAB = '24' "
	cQuery += " 	AND BTU_ALIAS = 'BAQ' "
	cQuery += " 	AND BTU_VLRBUS = BAQ_CODESP "
    cQuery += "     AND BTU.D_E_L_E_T_ = ' ' "

	cQuery += " LEFT JOIN "+RetSqlName("BTQ")+" BTQ  "
	cQuery += " 	ON BTQ_FILIAL = '"+xFilial("BTQ")+"' "
	cQuery += "  	AND BTQ_CODTAB = '24' "
	cQuery += "  	AND BTQ_CDTERM = BTU_CDTERM "
    cQuery += "     AND BTQ.D_E_L_E_T_ = ' ' "
	
	cQuery += " WHERE 1=1 "
	cQuery += "		AND BAQ_FILIAL = '"+xFilial("BAQ")+"' "
    cQuery += "     AND BQ1_FILIAL = '"+xFilial("BQ1")+"' "
	cQuery += "		AND BAQ_CODINT = '"+cOpePadr+"' "
    cQuery += "     AND BQ1_CODIGO = '"+cCodProf+"' "
    cQuery += "     AND BAQ.D_E_L_E_T_ = ' ' "
    cQuery += "     AND BTU_CDTERM IS NOT NULL "

    if lBuscaCbos
        if !empty(cFilterCbos)
            cQuery += " AND ( UPPER(BTQ_DESTER) LIKE UPPER('%" + cFilterCbos + "%') "
            cQuery += "           OR BTQ_CDTERM LIKE '%" + cFilterCbos + "%') "
        endif
        cQuery += " UNION ALL "
        If self:cDB <> "POSTGRES"
			cQuery += " SELECT ROW_NUMBER() OVER(ORDER BY BTQ_CDTERM) AS ROW#,BTQ_CDTERM, BTQ_DESTER "
		else
			cQuery += " SELECT ROW_NUMBER() OVER(ORDER BY BTQ_CDTERM) AS ROW,BTQ_CDTERM, BTQ_DESTER "
		endif	
        cQuery += " FROM "+RetSqlName("BTQ")+" BTQ  "
        cQuery += " INNER JOIN "+RetSqlName("BTU")+" BTU  "
        cQuery += "     ON BTU_FILIAL = '"+xFilial("BTU")+"' "
        cQuery += "     AND BTU_CODTAB = '24' "
	    cQuery += "     AND BTU_ALIAS = 'BAQ' "
	    cQuery += "     AND BTU_CDTERM = BTQ_CDTERM "
		cQuery += " 	AND BTU_VLRBUS = '"+cCodProf+"' "
	    cQuery += "     AND BTU.D_E_L_E_T_ = ' ' "
	    cQuery += "     AND BTU_CDTERM IS NOT NULL "
	    cQuery += " WHERE 1=1"
	    cQuery += "     AND BTQ_FILIAL = '"+xFilial("BTQ")+"' "
	    cQuery += "     AND BTQ_CODTAB = '24' "
	    cQuery += "     AND ( BTQ.D_E_L_E_T_ = ' ' OR BTQ.D_E_L_E_T_ = ' ' ) "
        if !empty(cFilterCbos)
	        cQuery += "     AND ( UPPER(BTQ_DESTER) LIKE UPPER('%" + cFilterCbos + "%') 
            cQuery += "           OR BTQ_CDTERM LIKE '%" + cFilterCbos + "%')"
        endif
    else
        cQuery += " GROUP BY BTQ_CDTERM, BTQ_DESTER, BQ1_CODESP, BAQ_DESCRI "
    endif

	cQuery += self:getWhereRow()

    self:setQuery(cQuery)
    lFound := self:executaQuery()

Return lFound
