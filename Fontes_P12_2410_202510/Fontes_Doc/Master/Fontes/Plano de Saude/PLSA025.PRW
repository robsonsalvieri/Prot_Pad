
#INCLUDE "plsa025.ch"
#include "PROTHEUS.CH"
#include "PLSMGER.CH"
/*/


Ŀ
Programa   PLSA025  Autor  Tulio Cesar           Data  10.04.2001 
Ĵ
Descrio  Cadastro de Faixas de Descontos Auto-Gestao                
Ĵ
Uso        Advanced Protheus 5.07                                     
Ĵ
Parametros Nenhum                                                     
Ĵ
            ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL           
Ĵ
Programador  Data    BOPS   Motivo da Alterao                     
Ĵ
ٱ


/*/
Function PLSA025
//Ŀ
// Define o cabecalho da tela de atualizacoes                               
//
PRIVATE aRotina := MenuDef()
PRIVATE cCadastro := STR0001 //"Cadastro de Faixas de Descontos Auto-Gestao"
//Ŀ
// Endereca a funcao de BROWSE                                              
//
BK3->(DbSetOrder(1))
BK3->(DbGoTop())
BK3->(mBrowse(06,01,22,75,"BK3"))

//Ŀ
// Fim da Rotina Principal                                                  
//
Return
/*/


Ŀ
Funcao     PL025MOV  Autor  Tulio Cesar            Data  10.04.01 
Ĵ
Descricao  Movimentacao do Cadastro de Faixas de Descontos            
ٱ


/*/
Function PL025MOV(cAlias,nReg,nOpc)
Local I__f := 0
//Ŀ
// Define Variaveis...                                                      
//
LOCAL nOpca	 := 0
LOCAL oDlg

PRIVATE oEnchoice
PRIVATE oGetDados
PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE aHeader
PRIVATE aCols
PRIVATE aVetTrab := {}
PRIVATE aChave := {}
//Ŀ
// Monta aCols e aHeader...                                                 
//
Store Header "BK4" TO aHeader For SX3->X3_ORDEM > "02"
If nOpc == K_Incluir
	Copy "BK3" TO Memory Blank
	Store COLS Blank "BK4" TO aCols FROM aHeader
Else
	Copy "BK3" TO MEMORY

	BK4->(DbSetOrder(1))
	If BK4->(DbSeek(xFilial("BK4")+BK3->BK3_CODIGO))
		Store COLS "BK4" TO aCols FROM aHeader VETTRAB aVetTrab While BK4->BK4_CODIGO == BK3->BK3_CODIGO
	Else
		Store COLS Blank "BK4" TO aCols FROM aHeader
	EndIf
EndIf
//Ŀ
// Define Dialogo...                                                        
//
DEFINE MSDIALOG oDlg TITLE cCadastro FROM ndLinIni,ndColIni TO ndLinFin,ndColFin OF GetWndDefault()
//Ŀ
// Monta Enchoice...                                                        
//
if Val(GetVersao(.F.)) >= 12 //Valida verso 12
	Zero();oEnchoice := MsMGet():New(cAlias,nReg,nOpc,,,,,{033,003,120,355},,,,,,oDlg,,,.F.)
Else
	Zero();oEnchoice := MsMGet():New(cAlias,nReg,nOpc,,,,,{025,001,120,355},,,,,,oDlg,,,.F.)
Endif
//Ŀ
// Monta GetDados...                                                        
//
oGetDados := MsGetDados():New(122,002,197,356,nOpc,"AllwaysTrue","AllwaysTrue",,.T.,,,,,,,,,oDlg)
//Ŀ
// Ativa o Dialogo...                                                       
//
ACTIVATE MSDIALOG oDlg ON INIT Eval({ || EnchoiceBar(oDlg,{|| nOpca := 1,If(Obrigatorio(aGets,aTela).And.oGetDados:TudoOK(),oDlg:End(),nOpca:=2),If(nOpca==1,oDlg:End(),.F.) },{||oDlg:End()},.F.,{})  })
//Ŀ
// Rotina de gravacao dos dados...                                          
//
If nOpca == K_OK
   //Ŀ
   // No caso de exclusao verifico se houve movimentacao...                    
   //
   If nOpc == K_Excluir
      //No nomento nao irei tratar pois tera de ser criada uma chave por este campo na tabela de movimentacao.
   Endif
   
   PLUPTENC("BK3",nOpc)   	
   
   aChave := { {"BK4_CODIGO",M->BK3_CODIGO} }

   PLUPTCOLS("BK4",aCols,aHeader,aVetTrab,nOpc,aChave)
EndIf
//Ŀ
// Fim da Rotina...                                                         
//
Return

/*/


Ŀ
Programa  MenuDef    Autor  Darcio R. Sporl        Data 22/12/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Private aRotina := { { STRPL01 , 'AxPesqui' , 0 , K_Pesquisar   , 0 ,.F.},; //'Pesquisar'
                      { STRPL02 , 'PL025MOV' , 0 , K_Visualizar ,0 ,Nil },; //'Visualizar'
                      { STRPL03 , 'PL025MOV' , 0 , K_Incluir    ,0 ,Nil},; //'Incluir'
                      { STRPL04 , 'PL025MOV' , 0 , K_Alterar    ,0 ,Nil},; //'Alterar'
                      { STRPL05 , 'PL025MOV' , 0 , K_Excluir    ,0 ,Nil} } //'Excluir'
Return(aRotina)