#include "tlpp-core.th"

namespace totvs.protheus.health.plan.manager

////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} EmailManager
	Classe utilizada para apoiar a execução de envio de e-mails
	do gerenciador de e-mails
/*/
Class EmailManager
	
	private data oJErrorData         as json 
	private data oJConfigurationData as json 
	private data oJsensitiveData     as json 
	private data oJAutenticator      as json 
     
	public method New() 
    public method SendEmailUsingManager(cConfigCode as character, cRecipient as character, aAttachments as array, cCopOcult as character) as json
	public method LoadConfiguration() as logical
	public method GetConfiguration()  as json
	public method GetErrorData()      as json
	
	private method SubstituteHtmlWithArrayValues(aData as array, cHtmlData as character) as character
	private method GetSensitiveData() as json
	private method setErrorData() 
	private method generateLogConfig() 
EndClass

////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} New
	Construtor da classe PlExCfMail
/*/
Method new() Class EmailManager
	self:oJErrorData         := JsonObject():New()
	self:oJConfigurationData := JsonObject():New()
	self:oJsensitiveData	 := JsonObject():New()
	self:oJAutenticator   	 := JsonObject():New()

Return Self

/*/{Protheus.doc} sendEmailUsingManager
////////////////////////////////////////////////////////////////////////
Realiza o envio do e-mail personalizado

@param cConfigCode, Caracter, É possível diretamente o código da configuração
de e-mail, e não é preciso vincular a função à configuração
@param cRecipient, Caracter, E-mail do destinatário
@param aAttachments, array, Array onde cada posição deve conter o caminho 
do arquivo junto com a extensão do arquivo

@param cBlindCopying, Caracter, endereços de e-mail que irão receber como cópia
oculta separados por virgula
/*/
Method sendEmailUsingManager(cConfigCode as character, cRecipient as character, aAttachments as array, cBlindCopying as character) as json class EmailManager

	Local oJSendmail       as json
	Local oJReplaceData    as json
	Local lFuncData := .T. as logical
	Local cHTML		       as character
	Local oClassEmail      := totvs.protheus.health.plan.util.Email():New() //classe de envio de e-mail
	
	Default cConfigCode    := ""
	Default cRecipient     := ""
	Default cBlindCopying  := ""
	Default aAttachments   := {}

	//carrega os dados de acodro com a configuracao de e-mail
	If self:loadConfiguration(cConfigCode) 

		//verifica se o campo que contem a funcao de retorno de dados esta preenchido
		If !Empty(self:getConfiguration():GetJsonObject("dataFunction")) 
			
			//verifica se a funcao foi compilada no RPO
			If FindFunction(self:getConfiguration():GetJsonObject("dataFunction"))
				
				//executa a funcao de retorno dos dados para o HTML
				oJReplaceData := &(self:GetConfiguration():GetJsonObject("dataFunction"))
			Else
				//se a funcao nao existir, nao permite continuar com o processo, se a funcao foi incluida no campo, obrigatoriamente
				//deve existir
				lFuncData := .F.
				self:setErrorData(lFuncData, "Não foi possível enviar o e-mail. A função de retorno de dados " +;
												self:GetConfiguration():GetJsonObject("dataFunction") + " não foi compilada no RPO")
				oJSendmail := self:GetErrorData()

				self:generateLogConfig(self:GetErrorData()["message"])
			EndIf
		EndIf

		If lFuncData
			
			//substitui os ## do codigo HTML pelos dados do array vindos da funcao de dados
			cHTML := self:substituteHtmlWithArrayValues(oJReplaceData, self:GetConfiguration():GetJsonObject("htmlCode"))
			
			//passa os dados para conectar na conta de e-mail de envio
			oClassEmail:connectAccount(self:GetSensitiveData():GetJsonObject("userAccount")      ,; 
									   self:GetSensitiveData():GetJsonObject("encryptedPassword"),;
									   self:GetConfiguration():GetJsonObject("smtp")		     ,; 
									   self:GetConfiguration():GetJsonObject("port")		     ,;
									   self:GetConfiguration():GetJsonObject("encryTls")         ,; 
									   self:GetConfiguration():GetJsonObject("encrySsl")         )

			//passa os dados  de autenticao
			oClassEmail:authenticate(self:GetSensitiveData():GetJsonObject("autenticator")  ,;
									 self:GetSensitiveData():GetJsonObject("authenticaUser"),;
									 self:GetSensitiveData():GetJsonObject("authenticaPass" ))
								
			//faz a chamada do envio de e-mail passando os dados do e-mail que sera enviado
			oJSendmail := oClassEmail:sendMail( self:getConfiguration():GetJsonObject("sender") ,;
												Alltrim(cRecipient)                             ,;
												self:GetConfiguration():GetJsonObject("subject"),; 
												cHTML											,;
												aAttachments									,;
												cBlindCopying)
		EndIf
	Else
		oJSendmail := self:GetErrorData()
	endif

	FreeObj(oClassEmail)
	
Return oJSendmail

/*/{Protheus.doc} loadConfiguration
Retorna os dados da configuracao selecionada

@param cCodeConfig, Caracter, Código da configuração de e-mail
/*/
Method loadConfiguration(cCodeConfig as character) as logical Class EmailManager
	
	Local lReturnGConfig := .T.

	Default cCodeConfig  := ""

	BZF->(DbSetOrder(1))

	//localiza o codigo da configuracao a partir da funcao que fez a chamada do metodo sendEmailUsingManager
	If Empty(cCodeConfig) .AND. BZF->(MsSeek(xFilial("BZF") + ProcName(2)))
		cCodeConfig := BZF->BZF_IDBZD
	EndIf

	If !Empty(cCodeConfig)

		//posiciona o cabecalho
		BZD->(DbSetOrder(1))

		If BZD->(MsSeek(xFilial("BZD") + cCodeConfig))

			self:oJConfigurationData["sender"       ] := AllTrim(BZD->BZD_REMTNT)
			self:oJConfigurationData["smtp"         ] := AllTrim(BZD->BZD_SMTP)
			self:oJConfigurationData["port"         ] := BZD->BZD_PORTA
			self:oJConfigurationData["subject"      ] := AllTrim(BZD->BZD_ASSUNT)
			self:oJConfigurationData["htmlCode"     ] := BZD->BZD_HTML
			self:oJConfigurationData["dataFunction" ] := AllTrim(BZD->BZD_FUNCAO)+"()"
			self:oJConfigurationData["encryTls"     ] := (BZD->BZD_TLS == "1") //TLS sim
			self:oJConfigurationData["encrySsl"     ] := (BZD->BZD_SSL == "1") //SSL sim

			self:oJsensitiveData["userAccount"      ] := AllTrim(BZD->BZD_USUARI)
			self:oJsensitiveData["encryptedPassword"] := AllTrim(BZD->BZD_SENHA)
			self:oJsensitiveData["autenticator"     ] := (BZD->BZD_AUTENT == "1") //autentica sim
			self:oJsensitiveData["authenticaUser"   ] := AllTrim(BZD->BZD_USUAUT)
			self:oJsensitiveData["authenticaPass"   ] := AllTrim(BZD->BZD_SENAUT)
		Else
			self:setErrorData(.F., "Não foi possível enviar o e-mail. O código de configuração de e-mail informado não foi encontrado no gerenciador de e-mails (PLMNG001)")
			lReturnGConfig := .F.

			self:generateLogConfig(self:GetErrorData()["message"])
		EndIf
	Else
		self:setErrorData(.F., "Não foi possível enviar o e-mail. O código da configuração de e-mail não foi informado")
		lReturnGConfig := .F.

		self:generateLogConfig(self:GetErrorData()["message"])
	EndIf
Return lReturnGConfig

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} SubstituteHtmlWithArrayValues
Gera o html com os dados customizados de acordo com a necessidade,
para isso deve existir um HTML base e nas posições onde deveram
ser exibidos os dados customizados deverá ter a chave ## para o 
sistema substituir pela posição correspondente no array de dados

@param aData, array, Array contendo os dados que vão substituir o ## 
@param cHtmlData, caracter, Código HTML contendo os trechos que devem ser substituidos pelos dados do array
@return cFormattedHTML, Código HTML formatado com os dados do array
/*/
Method SubstituteHtmlWithArrayValues(oJData as json, cHtmlData as character) as character Class EmailManager

	Local nLnData        as integer
	Local nI		     as integer
	Local cFormattedHTML as character
	Local cPosData       as character

	Default oJData    := JsonObject():New()   
	Default cHtmlData := ""

	//quantidade de posicoes no array
	nLnData        := Len(oJData:GetNames())
	cFormattedHTML := cHtmlData
	
	If !EMPTY(cFormattedHTML)

		//substitui as chaves no html pelos dados do array de acordo com a posicao
		For nI := 1 TO nLnData

			cPosData := cValToChar(nI)
			cFormattedHTML := STRTRAN(cFormattedHTML,"##" + cPosData, oJData:GetJsonObject(cPosData),,1)
		Next
	EndIf

Return cFormattedHTML

////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} GetErrorData
Retorna o objeto da negativa do envio de e-mail e a mensagem com o motivo
/*/
Method GetErrorData() as json Class EmailManager
Return self:oJErrorData

////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} setErrorData
Popula o objeto json com a negativa de envio e a mensagem do motivo
/*/
Method setErrorData(lSendMail as logical, cMessage as character) Class EmailManager
	self:oJErrorData["sendMail"] := lSendMail
	self:oJErrorData["message"] := cMessage
Return 

////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} getConfiguration
Retorna os dados da configuração identificada
/*/
Method getConfiguration() as json Class EmailManager
Return self:oJConfigurationData

////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} getSensitiveData
Retorna os dados sensiveis da configuração do e-mail do remetente
/*/
Method getSensitiveData() as json Class EmailManager
Return self:oJsensitiveData

/////////////////////////////////////////////////////////////////////
/*/{Protheus.doc} generateLogConfig
Gera os logs dos erros referente ao gerenciador de e-mails
/*/
Method generateLogConfig(cMesgeLog as character) Class EmailManager

	Local cLogNameCfg      := "LogConfigEmail.log"
	
	Default cMesgeLog := ""

	PlsLogFil("////////////////////////////////////////////////////////////", cLogNameCfg)
	PlsLogFil("DATA...............: " + DtoC( dDataBase )   				, cLogNameCfg)
	PlsLogFil("HORA...............: " + Time() 								, cLogNameCfg)
	PlsLogFil("ENVIRONMENT........: " + GetEnvServer() 						, cLogNameCfg)
	PlsLogFil("ERRO...............: " + cMesgeLog							, cLogNameCfg)
	PlsLogFil("------------------------- Termino --------------------------", cLogNameCfg)
Return 

