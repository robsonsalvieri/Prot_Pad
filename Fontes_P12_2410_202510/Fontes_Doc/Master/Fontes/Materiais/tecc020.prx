#INCLUDE "TECC020.CH" 
#INCLUDE "PROTHEUS.CH"      
#DEFINE nGetAlt 009 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECC020   ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao Historico de Equipamentos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/                                                 

Function TECC020()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := STR0001 //"Hist¢rico de Produtos/Eqto"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                       PARAMETROS                             ³
//³                                                              ³
//³ MV_PAR01 : Emissao de                                        ³
//³ MV_PAR02 : Emissao Ate                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()

If Pergunte("ATC020",.T.)
	Set Key VK_F12 TO c020Ativa()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	mBrowse( 6, 1,22,75,"AA3")
	Set Key VK_F12 To
EndIf
	
dbSelectArea("AA3")
dbSetOrder(1)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ MenuDef  ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 11.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECC020                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/                            
Static Function MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//³    6 - Alteracao sem inclusao de registro                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRotina := {{STR0002, "AxPesqui",  0, 1, 0, .F.},;	//"Pesquisar"
                  {STR0003, "Atc020Con", 0, 2, 0, .T.}}		//"Consultar"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtC020Con ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao Historico de Equipamentos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³31/01/2007³ Conrado Q.    ³ BOPS 115756: Adaptação para utilização em  ³±±
±±³          ³               ³ conjunto com a FillGetDb.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AtC020Con(cAlias,nReg,nOpc)

Local aArea			:= GetArea()
Local aAreaSB1		:= { SB1->(IndexOrd()) , SB1->(RecNo()) }
Local aAreaSA1		:= { SA1->(IndexOrd()) , SA1->(RecNo()) }
Local oDlg				:= Nil
Local oPanel			:= Nil
Local cCadastro		:= STR0001 //"Hist¢rico de Produtos/Eqto"
Local dUltPrev		:= Nil
Local cTabStat		:= Nil
Local nSize			:= Nil
Local aCusto			:= {} 
Local oGsTmpTable		:= NIL
Local nCustAtu		:= 0 
Local nCustHis		:= 0 
Local aSavHeaders		:= {}
Local cCodCli			:= ""
Local cLoja			:= ""
Local cNomCli			:= ""
Local cCgc				:= ""
Local cCodPro			:= ""
Local cDescPro		:= "" 
Local cNumSer			:= ""
Local dGar				:= ""
Local dFimVld			:= ""
Local cCtaPre			:= ""
Local cCobertura		:= ""    

Private INCLUI := .F.
Private ALTERA := .F.

Set Key VK_F12 To

dbSelectArea(cAlias)
dbGoto(nReg)

dbSelectArea("SA1")
dbSetOrder(1)
DbSeek(xFilial("SA1")+AA3->AA3_CODCLI+AA3->AA3_LOJA)

dbSelectArea("SB1")
dbSetOrder(1)
DbSeek(xFilial("SB1")+AA3->AA3_CODPRO)

dbSelectArea("AAH")
dbSetOrder(1)
DbSeek(xFilial("AAH")+AA3->AA3_CONTRT)

dbSelectArea("AAJ")
dbSetOrder(1)
DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE)
While ( !Eof() .AND. xFilial("AAJ")  == AAJ->AAJ_FILIAL .AND.;
							AA3->AA3_CTAPRE == AAJ->AAJ_CTAPRE )
	If ( !Empty(AAJ->AAJ_DTREAL) )
		dUltPrev := AAJ->AAJ_DTREAL
	EndIf
	dbSelectArea("AAJ")
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o conteudo dos gets                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
       
cCodCli  := SA1->A1_COD
cLoja    := SA1->A1_LOJA
cNomCli  := SA1->A1_NOME
cCgc     := SA1->A1_CGC
cCodPro  := SB1->B1_COD
cDescPro := SB1->B1_DESC 
cContrt  := AA3->AA3_CONTRT
cNumSer  := AA3->AA3_NUMSER
dGar     := AA3->AA3_DTGAR
dFimVld  := AAH->AAH_FIMVLD
cCtaPre  := AA3->AA3_CTAPRE  

cCobertura := AtBaseGrup( AA3->AA3_CODFAB, AA3->AA3_LOJAFA, AA3->AA3_CODPRO, AA3->AA3_NUMSER ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as Perguntas                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("ATC020",.F.)

aCusto   := AtCustoEqto(AA3->AA3_CODFAB,AA3->AA3_LOJAFA,AA3->AA3_CODPRO,AA3->AA3_NUMSER,MV_PAR02)
nCustAtu := aCusto[ 1 ]   
nCustHis := aCusto[ 2 ]	

DEFINE MSDIALOG oDlg FROM	009,000 TO 029,080 TITLE cCadastro OF oMainWnd

@ 003,004 TO 031,267 OF oDlg	PIXEL    

@ 007,009 SAY STR0004              SIZE 021,007     OF oDlg PIXEL //"C¢digo"
@ 016,009 MSGET cCodCli            SIZE 021,nGetAlt OF oDlg PIXEL WHEN .F.
@ 007,039 SAY STR0005              SIZE 016,007     OF oDlg PIXEL //"Loja"
@ 016,039 MSGET cLoja              SIZE 016,nGetAlt OF oDlg PIXEL WHEN .F.
@ 007,058 SAY STR0006              SIZE 017,007     OF oDlg PIXEL //"Nome"
@ 016,058 MSGET cNomCli            SIZE 140,nGetAlt OF oDlg PIXEL WHEN .F.
@ 007,202 SAY STR0007              SIZE 020,007     OF oDlg PIXEL //"CGC"
@ 016,202 MSGET cCgc               SIZE 053,nGetAlt OF oDlg PIXEL WHEN .F.

@ 035,004 MSPANEL oPanel PROMPT "" SIZE 263,97      OF oDlg CENTERED LOWERED //"Botoes"

@ 005,005 SAY STR0008              SIZE 065,009     OF oPanel PIXEL //"Produtos/Eqto"
@ 014,005 MSGET cCodPro      		SIZE 065,nGetAlt OF oPanel PIXEL WHEN .F.
@ 005,075 SAY STR0009              SIZE 120,009     OF oPanel PIXEL //"Descri‡Æo"
@ 014,075 MSGET cDescPro     		SIZE 120,nGetAlt OF oPanel PIXEL WHEN .F.

@ 005,200 SAY STR0038              SIZE 060,009     OF oPanel PIXEL // "Serie" //"Serie"
@ 014,200 MSGET cNumSer            SIZE 060,nGetAlt OF oPanel PIXEL WHEN .F.

@ 025,005 SAY STR0010              SIZE 042,009     OF oPanel PIXEL //"Dt.Garantia"
@ 034,005 MSGET dGar               SIZE 042,nGetAlt OF oPanel PIXEL WHEN .F.
@ 025,075 SAY STR0011              SIZE 070,009     OF oPanel PIXEL //"Contrato de Manuten‡Æo"
@ 034,075 MSGET cContrt            SIZE 070,nGetAlt OF oPanel PIXEL WHEN .F.
@ 025,150 SAY STR0012              SIZE 060,009     OF oPanel PIXEL //"Vencto do Contrato"
@ 034,150 MSGET dFimVld            SIZE 060,nGetAlt OF oPanel PIXEL WHEN .F.

@ 025,215 SAY STR0041              SIZE 045,009     OF oPanel PIXEL //"Vencto do Contrato" //"Cobertura"
@ 034,215 MSGET cCobertura         SIZE 045,nGetAlt OF oPanel PIXEL WHEN .F.

@ 045,005 SAY STR0013              SIZE 070,009     OF oPanel PIXEL //"N.Preventiva"
@ 054,005 MSGET cCtaPre            SIZE 070,nGetAlt OF oPanel PIXEL WHEN .F.
@ 045,080 SAY STR0014              SIZE 060,009     OF oPanel PIXEL //"Ult.Preventiva"
@ 054,080 MSGET dUltPrev           SIZE 060,nGetAlt OF oPanel PIXEL WHEN .F.
@ 045,150 SAY STR0037              SIZE 070,009     OF oPanel PIXEL //"Custo de Reposicao"
@ 054,150 MSGET nCustAtu           SIZE 070,nGetAlt OF oPanel PIXEL WHEN .F. PICTURE "@e 999,999,999.99"

@ 065,150 SAY STR0034              SIZE 070,009     OF oPanel PIXEL //"Custo de Manutencao"
@ 074,150 MSGET nCustHis           SIZE 070,nGetAlt OF oPanel PIXEL WHEN .F. PICTURE "@e 999,999,999.99"
@ 065,005 SAY STR0035              SIZE 070,009     OF oPanel PIXEL //"Status"

cTabStat := Capital( Tabela("A5",AA3->AA3_STATUS) )

@ 074,005 MSGET cTabStat           SIZE 120,nGetAlt OF oPanel PIXEL WHEN .F.

nSize := 11

@   3,272 BUTTON STR0015 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 1, @aSavHeaders, @oGsTmpTable) MESSAGE STR0039 OF oDlg PIXEL //"Chamados"  //"Chamados"
@  15,272 BUTTON STR0016 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 2, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Or‡amentos"
@  27,272 BUTTON STR0017 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 3, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"O.Servi‡o"
@  39,272 BUTTON STR0040 SIZE 040,nSize FONT oDlg:oFont ACTION AtContr()                                                 OF oDlg PIXEL //"Contrato"  //"Contrato"
@  51,272 BUTTON STR0018 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 4, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Hist¢rico"
@  63,272 BUTTON STR0019 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 5, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Garantia/OS"
@  75,272 BUTTON STR0020 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 6, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Preventivas"
@  87,272 BUTTON STR0031 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 8, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Manutencao"
@  99,272 BUTTON STR0033 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 9, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Pendencias"
@ 111,272 BUTTON STR0021 SIZE 040,nSize FONT oDlg:oFont ACTION AtcMostra( 7, @aSavHeaders, @oGsTmpTable)                 OF oDlg PIXEL //"Obsolescˆncia"
@ 123,272 BUTTON STR0042 SIZE 040,nSize FONT oDlg:oFont ACTION AtFNCs(oDlg, cCodCli, cLoja, cNomCli, cCodPro, cDescPro)  OF oDlg PIXEL //"FNCs"
@ 135,272 BUTTON STR0022 SIZE 040,nSize FONT oDlg:oFont ACTION oDlg:End()                                                OF oDlg PIXEL //"Sair"

ACTIVATE MSDIALOG oDlg CENTERED

If ( Select("QUERY1")<> 0 )
	dbSelectArea("QUERY1")
	dbCloseArea()
EndIf
If ( Select("QUERY2")<> 0 )
	dbSelectArea("QUERY2")
	dbCloseArea()
EndIf
If ( Select("QUERY3")<> 0 )
	dbSelectArea("QUERY3")
	dbCloseArea()
EndIf
If ( Select("QUERY4")<> 0 )
	dbSelectArea("QUERY4")
	dbCloseArea()
EndIf
If ( Select("QUERY5")<> 0 )
	dbSelectArea("QUERY5")
	dbCloseArea()
EndIf
If ( Select("QUERY6")<> 0 )
	dbSelectArea("QUERY6")
	dbCloseArea()
EndIf
If ( Select("QUERY7")<> 0 )
	dbSelectArea("QUERY7")
	dbCloseArea()
EndIf
If ( Select("QUERY8")<> 0 )
	dbSelectArea("QUERY8")
	dbCloseArea()
EndIf

If ( Select("QUERY9")<> 0 )
	dbSelectArea("QUERY9")
	dbCloseArea()
EndIf

If Valtype(oGsTmpTable) <> 'U'
	oGsTmpTable:Close()
EndIf

dbSelectArea("SA1")
dbSetOrder(aAreaSA1[1])
dbGoto(aAreaSA1[2])

dbSelectArea("SB1")
dbSetOrder(aAreaSB1[1])
dbGoto(aAreaSB1[2])

RestArea( aArea ) 

Set Key VK_F12 TO c020Ativa()
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcMostra ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra o Historico de Clientes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Query a ser Montada                                ³±±
±±³          ³ ExpA2 = Arquivos para serem deletados                      ³±±
±±³          ³ ExpA3 = aHeaders já criados                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³31/01/2007³Conrado Q.     ³Bops 155756: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDb.                ³±±
±±³13/06/2007³Conrado Q.     ³Bops 127255: No while da seleção dos        ³±±
±±³          ³               ³registros do banco de dados ABD o número de ³±±
±±³          ³               ³série é levado em consideração.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function AtcMostra( nCaso, aSavHeaders, oGsTmpTable )

Local oDlg			:= Nil											// Objeto da janela
Local oGetdb		:= Nil											// Objeto MsGetDB
Local cCadastro	:= ""											// Título da janela
Local aStruQuery	:= {}											// Estrutura do arquivo de trabalho
Local aRecnos		:= {}											// Listas dos registros selecionados
Local cAlias		:= "QUERY"+StrZero(nCaso,1)					// Alias temporário do arquivo de trabalho
Local aArea		:= { Alias() , IndexOrd() , RecNo() }		// Salva a area atual
Local aAreaAA3	:= { AA3->(IndexOrd()) , AA3->(RecNo()) }	// Salva posição e ordem da tabela AA3
Local bVisual		:= {||}										// Função de exibição dos dados
Local cQuery		:= ""											// Query de seleção de registros
Local aNoFields	:= Nil											// Se o banco de dados tiver campos MEMO esses devem ser colocados nessa variável

Default oGsTmpTable	:= Nil

Private aHeader 	:= {}											// Cabeçalho do MsGetDB

Do Case
	Case nCaso 	== 1
		cCadastro := STR0023 //"Chamados T‚cnicos"
		bVisual   := {|| AtcVisual(1,oGetDb)}
	Case nCaso 	== 2
		cCadastro := STR0024 //"Or‡amentos"
		bVisual   := {|| AtcVisual(2,oGetDb)}
	Case nCaso	== 3
		cCadastro := STR0025 //"Ordens de Servi‡o"
		bVisual   := {|| AtcVisual(3,oGetDb)}
	Case nCaso	== 4
		cCadastro := STR0026 //"Hist¢rico"
	Case nCaso	== 5
		cCadastro := STR0027 //"Garantia/OS"
		bVisual   := {|| AtcVisual(5,oGetDb)}
	Case nCaso	== 6
		cCadastro := STR0028 //"Preventiva(s)"
	Case nCaso	== 7
		cCadastro := STR0029 //"Obsolescˆncia"
	Case nCaso	== 8
		cCadastro := STR0030 //"Planos de Manutencao - Ocorrencias"
		bVisual   := {|| AtcVisual(8,oGetDb)}
	Case nCaso	== 9
      cCadastro := STR0033 //"Pendencias"
		bVisual   := {|| AtcVisual(9,oGetDb,aRecnos)}
EndCase

Do Case
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Chamados Tecnicos                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 1 )
	
		cSeek			:= xFilial("AB2")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile			:= "AB2->AB2_FILIAL+AB2->AB2_CODFAB+AB2->AB2_LOJAFA+AB2->AB2_CODPRO+AB2->AB2_NUMSER"
		bCond			:= {|| AB2->AB2_EMISSA >= MV_PAR01 .AND. AB2->AB2_EMISSA <= MV_PAR02 }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 3
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB2", { "AB2_STATUS", "AB2_NUMOS", "AB2_NUMORC", "AB2_NRCHAM" }, aHeader, aStruQuery ) }
		aNoFields		:= { "AB2_MEMO2" }
							
						
		cQuery := "SELECT AB2.* "
		cQuery += "FROM "+RetSqlName("AB2")+" AB2 "
		cQuery += "WHERE AB2.AB2_FILIAL='"+xFilial("AB2")+"'  AND "
		cQuery += 		" AB2.AB2_CODFAB='"+AA3->AA3_CODFAB+"' AND "
		cQuery +=      " AB2.AB2_LOJAFA='"+AA3->AA3_LOJAFA+"' AND "
		cQuery += 		" AB2.AB2_CODPRO='"+AA3->AA3_CODPRO+"' AND "
		cQuery +=		" AB2.AB2_NUMSER='"+AA3->AA3_NUMSER+"' AND "
		cQuery +=		" AB2.AB2_EMISSA>='"+Dtos(MV_PAR01)+"' AND "
		cQuery +=		" AB2.AB2_EMISSA<='"+Dtos(MV_PAR02)+"' AND "
		cQuery +=		" AB2.D_E_L_E_T_=' '"
		cQuery := ChangeQuery(cQuery)			

		AtcMonta(nCaso,   aSavHeaders,  "AB2",     cSeek,;
		         cWhile,  bCond,        cQuery,    bMountFile,;
		         nIdxOrd, bAfterHeader, aNoFields)		
					
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Orcamentos                                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 2 )
		cSeek			:= ""
		cWhile			:= ""
		bCond			:= {|| .T. }
		bMountFile		:= {|| ATCbMountFile( nCaso ) }
		nIdxOrd		:= 1
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB4", { "AB4_NUMOS", "AB4_NRCHAM", "AB4_NUMORC" }, aHeader, aStruQuery ) }

		AtcMonta(nCaso,   aSavHeaders, "AB4",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
						
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ordem de Servico                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 3 )
		cSeek			:= xFilial("AB7")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile			:= "AB7->AB7_FILIAL+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+AB7->AB7_NUMSER"
		bCond			:= {|| .T. }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 5
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB7", { "AB7_NUMORC", "AB7_NRCHAM", "AB7_NUMOS" }, aHeader, aStruQuery ) }
		aNoFields		:= {"AB7_MEMO2", "AB7_MEMO4"}
						
		cQuery := "SELECT AB7.* "
		cQuery += "FROM "+RetSqlName("AB7")+" AB7 "
		cQuery += "WHERE AB7.AB7_FILIAL='"+xFilial("AB7") +"' AND "
		cQuery += 		" AB7.AB7_CODFAB='"+AA3->AA3_CODFAB+"' AND "
		cQuery += 		" AB7.AB7_LOJAFA='"+AA3->AA3_LOJAFA+"' AND "
		cQuery += 		" AB7.AB7_CODPRO='"+AA3->AA3_CODPRO+"' AND "
		cQuery += 		" AB7.AB7_NUMSER='"+AA3->AA3_NUMSER+"' AND "
		cQuery += 		" AB7.AB7_EMISSA>='"+Dtos(MV_PAR01)+"' AND "
		cQuery += 		" AB7.AB7_EMISSA<='"+Dtos(MV_PAR02)+"' AND "
		cQuery += 		" AB7.D_E_L_E_T_=' '"			
		cQuery := ChangeQuery(cQuery)				
		
		AtcMonta(nCaso,   aSavHeaders,  "AB7",     cSeek,;
		         cWhile,  bCond,        cQuery,    bMountFile,;
		         nIdxOrd, bAfterHeader, aNoFields)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Historico                                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 4 )
		cSeek			:= xFilial("AAF")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile			:= "AAF->AAF_FILIAL+AAF->AAF_CODFAB+AAF->AAF_LOJAFA+AAF->AAF_CODPRO+AAF->AAF_NUMSER"
   		bCond			:= {||.T.}	
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 2
		bAfterHeader	:= {||}
		aNoFields		:= { "AAF_OBSERV" }
						
		cQuery := "SELECT AAF.* "
		cQuery += "FROM "+RetSqlName("AAF")+" AAF "
		cQuery += "WHERE AAF.AAF_FILIAL='"+xFilial("AAF") +"' AND "
		cQuery += 		" AAF.AAF_CODFAB='"+AA3->AA3_CODFAB+"' AND "
		cQuery += 		" AAF.AAF_LOJAFA='"+AA3->AA3_LOJAFA+"' AND "
		cQuery += 		" AAF.AAF_CODPRO='"+AA3->AA3_CODPRO+"' AND "
		cQuery += 		" AAF.AAF_NUMSER='"+AA3->AA3_NUMSER+"' AND "
		cQuery += 		" AAF.AAF_DTINI>='"+Dtos(MV_PAR01)+"' AND "
		cQuery += 		" AAF.AAF_DTINI<='"+Dtos(MV_PAR02)+"' AND "
		cQuery += 		" AAF.D_E_L_E_T_=' '"
		cQuery := ChangeQuery(cQuery)
		
		AtcMonta(nCaso,   aSavHeaders,  "AAF",     cSeek,;
		         cWhile,  bCond,        cQuery,    bMountFile,;
		         nIdxOrd, bAfterHeader, aNoFields)
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Garantia/OS                                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 5 )
		cSeek			:= xFilial("AB8")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile			:= "AB8->AB8_FILIAL+AB8->AB8_CODCLI+AB8->AB8_LOJA+AB8->AB8_CODPRD+AB8->AB8_NUMSER"
		bCond			:= {|| AB8->AB8_TIPO $ "24" .AND. AB8->AB8_DTGAR>=dDataBase }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 2
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB8", { "AB8_CODPRD", "AB8_NUMSER", "AB8_NUMOS" }, aHeader, aStruQuery ) }
						
		cQuery := "SELECT AB8.* "
		cQuery += "FROM "+RetSqlName("AB8")+" AB8 "
		cQuery += "WHERE AB8.AB8_FILIAL='"+xFilial("AB8")+"' AND "
		cQuery += 		" AB8.AB8_CODCLI='"+AA3->AA3_CODCLI+"' AND "
		cQuery += 		" AB8.AB8_LOJA='"+AA3->AA3_LOJA+"' AND "
		cQuery +=		" AB8.AB8_CODPRD='"+AA3->AA3_CODPRO+"' AND "
		cQuery +=		" AB8.AB8_NUMSER='"+AA3->AA3_NUMSER+"' AND "
		cQuery +=		" AB8.AB8_TIPO IN('2','4') AND "
		cQuery +=      " AB8.AB8_DTGAR>='"+Dtos(dDataBase)+"' AND "
		cQuery +=		" AB8.D_E_L_E_T_=' '"
		cQuery := ChangeQuery(cQuery)
		
		AtcMonta(nCaso,   aSavHeaders,  "AB8",  cSeek,;
		         cWhile,  bCond,        cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
						
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Preventivas                                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 6 )
		cSeek			:= xFilial("AAJ")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CTAPRE
		cWhile			:= "AAJ->AAJ_FILIAL+AAJ->AAJ_CODCLI+AAJ->AAJ_LOJA+AAJ->AAJ_CTAPRE"
		bCond			:= {|| .T. }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 2
		bAfterHeader	:= {||}
						
		AtcMonta(nCaso,   aSavHeaders, "AAJ",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
						
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Itens em Obsolescencia                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 7 )
		cSeek			:= xFilial("AAK")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO
		cWhile			:= "AAK->AAK_FILIAL+AAK->AAK_CODFAB+AAK->AAK_LOJAFA+AAK->AAK_CODPRO"
		bCond			:= {|| .T. }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 1
		bAfterHeader	:= {||}
						
		AtcMonta(nCaso,   aSavHeaders, "AAK",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
					
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Plano de Manutencao                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ						
	Case ( nCaso == 8 )
		cSeek			:= xFilial("ABE")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO
		cWhile			:= "ABE->ABE_FILIAL+ABE->ABE_CODFAB+ABE->ABE_LOJAFA+ABE->ABE_CODPRO"
		bCond			:= {|| .T. }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 1
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "ABE", { "ABE_CODFAB", "ABE_LOJAFA", "ABE_CODPRO", "ABE_NUMSER", "ABE_CODMEM" }, aHeader, aStruQuery ) }
		
		cQuery := "SELECT ABE.* "
		cQuery += "FROM "+RetSqlName("ABE")+" ABE "
		cQuery += "WHERE ABE.ABE_FILIAL='"+xFilial("ABE")+"' AND "
		cQuery += 		" ABE.ABE_CODFAB='"+AA3->AA3_CODFAB+"' AND "
		cQuery += 		" ABE.ABE_LOJAFA='"+AA3->AA3_LOJAFA+"' AND "
		cQuery +=		" ABE.ABE_CODPRO='"+AA3->AA3_CODPRO+"' AND "
		cQuery +=		" ABE.ABE_NUMSER='"+AA3->AA3_NUMSER+"' AND "
		cQuery +=		" ABE.D_E_L_E_T_ =' '"
		cQuery := ChangeQuery(cQuery)
		
		AtcMonta(nCaso,   aSavHeaders, "ABE",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pendencias da Base Instalada                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	Case ( nCaso == 9 )
		cSeek			:= xFilial("ABD")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile			:= "ABD->ABD_FILIAL+ABD->ABD_CODFAB+ABD->ABD_LOJAFA+ABD->ABD_CODPRO+ABD->ABD_NUMSER"
		bCond			:= {|| ( AAdd( aRecnos, ABD->( Recno() ) ), .T. ) }
		cQuery			:= Nil
		bMountFile		:= Nil
		nIdxOrd		:= 1
		bAfterHeader	:= {| aHeader,aStruQuery | ATCbAfterHeader( "ABD", { "ABD_FILIAL", "ABD_CODMEN" }, aHeader, aStruQuery ) }
		
		AtcMonta(nCaso,   aSavHeaders,  "ABD",  cSeek,;
		         cWhile,  bCond,        cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
EndCase

If	Select(cAlias) > 0
	dbSelectArea( cAlias )
	(cAlias)->( dbGoTop() )
	If ! (cAlias)->( Eof() )

		DEFINE MSDIALOG oDlg FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd
	
		@ 001,003 TO 031,267 OF oDlg	PIXEL
	
		@ 007,008 SAY STR0004 SIZE 021,007 OF oDlg PIXEL //"C¢digo"
		@ 016,007 MSGET AA3->AA3_CODCLI When .F. SIZE 021,nGetAlt OF oDlg PIXEL

		@ 007,036 SAY STR0005 SIZE 016,007 OF oDlg PIXEL //"Loja"
		@ 016,036 MSGET AA3->AA3_LOJA When .F. SIZE 016,nGetAlt OF oDlg PIXEL

		@ 007,055 SAY STR0006 SIZE 017,007 OF oDlg PIXEL //"Nome"
		@ 016,055 MSGET SA1->A1_NOME When .F. SIZE 107,nGetAlt OF oDlg PIXEL

		@ 007,165 SAY STR0008 			SIZE 065,007 OF oDlg PIXEL //"Produtos/Eqto"
		@ 016,165 MSGET AA3->AA3_CODPRO When .F. 	SIZE 065,nGetAlt OF oDlg PIXEL

		oGetDb := MsGetDB():New(035,003,141,314,2,,,,.T.,,,.F.,,cAlias)
	
		DEFINE SBUTTON 		FROM 002,278 TYPE 1  ENABLE OF oDlg ACTION ( oDlg:End() )
		DEFINE SBUTTON oBtn 	FROM 017,278 TYPE 15 ENABLE OF oDlg
   		oBtn:lAutDisable := .F.
		If ( nCaso <> 4 .And. nCaso <> 6 .And. nCaso <> 7 )
			oBtn:bAction := bVisual
		Else
			oBtn:SetDisable(.T.)
		EndIf
		ACTIVATE MSDIALOG oDlg
	Else
		Help(" ",1,"REGNOIS")
	EndIf
Else
	Help(" ",1,"REGNOIS")
EndIf

dbSelectArea("AA3")
dbSetOrder(aAreaAA3[1])
dbGoto(aAreaAA3[2])

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcVisual ³ Autor ³ Eduardo Riera         ³ Data ³ 28.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetuar a consulta generica ( AxVisual ) aos itens envol-  ³±±
±±³          ³ vidos.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhuma                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Consulta a ser Montada                             ³±±
±±³          ³ ExpO1 : Objeto da GetDb                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtcVisual(nCaso,oGetdb,aRecnosABD)

Local aArea 		:= { Alias() , IndexOrd() , RecNo() }
Local aSavHeader	:= aHeader
Local cCadBak       := ""

oGetdb:oBrowse:lDisablePaint := .T.
aHeader := {}

SetaPilha()

Do Case
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Chamados Tecnicos                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 1 )
		dbSelectArea("AB1")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AB1")+QUERY1->AB2_NRCHAM) )
			At300Visua("AB1",AB1->(RecNo()),2)
		EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Orcamentos                                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 2 )
		dbSelectArea("AB3")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AB3")+QUERY2->AB4_NUMORC) )
			At400Visua("AB3",AB3->(RecNo()),2)
		EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ordem de Servico                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 3 )
		dbSelectArea("AB6")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AB6")+QUERY3->AB7_NUMOS) )
			At450Visua("AB6",AB6->(RecNo()),2)
		EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Garantia/OS                                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 5 )
		dbSelectArea("AB6")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AB6")+QUERY5->AB8_NUMOS) )
			At450Visua("AB6",AB6->(RecNo()),2)
		EndIf
		
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Planos de Manutencao                                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 8 )
		dbSelectArea("AA9")
		dbSetOrder(1)
		If AA9->( DbSeek( xFilial("AA9")+QUERY8->ABE_PLANO+QUERY8->ABE_ITEM ) )

			cCadBak   := cCadastro
			cCadastro := STR0032 // Item do Plano de Manutencao
			AxVisual( "AA9", AA9->( Recno() ), 1 )
			cCadastro := cCadBak
			
			dbSelectArea( "QUERY8" ) 

			oGetDB:Refresh() 			

		EndIf

	Case ( nCaso == 9 )

		ABD->( MsGoto( aRecnosABD[ QUERY9->( Recno() ) ] ) )		

		cCadBak   := cCadastro
        cCadastro := STR0033 // Item do Plano de Manutencao
		AxVisual( "ABD", ABD->( Recno() ), 1 )
		cCadastro := cCadBak   

		dbSelectArea( "QUERY9" ) 
			
		oGetDB:Refresh() 			

EndCase
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a Rotina                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetaPilha()
aHeader := aSavHeader
oGetdb:oBrowse:lDisablePaint := .F.

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³C020Atica ³ Autor ³ Aline Correa do Vale  ³ Data ³08/04/1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ativa a tecla F12 de pergunta 									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ TECC020																	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function c020Ativa()
Pergunte("ATC020",.T.)           

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtContr   ³ Autor ³ Sergio Silveira     ³ Data ³ 20/01/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a chamada da rotina de visualizacao de contratos       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtContr()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function AtContr()

Local cCadBack 	:= cCadastro
Local aHeaderBKP:= {} 
Local aColsBKP	:= {}    
Local lTecc020	:= If (FunName() == "TECC020",.T.,.F.)

If !lTecc020
	aHeaderBKP:= aClone(aHeader) 
	aColsBKP	:= aClone(aCols)   
EndIf

AAH->( dbSetOrder(1) ) 
If AAH->( DbSeek(xFilial("AAH")+AA3->AA3_CONTRT) ) 
	cCadastro := STR0036 // "Contratos de manutencao" 
	At200Manut( "AAH", AAH->( Recno() ), 2 )
	cCadastro := cCadBack 
	If !lTecc020
		aHeader	:= aClone(aHeaderBKP)
		aCols		:= aClone(aColsBKP) 
	EndIf	 
Else
	Help( " ", 1, "ATC020NCT" ) // Equipamento nao possui contrato ! 
EndIf 	
	
Return(Nil) 
                                    
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AtFNCs   ³ Autor ³ Cicero Odilio Cruz  ³ Data ³ 16/02/2006 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega as FNC's da Entidade                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function AtFNCs(oDlg, cCodCli, cLoja, cNomCli, cCodPro, cDescPro)

Local oDlgFNC               // Objeto da Tela
Local oGetFNC               // Objeto do TCBrowse
Local aFNCs 	:= {} 		// Array com as FNCs
Local aMsSize 	:= {}		// Array com dados da Tela
Local cFiltro   := " "		// Filtro a ser aplicado

aMsSize := MsAdvSize()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifico as FNC's geradas pelo Chamado³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFiltro:= "AB2_FILIAL==xFilial('AB2') .AND. AB2_CODCLI=='" + cCodCli + "' .AND. AB2_LOJA=='" + cLoja + "' .AND. AB2_CODPRO=='" + cCodPro + "'"
If !Empty(cFiltro)
	DbSelectArea('AB2')
	DbSetFilter({||&cFiltro},cFiltro)
	DbGoTop()
EndIf
While !(AB2->(EOF()))
	If !Empty(AB2->AB2_CODFNC)
 		If Ascan(aFNCs,{|x| x[1]==AB2->AB2_CODFNC}) == 0
			AADD(aFNCs,{AB2->AB2_CODFNC, AB2->AB2_FNCREV, STR0043+AB2->AB2_NRCHAM+"/"+AB2->AB2_ITEM})   //"Chamado Tecnico: "
		EndIF
	EndIf
	DbSkip()
Enddo
DbSelectArea('AB2')
DbClearFilter()

aFNCs := aSort(aFNCs,,,{|x,y| x[1] < y[1]})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifico as FNC's geradas pela OS     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFiltro:= "AB9_FILIAL==xFilial('AB9') .AND. AB9_CODCLI=='" + cCodCli + "' .AND. AB9_LOJA=='" + cLoja + "' .AND. AB9_CODPRO=='" + cCodPro + "'"
If !Empty(cFiltro)
	DbSelectArea('AB9')
	DbSetFilter({||&cFiltro},cFiltro)
	DbGoTop()
EndIf
While !(AB9->(EOF()))
	If !Empty(AB9->AB9_CODFNC)
 		If Ascan(aFNCs,{|x| x[1]==AB9->AB9_CODFNC}) == 0
			AADD(aFNCs,{AB9->AB9_CODFNC, AB9->AB9_FNCREV, STR0044+SUBSTR(AB9->AB9_NUMOS,1,6)+"/"+SUBSTR(AB9->AB9_NUMOS,7,2)}) //"Ordem de Servico: "
		EndIF
	EndIf
	DbSkip()
Enddo
DbSelectArea('AB9')
DbClearFilter()

aFNCs := aSort(aFNCs,,,{|x,y| x[1] < y[1]})

If Len(aFNCs)>0

	DEFINE MSDIALOG oDlgFNC FROM	aMsSize[7],000 TO aMsSize[6],400 TITLE STR0045 PIXEL Of oDlg // "FNCs Relacionadas a Entidade"
	oDlgFNC:lMaximized := .F.

	@ 002,002 SAY   STR0004 		SIZE 021,007 	 OF oDlgFNC PIXEL 			//"C¢digo"
	@ 011,002 MSGET cCodCli 		 		SIZE 021,nGetAlt OF oDlgFNC PIXEL WHEN .F.
	@ 002,037 SAY   STR0005 		SIZE 016,007  	 OF oDlgFNC PIXEL 			//"Loja"
	@ 011,037 MSGET cLoja 		 			SIZE 016,nGetAlt OF oDlgFNC PIXEL WHEN .F.
	@ 002,060 SAY   STR0006 		SIZE 017,007 	 OF oDlgFNC PIXEL 			//"Nome"
	@ 011,060 MSGET cNomCli 				SIZE 140,nGetAlt OF oDlgFNC PIXEL WHEN .F.
	
	@ 025,002 SAY   STR0008 		SIZE 065,009 	 OF oDlgFNC PIXEL 			//"Produtos/Eqto"
	@ 034,002 MSGET cCodPro      			SIZE 065,nGetAlt OF oDlgFNC PIXEL WHEN .F.
	@ 025,080 SAY 	STR0009 		SIZE 120,009 	 OF oDlgFNC PIXEL 			//"Descri‡ao"
	@ 034,080 MSGET cDescPro     			SIZE 120,nGetAlt OF oDlgFNC PIXEL WHEN .F.	
    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta TCBrowse  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oGetFNC:= TCBrowse():New(050,002,198,(aMsSize[6]/2)-57,,,,oDlgFNC,,,,,,,,,,,,.F.,,.T.,,.F.)
	oGetFNC:AddColumn(TCColumn():New(STR0046, {|| aFNCs[oGetFNC:nAT,1]},,,,"LEFT",, .F., .F.,,,, .F.,)) //"Numero FNC"
	oGetFNC:AddColumn(TCColumn():New(STR0047, {|| aFNCs[oGetFNC:nAT,2]},,,,"LEFT",, .F., .F.,,,, .F.,)) //"Revisao"
	oGetFNC:AddColumn(TCColumn():New(STR0048, {|| aFNCs[oGetFNC:nAT,3]},,,,"LEFT",, .F., .F.,,,, .F.,)) //"Origem"
	oGetFNC:SetArray(aFNCs)
	
	oGetFNC:UpStable()
	oGetFNC:Refresh()          
	
	ACTIVATE MSDIALOG oDlgFNC CENTERED    
Else 
	MsgInfo(STR0049) // "Nao ha FNCs geradas para esta Entidade"
EndIf	
Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCMonta ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 31/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Montar aHeader e o Arq. Trab. através da FillGetDb           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCMonta( ExpN1, ExpA2,  ExpC3, ExpC4, ExpC5, ExpB6, ExpC7   ³±±
±±³          ³           ExpB8, ExpN9, ExpB10 )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Caso da consulta                                     ³±±
±±³          ³ ExpA2 : aHeaders reutilizáveis                               ³±±
±±³          ³ ExpC3 : Alias da tabela principal                            ³±±
±±³          ³ ExpC4 : Expressão utilizada no seek do FillGetDb             ³±±
±±³          ³ ExpC5 : Expressão utilizada no while do FillGetDb            ³±±
±±³          ³ ExpB6 : Bloco com a condição para a montagem do item         ³±±
±±³          ³ ExpC7 : Query quando é TOP Connect                           ³±±
±±³          ³ ExpB8 : Bloco de código alternativo para a montagem do arq.  ³±±
±±³          ³         trab.                                                ³±±
±±³          ³ ExpN9 : Ordem do cAlias                                      ³±±
±±³          ³ ExpB10: Bloco executado após a montagem do aHeader           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCMonta(nCaso,   aSavHeaders,  cAlias,    cSeek,;
                         cWhile,  bCond,        cQuery,    bMountFile,;
                         nIdxOrd, bAfterHeader, aNoFields)
Local bAction1	:= {||.T.}
Local bAction2	:= {||.F.}
Local aCpoNCopia	:= Nil
Local aCpoVirtual	:= Nil
Local cTempAlias	:= "QUERY" + Alltrim(Str(nCaso))
		
If ( Select( cTempAlias )==0 )
	FillGetDb(2,           cAlias,         cTempAlias,    /*cKeyInd*/,;
	          nIdxOrd,     cSeek,          {|| &cWhile},  /*{{bCond,bAction1,bAction2}}*/,;
	          aNoFields,   /*aYesFields*/, /*lOnlyYes*/,  cQuery,;
	          bMountFile,  /*bMountFile*/, .F.,           aCpoNCopia,;
	          aCpoVirtual, bAfterHeader,   /*bAfterRec*/, /*bBeforeRec*/)
	aAdd( aSavHeaders, { cTempAlias, aClone(aHeader) } )
Else
	If aScan( aSavHeaders, { |x| x[1]==cTempAlias } ) > 0
		aHeader := aClone( aSavHeaders[ aScan( aSavHeaders, { |x| x[1]==cTempAlias } ) ][2] )
	EndIf
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCbMountFile ³ Autor ³ Conrado Q. Gomes ³ Data ³ 31/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Função para montagem do arquivo de trabalho em casos espec.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCbMountFile( ExpN1, ExpA2 )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Caso da consulta                                     ³±±
±±³          ³ ExpA2 : Arquivos que devem ser apagados posteriormente       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCbMountFile(nCaso, oGsTmpTable)
Local lRet			:= .F.	// Retorno da função
Local lTmpOK		:= .F.	// Define se houve problemas na utilização da tabela temporária
Local nCntFor		:= 0	// Contador
Local aStruQuery	:= {}	// Estrutura do arquivo de trabalho
Local aIdx			:= {}	// Índices p/ a tabela temporária
Local aAuxIdx		:= {}
Local aInsertTmp	:= {}	// Conteúdo a ser preparado para a gravação na tabela temporária
Local nStpCmmIns	:= 500	// Quantidade do lote de registros a serem gravados na tabela temporária a cada INSERT do objeto GsTmpTable 
Local cQuery		:= ""	// Query de consulta
Local cAliasTemp	:= ""
Local cAlias_WT	:= ""
Local cAuxQuery	:= ""
Local cFldFlag	:= ""

Default oGsTmpTable := Nil

If	ValType(oGsTmpTable) == "O"
	oGsTmpTable:Close()
	TecDestroy(oGsTmpTable)
EndIf

If	nCaso == 2
	cAuxQuery	:= ",AB4."
	cFldFlag	:= "AB4_FLAG"
	aAuxIdx	:= {"AB4_NRCHAM","AB4_ITEM"}
	cAlias_WT	:= "AB4"
	cAliasTemp	:= "QUERY2"

	For nCntFor := 1 To Len( aHeader )
		If ( aHeader[nCntFor][10] <> "V" .And. aHeader[nCntFor][8] <> "M" )
			aAdd(aStruQuery, {aHeader[nCntFor][2],;
			                  aHeader[nCntFor][8],;
			                  aHeader[nCntFor][4],;
			                  aHeader[nCntFor][5]})
			If !IsHeadAlias( aHeader[nCntFor][2] ) .AND. !IsHeadRec( aHeader[nCntFor][2] )
				cQuery += cAuxQuery + aHeader[nCntFor][2]
			Endif
		EndIf
	Next nCntFor
	aAdd(aStruQuery, {cFldFlag, "L", 1, 0})

	AB3->( dbSetOrder( 1 ) )
	cQuery := "SELECT "+SubStr(cQuery,2)+" "
	cQuery +=   "FROM "+RetSqlName("AB3")+" AB3,"
	cQuery +=           RetSqlName("AB4")+" AB4 "
	cQuery +=  "WHERE AB3.AB3_FILIAL = '"+xFilial("AB3")+"' "
	cQuery +=    "AND AB3.D_E_L_E_T_ = ' ' "
	cQuery +=    "AND AB3.AB3_EMISSA >= '"+Dtos(MV_PAR01)+"' "
	cQuery +=    "AND AB3.AB3_EMISSA <= '"+Dtos(MV_PAR02)+"' "
	cQuery +=    "AND AB4.AB4_FILIAL = '"+xFilial("AB4")+"' "
	cQuery +=    "AND AB4.D_E_L_E_T_ = ' '"
	cQuery +=    "AND AB4.AB4_NUMORC = AB3.AB3_NUMORC "
	cQuery +=    "AND AB4.AB4_CODPRO = '"+AA3->AA3_CODPRO+"' "
	cQuery +=    "AND AB4.AB4_NUMSER = '"+AA3->AA3_NUMSER+"' "
	cQuery +=    "AND AB4.AB4_CODFAB = '"+AA3->AA3_CODFAB+"' "
	cQuery +=    "AND AB4.AB4_LOJAFA = '"+AA3->AA3_LOJAFA+"' "
	cQuery := ChangeQuery(cQuery)
	dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), "TOPQRY", .F., .T.)
					
	// Acerta os tipos de dados da Query
	For nCntFor := 1 To Len( aStruQuery )
		If aStruQuery[nCntFor,2] <> "C" .AND. AllTrim(aStruQuery[nCntFor,1]) <> cFldFlag
			TCSetField("TOPQRY", AllTrim(aStruQuery[nCntFor,1]), aStruQuery[nCntFor,2], aStruQuery[nCntFor,3], aStruQuery[nCntFor,4])
		EndIf
	Next nCntFor

	aIdx			:= {{"INDICE1", aAuxIdx}}
	oGsTmpTable	:= GSTmpTable():New(cAliasTemp /*cArea*/,;
	                                  aStruQuery /*aStruct*/,;
	                                  aIdx /*aIndex*/,;
	                                  {} /*aInitPad*/,;
	                                  nStpCmmIns /*nStepCommitInsert*/)
	If	( lTmpOK := oGsTmpTable:CreateTMPTable() )
		While ! TOPQRY->( Eof() )
			aInsertTmp	:= {}
			For nCntFor := 1 To Len(aStruQuery)
				If IsHeadAlias( aStruQuery[nCntFor][1] )
					aAdd(aInsertTmp, {aStruQuery[nCntFor][1], cAlias_WT})
				ElseIf IsHeadRec( aStruQuery[nCntFor][1] )
					aAdd(aInsertTmp, {aStruQuery[nCntFor][1], TOPQRY->(Recno())})
				ElseIf TOPQRY->(FieldPos(aStruQuery[nCntFor][1])) <> 0
					aAdd(aInsertTmp, {aStruQuery[nCntFor][1], TOPQRY->(FieldGet(FieldPos(aStruQuery[nCntFor][1])))})
				EndIf
			Next nCntFor
			If !( lTmpOK := oGsTmpTable:Insert(aInsertTmp) )
				lRet		:= .F.
				EXIT
			EndIf
			TOPQRY->(dBSkip())
		EndDo

		If lTmpOK
			lRet := oGsTmpTable:Commit()
		EndIf
	EndIf

	TOPQRY->(dBCloseArea())

EndIf

If	ValType(oGsTmpTable) == "O"
	If	oGsTmpTable:GetProp("ERROR")
		oGsTmpTable:ShowErro()
	EndIf
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCbAfterHeader ³ Autor ³ Conrado Q.     ³ Data ³ 31/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Função para adicionar campos no aHeader da FillGetDb         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCbAfterHeader( ExpC1, ExpA2, ExpA3, ExpA4 )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias da tabela                                      ³±±
±±³          ³ ExpA2 : Campos que serão adicionados ao final da aHeader     ³±±
±±³          ³ ExpA3 : aHeader da FillGetDb                                 ³±±
±±³          ³ ExpA4 : aStruQuery da FillGetDb                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCbAfterHeader( cAlias, aCampos, aHeader, aStruQuery )
Local aArea      := GetArea()	// Pega a área atual
Local nScan      := 0			// Posição do campo na array aCampos
Local aRet       := {}
Local aStruField := {}
Local cField     := ""
Local nC         := 0

aStruField := FWSX3Util():GetAllFields(cAlias)
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		aRet := FwTamSx3(cField)
		// Procura no aCampos algum campo com o mesmo título que o do registro atual
		If ( (nScan := aScan( aCampos, { |x| x == Alltrim(cField) } ) ) > 0 )
			// Procura no aStruQuery se o campo encontrado já existe
			If ( aScan( aStruQuery, { |x| Alltrim(x[1]) == aCampos[nScan] } ) == 0 )
				aadd(aStruQuery,{cField,;
								aRet[3],;
								aRet[1],;
								aRet[2]})
			EndIf
			// Procura no aHeader se o campo encontrado já existe
			If ( aScan( aHeader, { |x| Alltrim(x[2]) == aCampos[nScan] } ) == 0 )
				aadd(aHeader,{AllTrim(GetSx3Cache(cField, "X3_TITULO")),;
							cField,;
							AllTrim(GetSx3Cache(cField, "X3_PICTURE")),;
							aRet[1],;
							aRet[2],;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							aRet[3],;
							cAlias,;
							GetSx3Cache(cField, "X3_CONTEXT")})
			EndIf
		EndIf
	Next nC
EndIf
	
RestArea( aArea )
Return( .T. )
