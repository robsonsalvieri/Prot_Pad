#INCLUDE "TECA480.ch"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ TECA480  ³ Autor ³ Eduardo Riera         ³ Data ³06/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Implantar os titulos a pagar referente as despesas financei-³±±
±±³          ³ras com atendimento ( Reembolso de Despesas Financeiras )   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA480

Local aSays     := {}
Local aButtons  := {}
Local cCadastro := STR0001 //"Reembolso de despesas financeiras"
Local nOpca     := 0
Local oDlg

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                     ³
//³                                                          ³
//³ mv_par01            // Prefixo do Cta Pag ?              ³
//³ mv_par02            // Data de Vencto     ?              ³
//³ mv_par03            // Dt. Atendimento de ?              ³
//³ mv_par04            // Dt. Atendimento ate?              ³
//³ mv_par05            // Tecnico de ?                      ³
//³ mv_par06            // Tecnico ate?                      ³
//³ mv_par07            // Servico de ?                      ³
//³ mv_par08            // Servico ate?                      ³
//³ mv_par09            // OS Inicial ?                      ³
//³ mv_par10            // OS Final   ?                      ³
//³ mv_par11            // Quanto as OS? Todas / Encerradas  ³
//³ mv_par12            // Contabiliza On-Line?              ³
//³ mv_par13            // Mostra Lct. Ctb ?                 ³
//³ mv_par14            // Operacao? Incluir/Excluir         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pergunte("ATA480",.F.)
AADD(aSays,STR0002) //"    Esta rotina efetua o calculo e disponibiliza os titulos a pagar referente "
AADD(aSays,STR0003) //"ao reembolso das despesas financeira ocorridas no atendimento da Ordem de "
AADD(aSays,STR0004) //"serviço."
AADD(aButtons, { 5,.T.,{|| Pergunte("ATA480",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpcA:= 1,o:oWnd:End()} })
AADD(aButtons, { 2,.T.,{|o| nOpcA:= 0,o:oWnd:End()} })

FormBatch( cCadastro, aSays, aButtons )

If nOpca == 1
	Processa({|lEnd| AtDespFin(@lEnd,;
		MV_PAR01,;
		MV_PAR02,;
		MV_PAR03,;
		MV_PAR04,;
		MV_PAR05,;
		MV_PAR06,;
		MV_PAR07,;
		MV_PAR08,;
		MV_PAR09,;
		MV_PAR10,;
		MV_PAR11,;
		MV_PAR12==1,;
		MV_PAR13==1,;
		MV_PAR14==2)},,,.T.)
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtDespFin ³ Autor ³ Eduardo Riera         ³ Data ³06/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de calculo do reembolso de despesas financeiras      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpN1 := AtDespFin()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Flag de cancelamento do usuario                      ³±±
±±³          ³ExpC2: Prefixo do Titulo a pagar                            ³±±
±±³          ³ExpD3: Data de Vencto                                       ³±±
±±³          ³ExpD4: Data de Atendimento inicial                          ³±±
±±³          ³ExpD5: Data de Atendimento final                            ³±±
±±³          ³ExpC6: Tecnico Inicial                                      ³±±
±±³          ³ExpC7: Tecnico Final                                        ³±±
±±³          ³ExpC8: Codigo de Servico Inicial                            ³±±
±±³          ³ExpC9: Codigo de Servico Final                              ³±±
±±³          ³ExpCA: Ordem de Servico Inicial                             ³±±
±±³          ³ExpCB: Ordem de Servico Final                               ³±±
±±³          ³ExpNC: Quanto a OS?                                         ³±±
±±³          ³       [1] Todas                                            ³±±
±±³          ³       [2] Encerradas                                       ³±±
±±³          ³ExpLD: Contabiliza On-Line                                  ³±±
±±³          ³ExpLE: Mostra Lcto Contabil                                 ³±±
±±³          ³ExpLE: Exclui titulos gerados                               ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³10/11/2006³ Cleber M.     ³Bops 107722: Inclusao do P.E. AT480GRV apos ³±±
±±³          ³               ³a gravacao do titulo a pagar.               ³±±
±±³04/04/07  |Conrado Q      ³BOPS 122711: Alterada a utilização da cham. ³±±
±±³          ³               ³SubStr(cUsuario,7,15) Substr(cUserName,1,6) ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtDespFin(	lEnd,		cPrefixo,	dVencto,	dInicio,;
					dFim,		cCodTecI,	cCodTecF,	cCodSerI,;
					cCodSerF,	cOSI,		cOSF,		nSituaca,;
					lCtbOnLine,	lMostra,	lExclui	)

Local aArea     := GetArea()
Local aRegABC   := {}
Local cQuery    := ""
Local cQuebra   := ""
Local cKey      := "AB9_FILIAL+AB9_CODTEC"
Local cAliasAB7 := "AB7"
Local cAliasAB9 := "AB9"
Local cAliasABC := "ABC"
Local cArqInd   := ""
Local cLoteCtb  := ""
Local cArqCtb   := ""
Local cPadrao   := ""
Local nValor    := 0
Local nTotalCtb := 0
Local nHdlPrv   := 0
Local nX        := 0
Local nRecABC   := 0
Local nRegABC   := 0
Local nTitProc  := 0
Local nTitNoProc:= 0
Local nVlrProc  := 0
Local lQuery    := .F.
Local lValido   := .T.
Local lPadrao   := .F.
Local lAT480GRV	:= (ExistBlock("AT480GRV"))			//Indica se existe o PE AT480GRV
Local aTam := FwTamSx3("ABC_VALOR")
Local oDlg
Local oBmp
Local oBold
Local oBut
DEFAULT lExclui := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se ha contabilizacao                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPadrao    := IIf(!lExclui,"510","515")
lPadrao   := VerPadrao(cPadrao)
lCtbOnLine := lPadrao .AND. ( lCtbOnLine .OR. lExclui )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero do lote contabil                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX5")
dbSetOrder(1)
If DbSeek(xFilial()+"09TEC")
	cLoteCtb := AllTrim(X5Descri())
Else
	cLoteCtb := "TEC "
EndIf		
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa um execblock                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If At(UPPER("EXEC"),X5Descri()) > 0
	cLoteCtb := &(X5Descri())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se as despesas financeiras devem ser contabilizadas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCtbOnLine
	nHdlPrv := HeadProva(cLoteCtb,"TECA480",Substr(cUserName,1,6),@cArqCtb)
	If nHdlPrv <= 0
		lCtbOnLine := .F.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a operacao a ser executada                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lExclui
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecao dos registros validos para o processamento           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		lQuery := .T.

		cAliasAB9 := "AtDespFin"
		cAliasAB7 := "AtDespFin"
		cAliasABC := "AtDespFin"

		cQuery := "SELECT AB9.AB9_FILIAL,AB9.AB9_CODTEC,ABC.ABC_VALOR,ABC.R_E_C_N_O_ ABCRECNO "
		cQuery += "FROM "+RetSqlName("ABC")+" ABC,"
		cQuery += RetSQLName("AB9")+" AB9,"
		cQuery += RetSqlName("AB7")+" AB7 "
		cQuery += "WHERE ABC.ABC_FILIAL='"+xFilial("ABC")+"' AND "
		cQuery += "ABC.ABC_CODSER>='"+cCodSerI+"' AND "
		cQuery += "ABC.ABC_CODSER<='"+cCodSerF+"' AND "
		cQuery += "ABC.ABC_NUMERO='"+Space(Len(ABC->ABC_NUMERO))+"' AND "
		cQuery += "ABC.D_E_L_E_T_=' ' AND "
		cQuery += "AB9.AB9_FILIAL='"+xFilial("AB9")+"' AND "
		cQuery += "AB9.AB9_NUMOS=ABC.ABC_NUMOS AND "
		cQuery += "AB9.AB9_CODTEC=ABC.ABC_CODTEC AND "
		cQuery += "AB9.AB9_SEQ=ABC.ABC_SEQ AND "
		cQuery += "AB9.AB9_DTFIM >='"+Dtos(dInicio)+"' AND "
		cQuery += "AB9.AB9_DTFIM <='"+Dtos(dFim)+"' AND "
		cQuery += "AB9.AB9_CODTEC>='"+cCodTecI+"' AND "
		cQuery += "AB9.AB9_CODTEC<='"+cCodTecF+"' AND "
		cQuery += "AB9.AB9_NUMOS>='"+cOSI+"' AND "
		cQuery += "AB9.AB9_NUMOS<='"+cOSF+"zz' AND "
		cQuery += "AB9.D_E_L_E_T_=' ' AND "
		cQuery += "AB7.AB7_FILIAL='"+xFilial("AB7")+"' AND "
		cQuery += "AB7.AB7_NUMOS=SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+") AND "
		cQuery += "AB7.AB7_ITEM=SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+") AND "
		
		If nSituaca <> 1
			cQuery += "AB7.AB7_TIPO NOT IN('1','3') AND "
		EndIf
		cQuery += "AB7.D_E_L_E_T_=' ' "	
		cQuery += "ORDER BY "+SqlOrder(cKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABC,.T.,.T.)

		TcSetField(cAliasABC,"ABC_VALOR","N",aTam[1],aTam[2])
		
	#ELSE
		cArqInd := CriaTrab(,.F.)
		cQuery  := "AB9_FILIAL=='"+xFilial("AB9")+"'.AND."
		cQuery  += "DTOS(AB9_DTFIM)>='"+Dtos(dInicio)+"'.AND."
		cQuery  += "DTOS(AB9_DTFIM)<='"+Dtos(dFim)+"'.AND."
		cQuery  += "AB9_CODTEC>='"+cCodTecI+"'.AND."
		cQuery  += "AB9_CODTEC<='"+cCodTecF+"'.AND."
		cQuery  += "AB9_NUMOS>='"+cOSI+"'.AND."
		cQuery  += "AB9_NUMOS<='"+cOSF+"zz'"

		dbSelectArea("AB9")
		IndRegua("AB9",cArqInd,cKey,,cQuery)
		dbGotop()
	#ENDIF
	dbSelectArea(cAliasAB9)
	ProcRegua(AB9->(LastRec()))
	While ( !Eof() .AND. (cAliasAB9)->AB9_FILIAL == xFilial("AB9") )
		If !lQuery
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Posiciona os itens                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("AB7")
			dbSetOrder(1)
			DbSeek(xFilial("AB7")+AB9->AB9_NUMOS)		
			If nSituaca == 1 .OR. !AB7->AB7_TIPO$'1#3'
				dbSelectArea("ABC")
				dbSetOrder(1)
				DbSeek(xFilial("ABC")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)
				While !Eof() .AND. xFilial("ABC") == ABC->ABC_FILIAL .AND.;
						AB9->AB9_NUMOS == ABC->ABC_NUMOS .AND.;
						AB9->AB9_CODTEC == ABC->ABC_CODTEC .AND.;
						AB9->AB9_SEQ == ABC->ABC_SEQ
					If ABC->ABC_CODSER >= cCodSerI .AND. ABC->ABC_CODSER <= cCodSerF .AND.;
							Empty(ABC->ABC_NUMERO)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Calcula o valor da despesa financeira                        ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						nValor += ABC->ABC_VALOR
						aadd(aRegABC,ABC->(RecNo()))
					EndIf
					dbSelectArea("ABC")
					dbSkip()
				EndDo
			EndIf
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula o valor da despesa financeira                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aadd(aRegABC,(cAliasABC)->ABCRECNO)
			nValor += (cAliasABC)->ABC_VALOR
		EndIf
		cQuebra := (cAliasAB9)->AB9_CODTEC
		IncProc(RetTitle("AB9_CODTEC")+": "+(cAliasAB9)->AB9_CODTEC)		
		dbSelectArea(cAliasAB9)
		dbSkip()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a quebra de Tecnico                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasAB9)->(Eof()) .OR. cQuebra <> (cAliasAB9)->AB9_CODTEC
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gerar titulo financeiro                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If nValor > 0
				Begin Transaction
					dbSelectArea("AA1")
					dbSetOrder(1)
					If DbSeek(xFilial("AA1")+cQuebra) .AND. !Empty(AA1->AA1_CODFOR)
						FaDespFin(AA1->AA1_CODFOR,AA1->AA1_LOJFOR,nValor,cPrefixo,Nil,Nil,Nil,Nil,dVencto,IIf(lCtbOnLine,{|| nTotalCtb+=DetProva(nHdlPrv,cPadrao,"TECA480",cLoteCtb)},),"TECA480")
						If ( lAT480GRV )
							ExecBlock("AT480GRV",.F.,.F.)
						EndIf						
						nTitProc++
						nVlrProc += nValor
						For nX := 1 To Len(aRegABC)
							ABC->(MsGoto(aRegABC[nX]))
							RecLock("ABC")
							ABC->ABC_PREFIX := SE2->E2_PREFIXO
							ABC->ABC_NUMERO := SE2->E2_NUM
							ABC->ABC_PARCEL := SE2->E2_PARCELA
							ABC->ABC_TIPO   := SE2->E2_TIPO
							ABC->ABC_CODFOR := SE2->E2_FORNECE
							ABC->ABC_LOJFOR := SE2->E2_LOJA
							MsUnLock()
						Next nX
					EndIf
				End Transaction
			EndIf
			nValor  := 0
			aRegABC := {}
		EndIf
		If lEnd
			Exit
		EndIf
	EndDo
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Selecao dos registros validos para o processamento           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		lQuery := .T.

		cAliasAB9 := "AtDespFin"
		cAliasAB7 := "AtDespFin"
		cAliasABC := "AtDespFin"

		cQuery := "SELECT DISTINCT AB9.AB9_FILIAL,AB9.AB9_CODTEC,ABC.ABC_PREFIX,ABC.ABC_NUMERO,ABC.ABC_PARCEL,ABC.ABC_TIPO,ABC.ABC_CODFOR,ABC.ABC_LOJFOR "
		cQuery += "FROM "+RetSqlName("ABC")+" ABC,"
		cQuery += RetSqlName("AB9")+" AB9 "
		cQuery += "WHERE ABC.ABC_FILIAL='"+xFilial("ABC")+"' AND "
		cQuery += "ABC.ABC_CODSER>='"+cCodSerI+"' AND "
		cQuery += "ABC.ABC_CODSER<='"+cCodSerF+"' AND "
		cQuery += "ABC.ABC_NUMERO<>'"+Space(Len(ABC->ABC_NUMERO))+"' AND "
		cQuery += "ABC.D_E_L_E_T_=' ' AND "
		cQuery += "AB9.AB9_FILIAL='"+xFilial("AB9")+"' AND "
		cQuery += "AB9.AB9_NUMOS=ABC.ABC_NUMOS AND "
		cQuery += "AB9.AB9_CODTEC=ABC.ABC_CODTEC AND "
		cQuery += "AB9.AB9_SEQ=ABC.ABC_SEQ AND "
		cQuery += "AB9.AB9_DTFIM >='"+Dtos(dInicio)+"' AND "
		cQuery += "AB9.AB9_DTFIM <='"+Dtos(dFim)+"' AND "
		cQuery += "AB9.AB9_CODTEC>='"+cCodTecI+"' AND "
		cQuery += "AB9.AB9_CODTEC<='"+cCodTecF+"' AND "
		cQuery += "AB9.AB9_NUMOS>='"+cOSI+"' AND "
		cQuery += "AB9.AB9_NUMOS<='"+cOSF+"zz' AND "
		cQuery += "AB9.D_E_L_E_T_=' ' "
		cQuery += "GROUP BY AB9.AB9_FILIAL,AB9.AB9_CODTEC,ABC.ABC_PREFIX,ABC.ABC_NUMERO,ABC.ABC_PARCEL,ABC.ABC_TIPO,ABC.ABC_CODFOR,ABC.ABC_LOJFOR"

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABC,.T.,.T.)
	#ELSE
		cArqInd := CriaTrab(,.F.)
		cQuery  := "AB9_FILIAL=='"+xFilial("AB9")+"'.AND."
		cQuery  += "DTOS(AB9_DTFIM)>='"+Dtos(dInicio)+"'.AND."
		cQuery  += "DTOS(AB9_DTFIM)<='"+Dtos(dFim)+"'.AND."
		cQuery  += "AB9_CODTEC>='"+cCodTecI+"'.AND."
		cQuery  += "AB9_CODTEC<='"+cCodTecF+"'.AND."
		cQuery  += "AB9_NUMOS>='"+cOSI+"'.AND."
		cQuery  += "AB9_NUMOS<='"+cOSF+"zz'"

		dbSelectArea("AB9")
		IndRegua("AB9",cArqInd,cKey,,cQuery)
		dbGotop()
	#ENDIF
	dbSelectArea(cAliasAB9)
	ProcRegua(AB9->(LastRec()))
	While ( !Eof() .AND. (cAliasAB9)->AB9_FILIAL == xFilial("AB9") )
		If !lQuery
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Posiciona os itens                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("AB7")
			dbSetOrder(1)
			DbSeek(xFilial("AB7")+(cAliasAB9)->AB9_NUMOS)		
			dbSelectArea("ABC")
			dbSetOrder(1)
			DbSeek(xFilial("ABC")+(cAliasAB9)->AB9_NUMOS+(cAliasAB9)->AB9_CODTEC+(cAliasAB9)->AB9_SEQ)
			While !Eof() .AND. xFilial("ABC") == ABC->ABC_FILIAL .AND.;
					(cAliasAB9)->AB9_NUMOS == ABC->ABC_NUMOS .AND.;
					(cAliasAB9)->AB9_CODTEC == ABC->ABC_CODTEC .AND.;
					(cAliasAB9)->AB9_SEQ == ABC->ABC_SEQ
				If ABC->ABC_CODSER >= cCodSerI .AND. ABC->ABC_CODSER <= cCodSerF .AND. !Empty(ABC->ABC_NUMERO)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se a despesa financeira pode ser excluida           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SE2")
					dbSetOrder(1)
					If DbSeek(xFilial("SE2")+ABC->ABC_PREFIX+ABC->ABC_NUMERO+ABC->ABC_PARCEL+ABC->ABC_TIPO+ABC->ABC_CODFOR+ABC->ABC_LOJFOR)
						If FaCanDelCP("SE2","TECA480",.F.)
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Exclui a despesa financeira                                  ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							Begin Transaction
								nRecABC := ABC->(RecNo())
								dbSelectArea("ABC")
								dbSetOrder(2)
								If DbSeek(xFilial("ABC")+SE2->E2_PREFIXO+SE2->E2_NUM+SE2->E2_PARCELA+SE2->E2_TIPO+SE2->E2_FORNECE+SE2->E2_LOJA)
									While !Eof() .AND. ABC->ABC_FILIAL == xFilial("ABC") .AND.;
											ABC->ABC_PREFIX == SE2->E2_PREFIXO .AND.;
											ABC->ABC_NUMERO == SE2->E2_NUM .AND.;
											ABC->ABC_PARCEL == SE2->E2_PARCELA .AND.;
											ABC->ABC_TIPO   == SE2->E2_TIPO .AND.;
											ABC->ABC_CODFOR == SE2->E2_FORNECE .AND.;
											ABC->ABC_LOJFOR == SE2->E2_LOJA

										dbSelectArea("ABC")
										dbSkip()
										nRegABC := ABC->(RecNo())
										dbSkip(-1)

										RecLock("ABC")
										ABC->ABC_PREFIX := ""
										ABC->ABC_NUMERO := ""
										ABC->ABC_PARCEL := ""
										ABC->ABC_TIPO   := ""
										ABC->ABC_CODFOR := ""
										ABC->ABC_LOJFOR := ""
										MsUnLock()

										dbSelectArea("ABC")
										dbGoto(nRegABC)
									EndDo
								EndIf
								If lCtbOnLine .AND. "S"$SE2->E2_LA
									nTotalCtb+=DetProva(nHdlPrv,cPadrao,"TECA480",cLoteCtb)
								EndIf								
								RecLock("SE2")								
								FaAvalSE2(2,"TECA480")
								FaAvalSE2(3,"TECA480")
								SE2->(dbDelete())								
								MsUnLock()
								ABC->(dbSetOrder(1))
								ABC->(dbGoto(nRecABC))

								nTitProc++
								nVlrProc += SE2->E2_VALOR

							End Transaction
						Else
							nTitNoProc++
						EndIf
					EndIf					
				EndIf
				dbSelectArea("ABC")
				dbSkip()
			EndDo
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se a despesa financeira pode ser excluida           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SE2")
			dbSetOrder(1)
			If DbSeek(xFilial("SE2")+(cAliasABC)->ABC_PREFIX+(cAliasABC)->ABC_NUMERO+(cAliasABC)->ABC_PARCEL+(cAliasABC)->ABC_TIPO+(cAliasABC)->ABC_CODFOR+(cAliasABC)->ABC_LOJFOR)
				If FaCanDelCP("SE2","TECA480",.F.)
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Exclui a despesa financeira                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					Begin Transaction
						dbSelectArea("ABC")
						dbSetOrder(2)
						If DbSeek(xFilial("ABC")+SE2->E2_PREFIXO+SE2->E2_NUM+SE2->E2_PARCELA+SE2->E2_TIPO+SE2->E2_FORNECE+SE2->E2_LOJA)
							While !Eof() .AND. ABC->ABC_FILIAL == xFilial("ABC") .AND.;
									ABC->ABC_PREFIX == SE2->E2_PREFIXO .AND.;
									ABC->ABC_NUMERO == SE2->E2_NUM .AND.;
									ABC->ABC_PARCEL == SE2->E2_PARCELA .AND.;
									ABC->ABC_TIPO   == SE2->E2_TIPO .AND.;
									ABC->ABC_CODFOR == SE2->E2_FORNECE .AND.;
									ABC->ABC_LOJFOR == SE2->E2_LOJA

								dbSelectArea("ABC")
								dbSkip()
								nRegABC := ABC->(RecNo())
								dbSkip(-1)

								RecLock("ABC")
								ABC->ABC_PREFIX := ""
								ABC->ABC_NUMERO := ""
								ABC->ABC_PARCEL := ""
								ABC->ABC_TIPO   := ""
								ABC->ABC_CODFOR := ""
								ABC->ABC_LOJFOR := ""
								MsUnLock()

								dbSelectArea("ABC")
								dbGoto(nRegABC)
							EndDo
						EndIf
						If lCtbOnLine .AND. "S"$SE2->E2_LA
							nTotalCtb+=DetProva(nHdlPrv,cPadrao,"TECA480",cLoteCtb)
						EndIf
						RecLock("SE2")
						FaAvalSE2(2,"TECA480")
						FaAvalSE2(3,"TECA480")
						SE2->(dbDelete())
						MsUnLock()				
					End Transaction
					nTitProc++
					nVlrProc += SE2->E2_VALOR
				Else
					nTitNoProc++
				EndIf
			EndIf
		EndIf
		IncProc(RetTitle("AB9_CODTEC")+": "+(cAliasAB9)->AB9_CODTEC)
		dbSelectArea(cAliasAB9)
		dbSkip()		
		If lEnd
			Exit
		EndIf
	EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contabilizacao das despesas financeiras                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lCtbOnLine
	RodaProva(nHdlPrv,nTotalCtb)
	If nTotalCtb > 0
		PRIVATE INCLUI := .T.
		nTotalCtb := 0
		cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lMostra,.F.)
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exibe o resumo                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE STR0005  FROM 9,0 TO 25,50 OF oMainWnd //"Resumo"
@ 00,00 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 30, 120 NOBORDER WHEN .F. PIXEL
DEFINE FONT oBold NAME "Arial" SIZE 0, -12 BOLD
@ 08,38 SAY STR0006 FONT oBold PIXEL //"Resumo do processamento das despesas financeiras "
@ 24,30 TO 26 ,400 LABEL '' OF oDlg   PIXEL
@ 45,38 SAY STR0007 PIXEL OF oDlg //"Titulos processados: "
@ 55,38 SAY STR0008 PIXEL OF oDlg //"Valor dos titulos  : "
If lExclui
	@ 65,38 SAY STR0009 PIXEL OF oDlg //"Nao Estornados     :"
EndIf
@ 45,110 SAY nTitProc  SIZE 80, 10 PIXEL   RIGHT
@ 55,110 SAY Transform( nVlrProc, "@e 999,999,999.99" ) SIZE 80, 10 PIXEL   RIGHT
If lExclui
	@ 65,110 SAY nTitNoProc SIZE 80, 10 PIXEL   RIGHT
EndIf
@ 90,030 TO 92 ,400 LABEL '' OF oDlg PIXEL  	
DEFINE SBUTTON oBut FROM 98,162  TYPE 1 ACTION ( oDlg:End() ) ENABLE of oDlg
ACTIVATE MSDIALOG oDlg CENTERED
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integriade da rotina                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	dbSelectArea(cAliasAB9)
	dbCloseArea()
	dbSelectArea("AB9")
Else
	RetIndex("AB9")
	FErase(cArqInd+OrdBagExt())	
EndIf
RestArea(aArea)
Return(.T.)
