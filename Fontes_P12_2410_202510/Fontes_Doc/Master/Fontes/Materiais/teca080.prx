#Include 'Protheus.ch'
#INCLUDE "TECA080.CH"
#INCLUDE 'FWMVCDEF.CH'
#DEFINE MAXITENS 500

/*/


Ŀ
Program   TECA080    Autor Vendas e CRM            Data  09/03/12 
Ĵ
Descricao  Manutencao do Kit de Atendimento.                          
Ĵ
Retorno    Nenhum                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function TECA080()

Local oBrowse
Private cCadastro:= STR0001	//"Manipulacao do Kit de Atendimento"
Private aRotina := MenuDef()

oBrowse := FWMBrowse():New()
oBrowse :SetAlias('AA6')
oBrowse :SetDescription(STR0001) // "Manipulacao do Kit de Atendimento"
oBrowse :Activate()

Return(Nil)

/*/

Ŀ
Funcao     MenuDef   Autor  Vendas e CRM           Data  09/03/12 
Ĵ
Descricao  Definicao do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA080                                                    
ٱ


/*/

Static Function MenuDef()  

Local aRotina := {}

	//Ŀ
	// Define Array contendo as Rotinas a executar do programa      
	// ----------- Elementos contidos por dimensao ------------     
	// 1. Nome a aparecer no cabecalho                              
	// 2. Nome da Rotina associada                                  
	// 3. Usado pela rotina                                         
	// 4. Tipo de Transacao a ser efetuada                          
	//    1 - Pesquisa e Posiciona em um Banco de Dados             
	//    2 - Simplesmente Mostra os Campos                         
	//    3 - Inclui registros no Bancos de Dados                   
	//    4 - Altera o registro corrente                            
	//    5 - Remove o registro corrente do Banco de Dados          
	//    6 - Alteracao sem inclusao de registro                    
	//

ADD OPTION aRotina TITLE STR0002 ACTION 'PesqBrw'			 	OPERATION 1	ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.TECA080'	OPERATION 2	ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.TECA080'	OPERATION 3	ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TECA080'	OPERATION 4	ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TECA080'	OPERATION 5	ACCESS 0 //"Excluir"

Return (aRotina)

/*


Ŀ
Funcao    ModelDef   Autor Vendas CRM              Data 09/03/12  
Ĵ
Descricao  Definicao do modelo do cadastro Kit de atendimento    (MVC)
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA080                                                    
ٱ


*/
Static Function ModelDef()

// Cria a estrutura a ser usada no Modelo de Dados
Local oModel
Local oStruAA6		:= FWFormStruct(1,'AA6',{|cCampo| AllTrim(cCampo)+"|" $ "AA6_FILIAL|AA6_CODPRO|AA6_CODPRB|"})
Local oStruGrid  		:= FWFormStruct(1,'AA6')

oStruGrid:RemoveField('AA6_CODPRO')
oStruGrid:RemoveField('AA6_CODPRB')

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New('TECA080',/*bPreValidacao*/,{|oMdl| At080IncOk(oMdl)},/*bCommit*/,/*bCancel*/)

// Adiciona ao modelo uma estrutura de formulario de edicao por campo
oModel:AddFields('AA6MASTER',/*cOwner*/,oStruAA6,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)

// Adiciona ao modelo uma estrutura de formulario de edicao por grid
oModel:AddGrid( 'AA6GRID','AA6MASTER',oStruGrid,/*bPreValidacao*/,/*bLinePost*/,/*bCarga*/,/*bPost*/) // Adiciona uma Grid ao modelo

// Faz relaciomanto entre os compomentes do model
oModel:SetRelation("AA6GRID",{{"AA6_FILIAL",'xFilial("AA6")'},{"AA6_CODPRO","AA6_CODPRO"},{"AA6_CODPRB","AA6_CODPRB"}},AA6->(IndexKey(1)))

//Chave primaria para a entidade principal do modelo de dados.
oModel:SetPrimaryKey({'AA6_FILIAL','AA6_PRODUT'}) 

oModel:GetModel( 'AA6GRID' ):SetUniqueLine( { 'AA6_PRODUT' } )

Return oModel

/*                               


ͻ
Programa  ViewDef    Autor  Vendas CRM       Data  12/03/12 
͹
Desc.     Define a interface para Manutencao do Kit de          
          atendimento(MVC).                                     
͹
Uso       TECA080                                               
ͼ


*/
Static Function ViewDef()

Local oView
Local oModel   	:= FWLoadModel('TECA080')
Local oStruAA6 	:= FWFormStruct(2,'AA6',{|cCampo| AllTrim(cCampo)+"|" $ "AA6_FILIAL|AA6_CODPRO|AA6_CODPRB|"})
Local oStruGrid	:= FWFormStruct(2,'AA6')
Local oMdlGrid	:= oModel:GetModel('AA6GRID')

oStruGrid:RemoveField('AA6_CODPRO')
oStruGrid:RemoveField('AA6_CODPRB')

oView := FWFormView():New()
oView:SetModel(oModel)

oView:AddField('VIEW_CAB',oStruAA6,'AA6MASTER')
oView:AddGrid('VIEW_GRID',oStruGrid,'AA6GRID')
oView:AddIncrementField('VIEW_GRID','AA6_ITEM')

oView:CreateHorizontalBox('SUPERIOR',8)
oView:CreateHorizontalBox('INFERIOR',92)

oView:SetOwnerView('VIEW_CAB','SUPERIOR')
oView:SetOwnerView('VIEW_GRID','INFERIOR')

Return (oView)


/*/


Ŀ
Funcao    At080Grava Autor  Eduardo Riera          Data  21.12.98 
Ĵ
Descricao  Gravacao do Kit de Atendimento                             
Ĵ
Retorno    Indica se a gravacao foi efetuada ou nao                   
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function At080Grava(nOpcao)

Local nCntFor := 0
Local nUsado  := Len(aHeader)
Local lGravou := .F.
Local nCntFor2:= 0
Local nPosItem:= aScan(aHeader,{|x| AllTrim(x[2])=="AA6_ITEM" })

Do Case
	Case ( nOpcao <> 3 )
		For nCntFor := 1 To Len(aCols)
			If ( !aCols[nCntFor][nUsado+1] )
				dbSelectArea("AA6")
				dbSetOrder(1)
				If ( dbSeek(xFilial("AA6")+M->CAT080PRO+M->CAT080PRB+aCols[nCntFor][nPosItem]) )
					RecLock("AA6")
				Else
					RecLock("AA6",.T.)
				EndIf
				For nCntFor2 := 1 To nUsado
					If ( aHeader[nCntFor2][10] <> "V" )
						AA6->(FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2]))
					EndIf
				Next nCntFor2
				AA6->AA6_FILIAL 	:= xFilial("AA6")
				AA6->AA6_CODPRO 	:= M->CAT080PRO
				AA6->AA6_CODPRB	:= M->CAT080PRB
				lGravou := .T.
			Else
				dbSelectArea("AA6")
				dbSetOrder(1)
				If ( dbSeek(xFilial("AA6")+M->CAT080PRO+M->CAT080PRB+aCols[nCntFor][nPosItem]) )
					RecLock("AA6")
					dbDelete()
				EndIf
			EndIf
		Next nCntFor
	OtherWise
		For nCntFor := 1 To Len(aCols)
			dbSelectArea("AA6")
			dbSetOrder(1)
			If ( dbSeek(xFilial("AA6")+M->CAT080PRO+M->CAT080PRB+aCols[nCntFor][nPosItem]) )
				RecLock("AA6")
				dbDelete()
			EndIf
		Next
EndCase

Return(lGravou)

/*/


Ŀ
Funcao    At080LinOk Autor  Eduardo Riera          Data  21.12.98 
Ĵ
Descricao  Validacao da LinhaOk                                       
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At080LinOk()

Local nUsado 	:= Len(aHeader)
Local lRetorno := .T.
Local nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="AA6_PRODUT"})
Local nPosQuant:= aScan(aHeader,{|x| AllTrim(x[2])=="AA6_QUANT"})
Local nCntFor	 := 0

If ( !aCols[n][nUsado+1] )
	If ( (Empty(aCols[n][nPosProd]) .And. Len(aCols)==1) .Or.;
			Empty(aCols[n][nPosQuant]) )
		Help(" ",1,"AT080LIN01")
		lRetorno := .F.
	EndIf
EndIf
      
//Ŀ
//Verifica se nao ha produtos repitidos                         
//
If ( nPosProd <> 0 .And. lRetorno )
	For nCntFor := 1 To Len(aCols)
		If ( nCntFor <> n .And. !aCols[nCntFor][nUsado+1] .And. !aCols[n][nUsado+1])	
			If ( aCols[n][nPosProd] == aCols[nCntFor][nPosProd] )
				Help(" ",1,"AT080LIN02")
				lRetorno := .F. 
			EndIf
		EndIf
    Next nCntFor
EndIf


Return(lRetorno)

/*/


Ŀ
Funcao    At080TudOk Autor  Eduardo Riera          Data  21.12.98 
Ĵ
Descricao  Validacao da TudoOk                                        
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At080TudOk()  

Return( .T. ) 

/*


Ŀ
Funcao    At080IncOk Autor  Sergio Silveira        Data 17/09/2002
Ĵ
Descricao  Validacao da inclusao                                      
Ĵ
Sintaxe    ExpL1 := At080IncOk()                                      
Ĵ
Retorno    ExpL1 -> Validacao                                         
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


*/       

Function At080IncOk(oModel) 

LOCAL lRet := .T.       
Local nOperation 	:= oModel:GetOperation()

If nOperation == MODEL_OPERATION_INSERT

//Ŀ
// Verifica se ja existe na base                                          
//

dbSelectArea("AA6")
dbSetOrder(1)

	If dbSeek(xFilial("AA6")+ M->AA6_CODPRO + M->AA6_CODPRB )  
  	 lRet := .F.
 	  Help( " ", 1, "JAGRAVADO" ) 
	EndIf 
EndIf

Return(lRet)

