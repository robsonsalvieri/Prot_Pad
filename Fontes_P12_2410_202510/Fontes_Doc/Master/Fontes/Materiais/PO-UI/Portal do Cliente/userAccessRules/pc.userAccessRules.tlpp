#Include 'tlpp-core.th'
#Include 'tlpp-rest.th'
#Include "pc.useraccessrules.ch"

namespace pc.userAccessRules
using namespace pc.util

#Define CANVISUALIZE 'canVisualize'
#Define CANINCLUDE 'canInclude'
#Define ACTIONS 'actions'

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcUserAccessRules
	API para consulta de privilégios utilizada no Portal do Cliente
	@type		class
	@version	12.1.2310
	@author		Eduardo Paro / Squad CRM & Faturamento
	@since		06/12/2023
/*/
//------------------------------------------------------------------------------
Class pcUserAccessRules

    Public Data cUserLogin As Character
    Public Data cUserId As Character

	Public Method New() as Object

    @Get("/api/pc/v1/userAccessRules/:user")
	Public Method getUserAccessRules() As Logical

	Public Method salesOrderAccessRules() As Json 
    Public Method canAccessProgram() As Logical
	
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcUserAccessRules::New() as Object
	Obtém uma nova instancia da classe pcUserAccessRules
	@type		method
	@version	12.1.2310
	@author		Eduardo Paro / Squad CRM & Faturamento
	@since		06/12/2023
	@return		object, retorna uma nova instancia da classe.
/*/
//------------------------------------------------------------------------------
Method New() as Object Class pcUserAccessRules
    ::cUserLogin      	:= ""
	::cUserId   	  	:= ""
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcUserAccessRules::getUserAccessRules() as Logical
	Obtém uma lista de privilégios do usuário.
	@type		method
	@version	12.1.2310
	@author		Eduardo Paro / Squad CRM & Faturamento
	@since		06/12/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getUserAccessRules() As Logical Class pcUserAccessRules
	Local jResponse 		:= JsonObject():new()
	Local aMenus 			:= {}

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')

	::cUserLogin := getPathParams("user")

	aAdd(aMenus, ::salesOrderAccessRules())

	IF !Empty(aMenus[1]["menu"])
		oRest:setStatusCode(200)
		jResponse['items'] := aMenus
		oRest:setResponse(jResponse)
	Else
		SetRestFault(404, FwHttpEncode(STR0001) ) //"Login não localizado"
	EndIf

	aSize(aMenus, 0)
	FreeObj(jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcUserAccessRules::salesOrderAccessRules() as Json
	Obtém um JSON com os privilégios das ações de incluir e visualizar 
	do usuário sobre a rotina MATA410.
	@type		method
	@version	12.1.2310
	@author		Eduardo Paro / Squad CRM & Faturamento
	@since		06/12/2023
	@return		JSON, Json com os privilégios da rotina MATA410
/*/
//------------------------------------------------------------------------------
Method salesOrderAccessRules() As Json Class pcUserAccessRules
	Local jActions	:= JsonObject():New() 	As Json
	Local jMenu		:= JsonObject():New() 	As Json
	Local cProgram	:= "MATA410" 			As Character
    Local aArea	    := GetArea() 			As Array

    DbSelectArea("AI3")
    AI3->(DbSetOrder(2))
    IF AI3->(DbSeek(FWXFilial("AI3") + PadR(::cUserLogin, GetSx3Cache("AI3_LOGIN","X3_TAMANHO"))))
        ::cUserId := IIF(Empty(AI3->AI3_USRSIS), "000000", AI3->AI3_USRSIS)

		jMenu['menu'] := cProgram
		jActions[CANVISUALIZE] 	:= ::canAccessProgram(cProgram, 2)
		jActions[CANINCLUDE] 	:= ::canAccessProgram(cProgram, 3)

		jMenu[ACTIONS] := jActions
	EndIf
	RestArea(aArea)
	aSize(aArea, 0)
Return jMenu

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcUserAccessRules::canAccessProgram() as Logical
	Obtém os Privilégios de acesso de uma rotina.
	@type		method
	@version	12.1.2310
	@author		Eduardo Paro / Squad CRM & Faturamento
	@since		06/12/2023
	@param		cProgram, character, código do programa (Ex: MATA410)
	@param		nOperation, numeric, tipo de operação do menu.
	@return 	logical, retorna verdadeiro caso o usuário possua acesso ao programa.
/*/
//------------------------------------------------------------------------------
Method canAccessProgram(cProgram As Character, nOperation As Numeric) as Logical Class pcUserAccessRules
Return MPUserHasAccess(cProgram, nOperation, ::cUserId, .F., .F., /*reservado*/, Nil)


