#include 'tlpp-core.th'
#INCLUDE 'FWMVCDEF.CH'
#include 'tlpp-rest.th'
#include 'pc.salesOrders.data.protheus.ch'

namespace pc.salesOrders
using namespace pc.util

#DEFINE LABEL 1
#DEFINE VALUE 2
#DEFINE INCLUI 3


//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData
	Classe responsavel por consultar, incluir um Pedido de Venda
	@type 		class
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
/*/
//------------------------------------------------------------------------------
CLASS SalesOrdersProtheusData FROM FWAdapterBaseV2
	Public Method new() as Object
	Public Method getHeaders()
	Public Method getItems()
	Public Method getTotalSalesOrder()
	Public Method saveSalesOrders() as logical
	Public Method RowToJson()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData::New() as Object
	Cria uma nova intancia da classe SalesOrdersProtheusData
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@return		object, nova instancia da classe SalesOrdersProtheusData
/*/
//------------------------------------------------------------------------------
Method New() as Object CLASS SalesOrdersProtheusData
	_Super:New( "GET", .T. )
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData::GetHeaders(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character)
	Retorna todos os cabeçalhos de pedidos de vendas (SC5)
	@type		method
	@param		nPage		, Numeric	, Numero da pagina
	@param		nPageSize	, Numeric	, Tamanho da Pagina
	@param		cOrderId	, Character	, Número do Pedido de Venda (C5_NUM)
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param      aCustomers  , Array     , Lista dos clientes que o usuário tem acesso
	@param      cUser       , Character , Usuário da requisição
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method GetHeaders(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character, aCustomers as Array, cUser as Character) CLASS SalesOrdersProtheusData
	Local cWhere	                                     as character
	Local oTempTableCustomer                             as Object
	Local aArea		         := GetArea()                as Array

	Default cUser := ""

	addMapSC5( self )

	cWhere := GetFilterSC5( cOrderId )
	oTempTableCustomer := CreateTempTbCustomers( aCustomers, cUser )

	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQuerySalesOrder(.F., .T., .T., .T., .T., .T., /*cFilterDiff*/, oTempTableCustomer, cUser))
	::SetWhere( cWhere )
	::SetOrder( "C5_FILIAL, C5_NUM" )

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf
	
	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	If ::Execute()
		::FillGetResponse()
	EndIf

	oTempTableCustomer:Delete()
	FwFreeObj(oTempTableCustomer)
	oTempTableCustomer := Nil
	
	RestArea( aArea )
	aSize( aArea, 0 )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData::GetItems(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, cItemId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character)
	Retorna todo os Itens de Pedidos de vendas
	@type		method
	@param		nPage		, Numeric	, Numero da pagina
	@param		nPageSize	, Numeric	, Tamanho da Pagina
	@param		cOrderId	, Character	, Código do Pedido de venda (C5_NUM)
	@param		cItemId		, Character	, Código do item (C6_ITEM)
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Data de sincronismo
	@param		cSort		, Character	, Ordernação do Response
	@param      aCustomers  , Array     , Lista dos clientes que o usuário tem acesso
	@param      cUser       , Character , Usuário da requisição
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method GetItems(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, cItemId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character, aCustomers as Array, cUser as Character ) CLASS SalesOrdersProtheusData
	local cWhere	                                     as character
	local aArea 	         := GetArea()                as Array
	Local oTempTableCustomer := FwTemporaryTable():New() as Object

	Default cUser := ""

	cWhere := GetFilterSC5( cOrderId )
	oTempTableCustomer := CreateTempTbCustomers( aCustomers, cUser )

	If !Empty(cItemId)
		cWhere += " AND SC6.C6_ITEM = '" + cItemId + "'"
	Endif

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf
	
	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	addMapSC6( self )
	
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQuerySalesOrder(.T., .F., .F., .F., .F., .F., /*cFilterDiff*/, oTempTableCustomer, cUser ))
	::SetWhere( cWhere )
	::SetOrder( "C6_FILIAL, C6_NUM, C6_ITEM, C6_PRODUTO" )

	If ::Execute()
		::FillGetResponse()
	EndIf
	
	oTempTableCustomer:Delete()
	FwFreeObj(oTempTableCustomer)
	oTempTableCustomer := Nil

	RestArea( aArea )
	aSize( aArea, 0 )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData::GetTotalSalesOrder
	Obtem o total do Pedido de Venda especificado na URI.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		09/11/2023
	@param		cOrderId, character, Código do Pedido de Venda (C5_NUM)
/*/
//------------------------------------------------------------------------------
Method GetTotalSalesOrder(cOrderId as Character) class SalesOrdersProtheusData
	Local aHeader   := {} as Array
	Local aItems	:= {} as Array

	Private oApiManager := Nil
	Private lMsErroAuto := .F.
	Private lAutoErrNoFile := .T.

	oApiManager := FWAPIManager():New("MATSIMP","1.000")
	oApiManager:SetApiAlias({"SC5", "items", "items"})
	oApiManager:Activate()

	SC5->(DbSetOrder(1)) // C5_FILIAL, C5_NUM
	If SC5->(DbSeek(FwXFilial("SC5") + cOrderId))
		headerBuilderSC5(@aHeader)
		lineBuilderSC6(cOrderId, @aItems)
		MSExecAuto({|a, b, c, d, e| MATA410(a, b, c, d, e)}, aHeader, aItems, INCLUI, .F.)
		oRest:setResponse(oApiManager:ToObjectJson())
	EndIf

	oApiManager:Destroy()
    aSize(aHeader, 0)
    aSize(aItems, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} addMapSC5
	Adiciona uma lista de campos do cabeçalho do Pedidos de Venda.
	@type		function
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@param 		oSelf	, object	, Objeto herdado da classe FWAdapterBaseV2
/*/
//------------------------------------------------------------------------------
Function addMapSC5( oSelf as Object )
	Local aInternalId			:= GetInternalId( { "C5_FILIAL", "C5_NUM" } )
	Local aCustomerId			:= GetInternalId( { "A1_COD", "A1_LOJA"} )
	Local aCustomerInternalId	:= GetInternalId( { "A1_FILIAL", "A1_COD", "A1_LOJA"} )
	Local aPaymentInternalId	:= GetInternalId( { "E4_FILIAL", "C5_CONDPAG" } )
	Local lFixedId				:= !oSelf:oJsonObj:lList


	oSelf:AddMapFields( "internalid"		, "internalid"	, .T., .F., { "internalid"	, 'C', aInternalId[1] , 0 }, aInternalId[2])
	oSelf:AddMapFields( "externalid"		, "C5_NUMEXT"	, .T., .F., { "C5_NUMEXT"	, 'C', GetFieldLength("C5_NUMEXT")	, 0 } )
	oSelf:AddMapFields( "branchid"			, "C5_FILIAL"	, .T., .F., { "C5_FILIAL"	, 'C', GetFieldLength("C5_FILIAL")	, 0 }, "SC5.C5_FILIAL" )
	oSelf:AddMapFields( "orderid"			, "C5_NUM"		, .T., lFixedId, { "C5_NUM"	, 'C', GetFieldLength("C5_NUM")		, 0 }, "SC5.C5_NUM" )
	oSelf:AddMapFields( "ordertype"			, "C5_TIPO"		, .T., .F., { "C5_TIPO"		, 'C', GetFieldLength("C5_TIPO")    , 0 }, )
	oSelf:AddMapFields( "pricelistid"		, "C5_TABELA"	, .T., .F., { "C5_TABELA"	, 'C', GetFieldLength("C5_TABELA")  , 0 } )
	oSelf:AddMapFields( "paymentConditionid", "C5_CONDPAG"	, .T., .F., { "C5_CONDPAG"	, 'C', GetFieldLength("C5_CONDPAG") , 0 } )
	oSelf:AddMapFields( "paymentdesc", "paymentdesc"	, .T., .F., { "paymentdesc"	, 'C', GetFieldLength("E4_DESCRI") , 0 },  "' '" )
	oSelf:AddMapFields( "paymentinternalid" , "paymentinternalid", .T., .F., { "paymentinternalid"	, 'C', aPaymentInternalId[1] , 0 },;
		aPaymentInternalId[2])
	oSelf:AddMapFields( "issuedate"			, "C5_EMISSAO"	, .T., .F., { "C5_EMISSAO"	, 'D', GetFieldLength("C5_EMISSAO") , 0 } )
	oSelf:AddMapFields( "customerinternalid", "customerinternalid" , .T., .F., { "customerinternalid"	, 'C', aCustomerInternalId[1] , 0 },;
		aCustomerInternalId[2] )
	oSelf:AddMapFields( "customerid"		, "customerid"	, .T., .F., { "customerid"	, 'C', aCustomerId[1] , 0 }, aCustomerId[2] )
	oSelf:AddMapFields( "customername"		, "A1_NOME"		, .T., .F., { "A1_NOME"		, 'C', GetFieldLength("A1_NOME")	, 0 } )
	oSelf:AddMapFields( "clientid"			, "A1_COD"		, .T., .F., { "A1_COD"		, 'C', GetFieldLength("A1_COD")		, 0 } )
	oSelf:AddMapFields( "storeid"			, "A1_LOJA"		, .T., .F., { "A1_LOJA"		, 'C', GetFieldLength("A1_LOJA")	, 0 } )
	oSelf:AddMapFields( "sellerid"			, "C5_VEND1"	, .T., .F., { "C5_VEND1"	, 'C', GetFieldLength("C5_VEND1")	, 0 } )
	oSelf:AddMapFields( "freighttype"		, "C5_TPFRETE"	, .T., .F., { "C5_TPFRETE"	, 'C', GetFieldLength("C5_TPFRETE")	, 0 } )
	oSelf:AddMapFields( "freight"			, "C5_FRETE"	, .T., .F., { "C5_FRETE"	, 'N', GetFieldLength("C5_FRETE")	,;
		GetFieldDecimal("C5_FRETE") } )
	oSelf:AddMapFields( "insurance"			, "C5_SEGURO"	, .T., .F., { "C5_SEGURO"	, 'N', GetFieldLength("C5_SEGURO")	,;
		GetFieldDecimal("C5_SEGURO") } )
	oSelf:AddMapFields( "expenses"			, "C5_DESPESA"	, .T., .F., { "C5_DESPESA"	, 'N', GetFieldLength("C5_DESPESA"),;
		GetFieldDecimal("C5_DESPESA") } )
	oSelf:AddMapFields( "discount"			, "C5_DESC1"	, .T., .F., { "C5_DESC1"	, 'N', GetFieldLength("C5_DESC1"),;
		GetFieldDecimal("C5_DESC1") } )
	oSelf:AddMapFields( "currencyid"		, "C5_MOEDA"	, .T., .F., { "C5_MOEDA"	, 'N', GetFieldLength("C5_MOEDA"),;
		GetFieldDecimal("C5_MOEDA") } )
	oSelf:AddMapFields( "amount"			, "TOTAL"		, .T., .F., { "TOTAL"		, 'N', GetFieldLength("C6_VALOR"),;
		GetFieldDecimal("C6_VALOR") }, "TOTAL" )
	oSelf:AddMapFields( "typeofoperation", "C6_OPER"		, .T., .F., { "C6_OPER"		, 'N', GetFieldLength("C6_OPER"),;
		GetFieldDecimal("C6_OPER") }, "SC6OPER.C6_OPER" )
	oSelf:AddMapFields( "status"			, "STATUSPV"	, .T., .F., { "STATUSPV"	, 'C', 2, 0 }, GetStatus())
	oSelf:AddMapFields( "carriercode"		, "C5_TRANSP"	, .T., .F., { "C5_TRANSP"	, 'C', GetFieldLength("C5_TRANSP")	, 0 } )
	oSelf:AddMapFields( "releasetype"		, "C5_TIPLIB"	, .T., .F., { "C5_TIPLIB"	, 'C', GetFieldLength("C5_TIPLIB")	, 0 } )
	oSelf:AddMapFields( "carriername"		, "A4_NOME"		, .T., .F., { "A4_NOME"		, 'C', GetFieldLength("A4_NOME")	, 0 } )
	oSelf:AddMapFields( "sellername"		, "A3_NOME"		, .T., .F., { "A3_NOME"		, 'C', GetFieldLength("A3_NOME")	, 0 } )
	oSelf:AddMapFields( "selleremail"		, "A3_EMAIL"	, .T., .F., { "A3_EMAIL"	, 'C', GetFieldLength("A3_EMAIL")	, 0 } )
	oSelf:AddMapFields( "amounttaxes"		, "amounttaxes"	, .T., .F., { "amounttaxes"	, 'N', GetFieldLength("C6_VALOR")	,;
		GetFieldDecimal("C6_VALOR") }, "0")
	oSelf:AddMapFields( "notemessage"	, "C5_MENNOTA"   , .T., .F., { "C5_MENNOTA"	, 'C', GetFieldLength("C5_MENNOTA")	, 0 } )

	If canAddCommentField()
		oSelf:AddMapFields( "ordercomment", "C5_COMENT", .T., .F., { "C5_COMENT", 'C', GetFieldLength("C5_COMENT"), 0 } )
	EndIf

	aSize(aInternalId, 0)
	aSize(aCustomerId, 0)
	aSize(aCustomerInternalId, 0)
	aSize(aPaymentInternalId, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} addMapSC6
	Adiciona uma lista de campos dos itens do Pedidos de Venda (SC6).
	@type		function
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@param 		oSelf	, object	, Objeto herdado da classe FWAdapterBaseV2
/*/
//------------------------------------------------------------------------------
Function addMapSC6( oSelf as Object )
	Local aInternalId   := GetInternalId( { "C6_FILIAL", "C5_NUM", "C6_ITEM" } ) as Array
	Local aOrderId      := GetInternalId( { "C5_FILIAL", "C5_NUM" } ) as Array

	oSelf:AddMapFields( "internalid"		, "internalid"		, .T., .F., { "internalid"		, 'C', aInternalId[1]	, 0 }, aInternalId[2])
	oSelf:AddMapFields( "orderinternalid"	, "orderinternalid"	, .T., .F., { "orderinternalid"	, 'C', aOrderId[1]		, 0 }, aOrderId[2])
	oSelf:AddMapFields( "branchid"				, "C5_FILIAL"		, .T., .F., { "C5_FILIAL"		, 'C', GetFieldLength("C5_FILIAL")	, 0 }, "SC5.C5_FILIAL" )
	oSelf:AddMapFields( "orderid"				, "C5_NUM"			, .T., .T., { "C5_NUM"			, 'C', GetFieldLength("C5_NUM")		, 0 }, "SC5.C5_NUM" )
	oSelf:AddMapFields( "itemid"				, "C6_ITEM"			, .T., .F., { "C6_ITEM"			, 'C', GetFieldLength("C6_ITEM")	, 0 }, "SC6.C6_ITEM" )
	oSelf:AddMapFields( "productid"				, "C6_PRODUTO"		, .T., .F., { "C6_PRODUTO"		, 'C', GetFieldLength("C6_PRODUTO")	, 0 })
	oSelf:AddMapFields( "description"			, "C6_DESCRI"		, .T., .F., { "C6_DESCRI"		, 'C', GetFieldLength("C6_DESCRI")	, 0 })
	oSelf:AddMapFields( "quantity"				, "C6_QTDVEN"		, .T., .F., { "C6_QTDVEN"		, 'N', GetFieldLength("C6_QTDVEN")	,;
		GetFieldDecimal("C6_QTDVEN") } )
	oSelf:AddMapFields( "pricelist"				, "C6_PRUNIT"		, .T., .F., { "C6_PRUNIT"		, 'N', GetFieldLength("C6_PRUNIT")	,;
		GetFieldDecimal("C6_PRUNIT") } )
	oSelf:AddMapFields( "unitprice"				, "C6_PRCVEN"		, .T., .F., { "C6_PRCVEN"		, 'N', GetFieldLength("C6_PRCVEN")	,;
		GetFieldDecimal("C6_PRCVEN") } )
	oSelf:AddMapFields( "amount"				, "C6_VALOR"		, .T., .F., { "C6_VALOR"		, 'N', GetFieldLength("C6_VALOR")	,;
		GetFieldDecimal("C6_VALOR") } )
	oSelf:AddMapFields( "discount"				, "C6_DESCONT"		, .T., .F., { "C6_DESCONT"		, 'N', GetFieldLength("C6_DESCONT")	,;
		GetFieldDecimal("C6_DESCONT") } )
	oSelf:AddMapFields( "discountamount"		, "C6_VALDESC"		, .T., .F., { "C6_VALDESC"		, 'N', GetFieldLength("C6_VALDESC")	,;
		GetFieldDecimal("C6_VALDESC") } )
	oSelf:AddMapFields( "quantitydelivered"		, "C6_QTDENT"		, .T., .F., { "C6_QTDENT"		, 'N', GetFieldLength("C6_QTDENT")	,;
		GetFieldDecimal("C6_QTDENT") } )
	oSelf:AddMapFields( "blockstatus"			, "C6_BLQ"			, .T., .F., { "C6_BLQ"			, 'C', GetFieldLength("C6_BLQ")		, 0 } )
	oSelf:AddMapFields( "outflowtype"			, "C6_TES"			, .T., .F., { "C6_TES"			, 'C', GetFieldLength("C6_TES")		, 0 } )
	oSelf:AddMapFields( "fiscalcode"			, "C6_CF"			, .T., .F., { "C6_CF"			, 'C', GetFieldLength("C6_CF")		, 0 } )
	oSelf:AddMapFields( "customerorder"			, "C6_PEDCLI"		, .T., .F., { "C6_PEDCLI"	    , 'C', GetFieldLength("C6_PEDCLI")	, 0 } )
	oSelf:AddMapFields( "deleted"				, "deleted"			, .T., .F., { "deleted"			, 'C', 1, 0 }, "SC6.D_E_L_E_T_" )

	aSize(aOrderId, 0)
	aSize(aInternalId, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQuerySalesOrder
	Monta a Expressão SQL para consulta dos Pedidos de venda
	@type		function
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		19/10/2023
	@param		lItems		      , Logical	  , Adiciona uma junção com os itens do pedido de venda
	@param		lCustomers	      , Logical	  , Adiciona uma junção com o cliente do pedido de venda
	@param		lTotal		      , Logical	  , Adiciona uma junção com calculo total do Pedido
	@param		lCarriers	      , Logical	  , Adiciona uma junção com as transportadoras
	@param		lSellers	      , Logical	  , Adiciona uma junção com os vendedores
	@param		lConditions	      , Logical	  , Adiciona uma junção com as Condições de Pagamento
	@param		cWhereDiff	      , Character , Expressão SQL para busca de registros quando utilizando DIFF/SYNC
	@param      oTempTableCustomer, Object    , Objeto da tabela temporária criada e populada
	@param      cUser             , Character , Usuário da requisição
	@return		character, Expressao SQL para consulta de Pedidos ou Itens do Pedido
/*/
//------------------------------------------------------------------------------
Function GetQuerySalesOrder(lItems as Logical, lCustomers as Logical, lTotal as Logical, lCarriers as Logical, lSellers as Logical,;
	lConditions as Logical, cWhereDiff as Character, oTempTableCustomer as Object, cUser as Character ) as Character
	Local cQuery   := "" as Character
	
	cQuery += " SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("SC5") + " SC5 "

	If lItems
		cQuery += " INNER JOIN " + RetSqlName("SC6") + " SC6 ON "
		cQuery += " SC6.C6_NUM = SC5.C5_NUM AND SC6.C6_FILIAL = SC5.C5_FILIAL "
		cQuery += " AND SC6.D_E_L_E_T_ = ' ' "
	Endif

	If lCustomers
		cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 ON "
		cQuery += " A1_FILIAL = '" + FwXFilial("SA1") + "' AND C5_CLIENTE = A1_COD AND C5_LOJACLI = A1_LOJA AND SA1.D_E_L_E_T_ = ' ' "
	Endif

	If lConditions
		cQuery += " INNER JOIN " + RetSqlName("SE4") + " SE4 ON "
		cQuery += " E4_FILIAL = '" + FwXFilial("SE4") + "' AND C5_CONDPAG = E4_CODIGO AND SE4.D_E_L_E_T_ = ' ' "
	Endif

	If lCarriers
		cQuery += " LEFT JOIN " + RetSqlName("SA4") + " SA4 ON "
		cQuery += " SA4.A4_FILIAL = '" + FwXFilial("SA4") + "' AND SA4.A4_COD = SC5.C5_TRANSP AND SA4.D_E_L_E_T_ = ' ' "
	Endif

	If lSellers
		cQuery += " LEFT JOIN " + RetSqlName("SA3") + " SA3 ON "
		cQuery += " SA3.A3_FILIAL = '" + FwXFilial("SA3") + "' AND SC5.C5_VEND1 = SA3.A3_COD AND SA3.D_E_L_E_T_ = ' ' "
	Endif

	If lTotal

		cQuery += " INNER JOIN ( "
		cQuery += " SELECT SC6OPER.C6_FILIAL, SC6OPER.C6_NUM, SC6OPER.C6_OPER"
		cQuery += " FROM " + RetSqlName("SC6") + " SC6OPER "
		cQuery += " 	INNER JOIN ( "
		cQuery += " 		SELECT SC6MIN.C6_FILIAL, SC6MIN.C6_NUM, MIN(SC6MIN.C6_ITEM) C6_ITEM "
		cQuery += " 		FROM " + RetSqlName("SC6") + " SC6MIN "
		cQuery += "			WHERE SC6MIN.C6_FILIAL = '" + FwXFilial("SC6") + "' AND SC6MIN.D_E_L_E_T_ = ' ' "
		cQuery += "			GROUP BY SC6MIN.C6_FILIAL, SC6MIN.C6_NUM "
		cQuery += "		) SC6MIN"
		cQuery += " 	ON SC6OPER.C6_FILIAL = SC6MIN.C6_FILIAL AND SC6OPER.C6_NUM = SC6MIN.C6_NUM AND SC6OPER.C6_ITEM = SC6MIN.C6_ITEM "	
		cQuery += " WHERE "
		cQuery += " SC6OPER.D_E_L_E_T_ = ' ' "
		cQuery += " 	GROUP BY SC6OPER.C6_FILIAL, SC6OPER.C6_NUM, SC6OPER.C6_OPER	"
		cQuery += " ) SC6OPER "
		cQuery += " ON SC6OPER.C6_FILIAL = SC5.C5_FILIAL AND SC6OPER.C6_NUM = SC5.C5_NUM "
		cQuery += " INNER JOIN "
		cQuery += " ( SELECT SC6TOTAL.C6_FILIAL, SC6TOTAL.C6_NUM, SUM(SC6TOTAL.C6_VALOR) TOTAL "
		cQuery += "	FROM " + RetSqlName("SC6") + " SC6TOTAL "
		cQuery += "	WHERE SC6TOTAL.D_E_L_E_T_ = ' ' "
		cQuery += "	GROUP BY SC6TOTAL.C6_FILIAL, SC6TOTAL.C6_NUM "
		cQuery += ") SC6TOTAL "
		cQuery += "	ON SC6TOTAL.C6_FILIAL = SC5.C5_FILIAL "
		cQuery += "	   AND SC6TOTAL.C6_NUM = SC5.C5_NUM "

	Endif

	cQuery += " INNER JOIN " + oTempTableCustomer:GetTableNameForQuery() + " CLI_SC5 ON "
	cQuery += " SC5.C5_FILIAL = CLI_SC5.FILIAL AND SC5.C5_CLIENTE = CLI_SC5.C5_CLIENTE AND SC5.C5_LOJACLI = CLI_SC5.C5_LOJACLI "

	If (!Empty(cUser))
		cQuery += " INNER JOIN " + RetSQLName("AI3") + " AI3 ON "
		cQuery += " AI3.AI3_FILIAL = CLI_SC5.FILIAL AND AI3.AI3_LOGIN = '" + PadR(cUser, GetSX3Cache("AI3_LOGIN", "X3_TAMANHO")) + "' "
		cQuery += " AND AI3.D_E_L_E_T_ = ' ' "
		
		cQuery += " INNER JOIN " + RetSQLName("AI4") + " AI4 ON "
		cQuery += " AI4.AI4_FILIAL = AI3.AI3_FILIAL AND AI4.AI4_CODUSU = AI3.AI3_CODUSU AND AI4.AI4_CODCLI = SC5.C5_CLIENTE AND AI4.AI4_LOJCLI = SC5.C5_LOJACLI "
		cQuery += " AND AI4.D_E_L_E_T_ = ' ' "
	Endif

	cQuery += " WHERE #QueryWhere#"
Return cQuery

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetStatus
	Monta a expressão SQL utilizado para retorna o status do Pedido de Venda
	@type		function
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/10/2023
	@return		Character, Expressão CASE WHEN utilizado na consulta SQL.
/*/
//------------------------------------------------------------------------------
Static Function GetStatus() as Character
	Local cStatus 	as Character
	Local cNota		:= Space(GetFieldLength("C5_NOTA")) as Character
	Local cBql		:= Space(GetFieldLength("C5_BLQ")) as Character
	Local cLibOk	:= Space(GetFieldLength("C5_LIBEROK")) as Character

	cStatus := " CASE WHEN SC5.C5_LIBEROK = '" + cLibOk +"' AND SC5.C5_NOTA = '" + cNota + "' AND SC5.C5_BLQ = '" + cBql + "' THEN 'A' "
	cStatus += " ELSE CASE WHEN SC5.C5_NOTA <> '" + cNota + "' OR (SC5.C5_LIBEROK = 'E' AND SC5.C5_BLQ = '" + cBql + "') THEN 'E' "
	cStatus += " ELSE CASE WHEN SC5.C5_LIBEROK <> '" + cLibOk +"'  AND SC5.C5_NOTA = '" + cNota + "' AND SC5.C5_BLQ = '" + cBql + "' THEN 'L' "
	cStatus += " ELSE CASE WHEN SC5.C5_BLQ = '1' THEN 'BR' "
	cStatus += " ELSE CASE WHEN SC5.C5_BLQ = '2' THEN 'BV' "
	cStatus += " ELSE ' ' END END END END END "

Return cStatus

//------------------------------------------------------------------------------
/*/{Protheus.doc} canAddCommentField
	Verifica se o campo C5_COMENT foi criado no dicionario de dados.
	@type		function
	@since		04/11/2023
	@author		Lucas Panão / Squad CRM & Faturamento
	@version	12.1.2310
	@return		Logical	, Verifica se pode adicionar o campo de comentario do pedido.
/*/
//------------------------------------------------------------------------------
Static Function canAddCommentField() as Logical
	Local aArea		:= GetArea() as Array
	Local lReturn	:= .F. as Logical

	DbSelectArea("SC5")
	IF SC5->(ColumnPos("C5_COMENT")) > 0
    	lReturn := .T.
	Endif

	SC5->(DbCloseArea())

	RestArea(aArea)
	aSize(aArea, 0)
Return lReturn

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilterSC5
	Obtem o filtro por pedido de venda.
	@type		function
	@since		26/11/2023
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@param		cOrderId			, Character	, Número do pedido de venda
	@return		Character			, Expressão utilizada na consulta	
/*/
//------------------------------------------------------------------------------
Function GetFilterSC5( cOrderId as character ) as Character
	Local cWhere	as Character

	cWhere := " SC5.C5_FILIAL = '" + FwXFilial("SC5") + "'"

	If !Empty( cOrderId )
		cWhere += " AND SC5.C5_NUM = '" + cOrderId + "'"
	Endif

	cWhere += " AND SC5.C5_TIPO = 'N' "	
	cWhere += " AND SC5.D_E_L_E_T_ = ' ' "

Return cWhere

//---------------------------------------------------------------------------------------------
/*/{Protheus.doc} CreateTempTbCustomers
	Cria tabela temporária e a popula para realizar join para filtrar registros da tabela SC5.
	@type		function
	@since		23/10/2024
	@version	12.1.2410
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@param		aCustomers	      , Array     , Lista de cliente
	@param      cUser             , Character , Usuário da Requisição
	@return		oTempTableCustomer, Object , Objeto da tabela temporária criada e populada
/*/
//---------------------------------------------------------------------------------------------
Function CreateTempTbCustomers( aCustomers as Array, cUser as Character ) as Object

	Local cAlias             := ""                       as Character
	Local aFields            := {}                       as Array
	Local nCustomer          := 0                        as Numeric
	Local oTempTableCustomer := FwTemporaryTable():New() as Object

	aAdd(aFields, {"FILIAL"    , GetSX3Cache("C5_FILIAL" , "X3_TIPO"), GetSX3Cache("C5_FILIAL" , "X3_TAMANHO"), GetSX3Cache("C5_FILIAL" , "X3_DECIMAL")})
	aAdd(aFields, {"C5_CLIENTE", GetSX3Cache("C5_CLIENTE", "X3_TIPO"), GetSX3Cache("C5_CLIENTE", "X3_TAMANHO"), GetSX3Cache("C5_CLIENTE", "X3_DECIMAL")})
	aAdd(aFields, {"C5_LOJACLI", GetSX3Cache("C5_LOJACLI", "X3_TIPO"), GetSX3Cache("C5_LOJACLI", "X3_TAMANHO"), GetSX3Cache("C5_LOJACLI", "X3_DECIMAL")})
	aAdd(aFields, {"AI3_LOGIN" , GetSX3Cache("AI3_LOGIN" , "X3_TIPO"), GetSX3Cache("AI3_LOGIN" , "X3_TAMANHO"), GetSX3Cache("AI3_LOGIN" , "X3_DECIMAL")})

	oTempTableCustomer:SetFields(aFields)
	oTempTableCustomer:AddIndex("01", {"FILIAL", "C5_CLIENTE", "C5_LOJACLI"})
	oTempTableCustomer:AddIndex("02", {"FILIAL", "AI3_LOGIN" , "C5_CLIENTE", "C5_LOJACLI"})

	oTempTableCustomer:Create()

	cAlias := oTempTableCustomer:GetAlias()
	DbSelectArea(cAlias)

	For nCustomer := 1 to Len(aCustomers)
		RecLock(cAlias, .T.)
		(cAlias)->FILIAL  := FWXFilial("SC5")
		(cAlias)->AI3_LOGIN  := cUser
		(cAlias)->C5_CLIENTE := aCustomers[nCustomer]['customerId']
		(cAlias)->C5_LOJACLI := aCustomers[nCustomer]['storeId']
		(cAlias)->(MsUnLock())
	Next

	FwFreeObj(aFields)

Return oTempTableCustomer

//------------------------------------------------------------------------------
/*/{Protheus.doc} headerBuilderSC5
    Monta o cabeçalho do Pedido de venda
    Observação: O Pedido de venda na tabela SC5 deve ser previamente posicionado
    @type       function
    @version    12.1.2310
    @author     Lucas Panão / Squad CRM & Faturamento
    @since      09/11/2023
    @param      aHeader, array, Lista de campos do cabeçalho (Deve ser passado por referencia)
/*/
//------------------------------------------------------------------------------
Static Function headerBuilderSC5(aHeader as array)	
    aadd(aHeader, {"C5_TIPO"    , SC5->C5_TIPO   ,  Nil})
    aadd(aHeader, {"C5_CLIENTE" , SC5->C5_CLIENTE,  Nil})
    aadd(aHeader, {"C5_LOJACLI" , SC5->C5_LOJACLI,  Nil})
    aadd(aHeader, {"C5_CLIENT"  , SC5->C5_CLIENT ,  Nil})
    aadd(aHeader, {"C5_LOJAENT" , SC5->C5_LOJAENT,  Nil})
    aadd(aHeader, {"C5_TIPOCLI" , SC5->C5_TIPOCLI,  Nil})
    aadd(aHeader, {"C5_CONDPAG" , SC5->C5_CONDPAG,  Nil})
    aadd(aHeader, {"C5_DESC1"   , SC5->C5_DESC1  ,  Nil})
    aadd(aHeader, {"C5_DESC2"   , SC5->C5_DESC2  ,  Nil})
    aadd(aHeader, {"C5_DESC3"   , SC5->C5_DESC3  ,  Nil})
    aadd(aHeader, {"C5_DESC4"   , SC5->C5_DESC4  ,  Nil})
    aadd(aHeader, {"C5_MOEDA"   , SC5->C5_MOEDA  ,  Nil})
    aadd(aHeader, {"C5_FRETE"   , SC5->C5_FRETE  ,  Nil})
    aadd(aHeader, {"C5_SEGURO"  , SC5->C5_SEGURO ,  Nil})
    aadd(aHeader, {"C5_DESPESA" , SC5->C5_DESPESA,  Nil})

Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} lineBuilderSC6
    Monta os Itens do Pedido de venda
    @type       function
    @version    12.1.2310
    @author     Lucas Panão / Squad CRM & Faturamento
    @since      09/11/2023
    @param      cOrderId, character, Número do Pedido de venda
    @param      aItems, array, Array de itens do pedido de venda (Deve ser passando por referencia)
/*/
//------------------------------------------------------------------------------
Static Function lineBuilderSC6(cOrderId as character, aItems as Array)
	Local aItem         := {} as array
	Local cFilialSC6    := FwxFilial('SC6') as character
	dbSelectArea("SC6")
	SC6->(dbSetOrder(1)) // C6_FILIAL, C6_NUM, C6_ITEM, C6_PRODUTO
	If SC6->(DbSeek(cFilialSC6 + cOrderId ))
		While SC6->C6_FILIAL == cFilialSC6  .AND. SC6->C6_NUM == cOrderId
			aItem := {}
			aadd(aItem, {"C6_ITEM"      , SC6->C6_ITEM      ,   Nil})
			aadd(aItem, {"C6_PRODUTO"   , SC6->C6_PRODUTO   ,   Nil})
			aadd(aItem, {"C6_QTDVEN"    , SC6->C6_QTDVEN    ,   Nil})
			aadd(aItem, {"C6_PRCVEN"    , SC6->C6_PRCVEN    ,   Nil})
			aadd(aItem, {"C6_VALDESC"   , SC6->C6_VALDESC   ,   Nil})
			aadd(aItem, {"C6_TES"       , SC6->C6_TES       ,   Nil})
			aadd(aItems, aItem)
            SC6->(DbSkip())
		EndDo
	EndIf
	SC6->(dbCloseArea())
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusData::saveSalesOrders(jSalesOrder as Json ) as Logical
	Efetua a inclusão de um Pedido de Venda
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		14/11/2023
	@param		jSalesOrder		, json		, json do pedido de venda
	@return		logical, Se obteve sucesso na transação retorna verdadeiro.
/*/
//------------------------------------------------------------------------------
Method saveSalesOrders(jSalesOrder as Json) as Logical Class SalesOrdersProtheusData
	Local aArea			:= FwGetArea() as Array
	Local oSalesOrders	:= pcSalesOrdersBase():new() as Object
	Local lContinue		:= .T. as Logical
	Local nStatusCode	:= 201 as Numeric

	oSalesOrders:setOperation( INCLUI )
        If lContinue .And. oSalesOrders:fromExecAuto(jSalesOrder)
            If oSalesOrders:commitData()
				oSalesOrders:updateSalesOrderId()
				oRest:setStatusCode( nStatusCode )
				oRest:setResponse( getResponseSalesOrders( INCLUI ,oSalesOrders:cSalesOrderId, oSalesOrders:cClientId ) )
			Else
				SetRestFault(400, FWHttpEncode(oSalesOrders:getErrorMessage()) )
				lContinue := .F.
            Endif
        Endif

    oSalesOrders:Destroy()
    FreeObj(oSalesOrders)
	FwrestArea( aArea )
	aSize( aArea, 0)
Return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} getResponseSalesOrders
	Obtem uma resposta de sucesso de acordo com a operação informada.
	@type		function
	@version	12.1.2310  
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		09/11/2023
	@param		nOperation	, numeric, Operação (3)
	@param		cSalesOrderId, character, N. do Pedido de Vendas
	@param		cClientId , character	, N. do Cliente
	@return 	json, resposta de sucesso da requisição
/*/
//------------------------------------------------------------------------------
static function getResponseSalesOrders(nOperation as Numeric,  cSalesOrderId as Character, cClientId as Character) as json
	Local jResponse		:= JsonObject():New() as json
	Local jSalesOrders	:= JsonObject():New() as json
	Local aSalesOrders	:= {} as Array

	jResponse['messageSuccess'] := FWHttpEncode("Pedido de venda incluído com sucesso")
	jResponse['status'] := "OK"
	jSalesOrders['orderid'] := cSalesOrderId
	jSalesOrders['clientid'] := cClientId
	aAdd(aSalesOrders, jSalesOrders)
	jResponse['items'] := aClone(aSalesOrders)

	FreeObj(jSalesOrders)
	aSize(aSalesOrders, 0)
Return jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} RowToJson
	Converte o retorno da consulta SQL em JSON

	@sample		oSalesOrdersData:RowToJson( cAlias, aFields, .F.)
	@type		method
	@param		cAlias	, Character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio.
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		13/12/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------

METHOD RowToJson( cAlias as Character, aFields as Array , lEmpty as Logical) Class SalesOrdersProtheusData
    Local nLoop     as Integer
    Local cContent  as Character

    For nLoop := 1 to len( aFields )
        If lEmpty
            cContent := ''
        Else
            If aFields[nLoop, LABEL] == "paymentdesc"
                cContent := Posicione('SE4', 1, (cAlias)->C5_FILIAL + (cAlias)->C5_CONDPAG , 'E4_DESCRI')
			Else
				cContent := ::ValueToJson( aFields[nLoop, LABEL], (cAlias)->( &(aFields[nLoop, VALUE]) ))	
            Endif
        EndIf
		Self:oJsonObj:setProp( aFields[nLoop, LABEL], cContent, lEmpty  )
    Next
return Nil
