#include 'tlpp-core.th'

namespace pc.salesOrders
using namespace pc.util

static __instance as Object

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService
	Disponibiliza um serviço para visualizar, incluir 
	um pedido de venda ou visualizar um ou mais itens do pedido de venda
	@type		class
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
/*/
//------------------------------------------------------------------------------
Class SalesOrdersService
	Public Data jResponse as Json

	Public Method New() as Object
	Public Method GetInstance() as Object
	Public Method GetTotalSalesOrder() as Json
	Public Method GetItems() as Json
	Public Method GetHeaders() as Json
	Public Method saveSalesOrders()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService::New() as Object
	Disponibiliza uma nova intancia do serviço Pedidos de venda.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
	@return		object, cria uma nova instancia da classe SalesOrdersService
/*/
//------------------------------------------------------------------------------
Method New() as Object Class SalesOrdersService
	::jResponse := JsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService::GetInstance() as Object
	obtem uma instancia da classe SalesOrdersService
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
	@return		object, obtem uma instancia da classe SalesOrdersService
/*/
//------------------------------------------------------------------------------
Method GetInstance() as Object Class SalesOrdersService
	If (__instance == Nil)
		__instance := SalesOrdersService():New()
	Endif
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService::GetTotalSalesOrder(cOrderId as Character) as Json
	Retorna o total do Pedido de Venda especificado na URI.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
	@param		cOrderId	, Character , Código do Pedido de venda
	@return		json, obtem o total de um pedido de venda.
/*/
//------------------------------------------------------------------------------
Method GetTotalSalesOrder(cOrderId as Character) as Json Class SalesOrdersService
	Local oData := SalesOrdersDataFactory():GetData()
	oData:GetTotalSalesOrder( cOrderId )
	oData:DeActivate()
	FreeObj(oData)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService::GetItems(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, cItemId as Character,
 aURLFilter as Array, cFields as Character, cSort as Character) as Json
	Obtem uma lista ou um items do Pedidos de Venda especifico (SC6) , 
	respeitando os parametros informados. 
	@type		method
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		cOrderId	, Character , Código do Pedido de venda
	@param		cItemId		, Character , Código do Item
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param      cCustomer   , Character , Clientes do usuário da requisição
	@param      cUser       , Character , Login do usuário da requisição
	@return		json		, retorna uma lista de itens do pedido de vendas.
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method GetItems(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, cItemId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character, cCustomer as Character, cUser as Character ) as Json Class SalesOrdersService
	Local oData := SalesOrdersDataFactory():GetData()
	Local aCustomer as Array

	Default cUser := ""

	If !Empty(cItemId)
		oData:oJsonObj:lList := .F.
	Endif

	aCustomer := setCustomerInArray(cCustomer)

	oData:GetItems(nPage, nPageSize, cOrderId, cItemId, aURLFilter, cFields, cSort, aCustomer, cUser)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	EndIf

	oData:DeActivate()
	FreeObj(oData)
	aSize(aCustomer, 0)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersService::GetHeaders(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, ;
	aURLFilter as Array, cFields as Character, cSort as Character) as Json
	Obtem uma lista ou um Cabeçalhos de Pedidos de Venda especifico (SC5) , 
	respeitando os parametros informados. 
	@type		method
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		cOrderId	, Character , Código do Pedido de venda
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param		cCustomer	, Character	, Código do Cliente
	@param      cUser       , Character , Login do usuário da requisição
	@return		json		, retorna uma lista de cabeçalho de pedido de vendas.
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		01/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method GetHeaders(nPage as Numeric, nPageSize as Numeric, cOrderId as Character, aURLFilter as Array, cFields as Character,;
 cSort as Character, cCustomer as Character, cUser as Character) as Json Class SalesOrdersService
	Local oData := SalesOrdersDataFactory():GetData()
	Local aCustomer as Array

	Default cUser := ""

	If !Empty(cOrderId)
		oData:oJsonObj:lList := .F.
	Endif

	aCustomer := setCustomerInArray(cCustomer)

	oData:GetHeaders(nPage, nPageSize, cOrderId, aURLFilter, cFields, cSort, aCustomer, cUser)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	EndIf

	oData:DeActivate()
	FreeObj(oData)
	aSize(aCustomer, 0)
Return ::jResponse

//------------------------------------------------------------------------------
	/*/{Protheus.doc} SalesOrdersService::saveSalesOrders
	Efetua a gravação de um pedido de venda
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		14/11/2023
	@param		jSalesOrder		, json		, json do pedido de venda
/*/
//------------------------------------------------------------------------------
Method saveSalesOrders(jSalesOrder as Json) Class SalesOrdersService
	Local oData := SalesOrdersDataFactory():GetData()	
	oData:saveSalesOrders(jSalesOrder)
	oData:DeActivate()
	FreeObj(oData)
Return Nil
