#include "tlpp-core.th"
#include "tlpp-object.th"
#include 'fwlibversion.ch'

using namespace pc.util

Static lPCOrdFld := Nil

#DEFINE PROPERTY 1 
#DEFINE FIELD 2
#DEFINE INCLUI 3

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase
	Classe responsavel pela Inclusão de 
	um Pedido de Venda utilizando o padrão de dados do TOTVS Gestão de Vendas.
	@type class
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@see pcbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
Class pcSalesOrdersBase From pcbaseResourcesSalesManagement
	public data cSalesOrderId	as character
	public data cClientId	as character

	public method new() as object
	public method fromExecAuto() as logical
	public method commitData() as logical
	public method updateSalesOrderId()
	
	private method setHeader()
	private method setItems()
	private method setItem() as array
	
	protected method addMapFieldsHeader()
	protected method addMapFieldsItem()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::new() as Object
	Obtem uma nova instacia da classe pcSalesOrdersBase.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return object, objeto do pcSalesOrdersBase utilizando herança da classe pcbaseResourcesSalesManagement
	@see pcbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
method new() as Object Class pcSalesOrdersBase
	_Super:New( "SC5",  INCLUI)
	::oStatement := IIF(checkFWExecStat(), FwExecStatement():New( getQuery() ), FWPreparedStatement():New( getQuery() ))
	::oStatement:SetString(1,::cBranch)
return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::fromExecAuto(jSalesOrder as Json) as Logical
	Efetua a conversão dos dados do JSON recebido no padrão utilizado pelo 
	MSExecAuto da Rotina.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return Logical, se houve sucesso retorna verdadeiro (.T.)
/*/
//------------------------------------------------------------------------------
method fromExecAuto(jSalesOrder as Json) as Logical Class pcSalesOrdersBase
	Local aArea := FwGetArea() as Array
	Local lContinue := .T. as Logical
	Local cTypeOperation as Character
	
	::clean()
	::addMapFields()
	::setHeader(jSalesOrder, @cTypeOperation)
	::setItems(jSalesOrder, cTypeOperation)

	FwrestArea( aArea )
	aSize(aArea, 0)
return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::addMapFieldsHeader
	Adiciona no Hashmap de Cabeçalho todos os campos utilizados pelo TOTVS Gestão
	de Vendas no cabeçalho (SC5) do pedido de venda.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@see protected
/*/
//------------------------------------------------------------------------------
method addMapFieldsHeader() Class pcSalesOrdersBase
	::oHashFieldsHeader := tHashMap():New()
	::oHashFieldsHeader:Set("branchid"			, "C5_FILIAL")
	::oHashFieldsHeader:Set("orderid"			, "C5_NUM")
	::oHashFieldsHeader:Set("externalid"		, "C5_NUMEXT")
	::oHashFieldsHeader:Set("ordertype"			, "C5_TIPO")
	::oHashFieldsHeader:Set("pricelistid"		, "C5_TABELA")
	::oHashFieldsHeader:Set("paymentconditionid", "C5_CONDPAG")
	::oHashFieldsHeader:Set("issuedate"			, "C5_EMISSAO")
	::oHashFieldsHeader:Set("customerid"		, "C5_CLIENTE")
	::oHashFieldsHeader:Set("customerstore"		, "C5_LOJACLI")
	::oHashFieldsHeader:Set("carriercode"		, "C5_TRANSP")
	::oHashFieldsHeader:Set("sellerid"			, "C5_VEND1")
	::oHashFieldsHeader:Set("freighttype"		, "C5_TPFRETE")
	::oHashFieldsHeader:Set("freight"			, "C5_FRETE")
	::oHashFieldsHeader:Set("insurance"			, "C5_SEGURO")
	::oHashFieldsHeader:Set("expenses"			, "C5_DESPESA")
	::oHashFieldsHeader:Set("discount"			, "C5_DESC1")
	::oHashFieldsHeader:Set("currencyid"		, "C5_MOEDA")
	::oHashFieldsHeader:Set("releasetype"		, "C5_TIPLIB")
	::oHashFieldsHeader:Set("typeofoperation"	, "typeofoperation")
	::oHashFieldsHeader:Set("intermediaryid"	, "C5_CODA1U")
	::oHashFieldsHeader:Set("purchaserpresence"	, "C5_INDPRES")
	::oHashFieldsHeader:Set("notemessage"		, "C5_MENNOTA")
	::oHashFieldsHeader:Set("ordercomment"		, "C5_COMENT")

return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::addMapFieldsItem
	Adiciona no Hashmap de Items todos os campos utilizados pelo TOTVS Gestão de
	Vendas nos itens (SC6) do pedido de vendas.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@see protected
/*/
//------------------------------------------------------------------------------
method addMapFieldsItem() Class pcSalesOrdersBase
	::oHashFieldsItem := tHashMap():New()
	::oHashFieldsItem:Set("itemid"		, "C6_ITEM")
	::oHashFieldsItem:Set("productid"	, "C6_PRODUTO")
	::oHashFieldsItem:Set("description"	, "C6_DESCRI")
	::oHashFieldsItem:Set("quantity"	, "C6_QTDVEN")
	::oHashFieldsItem:Set("unitprice"	, "C6_PRCVEN")
	::oHashFieldsItem:Set("discount"	, "C6_DESCONT")
	::oHashFieldsItem:Set("pricelist"	, "C6_PRUNIT")
	::oHashFieldsItem:Set("customerorder","C6_PEDCLI")
	::oHashFieldsItem:Set("isDeleted"	, "isDeleted")

return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::setHeader
	Efetua a conversão dos campos informados no json do Pedido de Venda em uma
	estrutura de Cabeçalhos do pedido de venda utilizado no MSExecAuto da rotina MATA410.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param jSalesOrder, json, JSON contendo os dados do Pedido de Venda
	@param cTypeOperation, character, Tipo de operação (enviado por referencia).
/*/
//------------------------------------------------------------------------------
method setHeader(jSalesOrder as Json, cTypeOperation as Character) Class pcSalesOrdersBase
	Local aFieldsHeader as Array
	Local jHeader		as Json
	Local nField		as Integer
	Local cField		as Character
	Local xValue		as variant

	jHeader := jSalesOrder:GetJsonObject("header")
	aFieldsHeader := jHeader:GetNames()

	For nField := 1 To Len( aFieldsHeader )
		If ::oHashFieldsHeader:Get( aFieldsHeader[nField] , @cField )
			if cField == "C5_NUMEXT"
				::cExternalId := jHeader:GetJsonObject(aFieldsHeader[nField])
				aAdd(::aHeader, {"C5_NUMEXT", ::cExternalId, Nil})
			Else
				xValue := ConvertValue(jHeader:GetJsonObject(aFieldsHeader[nField]), GetSx3Cache(cField, "X3_TIPO"))
				if cField == "C5_CLIENTE"
					::cClientId := xValue
				Endif
				aAdd(::aHeader, {cField, xValue, Nil})
			Endif
		Endif
	Next

	If Empty(::cSalesOrderId)
		::SetOperation(INCLUI)
	Endif

	aAdd(::aHeader, {'C5_ORIGEM', 'PC.SALESORDER', Nil})


	::aHeader := FwVetByDic(::aHeader, "SC5", .F.)

	aSize(aFieldsHeader, 0)
	FreeObj(jHeader)
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::setItems
	Efetua a conversão dos campos informados no json do Pedido de Venda em uma
	estrutura de Items do pedido de venda utilizado no MSExecAuto da rotina MATA410.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param jSalesOrder, json, JSON contendo os dados do Pedido de Venda
	@param cTypeOperation, character, Tipo de operação do Pedido de Venda
/*/
//------------------------------------------------------------------------------
method setItems(jSalesOrder as Json, cTypeOperation as Character) Class pcSalesOrdersBase
	Local aItems		as Array
	Local aItem			as Array
	Local aItemsClone   as Array
	Local nItem			as Integer

	aItems := jSalesOrder:GetJsonObject("items")

	For nItem := 1 To Len(aItems)
		aItem := ::setItem(aItems[nItem], cTypeOperation )
		aAdd(::aItems, aClone(aItem))
		aSize(aItem, 0)
	Next

	aItemsClone := aClone(::aItems)
	::aItems := FwVetByDic(::aItems, "SC6", .T.)

	aSize(aItemsClone, 0)
	aSize(aItems, 0)
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::setItem(jItem as Json, cTypeOperation as Character ) as Array
	Efetua a conversão dos campos informados no json de Item Pedido de Venda em
	um array de um item.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return array, Item do Pedido de Venda
	@param jItem, json, JSON contendo o item
	@param cTypeOperation, character, Tipo de operação do Pedido de Venda
	@param jSalesOrder, json, JSON contendo os dados do Pedido de Venda
/*/
//------------------------------------------------------------------------------
method setItem(jItem as Json, cTypeOperation as Character) as Array Class pcSalesOrdersBase
	Local aArea			:= GetArea() as Array
	Local aFieldsItem 	as Array
	Local aItem			:= {} as Array
	Local cOrderItem	as Character
	Local cField		as Character
	Local lNewItem		as Logical
	Local nField		as Integer
	Local xValue		as variant	

	aFieldsItem := jItem:GetNames()
	aAdd(aItem, {"C6_OPER", cTypeOperation, Nil})

	For nField := 1 To Len(aFieldsItem)
		If ::oHashFieldsItem:Get( aFieldsItem[nField] , @cField )
			If cField == "C6_ITEM"
				lNewItem := .T.
				cOrderItem := jItem:GetJsonObject(aFieldsItem[nField])

				If lNewItem
					aAdd(aItem, {"C6_ITEM", cOrderItem, Nil})
				Endif
			Else
				xValue := ConvertValue(jItem:GetJsonObject(aFieldsItem[nField]), GetSx3Cache(cField, "X3_TIPO"))
				If cField != "C6_DESCONT" 
					aAdd(aItem, {cField, xValue, Nil})
				EndIf
			Endif
		Endif
	Next

	RestArea(aArea)

	aSize(aFieldsItem, 0)
	aSize(aArea, 0)
return aItem

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::commitData() as Logical
	Efetua a gravação de um Pedido de Venda.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return logical, Se transação ocorreu com sucesso retorna verdadeiro (.T.)
/*/
//------------------------------------------------------------------------------
method commitData() as Logical Class pcSalesOrdersBase
	Local aArea := FwGetArea() as Array
	Local aMsgErro	as Array
	Local lContinue	:= .T. as Logical
	Local nItem		as Integer
	Local cError    as character
	Local aPCOrdFld as Array

	private lMsErroAuto		:= .F. as Logical
	private lAutoErrNoFile	:= .T. as Logical

	If lContinue
		checkExistBlock()
		IF lPCOrdFld
			aPCOrdFld := ExecBlock("PCOrdFld", .F., .F., {::aHeader, ::aItems, INCLUI })
			::aHeader := IIF(ValType(aPCOrdFld[1]) == 'A', aPCOrdFld[1], ::aHeader )
			::aItems  := IIF(ValType(aPCOrdFld[2]) == 'A', aPCOrdFld[2], ::aItems  )
		EndIf

		MSExecAuto( {| x, y, z | MATA410( x, y, z ) }, ::aHeader, ::aItems, INCLUI )

		If lMsErroAuto
			aMsgErro := GetAutoGRLog()
			For nItem := 1 To Len( aMsgErro )
				cError := StrTran( StrTran( aMsgErro[nItem], "<", "" ), "-", "" ) + (" ")
				::setErrorMessage( cError, .F. )
			Next
			lContinue := .F.
			aSize(aMsgErro, 0)
		EndIf
	Endif
	
	FwrestArea( aArea )
	aSize(aArea, 0)
	aSize(::aItems, 0)
	aSize(::aHeader, 0)
return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Obtem uma Query utilizada para buscar Pedidos de vendas utilizado a
	Filial e Número Externo como chave.
	@type function
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param cBranch, character, Código da Filial
	@return character, Query no padrão FWPreparedStatement
	@see FWPreparedStatement
/*/
//------------------------------------------------------------------------------
Static Function getQuery() as Character
	Local cQuerySC5 as Character

	cQuerySC5 := " SELECT C5_NUM SALESID FROM " + RetSqlName("SC5") + " SC5 "
	cQuerySC5 += " WHERE SC5.C5_FILIAL = ? AND SC5.C5_NUMEXT = ? AND SC5.D_E_L_E_T_ = ' ' "
	
	cQuerySC5 := ChangeQuery(cQuerySC5)
return cQuerySC5

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} checkFWExecStat
	Função utilizada para validar a data da LIB para utilização da classe FWExecStatement
	@type       Function
	@return     logical, lCheckVerLib
	@author     Lucas Panão / Squad CRM & Faturamento
	@since      20/11/2023
	@version    12.1.2310	
/*/
//-------------------------------------------------------------------------------------
Static Function checkFWExecStat()
	Static lCheckVerLib := Nil

	If lCheckVerLib == Nil
		lCheckVerLib := FWLibVersion() >= "20211116"
	EndIf
Return lCheckVerLib

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} checkExistBlock
	Função utilizada para popular as variaveis referente aos pontos de entrada
	@type       Function
	@author     Lucas Panão / Squad CRM & Faturamento
	@since      20/11/2023
	@version    12.1.2310	
/*/
//-------------------------------------------------------------------------------------
Static Function checkExistBlock()
	If lPCOrdFld == Nil 
    	lPCOrdFld := ExistBlock("PCOrdFld")
	EndIf
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrdersBase::updateSalesOrderId
	Atualiza o código do Pedido de Venda
	@type			method
	@version		12.1.2310
	@author			Lucas Panão / Squad CRM & Faturamento
	@since			28/11/2023
/*/
//------------------------------------------------------------------------------
method updateSalesOrderId() Class pcSalesOrdersBase
	::cSalesOrderId := ::getSalesId()
return Nil
