#include "tlpp-core.th"

namespace pc.products
using namespace pc.util

#DEFINE LABEL 1
#DEFINE VALUE 2

Static __lPCPRDQRY := ExistBlock("PCPRDQRY")

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProductsProtheusData
	Efetua a consulta de produtos do Protheus.

	@type		Class
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Class ProductsProtheusData from FWAdapterBaseV2
	Public Data cPriceListId as Character
	Public Data cCustomerId as Character
	Public Data cStoreId as Character

	Public Method New() as Object
	Public Method Get()
	Public Method RowToJson()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProductsProtheusData
	Efetua a consulta de produtos do Protheus.

	@sample		ProductsProtheusData():New("GET", .T.)
	@type		method
	@param		cVerbo	, Character	, Verbo que será utilizado na requisição
	@param		lList	, Logical	, Se irá listar o Json
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method New() as Object Class ProductsProtheusData
	_Super:New("GET", .T.)
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} Get
	Efetua a consulta de Produtos.

	@sample		oProductData:Get(1, 10, {}, 'code, description', '-code', Nil)
	@type		method
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por página
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param		cPriceListId, Character	, Código da Tabela de Preço
	@param		cCustomerId	, Character	, Código do Cliente
	@param		cStoreId	, Character	, Loja do Cliente
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method Get(nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, cSort as Character,;
 cPriceListId as Character, cCustomerId as Character, cStoreId as Character ) class ProductsProtheusData
	Local aArea	:= GetArea()

	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQuerySB1(cPriceListId))
	::SetWhere( getFilterSB1( cCustomerId, cStoreId, cPriceListId ))
	::SetOrder( "B1_FILIAL, B1_COD" )

	If !Empty( cPriceListId )
		::cPriceListId := cPriceListId
		::cCustomerId := cCustomerId
		::cStoreId := cStoreId
	Endif

	AddMapFieldsSB1( self )

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf

	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	If ::Execute()
		::FillGetResponse()
	EndIf

	RestArea( aArea )
	aSize( aArea, 0 )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} RowToJson
	Converte o retorno da consulta SQL em JSON

	@sample		oProductData:RowToJson( cAlias, aFields, .F.)
	@type		method
	@param		cAlias	, Character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio.
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
METHOD RowToJson( cAlias as Character, aFields as Array , lEmpty as Logical) CLASS ProductsProtheusData
	Local nLoop		as Integer
	Local cContent	as Character

	For nLoop := 1 to Len( aFields )
		If lEmpty
			cContent := ''
		Else
			If !Empty(::cPriceListId) .And. aFields[nLoop, LABEL] == "price"
				cContent := ::ValueToJson( aFields[nLoop, LABEL], MaTabPrVen(::cPriceListId, (cAlias)->B1_COD, 1, ::cCustomerId, ::cStoreId) )
			Else
				cContent := ::ValueToJson( aFields[nLoop, LABEL], (cAlias)->( &(aFields[nLoop, VALUE]) )  )
			Endif
		EndIf
		Self:oJsonObj:setProp( aFields[nLoop, LABEL], cContent, lEmpty  )
	Next
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFieldsSB1
	Cria o Mapa de campos Protheus x API para os produtos.

	@sample		AddMapFieldsSB1( self, .F. )
	@type		Function
	@param		oSelf	, Object	, Objeto com herança da classe FWAdapterBaseV2
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Function AddMapFieldsSB1(oSelf as Object)
	Local aInternalId	:= GetInternalId( { "B1_FILIAL", "B1_COD" } )
	Local lFixedId		:= !Empty(oSelf:cPriceListId)

	oSelf:AddMapFields("internalid"	  , "internalid", .T., .F.,	{ "internalid"	, 'C', aInternalId[1], 0 }, aInternalId[2])
	oSelf:AddMapFields("branchid"	  , "B1_FILIAL" , .T., .F.,	{ "B1_FILIAL"	, 'C', GetFieldLength("B1_FILIAL") 	, 0 }, "SB1.B1_FILIAL")
	oSelf:AddMapFields("code"		  , "B1_COD" 	, .T., lFixedId,{ "B1_COD"	, 'C', GetFieldLength("B1_COD") 	, 0 }, "SB1.B1_COD")
	oSelf:AddMapFields("description"  , "B1_DESC"	, .T., .F.,	{ "B1_DESC"		, 'C', GetFieldLength("B1_DESC")	, 0 })
	oSelf:AddMapFields("type"		  , "B1_TIPO"	, .T., .F.,	{ "B1_TIPO"		, 'C', GetFieldLength("B1_TIPO")	, 0 })
	oSelf:AddMapFields("price"		  , "B1_PRV1"	, .T., .F.,	{ "B1_PRV1"		, 'N', GetFieldLength("B1_PRV1")	, GetFieldDecimal("B1_PRV1") })
	oSelf:AddMapFields("active"		  , "B1_ATIVO"	, .T., .F.,	{ "B1_ATIVO"	, 'C', GetFieldLength("B1_ATIVO")	, 0 })
	oSelf:AddMapFields("status"		  , "B1_MSBLQL"	, .T., .F.,	{ "B1_MSBLQL"	, 'C', GetFieldLength("B1_MSBLQL")	, 0 })
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} getFilterSB1
	Obtem o filtro da SB1 utilizada no TOTVS Gestão de Vendas

	@sample		getFilterSB1()
	@type		Function
	@param		cCliente			, Character	, Código do cliente
	@param		cLoja   			, Character	, Loja do cliente
	@param		cCodTab 			, Character	, Código do tabela de preco
	@return		Character			, Expressão utilizada na consulta
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Function getFilterSB1(cCliente as Character, cLoja as Character, cCodTab as Character) as Character
	Local cWhere as Character

	cWhere := " SB1.B1_FILIAL = '" + FwXFilial("SB1") + "'"

	If __lPCPRDQRY
		cWhere += ExecBlock("PCPRDQRY", .F., .F., {cCliente, cLoja, cCodTab })
	Endif

	cWhere += " AND B1_MSBLQL = '2' "
	cWhere += " AND SB1.D_E_L_E_T_ = ' ' "

return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQuerySB1
	Obtém a Query utilizada para buscar os produtos
	@type  Function
	@author Lucas Panão
	@since 27/11/2023
	@version 1.0
	@param cCodTab, character, Código da Tabela de Preços
	@return character, expressão SQL
	@example getQuerySB1()
/*/
//------------------------------------------------------------------------------
Function GetQuerySB1(cCodTab as character )
	Local cQuery as character

	cQuery := " SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName('SB1') + " SB1 "	

	If !Empty(cCodTab)
		cQuery += "INNER JOIN ( "
		cQuery += "			SELECT "
		cQuery += "				DA1_FILIAL, "
		cQuery += "				DA1_CODPRO, "
		cQuery += "				DA1_CODTAB, "
		cQuery += "				D_E_L_E_T_ "
		cQuery += "			FROM "
		cQuery += "				" + RetSqlName("DA1") + " DA1 "
		cQuery += "			GROUP BY "
		cQuery += "				DA1_FILIAL, "
		cQuery += "				DA1_CODPRO, "
		cQuery += "				DA1_CODTAB, "
		cQuery += "				D_E_L_E_T_ "
		cQuery += "			) DA1GROUP "
		cQuery += "		ON	DA1GROUP.DA1_FILIAL = '" + FwXFilial("DA1") + "'"
		cQuery += "			AND DA1GROUP.DA1_CODPRO = SB1.B1_COD "
		cQuery += "			AND DA1GROUP.DA1_CODTAB = '" + cCodTab + "'"
		cQuery += "			AND DA1GROUP.D_E_L_E_T_ = ' ' "
	Endif
	cQuery += " WHERE #QueryWhere#"
Return cQuery


