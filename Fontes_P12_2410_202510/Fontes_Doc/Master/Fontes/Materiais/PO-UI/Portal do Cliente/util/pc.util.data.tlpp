#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'pc.util.data.ch'

namespace pc.util

/*/{Protheus.doc} GetFieldLength
	Retorna o Tamanho do Campo

	@type		Function
	@sample 	GetFieldLength("C5_NUM")
	@param		cField		, Character	, Campo do SX3 (X3_CAMPO)
	@return		Character	, Tamanho do campo no SX3 (X3_TAMANHO)
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		03/11/2023
	@version	12.1.2310 ou Superior
/*/
//----------------------------------------------------------------------------
Function GetFieldLength(cField as Character) as Numeric
Return GetSx3Cache(cField, "X3_TAMANHO")

//----------------------------------------------------------------------------
/*/{Protheus.doc} GetFieldDecimal
	Retorna a Quantidade de Casas decimas de um campo

	@type		Function
	@sample		GetFieldDecimal("C5_NUM")
	@param		cField	, Character	, Campo do SX3 (X3_CAMPO)
	@return		Character,	Tamanho do campo no SX3 (X3_DECIMAL)
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		03/11/2023
	@version	12.1.2310 ou Superior
/*/
//----------------------------------------------------------------------------
Function GetFieldDecimal(cField as Character) as Numeric
Return GetSx3Cache(cField, "X3_DECIMAL")

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInternalId
	Obtem um Array com os Campos utilizados no InternalId convertido para o BD
	Utilizado na aplicação e o tamanho total dos campos.

	@type		Function
	@sample		GetInternalId({"A1_FILIAL", "A1_COD"})
	@param		aFields	, Array	, Array de Campos
	@return		Array	, Campos convertidos e Soma do Tamanho dos campos.
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		03/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Function GetInternalId(aFields as Array) as Array
	Local aInternalId	as Array
	Local nFields		as Numeric
	Local nLength		:= 0
	Local cFields		:= ""
	Local cConcate		:= IIF(AllTrim( Upper( TcGetDb() ) ) == "MSSQL","+","||")

	For nFields := 1 To Len(aFields)
		nLength += GetFieldLength(aFields[nFields])
		cFields += aFields[nFields] + IIF(Len(aFields) > nFields , cConcate,"")
	Next

	aInternalId := { nLength, cFields }

	aSize(aFields, 0)
Return aInternalId

//------------------------------------------------------------------------------
	/*/{Protheus.doc} SalesOrdersService::setCustomerInArray
	Insere os clientes que estão em Character dentro de um Array 
	@type		function
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		03/11/2023
	@param		cCustomer		, character		, clientes
/*/
//------------------------------------------------------------------------------
function setCustomerInArray(cCustomer as Character) as Array
	local aCustomerId := {} as Array
	Local cErro  	  := ''
	Local oJson		  as Json

    oJson := JsonObject():New()
    cErro := oJson:FromJson(cCustomer)
    If ! Empty(oJson:GetJsonObject('items'))
        aCustomerId := oJson:GetJsonObject('items')
    EndIf
Return aCustomerId

//----------------------------------------------------------------------------
/*/{Protheus.doc} ConvertValue
	Efetua a conversão de um valor para o valor fornecido no parametro type.
	@type function
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param xValue, variant, Valor que deve ser convertido
	@param cType, character, tipo do campo que será convertido
	@return variant, retorna o valor convertido no formato correto.
	@sample ConvertValue("1", "C")
/*/
//----------------------------------------------------------------------------
Function ConvertValue(xValue as variant, cType as Character) as variant
	DEFAULT xValue := ""
	cType := IIF(Empty(cType), "C", cType)

	If ValType(xValue) <> cType
		If cType == "D"
			xValue := ConvertDate(xValue)
		EndIf
	Endif
Return xValue

//----------------------------------------------------------------------------
/*/{Protheus.doc} ConvertDate
	Efetua a conversão da Data no formato yyyy-M-dd para o padrão Prothes.
	@type function
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 09/11/2023
	@param cDate, character, String no formato de data 'yyyy-M-dd
	@return date, retorna o string convertido no formato de data (DD/MM/AAAA).
	@sample ConvertDate("2020-3-1")
/*/
//----------------------------------------------------------------------------
Static Function ConvertDate(cDate as Character) as Date
	Local cYear		as Character
	Local cMonth	as Character
	Local cDay		as Character
	Local nPosMonth	as Numeric
	Local nPosDay	as Numeric
	Default cDate := ""

	nPosMonth := At( "-", cDate )
	nPosDay := At( "-", cDate, nPosMonth + 1 )
	cYear := Substr(cDate, 1, nPosMonth - 1)
	cMonth := StrZero( Val( Substr( cDate, nPosMonth + 1, ( nPosDay - 1 ) - nPosMonth )), 2)
	cDay := StrZero( Val( Substr( cDate, nPosDay + 1 )), 2)

Return SToD(cYear + cMonth + cDay )
