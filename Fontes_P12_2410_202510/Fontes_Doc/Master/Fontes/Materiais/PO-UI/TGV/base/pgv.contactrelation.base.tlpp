#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'FWMVCDEF.CH'
#include 'pgv.contactrelation.base.ch'

#DEFINE INCLUI 3
#DEFINE ALTERA 4
#DEFINE EXCLUI 5

#DEFINE FIELD 1
#DEFINE VALUE 2

#DEFINE CLASSERRORCODE        1
#DEFINE CLASSDESCRIPTIONERROR 2

namespace totvs.protheus.backoffice.contactrelation
using namespace tgv.util


//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationBase
	Classe responsavel pela gravação do Relação do Contato  utilizando o padrão de
	dados do Portal Gestão de vendas
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
/*/
//------------------------------------------------------------------------------
class pgvContactRelationsBase from tgvbaseResourcesSalesManagement

    public  data cBranch       as character  
    public  data cBranchentity as character 
    public  data cEntity       as character
    public  data cCodeEntity   as character
    public  data nCodeError    as numeric
    private data aCodeErrors   as array

    public method new() as object
    public method fromExecAuto() as logical
    public method commitData() as logical
    public method getDescriptionError() as character
    private method setHeader()
    private method setItems()
    private method setListErrors()
    private method getListErrors() as array

    protected method addMapFieldsHeader()
    protected method addMapFieldsItem()
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::new() as object
	Cria uma nova instancia da classe pgvContactRelationsBase
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
	@return self, object, instancia da classe pgvContactRelationsBase
/*/
//------------------------------------------------------------------------------
method new() as object class pgvContactRelationsBase
    _Super:New("AC8", MODEL_OPERATION_INSERT)
return self

//--------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::fromExecAuto(jContactRelation as json) as logical
	Cria a estrutura utilizada na gravação do filtro
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
	@param jContactRelation, json, Objeto com os dados a serem transformados no array para execauto
	@return lContinue, logical, indica se a montagem dos array ocorreu com sucesso (.T.) ou não (.F.)
/*/
//--------------------------------------------------------------------------------------------------------
method fromExecAuto(jContactRelation as json) as logical class pgvContactRelationsBase
    Local lContinue := .T. as logical
    ::clean()

    If ::nOperation == MODEL_OPERATION_DELETE
        aAdd(::aHeader, {"AC8_FILIAL", ::cBranch      , Nil})
        aAdd(::aHeader, {"AC8_FILENT", ::cBranchEntity, Nil})
        aAdd(::aHeader, {"AC8_ENTIDA", ::cEntity      , Nil})
        aAdd(::aHeader, {"AC8_CODENT", ::cCodeEntity  , Nil})
    Else
        If jContactRelation == Nil .OR. EMPTY(jContactRelation['items'])
			lContinue := .F.
		Else
            ::addMapFields()
            ::setHeader(jContactRelation)            
            ::setItems(jContactRelation)
        EndIf
    EndIf
return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::setHeader(jContactRelation as json) 
	Define um Cabeçalho considerando os campos mapeados
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
	@param jContactRelation, json, json com os dados do Relação do Contato 
/*/
//------------------------------------------------------------------------------
method setHeader(jContactRelation as json) class pgvContactRelationsBase
    Local aFieldsHeader as array
    Local jHeader as json
    Local cField  as character
    Local nField as numeric
    Local xValue as variant
    
    If jContactRelation['header'] <> NIL
        jHeader :=jContactRelation:getJsonObject("header")
        aFieldsHeader := jHeader:getNames()

        For nField := 1 To Len( aFieldsHeader )
            If ::oHashFieldsHeader:Get(aFieldsHeader[nField] , @cField)
                xValue := ConvertValue(jHeader:GetJsonObject(aFieldsHeader[nField]), GetSx3Cache(cField, "X3_TIPO"))
                aAdd(::aHeader, {cField, xValue, Nil})
            EndIf
        Next
        ::aHeader := FwVetByDic(::aHeader, "AC8", .F.)
        aSize(aFieldsHeader, 0)
	    FwFreeObj(jHeader)
    EndIf 
return nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::setItems(jContactRelation as json) 
	Define um Cabeçalho considerando os campos mapeados
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
	@param jContactRelation, json, json com os dados do Relação do Contato 
/*/
//------------------------------------------------------------------------------
method setItems(jContactRelation as json) class pgvContactRelationsBase
    Local aFieldsItems as array
    Local jItem        as json
    Local cField       as character
    Local nField       as numeric
    Local xValue       as variant
    Local aItem        as array
    Local nItem        as numeric
    Local aItemsClone  as array
    Local nIsDeleted   as numeric

    If jContactRelation['items'] <> NIL
        jItem :=jContactRelation:getJsonObject("items")
        For nItem := 1 To Len(jItem)
            aFieldsItems := jItem[nItem]:getNames()
            aItem := {}

            For nField := 1 To Len( aFieldsItems )
                If ::oHashFieldsItem:Get(aFieldsItems[nField] , @cField)
                    If cField == "isDeleted" .And. ::nOperation == ALTERA                    
                        xValue := ConvertValue(jItem[nItem]:GetJsonObject(aFieldsItems[nField]), "L")
                        aAdd(aItem, {"isDeleted", xValue, Nil})
                    Else
                        xValue := ConvertValue(jItem[nItem]:GetJsonObject(aFieldsItems[nField]), GetSx3Cache(cField, "X3_TIPO"))
                        aAdd(aItem, {cField, xValue, Nil})
                    EndIf
                EndIf
            Next
            aAdd(::aItems, aClone(aItem))
            FwFreeObj(aFieldsItems)
            FWFreeObj(aItem)
        Next

        aItemsClone := aClone(::aItems)
        ::aItems := FwVetByDic(::aItems, "AC8", .T.)

        If ::nOperation == ALTERA
            For nItem := 1 To Len( ::aItems )
                nIsDeleted := aScan(aItemsClone[nItem], {|aContact| aContact[1] = "isDeleted"})
                If nIsDeleted > 0
                    lIsDeleted := aItemsClone[nItem][nIsDeleted][2]
                    aAdd(::aItems[nItem], {"isDeleted", lIsDeleted, Nil})
                EndIf
            Next
        EndIf

        FwFreeObj(aItemsClone)
	    FwFreeObj(jItem)
    EndIf 
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::addMapFieldsHeader
	Adiciona um Mapa de campos utilizando os dados da (AC8)
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
/*/
//------------------------------------------------------------------------------
method addMapFieldsHeader() class pgvContactRelationsBase
    ::oHashFieldsHeader := tHashMap():New()
    ::oHashFieldsHeader:set("branch"      ,"AC8_FILIAL") 
    ::oHashFieldsHeader:set("branchentity","AC8_FILENT") 
    ::oHashFieldsHeader:set("entity"      ,"AC8_ENTIDA") 
    ::oHashFieldsHeader:set("codeentity"  ,"AC8_CODENT") 
    
return nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::addMapFieldsItem
	Adiciona um Mapa de campos utilizando os dados da (AC8)
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
/*/
//------------------------------------------------------------------------------
method addMapFieldsItem() class pgvContactRelationsBase
    ::oHashFieldsItem := tHashMap():New()
    ::oHashFieldsItem:set("code","AC8_CODCON") 
    ::oHashFieldsItem:set("isDeleted","isDeleted")
return nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::commitData() as logical
	Efetua a gravação do Relação do Contato 
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 01/12/2023
	@return lContinue, logical, retorna .T. em caso de sucesso
/*/
//------------------------------------------------------------------------------
method commitData() as logical class pgvContactRelationsBase
    Local aMsgErro   := {}   as array
    Local cError     := ''   as character
    Local lContinue  := .T.  as logical
    Local nField     := 1    as numeric
    Local nContact   := 1    as numeric
    Local oModel     := FWLoadModel('CRMA060') as object
    Local oModelAC8  as object
    Local oModelAC8Detail  as object
    Local nOperation as numeric
    Local nError     as numeric
    Local aContact   as array
    Local aArea      as array
    Local nPosFilial := aScan(::aHeader  , {|aField| aField[FIELD] == "AC8_FILIAL"}) as numeric
    Local nPosFilEnt := aScan(::aHeader  , {|aField| aField[FIELD] == "AC8_FILENT"}) as numeric
    Local nPosEntida := aScan(::aHeader  , {|aField| aField[FIELD] == "AC8_ENTIDA"}) as numeric
    Local nPosCodEnt := aScan(::aHeader  , {|aField| aField[FIELD] == "AC8_CODENT"}) as numeric
    Local nPosIsDel  as numeric
    Local cFilRelat  as character
    Local cFilEnt    as character
    Local cEntida    as character
    Local cCodEnt    as character
    Local cCodCon    as character
    Local lFoundAC8  as logical
    Local nLenItems  as numeric
    Local lIsDeleted as logical
    Local lFoundItem as logical
    
    aArea := FWGetArea()

    cFilRelat := PadR(::aHeader[nPosFilial][VALUE], GetSx3Cache(::aHeader[nPosFilial][FIELD], "X3_TAMANHO"))
    cFilEnt   := PadR(::aHeader[nPosFilEnt][VALUE], GetSx3Cache(::aHeader[nPosFilEnt][FIELD], "X3_TAMANHO"))
    cEntida   := PadR(::aHeader[nPosEntida][VALUE], GetSx3Cache(::aHeader[nPosEntida][FIELD], "X3_TAMANHO"))
    cCodEnt   := PadR(::aHeader[nPosCodEnt][VALUE], GetSx3Cache(::aHeader[nPosCodEnt][FIELD], "X3_TAMANHO"))

    AC8->(DbSetOrder(3))    // AC8_FILIAL+AC8_FILENT+AC8_ENTIDA+AC8_CODENT
    lFoundAC8 := AC8->(DbSeek(cFilRelat + cFilEnt + cEntida + cCodEnt))

    If ::nOperation == INCLUI
        If lFoundAC8
            ::setErrorMessage(STR0001, .f.)     // "Tentativa de inclusão de um Relacionamento de Cliente ou Prospect existente na base de dados."
            ::nCodeError := 1   
            lContinue := .F.
        EndIf
    Else
        If !lFoundAC8
            ::setErrorMessage(STR0002, .f.)     // "Relacionamento de Cliente ou Prospect inexistente na base de dados."
            ::nCodeError := 2
            lContinue := .F.
        EndIf
    EndIf

    If lContinue
        oModel:SetOperation( ::nOperation )
        If oModel:Activate()
            If oModel:IsActive()
                lContinue := .T.
                oModelAC8 := oModel:GetModel("AC8MASTER")
                oModelAC8Detail := oModel:GetModel("AC8CONTDET")
                nOperation := oModel:getOperation()

                // Preencher o modelo
                If nOperation <> MODEL_OPERATION_DELETE
                    // Cabecalho
                    For nField := 1 To Len(::aHeader)
                        If !oModelAC8:SetValue(::aHeader[nField, FIELD], ::aHeader[nField, VALUE])
                            lContinue := .F.
                            Exit
                        EndIf
                    Next

                    // Itens
                    If lContinue
                        nLenItems := Len(::aItems)
                        For nContact := 1 To nLenItems
                            aContact := ::aItems[nContact]
                            lIsDeleted := .F.

                            nPosCodCon := aScan(aContact, {|aField| aField[FIELD] == "AC8_CODCON"})
                            nPosIsDel  := aScan(aContact, {|aField| aField[FIELD] == "isDeleted" })
                            cCodCon    := PadR(aContact[nPosCodCon][VALUE], GetSx3Cache(aContact[nPosCodCon][FIELD], "X3_TAMANHO"))
                            
                            If nPosIsDel > 0
                                lIsDeleted := aContact[nPosIsDel][VALUE]
                            EndIf

                            If ::nOperation == ALTERA
                                lFoundItem := oModelAC8Detail:SeekLine({ ;
                                    {"AC8_FILIAL", cFilRelat}, ;
                                    {"AC8_CODCON", cCodCon}  , ;
                                    {"AC8_ENTIDA", cEntida}  , ;
                                    {"AC8_FILENT", cFilEnt}  , ;
                                    {"AC8_CODENT", cCodEnt}    ;
                                }) 

                                If lFoundItem 
                                    If lIsDeleted
                                        IIf(!oModelAC8Detail:DeleteLine(), (lContinue := .F.), .T.)
                                    EndIf
                                Else
                                    oModelAC8Detail:AddLine()
                                EndIf
                            Else
                                If nContact > oModelAC8Detail:Length()
                                    oModelAC8Detail:AddLine()
                                EndIf
                            EndIf

                            If !lIsDeleted
                                For nField := 1 To Len(aContact)
                                    If !(aContact[nField][FIELD] == "isDeleted") .And. !oModelAC8Detail:SetValue(aContact[nField][FIELD], aContact[nField][VALUE])
                                        lContinue := .F.
                                        Exit
                                    EndIf
                                Next
                            EndIf
                        Next
                    EndIf
                EndIf

                //Validacao e gravacao dos dados
                If !lContinue .Or. !( oModel:VldData() .And. oModel:CommitData() )
                    lContinue := .F.
                    cError := ""
                    aMsgErro := oModel:GetErrorMessage()
                    For nError := 1 To Len(aMsgErro)
                        If ValType(aMsgErro[nError]) == "C" .and. !Empty(aMsgErro[nError])
                            cError := convertStringToLine(StrTran( StrTran( aMsgErro[nError], "<", "" ), "-", "" ) + (" "), .f.)
                            ::setErrorMessage(cError, .f.)
                        EndIf
                    Next
                    aSize(aMsgErro, 0)
                Endif
            EndIf
            oModel:DeActivate()
            oModel:Destroy()
            FwFreeObj(oModelAC8Detail)
            FwFreeObj(oModelAC8)
        Endif
    EndIf
	FwFreeObj(oModel)
    FwRestArea(aArea)
    FwFreeObj(aArea)

return lContinue
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::setListErrors()
	Cria a lista com os codigos e descricoes de erros do processamento da classe
	@type method
	@version 12.1.2310
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 18/12/2023
	@return nil
/*/
//------------------------------------------------------------------------------
method setListErrors() class pgvContactRelationsBase
    ::aCodeErrors :=    { ;
                            {1, STR0003}, ;     // "Registro duplicado - Chave única"
                            {2, STR0004} ;      // "Registro inexitente na tentativa de exclusão ou alteração do registro"
                        }
return nil
//------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::getListErrors()
	Retorna a lista com os codigos e descricoes de erros do processamento da classe
	@type method
	@version 12.1.2310
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 18/12/2023
	@return ::aCodeError, array, Array com a lista de erros e seus respectivos códigos
/*/
//------------------------------------------------------------------------------------
method getListErrors() as array class pgvContactRelationsBase
    If Empty(::aCodeErrors)
        ::setListErrors()
    Endif
return ::aCodeErrors
//--------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelationsBase::getDescriptionError()
	Retorna a descrição do erro da classe referente ao código informado no parâmetro
	@type method
	@version 12.1.2310
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 18/12/2023
	@return cErrorDescription, character, descricao do erro referente ao codigo informado no parametro
/*/
//--------------------------------------------------------------------------------------------------------
method getDescriptionError() as character class pgvContactRelationsBase
    Local cErrorDescription := "" as character
    Local aListErrors       := {} as array
    Local nPosErrorInArray  := 0  as numeric

    aListErrors := ::getListErrors()
    
    nPosErrorInArray := aScan(aListErrors, {|aError| aError[CLASSERRORCODE] == ::nCodeError})
    If nPosErrorInArray > 0
        cErrorDescription := ::aCodeErrors[nPosErrorInArray][CLASSDESCRIPTIONERROR]
    EndIf

Return cErrorDescription
