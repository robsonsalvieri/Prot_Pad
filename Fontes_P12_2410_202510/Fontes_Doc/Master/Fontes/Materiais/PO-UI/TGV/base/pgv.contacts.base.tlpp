#include "tlpp-core.th"
#include "tlpp-object.th"
#include "pgv.contactsbase.ch"
#include 'fwlibversion.ch'

namespace totvs.protheus.backoffice.pgvContacts
using namespace tgv.util

Static lExistConvertStringToLine := FindFunction("tgv.util.convertStringToLine")

#DEFINE INCLUI 3
#DEFINE ALTERA 4
#DEFINE DELETE 5

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase
	Classe responsavel pela Inclusão, Alteração ou Exclusão de 
	um Contatos utilizando o padrão de dados do TOTVS Gestão de Vendas.
	@type class
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@see tgvbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
Class pgvContactsBase From tgvbaseResourcesSalesManagement
	public data cContactId	as character

	public method new() as object
	public method fromExecAuto() as logical
	public method commitData() as logical
	
	private method setHeader()
	private method setItemsPhone()
	private method setItemsAddress()
	private method setItemPhone() as array
	private method setItemAddress() as array
	
	protected method addMapFieldsHeader()
	protected method addMapFieldsItem()
	protected method addMapFieldsItem2()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::new() as Object
	Obtem uma nova instacia da classe pgvContactsBase.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@return object, objeto do pgvContactsBase utilizando herança da classe tgvbaseResourcesSalesManagement
	@see tgvbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
method new() as Object Class pgvContactsBase
	_Super:New("SU5",  INCLUI)
return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::fromExecAuto(jSalesOrder as Json) as Logical
	Efetua a conversão dos dados do JSON recebido no padrão utilizado pelo 
	MSExecAuto da Rotina.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@param jContact, Json, Objeto JSON com os dados do contatos
	@return Logical, se houve sucesso retorna verdadeiro (.T.)
/*/
//------------------------------------------------------------------------------
method fromExecAuto(jContact as Json) as Logical Class pgvContactsBase
	Local aArea := FwGetArea() as Array
	Local lContinue := .T. as Logical
	
	::clean()

	If ::nOperation == DELETE
		If Empty( ::cContactId )
			::setErrorMessage(STR0001)	//#"Contato não localizado"
			lContinue := .F.
		Else
			aAdd(::aHeader, {"U5_CODCONT", ::cContactId, Nil})
		Endif
	Else
		If jContact == Nil
			lContinue := .F.
		Else
			::addMapFields()
			::setHeader(jContact)
			::setItemsPhone(jContact)
			::setItemsAddress(jContact)
		Endif
	Endif

	FwrestArea( aArea )
	aSize(aArea, 0)
return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::addMapFieldsHeader
	Adiciona no Hashmap de Cabeçalho todos os campos utilizados pelo TOTVS Gestão
	de Vendas no cabeçalho (SU5) do cadastro de contatos.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@see protected
/*/
//------------------------------------------------------------------------------
method addMapFieldsHeader() Class pgvContactsBase
	::oHashFieldsHeader := tHashMap():New()
	::oHashFieldsHeader:Set("branchid"		, "U5_FILIAL")
	::oHashFieldsHeader:Set("code"			, "U5_CODCONT")
	::oHashFieldsHeader:Set("name"      	, "U5_CONTAT")
	::oHashFieldsHeader:Set("email"  		, "U5_EMAIL")
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::addMapFieldsItem
	Adiciona no Hashmap de Items todos os campos utilizados pelo TOTVS Gestão de
	Vendas nos endereços de um contato (AGB).
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 03/10/2023
	@see protected
/*/
//------------------------------------------------------------------------------
method addMapFieldsItem() Class pgvContactsBase
	::oHashFieldsItem := tHashMap():New()
	::oHashFieldsItem:Set("code"		, "AGA_CODIGO")
	::oHashFieldsItem:Set("addresstype" , "AGA_TIPO")
	::oHashFieldsItem:Set("address"     , "AGA_END")
	::oHashFieldsItem:Set("zipcode"   	, "AGA_CEP")
	::oHashFieldsItem:Set("neighborhood", "AGA_BAIRRO")
	::oHashFieldsItem:Set("citycode"    , "AGA_MUN")
	::oHashFieldsItem:Set("complement"  , "AGA_COMP")
	::oHashFieldsItem:Set("state"       , "AGA_EST")
	::oHashFieldsItem:Set("isDeleted"	, "isDeleted")
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::addMapFieldsItem2
	Adiciona no Hashmap de Items todos os campos utilizados pelo TOTVS Gestão de
	Vendas nos telefones de um contato (AGB).
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 03/10/2023
	@see protected
/*/
//------------------------------------------------------------------------------
method addMapFieldsItem2() Class pgvContactsBase
	::oHashFieldsItem2 := tHashMap():New()
	::oHashFieldsItem2:Set("code"		, "AGB_CODIGO")
	::oHashFieldsItem2:Set("phonetype"	, "AGB_TIPO")
	::oHashFieldsItem2:Set("ddd"	    , "AGB_DDD")
	::oHashFieldsItem2:Set("ddi"      	, "AGB_DDI")
	::oHashFieldsItem2:Set("phone"   	, "AGB_TELEFO")
	::oHashFieldsItem2:Set("complement"	, "AGB_COMP")
	::oHashFieldsItem2:Set("isDeleted"	, "isDeleted")
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::setHeader
	Efetua a conversão dos campos informados no json do Contato em uma
	estrutura de Cabeçalhos do cadastro de contatos utilizado no MSExecAuto da rotina TMKA070.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@param jContact, Json, JSON contendo os dados do Contato
/*/
//------------------------------------------------------------------------------
method setHeader(jContact as Json) Class pgvContactsBase
	Local aFieldsHeader as Array
	Local jHeader		as Json
	Local nField		as Integer
	Local cField		as Character
	Local xValue		as Variant

	jHeader := jContact:GetJsonObject("header")
	aFieldsHeader := jHeader:GetNames()

	For nField := 1 To Len( aFieldsHeader )
		If ::oHashFieldsHeader:Get( aFieldsHeader[nField] , @cField )
			xValue := ConvertValue(jHeader:GetJsonObject(aFieldsHeader[nField]), GetSx3Cache(cField, "X3_TIPO"))
			If cField == "U5_CODCONT"
				If Empty(xValue)
					::SetOperation(INCLUI)
					Loop
				Else
					::cContactId := PadR(xValue, GetSx3Cache(cField, "X3_TAMANHO"))
				EndIf
			EndIf

			If cField $ "U5_CODCONT|U5_FILIAL"
				xValue := PadR(xValue, GetSx3Cache(cField, "X3_TAMANHO"))
			EndIf

			aAdd(::aHeader, {cField, xValue, Nil})
		Endif
	Next

	If Empty(::cContactId)
		::SetOperation(INCLUI)
	Endif

	::aHeader := FwVetByDic(::aHeader, "SU5", .F.)

	aSize(aFieldsHeader, 0)
	FreeObj(jHeader)
return Nil

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::setItemsPhone
	Efetua a conversão dos campos informados no json do Cadastro de Contatos em uma
	estrutura de Items de Endereços utilizado no MSExecAuto da rotina TMKA070.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@param jContacts, json, JSON contendo os dados do Cadastro de Contatos
/*/
//-----------------------------------------------------------------------------------
method setItemsPhone(jContacts as Json) Class pgvContactsBase
	Local aItems		as Array
	Local aItem			as Array
	Local aItemsClone   as Array
	Local cBranchAGB	:= FwXFilial("AGB") as Character
	Local cAutDeleta	as Character
	Local cLinPos		as Character
	Local nAutdeleta	as Numeric
	Local nItem			as Integer
	Local nLinpos		as Numeric	
	
	aItems := jContacts:GetJsonObject("itemsPhone")

	For nItem := 1 To Len(aItems)
		aItem := ::setItemPhone(aItems[nItem], cBranchAGB)
		aAdd(::aItems2, aClone(aItem))
		aSize(aItem, 0)
	Next

	aItemsClone := aClone(::aItems2)
	::aItems2 := FwVetByDic(::aItems2, "AGB", .T.)

	If ::nOperation == ALTERA
		For nItem := 1 to Len(::aItems2)
			nLinpos := aScan(aItemsClone[nItem], {|x| x[1] == "LINPOS"})
			nAutdeleta := aScan(aItemsClone[nItem], {|x| x[1] == "AUTDELETA"})
			If nLinpos > 0 .And. nAutdeleta > 0
				cLinPos := aItemsClone[nItem, nLinpos, 3]
				cAutDeleta := aItemsClone[nItem, nAutdeleta, 2]
				
				aAdd(::aItems2[nItem], {"LINPOS"		, "AGB_CODIGO"	, cLinPos})
				aAdd(::aItems2[nItem], {"AUTDELETA"	, cAutDeleta, NIL})
			EndIf	
		Next
	EndIf

	aSize(aItemsClone, 0)
	aSize(aItems, 0)
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::setItemPhone(jItemPhone as Json, cBranchAGB as Character) as Array
	Efetua a conversão dos campos informados no json de Telefones do Cadastro de Contatos em
	um array de um item de telefones dos contatos.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@jItemPhone, Json, JSON com os dados do telefone
	@cBranchAGB, Character, Filial da tabela AGB
	@return array, Item do Telefone do Cadastro de Contatos
/*/
//------------------------------------------------------------------------------
method setItemPhone(jItemPhone as Json, cBranchAGB as Character) as Array Class pgvContactsBase
	Local aArea			    := FwGetArea() as Array
	Local aAreaAGB		    := AGB->(FwGetArea()) as Array
	Local aFieldsItem 	    as Array
	Local aItem			    := {} as Array
	Local cContactPhoneItem	as Character
	Local cField		    as Character
	Local lAutdeleta	    := .F. as Logical
	Local lNewItem		    as Logical
	Local nField		    as Integer
	Local xValue		    as variant	
	Local cCodEnt           as Character
	Local nTamCodigo        as Numeric

	aFieldsItem := jItemPhone:GetNames()
	nTamCodigo := GetSX3Cache('AGB_CODIGO', 'X3_TAMANHO')
	cCodEnt := PadR(::cContactId, GetSX3Cache('AGB_CODENT', 'X3_TAMANHO'))

	For nField := 1 To Len(aFieldsItem)
		If ::oHashFieldsItem2:Get( aFieldsItem[nField] , @cField )
			If cField == "AGB_CODIGO"
				lNewItem := .T.
				cContactPhoneItem := PadR(jItemPhone:GetJsonObject(aFieldsItem[nField]), nTamCodigo)
				If ::nOperation == ALTERA
					DbSelectArea("AGB")
					AGB->( DbSetOrder(1) ) // AGB_FILIAL+AGB_ENTIDA+AGB_CODENT+AGB_TIPO+AGB_PADRAO
					If AGB->(DbSeek(cBranchAGB + "SU5" + cCodEnt ))
						While(!AGB->(EOF()) .And. (AGB->AGB_FILIAL + AGB->AGB_ENTIDA + AGB->AGB_CODENT) == cBranchAGB + "SU5" + cCodEnt)
							If cContactPhoneItem == AGB->AGB_CODIGO
								lNewItem := .F.
								aAdd(aItem, {"LINPOS", "AGB_CODIGO", cContactPhoneItem})
								Exit
							EndIf
							AGB->(DbSkip())
						EndDo
					Endif
				Endif

				If lNewItem
					aAdd(aItem, {"AGB_CODIGO", cContactPhoneItem, Nil})
				Endif
			ElseIf cField == "isDeleted" .And. ::nOperation == ALTERA
				lAutdeleta := jItemPhone:GetJsonObject(aFieldsItem[nField])
			Else
				xValue := ConvertValue(jItemPhone:GetJsonObject(aFieldsItem[nField]), GetSx3Cache(cField, "X3_TIPO"))
				aAdd(aItem, {cField, xValue, Nil})
			Endif
		Endif
	Next

	If ::nOperation == ALTERA .And. !lNewItem
		If lAutdeleta
			aAdd(aItem, {"AUTDELETA", "S", Nil})
		Else
			aAdd(aItem, {"AUTDELETA", "N", Nil})
		EndIf
	EndIf

	FwRestArea(aAreaAGB)
	FwRestArea(aArea)

	aSize(aFieldsItem, 0)
	aSize(aArea, 0)
	aSize(aAreaAGB, 0)
return aItem

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::setItemsAddress
	Efetua a conversão dos campos informados no json do Cadastro de Contatos em uma
	estrutura de Items do Endereços utilizado no MSExecAuto da rotina TMKA070.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@param jContacts, json, JSON contendo os dados do Cadastro de Contatos
/*/
//-----------------------------------------------------------------------------------
method setItemsAddress(jContacts as Json) Class pgvContactsBase
	Local aItems		as Array
	Local aItem			as Array
	Local aItemsClone   as Array
	Local cBranchAGA	:= FwXFilial("AGA") as Character
	Local cAutDeleta	as Character
	Local cLinPos		as Character
	Local nAutdeleta	as Numeric
	Local nItem			as Integer
	Local nLinpos		as Numeric	
	
	aItems := jContacts:GetJsonObject("itemsAddress")

	For nItem := 1 To Len(aItems)
		aItem := ::setItemAddress(aItems[nItem], cBranchAGA)
		aAdd(::aItems, aClone(aItem))
		aSize(aItem, 0)
	Next

	aItemsClone := aClone(::aItems)
	::aItems := FwVetByDic(::aItems, "AGA", .T.)

	If ::nOperation == ALTERA
		For nItem := 1 to Len(::aItems)
			nLinpos := aScan(aItemsClone[nItem], {|x| x[1] == "LINPOS"})
			nAutdeleta := aScan(aItemsClone[nItem], {|x| x[1] == "AUTDELETA"})
			If nLinpos > 0 .And. nAutdeleta > 0
				cLinPos := aItemsClone[nItem, nLinpos, 3]
				cAutDeleta := aItemsClone[nItem, nAutdeleta, 2]
				
				aAdd(::aItems[nItem], {"LINPOS"		, "AGA_CODIGO"	, cLinPos})
				aAdd(::aItems[nItem], {"AUTDELETA"	, cAutDeleta, NIL})
			EndIf	
		Next
	EndIf

	aSize(aItemsClone, 0)
	aSize(aItems, 0)
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::setItemAddress(jItemAddress as Json, cBranchAGA as Character) as Array
	Efetua a conversão dos campos informados no json de Endereços do Cadastro de Contatos em
	um array de um item de endereços dos contatos.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 26/09/2023
	@jItemAddress, Json, JSON com os dados do endereço
	@cBranchAGA, Character, Filial da tabela AGA
	@return array, Item do Endereço do Cadastro de Contatos para utilizar no execauto
/*/
//------------------------------------------------------------------------------
method setItemAddress(jItemAddress as Json, cBranchAGA as Character) as Array Class pgvContactsBase
	Local aArea			    	:= FwGetArea() as Array
	Local aAreaAGA		    	:= AGA->(FwGetArea()) as Array
	Local aFieldsItem 	    	as Array
	Local aItem			    	:= {} as Array
	Local cContactAddressItem	as Character
	Local cField		    	as Character
	Local lAutdeleta	    	:= .F. as Logical
	Local lNewItem		    	as Logical
	Local nField		    	as Integer
	Local xValue		    	as variant	
	Local cCodEnt               as Character
	Local nTamCodigo            as Numeric

	aFieldsItem := jItemAddress:GetNames()
	nTamCodigo := GetSX3Cache('AGA_CODIGO', 'X3_TAMANHO')
	cCodEnt := PadR(::cContactId, GetSX3Cache('AGA_CODENT', 'X3_TAMANHO'))

	For nField := 1 To Len(aFieldsItem)
		If ::oHashFieldsItem:Get( aFieldsItem[nField] , @cField )
			If cField == "AGA_CODIGO"
				lNewItem := .T.
				cContactAddressItem := PadR(jItemAddress:GetJsonObject(aFieldsItem[nField]), nTamCodigo)
				If ::nOperation == ALTERA
					DbSelectArea("AGA")
					AGA->( DbSetOrder(1) ) // AGA_FILIAL+AGA_ENTIDA+AGA_CODENT+AGA_TIPO
					If AGA->(DbSeek(cBranchAGA + "SU5" + cCodEnt ))
						While(!AGA->(EOF()) .And. (AGA->AGA_FILIAL + AGA->AGA_ENTIDA + AGA->AGA_CODENT) == cBranchAGA + "SU5" + cCodEnt)
							If cContactAddressItem  == AGA->AGA_CODIGO
								lNewItem := .F.
								aAdd(aItem, {"LINPOS", "AGA_CODIGO", cContactAddressItem})
								Exit
							EndIf

							AGA->(DbSkip())
						EndDo
					Endif
				Endif

				If lNewItem
					aAdd(aItem, {"AGA_CODIGO", cContactAddressItem, Nil})
				Endif
			ElseIf cField == "isDeleted" .And. ::nOperation == ALTERA
				lAutdeleta := jItemAddress:GetJsonObject(aFieldsItem[nField])
			Else
				xValue := ConvertValue(jItemAddress:GetJsonObject(aFieldsItem[nField]), GetSx3Cache(cField, "X3_TIPO"))
				aAdd(aItem, {cField, xValue, Nil})
			Endif
		Endif
	Next

	If ::nOperation == ALTERA .And. !lNewItem
		If lAutdeleta
			aAdd(aItem, {"AUTDELETA", "S", Nil})
		Else
			aAdd(aItem, {"AUTDELETA", "N", Nil})
		EndIf
	EndIf

	FwRestArea(aAreaAGA)
	FwRestArea(aArea)

	aSize(aFieldsItem, 0)
	aSize(aArea, 0)
	aSize(aAreaAGA, 0)
return aItem

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactsBase::commitData() as Logical
	Efetua a gravação de um Contato, seus endereços e telefones.
	@type method
	@version 12.1.2210
	@author Rafael Mota Previdi / Squad CRM & Faturamento
	@since 03/10/2023
	@return logical, Se transação ocorreu com sucesso retorna verdadeiro (.T.)
/*/
//------------------------------------------------------------------------------
method commitData() as Logical Class pgvContactsBase
	Local aArea     := FwGetArea() as Array
	Local aAreaSU5  := SU5->(FwGetArea()) as Array
	Local aMsgErro	as Array
	Local lContinue	:= .T. as Logical
	Local nItem		as Integer
	Local cError    as character

	private lMsErroAuto		:= .F. as Logical
	private lAutoErrNoFile	:= .T. as Logical

	If ::nOperation <> INCLUI
		DbSelectArea("SU5")
		SU5->(DbSetOrder(1)) // U5_FILIAL+U5_CODCONT+U5_IDEXC
		If aScan(::aHeader, {|field| field[1] == "U5_CODCONT"}) <= 0 .Or. !SU5->( DbSeek( ::cBranch + ::cContactId ) )
			lContinue := .F.
			::setErrorMessage( STR0001, .T. )	//#"Contato não localizado"
		ElseIf ::nOperation == DELETE
			aAdd(::aHeader, {"U5_CONTAT", SU5->U5_CONTAT, Nil})
		Endif
	Endif

	If lContinue
		MSExecAuto( {| x, y, z, w | TMKA070( x, y, z, w ) }, ::aHeader, ::nOperation, ::aItems, ::aItems2 )

		If lMsErroAuto
			aMsgErro := GetAutoGRLog()
			
			For nItem := 1 To Len( aMsgErro )
				cError := StrTran( StrTran( aMsgErro[nItem], "<", "" ), "-", "" ) + (" ")
				If lExistConvertStringToLine
					cError := convertStringToLine(cError, .F.)
				EndIf
				::setErrorMessage( cError, .F. )
			Next
			lContinue := .F.
			aSize(aMsgErro, 0)
		Else
			If ::nOperation == INCLUI
				::cContactId := SU5->U5_CODCONT
			EndIf
		EndIf
	Endif
	
	FwRestArea(aAreaSU5)
	FwRestArea(aArea)

	aSize(aArea, 0)
	aSize(aAreaSU5, 0)
return lContinue
