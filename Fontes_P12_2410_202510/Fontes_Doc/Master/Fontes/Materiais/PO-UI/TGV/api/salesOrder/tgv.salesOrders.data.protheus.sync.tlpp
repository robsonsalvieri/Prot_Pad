#include 'tlpp-core.th'
#INCLUDE 'FWMVCDEF.CH'
#include "tlpp-rest.th"
#include "tgv.salesOrders.data.protheus.ch"

namespace tgv.salesOrders
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersProtheusDataSync
	Efetua a consulta dos Pedidos de Venda no Protheus.
	@type 		class
	@sample		SalesOrdersProtheusDataSync:New()
	@author		Squad CRM & Faturamento
	@since		04/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
CLASS SalesOrdersProtheusDataSync FROM FWAdapterBaseV2
	Public Data aCustomFields		as Array
	Public Data cCustomFields		as Character
	Public Data cSource				as Character
	Public Data isAdmin				as Logical
	Public Data lcanObfuscateField	as Logical
	Public Data lItems 				as Logical
	Public Data oHashFieldsPD		as Object
	Public Data nFields				as Numeric

	Public Method new() as Object
	Public Method getHeaders()
	Public Method getItems()
	Public Method rowToJson()
	Public Method addCustomFields()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Cria um nova instancia.
	@type 		Method
	@sample		SalesOrdersProtheusDataSync:New()
	@author		Danilo Salve
	@since		29/09/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object CLASS SalesOrdersProtheusDataSync
	_Super:New( "GET", .T. )
	::oHashFieldsPD := tHashMap():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetAllHeaders
	Retorna todos os cabeçalhos de pedidos de vendas (SC5)
	@type 		Method
	@sample		SalesOrdersProtheusDataSync:GetAll( 1, 10, '2020-09-30' )
	@param		nPage		, Numeric	, Numero da pagina
	@param		nPageSize	, Numeric	, Tamanho da Pagina
	@param		cData		, Character	, Data de sincronismo
	@author		Squad CRM & Faturamento
	@since		04/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetHeaders(nPage as Numeric, nPageSize as Numeric, cData as Character) CLASS SalesOrdersProtheusDataSync
	Local aArea			:= GetArea()                     as Array
	Local cQueryFields	                                 as Character
	Local cWhereDiff	                                 as Character
	Local cWhere		                                 as Character
	local isAdmin		:= FWIsAdmin()                   as Logical
	Local cSeller 	    := getSellerFromFilter(.F., .T.) as Character

	addMapSC5( self, .F. )
	::lItems := .F.
	::AddCustomFields()

	cWhere := GetFilterSC5(/*cOrderId*/, Empty(cData), cSeller, isAdmin, .f. )
	cWhere += " AND AQ4.AQ4_FILIAL = '" + FWXFilial("AQ4") + "'"
	cWhere += " AND AQ4.AQ4_VEND   = '" + cSeller + "'"
	cWhere += " AND AQ4.AQ4_SCHEMA = '000009'"
	cWhere += " AND AQ4.AQ4_DATA   = '"+DTOS(Date())+"'"
	cWhere += " AND AQ4.D_E_L_E_T_ = ' '"

	If !Empty(cData) .And. SubStr(cData,1,1) != '-'
		cWhereDiff := " WHERE SC5.STAMP > " + ConvertTimeStamp( cData, "value", "timestamp" )	
		cQueryFields := GetFieldsMap(Self:oJsonObj:AMAPFIELDS)
	EndIf

	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQuerySalesOrder(.F., .T., .T., .T., .T., .T., cWhereDiff, cQueryFields, .F. ) )
	::SetWhere( cWhere )
	::SetOrder( "C5_FILIAL, C5_NUM" )

	If ::Execute()
		::FillGetResponse()
	EndIf

	FTPDUnload()
	::oHashFieldsPD:Clean()

	aSize(::aCustomFields, 0)
	RestArea( aArea )
	aSize( aArea, 0 )
	FreeObj(::oHashFieldsPD)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetItems
	Retorna todo os Itens de Pedidos de vendas
	@type 		Method
	@sample		SalesOrdersProtheusDataSync:GetItems( 1, 10, '2020-09-30')
	@param		nPage		, Numeric	, Numero da pagina
	@param		nPageSize	, Numeric	, Tamanho da Pagina
	@param		cData		, Character	, Data de sincronismo
	@author		Squad CRM & Faturamento
	@since		04/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetItems(nPage as Numeric, nPageSize as Numeric, cData as Character ) CLASS SalesOrdersProtheusDataSync
	Local aArea	  := GetArea()                     as Array
	Local cWhere	                               as character
	local isAdmin := FWIsAdmin()                   as logical
	Local cSeller := getSellerFromFilter(.F., .T.) as Character

	cWhere := GetFilterSC5(/*cOrderId*/, Empty(cData), cSeller, isAdmin, .f. )
	cWhere += " AND AQ4.AQ4_FILIAL = '" + FWXFilial("AQ4") + "'"
	cWhere += " AND AQ4.AQ4_VEND   = '" + cSeller + "'"
	cWhere += " AND AQ4.AQ4_SCHEMA = '000010'"
	cWhere += " AND AQ4.AQ4_DATA   = '"+DTOS(Date())+"'"
	cWhere += " AND AQ4.D_E_L_E_T_ = ' '"

	If !Empty(cData) .And. SubStr(cData,1,1) != '-'
		cWhere += " AND SC6.S_T_A_M_P_ > " + ConvertTimeStamp( cData, "value", "timestamp" )
	EndIf

	addMapSC6( self , .F. )
	::lItems := .T.
	::AddCustomFields()
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQuerySalesOrder(.T., .F., .F., .F., .F., .F.,/*cFilterDiff*/, /*cQueryFields*/, .F. ))
	::SetWhere( cWhere )
	::SetOrder( "C6_FILIAL, C6_NUM, C6_ITEM, C6_PRODUTO" )

	If ::Execute()
		::FillGetResponse()
	EndIf

	FTPDUnload()
	::oHashFieldsPD:Clean()

	RestArea( aArea )
	aSize(::aCustomFields, 0)
	aSize( aArea, 0 )
	FreeObj(::oHashFieldsPD)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} RowToJson
	Converte o retorno consulta SQL em JSON
	@type 		Method
	@sample		SalesOrdersProtheusDataSync:RowToJson( cAlias, aFields, .F. )
	@param		cAlias	, Character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio.
	@author		Squad CRM & Faturamento
	@since		04/06/2021
	@version	12.1.27 ou Superior
/*/
//------------------------------------------------------------------------------
METHOD RowToJson( cAlias as Character, aFields as Array, lEmpty as Logical) CLASS SalesOrdersProtheusDataSync
	Default lEmpty := .F.
	ConvertResponseValues(aFields, cAlias, lEmpty, @Self )
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddCustomFields
	Metodo Auxiliar, deve carregar os Atributos utilizados na converso dos valores.
	@type 		Method
	@author		Squad CRM & Faturamento
	@since		04/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
METHOD AddCustomFields() CLASS SalesOrdersProtheusDataSync
	Local cFilialSC6 := FwxFilial("SC6") as Character
	Local cFilialSE4 := FwxFilial("SE4") as Character
	Local cFilialSA1 := FwxFilial("SA1") as Character

	Self:cSource:= "TGVSALESORDERS"
	Self:isAdmin := FwIsAdmin()
	Self:aCustomFields := {}
	Self:nFields := Len(Self:oJsonObj:aMapFields)

	If ::lItems
		aAdd(::aCustomFields, {"internalid"			, {||GetInternalIdQry( cFilialSC6 + (cAlias)->C5_NUM + (cAlias)->C6_ITEM )}})
		aAdd(::aCustomFields, {"orderinternalid"	, {||GetInternalIdQry( (cAlias)->C5_FILIAL + (cAlias)->C5_NUM )}})
		aAdd(::aCustomFields, {"externalitemid"		, {||GetInternalIdQry( (cAlias)->C5_NUMEXT + (cAlias)->C6_ITEM  )}})
		Self:cCustomFields := "internalid#orderinternalid#externalitemid"
	Else
		aAdd(::aCustomFields, {"internalid"				, {||GetInternalIdQry( (cAlias)->C5_FILIAL + (cAlias)->C5_NUM )}})
		aAdd(::aCustomFields, {"paymentinternalid"		, {||GetInternalIdQry( cFilialSE4 + (cAlias)->C5_CONDPAG )}})
		aAdd(::aCustomFields, {"customerinternalid"		, {||GetInternalIdQry( cFilialSA1 + (cAlias)->A1_COD + (cAlias)->A1_LOJA )}})
		aAdd(::aCustomFields, {"customerid"				, {||GetInternalIdQry( (cAlias)->A1_COD + (cAlias)->A1_LOJA )}})
		Self:cCustomFields := "internalid#paymentinternalid#customerinternalid#customerid"
	Endif

	If !Self:isAdmin
		Self:oHashFieldsPD := setProtectedFields(Self:oJsonObj:aMapFields, Self:cSource)
	Endif

	Self:lcanObfuscateField := CanAccessProtectedData(Self:isAdmin)
Return Nil
