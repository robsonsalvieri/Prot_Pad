#include "tlpp-core.th"

using namespace tgv.util
namespace tgv.contacts

static __instance as object
Static lExistFindMethod     := FindFunction("tgv.util.findMethod")
static lExistSaveContact

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService
	Disponibiliza um serviço de Contatos

	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class ContactsService
	Public Data jResponse 				as Json
	Public Method New() 				as Object
	Public Method getInstance() 		as Object
	Public Method GetContactsSync() 	as Json
	Public Method GetContacts() 		as Json
	Public Method GetAllContacts() 		as Json
	Public Method GetContactsAddress() 	as Json
	Public Method getContactsPhone() 	as Json
	Public Method saveContact()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:New
	Disponibiliza uma nova intancia do serviço Contatos.

	@sample		ContactsService:New()
	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class ContactsService
 	::jResponse := JsonObject():New()
return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:GetInstance
	Retorna uma instancia do serviço

	@sample		ContactsService:GetInstance()
	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object Class ContactsService
	If (__instance == NIL)
		__instance := ContactsService():New()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:GetContactsSync
	Efetua a consulta de Contatos

	@sample		ContactsService:GetInstance()
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		cDate		, Character , Data de Sincronismo/alteracao
	@return		Json  		, Reposta da Consulta/Requisição
	@author		Danilo Salve
	@since		20/01/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetContactsSync(nPage as Numeric, nPageSize as Numeric, cDate as Character) as Json Class ContactsService
	Local oContacts := ContactsDataFactory():getDataSync()

	oContacts:Get(nPage, nPageSize, cDate)

	If oContacts:lOk
		::jResponse := oContacts:getJSONResponse()
	Else
		::jResponse := SetRestFault(oContacts:GetCode(), oContacts:GetMessage())
	EndIf

	oContacts:DeActivate()
	FreeObj(oContacts)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:GetContacts
	Obtem os Cabeçalhos de Pedidos de Venda (SU5)

	@sample		oService:GetItems(1, 10, '000001', '000001', {}, '', '-orderid')
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param		cContactId	, Character	, Codigo do contato
	@author		Danilo Salve
	@since		27/07/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method GetContacts( nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character,;
 cSort as Character, cContactId as Character) as Json Class ContactsService
	Local oData := ContactsDataFactory():GetData()	

	If !Empty(cContactId)
		oData:oJsonObj:lList := .F.
	Endif

	oData:get(nPage, nPageSize, aURLFilter, cFields, cSort, cContactId)	

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	Else
		::jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
	EndIf

	oData:DeActivate()
	FreeObj(oData)
Return ::jResponse
//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:GetContactsAddress
	Obtem endereço dos contatos 

	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cContactId	, Character	, Codigo do contato
	@author		Gabriel Oliveira dos Santos
	@since		27/07/2021
	@version	12.1.27
	@return Json, Json com a lista dos endereços do contato desejado
/*/
//------------------------------------------------------------------------------
Method GetContactsAddress(	nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character,;
							cContactId as Character ) as Json Class ContactsService
	Local oData := ContactsProtheusData():New()

	If !Empty(cContactId) 
		::jResponse := oData:getContactsAddress( nPage, nPageSize, aURLFilter, cFields, cContactId )

		If oData:lOk
			::jResponse := oData:GetJSONResponse()
		Else
			::jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
		EndIf
	EndIf

	oData:DeActivate()
	FreeObj(oData)
return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:getContactsPhone
	Obtem telefone dos contatos 

	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cContactId	, Character	, Codigo do contato
	@author		Gabriel Oliveira dos Santos
	@since		27/07/2021
	@version	12.1.27
	@return Json, Json com a lista dos telefones do contato desejado
/*/
//------------------------------------------------------------------------------
Method getContactsPhone(	nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character,;
							cContactId as Character ) as Json Class ContactsService
	Local oData := ContactsProtheusData():New()

	If !Empty(cContactId) 
		oData:getContactsPhone( nPage, nPageSize, aURLFilter, cFields, cContactId )

		If oData:lOk
			::jResponse := oData:GetJSONResponse()
		Else
			::jResponse := SetRestFault(oData:GetCode(), oData:GetMessage())
		EndIf
	EndIf

	oData:DeActivate()
	FreeObj(oData)
return ::jResponse

//------------------------------------------------------------------------------
	/*/{Protheus.doc} ContactsService::saveContact
	Efetua a gravação de um contato
	@type		method
	@version	12.1.33
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		25/09/2023
	@param		jContact		, json		, json do contato
	@param		nOperation		, numeric	, operação (3, 4 ou 5)
	@param		cContactId		, character	, U5_CODCONT
/*/
//------------------------------------------------------------------------------
Method saveContact(jContact as Json, nOperation as Numeric, cContactId as Character) Class ContactsService
	Local oData := ContactsProtheusData():New()

	Default cContactId := "" 

	If (Empty( lExistSaveContact))
		lExistSaveContact := IIF(lExistFindMethod, findMethod(oData, "saveContact"), .F.)
	EndIf

	If lExistSaveContact
		oData:saveContact(jContact, nOperation, cContactId)
	EndIf

	oData:DeActivate()
	FreeObj(oData)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsService:GetAllContacts
	Obtem todos os contatos 

	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param      cEntBranch  , Character , Filial da entidade a ser utilizada para verificar os contatos relacionados
	@param      cEntity     , Character , Entidade a ser utilizada para verificar os contatos relacionados
	@param      cCodEntity  , Character , Código da entidade a ser utilizada para verificar os contatos relacionados
	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
	@return Json, Json com a lista de todos os contatos desejados
/*/
//------------------------------------------------------------------------------
Method GetAllContacts(	nPage      as Numeric  , ;
						nPageSize  as Numeric  , ;
						aURLFilter as Array    , ;
						cFields    as Character, ;
						cEntBranch as Character, ;
						cEntity    as Character, ;
						cCodEntity as Character ) as Json Class ContactsService
	Local oData := ContactsProtheusData():New()

	::jResponse := oData:getAllContacts( nPage, nPageSize, aURLFilter, cFields, cEntBranch, cEntity, cCodEntity )

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	EndIf

	oData:DeActivate()
	FreeObj(oData)

return ::jResponse

