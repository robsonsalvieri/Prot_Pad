#include 'tlpp-core.th'
#include "pgv.pdfCustom.controller.ch"

namespace totvs.protheus.backoffice.pgvPDFCustomBase
using namespace tgv.util
//-------------------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustom
	API do Cadastro de Configurações de PDFs Customizados de Pedido de Venda e Orcamentos.
	@type		Class
	@author		Squad CRM/Faturamento - Inovação
	@since		30/10/2024
	@version	12.1.2410 ou Superior
/*/
//-------------------------------------------------------------------------------------------
Class pdfCustom
    Data nPage            as Numeric
	Data nPageSize        as Numeric
    Data jResponse	      as Json
	Data oService 		  as Json

    Public Method new() as Object

    @Get("/api/pgv/pdfcustom/")
    Public Method getPdfCustomHeader() as Logical  

	@Post("/api/pgv/pdfcustom/")
	Public Method postPdfCustomHeader() as Logical

	@Get("/api/pgv/pdfcustom/:id/")
    Public Method getPdfCustomHeaderid() as Logical 

	@Delete("/api/pgv/pdfcustom/:id/") 
	Public Method deletePdfCustomHeaderid() as Logical

	@Put("/api/pgv/pdfcustom/:id/")
	Public Method putPdfCustomHeaderid() as Logical

	@Get("/api/pgv/pdfcustom/:id/sections/")  
    Public Method getPdfCustomsections() as Logical  

	@Get("/api/pgv/pdfcustom/:id/sections/:idsections")  
    Public Method getPdfCustomsectionsid() as Logical  

	@Get("/api/pgv/pdfcustom/:id/sections/:idsections/fields")
    Public Method getPdfCustomfields() as Logical  

	@Get("/api/pgv/pdfcustom/:id/sections/:idsections/fields/:idfields")  
    Public Method getPdfCustomfieldid() as Logical  

End Class

//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustom::new() as Object
	Cria uma nova instacia do objeto

	@type		method
	@sample     pdfCustom:New()
	@author		Squad CRM/Faturamento - Inovação
	@since		30/10/2024
	@version	12.1.2410
/*/
//------------------------------------------------------------------------------
Method new() as Object Class pdfCustom
    ::nPage		:= 1
	::nPageSize	:= 10
    ::jResponse	:= JsonObject():New()
	::oService  := pdfCustomService():GetInstance()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomHeader() as logical
	Obtem uma Lista das Configuracao de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		30/10/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method getPdfCustomHeader() as Logical Class pdfCustom
	
	Local aItems        := {}                            as Array
	Local aURLFilter    := getFilterParam()              as Array
	Local cFields       := getQueryParam('FIELDS')       as Character
	Local cSort         := getQueryParam('SORT')         as Character
	Local cType 		:= 'H'							 as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)

	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, , , , cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0001) )  // Não tem itens
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)

Return .T.
//------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:postPdfCustomHeader() as logical
	Realiza o post da Configuração de PDF Customizado juntamente com as seções 
	e os campos especificados no Body da Requisição.
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		30/10/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method postPdfCustomHeader() as Logical Class pdfCustom 
	Local jBody := GetRequestBody() as Json

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')

	If jBody <> Nil
		::oService:savePdfCustom(jBody, 3, /*cPdfCustomId*/)
		FWFreeObj(jBody)
	Endif
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomHeaderid() as logical
	Obtem uma Configuração de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		30/10/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method getPdfCustomHeaderid() as Logical Class pdfCustom 

	Local aItems        := {}                            as Array
	Local aURLFilter    := getFilterParam()              as Array
	Local cFields       := getQueryParam('FIELDS')       as Character
	Local cSort         := getQueryParam('SORT')         as Character
	Local cType 		:= 'H'							 as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)

	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, GetPathParams('id'), , , cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0002) ) //Não possui itens com o Código procurado
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustom:deletePdfCustomHeaderid() as Logical
	Efetua a exclusão de um Pdf Customizado.
	@type		method
	@version	12.1.2410 ou Superior
	@author		Squad CRM/Faturamento - Inovação
	@since		25/11/2024
/*/
//------------------------------------------------------------------------------
Method deletePdfCustomHeaderid() as Logical Class pdfCustom
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	::oService:savePdFCustom(Nil, 5, GetPathParams("id"))
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustom:putPdfCustomHeaderid() as Logical
	Efetua a alteração de um Pdf Customizado
	@type		method
	@version	12.1.2410 ou Superior
	@author		Squad CRM/Faturamento - Inovação
	@since		25/11/2024
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method putPdfCustomHeaderid() as Logical Class pdfCustom
	Local jBody := GetRequestBody() as Json

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If jBody <> Nil
		::oService:savePdFCustom(jBody, 4, GetPathParams("id"))
		FWFreeObj(jBody)
	EndIf
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomsections() as logical
	Obtem uma Lista de Seções de uma Configuracao de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		31/10/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method getPdfCustomsections() as Logical Class pdfCustom
	Local aURLFilter	:= getFilterParam()        as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT')   as Character
	Local aItems    	:= {}                      as Array
	Local cType			:= 'S'                     as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, GetPathParams('id'), , , cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0004) ) //Sessões não encontradas
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomsectionsid() as logical
	Obtem uma Configuração Específica de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		31/10/2024
	@version	12.1.2410
/*/
//------------------------------------------------------------------------------

Method getPdfCustomsectionsid() as Logical Class pdfCustom
	Local aURLFilter	:= getFilterParam()            as Array
	Local cFields		:= getQueryParam('FIELDS')     as Character
	Local cSort			:= getQueryParam('SORT')       as Character
	Local aItems        := {}                      	   as Array
	Local cType			:= 'S'                         as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, GetPathParams('id'), GetPathParams("idsections"), , cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0003) ) //Sessão não encontrada
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)
Return .T.

//------------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomfields() as logical
	Obtem uma Lista de Campos de uma Seção de uma Configuração de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		31/10/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------------
Method getPdfCustomfields() as Logical Class pdfCustom 
	Local aURLFilter	:= getFilterParam() 		   as Array
	Local cFields		:= getQueryParam('FIELDS')     as Character
	Local cSort			:= getQueryParam('SORT')       as Character
	Local aItems        := {}                      	   as Array
	Local cType			:= 'F'		         		   as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, GetPathParams('id'), GetPathParams("idsections"), , cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0005) ) //Campos não encontrados
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)
Return .T.

//----------------------------------------------------------------------------------
/*/{Protheus.doc}  pdfCustom:getPdfCustomfieldid() as logical
	Obtem um Campo Específico de uma Seção de uma Configuração de PDF Customizado
	@type		method
	@author		Squad CRM/Faturamento - Inovação
	@since		31/10/2024
	@version	12.1.2410 ou Superior
/*/
//----------------------------------------------------------------------------------
Method getPdfCustomfieldid() as Logical Class pdfCustom 
	Local aURLFilter	:= getFilterParam()            as Array
	Local cFields		:= getQueryParam('FIELDS')     as Character
	Local cSort			:= getQueryParam('SORT')       as Character
	Local aItems        := {}                      	   as Array
	Local cType 		:= 'F'                         as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getPdfCustom(::nPage, ::nPageSize, aURLFilter, cFields, cSort, GetPathParams("id"), GetPathParams("idsections"),GetPathParams("idfields"), cType))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0006)) //Campo não encontrado na sessão
	EndIf

	FWFreeObj(aURLFilter)
	FWFreeObj(aItems)
Return .T.
