#include 'tlpp-core.th'

namespace totvs.protheus.backoffice.pgvsalesgoals
using namespace tgv.util
static __instance as object
//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalService
	disponibiliza um classe de servicos da API salesgoals
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
/*/
//------------------------------------------------------------------------------
class salesgoalService
	data jResponse as Json

	public method new()                 as object
	public method getInstance()         as object
	public method getsalesgoals()       as json
    public method getsalesgoalsbydate() as json

EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalService::new() as object
	Disponibiliza uma nova intancia do servico salesgoals.
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
	@return object, nova instancia da classe salesgoalService
/*/
//------------------------------------------------------------------------------
method new() as object class salesgoalService
	::jResponse := jsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalService::getInstance() as object
	obtem uma instancia da classe salesgoalService
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
	@return object, instancia da classe salesgoalService
/*/
//------------------------------------------------------------------------------
method getInstance() as object class salesgoalService
	If (__instance == Nil)
		__instance := salesgoalService():new()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalService::getsalesgoals(nPage as Numeric, nPageSize as Numeric, cDate as Character) as json
	Obtem uma lista de salesgoals, seguindo o padrao de campos utilizados 
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
	@param nPage, numeric, Numero da pagina
	@param nPageSize, numeric, Quantidade de registros por pagina
	@param aURLFilter, array, filtros a serem aplicados nos dados a serem retornados
	@param cFields, character, atributos referente aos campos a serem retornados
	@param cSort, character, atributos para ordenacao dos dados retornados
	@return json, resposta da requisicao formatada em json
/*/
//------------------------------------------------------------------------------
method getsalesgoals(nPage as numeric  , nPageSize as numeric, aURLFilter as array, cFields as character, ;
					cSort as character) as json class salesgoalService
	local oSalesGoals := salesgoalsProtheusData():new()
	::jResponse := JsonObject():New()

	oSalesGoals:getByDate(nPage, nPageSize, aURLFilter, cFields, cSort)

	If oSalesGoals:lOk
		::jResponse := oSalesGoals:getJSONResponse()
	EndIf

	oSalesGoals:DeActivate()
	FreeObj(oSalesGoals)
Return ::jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalService::getsalesgoals(nPage as Numeric, nPageSize as Numeric, cDate as Character) as json
	Obtem uma lista de salesgoals, seguindo o padrao de campos utilizados 
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
	@param nPage, numeric, Numero da pagina
	@param nPageSize, numeric, Quantidade de registros por pagina
	@param aURLFilter, array, filtros a serem aplicados nos dados a serem retornados
	@param cFields, character, atributos referente aos campos a serem retornados
	@param cSort, character, atributos para ordenacao dos dados retornados
	@param dDataDe, date, Data de referente ao filtragem dos dados
	@param dDataAte, date, Data ate referente ao filtragem dos dados
	@return json, resposta da requisicao formatada em json
/*/
//------------------------------------------------------------------------------
method getsalesgoalsbydate(nPage as numeric  , nPageSize as numeric, aURLFilter as array, cFields as character, ;
					cSort as character, dDataDe as date, dDataAte as date) as json class salesgoalService
	local oSalesGoals := salesgoalsProtheusData():new()
	::jResponse := JsonObject():New()

	oSalesGoals:getByDate(nPage, nPageSize, aURLFilter, cFields, cSort, dDataDe, dDataAte)

	If oSalesGoals:lOk
		::jResponse := oSalesGoals:getJSONResponse()
	EndIf

	oSalesGoals:DeActivate()
	FreeObj(oSalesGoals)
Return ::jResponse

