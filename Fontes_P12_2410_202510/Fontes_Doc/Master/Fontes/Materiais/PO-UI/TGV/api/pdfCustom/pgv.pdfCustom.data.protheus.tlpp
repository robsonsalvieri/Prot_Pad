#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'FWMVCDEF.CH' 
#include "pgv.pdfCustom.data.ch"

namespace totvs.protheus.backoffice.pgvPDFCustomBase
using namespace tgv.util
//-------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvPdfCustomProtheusData
	Classe de acesso aos dados para manter e consultar as Configurações de PDF Customizado
	@type		Class
	@author		Squad CRM/Faturamento - Inovação
	@since		13/11/2024
	@version	12.1.2410 ou Superior
/*/
//-------------------------------------------------------------------------------------------
Class pgvPdfCustomProtheusData FROM FWAdapterBaseV2
    Public method new() as Object
    Public method setMapFields() 
	Public Method getData()
	Public method savePDFCustom()
EndClass
//-----------------------------------------------------------------------
/*/{Protheus.doc} pgvPdfCustomProtheusData:New() as object
    Obter uma nova instância da class pgvPdfCustomProtheusData
    @author Squad CRM & Faturamento
    @since 17/11/2023
    @version 12.1.2410
    @return @return object, retorna uma nova instancia da classe
/*/
//-----------------------------------------------------------------------
Method New() as object Class pgvPdfCustomProtheusData
	_Super:New("GET", .T.)
Return Self
//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvPdfCustomProtheusData:setMapFields
	Método para mapear os atributos x campos das tabelas a serem consultadas
	@author		Squad CRM & Faturamento - Inovação
	@param      cType, character, Indica se é Header (AQ6), Section (AQ7) ou Fields (AQ8)
	@since		25/11/2024
	@version	12.1.2410 ou Superior
/*/
//-----------------------------------------------------------------------------------------
Method setMapFields(cType as Character) Class pgvPdfCustomProtheusData
    Do Case
        Case cType == 'H'
            Self:AddMapFields("id",                "AQ6_ID"    ,   .T., .T., { "AQ6_ID"    , 'C', GetFieldLength('AQ6_ID')        , 0 })
            Self:AddMapFields("type",              "AQ6_TIPO"  ,   .T., .F., { "AQ6_TIPO"  , 'C', GetFieldLength('AQ6_TIPO')      , 0 })
            Self:AddMapFields("title",             "AQ6_TITULO",   .T., .F., { "AQ6_TITULO", 'C', GetFieldLength('AQ6_TITULO')    , 0 })
            Self:AddMapFields("description",       "AQ6_DESCRI",   .T., .F., { "AQ6_DESCRI", 'C', GetFieldLength('AQ6_DESCRI')    , 0 })
            Self:AddMapFields("url_image",         "AQ6_URLIMG",   .T., .F., { "AQ6_URLIMG", 'C', GetFieldLength('AQ6_URLIMG')    , 0 })
        Case cType == 'S'    
            Self:AddMapFields("idcfg",             "AQ7_IDCFG" ,   .F., .T., { "AQ7_IDCFG" , 'C', GetFieldLength('AQ7_IDCFG')     , 0 })
			Self:AddMapFields("id",        	       "AQ7_ID"    ,   .T., .T., { "AQ7_ID"    , 'C', GetFieldLength('AQ7_ID')        , 0 })
            Self:AddMapFields("order",             "AQ7_ORDEM" ,   .T., .F., { "AQ7_ORDEM" , 'N', GetFieldLength('AQ7_ORDEM')     , 0 })
            Self:AddMapFields("title",             "AQ7_TITULO",   .T., .F., { "AQ7_TITULO", 'C', GetFieldLength('AQ7_TITULO')    , 0 })
            Self:AddMapFields("description",       "AQ7_DESCRI",   .T., .F., { "AQ7_DESCRI", 'C', GetFieldLength('AQ7_DESCRI')    , 0 })
            Self:AddMapFields("type",              "AQ7_TIPO"  ,   .T., .F., { "AQ7_TIPO"  , 'C', GetFieldLength('AQ7_TIPO')      , 0 })
        Case cType == 'F'    
            Self:AddMapFields("idsect",            "AQ8_IDSECT",   .F., .T., { "AQ8_IDSECT", 'C', GetFieldLength('AQ8_IDSECT')    , 0 })
			Self:AddMapFields("idcfg",             "AQ8_IDCFG" ,   .F., .T., { "AQ8_IDCFG" , 'C', GetFieldLength('AQ8_IDCFG')     , 0 })
			Self:AddMapFields("id",        	       "AQ8_ID"    ,   .T., .T., { "AQ8_ID"    , 'C', GetFieldLength('AQ8_ID')        , 0 })
            Self:AddMapFields("title",             "AQ8_TITULO",   .T., .F., { "AQ8_TITULO", 'C', GetFieldLength('AQ8_TITULO')    , 0 })
            Self:AddMapFields("property",          "AQ8_PROPRI",   .T., .F., { "AQ8_PROPRI", 'C', GetFieldLength('AQ8_PROPRI')    , 0 })
            Self:AddMapFields("type",              "AQ8_TIPO"  ,   .T., .F., { "AQ8_TIPO"  , 'C', GetFieldLength('AQ8_TIPO')      , 0 })
            Self:AddMapFields("format",            "AQ8_FORMAT",   .T., .F., { "AQ8_FORMAT", 'C', GetFieldLength('AQ8_FORMAT')    , 0 })
            Self:AddMapFields("order",             "AQ8_ORDEM" ,   .T., .F., { "AQ8_ORDEM" , 'N', GetFieldLength('AQ8_ORDEM')     , 0 })
			Self:AddMapFields("decimal",           "AQ8_DECIMA",   .T., .F., { "AQ8_DECIMA", 'N', GetFieldLength('AQ8_DECIMA')    , 0 })
			Self:AddMapFields("thousandseparator", "AQ8_SEPMIL",   .T., .F., { "AQ8_SEPMIL", 'L', GetFieldLength('AQ8_SEPMIL')    , 0 })
    EndCase
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvPdfCustomProtheusData:getData( ;
                nPage as Numeric       , nPageSize as Numeric , aURLFilter as Array, ;
				cFields as Character   , cSort as Character   , cConfigPDFId as Character, ;
				cIdSection as Character, cFieldId as Character, cType as Character)
	Retorna os campos de uma entidado do PDF customizado
	@type	method
	@param nPage		 , Numeric   , Numero da pagina
	@param nPageSize	 , Numeric   , Tamanho da Pagina
	@param aURLFilter	 , Array     , Lista de Filtros no padrão do FWAdapterBaseV2
	@param cFields		 , Character , Lista de campos que devem ser retornados
	@param cSort		 , Character , Ordernação do Response
	@param cConfigPDFId , Character  , Id da Configuração de PDF
	@param cIdsection   , character  , Id da Seção da Configuração de PDF
	@param cFieldId     , character  , Id do Campo da Configuração de PDF
	@param cType        , character  , Tipo da entidade a ser consultada - H=AQ6, S=AQ7, F=AQ8
	@author		Squad CRM & Faturamento - Inovação
	@since		25/11/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method getData( nPage as Numeric       , nPageSize as Numeric , aURLFilter as Array, ;
				cFields as Character   , cSort as Character   , cConfigPDFId as Character, ;
				cIdSection as Character, cFieldId as Character, cType as Character) CLASS pgvPdfCustomProtheusData
	local aArea		:= GetArea() as Array
	local cWhere	as character
	local cQuery    as Character
	local cOrder    as Character

	cQuery := getQuery( cType )

	cWhere := getFilterPdfCustom( cConfigPDFId, cIdSection, cFieldId, cType )
	
	::setMapFields(cType)
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( cQuery )
	::SetWhere( cWhere )

	cOrder := getOrderPdfCustom(cType)
	::SetOrder(cOrder)

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf
	
	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	If ::Execute()
		::FillGetResponse()
	EndIf
	
	RestArea( aArea )
	FWFreeObj( aArea )
Return Nil

//-----------------------------------------------------------------------------------------------
/*/{Protheus.doc} getFilterPdfCustom
	Obtem a ordem dos intes de retorno da entidade consulta referente ao PDF Customizado
	@type		function
	@version	12.1.2410 ou Superior
	@author		Squad CRM & Faturamento - Inovação
	@since      25/11/2024
	@param      cType     , character  , Tipo da entidade a ser consultada - H=AQ6, S=AQ7, F=AQ8
	@return		Character , Expressão utilizada na ordenação dos itens do retorno da consulta
/*/
//-----------------------------------------------------------------------------------------------
Static Function getOrderPdfCustom( cType as Character) 
	Local cOrder	as Character

    Do Case
       Case cType == 'H'
			cOrder := "AQ6_FILIAL,AQ6_ID"
        Case cType == 'S'
            cOrder := "AQ7_FILIAL,AQ7_IDCFG,AQ7_ORDEM"
        Case cType == 'F'
            cOrder := "AQ8_FILIAL,AQ8_IDCFG,AQ8_IDSECT,AQ8_ORDEM"
    EndCase
Return cOrder

//------------------------------------------------------------------------------
/*/{Protheus.doc} getFilterPdfCustom
	Obtem os filtros de PDF Customizado
	@type		function
	@since      25/11/2024
	@version	12.1.2410 ou Superior
	@author		Squad CRM & Faturamento - Inovação
	@param      cConfigPDFId , Character  , Id da Configuração de PDF
	@param      cIdsection   , character  , Id da Seção da Configuração de PDF
	@param      cFieldId     , character  , Id do Campo da Configuração de PDF
	@param      cType        , character  , Tipo da entidade a ser consultada - H=AQ6, S=AQ7, F=AQ8
	@return		Character			, Expressão utilizada no filtro da consulta	
/*/
//------------------------------------------------------------------------------
Static Function getFilterPdfCustom( cConfigPDFId as character, cIdSection as Character , cFieldId as Character, cType as Character) 
	Local cWhere	as Character

   	Default cConfigPDFId := ''	
	Default cFieldId     := ''

    Do Case
       Case cType == 'H'
          	cWhere := "  AQ6.AQ6_FILIAL = '" + FwXFilial("AQ6") + "'"
            If !Empty( cConfigPDFId )
                cWhere += " AND AQ6.AQ6_ID = '" + cConfigPDFId + "'"
            Endif
            cWhere += " AND AQ6.D_E_L_E_T_ = ' ' "
        Case cType == 'S'
            cWhere := "  AQ7.AQ7_FILIAL = '" + FwXFilial("AQ7") + "'"
            If !Empty( cIdSection )
               cWhere += " AND AQ7.AQ7_ID = '" + cIdSection + "'"
            Endif   
			If !Empty( cConfigPDFId )
                cWhere += " AND AQ7.AQ7_IDCFG = '" + cConfigPDFId + "'"
            Endif         
            cWhere += " AND AQ7.D_E_L_E_T_ = ' ' "
        Case cType == 'F'
            cWhere := "  AQ8.AQ8_FILIAL = '" + FwXFilial("AQ8") + "'" 
            If !Empty( cConfigPDFId )
               cWhere += " AND AQ8.AQ8_IDCFG = '" + cConfigPDFId + "'"
            Endif   
			If !Empty( cIdSection )
                cWhere += " AND AQ8.AQ8_IDSECT = '" + cIdSection + "'"
            Endif 
			If !Empty( cFieldId )
                cWhere += " AND AQ8.AQ8_ID = '" + cFieldId + "'"
            Endif 
            cWhere += " AND AQ8.D_E_L_E_T_ = ' ' "
    EndCase

Return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	Obtem a query de PDF Customizado
	@type		function
	@since      25/11/2024
	@version	12.1.2410 ou Superior
	@author		Squad CRM & Faturamento - Inovação
	@param      cType     , character  , Tipo da entidade a ser consultada - H=AQ6, S=AQ7, F=AQ8
	@return		Character , Expressão utilizada na consulta	
/*/
//------------------------------------------------------------------------------
Static Function getQuery(  cType as Character) 
	Local cQuery	 := "" as Character
	Local cAlias     := "" as Character

    Do Case
       Case cType == 'H'
	   		cAlias := "AQ6"
        Case cType == 'S'
            cAlias := "AQ7"
        Case cType == 'F'
            cAlias := "AQ8"
    EndCase
	cQuery := " SELECT #QueryFields# FROM " + RetSqlName(cAlias) + " " + cAlias
	cQuery += " WHERE #QueryWhere#"

Return cQuery

//----------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvPdfCustomProtheusData:savePDFCustom
	Deve efetuar a gravação de uma Configuração de PDF Customizado utilizando o JSON com o corpo da configuração 
	e operação informada nos parâmetros
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param jPDFCustom, json, Configuração do PDF Customizado - corpo.
	@param nOperation, numeric, Operação (3 - Inclusão, 4 - Alteração ou 5 - Exclusão)
	@param cPDFCustomId, character, Código identificador (AQ6_ID) da Configuração
/*/
//----------------------------------------------------------------------------------------------------------------
method savePDFCustom(jPDFCustom as json, nOperation as integer, cPDFCustomId as character) class pgvPdfCustomProtheusData
	local aArea				:= FwGetArea()              as array
	local lContinue			:= .T.                      as logical
	local nStatusCode		:= 201                      as numeric
	local oPDFCustomBase	:= pgvpdfcustombase():new() as object
	local oJsonResponse     := JsonObject():New()       as object

	If oPDFCustomBase:setOperation( nOperation )
		If nOperation <> MODEL_OPERATION_INSERT
			dbSelectArea("AQ6")
			AQ6->(dbSetOrder(1)) // AQ6_FILIAL+AQ6_ID
			If !AQ6->(dbSeek( FwXFilial('AQ6') + cPDFCustomId ))
				If nOperation == MODEL_OPERATION_UPDATE
					nOperation := MODEL_OPERATION_INSERT
					oPDFCustomBase:setOperation(MODEL_OPERATION_INSERT)
				Else
					oPDFCustomBase:setErrorMessage(STR0001, .T. ) // "Nenhuma configuração localizada para o ID informado"
					lContinue := .F.
					nStatusCode := 404
				Endif
			Else
				oPDFCustomBase:cPDFCustomId := cPDFCustomId
				nStatusCode := 200
			Endif
		Endif

		If lContinue .And. oPDFCustomBase:fromExecAuto(jPDFCustom)
		    If oPDFCustomBase:commitData()
				oRest:setStatusCode( nStatusCode )
				oJsonResponse := getSuccessResponse(nOperation, oPDFCustomBase:cPDFCustomId)
				oRest:setResponse( FWHttpEncode(oJsonResponse:ToJson()) )
			Else
				nStatusCode := 400
				lContinue := .F.
            Endif
		Endif
	Endif

	If !lContinue
		oRest:setStatusCode( nStatusCode )
		oJsonResponse := getErrorResponse(nOperation, nStatusCode, oPDFCustomBase:getErrorMessage())
		oRest:setResponse( FWHttpEncode(oJsonResponse:ToJson()) )
	Endif

	oPDFCustomBase:Destroy()
    FwFreeObj(oPDFCustomBase)
	FwFreeObj(oJsonResponse)
	FwrestArea(aArea)
	FWFreeObj(aArea)
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} getSuccessResponse
	Obtem uma resposta de sucesso conforme operação informada
	@type function
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento
	@since 26/11/2024
	@param nOperation, numeric, Operação (3 - Inclusão, 4 - Alteração ou 5 - Exclusão)
	@param cPDFCustomId, character, Código identificador da Configuração (AQ6_ID) do PDF
	@return json, Objeto JSON com a messagem de sucesso formatada
/*/
//------------------------------------------------------------------------------
static function getSuccessResponse(nOperation as integer, cPDFCustomId as Character) as json
	local jResponse		:= jsonObject():New() as json
	local jPDFCustom	:= jsonObject():New() as json
	local aPDFCustom:= {} as array

	If nOperation == MODEL_OPERATION_INSERT
		jResponse['messageSuccess'] := STR0002 //"Configuração de PDF incluída com sucesso"
	Elseif nOperation == MODEL_OPERATION_UPDATE
		jResponse['messageSuccess'] := STR0003 // "Configuração de PDF alterada com sucesso." 
	Elseif nOperation == MODEL_OPERATION_DELETE
		jResponse['messageSuccess'] := STR0004 //"Configuração de PDF excluída com sucesso." 
	Endif

	jResponse['status'] := "OK"

	If nOperation <> MODEL_OPERATION_DELETE
		jPDFCustom['id'] := cPDFCustomId
		aAdd(aPDFCustom, jPDFCustom)
		jResponse['items'] := aClone(aPDFCustom)
	Endif

	FWFreeObj(jPDFCustom)
	FWFreeObj(aPDFCustom)
return jResponse

//------------------------------------------------------------------------------
/*/{Protheus.doc} getErrorResponse
	Obtem uma resposta de erro conforme operação informada
	@type function
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento
	@since 26/11/2024
	@param nOperation, numeric, Operação (3 - Inclusão, 4 - Alteração ou 5 - Exclusão)
	@param nCode, numeric, Código do Erro
	@param cError, character, Detalhe do erro
	@return json, Objeto JSON com o erro formatado
/*/
//------------------------------------------------------------------------------
static function getErrorResponse(nOperation as integer, nCode as integer, cError as character) as json
	local jResponse	:= jsonObject():New() as json

	jResponse['code'] := nCode
	jResponse['type'] := 'error'

	If nOperation == MODEL_OPERATION_INSERT
		jResponse['message'] := STR0005 //"Não foi possivel incluir a Configuração de PDF Customizada"
	Elseif nOperation == MODEL_OPERATION_UPDATE
		jResponse['message'] := STR0006 //"Não foi possivel alterar a Configuração de PDF Customizada"
	Elseif nOperation == MODEL_OPERATION_DELETE
		jResponse['message'] := STR0007 //"Não foi possivel excluir a Configuração de PDF Customizada"
	Endif

	jResponse['detailedMessage'] := cError

return jResponse

