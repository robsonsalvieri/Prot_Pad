#include "tlpp-core.th"
#include "tgv.contacts.controller.ch"

namespace tgv.contacts
using namespace tgv.util

Static lExistCheckDbUseArea := FindFunction("tgv.util.checkDbUseArea")
Static lExistFindMethod     := FindFunction("tgv.util.findMethod")
Static lExistSaveContact
Static lExistGetContactsAddress
Static lExistGetContactsPhone
Static lExistGetAllContacts

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts
	API para consulta de Contatos do TOTVS Gestão de Vendas
	@type		class
	@version	12.1.27
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		27/08/2021
/*/
//------------------------------------------------------------------------------
Class pgvContacts
	Data jResponse  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method new() as Object

	@Get("/api/tgv/contacts/sync")
	Public Method getContactsSync() as Logical

	@Get("/api/tgv/contacts/sync/diff/:dateSync")
	Public Method getContactsDiff() as Logical

	@Get("/api/tgv/contacts/")
	Public Method getContacts() as Logical

	@Get("/api/tgv/contacts/allcontacts/")
	Public Method getAllContacts() as Logical

	@Get("/api/tgv/contacts/byRelation/:entityBranch/:entity/:codEntity")
	Public Method getContactsByRelation() as Logical

	@Get("/api/tgv/contacts/:contactId")
	Public Method getContactsById() as Logical

	@Get("/api/tgv/contacts/address/:contactId")
	Public Method getContactAddress() as Logical
	
	@Get("/api/tgv/contacts/phone/:contactId")
	Public Method getContactPhone() as Logical

	@Post("/api/tgv/contacts/")
	Public Method postContact() as Logical

	@Put("/api/tgv/contacts/:contactId")
	Public Method putContact() as Logical

	@Delete("/api/tgv/contacts/:contactId")
	Public Method deleteContact() as Logical

EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::new() as object
	Método para nova instância da classe pgvContacts
	@type		method
	@since		01/10/2020
	@author		Danilo Salve / Squad CRM & Faturamento
	@version	12.1.27
	@return		object, Nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as Object Class pgvContacts
	::nPage     := 1
	::nPageSize := 10
	::oService  := ContactsService():getInstance()
	::jResponse := JsonObject():New()

	If (Empty( lExistSaveContact))
		lExistSaveContact := IIF(lExistFindMethod, findMethod(::oService, "saveContact"), .F.)
	EndIf

	If (Empty( lExistGetContactsAddress))
		lExistGetContactsAddress := IIF(lExistFindMethod, findMethod(::oService, "GetContactsAddress"), .F.)
	EndIf

	If (Empty( lExistGetContactsPhone))
		lExistGetContactsPhone := IIF(lExistFindMethod, findMethod(::oService, "GetContactsPhone"), .F.)
	EndIf

	If (Empty( lExistGetAllContacts ))
		lExistGetAllContacts := IIF(lExistFindMethod, findMethod(::oService, "GetAllContacts"), .F.)
	EndIf

return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactsSync() as logical
	Efetua o GET de todas os Contatos
	@type		method
	@since		01/10/2020
	@author		Danilo Salve / Squad CRM & Faturamento
	@version	12.1.27
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContactsSync() as Logical Class pgvContacts
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:GetContactsSync(::nPage, ::nPageSize, /*cData*/))
	AnswerRest(::jResponse)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactsDiff() as logical
	Efetua o GET de todas os Contatos alteradas/incluídas e não sincronizadas
	@type		method
	@since		01/10/2020
	@author		Danilo Salve / Squad CRM & Faturamento
	@version	12.1.27
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContactsDiff() as Logical Class pgvContacts
	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:GetContactsSync(::nPage, ::nPageSize, GetSyncDate()))
	AnswerRest(::jResponse)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContacts() as logical
	Efetua o GET de todas os Contatos
	@type		method
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		27/07/2021
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContacts() as Logical Class pgvContacts
	Local aItems		as Array
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:GetContacts(::nPage, ::nPageSize, aURLFilter, cFields, cSort))
	aItems := ::jResponse:GetJsonObject( "items" )

	If Empty( aURLFilter ) .Or. ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		AnswerRest( ::jResponse )
		aSize(aItems, 0)
	Else
		SetRestFault(404, STR0001 ) //"Contato nao localizado"
	Endif

	aSize(aURLFilter, 0)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactsById() as logical
	Obtem um contato especifico
	@type		method
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		28/07/2021
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContactsById() as Logical Class pgvContacts	
	Local cFields	:= getQueryParam('FIELDS') as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	
	::jResponse:fromJson(::oService:GetContacts(1, 1, /*aURLFilter*/, cFields, /*cSort*/,  GetPathParams("contactId")))

	If Empty(::jResponse["contactid"])
		SetRestFault(404, STR0001 ) //"Contato nao localizado"
	Else
		oRest:setResponse(::jResponse)
	Endif	
	
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::postContact() as Logical
	Efetua a inclusão de um Contato.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		25/09/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method postContact() as Logical Class pgvContacts
	Local jBody := GetRequestBody() as Json
	Local lExistCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')

	If lExistCheckDbUseArea .And. lExistSaveContact
		If jBody <> Nil
			::oService:saveContact(jBody, 3)
		Endif
	EndIf

	FreeObj(jBody)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::putContact() as Logical
	Efetua a alteração de um Contato.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		25/09/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method putContact() as Logical Class pgvContacts
	Local jBody := GetRequestBody() as Json
	Local lExistCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')

	If lExistCheckDbUseArea .And. lExistSaveContact
		If jBody <> Nil
			::oService:saveContact(jBody, 4,  GetPathParams("contactId"))
		Endif
	EndIf

	FreeObj(jBody)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::deleteContact() as Logical
	Efetua a exclusão de um Contato.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		25/09/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method deleteContact() as Logical Class pgvContacts
	Local lExistCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If lExistCheckDbUseArea .And. lExistSaveContact
		::oService:saveContact(/*jBody*/, 5,  GetPathParams("contactId"))
	EndIf
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactAddress() as logical
	Obtem os endereços de um contato especifico
	@type		method
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		27/09/2023
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContactAddress() as Logical Class pgvContacts	
	Local cFields	 := GetQueryParam('FIELDS') as Character
	Local aURLFilter := GetFilterParam()        as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	oRest:setKeyHeaderResponse('Content-Type','application/json')
	
	If lExistGetContactsAddress
		::jResponse:fromJson(::oService:GetContactsAddress(::nPage, ::nPageSize, aURLFilter, cFields, GetPathParams("contactId")))

		If Empty(::jResponse["items"])
			SetRestFault(404, STR0001 ) //"Contato nao localizado"
		Else
			oRest:setResponse(::jResponse)
		Endif
	EndIf

	aSize(aURLFilter, 0)
	
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactPhone() as logical
	Obtem os telefones de um contato especifico
	@type		method
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		27/09/2023
	@version	12.1.33
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getContactPhone() as Logical Class pgvContacts	
	Local cFields	 := GetQueryParam('FIELDS') as Character
	Local aURLFilter := GetFilterParam()        as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	oRest:setKeyHeaderResponse('Content-Type','application/json')
	
	If lExistGetContactsPhone
		::jResponse:fromJson(::oService:GetContactsPhone(::nPage, ::nPageSize, aURLFilter, cFields, GetPathParams("contactId")))

		If Empty(::jResponse["items"])
			SetRestFault(404, STR0001 ) //"Contato nao localizado"
		Else
			oRest:setResponse(::jResponse)
		Endif
	EndIf

	aSize(aURLFilter, 0)
	
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getAllContacts() as logical
	Obtem os dados de todos os contatos
	@type		method
	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getAllContacts() as Logical Class pgvContacts	
	Local cFields	 := GetQueryParam('FIELDS') as Character
	Local aURLFilter := GetFilterParam()        as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	oRest:setKeyHeaderResponse('Content-Type','application/json')
	
	If lExistGetAllContacts
		::jResponse:fromJson(::oService:GetAllContacts(::nPage, ::nPageSize, aURLFilter, cFields))

		If !Empty(::jResponse["items"])
			oRest:setResponse(::jResponse)
		Endif
	EndIf

	aSize(aURLFilter, 0)
	
return .T.

//------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContacts::getContactsByRelation() as logical
	Obtem os contatos relacionados ao registro da entidade informada no queryString
	@type		method
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@since		14/12/2023
	@version	12.1.2310
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------------
Method getContactsByRelation() as Logical Class pgvContacts	
	Local cFields	 := GetQueryParam('FIELDS')      as Character
	Local cEntBranch := GetPathParams('entityBranch') as Character
	Local cEntity    := GetPathParams('entity')       as Character
	Local cCodEntity := GetPathParams('codEntity')    as Character
	Local aURLFilter := GetFilterParam()             as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	oRest:setKeyHeaderResponse('Content-Type','application/json')
	
	If lExistGetAllContacts
		::jResponse:fromJson(::oService:GetAllContacts(::nPage, ::nPageSize, aURLFilter, cFields, cEntBranch, cEntity, cCodEntity))

		If !Empty(::jResponse["items"])
			oRest:setResponse(::jResponse)
		Endif
	EndIf

	aSize(aURLFilter, 0)
return .T.
