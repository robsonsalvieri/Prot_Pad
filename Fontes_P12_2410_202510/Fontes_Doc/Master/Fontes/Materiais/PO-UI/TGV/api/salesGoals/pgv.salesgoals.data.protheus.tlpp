#include 'tlpp-core.th'
#INCLUDE "FWLIBVERSION.CH"

namespace totvs.protheus.backoffice.pgvsalesgoals
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalsProtheusData
	Classe para a consulta dos salesgoals no Protheus.
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 16/11/2023
/*/
//------------------------------------------------------------------------------
class salesgoalsProtheusData from FWAdapterBaseV2
	public method new() 		as object
	public method getByDate()	as variant
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalsProtheusData::new() as Object
	obtem uma nova instancia da classe salesgoalsProtheusData
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 16/11/2023
	@return object, nova intancia da classe salesgoalsProtheusData
/*/
//------------------------------------------------------------------------------
method new() as object class salesgoalsProtheusData
	_Super:New( "GET", .T. )
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesgoalsProtheusData::getbydate
	Obtem uma Lista de salesgoalss
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 16/11/2023
	@param nPage, numeric, numero da pagina
	@param nPageSize, numeric, Quantidade de registros por pagina
	@param aURLFilter , array, Lista de Filtros no padrao do FWAdapterBaseV2
	@param cFields, character, Lista de campos que devem ser retornados
	@param cSort, character, Ordernacao do Response
	@param dDataDe, date, Data de referente ao filtragem dos dados
	@param dDataAte, date, Data ate referente ao filtragem dos dados
/*/
//------------------------------------------------------------------------------
method getbydate(	nPage as Numeric    , nPageSize as Numeric, aURLFilter as Array, ;
			cFields as Character, cSort as Character, dDataDe as date, dDataAte as date ) CLASS salesgoalsProtheusData
	local aArea 	 := GetArea()   as array
	local cSeller	                as character
	local isAdmin	 := FWIsAdmin() as logical

	AddMapFieldsSCT( self )

	If Empty(dDataDe) .Or. Empty(dDataAte)
		dDataDe  := SToD(AnoMes(DATE())+ "01")
		dDataAte := LastDate(DATE())
	EndIf

	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( GetQueryTGV("SCT") )

	If !Empty(aURLfilter) .AND. Len( aURLFilter ) > 0
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf

	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	::SetWhere( getsalesgoalsbydatefilter(isAdmin, @cSeller, .T., dDataDe, dDataAte))
	::SetOrder("SCT.CT_FILIAL, SCT.CT_DOC, SCT.CT_SEQUEN" )

	If canExecutewithUser(isAdmin, cSeller) .and. ::Execute()
		::FillGetResponse()
	EndIf

	RestArea( aArea )
	aSize( aArea, 0 )
return nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFieldsSCT
	Cria o Mapa de campos Protheus x API - SCT - salesgoalss
	@type function
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/11/2022
	@param oSelf, object, Objeto com heranca da classe FWAdapterBaseV2
/*/
//------------------------------------------------------------------------------
function AddMapFieldsSCT( oSelf as object )

	Local aInternalId  := GetInternalId( { "CT_FILIAL", "CT_DOC", "CT_SEQUEN" } )

	oSelf:AddMapFields( "internalid"		    , "internalid"  	, .T., .F., { "internalid", 'C', aInternalId[1]                 , 0 }, aInternalId[2])
	oSelf:AddMapFields( "branch"                , "CT_FILIAL" 	    , .T., .F., { "CT_FILIAL" , 'C', GetFieldLength("CT_FILIAL")	, 0 } )
	oSelf:AddMapFields( "doc"                   , "CT_DOC"	        , .T., .F., { "CT_DOC"    , 'C', GetFieldLength("CT_DOC")		, 0 } )
	oSelf:AddMapFields( "sequen"                , "CT_SEQUEN"       , .T., .F., { "CT_SEQUEN" , 'C', GetFieldLength("CT_SEQUEN")	, 0 } )
	oSelf:AddMapFields( "desc"                  , "CT_DESCRI"   	, .T., .F., { "CT_DESCRI" , 'C', GetFieldLength("CT_DESCRI")	, 0 } )
	oSelf:AddMapFields( "vendor"				, "CT_VEND"	    	, .T., .F., { "CT_VEND"	  , 'C', GetFieldLength("CT_VEND")	    , 0 } )
	oSelf:AddMapFields( "date"					, "CT_DATA"	    	, .T., .F., { "CT_DATA"	  , 'D', GetFieldLength("CT_DATA")	    , 0 } )
	oSelf:AddMapFields( "region"				, "CT_REGIAO"	    , .T., .F., { "CT_REGIAO" , 'C', GetFieldLength("CT_REGIAO")	, 0 } )
	oSelf:AddMapFields( "category"			    , "CT_CATEGO"	    , .T., .F., { "CT_CATEGO" , 'C', GetFieldLength("CT_CATEGO")	, 0 } )
	oSelf:AddMapFields( "type"				    , "CT_TIPO"		    , .T., .F., { "CT_TIPO"	  , 'C', GetFieldLength("CT_TIPO")		, 0 } )
	oSelf:AddMapFields( "group"				    , "CT_GRUPO"		, .T., .F., { "CT_GRUPO"  , 'C', GetFieldLength("CT_GRUPO")		, 0 } )
	oSelf:AddMapFields( "product"			    , "CT_PRODUTO"		, .T., .F., { "CT_PRODUTO" , 'C', GetFieldLength("CT_PRODUTO")	, 0 } )
	oSelf:AddMapFields( "quantity"				, "CT_QUANT"		, .T., .F., { "CT_QUANT"   , 'N', GetFieldLength("CT_QUANT")	, GetFieldDecimal("CT_QUANT") } )
	oSelf:AddMapFields( "value"					, "CT_VALOR"		, .T., .F., { "CT_VALOR"   , 'N', GetFieldLength("CT_VALOR")	, GetFieldDecimal("CT_VALOR") } )
	oSelf:AddMapFields( "currency"				, "CT_MOEDA"		, .T., .F., { "CT_MOEDA"   , 'N', GetFieldLength("CT_MOEDA")	, GetFieldDecimal("CT_MOEDA") } )
	oSelf:AddMapFields( "cost"					, "CT_CCUSTO"	    , .T., .F., { "CT_CCUSTO"  , 'C', GetFieldLength("CT_CCUSTO")	, 0 } )
	oSelf:AddMapFields( "item"				    , "CT_ITEMCC"		, .T., .F., { "CT_ITEMCC"  , 'C', GetFieldLength("CT_ITEMCC")	, 0 } )
	oSelf:AddMapFields( "class"			        , "CT_CLVL"	        , .T., .F., { "CT_CLVL"    , 'C', GetFieldLength("CT_CLVL")	    , 0 } )
	oSelf:AddMapFields( "status"				, "CT_MSBLQL"	    , .T., .F., { "CT_MSBLQL"  , 'C', GetFieldLength("CT_MSBLQL")	, 0 } )

return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} getsalesgoalsbydatefilter
	obtem o filtro utilizado para filtrar os salesgoalss (sem aURLFilter )
	@type function
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/01/2022
	@param isAdmin, logical, usuario e administrador do sistema?
	@param cDateSync, character, data de sincronismo
	@param cSeller, character, Codigo do Vendedor
	@param lShowError, logical, Exibir Erro quando vendedor nao e localizado
	@param dDataDe, date, Data de referente ao filtragem dos dados
	@param dDataAte, date, Data ate referente ao filtragem dos dados
	@return character, Expresscao SQL utilizado para filtrar um salesgoals
/*/
//------------------------------------------------------------------------------
function getsalesgoalsbydatefilter( isAdmin as logical, cSeller as character, lShowError as logical, dDataDe as date, dDataAte as date ) as character
	local cFilter 									as character

	cFilter := " SCT.CT_FILIAL = '" + FwXFilial("SCT") + "'"

	If !isAdmin
		If !Empty(cSeller := getSellerFromFilter(isAdmin, lShowError))
			cFilter += " AND SCT.CT_VEND = '" + cSeller + "' "
		EndIf
	EndIf
 	
    cFilter += " AND SCT.D_E_L_E_T_ = ' ' "
    cFilter += " AND SCT.CT_DATA BETWEEN '"+ dtos(dDataDe) +"' AND '"+ dtos(dDataAte) +"' "	

return cFilter


