#include 'tlpp-core.th'

namespace tgv.util
using namespace totvs.protheus.backoffice.pgvCustomFields

#DEFINE PROPERTY    1
#DEFINE FIELD       2
#DEFINE EXPORTJSON  3
#DEFINE FIXED       4
#DEFINE ASTRUCT     5
#DEFINE RENAME      6
#DEFINE SCHEMASC5 "000009"
#DEFINE SCHEMASC6 "000010"
#DEFINE SCHEMASCJ "000017"
#DEFINE SCHEMASCK "000018"

//----------------------------------------------------------------------------
/*/{Protheus.doc} loadCustomFieldsBySchema
  Função reponsavel por buscar todos os campos customizados dos pontos de entradas
  GVFLDC5 e GVFLDC6 que pode ser utilizado no AddMapFields do Schema informado.

  @param    cSchema   , Character, Código do Schema
  @return   Array     , Estrutura Lista de campos customizados do Schema.
  @author		Eduardo Paro
  @since		05/05/2023
  @version	12.1.2210 ou superior
/*/
//---------------------------------------------------------------------------
Function loadCustomFieldsBySchema( cSchema as Character ) as Array
	Local aArea             := GetArea()
	Local aAllFields        := {}
	Local aFields           := {}
  Local nY		            := 1
  Local oPGVCustomFields  := pgvCustomFields():New()

  Default cSchema := ''

  Do Case
    Case cSchema == SCHEMASC5
      aAllFields := oPGVCustomFields:getAFieldsSC5()
    Case cSchema == SCHEMASC6
      aAllFields := oPGVCustomFields:getAFieldsSC6()
    Case cSchema == SCHEMASCJ
      aAllFields := oPGVCustomFields:getAFieldsSCJ()
    Case cSchema == SCHEMASCK
      aAllFields := oPGVCustomFields:getAFieldsSCK()
  EndCase

  For nY := 1 To Len(aAllFields)
	  If oPGVCustomFields:isValidField( aAllFields[nY], cSchema)
      aAdd( aFields, {;
        Lower(aAllFields[nY]),;
		    aAllFields[nY],;
		    .T.,;
		    .F.,;
		    {;
          aAllFields[nY],;
			    GetFieldType(aAllFields[nY]),;
			    GetFieldLength(aAllFields[nY]),;
			    GetFieldDecimal(aAllFields[nY]);
		    };
      })
	  Endif
  Next

  FreeObj(oPGVCustomFields)
	RestArea(aArea)
	aSize(aAllFields, 0)
	aSize(aArea, 0)
Return aFields

//----------------------------------------------------------------------------
/*/{Protheus.doc} AddMapCustomFields
  Adiciona os campos customizados no MapFields informado no parametro.

  @param    cSchema      , Character , Código do Schema
  @param    oMainRequest , Objeto    , Objeto responsavel por retornar os dados para o Service.
  @author		Danilo Salve
  @since		19/04/2021
  @version	12.1.27
/*/
//---------------------------------------------------------------------------
Function AddMapCustomFields( cSchema as Character, oMainRequest as Object )
  Local nField        as Numeric
  Local aCustomFields as Array

  Default cSchema := ""

  aCustomFields := loadCustomFieldsBySchema( cSchema )

  For nField := 1 To Len(aCustomFields)
    If Empty( aScan(oMainRequest:oJsonObj:aMapFields, {|x| x[2] == aCustomFields[nField, FIELD]}))
     oMainRequest:AddMapFields( ;
	    aCustomFields[nField, PROPERTY],;
	    aCustomFields[nField, FIELD],;
	    aCustomFields[nField, EXPORTJSON],;
      aCustomFields[nField, FIXED],;
	    aCustomFields[nField, ASTRUCT] )
    EndIf
  Next
  aSize(aCustomFields, 0)
Return Nil
