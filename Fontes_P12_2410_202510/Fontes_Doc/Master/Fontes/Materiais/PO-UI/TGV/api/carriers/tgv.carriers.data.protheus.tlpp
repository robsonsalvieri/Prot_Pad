#include "tlpp-core.th"

namespace tgv.carriers
using namespace tgv.util

Static __lPGVCRQRY := ExistBlock("GVCRQRY")

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersProtheusData
	Efetua a consulta de transportadoras Protheus.
	@type		class
	@version	12.1.33
	@author		Squad CRM/Faturamento
	@since		07/10/2020
/*/
//------------------------------------------------------------------------------
Class CarriersProtheusData from FWAdapterBaseV2
	Public Data aCustomFields		as Array
	Public Data cCustomFields		as Character
	Public Data cSource				as Character
	Public Data isAdmin				as Logical
	Public Data lcanObfuscateField	as Logical
	Public Data lItems 				as Logical
	Public Data oHashFieldsPD		as Object
	Public Data nFields				as Numeric
	Public Data hasStatusField		as logical

	Public Method New() as Object
	Public Method Get()
	Public Method RowToJson()
	Public Method AddCustomFields()
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersProtheusData::New(cVerbo as Character, lList as Logical) as Object
	Cria uma nova instancia do Objeto CarriersProtheusData
	@type		method
	@version	12.1.33
	@param		cVerbo	, Character	, Verbo que ser· utilizado na requisiÁ„o
	@param		lList	, Logical	, Se ir· listar o Json
	@author		Squad CRM/Faturamento
	@since		07/10/2020
	@return		object, nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method New(cVerbo as Character, lList as Logical) as Object Class CarriersProtheusData
	Default cVerbo 	:= "GET"
	Default lList 	:= .T.

	_Super:New(cVerbo, lList)
	::oHashFieldsPD := tHashMap():New()
	::hasStatusField := hasStatusField("SA4", "A4_MSBLQL")
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersProtheusData::Get
	Efetua a consulta de transportadoras.
	@type		method
	@version	12.1.33
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		19/01/2021
	@param		nPage		, Numeric	, N˙mero da p·gina que ser· retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por p·gina
	@param 		cDate		, Character	, Data de Sincronismo/Alteracao
/*/
//------------------------------------------------------------------------------
Method Get(nPage as Numeric, nPageSize as Numeric, cDate as Character) Class CarriersProtheusData
	Local aArea 	as Array
	Local cWhere	as Character

	Default nPage		:= 1
	Default nPageSize	:= 10
	Default cDate		:= ""

	aArea := GetArea()
	AddMapFields( self )

	::AddCustomFields()
	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( getQueryTGV("SA4") )

	cWhere := " SA4.A4_FILIAL = '" + FwXFilial("SA4") + "'"

	If __lPGVCRQRY
		cWhere += ExecBlock("GVCRQRY", .F., .F.)
	Endif

	If !Empty(cDate) .And. SubStr(cDate,1,1) != '-'
		cWhere += " AND SA4.S_T_A_M_P_ > " + ConvertTimeStamp( cDate, "value", "timestamp" )
	Else
		cWhere += " AND SA4.D_E_L_E_T_ = ' ' "
	EndIf

	::SetWhere( cWhere )
	::SetOrder( "A4_FILIAL, A4_COD" )

	If ::Execute()
		::FillGetResponse()
	EndIf

	FTPDUnload()
	::oHashFieldsPD:Clean()

	RestArea( aArea )
	aSize( aArea, 0 )
	aSize(::aCustomFields, 0)
	FreeObj(::oHashFieldsPD)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersProtheusData::RowToJson
	Converte o retorno da consulta SQL em JSON
	@version	12.1.33
	@type 		method
	@author		Danilo Salve / Squad CRM & Faturamento
	@since		22/12/2020
	@param		cAlias	, character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio.
	/*/
//------------------------------------------------------------------------------
Method RowToJson(cAlias as Character, aFields as Array, lEmpty as Logical) CLASS CarriersProtheusData
	Default lEmpty := .F.
	ConvertResponseValues(aFields, cAlias, lEmpty, @Self )

	if (cAlias)->(!Eof()) .And. (cAlias)->(!Bof())
		Self:oJsonObj:setProp( "internalid"	, ((cAlias)->A4_FILIAL + (cAlias)->A4_COD) , .F. )

		if !Self:hasStatusField
			Self:oJsonObj:setProp( 'status'	, '2', .F.  )
		endif
	endIf
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFields
	Cria o Mapa de campos Protheus x API para as transportadoras.
	@type 		function
	@author		Squad CRM/Faturamento
	@since		07/10/2020
	@version	12.1.33
	@param		oSelf,	object, Objeto com heran√ßa da Classe FWAdapterBaseV2
/*/
//------------------------------------------------------------------------------
Static Function AddMapFields(oSelf as Object)
	oSelf:AddMapFields("branchid"   , "A4_FILIAL" , .T., .F., { "A4_FILIAL"	  , 'C', GetFieldLength("A4_FILIAL"), 0 })
	oSelf:AddMapFields("code"       , "A4_COD"    , .T., .F., { "A4_COD"	  , 'C', GetFieldLength("A4_COD")   , 0 })
	oSelf:AddMapFields("name"       , "A4_NOME"   , .T., .F., { "A4_NOME"	  , 'C', GetFieldLength("A4_NOME")  , 0 })
	oSelf:AddMapFields("deleted"    , "deleted"   , .T., .F., { "deleted"	  , 'C', 1, 0 }, "SA4.D_E_L_E_T_")

	if oSelf:hasStatusField
		oSelf:AddMapFields("status"       , "A4_MSBLQL"   , .T., .F., { "A4_MSBLQL"	, 'C', GetFieldLength("A4_MSBLQL"), 0 })
	endIf
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} CarriersProtheusData::AddCustomFields
	Metodo Auxiliar, deve carregar os Atributos utilizados na convers„o dos valores.
	@type		method
	@author		Squad CRM & Faturamento
	@since		10/06/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
METHOD AddCustomFields() CLASS CarriersProtheusData
	Self:cSource:= "TGVCARRIERS"
	Self:isAdmin := FwIsAdmin()
	Self:aCustomFields := {}
	Self:cCustomFields := ""
	Self:nFields := Len(Self:oJsonObj:aMapFields)

	If !Self:isAdmin
		Self:oHashFieldsPD := setProtectedFields(Self:oJsonObj:aMapFields, Self:cSource)
	Endif

	Self:lcanObfuscateField := CanAccessProtectedData(Self:isAdmin)
Return Nil
