#include 'tlpp-core.th'

namespace totvs.protheus.backoffice.pgvPDFCustomBase
using namespace tgv.util

static __instance    as object
static __oActiveData as object
//-------------------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustom
	Disponibiliza uma classe de servico da API REST para Customizacao dos PDFs de Pedido 
	de Venda e Orcamentos.
	@type		Class
	@author		Squad CRM/Faturamento
	@since		07/11/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Class pdfCustomService
	Public Data jResponse                   as Json     
	  
	Public Method New()                     as Object  
	Public Method Get()                     as Json   
    Public Method getInstance()             as Object  
	Public Method getPdfCustom()            as Json
	Public Method getPdfCustomHeader()      as Json  
	Public Method getPdfCustomsections()    as Json
	Public Method getPdfCustomfields()      as Json
	Public Method getInstanceData()         as Object
	Public Method savePDFCustom()
EndClass
//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustomService:New
	Disponibiliza uma nova instância do servico pdfCustom.

	@sample		pdfCustomService:New()
	@author		Squad CRM/FAT - Inovação
	@since		07/11/2024
	@version	12.1.2410 ou Superior
/*/
//------------------------------------------------------------------------------
Method New() as Object Class pdfCustomService  
	::jResponse := JsonObject():New()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustomService:getInstance() as object
	Obtem uma instância da classe pdfCustomService
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento
	@since 25/11/2024
	@return object, instância da classe pdfCustomService
/*/
//------------------------------------------------------------------------------
Method getInstance() as object class pdfCustomService
	If (__instance == nil)
		__instance := pdfCustomService():New()
	Endif
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustomService:getInstanceData() as object
  Obtem uma instância da classe pgvPdfCustomProtheusData.
  @type method
  @author Squad CRM & Faturamento
  @since 25/11/2024
  @version 12.1.2410
  @return object, Instância da classe Data
/*/
//------------------------------------------------------------------------------
Method getInstanceData()  class pdfCustomService as object
	If __oActiveData == Nil
		__oActiveData := pgvPdfCustomProtheusData():New()
	Endif
Return __oActiveData

//----------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustomService:getPdfCustom
	Retornar os campos de uma entidade do PDF Customizado - AQ6, AQ7 ou AQ8
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento
	@since 25/11/2024
	@param nPage        , numeric  , número da página
	@param nPageSize    , numeric  , Quantidade de registros por página
	@param aURLFilter   , array    , Lista de Filtros no padrão do FWAdapterBaseV2
	@param cFields      , character, Lista de campos que devem ser retornados
	@param cSort        , character, Ordernação do Response
	@param cConfigPDFId , character, Id da Configuração de PDF
	@param cIdsection   , character, Id da Seção da Configuração de PDF
	@param cFieldId     , character, Id do Campo da Configuração de PDF
	@param cType        , character, Tipo da entidade a ser consultada - H=AQ6, S=AQ7, F=AQ8
	@return json, resposta da requisição formatada em json
/*/
//----------------------------------------------------------------------------------------------------------------------------
Method getPdfCustom(nPage as numeric  , nPageSize as numeric     , aURLFilter as array    , cFields as character, ;
					cSort as character, cConfigPDFId as Character, cIdsection as Character, cFieldId as Character, ;
					cType as Character) as json  Class pdfCustomService

	Local oData := ::getInstanceData()

	oData:getData(nPage, nPageSize, aURLFilter, cFields, cSort, cConfigPDFId, cIdSection, cFieldId, cType)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	EndIf

	oData:DeActivate()
	FreeObj(oData)

Return ::jResponse

//--------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pdfCustomService:savePDFCustom
	Efetua a gravação da Configuração do PDF Customizado
	@type method
	@version 12.1.2410
	@author Squad CRM & Faturamento
	@since 26/11/2024
	@param jBody, json, Configuração do PDF Customizado - corpo.
	@param nOperation, numeric, operação (3, 4 ou 5)
	@param cPDFCustomId, character, Código identificador (AQ6_ID) da Configuração
/*/
//--------------------------------------------------------------------------------------------------------------------------
method savePDFCustom(jBody as json, nOperation as integer, cPDFCustomId as character) class pdfCustomService
	Local oData := ::getInstanceData()
	oData:savePDFCustom(jBody, nOperation, cPDFCustomId)
return nil
