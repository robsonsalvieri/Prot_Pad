#include "tlpp-core.th"
#include "protheus.ch"
#include "pgv.checkconfigappserver.ch"

namespace totvs.protheus.backoffice.pgvCheckConfigAppserver
using namespace tgv.util

//----------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvCheckConfigAppserver
    API para consulta das configurações do Appserver utilizado no portal Gestão de vendas
    @type       class
    @version    12.1.2210
    @author     Rafael Mota Previdi / Squad CRM & Faturamento
    @since      22/06/2023
/*/
//----------------------------------------------------------------------------------------
class pgvCheckConfigAppserver
    private data cAppserverFile as character
    private data cDefaultValue  as character

    @Get("/api/pgv/checkconfigappserver/:section/:key")
	public method getConfigAppserver()    as logical
    
    @Get("/api/pgv/checkconfigappserver/getsecuritykey")
	public method getSecurityKey()       as logical

    public method new()                   as object
    private method queryConfigAppserver() as character
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvCheckConfigAppserver::new() as object
    obtem uma nova instancia da classe pgvCheckConfigAppserver
    @type       method
    @version    12.1.2210
    @author     Rafael Mota Previdi / Squad CRM & Faturamento
    @since      22/06/2023
    @return object, nova instancia da classe pgvCheckConfigAppserver
/*/
//------------------------------------------------------------------------------
method new() as object class pgvCheckConfigAppserver
    ::cAppserverFile := getSrvIniName()
    ::cDefaultValue  := ""
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvCheckConfigAppserver::getConfigAppserver() as logical
    Obtêm os dados necessarios para consultar o valor referente a seção e chave
    informados na requisicao, e executa a funcao para este fim
    @type       method
    @version    12.1.2210
    @author     Rafael Mota Previdi / Squad CRM & Faturamento
    @since      22/06/2023
    @return     logical, retorna sempre verdadeiro
/*/
//------------------------------------------------------------------------------
method getConfigAppserver() as logical class pgvCheckConfigAppserver
    local jResponse := JsonObject():New()       as json
    local cSection  := getPathParams("section") as character
    local ckey      := getPathParams("key")     as character
    local cValue                                as character

    if empty(cSection) .Or. empty(cKey)
        SetRestFault(400, FWHttpEncode(STR0001))   // #"Seção ou chave do arquivo appserver.ini não informada."
        return .t.
    endIf
    
    cValue := ::queryConfigAppserver(cSection, cKey)

    jResponse['section'] := FWHttpEncode(cSection)
    jResponse['key']     := FWHttpEncode(cKey)
    jResponse['value']   := FWHttpEncode(cValue)

    oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
    oRest:setStatusCode(200)
    oRest:setResponse(jResponse)

    FreeObj(jResponse)
return .t.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvCheckConfigAppserver::getSecurityKey() as logical
    Obtêm o valor da chave security
    @type       method
    @version    12.1.2210
    @author     Eduardo Paro / Squad CRM & Faturamento
    @since      31/07/2023
    @return     logical, retorna sempre verdadeiro
/*/
//------------------------------------------------------------------------------
method getSecurityKey() as logical class pgvCheckConfigAppserver
    local jResponse := JsonObject():New()       as json

    jResponse['value']   := FWHttpEncode(cValToChar(HTTPGetSecurity()))

    oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
    oRest:setStatusCode(200)
    oRest:setResponse(jResponse)

    FreeObj(jResponse)
return .t.

//--------------------------------------------------------------------------------
/*/{Protheus.doc} pgvCheckConfigAppserver::queryConfigAppserver() as logical
    Realizar a consulta do valor no arquivo appserver.ini referente a seção e 
    chave informados no parâmetro
    @type       method
    @version    12.1.2210
    @author     Rafael Mota Previdi / Squad CRM & Faturamento
    @since      22/06/2023
    @param      cSection, character, seção no arquivo appserver.ini
    @param      cKey    , character, chave da seção no arquivo appserver.ini
    @return     cValue  , character, o valor referente a seção e chave informados
/*/
//--------------------------------------------------------------------------------
method queryConfigAppserver(cSection as character, cKey as character) as character class pgvCheckConfigAppserver
    local cValue as character

    default cSection := ""
    default cKey     := ""

    cValue := GetPvProfString(cSection, cKey, ::cDefaultValue, ::cAppserverFile)
return cValue
