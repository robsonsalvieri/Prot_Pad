#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.salesGoalsManagement
using namespace tgv.util

#DEFINE LABEL 1
#DEFINE VALUE 2

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData
	disponibiliza um classe para consulta de dados 
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/04/2024
/*/
//------------------------------------------------------------------------------
Class salesGoalsManagementProtheusData from FWAdapterBaseV2

	public method new()             as Object
	public method get()
	public method setAddMapFields()
	public method getSalesFilter()  as Character
	public method getQueryGoals()   as Character
    public method getSalesWhere()   as Character
	public method getJoin()     	as Character
    public method getGoals()        as Logical
	public method RowToJson()

EndClass

//-----------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData::new() as object
    Obter uma nova instancia da class salesGoalsManagementProtheusData
    @author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
    @since 19/04/2024
    @version 12.1.2310
    @return @return object, retorna uma nova instancia da classe
/*/
//-----------------------------------------------------------------------
Method new() as object Class salesGoalsManagementProtheusData
	_Super:New("GET", .T.)
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData:setAddMapFields
	Cria o Mapa de campos Protheus x API
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		17/04/2024
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method setAddMapFields(lAchGoal as logical) Class salesGoalsManagementProtheusData

    Self:AddMapFields("branch"		, "CT_FILIAL"	, .T., .F., { "CT_FILIAL"  	, 'C', GetFieldLength("CT_FILIAL")	, 0 })
    Self:AddMapFields("document"	, "CT_DOC"		, .T., .F., { "CT_DOC"  	, 'C', GetFieldLength("CT_DOC")	    , 0 })
    Self:AddMapFields("sequen"		, "CT_SEQUEN"	, .T., .F., { "CT_SEQUEN"   , 'C', GetFieldLength("CT_SEQUEN")	, 0 })
    Self:AddMapFields("description"	, "CT_DESCRI"	, .T., .F., { "CT_DESCRI"	, 'C', GetFieldLength("CT_DESCRI")	, 0 })
    Self:AddMapFields("date"		, "CT_DATA"		, .T., .F., { "CT_DATA"		, 'C', GetFieldLength("CT_DATA")	, 0 })
    Self:AddMapFields("seller"		, "CT_VEND"		, .T., .F., { "CT_VEND"		, 'C', GetFieldLength("CT_VEND")	, 0 })
	Self:AddMapFields("nameseller"	, "A3_NOME"		, .T., .F., { "A3_NOME"		, 'C', GetFieldLength("A3_NOME")	, 0 })
    Self:AddMapFields("region"		, "CT_REGIAO"	, .T., .F., { "CT_REGIAO"	, 'C', GetFieldLength("CT_REGIAO")	, 0 })
	Self:AddMapFields("descregion"  , "X5_DESCRI"  	, .T., .F., { "X5_DESCRI"  	, 'C', GetFieldLength("X5_DESCRI")	, 0 })
    Self:AddMapFields("categorie"	, "CT_CATEGO"	, .T., .F., { "CT_CATEGO"	, 'C', GetFieldLength("CT_CATEGO")	, 0 })
    Self:AddMapFields("type"		, "CT_TIPO"	    , .T., .F., { "CT_TIPO"	    , 'C', GetFieldLength("CT_TIPO")	, 0 })
    Self:AddMapFields("group"		, "CT_GRUPO"	, .T., .F., { "CT_GRUPO"	, 'C', GetFieldLength("CT_GRUPO")	, 0 })
    Self:AddMapFields("product"		, "CT_PRODUTO"	, .T., .F., { "CT_PRODUTO"	, 'C', GetFieldLength("CT_PRODUTO")	, 0 })
    Self:AddMapFields("quantity"	, "CT_QUANT"	, .T., .F., { "CT_QUANT" 	, 'N', GetFieldLength("CT_QUANT")	, GetFieldDecimal("CT_QUANT") })
    Self:AddMapFields("value"		, "CT_VALOR"	, .T., .F., { "CT_VALOR" 	, 'N', GetFieldLength("CT_VALOR")	, GetFieldDecimal("CT_VALOR") })
    Self:AddMapFields("coin"		, "CT_MOEDA"	, .T., .F., { "CT_MOEDA" 	, 'N', GetFieldLength("CT_MOEDA")	, GetFieldDecimal("CT_MOEDA") })
    Self:AddMapFields("coust"	    , "CT_CCUSTO"	, .T., .F., { "CT_CCUSTO"	, 'C', GetFieldLength("CT_CCUSTO")	, 0 })
    Self:AddMapFields("coustitem"	, "CT_ITEMCC"	, .T., .F., { "CT_ITEMCC"	, 'C', GetFieldLength("CT_ITEMCC")	, 0 })
    Self:AddMapFields("valueclass"	, "CT_CLVL"	    , .T., .F., { "CT_CLVL"	    , 'C', GetFieldLength("CT_CLVL")	, 0 })
    Self:AddMapFields("blocked"	    , "CT_MSBLQL"	, .T., .F., { "CT_MSBLQL"   , 'C', GetFieldLength("CT_MSBLQL")	, 0 })
	If lAchGoal
		Self:AddMapFields( "achivied" , "ACHIVIED", .T., .F., { "ACHIVIED", 'N', 12 , 2 }, "0" )
	EndIf

Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData:Get
	Obtem uma Lista de metas retornando um JSON respeitando a Quantidade de 
	registro por pagina, especificado no paramentro PageSize.
	@type		method
	@param		nPage		, Numeric	, Numero da pagina que sera retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrao do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernacao do Response
	@param		lGoals		, Logical	, se Será enviado vendedores 
	@param		cSellers	, Character	, Vendedores
	@param		cStates		, Character	, Regioes (estados)
	@param		lAchGoal	, Logico	, Atingimento de meta
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		19/04/2024
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method get(	nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, cSort as Character, lGoals as logical, cSellers as character, cStates as character, lAchGoal as logical) Class salesGoalsManagementProtheusData
	Local   aArea   := GetArea()
	Local cSqlJoin := ::getJoin() as character

    Default lGoals  := .F.

	::setAddMapFields( lAchGoal )
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( ::getQueryGoals(cSqlJoin) )

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0
		::SetUrlFilter( aURLFilter )
	EndIf

	If !Empty( cFields )
		::SetFields( cFields )
	EndIf

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf

	::SetWhere( ::getSalesWhere( lGoals, cSellers, cStates ) )
	::SetOrder( "CT_FILIAL, CT_DOC, CT_SEQUEN" )

	If ::Execute()
	    ::FillGetResponse()
	EndIf

	RestArea(aArea)
	aSize(aArea, 0)

Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} getQueryGoals
	@type		function
	@version	12.2310
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		23/01/2024
	@return		character, Expressao SQL para consulta
/*/
//------------------------------------------------------------------------------
Method getQueryGoals(cSqlJoin as character) 	as Character class salesGoalsManagementProtheusData
	Local cQuery := "" as Character
	Default cSqlJoin := ""

    cQuery := " SELECT #QueryFields# "
    cQuery += " FROM " + RetSqlName("SCT") + " SCT "
	cQuery += IIf( !Empty(cSqlJoin), cSqlJoin, "" )
	cQuery += " WHERE #QueryWhere# "

Return cQuery

//-----------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData:getLeftJoin
	Realiza Left Join com as tabelas SA3 e SX5
	@author Misael Ramos / Squad CRM & Faturamento
	@since 22/05/2024
	@version 12.1.2310
	@return cSqlJoin, character, retorna o cSqlJoin da query
/*/
//-------------------------------------------------------------------
Method getJoin() as Character Class salesGoalsManagementProtheusData
	Local cSqlJoin := "" as character

	cSqlJoin := " LEFT JOIN " + RetSqlName("SA3") + " SA3 "
	cSqlJoin += " ON  SA3.A3_COD     = SCT.CT_VEND "
	cSqlJoin += " AND SA3.A3_FILIAL  = '" + FWxFilial("SA3") + "' "
	cSqlJoin += " AND SA3.D_E_L_E_T_ = ' ' "
	cSqlJoin += " LEFT JOIN " + RetSqlName("SX5") + " SX5 "
	cSqlJoin += " ON SX5.X5_CHAVE    = SCT.CT_REGIAO "
	cSqlJoin += " AND SX5.X5_FILIAL  = '" + FWxFilial("SX5") + "' "
	cSqlJoin += " AND SX5.X5_TABELA  = 'A2' "
	cSqlJoin += " AND SX5.D_E_L_E_T_ = ' ' "

Return cSqlJoin

//------------------------------------------------------------------------------
/*/{Protheus.doc} getSalesWhere
	@type		function
	@version	12.2310
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		23/01/2024
	@param		lGoals		, Logical	, se Será enviado vendedores 
	@param		cSellers	, character	, Vendedores
	@param		cStates		, character	, Estados (Regiões)
	@return		character, Expressao SQL para consulta
/*/
//------------------------------------------------------------------------------
Method getSalesWhere(lGoals as Logical, cSellers as character, cStates as character) as Character class salesGoalsManagementProtheusData
	Local cWhere := "" as Character
	Local cDataIni  := AnoMes(Date()) + "01"
	Local cDataFim  := DTos(LastDate(Date()))

    Default lGoals := .F.

    cWhere := "SCT.D_E_L_E_T_     = ' ' "
	cWhere += "AND SCT.CT_MSBLQL  = '2' "
	cWhere += "AND SCT.CT_DATA BETWEEN '" + cDataIni + "' AND '" + cDataFim + "' "
    cWhere += IIf( lGoals .And. !Empty(cSellers), " AND SCT.CT_VEND   IN (" + cSellers + ") ", "" )
	cWhere += IIf( lGoals .And. !Empty(cStates),  " AND SCT.CT_REGIAO IN (" + cStates + ") ",  "" )

Return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementProtheusData:getGoals(jGoals as Json) as Logical
	Recebe os vendedores para efetuar filtro
	@type		method
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/04/2024
    @version 12.1.2310
	@param		nPage		, Numeric	, Numero da pagina que sera retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrao do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernacao do Response
	@param		lGoals		, Logical	, se Será enviado vendedores 
	@param		jGoals		, Json		, Vendedores
	@return		logical, Se obteve sucesso na transação retorna verdadeiro.
/*/
//------------------------------------------------------------------------------
Method getGoals(nPage as numeric, nPageSize as numeric, aURLFilter as array, cFields as character, cSort as character, lGoals as logical, cSellers as character, cStates as character, lAchGoal as logical) as Logical Class salesGoalsManagementProtheusData

	Local aArea := FwGetArea() as Array

    ::Get(nPage, nPageSize, aURLFilter, cFields, cSort, lGoals, cSellers, cStates, lAchGoal)

	FwRestArea(aArea)
	aSize(aArea, 0)

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} RowToJson
	Converte o retorno da consulta SQL em JSON

	@sample		oProductData:RowToJson( cAlias, aFields, .F.)
	@type		method
	@param		cAlias	, Character	, Alias da Consulta
	@param		aFields	, Array		, Campos (MapFields)
	@param		lEmpty	, Logical	, Vazio
	@return     Nil
	@author		Misael Ramos
	@since		22/05/2024
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method RowToJson(cAlias as Character, aFields as Array, lEmpty as Logical) Class salesGoalsManagementProtheusData
    Local nLoop     := 0   		 as integer
	Local aVendas	:= {}  		 as array
	Local aDevolu	:= {}  		 as array
	Local cContent  := ""  		 as character
	Local cDupli	:= "'S','N'" as character
	Local cEstoq	:= "'S','N'" as character
	Local dDataIni	:= SToD( AnoMes( Date() ) + "01" ) as date
	Local dDataFim	:= LastDate( Date() ) as date

    For nLoop := 1 to Len(aFields)
        If lEmpty
            cContent := ''
        Else
            If aFields[nLoop, LABEL] == "achivied"
				aVendas  := FtNfVendas(4,(cAlias)->CT_VEND,dDataIni,dDataFim,(cAlias)->CT_REGIAO,(cAlias)->CT_TIPO,(cAlias)->CT_GRUPO,(cAlias)->CT_PRODUTO,"","","","",,cDupli,cEstoq, (cAlias)->CT_CATEGO)
				aDevolu  := FtNfDevol(4,(cAlias)->CT_VEND,dDataIni,dDataFim,(cAlias)->CT_REGIAO,(cAlias)->CT_TIPO,(cAlias)->CT_GRUPO,(cAlias)->CT_PRODUTO,"","","",,cDupli,cEstoq, (cAlias)->CT_CATEGO)
				cContent := aVendas[1] - aDevolu[1]
			Else
				cContent := ::ValueToJson( aFields[nLoop, LABEL], (cAlias)->( &(aFields[nLoop, VALUE]) ) )	
			EndIf
		EndIf

		Self:oJsonObj:setProp( aFields[nLoop, LABEL], cContent, lEmpty )

		aSize(aVendas, 0)
		aSize(aDevolu, 0)
    Next

Return Nil
