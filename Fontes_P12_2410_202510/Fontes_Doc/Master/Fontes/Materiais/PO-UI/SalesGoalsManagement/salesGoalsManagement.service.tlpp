#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.salesGoalsManagement
using namespace tgv.util

STATIC __instance as Object
STATIC __oActiveData as Object

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagementService
	disponibiliza um classe de serviços da API salesGoalsManagementService
	@type class
	@since 17/04/2024
    @version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
/*/
//------------------------------------------------------------------------------
Class salesGoalsManagementService
	data jResponse                  	as Json

	public method new()             	as Object
	public method getInstance()     	as Object
	public method getInstanceData() 	as Object
    public method getGoals()        	as json
	public method get()             	as Json
	public method getJsonProperties() 	as json

EndClass

//-----------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagement::new() as object
    Obter uma nova instancia da class salesGoalsManagement
    @author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
    @since 17/04/2024
    @version 12.1.2310
    @return @return object, retorna uma nova instancia da classe
/*/
//-----------------------------------------------------------------------
Method new() as object class salesGoalsManagementService
	::jResponse := JsonObject():new()
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersListService::getInstance() as object
	obtem uma instancia da classe SalesOrdersListService
	@type method
	@version 12.1.2310
	@since 17/04/2024
    @version 12.1.2310
	@return object, instancia da classe SalesOrdersListService
/*/
//------------------------------------------------------------------------------
Method getInstance() as object class salesGoalsManagementService
	If (__instance == nil)
		__instance := salesGoalsManagementService():new()
	Endif
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersListService::getInstanceData() as object
  obtem uma instancia da classe salesGoalsManagementProtheusData.
  @type method
  @author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
  @since 17/04/2024
  @version 12.1.2310
  @return object, Instancia da classe Data
/*/
//------------------------------------------------------------------------------
Method getInstanceData()  class salesGoalsManagementService as object
	If __oActiveData == Nil
		__oActiveData := salesGoalsManagementProtheusData():new()
	Endif
Return __oActiveData

//----------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} SalesOrdersListService::getSalesOrdersList(nPage as numeric, nPageSize as numeric,;
	URLFilter as array, cFields as character, cSort as character) as Json
	Obtem todas as metas em sua sequencia 001, após selecionada deve retornar todas as sequencias do vendedor selecionado
	@type method
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/04/2024
    @version 12.1.2310
	@param nPage        , numeric  , número da página
	@param nPageSize    , numeric  , Quantidade de registros por página
	@param aURLFilter   , array    , Lista de Filtros no padrão do FWAdapterBaseV2
	@param cFields      , character, Lista de campos que devem ser retornados
	@param cSort        , character, Ordernação do Response
	@return json, resposta da requisição formatada em json
/*/
//----------------------------------------------------------------------------------------------------------------------------
Method get(nPage as numeric, nPageSize as numeric, aURLFilter as array, cFields as character, cSort as Character) as Json class salesGoalsManagementService
	Local oData        := ::getInstanceData()

	Default nPage      := 1
	Default nPageSize  := 10
	Default aURLFilter := {}
	Default cFields    := ""
	Default cSort      := ""

	oData:get(nPage, nPageSize, aURLFilter, cFields, cSort)

	If oData:lOk
		::jResponse := oData:GetJSONResponse()
	EndIf

	oData:DeActivate()
	FreeObj(oData)

Return ::jResponse

//------------------------------------------------------------------------------
	/*/{Protheus.doc} salesGoalsManagementService::getGoals(nPage as numeric, nPageSize as numeric,;
	URLFilter as array, cFields as character, cSort as character,lGoals as Logical, jGoals as Json) as Json
	Após o gerente selecionar um vendedor deve retornar todas as sequencias da respectiva meta
	@type		method
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 17/04/2024
    @version 12.1.2310
	@param		jGoals		, json		, json do contato
	@param		jStates		, json		, json dos estados
/*/
//------------------------------------------------------------------------------
Method getGoals(nPage as numeric, nPageSize as numeric, aURLFilter as array, cFields as character, cSort as character, lGoals as logical, jGoals as Json) as Json Class salesGoalsManagementService

    Local oData    := ::getInstanceData()
	Local cSellers := ""  as character
	Local cStates  := ""  as character
	Local lAchGoal := .F. as logical
	Local jBody    := {}  as json

	Default nPage      := 1
	Default nPageSize  := 10
	Default aURLFilter := {}
	Default cFields    := ""
	Default cSort      := ""
	Default lGoals     := .F.
	Default jGoals     := Nil

	jBody 	 := ::getJsonProperties(jGoals)
	cSellers := jBody['sellers']
	cStates  := jBody['states']
	lAchGoal := jBody['achivieds']

    If oData:lOk
		::jResponse := oData:GetJSONResponse(oData:getGoals(nPage, nPageSize, aURLFilter, cFields, cSort, lGoals, cSellers, cStates, lAchGoal))
	EndIf

	oData:DeActivate()
	FreeObj(oData)

Return ::jResponse

//------------------------------------------------------------------------------
	/*/{Protheus.doc} salesGoalsManagementService::getJsonProperties(jBody as Json) as Array
	Metodo que recebe o json da requisição para leitura e validação das propriedades
	@type		method
	@author		Misael Ramos / Squad CRM & Faturamento
	@since 		21/05/2024
    @version 	12.1.2310
	@param		jBody	, type json
	@return		jReturn	, type json
/*/
//------------------------------------------------------------------------------
Method getJsonProperties(jBody as Json) as Json Class salesGoalsManagementService
	Local nTotSellers	:= 0   as numeric
	Local nTotStates	:= 0   as numeric
	Local nProps		:= 0   as numeric
	Local cSellers		:= ""  as character
	Local cStates		:= ""  as character
	Local lAchivied		:= .F. as logical
	Local jReturn		:= {}  as json

	Default jBody 		:= Nil

	If jBody != Nil
		nTotSellers := IIf( jBody['seller'] != Nil, Len(jBody['seller']), 0 )
		nTotStates  := IIf( jBody['state']  != Nil, Len(jBody['state']),  0 )
		lAchivied	:= IIf( jBody['goalsAchivied'] != Nil, jBody['goalsAchivied'], .F. )
		jReturn		:= JsonObject():new()

		If nTotSellers > 0
			For nProps := 1 To nTotSellers
				cSellers += IIf(nProps == nTotSellers, " '" + jBody['seller'][nProps] + "' ", " '" + jBody['seller'][nProps] + "', ")
			Next
		EndIf

		If nTotStates > 0
			For nProps := 1 To nTotStates
				cStates += IIf(nProps == nTotStates, " '" + jBody['state'][nProps] + "' ", " '" + jBody['state'][nProps] + "', ")
			Next
		EndIf

		jReturn['sellers'] 	 := cSellers
		jReturn['states'] 	 := cStates
		jReturn['achivieds'] := lAchivied

	EndIf

Return jReturn
