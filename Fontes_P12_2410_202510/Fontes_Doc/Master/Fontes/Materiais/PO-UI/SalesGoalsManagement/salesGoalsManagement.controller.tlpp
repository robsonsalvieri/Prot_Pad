 #include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'salesGoalsManagement.controller.ch'

namespace totvs.protheus.backoffice.salesGoalsManagement
using namespace tgv.util

Class salesGoalsManagement
	data nPage as Numeric
	data nPageSize as Numeric
	data jResponse as Json
	data oService as Object

	public method new() as Object

	@Get("/api/sales/salesGoalsManagement")
	public method getSalesGoalsManagement() as Logical

	@Post("/api/sales/salesGoalsManagement/goals")
	public method postSalesGoalsManagement() as Logical

EndClass

//-----------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagement::new() as object
    Obter uma nova instancia da class salesGoalsManagement
    @author     Gabriel Oliveira dos Santos / Squad CRM & Faturamento
    @since 17/04/2024
    @version 12.1.2310
    @return @return object, retorna uma nova instancia da classe
/*/
//-----------------------------------------------------------------------
Method new() as object Class salesGoalsManagement
	::nPage     := 1
	::nPageSize := 10
	::jResponse := JsonObject():new()
	::oService  := salesGoalsManagementService():getInstance()
Return Self

//-----------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagement::getSalesGoalsManagement() as logical
    Obtem todos as metas para o Gerente logado
    @author     Gabriel Oliveira dos Santos / Squad CRM & Faturamento
    @since 17/04/2024
    @version 12.1.2310
    @return .T., boolean, retorno da requisição
/*/
//-----------------------------------------------------------------------
Method getSalesGoalsManagement() as logical Class salesGoalsManagement

	Local aItems        := {}                            as Array
	Local aURLFilter    := getFilterParam()              as Array
	Local cFields       := getQueryParam('FIELDS')       as Character
	Local cSort         := getQueryParam('SORT')         as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	getPageAndPageSize(@::nPage, @::nPageSize)

	::jResponse:fromJson(::oService:get(::nPage, ::nPageSize, aURLFilter, cFields, cSort, .F.))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(FWhttpEncode(::jResponse:toJSON()))
	Else
		SetRestFault(404, FWhttpEncode(STR0001) ) 
	EndIf

	aSize(aURLFilter, 0)
	aSize(aItems, 0)

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} salesGoalsManagement::postSalesGoalsManagementGoals() as Logical
	Recebe os vendedores e envia o resultado para o Gerente
	@type		method
	@version	12.1.2310
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		19/04/2024
	@return		logical
/*/
//------------------------------------------------------------------------------
Method postSalesGoalsManagement() as logical Class salesGoalsManagement
    Local jBody 		:= GetRequestBody() as Json
    Local aURLFilter    := getFilterParam()              as Array
	Local cFields       := getQueryParam('FIELDS')       as Character
	Local cSort         := getQueryParam('SORT')         as Character

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
    getPageAndPageSize(@::nPage, @::nPageSize)

	If jBody <> Nil
    	::jResponse:fromJson(::oService:getGoals(::nPage, ::nPageSize, aURLFilter, cFields, cSort, .T., jBody))
		aItems := ::jResponse:GetJsonObject( "items" )
		aItems := IIF( !Empty( aItems ), aItems, {})
		
		If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
			oRest:setResponse(FWhttpEncode(::jResponse:toJSON()))
		Else
			SetRestFault(404, FWhttpEncode(STR0002) ) 
		EndIf	
		aSize(aItems, 0)
	EndIf

	FreeObj(jBody)
    aSize(aURLFilter, 0)

Return .T.
