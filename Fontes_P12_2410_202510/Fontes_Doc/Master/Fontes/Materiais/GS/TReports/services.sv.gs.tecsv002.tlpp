#include "msobject.ch"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tecsv002.ch"

namespace custom.atendentessemagenda

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATEC", tables="AA1,SRA,ABB", name="Atendentes - GS", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc} AtendentesReportsBusinessObject
Classe para geração de relatorio de funcionários que não estejam demitidos, dentro do SmartView.

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
class AtendentesReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDisplayName() as character
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data lExistPergunte as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new 
Método de inicializacao da classe

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class AtendentesReportsBusinessObject
_Super:new()

self:appendArea(STR0001)//"Gestão de serviços"

self:aFields := {"AA1_CODTEC", "AA1_NOMTEC", "AA1_CDFUNC", "AA1_FUNFIL", "AA1_ALOCA", "RA_TPCONTR"}

self:lExistPergunte := self:setPergunte("TECR050A")

if !self:lExistPergunte
     self:setErrorStatus(400, STR0002, STR0003)//"Pergunte não encontrado.", "Grupo de perguntas TECR050A não cadastrado na base. Contate o administrador do sistema."
endif

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Retorna o nome de exibição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDisplayName() as character class AtendentesReportsBusinessObject
return STR0004//"Atendentes sem agenda."

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Retorna a descrição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class AtendentesReportsBusinessObject
return STR0005 //"Relatórios de atendentes sem agenda na mesa operacional."

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Obtém os dados do relatorio utilizando a tabela AA1

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class AtendentesReportsBusinessObject
    Local aAloca     as array
    Local aTpContr   as array
    local cAliasAA1  as character
    local cId        as character
    local cQuery     as character
    local cRealName  as character
    Local dDataQry   as date
    local jItems     as json
    local jParams    as json
    Local nOrder     as numeric
    local nX         as numeric
    Local oStatement as object

    aAloca   := StrTokArr(GetSx3Cache( 'AA1_ALOCA' , 'X3_CBOX' ), ';' )
    aTpContr := StrTokArr(GetSx3Cache( 'RA_TPCONTR' , 'X3_CBOX' ), ';' )

    If !self:lExistPergunte
        return self:oData
    Else
        nOrder := 1
        cQuery := " SELECT AA1.AA1_CODTEC, "
        cQuery +=        " AA1.AA1_NOMTEC, "
        cQuery +=        " AA1.AA1_CDFUNC, "
        cQuery +=        " AA1.AA1_FUNFIL, "
        cQuery +=        " AA1.AA1_ALOCA, "
        cQuery +=        " SRA.RA_TPCONTR, "
        cQuery +=        " SRA.RA_ADMISSA, "
        cQuery +=        " SRA.RA_DEMISSA "
        cQuery += " FROM ? AA1 "
        cQuery +=        " LEFT JOIN ? SRA ON SRA.RA_FILIAL = AA1.AA1_FUNFIL "
        cQuery +=                        " AND SRA.RA_MAT = AA1.AA1_CDFUNC "
		cQuery +=                        " AND SRA.D_E_L_E_T_ = ' ' "
        cQuery += " WHERE AA1.AA1_FILIAL = ? "
        cQuery +=       " AND ( SRA.RA_MAT IS NULL "
        cQuery +=             " OR ( SRA.RA_ADMISSA <= ? "
        cQuery +=                  " AND ( SRA.RA_DEMISSA = ' '
        cQuery +=                        " OR SRA.RA_DEMISSA >= ?))) "
        cQuery +=       " AND AA1_CODTEC NOT IN (SELECT TGY.TGY_ATEND "
        cQuery +=                              " FROM ? TGY "
        cQuery +=                              " WHERE TGY.TGY_FILIAL = ? "
        cQuery +=                                    " AND TGY.TGY_ULTALO <> ' ' "
        cQuery +=                                    " AND (TGY.TGY_DTINI <= ? AND TGY.TGY_DTFIM >= ?) "
        cQuery +=                                    " AND TGY.D_E_L_E_T_ = ' ' ) "
        cQuery +=       " AND AA1.D_E_L_E_T_ = ' ' "
        //Método para retorno do json dos parâmetros:
        jParams := oFilter:getParameters()

        //Filtro de Datas:
        If !Empty(jParams["MV_PAR01"][1]) 
            dDataQry := DataConverte(jParams["MV_PAR01"][1])
        else 
            dDataQry := dDatabase
        EndIf

        //Aplicação de filtros do SmartView:
        If oFilter:hasFilter()
            cQuery += " AND ?"
        EndIf

        //Ordena os registros:
        cQuery += " ORDER BY AA1.AA1_CODTEC "

        //Arruma a query para o banco conectado:
        cQuery := ChangeQuery(cQuery)

        //Execucao da query:
        oStatement := FwExecStatement():new(cQuery)

        oStatement:SetUnsafe(nOrder++, RetSQLName("AA1"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("SRA"))
        oStatement:SetString(nOrder++, xFilial("AA1"))
        oStatement:SetDate(nOrder++, dDataQry)
        oStatement:SetDate(nOrder++, dDataQry)
        oStatement:SetUnsafe(nOrder++, RetSQLName("TGY"))
		oStatement:SetString(nOrder++, xFilial("TGY"))
        oStatement:SetDate(nOrder++, dDataQry)
        oStatement:SetDate(nOrder++, dDataQry)

        // Aplicação de filtros do SmartView 
        If oFilter:hasFilter()
            oStatement:SetUnsafe(nOrder++, oFilter:getSQLExpression())
        EndIf

        //Abre o alias
        cAliasAA1 := oStatement:OpenAlias()

        //Pega os campos da estrutura:
        aAllFields := self:getStructFields()
       
        While !(cAliasAA1)->(Eof())
            If EMPTY((cAliasAA1)->RA_DEMISSA) .OR. (cAliasAA1)->RA_DEMISSA >= DTOS(dDataQry)
                jItems := JsonObject():new()

                For nx := 1 To Len(aAllFields)
                    cId := aAllFields[nX]:getName()
                    cRealName := aAllFields[nX]:getRealName()
                    If cId == 'AA1_ALOCA'
                        jItems[cId] := alltrim(substr( aAloca[ val((cAliasAA1)->AA1_ALOCA) ], 3)) 
                    ElseIf cId == 'RA_TPCONTR' .AND. !Empty((cAliasAA1)->RA_TPCONTR)
                        jItems[cId] := alltrim(substr( aTpContr[ val((cAliasAA1)->RA_TPCONTR) ], 3)) 
                    Else
                        jItems[cId] := (cAliasAA1)->&(cRealName)
                    EndIf
                Next nX

                self:appendData(jItems)
            EndIf

            (cAliasAA1)->(DBSkip())
        EndDo

        self:setHasNext(!(cAliasAA1)->(Eof())) 

        (cAliasAA1)->(DBCloseArea())
        oStatement:Destroy()
        oStatement := nil
    EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class AtendentesReportsBusinessObject
self:aliasToSchema("AA1", self:aFields)
self:addProperty("RA_TPCONTR", "Tipo de contrato", "string", "Tp.Cont.Trab", "RA_TPCONTR")
Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} DataConverte
Exemplo de função para converter a data para o formato 'YYYYMMDD'

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
Static Function DataConverte( cData )
Local cDataConvertida := ""

// Remover a parte de hora e fuso horário da data
cData := SubStr(cData, 1, 10)

// Substituir os separadores "-" por vazio
cDataConvertida := StrTran(cData, "-", "")

Return STOD(cDataConvertida) 
