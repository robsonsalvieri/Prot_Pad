#include 'msobject.ch'
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tecsv003.ch"

namespace custom.postosvagos

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATEC", tables="TFJ,TFL,TFF", name="Postos Vagos/Descobertos", country="BRA", initialRelease="12.1.2310")


//-------------------------------------------------------------------
/*{Protheus.doc} PostosvagosBusinessObject
Classe para geracao de relatorio de postos vagos/descobertos, dentro do SmartView. TECR988

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
class PostosvagosBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDisplayName() as character
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    protected data aFieldsTFJ  as array
    protected data aFieldsTFL  as array
    protected data aFieldsTFF  as array
	protected data lExistPergunte as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Metodo de instancia da classe

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class PostosvagosBusinessObject
_Super:new()

self:appendArea(STR0001)//"Gestão de serviços"

self:aFieldsTFJ := {"TFJ_CONTRT", "TFJ_CONREV","TFJ_CODIGO"}
self:aFieldsTFL := {"TFL_LOCAL","TFL_DTINI","TFL_DTFIM","TFL_CODPAI","TFL_CODIGO", "TFL_DESCRI"}
self:aFieldsTFF := {"TFF_COD","TFF_ESCALA","TFF_FUNCAO","TFF_QTDVEN","TFF_QTPREV","TFF_PERINI","TFF_PERFIM","TFF_CODPAI","TFF_CALEND"}

self:lExistPergunte := self:setPergunte("TECR988")

if !self:lExistPergunte
	self:setErrorStatus(400, STR0002, STR0003) //"Pergunte não encontrado." "Grupo de perguntas TECR988 não cadastrado na base. Contate o administrador do sistema."
endif

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Metodo que retorna o nome de exibição do objeto de negócio

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDisplayName() as character class PostosvagosBusinessObject
return STR0004 //"Postos Vagos/Descobertos"

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class PostosvagosBusinessObject
return STR0005 //"Relatório de Postos Vagos/Descobertos"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna as dados do objeto de negócio

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object class PostosvagosBusinessObject
    Local aAllFields as array
    Local cAlias     as character
    Local cId        as character
    Local cQuery     as character
	Local dData      as date
	Local dDataFim   as date
    Local jItems     as json
	local jParams    as json
    Local nX         as numeric
    Local oQuery     as object

	If !self:lExistPergunte
        Return self:oData
    Else
		//Método para retorno do json dos parâmetros:
        jParams := oFilter:getParameters()

		MV_PAR01 := jParams["MV_PAR01"][1]
		MV_PAR02 := jParams["MV_PAR02"][1]
		MV_PAR03 := jParams["MV_PAR03"][1]
		MV_PAR04 := jParams["MV_PAR04"][1]
		MV_PAR05 := jParams["MV_PAR05"][1]
		MV_PAR06 := jParams["MV_PAR06"][1]
		MV_PAR07 := jParams["MV_PAR07"][1]
		MV_PAR08 := jParams["MV_PAR08"][1]
		MV_PAR09 := TecConvData(jParams["MV_PAR09"][1])
		MV_PAR10 := TecConvData(jParams["MV_PAR10"][1])
		MV_PAR11 := Iif(ValType(jParams["MV_PAR11"][1]) == "C", Val(jParams["MV_PAR11"][1]), jParams["MV_PAR11"][1])

		dData    := MV_PAR09
		dDataFim := MV_PAR10

		cQuery := Rt988QryCon(oFilter:getSQLExpression())//Query de contrato
	
		cQuery := ChangeQuery(cQuery)
		oQuery := FwExecStatement():New(cQuery)

		cAlias := oQuery:OpenAlias()

		//Seta a quantidade de itens por página (Default 100)
		aAllFields  := self:getStructFields()

		While !(cAlias)->(Eof())
			//Periodo do posto
			dIniPosto := sToD((cAlias)->TFF_PERINI)
			dFimPosto := sToD((cAlias)->TFF_PERFIM)
			
			dData    := Iif(dIniPosto > dData, dIniPosto, dData)
			dDataFim := Iif(dFimPosto > dDataFim, dDataFim, dFimPosto)
			
			jItems := JsonObject():new()

			For nX := 1 To Len(aAllFields)
				cId := aAllFields[nX]:getName()
				cRealName := aAllFields[nX]:getRealName()
			
				If aAllFields[nX]:getType() == "date"
					jItems[cId] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cRealName))
				Else
					jItems[cId] := (cAlias)->&(cRealName)
				EndIf
			Next nX
			//Dias da semana
			FormatNestedJson(dData, dDataFim, @jItems, (cAlias)->TFJ_CONTRT,(cAlias)->TFL_LOCAL,(cAlias)->TFF_COD,(cAlias)->TFF_ESCALA,(cAlias)->TFF_QTDVEN,(cAlias)->TFF_CALEND)//verificar ncount
			
			self:appendData(jItems)
			
			(cAlias)->(DBSkip())
			dData := MV_PAR09 //Volta a data inicial do parametro, para o proximo contrato
		EndDo

		self:setHasNext(!(cAlias)->(Eof()))

		(cAlias)->(DBCloseArea())
		oQuery:Destroy()
		oQuery := nil
	EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos

@author Breno.Gomes
@since 25/04/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class PostosvagosBusinessObject
	Local aFldDiaSemana as array
	Local aFldSubTotal  as array

	self:aliasToSchema("TFJ", self:aFieldsTFJ)
	self:aliasToSchema("TFL", self:aFieldsTFL)
	self:aliasToSchema("TFF", self:aFieldsTFF)

	self:addProperty("ABS_DESCRI", "Descrição", "string", "Descricao", "TFL_DESCRI")
	self:addProperty("TDW_DESC"  , "Desc. Escala", "string", "Desc Escala", "TDW_DESC")
	self:addProperty("RJ_DESC"   , "Desc. Função", "string", "Desc Funcao", "RJ_DESC")
	
	aFldDiaSemana := Array(0)
	aFldSubTotal := Array(0)
	//Posições do array = 1-Id         | 2-Description           | 3-Type  | 4-DisplayName
	aAdd(aFldDiaSemana, {"FK_AGENDA"   , "Data Agenda"           , "date"  , "Data Agenda"           })
	aAdd(aFldDiaSemana, {"FK_DIASEMANA", "Dia Semana"            , "string", "Dia Semana"            })
	aAdd(aFldDiaSemana, {"FK_FLTEFV"   , "Qtd. Não Alocados"     , "number", "Qtd. Não Alocados"     })
	aAdd(aFldDiaSemana, {"FK_AUSEFV"   , "Qtd. Efetivos Ausentes", "number", "Qtd. Efetivos Ausentes"})
	aAdd(aFldDiaSemana, {"FK_DESCOB"   , "Qtd. Descoberta"       , "number", "Qtd. Descoberta"       })
	aAdd(aFldDiaSemana, {"FK_QTDVEN"   , "Qtd. Pessoas"          , "number", "Qtd. Pessoas"          })

	aAdd(aFldSubTotal, {"TotalNaoAlocado", "Total nao alocado"    , "number", "Total nao alocado"    })
	aAdd(aFldSubTotal, {"TotalEfetivoAus", "Total efetivo ausente", "number", "Total efetivo ausente"})
	aAdd(aFldSubTotal, {"TotalDescoberto", "Total descoberto"     , "number", "Total descoberto"     })
	
	self:addNestedProperty("SubTotal", "SubTotal", "SubTotal",, aFldSubTotal)
	self:addNestedProperty("DiaSemanaDetalhes", "DiaSemanaDetalhes", "DiaSemanaDetalhes",, aFldDiaSemana)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FormatNestedJson
Formato o json dos objetos aninhados - Dias da semana

@param dData     date, Data inicial da semana
@param dDataFim  date, Data final da semana
@param jItems    json, Json de resposta na requisição
@param cContrato character, Código do contrato
@param cLocal    character, Código do local
@param cCodTFF   character, Código da TFF
@param cEscala   character, Código da escala
@param nQtdVen   numeric, quantidade de pessoas 
@param cCalend   character, Código do calendário

@author Breno.gomes
@since 15/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
Static Function FormatNestedJson( dData as date, dDataFim as date, jItems as json, cContrato as character, cLocal as character,;
								  cCodTFF as character, cEscala as character, nQtdVen as numeric, cCalend as character)
Local dDataAgenda   as character
Local nQtdDescob    as numeric
Local cDiaSemana    as numeric
Local nQtdNaoAloc   as numeric
Local nQtdAusente   as numeric
Local aQuantidades	as array
Local nTotalNaoAloc as numeric
Local nTotalAusente as numeric
Local nTotalDescob  as numeric

Default nQtdVen := 0

	nTotalNaoAloc := 0
	nTotalAusente := 0
	nTotalDescob  := 0
	jItems["DiaSemanaDetalhes"] := {}
	jItems["SubTotal"] := {}
	While ( dData <= dDataFim )
		aQuantidades := Rt988GetAg(DToS(dData),cContrato, cLocal, cCodTFF, cEscala, nQtdVen,, cCalend)
		nQtdNaoAloc  := aQuantidades[1]
		nQtdAusente  := aQuantidades[2]

		If nQtdNaoAloc > 0 .Or. nQtdAusente > 0
			nQTDPessoas := nQtdVen
			cDiaSemana  := DiaSemana(dData)
			dDataAgenda := dToS(dData)
			nQtdDescob  := nQtdNaoAloc + nQtdAusente
			
			nTotalNaoAloc += nQtdNaoAloc
			nTotalAusente += nQtdAusente
			nTotalDescob  += nQtdDescob
			aAdd(jItems["DiaSemanaDetalhes"], {"FK_AGENDA": totvs.framework.treports.date.stringToTimeStamp(&("dDataAgenda")),;
												"FK_DIASEMANA": cDiaSemana,;
												"FK_FLTEFV": nQtdNaoAloc,;
												"FK_AUSEFV": nQtdAusente,;
												"FK_DESCOB": nQtdDescob,;
												"FK_QTDVEN": nQTDPessoas})
		EndIf
		dData++
	EndDo

	aAdd(jItems["SubTotal"],;
				 { "TotalNaoAlocado": nTotalNaoAloc,;
				   "TotalEfetivoAus": nTotalAusente,;
				   "TotalDescoberto": nTotalDescob})
Return
