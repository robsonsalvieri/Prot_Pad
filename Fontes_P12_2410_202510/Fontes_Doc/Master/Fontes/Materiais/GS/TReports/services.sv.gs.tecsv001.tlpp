#include "msobject.ch"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tecsv001.ch"

namespace custom.atendentes

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATEC", tables="AA1,SRA,ABB", name="Funcionários - GS", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc} FuncionarioReportsBusinessObject 
Classe para geracao de relatorio de funcionários com agenda que não estejam demitidos, dentro do SmartView.

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
class FuncionarioReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()            as object
    public method getDisplayName() as character
    public method getDescription() as character
    public method getData()        as object
    public method getSchema()      as object

    protected data aFields        as array
    protected data lExistPergunte as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Metodo de instancia da classe

@author Julia.marcela
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class FuncionarioReportsBusinessObject
_Super:new()

self:appendArea(STR0001)//"Gestão de serviços"

self:aFields    := {"AA1_CODTEC", "AA1_NOMTEC", "AA1_CDFUNC", "AA1_FUNFIL", "AA1_ALOCA","RA_TPCONTR"}

self:lExistPergunte := self:setPergunte("TECR020A")

if !self:lExistPergunte
     self:setErrorStatus(400, STR0002, STR0003)//"Pergunte não encontrado." "Grupo de perguntas TECR020A não cadastrado na base. Contate o administrador do sistema."
endif

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Metodo que retorna o nome de exibição do objeto de negócio

@author Julia.marcela
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDisplayName() as character class FuncionarioReportsBusinessObject
return STR0004//"Atendentes com agenda."

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio

@author Julia.marcela
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class FuncionarioReportsBusinessObject
return STR0005 //"Relatórios de atendentes com agenda na mesa operacional."


//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna as dados do objeto de negócio

@author Julia.marcela
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class FuncionarioReportsBusinessObject
    Local aAloca     as array
    Local aTpContr   as array
    local cAlias     as character
    local cId        as character
    local cQuery     as character
    local cRealName  as character
    local jItems     as json
    local jParams    as json
    Local nOrder     as numeric
    local nX         as numeric
    Local oStatement as object

    aAloca   := StrTokArr(GetSx3Cache('AA1_ALOCA' ,'X3_CBOX'),';')
    aTpContr := StrTokArr(GetSx3Cache('RA_TPCONTR','X3_CBOX'),';')

    If !self:lExistPergunte
        return self:oData
    Else
        nOrder := 1
        cQuery := " SELECT ?, SRA.RA_DEMISSA"
        cQuery += " FROM ? ABB "
        cQuery +=        " LEFT JOIN ? AA1 "
        cQuery +=               " ON AA1.AA1_FILIAL = ?"
        cQuery +=                  " AND AA1.AA1_CODTEC = ABB.ABB_CODTEC"
        cQuery +=                  " AND AA1.D_E_L_E_T_ = ' '"
        cQuery +=        " LEFT JOIN ? SRA "
        cQuery +=               " ON SRA.RA_FILIAL = AA1.AA1_FUNFIL"
        cQuery +=                  " AND SRA.RA_MAT = AA1.AA1_CDFUNC"
        cQuery +=                  " AND SRA.RA_ADMISSA IS NOT NULL"
        cQuery +=                  " AND SRA.RA_ADMISSA <= ?"
        cQuery +=                  " AND SRA.D_E_L_E_T_ = ' '"
        cQuery += " WHERE ABB.ABB_FILIAL = ?"

        //Método para retorno do json dos parâmetros:
        jParams := oFilter:getParameters()

        //Filtro de Datas:
        If !Empty(jParams["MV_PAR01"][1]) .AND. !Empty(jParams["MV_PAR02"][1])
            cQuery += " AND ABB.ABB_DTINI BETWEEN ? AND ?"
        EndIf

        //Filtro de Atendentes:
        If !Empty(jParams["MV_PAR03"][1]) .AND. !Empty(jParams["MV_PAR04"][1])
            cQuery += " AND AA1.AA1_CODTEC BETWEEN ? AND ?"
        EndIf

        //Aplicacao de filtros do SmartView:
        If oFilter:hasFilter()
            cQuery += " AND ?"
        EndIf

        cQuery +=     " AND ABB.D_E_L_E_T_ = '' "
        //Ordena os registros:
        cQuery += " GROUP BY ?, SRA.RA_DEMISSA"

        //Execucao da query:
        oStatement := FwExecStatement():new(cQuery)

        //Seta as 'interrogações' ? do cQuery
        oStatement:SetUnsafe(nOrder++, self:getSQLFields(,,,.T.))
        oStatement:SetUnsafe(nOrder++, RetSQLName("ABB"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("AA1"))
        oStatement:SetString(nOrder++, xFilial("AA1"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("SRA"))
        oStatement:SetDate(nOrder++, dDatabase)
        oStatement:SetString(nOrder++, xFilial("ABB"))

        //Filtro de Datas:
        If !Empty(jParams["MV_PAR01"][1]) .AND. !Empty(jParams["MV_PAR02"][1])
            oStatement:SetDate(nOrder++, TecConvData(jParams["MV_PAR01"][1]))
            oStatement:SetDate(nOrder++, TecConvData(jParams["MV_PAR02"][1]))
        EndIf

        //Filtro de Atendentes:
        If !Empty(jParams["MV_PAR03"][1]) .AND. !Empty(jParams["MV_PAR04"][1])
            oStatement:SetString(nOrder++, jParams["MV_PAR03"][1])
            oStatement:SetString(nOrder++, jParams["MV_PAR04"][1])
        EndIf

        // Aplicacao de filtros do SmartView 
        If oFilter:hasFilter()
            oStatement:SetUnsafe(nOrder++, oFilter:getSQLExpression())
        EndIf

        //Order By
        oStatement:SetUnsafe(nOrder++, self:getSQLFields(,,,.T.))

        //Arruma a query para o banco conectado:
        cQuery := oStatement:GetFixQuery()

        //Abre o alias
        cAlias := oStatement:OpenAlias()

        //Pega os campos da estrutura:
        aAllFields := self:getStructFields()
       
        While !(cAlias)->(Eof())
            If EMPTY((cAlias)->RA_DEMISSA) .OR. (cAlias)->RA_DEMISSA >= DTOS(dDatabase) 
                jItems := JsonObject():new()

                For nx := 1 To Len(aAllFields)
                    cId         := aAllFields[nX]:getName()
                    cRealName   := aAllFields[nX]:getRealName()
                    If cId == 'AA1_ALOCA'
                        jItems[cId] := alltrim(substr( aAloca[ val((cAlias)->AA1_ALOCA) ], 3)) 
                    ElseIf cId == 'RA_TPCONTR' .AND. !Empty((cAlias)->RA_TPCONTR)
                        jItems[cId] := alltrim(substr( aTpContr[ val((cAlias)->RA_TPCONTR) ], 3)) 
                    Else
                        jItems[cId] := (cAlias)->&(cRealName)
                    EndIf
                Next nX

                self:appendData(jItems)
            EndIf
            (cAlias)->(DBSkip())
        EndDo
 
        self:setHasNext(!(cAlias)->(Eof())) 

        (cAlias)->(DBCloseArea())
        oStatement:Destroy()
        oStatement := nil
    EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a estrutura dos campos

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class FuncionarioReportsBusinessObject
    self:aliasToSchema("AA1", self:aFields)
    self:addProperty("RA_TPCONTR", "Tipo de contrato", "string", "Tp.Cont.Trab", "RA_TPCONTR")
Return self:oSchema

