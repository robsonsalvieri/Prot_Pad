#INCLUDE "PROTHEUS.CH"
#INCLUDE "TECA700.CH"

#DEFINE  CADASTRO STR0001 //"Projetos"  
#DEFINE  NUMETAPAS 300

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TECA700  ³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de atualizacao dos Projetos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void TECA700(void)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                         ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA700( cRotina )

Private cCadastro	:= CADASTRO
Private aRotina		:= MenuDef()
							
DbSelectArea("ABH")

If ValType( cRotina ) <> "C"
	DbSetOrder(1)
	DbSeek(xFilial())    
	mBrowse( 6, 1,22,75,"ABH" )            
Else 	         
	cRotina := Upper( cRotina ) 
	If !Empty( nScan := AScan( aRotina, { |x| Upper( x[2] ) == cRotina } ) ) 
		cRoda := cRotina + "( 'ABH', ABH->( Recno() ), " + Str( nScan, 2 ) + ") " 		
		Eval( { || &( cRoda ) } ) 
	EndIf 	
EndIf

DbSelectArea("ABH")
DbSetOrder(1)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ MenuDef  ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 11.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := {	{ STR0002	,"AxPesqui"		,0	,1	,0	,.F.	}	,;	//"Pesquisar"
						{ STR0003	,"AT700Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
						{ STR0004	,"AT700Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
						{ STR0005	,"AT700Alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
						{ STR0006	,"AT700Exclu"	,0	,5	,0	,.T.	}	,;	//"Exclui"
						{ STR0014	,"AT700Progr"	,0	,5	,0	,.T.	}	,;	//"Programacao"
						{ STR0041	,"AT700Ped"		,0	,5	,0	,.T.	}	,;	//"Pedido"
						{ STR0028	,"MsDocument"	,0	,4	,0	,.T.	}	}	//"Conhecimento"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700Inclu³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de Projetos                           ³±±    
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT700Inclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/01/2007³ Conrado Q.    ³Bops 115754: Montagem do aCols e aHeader    ³±±
±±³          ³               ³adaptada para utilização do Walk-Thru.      ³±±
±±³15/06/2007³ Conrado Q.    ³Bops 122057: Correção no preenchimento do   ³±±
±±³          ³               ³aHeader.                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700Inclu(cAlias,nReg,nOpcx)

Local aObjects	:= {}
Local aInfo		:= {}
Local aSize		:= MsAdvSize() 							
Local aArea		:= GetArea()
Local aPosGet	:= {}
Local cPict		:= "9999:99"
Local nUsado	:= 0
Local nPosItem	:= 0
Local nSaveSX8	:= GetSX8Len()
Local nOpcA		:= 0
Local oGetD		:= Nil                
Local oDlg		:= Nil
Local oSay1		:= Nil
Local oSay2		:= Nil
Local oSay3		:= Nil

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT700INC")
	Execblock("AT700INC",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aHeaderABI	:= {}
Private aHeaderABJ	:= {} 
Private aCols		:= {}
Private aColsABJ	:= {} 
Private nOpcF4		:= nOpcX

INCLUI := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("ABH")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField,.T.)
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT700Monta()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderABI)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderABI,{|x| AllTrim(x[2])=="ABI_ITEM"})
aAdd(aCols,Array(nUsado+1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria elemento vazio                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AEval( aHeader, { |x,y| If( IsHeadRec(x[2]) .OR. IsHeadAlias(x[2]), Nil, aCols[ 1, y ] := CriaVar( x[2] ) ) } ) 

aCols[1,GdFieldPos("ABI_ITEM")]		:= "01"
aCols[1,GdFieldPos("ABI_ALI_WT")]	:= "ABI"
aCols[1,GdFieldPos("ABI_REC_WT")]	:= 0
aCols[1,nUsado+1]					:= .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Habilita a Tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,{||AT700F4(oGetD)})

aObjects := {} 
aAdd( aObjects, { 100, 100, .T., .T. } )
aAdd( aObjects, { 100, 100, .T., .T. } )
aAdd( aObjects, { 100,  10, .T., .F. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE DIALOG oDlg TITLE cCadastro From aSize[ 7 ], 0 to aSize[ 6 ], aSize[ 5 ] PIXEL STYLE WS_DLGFRAME

EnChoice( "ABH" ,nReg,nOpcx,,,,,aPosObj[ 1 ],,3,,,,,,.T.)
oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT700LinOk","AT700TudOk","+ABI_ITEM",.T.,,,,NUMETAPAS)
    
nPosYSay := aPosObj[3,1] 
nPosXSay := aPosObj[3,2]                          

aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
			{{00,060,100,160,200,260}} )

@ nPosYSay,nPosXSay                SAY STR0011 SIZE 60,09 OF oDlg PIXEL // "Horas Previstas: "
@ nPosYSay,nPosXSay + aPosGet[1,2] SAY oSay1 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ nPosYSay,nPosXSay + aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL // "Horas realizadas: "
@ nPosYSay,nPosXSay + aPosGet[1,4] SAY oSay2 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ nPosYSay,nPosXSay + aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL // "Saldo: "
@ nPosYSay,nPosXSay + aPosGet[1,6] SAY oSay3 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL

oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
							oSay2:SetText(n2),;
							oSay3:SetText(n3),;
							oSay3:SetColor( If( HoraToInt(n3,4)<0, CLR_HRED, CLR_HBLUE ), oDlg:nClrPane ) }
At700Total(oDlg,.F.)

ACTIVATE MSDIALOG oDlg ON INIT AT700Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)

If ( nOpcA == 1 )
	Begin Transaction
		If ( AT700Grava(1) )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSX8()
			EndDo
		EndIf
	End Transaction
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)          

RestArea( aArea ) 

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700Visua³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Visualizacao de Projetos                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT700Visua(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700Visua(cAlias,nReg,nOpcx)

Local aArea      := GetArea()
Local aAux       := {}                   
Local aObjects   := {}
Local aInfo      := {}
Local aSize      := MsAdvSize() 							
Local aPosGet    := {}

Local cPict      := "9999:99"

Local nUsado     := 0
Local nOpcA      := 0
Local nPosItem   := 0        

Local oGetD
Local oDlg
Local oSay1
Local oSay2
Local oSay3

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT700VIS")
	Execblock("AT700VIS",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderABI := {}, aHeaderABJ := {} 
Private aCols  := {} , aColsABJ   := {} 
Private nOpcF4 := nOpcX 

INCLUI := .F.   
ALTERA := .F. 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("ABH")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := ABH->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT700Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderABI)
nPosItem := GDFieldPos( "ABJ_SUBITE", aHeaderABJ ) 

AT700ACols( @aHeader, @aCols, @aColsABJ, nPosItem ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Habilita a Tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,{||AT700F4(oGetD)})

aObjects := {} 
aAdd( aObjects, { 100, 100, .T., .T. } )
aAdd( aObjects, { 100, 100, .T., .T. } )
aAdd( aObjects, { 100,  10, .T., .F. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE DIALOG oDlg TITLE cCadastro From aSize[ 7 ], 0 to aSize[ 6 ], aSize[ 5 ] PIXEL STYLE WS_DLGFRAME

EnChoice( "ABH" ,nReg,nOpcx,,,,,aPosObj[ 1 ],,3,,,,,,.T.)
oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT700LinOk","AT700TudOk","+ABI_ITEM",.F.)
    
nPosYSay := aPosObj[3,1] 
nPosXSay := aPosObj[3,2] 

aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
			{{00,060,100,160,200,260}} )

@ nPosYSay,nPosXSay                SAY STR0011 SIZE 60,09 OF oDlg PIXEL // "Horas Previstas: "
@ nPosYSay,nPosXSay + aPosget[1,2] SAY oSay1 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ nPosYSay,nPosXSay + aPosget[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL // "Horas realizadas: "
@ nPosYSay,nPosXSay + aPosget[1,4] SAY oSay2 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ nPosYSay,nPosXSay + aPosget[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL // "Saldo: "
@ nPosYSay,nPosXSay + aPosget[1,6] SAY oSay3 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL

oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
							oSay2:SetText(n2),;
							oSay3:SetText(n3),;
							oSay3:SetColor( If( HoraToInt(n3,4)<0, CLR_HRED, CLR_HBLUE ), oDlg:nClrPane ) }
At700Total(oDlg,.F.)

ACTIVATE MSDIALOG oDlg ON INIT AT700Bar(oDlg,{||oDlg:End()},{||oDlg:End()},nOpcx,oGetD)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)

RestArea( aArea ) 

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700Alter³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Projetos                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT700Alter(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/01/2007³ Conrado Q.    ³Bops 115754: Montagem do aCols e aHeader    ³±±
±±³          ³               ³adaptada para utilização do Walk-Thru.      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT700Alter(cAlias,nReg,nOpcx)

Local aArea      := GetArea() 
Local aAux       := {}
Local aTravas    := {}
Local aObjects   := {}
Local aInfo      := {}
Local aSize      := MsAdvSize() 							
Local aPosGet    := {}

Local cPict      := "9999:99"

Local lTravas    := .T.

Local nUsado     := 0
Local nOpcA      := 0
Local nPosItem   := 0
Local nTotAtend  := 0
Local nSaveSX8	 := GetSX8Len()
Local nCntFor    := 0

Local oDlg
Local oGetD

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT700ALT")
	Execblock("AT700ALT",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderABI := {}, aHeaderABJ := {} 
Private aCols  := {} , aColsABJ   := {} 
Private nOpcF4 := nOpcX
            
INCLUI := .F. 
ALTERA := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("ABH")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := ABH->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava Registros                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SoftLock("ABH") )
	aAdd(aTravas,{ Alias() , RecNo() })
Else
	lTravas := .F.
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT700Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderABI)            
nPosItem := aScan(aHeaderABI,{|x| AllTrim(x[2])=="ABI_ITEM"})

nUsado   := Len(aHeader)
DbSelectArea("ABI")
DbSetOrder(1)
DbSeek(xFilial("ABI")+ABH->ABH_PROJET)
While ( !ABI->( Eof() ) .And. xFilial("ABI")    == ABI->ABI_FILIAL .And.;
							ABH->ABH_PROJET == ABI->ABI_PROJET )
	If ( SoftLock("ABI") )
		aAdd(aTravas, { Alias() , RecNo() })
	Else
		lTravas := .F.
	EndIf
	aAdd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := ABI->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "ABI"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aCols[Len(aCols)][nUsado+1] := .F.
    
	aAux := {}
	DbSelectArea("ABJ")
	DbSetOrder(1)
	If ( DbSeek(xFilial("ABJ")+ABH->ABH_PROJET+ABI->ABI_ITEM) )
		While ( !Eof() .And. xFilial("ABJ") == ABJ->ABJ_FILIAL .And.;
								M->ABH_PROJET== ABJ->ABJ_PROJET .And.;
								ABI->ABI_ITEM== ABJ->ABJ_ITEM )
								
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Percorre os atendimentos desta tarefa para apurar o valor de ³
			//³ horas realizado.                                             ³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			nHorasReal := 0 
			nTotAtend  := 0 	
			
			dIniReal   := CToD( "  /  /  " ) 
			dFimReal   := CToD( "  /  /  " ) 
			
			AB9->( DbSetOrder( 4 ) ) 			
			cSeekAB9 := xFilial( "AB9" )+ABJ->ABJ_PROJET+ABJ->ABJ_ITEM+ABJ->ABJ_SUBITE
			
			If AB9->( DbSeek( cSeekAB9 ) ) 
			
				dIniReal   := CToD( "31/12/49" ) 
				dFimReal   := CToD( "  /  /  " ) 
			
				While !AB9->( Eof() ) .And. cSeekAB9 == AB9->AB9_FILIAL + ;
						AB9->AB9_TAREFA  
					nHorasReal += SubtHoras( AB9->AB9_DTINI, AB9->AB9_HRINI,;
						AB9->AB9_DTFIM, AB9->AB9_HRFIM )

					dIniReal   := If(AB9->AB9_DTINI<dIniReal,AB9->AB9_DTINI,dIniReal) 
					If ABJ->ABJ_SITUAC == "1"
						dFimReal   := If(AB9->AB9_DTFIM>dFimReal,AB9->AB9_DTFIM,dFimReal) 		
					EndIf 	
					nTotAtend++ 
					AB9->( DbSkip() ) 
				EndDo
			EndIf 
								
			aAdd(aAux,Array(Len(aHeaderABJ)+1))
			For nCntFor := 1 To Len(aHeaderABJ)
				If IsHeadRec(aHeaderABJ[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := ABJ->(RecNo())
				Elseif IsHeadAlias(aHeaderABJ[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "ABJ"
				Elseif ( aHeaderABJ[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderABJ[nCntFor][2]))
				Else	
					Do Case
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_TREALI" 
						aAux[Len(aAux)][nCntFor] := IntToHora( nHorasReal, 4,.T. )  									 
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_NATEND" 
						aAux[Len(aAux)][nCntFor] := nTotAtend
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_INIREA" 
						aAux[Len(aAux)][nCntFor] := dIniReal 
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_FIMREA" 
						aAux[Len(aAux)][nCntFor] := dFimReal 
					Otherwise  		
						aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderABJ[nCntFor][2])
					EndCase 								
				EndIf 	
					
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderABJ)+1] := .F.
			DbSelectArea("ABJ")
			DbSkip()
		EndDo
		aAdd(aColsABJ,aClone(aAux))
	Else
		aAdd(aAux,Array(Len(aHeaderABJ)+1))
		For nCntFor := 1 To Len(aHeaderABJ)
			If IsHeadRec(aHeaderABJ[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := ABJ->(RecNo())
			Elseif IsHeadAlias(aHeaderABJ[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "ABJ"
			Else
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderABJ[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderABJ)+1] := .F.
		aAdd(aColsABJ,aClone(aAux))
	EndIf

	ABI->( DbSkip() ) 
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Habilita a Tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,{||AT700F4(oGetD)})

If ( lTravas )
	
	aObjects := {} 
	aAdd( aObjects, { 100, 100, .T., .T. } )
	aAdd( aObjects, { 100, 100, .T., .T. } )
	aAdd( aObjects, { 100,  10, .T., .F. } )
	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	DEFINE DIALOG oDlg TITLE cCadastro From aSize[ 7 ], 0 to aSize[ 6 ], aSize[ 5 ] PIXEL STYLE WS_DLGFRAME
	
	EnChoice( "ABH" ,nReg,nOpcx,,,,,aPosObj[ 1 ],,3,,,,,,.T.)
	oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT700LinOk","AT700TudOk","+ABI_ITEM",.T.,,,,NUMETAPAS)
	    
	nPosYSay := aPosObj[3,1] 
	nPosXSay := aPosObj[3,2] 
	
	aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
			{{00,060,100,160,200,260}} )
	
	@ nPosYSay,nPosXSay                SAY STR0011 SIZE 60,09 OF oDlg PIXEL // "Horas Previstas: "
	@ nPosYSay,nPosXSay + aPosGet[1,2] SAY oSay1 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
	@ nPosYSay,nPosXSay + aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL // "Horas realizadas: "
	@ nPosYSay,nPosXSay + aPosGet[1,4] SAY oSay2 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
	@ nPosYSay,nPosXSay + aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL // "Saldo: "
	@ nPosYSay,nPosXSay + aPosGet[1,6] SAY oSay3 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
    
	oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
								oSay2:SetText(n2),;
								oSay3:SetText(n3),;
								oSay3:SetColor( If( HoraToInt(n3,4)<0, CLR_HRED, CLR_HBLUE ), oDlg:nClrPane ) }
								
	At700Total(oDlg,.F.)
		
	ACTIVATE MSDIALOG oDlg ON INIT AT700Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)
EndIf
If ( nOpcA == 1 )
	Begin Transaction
		If ( AT700Grava(2)  )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSX8()
			EndDo
		EndIf
	End Transaction
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Destrava os registros                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To Len(aTravas)
	DbSelectArea(aTravas[nCntFor,1])
	DbGoto(aTravas[nCntFor,2])
	MsUnLock()
Next nCntFor
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)           

RestArea( aArea ) 

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700Exclu³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Exclusao  de Projetos                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT700Exclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700Exclu(cAlias,nReg,nOpcx)

Local aArea    := GetArea() 
Local aAux     := {}
Local aTravas  := {}
Local aObjects := {}
Local aInfo    := {}
Local aSize    := MsAdvSize() 							
Local aPosGet  := {}

Local cPict    := "9999:99"

Local lTravas  := .T.
Local lRetorno := .T. 
Local lExclui  := .T.

Local nPosItem := 0
Local nUsado   := 0
Local nCntFor  := 0
Local nOpcA    := 0
Local nSaveSX8	:= GetSX8Len()

Local oGetD
Local oDlg

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT700EXC")
	Execblock("AT700EXC",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderABI := {}, aHeaderABJ := {}
Private aCols  := {} , aColsABJ   := {} 
Private nOpcF4 := nOpcX

INCLUI := .F. 
ALTERA := .F. 

If ( !Eof() )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aStruField := FWSX3Util():GetAllFields("ABH")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			If GetSx3Cache(cField,"X3_CONTEXT")=="V"
				M->&(cField) := CriaVar(cField,.T.)
			Else
				M->&(cField) := ABH->(FieldGet(FieldPos(cField)))
			EndIf
		Next nC
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Trava Registros                                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( SoftLock("ABH") )
		aAdd(aTravas,{ Alias() , RecNo() })
	Else
		lTravas := .F.
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AT700Monta()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aCols                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aHeader  := aClone(aHeaderABI)
	nPosItem := GDFieldPos( "ABJ_SUBITE", aHeaderABJ ) 
	
	AT700ACols( @aHeader, @aCols, @aColsABJ, nPosItem, @lExclui ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,{||AT700F4(oGetD)})
	
	If ( lTravas .And. lExclui )

		aObjects := {} 
		aAdd( aObjects, { 100, 100, .T., .T. } )
		aAdd( aObjects, { 100, 100, .T., .T. } )
		aAdd( aObjects, { 100,  10, .T., .F. } )
		
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
		
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		DEFINE DIALOG oDlg TITLE cCadastro From aSize[ 7 ], 0 to aSize[ 6 ], aSize[ 5 ] PIXEL STYLE WS_DLGFRAME
		
		EnChoice( "ABH" ,nReg,nOpcx,,,,,aPosObj[ 1 ],,3,,,,,,.T.)
		oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT700LinOk","AT700TudOk","+ABI_ITEM",.F.)
		    
		nPosYSay := aPosObj[3,1] 
		nPosXSay := aPosObj[3,2] 
		
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],310,;
		{{00,060,100,160,200,260}} )
		
		@ nPosYSay,nPosXSay                SAY STR0011 SIZE 60,09 OF oDlg PIXEL // "Horas Previstas: "
		@ nPosYSay,nPosXSay + aPosGet[1,2] SAY oSay1 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
		@ nPosYSay,nPosXSay + aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL // "Horas realizadas: "
		@ nPosYSay,nPosXSay + aPosGet[1,4] SAY oSay2 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
		@ nPosYSay,nPosXSay + aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL // "Saldo: "
		@ nPosYSay,nPosXSay + aPosGet[1,6] SAY oSay3 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
		
		oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
									oSay2:SetText(n2),;
									oSay3:SetText(n3),;
									oSay3:SetColor( If( HoraToInt(n3,4)<0, CLR_HRED, CLR_HBLUE ), oDlg:nClrPane ) }
		At700Total(oDlg,.F.)
		
		ACTIVATE MSDIALOG oDlg ON INIT AT700Bar(oDlg,{||nOpca:=1,oDlg:End()},{||oDlg:End()},nOpcx,oGetD)
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
			If ( AT700Grava(3)  )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				EndDo
			EndIf
		End Transaction
	EndIf
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSX8()
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Destrava os registros                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aTravas)
		DbSelectArea(aTravas[nCntFor,1])
		DbGoto(aTravas[nCntFor,2])
		MsUnLock()
	Next nCntFor

Else 
	lRetorno := .F. 
EndIf 	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)           
	
RestArea( aArea ) 

Return( lRetorno ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700MONTA  ³ Autor ³Sergio Silveira      ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta aHeader                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT700MONTA(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/01/2007³ Conrado Q.    ³Bops 115754: Montagem do aCols e aHeader    ³±±
±±³          ³               ³adaptada para utilização do Walk-Thru.      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700MONTA()

Local aArea    := GetArea()
Local aAreaABI := ABI->( GetArea() )
Local aAreaABJ := ABJ->( GetArea() )
Local aHeader  := {}
Local nUsado   := 0

Local aStruField := {}
Local cField     := ""
Local nC         := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do ABI                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}

aStruField := FWSX3Util():GetAllFields("ABI")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"ABI",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("ABI"	,aHeader)
    
aHeaderABI := aClone(aHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do ABJ                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}

aStruField := FWSX3Util():GetAllFields("ABJ")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			nUsado++
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"ABJ",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("ABJ", aHeader)

aHeaderABJ := aClone(aHeader)                              

ABI->( RestArea( aAreaABI ) ) 
ABJ->( RestArea( aAreaABJ ) ) 

RestArea( aArea ) 

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700LinOk  ³ Autor ³Sergio Silveira      ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Linha na GetDados                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AT700LinOk(Void)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700LinOk(oGet)

Local lRetorno  := .T.
Local nUsado    := Len(aHeader)
Local nPosEtapa := GDFieldPos( "ABI_ETAPA" )           
Local nCntFor   := 0 

If ( !aCols[n][nUsado+1] )
	If (  Empty( GDFieldGet( "ABI_ETAPA" ) ) .Or.;
			Empty( GDFieldGet( "ABI_INIPRV"  ) ) .Or.;
			Empty( GDFieldGet( "ABI_FIMPRV"  ) ) .Or.;
			Empty( GDFieldGet( "ABI_DESCRI" ) ) ) 
		lRetorno := .F.
		Help(" ",1,"AT700LIN01") // Campos obrigatorios zerados 
	EndIf
	If ( lRetorno )
		For nCntFor := 1 To Len(aCols)
			If ( aCols[ n, nPosEtapa ] == aCols[ nCntFor, nPosEtapa ] .And. ;
					n <> nCntFor .And. !aCols[nCntFor, nUsado+1])
				Help(" ",1,"AT700LIN02") // Etapas iguais 
				lRetorno := .F.
			EndIf
		Next nCntFor
	EndIf
	
Else 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se permite excluir a etapa                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existem pedidos de venda                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty( GDFieldGet( "ABI_NUMPV" ) ) 
		lRetorno := .F. 
		Help( " ", 1, "AT700ETPED" ) // Existem pedidos de venda para a etapa 		
	EndIf 	                   
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existem atendimentos                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno 
		AB9->( DbSetOrder( 4 ) ) 			
		If AB9->( DbSeek( xFilial( "AB9" )+ M->ABH_PROJET + GDFieldGet( "ABI_ITEM" ) ) )
			lRetorno := .F. 
			Help( " ", 1, "AT700ETATE" ) // Existem atendimentos para a etapa 		
		EndIf 			
	EndIf 
		
EndIf                   

AT700Total( oGet:oWnd, .F. ) 

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700TudOk  ³ Autor ³Sergio Silveira      ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao geral da GetDados                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AT700TudOk(Void)                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AT700TudOk()

Local lRetorno := .T.
Local nPosDel  := Len( aHeader ) + 1 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se todos os itens estao excluidos                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( AScan( aCols, { |x| !x[ nPosDel ] } ) ) 
	lRetorno := .F. 
	Help( " ", 1, "AT700VAZIO" ) // Nao existe nenhuma etapa !
EndIf                    

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AT700F4    ³ Autor ³Sergio Silveira      ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento da tecla F4 ( Tarefas da etapa )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT700F4(Void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ a,b,c                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Conrado Q.   ³17/11/06³113274³ Utilização indevida de variável nOpcx, ³±± 
±±³              ³        ³      ³ essa pode ser substituida por nOpcF4.  ³±± 
±±³ Conrado Q.   ³15/12/06³115662³ Passado as variaveis aSavAcols, nSavN, ³±± 
±±³              ³        ³      ³ para serem utilizados no P.E. AT700BU2.³±± 
±±³ Conrado Q.   |03/01/07³115754³ Montagem do aCols e aHeader            ³±±
±±³              ³        |      ³ adaptada para utilização do Walk-Thru. ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700F4(oGetD)

Local aArea      := GetArea()					 			// Salva a area atual
Local aSavAcols  := {}							 			// Utilizado para salvar o aCols atual
Local aAux       := {}										// Array auxiliar de uso geral

Local bSetKey    := SetKey(VK_F4,)							// Salva o bloco de código da tecla F4

Local cCadastro  := STR0010 //"Apontamento(s)"				// Título da janela
Local cProjet    := ""										// Código do projeto
Local cEtapa     := "" 							 			// Código da etapa 
Local cProjDesc  := ""										// Descrição do projeto
Local cEtapaDesc := ""                    					// Descrição da etapa
Local cPict      := "9999:99"								// Picture das horas previstas
Local cItem      := GDFieldGet( "ABI_ITEM" ) 	 			// Número da tarefa

Local nOpcA      := 0										// Opção selecionada (Confirmou ou cancelou a tela de apontamentos)
Local nSavN      := If(Type("N")=="U",1,N)					// Utilizado para salvar n anterior
Local nCntFor    := 0										// Utilizado em laço
Local nUsado     := Len(aHeaderABJ)							// Quantidade de campos
Local nItAcols   := If(Type("N")=="U",1,N)					// Utilizado em laço
Local nPosEtapa  := GDFieldPos( "ABI_ETAPA" ) 	 			// Posição da etapa 
Local nPosItem   := GDFieldPos( "ABJ_SUBITE", aHeaderABJ )	// Posição do item
Local nPosEtDesc := GDFieldPos( "ABJ_DESCRI", aHeaderABJ )	// Posição da descrição da etapa

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Troca a opcao para visualizar caso o status nao seja "Normal"         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nOpcGet := If( GDFieldGet( "ABI_SITATU" ) == "1", nOpcF4, 1 ) 

Local oDlg
Local oGetF4      
Local oSay1
Local oSay2
Local oSay3

SetKey(VK_F4,Nil)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Salva o aCols   de Entrada e Desabilita o Paint da Primeira GetDados    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oGetD:oBrowse:lDisablePaint := .T.
aSavAcols   := aClone(aCols)
cProjet     := M->ABH_PROJET
cProjDesc   := M->ABH_DESCRI 

cEtapa      := aSavaCols[nItAcols,nPosEtapa ]
cEtapaDesc  := aSavaCols[nItAcols,nPosEtDesc]

aHeader     := aClone(aHeaderABJ)
If ( Empty(aColsABJ) .Or. Len(aColsABJ)<=nItAcols )
	aAdd(aAux,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeaderABJ[nCntFor][2])
			aAux[1][nCntFor] := ABJ->(RecNo())
		Elseif IsHeadAlias(aHeaderABJ[nCntFor][2])
			aAux[1][nCntFor] := "ABJ"
		Else
			aAux[1][nCntFor] := CriaVar(aHeaderABJ[nCntFor][2],.T.)
		Endif
	Next nCntFor
	aAux[1][nUsado+1] := .F.
	aAux[1][nPosItem] := "01"
	For nCntFor := Len(aColsABJ)+1 To ( nItAcols )
		aAdd(aColsABJ,aClone(aAux))
	Next nCntFor
EndIf                                        


aCols := aClone( aColsABJ[nItAcols] ) 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Registros                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->ABH_CODCLI+M->ABH_LOJA)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a GetDados                                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE cCadastro From 10,0 to 28,80
@ 001,01 SAY RetTitle("ABH_CODCLI")+" "+M->ABH_CODCLI+"-"+M->ABH_LOJA+"/"+SA1->A1_NOME
@ 002,01 SAY RetTitle("ABH_PROJET")+":"+AllTrim( cProjet ) + " - " + ;
		AllTrim( cProjDesc ) + "  /  " + RetTitle("ABI_ETAPA")+":"+cEtapa + ;
		" - " + cEtapaDesc 

oGetF4 := MsGetDados():New(035,005,123,311,nOpcGet,"AT700F4LOk","AllwaysTrue","+ABJ_SUBITE", (nOpcF4=3 .Or. nOpcF4==4) )
@ 126,005 SAY STR0011 SIZE 60,09 OF oDlg PIXEL // "Horas Previstas: "
@ 126,065 SAY oSay1 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ 126,105 SAY STR0012 SIZE 60,09 OF oDlg PIXEL // "Horas realizadas: "
@ 126,165 SAY oSay2 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
@ 126,205 SAY STR0013 SIZE 60,09 OF oDlg PIXEL // "Saldo: "
@ 126,265 SAY oSay3 PROMPT 0 PICTURE cPict      SIZE 40,09 OF oDlg PIXEL
							
oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
							oSay2:SetText(n2),;
							oSay3:SetText(n3),;
							oSay3:SetColor( If( HoraToInt(n3,4)<0, CLR_HRED, CLR_HBLUE ), oDlg:nClrPane ) }
							
							
At700Total(oDlg,.T.)

ACTIVATE MSDIALOG oDlg ON INIT At700Bar2(oDlg,{||nOpca:=1,if(oGetF4:TudoOk(),oDlg:End(),nOpca:=0)},{||nOpca:=0,oDlg:End()},,oGetF4,cItem,cProjet,aSavAcols,nSavN)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Volta a situacao anterior                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)
SetKey(VK_F4,bSetKey)

If ( nOpcA == 1 )
	aColsABJ[nItAcols] := aClone(aCols)
EndIf           

aHeader  := aClone(aHeaderABI)
aCols    := aClone(aSavACols)
oGetD:oBrowse:lDisablePaint := .F.

If ( nOpcA == 1 ) 
	At700Total( oGetD:oWnd, .F. ) 
EndIf                 

n := nSavN

RestArea( aArea ) 

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT700F4LOk  ³ Autor ³Sergio Silveira      ³ Data ³29/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Linha na GetDados 2                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT700F4LOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT700F4LOk(oGet)

Local lRetorno   := .T.

Local nUsado     := Len(aHeaderABJ)
Local nPosDesc   := GDFieldPos( "ABJ_DESCRI", aHeaderABJ ) 
Local nPosTarefa := GDFieldPos( "ABJ_TAREFA", aHeaderABJ ) 
Local nCntFor    := 0

If ( !GDDeleted() )
	If ( Empty(aCols[n,nPosDesc] ) .And. ( Len(aCols) > 1 .Or. !Empty(aCols[n,nPosTarefa ] ) ) ) 
		lRetorno := .F.
		Help(" ",1,"AT700F4L01")
	EndIf
	
	If ( lRetorno )
		For nCntFor := 1 To Len(aCols)
			If ( aCols[ n, nPosTarefa ] == aCols[ nCntFor, nPosTarefa ] .And. ;
					n <> nCntFor .And. !aCols[nCntFor, nUsado+1])
				Help(" ",1,"AT700F4L02") // Tarefas iguais nao permitidas 
				lRetorno := .F.
			EndIf
		Next nCntFor
	EndIf 	

EndIf                   

At700Total(oGet:oWnd,.T.)

Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT700Eqpto³ Autor ³ Sergio Silveira       ³ Data ³ 29.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a amarracao Produto X Equipamento                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT700Eqpto()                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT700Eqpto()

Local lRetorno    := .F.
Local cCampo      := ReadVar()
Local uConteudo   := &(ReadVar())
Local nPosProd    := aScan(aHeader,{|x| AllTrim(x[2])=="ABI_CODPRO" })
Local aArea       := GetArea()

RestArea( aArea ) 

Return(lRetorno)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT700Bar  ³ Autor ³ Sergio Silveira       ³ Data ³ 21.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Mostra a EnchoiceBar na tela                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Hanna C.      |30/03/07|9.12  ³Bops 118469 -Alterado o nome dos Bitmaps³±±
±±³        	     ³        |      ³definidos pela Engenharia p/ Protheus 10³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static ;
Function AT700Bar(oDlg,bOk,bCancel,nOpc,oGetD)
Local aButtons   := {}                         
Local aUsButtons := {}
Local lTECA700   := AtIsRotina("ATC010CON").Or.AtIsRotina("ATC020CON")
Local nOpcx      := aRotina[ nOpc, 4 ] 

aAdd(aButtons,{"NOTE",      {|| At700F4(oGetD,nOpc) }, STR0027, STR0052 } ) 
aAdd(aButtons,{"RELATORIO", {|| At700OS() }     , STR0025, STR0053 } ) 

If nOpcx == 3 .Or. nOpcx == 4 
	aAdd(aButtons,{"SIMULACAO", {|| At700VlCal(oGetD) }, STR0042, STR0054 } ) 	 //"Calcula valores"
ElseIf !AtIsRotina("AT700TRACK")
	aAdd(aButtons,{"bmpord1",{|| At700Track(oGetD) }, STR0043, STR0055	 }) //"System Tracker"
EndIf	 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistBlock( "AT700BUT" ) 
	If ValType( aUsButtons := ExecBlock( "AT700BUT", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| aAdd( aButtons, x ) } )  
	EndIf 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT700Bar2 ³ Autor ³ Sergio Silveira       ³ Data ³ 29.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Mostra a EnchoiceBar na tela                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Conrado Q.   ³15/12/06³115662³ Adicionado parâmetros aCols, aHeader, n³±± 
±±³              ³        ³      ³ na chamada do P.E. AT700BU2.    		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static ;
Function AT700Bar2(oDlg,bOk,bCancel,nOpc,oGetD,cItem,cProjet,aSavAcols,nSavN)

Local aButtons := {}                                     
Local cEtapa   := cProjet + cItem 

aAdd(aButtons,{ "RELATORIO",{ || AT450Laudo(oGetD, cEtapa ) }, STR0026, STR0056 }) // "Atendimentos"
       
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistBlock( "AT700BU2" ) 
	If ValType( aUsButtons := ExecBlock( "AT700BU2", .F., .F., { aSavAcols, aHeaderABI, nSavN } ) ) == "A"
		AEval( aUsButtons, { |x| aAdd( aButtons, x ) } )  
	EndIf 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT700Grava³ Autor ³ Sergio Silveira       ³ Data ³26/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de Gravacao do projeto                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700Grava( ExpN1 )                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExcN1 : Operacao : 1-Inclusao / 2-Alteracao / 3-Exclusao    ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³20/03/06  ³ Cleber M.     ³Bops 95495 - Tratamento na gravacao dos cam-³±±
±±³          ³               ³pos Memo do ABH e ABI.                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
 
Function At700Grava( nOper ) 
                      
Local aColsABJIt  := {}              
Local aEnchoice   := {} 
             
Local cCampo      := "" 
Local cCampoNVal  := ""  
Local cCodFa      := ""
Local cLojaFa     := ""
Local cNumSer     := ""
Local cABHCodMem  := ""

Local lFoundABI   := .F.
Local lFoundABJ   := .F. 
                 
Local nLoop       := 0 
Local nLoop2      := 0      
Local nLoop3      := 0 
Local nPosCodMem  := GDFieldPos( "ABI_CODMEM" ) 
Local nPosItem    := GDFieldPos( "ABI_ITEM"   ) 
Local nPosMemo1   := GDFieldPos( "ABI_MEMO1"  ) 
Local nFieldPos   := 0

BEGIN TRANSACTION 

	Do Case
		Case nOper <> 3  
		
            DbSelectArea("ABH")
			ABH->( DbSetOrder( 1 ) ) 
			If ABH->( DbSeek( xFilial( "ABH" ) + M->ABH_PROJET ) ) 
				RecLock( "ABH", .F. ) 
			Else 
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Inclui os campos chave                                                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				RecLock( "ABH", .T. ) 
				
				ABH->ABH_FILIAL := xFilial( "ABH" ) 
				ABH->ABH_PROJET := M->ABH_PROJET 
			
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava os demais campos, inclusive especificos                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			For nLoop := 1 To FCount()
				cCampo := FieldName( nLoop )
				If !( cCampo $ "ABH_FILIAL|ABH_PROJET" )
					FieldPut( nLoop, M->&cCampo )
				EndIf
			Next nLoop
			DbSelectArea("ABH")
			MsUnlock()

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava os campo memo                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//MSMM( ABH->ABH_CODMEM, , , M->ABH_MEMO1, 1, , , "ABH","ABH_CODMEM")
		 	
		 	If nOper == 1 
		  		If !Empty(M->ABH_MEMO1)      //Inclusao
		  			MSMM(NIL,TamSx3("ABH_MEMO1")[1],NIL, M->ABH_MEMO1,1,NIL,NIL, "ABH","ABH_CODMEM")
		     	EndIf
		 	Else
		  		If Empty(ABH->ABH_CODMEM)    // Inclusao
		    		MSMM(NIL,TamSx3("ABH_MEMO1")[1],NIL, M->ABH_MEMO1,1,NIL,NIL, "ABH","ABH_CODMEM")
		      	Else                               // Alteracao
		      		MSMM(ABH->ABH_CODMEM,TamSx3("ABH_MEMO1")[1],NIL,M->ABH_MEMO1,1,NIL,NIL,"ABH","ABH_CODMEM")
		        EndIf
		 	EndIf			
			
			ABH->(FkCommit()) // Commit para Integridade Referencial do ABH
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Faz a gravacao dos itens ( etapas )                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("ABI")
			ABI->( DbSetOrder( 1 ) ) 
		
			For nLoop := 1 To Len( aCols ) 
			
				lFoundABI  := ABI->( DbSeek( xFilial( "ABI" ) + ABH->ABH_PROJET + aCols[ nLoop, nPosItem ] ) )
				
				If aCols[ nLoop, Len( aHeader ) + 1 ] 
					
					If lFoundABI            
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Se existe na base e esta deletado                                      ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Exclui os subitens ( tarefas ) desta etapa                             ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
						ABJ->( DbSetOrder( 1 ) ) 
						If ABJ->( DbSeek( xFilial( "ABJ" ) + M->ABH_PROJET + ABI->ABI_ITEM ) ) 	
							ABJ->( DbEval( { || RecLock( "ABJ", .F., .T. ),;
								DbDelete(), MsUnLock() },,; // Processo  
								{ || xFilial( "ABJ" ) + M->ABH_PROJET + ABI->ABI_ITEM == ABJ_FILIAL + ABJ_PROJET + ABJ->ABJ_ITEM } ) )   // Condicao While 	                                                       
						EndIf 	       
		
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Exclui o item ( etapa )                                                ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
						MSMM(ABI->ABI_CODMEM,,,,2)			
						RecLock( "ABI", .F., .T. ) 
						ABI->( DbDelete() ) 
						ABI->( MsUnLock() ) 
						
					EndIf     
				
				Else 
				        
				    cCampoNVal := "ABI_FILIAL|ABI_PROJET|ABI_ITEM" 
				     
					If lFoundABI  
						RecLock( "ABI", .F. ) 
					Else
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava os campos chave                                                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
						RecLock( "ABI", .T. ) 		
						ABI->ABI_FILIAL := xFilial( "ABI") 
						ABI->ABI_PROJET := M->ABH_PROJET
						ABI->ABI_ITEM   := aCols[ nLoop, nPosItem ] 
					EndIf 
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Grava os demais campos, inclusive especificos                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							
					For nLoop2 := 1 To Len( aHeader ) 
						cCampo := AllTrim( aHeader[ nLoop2, 2 ] )
						
						nFieldPos := FieldPos( cCampo ) 
						
						If !( cCampo $ cCampoNVal ) .And. !Empty( nFieldPos )   				
							ABI->( FieldPut( nFieldPos, aCols[ nLoop, nLoop2 ] ) )						
						EndIf 	
					Next nLoop2 		
					DbSelectArea("ABI")
					MsUnlock()
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Grava os campo memo                                                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					//MSMM( ABI->ABI_CODMEM, , , aCols[ nLoop, nPosMemo1 ], 1, , , "ABI","ABI_CODMEM")
					
				 	If nOper == 1 
				  		If !Empty( GDFieldGet("ABI_MEMO1") )      //Inclusao
				  			MSMM(NIL,TamSx3("ABI_MEMO1")[1],NIL,aCols[ nLoop, nPosMemo1 ],1,NIL,NIL, "ABI","ABI_CODMEM")
				     	EndIf
				 	Else
				  		If Empty(ABI->ABI_CODMEM)    // Inclusao
				    		MSMM(NIL,TamSx3("ABI_MEMO1")[1],NIL,aCols[ nLoop, nPosMemo1 ],1,NIL,NIL, "ABI","ABI_CODMEM")
				      	Else                               // Alteracao
				      		MSMM(ABI->ABI_CODMEM,TamSx3("ABI_MEMO1")[1],NIL,aCols[ nLoop, nPosMemo1 ],1,NIL,NIL, "ABI","ABI_CODMEM")
				        EndIf
				 	EndIf

					ABI->(FKCommit())
		
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Faz a gravacao dos subitens ( tarefas )                                ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If Len( aColsABJ ) >= nLoop 
					
						nPosSubIte := GDFieldPos( "ABJ_SUBITE", aHeaderABJ ) 
						aColsABJIt := AClone( aColsABJ[ nLoop ] ) 
						
						For nLoop2 := 1 To Len( aColsABJIt ) 
						
							lFoundABJ  := ABJ->( DbSeek( xFilial( "ABJ" ) + ABI->ABI_PROJET + ABI->ABI_ITEM + aColsABJIt[ nLoop2, nPosSubIte ] ) )
						
							If aColsABJIt[ nLoop2, Len( aHeaderABJ ) + 1 ] 
								
								If lFoundABJ            
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Se existe na base e esta deletado                                      ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								
									// MSMM(ABI_CODMEM,,,,2)			
									RecLock( "ABJ", .F., .T. ) 
									ABJ->( DbDelete() ) 
									ABJ->( MsUnLock() ) 
								EndIf     
							
							Else 
							        
							    cCampoNVal := "ABJ_FILIAL|ABJ_PROJET|ABJ_ITEM|ABJ_SUBITE" 
							     
								If lFoundABJ  
									RecLock( "ABJ", .F. ) 
								Else
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Grava os campos chave                                                  ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								
									RecLock( "ABJ", .T. ) 		
									ABJ->ABJ_FILIAL := xFilial( "ABJ") 
									ABJ->ABJ_PROJET := ABI->ABI_PROJET
									ABJ->ABJ_ITEM   := ABI->ABI_ITEM
									ABJ->ABJ_SUBITE := aColsABJIt[ nLoop2, nPosSubIte ]  
								EndIf 
								
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Grava os demais campos, inclusive especificos                          ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
										
								For nLoop3 := 1 To Len( aHeaderAbj ) 
									cCampo := AllTrim( aHeaderABJ[ nLoop3, 2 ] )
									                                 
									nFieldPos := FieldPos( cCampo ) 
									
									If !( cCampo $ cCampoNVal ) .And. !Empty( nFieldPos )  				
										ABJ->( FieldPut( nFieldPos, aColsABJIt[ nLoop2, nLoop3 ] ) )						
									EndIf 	
								Next nLoop3 
								ABJ->( MsUnLock() ) 		            
					        
							EndIf
							
//							ABJ->( MsUnLock() ) 
					
			    		Next nLoop2 
			    		
					EndIf 	    		
			
				EndIf
				
//				ABI->( MsUnLock() ) 
			
			Next nLoop 	
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Inclui o equipamento na Base Instalada                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		     
			If Inclui 
		    
				cCodFab   := CriaVar( "AA3_CODFAB", .F. )
				cLojaFa   := CriaVar( "AA3_LOJAFA", .F. )
			    
				cNumSer   := "PROJETO " + ABH->ABH_PROJET   
			           
				aEnchoice := {}                                                                                
			   aAdd( aEnchoice, { "AA3_DTVEND", dDataBase       } )   
			   aAdd( aEnchoice, { "AA3_DTINST", ABH->ABH_ENTREG } )   	    
			    
				AtTrfEqpto(cCodFab,cLojaFa,M->ABH_CODPRO,cNumSer,M->ABH_CODCLI,M->ABH_LOJA,,,aEnchoice)
			
			EndIf 
			
		Otherwise 
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui as tarefas das etapas                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		                
			ABJ->( DbSetOrder( 1 ) ) 
			If ABJ->( DbSeek( xFilial( "ABJ" ) + M->ABH_PROJET ) ) 	
				ABJ->( DbEval( { || RecLock( "ABJ", .F., .T. ),;
					DbDelete(), MsUnLock() },,; // Processo  
					{ || xFilial( "ABJ" ) + M->ABH_PROJET == ABJ_FILIAL + ABJ_PROJET } ) )   // Condicao While 	                                                       
				ABJ->(FKCommit())
			EndIf 	       
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui as etapas do projeto                                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		                
			ABI->( DbSetOrder( 1 ) ) 
			If ABI->( DbSeek( xFilial( "ABI" ) + M->ABH_PROJET ) ) 	
				ABI->( DbEval( { || MSMM(ABI_CODMEM,,,,2),RecLock( "ABI", .F., .T. ),;
					DbDelete(), MsUnLock() },,; // Processo  
					{ || xFilial( "ABI" ) + M->ABH_PROJET == ABI_FILIAL + ABI_PROJET } ) )   // Condicao While 	                                                       
				ABI->(FKCommit())
			EndIf 
			      
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui a amarracao com os conhecimentos                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MsDocument( "ABH", ABH->( RecNo() ), 2, , 3 ) 
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui o projeto                                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			RecLock( "ABH", .F., .T. ) 
			
			cABHCodMem := ABH->ABH_CODMEM
			
			ABH->( DbDelete() ) 
			ABH->( MsUnLock() ) 	      
			ABH->( FKCommit() )
			
			MSMM(cABHCodMem,,,,2)
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui o equipamento da Base Instalada                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		    
			cCodFab   := CriaVar( "AA3_CODFAB", .F. )
			cLojaFa   := CriaVar( "AA3_LOJAFA", .F. )
		                                                                         
			cNumSer   := "PROJETO " + M->ABH_PROJET 
		    
			AA3->( DbSetOrder( 4 ) ) 
			If AA3->(DbSeek(xFilial("AA3")+ cCodFab+cLojaFa+M->ABH_CODPRO+cNumSer ) )  	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria as variaveis de memoria para permitir a exclusao na At040Grava    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				M->AA3_CODFAB := AA3->AA3_CODFAB
				M->AA3_LOJAFA := AA3->AA3_LOJAFA
				M->AA3_CODPRO := AA3->AA3_CODPRO
				M->AA3_NUMSER := AA3->AA3_NUMSER
				M->AA3_CODCLI := AA3->AA3_CODCLI
				M->AA3_LOJA   := AA3->AA3_LOJA
				M->AA3_CODMEM := AA3->AA3_CODMEM 
				DbSelectArea( "AA3" )	
				At040Grava( 3 ) 			
			EndIf 
		
	EndCase           
	
End Transaction 

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700VlCli³ Autor ³ Sergio Silveira       ³ Data ³28/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao do Cliente                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700VlCli()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700VlCli()

Local aArea       := GetArea() 
Local cCampo      := ReadVar()
Local lRetorno    := .F.
Local uConteudo   := &(ReadVar())

Do Case
Case ( "ABH_CODCLI"$cCampo )
	If ( SA1->A1_COD==uConteudo )
		uConteudo += SA1->A1_LOJA
	EndIf
	DbSelectArea("SA1")
	DbSetOrder(1)
	If ( DbSeek(xFilial("SA1")+uConteudo) )
		M->ABH_NOMCLI := SA1->A1_NOME
		lRetorno := .T.
		lRetorno := RegistroOk("SA1")
	Else
		Help(" ",1,"REGNOIS")
	EndIf
	
Case ( "ABH_LOJA"$cCampo )
	DbSelectArea("SA1")
	DbSetOrder(1)
	If ( DbSeek(xFilial("SA1")+M->ABH_CODCLI+uConteudo) )
		M->ABH_NOMCLI := SA1->A1_NOME
		lRetorno := .T.
		lRetorno := RegistroOk("SA1")
	Else
		Help(" ",1,"REGNOIS")
	EndIf
EndCase

If ( aArea[1] <> "SA1" )
	RestArea( aArea ) 
EndIf   

Return(lRetorno)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700ACols³ Autor ³ Sergio Silveira       ³ Data ³04/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Montagem de ACols e AColsABJ                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700VlCli( ExpA1, ExpA2, ExpA3, ExpN1 )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 -> aHeader / ExpA2 -> aCols / ExpA3 -> aColsABJ      ³±±
±±³          ³ ExpN1 -> nPosItem                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ TECA700                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/01/2007³ Conrado Q.    ³Bops 115754: Montagem do aCols e aHeader    ³±±
±±³          ³               ³adaptada para utilização do Walk-Thru.      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT700ACols( aHeader, aCols, aColsABJ, nPosItem, lExclui ) 

Local aAux       := {} 

Local cSeekAB9   := ""

Local dIniReal   := CToD( "" ) 
Local dFimReal   := CToD( "" )    

Local lAtend     := .F. 
Local lPedido    := .F. 

Local nUsado     := Len(aHeader)
Local nHorasReal := 0 
Local nCntFor    := 0 
Local nTotAtend  := 0 

lExclui := If( ValType( lExclui ) == "L", lExclui, .F. ) 

DbSelectArea("ABI")
DbSetOrder(1)
DbSeek(xFilial("ABI")+ABH->ABH_PROJET)
While ( !ABI->( Eof() ) .And. xFilial("ABI") == ABI->ABI_FILIAL .And.;
							ABH->ABH_PROJET == ABI->ABI_PROJET )
							
	aAdd(aCols,Array(nUsado))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := ABI->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "ABI"	
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor    
	
	aAux := {}    
	
	lPedido := ( lPedido .Or. !Empty( ABI->ABI_NUMPV + ABI->ABI_ITEMPV ) ) 
	
	DbSelectArea("ABJ")
	DbSetOrder(1)
	If ( DbSeek(xFilial("ABJ")+ABH->ABH_PROJET+ABI->ABI_ITEM) )
		While ( !Eof() .And. xFilial("ABJ") == ABJ->ABJ_FILIAL .And.;
								M->ABH_PROJET== ABJ->ABJ_PROJET .And.;
								ABI->ABI_ITEM== ABJ->ABJ_ITEM )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Percorre os atendimentos desta tarefa para apurar o valor de ³
			//³ horas realizado.                                             ³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			nHorasReal := 0 
			nTotAtend  := 0 	

			dIniReal   := CToD( "" ) 
			dFimReal   := CToD( "" ) 
			
			AB9->( DbSetOrder( 4 ) ) 			
			cSeekAB9 := xFilial( "AB9" )+ABJ->ABJ_PROJET+ABJ->ABJ_ITEM+ABJ->ABJ_SUBITE
			
			If AB9->( DbSeek( cSeekAB9 ) )                   
			
				While !AB9->( Eof() ) .And. cSeekAB9 == AB9->AB9_FILIAL + ;
						AB9->AB9_TAREFA  
					nHorasReal += SubtHoras( AB9->AB9_DTINI, AB9->AB9_HRINI,;
								AB9->AB9_DTFIM, AB9->AB9_HRFIM )
					dIniReal   := If(AB9->AB9_DTINI<dIniReal.Or.Empty(dIniReal),AB9->AB9_DTINI,dIniReal) 
					
					If ABJ->ABJ_SITUAC == "1"
					    	dFimReal   := If(AB9->AB9_DTFIM>dFimReal.Or.Empty(dIniReal),AB9->AB9_DTFIM,dFimReal) 		
					 EndIf 	
						
					nTotAtend++ 
					lAtend := .T. 
					AB9->( DbSkip() ) 
				EndDo
			EndIf 
								
			aAdd(aAux,Array(Len(aHeaderABJ)+1))
			For nCntFor := 1 To Len(aHeaderABJ)
				If IsHeadRec(aHeaderABJ[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := ABJ->(RecNo())					
				Elseif IsHeadAlias(aHeaderABJ[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "ABJ"
				Elseif ( aHeaderABJ[nCntFor][10] <> "V" )                   
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderABJ[nCntFor][2]))
				Else        
					Do Case
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_TREALI" 
						aAux[Len(aAux)][nCntFor] := IntToHora( nHorasReal, 4 ,.T.)  									 
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_NATEND" 
						aAux[Len(aAux)][nCntFor] := nTotAtend
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_INIREA" 
						aAux[Len(aAux)][nCntFor] := dIniReal 
					Case AllTrim( aHeaderABJ[nCntFor,2] ) == "ABJ_FIMREA" 
						aAux[Len(aAux)][nCntFor] := dFimReal 
					Otherwise  		
						aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderABJ[nCntFor][2])
					EndCase 								
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderABJ)+1] := .F.
			
			DbSelectArea("ABJ")
			DbSkip()
		EndDo
		aAdd(aColsABJ,aClone(aAux))
	Else
		aAdd(aAux,Array(Len(aHeaderABJ)+1))
		For nCntFor := 1 To Len(aHeaderABJ)
			If IsHeadRec(aHeaderABJ[nCntFor][2])
				aAux[Len(aAux)][nCntFor]:= 0
			Elseif IsHeadAlias(aHeaderABJ[nCntFor][2])
				aAux[Len(aAux)][nCntFor]:= "ABJ"
			Else
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderABJ[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderABJ)+1] := .F.
		aAdd(aColsABJ,aClone(aAux))
	EndIf

	DbSelectArea( "ABI" )     

	ABI->( DbSkip() ) 
EndDo     

If lAtend .And. lExclui
	lExclui := .F.
	Help( " ", 1, "AT700NEXCL" ) // Existem atendimentos para as tarefas 		
EndIf 
                       
If lPedido .And. lExclui
	lExclui := .F.
	Help( " ", 1, "AT700NEXC2" ) // Existem pedidos gerados para este projeto 
EndIf 

Return( Nil ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Total³ Autor ³ Sergio Silveira       ³ Data ³05/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Totalizacao da Janela                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³A700Total( ExpO1, ExpL1 )                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1   : Objeto da Janela                                  ³±±
±±³          ³ExpL1   : Se foi chamado da funcao F4                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static ;
Function At700Total(oDlg,lF4)

Local bAjustaHora := { |x| Left( Transform( x, "@R 9999:99" ), 7 ) } 
                                     
Local cTotPrev     := ""
Local cTotReal     := ""
Local cDifer       := ""
Local cStatusEtp   := ""

Local dIniReal     := CToD( "" ) 
Local dFimReal     := CToD( "" ) 
Local dIniCols     := CToD( "" ) 
Local dFimCols     := CToD( "" ) 

Local lDel         := .F.

Local nUsado	    := Len(aHeaderABJ)
Local nUsadoMain   := Len(aHeader)
Local nCntFor	    := 0
Local nCntFor2	    := 0
Local nPosPrev     := GDFieldPos( "ABJ_TPREVI", aHeaderABJ )
Local nPosOrca     := GDFieldPos( "ABJ_TORCAD", aHeaderABJ )
Local nPosReal     := GDFieldPos( "ABJ_TREALI", aHeaderABJ )
Local nPosStatus   := GDFieldPos( "ABJ_SITUAC", aHeaderABJ )
Local nPosDi       := GDFieldPos( "ABJ_INIREA", aHeaderABJ ) 
Local nPosDf       := GDFieldPos( "ABJ_FIMREA", aHeaderABJ ) 
Local nPosMainPrev := GDFieldPos( "ABI_TPREVI" )
Local nPosMainOrca := GDFieldPos( "ABI_TORCAD" )
Local nPosMainReal := GDFieldPos( "ABI_TREALI" ) 
Local nPosMainStat := GDFieldPos( "ABI_SITUAC" ) 
Local nPosMainDi   := GDFieldPos( "ABI_INIREA" ) 
Local nPosMainDf   := GDFieldPos( "ABI_FIMREA" ) 
Local nTotPrev	    := 0
Local nTotReal	    := 0
Local nParcPrev	 := 0
Local nParcReal	 := 0
Local nParcOrca	 := 0
Local nDigiPrev    := Len( CriaVar( "ABJ_TPREVI" ) ) - 3
Local nDigiOrca    := Len( CriaVar( "ABJ_TORCAD" ) ) - 3
Local nDigiReal    := Len( CriaVar( "ABJ_TREALI" ) ) - 3

If ( lF4 )
	For nCntFor := 1 To Len(aCols)
		If ( Len(aCols[nCntFor])==nUsado .Or. !aCols[nCntFor][nUsado+1] )
			nTotPrev += HoraToInt( Eval( bAjustaHora, aCols[ nCntFor, nPosPrev ] ), nDigiPrev )
			nTotReal += HoraToInt( Eval( bAjustaHora, aCols[ nCntFor, nPosReal ] ), nDigiReal )
		EndIf
	Next nCntFor
Else
	For nCntFor := 1 To Len(aColsABJ) 
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Exclui o equipamento da Base Instalada                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lDel := If( !ALTERA .And. !INCLUI, .F., aCols[nCntFor,nUsadoMain+1] )
	
		If !lDel 
		
			dIniReal := CToD( "" ) 
			dFimReal := CToD( "" ) 		
		
			nParcPrev := 0 
			nParcReal := 0 
			nParcOrca := 0 
			
			cStatus   := ""	
		
			For nCntFor2 := 1 To Len(aColsABJ[nCntFor])
				If ( Len(aColsABJ[nCntFor][nCntFor2])==nUsado .Or. !aColsABJ[nCntFor][nCntFor2][nUsado+1] )
					nParcPrev  += HoraToInt( Eval( bAjustaHora, aColsABJ[ nCntFor, nCntFor2, nPosPrev ] ), nDigiPrev )				
					nParcReal  += HoraToInt( Eval( bAjustaHora, aColsABJ[ nCntFor, nCntFor2, nPosReal ] ), nDigiReal )
					nParcOrca  += HoraToInt( Eval( bAjustaHora, aColsABJ[ nCntFor, nCntFor2, nPosOrca ] ), nDigiOrca )					
					cStatTaref := aColsABJ[ nCntFor, nCntFor2, nPosStatus ]	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Marca a etapa como concluida apenas se todas as tarefas estiverem      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cStatusEtp  := If( cStatusEtp == "2", cStatusEtp, cStatTaref )  
					
					dIniCols := aColsABJ[ nCntFor, nCntFor2, nPosDi ]
					dFimCols := aColsABJ[ nCntFor, nCntFor2, nPosDf ]               					               
					
					If !Empty( dIniCols ) 
						dIniReal := If( dIniCols<dIniReal .Or. Empty( dIniReal ), dIniCols, dIniReal )  
					EndIf 	
						
					dFimReal := If( dFimCols>dFimReal .Or. Empty( dFimReal ), dFimCols, dFimReal )  					
					
				EndIf
			Next nCntFor2     
			
			
			nTotPrev += nParcPrev
			nTotReal += nParcReal  

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o Status da etapa ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCols[ nCntFor, nPosMainStat ] := cStatusEtp 			

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza o previsto e o realizado da etapa baseado nas tarefas         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCols[ nCntFor, nPosMainPrev ] := IntToHora( nParcPrev, nDigiPrev )  
			aCols[ nCntFor, nPosMainOrca ] := IntToHora( nParcOrca, nDigiOrca )  
			aCols[ nCntFor, nPosMainReal ] := IntToHora( nParcReal, nDigiReal )  

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza as datas de inicio e fim baseado nas tarefas         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aCols[ nCntFor, nPosMainDi   ] := dIniReal 
			aCols[ nCntFor, nPosMainDf   ] := If( cStatusEtp == "1", dFimReal, CToD( "" ) )   
			
		EndIf	
	Next nCntFor
EndIf           

cTotPrev := IntToHora( nTotPrev, nDigiPrev ,.T.) 
cTotReal := IntToHora( nTotReal, nDigiReal ,.T.) 
cDifer   := IntToHora( nTotPrev - nTotReal, If( nDigiPrev > nDigiReal, nDigiPrev, nDigiReal ) ,.T.)  

Eval(oDlg:Cargo,cTotPrev,cTotReal,cDifer)
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Progr³ Autor ³ Sergio Silveira       ³ Data ³05/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a programacao do Projeto ( geracao das OSs )        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700Progr()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 20/04/07 ³ Conrado Q.    ³ - Bops 122039: Adicionado P.E. no final da ³±±
±±³          ³               ³ gravação das OS.                           ³±±
±±³ 16/07/07 ³ Conrado Q.    ³ - Bops 122057: Corrigido montagem o aCols  ³±±
±±³          ³               ³ quando utilizados campos do Walk-Thru.     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function AT700Progr(cAlias,nReg,nOpcx)
           
Local aColsLine		:= {} 								// aCols com os itens que serão programados
Local aListBox		:= {}								// Array com o conteúdo do listbox, nesse listbox estão as etapas do projeto

Local bCampo		:= { |nCpo| FieldName( nCpo ) }		// Nome do campo
            
Local cItemAB7		:= ""    							// Número da etapa do projeto
Local cDlgCaption	:= ""								// Título do diáglo das etapas do projeto
               
Local nOpcA			:= 0 								// Opção escolhida durante o processo de programação
Local nLinIni		:= 0								// Linha inicial
Local nColIni		:= 0 								// Coluna inicial 
Local nSaveSX8		:= GetSX8Len()						// Pega um número exclusivo no SX8
Local nLoop			:= 0								// Utilizado em loops

Local oDlgLeg		:= Nil								// Diálogo com as lista de etapas programadas
Local oList			:= Nil								// Lista com as etapas do projeto
                                                    	
Local lAT700PRO		:= ExistBlock("AT700PRO")			// Ponto de entrada executado no final da gravação das OS
                                                    	
Private aCols       := {}								// aCols do AB6
Private aColsAB7    := {}								// aCols do AB7
Private aColsAB8    := {}								// aCols do AB8
Private aHeader     := {}								// aHeader do AB6
Private aHeaderAB7  := {}								// aHeader do AB7
Private aHeaderAB8  := {}								// aHeader do AB8
    
ABI->( DbSetOrder( 1 ) ) 	                          

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a tela de processamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

FormBatch(  STR0015 + " - " + ABH->ABH_PROJET ,; 
			  { STR0016,;
				STR0017,;
				STR0018,;
				STR0019 },; 				
				{ {5,.F.,{|o| o:oWnd:End()}         },;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}  },;
				{2,.T.,{|o| o:oWnd:End() }}         })                      

/*
STR0016  "Esta rotina efetua a criacao de Ordens de Servico para cada etapa do projeto"
STR0017  "que ainda nao possua uma Ordem de Servico associada."
STR0018  "Uma etapa pode ser programada mais de uma vez desde que a OS associada"     
STR0019  "seja excluida."    
*/

If nOpcA == 1 

	cSeekABI := xFilial( "ABI" ) + ABH->ABH_PROJET 
	
	If ABI->( DbSeek( cSeekABI ) ) 
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta o aHeader e o aHeaderAB7        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		At450Monta() 
		
		While !ABI->( Eof() ) .And. cSeekABI == ABI->ABI_FILIAL + ABI->ABI_PROJET 		       	 	
		
			aColsAB7 := {} 	
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Percorre todas as etapas do projeto que ainda nao foram programadas   ³
			//³ e cujo status informado seja "normal"                                 ³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Empty( ABI->ABI_NUMOS ) .And. ABI->ABI_SITATU == "1"
		  	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Monta o aColsLine e adiciona ao aCols ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				aColsLine := Array( Len( aHeaderAB7 ) + 1 )
				AEval( aHeader, { |x,y| If( IsHeadRec(x[2]) .OR. IsHeadAlias(x[2]), Nil, aColsLine[ y ] := CriaVar( x[ 2 ] ) ) } )                       
				
				cItemAB7 := StrZero( Len( aColsAB7 ) + 1, 2 )
					
				aColsLine[ GDFieldPos( "AB7_CODPRO", aHeaderAB7 ) ] := ABH->ABH_CODPRO
				aColsLine[ GDFieldPos( "AB7_NUMSER", aHeaderAB7 ) ] := "PROJETO " + ABH->ABH_PROJET 
				aColsLine[ GDFieldPos( "AB7_ITEM"  , aHeaderAB7 ) ] := cItemAB7 
				aColsLine[ GDFieldPos( "AB7_CODPRB", aHeaderAB7 ) ] := ABI->ABI_CODPRB
				aColsLine[ GDFieldPos( "AB7_TIPO"  , aHeaderAB7 ) ] := "1"
				
				aColsLine[ Len( aHeaderAB7 ) + 1 ] := .F.
					
				aAdd( aColsAB7, AClone( aColsLine ) )
		         
				aCols    := aClone( aColsAB7   )
				aHeader  := aClone( aHeaderAB7 )
	             
				If !Empty( aCols )
				
					DbSelectArea("AB6")
					For nLoop := 1 To FCount()
						M->&( Eval( bCampo, nLoop ) ) := CriaVar( FieldName( nLoop ) )
					Next nLoop
						           
					SA1->( DbSetOrder( 1 ) ) 					    
					SA1->( DbSeek( xFilial( "SA1" ) + ABH->ABH_CODCLI + ABH->ABH_LOJA ) ) 
						
					M->AB6_CODCLI := ABH->ABH_CODCLI
					M->AB6_LOJA   := ABH->ABH_LOJA
					M->AB6_CONPAG := SA1->A1_COND
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Chama a funcao de gravacao     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					Begin Transaction
						
						At450Grava( 1 )
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Confirma o SX8                 ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						EvalTrigger()
						While ( GetSX8Len() > nSaveSx8 )
							ConfirmSX8()
						EndDo
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava o numero da OS + Item na etapa   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						
						RecLock( "ABI", .F. )
						ABI->ABI_NUMOS := M->AB6_NUMOS + cItemAB7 
						ABI->( MsUnlock() )			

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Alimenta o array para exibir na ListBoxt ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aAdd( aListBox, { ABI->ABI_ETAPA, ABI->ABI_NUMOS } )  
						
					End Transaction
	
				EndIf
				
			EndIf
	         
			ABI->( DbSkip() ) 
	
		EndDo  
	
	EndIf	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Executa o P.E. no final da gravação das Ordens de Serviço.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lAT700PRO
		ExecBlock("AT700PRO",.F.,.F.)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Exibe as etapas que foram programadas ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(aListBox)
		Help( "", 1, "AT700NPROG" ) // "Nenhuma etapa foi programada !"
	Else 
		cDlgCaption := STR0021 + ABH->ABH_PROJET // "Etapas programadas - Projeto " 
		DEFINE MSDIALOG oDlgLeg TITLE cDlgCaption From 0 + nLinIni,0 + nColIni To ;
				 220 + nLinIni,340 + nColIni OF oMainWnd PIXEL STYLE WS_DLGFRAME          
				 
			@  0,-25 BITMAP oBmp RESNAME "PROJETOAP" oF oDlgLeg SIZE 55, 1000 NOBORDER WHEN .F. PIXEL 
		  	@  8, 38 LISTBOX oList VAR cVar Fields HEADER STR0024, STR0025 SIZE 125, 80 Of oDlgLeg PIXEL // Etapa, O.S. 
			oList:SetArray( aListBox )
			oList:bLine := { || { aListBox[oList:nAT,1], aListBox[oList:nAT,2] } }
			DEFINE SBUTTON FROM 93, 136 TYPE 1 ACTION (oDlgLeg:End()) ENABLE OF oDlgLeg
		ACTIVATE MSDIALOG oDlgLeg CENTERED 
		CursorArrow() 
	EndIf 
	
EndIf 	
	
Return( Nil )        

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700OS   ³ Autor ³ Sergio Silveira       ³ Data ³11/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a chamada da visualizacao da OS                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700OS()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
 
Function AT700OS() 

Local aArea	:= GetArea() 
Local cSeek := GDFieldGet( "ABI_NUMOS" ) 

Private aRotina := {	{ "","AllwaysTrue",0,2},;
						{ "","AllwaysTrue",0,2}}
Private INCLUI  := .F.
Private ALTERA  := .F.
                      
DbSelectArea("AB6")
DbSetOrder(1)
If ( DbSeek(xFilial("AB6")+cSeek) )
	At450Visua("AB6",AB6->(RecNo()),1)
Else
	Help(" ",1,"ATC040OS")
EndIf
	
RestArea( aArea ) 	

CursorArrow()

Return(Nil)       

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ At700Sit ³ Autor ³ Sergio Silveira       ³ Data ³06/09/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a situacao do projeto                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700Sit()                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
               
Function At700Sit() 

Local lRet := .T.

If &( ReadVar() ) $ "123"
	lRet := .T. 
Else 
	lRet := .F.	
EndIf 

Return( lRet ) 
                     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ At700VlHr³ Autor ³ Sergio Silveira       ³ Data ³31/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a hora digitada                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700VlHr()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
               
Function At700VlHr()

Local cConteudo := &( ReadVar() ) 
Local lRetorno  := .T. 
                                   
cConteudo := Left( Transform( cConteudo, "@R 9999:99" ), 7 ) 

lRetorno := AtVldHora( cConteudo, .T. ) 

Return( lRetorno ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700VlCal³ Autor ³ Sergio Silveira       ³ Data ³13/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o valor das etapas do projeto e alimenta o aCols   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700VlCal()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³11/06/2007³Conrado Q.     ³- BOPS 127131: Corrigido texto exibido no   ³±±
±±³          ³               ³no botão utilizado para recalcular.         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
               
Function At700VlCal()
                                       
Local aValores  := {} 
Local aAux      := {}  

Local bAjustaHora := { |x| Left( Transform( x, "@R 9999:99" ), 7 ) } 

Local cCodTec   := ""
Local cVar      := ""

Local nLoop     := 0 
Local nPosVal   := GDFieldPos( "ABI_VALOR" ) 
Local nFator    := 1 
Local nOpca     := 0 

Local oDlg   
Local oBrowse 
Local oBut1
Local oBut2
Local oBut3           

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Chama a rotina de calculo             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
At700Recal( @aValores ) 

If !Empty( aValores ) 
                                                    
	DEFINE MSDIALOG oDlg TITLE CCADASTRO FROM 09,0 TO 33.8,60 OF oMainWnd
	
	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
	
	@  0, -25 BITMAP oBmp RESNAME "PROJETOAP" oF oDlg SIZE 55, 1000 NOBORDER WHEN .F. PIXEL
	
	@ 03, 40 SAY STR0029 FONT oBold PIXEL  //"Assistente para calculo de valores / hora orcados"
	
	@ 14, 30 TO 16 ,400 LABEL '' OF oDlg   PIXEL
	
	@ 25, 40 SAY STR0030 SIZE 40, 10 PIXEL   //"Fator"
	@ 23, 85 MSGET oGetPesq3 VAR nFator PICTURE "@E 999.99" SIZE 80, 10 VALID .T. PIXEL 
	
	aOrdem := { STR0031, STR0032, STR0033 } //"Etapa"###"Valor ascendente"###"Valor descendente"
	cOrdem := ""	
	
	@ 38, 40 SAY STR0034 SIZE 40, 10 PIXEL 	 //"Ordenar por"
	
	@ 36, 85 COMBOBOX oOrdem VAR cOrdem ITEMS aOrdem SIZE 80,10 OF oDlg PIXEL;
		ON CHANGE At700Ordem( oOrdem, @aValores, @oBrowse ) 
	
	@ 22, 200 BUTTON oBut1 PROMPT STR0035 SIZE 30,15 ACTION ( At700ReCal( @aValores, nFator ), oBrowse:Refresh() ) PIXEL OF oDlg //"Recalcular"		
	
	oBrowse := TWBrowse():New( 55, 40,190, 106,,{ STR0031, STR0036 },,oDlg,,,,,,,,,,,,.F.,,.T.,,.F.,,,)  //"Etapa"###"Valor orcado"
	
	oBrowse:SetArray(aValores)
	oBrowse:bLine := { || { aValores[oBrowse:nAT,2], Transform( aValores[oBrowse:nAT,4], "@e 999,999,999.99" ) } }
		                                                                                        
	DEFINE SBUTTON oBut3 FROM 168, 169 TYPE 1 ACTION ( nOpca := 1, oDlg:End() )  ENABLE of oDlg		 
	DEFINE SBUTTON oBut2 FROM 168, 202 TYPE 2 ACTION ( nOpca := 0, oDlg:End() )  ENABLE of oDlg
	
	ACTIVATE MSDIALOG oDlg CENTERED  
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza o ACOLS                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpca == 1                             
		If Aviso( STR0037, STR0038, { STR0039, STR0040 }, 2 ) == 1  //"Atencao"###"Atualiza os valores orcados das etapas ?"###"Sim"###"Nao"
			For nLoop := 1 To Len( aValores ) 
				aCols[ aValores[ nLoop, 1 ], nPosVal ] := aValores[ nLoop, 4 ]  
		   Next nLoop                                
		EndIf		   
	EndIf                                

Else
	      
   Help( "", 1, "AT700NORC" ) // Nao existem dados suficientes 

EndIf 
	    
                                  
Return( .T.) 
                                           
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Ordem³ Autor ³ Sergio Silveira       ³ Data ³14/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ordena a TwBrowse de valores orcados                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700Ordem( ExpO1, @ExpA1, @ExpO2 )                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto combobox                                   ³±±
±±³          ³ ExpA1 -> Array da TwBrowse                                 ³±±
±±³          ³ ExpO2 -> Objeto TwBrowse                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700Ordem( oOrdem, aValores, oBrowse ) 

Local nCombo := oOrdem:nAt

Do Case
Case nCombo == 1
	ASort( aValores, , , { |x,y| y[1] > x[1] } ) 
Case nCombo == 2
	ASort( aValores, , , { |x,y| y[2] > x[2] } ) 
Case nCombo == 3
	ASort( aValores, , , { |x,y| x[2] > y[2] } ) 
EndCase 	

oBrowse:Refresh() 

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Recal³ Autor ³ Sergio Silveira       ³ Data ³14/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua o calculo do valor orcado com base nas tarefas      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700Recal( @ExpA1, [ ExpN1 ] )                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 -> Array contendo os valores po etapa                ³±±
±±³          ³ ExpN1 -> Fator a ser aplicado ( multiplicacao )            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700Recal( aValores, nFator )                              

Local bAjustaHora := { |x| Left( Transform( x, "@R 9999:99" ), 7 ) } 

Local cCodTec   := ""

Local nPosTec   := GDFieldPos( "ABJ_CODTEC", aHeaderABJ ) 
Local nPosHor   := GDFieldPos( "ABJ_TORCAD", aHeaderABJ ) 
Local nSitAtu   := GDFieldPos( "ABJ_SITATU", aHeaderABJ ) 
Local nPosVal   := GDFieldPos( "ABI_VALOR"  ) 
Local nPosDes   := GDFieldPos( "ABI_DESCRI" ) 
Local nPosEtp   := GDFieldPos( "ABI_ETAPA"  ) 
Local nPosPed   := GDFieldPos( "ABI_NUMPV"  ) 
Local nPosPedIt := GDFieldPos( "ABI_ITEMPV" ) 
Local nDigiPrev := Len( CriaVar( "ABJ_TORCAD" ) ) - 3
Local nHoras    := 0 
Local nOpca     := 0 
Local nLoop     := 0 
Local nLoop2    := 0                      

nFator := If( ValType( nFator ) == "N", nFator, 1 ) 

aValores := {} 

For nLoop := 1 To Len( aCols ) 

	nValor := 0                      

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o item nao foi excluido e se nao possui pedido gerado ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !GDDeleted( nLoop ) .And. Empty( aCols[ nLoop, nPosPed ] + aCols[ nLoop, nPosPedIt ] )
	
		If Len( aColsABJ ) >= nLoop .And. ValType( aColsABJ[ nLoop ] ) == "A" 
		
			aAux := AClone( aColsABJ[ nLoop ] ) 		
		
			For nLoop2 := 1 To Len( aAux )      
			                                        
				If aAux[ nLoop2, nSitAtu ] == "1" 
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Obtem o valor hora do tecnico responsavel ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cCodTec := aAux[ nLoop2, nPosTec ]
				
					If !Empty( cCodTec ) 
						AA1->( DbSetOrder( 1 ) )
						If AA1->( DbSeek( xFilial( "SA1" ) + cCodTec ) )
							nHoras := HoraToInt( Eval( bAjustaHora, aAux[ nLoop2, nPosHor ] ), nDigiPrev )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Converte para a moeda do projeto          ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							nValor += AA1->AA1_VALOR * nHoras
						EndIf 	
			      EndIf 
			      
				EndIf 			      
			
			Next nLoop2 	
			
			aAdd( aValores, { nLoop, aCols[ nLoop, nPosEtp ], aCols[ nLoop, nPosDes ], nFator * xMoeda( nValor, 1, M->ABH_MOEDA, dDataBase ) } ) 
		
		EndIf		
		
	EndIf 
	
Next nLoop        

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Track³ Autor ³ Sergio Silveira       ³ Data ³18/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function At700Track(oGetD) 

Local aEnt      := {}
Local cProjeto  := M->ABH_PROJET
Local nPosItem  := GDFieldPos( "ABI_ITEM" ) 
Local nLoop     := 0 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os itens para rastreamento          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aCols ) 
	aAdd( aEnt, { "ABI", cProjeto + aCols[ nLoop, nPosItem ] } ) 
Next nLoop 

MaTrkShow( aEnt )              

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua um refresh da getdados               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ValType( oGetD ) == "O"
	oGetD:oBrowse:Refresh()
EndIf 	
           
Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700VlPrd³ Autor ³ Sergio Silveira       ³ Data ³18/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da digitacao do produto orcado                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700VlPrd()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700VlPrd()

Local lReturn := .T. 

If ( lReturn := ExistCpo( "SB1" ) ) 
	If !Empty( GDFieldGet( "ABI_NUMPV" ) + GDFieldGet( "ABI_ITEMPV" ) ) 
		lReturn := .F. 
		Help( "", 1, "AT700PRVL" ) // O produto / valor nao pode ser alterado quando existe pedido de venda 
	EndIf	
EndIf 

Return( lReturn ) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700VlOrc³ Autor ³ Sergio Silveira       ³ Data ³24/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da digitacao do valor orcado                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At700VlOrc()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700VlOrc()

Local lReturn := .T. 

If ( lReturn := Positivo() ) 
	If !Empty( GDFieldGet( "ABI_NUMPV" ) + GDFieldGet( "ABI_ITEMPV" ) ) 
		lReturn := .F. 
		Help( "", 1, "AT700PRVL" ) // O produto / valor nao pode ser alterado quando existe pedido de venda 
	EndIf	
EndIf 

Return( lReturn ) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700Ped  ³ Autor ³ Sergio Silveira       ³ Data ³17/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a chamada da geracao do pedido de venda do projeto  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700Ped()                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function AT700Ped(cAlias,nReg,nOpcx)
           
Local aProjPed    := {}              

Local cDlgCaption := ""
Local cVar        := ""

Local nOpca       := 0  
Local nLinIni     := 0 
Local nColIni     := 0 

Local oList
Local oBmp
Local oDlgLeg
Local oBold 
    
ABI->( DbSetOrder( 1 ) ) 	                          

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a tela de processamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

FormBatch( STR0051 + " - " + ABH->ABH_PROJET,; // Geracao do pedido de venda do projeto 
			  { STR0044,; //"Esta rotina efetua a criacao de pedidos de venda para cada etapa do projeto"
				STR0045,; //"que ainda nao possua um pedido de venda associado e possua valor de orcamento."
				STR0046,; //"Uma etapa pode ter seu pedido criado mais de uma vez desde que o pedido associado"
				STR0047 },; 				 //"seja excluido."
				{ {5,.F.,{|o| o:oWnd:End()}         },;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}  },;
				{2,.T.,{|o| o:oWnd:End() }}         })                      

If nOpcA == 1 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama a geracao dos pedidos           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	At700PrPed( @aProjPed ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Exibe os pedidos que foram criados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty(aProjPed)
		Help( "", 1, "AT700NPED" ) // Nenhum pedido foi gerado 
	Else 
		cDlgCaption := STR0048  //"Etapas que geraram pedidos"
		DEFINE MSDIALOG oDlgLeg TITLE cDlgCaption From 0 + nLinIni,0 + nColIni To ;
				 280 + nLinIni,400 + nColIni OF oMainWnd PIXEL STYLE WS_DLGFRAME          
				 
			DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
			
			@  0, -25 BITMAP oBmp RESNAME "PROJETOAP" oF oDlgLeg SIZE 55, 1000 NOBORDER WHEN .F. PIXEL
			
			@ 03, 40 SAY STR0049 + ABH->ABH_PROJET FONT oBold PIXEL   //"Projeto - "
			@ 14, 30 TO 16 ,400 LABEL '' OF oDlgLeg   PIXEL
			
		  	@  26, 38 LISTBOX oList VAR cVar Fields HEADER STR0024, STR0041, STR0050 SIZE 155, 92 Of oDlgLeg PIXEL // Etapa //"Pedido"###"Item"
		  				oList:SetArray( aProjPed )
			oList:bLine := { || { aProjPed[oList:nAT,2], aProjPed[oList:nAT,4], aProjPed[oList:nAT,3] } }
			
			DEFINE SBUTTON FROM 123, 166 TYPE 1 ACTION (oDlgLeg:End()) ENABLE OF oDlgLeg
		ACTIVATE MSDIALOG oDlgLeg CENTERED 
		CursorArrow() 
	EndIf 
	
EndIf 	
	
Return( Nil )        


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At700PrPed³ Autor ³ Sergio Silveira       ³ Data ³20/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a geracao do pedido de venda do projeto             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At700PrPed( [ @ExpA1 ] )                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array de pedidos gerados + itens                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At700PrPed( aProjPed ) 

Local   aColsLine   := {} 
Local   aListBox    := {} 
Local   aDadosIt    := {} 
Local   aDadosCFO   := {} 

Local   bCampo      := { |nCpo| FieldName( nCpo ) }
            
Local   cDlgCaption := ""
Local   cItem       := "00"
Local   cCfo        := ""
Local   cEstado     := SuperGetMV( "MV_ESTADO" )       
Local   cProdPad    := ""
Local   cSeekABI    := ""
Local   cLocPad     := ""

Local   lAt700Ped   := ExistBlock( "AT700PED" ) 
Local   lAta700PV   := ExistBlock( "ATA700PV" ) 
Local   lTravas     := .T. 
               
Local   nOpcA       := 0 
Local   nProg       := 0 
Local   nLinIni     := 0 
Local   nColIni     := 0 
Local   nLoop       := 0  
Local   nCampos     := 0 
Local   nCntFor     := 0 
Local   nSaveSX8	  := GetSX8Len()

Local   oDlgLeg    
Local   oList

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private aCols       := {}
Private aHeader     := {}

aProjPed := If( ValType( aProjPed ) == "A", aProjPed, {} ) 

aStruField := FWSX3Util():GetAllFields("SC6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If ((X3USO(GetSx3Cache(cField, "X3_USADO"))  .Or. AllTrim(cField) $ "C6_PROJET|C6_ITPROJ" ) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"SC6",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

cProdPad := ABH->ABH_CODPRO

cSeekABI := xFilial( "ABI" ) + ABH->ABH_PROJET 

cItem    := "00"
aDadosIt := {}
                                  
ABI->( DbSetOrder( 1 ) )
If ABI->( DbSeek( cSeekABI ) ) 
	
	While !ABI->( Eof() ) .And. cSeekABI == ABI->ABI_FILIAL + ABI->ABI_PROJET
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Percorre todas as etapas do projeto que ainda nao possuem pedido      ³
		//³ e cujo status informado seja "normal" e cujo valor nao seja 0         ³			
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty( ABI->ABI_NUMPV + ABI->ABI_ITEMPV ) .And. ABI->ABI_SITATU == "1" .And. !Empty( ABI->ABI_VALOR ) 
		
			If Empty( ABI->ABI_CODPRO ) 
				cCodPro := cProdPad 
			Else 					
				cCodPro := ABI->ABI_CODPRO				
			EndIf
		
			SB1->(DbSetOrder(1))
			If SB1->( DbSeek( xFilial("SB1") + cCodPro ) )
			
				lTravas := .T.
				
				cLocPad := If( Empty( RetFldProd(SB1->B1_COD,"B1_LOCPAD") ), "01", RetFldProd(SB1->B1_COD,"B1_LOCPAD") ) 

				SB2->(DbSetOrder(1))
				If SB2->(DbSeek( xFilial("SB2") + SB1->B1_COD + cLocPad ))
					If !( SoftLock("SB2") )
						lTravas := .F.
					EndIf
				EndIf 		
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ O produto deve possuir TES de saida                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SF4->(DbSetOrder(1))
				If SF4->(DbSeek( xFilial("SF4") + RetFldProd(SB1->B1_COD,"B1_TS") )) .And. lTravas 
					
					aAdd( aCols, Array(Len(aHeader)+1) )
					nCampos := Len( aCols )                         
					                                
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ O produto deve possuir TES de saida                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SA1->( DbSetOrder( 1 ) )
					SA1->( DbSeek( xFilial( "SA1" ) + ABH->ABH_CODCLI + ABH->ABH_LOJA ) )  
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Define o CFO                                         ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aDadosCFO := {}
				 	aAdd(aDadosCfo,{"OPERNF","S"})
				 	aAdd(aDadosCfo,{"TPCLIFOR",SA1->A1_TIPO})					
				 	aAdd(aDadosCfo,{"UFDEST"  ,SA1->A1_EST})
					cCfo := MaFisCfo(,SF4->F4_CF,aDadosCfo)
					
					cItem := SomaIt( cItem ) 
					
					aAdd( aDadosIt, Array( 5 ) )
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Preenche antecipadamente o valor unitario e a quantidade ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !Empty( nPosPrcVen := AScan( aHeader, { |x| AllTrim( x[ 2 ] ) == "C6_PRCVEN" } ) ) 
						aCols[ nCampos, nPosPrcVen ] := NoRound( ABI->ABI_VALOR, aHeader[ nPosPrcVen, 5 ] )
						aDadosIt[ Len( aDadosIt ), 3 ] := aCols[ nCampos, nPosPrcVen ]
					EndIf 	

					If !Empty( nPosQtdVen := AScan( aHeader, { |x| AllTrim( x[ 2 ] ) == "C6_QTDVEN" } ) )
						aCols[ nCampos, nPosQtdVen ]	:= NoRound( 1, aHeader[ nPosQtdVen, 5 ] )
						aDadosIt[ Len( aDadosIt ), 2 ] := aCols[ nCampos, nPosQtdVen ]	
					EndIf 	
					
					For nCntFor := 1 To Len(aHeader)
						If	AllTrim(aHeader[nCntFor,2]) == "C6_ITEM"
							aCols[nCampos,nCntFor]	:= cItem 
							aDadosIt[Len(aDadosIt),5] := cItem 
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_PRODUTO"
							aCols[nCampos,nCntFor]	:= cCodPro
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_UM"
							aCols[nCampos,nCntFor]	:= SB1->B1_UM             
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_VALOR"
							aCols[nCampos,nCntFor]	:= NoRound( aCols[nCampos,nPosPrcVen]*;
										aCols[nCampos,nPosQtdVen], aHeader[ nCntFor, 5 ] ) 
							aDadosIt[ Len( aDadosIt ), 4 ] := aCols[nCampos,nCntFor]
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_TES"
							aCols[nCampos,nCntFor]	:= RetFldProd(SB1->B1_COD,"B1_TS")      
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_CF"
							aCols[nCampos,nCntFor]	:= cCfo
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_SEGUM"
							aCols[nCampos,nCntFor]	:= SB1->B1_SEGUM
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_LOCAL"
							aCols[nCampos,nCntFor]	:= RetFldProd(SB1->B1_COD,"B1_LOCPAD")
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_ENTREG"
							aCols[nCampos,nCntFor]	:= dDataBase
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_DESCRI"
							aCols[nCampos,nCntFor]	:= SB1->B1_DESC
							aDadosIt[ Len( aDadosIt ), 1 ] := SB1->B1_DESC
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_PRUNIT"
							aCols[nCampos,nCntFor]	:= ABI->ABI_VALOR 
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_PROJET"
							aCols[nCampos,nCntFor]	:= ABI->ABI_PROJET
						ElseIf AllTrim(aHeader[nCntFor,2]) == "C6_ITPROJ"
							aCols[nCampos,nCntFor]	:= ABI->ABI_ITEM 
						ElseIf AllTrim(aHeader[nCntFor,2]) <> "C6_PRCVEN" .And. ;
								AllTrim(aHeader[nCntFor,2]) <> "C6_QTDVEN"
							aCols[nCampos,nCntFor] := CriaVar(aHeader[nCntFor,2],.T.)
						EndIf
					Next nCntFor 
					
					aCols[nCampos,Len(aHeader)+1] := .F.
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Armazena os itens que geraram PV                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aAdd( aProjPed, { ABI->ABI_PROJET, ABI->ABI_ETAPA, cItem, "" } ) 
				
				EndIf
					
			EndIf
			
		EndIf    
         
		ABI->( DbSkip() ) 

	EndDo  
	
	If	nCampos > 0
	
		SA1->(DbSetOrder(1))
		If SA1->(DbSeek( xFilial("SA1") + ABH->ABH_CODCLI + ABH->ABH_LOJA ))

			DbSelectArea("SC5")
			nCampos := SC5->( FCount() )
			For nCntFor := 1 To nCampos
				M->&(Eval(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
			Next
			M->C5_TIPO    := "N"
			M->C5_CLIENTE := SA1->A1_COD
			M->C5_LOJAENT := SA1->A1_LOJA 
			M->C5_LOJACLI := SA1->A1_LOJA 
			M->C5_TIPOCLI := SA1->A1_TIPO
			M->C5_CONDPAG := ABH->ABH_CPAGPV
			M->C5_TABELA  := "1"
			M->C5_DESC1   := 0
			M->C5_DESC2   := 0
			M->C5_DESC3   := 0
			M->C5_DESC4   := 0
			M->C5_MOEDA   := ABH->ABH_MOEDA 

			Begin Transaction
			Inclui := .T.                         
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada antes da gravacao do pedido         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lAt700Ped
				ExecBlock( "AT700PED", .F., .F. ) 				
			EndIf 
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ funcao de gravacao de pedido. Padrao                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A410Grava("SC5","SC6")
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			EndDo
			EvalTrigger()  
				
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada apos a gravacao do pedido           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lATA700PV
				ExecBlock("ATA700PV",.F.,.F.)
			EndIf         

			End Transaction
		   
			While ( GetSX8Len() > nSaveSx8 )
				RollBackSx8()
			EndDo

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona o numero de pedido ao array                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AEval( aProjPed, { |x| x[4] := M->C5_NUM } ) 
         
		EndIf 

	EndIf    

EndIf	

Return( .T. ) 
