#include "pmsa110.ch"
#include "dbtree.ch"
#include "mproject.ch"
#include "protheus.ch"
#include "pmsicons.ch"

/*/


Ŀ
Funo     PMSA110   Autor  Edson Maricate         Data  09-02-2001 
Ĵ
Descrio  Programa de geracao de Projetos a partir dos Orcamentos.     
Ĵ
 Uso       Generico                                                     
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
                                                                      
ٱ


*/
Function PMSA110()

PRIVATE cCadastro	:= STR0001 //"Preparacao do Projeto"
PRIVATE aRotina := MenuDef()
PRIVATE nDlgPln := PMS_VIEW_TREE

If AMIIn(44) .And. !PMSBLKINT()
	dbSelectArea("AF1")
	dbSetOrder(1)
	mBrowse(6,1,22,75,"AF1",,,,,,PmsAF1Color())
EndIf

Return



/*/


Ŀ
Funo    PMS110Gera Autor  Edson Maricate         Data  09-02-2001 
Ĵ
Descrio  Efetua a gravacao dos arquivos de projetos com base no orcam.
Ĵ
 Uso       Generico                                                     
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
                                                                      
ٱ


*/
Static Function PMS110Gera()
Local cMvGer	 := GetMv("MV_FASEGER")
Local cFaseOrc	 := Substr(cMvGer,AT("ORC=",cMvGer)+4,2)
Local cFasePrj	 := Substr(cMvGer,AT("PROJ=",cMvGer)+5,2)
Local bCampo 	:= {|n| FieldName(n) }
Local nX,nY,nA,nI
Local aPredec	:=	{}
Local lPENewCode := ExistBlock('PMS110CD')
Local cNewCode  := ""
Local cTabOk	:= ""
Local aTemp	:= {}

PcoIniLan("000350")

aTemp	:=	{{"TAREFA","C",TamSx3("AF9_TAREFA")[1],TamSx3("AF9_TAREFA")[2]}}
cTabOk:=	CriaTrab(Nil,.F.)
MsCreate(cTabOk,aTemp,"TOPCONN")
Sleep(1000)

Begin Transaction  

dbSelectArea("AF8")

For nI:= 1 to FCount()
	If FieldName(nI) <> "AF8_PROJET" 
		&("M->"+FieldName(nI)):= CriaVar(FieldName(nI))
	EndIf
Next

//RegToMemory("AF8",.T.)
RecLock("AF8",.T.)

For nx := 1 TO FCount()
	nPosCpo := AF1->(FieldPos("AF1_"+Substr(AF8->(FieldName(nx)),5)))
	If FieldName(nX) <> "AF8_PROJET" 
		If nPosCpo > 0
			FieldPut(nx,AF1->(FieldGet(nPosCpo)))
		Else
			FieldPut(nx,M->&(EVAL(bCampo,nx)))
		EndIf 
	EndIf
Next nx                      

cNewCode := UPPER(MV_PAR01)

If lPENewCode
	cNewCode := Execblock("PMS110CD",.F.,.F.)
Endif

AF8->AF8_FILIAL 	:= xFilial("AF8")
AF8->AF8_PROJET	:= cNewCode
AF8->AF8_DATA		:= dDataBase
AF8->AF8_DESCRI	:= AF1->AF1_DESCRI
AF8->AF8_REVISA	:= StrZero(1, TamSX3("AF8_REVISA")[1])
AF8->AF8_TPPRJ	:= AF1->AF1_TPORC
AF8->AF8_FASE	:= cFasePrj
AF8->AF8_PRJREV	:= CriaVar("AF8_PRJREV")
AF8->AF8_CALEND	:= MV_PAR03
AF8->AF8_LOCPAD	:= MV_PAR02
MsUnlock()
MsMM(,,,MsMM(AF1->AF1_CODMEM),1,,,"AF8", "AF8_CODMEM")

PmsCopyCon("AF1",AF1->(Recno()))

If ExistBlock("PM110AF8")
	ExecBlock("PM110AF8",.F.,.F.)
EndIf

PmsAvalPrj("AF8" ,1 ,.F.)

PmsIncProc(.T.)

dbSelectArea("AF5")
dbSetOrder(3)
dbSeek(xFilial("AF5")+AF1->AF1_ORCAME)
AF2->(MsSeek(xFilial("AF2")+AF1->AF1_ORCAME))
While !Eof() .And. xFilial()+AF1->AF1_ORCAME==;
	AF5->AF5_FILIAL+AF5->AF5_ORCAME
		
	RegToMemory("AFC",.T.)
	RecLock("AFC",.T.)
	For nx := 1 TO FCount()
		nPosCpo := AF5->(FieldPos("AF5_"+Substr(AFC->(FieldName(nx)),5)))
		If nPosCpo > 0
			FieldPut(nx,AF5->(FieldGet(nPosCpo)))
		Else
			FieldPut(nx,M->&(EVAL(bCampo,nx)))
		EndIf
	Next nx
	AFC->AFC_FILIAL	:= xFilial("AFC")
	AFC->AFC_PROJET	:= cNewCode
	AFC->AFC_REVISA	:= StrZero(1, TamSX3("AFC_REVISA")[1])
	If AF5->AF5_NIVEL == "001"
		AFC->AFC_EDT	:= cNewCode
	EndIf
	If AF5->AF5_NIVEL == "002"
		AFC->AFC_EDTPAI	:= cNewCode
	Else
		If AF5->AF5_NIVEL <> "001"
			AFC->AFC_EDTPAI	:= AF5->AF5_EDTPAI
		EndIf
	EndIf
	AFC->AFC_CALEND	:= MV_PAR03
	MsUnlock()
	MSMM(,TamSx3("AFC_OBS")[1],,MSMM(AF5->AF5_CODMEM,80),1,,,"AFC","AFC_CODMEM")
	PmsCopyCon("AF5",AF5->(Recno()))
	
	If ExistBlock("PM110AFC")
		ExecBlock("PM110AFC",.F.,.F.)
	EndIf
	
	dbSelectArea("AF5")
	dbSkip()
	
	PmsIncProc(.T.)
End

dbSelectArea("AF2")
dbSetOrder(1)
dbSeek(xFilial("AF2")+AF1->AF1_ORCAME)
While !Eof() .And. xFilial()+AF1->AF1_ORCAME==;
	AF2->AF2_FILIAL+AF2->AF2_ORCAME
	RegToMemory("AF9",.T.)
	RecLock("AF9",.T.)
	For nx := 1 TO FCount()
		nPosCpo := AF2->(FieldPos("AF2_"+Substr(AF9->(FieldName(nx)),5)))
		If nPosCpo > 0
			FieldPut(nx,AF2->(FieldGet(nPosCpo)))
		Else
			FieldPut(nx,M->&(EVAL(bCampo,nx)))
		EndIf
	Next nx
	AF9->AF9_FILIAL := xFilial("AF9")
	AF9->AF9_PROJET	:= cNewCode
	AF9->AF9_REVISA	:= StrZero(1, TamSX3("AF9_REVISA")[1])
	If AF2->AF2_NIVEL == "002"
		AF9->AF9_EDTPAI	:= cNewCode
	EndIf
	AF9->AF9_CALEND	:= If(Empty(AF2->AF2_CALEND),MV_PAR03,AF2->AF2_CALEND)
	AF9->AF9_START	:= dDataBase
	AF9->AF9_FINISH := dDataBase
	AF9->AF9_HUTEIS := AF2->AF2_HDURAC
	MsUnlock()
	MSMM(,TamSx3("AF9_OBS")[1],,MSMM(AF2->AF2_CODMEM,80),1,,,"AF9","AF9_CODMEM")
	
	PmsCopyCon("AF2",AF2->(Recno()))
	
	If ExistBlock("PM110AF9")
		ExecBlock("PM110AF9",.F.,.F.)
	EndIf
	
	
	PmsIncProc(.T.)
	
	dbSelectArea("AF3")
	dbSetOrder(1)
	dbSeek(xFilial("AF3")+AF2->AF2_ORCAME+AF2->AF2_TAREFA)
	While !Eof() .And.xFilial()+AF2->AF2_ORCAME+AF2->AF2_TAREFA==;
		AF3->AF3_FILIAL+AF3->AF3_ORCAME+AF3->AF3_TAREFA
		RegToMemory("AFA",.T.)
		RecLock("AFA",.T.)
		For nx := 1 TO FCount()
			nPosCpo := AF3->(FieldPos("AF3_"+Substr(AFA->(FieldName(nx)),5)))
			If nPosCpo > 0
				FieldPut(nx,AF3->(FieldGet(nPosCpo)))
			Else
				FieldPut(nx,M->&(EVAL(bCampo,nx)))
			EndIf
		Next nx
		
		If ! Empty(AF3->AF3_RECURS)
			AFA->AFA_FIX := "2"
		EndIf

		
		AFA->AFA_FILIAL := xFilial("AFA")
		AFA->AFA_PROJET	:= cNewCode
		AFA->AFA_REVISA := StrZero(1, TamSX3("AFA_REVISA")[1])
		If AF3->AF3_ACUMUL$"5/6"
			AFA->AFA_DTAPRO	:= AF9->AF9_START
		EndIf
		
		MsUnlock()
		
		PmsCopyCon(Alias(),Recno())
		
		If ExistBlock("PM110AFA")
			ExecBlock("PM110AFA",.F.,.F.)
		EndIf
		
		dbSelectArea("AF3")
		dbSkip()
		PmsIncProc(.T.)
	End
	
	DbSelectArea("AF7")
	DbSetOrder(1)
	If MsSeek(xFilial("AF7")+AF2->AF2_ORCAME+AF2->AF2_TAREFA)
		Aadd(aPredec,{AF9->(Recno()),{}})
		While !Eof() .And.xFilial()+AF2->AF2_ORCAME+AF2->AF2_TAREFA==;
			AF7->AF7_FILIAL+AF7->AF7_ORCAME+AF7->AF7_TAREFA
			AAdd(aPredec[Len(aPredec)][2],AF7->(Recno()))
			dbSelectArea("AF7")
			dbSkip()
			PmsIncProc(.T.)
		EndDo
		
	Endif
	
	DbSelectArea("AF4")
	DbSetOrder(1)
	MsSeek(xFilial()+AF2->AF2_ORCAME+AF2->AF2_TAREFA)
	While !Eof() .And.xFilial()+AF2->AF2_ORCAME+AF2->AF2_TAREFA==;
		AF4->AF4_FILIAL+AF4->AF4_ORCAME+AF4->AF4_TAREFA
		RegToMemory("AFB",.T.)
		RecLock("AFB",.T.)
		For nx := 1 TO FCount()
			nPosCpo := AF4->(FieldPos("AF4_"+Substr(AFB->(FieldName(nx)),5)))
			If nPosCpo > 0
				FieldPut(nx,AF4->(FieldGet(nPosCpo)))
			Else
				FieldPut(nx,M->&(EVAL(bCampo,nx)))
			EndIf
		Next nx
		AFB->AFB_FILIAL := xFilial()
		AFB->AFB_PROJET := cNewCode
		AFB->AFB_REVISA := StrZero(1, TamSX3("AFB_REVISA")[1])
		MsUnlock()
		If ExistBlock("PM110AFB")
			ExecBlock("PM110AFB",.F.,.F.)
		EndIf
		
		dbSelectArea("AF4")
		dbSkip()
		PmsIncProc(.T.)
	End
	
	dbSelectArea("AF2")
	dbSkip()
	PmsIncProc(.T.)
End
//Gravar as predecessoras so no final apra evitar erro de chave estrangeira
For nA:=1 To Len(aPredec)
	AF9->(MsGoTo(aPredec[nA,1]))
	For nY:=1 To Len(aPredec[nA,2])
		AF7->(MsGoTo(aPredec[nA,2,nY]))
		RegToMemory("AFD",.T.)
		RecLock("AFD",.T.)
		For nx := 1 TO FCount()
			nPosCpo := AF7->(FieldPos("AF7_"+Substr(AFD->(FieldName(nx)),5)))
			If nPosCpo > 0
				FieldPut(nx,AF7->(FieldGet(nPosCpo)))
			Else
				FieldPut(nx,M->&(EVAL(bCampo,nx)))
			EndIf
		Next nx
		
		AFD->AFD_FILIAL := xFilial("AFD")
		AFD->AFD_PROJET := cNewCode
		AFD->AFD_REVISA := StrZero(1, TamSX3("AFD_REVISA")[1])
		MsUnlock()
		
		If ExistBlock("PM110AFD")
			ExecBlock("PM110AFD",.F.,.F.)
		EndIf
	Next nY
Next nX
RecLock("AF1",.F.)
AF1->AF1_FASE	:= cFaseOrc
MsUnlock()

// Ponto de Entrada para a gravao de campos do usuario
If ExistBlock("PMA110GERA")
	ExecBlock("PMA110GERA",.F.,.F.)
EndIf

// Ponto de Entrada para a gravao de campos do usuario no Template
If ExistTemplate("PMA110GERA")
	ExecTemplate("PMA110GERA",.F.,.F.)
EndIf

AF8CalcNew(AF8->(Recno()),MV_PAR04,MV_PAR05, .T. , AF8->AF8_REVISA ,/*OTREE*/ ,/*cArquivo*/,,,,,.T.,.F.,.F.,,cTabOk)

AF9->(DbSetOrder(2))
AF9->(MsSeek(xFilial()+AF8->AF8_PROJET+AF8->AF8_REVISA))
While !AF9->(Eof()) .And. xFilial('AF9')+AF8->AF8_PROJET+AF8->AF8_REVISA== AF9->AF9_Filial+AF9->AF9_PROJET+AF9->AF9_REVISA
	PcoDetLan("000350","01","PMSA110")
	AF9->(DbSkip())
Enddo
PcoFinLan("000350")    

End Transaction

MsErase(cTabOk)

Return .T.


/*/


Ŀ
Funo    PMS110Aval  Autor Fabio Rogerio Pereira   Data  07-05-2002
Ĵ
Descrio  Efetua a validacao do orcamento 							          
Ĵ
 Uso       Generico                                                     
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
                                                                      
ٱ


*/
Function PMS110Aval()
Local cProjet  := ""
Local aAreaAF8 := {}
Local lRet := .T.

If ExistBlock("PMA110Orc")
	If ExecBlock("PMA110Orc", .T., .T.) 
		If __lSX8
			RollBackSX8()
		EndIf
		lRet := .F.
	EndIf	
EndIf

// validacao de arquivo vazio
If lRet
	If AF1->(BOF()) .AND. AF1->(EOF())
		HELP(" " , 1 , "ARQVAZIO")
		lRet := .F.
	Endif
EndIf

If lRet .AND. !PmsOrcUser(AF1->AF1_ORCAME, , Padr(AF1->AF1_ORCAME, Len(AF5->AF5_EDT)), "  ", 2, "PROJET")
	Aviso(STR0006, STR0007, {STR0008}, 2) //"Usurio sem permisso"###"Usurio sem permisso para gerar projeto deste oramento. Verifique as permisses do usurio na estrutura principal do oramento."###"Fechar"
	lRet := .F.
EndIf

If lRet .AND. !PmsVldFase("AF1", AF1->AF1_ORCAME, "43")
	lRet := .F.
EndIf

If lRet 
	aAreaAF8:=AF8->(GetArea())
	dbSelectArea("AF8")
	AF8->(dbSetOrder(1))
	AF8->(dbGoBottom())
	cProjet := Soma1(RTrim(AF8->AF8_PROJET))
	If Empty(cProjet)
		cProjet := StrZero(1, TAMSX3("AF8_PROJET")[1])
	EndIf
	RestArea(aAreaAF8)
	
	SetMVValue("PS110A","MV_PAR01",cProjet)

	If Pergunte("PS110A", .T.) 
	
		If !Empty(MV_PAR01)
			If ExistBlock("PMA110INC")
				If ExecBlock("PMA110INC", .T., .T.) 
				
					If __lSX8
						RollBackSX8()
					EndIf	
				
					lRet := .F.					
				End
			EndIf

			If lRet	
				Processa({||PMS110Gera()}, STR0005) //"Gerando Projeto..."
				
				If __lSX8
					ConfirmSX8()
				EndIf	
				
			EndIf
		Else
	
			If __lSX8
				RollBackSX8()
			EndIf
	
			Help(1, " ", "OBRIGAT", , , 3, 0)
		EndIf
	Else
				
		If __lSX8
			RollBackSX8()
		EndIf
	EndIf

EndIf

Return lRet

/*/


Ŀ
Funo    PMSSelAF3OP Autor Adriano Ueda            Data  26/04/2005
Ĵ
Descrio  Validao do produto para o usurio selecionar o opcional    
Ĵ
ParametrosNenhum                                                        
Ĵ
Retorno    .T., nenhum problema ocorreu                                 
Ĵ
 Uso      Generico                                                      
ٱ


*/
Function PMSSelAF3Opc()
Local lRet := .T.

Local nPosOpc		:= aScan(aHeader,{|x| AllTrim(x[2])=="AF3_OPC"})
Local nPosMOpc	:= aScan(aHeader,{|x| AllTrim(x[2])=="AF3_MOPC"})
Local nPosQuant	:= aScan(aHeader, {|x| AllTrim(x[2])=="AF3_QUANT"})

If (nPosOpc > 0 .Or. nPosMOpc > 0)
	lRet := SeleOpc(2,"PMSA110",M->AF3_PRODUT,,,,"M->AF3_MOPC",,aCols[n][nPosQuant],,,,)
EndIf

Return lRet

/*/


Ŀ
Funo    PMSSelAFAOP Autor Adriano Ueda            Data  26/04/2005
Ĵ
Descrio  Validao do produto para o usurio selecionar o opcional    
Ĵ
ParametrosNenhum                                                        
Ĵ
Retorno    .T., nenhum problema ocorreu                                 
Ĵ
 Uso      Generico                                                      
ٱ


*/
Function PMSSelAFAOpc()
Local lRet := .T.

Local nPosQuant	:= aScan(aHeader,{|x| AllTrim(x[2])=="AFA_QUANT"})
Local nPosOpc		:= aScan(aHeader,{|x| AllTrim(x[2])=="AFA_OPC"})
Local nPosMOpc	:= aScan(aHeader,{|x| AllTrim(x[2])=="AFA_MOPC"})
Local nPosProd	:= aScan(aHeader,{|x| AllTrim(x[2])=="AFA_PRODUT"})

If (nPosOpc > 0 .Or. nPosMOpc > 0)
	lRet := SeleOpc(2,"PMSA110",aCols[n][nPosProd],,,,"M->AFA_MOPC",,aCols[n][nPosQuant],,,,)
EndIf         

Return lRet

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 30/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina 	:= {	{ STR0002,"AxPesqui"  , 0 , 1, 0, .F.},; //"Pesquisar"
						{ STR0003,"PMS100Dlg" , 0 , 2},; //"Visualizar"
						{ STR0004,"PMS110Aval", 0 , 2} } //"Gerar"
Return(aRotina)
