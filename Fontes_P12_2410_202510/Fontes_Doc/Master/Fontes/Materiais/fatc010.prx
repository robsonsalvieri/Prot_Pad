#INCLUDE "FATC010.CH" 
#INCLUDE "PROTHEUS.CH" 
#INCLUDE "FWMVCDEF.CH"      
#INCLUDE "FATC010.CH"  
#include "msgraphi.ch"

STATIC __oTempTable := NIL

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ FATC010  ³ Autor ³ Sergio Silveira       ³ Data ³05/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pipeline - Administracao de vendas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function FATC010()

PRIVATE cCadastro := OemToAnsi( STR0001 )  //"Pipeline - AFV"
PRIVATE aRotina   := MenuDef()

mBrowse( 6, 1,22,75,"AC1", , , , , 2 )       

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Pipe³ Autor ³ Sergio Silveira       ³ Data ³07/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pipeline - Administracao de vendas                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Ftc010Pipe( cAlias, nRecNo, nOpc, xFil, aMens, cRepres ) 

Local aArea	      := GetArea()
Local aPosObj     := {} 
Local aObjects    := {}                        
Local aSize       := MsAdvSize( .F. ) 
Local aItensAC2   := {} 
Local aLegenda    := {}
Local aUsButtons  := {} 
LOCAL cCbx := ""
Local cTitulo     := STR0004 + Capital( AC1->AC1_DESCRI ) //"Pipeline - "
Local lPergunte   := .T.
Local nLinIni     := 0
Local nRight      := 0
Local nModelo     := 1 
Local nLoop       := 0
Local aCbx 		  := {STR0044,STR0045,STR0025}	//"Colunas"###"Funil"###"Pizza"
Local aTpGraph    := {COLUMNCHART,FUNNELCHART,NEWPIECHART}	//Tipo Gráfico -> Colunas/Funil/Pizza

Local oDlg
Local oGraph
Local oListBox  
Local oMenu 
Local oBold
Local oCbx             
Local oBut1
Local oPanel1
Local oPanel2

PRIVATE nSerie 
                
lPergunte := If( Valtype( aMens ) == "A", .T., Pergunte( "FTC010", .T., STR0005 ) )  //"Parametros do Pipeline"

If lPergunte // "Parametros de filtro - Service Tracker"  
	
	DEFINE MSDIALOG oDlg TITLE STR0039 FROM 09,0 TO 20,40 OF oMainWnd //"Modelo de Grafico"
	
	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
	
	@  0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 1000 NOBORDER WHEN .F. PIXEL 
	
	@ 03, 50 SAY STR0039 FONT oBold PIXEL  //"Modelo de Grafico"
	
	@ 14, 40 TO 16 ,400 LABEL '' OF oDlg   PIXEL
	
	@ 030, 50 SAY STR0038 of oDlg PIXEL  //"Modelo"
	
	@ 039, 50 MSCOMBOBOX oCbx VAR cCbx ITEMS aCbx SIZE 061, 65 OF oDlg PIXEL ON CHANGE nModelo := oCbx:nAt 
		                                                                                        
	DEFINE SBUTTON oBut1 FROM 062, 120 TYPE 1 ACTION ( nOpca := 1, oDlg:End() )  ENABLE of oDlg		 

	ACTIVATE MSDIALOG oDlg CENTERED  

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Constroi a Janela da Consulta                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua os calculos de Auto Size                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aObjects := {} 
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )
	AAdd( aObjects, {  94, 100, .F., .T. } )
	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	aPosObj := MsObjSize( aInfo, aObjects, , .T. )
                              
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua os calculos de Auto Size ( direita da tela )                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	aObjects := {}                                
	AAdd( aObjects, { 100,  10, .T., .F. } )	
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )
	AAdd( aObjects, { 100, 100, .T., .T., .T. } )	
	AAdd( aObjects, { 100,  35, .T., .F. } )
	
	aInfo    := { aPosObj[ 2, 2 ], aPosObj[ 2, 1 ], aPosObj[ 2, 4 ], aPosObj[ 2, 3 ], 3, 3 } 
	aPosObj2 := MsObjSize( aInfo, aObjects )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada para a criacao de botoes de usuario                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "FC010BUT" ) 
		aUsButtons := AClone( ExecBlock( "FC010BUT", .F., .F. ) ) 
	EndIf 
	
	DEFINE MSDIALOG oDlg FROM aSize[7],00 TO aSize[6],aSize[5] TITLE cTitulo OF oMainWnd PIXEL

	nLinIni := aPosObj[2,1] 
	nRight  := aSize[5]/2

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria o objeto grafico                                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oPanel1 := TPanel():New(0,0,,oDlg,,,,,,0,0)
	oPanel2 := TPanel():New(aPosObj[1][1],aPosObj[1][2],,oPanel1,,,,,,aPosObj[1,3],aPosObj[1,4])
	oPanel1:Align := CONTROL_ALIGN_ALLCLIENT
	
	oGraph := FWChartFactory():New()
	oGraph:SetOwner(oPanel2)

	Ftc010Disp(@oGraph,cRepres,@aItensAC2,nModelo)

	oGraph:SetChartDefault(aTpGraph[nModelo])

	oGraph:SetLegend(CONTROL_ALIGN_BOTTOM)
	oGraph:SetAlignSerieLabel(CONTROL_ALIGN_LEFT)
	oGraph:lSerieLabel := .F.
	oGraph:lEnableMenu := .F.	

	oGraph:Activate()

	cLegenda := STR0006    //"Legenda"
	
	aLegenda := {}
	AEval( aItensAC2, { |x| AAdd( aLegenda, x[1] ) } )

	@ aPosObj2[3,1]-8,aPosObj2[1,2] SAY STR0006 + " - " + STR0012 OF oDlg PIXEL  //"Legenda"##"Etapas"
	@ aPosObj2[3,1]  ,aPosObj2[3,2] LISTBOX oListBox VAR cLegenda ITEMS aLegenda PIXEL SIZE aPosObj2[3,3], aPosObj2[3,4] OF oDlg MULTI
	
	DEFINE SBUTTON FROM aPosObj[2,3] - 17,nRight - 36 TYPE 1 ENABLE OF oDlg ACTION ( oDlg:End() )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a criacao dos botoes de usuario                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType( aUsButtons ) == "A"
		If !Empty( aUsButtons ) 
			For nLoop := 1 To Len( aUsButtons ) 
				TButton():New( aPosObj[2,3] - 31, nRight - 07 - nLoop * 29,OemToAnsi(AllTrim(aUsButtons[nLoop,1])), oDlg,aUsButtons[ nLoop, 2],28,12,,oDlg:oFont,.F.,.T.,.F.,,.F.,,,.F.)
			Next nLoop 
		EndIf	
	EndIf
	            
	oListBox:bRClicked  := { |o,nX,nY| oMenu:Activate( nX, nY, oListBox ) } // Posição x,y em relação a Dialog 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define os itens do Menu PopUp                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MENU oMenu POPUP 
		MENUITEM STR0007   Action Ftc010Etp( oListBox, aItensAC2 )   //"Visualiza etapa"
		MENUITEM STR0008     Action FTC010Opor( oListBox, aItensAC2, cRepres )  //"Oportunidades"
	ENDMENU
	
	ACTIVATE MSDIALOG oDlg 

EndIf
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a situacao de entrada                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea( aArea ) 

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Col ³ Autor ³ Sergio Silveira       ³ Data ³08/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Montagem das colunas                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010Col( ExpO1, [ ExpC1 ], ExpA1 )                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto grafico                                    ³±±
±±³          ³ ExpC1 -> Representante                                     ³±±
±±³          ³ ExpA1 -> Array da legenda                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010Col( oGraph, cRepres, aItensAC2, nModelo ) 

Local aDadosGRF		:= {} 
Local aFilBlocks	:= Array( 8 )         
Local cSeekAC2		:= ""
Local lPercent		:= ( MV_PAR02 == 2 )
Local lFirst		:= .T.                 
Local nQuant		:= 0 
Local nValor		:= 0 
Local nTotQuant		:= 0 
Local nTotValor		:= 0 
Local nTotMedia		:= 0 
Local nAltura		:= 0 
Local nRelAcumul	:= 0
Local nSoma			:= 0
Local nLoop			:= 0 

DEFAULT nModelo    := 0 

aItensAC2 := {}
AFill( aFilBlocks, { || .T. } ) 
                                   
cRepres := If( ValType( cRepres ) == "C", cRepres, "" ) 

AC2->( dbSetOrder( 1 ) ) 

cSeekAC2 := xFilial( "AC2" ) + AC1->AC1_PROVEN 
AC2->( dbSeek( cSeekAC2 + MV_PAR05, .T. ) )                 
	                  
aDadosGRF := {}        
nTotValor := 0 
nTotQuant := 0 
nTotMedia := 0 
	
While !AC2->( Eof() ) .And. cSeekAC2 == AC2->AC2_FILIAL + AC2->AC2_PROVEN .And. AC2->AC2_STAGE <= MV_PAR06   

	nQuant     := 0 
	nValor     := 0 
	
	nRelAcumul += ( AC2->AC2_RELEVA ) / 100
	
	Ftc010Stage( cRepres, @nQuant, @nValor, @nTotValor, @nTotQuant, @nTotMedia, @aDadosGRF, @lFirst, @aFilBlocks, nRelAcumul )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Adiciona ao array da legenda                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AAdd( aItensAC2, { AllTrim( AC2->AC2_STAGE ) + " - " + Capital( AC2->AC2_DESCRI ), AC2->( Recno() ) } ) 
	
	AC2->( dbSkip() ) 
	
EndDo
             
nSoma  := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percorre o array de colunas                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aDadosGRF ) 

	nQuant := aDadosGRF[ nLoop, 1 ] 
	nValor := aDadosGRF[ nLoop, 2 ] 	
	                    
	If lPercent 
		Do Case
		Case MV_PAR01 == 1
			nAltura := ( nQuant / nTotQuant ) * 100
		Case MV_PAR01 == 2
			nAltura := ( nValor / nTotValor ) * 100
		Case MV_PAR01 == 3
			nAltura := ( ( nValor / nQuant ) / nTotMedia ) * 100
		EndCase		                                                                     
	Else
		Do Case
		Case MV_PAR01 == 1
			nAltura := nQuant
		Case MV_PAR01 == 2
			nAltura := nValor
		Case MV_PAR01 == 3
			nAltura := nValor / nQuant 
		EndCase		                                                                     
	EndIf 
	
	nSoma += nAltura

Next nLoop

If Empty(nSoma) .And. nModelo == 3	//Gráfico Pizza
	Aviso( STR0041, STR0042, { STR0043 } ) //"Atenção" "Não é possível compor este modelo de gráfico, pois todos os valores envolvidos na análise estão zerados." "OK"
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Percorre o array de colunas                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nLoop := 1 To Len( aDadosGRF ) 
	
		nQuant := aDadosGRF[ nLoop, 1 ] 
		nValor := aDadosGRF[ nLoop, 2 ] 	
		                    
		If lPercent 
			Do Case
			Case MV_PAR01 == 1
				nAltura := ( nQuant / nTotQuant ) * 100
			Case MV_PAR01 == 2
				nAltura := ( nValor / nTotValor ) * 100
			Case MV_PAR01 == 3
				nAltura := ( ( nValor / nQuant ) / nTotMedia ) * 100
			EndCase		                                                                     
		Else
			Do Case
			Case MV_PAR01 == 1
				nAltura := nQuant
			Case MV_PAR01 == 2
				nAltura := nValor
			Case MV_PAR01 == 3
				nAltura := nValor / nQuant 
			EndCase		                                                                     
		EndIf 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria a coluna do grafico                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oGraph:addSerie(aDadosGRF[nLoop,3],nAltura)

		If lPercent
			oGraph:setPicture("@E 999.99")
		ElseIf MV_PAR01 >= 2
			oGraph:setPicture("@E 9,999,999,999,999.99")
		EndIf
		
	Next nLoop
EndIf

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Disp³ Autor ³ Sergio Silveira       ³ Data ³08/02/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua o redesenho do grafico                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010Disp( ExpO1, [ ExpC1 ], @ExpA1 )                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto grafico                                    ³±± 
±±³          ³ ExpC1 -> Representante                                     ³±±
±±³          ³ ExpA1 -> Array da legenda                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010Disp( oGraph, cRepres, aItensAC2, nModelo )
                                                
Local cEixoY  := ""                               
                             
If MV_PAR02 == 2 
	cEixoY := STR0009 //"Percentual"
Else	
	If MV_PAR01 == 1
		cEixoY := STR0008 //"Oportunidades"
	ElseIf MV_PAR01 == 2
		cEixoY := STR0010 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) ) //"Valores em "
	Else
		cEixoY := STR0011 + Capital( AllTrim( GetMV( "MV_MOEDA" + AllTrim( Str( MV_PAR25 ) ) ) ) )  //"Valores medios em "
	EndIf 		
EndIf                 
	
oGraph:SetTitle(cEixoY,CONTROL_ALIGN_LEFT)	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua a montagem das colunas                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Ftc010Col( @oGraph, cRepres, @aItensAC2, nModelo )

Return( .t. )  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Etp ³ Autor ³ Sergio Silveira       ³ Data ³06/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualiza a etapa                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010Etp( ExpO1, ExpA1 )                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto listbox                                    ³±± 
±±³          ³ ExpA1 -> Array de recnos                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010Etp( oListBox, aItensAC2 )

PRIVATE aRotina   := { { STR0012, "AxVisual", 0, 2 } }   //"Etapas"
PRIVATE cCadastro := STR0012  //"Etapas"
       
If !Empty( oListBox:nAt ) 
	AC2->( dbGoto( aItensAC2[ oListBox:nAt, 2 ] ) )  
	AxVisual( "AC2", AC2->( Recno() ), 1 ) 
EndIf
	
Return( .T. ) 
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Opor³ Autor ³ Sergio Silveira       ³ Data ³07/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualiza as oportunidades da etapa                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010Opor( ExpO1, ExpA1, ExpC1 )                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto listbox                                    ³±± 
±±³          ³ ExpA1 -> Array de itens AC2                                ³±± 
±±³          ³ ExpC1 -> Codigo do representante                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010Opor( oListBox, aItensAC2, cRepres )

Local aFilBlocks  := Array( 8 ) 
Local aStruQuery  := {}
Local cProven     := ""
Local cStage      := ""
Local cDescri     := ""
Local lFirst      := .T.
Local oDlg2	      := Nil     
Local oGetDB	  := Nil
Local aCposAD1    := {}
Local aCmpsAux1	  := {}
Local aCmpsAux2	  := {}
Local nQtdCpos    := 0
Local cRealName	  := ""
Local nX		  := 0

AFill( aFilBlocks, { || .T. } ) 

PRIVATE aRotina   := { { STR0012, "AxVisual", 0, 2 } }   //"Etapas"
PRIVATE aHeader   := {}
PRIVATE cCadastro := STR0012  //"Etapas"

Static cTmpTable	:= ""

nSel := oListBox:GetPos()
       
If !Empty(nSel) 
	AC2->(dbGoto(aItensAC2[nSel,2]))  

	aCmpsAux1 := FWSX3Util():GetListFieldsStruct("AD1",.F.)
	nQtdCpos := Len(aCmpsAux1)
	//Ordenar pelo campo X3_ORDEM
	For nX := 1 To nQtdCpos
		aAdd(aCmpsAux2,{aCmpsAux1[nX],GetSX3Cache(aCmpsAux1[nX][1],"X3_ORDEM")})
	Next nX
	aSort(aCmpsAux2,,,{|x,y| x[2] < y[2] })
	For nX := 1 To nQtdCpos
		aAdd(aCposAD1,aCmpsAux2[nX][1])
	Next nX
	For nX := 1 To nQtdCpos
		If ( X3Uso(GetSX3Cache(aCposAD1[nX][1],"X3_USADO")) .And.;
				 aCposAD1[nX][2] <> "M" .And.;
				 cNivel >= GetSX3Cache(aCposAD1[nX][1],"X3_NIVEL") .And.;
				 GetSX3Cache(aCposAD1[nX][1],"X3_CONTEXT") <> "V" ) .Or. AllTrim(aCposAD1[nX][1]) == "AA3_CONTRT"
			AAdd(aStruQuery,{	aCposAD1[nX][1],;
									aCposAD1[nX][2],;
									aCposAD1[nX][3],;
									aCposAD1[nX][4]	})
			AAdd(aHeader,{ AllTrim(FWX3Titulo(aCposAD1[nX][1])),;
					aCposAD1[nX][1],;
					GetSX3Cache(aCposAD1[nX][1],"X3_PICTURE"),;
					aCposAD1[nX][3],;
					aCposAD1[nX][4],;
					GetSX3Cache(aCposAD1[nX][1],"X3_VALID"),;
					GetSX3Cache(aCposAD1[nX][1],"X3_USADO"),;
					aCposAD1[nX][2],;
					GetSX3Cache(aCposAD1[nX][1],"X3_ARQUIVO"),;
					GetSX3Cache(aCposAD1[nX][1],"X3_CONTEXT") })
		EndIf
	Next nX

	AAdd(aStruQuery,{"AD1_FLAG","L",1,0})

	If __oTempTable <> NIL
		cTmpTable := __oTempTable:GetAlias()
		//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
		If Select(cTmpTable) == 0
			__oTempTable:Delete()
			FreeObj(__oTempTable)
		EndIf
	EndIf

	If __oTempTable == NIL
		cTmpTable := GetNextAlias()
		//-------------------------------------------------------------------
		// Criação do objeto  
		//-------------------------------------------------------------------
		__oTempTable := FWTemporaryTable():New(cTmpTable)

		//-------------------------------------------------------------------
		// Atribui campos e índices.  
		//-------------------------------------------------------------------
		__oTempTable:SetFields(aStruQuery)
		__oTempTable:AddIndex("1",{"AD1_NROPOR"})
		
		//------------------
		//Criação da tabela
		//------------------
		__oTempTable:Create()	
	Else
		cRealName := __oTempTable:GetRealName()
		TcSqlExec("TRUNCATE TABLE " + cRealName)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama a funcao que alimenta o arquivo de trabalho                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Ftc010Stage( cRepres, , , , , , , @lFirst, @aFilBlocks, , 2, aStruQuery )
		
	If ( (cTmpTable)->( !Eof() ) )
	
		cProVen := AC2->AC2_PROVEN
		cStage  := AC2->AC2_STAGE
		cDescri := Capital( AC2->AC2_DESCRI ) 
	
		DEFINE MSDIALOG oDlg2 FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd

			@ 008,008 SAY STR0013  SIZE 028,007 OF oDlg2 PIXEL //FONT oBold  //"Processo"
			@ 017,007 MSGET cProven When .F. SIZE 026,009 OF oDlg2 PIXEL
		                              
			@ 008,042 SAY STR0014    SIZE 024,007 OF oDlg2 PIXEL //FONT oBold //"Etapa"
			@ 017,042 MSGET cStage When .F. SIZE 018,009 OF oDlg2 PIXEL
			                          
			@ 008,077 SAY STR0015 SIZE 030,007 OF oDlg2 PIXEL //FONT oBold  //"Descricao"
			@ 017,077 MSGET cDescri When .F. SIZE 109,009 OF oDlg2 PIXEL

			oGetDB:=MsGetDB():New(038,005,139,312,1,,,,.F.,,,.F.,,cTmpTable)
			
			DEFINE SBUTTON 		FROM 005,284 TYPE 1  ENABLE OF oDlg2 ACTION ( oDlg2:End() )
		   	DEFINE SBUTTON oBtn FROM 020,284 TYPE 15 ENABLE OF oDlg2 ACTION ( oGetDB:oBrowse:SetFocus(),Ftc010ViOp()) 

		ACTIVATE MSDIALOG oDlg2 CENTERED
	Else
		Help( " ", 1, "ATC010OPOR" ) 
	EndIf  

	dbSelectArea( "AD1" ) 
	
EndIf		
	
Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010ViOp³ Autor ³ Sergio Silveira       ³ Data ³07/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a visualizacao de uma oportunidade                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010ViOp( )                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010ViOp()                   

DbSelectArea("AD1")
AD1->(DbSetOrder(1)) 
If AD1->(DbSeek(xFilial("AD1")+(cTmpTable)->AD1_NROPOR)) 
	FWExecView(Upper(STR0040),"VIEWDEF.FATA300",MODEL_OPERATION_VIEW,/*oDlg*/,/*bCloseOnOk*/,/*bOk*/,/*nPercReducao*/)  //"Visualizar"                    
EndIf 

Return( .T. )  

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Ftc010Stag³ Autor ³ Sergio Silveira       ³ Data ³06/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna dados de uma etapa para grafico ou alimenta        ³±±
±±³          ³ arquivo de trabalho das oportunidades                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Sintaxe   ³ Ftc010Stag( ExpC1, @ExpN1, @ExpN2, @ExpN3, @ExpN4, @ExpN5, ³±± 
±±³          ³    @ExpA1, @ExpL1, @ExpA2, @ExpN6, ExpN7, ExpA3 )          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Representante                                     ³±± 
±±³          ³ ExpN1 -> Quantidade                                        ³±±
±±³          ³ ExpN2 -> Valor                                             ³±±
±±³          ³ ExpN3 -> Total valor                                       ³±±
±±³          ³ ExpN4 -> Total quantidade                                  ³±±
±±³          ³ ExpN5 -> Total media de valor                              ³±±
±±³          ³ ExpA1 -> Array de dados do grafico                         ³±±
±±³          ³ ExpL1 -> Controle de primeira chamada                      ³±±
±±³          ³ ExpA2 -> Array contendo code-blocks de filtro              ³±±
±±³          ³ ExpN6 -> Acumulado de relevancia                           ³±±
±±³          ³ ExpN7 -> Modelo : 1 - Dados do grafico                     ³±±
±±³          ³                   2 - Alimenta arquivo de oportunidades    ³±±
±±³          ³ ExpA3 -> Estrutura do Array de trabalho                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Ftc010Stage( cRepres, nQuant, nValor, nTotValor, nTotQuant, nTotMedia,;
		 aDadosGRF, lFirst, aFilBlocks, nRelAcumul, nModelo, aStruQuery )
  
Local bBlockMais	:= aFilBlocks[ 1 ] 
Local bBlockMenos	:= aFilBlocks[ 2 ] 
Local bFilGrupo		:= aFilBlocks[ 3 ] 
Local bWhileAD1		:= aFilBlocks[ 4 ] 
Local bFiltroAD1	:= aFilBlocks[ 5 ] 
Local bFilUsAD1		:= aFilBlocks[ 6 ] 
Local bFilUsSA3		:= aFilBlocks[ 7 ] 
Local bFilUsSB1		:= aFilBlocks[ 8 ] 
     
Local cAliasQry	:= GetNextAlias()                                             
Local cQuery		:= ""
Local cFilGrupo	:= ""
Local cSeekSD1	:= "" 

Local lInferiores  := ( MV_PAR23 == 1 )  
Local lInclui      := .F.

Local nLoop        := 0  
Local nCntFor      := 0
Local nRecnoAD1	   := 0

DEFAULT aDadosGRF  := {}

DEFAULT nQuant     := 0 
DEFAULT nValor     := 0 
DEFAULT nTotValor  := 0 
DEFAULT nTotQuant  := 0 
DEFAULT nTotMedia  := 0                                      
DEFAULT nRelAcumul := 0 

nModelo            := If( ValType( nModelo ) == "N", nModelo, 1 ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao dos blocos de filtro - efetuada apenas uma vez               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lFirst 

	If MV_PAR09 == 1 
		bBlockMais  := { || .T. } 
	Else
		If MV_PAR09 <> 5      
			bBlockMais := { || AD1->AD1_STATUS == Str( MV_PAR09 - 1, 1 ) }  
		Else 		
			bBlockMais := { || AD1->AD1_STATUS == "9" }                     
		EndIf                                         
	EndIf 
	
	If MV_PAR10 == 1 
		bBlockMenos := { || .F. } 
	Else
		If MV_PAR10 <> 5      
			bBlockMenos := { || AD1->AD1_STATUS == Str( MV_PAR10 - 1, 1 ) }  
		Else 		
			bBlockMenos := { || AD1->AD1_STATUS == "9" }  
		EndIf                                         
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a chamada para definicao dos filtros de usuario  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "FT320FIL" ) 
		If ValType( aFiltro := ExecBlock( "FT320FIL", .F., .F. ) ) == "A"
			bFilUsAD1 := aFiltro[ 1 ]
			bFilUsSA3 := aFiltro[ 2 ]
			bFilUsSB1 := aFiltro[ 3 ]					
		EndIf 
	EndIf 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a montagem do filtro de representantes considerando a estrutura    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	bFilGrupo := { || .T. }     
	           
	If !Empty( cRepres )            
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Adiciona o proprio representante                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cFilGrupo := "{ || SA3->A3_COD=='" + cRepres + "'"
		                                       
		If lInferiores
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona os grupos que estao abaixo deste representante                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty( aGrupos := FtReprEst( cRepres ) )
				For nLoop := 1 to Len( aGrupos ) 
					cFilGrupo += ".Or.SA3->A3_GRPREP=='" + aGrupos[ nLoop, 1 ] + "'" 
				Next nLoop  
			EndIf	
		EndIf 		
		
		cFilGrupo += " } "
		bFilGrupo := &cFilGrupo  
	
	EndIf                           
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza o array de code-blocks                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aFilBlocks[ 1 ] := bBlockMais   
	aFilBlocks[ 2 ] := bBlockMenos 
	aFilBlocks[ 3 ] := bFilGrupo  
	aFilBlocks[ 4 ] := bWhileAD1 
	aFilBlocks[ 5 ] := bFiltroAD1
	aFilBlocks[ 6 ] := bFilUsAD1 
	aFilBlocks[ 7 ] := bFilUsSA3 
	aFilBlocks[ 8 ] := bFilUsSB1 
	
	lFirst := .F.

EndIf 
	
cQuery := "SELECT R_E_C_N_O_ AD1RECNO FROM " + RetSqlName( "AD1" ) + " "
cQuery += "WHERE "
cQuery += "AD1_DTINI>='"  + DToS(MV_PAR03)+"' AND AD1_DTFIM<='"+DToS(MV_PAR04)+"' AND "
cQuery += "AD1_VEND>='"   + MV_PAR07 + "' AND AD1_VEND<='"   + MV_PAR08 + "' AND "
cQuery += "AD1_CODPRO>='" + MV_PAR11 + "' AND AD1_CODPRO<='" + MV_PAR12 + "' AND "			
cQuery += "AD1_PROSPE>='" + MV_PAR19 + "' AND AD1_PROSPE<='" + MV_PAR20 + "' AND "			
cQuery += "AD1_PROVEN='"  + AC2->AC2_PROVEN + "' AND "			
cQuery += "AD1_STAGE='"   + AC2->AC2_STAGE  + "' AND "						
cQuery += "D_E_L_E_T_=' '"

dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasQry, .F., .T. ) 			

TcSetField( cAliasQry, "AD1RECNO", "N", 8, 0 ) 
  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percorre as oportunidades                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

While ( cAliasQry )->( !Eof() )  

	nRecnoAD1 := AD1RECNO 
	AD1->( dbGoto( nRecnoAD1 ) )
            
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica as oportunidades considerando os parametros                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If  Eval( bBlockMais ) .And. !Eval( bBlockMenos ) .And. Eval( bFilUsAD1 ) 

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica os representantes considerando os parametros                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SA3->( dbSetOrder( 1 ) ) 
		If SA3->( MsSeek( xFilial( "SA3" ) + AD1->AD1_VEND ) ) 

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua o filtro baseado na estrutura de vendas vigente                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Eval( bFilGrupo ) .And. Eval( bFilUsSA3 ) .And. ( SA3->A3_REGIAO >= MV_PAR17 .And. SA3->A3_REGIAO <= MV_PAR18 ) 
                                 
				lInclui := .F. 
				If Empty( AD1->AD1_CODPRO ) 	
					lInclui := .T.
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica as produtos considerando os parametros                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SB1->( dbSetOrder( 1 ) )     
					If SB1->( dbSeek( xFilial( "SB1" ) + AD1->AD1_CODPRO ) )
					    If SB1->B1_GRUPO >= MV_PAR15 .And. SB1->B1_GRUPO <= MV_PAR16 .And.;
								SB1->B1_TIPO >= MV_PAR13 .And. SB1->B1_TIPO <= MV_PAR14 .And. Eval( bFilUsSB1 ) 
							lInclui := .T. 
						EndIf 						
					EndIf 
				EndIf	
				
				If lInclui 
				
					If nModelo == 1 
						nQuant ++  
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Converte para a moeda de analise ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						nValor += xMoeda( AD1->AD1_VERBA, AD1->AD1_MOEDA, MV_PAR25, dDataBase )
					Else
						RecLock(cTmpTable,.T.)
						For nCntFor := 1 To Len(aStruQuery)
							If AllTrim( aStruQuery[nCntFor,1] ) <> "AD1_FLAG"
								FieldPut(nCntFor,AD1->(FieldGet(FieldPos(aStruQuery[nCntFor][1]))))
							EndIf 								
						Next nCntFor
						MsUnLock()
					EndIf 	
						
				EndIf
				
			EndIf
		EndIf					
		 	
	EndIf 	
	
	(cAliasQry)->( DBSkip() )

EndDo 	

(cAliasQry)->( DBCloseArea() )
      
If nModelo == 1                                                                        
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aplica o fator de Contribuicao                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MV_PAR24 == 2 
		nQuant *= nRelAcumul 
		nValor *= nRelAcumul 
	EndIf 					
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Alimenta os totalizadores                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTotValor += nValor
	nTotQuant += nQuant
	nTotMedia += ( nValor / nQuant ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Alimenta o array de colunas do grafico                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AAdd( aDadosGRF, { nQuant, nValor, AC2->AC2_STAGE, AC2->AC2_DESCRI } )
	
EndIf

Return( .T. ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados          ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
     
Private aRotina := {	{ OemToAnsi(STR0002) ,"AxPesqui"		,0,1,0,.F.},;		//"Pesquisar"	//"Pesquisar"
							{ OemToAnsi(STR0003 ),"Ftc010Pipe"	,0,2,0,NIL} }		//"Visual"		//"Pipeline"

If ExistBlock("FC010MNU")
	ExecBlock("FC010MNU",.F.,.F.)
EndIf

Return(aRotina)
