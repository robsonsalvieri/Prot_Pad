#INCLUDE "PROTHEUS.CH"
#INCLUDE "TECA300.CH"
#INCLUDE "TBICONN.CH"


#DEFINE CADASTRO STR0007	//"Manuten‡„o Chamado T‚cnico"
#DEFINE  NUMITENS 300

Static aCtrRodape := {} 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ TECA300  ³ Autor ³         Eduardo Riera ³ Data ³ 21.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Manutencao no Chamado Tecnico                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpX1 := TECA300( ExpC1, ExpA1 )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Rotina a ser chamada / ExpA1 -> Dados telemarket. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpX1 -> .T. ou .F. ou array retorno p/ telemarketig       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aline C.Vale  ³01/06/99³Prothe³Ver chamada para controle de botoes     ³±±
±±³Andrea Farias ³20/06/05³811   ³BOPS 83192 - Alterada expressao logica  ³±± 
±±³              ³        ³      ³do 9. parametro da Mbrowse.             ³±± 
±±³Cleber M.     ³12/04/06³96040 ³Inclusao de legenda na rotina.          ³±± 
±±³Conrado Q.    ³26/04/07³124187³Inclusão do ponto de entrada AT300Fil   ³±± 
±±³              ³        ³      ³para filtrar os registros exibidos.     ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA300( cRotina, aDadosTMK, xAutoCab, xAutoItens, nOpcAuto )

Local xRet			:= .T.			// Retorno da função
Local cAt300Fil		:= ""			// Filtro fornecido pelo ponto de entrada
Local bFiltraBrw	:= {||}			// Bloco de código para execução do filtro
Local aIndexAB1		:= {}			// Indice do AB1
Local cFiltraAB1	:= ""			// Filtro utilizado no bloco de código executado para ativar o filtro
Local aCores     := {}
Local aCoresNew  := {}

Private aRotina 	:= MenuDef()	// Itens do Menu
Private cCadastro	:= CADASTRO		// Título do cadastro
Private cRoda  		:= ""			// Rotina a ser executada quando é função é chamada por outra fonte
Private aDados		:= {}			// Clone do aDadosTMK
Private aAutoCab	:= {}			// Cabeçalho da rotina automático
Private aAutoItens	:= {}			// Itens da rotina automática

aDados := AClone( aDadosTMK ) 

aCores := {{"AB1_STATUS=='A'",'ENABLE' },{"AB1_STATUS=='E'",'DISABLE' }}

If	xAutoCab <> Nil .AND. xAutoItens <> NIL
	Private lAt300Auto := .T.

	aAutoCab  := xAutoCab
	aAutoItens:= xAutoItens

	aEval(aAutoItens,{|x| aEval(x,{|y,z| If( y[1] == AllTrim("AB2_ITEM"), y[3] := ".T.", )})})

	MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"AB1")
Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a existencia de Filtros na mBrowse                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB1")
	AB1->(DbSetOrder(1)) //AB1_FILIAL+AB1_NRCHAM
	If (ExistBlock("AT300FIL"))
		cAt300Fil := ExecBlock("AT300FIL",.F.,.F.)
		If ( ValType(cAt300Fil) == "C" ) .AND. !Empty(cAt300Fil)
			cFiltraAB1 := cAt300Fil
		EndIf
		bFiltraBrw 	:= {|| FilBrowse("AB1",@aIndexAB1,@cFiltraAB1) }
		Eval(bFiltraBrw)
	EndIf
   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock("AT30COR")
		aCoresNew := ExecBlock("AT30COR",.F.,.F.,aCores)
		If ValType( aCoresNew ) == "A"
			aCores := aClone(aCoresNew)
		EndIf
	Endif

   
    If ValType( cRotina ) == "C"
   		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz tratamento para chamada por outra rotina             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty( nScan := AScan( aRotina, { |x| x[2] == cRotina } ) ) 
			cRoda := cRotina + "( 'AB1', AB1->( Recno() ), " + Str(nScan,2) + If(ValType(aDados)=="A",",,aDados)",")") 
			xRet  := Eval( { || &( cRoda ) } ) 
		EndIf 
	Else    
		mBrowse( 6, 1,22,75,"AB1",,,,,,aCores)
	EndIf 
EndIf
DbSelectArea("AB1")
DbSetOrder(1)

Return( xRet ) 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MenuDef   ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 08.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := { 	{ STR0001	,"AxPesqui" 	,0	,1	,0	,.F.	}	,;	//"Pesquisar"
						{ STR0002	,"At300Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
						{ STR0003	,"At300Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
						{ STR0004	,"At300Alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
						{ STR0005	,"At300Exclu"	,0	,5	,0	,.T.	}	,;	//"Excluir"
						{ STR0006	,"At300Efet" 	,0	,4	,0	,.T.	}	,;	//"eFetivar"
						{ STR0054	,"At300Leg" 	,0	,2	,0	,.T.	}	}	//"Legenda" 
Return(aRotina)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³At300Inclu³ Autor ³Eduardo Riera          ³ Data ³ 21.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Inclusao dos Chamados Tecnicos                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At300Inclu(ExpC1,ExpN1,ExpN2)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Concluiu a operacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115746: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300Inclu(cAlias,nReg,nOpc,aX,aDadosTMK)

Local aRet      := { .F., "" }		// Retorno da função de gravação    
Local cConteudo := ""				// Conteúdo do campo de memória
Local oDlg		:= Nil				// Objeto do diálogo exibido
Local oSay		:= Nil				// Objeto do say exibido
Local oGetD		:= Nil				// Objeto do getdados exibido
Local lTmk		:= ( ValType( aDadosTMK ) == "A" )	// Se houve campos passados pelo TMK
Local lRetorno	:= .F.				// Se o usuário confirmou a inclusão e a gravação ocorreu com sucesso
Local nOpcA		:= 0				// Se 1 o usuário pressionou OK se 0 o usuário pressionou cancelar
Local nScan		:= 0 				// Utilizado para localizar a posição de um determinado campo
Local aObjects	:= {}				// Array com os objetos exibidos
Local aPosObj	:= {} 				// Array com as posições dos objetos exibidos
Local aSizeAut	:= {}				// Array com os tamanhos automáticos
Local aPosGet	:= {}				// Posição do objeto Get
Local nSaveSX8	:= GetSX8Len()		// Número gerado para identificação do registro

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private aTela	:= {}				// Array da tela
Private aGets	:= {}				// Array dos gets
Private aHeader := {}				// Cabeçalho do GetDados
Private aCols   := {}				// Itens do GetDados
Private INCLUI  := .T.  			// Informa que é inclusão
Private oGetDUS	:= Nil				// Backup do objeto oGetD

If ExistBlock("AT300INC")
	Execblock("AT300INC",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Variaveis de Memoria da Enchoice                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB1")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz tratamento para inicializar campos passados pelo TMK ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lTmk 
			If !Empty( nScan := AScan( aDadosTMK, { |x| AllTrim( x[1] ) == AllTrim( cField ) } ) )  	
				cConteudo := aDadosTMK[ nScan, 2 ] 
			Else
				cConteudo := CriaVar( cField ) 
			EndIf            	
		Else	 
			If cField == "AB1_NRCHAM"
				cConteudo :=GetNumAB1()
			Else
				cConteudo := CriaVar( cField )
			EndIf 
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz a declaracao da variavel como private (nao remover)  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ   
		SetPrvt( Trim(cField) )
		M->&(cField) := cConteudo
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AB2"		,1				,/*cSeek*/		,;
					/*{||cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
Endif

aCols[1][GdFieldPos("AB2_ITEM")] := "01"

If Type("lAt300Auto")=="U" .OR. !lAt300Auto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta tela de entrada                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Carrega definicoes de objetos para calculo automatico                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSizeAut := MsAdvSize() 
	aObjects := {} 
	aAdd( aObjects, { 315,  70, .T., .T. } )
	aAdd( aObjects, { 100, 100, .T., .T. } )
	aAdd( aObjects, { 100,  14, .T., .F. } )
	
	aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta tela de entrada                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME 
	
	oEnch := MsMGet():New( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
	
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At300LinOk","At300TudOk","+AB2_ITEM",.T.,,,,NUMITENS)
	oGetd:oBrowse:bChange := {|| At300Rodape(),.T.}
	
	nLinSay  := aPosObj[ 3,1 ] 
	nColSay  := aPosObj[ 3,2 ] 
	
	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
				{{005,060,075,130,145,200,230,285}} )
   
	aCtrRodape := {} 

	@ nLinSay ,aPosGet[1,1] SAY STR0008			SIZE 055,007 PIXEL	//"Contrato: "
	@ nLinSay ,aPosGet[1,2] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-CONT", oSay } ) 	
	@ nLinSay ,aPosGet[1,3] SAY STR0009			SIZE 055,007 PIXEL	//"Garantia: "
	@ nLinSay ,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-GARA", oSay } ) 		
	@ nLinSay ,aPosGet[1,5] SAY STR0010			SIZE 055,007 PIXEL	//"Preventiva:"
	@ nLinSay ,aPosGet[1,6] SAY oSay PROMPT ""	 			SIZE 030,007 PIXEL
	aAdd( aCtrRodape, { "R-PREV", oSay } ) 			
	@ nLinSay  + 8,aPosGet[1,1] SAY STR0011			SIZE 055,007 PIXEL	//"Chamado T‚cnico:"
	@ nLinSay  + 8,aPosGet[1,2] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-CHAM", oSay } ) 				
	@ nLinSay  + 8,aPosGet[1,3] SAY STR0012			SIZE 055,007 PIXEL	//"Or‡amento:"
	@ nLinSay  + 8,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-ORCA", oSay } ) 					
	@ nLinSay  + 8,aPosGet[1,5] SAY STR0013			SIZE 055,007 PIXEL	//"Ordem Servi‡o:"
	@ nLinSay  + 8,aPosGet[1,6] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-OSRV", oSay } ) 						
	@ nLinSay ,aPosGet[1,7] SAY STR0014			SIZE 055,007 PIXEL	//"Obsolescˆncia:"
	@ nLinSay ,aPosGet[1,8] SAY oSay PROMPT "" 				SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-OBSO", oSay } ) 							
	@ nLinSay  + 8,aPosGet[1,7] SAY STR0015			SIZE 055,007 PIXEL	//"              "
	@ nLinSay  + 8,aPosGet[1,8] SAY oSay PROMPT ""				SIZE 015,007 PIXEL
	aAdd( aCtrRodape, { "R-MENS", oSay } ) 							
	                
	oGetDUS := oGetD 
	
	ACTIVATE MSDIALOG oDlg ;
	  ON INIT At300Bar(oDlg,;
	  {||nOpcA:=1,If(Obrigatorio(aGets,aTela).AND.oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},;
	  {||nOpcA:=0,If(At300VldUs( nOpca ),oDlg:End(),)}, nOpc, oGetD, @oEnch ) VALID At300VldUs( nOpca )
Else
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
		MsGetDAuto(aAutoItens,"At300LinOk",{|| At300TudOk() .AND. At300VldUs(1)},aAutoCab,aRotina[nOpc][4])
		nOpcA := 1
	EndIf
EndIf

If ( nOpcA == 1 )
	Begin Transaction
		aRet := At300Grava(1)  
		If ( aRet[1] )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			End
			lRetorno := .T.
		Else			
			While ( GetSX8Len() > nSaveSx8 )
				RollBackSx8()
			End
		EndIf
	End Transaction
Else	
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	End
EndIf

DbSelectArea( cAlias )
Return( { lRetorno, aRet[2] } ) 


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³At300Visua³ Autor ³Eduardo Riera          ³ Data ³ 23.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Visualizacao dos Chamados Tecnicos               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At300Visua(ExpC1,ExpN1,ExpN2)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115746: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At300Visua(cAlias,nReg,nOpc)

Local oDlg		:= Nil				// Objeto do diálogo exibido
Local oSay		:= Nil				// Objeto do say exibido
Local oGetD		:= Nil				// Objeto do getdados exibido
Local nUsado	:= 0				// Quantidade de campos usados no aHeader
Local nCntFor	:= 0				// Contador
Local aArea		:= GetArea()		// Salva a area atual
Local aObjects	:= {} 				// Array com os objetos exibidos
Local aPosObj	:= {} 				// Array com as posições dos objetos exibidos
Local aSizeAut	:= {}				// Array com os tamanhos automáticos
Local aPosGet	:= {}				// Posição do objeto Get
Local cSeek		:= ""				// Seek para montagem da aCols
Local cWhile	:= ""				// While para montagem da aHeader

Private aTela	:= {}				// Array da tela
Private aGets	:= {}				// Array dos gets
Private aHeader := {}				// Cabeçalho do GetDados
Private aCols   := {} 				// Itens do GetDados
Private INCLUI  := .F.				// Informa que não é inclusão

If ExistBlock("AT300VIS")
	Execblock("AT300VIS",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Variaveis de Memoria da Enchoice                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ     
RegToMemory( "AB1", .F. ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa as variaveis Virtuais do Programa                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
M->AB1_NOMCLI := SA1->A1_NOME

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek	:= xFilial("AB2")+M->AB1_NRCHAM
cWhile	:= "AB2->AB2_FILIAL+AB2->AB2_NRCHAM"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AB2"		,1				,cSeek			,;
					{|| &cWhile }	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		)
Endif
                        
aSizeAut := MsAdvSize() 
aObjects := {} 
aAdd( aObjects, { 315,  70, .T., .T. } )
aAdd( aObjects, { 100, 100, .T., .T. } )
aAdd( aObjects, { 100,  14, .T., .F. } )

aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 

aPosObj := MsObjSize( aInfo, aObjects ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta tela de entrada                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME 

EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )

M->AB1_NOMCLI := SA1->A1_NOME

oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At300LinOk","At300TudOk","+AB2_ITEM",.F.)
oGetd:oBrowse:bChange := {|| At300Rodape(),.T.}

nLinSay  := aPosObj[ 3,1 ] 
nColSay  := aPosObj[ 3,2 ] 

aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
			{{005,060,075,130,145,200,230,285}} )

aCtrRodape := {} 

@ nLinSay ,aPosGet[1,1] SAY STR0008			SIZE 055,007 PIXEL	//"Contrato: "
@ nLinSay ,aPosGet[1,2] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-CONT", oSay } ) 	
@ nLinSay ,aPosGet[1,3] SAY STR0009			SIZE 055,007 PIXEL	//"Garantia: "
@ nLinSay ,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-GARA", oSay } ) 		
@ nLinSay ,aPosGet[1,5] SAY STR0010			SIZE 055,007 PIXEL	//"Preventiva:"
@ nLinSay ,aPosGet[1,6] SAY oSay PROMPT ""	 			SIZE 030,007 PIXEL
aAdd( aCtrRodape, { "R-PREV", oSay } ) 			
@ nLinSay  + 8,aPosGet[1,1] SAY STR0011			SIZE 055,007 PIXEL	//"Chamado T‚cnico:"
@ nLinSay  + 8,aPosGet[1,2] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-CHAM", oSay } ) 				
@ nLinSay  + 8,aPosGet[1,3] SAY STR0012			SIZE 055,007 PIXEL	//"Or‡amento:"
@ nLinSay  + 8,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-ORCA", oSay } ) 					
@ nLinSay  + 8,aPosGet[1,5] SAY STR0013			SIZE 055,007 PIXEL	//"Ordem Servi‡o:"
@ nLinSay  + 8,aPosGet[1,6] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-OSRV", oSay } ) 						
@ nLinSay ,aPosGet[1,7] SAY STR0014			SIZE 055,007 PIXEL	//"Obsolescˆncia:"
@ nLinSay ,aPosGet[1,8] SAY oSay PROMPT "" 				SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-OBSO", oSay } ) 							
@ nLinSay  + 8,aPosGet[1,7] SAY STR0015			SIZE 055,007 PIXEL	//"              "
@ nLinSay  + 8,aPosGet[1,8] SAY oSay PROMPT ""				SIZE 015,007 PIXEL
aAdd( aCtrRodape, { "R-MENS", oSay } ) 							

At300Rodape(aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_CODPRO"})],aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_NUMSER"})])
ACTIVATE MSDIALOG oDlg ;
  ON INIT At300Bar(oDlg,{||oDlg:End()},{||oDlg:End()},nOpc)

DbSelectArea( aArea[1] )
DbSetOrder(aArea[2])
DbGoTo(aArea[3])
Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³At300Alter³ Autor ³Eduardo Riera          ³ Data ³ 23.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Alteracao dos Chamados Tecnicos                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At300Alter(ExpC1,ExpN1,ExpN2)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Concluiu a operacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115746: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±³17/07/2007³ Conrado Q.    ³Bops 128851: Analisa o retorno do P.E.      ³±±
±±³          ³               ³AT300Alt.                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At300Alter(cAlias,nReg,nOpc)
                                
Local aRet			:= {}			// Retorno da função de gravação
Local oDlg			:= Nil			// Objeto do diálogo exibido
Local oSay			:= Nil			// Objeto do say exibido
Local oGetD			:= Nil			// Objeto do getdados exibido
Local nOpcA			:= 0			// Se 1 o usuário pressionou OK se 0 o usuário pressionou cancelar
Local lRetorno		:= .F.			// Se o usuário confirmou a alteração e a gravação ocorreu com sucesso
Local lRollBackSX8	:= .F. 			// Se é para devolver o número gerado para identificação do registro
Local aObjects		:= {} 			// Array com os objetos exibidos
Local aPosObj		:= {} 			// Array com as posições dos objetos exibidos
Local aSizeAut		:= {}			// Array com os tamanhos automáticos
Local aPosGet		:= {}			// Posição do objeto Get
Local nSaveSX8		:= GetSX8Len()	// Número gerado para identificação do registro
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aTravas		:= {}			// Campos que foram travados
Local lTravas		:= .T.			// Se todos os campos foram travados com sucesso
Local lAltera		:= .T.			// Se poderá ocorrer a alteração ou não.
Local lRet			:= .T.			// Retorno do P.E. AT300ALT

Private aTela	:= {}				// Array da tela
Private aGets	:= {}				// Array dos gets
Private aHeader := {}				// Cabeçalho do GetDados
Private aCols   := {}				// Itens do GetDados
Private INCLUI  := .F.				// Informa que não é inclusão
Private oGetDUS := Nil				// Backup do objeto oGetD

If ExistBlock("AT300ALT")
	lRet := Execblock("AT300ALT",.F.,.F.)
	If ValType( lRet ) == "L"
		lAltera := lRet
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava os registros necessarios                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !AtTravaReg("AB1", aTravas)
	lTravas := .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Variaveis de Memoria da Enchoice                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RegToMemory( "AB1", .F. ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa as variaveis Virtuais do Programa                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
M->AB1_NOMCLI := SA1->A1_NOME

If Len(aHeader) == 0 .AND. Len(aCols) == 0 .AND. lTravas
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Montagem aHeader, aCols³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSeek	:= xFilial("AB2")+M->AB1_NRCHAM
	cWhile	:= "AB2->AB2_FILIAL+AB2->AB2_NRCHAM"
	
	If !FillGetDados(	nOpc			,"AB2"		,1				,cSeek			,;
						{|| &cWhile }	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
						/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AB2", aTravas) .AND. At300Verif(nOpc) }	)
		lTravas := .F.
	Endif
Endif

If ( lTravas .AND. lAltera )
	cItem := SOMA1(aCols[Len(aCols)][GdFieldPos("AB2_ITEM")])
	DbSelectArea("AB2")
	DbSetOrder(1)
	DbSeek(xFilial("AB2") + M->AB1_NRCHAM + cItem)

	If Empty(aCols[1][GdFieldPos("AB2_ITEM")])
		aCols[1][GdFieldPos("AB2_ITEM")] := "01"
	Endif

	If Type("lAt300Auto")=="U" .OR. !lAt300Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		                 
		aSizeAut := MsAdvSize() 
		aObjects := {} 
		aAdd( aObjects, { 315,  70, .T., .T. } )
		aAdd( aObjects, { 100, 100, .T., .T. } )
		aAdd( aObjects, { 100,  14, .T., .F. } )
		
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME 
		
		EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
		
		M->AB1_NOMCLI := SA1->A1_NOME
		
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At300LinOk","At300TudOk","+AB2_ITEM",.T.,,,,NUMITENS,"At300FldOk",/*cSuperDel*/,/*uPar*/,"At300DelOk")
		oGetd:oBrowse:bChange := {|| At300Rodape(,,oGetd),.T.}
		
		nLinSay  := aPosObj[ 3,1 ] 
		nColSay  := aPosObj[ 3,2 ] 
		                      
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
				{{005,060,075,130,145,200,230,285}} )

		aCtrRodape := {} 
	
		@ nLinSay ,aPosGet[1,1] SAY STR0008			SIZE 055,007 PIXEL	//"Contrato: "
		@ nLinSay ,aPosGet[1,2] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-CONT", oSay } ) 	
		@ nLinSay ,aPosGet[1,3] SAY STR0009			SIZE 055,007 PIXEL	//"Garantia: "
		@ nLinSay ,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-GARA", oSay } ) 		
		@ nLinSay ,aPosGet[1,5] SAY STR0010			SIZE 055,007 PIXEL	//"Preventiva:"
		@ nLinSay ,aPosGet[1,6] SAY oSay PROMPT ""	 			SIZE 030,007 PIXEL
		aAdd( aCtrRodape, { "R-PREV", oSay } ) 			
		@ nLinSay  + 8,aPosGet[1,1] SAY STR0011			SIZE 055,007 PIXEL	//"Chamado T‚cnico:"
		@ nLinSay  + 8,aPosGet[1,2] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-CHAM", oSay } ) 				
		@ nLinSay  + 8,aPosGet[1,3] SAY STR0012			SIZE 055,007 PIXEL	//"Or‡amento:"
		@ nLinSay  + 8,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-ORCA", oSay } ) 					
		@ nLinSay  + 8,aPosGet[1,5] SAY STR0013			SIZE 055,007 PIXEL	//"Ordem Servi‡o:"
		@ nLinSay  + 8,aPosGet[1,6] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-OSRV", oSay } ) 						
		@ nLinSay ,aPosGet[1,7] SAY STR0014			SIZE 055,007 PIXEL	//"Obsolescˆncia:"
		@ nLinSay ,aPosGet[1,8] SAY oSay PROMPT "" 				SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-OBSO", oSay } ) 							
		@ nLinSay  + 8,aPosGet[1,7] SAY STR0015			SIZE 055,007 PIXEL	//"              "
		@ nLinSay  + 8,aPosGet[1,8] SAY oSay PROMPT ""				SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-MENS", oSay } ) 							
			
		At300Rodape(aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_CODPRO"})],aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_NUMSER"})],oGetd)
		
		oGetDUS := oGetD
		
		ACTIVATE MSDIALOG oDlg ;
		   ON INIT At300Bar(oDlg,;
			{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},;
			{||nOpcA:=0,If(At300VldUs( nOpca ),oDlg:End(),)}, nOpc ) VALID At300VldUs( nOpca )
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
			MsGetDAuto(aAutoItens,"At300LinOk",{|| At300TudOk() .AND. At300VldUs(1)},aAutoCab,aRotina[nOpc][4])
			nOpcA := 1
		EndIf
	EndIf
	
	If ( nOpcA == 1 )
		Begin Transaction
			aRet := aT300Grava(2)
			If ( aRet[1] )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSx8()
				End
				lRetorno := .T. 
			Else
				lRollBackSX8 := .T. 					
			EndIf
		End Transaction
	Else 			
		lRollBackSX8 := .T. 	
	EndIf
Else	
	lRollBackSX8 := .T. 		
EndIf 

If lRollBackSX8
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	End
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o destravamento dos registros travados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )

DbSelectArea( cAlias )
DbSetOrder(1)
Return( lRetorno ) 

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³At300Exclu³ Autor ³Eduardo Riera          ³ Data ³ 23.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Exclusao dos Chamados Tecnicos                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At300Exclu(ExpC1,ExpN1,ExpN2)                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Concluiu a operacao                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³27/12/2006³ Conrado Q.    ³Bops 115746: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±³17/07/2007³ Conrado Q.    ³Bops 128851: Analisa o retorno do P.E.      ³±±
±±³          ³               ³AT300Exc.                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At300Exclu(cAlias,nReg,nOpc)

Local oDlg		:= Nil			// Objeto do diálogo exibido
Local oSay		:= Nil			// Objeto do say exibido
Local oGetD		:= Nil			// Objeto do getdados exibido
Local nOpcA		:= 0			// Se 1 o usuário pressionou OK se 0 o usuário pressionou cancelar
Local aObjects	:= {} 			// Array com os objetos exibidos
Local aPosObj	:= {} 			// Array com as posições dos objetos exibidos
Local aSizeAut	:= {}			// Array com os tamanhos automáticos
Local aPosGet	:= {}			// Posição do objeto Get
Local lRetorno	:= .F.			// Se o usuário confirmou a alteração e a gravação ocorreu com sucesso
Local nSaveSX8	:= GetSX8Len()	// Número gerado para identificação do registro
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader
Local aTravas	:= {}			// Campos que foram travados
Local lTravas	:= .T.			// Se todos os campos foram travados com sucesso
Local lExclui	:= .T.			// Se poderá ocorrer a exclusão ou não
Local lRet		:= .T.			// Retorno do P.E. AT300EXC.

Private aTela	:= {}			// Array da tela
Private aGets	:= {}			// Array dos gets
Private aHeader := {}			// Cabeçalho do GetDados
Private aCols   := {}			// Itens do GetDados
Private INCLUI  := .F.			// Informa que não é inclusão

If ExistBlock("AT300EXC")
	lRet := Execblock("AT300EXC",.F.,.F.)
	If ValType( lRet ) == "L"
		lExclui := lRet
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava os registros necessarios                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !AtTravaReg( "AB1", aTravas )
	lTravas := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Variaveis de Memoria da Enchoice                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RegToMemory( "AB1", .F. ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa as variaveis Virtuais do Programa                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
M->AB1_NOMCLI := SA1->A1_NOME

If Len(aHeader) == 0 .AND. Len(aCols) == 0 .AND. lTravas
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Montagem aHeader, aCols³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cSeek	:= xFilial("AB2")+M->AB1_NRCHAM
	cWhile	:= "AB2->AB2_FILIAL+AB2->AB2_NRCHAM"
	
	If !FillGetDados(	nOpc			,"AB2"		,1				,cSeek			,;
						{|| &cWhile }	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
						/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AB2", aTravas) .AND. At300Verif(nOpc) }	)
		lTravas := .F.
	Endif
Endif

If ( lTravas .AND. lExclui )
	If Type("lAt300Auto")=="U" .OR. !lAt300Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		                 
		aSizeAut := MsAdvSize() 
		aObjects := {} 
		aAdd( aObjects, { 315,  70, .T., .T. } )
		aAdd( aObjects, { 100, 100, .T., .T. } )
		aAdd( aObjects, { 100,  14, .T., .F. } )
		
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME 
		
		EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
		
		M->AB1_NOMCLI := SA1->A1_NOME
		
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At300LinOk","At300TudOk","+AB2_ITEM",.F.)
		oGetd:oBrowse:bChange := {|| At300Rodape(),.T.}
		
		nLinSay  := aPosObj[ 3,1 ] 
		nColSay  := aPosObj[ 3,2 ] 
	
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
				{{005,060,075,130,145,200,230,285}} )

		aCtrRodape := {} 
	
		@ nLinSay ,aPosGet[1,1] SAY STR0008			SIZE 055,007 PIXEL	//"Contrato: "
		@ nLinSay ,aPosGet[1,2] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-CONT", oSay } ) 	
		@ nLinSay ,aPosGet[1,3] SAY STR0009			SIZE 055,007 PIXEL	//"Garantia: "
		@ nLinSay ,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-GARA", oSay } ) 		
		@ nLinSay ,aPosGet[1,5] SAY STR0010			SIZE 055,007 PIXEL	//"Preventiva:"
		@ nLinSay ,aPosGet[1,6] SAY oSay PROMPT ""	 			SIZE 030,007 PIXEL
		aAdd( aCtrRodape, { "R-PREV", oSay } ) 			
		@ nLinSay  + 8,aPosGet[1,1] SAY STR0011			SIZE 055,007 PIXEL	//"Chamado T‚cnico:"
		@ nLinSay  + 8,aPosGet[1,2] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-CHAM", oSay } ) 				
		@ nLinSay  + 8,aPosGet[1,3] SAY STR0012			SIZE 055,007 PIXEL	//"Or‡amento:"
		@ nLinSay  + 8,aPosGet[1,4] SAY oSay PROMPT ""	 			SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-ORCA", oSay } ) 					
		@ nLinSay  + 8,aPosGet[1,5] SAY STR0013			SIZE 055,007 PIXEL	//"Ordem Servi‡o:"
		@ nLinSay  + 8,aPosGet[1,6] SAY oSay PROMPT ""		 		SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-OSRV", oSay } ) 						
		@ nLinSay ,aPosGet[1,7] SAY STR0014			SIZE 055,007 PIXEL	//"Obsolescˆncia:"
		@ nLinSay ,aPosGet[1,8] SAY oSay PROMPT "" 				SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-OBSO", oSay } ) 							
		@ nLinSay  + 8,aPosGet[1,7] SAY STR0015			SIZE 055,007 PIXEL	//"              "
		@ nLinSay  + 8,aPosGet[1,8] SAY oSay PROMPT ""				SIZE 015,007 PIXEL
		aAdd( aCtrRodape, { "R-MENS", oSay } ) 							
			
		At300Rodape(aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_CODPRO"})],aCols[1][aScan(aHeader,{|x|AllTrim(x[2])=="AB2_NUMSER"})])
		ACTIVATE MSDIALOG oDlg ;
		   ON INIT At300Bar(oDlg,;
			{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},;
			{||oDlg:End()},nOpc)
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
			MsGetDAuto(aAutoItens,"At300LinOk",{|| At300TudOk()},aAutoCab,aRotina[nOpc][4])
			nOpcA := 1
		EndIf
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
			aT300Grava(3)
			EvalTrigger()
		End Transaction
		While ( GetSX8Len() > nSaveSx8 )
			ConfirmSx8()
		End           
		lRetorno := .T. 
	Else
		While ( GetSX8Len() > nSaveSx8 )
			RollBackSx8()
		End
	EndIf
Else
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	End
EndIf 	
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o destravamento dos registros travados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )

DbSelectArea( cAlias )
DbSetOrder(1)
Return( lRetorno ) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Efet ³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetiva Chamados Tecnicos                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300Efet(cAlias,nReg,nOpc)

Local aSavRot 	:= aClone(aRotina)			// aRotina da janela anterior
Local cQuery  	:= ""						// Query
Local cArqInd 	:= CriaTrab(,.F.)			// Arquivo temporário de trabalho
Local cMarca	:= GetMark()				// Marcação
Local nIndex	:= 0						// Índice do AB1
Local lAT300Efe	:= ExistBlock("AT300Efe")	// Se existe ou não o P.E. AT300Efe
Local aAreaAB1	:= AB1->(GetArea())
Private cFiltroAB1 := ""

aRotina := { 	{ STR0016,"At300Visua",0,2},;	//"Visual   "
				{ STR0017,"At300GrvOr",0,4},;	//"Or‡amento"	
				{ STR0018,"At300GrvOs",0,4},;	//"ord.Servi‡o"
				{ STR0039,"At300GrvHd",0,4} }	//"Help Desk"
					
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³P.E. para alteração dos botões exibidos na efetivação da O.S.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAT300Efe
	ExecBlock("AT300Efe",.F.,.F.)
EndIf
					
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros                                                             ³
//³                                                                        ³
//³ MV_PAR01 - Cliente de                                                  ³
//³ MV_PAR02 - Cliente ate                                                 ³
//³ MV_PAR03 - Emissao de                                                  ³
//³ MV_PAR04 - Emissao ate                                                 ³
//³ MV_PAR05 - Chamado de                                                  ³
//³ MV_PAR06 - Chamado ate                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Pergunte("ATA300",.T.) )
	cQuery += "AB1_FILIAL=='"+xFilial("AB1")+"'.AND."
	cQuery += "AB1_STATUS=='A'.AND."
	cQuery += "AB1_CODCLI>='"+MV_PAR01+"'.AND."
	cQuery += "AB1_CODCLI<='"+MV_PAR02+"'.AND."
	cQuery += "Dtos(AB1_EMISSA)>='"+Dtos(MV_PAR03)+"'.AND."
	cQuery += "Dtos(AB1_EMISSA)<='"+Dtos(MV_PAR04)+"'.AND."
	cQuery += "AB1_NRCHAM>='"+MV_PAR05+"'.AND."
	cQuery += "AB1_NRCHAM<='"+MV_PAR06+"'"
	DbSelectArea("AB1")
	DbSetOrder(1)
	IndRegua("AB1",cArqInd,IndexKey(),,cQuery)
	nIndex := RetIndex("AB1")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	cFiltroAB1 := AB1->(DbFilter())
	DbGoTop()
	DbSetOrder(nIndex+1)
	If ( !Eof() .AND. !Bof() )
		MarkBrow("AB1","AB1_OK",,,,cMarca)
	Else
		Help(" ",1,"REGNOIS")
	EndIf
	DbSelectArea("AB1")
	RetIndex("AB1")
	dbClearFilter()
	Ferase(cArqInd+OrdBagExt())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna as Situacao de Entrada                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRotina := aClone(aSavRot)
AB1->(DbCloseArea())
DBSelectArea("AB1")
RestArea(aAreaAB1)
Return(.T.)


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At300Bar  ³ Autor ³ Eduardo Riera         ³ Data ³ 21.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Mostra a EnchoiceBar na tela                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cleber M.     ³27/10/05³87808 ³ Alterada a chamada da rotina de Base   ³±±
±±³              ³        ³      ³ Instalada 							  ³±±
±±³Conrado Q.    ³23/10/06³111294³Quando for visualizacao do system tracke³±± 
±±³              ³        ³      ³nao exibe o botao "System Tracker"      ³±± 
±±ºHanna C.      |30/03/07|9.12  ³Bops 118469 -Alterado o nome dos Bitmaps³±±
±±º        	     ³        |      ³definidos pela Engenharia p/ Protheus 10³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function At300Bar(	oDlg	,bOk	,bCancel	,nOpc,;
	   						oGetD	,oEnch )

Local lTECA300   	:= AtIsRotina("ATC010CON").OR.AtIsRotina("ATC020CON")		// Indica se pertence a rotina TECA300
Local aButtons   	:= { {"SUGESTAO" ,{|| At300bFaq()}, STR0029, STR0029} }	// "F.A.Q." 
Local aUsButtons 	:= {}														// Array de botoes de usuario
Local aAcesso	 	:= {.T., .T., .T.}											// Permissões de visualização dos botões
Local aAT300ACE		:= {}														// Permissões retornadas do ponto de entrada AT300ACE

If ExistBlock( "AT300ACE" )
	If ValType( aAT300ACE := ExecBlock( "AT300ACE", .F., .F. ) ) == "A"
		aAcesso := aAT300ACE
	EndIf
EndIf

If !lTeca300
	If aAcesso[1]
		aAdd(aButtons,{"PRODUT2"   ,{|| At300BPro()}, STR0026, STR0041})  //"Hist¢rico do Equipamento..."
	EndIf
	If aAcesso[2]
		aAdd(aButtons,{"POSCLI"    ,{|| At300BCli()}, STR0027, STR0042})  //"Hist¢rico do Cliente..."
	EndIf
	If aAcesso[3]
		aAdd(aButtons,{"NOTE"      ,{|| At300BInst()  }, STR0028, STR0043})  //"Cliente x Equipamento..."
	EndIf

	If nOpc == 3 	
		aAdd(aButtons,{"bmpincluir",{|| At300Carga( oGetD, @oEnch )}, STR0038, STR0044 }) // "Carrega equipamento"
	EndIf 	

	If nOpc == 2 .AND. !AtIsRotina("AT300TRACK") 	
		aAdd(aButtons,{"bmpord1",{|| At300Track()}, STR0040, STR0045 }) // "System Tracker"
	EndIf 	
	
	aAdd(aButtons,{"IMPRESSAO" ,{|| TECR300(M->AB1_NRCHAM)}, STR0030, STR0046}) //"Impress„o do Chamado Gravado"
	
EndIf
          
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistBlock( "AT300BUT" ) 
	If ValType( aUsButtons := ExecBlock( "AT300BUT", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| aAdd( aButtons, x ) } ) 	 	
	EndIf 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))


/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At300BInst³ Autor ³ Cleber Martinez       ³ Data ³ 27.10.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Chama a rotina de Base Instalada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL. 					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³	MOTIVO DA ALTERACAO					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Conrado Q.    ³14/07/07³10    ³ -BOPS 129711: Adicionado controle de   ³±±
±±³              ³        ³      ³acesso a base instalada através da      ³±± 
±±³              ³        ³      ³manutenção do chamado técnico.          ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function At300BInst()

Local nBack		:= 0	// Backup da variavel de indice n
Local aHeadBack	:= {}	// Variavel de backup do aHeader
Local aColsBack	:= {}	// Variavel de backup do aCols

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Acesso: "Acessa Base Instalada no Cad. Técnic".³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If VerSenha(157)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Guarda os valores originais ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nBack		:= n
	aHeadBack	:= AClone( aHeader )
	aColsBack	:= AClone( aCols )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a chamada da rotina de Base Instalada			³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	TECA040()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura os dados originais   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aHeader		:= AClone( aHeadBack )
	aCols		:= AClone( aColsBack )
	n			:= nBack
Else
	Help(" ",1,"AT300NOACE")
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300VlCli³ Autor ³ Eduardo Riera         ³ Data ³ 22.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao do Cliente                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At300VlCli()

Local lRetorno := .F.
Local aArea       := { Alias() , IndexOrd() , RecNo() }
Local cCampo      := ReadVar()
Local cSeekAA3    := ""
Local cQuery      := ""
Local cAliasQry   := ""
Local uConteudo   := &(ReadVar())
Local nCountAA3   := 0 
Local nRecnoAA3   := 0 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o atendimento foi incluido pelo telemarketing              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( M->AB1_NUMTMK ) 		
	Do Case
		Case ( "AB1_CODCLI"$cCampo )
			
				If ( SA1->A1_COD==uConteudo )
					uConteudo += SA1->A1_LOJA
				ElseIf !Empty(M->AB1_LOJA)
					uConteudo += M->AB1_LOJA
				EndIf
				DbSelectArea("SA1")
				DbSetOrder(1)
				If ( DbSeek(xFilial("SA1")+uConteudo) )
					If !Empty(M->AB1_LOJA)
						M->AB1_NOMCLI := SA1->A1_NOME
					EndIf
					lRetorno := .T.
					lRetorno := RegistroOk("SA1")
				Else
					Help(" ",1,"REGNOIS")
				EndIf
				
		Case ( "AB1_LOJA"$cCampo )
			DbSelectArea("SA1")
			DbSetOrder(1)
			If ( DbSeek(xFilial("SA1")+M->AB1_CODCLI+uConteudo) )
				M->AB1_NOMCLI := SA1->A1_NOME
				lRetorno := .T.
				lRetorno := RegistroOk("SA1")
			Else
				Help(" ",1,"REGNOIS")
			EndIf
	EndCase
Else 
	lRet := .F. 
	Help( " ", 1, "AT300CLITMK" ) 		
EndIf 
           
If lRetorno 
	If Type("lAt300Auto")=="U" .OR. !lAt300Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Rotina para preenchimento automatico da base instalada                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len( aCols ) == 1
		
			#IFDEF TOP
	
				cAliasQry := GetNextAlias() 		
				cQuery    := ""                            
				cQuery += "SELECT COUNT(*) TOTAL FROM " + RetSqlName( "AA3" ) + " " 
				cQuery += "WHERE " 
				cQuery += "AA3_FILIAL='" + xFilial( "AA3" ) + "' AND " 
				cQuery += "AA3_CODCLI='" + SA1->A1_COD      + "' AND " 
				cQuery += "AA3_LOJA='"   + SA1->A1_LOJA     + "' AND " 
				cQuery += "D_E_L_E_T_=' '" 
		
				cQuery := ChangeQuery( cQuery ) 
				
				dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )
				
				TcSetField( cAliasQry, "TOTAL", "N", 8, 0 ) 
				
				nCountAA3 := ( cAliasQry )->TOTAL
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o cliente possui um unico registro na base instalada       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If nCountAA3 == 1
					( cAliasQry )->( dbCloseArea() ) 
					
					cAliasQry := GetNextAlias() 		
					cQuery    := ""                            
					
					cQuery += "SELECT R_E_C_N_O_ AA3RECNO FROM " + RetSqlName( "AA3" ) + " " 
					cQuery += "WHERE " 
					cQuery += "AA3_FILIAL='" + xFilial( "AA3" ) + "' AND " 
					cQuery += "AA3_CODCLI='" + SA1->A1_COD      + "' AND " 
					cQuery += "AA3_LOJA='"   + SA1->A1_LOJA     + "' AND " 
					cQuery += "D_E_L_E_T_=' '" 
			
					cQuery := ChangeQuery( cQuery ) 
					
					dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )
					
					TcSetField( cAliasQry, "AA3RECNO", "N", 8, 0 ) 
					
					If !Eof() 
						nRecnoAA3 := ( cAliasQry )->AA3RECNO  
					EndIf   
					
				EndIf					
		
				( cAliasQry )->( dbCloseArea() ) 
				DbSelectArea( "AA3" ) 			
		
			#ELSE 
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se o cliente possui um unico registro na base instalada       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nCountAA3 := 0 
				cSeekAA3  := xFilial( "AA3" ) + SA1->A1_COD + SA1->A1_LOJA 
				AA3->( DbSetOrder( 1 ) ) 
				If AA3->( DbSeek( cSeekAA3 ) ) 
					nRecNoAA3 := AA3->( Recno() ) 
					While !AA3->( Eof() ) .AND. AA3->AA3_FILIAL + AA3->AA3_CODCLI + AA3->AA3_LOJA == cSeekAA3
						nCountAA3++
						If nCountAA3 > 1 
							Exit								
						EndIf 																					
						AA3->( dbSkip() ) 										
					End 	
				EndIf 	
				
		   #ENDIF 
																	
			If nCountAA3 == 1   
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Alimenta automaticamente o browse                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AA3->( DbGoTo( nRecnoAA3 ) )
				If At300Eqpto( { "AB2_CODPRO", AA3->AA3_CODPRO } ) 
					GDFieldPut( "AB2_CODPRO", AA3->AA3_CODPRO ) 
					GDFieldPut( "AB2_NUMSER", AA3->AA3_NUMSER ) 
					oGetDUS:oBrowse:Refresh() 				
				EndIf 			
  			Else
				AA3->( DbGoTo( nRecnoAA3 ) )
				GDFieldPut( "AB2_CODPRO", AA3->AA3_CODPRO ) 
				GDFieldPut( "AB2_NUMSER", AA3->AA3_NUMSER ) 
				oGetDUS:oBrowse:Refresh() 				
			EndIf 					
		EndIf
	EndIf 				
EndIf

If ( aArea[1] <> "SA1" )
	DbSelectArea(aArea[1])
	DbSetOrder(aArea[2])
	DbGoTo(aArea[3])
EndIf
Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At300Eqpto³ Autor ³ Eduardo Riera         ³ Data ³ 22.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a amarracao Produto X Equipamento                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void At300Eqpto()                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA300                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At300Eqpto( aConteudo )

Local lRetorno    := .F.
Local lTeca310    := AtIsRotina( "AT310INCLU" ) 
Local cCampo      
Local uConteudo   
Local nPosProd    := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO" })
Local aArea       := GetArea()

If ValType( aConteudo ) == "A"
	cCampo    := aConteudo[ 1 ]
	uConteudo := aConteudo[ 2 ]                    
Else
	cCampo    := ReadVar()
	uConteudo := &(ReadVar())
EndIf

If !lTeca310
	At300Rodape(AA3->AA3_CODPRO,AA3->AA3_NUMSER)
EndIf 	

If !Empty(M->AB1_LOJA)	
	Do Case
		Case ( "AB2_CODPRO"$cCampo )
			DbSelectArea("AA3")
			DbSetOrder(1)
			If ( DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+uConteudo) )
				lRetorno := .T.
			Else
				Help(" ",1,"REGNOIS")
			EndIf
		Case ( "AB2_NUMSER"$cCampo )
			DbSelectArea("AA3")
			DbSetOrder(1)
			If ( DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+aCols[n][nPosProd]+uConteudo) )
				lRetorno := .T.
			Else
				Help(" ",1,"REGNOIS")
			EndIf 
			If !lTeca310		
				At300Rodape(AA3->AA3_CODPRO,AA3->AA3_NUMSER)
			EndIf 	
	EndCase
EndIf
If ( !lRetorno )       
	If !lTeca310		
		At300Rodape()
	EndIf 	
EndIf

RestArea( aArea ) 

Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300LinOk³ Autor ³ Eduardo Riera         ³ Data ³ 23.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valicao da LinhaOk                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 15/02/07 ³ Cleber M.	 ³Bops 115746: Alteracao para validar tambem  ³±±
±±³			 ³				 ³a primeira linha do aCols.	          	  ³±± 
±±³ 21/05/07 ³ Conrado Q.    ³-Bops 125499: Inclusão do PE AT300LLK para  ³±±
±±³          ³               ³ser utilizado em validações adicionais.     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300LinOk()

Local nUsado   := Len(aHeader)
Local lRetorno := .T.
Local nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO" })
Local nPosNSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_NUMSER" })
Local nPosPrb  := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRB" })
Local nCntFor  := 0

If ( Len(aCols)>=1 .OR. !Empty(aCols[n][nPosProd] ) )
	If !aCols[n][nUsado+1] 
		If (  Empty(aCols[n][nPosProd]) .OR.;
				Empty(aCols[n][nPosNser]) .OR.;
				Empty(aCols[n][nPosPrb]) )
			Help(" ",1,"AT300LIN01")
			lRetorno := .F.
		EndIf
		If ( lRetorno )
			For nCntFor := 1 To Len(aCols)
				If (  aCols[n][nPosProd]+aCols[n][nPosNSer]+aCols[n][nPosPrb]==;
						aCols[nCntFor][nPosProd]+aCols[nCntFor][nPosNSer]+aCols[nCntFor][nPosPrb] .AND.;
						n <> nCntFor .AND. !aCols[nCntFor][nUsado+1] )
					Help(" ",1,"AT300LIN02")
					lRetorno := .F.
				EndIf
			Next nCntFor
		EndIf
		If ( lRetorno )
			DbSelectArea("AA3")
			DbSetOrder(1)
			If ( !DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+aCols[n][nPosProd]+aCols[n][nPosNSer]) )
				Help(" ",1,"AT300LIN03")
				lRetorno := .F.
			EndIf
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Chama o ponto para validar a exclusao da linha           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRetorno
			If ExistBlock( "AT300LDL" ) 	
				lRetorno := ExecBlock( "AT300LDL", .F., .F. ) 
			EndIf 
		EndIf 
	EndIf	
EndIf   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Usuario                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "AT300LLK" ) 
   lRetorno := ExecBlock( "AT300LLK", .F., .F., { lRetorno } ) 
EndIf 	   
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300TudOk³ Autor ³ Eduardo Riera         ³ Data ³ 23.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valicao da TudoOk                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At300TudOk()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 26/04/07 ³ Conrado Q.    ³Bops 124928: Adicionado PE AT300TDD e       ³±±
±±³          ³               ³AT300TDA para a validação dos campos.       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300TudOk()

Local lRetorno := .T.
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO"})
Local nPosnSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_NUMSER"})
Local nUsado   := Len(aHeader)
Local nCntFor  := 0 
For nCntFor := 1 To Len(aCols)
	If ( lRetorno .AND. !aCols[nCntFor][nUsado+1] )
		DbSelectArea("AA3")
		DbSetOrder(1)
		If ( !DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+aCols[nCntFor][nPosPrd]+aCols[nCntFor][nPosNSer]) )
			Help(" ",1,"AT300TUDOK")
			lRetorno := .F.
		EndIf
	EndIf
Next nCntFor

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz a chamada do ponto de entrada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If INCLUI
	If lRetorno .AND. ExistBlock( "AT300TDD" ) 
		lRetorno := ExecBlock( "AT300TDD", .F., .F., { lRetorno } )
	EndIf 
Else	
	If lRetorno .AND. ExistBlock( "AT300TDA" ) 
		lRetorno := ExecBlock( "AT300TDA", .F., .F., { lRetorno } )
	EndIf 
EndIf  

If INCLUI .And. AB1->(DbSeek(xFilial("AB1")+M->AB1_NRCHAM))      
	Help(" ",1,"NODELETA",,STR0060,2,0) //Código do chamado técnico já gravado.
	lRetorno := .F. 
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Grava³ Autor ³ Eduardo Riera         ³ Data ³ 22.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a Gravacao dos Chamados Tecnicos                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> Operacao : 1-Inclusao/2-Alteracao/3-Exclusao      ³±±
±±³          ³ ExpN2 -> Tipo de inclusao na fila de Help Desk             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³19/07/2006³ Cleber M.     ³Bops 102765: Alteracao para gravar o campo  ³±±
±±³          ³               ³AB1_ATEND em maiusculas, devido ao filtro   ³±±
±±³          ³               ³dos relatorios serem somente em maiusculas. ³±±
±±³10/01/2007³ Conrado Q.    ³Bops 115749: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300Grava(nOpcao,nTipoHD)

Local aArea      := GetArea() 
Local aAreaAb1   := AB1->( GetArea() ) 
Local aAreaAb2   := AB2->( GetArea() )
Local aColsAB2   := AClone( aCols ) 
Local aHeaderAB2 := AClone( aHeader ) 
Local aOrcamento := {}
Local aOs        := {}            
Local aCpoQNC 	 := {}    						   			// Array com Campos  que  serão preenchidos na FNC
Local aRetQNC 	 := {}    									// Array retorno com dados a serem gravados na FNC

Local bCampo     := {|x| FieldName(x) }

Local cSequen    := ""
Local cPrxSeq    := ""
Local cPriori    := ""
Local cNumHDE    := ""
Local cMemo      := ""
Local cNumOrc    := ""
Local cNumOS     := ""
Local cTipoAnt   := "0"
Local cTipoAB4   := ""
Local cTipoAB7   := ""
Local cCodUsr    := ""
Local cPrxTec    := ""
Local cHrVisit   := "" 
Local cDesOco    := ""     									// Descricao da Ocorrencia

Local dDtVisit   := Ctod("")

Local lStatus    := .T.
Local lGravou    := .F.
Local lFoundAB1  := .F. 
Local lFoundAB4  := .F.
Local lFoundAB7  := .F.
Local lCria      := .F.           
Local lAtuABL    := .F. 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis usadas na Integracao com Controle de Nao-Conformidades ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ     
Local lGravaFNC  := .F. 									// Define se ira ser aberta uma FNC 
Local lDelFNC    := .T. 									// Indica se a FNC pode ser deletada

Local nCntFor    := 0
Local nCntFor2   := 0
Local nPosProd   := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO" })
Local nPosItem   := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_ITEM"})
Local nPosMemo 	 := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_MEMO2"})	//Posição do campo AB2_MEMO2 no aHeader.
Local nPMemoAB7	 := 0 				
Local nUsado     := Len(aHeader)
Local nPosCampo  := 0 
Local nOpcOs     := 0 
Local nOpcOrc    := 0                          
Local nPosAhead  := 0       
Local nRecnoAB1  := 0 

Local uCampo     := ""   

Private aColsAB4     := {}
Private aColsAB5     := {}
Private aHeaderAB4   := {}
Private aHeaderAB5   := {}
Private aColsAB7     := {}
Private aColsAB8     := {}
Private aHeaderAB7   := {}
Private aHeaderAB8   := {}       

nTipoHD := If( ValType( nTipoHD ) == "N", nTipoHD, 1 ) 

Do Case
	Case nOpcao <> 3
		aT400Monta()
		aT450Monta()  

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se existe um item valido no acols            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lGravou := !Empty( Ascan( aCols, { |x| !x[nUsado+1] .AND. !Empty(x[nPosProd]) } ) )
		
		DbSelectArea("SA1")
		DbSetOrder(1)
		DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
		
		DbSelectArea("AB1")
		DbSetOrder(1)
		DbSeek(xFilial("AB1")+M->AB1_NRCHAM)
		
		lFoundAB1 := AB1->( Found() ) 
		
		If lFoundAB1 
			nRecnoAB1 := AB1->( Recno() ) 
		EndIf 	

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava arquivo AB1 (Cabecalho da Chamado Tecnico)      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lGravou )
			RecLock("AB1",!Found())
			For nCntFor := 1 To FCount()
				If ( "FILIAL"$Field(nCntFor) )
					AB1->(FieldPut(nCntFor,xFilial("AB1")))
				Else
					AB1->(FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor))))
				EndIf
			Next nCntFor
            
			AB1->AB1_ATEND	:= Upper(M->AB1_ATEND)
			AB1->AB1_REGIAO := SA1->A1_REGIAO
			If ( Empty(AB1->AB1_HORAF) )
				AB1->AB1_HORAF  := Time()
			EndIf
			
			nRecnoAB1 := AB1->( Recno() ) 
			
			AB1->( FKCommit() ) 
			aAreaAb1 := AB1->( GetArea() )
		EndIf 	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava os Itens do Chamado Tecnico                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AB2")
		For nCntFor := 1 To Len(aCols)
			If ( !aCols[nCntFor][nUsado+1] .AND. !Empty(aCols[nCntFor][nPosProd]) )
				DbSelectArea("AB2")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB2")+M->AB1_NRCHAM+aCols[nCntFor][nPosItem]) )
					RecLock("AB2",.F.)
					cTipoAnt := AB2->AB2_TIPO
				Else
					RecLock("AB2",.T.)
					cTipoAnt := "0"
				EndIf
				For nCntFor2 := 1 To nUsado
					If ( aHeader[nCntFor2][10] <> "V" )
						AB2->(FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2]))
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Caso  Haja   integracao  com  o  modulo QNC devera ser ³
						//³habilitada a  gravacao  dos dados  se  o canpo GeraFNC ³
						//³estiver  igual a "S".                                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( AllTrim(aHeader[nCntFor2][2])=="AB2_CODPRB" )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Caso a opcao  Gera FNC estiver  igual a  nao e  o campo  ³
							//³NumFNC  estiver preenchido nao  geraremos  a FNC         ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
                            If AtVerOcor(AB2->AB2_CODPRB)
	                            If !Empty(AllTrim(AB2->AB2_CODFNC))
									lGravaFNC := .F.
								Else
								 	If AB2->AB2_TIPO == "1" // Na tela de Chamado so sera gerada para o chamado
								   		lGravaFNC := .T.
								  	Else
										lGravaFNC := .F.
								  	EndIf
								EndIf
						  	Else
								lGravaFNC := .F.
	     					EndIf
						EndIf
					Else
						If ( AllTrim(aHeader[nCntFor2][2])=="AB2_MEMO2" )
							cMemo := aCols[nCntFor][nCntFor2]
						EndIf
					EndIf
				Next nCntFor2              
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava os campos fixos                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AA3")
				DbSetOrder(1)
				DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+AB2->AB2_CODPRO+AB2->AB2_NUMSER)
				AB2->AB2_FILIAL   := xFilial("AB2")
				AB2->AB2_NRCHAM   := M->AB1_NRCHAM
				AB2->AB2_STATUS   := If(AB2->AB2_TIPO=="1","A","E")
				AB2->AB2_CODFAB   := AA3->AA3_CODFAB
				AB2->AB2_LOJAFA   := AA3->AA3_LOJAFA
				AB2->AB2_EMISSA   := M->AB1_EMISSA
				AB2->AB2_CODCLI	:= M->AB1_CODCLI
				AB2->AB2_LOJA		:= M->AB1_LOJA
				If ( Empty(AB2->AB2_TIPO) )
					AB2->AB2_TIPO := "1"
				EndIf
				If ( AB2->AB2_TIPO == "1" )
					AB2->AB2_BXHORA := ""
					AB2->AB2_BXDATA := Ctod("")
				Else
					AB2->AB2_BXHORA := Time()
					AB2->AB2_BXDATA := dDataBase
				EndIf
				If ( Empty(AB2->AB2_CLASSI) )
					AB2->AB2_CLASSI := "001"
				EndIf

			 	If nOpcao == 1 	//Inclusao
			  		If !Empty( GDFieldGet("AB2_MEMO2") )
			  			MSMM(NIL,TamSx3("AB2_MEMO2")[1],NIL,cMemo,1,NIL,NIL,"AB2","AB2_MEMO")
			     	EndIf
			 	Else
			  		If Empty(AB2->AB2_MEMO)  	// Inclusao
			    		MSMM(NIL,TamSx3("AB2_MEMO2")[1],NIL,cMemo,1,NIL,NIL,"AB2","AB2_MEMO")
			      	Else                       	// Alteracao
			      		MSMM(AB2->AB2_MEMO,TamSx3("AB2_MEMO2")[1],NIL,cMemo,1,NIL,NIL,"AB2","AB2_MEMO")
			        EndIf
			 	EndIf
							
				MsUnLock()
				AB2->( FKCommit() ) 
				
				If ( lStatus .AND. AB2->AB2_STATUS=="A" )
					lStatus := .F.
				EndIf  
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Realiza integracao com o ambiente Controle de Nao-Conformidades ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ				
				If lGravaFNC 
					If Empty(AllTrim(AB2->AB2_CODFNC))
						aCpoQNC := {}
						cDesOco := ""
						aAdd(aCpoQNC,{"QI2_TPFIC" 	,"1"})
						aAdd(aCpoQNC,{"QI2_ORIGEM"	,"TEC"})
						aAdd(aCpoQNC,{"QI2_DESCR"	,STR0047+AllTrim(AB2->AB2_NRCHAM)+" - "+AB2->AB2_ITEM}) //"CHAMADO - ITEM: "
						cDesOco := Posicione("AAG",1,xFilial("AAG") + AB2->AB2_CODPRB,"AAG_DESCRI")
					    If !Empty(AllTrim(cDesOco))
							cDesOco := STR0048+AB2->AB2_CODPRB+CHR(13)+CHR(10)+" -> "+cDesOco // "Ocorrencia: "
							If !EMPTY(Alltrim(cMemo))
								cDesOco += CHR(13)+CHR(10)+STR0049+CHR(13)+CHR(10)+cMemo // "Comentarios: "
							EndIf
						Else
							If !EMPTY(Alltrim(cMemo))
								cDesOco := STR0049+CHR(13)+CHR(10)+cMemo // "Comentarios: "
							EndIf						
						EndIf						
						aAdd(aCpoQNC,{"QI2_MEMO1", cDesOco}) 														
						aAdd(aCpoQNC,{"QI2_CODCLI"	, AB2->AB2_CODCLI})
						aAdd(aCpoQNC,{"QI2_LOJCLI"	, AB2->AB2_LOJA})
						aAdd(aCpoQNC,{"QI2_CODPRO"	, AB2->AB2_CODPRO})	 
						//Campos especificos da integracao
						aAdd(aCpoQNC,{"QI2_NRCHAM"  , AB2->AB2_NRCHAM})  // Numero do Chamado
						aAdd(aCpoQNC,{"QI2_NUMSER"  , AB2->AB2_NUMSER})  // Numero de Serie do Produto
						aAdd(aCpoQNC,{"QI2_ITEMCH"  , AB2->AB2_ITEM})    // Item do Chamado
 				
						aRetQNC := QNCGERA(1,aCpoQNC)
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Grava o Codigo+Revisao da NC no Chamado					 	 ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						RecLock("AB2",.F.)
						AB2->AB2_CODFNC := aRetQNC[2] //Codigo da Nao-conformidade
						AB2->AB2_FNCREV := aRetQNC[3] //Revisao da Nao-conformidade
				 		MsUnLock()
						AB2->( FKCommit() ) 
					EndIf
					lGravaFNC := .F. //Forco a variavel
				EndIf				
				 				
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Orcamento                                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( AB2->AB2_TIPO == "2" )
					DbSelectArea("AB4")
					DbSetOrder(2)
					If ( DbSeek(xFilial("AB4")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
						cNumOrc := AB4->AB4_NUMORC
						lFoundAB4 := .T.
						cTipoAB4  := AB4->AB4_TIPO 
					Else
						lFoundAB4 := .F. 	
					EndIf     

					lCria := .T. 													
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Caso o item do AB4 exista e possua tipo diferente de Orcamento, nao altera ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lFoundAB4 .AND. cTipoAB4 <> "1"                
						lCria := .F.
					EndIf
					
					If lCria                    
						aAdd(aColsAB4,Array(Len(aHeaderAB4)+1))
						For nCntFor2 := 1 To Len(aHeaderAB4)
	
							nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
														SubStr(AllTrim(aHeaderAB4[nCntFor2][2]),4)} )
														
						
							If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB4[nCntFor2][2])<>"AB4_TIPO" )
								If AB2->(FieldPos(aHeader[nPosAhead][2])) > 0
									aColsAB4[Len(aColsAB4)][nCntFor2] := AB2->(FieldGet(FieldPos(aHeader[nPosAhead][2])))
								ElseIf AllTrim(aHeader[nPosAhead][2]) == 'AB2_MEMO2' 
								    If  IsInCallStack("AT300ALTER")
									     aColsAB4[Len(aColsAB4)][nCntFor2] := MSMM(AB4->AB4_MEMO)
									else
										 aColsAB4[Len(aColsAB4)][nCntFor2] := MSMM(AB2->AB2_MEMO)	 
									Endif
								EndIf
							Else
								If !IsHeadRec(aHeaderAb4[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb4[nCntFor2][2])
									aColsAB4[Len(aColsAb4)][nCntFor2] := CriaVar(aHeaderAb4[nCntFor2][2])
								Endif
							Endif                
							
						Next nCntFor2
						aColsAB4[Len(aColsAb4)][Len(aHeaderAB4)+1] := .F.
						aAdd(aOrcamento, AB2->AB2_NRCHAM+AB2->AB2_ITEM )
					EndIf 	
						
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Marca p/Excluir o item do Orcamento                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( cTipoAnt=="2" )
						DbSelectArea("AB4")
						DbSetOrder(2)
						If ( DbSeek(xFilial("AB4")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
							cNumOrc := AB4->AB4_NUMORC
						EndIf
						aAdd(aColsAB4,Array(Len(aHeaderAB4)+1))
						For nCntFor2 := 1 To Len(aHeaderAB4)
							nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
														SubStr(AllTrim(aHeaderAB4[nCntFor2][2]),4)} )
							If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB4[nCntFor2][2])<>"AB4_TIPO" )
								If !Empty( nPosCampo := AB2->( FieldPos(aHeader[nPosAhead][2] ) ) ) 
						  			aColsAB4[Len(aColsAB4)][nCntFor2] := AB2->(FieldGet( nPosCampo ) )
						  		EndIf	
							Else
								If !IsHeadRec(aHeaderAb4[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb4[nCntFor2][2])
									aColsAB4[Len(aColsAb4)][nCntFor2] := CriaVar(aHeaderAb4[nCntFor2][2])
								Endif
							Endif
						Next nCntFor2
						aColsAB4[Len(aColsAb4)][Len(aHeaderAB4)+1] := .T.
						aAdd(aOrcamento,AB2->AB2_NRCHAM+AB2->AB2_ITEM)
					EndIf
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ordem de Servico                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( AB2->AB2_TIPO == "3" )
					DbSelectArea("AB7")
					DbSetOrder(2)
					
					If ( DbSeek(xFilial("AB7")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
						cNumOs    := AB7->AB7_NUMOS
						lFoundAB7 := .T.
						cTipoAB7  := AB7->AB7_TIPO 
					Else
						lFoundAB7 := .F. 	
					EndIf                        
					
					lCria := .T. 													
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Caso o item do AB7 exista e possua tipo diferente de OS, nao altera ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lFoundAB7 .AND. cTipoAB7 <> "1"                
						lCria := .F.
					EndIf
					
					If lCria 
						aAdd(aColsAB7,Array(Len(aHeaderAB7)+1))
						nPMemoAB7 := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_MEMO2"})
						For nCntFor2 := 1 To Len(aHeaderAB7)
						
							nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
									SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )
							
							If nCntFor2 == nPosMemo	//Alimenta o campo memo observação.
								aColsAB7[Len(aColsAB7)][nPMemoAB7] := MSMM(AB2->AB2_MEMO)
							EndIf
							
							If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )                   
							
								If !Empty( nPosCampo := AB2->( FieldPos(aHeader[nPosAhead][2] ) ) ) 
									aColsAB7[Len(aColsAB7)][nCntFor2] := AB2->(FieldGet( nPosCampo ))
								EndIf 	
							Else
								If IsHeadRec(aHeaderAb7[nCntFor2][2])
									aColsAB7[Len(aColsAb7)][nCntFor2] := 0
								Elseif IsHeadAlias(aHeaderAb7[nCntFor2][2])
									aColsAB7[Len(aColsAb7)][nCntFor2] := "AB7"
								Else
									aColsAB7[Len(aColsAb7)][nCntFor2] := CriaVar(aHeaderAb7[nCntFor2][2])
								Endif
							Endif
								
						Next nCntFor2
						aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .F.
						aAdd(aOS,AB2->AB2_NRCHAM+AB2->AB2_ITEM)
					EndIf 	
						
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Marca p/Excluir o item da OS.                       ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( cTipoAnt=="3" )
						DbSelectArea("AB7")
						DbSetOrder(2)
						If ( DbSeek(xFilial("AB7")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
							cNumOS := AB7->AB7_NUMOS
						EndIf
						aAdd(aColsAB7,Array(Len(aHeaderAB7)+1))
						nPMemoAB7 := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_MEMO2"})
						For nCntFor2 := 1 To Len(aHeaderAB7)
							nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
													SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )
							
							If nCntFor2 == nPosMemo	//Alimenta o campo memo observação.
								aColsAB7[Len(aColsAB7)][nPMemoAB7] := MSMM(AB2->AB2_MEMO)
							EndIf
													
							If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )
								If !Empty( nPosCampo := AB2->( FieldPos(aHeader[nPosAhead][2] ) ) )
									aColsAB7[Len(aColsAB7)][nCntFor2] := AB2->(FieldGet( nPosCampo ))
								EndIf 	
							Else
								If IsHeadRec(aHeaderAb7[nCntFor2][2])
									aColsAB7[Len(aColsAb7)][nCntFor2] := 0
								Elseif IsHeadAlias(aHeaderAb7[nCntFor2][2])
									aColsAB7[Len(aColsAb7)][nCntFor2] := "AB7"
								Else
									aColsAB7[Len(aColsAb7)][nCntFor2] := CriaVar(aHeaderAb7[nCntFor2][2])
								Endif
							EndIf
						Next nCntFor2
						aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .T. //Deletar
						aAdd(aOS,AB2->AB2_NRCHAM+AB2->AB2_ITEM)
					EndIf
				EndIf
            
				cNumHde := AB2->AB2_NUMHDE 
				lAtuABL := .F. 

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria a sequencia na fila de Help desk               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( AB2->AB2_TIPO == "6" )
					If !ABL->(DbSeek(xFilial("ABL")+AB2->AB2_NRCHAM+AB2->AB2_ITEM))
					
						lAtuABL := .T. 
					    
						If nTipoHD == 2 
							cPriori := "000" 
							If Empty(cCodUsr)
								cCodUsr := RetCodUsr() 
							EndIf
							      
							AA1->( DbSetOrder( 4 ) ) 
							If AA1->( DbSeek( xFilial( "AA1" ) + cCodUsr ) ) 
								cPrxTec := AA1->AA1_CODTEC 								                                 																
							EndIf 
						Else	
							cPriori := CriaVar( "ABL_PRIORI" )  
						EndIf								
							
						cPrxSeq := CriaVar( "ABL_PRXSEQ" ) 
						cSequen := GetSXENum( "ABL", "ABL_SEQUEN" )  
						
						DbSelectArea( "ABL" ) 
						RecLock( "ABL", .T. ) 
						ABL->ABL_FILIAL  := xFilial( "ABL" ) 
						ABL->ABL_NRCHAM  := AB2->AB2_NRCHAM + AB2->AB2_ITEM 
						ABL->ABL_SEQUEN  := cSequen 
						ABL->ABL_DTINCL  := dDataBase
						ABL->ABL_HREMIS  := Left( Time(), 5 ) 
						ABL->ABL_SITUAC  := "1"
						ABL->ABL_PRIORI  := cPriori 
						ABL->ABL_PRXSEQ  := cPrxSeq 
						ABL->ABL_REAVAL  := "1" // Avalia na fila 
						ABL->ABL_REAMOT  := "1" // Motivo : Chamado 
						
						If !Empty( cPrxTec ) 
							ABL->ABL_PRXTEC := cPrxTec 
						EndIf 							
						
						ABL->( MsUnlock() ) 						
						
						cNumHDE := ABL->ABL_SEQUEN 
													
					EndIf 
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Marca p/Excluir o item do Help Desk                 ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( cTipoAnt=="6" )
						If ABL->(DbSeek(xFilial("ABL")+AB2->AB2_NRCHAM+AB2->AB2_ITEM))
							lAtuABL := .T. 
							RecLock( "ABL", .F., .T. ) 
							ABL->( dbDelete() ) 																																	
							ABL->( MsUnlock() ) 
							cNumHDE := CriaVar( "AB2_NUMHDE", .F. ) 
						EndIf 					
					EndIf
				EndIf
                  
				ABL->( FKCommit() ) 

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza a amarracao com o chamado                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				If lAtuABL
					AB2->( DbSetOrder( 1 ) ) 
					If AB2->( DbSeek( xFilial("AB2") + ABL->ABL_NRCHAM ) )
						RecLock("AB2")
						AB2->AB2_NUMHDE := cNumHDE 
						AB2->( MsUnlock() ) 
					EndIf
				EndIf 	
					
			Else
			
				lDelFNC := .T.
				
				DbSelectArea("AB2")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB2")+M->AB1_NRCHAM+aCols[nCntFor][nPosItem]) )
					
					If !EMPTY(AB2->AB2_CODFNC)                        .AND.; 	// Tem FNC
			  	   		AB2->AB2_TIPO == "1"                           .AND.; 	// Chamado Tecnico
			       		QNCVERI(AB2->AB2_CODFNC,AB2->AB2_FNCREV,"TEC") 			// Tem Plano de Acao ou esta baixada			  	   
							lDelFNC := .F.       // Impede a exclusao da FNCs e do Chadmado Tecnico
				  	EndIf
					
					If lDelFNC
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Deleta a FNC relacionada ao Item                    ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !EMPTY(AB2->AB2_CODFNC)  .AND.; 	// Tem FNC
					    	AB2->AB2_TIPO == "1"                           
								At300DFNC(.T., nCntFor, nPosItem, @aCols)
						EndIf

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Deleta o Item do Orcamento                          ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( AB2->AB2_TIPO == "2" )
							DbSelectArea("AB4")
							DbSetOrder(2)
							If ( DbSeek(xFilial("AB4")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
								cNumOrc := AB4->AB4_NUMORC
							EndIf
							aAdd(aColsAB4,Array(Len(aHeaderAB4)+1))
							For nCntFor2 := 1 To Len(aHeaderAB4)
								nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
															SubStr(AllTrim(aHeaderAB4[nCntFor2][2]),4)} )
								If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB4[nCntFor2][2])<>"AB4_TIPO" )
									If !Empty( nPosCampo := AB2->( FieldPos(aHeader[nPosAhead][2] ) ) )
										aColsAB4[Len(aColsAB4)][nCntFor2] := AB2->(FieldGet( nPosCampo ))
									EndIf 	
								Else
									If !IsHeadRec(aHeaderAb4[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb4[nCntFor2][2])
										aColsAB4[Len(aColsAb4)][nCntFor2] := CriaVar(aHeaderAb4[nCntFor2][2])
									Endif
								EndIf
							Next nCntFor2
							aColsAB4[Len(aColsAb4)][Len(aHeaderAB4)+1] := .T. //Deletar
							aAdd(aOrcamento,AB2->AB2_NRCHAM+AB2->AB2_ITEM)
						EndIf
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Deleta o Item da Ordem de Servico                   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( AB2->AB2_TIPO == "3" )
							DbSelectArea("AB7")
							DbSetOrder(2)
							If ( DbSeek(xFilial("AB7")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
								cNumOS := AB7->AB7_NUMOS
							EndIf
							aAdd(aColsAB7,Array(Len(aHeaderAB7)+1))
							For nCntFor2 := 1 To Len(aHeaderAB7)
								nPosAhead := aScan(aHeader,{|x| SubStr(AllTrim(x[2]),4)==;
														SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )
								If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )
								    If !Empty( nPosCampo := AB2->( FieldPos(aHeader[nPosAhead][2]) ) )
										aColsAB7[Len(aColsAB7)][nCntFor2] := AB2->(FieldGet( nPosCampo ))
									EndIf 	
								Else
									If IsHeadRec(aHeaderAb7[nCntFor2][2])
										aColsAB7[Len(aColsAb7)][nCntFor2] := AB7->(RecNo())
									Elseif IsHeadAlias(aHeaderAb7[nCntFor2][2])
										aColsAB7[Len(aColsAb7)][nCntFor2] := "AB7"
									Else
										If !IsHeadRec(aHeaderAb7[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb7[nCntFor2][2])
											aColsAB7[Len(aColsAb7)][nCntFor2] := CriaVar(aHeaderAb7[nCntFor2][2])
										Endif
									Endif
								EndIf
							Next nCntFor2
							aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .T. //Deletar
							aAdd(aOs,AB2->AB2_NRCHAM+AB2->AB2_ITEM)
						EndIf
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Deleta a Item da Fila de Held Desk                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( AB2->AB2_TIPO == "6" )
							ABL->( DbSetOrder( 2 ) ) 		
							If ABL->( DbSeek(xFilial("ABL")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
								Reclock( "ABL", .F. )
								ABL->( dbDelete() ) 
								ABL->( MsUnlock() ) 					
							EndIf
						EndIf 
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Deleta o Item do Chamado Tecnico                    ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						MSMM(AB2->AB2_MEMO,,,,2)
						RecLock("AB2")
						dbDelete()
						
						AB2->( FKCommit() ) 
					Else 
						MSGInfo(STR0053) // "Nao e possivel executar a exclusao do item pois existe uma FNC Relacionada com Plano de Acao em Execucao "
					EndIF
				EndIf
			EndIf
		Next nCntFor
		
		If lGravou 
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gravacao de informacoes adicionais no AB1           ³
			//³ e criacao das entidades vinculadas ( orcam./OS/HD)  ³			
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
			AB1->( MsGoto( nRecnoAB1 ) ) 

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Grava o status                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AB1->AB1_STATUS := If(lStatus,"E","A")  
			AB1->( MsUnLock() ) 
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Orcamento                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Len(aColsAB4) > 0 )
				If ( Empty(cNumOrc) )
					DbSelectArea("AB3")
					For nCntFor := 1 To FCount()
						uCampo := FieldName(nCntFor)
						M->&(uCampo) := CriaVar(uCampo,.T.)
					Next nCntFor 	
					M->AB3_CODCLI := AB1->AB1_CODCLI
					M->AB3_LOJA   := AB1->AB1_LOJA
				Else
					DbSelectArea("AB3")
					DbSetOrder(1)
					DbSeek(xFilial("AB3")+cNumOrc)
					For nCntFor := 1 To FCount()
						uCampo := FieldName(nCntFor)
						M->&(uCampo) := AB3->(FieldGet(nCntFor))
					Next nCntFor 	
				EndIf
            If ( Empty(M->AB3_CONPAG) )
					DbSelectArea("SA1")
					DbSetOrder(1)
					DbSeek(xFilial("SA1")+M->AB3_CODCLI+M->AB3_LOJA)
					M->AB3_CONPAG := SA1->A1_COND
				EndIf
				aHeader  := aClone(aHeaderAB4)
				aCols    := aClone(aColsAB4)
				DbSelectArea("AB3")
				DbSetOrder(1)
				nOpcOrc := If( DbSeek(xFilial("AB3")+M->AB3_NUMORC), 2, 1 ) 
				At400Grava(nOpcOrc,aOrcamento)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ordem de Servico                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Len(aColsAB7) > 0 )
				If ( Empty(cNumOs) )
					DbSelectArea("AB6")
					AB6->(dbSetOrder(1))
					For nCntFor := 1 To FCount()
						uCampo := FieldName(nCntFor)
						If uCampo == "AB6_NUMOS"                       
							cNumOS := CriaVar(uCampo,.T.)
							While AB6->(DbSeek(xFilial("AB6")+cNumOS)) 
								ConfirmSX8()
								cNumOS := GetSXENum("AB6","AB6_NUMOS")
							EndDo                                     
							M->&(uCampo) := cNumOS
						Else
							M->&(uCampo) := CriaVar(uCampo,.T.)
						EndIf 
					Next nCntFor	
					M->AB6_CODCLI := AB1->AB1_CODCLI
					M->AB6_LOJA   := AB1->AB1_LOJA
				Else
					DbSelectArea("AB6")
					DbSetOrder(1)
					DbSeek(xFilial("AB6")+cNumOS)
					For nCntFor := 1 To FCount()
						uCampo := FieldName(nCntFor)
						M->&(uCampo) := AB6->(FieldGet(nCntFor))
					Next nCntFor
				EndIf
				If ( Empty(M->AB6_CONPAG) )
					DbSelectArea("SA1")
					DbSetOrder(1)
					DbSeek(xFilial("SA1")+M->AB6_CODCLI+M->AB6_LOJA)
					M->AB6_CONPAG := SA1->A1_COND
				EndIf
				aHeader  := aClone(aHeaderAB7)
				aCols    := aClone(aColsAB7)
				DbSelectArea("AB6")
				DbSetOrder(1)
				nOpcOs := If( DbSeek(xFilial("AB6")+M->AB6_NUMOS), 2, 1 ) 
				At450Grava(nOpcOs,aOS)
				If ( SuperGetMv("MV_ITMPSTD") )
					DbSelectArea("ABB")
					DbSetOrder(3)
					If ( DbSeek(xFilial("ABB")+M->AB6_NUMOS) )
						While ( !Eof() .AND. xFilial("ABB") == ABB->ABB_FILIAL .AND.;
													M->AB6_NUMOS	== ABB->ABB_NUMOS )
							dDtVisit := If(Empty(dDtVisit),ABB->ABB_DTINI,Min(dDtVisit,ABB->ABB_DTINI))
							If ( dDtVisit == ABB->ABB_DTINI )
								cHrVisit := If(Empty(cHrVisit),ABB->ABB_HRINI,If(cHrVisit>ABB->ABB_HRINI,ABB->ABB_HRINI,cHrVisit))
							EndIf
							DbSelectArea("ABB")
							dbSkip()
						End
					EndIf
				EndIf
			EndIf
		
		Else
			If lFoundAB1 .AND. lDelFNC
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Caso o chamado nao possua itens validos, exclui     ³			
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AB1->( MsGoto( nRecnoAB1 ) ) 
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Deleta o Orcamento                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB4")
				DbSetOrder(2)
				If ( DbSeek(xFilial("AB4")+AB1->AB1_NRCHAM) )
					DbSelectArea("AB3")
					DbSetOrder(1)
					DbSeek(xFilial("AB3")+AB4->AB4_NUMORC)
	               At400Grava(3,aOrcamento)
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Deleta a Ordem de Servico                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB7")
				DbSetOrder(2)
				If ( DbSeek(xFilial("AB7")+AB1->AB1_NRCHAM) )
					DbSelectArea("AB6")
					DbSetOrder(1)
					DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)
	               At450Grava(3,aOS)
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Deleta o Chamado Tecnico                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("AB1")
				dbDelete()
				
			EndIf
		
		EndIf 	
		
	OtherWise
		
	  	lDelFNC := .T.
		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Identifico se existe alguma FNC que impessa da delecao do Chamado Tecnico   ³
		//³em caso afirmativo não deleto o Cadastro.                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AB2")
		DbSetOrder(1)
		If DbSeek(xFilial("AB2")+M->AB1_NRCHAM)
			While !Eof() .AND. xFilial("AB2")+M->AB1_NRCHAM == xFilial("AB2")+AB2->AB2_NRCHAM   
			  	If !EMPTY(AB2->AB2_CODFNC)                        .AND.; 	// Tem FNC
			  	   AB2->AB2_TIPO == "1"                           .AND.; 	// Chamado Tecnico
			       QNCVERI(AB2->AB2_CODFNC,AB2->AB2_FNCREV,"TEC") 			// Tem Plano de Acao ou esta baixada			  	   
					lDelFNC := .F.       // Impede a exclusao da FNCs e do Chadmado Tecnico
			  	EndIf
			  	DbSkip()
			End
		EndIF
			
		If lDelFNC
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deletar FNCs                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB2")
			DbSetOrder(1)
			If DbSeek(xFilial("AB2")+M->AB1_NRCHAM)		    
	   			While !Eof() .AND. xFilial("AB2")+M->AB1_NRCHAM == xFilial("AB2")+AB2->AB2_NRCHAM   
				  	If !EMPTY(AB2->AB2_CODFNC)                        .AND.; 	// Tem FNC
				  	   AB2->AB2_TIPO == "1"                           
						At300DFNC()
				  	EndIf
				  	AB2->(DbSkip())
				End
			EndIf 
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deletar Orcamento                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB4")
			DbSetOrder(2)
			If ( DbSeek(xFilial("AB4")+AB1->AB1_NRCHAM) )
				DbSelectArea("AB3")
				DbSetOrder(1)
				DbSeek(xFilial("AB3")+AB4->AB4_NUMORC)
				At400Grava(3,aOrcamento)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deletar Ordem de Servico                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB7")
			DbSetOrder(2)
			If ( DbSeek(xFilial("AB7")+AB1->AB1_NRCHAM) )
				DbSelectArea("AB6")
				DbSetOrder(1)
				DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)
				At450Grava(3,aOs)
			EndIf                                                
			
			DbSelectArea("AB2")
			DbSetOrder(1)
			If DbSeek(xFilial("AB2")+M->AB1_NRCHAM)
				While ( !Eof() .AND. xFilial("AB2") == AB2->AB2_FILIAL .AND.;
											M->AB1_NRCHAM  == AB2->AB2_NRCHAM )
											
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Exclui o item da fila de Help Desk                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If AB2->AB2_TIPO == "6"
						If ABL->(DbSeek(xFilial("ABL")+AB2->AB2_NRCHAM+AB2->AB2_ITEM))
							RecLock( "ABL", .F., .T. ) 
							ABL->( DbDelete() ) 																																	
							ABL->( MsUnlock() ) 
						EndIf 					
					EndIf
											
					MSMM(AB2->AB2_MEMO,,,,2)
					RecLock("AB2")
					DbDelete()
					DbSelectArea("AB2")
					DbSkip()
				End
				AB2->( FKCommit() ) 	
			EndIf	
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta Chamado Tecnico                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB1")
			RecLock("AB1")
			DbDelete()
		Else
			MsgInfo(STR0050) // "Nao e possivel executar a exclusao do registro - Existem FNCs com Plano de Acao em Execucao"
		EndIf			
EndCase
DbSelectArea("AB1")
If ( !Empty(dDtVisit) .AND. SuperGetMv("MV_ITMPSTD") .AND.;
		( AtIsRotina("AT300INCLU") .OR. AtIsRotina("AT300ALTER")) )
	MsgInfo(PadR(STR0035+Dtoc(dDtVisit),20)+Chr(10)+Chr(13)+STR0036+cHrVisit,STR0037) //"Data: "###"Hora:"###"Previs„o de Atendimento: "
EndIf                  

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura o aCols e o aHeader antes do ponto de entrada                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := AClone( aHeaderAB2 ) 
aCols   := AClone( aColsAB2 ) 

If ExistBlock("AT300GRV")
	Execblock("AT300GRV",.F.,.F.)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a Entrada da Rotina                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RestArea( aAreaAB1 )
RestArea( aAreaAB2 )
RestArea( aArea ) 

Return( { lGravou, M->AB1_NRCHAM } )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300GrvOr³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera o Orcamento                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300GrvOr()

Processa({|| At300Proc(1) })
CloseBrowse()
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300GrvOs³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera a Ordem de Servico                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300GrvOs()

Processa({|| At300Proc(2) })
CloseBrowse()
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300GrvHd³ Autor ³ Sergio Silveira       ³ Data ³25/09/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera o item de Help Desk                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At300GrvHd()

Processa({|| At300Proc(3) })
CloseBrowse()
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Proc ³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento da Efetivacao de Orcamentos e Os             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : 1 - Orcamento / 2 - OS / 3 - Help Desk             ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function At300Proc(nTipo)

Local nCntFor 			:= 0
Local uCampo			:= ""
Local lTravas			:= .T.
Local aTravas			:= {}
Local nUsado			:= 0
Local nPosItem			:= 0
Local aSavaHeader		:= {}
Local aTpStatus      := { "2", "3", "6" } 
Local nSaveSX8			:= GetSX8Len()
Local lAt300Val	:= ExistBlock("AT300Val")	
Local lRet := .T.

Local aRet       := {}
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private aCols		:= {}
Private aHeader		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta aHeader                                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB2")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			nUsado++
			aRet := FwTamSx3(cField)
			aadd(aHeader,{AllTrim(FWX3Titulo(cField)),;
						cField,;
						AllTrim(X3Picture(cField)),;
						aRet[1],;
						aRet[2],;
						GetSx3Cache(cField, "X3_VALID"),;
						GetSx3Cache(cField, "X3_USADO"),;
						aRet[3],;
						"AB2",;
						GetSx3Cache(cField, "X3_CONTEXT")})
			If ( AllTrim(cField) == "AB2_ITEM" )
				nPosItem := nUsado
			EndIf
		EndIf
	Next nC
EndIf

aSavAheader := aClone(aHeader)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processamento da MarkBrowse                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("AB1")
AB1->(DbSetFilter({|| &cFiltroAB1}, cFiltroAB1))
DbGoTop() //IndRegua()
ProcRegua(LastRec())
While ( !Eof() )
	
	If lAt300Val
		lRet := ExecBlock("AT300Val",.F.,.F.,{ nTipo ,AB1->AB1_NRCHAM,IsMark("AB1_OK",ThisMark(),ThisInv())})
	EndIf

	If ( IsMark("AB1_OK",ThisMark(),ThisInv()) .AND. SoftLock("AB1") .And. lRet)
		lTravas	:= .T.
		aTravas	:= {}
		aCols		:= {}
		aHeader	:= aClone(aSavAheader)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria Variaveis de Memoria da Enchoice                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AB1")
		aAdd(aTravas , { Alias() , RecNo() })
		For nCntFor := 1 To FCount()
			uCampo := FieldName(nCntFor)
         M->&(uCampo) := AB1->(FieldGet(FieldPos(uCampo)))
		Next nCntFor
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta Acols                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    DbSelectArea("AB2")
		DbSetOrder(1)
		DbSeek(xFilial("AB2")+M->AB1_NRCHAM)
		While ( !Eof() .AND. xFilial("AB2") == AB2->AB2_FILIAL .AND.;
									M->AB1_NRCHAM  == AB2->AB2_NRCHAM )
			aAdd(aCols,Array(nUsado+1))
			For nCntFor := 1 To nUsado
				If ( aHeader[nCntFor][10] <> "V" )
					aCols[Len(aCols)][nCntFor] := AB2->(FieldGet(FieldPos(aHeader[nCntFor,2])))
					If ( AllTrim(aHeader[nCntFor][2]) == "AB2_TIPO" )
						If ( AB2->AB2_TIPO=="1" )
							aCols[Len(aCols)][nCntFor] := aTpStatus[ nTipo ] 
						EndIf
					EndIf
	            Else
          			If IsHeadRec(aHeader[nCntFor][2])
						aCols[Len(aCols)][nCntFor] := AB2->(Recno())
					Elseif IsHeadAlias(aHeader[nCntFor][2])
						aCols[Len(aCols)][nCntFor] := "AB2"
					Else
						aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
					Endif
    	        EndIf
			Next nCntFor
			aCols[Len(aCols)][nUsado+1] := .F.
			If ( SoftLock("AB2") .AND. lTravas )
				aAdd(aTravas, { Alias() , RecNo() })
			Else
				lTravas := .F.
			EndIf
			DbSelectArea("AB2")
			dbSkip()
		End
		If ( Len(aCols) == 0 )
			aAdd(aCols,Array(nUsado))
			For nCntFor := 1 To nUsado
       			If IsHeadRec(aHeader[nCntFor,2])
					aCols[1][nCntFor] := AB2->(Recno())
				Elseif IsHeadAlias(aHeader[nCntFor,2])
					aCols[1][nCntFor] := "AB2"
				Else
					aCols[1][nCntFor] := CriaVar(aHeader[nCntFor,2])
				Endif
			Next nCntFor
			aCols[Len(aCols)][nUsado+1] := .F.
      EndIf
		Begin Transaction
			at300Grava(2)
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			End
		End Transaction
		For nCntFor := 1 To Len(aTravas)
			DbSelectArea(aTravas[nCntFor][1])
			DbGoTo(aTravas[nCntFor][2])
			MsUnLock()
		Next nCntFor
	EndIf
	DbSelectArea("AB1")
	dbSkip()
	IncProc()
End
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300BCli ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra o Historico de Clientes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300BCli()

Local aArea     := GetArea()
Local lConsulta := .T. 

DbSelectArea("SA1")
DbSetOrder(1)
If ( DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA) )
	If GetNewPar( "MV_ATHISPA", 2 ) == 1 
		lConsulta := Pergunte("ATC010",.T.)
	EndIf 	
	If lConsulta 
		AtC010con(Alias(),RecNo(),2)
	EndIf 	
Else
	Help(" ",1,"AT300BCLI")
EndIf

RestArea( aArea ) 

Return() 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300BPro ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra o Historico de Equipamentos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300BPro()

Local aArea      := GetArea()
Local nPosProd   := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO" })
Local nPosNser   := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_NUMSER" })
Local lConsulta  := .T. 

DbSelectArea("AA3")
DbSetOrder(1)
If ( DbSeek(xFilial("AA3")+M->AB1_CODCLI+M->AB1_LOJA+aCols[n][nPosProd]+aCols[n][nPosNser]) )
	If GetNewPar( "MV_ATHISPA", 2 ) == 1 
		lConsulta := Pergunte("ATC020",.T.)
	EndIf 	
	
	If lConsulta 
		AtC020con(Alias(),RecNo(),2)
	EndIf 	
Else
	Help(" ",1,"AT300BPRO") // Informe o cliente, Produto e numero de serie
EndIf

RestArea( aArea ) 

Return() 


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300BFaq ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao FAQ                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300BFAQ()

Local aArea    := { Alias() , IndexOrd() , RecNo() }
Local nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO" })
Local nPosPrb  := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRB" })
Local cPrbIni  := aCols[n][nPosPrb]
Local cPrbFim  := If(Empty(aCols[n][nPosPrb]),Repl(Chr(255),Len(cPrbIni)),aCols[n][nPosPrb])

AtC030Faq(cPrbIni,cPrbFim,aCols[n][nPosProd],aCols[n][nPosProd])

DbSelectArea(aArea[1])
DbSetOrder(aArea[2])
DbGoTo(aArea[3])

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Rodap³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Atualizacao de Rodape                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Logico                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Produto                                            ³±±
±±³          ³ ExpC2 : Nr.Serie                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function At300Rodape(cProduto,cNumSer,oGetd)

Local aSimNao	:= {STR0033,STR0034}	//"Sim"###"N„o"
Local nPosPro	:= aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRO"})
Local nPosNSe	:= aScan(aHeader,{|x| AllTrim(x[2])=="AB2_NUMSER"})
Local nPosPrb := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_CODPRB"})
Local nPosDesc:= aScan(aHeader,{|x| AllTrim(x[2])=="AB2_DESCRI"})
Local cCodFab	:= ""
Local cLojaFa	:= ""
Local cCodcli	:= M->AB1_CODCLI
Local cLoja  	:= M->AB1_LOJA
Local aArea  	:= GetArea()
Local lLogico
Local aColor 	:= { CLR_HBLUE,CLR_HRED }
Local nCntFor	:= 0

Local oDlg   


If Type("lAt300Auto")=="U" .OR. !lAt300Auto
	If ALTERA
		If Empty(aCols[n][nPosPrb]) .And. !Empty(aCols[n][nPosDesc])
			aCols[n][nPosDesc] := "" 
			oGetd:oBrowse:Refresh()
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisa a janela correta                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oDlg   	:= GetWndDefaut()
	If ( oDlg <> Nil )
		
		cProduto := If(cProduto==Nil,aCols[n][nPosPro],cProduto)
		cNumSer  := If(cNumSer ==Nil,aCols[n][nPosNSe],cNumSer)
		
		If( Empty(cProduto) )
			aColor := { CLR_BLACK , CLR_BLACK }
			aSimNao:= { "___","___" }
		EndIf
		
		DbSelectArea("AA3")
		DbSetOrder(1)
		DbSeek(xFilial("AA3")+cCodCli+cLoja+cProduto+cNumSer)
		cCodFab := AA3->AA3_CODFAB
		cLojaFa := AA3->AA3_LOJAFA
		
		For nCntFor := 1 To Len( aCtrRodape ) 
		
			oControl := aCtrRodape[ nCntFor, 2 ] 
			
			Do Case
				Case aCtrRodape[ nCntFor, 1 ] == "R-CONT"
					lLogico := AtIsCtrMan(cCodFab,cLojaFa,cProduto,cNumSer)
					oControl:SetColor(aColor[If(lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)])
				Case aCtrRodape[ nCntFor, 1 ]  == "R-GARA"
					lLogico := AtIsGar(cCodFab,cLojaFa,cProduto,cNumSer)
					oControl:SetColor(aColor[If(lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)])
				Case aCtrRodape[ nCntFor, 1 ]  == "R-PREV"
					lLogico := AtIsCtrPre(cCodFab,cLojaFa,cProduto,cNumSer)
					oControl:SetColor(aColor[If(!lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)]+" - "+StrZero(AtIsNumPre(cCodFab,cLojaFa,cProduto,cNumSer),3))
				Case aCtrRodape[ nCntFor, 1 ]  == "R-CHAM"
					lLogico := AtIsChamado(cCodCli,cLoja)
					oControl:SetColor(aColor[If(!lLogico,1,2)],oControl:nClrPane)	
					oControl:SetText(aSimNao[If(lLogico,1,2)])
				Case aCtrRodape[ nCntFor, 1 ]  == "R-ORCA"
					lLogico := AtIsOrc(cCodCli,cLoja)
					oControl:SetColor(aColor[If(!lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)])
				Case aCtrRodape[ nCntFor, 1 ]  == "R-OSRV"
					lLogico := AtIsOS(cCodCli,cLoja)
					oControl:SetColor(aColor[If(!lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)])
				Case aCtrRodape[ nCntFor, 1 ]  == "R-OBSO"
					lLogico := AtIsObsol(cCodFab,cLojaFa,cProduto,cNumSer)
					oControl:SetColor(aColor[If(!lLogico,1,2)],oControl:nClrPane)
					oControl:SetText(aSimNao[If(lLogico,1,2)])
			EndCase
		Next nCntFor
	EndIf
EndIf

RestArea( aArea ) 
	
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtBofAA3  ³ Autor ³Eduardo Riera          ³ Data ³21.06.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para definicao do Topo no F3 / Produto-Equipamento   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpC1: String de definicao do Topo                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AtBofAA3()

Local cFiltro	:= ""

If ( AA3->(IndexOrd())<6 )
	cFiltro := xFilial("AA3")+SA1->A1_COD+SA1->A1_LOJA
Else
	cFiltro := xFilial("AA3")
EndIf

Return(cFiltro)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtEofAA3  ³ Autor ³Eduardo Riera          ³ Data ³21.06.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para definicao do Final no F3 / Produto-Equipamento  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpC1: String de definicao do Final                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AtEofAA3()

Local cFiltro	:= ""                

If ( AA3->(IndexOrd())<6 )
	cFiltro := xFilial("AA3")+SA1->A1_COD+SA1->A1_LOJA
Else
	cFiltro := xFilial("AA3")
EndIf

Return(cFiltro)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Carga³ Autor ³ Sergio Silveira       ³ Data ³28/06/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Carrega o equipamento posicionado no AA3 para o atendimento³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At300Carga( ExpO1, oEnch )                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto GetDados                                   ³±±
±±³          ³ ExpO2 -> Objeto Enchoice                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³11/06/2007³Conrado Q Gomes³-BOPS 127131: Adição do ponto de entrada    ³±±
±±³          ³               ³AT300CAR.                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At300Carga( oGetD, oEnch )  
Local lAT300CAR := ExistBlock("AT300CAR")	// Validação da existência do ponto de entrada AT300CAR

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa o ponto de entrada AT300CAR.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAT300CAR
	ExecBlock("AT300CAR")
EndIf

 At300LastB(oGetD, oEnch)
 
If ( Empty( M->AB1_CODCLI ) .AND. Empty( M->AB1_LOJA ) ) .OR. ;
	 ( M->AB1_CODCLI == AA3->AA3_CODCLI .AND. M->AB1_LOJA == AA3->AA3_LOJA ) 

	M->AB1_CODCLI := AA3->AA3_CODCLI
	M->AB1_LOJA   := AA3->AA3_LOJA 
           
	If At300Eqpto( { "AB2_CODPRO", AA3->AA3_CODPRO } ) 
		GDFieldPut( "AB2_CODPRO", AA3->AA3_CODPRO ) 
		GDFieldPut( "AB2_NUMSER", AA3->AA3_NUMSER ) 
	EndIf 			

	oGetD:oBrowse:Refresh() 
	oEnch:EnchRefreshAll() 

Else 
	Help( " ", 1, "AT300CLIDF" ) // O cliente do atendimento nao eh o mesmo do eqto. 
EndIf	

Return( Nil ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300VldUs³ Autor ³ Sergio Silveira       ³ Data ³12/03/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento da confirmacao / nao confirmacao da inclusao    ³±±
±±³          ³ ou alteracao                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := At300VldUs( ExpN1 )                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> Opcao : 1 -> Confirma / 0 -> Nao Confirma         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At300VldUs( nOpca ) 

Local lRet := .T.                

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz a chamada do ponto passando nOpca como parametro ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If INCLUI
	If ExistBlock( "AT300VLD" ) 
		lRet := ExecBlock( "AT300VLD", .F., .F., { nOpca } ) 	
	EndIf 
Else	
	If ExistBlock( "AT300VLA" ) 
		lRet := ExecBlock( "AT300VLA", .F., .F., { nOpca } ) 	
	EndIf 
EndIf 
		
Return( lRet ) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300Track³ Autor ³ Sergio Silveira       ³ Data ³09/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function At300Track() 

Local aEnt     := {}
Local cChamado := M->AB1_NRCHAM                                     
Local nPosItem := GDFieldPos( "AB2_ITEM" ) 
Local nLoop    := 0 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os itens para rastreamento          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aCols ) 
	aAdd( aEnt, { "AB2", cChamado + aCols[ nLoop, nPosItem ] } ) 
Next nLoop 

MaTrkShow( aEnt ) 
           
Return( .T. ) 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtVerOcor ³ Autor ³ Cicero Cruz                     ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Verifica se a Ocorrencia escolhida gera FNC ou Nao                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FIELD SERVICE                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista  ³ Data/Bops/Ver ³Manutencao Efetuada                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cicero C. ³19/01/06³8.11  ³-Incluida a Integracao com o Controle de Nao-Conformi-³±±
±±³          ³89415   ³      ³dades                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtVerOcor(cAcao)
Local lRet := .F. 															// Indica se a Ocorrencia  ira gerar FNC
Local cGerFNC := Posicione("AAG",1,xFilial("AAG") + cAcao,"AAG_GERFNC") 	// Resgata o conteudo do campo AAG_GERFNC

If cGerFNC == "1"
	lRet := .T.
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At300DFNC ³ Autor ³ Cicero Cruz                     ³ Data ³ 19/01/06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Deleta as FNC criadas                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ FIELD SERVICE - Exclusão de Chamado Tecnico                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Analista  ³ Data/Bops/Ver ³Manutencao Efetuada                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Cicero C. ³19/01/06³8.11  ³-Incluida a Integracao com o Controle de Nao-Conformi-³±±
±±³          ³89415   ³      ³dades                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At300DFNC (lItem, nLinha, nPosItem, aCols)
Local aArea      := GetArea()          // Salvo as posicoes  dos registros
Default lItem    := .F.					// Deleta so o item do acols
Default nLinha	 := 0 					// Posicao da linha no aCols
Default nPosItem := 0					// Posicao na linha do aCols

DbSelectArea("AB2")
DbSetOrder(1)
If DbSeek(xFilial("AB2")+M->AB1_NRCHAM+IF(lItem,aCols[nLinha][nPosItem],""))
	DbSelectArea("QI2")
	DbSetOrder(2)
	While !AB2->(Eof()) .AND. xFilial("AB2")+M->AB1_NRCHAM == xFilial("AB2")+AB2->AB2_NRCHAM   .AND. IF(lItem,aCols[nLinha][nPosItem] == AB2->AB2_ITEM,.T.)
  		If !EMPTY(AB2->AB2_CODFNC) .AND.; 					// Tem FNC
	  		AB2->AB2_TIPO == "1"         					// Chamado Tecnico
		    If DbSeek(xFilial("QI2")+AB2->AB2_CODFNC+AB2->AB2_FNCREV)
		   		DbSelectArea("QI2")
                // Prevencao Auditorias
				RecLock("QI2",.F.)    
				cVar := ""
				If Empty(QI2->QI2_COMEN)	// Inclusao	  
					MSMM(,TamSx3("QI2_MEMO2")[1],,STR0051+UPPER(QA_AmbDet( Nil, "SIGATEC", 4 ))+STR0052,1,,,"QI2","QI2_COMEN") // " ** FICHA DE NAO CONFORMIDADE CANCELADA PELO"###" ** - Motivo: Chamado Excluido"
				Else						// Alteracao
					MSMM(QI2->QI2_COMEN,TamSx3("QI2_MEMO2")[1],,STR0051+UPPER(QA_AmbDet( Nil, "SIGATEC", 4 ))+STR0052,1,,,"QI2","QI2_COMEN") // " ** FICHA DE NAO CONFORMIDADE CANCELADA PELO "###" ** - Motivo: Chamado Excluido"
				Endif
				MsUnLock()				
			EndIf
		    If DbSeek(xFilial("QI2")+AB2->AB2_CODFNC+AB2->AB2_FNCREV)
		    	Begin Transaction
			   		DbSelectArea("QI2")
				    // Deleto o Registro
					RecLock("QI2",.F.,.T.)
			   		DbDelete()
					MsUnLock()
				End Transaction  
			EndIf
		EndIf
		AB2->(DbSkip())
	End
EndIf          
RestArea(aArea)
Return Nil


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ At300Leg ³ Autor ³ Cleber Martinez       ³ Data ³12/04/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Exibe a legenda do Chamado Tecnico                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At300Leg() 
Local aCoresNew := {}
Local aCores    := {{"ENABLE",STR0056},;// "Encerrado"
						   {"DISABLE",STR0057}}// "Encerrado"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores da legenda    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("AT30LEG")
	aCoresNew := ExecBlock("AT30LEG",.F.,.F.,aCores)
	If ValType( aCoresNew ) == "A"
		aCores := aClone(aCoresNew)
	EndIf
Endif

BrwLegenda( cCadastro,  STR0055 , aCores ) //"Status"

Return( Nil ) 
           
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³At300Verif³ Autor ³ Conrado Q. Gomes      ³ Data ³27/12/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Verifica se o item pode ser adicionado ao aCols            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Verdadeiro ou Falso, onde: .T. - Pode, .F. - Não pode      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nOpc - Opção da janela                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function At300Verif(nOpc)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aqui eh verificado se o item esta amarrado a alguma OS ou Orcamento que³
	//³ foi baixado, ou help Desk alocado.                                     ³
	//³ Quando isto acontece a alteracao nao eh permitida.                     ³ 
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpc != 4 //alterar
		DbSelectArea("AB4")
		DbSetOrder(1)
		If ( !Empty(AB2->AB2_NUMORC) .AND. DbSeek(xFilial("AB4")+AB2->AB2_NUMORC) )
			If ( AB4->AB4_TIPO <> "1" )			
				Help(" ",1,"AT300TPORC")			
				DbSelectArea("AB2")
				Return(.F.)
			EndIf
		EndIf
	
		DbSelectArea("AB7")
		DbSetOrder(1)
		If ( !Empty(AB2->AB2_NUMOS) .AND. DbSeek(xFilial("AB7")+AB2->AB2_NUMOS) )
	      If ( AB7->AB7_TIPO <> "1" )
				Help(" ",1,"AT300TIPOS")
				DbSelectArea("AB2")
				Return(.F.)
			EndIf
		EndIf
	EndIf
	ABL->( DbSetOrder( 2 ) ) 		
	If ABL->( DbSeek(xFilial("ABL")+AB2->AB2_NRCHAM+AB2->AB2_ITEM) )
      If ( ABL->ABL_SITUAC<>"1" )
			Help(" ",1,"AT300TIPHD")
			DbSelectArea("AB2")
			Return(.F.)
		EndIf
	EndIf
	
	If ( nOpc == 5 )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se ha algum item alocado que foi sacramentado                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("ABB")
		DbSetOrder(3)
		If ( AB7->(Found()) .AND. DbSeek(xFilial("ABB")+AB7->AB7_NUMOS) )
			If ( ABB->ABB_SACRA == "S" )
				Help(" ",1,"AT300SACRA")
				DbSelectArea("AB2")
				Return(.F.)
			EndIf
		EndIf
	Endif
	DbSelectArea("AB2")
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³GetNumAB1 ³ Autor ³Vendas e CRM           ³ Data ³ 02/08/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializa o Numero de Chamado Técnico                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Numero do Chamado Técnico                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GetNumAB1()
Local aArea		:= GetArea()
Local aAreaAB1	:= AB1->(GetArea())
Local cNum		:= GetSx8Num("AB1","AB1_NRCHAM")
Local nSaveSX8 := GetSX8Len()

dbSelectArea("AB1")
dbSetOrder(1)
While AB1->(DbSeek(xFilial("AB1")+cNum))
	While ( GetSX8Len() > nSaveSX8 )
		ConfirmSX8()
	EndDo
	cNum := GetSx8Num("AB1","AB1_NRCHAM")
EndDo

RestArea(aAreaAB1)
RestArea(aArea)
Return(cNum)

/*/{Protheus.doc} At300LastB
Atribui a grid a ultima base de atendimento criada para o cliente
@author Vendas e CRM
@since 06/10/2014
@version 1.0
/*/
Function At300LastB(oGetD, oEnch)
Local cAliasQry := ""
Local cQuery    := ""
Local nRecnoAA3 := 0

If Len( aCols ) == 1	.AND. !Empty( M->AB1_CODCLI ) .AND. !Empty( M->AB1_LOJA )

	cAliasQry := GetNextAlias() 		

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o cliente possui um unico registro na base instalada       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	cAliasQry := GetNextAlias() 		
	cQuery    := ""                            
	
	cQuery += "SELECT R_E_C_N_O_ AA3RECNO FROM " + RetSqlName( "AA3" ) + " " 
	cQuery += "WHERE " 
	cQuery += "AA3_FILIAL='" + xFilial( "AA3" ) + "' AND " 
	cQuery += "AA3_CODCLI='" + M->AB1_CODCLI     + "' AND " 
	cQuery += "AA3_LOJA='"   + M->AB1_LOJA    + "' AND " 
	cQuery += "D_E_L_E_T_=' ' "
	cQuery += "ORDER BY R_E_C_N_O_ DESC" 

	cQuery := ChangeQuery( cQuery ) 
	
	dbUseArea( .t., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )
	
	TcSetField( cAliasQry, "AA3RECNO", "N", 8, 0 ) 
	
	If !Eof() 
		nRecnoAA3 := ( cAliasQry )->AA3RECNO  
	EndIf  							

	( cAliasQry )->( dbCloseArea() ) 
	
	If nRecnoAA3 > 0
		DbSelectArea( "AA3" ) 	
		AA3->( DbGoTo( nRecnoAA3 ) )
		If At300Eqpto( { "AB2_CODPRO", AA3->AA3_CODPRO } ) 
			GDFieldPut( "AB2_CODPRO", AA3->AA3_CODPRO ) 
			GDFieldPut( "AB2_NUMSER", AA3->AA3_NUMSER ) 
			oGetDUS:oBrowse:Refresh() 	
		EndIf
	EndIf
EndIf	
	
Return		
	
/*/{Protheus.doc} At300FldOk
Pré-valid da alteração
@author Mateus Boiani
@since 21/05/2018
@version 1.0
/*/	
Function At300FldOk()
Local lRet := .T.
Local nPosRecn
Local nPosTipo
Local nRecno := 0

If TYPE("aHeader") == 'A' .AND. TYPE("aCols") == 'A' .AND. TYPE("n") == 'N'

	nPosRecn := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_REC_WT" })
	nPosTipo := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_TIPO" })
	
	nRecno := AB2->(RECNO())
	AB2->(DbGoTo(aCols[n][nPosRecn]))
	
	If aCols[n][nPosTipo] == '2' //Orçamento
		DbSelectArea("AB4")
		DbSetOrder(1)
		
		If !Empty(AB2->AB2_NUMORC) .AND. DbSeek(xFilial("AB4")+AB2->AB2_NUMORC)
			If ( AB4->AB4_TIPO <> "1" )			
				Help(" ",1,"AT300TPORC")
				lRet := .F.
			EndIf
		EndIf
	EndIf
	
	If aCols[n][nPosTipo] == '3' //O.S.
		DbSelectArea("AB7")
		DbSetOrder(1)
		If ( !Empty(AB2->AB2_NUMOS) .AND. DbSeek(xFilial("AB7")+AB2->AB2_NUMOS) )
	      If ( AB7->AB7_TIPO <> "1" )
				Help(" ",1,"AT300TIPOS") // 
				lRet := .F.
			EndIf
		EndIf
	EndIf
	
	DbSelectArea("AB2")
	AB2->(DbGoTo(nRecno))
EndIf

Return lRet

/*/{Protheus.doc} At300DelOk
Pré-valid na exclusão
@author Mateus Boiani
@since 21/05/2018
@version 1.0
/*/
Function At300DelOk()
Local lRet := .T.
Local nPosRecn
Local nPosTipo
Local nRecno := 0

If TYPE("aHeader") == 'A' .AND. TYPE("aCols") == 'A' .AND. TYPE("n") == 'N'

	nPosRecn := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_REC_WT" })
	nPosTipo := aScan(aHeader,{|x| AllTrim(x[2])=="AB2_TIPO" })
	
	nRecno := AB2->(RECNO())
	AB2->(DbGoTo(aCols[n][nPosRecn]))
	
	If aCols[n][nPosTipo] == '2' //Orçamento
		DbSelectArea("AB4")
		DbSetOrder(1)
		If !Empty(AB2->AB2_NUMORC) .AND. DbSeek(xFilial("AB4")+AB2->AB2_NUMORC)
			If ( AB4->AB4_TIPO <> "1" )
				lRet := .F.
			EndIf
		EndIf
	EndIf
	
	If aCols[n][nPosTipo] == '3' //O.S.
		DbSelectArea("AB7")
		DbSetOrder(1)
		If ( !Empty(AB2->AB2_NUMOS) .AND. DbSeek(xFilial("AB7")+AB2->AB2_NUMOS) )
			If ( AB7->AB7_TIPO <> "1" )
				lRet := .F.
			EndIf
		EndIf
	EndIf
	
	DbSelectArea("AB2")
	AB2->(DbGoTo(nRecno))
EndIf

Return lRet
