#INCLUDE "FATXJOB.ch"
#INCLUDE "XMLXFUN.CH"
#INCLUDE "Protheus.ch"
#INCLUDE "TBICONN.CH"
#DEFINE DIRXMLTMP "\MSXMLTMP\"  
#DEFINE LIMITE 80
#DEFINE ARQ_FATJOB "FATXJOB.CFG"  
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FATXJOB   ³ Autor ³ Eduardo Riera         ³ Data ³ 13.08.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Biblioteca dos jobs das APIs de Venda e Distribuicao         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³Materiais                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function JobFat()

Local aRotina   := {}
Local cAuxiliar := ""
Local cPath     := "\BIN\SERVER\"
Local nX        := 0
Local nY        := 0
Local nZ        := 0
Local nIntervalo:= 0
Local nJobs     := 0
Local nSleepJob := 0
Local cHoraIni  := ""
Local cHoraFim  := ""
Local cAtivo    := ""
Local lContinua := .T.

Private cSDoc 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os parametros da rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",LIMITE))
ConOut(PadC("STARTING JOBS - SALES AND DISTRIBUTION",LIMITE))
ConOut("")
If !File(cPath+ARQ_FATJOB)
	ConOut("WARNING: "+"File not found"+" : "+cPath+ARQ_FATJOB)
	lContinua := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verificando as Empresas e Rotinas Habilitadas                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua
	If !Empty(GetPvProfString("JOBS","FTJOBNFSPV","",ARQ_FATJOB))
		cAuxiliar := GetPvProfString("JOBS","FTJOBNFSPV","",ARQ_FATJOB)
		While !Empty(cAuxiliar)
			aadd(aRotina,{"FTJOBNFSPV",SubStr(cAuxiliar,1,2),SubStr(cAuxiliar,3,2),"01:00:00","23:59:59",0,"ON",Time()})
			nX := At(cAuxiliar,";")
			cAuxiliar := If(nX == 0, "", SubStr(cAuxiliar,nX+1))
		EndDo
	EndIf
	If !Empty(GetPvProfString("JOBS","FTJOBXML","",ARQ_FATJOB))
		cAuxiliar := GetPvProfString("JOBS","FTJOBXML","",ARQ_FATJOB)
		While !Empty(cAuxiliar)
			aadd(aRotina,{"FTJOBXML",SubStr(cAuxiliar,1,2),SubStr(cAuxiliar,3,2),"01:00:00","23:59:59",0,"ON",Time()})
			nX := At(cAuxiliar,";")
			cAuxiliar := If(nX == 0, "", SubStr(cAuxiliar,nX+1))
		EndDo
	EndIf
	If !Empty(GetPvProfString("JOBS","FTJOBNFSB2","",ARQ_FATJOB))
		cAuxiliar := GetPvProfString("JOBS","FTJOBNFSB2","",ARQ_FATJOB)
		While !Empty(cAuxiliar)
			aadd(aRotina,{"FTJOBNFSB2",SubStr(cAuxiliar,1,2),SubStr(cAuxiliar,3,2),"01:00:00","23:59:59",0,"ON",Time()})
			nX := At(cAuxiliar,";")
			cAuxiliar := If(nX == 0, "", SubStr(cAuxiliar,nX+1))
		EndDo		
	EndIf
	If !Empty(GetPvProfString("JOBS","FTJOBPVMAIL","",ARQ_FATJOB))
		cAuxiliar := GetPvProfString("JOBS","FTJOBPVMAIL","",ARQ_FATJOB)
		While !Empty(cAuxiliar)
			aadd(aRotina,{"FTJOBPVMAIL",SubStr(cAuxiliar,1,2),SubStr(cAuxiliar,3,2),"01:00:00","23:59:59",0,"ON",Time()})
			nX := At(cAuxiliar,";")
			cAuxiliar := If(nX == 0, "", SubStr(cAuxiliar,nX+1))
		EndDo
	EndIf
	If Empty(aRotina)
		ConOut("")		
		ConOut("WARNING: "+"Routine not found")
		ConOut("Section: JOBS ")
		ConOut("{Process Name}: {Company/Branch[;...]}")
		ConOut("Example: [JOBS])")
		ConOut("         FTJOBNFSPV=9901;9902;9903;...")
		ConOut("         FTJOBXML  =9901;9902;9903;...")
		ConOut("         FTJOBNFSB2=9901;9902;9903;...")
		ConOut("")
		ConOut(Repl("-",LIMITE))
		lContinua := .F.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verificando os Parametros de cada Rotina                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua
	For nX := 1 To Len(aRotina)
		cHoraIni   := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"START_TIME","01:00:00",ARQ_FATJOB)
		cHoraFim   := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"FINISH_TIME","23:59:59",ARQ_FATJOB)
		nIntervalo := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"INTERVAL","5",ARQ_FATJOB)
		cAtivo     := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"ACTIVATE","ON",ARQ_FATJOB)
		nJobs      := Val(GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"JOBS","1",ARQ_FATJOB))
		nSleepJob  := Max(Val(GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"SLEEPJOB","1",ARQ_FATJOB)),10)

		aRotina[nX][4] := cHoraIni
		aRotina[nX][5] := cHoraFim
		aRotina[nX][6] := nIntervalo
		aRotina[nX][7] := cAtivo
		aRotina[nX][8] := cHoraIni

		ConOut("Processing in: ")
		ConOut("          COMPANY    ="+aRotina[nX][2])
		ConOut("          BRANCH     ="+aRotina[nX][3])
		ConOut("")
		ConOut("["+aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3]+"]")
		ConOut("          START_TIME ="+aRotina[nX][4])
		ConOut("          FINISH_TIME="+aRotina[nX][5])
		ConOut("          INTERVAL   ="+aRotina[nX][6])
		ConOut("          ACTIVATE   ="+aRotina[nX][7])
		ConOut("          JOBS       ="+StrZero(nJobs,2))
		ConOut("          SLEEPJOB   ="+StrZero(nSleepJob,3))
		If aRotina[nX][1] == "FTJOBNFSPV"
			ConOut("          SERIES     ="+GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"SERIES","1  ",ARQ_FATJOB))
		EndIf
		ConOut("")						
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processamento das Rotinas                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While !KillApp()
		For nX := 1 To Len(aRotina)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se a Rotina deve ser executada                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cHoraIni   := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"START_TIME","01:00:00",ARQ_FATJOB)
			cHoraFim   := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"FINISH_TIME","23:59:59",ARQ_FATJOB)
			nIntervalo := Val(GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"INTERVAL","5",ARQ_FATJOB))
			cAtivo     := GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"ACTIVATE","OFF",ARQ_FATJOB)
			nJobs      := Val(GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"JOBS","1",ARQ_FATJOB))
			nSleepJob  := Max(Val(GetPvProfString(aRotina[nX][1]+"_"+aRotina[nX][2]+aRotina[nX][3],"SLEEPJOB","1",ARQ_FATJOB)),10)
			If cAtivo == "ON"
				For nY := 1 To nJobs
					StartJob(aRotina[nX][1],GetEnvServer(),.F.,aRotina[nX][2],aRotina[nX][3],nY)
					For nZ := 0 To nSleepJob
						Sleep(1000)
						If KillApp()
							Exit
						EndIf
					Next nZ
					If KillApp()
						Exit
					EndIf
				Next nY
			EndIf   	
		Next nX
		Sleep(1000)
	EndDo
EndIf
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobNfsPV³ Autor ³Eduardo Riera          ³ Data ³16.08.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de processamento dos documentos de saida para PV        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da Empresa                                    ³±±
±±³          ³ExpC2: Codigo da Filial                                     ³±±
±±³          ³ExpN3: Codigo do Job, utilizado no controle de execucao     ³±±
±±³          ³ExpL4: Mostra Lanc.Ctb.                                     ³±±
±±³          ³ExpL5: Aglut.Lancamentos                                    ³±±
±±³          ³ExpL6: Lct.Ctb.On-Line                                      ³±±
±±³          ³ExpL7: Lct.Ctb.Custo On-Line                                ³±±
±±³          ³ExpL8: Reajusta na mesma nota                               ³±±
±±³          ³ExpN9: Calc.Acr.Fin                                         ³±±
±±³          ³ExpNA: Arred.Prc.Unit                                       ³±±
±±³          ³ExpNB: Valor Minimo para faturamento                        ³±±
±±³          ³ExpNC: Atualiza Amarracao Cliente x Produto                 ³±±
±±³          ³ExpND: Cupom Fiscal                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobNfsPV(cCodEmp,cCodFil,nIDJob,lMostraCtb,lAglutCtb,lCtbOnLine,lCtbCusto,lReajusta,nCalAcrs,nArredPrcLis,nFatMin,lAtuSA7,lECF)

Local cAliasSC9 := "SC9"
Local lExecuta  := .F.
Local cArqcLk   := Nil
Local cHoraUlt  := "01:00:00"
Local cHora     := cHoraUlt
Local cHoraIni  := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"START_TIME","01:00:00",ARQ_FATJOB)
Local cHoraFim  := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
Local nIntervalo:= Val(GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
Local cAtivo    := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"ACTIVATE","OFF",ARQ_FATJOB)
Local nJobs     := Val(GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
Local cSerie    := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"SERIES","1  ",ARQ_FATJOB)
Local cQrySC9   := ""

DEFAULT nIDJob := 0 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Preparando o ambiente para execucao                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",LIMITE))
ConOut("FTJOBNFSPV("+StrZero(nIDJob,2)+"): "+"Starting environment")
ConOut(Repl("-",LIMITE))
RpcSetType ( 3 )
PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "FAT"

lLockByFil:= !Empty(cCodFil)
cArqcLk   := "FTJOBNFSPV"+cCodEmp+cCodFil+StrZero(nIDJob,2)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle de execucao. Nao permite que o mesmo JOB seja inicializado mais³
//³de uma vez                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LockByName(cArqcLk,.T.,lLockByFil)
	
	cSDoc := SerieNFID("SD2", 3, "D2_SERIE")

	PRIVATE MVNOTAFIS := "NF "
	Pergunte("MT460A",.F.)

	DEFAULT lMostraCtb  := MV_PAR01==1
	DEFAULT lAglutCtb   := MV_PAR02==1
	DEFAULT lCtbOnLine  := MV_PAR03==1
	DEFAULT lCtbCusto   := MV_PAR04==1
	DEFAULT lReajusta   := MV_PAR05==1
	DEFAULT nCalAcrs    := MV_PAR07
	DEFAULT nArredPrcLis:= MV_PAR08
	DEFAULT nFatMin     := MV_PAR12
	DEFAULT lAtuSA7     := MV_PAR15==1
	DEFAULT lECF        := MV_PAR16==2

	While cAtivo == "ON" .And. nJobs >= nIDJob .And. !Killapp()
		lExecuta := .F.
		cAtivo     := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"ACTIVATE","OFF",ARQ_FATJOB)
		If cHoraIni > cHoraFim
			If !(Time() >= cHoraFim .And. Time() <= cHoraIni)
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		Else
			If Time() >= cHoraIni .And. Time() <= cHoraFim
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		EndIf
		If lExecuta
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os Pedidos que possuem itens liberados                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			ConOut('FTJOBNFSPV('+StrZero(nIDJob,2)+'): '+'Searching Sales Order... ('+Time()+' - '+DtoC(Date())+')')

			cAliasSC9 := "FtJobNfsPV"
			
			cQrySC9 := "SELECT C9_FILIAL,C9_PEDIDO,SC5.R_E_C_N_O_ SC5RECNO "
			cQrySC9 += "FROM "+RetSqlName("SC9")+" SC9, "
			cQrySC9 += RetSqlName("SC5")+" SC5 "
			cQrySC9 += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"'"
			cQrySC9 += " AND SC9.C9_BLCRED='"+Space(Len(SC9->C9_BLCRED))+"'"
			cQrySC9 += " AND SC9.C9_BLEST='"+Space(Len(SC9->C9_BLEST))+"'"
			cQrySC9 += " AND SC9.C9_BLWMS IN('  ','05','06','07') "
			cQrySC9 += " AND SC9.D_E_L_E_T_=' ' "
			cQrySC9 += " AND SC5.C5_FILIAL='"+xFilial("SC5")+"' "
			cQrySC9 += " AND SC5.C5_NUM=SC9.C9_PEDIDO "	
			cQrySC9 += " AND SC5.D_E_L_E_T_=' ' "
			cQrySC9 += " GROUP BY C9_FILIAL,C9_PEDIDO,SC5.R_E_C_N_O_ "
			cQrySC9 += " ORDER BY C9_FILIAL,C9_PEDIDO "

			cQrySC9 := ChangeQuery(cQrySC9)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQrySC9),cAliasSC9)

			While !Eof() .And. (cAliasSC9)->C9_FILIAL == xFilial("SC9") .And. !KillApp()
				
				SC5->(MsGoto((cAliasSC9)->SC5RECNO))
				If SC5->(SimpleLock())
					//Bruno Cremaschi - Projeto chave única.
					FtJobNFs("SC9",PadR(cSerie,Len(SD2->&cSDoc)),lMostraCtb,lAglutCtb,lCtbOnLine,lCtbCusto,lReajusta,nCalAcrs,nArredPrcLis,nFatMin,lAtuSA7,lECF,(cAliasSC9)->C9_PEDIDO,nIdJob)
				EndIf
				
				ConOut('FTJOBNFSPV('+StrZero(nIDJob,2)+'): '+'Searched Sales Order... ('+Time()+' - '+DtoC(Date())+') -> '+SC5->C5_NUM)
				dbSelectArea(cAliasSC9)
				dbSkip()
			EndDo
			
			dbSelectArea(cAliasSC9)
			dbCloseArea()
			dbSelectArea("SC9")
			
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verificando o ambiente novamente para assumir novos parametros          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cHoraIni   := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"START_TIME","01:00:00",ARQ_FATJOB)
		cHoraFim   := GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
		nIntervalo := Val(GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
		nJobs      := Val(GetPvProfString("FTJOBNFSPV_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
		Sleep(1000)
		SC5->(MsRUnlock())		
	EndDo
	UnLockByName(cArqcLk,.T.,lLockByFil)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Finalisando o ambiente para execucao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",LIMITE))
ConOut("FTJOBNFSPV("+StrZero(nIDJob,2)+"): "+"Environment reseted")
ConOut(Repl("-",LIMITE))	
RESET ENVIRONMENT

Return(.T.)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobNfs  ³ Autor ³Eduardo Riera          ³ Data ³16.08.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de processamento dos documentos de saida                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias da MarkBrowse                                  ³±±
±±³          ³ExpC2: Serie da Nota Fiscal a ser considerada               ³±±
±±³          ³ExpL3: Mostra Lanc.Ctb.                                     ³±±
±±³          ³ExpL4: Aglut.Lancamentos                                    ³±±
±±³          ³ExpL5: Lct.Ctb.On-Line                                      ³±±
±±³          ³ExpL6: Lct.Ctb.Custo On-Line                                ³±±
±±³          ³ExpL7: Reajusta na mesma nota                               ³±±
±±³          ³ExpN8: Calc.Acr.Fin                                         ³±±
±±³          ³ExpN9: Arred.Prc.Unit                                       ³±±
±±³          ³ExpNA: Valor Minimo para faturamento                        ³±±
±±³          ³ExpNB: Atualiza Amarracao Cliente x Produto                 ³±±
±±³          ³ExpNC: Cupom Fiscal                                         ³±±
±±³          ³ExpCD: Numero do documento ( Pedido/Carga )                 ³±±
±±³          ³ExpNE: Numero do ID do Job                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobNFs(cTabela,cSerie,lMostraCtb,lAglutCtb,lCtbOnLine,lCtbCusto,lReajusta,nCalAcrs,nArredPrcLis,nFatMin,lAtuSA7,lECF,cDocumento,nIdJob,cTypeDoc)

Local aArea     := GetArea()
Local aAreaSC9  := SC9->(GetArea())
Local aQuebra   := {}
Local aQuebra2  := {}
Local aPvlNfs   := {}
Local cKeySC9   := "C9_FILIAL+"
Local cAuxKey   := ""
Local cQrySc9   := ""
Local cCursor1  := IIf(cTabela==Nil,"SC9",cTabela)
Local cCursor2  := "SC9"
Local cCursor3  := "SC9"
Local cFldQry   := ""
Local cPedido   := ""
Local lQuebra   := .F.
Local lContinua := .T.
Local lConfirma := .T.
Local lQuery    := .F.
Local lAcima    := .F.
Local lFilDAK   := cTabela == "DAK" .And. OsVlEntCom()<>1
Local nPrcVen   := 0
Local nItemNf   := a460NumIt(cSerie)
Local nX        := 0
Local nTotal    := 0
Local bWhile1   := {||.T.}
Local bWhile2   := {||.T.}
Local bWhile3   := {||.T.}

Default cTypeDoc := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o Fechamento do Estoque                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( MVUlmes() >= dDataBase )
	lContinua := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o Fechamento do Fiscal                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !FisChkDt(dDataBase) )
	lContinua := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem das querys necessarias a fim de otimizar o programa            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

TcSrvMap("SB2")
SB2->(MsGoto(RecNo()))
TcSrvMap("SB6")
SB6->(MsGoto(RecNo()))
TcSrvMap("SC9")
SC9->(MsGoto(RecNo()))
TcSrvMap("SF4")
SF4->(MsGoto(RecNo()))
TcSrvMap("SED")
SED->(MsGoto(RecNo()))
TCInternal(5,"*OFF")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa as variaveis de otimizacao da rotina de documento de saida   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MaNfsInit()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processamento do Job                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContinua
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Analise dos itens do pedido de venda aptos a faturar                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case
	Case cTabela == "SC9"
		bWhile1 := {|| !Eof() .And. xFilial("SC9") == (cCursor1)->C9_FILIAL .And. (cCursor1)->C9_PEDIDO == cDocumento}
		bWhile2 := {|| !Eof() .And. xFilial("SC9") == (cCursor2)->C9_FILIAL .And. (cCursor1)->C9_PEDIDO == cDocumento}
		bWhile3 := {|| !Eof() .And. xFilial("SC9") == (cCursor3)->C9_FILIAL .And. (cCursor1)->C9_PEDIDO == cDocumento}
	Case cTabela == "DAK"
		cFilBrw += ".AND.DAK_FEZNF<>'1' "
		cQryBrw += " AND DAK_FEZNF<>'1' "
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica a necessidade do DAI                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lFilDAK )
			cCursor2:= "DAI"
			cQryBrw += "AND DAI.DAI_FILIAL='"+xFilial("DAI")+"' "
			cQryBrw += "AND DAI.DAI_COD=DAK.DAK_COD "
			cQryBrw += "AND DAI.DAI_SEQCAR=DAK.DAK_SEQCAR "
			cQryBrw += "AND DAI.D_E_L_E_T_ = ' ' "
			cQryBrw += "AND DAI.DAI_PEDIDO = SC9.C9_PEDIDO "
			bWhile1 := {|| !Eof() .And. (cCursor1)->DAK_FILIAL == xFilial("DAK") .AND. (cCursor1)->DAK_CARGA == cDocumento }
			bWhile2 := {|| !Eof() .And. (cCursor2)->DAI_FILIAL == xFilial("DAI") .AND. (cCursor2)->DAI_COD==(cCursor1)->DAK_COD .And. (cCursor2)->DAI_SEQCAR==(cCursor1)->DAK_SEQCAR}
			bWhile3 := {|| !Eof() .And. xFilial("SC9") == (cCursor3)->C9_FILIAL .AND. (cCursor3)->C9_CARGA==(cCursor1)->DAK_COD .And. (cCursor3)->C9_SEQCAR==(cCursor1)->DAK_SEQCAR}
			If lFilDAK
				cFldQry := ",DAI.DAI_FILIAL,DAI.DAI_COD,DAI.DAI_SEQCAR,DAI.DAI_FILPV,DAK.DAK_COD,DAK.DAK_SEQCAR,DAK.R_E_C_N_O_ DAKRECNO "
			Else
				cFldQry := ",DAI.DAI_FILIAL,DAI.DAI_COD,DAI.DAI_SEQCAR,DAK.DAK_COD,DAK.DAK_SEQCAR,DAK.R_E_C_N_O_ DAKRECNO "			
			EndIf
		Else
			bWhile1 := {|| !Eof() .And. (cCursor1)->DAK_FILIAL == xFilial("DAK") .AND. (cCursor1)->DAK_CARGA == cDocumento}
			bWhile2 := {|| !Eof() .And. xFilial("SC9") == (cCursor3)->C9_FILIAL .AND. (cCursor3)->C9_CARGA==(cCursor1)->DAK_COD .AND. (cCursor3)->C9_SEQCAR==(cCursor1)->DAK_SEQCAR }
			bWhile3 := {|| !Eof() .And. xFilial("SC9") == (cCursor3)->C9_FILIAL .AND. (cCursor3)->C9_CARGA==(cCursor1)->DAK_COD .AND. (cCursor3)->C9_SEQCAR==(cCursor1)->DAK_SEQCAR}
			cFldQry := ",DAK.DAK_COD,DAK.DAK_SEQCAR,DAK.R_E_C_N_O_ DAKRECNO "
		EndIf
	EndCase
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica qual a Ordem em que a NFS deve ser gerada                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lFilDAK
		cKeySC9 += "C9_CARGA+C9_SEQCAR+"
		cKeySC9 += "C9_SEQENT+C9_AGREG+"
	Else
		cKeySC9 += "C9_CARGA+C9_SEQCAR+C9_SEQENT+"
		cKeySC9 += "C9_AGREG+"
	EndIf
	cKeySC9 += "C9_PEDIDO+"
	If ( SuperGetMv("MV_LIBGRUPO")=="S" )
		cKeySC9 += "C9_GRUPO+"
	EndIf
	dbSelectArea("SC9")
	dbSetOrder(1)
	cKeySC9 += IndexKey()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retira os itens duplicados da ordem de Geracao da NFS                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAuxKey := cKeySC9
	cKeySC9 := ""
	While ( (nX := At("+",cAuxKey))<>0 )
		If ( !SubStr(cAuxKey,1,nX-1)$cKeySC9 )
			cKeySC9 += "+"+SubStr(cAuxKey,1,nX-1)
		EndIf
		cAuxKey := SubStr(cAuxKey,nX+1)
	EndDo
	If ( !cAuxKey$cKeySC9 )
		cKeySC9 += "+"+cAuxKey
	EndIf
	cKeySC9 := SubStr(cKeySC9,2)
	If ExistBlock("M460ORD")
		cKeySC9 := ExecBlock("M460ORD",.F.,.F.,cKeySC9)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Realiza a Selecao do SC9 e da Tabela da Markbrowse se esta existir      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If TcSrvType()<>"AS/400"
		cQrySC9 := ""
		cCursor1:= "MA460PROC"
		cCursor2:= cCursor1
		cCursor3:= cCursor2
		lQuery := .T.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Montagem dos campos do SC9                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC9")
		For nX := 1 To FCount()
			cQrySC9 += ","+FieldName(nX)
		Next nX
		Do Case
		Case "ORACLE"==Upper(TcGetDb())
			cQrySC9 := "SELECT DISTINCT /*+FIRST_ROWS*/ "+SubStr(cQrySC9,2)
		Case "CACHE"==Upper(TcGetDb())
			cQrySC9 := "SELECT "+SubStr(cQrySC9,2)
		OtherWise
			cQrySC9 := "SELECT DISTINCT "+SubStr(cQrySC9,2)
		EndCase
		cQrySC9 += ",SC9.R_E_C_N_O_ C9RECNO "
		cQrySC9 += ",SC5.R_E_C_N_O_ C5RECNO "
		cQrySC9 += ",SC6.R_E_C_N_O_ C6RECNO, SC6.C6_QTDENT, SC6.C6_QTDVEN "
		cQrySC9 += ",SE4.R_E_C_N_O_ E4RECNO "
		cQrySC9 += ",SB1.R_E_C_N_O_ B1RECNO "
		cQrySC9 += ",SB2.R_E_C_N_O_ B2RECNO "
		cQrySC9 += ",SF4.R_E_C_N_O_ F4RECNO "
		cQrySC9 += ",SF4.F4_ISS F4ISS "
		cQrySC9 += ",SC5.C5_MOEDA "
		cQrySC9 += cFldQry
		cQrySC9 += ",SB2.B2_LOCAL "
		cQrySC9 += "FROM "+RetSqlName("SC9")+" SC9 ,"
		If ( cTabela == "DAK" )
			cQrySC9 += RetSqlName(cTabela)+" "+cTabela+","
			cQrySC9 += RetSqlName("DAI")+" DAI,"
		EndIf
		cQrySC9 += RetSqlName("SC5")+" SC5 ,"
		cQrySC9 += RetSqlName("SC6")+" SC6 ,"
		cQrySC9 += RetSqlName("SE4")+" SE4 ,"
		cQrySC9 += RetSqlName("SB1")+" SB1 ,"
		cQrySC9 += RetSqlName("SB2")+" SB2 ,"
		cQrySC9 += RetSqlName("SF4")+" SF4  "
		cQrySC9 += "WHERE "
		If cTabela <> "SC9"
			cQrySC9 += "DAK.DAK_FILIAL='"+xFilial("DAK")+"' "+cQryBrw
		Else
			cQrySC9 += "SC9.C9_FILIAL='"+xFilial("SC9")+"' "
		EndIf
		cQrySC9 += " AND SC9.C9_PEDIDO='"+cDocumento+"'"
		cQrySC9 += " AND SC9.C9_BLCRED='"+Space(Len(SC9->C9_BLCRED))+"'"
		cQrySC9 += " AND SC9.C9_BLEST='"+Space(Len(SC9->C9_BLEST))+"'"
		cQrySC9 += " AND SC9.C9_BLWMS IN('  ','05','06','07') "
		cQrySC9 += " AND SC9.D_E_L_E_T_=' ' "
		cQrySC9 += " AND SC5.C5_FILIAL="+IIF(lFilDAK,OsFilQry("SC5","DAI.DAI_FILPV"),"'"+xFilial("SC5")+"'")
		cQrySC9 += " AND SC5.C5_NUM=SC9.C9_PEDIDO"
		cQrySC9 += " AND SC5.D_E_L_E_T_=' '"
		cQrySC9 += " AND SC6.C6_FILIAL="+IIf(lFilDAK,OsFilQry("SC6","DAI.DAI_FILPV"),"'"+xFilial("SC6")+"'")
		cQrySC9 += " AND SC6.C6_NUM=SC9.C9_PEDIDO"
		cQrySC9 += " AND SC6.C6_ITEM=SC9.C9_ITEM"
		cQrySC9 += " AND SC6.C6_PRODUTO=SC9.C9_PRODUTO"
		cQrySC9 += " AND SC6.D_E_L_E_T_=' '"
		cQrySC9 += " AND SE4.E4_FILIAL="+IIf(lFilDAK,OsFilQry("SE4","DAI.DAI_FILPV"),"'"+xFilial("SE4")+"'")
		cQrySC9 += " AND SE4.E4_CODIGO=SC5.C5_CONDPAG "
		cQrySC9 += " AND SE4.D_E_L_E_T_=' '"
		cQrySC9 += " AND SB1.B1_FILIAL="+IIf(lFilDAK,OsFilQry("SB1","DAI.DAI_FILPV"),"'"+xFilial("SB1")+"'")
		cQrySC9 += " AND SB1.B1_COD=SC9.C9_PRODUTO"
		cQrySC9 += " AND SB1.D_E_L_E_T_=' '"
		cQrySC9 += " AND SB2.B2_FILIAL="+IIf(lFilDAK,OsFilQry("SB2","DAI.DAI_FILPV"),"'"+xFilial("SB2")+"'")
		cQrySC9 += " AND SB2.B2_COD=SC9.C9_PRODUTO"
		cQrySC9 += " AND SB2.B2_LOCAL=SC9.C9_LOCAL"
		cQrySC9 += " AND SB2.D_E_L_E_T_=' '"
		cQrySC9 += " AND SF4.F4_FILIAL="+IIf(lFilDAK,OsFilQry("SF4","DAI.DAI_FILPV"),"'"+xFilial("SF4")+"'")
		cQrySC9 += " AND SF4.F4_CODIGO=SC6.C6_TES"
		cQrySC9 += " AND SF4.D_E_L_E_T_=' '"
		If lFilDAK
			cKeySC9 := StrTran(cKeySC9,"C9_PEDIDO","DAI_FILPV,C9_PEDIDO")
		EndIf
		cQrySC9 += " ORDER BY "+SqlOrder(cKeySC9)

		cQrySC9 := ChangeQuery(cQrySC9)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQrySC9),cCursor3,.F.,.T.)
	Else
		dbSetOrder(1)		
		MsSeek(xFilial()+cDocumento)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica as condicoes de quebra de nota fiscal                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lFilDAK
		aadd(aQuebra2,{"DAI->DAI_FILPV",})
	EndIf
	aadd(aQuebra,{"SC9->C9_CARGA",})
	aadd(aQuebra,{"SC9->C9_SEQCAR",})
	aadd(aQuebra,{"SC9->C9_AGREG",})
	aadd(aQuebra,{"SC9->C9_PEDIDO",})
	SA1->(dbSetOrder(1))
	SA1->(MsSeek(xFilial("SA1")+(cCursor3)->(C9_CLIENTE+C9_LOJA)))

	If ( lQuery )
		aEval(aQuebra,{|x| x[1]:= SubStr(x[1],6)})
		aEval(aQuebra2,{|x| x[1]:= SubStr(x[1],6)})
	EndIf	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processa o Arquivo do Browse                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cCursor1)
	While Eval(bWhile1)
		Do Case
			Case cCursor2 == "DAI" .And. cTabela == "DAK"
				dbSelectArea(cCursor2)
				dbSetOrder(1)
				MsSeek(xFilial("DAI")+(cCursor1)->DAK_COD+(cCursor1)->DAK_SEQCAR)
			Case cCursor2 == "SC9" .And. cTabela == "DAK"
				If !lQuery
					dbSelectArea(cCursor3)
					dbSetOrder(nIndSC9+1)
					MsSeek(xFilial("SC9")+(cCursor1)->DAK_COD+(cCursor1)->DAK_SEQCAR)
				EndIf
		EndCase
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Processa a tabela vinculada ao browse                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		dbSelectArea(cCursor2)
		While Eval(bWhile2)
			If cTabela == "DAK"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica a Filial nal qual deve ser gerada a Nota Fiscal de Saida       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lFilDAK
					If cFilAnt <> (cCursor2)->DAI_FILPV
						cFilAnt := (cCursor2)->DAI_FILPV
						MaNFSEnd()
						MaNFSInit()
					EndIf
				EndIf
				If !lQuery
					dbSelectArea(cCursor3)
					dbSetOrder(nIndSC9+1)
					MsSeek(xFilial("SC9")+(cCursor1)->DAK_COD+(cCursor1)->DAK_SEQCAR)
					If cCursor2 <> cCursor3
						(cCursor2)->(dbSkip())
					EndIf
				EndIf				
			EndIf
			dbSelectArea(cCursor3)
			While Eval(bWhile3)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Posiciona Registros                                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( !lQuery )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(xFilial("SB1")+SC9->C9_PRODUTO)

					dbSelectArea("SC5")
					dbSetOrder(1)
					MsSeek(xFilial("SC5")+SC9->C9_PEDIDO)

					dbSelectArea("SC6")
					dbSetOrder(1)
					MsSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO)

					dbSelectArea("SB2")
					dbSetOrder(1)
					MsSeek(xFilial("SB2")+SC6->C6_PRODUTO+SC9->C9_LOCAL)

					dbSelectArea("SF4")
					dbSetOrder(1)
					MsSeek(xFilial("SF4")+SC6->C6_TES)

					dbSelectArea("SE4")
					dbSetOrder(1)
					MsSeek(xFilial("SE4")+SC5->C5_CONDPAG)
				EndIf	
				dbSelectArea(cCursor3)	
				lConfirma := .T.
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza os itens de Quebra                                             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aEval(aQuebra,{|x| x[2] := &(x[1])})
				aEval(aQuebra2,{|x| x[2] := &(x[1])})
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Inicializa as variaveis de quebra do SC9                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
				If lQuery
					cPedido := (cCursor3)->C9_PEDIDO
				Else
					cPedido := SC9->C9_PEDIDO
					If !( SC9->C9_BLCRED==Space(Len(SC9->C9_BLCRED)) .And.;
							SC9->C9_BLWMS$"05/06/07/  " .And.;
							SC9->C9_BLEST==Space(Len(SC9->C9_BLEST)) )
						lConfirma:= .F.
					EndIf
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Efetua a selecao dos registros                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( lConfirma )
					nPrcVen := C9_PRCVEN
					If ( !lQuery )
						dbSelectArea("SC5")
					EndIf
					If ( C5_MOEDA <> 1 )
						nPrcVen := A410Arred(xMoeda(nPrcVen,C5_MOEDA,1,dDataBase,8),"D2_PRCVEN")
					EndIf
					If ( !lQuery )
						dbSelectArea("SC9")
					EndIf
					If nPrcVen <> 0
						If Ma461QIsOk(aPvlNfs,IIf(!lQuery,"SC6",cCursor3),IIf(!lQuery,"SC9",cCursor3))
							aadd(aPvlNfs,{ C9_PEDIDO,;
								C9_ITEM,;
								C9_SEQUEN,;
								C9_QTDLIB,;
								nPrcVen,;
								C9_PRODUTO,;
								If(lQuery,F4ISS=="S",SF4->F4_ISS=="S"),;
								If(lQuery,C9RECNO,SC9->(RecNo())),;
								If(lQuery,C5RECNO,SC5->(RecNo())),;
								If(lQuery,C6RECNO,SC6->(RecNo())),;
								If(lQuery,E4RECNO,SE4->(RecNo())),;
								If(lQuery,B1RECNO,SB1->(RecNo())),;
								If(lQuery,B2RECNO,SB2->(RecNo())),;
								If(lQuery,F4RECNO,SF4->(RecNo())),;
								If(lQuery,B2_LOCAL,SB2->B2_LOCAL),;
								If(cTabela<>"DAK",0,If(lQuery,DAKRECNO,DAK->(RecNo())))})
						Else
							lAcima := .T.
						EndIf
					Else
						lTxMoeda := .T.
					EndIf
				EndIf
				dbSelectArea(cCursor3)
				dbSkip()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Posiciona Registros                                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( !lQuery )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(xFilial("SB1")+SC9->C9_PRODUTO)

					dbSelectArea("SC5")
					dbSetOrder(1)
					MsSeek(xFilial("SC5")+SC9->C9_PEDIDO)

					dbSelectArea("SC6")
					dbSetOrder(1)
					MsSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO)

					dbSelectArea("SB2")
					dbSetOrder(1)
					MsSeek(xFilial("SB2")+SC6->C6_PRODUTO+SC9->C9_LOCAL)

					dbSelectArea("SF4")
					dbSetOrder(1)
					MsSeek(xFilial("SF4")+SC6->C6_TES)

					dbSelectArea("SE4")
					dbSetOrder(1)
					MsSeek(xFilial("SE4")+SC5->C5_CONDPAG)
				EndIf	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica a quebra                                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lQuebra := .F.
				If ( aScan(aQuebra,{|x| If(!Empty(x[2]),&(x[1])<>x[2],.F.) }) <> 0 )
					lQuebra := .T.
				ElseIf ( aScan(aQuebra2,{|x| If(!Empty(x[2]),&(x[1])<>x[2],.F.) }) <> 0 )
					lQuebra := .T.
				EndIf
				If ( ExistBlock("M460NITE") )
					nItemNf := ExecBlock("M460NITE",.F.,.F.,{Len(aPvlNfs),aPvlNfs})
				EndIf
				If ( Len(aPvlNfs) >= nItemNf )
					lQuebra := .T.
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Efetua a Geracao da Nfs                                                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea(cCursor3)
				If ( lQuebra .Or. ( !Eval(bWhile3) .And. !Eval(bWhile2) ) )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica a quebra por numero de itens de nota fiscal                    ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aEval(aPvlNfs,{|x| nTotal += NoRound(If(x[4]<>0,x[4],1)*x[5],2)})
					If ( nTotal > nFatMin .And. !Empty(aPvlNfs) )
						dbSelectArea("SC9")
						cNota := MaPvlNfs(aPvlNfs,cSerie,lMostraCtb,lAglutCtb,lCtbOnLine,lCtbCusto,lReajusta,nCalAcrs,nArredPrcLis,lAtuSA7,lECF,,,,,,,,cTypeDoc)						
						ConOut(Repl("-",LIMITE))
						ConOut("FtJobNFs("+StrZero(nIdJob,2)+"): "+"Document number -> "+cSerie+"/"+cNota)
						ConOut(Repl("-",LIMITE))
					EndIf
					nTotal  := 0
					aPvlNfs := {}
				EndIf
				dbSelectArea(cCursor3)
			EndDo
		EndDo
		If ( cCursor1 <> cCursor2 )
			dbSelectArea(cCursor1)
			dbSkip()
		EndIf
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Restaura a entrada da rotina                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lQuery )
		dbSelectArea(cCursor3)
		dbCloseArea()
		dbSelectArea("SC9")
	EndIf
EndIf
RestArea(aAreaSC9)
RestArea(aArea)
Return(Nil)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobXML  ³ Autor ³Eduardo Riera          ³ Data ³08.02.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de processamento dos documentos de XML                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da Empresa                                    ³±±
±±³          ³ExpC2: Codigo da Filial                                     ³±±
±±³          ³ExpN3: Codigo do Job, utilizado no controle de execucao     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobXml(cCodEmp,cCodFil,nIDJob)

Local aXml      := {}
Local bError    := Nil
Local lExecuta  := .F.
Local lXml      := .F.
Local lEmpresa  := .F.
Local cArqLck   := Nil
Local cHoraUlt  := "01:00:00"
Local cHora     := cHoraUlt
Local cErro     := ""
Local cAviso    := ""
Local cCompany  := ""
Local cBranch   := ""
Local cHoraIni  := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"START_TIME","01:00:00",ARQ_FATJOB)
Local cHoraFim  := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
Local cAtivo    := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"ACTIVATE","OFF",ARQ_FATJOB)
Local nIntervalo:= Val(GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
Local nJobs     := Val(GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
Local nX        := 0
Local nXml      := 0
Local nLenVar   := SetVarNameLen(255)
Local oXml
Local cLockXml  := ""
Local lLockByFil:= .T.

DEFAULT nIDJob := 0

//--- A variável lAutoCOVER é utilizada APENAS na automação POR FUNÇÃO na suite FATXJOB
lAutoCOVER	:= If(( Type("lAutoCOVER") == "U" .OR. Type("lAutoCOVER") <> "L" ), .F., lAutoCOVER)
cAtivo		:= If(lAutoCOVER, "ON", cAtivo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Preparando o ambiente para execucao                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",LIMITE))
ConOut("FTJOBXML("+StrZero(nIDJob,2)+"): "+"Starting environment")
ConOut(Repl("-",LIMITE))
RpcSetType ( 3 )
PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "FAT"

DEFAULT cCodEmp := cEmpAnt
DEFAULT cCodFil := cFilAnt

lLockByFil:= !Empty(cCodFil)
cArqLck   := "FTJOBXML"+cCodEmp+cCodFil+StrZero(nIDJob,2)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle de execucao. Nao permite que o mesmo JOB seja inicializado mais³
//³de uma vez                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LockByName(cArqLck,.T.,lLockByFil)

	While cAtivo == "ON" .And. nJobs >= nIDJob .And. !Killapp()
		lExecuta := .F.
		cAtivo     := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"ACTIVATE","OFF",ARQ_FATJOB)
		If cHoraIni > cHoraFim
			If !(Time() >= cHoraFim .And. Time() <= cHoraIni)
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		Else
			If Time() >= cHoraIni .And. Time() <= cHoraFim
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		EndIf
		If lExecuta
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se ha XML a processar                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aXml := Directory(DIRXMLTMP+"*.XML")
			For nX := 1 To Len(aXml)
				lEmpresa := .F.
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se ha XML a processar                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cLockXml	:= StrTran(DIRXMLTMP+aXml[nX][1],".XML",".LCK")				
				lXml		:= !( (nXml := FCreate(cLockXml,0)) < 0 )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se ha XML a processar                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lXml
					oXML := XmlParserFile(DIRXMLTMP+aXml[nX][1],"_",@cErro,@cAviso)
					If ( Empty(cErro) .And. Empty(cAviso) .And. oXml <> Nil)
						bError := ErrorBlock({|| lXml := .F.})
						Begin Sequence
							cCompany := oXml:_MSDoc:_DocCompany:TEXT
							cBranch  := oXml:_MSDoc:_DocBranch:TEXT
							cRotina  := oXml:_MSDoc:_GlobalDocumentFunctionCode:TEXT
						End Sequence
						ErrorBlock(bError)
					Else
						lXml := .F.
					EndIf
					lEmpresa := .T.
					If lXml
						If cCompany==cEmpAnt .And. cBranch == cFilAnt
							If cRotina == "MS_MATA410"
								lXML := FtJobPVXml(oXml,nIdJOB)
							EndIf
						Else
							lEmpresa := .F.
						EndIf
					Else
						ConOut("WARNING: XML NOT VALID")
					EndIf
				EndIf
				If lEmpresa
					If lXml
						FErase(DIRXMLTMP+aXml[nX][1])
					ElseIf oXml <> Nil
						FRename(DIRXMLTMP+aXml[nX][1],DIRXMLTMP+SubStr(aXml[nX][1],1,8)+".ERR")
					EndIf
				EndIf
				FClose(nXml)
				FErase(cLockXml)
				oXml := Nil
				DelClassIntF()
			Next nX
			ConOut('FTJOBXML('+StrZero(nIDJob,2)+'): '+'Searching XML... ('+Time()+' - '+DtoC(Date())+')')
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verificando o ambiente novamente para assumir novos parametros          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cHoraIni   := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"START_TIME","01:00:00",ARQ_FATJOB)
		cHoraFim   := GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
		nIntervalo := Val(GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
		nJobs      := Val(GetPvProfString("FTJOBXML_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
		Sleep(1000)
		If lAutoCOVER
			EXIT
		EndIf
	EndDo	
	UnLockByName(cArqLck,.T.,lLockByFil)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Finalisando o ambiente para execucao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",LIMITE))
ConOut("FTJOBXML("+StrZero(nIDJob,2)+"): "+"Environment reseted")
ConOut(Repl("-",LIMITE))
RESET ENVIRONMENT

SetVarNameLen(nLenVar)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobPVXML³ Autor ³Eduardo Riera          ³ Data ³08.02.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inclusao do Pedido de Venda XML                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto XML                                           ³±±
±±³          ³ExpN2: ID do JOB                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobPVXml(oXml,nIdJob)

Local aArea    := GetArea()
Local aHeadFor := {}
Local aColsFor := {}
Local aCab     := {}
Local aItens   := {}
Local aLinha   := {}
Local nX       := 0
Local nY       := 0
Local nZ       := 0
Local nUsado   := 0
Local nOpcao   := 0
Local cMode    := oXml:_MSDoc:_Message:_Order:_OrderMode:TEXT
Local cPedido  := oXml:_MSDoc:_Message:_Order:_OrderIdentifier:TEXT
Local xValor   := ""
Local lRetorno := .F.

PRIVATE aHeader := {}
PRIVATE aCols   := {}
PRIVATE INCLUI  := .F.
PRIVATE ALTERA  := .F.
PRIVATE lLiber  := .F.
PRIVATE lTransf := .F.
PRIVATE lMsErroAuto := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega perguntas do MATA440 e MATA410                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTA440",.F.)
lLiber := MV_PAR02 == 1
lTransf:= MV_PAR01 == 1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o Modo de gravacao                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do Case
	Case cMode == "DIRECTUPDATE"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Modo de Gravacao Direto - A410GRAVA                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		INCLUI := .T.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Montagem do aHeader                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SX3")
		dbSetOrder(1)
		MsSeek("SC6")
		While ( !Eof() .And. (SX3->X3_ARQUIVO == "SC6") )
			IF ( X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL ) .And.;
					(  !AllTrim(SX3->X3_CAMPO)=="C6_NUM"   .And.;
					AllTrim(SX3->X3_CAMPO)<>"C6_QTDEMP" .And.;
					AllTrim(SX3->X3_CAMPO)<>"C6_QTDENT" )
				nUsado++
				Aadd(aHeader,{ AllTrim(X3Titulo()),;
					SX3->X3_CAMPO  ,;
					SX3->X3_PICTURE,;
					SX3->X3_TAMANHO,;
					SX3->X3_DECIMAL,;
					SX3->X3_VALID  ,;
					SX3->X3_USADO  ,;
					SX3->X3_TIPO   ,;
					SX3->X3_ARQUIVO,;
					SX3->X3_CONTEXT } )
			EndIf
			dbSelectArea("SX3")
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Inicializa as variaveis de Memoria                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC5")
		RegToMemory( "SC5", .T.,.F.,.F.)
		While __lSx8
			RollBackSX8()
		EndDo
		For nX := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData)
			dbSelectArea("SX3")
			dbSetOrder(2)
			MsSeek(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT))
			Do Case
				Case SX3->X3_TIPO == "D"
					M->&(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT)) := CTOD(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderFieldData:TEXT)
				Case SX3->X3_TIPO == "N"
					M->&(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT)) := Val(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderFieldData:TEXT)
				OtherWise
					M->&(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT)) := oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderFieldData:TEXT
			EndCase
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Montagem do aCols                                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		If ValType(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem) == "A"
			For nX := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem)
				aadd(aCols,Array(nUsado+1))
				For nY := 1 To nUsado
					aCols[nX][nY] := CriaVar(aHeader[nY][2],.F.)
				Next nY
				aCols[nX][nUsado+1] := .F.
				For nY := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData)
					nZ := aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderField:TEXT)})
					If nZ <> 0
						Do Case
							Case aHeader[nZ][8] == "D"
								aCols[nX][nZ] := Ctod(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderFieldData:TEXT)
							Case aHeader[nZ][8] == "N"
								aCols[nX][nZ] := Val(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderFieldData:TEXT)
							OtherWise
								aCols[nX][nZ] := oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderFieldData:TEXT
						EndCase
					EndIf
				Next nY
			Next nX
		Else
			aadd(aCols,Array(nUsado+1))
			nX := 1
			For nY := 1 To nUsado
				aCols[nX][nY] := CriaVar(aHeader[nY][2],.F.)
			Next nY
			aCols[nX][nUsado+1] := .F.
			For nY := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem:_OrderData)
				nZ := aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem:_OrderData[nY]:_OrderField:TEXT)})
				If nZ <> 0
					Do Case
						Case aHeader[nZ][8] == "D"
							aCols[nX][nZ] := Ctod(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem:_OrderData[nY]:_OrderFieldData:TEXT)
						Case aHeader[nZ][8] == "N"
							aCols[nX][nZ] := Val(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem:_OrderData[nY]:_OrderFieldData:TEXT)
						OtherWise
							aCols[nX][nZ] := oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem:_OrderData[nY]:_OrderFieldData:TEXT
					EndCase
				EndIf
			Next nY	
		EndIf
		Ma410MtFor(@aHeadFor,@aColsFor)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o Pedido ja foi gravado                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC5")
		dbSetOrder(1)
		If !MsSeek(xFilial("SC5")+M->C5_NUM)
			lRetorno := A410Grava(lLiber,lTransf,4,aHeadFor,aColsFor)
		Else
			lRetorno := .F.
		EndIf
	OtherWise
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Modo de Gravacao com Validacao                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Do Case
			Case cMode == "INSERT"
				nOpcao := 3
			Case cMode == "UPDATE"
				nOpcao := 4
			Case cMode == "DELETE"
				nOpcao := 5
		EndCase
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Montagem do Cabecalho                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC5")
		For nX := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData)
			xValor := oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderFieldData:TEXT
			dbSelectArea("SX3")
			dbSetOrder(2)
			MsSeek(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT))
			Do Case
				Case SX3->X3_TIPO == "D"
					xValor := CTOD(xValor)
				Case SX3->X3_TIPO == "N"
					xValor := Val(xValor)
			EndCase
			aadd(aCab,{AllTrim(oXml:_MSDoc:_Message:_Order:_OrderDataCollection:_OrderData[nX]:_OrderField:TEXT),xValor,Nil})
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Montagem dos Itens                                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		For nX := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem)
			aLinha := {}
			For nY := 1 To Len(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData)
				xValor := oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderFieldData:TEXT
				dbSelectArea("SX3")
				dbSetOrder(2)
				MsSeek(AllTrim(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderField:TEXT))			
				Do Case
					Case SX3->X3_TIPO == "D"
						xValor := CTOD(xValor)
					Case SX3->X3_TIPO == "N"
						xValor := Val(xValor)
				EndCase
				aadd(aLinha,{AllTrim(oXml:_MSDoc:_Message:_Order:_OrderProductCollection:_OrderProductItem[nX]:_OrderData[nY]:_OrderField:TEXT),;
					xValor,;
					Nil})
			Next nY
			aadd(aItens,aLinha)
		Next nX
		Mata410(aCab,aItens,nOpcao)
		If !lMsErroAuto
			lRetorno := .T.
		EndIf
EndCase
If lRetorno .AND. ExistBlock("FTJOBPV")
	ExecBlock("FTJOBPV",.F.,.F.,{cPedido})
EndIf
ConOut(Repl("-",LIMITE))
ConOut("FtJobPV("+StrZero(nIdJob,2)+"): "+"Document number -> "+cPedido)
ConOut(Repl("-",LIMITE))
RestArea(aArea)
Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobNfsB2³ Autor ³Eduardo Riera          ³ Data ³07.07.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de processamento dos documentos de saida quando a atuali³±±
±±³          ³zacao do estoque é tardia.                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da Empresa                                    ³±±
±±³          ³ExpC2: Codigo da Filial                                     ³±±
±±³          ³ExpN3: Codigo do Job, utilizado no controle de execucao     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobNfsB2(cCodEmp,cCodFil,nIDJob)

Local cAliasSC9 := "SC9"
Local lExecuta  := .F.
Local cArqLck   := Nil
Local cHoraUlt  := "00:00:01"
Local cHora     := cHoraUlt
Local cHoraIni  := ""
Local cHoraFim  := ""
Local nIntervalo:= 0
Local cAtivo    := "ON"
Local nJobs     := 1
Local aPedido   := {}
Local aNota     := {}
Local lSchedule := ValType(cCodEmp) == 'U'    
Local cQrySC9   := ""
Local lLockByFil:= .T.

Local nX        := 0

DEFAULT nIDJob := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//Quando chamado por Schedule, o ambiente já está preparado com os parâmetros que estão   ³
//cadastradas na tabela SXD, por isso os parâmetros (cCodEmp,cCodFil,nIDJob) vem como Nil.³
//No caso de JOB os mesmos vem preenchidos conforme os parâmetros configurados no mesmo.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lSchedule
	ConOut(Repl("-",LIMITE))
	ConOut("FTJOBNFSB2("+StrZero(nIDJob,2)+"): "+"Starting environment")
	ConOut(Repl("-",LIMITE))
	RpcSetType ( 3 )
	PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "FAT"
Else
	ConOut("FTJOBNFSB2("+StrZero(nIDJob,2)+")")
EndIf

DEFAULT cCodEmp := cEmpAnt
DEFAULT cCodFil := cFilAnt

lLockByFil:= !Empty(cCodFil)
cArqLck   := "FTJOBNFSB2"+cCodEmp+cCodFil+StrZero(nIDJob,2)
cHoraIni  := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"START_TIME","00:00:01",ARQ_FATJOB)
cHoraFim  := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
nIntervalo:= Val(GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
cAtivo    := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"ACTIVATE","ON",ARQ_FATJOB)
nJobs     := Val(GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))

//--- A variável lAutoCOVER é utilizada APENAS na automação POR FUNÇÃO na suite FATXJOB
lAutoCOVER := If(( Type("lAutoCOVER") == "U" .OR. Type("lAutoCOVER") <> "L" ), .F., lAutoCOVER)
cAtivo := If(lAutoCOVER, "ON", cAtivo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle de execucao. Nao permite que o mesmo JOB seja inicializado mais³
//³de uma vez                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄlLockByFilÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LockByName(cArqLck,.T.,lLockByFil)
	While (cAtivo == "ON" .And. nJobs >= nIDJob .And. !Killapp())
		lExecuta := .F.
		cAtivo     := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"ACTIVATE","ON",ARQ_FATJOB)
		If cHoraIni > cHoraFim
			If !(Time() >= cHoraFim .And. Time() <= cHoraIni)
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		Else
			If Time() >= cHoraIni .And. Time() <= cHoraFim
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		EndIf
		If lExecuta
			GetMV("MV_NFS_JOB",.F.)
			If SX6->(SimpleLock())
				PutMv("MV_NFS_JOB",.T.)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os Pedidos que possuem itens liberados                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !lSchedule
				ConOut('FTJOBNFSB2('+StrZero(nIDJob,2)+'): '+'Searching Sales Invoices... ('+Time()+' - '+DtoC(Date())+')')
			EndIf

			cAliasSC9 := "FtJobNfsB2"

			cQrySC9 := "SELECT C9_FILIAL,C9_NUMSEQ,R_E_C_N_O_ SC9RECNO "
			cQrySC9 += "FROM "+RetSqlName("SC9")+" SC9 "
			cQrySC9 += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"'"
			cQrySC9 += " AND SC9.C9_BLCRED='ZZ'"
			cQrySC9 += " AND SC9.C9_BLEST='ZZ'"
			cQrySC9 += " AND SC9.D_E_L_E_T_=' ' "
			cQrySC9 += " ORDER BY C9_FILIAL,C9_NUMSEQ "

			cQrySC9 := ChangeQuery(cQrySC9)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQrySC9),cAliasSC9)

			aPedido := {}
			aNota   := {}

			If ( cAliasSC9 )->( !Eof() )
				MaNfsInit()
			EndIf
			While !Eof() .And. (cAliasSC9)->C9_FILIAL == xFilial("SC9") .And. !KillApp()
				SC9->(MsGoto((cAliasSC9)->SC9RECNO))
				If SC9->(SimpleLock()) .And. SC9->C9_BLEST=="ZZ"
					If aScan(aPedido,SC9->C9_PEDIDO)==0
						aadd(aPedido,SC9->C9_PEDIDO)
					EndIf
					If aScan(aNota,SC9->C9_SERIENF+SC9->C9_NFISCAL)==0
						aadd(aNota,SC9->C9_SERIENF+SC9->C9_NFISCAL)
					EndIf
					
					Begin Transaction
						dbSelectArea("SD2")
						dbSetOrder(1)
						If MsSeek(xFilial("SD2")+SC9->C9_PRODUTO+SC9->C9_LOCAL+SC9->C9_NUMSEQ)
							RecLock("SD2")
							RecLock("SC9")
							SC9->C9_BLCRED := "10"
							SC9->C9_BLEST  := "10"
							MsUnLock()
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Posiciona Registros                                                     ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							dbSelectArea("SF4")
							If xFilial("SF4")+SD2->D2_TES<>SF4->F4_FILIAL+SF4->F4_CODIGO
								dbSelectArea("SF4")
								dbSetOrder(1)
								MsSeek(xFilial("SF4")+SD2->D2_TES)
							EndIf
							MaAvalSD2(1,"SD2",/*lAtuSA7*/,0/*nItem*/,.T.)
						EndIf
					End Transaction
				EndIf
				SC9->(MsRUnlock())
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verificando a troca do dia                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Time() >= cHoraIni
					dDataBase := MsDate()
				EndIf		
				dbSelectArea(cAliasSC9)
				dbSkip()
			EndDo

			If SuperGetMV("MV_DEL_PVL")<>3
				For nX := 1 To Len(aPedido)
					cQuery := "DELETE FROM "+RetSqlName("SC9")+" "
					cQuery += "WHERE C9_FILIAL='"+xFilial("SC9")+"' AND "
					cQuery += "C9_PEDIDO='"+aPedido[nX]+"' AND "
					cQuery += If(SuperGetMV("MV_DEL_PVL")==1,;
					             "((C9_BLCRED = '10' AND C9_BLEST='10') OR D_E_L_E_T_='*' )",;
								 "D_E_L_E_T_='*' ")
					TcSqlExec(cQuery)
					cQuery := "DELETE FROM "+RetSqlName("SDC")+" "
					cQuery += "WHERE DC_FILIAL='"+xFilial("SDC")+"' AND "
					cQuery += "DC_ORIGEM='SC6' AND "
					cQuery += "DC_PEDIDO='"+aPedido[nX]+"' AND "
					cQuery += "D_E_L_E_T_='*' "
					TcSqlExec(cQuery)
				Next nX
			EndIf

			dbSelectArea(cAliasSC9)
			dbCloseArea()
			dbSelectArea("SC9")
			If !lSchedule
				ConOut('FTJOBNFSB2('+StrZero(nIDJob,2)+'): '+'Searched Sales Invoices... ('+Time()+' - '+DtoC(Date())+')')
			EndIf
			If ExistBlock("FTJNFSB2")
				ExecBlock("FTJNFSB2",.F.,.F.,{aPedido,aNota})
			EndIf
		EndIf
		If !lSchedule
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verificando o ambiente novamente para assumir novos parametros          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cHoraIni   := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"START_TIME","00:00:01",ARQ_FATJOB)
			cHoraFim   := GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
			nIntervalo := Val(GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
			nJobs      := Val(GetPvProfString("FTJOBNFSB2_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
		Else
			Exit
		EndIf
		Sleep(1000)
		If lAutoCOVER
			EXIT
		EndIf
	EndDo
	If !lSchedule
		GetMV("MV_NFS_JOB",.F.)
		If SX6->(SimpleLock())
			PutMv("MV_NFS_JOB",.F.)
		EndIf
	EndIf
	UnLockByName(cArqLck,.T.,lLockByFil)
	ConOut('FTJOBNFSB2 FIM')
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Finalisando o ambiente para execucao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lSchedule
	ConOut(Repl("-",LIMITE))
	ConOut("FTJOBNFSB2("+StrZero(nIDJob,2)+"): "+"Environment reseted")
	ConOut(Repl("-",LIMITE))
	RESET ENVIRONMENT
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtVldJobFt³ Autor ³Eduardo Riera          ³ Data ³11.07.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de validacao do Job de atualizacao do SD2. Utilizado ³±±
±±³          ³nas rotinas de atualizacao batch do estoque                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtVldJobFt()

Local aArea    := GetArea()
Local lRetorno := .F.

Local lQuery    := .T.
Local cAliasSC9 := "SC9"
Local cQuery    := ""

cAliasSC9 := "FtJobNfsB2"
lQuery    := .T.

cQuery := "SELECT count(*) SC9COUNT "
cQuery += "FROM "+RetSqlName("SC9")+" SC9 "
cQuery += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"'"
cQuery += " AND SC9.C9_BLCRED='ZZ'"
cQuery += " AND SC9.C9_BLEST='ZZ'"
cQuery += " AND SC9.D_E_L_E_T_=' ' "
cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9)
If !IsBlind()
	If (cAliasSC9)->SC9COUNT > 0
		Aviso( STR0001, STR0002, {"Ok"}) //"Atencao"###"O Job de estoque não encerrou as pendências, favor aguardar o Job"
	Else
		lRetorno := .T.
	EndIf
Else
	lRetorno := !(cAliasSC9)->SC9COUNT > 0
EndIf
dbCloseArea()
dbSelectArea("SC9")
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobPvCF ³ Autor ³Eduardo Riera          ³ Data ³08.02.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de calculo do fluxo de caixa para os Pedidos de Venda   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto XML                                           ³±±
±±³          ³ExpN2: ID do JOB                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobPvCF(cCodEmp,cCodFil,cNumPv)

Local lContinua 	:= .T.
Local lNoRunFlCx 	:= ExistBlock("FtNRunFlCx")
Local LExecFlCx 	:= .T.
Local cQuery    := ""

If lContinua .And. GetRpoRelease() <= "12.1.033" .And. Date() <= STOD('20220822')
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Preparando o ambiente para execucao                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RpcSetType ( 3 )
	PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "FAT"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisa o Pedido de Venda solicitado                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ConOut(Repl("-",LIMITE))
	ConOut("FTJOBPVCF("+cNumPV+"): "+"Cash flow for sales order")
	Begin Transaction
		dbSelectArea("AID")

		cQuery := "DELETE FROM "+RetSqlName("AID")+" WHERE AID_FILIAL='"+xFilial("AID")+"' AND AID_NUMPV='"+cNumPV+"' "
		TcSqlExec(cQuery)
		dbSelectArea("SC5")
		dbSetOrder(1)
		If lNoRunFlCx
			LExecFlCx := ExecBlock("FtNRunFlCx",.F.,.F.)
			If Valtype(LExecFlCx) <> "L"
				LExecFlCx := .T.
			EndIf
		EndIf
		If MsSeek(xFilial("SC5")+cNumPV) .And. LExecFlCx
			Ma410Fluxo(cNumPV)
		EndIf
	End Transaction
	ConOut(Repl("-",LIMITE))
	Conout(STR0025)//"A rotina de Fluxo de Caixa, será descontinuada em 22/08/2022. "   
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Finalisando o ambiente para execucao                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RESET ENVIRONMENT
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FtPvMail  ³ Autor ³Eduardo Perusso Riera  ³ Data ³08/03/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Rotina de envio de e-mail para o cliente                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³FtPvMail()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³ExpC1: Numero do Pedido de venda                            ³±±
±±³          ³ExpL2: Status do pedido de venda                            ³±±
±±³          ³ExpL3: Status do documento de saida                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA410                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtPVMail(cNumPed,lStatusPed,lStatusFat)

Local aArea     := GetArea()
Local aDestinos := {}
Local cVendedor := ""
Local cVend     := ""
Local cHead     := ""
Local cBody     := ""
Local cTail     := ""
Local cMailTo   := SuperGetMV("MV_MAILADT",.F.)
Local lCredito  := .T.
Local lEstoque  := .T.
Local cAliasSC6 := "SC6"
Local cClient   := ""
Local cAux      := ""
Local nX        := 0
Local nY        := 0
Local nTotal    := 0
Local aStruSC6  := {}
Local cQuery    := ""

DEFAULT lStatusPed := .F.
DEFAULT lStatusFat := .F.

//--- A variável lAutoCOVER é utilizada APENAS na automação POR FUNÇÃO na suite FATXJOB
lAutoCOVER := If(( Type("lAutoCOVER") == "U" .OR. Type("lAutoCOVER") <> "L" ), .F., lAutoCOVER)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Copia administrador do sistema                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cMailTo)
	aadd(aDestinos,{{"",cMailTo}})
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona o pedido de venda                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC5")
dbSetOrder(1)
If ( cNumPed <> SC5->C5_NUM  )
	MsSeek(xFilial("SC5")+cNumPed)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona condicao de pagamento                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SE4")
dbSetOrder(1)
MsSeek(xFilial("SE4")+SC5->C5_CONDPAG)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona nos vendedores                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cVend := "1"
For nX := 1 To Fa440CntVen()
	cVendedor := SC5->(FieldGet(FieldPos("C5_VEND"+cVend)))
	If !Empty(cVendedor)
		dbSelectArea("SA3")
		dbSetOrder(1)
		MsSeek(xFilial("SA3")+cVendedor)

		aadd(aDestinos,{{"",SA3->A3_EMAIL}})

	EndIf	
	cVend := Soma1(cVend,1)
Next nX
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no cliente                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  ( SC5->C5_TIPO $ "DB" )
	dbSelectArea("SA2")
	dbSetOrder(1)
	MsSeek(xFilial("SA2")+SC5->C5_CLIENTE+SC5->C5_LOJACLI)
Else
	dbSelectArea("SA1")
	dbSetOrder(1)
	MsSeek(xFilial("SA1")+SC5->C5_CLIENTE+SC5->C5_LOJACLI)
	If !Empty(SA1->A1_EMAIL)
		aadd(aDestinos,{{"",SA1->A1_EMAIL}})
	EndIf	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona no destinatário                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cCliEnt  := IIf(!Empty(SC5->C5_CLIENT),SC5->C5_CLIENT,SC5->C5_CLIENTE)
If  ( SC5->C5_TIPO $ "DB" )
	If ( SC5->C5_CLIENTE+SC5->C5_LOJACLI <> cCliEnt+SC5->C5_LOJAENT )
		dbSelectArea("SA2")
		dbSetOrder(1)
		MsSeek(xFilial("SA2")+cCliEnt+SC5->C5_LOJAENT)
	Else
		dbSelectArea("SA2")
		dbSetOrder(1)
		MsSeek(xFilial("SA2")+SC5->C5_CLIENTE+SC5->C5_LOJACLI)
	EndIf

Else
	If ( SC5->C5_CLIENTE+SC5->C5_LOJACLI <> cCliEnt+SC5->C5_LOJAENT )
		dbSelectArea("SA1")
		dbSetOrder(1)
		MsSeek(xFilial("SA1")+cCliEnt+SC5->C5_LOJAENT)
	Else
		dbSelectArea("SA1")
		dbSetOrder(1)
		MsSeek(xFilial("SA1")+SC5->C5_CLIENTE+SC5->C5_LOJACLI)
	EndIf	
EndIf
If !Empty(aDestinos)
	dbSelectArea("SC6")
	dbSetOrder(1)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cabecalho do e-mail                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cHead := '<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">'
	cHead += '<html><head>'
	cHead += '<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">'
	cHead += '<title></title></head>'
	cHead += '<body>'
	cHead += '<font face="verdana, tahoma, arial, helvetica">'
	cHead += '<table style="width: 614px; height: 168px;" border="0" cellpadding="0" cellspacing="0">'
	cHead += '<tbody><tr><td>'
	cHead += ' <h3 style="text-align: center;"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
	cHead += STR0011 //"***Esse é um e-mail automático. Não é necessário respondê-lo ***"
	cHead += '</font></h3></td></tr>'
	cHead += '<tr><td><h3><span style="text-decoration: underline;">'
	cHead += STR0012 //"Processamento de Pedido"
	cHead += '</span></h3>
	cHead += '<div style="text-align: justify;">'
	cHead += STR0013 //"Obrigado por escolher-nos!"
	cHead += '<br><br>'
	If !lStatusPed
		cHead += STR0014 //"O seu pedido foi registrado em nosso sistema sob o número "
		cHead += SC5->C5_NUM
		cHead += STR0015 //" e estamos em processo de liberação do mesmo."
	Else
		cHead += STR0016 //"Informamos abaixo o estágio atual do seu pedido de venda, registrado em nosso sistema sob número "
		cHead += SC5->C5_NUM
	EndIf
	cHead += '<br>'	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Itens do pedido de venda                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cAliasSC6 := "FtPvMail"
	aStruSC6  := SC6->(dbStruct())

	cQuery := "SELECT C6_FILIAL,C6_NUM,C6_ITEM,C6_PRODUTO,C6_DESCRI,C6_QTDVEN,C6_QTDEMP,C6_QTDENT,C6_VALOR,C6_ENTREG,C6_PEDCLI "
	cQuery += "FROM "+RetSqlName("SC6")+" SC6 "
	cQuery += "WHERE "
	cQuery += "SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
	cQuery += "SC6.C6_NUM='"+cNumPed+"' AND "
	cQuery += "SC6.D_E_L_E_T_=' ' "
	cQuery += "ORDER BY "+SqlOrder(SC6->(IndexKey()))

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6)

	For nX := 1 To Len(aStruSC6)
		If aStruSC6[nX][2] <> "C" .And. (cAliasSC6)->(FieldPos(aStruSC6[nX][1]))<>0
			TcSetField(cAliasSC6,aStruSC6[nX][1],aStruSC6[nX][2],aStruSC6[nX][3],aStruSC6[nX][4])
		EndIf
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Corpo do e-mail                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cBody := ''
	cBody += '<h3><small><span style="font-weight: bold; text-decoration: underline;">'
	cBody += STR0017 //"Resumo do seu pedido"
	cBody += '</span></small></h3>'
	cBody += '<table style="width: 616px; height: 166px;" border="0" cellpadding="0" cellspacing="5">'
	cBody += '<tbody><tr><td>'
	cBody += '<table border="0" cellpadding="0" cellspacing="1" width="100%">'
	cBody += '<tbody>'
	cBody += '<tr>'
	cBody += '<td style="text-align: left; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_PRODUTO")
	cBody += '</b></font></td>'
	cBody += '<td style="text-align: left; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_DESCRI")
	cBody += '</b></font></td>'
	cBody += '<td style="text-align: right; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_QTDVEN")
	cBody += '</b></font></td>'
	cBody += '<td style="text-align: right; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_VALOR")
	cBody += '</b></font></td>'
	cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_ENTREG")
	cBody += '</b></font></td>'	
	cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += RetTitle("C6_PEDCLI")
	cBody += '</b></font></td>'		
	If lStatusPed
		cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
		cBody += RetTitle("C6_QTDLIB")
		cBody += '</b></font></td>'
		cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
		cBody += RetTitle("C6_QTDENT")
		cBody += '</b></font></td>'
	EndIf
	If lStatusFat
		cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
		cBody += RetTitle("F2_SERIE")
		cBody += '</b></font></td>'
		cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
		cBody += RetTitle("F2_DOC")
		cBody += '</b></font></td>'
		cBody += '<td style="text-align: center; background-color: rgb(232, 231, 223);"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
		cBody += RetTitle("F2_EMISSAO")
		cBody += '</b></font></td>'	
	EndIf
	cBody += '</tr>'

	nX := 1	
	While ( !Eof() .And. xFilial("SC6") == (cAliasSC6)->C6_FILIAL .And.;
			(cAliasSC6)->C6_NUM == cNumPed )

		If Mod(nX,2)==0
			cAux := 'rgb(232, 231, 223);'
		Else
			cAux := 'rgb(252, 251, 243);'
		EndIf
		nX++

		cBody += '<tr>'
		cBody += '<td style="text-align: left; background-color: '+cAux+'"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += (cAliasSC6)->C6_PRODUTO
		cBody += '</font></td>'
		cBody += '<td style="text-align: left; background-color: '+cAux+'"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += (cAliasSC6)->C6_DESCRI
		cBody += '</font></td>'
		cBody += '<td style="text-align: right; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += TransForm((cAliasSC6)->C6_QTDVEN,PesqPict("SC6","C6_QTDVEN"))
		cBody += '</font></td>'
		cBody += '<td style="text-align: right; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += TransForm((cAliasSC6)->C6_VALOR,PesqPict("SC6","C6_VALOR"))
		cBody += '</font></td>'		
		cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += DTOC((cAliasSC6)->C6_ENTREG)
		cBody += '</font></td>'
		cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
		cBody += (cAliasSC6)->C6_PEDCLI
		cBody += '</font></td>'		
		If lStatusPed
			cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
			cBody += TransForm((cAliasSC6)->C6_QTDEMP,PesqPict("SC6","C6_QTDEMP"))
			cBody += '</font></td>'
			cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
			cBody += TransForm((cAliasSC6)->C6_QTDENT,PesqPict("SC6","C6_QTDENT"))
			cBody += '</font></td>'
		EndIf
		If lStatusFat
			nY := 1
			dbSelectArea("SD2")
			dbSetOrder(8)
			MsSeek(xFilial("SD2")+(cAliasSC6)->C6_NUM+(cAliasSC6)->C6_ITEM)
			While !Eof() .And. xFilial("SD2") == SD2->D2_FILIAL .And.;
				(cAliasSC6)->C6_NUM == SD2->D2_PEDIDO .And.;
				(cAliasSC6)->C6_ITEM == SD2->D2_ITEMPV
				If nY <> 1
					cBody += '</tr>'
					cBody += '<tr>'
					cBody += '<td style="text-align: left; background-color: '+cAux+'"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'
					cBody += '<td style="text-align: left; background-color: '+cAux+'"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'
					cBody += '<td style="text-align: right; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'
					cBody += '<td style="text-align: right; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'
					cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'
					cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
					cBody += "--"
					cBody += '</font></td>'		
					If lStatusPed
						cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
						cBody += "--"
						cBody += '</font></td>'
						cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
						cBody += "--"
						cBody += '</font></td>'
					EndIf
				EndIf
				nY ++
				cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
				//Bruno Cremaschi - Projeto chave única.
				cBody += SD2->&cSDoc
				cBody += '</font></td>'
				cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
				cBody += SD2->D2_DOC
				cBody += '</font></td>'
				cBody += '<td style="text-align: center; background-color: '+cAux+'""><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
				cBody += Dtoc(SD2->D2_EMISSAO)
				cBody += '</font></td>'				
				
				dbSelectArea("SD2")
				dbSkip()				
			EndDo
		EndIf		
		cBody += '</tr>'

		nTotal += (cAliasSC6)->C6_VALOR

		dbSelectArea(cAliasSC6)	
		dbSkip()
	EndDo
	
	dbSelectArea(cAliasSC6)	
	dbCloseArea()
	dbSelectArea("SC6")	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Liberacao do pedido de venda                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lStatusPed
		dbSelectArea("SC9")
		dbSetOrder(1)

		cAliasSC9 := "FtPvMail"
		aStruSC9  := SC9->(dbStruct())

		cQuery := "SELECT C9_FILIAL,C9_PEDIDO,C9_BLEST,C9_BLCRED,C9_DATALIB "
		cQuery += "FROM "+RetSqlName("SC9")+" SC9 "
		cQuery += "WHERE "
		cQuery += "SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
		cQuery += "SC9.C9_PEDIDO='"+cNumPed+"' AND "
		cQuery += "SC9.D_E_L_E_T_=' ' "
		cQuery += "ORDER BY "+SqlOrder(SC9->(IndexKey()))

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC9)

		For nX := 1 To Len(aStruSC9)
			If aStruSC9[nX][2] <> "C" .And. (cAliasSC9)->(FieldPos(aStruSC9[nX][1]))<>0
				TcSetField(cAliasSC9,aStruSC9[nX][1],aStruSC9[nX][2],aStruSC9[nX][3],aStruSC9[nX][4])
			EndIf
		Next nX
		While ( !Eof() .And. xFilial("SC9") == (cAliasSC9)->C9_FILIAL .And.;
				(cAliasSC9)->C9_PEDIDO == cNumPed )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica o status da liberacao do pedido de venda    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			If lCredito
				lCredito := Empty((cAliasSC9)->C9_BLCRED)
			EndIf
			If lEstoque
				lEstoque := Empty((cAliasSC9)->C9_BLEST)
			EndIf

			dbSelectArea(cAliasSC9)	
			dbSkip()
		EndDo
		
		dbSelectArea(cAliasSC9)	
		dbCloseArea()
		dbSelectArea("SC9")	
		
	EndIf	
	cBody += '<tr></tr><tr><td bgcolor="#e8e7df"></td>'
	cBody += '<td bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b><br></b></font></td>'
	cBody += '<td style="text-align: right;" bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += STR0018 //"Total do pedido: "
	cBody += '</b></font></td>'
	cBody += '<td style="text-align: right;" bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cBody += TransForm(nTotal,TM(nTotal,18,2))
	cBody += '</b></font></td>
	cBody += '<td bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b><br></b></font></td>'
	If lStatusPed
		cBody += '<td bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b><br></b></font></td>'
		cBody += '<td bgcolor="#e8e7df"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b><br></b></font></td>'
	EndIf
	cBody += '</tr></tbody></table></td></tr>'	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Encerramento do e-mail                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cTail := ''
	cTail += '<tr><td><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cTail += RetTitle("C5_CONDPAG")+": </b>"+SC5->C5_CONDPAG+" - "+SE4->E4_DESCRI
	cTail += '<br></font></td></tr>'
	cTail += '<tr><td><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1"><b>'
	cTail += STR0019+" :" //"Entrega"
	cTail += '</b></font></td></tr>
	cTail += '<tr><td><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">'
	If  !( SC5->C5_TIPO $ "DB" )
		If SuperGetMv("MV_ROTCEP",.F.,"1") == "1"
			cTail += SA1->A1_NOME+'<br>'
			cTail += SA1->A1_END+'<br>'
			cTail += SA1->A1_BAIRRO+'<br>'
			cTail += SA1->A1_MUN+'<br>'
			cTail += SA1->A1_EST+'<br>'
			cTail += SA1->A1_CEP+'<br>'
		Else
			cTail += SA1->A1_NOME+'<br>'		
			cTail += SA1->A1_ENDENT+'<br>'
			cTail += SA1->A1_BAIRROE+'<br>'
			cTail += SA1->A1_MUNE+'<br>'
			cTail += SA1->A1_ESTE+'<br>'
			cTail += SA1->A1_CEPE+'<br>'
		EndIf
	Else
		cTail += SA2->A2_NOME+'<br>'	
		cTail += SA2->A2_END+'<br>'
		cTail += SA2->A2_BAIRRO+'<br>'
		cTail += SA2->A2_MUN+'<br>'
		cTail += SA2->A2_EST+'<br>'
		cTail += SA2->A2_CEP+'<br>'
	EndIf
	cTail += '<br>'
	cTail += '</font></td></tr></tbody>'
	cTail += '</table>
	cTail += '<h3><small><span style="font-weight: bold; text-decoration: underline;">'
	cTail += STR0020+" :"//"Observações"
	cTail += '</span></small></h3>'
	cTail += '<table style="width: 618px; height: 109px;" border="0" cellpadding="0" cellspacing="5">'
	cTail += '<tbody><tr><td colspan="2"><font color="#000000" face="verdana, tahoma, arial, helvetica" size="1">&bull;'
	cTail += STR0021 //" A partir deste momento não será mais possível alterar os dados do pedido de venda."
	cTail += '<br><br>&bull;'
	cTail += STR0022 //" Se desejar mais informações sobre o pedido de venda entre em contato com seu representante comercial. "
	cTail += '<br><br><br><br><br></font></td></tr>'
	cTail += '<tr>
	cTail += '<td align="right" width="100%">'
	cTail += STR0023 //"Atenciosamente,"
	cTail += '<br><br>'
	cTail += STR0024 //"Departamento Comercial"
	cTail += '<br>'
	cTail += SM0->M0_NOME
	cTail += '<br></td></tr></tbody></table>'
	cTail += '</font>'
	cTail += '</body>'
	cTail += '</html>'	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Complemento do Cabecalho                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lStatusPed
		cHead += '<br>'
		cHead += '<br><b>'
		cHead += 'Status: '
		cHead += '<br><b>'
		Do Case
			Case Empty(SC5->C5_LIBEROK).And.Empty(SC5->C5_NOTA) .And. Empty(SC5->C5_BLQ)
				cHead += STR0003 //"Pedido de venda em carteira"
			Case !Empty(SC5->C5_NOTA).Or.SC5->C5_LIBEROK=='E' .And. Empty(SC5->C5_BLQ)
				cHead += STR0004 //"Pedido de venda encerrado"
			Case !Empty(SC5->C5_LIBEROK).And.Empty(SC5->C5_NOTA).And. Empty(SC5->C5_BLQ)
				cHead += STR0005 //"Pedido de venda totalmente liberado"
			Case !Empty(C5_BLQ)
				cHead += STR0006 //"Pedido de venda com restrição de comercialização"
		EndCase
		Do Case
			Case !lCredito
				cHead += '<br><b>'
				cHead += STR0007 //"Analise de Credito"
				cHead += '<br></b>'	
			Case !lEstoque
				cHead += '<br><b>'
				cHead += STR0008 //"Restrição de Estoque"
				cHead += '<br></b>'
			Otherwise
				cHead += '<br><b>'
				cHead += STR0009 //"Apto a faturar"
				cHead += '<br></b>'
		EndCase
		cHead += '<br>'
	EndIf
	cHead += '</div></td></tr></tbody></table>'

	For nX := 1 To Len(aDestinos)
		If !Empty(aDestinos[nX])
			MConnect(cHead+cBody+cTail,aDestinos[nX],STR0010,.T.,.F.) //"Acompanhamento de pedido de venda"
			If( !lAutoCOVER, MDisconnect(), NIL)
		EndIf		
	Next nX	
EndIf

RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³FtJobPvMai³ Autor ³Eduardo Riera          ³ Data ³07.07.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Job de processamento dos documentos de saida quando a atuali³±±
±±³          ³zacao do estoque é tardia.                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da Empresa                                    ³±±
±±³          ³ExpC2: Codigo da Filial                                     ³±±
±±³          ³ExpN3: Codigo do Job, utilizado no controle de execucao     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FtJobPvMail(cCodEmp,cCodFil,nIDJob)

Local cAlias    := ""
Local lExecuta  := .F.
Local cArqLck   := Nil
Local cHoraUlt  := "00:00:01"
Local cHora     := cHoraUlt
Local cHoraIni  := ""
Local cHoraFim  := ""
Local nIntervalo:= 0
Local cAtivo    := "ON"
Local nJobs     := 1
Local lSchedule := Empty(cCodEmp)
Local cQry      := ""
Local cUltPed   := ""
Local dUltNf    := Nil
Local cUltHoraNf:= ""
Local cPedido   := ""
Local lLockByFil:= .T.

DEFAULT nIDJob := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Preparando o ambiente para execucao                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lSchedule
	ConOut(Repl("-",LIMITE))
	ConOut("FTJOBPVMAIL("+StrZero(nIDJob,2)+"): "+"Starting environment")
	ConOut(Repl("-",LIMITE))
	RpcSetType ( 3 )
	PREPARE ENVIRONMENT EMPRESA cCodEmp FILIAL cCodFil MODULO "FAT"
Else
	ConOut("FTJOBPVMAIL("+StrZero(nIDJob,2)+")")
EndIf

DEFAULT cCodEmp := cEmpAnt
DEFAULT cCodFil := cFilAnt

lLockByFil	:= !Empty(cCodFil)
cArqLck		:= "FTJOBPVMAIL"+cCodEmp+cCodFil+StrZero(nIDJob,2)
cHoraIni	:= GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"START_TIME","00:00:01",ARQ_FATJOB)
cHoraFim	:= GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
nIntervalo	:= Val(GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
cAtivo		:= GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"ACTIVATE","ON",ARQ_FATJOB)
nJobs		:= Val(GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))

//--- A variável lAutoCOVER é utilizada APENAS na automação POR FUNÇÃO na suite FATXJOB
lAutoCOVER	:= If(( Type("lAutoCOVER") == "U" .OR. Type("lAutoCOVER") <> "L" ), .F., lAutoCOVER)
cAtivo := If(lAutoCOVER, "ON", cAtivo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Controle de execucao. Nao permite que o mesmo JOB seja inicializado mais³
//³de uma vez                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If LockByName(cArqLck,.T.,lLockByFil)
	
	cSDoc := SerieNFID("SD2", 3, "D2_SERIE")
	
	While (cAtivo == "ON" .And. nJobs >= nIDJob .And. !Killapp())
		lExecuta := .F.
		cAtivo     := GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"ACTIVATE","ON",ARQ_FATJOB)
		If cHoraIni > cHoraFim
			If !(Time() >= cHoraFim .And. Time() <= cHoraIni)
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		Else
			If Time() >= cHoraIni .And. Time() <= cHoraFim
				cHora := cHoraUlt
				SomaDiaHor(Date(),@cHora,nIntervalo/60)
				If Time() >= cHora .Or. nIntervalo == 0
					cHoraUlt := Time()
					lExecuta := .T.
				EndIf
			Else
				cAtivo := "OFF"
			EndIf
		EndIf
		If lExecuta
			If !lSchedule
				ConOut('FTJOBPVMAIL('+StrZero(nIDJob,2)+'): '+'Searching Sales Order... ('+Time()+' - '+DtoC(Date())+')')
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os Pedidos que foram incluido no dia                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC5")
			dbSetOrder(1)
			
			cUltPed := GetMv("MV_PV_MAIL",.F.,"")

			cAlias    := "FTJOBPVMAIL"
			
			cQry := "SELECT C5_FILIAL,C5_NUM "
			cQry += "FROM "+RetSqlName("SC5")+" SC5 "
			cQry += "WHERE SC5.C5_FILIAL='"+xFilial("SC5")+"' AND "
			cQry += "SC5.C5_NUM > '"+cUltPed+"' AND "
			cQry += "SC5.D_E_L_E_T_ = ' ' "

			If ExistBlock("JOBSC5MAIL")
				cQry := ExecBlock("JOBSC5MAIL",.F.,.F.,{cQry,1})
			EndIf

			cQry += "ORDER BY C5_FILIAL,C5_NUM "
			cQry := ChangeQuery(cQry)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),cAlias)

			While (cAlias)->(!Eof()) .And. (cAlias)->C5_FILIAL == xFilial("SC5") .And. !KillApp()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Envia o e-mail                                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (cAlias)->C5_NUM > cUltPed
					FtPvMail((cAlias)->C5_NUM,.F.)
					If SX6->(SimpleLock())
						PutMv("MV_PV_MAIL",(cAlias)->C5_NUM)
					EndIf
					If !lSchedule
						ConOut('FTJOBPVMAIL('+StrZero(nIDJob,2)+'): '+'Searched Sales ... ('+Time()+' - '+DtoC(Date())+') -> '+SC5->C5_NUM)
					EndIf
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verificando a troca do dia                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lAutoCOVER .AND. Time() >= cHoraIni
					dDataBase := MsDate()
				EndIf
				dbSelectArea(cAlias)
				dbSkip()
			EndDo

			
			dbSelectArea(cAlias)
			dbCloseArea()
			dbSelectArea("SC5")
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os Pedidos que foram liberados no dia                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC9")
			dbSetOrder(1)

			cAlias    := "FTJOBPVMAIL"
			
			cQry := "SELECT DISTINCT C9_FILIAL,C9_PEDIDO "
			cQry += "FROM "+RetSqlName("SC9")+" SC9 "
			cQry += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
			cQry += "SC9.C9_DATALIB = '"+Dtos(dDataBase)+"' AND "
			cQry += "SC9.D_E_L_E_T_ = ' ' "
			If ExistBlock("JOBSC9MAIL")
				cQry := ExecBlock("JOBSC9MAIL",.F.,.F.,{cQry,1})
			EndIf

			cQry := ChangeQuery(cQry)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),cAlias)

			cPedido := ""
			While (cAlias)->(!Eof()) .And. (cAlias)->C9_FILIAL == xFilial("SC9") .And. !KillApp()	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Envia o e-mail                                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cPedido <> (cAlias)->C9_PEDIDO
					FtPvMail((cAlias)->C9_PEDIDO,.T.,.F.)
					If !lSchedule
						ConOut('FTJOBPVMAIL('+StrZero(nIDJob,2)+'): '+'Searched Set Free Order... ('+Time()+' - '+DtoC(Date())+') -> '+(cAlias)->C9_PEDIDO)
					EndIf
				EndIf
				cPedido := (cAlias)->C9_PEDIDO
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verificando a troca do dia                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lAutoCOVER .AND. Time() >= cHoraIni
					dDataBase := MsDate()
				EndIf
				dbSelectArea(cAlias)
				dbSkip()
			EndDo			
			
			dbSelectArea(cAlias)
			dbCloseArea()
			dbSelectArea("SC9")
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os Pedidos que foram faturados no dia                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SD2")
			dbSetOrder(1)
			cPedido    := ""
			dUltNf     := GetMv("MV_DNFMAIL",.F.,cTod(""))//- Ultima data da NF
			cUltHoraNf := GetMv("MV_HNFMAIL",.F.,"")//- Ultima hora da NF
			
			If dUltNF < dDataBase  //- Faz acerto quando mudar a database 
				dUltNf     := dDataBase
				cUltHoraNf := '00:00'
			EndIf  

			cAlias    := "FTJOBPVMAIL"
						
			//Bruno Cremaschi
			cQry := "SELECT DISTINCT D2_FILIAL,D2_PEDIDO,D2_DOC,D2_SERIE, F2_HORA "
			cQry += if( !(cSDoc == "D2_SERIE"), ", " + cSDoc, "")
			cQry += "FROM "+RetSqlName("SD2")+" SD2, "
			cQry += RetSqlName("SF2")+" SF2 "
			cQry += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
			cQry += "SD2.D2_EMISSAO = '"+Dtos(dUltNf)+"' AND "
			cQry += "SD2.D_E_L_E_T_ = ' ' AND "
			cQry += "SF2.F2_FILIAL = '"+xFilial("SF2")+"' AND "
			cQry += "SF2.F2_DOC = SD2.D2_DOC AND "
			cQry += "SF2.F2_SERIE = SD2.D2_SERIE AND "
			cQry += "SF2.F2_CLIENTE = D2_CLIENTE AND "
			cQry += "SF2.F2_LOJA = D2_LOJA AND "
			cQry += "SF2.F2_HORA > '"+cUltHoraNf+"' AND "
			cQry += "SF2.D_E_L_E_T_ = ' ' "
			
			If ExistBlock("JOBSD2MAIL")
				cQry := ExecBlock("JOBSD2MAIL",.F.,.F.,{cQry,1})
			EndIf
			cQry += "ORDER BY D2_FILIAL,F2_HORA,D2_PEDIDO,D2_DOC,D2_SERIE"
			cQry := ChangeQuery(cQry)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),cAlias)
			While (cAlias)->(!Eof()) .And. (cAlias)->D2_FILIAL == xFilial("SD2") .And. !KillApp()	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Envia o e-mail                                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SF2")
				dbSetOrder(1)
				
				SF2->(MsSeek(xFilial("SF2")+(cAlias)->D2_DOC+(cAlias)->D2_SERIE))
				If SF2->F2_EMISSAO >= dDataBase .And. SF2->F2_HORA > cUltHoraNf
					cUltHoraNf := SF2->F2_HORA
					While (cAlias)->(!Eof()) .And. (cAlias)->D2_FILIAL == xFilial("SD2") .And. !KillApp() .and. (cAlias)->F2_HORA == cUltHoraNf
						If cPedido <> (cAlias)->D2_PEDIDO
							SF2->(MsSeek(xFilial("SF2")+(cAlias)->D2_DOC+(cAlias)->D2_SERIE))
							If !lSchedule
								//Bruno Cremaschi - Projeto chave única.
								ConOut('FTJOBPVMAIL('+StrZero(nIDJob,2)+'): '+'Sent Sales Order... ('+Time()+;
								' - '+DtoC(Date())+') -> '+(cAlias)->&cSDoc+'-'+(cAlias)->D2_DOC)
							EndIf
							FtPvMail((cAlias)->D2_PEDIDO,.T.,.T.)
							PutMv("MV_DNFMAIL",SF2->F2_EMISSAO)
							PutMv("MV_HNFMAIL",SF2->F2_HORA)
						EndIf
						cPedido := (cAlias)->D2_PEDIDO
						(cAlias)->(dbSkip())
					EndDo 
				Else 
					If !lSchedule
						//Bruno Cremaschi - Projeto chave única.
						ConOut('FTJOBPVMAIL('+StrZero(nIDJob,2)+'): '+'Do not sent the Sales Order... ('+Time()+;
						' - '+DtoC(Date())+') -> '+(cAlias)->&cSDoc+'-'+(cAlias)->D2_DOC)
					EndIf
					cUltHoraNf := SF2->F2_HORA
					dbSelectArea(cAlias)
					dbSkip()
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verificando a troca do dia                                              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lAutoCOVER .AND. Time() >= cHoraIni
					dDataBase := MsDate()
				EndIf
			EndDo
			
			dbSelectArea(cAlias)
			dbCloseArea()
			dbSelectArea("SD2")
		EndIf		
		If !lSchedule
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verificando o ambiente novamente para assumir novos parametros          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cHoraIni   := GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"START_TIME","00:00:01",ARQ_FATJOB)
			cHoraFim   := GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"FINISH_TIME","23:59:59",ARQ_FATJOB)
			nIntervalo := Val(GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"INTERVAL","5",ARQ_FATJOB))
			nJobs      := Val(GetPvProfString("FTJOBPVMAIL_"+cCodEmp+cCodFil,"JOBS","1",ARQ_FATJOB))
		EndIf
		Sleep(1000)
		If lAutoCOVER
			EXIT
		EndIf
	EndDo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Finalisando o ambiente para execucao                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	UnLockByName(cArqLck,.T.,lLockByFil)
EndIf
If !lSchedule
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Finalisando o ambiente para execucao                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RESET ENVIRONMENT		
EndIf
Return(.T.)
