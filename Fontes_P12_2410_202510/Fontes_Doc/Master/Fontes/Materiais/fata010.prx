#INCLUDE "FATA010.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "PROTHEUS.CH"
#DEFINE NMAX 300 
 
PUBLISH MODEL REST NAME FATA010 source FATA010

/*/


Ŀ
Program   FATA010    Autor Vendas & CRM            Data 20.01.2012  
Ĵ
Descrio Cadastro de Processo de Vendas.                               
Ĵ
Retorno   Nenhum                                                        
Ĵ
ParametrosNenhum                                                        
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
ٱ


/*/
Function Fata010()

Local oTableAtt := TableAttDef()

PRIVATE cCadastro := OemToAnsi(STR0001) //"Processo de Venda"
PRIVATE aRotina	  := MenuDef()


DEFINE FWMBROWSE oMBrowse ALIAS "AC1" DESCRIPTION STR0001 //"Processo de Venda"		
		oMBrowse:SetAttach( .T. ) 				  //Habilita as vises do Browse
		oMBrowse:SetViewsDefault( oTableAtt:aViews )
		//Se no for SIGACRM inibe a exibio do grfico
		If nModulo <> 73
			oMBrowse:SetOpenChart( .F. )
		EndIf 
		
		oMBrowse:SetTotalDefault('AC1_FILIAL','COUNT',STR0020) // "Total de Registros"
ACTIVATE FWMBROWSE oMBrowse

Return(.T.)


//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as vises e grficos.

@sample	TableAttDef()

@param		Nenhum

@return	ExpO - Objetos com as Visoes e Grficos.

@author	Cristiane Nishizaka
@since		28/04/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt 	:= FWTableAtt():New()
Local oAtivos		:= Nil // Processos de Venda Ativos
Local oInativos		:= Nil // Processos de Venda Inativos

oTableAtt:SetAlias("AC1")

// Processos de Venda Ativos
oAtivos := FWDSView():New()
oAtivos:SetName(STR0022) // "Processos de Venda Ativos"
oAtivos:SetID("Ativos") 
oAtivos:SetOrder(1) // AC1_FILIAL+AC1_PROVEN
oAtivos:SetCollumns({"AC1_PROVEN","AC1_DESCRI","AC1_DTOTAL","AC1_HTOTAL"})
oAtivos:SetPublic( .T. )
oAtivos:AddFilter(STR0022, "AC1_MSBLQL == '2'") // "Processos de Venda Ativos"

oTableAtt:AddView(oAtivos)

// Processos de Venda Inativos
oInativos := FWDSView():New()
oInativos:SetName(STR0021) // "Processos de Venda Inativos"
oInativos:SetID("Inativos") 
oInativos:SetOrder(1) // AC1_FILIAL+AC1_PROVEN
oInativos:SetCollumns({"AC1_PROVEN","AC1_DESCRI","AC1_DTOTAL","AC1_HTOTAL"})
oInativos:SetPublic( .T. )
oInativos:AddFilter(STR0021, "AC1_MSBLQL == '1'") // "Processos de Venda Inativos"

oTableAtt:AddView(oInativos)

Return (oTableAtt)

/*/


Ŀ
Programa  MenuDef    Autor  Vendas & CRM           Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          	  1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
     
Local nSize	:= 0

PRIVATE aRotina := {}


ADD OPTION aRotina Title STR0002 Action 'PesqBrw'			 OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina Title STR0003 Action 'VIEWDEF.FATA010' OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina Title STR0004 Action 'VIEWDEF.FATA010' OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina Title STR0005 Action 'VIEWDEF.FATA010' OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina Title STR0006 Action 'VIEWDEF.FATA010' OPERATION 5 ACCESS 0 //"Excluir"    

If !Empty( Select( "AC9" ) ) 
	AAdd( aRotina, { STR0013,"MsDocument",0,7,0,NIL} ) //Conhecimento
EndIf 	

//Retira acoes de inclusao, alteracao e exclusao quando o MenuDef for acionado pelo Pipeline
If IsInCallStack("FATC020")
	nSize := Len(aRotina)
	aDel(aRotina,5)
	aDel(aRotina,4)
	aDel(aRotina,3)
	aSize(aRotina,nSize-3)
EndIf

If IsInCallStack("FATA010")    
	aRotina:= CRMXINCROT("AC1",aRotina)
EndIf

If ExistBlock("FT010MNU")
	ExecBlock("FT010MNU",.F.,.F.)
EndIf

Return(aRotina)

/*/


Ŀ
Funcao    ViewDef      Autor Vendas & CRM            Data  16/01/12 
Ĵ
Descrio  Definicao da View                                          	
Ĵ
Retorno    oView                                                       	
Ĵ
Parametros Nenhum														
ٱ


/*/
Static Function ViewDef()
Local oModel   := FWLoadModel( 'FATA010' )	// Cria um objeto de Modelo de dados baseado no ModelDef do fonte informado
Local oStruAC1 := FWFormStruct( 2, 'AC1' )	// Cria as estruturas a serem usadas na View
Local oStruAC2 := FWFormStruct( 2, 'AC2' ) // Cria as estruturas a serem usadas na View
Local oStruACZ := FWFormStruct( 2, 'ACZ' ) // Cria as estruturas a serem usadas na View
Local oView									// Interface de visualizao construda
Local aUsrMemo := If( ExistBlock( "FT010MEM" ), ExecBlock( "FT010MEM", .F.,.F. ), {} ) //Memos de Usurio
Local nLoop	   := 1 

                 
oStruAC2:SetProperty("AC2_STAGE",MVC_VIEW_CANCHANGE,.F.)

oStruAC1:RemoveField("AC1_CODMEM")
oStruAC2:RemoveField("AC2_CODMEM")  

If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "AC1"
			oStruAC1:RemoveField(aUsrMemo[ nLoop, 2 ])			
		ElseIf aUsrMemo[ nLoop, 1 ] == "AC2"
			oStruAC2:RemoveField(aUsrMemo[ nLoop, 2 ])
		EndIf
	Next nLoop
EndIf
 
oView := FWFormView():New()								// Cria o objeto de View
oView:SetContinuousForm()
oView:SetModel( oModel )									// Define qual Modelo de dados ser utilizado				
oView:AddField( 'VIEW_AC1', oStruAC1, 'AC1MASTER' )	// Adiciona no nosso View um controle do tipo formulrio (antiga Enchoice)
oView:AddGrid( 'VIEW_AC2' , oStruAC2, 'AC2DETAIL' )	// Adiciona no nosso View um controle do tipo Grid (antiga Getdados)
oView:AddGrid( 'VIEW_ACZ' , oStruACZ, 'ACZDETAIL' )	// Adiciona no nosso View um controle do tipo Grid (antiga Getdados)
oView:AddIncrementField( 'VIEW_AC2', 'AC2_STAGE' )		// Item Incremental do Grid
oView:AddIncrementField( 'VIEW_ACZ', 'ACZ_ITEM' )		// Item Incremental do Grid

If nModulo == 73
	oView:AddUserButton( STR0019, "",{ |oView| CRMA090( "AC1", AC1->( RECNO() ) ) },,,{MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE} )  //"Anotaes"
EndIf

oView:CreateHorizontalBox( 'SUPERIOR'  , 30 )
oView:CreateHorizontalBox( 'INFERIOR'  , 70 )
//Cria pastas
oView:CreateFolder('PASTAS','INFERIOR')

oView:AddSheet( 'PASTAS', 'ABA01', STR0014) // Estagios
oView:AddSheet( 'PASTAS', 'ABA02', STR0015) // Regras

// Cria um "box" horizontal para receber cada elemento da view			
oView:CreateHorizontalBox( 'ABAS1'  , 100,,, 'PASTAS', 'ABA01' )	//Estagios
oView:CreateHorizontalBox( 'ABAS2' ,  100,,, 'PASTAS', 'ABA02' )	//Regras

// Relaciona o identificador (ID) da View com o "box" para exibio
oView:SetOwnerView( 'VIEW_AC1', 'SUPERIOR' )		
oView:SetOwnerView( 'VIEW_AC2', 'ABAS1' )
oView:SetOwnerView( 'VIEW_ACZ', 'ABAS2' )

Return oView

/*/


Ŀ
Funcao    ModelDef     Autor Vendas & CRM            Data  16/01/12 
Ĵ
Descrio  Definicao do Model                                         	
Ĵ
Retorno    oModel                                                      	
Ĵ
Parametros Nenhum														
ٱ


/*/
Static Function ModelDef()
 
// Cria as estruturas a serem usadas no Modelo de Dados
Local oStruAC1 := FWFormStruct( 1, 'AC1' )
Local oStruAC2 := FWFormStruct( 1, 'AC2' )
Local oStruACZ := FWFormStruct( 1, 'ACZ' )
Local oModel // Modelo de dados construdo
Local aUsrMemo := If( ExistBlock( "FT010MEM" ), ExecBlock( "FT010MEM", .F.,.F. ), {} ) //Memos de Usurio 
Local aMemoAC1 := { { 'AC1_CODMEM' , 'AC1_MEMO' } }
Local aMemoAC2 := { { 'AC2_CODMEM' , 'AC2_MEMO' } }
Local nLoop	 := 1
Local bCond	 := {||.T.}						// Condicao para soma.
Local aAux		:= {}
Local bCommit := { |oModel| FT010Cmt( oModel ) }
                           

// Campo para converter a hora em inteiro para somar o total de horas do processo de vendas      
oStruAC2:AddField(	AllTrim(STR0016)	,;  	// [01] C Titulo do campo
					AllTrim(STR0016)	,;   	// [02] C ToolTip do campo
     				"AC2_HRINT" 		,;    	// [03] C identificador (ID) do Field
         			"N" 				,;    	// [04] C Tipo do campo
            		5 					,;    	// [05] N Tamanho do campo
              		2 					,;    	// [06] N Decimal do campo
                	Nil 				,;    	// [07] B Code-block de validao do campo
                 	Nil					,;     	// [08] B Code-block de validao When do campo
                  	Nil 				,;    	// [09] A Lista de valores permitido do campo
                   	Nil 				,;  	// [10] L Indica se o campo tem preenchimento obrigatrio
                    Nil				 	,;   	// [11] B Code-block de inicializacao do campo
                    Nil 				,;  	// [12] L Indica se trata de um campo chave
                    Nil 				,;     	// [13] L Indica se o campo pode receber valor em uma operao de update.
                    .T. )              			// [14] L Indica se o campo  virtual

//Ŀ
// Criacao da Trigger. 
//
oStruAC2:AddTrigger("AC2_HDURAC","AC2_HRINT",Nil,{|| oModel:GetModel('AC2DETAIL'):SetValue('AC2_HRINT',HoraToInt(FwFldGet('AC2_HDURAC'))),HoraToInt(FwFldGet('AC2_HDURAC'))}) 
oStruACZ:SetProperty("ACZ_STAGE", MODEL_FIELD_VALID, FWBuildFeature( STRUCT_FEATURE_VALID, "FT310VdStd()"))  	

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New( 'FATA010' , NIL, {|oModel| A010Pos(oModel)}, bCommit )

If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "AC1"
			AAdd( aMemoAC1, { aUsrMemo[ nLoop, 2 ], aUsrMemo[ nLoop, 3 ] } )			
		ElseIf aUsrMemo[ nLoop, 1 ] == "AC2"
			AAdd( aMemoAC2, { aUsrMemo[ nLoop, 2 ], aUsrMemo[ nLoop, 3 ] } )
		EndIf
	Next nLoop
EndIf

FWMemoVirtual( oStruAC1, aMemoAC1)

FWMemoVirtual( oStruAC2, aMemoAC2)

// Adiciona ao modelo um componente de formulrio
oModel:AddFields( 'AC1MASTER', /*cOwner*/, oStruAC1 )
// Adiciona ao modelo uma componente de grid
oModel:AddGrid( 'AC2DETAIL', 'AC1MASTER', oStruAC2, {|oMdlAC2,  nLine, cAction, cField, xValue, xOldValue| FT010PreAC2(oMdlAC2, nLine, cAction, cField, xValue, xOldValue)} )
// Adiciona ao modelo uma componente de grid
oModel:AddGrid( 'ACZDETAIL', 'AC1MASTER', oStruACZ , , {|oMdl| A010ACZOK(oModel,oMdl)})

//Linhas unicas dos grids
oModel:GetModel( 'AC2DETAIL' ):SetUniqueLine( { 'AC2_STAGE'} )
oModel:GetModel( 'ACZDETAIL' ):SetUniqueLine( { 'ACZ_OPER','ACZ_EVENTO'} )

//Deixa opcional adicionar itens ao ACZ
oModel:GetModel( 'ACZDETAIL'):SetOptional( .T. )
 
oModel:AddCalc("CALCPIPE","AC1MASTER","AC2DETAIL","AC2_DDURAC","AC2__DTOT","SUM",/*bCond*/,/*bInitValue*/,;
STR0017,/*bFormula*/,TamSX3("AC1_DTOTAL")[1],TamSX3("AC1_DTOTAL")[2])	// "Dias"

oModel:AddCalc("CALCPIPE","AC1MASTER","AC2DETAIL","AC2_HRINT","AC2__HTOT","SUM",/*bCond*/,/*bInitValue*/,;
STR0018,/*bFormula*/,5,2) // "Horas" 

oMdlCalc := oModel:GetModel("CALCPIPE")		
oMdlCalc:AddEvents("CALCPIPE","AC2__DTOT","AC2__HTOT",{|| Ft010TPipe(oModel) })			

// Faz relacionamento entre os componentes do model
oModel:SetRelation( 'AC2DETAIL', { { 'AC2_FILIAL', 'xFilial( "AC2" )' }, { 'AC2_PROVEN', 'AC1_PROVEN' } }, AC2->( IndexKey( 1 ) ) )
oModel:SetRelation( 'ACZDETAIL', { { 'ACZ_FILIAL', 'xFilial( "ACZ" )' }, { 'ACZ_PROVEN', 'AC1_PROVEN' } }, ACZ->( IndexKey( 1 ) ) )

// Adiciona a descrio do Modelo de Dados
oModel:SetDescription( STR0001 ) //"Processo de Venda"

oModel:SetActivate({|| Ft010HrInt(oModel) })

// Retorna o Modelo de dados
Return oModel

//-------------------------------------------------------------------
/*/{Protheus.doc} CRMA980
Valida o estgio cadastrado quando  informado na regra

@param		Nenhum

@return	lRet, logico	, Verdadeiro se o estagio existe.

@author 	Squad CRM / FAT
@version	12.1.17 / Superior
@since		09/09/2017 
/*/
//-------------------------------------------------------------------
Function FT310VdStd()

Local oModel	:= FwModelActive()
Local oMdlAC2 := oModel:GetModel("AC2DETAIL")
Local oMdlACZ	:= oModel:GetModel("ACZDETAIL")	
Local nLine	:= 0
Local lRet 	:= .T.

nLine := oMdlAC2:GetLine()
lRet  := oMdlAC2:SeekLine({{"AC2_STAGE",oMdlACZ:GetValue("ACZ_STAGE")}})
oMdlAC2:GoLine(nLine)

If !lRet
	Help(" ",1,"REGNOIS")
EndIf

Return( lRet )
 
/*/


Ŀ
Funcao    A010Pos    Autor Vendas & CRM            Data 24.01.2012
Ĵ
Descrio Funcao de validacao da linha Ok                             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosoModel                                                      
ٱ


/*/
Function A010Pos(oModel)

Local lRet 		:= .T.
Local oModelAC2 	:= oModel:GetModel("AC2DETAIL")
Local nLoop     	:= 0
Local nTotal    	:= 0

If oModel:getOperation() == MODEL_OPERATION_DELETE
	AD1->( dbSetOrder( 5 ) ) 
	If AD1->( dbSeek( xFilial( "AD1" ) + M->AC1_PROVEN ) ) 
		lRet := .F. 
		Help( " ", 1, "FT010OPOR" ) // Este processo nao pode ser excluido pois esta em uso por uma ou mais oportunidades 
	EndIf 
EndIf
If lRet .AND. (oModel:getOperation() == MODEL_OPERATION_INSERT .OR.;
				oModel:getOperation() == MODEL_OPERATION_UPDATE .OR.;
				oModel:getOperation() == MODEL_OPERATION_DELETE)

	For nLoop := 1 To oModelAC2:Length()
		oModelAC2:GoLine(nLoop)
		If !oModelAC2:IsDeleted()
			nTotal += FwFldGet("AC2_RELEVA")
		Else 
			//Ŀ
			// Permite excluir apenas se nao estiver em uso por oportunidade
			//
			AD1->( dbSetOrder( 5 ) )
			If AD1->( dbSeek( xFilial( "AD1" ) + M->AC1_PROVEN + FwFldGet("AC2_STAGE") ) )
				Help( " ", 1, "FT010ETPUS", , FwFldGet("AC2_STAGE") ) // A etapa nao pode ser excluida pois esta em uso por uma ou mais oportunidades
				lRet := .F.
				Exit
			EndIf		
		EndIf									
	Next nLoop
	
	If lRet
		If nTotal <> 100
			Help( " ", 1, "FT010PERC" ) // O total dos percentuais de contribuicao deve somar 100 %
			lRet := .F.
		EndIf
	EndIf	
			
EndIf
	
Return lRet

/*/


Ŀ
Funcao    A010AC2OK  Autor Vendas & CRM            Data 24.01.2012
Ĵ
Descrio Funcao de validacao da linha Ok                             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
ٱ


/*/
Function A010ACZOK(oModel,oMdl)

Local lRetorno		:= .T.
Local aSaveLines	:= FWSaveRows()
Local oModelAC2 	:= oModel:GetModel("AC2DETAIL")
Local nCntFor 		:= 0

If FwFldGet("ACZ_ACAO") == "2" .And. Empty(FwFldGet("ACZ_STAGE"))
	Help(" ",1,"FT010LOK03")
	lRetorno := .F.
EndIf
If FwFldGet("ACZ_ACAO") $ "13" .And. !Empty(FwFldGet("ACZ_STAGE"))
	Help(" ",1,"FT010LOK04")
	lRetorno := .F.
EndIf
If FwFldGet("ACZ_ACAO") == "2" .And. !Empty(FwFldGet("ACZ_STAGE"))
	For nCntFor := 1 To oModelAC2:Length()
		oModelAC2:GoLine(nCntFor)
		lRetorno := .F.
		If ( FwFldGet("AC2_STAGE") == FwFldGet("ACZ_STAGE") )
			lRetorno := .T.
			Exit
		EndIf
	Next nCntFor
	If !lRetorno
		Help(" ",1,"FT010LOK05")
	EndIf
EndIf

FwRestRows(aSaveLines)
Return(lRetorno)
          
//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft010TPipe
Calcula a durao do pipeline em dias e horas.
@sample 	Ft010TPipe(oModel) 

@param		ExpO1	Modelo de Dados.
	
@return	ExpL	Falso 

@author	Anderson Silva
@since		06/05/2013       
@version	P12   
/*/         
//------------------------------------------------------------------------------

Function Ft010TPipe(oModel)

Local oMdlAC1	 	:= oModel:GetModel("AC1MASTER")
Local oMdlCalc		:= oModel:GetModel("CALCPIPE")
Local nDias 		:= oMdlCalc:GetValue("AC2__DTOT")
Local nTotHrs		:= oMdlCalc:GetValue("AC2__HTOT")
Local nDiasAux		:= 0 


While nTotHrs >= 24
	nDiasAux += 1
	nTotHrs -= 24
End

If ( oMdlAC1:GetOperation() ==  MODEL_OPERATION_INSERT ) .OR. ( oMdlAC1:GetOperation() == MODEL_OPERATION_UPDATE )
	oMdlAC1:SetValue("AC1_DTOTAL",nDias+nDiasAux)
	oMdlAC1:SetValue("AC1_HTOTAL",IntToHora(nTotHrs))
EndIf

Return(.F.) 

//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft010HrInt
Converte as horas do grid AC2 em inteiro antes de abrir o formulario 
para calcular o total de horas do pipeline.

@sample 	Ft010HrInt(oModel) 

@param		ExpO1	Modelo de Dados.
	
@return	ExpL	Verdadeiro 

@author	Anderson Silva
@since		06/05/2013       
@version	P12   
/*/         
//------------------------------------------------------------------------------
Function Ft010HrInt(oModel)

Local oMdlAC2 := oModel:GetModel("AC2DETAIL")
Local nLinha	:= oMdlAC2:GetLine()
Local nX		:= 0

If ( oMdlAC2:GetOperation() ==  MODEL_OPERATION_INSERT ) .OR. ( oMdlAC2:GetOperation() == MODEL_OPERATION_UPDATE )   
	For nX := 1 To oMdlAC2:Length() 
		oMdlAC2:GoLine(nX)
		oMdlAC2:SetValue("AC2_HRINT",HoraToInt(oMdlAC2:GetValue('AC2_HDURAC')))	
	Next nX 
	oMdlAC2:GoLine(nLinha)
EndIf   

Return(.T.)  

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} FtVrfTNotf
Verifica se o total de dias e horas da notificao  maior que os dias e horas de durao do estgio

@sample      FtVrfTNotf() 

@param             
       
@return    .T.   Verdadeiro se o total de dias e horas para notificao 
                   for menor que o total da durao
                   
            .F.   Falso se o total de dias e horas para notificao 
                   for maior que o total da durao


@author     Aline Kokumai
@since             10/05/2013       
@version    P11.80 
/*/
//------------------------------------------------------------------------------
Function FtVrfTNotf()

Local nDiaDur   := FWFLDGET("AC2_DDURAC") //Dias de durao do estagio
Local cHrDur    := FWFLDGET("AC2_HDURAC") //Horas de durao do estagio
Local nDiaNotf  := FWFLDGET("AC2_DNOTIF") //Dias de notificao do estgio
Local cHrNotf   := FWFLDGET("AC2_HNOTIF") //Horas de notificao do estgio
Local lRetorno  := Nil

If (nDiaNotf > nDiaDur) //Verifica se os dias para notificao  maior que o da durao
	lRetorno := .F.
ElseIf (nDiaNotf == nDiaDur) //Verifica se os dias para notificao  igual ao da durao
	If (Val(StrTran(cHrNotf,":","")) > Val(StrTran(cHrDur,":",""))) //Verifica se as horas para notificao  maior que a da durao
		lRetorno := .F.
	Else
		lRetorno := .T.
	EndIf
Else
	lRetorno := .T.
EndIf

//Ŀ
//   Problema: O perodo de notificao no pode ser superior ao perodo de 
//                     durao                                              
//   Solucao: Diminua o perodo de notificao                              
//                                                                          
//
Help( " ", 1, "AT010DRNOTIF" )

Return (lRetorno)

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT010Cmt

Commit 

@sample 	FT010Cmt( oModel )

@param		oModel -  Modelo de dados
		

@return   	Nil

@author	Thamara Villa Jacomo
@since		06/02/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function FT010Cmt( oModel )

Local aArea := GetArea()
Local nOper	:= oModel:GetOperation() 

If nOper == MODEL_OPERATION_DELETE
	//Ŀ
	// Exclusao de Anotaes    
	//		                                                                        
	If nModulo == 73
		CRMA090Del( "AC1", AC1->( Recno() ) )
	EndIf	
EndIf

RestArea( aArea )

FWFormCommit( oModel )

Return( .T. )

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT010PreAC2

Pr-Validao da grid.

@sample 	FT010PreAC2( oMdlAC2, nLine, cAction, cField, xValue, xOldValue )
@param		oMdlAC2 -  Modelo de dados
@param 		nLine - Linha Atual
@param 		cAction - Ao do usurio (Insert, delete, undelete, update)
@param 		cField - Campo que esta recebendo a ao
@param 		xValue - Valor Atual
@param 		xOldValue - Valor antes da ao do usurio
		
@return   	lRet - Retorno lgico

@author		Vendas CRM
@since		09/04/2018
@version	12
/*/
//------------------------------------------------------------------------------
Static Function FT010PreAC2(oMdlAC2, nLine, cAction, cField, xValue, xOldValue)

Local lRet 		:= .T.
Local cAcao		:= ""

If cField == "AC2_ACAO" .And. cAction == "SETVALUE"
	cAcao := AllTrim(xValue)
	If !Empty( cAcao )
		If (At("(",cAcao) > 0 .And. At(")",cAcao) == 0) .Or.;
			(At("(",cAcao) == 0 .And. At(")",cAcao) > 0)
			lRet := .F.	
		EndIf
		If lRet
			lRet := FindFunction( cAcao )
		EndIf
		If !lRet
			Help("",1,"FT010NOFUN",,STR0023,1,0,,,,,,{STR0024}) //##"A funo informada no existe."##"Informe uma funo vlida." 
		EndIf
	EndIf
EndIf

Return lRet
