#INCLUDE "pmsa090.ch"
#INCLUDE "protheus.ch"

/*/


Ŀ
Funo     PMSA090   Autor  Marcelo Akama          Data  05-05-2010 
Ĵ
Descrio  Itens de Check List                                          
Ĵ
 Uso       Generico                                                     
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                     
Ĵ
                                                                      
ٱ


*/
Function PMSA090()

PRIVATE cCadastro	:= STR0001 //"Itens de Check List"
PRIVATE aRotina := MenuDef()

If AMIIn(44) .And. !PMSBLKINT()
	mBrowse(6,1,22,75,"AJM")
EndIf

Return 

/*


ͻ
Programa   PMS090Chk   Autor  Marcelo Akama            Data  20/05/10 
͹
Desc.      Manutencao de items de check list                              
͹
Uso        PMS                                                            
ͼ


*/
Function PMSA090Rot(cAlias,nReg,nOpc)
Local cTabl  := ""
Local aArea  := {}
Local lUtlzd := .F.

If nOpc == 3				// Inclusao
	nOpca := AxInclui(cAlias,nReg,nOpc,,,,,,,)
ElseIf nOpc == 4			// Alteracao
	nOpca := AxAltera(cAlias,nReg,nOpc,,,,,,,,)
ElseIf nOPc == 5
	cTabl := Alias()
	aArea := AJN->( GetArea() )
	AJN->( DbSetOrder(2) )
	lUtlzd := AJN->( dbSeek(xFilial("AJM")+AJM->AJM_CODIGO) )
	RestArea( aArea )
	dbSelectArea( cTabl )

	If lUtlzd
		MsgAlert(STR0010) //"O item nao pode ser excluido pois esta sendo utilizado em um check list"
	Else
		nOpca := AxDeleta(cAlias,nReg,nOpc,,,,)
	EndIf
EndIf

Return

/*


ͻ
Programa   PMS090Chk   Autor  Marcelo Akama            Data  20/05/10 
͹
Desc.      Verifica se a expresso  valida (retorno deve ser booleano)   
͹
Uso        PMS                                                            
ͼ


*/
Function PMS090Chk(cExpr)

Local bBlock 	:= ErrorBlock( { |e| ChecErro(e) } )
Local lValid	:= .T.
Local lResult

If !Empty(cExpr)
		
	BEGIN SEQUENCE
		lResult	:= &(cExpr)
	RECOVER
		lValid	:= .F.
	END SEQUENCE
	ErrorBlock(bBlock)
	
	If lValid
		If Valtype(lResult) <> "L"
			lValid := .F.
			Aviso(STR0007,STR0008,{STR0009},2) //"Expressao invalida"//"A expressao deve retornar .T. ou .F."//"Fechar"
		Endif		
	Endif

Endif

Return lValid

/*/


Ŀ
Program    ChecErro  Autor  Pilar S. Albaladejo    Data  17.11.99 
Ĵ
Descrio  Rotina verificadora do erro                                
Ĵ
Sintaxe    ChecErro()                                                 
Ĵ
Retorno    .F./Nenhum                                                 
Ĵ
 Uso       CTBA080                                                    
Ĵ
Parametros Nenhum                                                     
ٱ


/*/
STATIC FUNCTION ChecErro(e)
	Help(" ",1,"ERR_FORM",,e:Description,3,1)
	BREAK

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 30/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
              1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function MenuDef()
Local aRotina 	:= {	{ STR0002, "AxPesqui"  , 0 , 1,,.F.},; //"Pesquisar"
						{ STR0003, "AxVisual"  , 0 , 2},;	 //"Visualizar"
						{ STR0004, "PMSA090Rot", 0 , 3},;	 //"Incluir"
						{ STR0005, "PMSA090Rot", 0 , 4},;	 //"Alterar"
						{ STR0006, "PMSA090Rot", 0 , 5} } //"Excluir"
Return(aRotina)