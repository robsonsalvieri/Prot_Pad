#INCLUDE "FATA320.CH"
#INCLUDE "PROTHEUS.CH" 
#INCLUDE "FWMVCDEF.CH"

#DEFINE NMAXPAGE   		40
#DEFINE WRK_REPR		"1"
#DEFINE WRK_REPINDF		"2"
#DEFINE WRK_TODOS  		"3"   

STATIC cStrFil 		:= ""
STATIC cVendorList	:= ""
STATIC aTimeSta		:= {}
STATIC cVendAtu		:= ""
STATIC cVendLog		:= ""
STATIC cUltVndPrc	:= ""
STATIC cUltGrpPrc	:= ""
STATIC cNomeLog		:= ""
STATIC aGrupo		:= {}  
STATIC lSuperior	:= NIL
STATIC __oTable		:= NIL

//Ŀ
// Define as posies da array dos dados do usuario Exchange.	 
//
#define _USUAR	 	1  
#define _SENHA	 	2  
#define _ISAGENDA 	3  
#define _DTAGEINI	4  
#define _DTAGEFIM	5  
#define _ISTAREFA	6  
#define _DTTARINI	7  
#define _DTTARFIM	8  

//Ŀ
// Define as posies da array de consulta do apontamento.		 
//
#define _HREF	 	1  
#define _NOME	 	2 
#define _DTEND	 	3  
#define _DTSTART	4  
#define _BUSYSTATUS	5  
#define _LASTMOD 	6  
#define _LOCAL	 	7  
#define _UID	 	8  
#define _DESCRI	 	9  
#define _SUBJECT 	10  
#define _TO		 	11  
#define _CC		 	12  
#define _BCC		13      

/*/


Ŀ
Funcao    Ft320Visit Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Programacao automatica das visitas aos clientes.            
Ĵ
Retorno   Verdadeiro                                                  
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft320Visit()

Local nOpcA := 0

Pergunte("FTA320",.F.)

FormBatch(OemToAnsi(STR0013),;			//"Programaao da Visita"
			{	OemToAnsi( STR0014),;	//" Este programa tem como objetivo gerar automaticamente "
				OemToAnsi( STR0015),;	//" as programaes de visita do representante comercial, "
				OemToAnsi( STR0016)},;	//" conforme os parametros definidos pelo usurio. "
			{  {5,.T.,{|o| Pergunte("FTA320",.T.)}},;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}},;
				{2,.T.,{|o| o:oWnd:End()}}})

If ( nOpcA == 1 ) 
	Processa({|| Ft320PVis()})
ElseIf __cInternet == "AUTOMATICO"
	Ft320PVis()	
EndIf

Return(.T.)

/*/


Ŀ
Funcao    Ft320PVis  Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Programacao automatica das visitas aos clientes.            
Ĵ
Retorno   Verdadeiro                                                  
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft320PVis()

Local aArea     := GetArea()
Local aContatos := {} 
Local aRestri   := {} 

Local lContinua := .F.
Local lContBlk  := .F.
Local lQuery	:= .F.
Local lProximo  := .F.
Local lFT320DVA := ExistBlock("FT320DVA")
Local lFT320GRV := ExistBlock("FT320GRV")
Local lFT320VLD := ExistBlock("FT320VLD")
Local dData		 := dDataBase
Local dUltData  := dData
Local dDataVis  := CToD( "" ) 
Local dDataForm	:= cTod( "" )

Local cHoraIni  := ""
Local cHoraFim  := ""
Local cLimite   := ""
Local cLimite1  := ""
Local cLimite2  := ""
Local cAgeLimite:= ""
Local cContato  := ""
Local cAlias    := "SA1"
Local cAlias2   := "AD7"
Local cQuery    := ""
Local cFiller   := ""

Local nLoop		:= 0
Local nTempVis  := 0
Local nTempStd  := 0 
Local cUserExg	:= ""
Local cSenhaExg	:= "" 
Local lRetExg	:= .T. 
Local aInfoSinc	:= {}


Local cVend := ""
Local aNvlEstrut	:= {}

If ( nModulo == 73 )
 	cVend := CRMXRetVend() //vendedor logado  
Else
	cVend := Ft320RpSel()
EndIf
If !Empty(cVend)
	aNvlEstrut := CRMXNvlEst(cVend)
EndIf

ProcRegua(SA1->(LastRec()))
		
cFiller := Space( Len( AC8->AC8_CODENT ) - ( Len( SA1->A1_COD ) + Len( SA1->A1_LOJA ) ) ) 

cQuery  := "SELECT DISTINCT A1_COD,A1_LOJA,SA1.R_E_C_N_O_ RECNOSA1 FROM "+RetSqlName("SA1")+" SA1,"
cQuery  += RetSqlName("SA3")+" SA3 "
cQuery  += "WHERE A1_FILIAL='"+xFilial("SA1")+"' AND "
cQuery  += "A1_COD>='" +MV_PAR01+"' AND "
cQuery  += "A1_COD<='" +MV_PAR02+"' AND "
cQuery  += "A1_VEND>='"+MV_PAR03+"' AND "
cQuery  += "A1_VEND<='"+MV_PAR04+"' AND "
cQuery  += "A1_VEND<>'"+Space(Len(SA1->A1_VEND))+"' AND "
cQuery  += "(SA1.A1_TEMVIS<>0 OR SA1.A1_FORMVIS<>'"+Space(Len(SA1->A1_FORMVIS))+"') AND "
cQuery  += "SA1.D_E_L_E_T_=' ' AND "
cQuery  += "A3_FILIAL='"+xFilial("SA3")+"' AND "
cQuery  += "A3_COD = SA1.A1_VEND AND "
cQuery  += "SA3.D_E_L_E_T_=' ' "
cQuery  += "ORDER BY 1,2"

If ExistBlock("FT320QRY")
	cQuery := ExecBlock("FT320QRY",.F.,.F.,{ cQuery })
EndIf

cAlias := GetNextAlias()
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
lQuery := .T. // Mantido como legado do PE FT320VLD

//Ŀ
// Percorre os clientes validos                                        
//
While !( cAlias )->( Eof() ) .AND. ( cAlias )->A1_COD >= MV_PAR01 .AND. ( cAlias )->A1_COD<=MV_PAR02 
	//Ŀ
	//Efetua a Selecao do Cliente para criar o Cronograma                  
	//
	lContinua := .T.
	lProximo  := .T.
	
	SA1->( MsGoto((cAlias)->(RECNOSA1)) )
	
	If lFT320VLD
		lContBlk  := ExecBlock("FT320VLD",.F.,.F.,{lQuery})
		lContinua := IIf(ValType(lContBlk) == "L",lContBlk,lContinua)
	Endif	
	
	If lContinua 
	
		//Ŀ
		// Adiciona os contatos                                                
		//
		aContatos := {} 				                             
		
		Ft320AdCon( SA1->A1_COD, SA1->A1_LOJA, aContatos ) 

		//Ŀ
		// Se nao possui nenhum contato, cria um contato para permitir o processamento  
		//
		If Empty(aContatos)
		  AAdd(aContatos,{"",Nil,Nil,"1234567"})
		EndIf  
		
		dDataFim := MV_PAR05 

		//Ŀ
		//Calcula a Primeira data de visita                                    
		//
		If ExistBlock("FT320DTI")
			dDataVis := ExecBlock("FT320DTI",.F.,.F.)
		Else
			If ( !Empty(SA1->A1_FORMVIS) )
				dDataForm := Formula(SA1->A1_FORMVIS)
				If ValType(dDataForm) == "D"
					dDataVis := dDataForm
				EndIf 
			Else
				If GetNewPar("MV_MODINIV",1) == 1
					dDataVis := If(Empty(SA1->A1_ULTVIS),dDataBase,SA1->A1_ULTVIS)+Int(SA1->A1_TEMVIS)
				Else
					dDataVis := If(Empty(SA1->A1_ULTVIS),dDataBase+1,SA1->A1_ULTVIS+Int(SA1->A1_TEMVIS))
				EndIf
			EndIf
		EndIf
		//Ŀ
		// Pesquisa uma data valida                                            
		//
		If lFT320DVA
			dDataVis := ExecBlock("FT320DVA",.F.,.F.,{dDataVis})
		Else
			While dDataVis <> DataValida( dDataVis ) 
				dDataVis++
			End
		EndIf

		//Ŀ
		// Coloca a data de visita como sendo a data de inicio                 
		//
		dData := dDataVis 

		lDataVis := .T. 

		//Ŀ
		// Percorre todas as datas para este cliente                                                 
		//
		While dData <= dDataFim 

			//Ŀ
			// Verifica se e a data de Visita.                                     
			//
			If dData == dDataVis 

				//Ŀ
				// Para gerar a alocacao, a data deve ser posterior a data atual e valida perante ao sistema 
				//
				If dData > dDataBase .AND. If(!lFT320DVA,dData == DataValida( dData ),.T.)

					//Ŀ
					// Marca o flag de alocacao na data de visita como falso                                     
					//
					lDataVis := .F. 
					 
					For nLoop := 1 To Len(aContatos)      

						//Ŀ
						// Verifica se o contato recebe neste dia da semana                    
						//				
						If StrZero( Dow( dData ),1,0) $ aContatos[nLoop,4] 
					
							//Ŀ
							//Verifica a primeira hora disponivel para agendamento.                
							//
							cLimite1 := &(SuperGetMv("MV_FVHRINI"))
							cLimite2 := &(SuperGetMv("MV_FVHRFIM"))
							cLimite  := SubStr(cLimite2,1,5)
							cHoraIni := SubStr(cLimite1,1,5)
							nTempVis := HoraToInt(SA1->A1_TMPVIS)
							nTempStd := HoraToInt(SA1->A1_TMPSTD)
							//Ŀ
							//Verifica a janela de contato                                         
							//
							cContato := aContatos[ nLoop, 1 ]
							If !Empty(aContatos[ nLoop, 2 ])
								cLimite1 := aContatos[ nLoop, 2 ]
							EndIf
							If !Empty(aContatos[ nLoop, 3 ])
								cLimite2 := aContatos[ nLoop, 3 ]
							EndIf
							//Ŀ
							//Retira o tempo de deslocamento da primeira hora disponivel do contato
							//
							SomaDiaHor(dData,@cLimite1,(-1)*nTempStd)
							cHoraIni := cLimite1
							
							cAgeLimite := cLimite
							//Ŀ
							//Verificar o Proximo horario disponivel do Vendedor na agenda         
							//
							
							DbSelectArea("AD7")
							DBSetOrder(1) 
							
							cQuery := "SELECT AD7_FILIAL,AD7_VEND,AD7_DATA,AD7_HORA1,AD7_HORA2 "
							cQuery += "FROM "+RetSqlName("AD7")+" "
							cQuery += "WHERE AD7_FILIAL='"+xFilial("AD7")+"' AND "
							cQuery += "AD7_VEND='"+SA1->A1_VEND+"' AND "
							cQuery += "AD7_DATA='"+Dtos(dData)+"' AND "
							cQuery += "D_E_L_E_T_=' ' "
							cQuery += "ORDER BY "
							cQuery += SqlOrder(AD7->(IndexKey()))
								
							cQuery := ChangeQuery(cQuery)
							cAlias2:= cAlias+"b"
							DBUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias2,.T.,.T.)					
								
							TcSetField(Alias(),"AD7_DATA","D", 8, 0 )
															
							While ( (cAlias2)->( !Eof() ) .AND. xFilial("AD7")==(cAlias2)->(AD7_FILIAL) .AND.;
									 SA1->A1_VEND == (cAlias2)->(AD7_VEND) .AND. dData == (cAlias2)->(AD7_DATA) )					
								dUltData := dData
								cHoraFim := cHoraIni
								SomaDiaHor(@dUltData,@cHoraFim,nTempVis)
								Do Case
									Case ( dUltData == dData .AND. cHoraFim < (cAlias2)->AD7_HORA1 )
										cAgeLimite := (cAlias2)->AD7_HORA1
										Exit
									Case ( dUltData <> dData )
										cHoraIni := cLimite
										Exit
									OtherWise
										If ( cHoraIni < (cAlias2)->AD7_HORA2 )
											cHoraIni := (cAlias2)->AD7_HORA2
										EndIf
								EndCase
								
								(cAlias2)->( DBSkip() )
							End
							
							(cAlias2)->( DBCloseArea() )
							
							DbSelectArea("AD7")
							
							//Ŀ
							//Verificar se a data de agenda esta de acordo com a disponibilidade do
							//contato                                                              
							//
							cHoraFim := cHoraIni
							dUltData := dData
							
							SomaDiaHor(@dUltData,@cHoraFim,nTempVis+nTempStd)
                           
							lContinua := .F. 

							If ( dUltData == dData .AND. cHoraFim <= cLimite .AND.;
								 cHoraIni <= cLimite2 .AND. cHoraFim <= cLimite2 .AND.;
								 cHoraIni >= cLimite1 .AND. cHoraFim >= cLimite1 )

								aRestri := FaAvRestri( SA1->A1_COD, SA1->A1_LOJA, 1, cContato, dData, cHoraIni, cHoraFim, cAgeLimite )
								If aRestri[ 1 ] 
									//Ŀ
									// Se nao ancontrou restricao, sai                                     
									//				
									lContinua := .T. 
								Else
									//Ŀ
									// Se encontrou restricao mas realocou o horario, atualiza             
									//				
									If aRestri[ 2 ]
										cHoraIni := aRestri[ 3 ] 
										cHoraFim := aRestri[ 4 ] 
										lContinua := .T. 
									EndIf 						
								EndIf 
							EndIf    
								
							Begin Transaction
								If ( lContinua )                       					
				
									//Ŀ
									// Marca o flag de alocacao na data de visita como verdadeiro                                
									//
									lDataVis := .T. 

									//Ŀ
									//Nao se deve permitir mais de uma visita por dia.                     
									//
									DbSelectArea("AD7")
									dbSetOrder(2)
									If ( !DbSeek(xFilial("AD7")+SA1->A1_COD+SA1->A1_LOJA+DTOS(dData)+cContato) )
										RecLock("AD7",.T.)
										Replace AD7->AD7_FILIAL With xFilial("AD7")
										Replace AD7->AD7_TOPICO With STR0022 //"Visita Programada"
										Replace AD7->AD7_CODCLI With SA1->A1_COD
										Replace AD7->AD7_LOJA   With SA1->A1_LOJA
										Replace AD7->AD7_VEND   With SA1->A1_VEND
										Replace AD7->AD7_DATA   With dData
										Replace AD7->AD7_HORA1  With cHoraIni
										Replace AD7->AD7_HORA2  With cHoraFim
										Replace AD7->AD7_CONTAT With cContato
										Replace AD7->AD7_ORIGEM With "2" 
										// grava o nivel do vendedor loga
										If !Empty(aNvlEstrut)
										   	AD7->AD7_IDESTN  := aNvlEstrut[1]
										   	AD7->AD7_NVESTN  := aNvlEstrut[2] 
										EndIf
										MsUnLock()						
										//Ŀ
										// Ponto de entrada para gravao de campos de usurio. 			    
										//
										If lFT320GRV
											ExecBlock("FT320GRV",.F.,.F.)
										EndIf														
										//Ŀ
										//Atualiza a data de Ultima Visita.                                    
										//
										RecLock("SA1")
										Replace SA1->A1_ULTVIS With IIf(dData>SA1->A1_ULTVIS,dData,SA1->A1_ULTVIS)
										MsUnLock()
										
									EndIf					
								EndIf		
								
							End Transaction
							
						EndIf 	
							
					Next nLoop

				EndIf 	
				
				//Ŀ
				// Calcula a Proxima data de Visita.                                   
				//
				If lDataVis

					//Ŀ
					// Se conseguiu alocar, segue o procedimento normal                    
					//
					If ( !Empty(SA1->A1_FORMVIS) )
						dDataVis := Formula( SA1->A1_FORMVIS )
					Else
						dDataVis += Int( SA1->A1_TEMVIS )
					EndIf 		
					
					//Ŀ
					// Pesquisa uma data valida                                            
					//
					While dDataVis <> DataValida( dDataVis ) .and. !lFT320DVA
						dDataVis++
					End
				Else	
					 
					//Ŀ
					// No caso de nao haver nenhuma alocacao, procura uma data que algum contato possa receber  
					//

					//Ŀ
					// Calcula a Proxima data contando os dias da semana de todos os contatos 
					//
					dDataVis++ 
					While Empty( Ascan( aContatos, { |x| StrZero( Dow( dDataVis ),1,0) $ x[ 4 ] } ) ) 
					   dDataVis++
					End
					
				EndIf
	          
 			EndIf 

			//Ŀ
			// Incrementa o contador de datas                                                            
			//
 			dData++ 	
 			
		End
	
	EndIf 
	
	If ExistBlock("FT320AG2")
		ExecBlock("FT320AG2",.F.,.F.)
	EndIf	
	//Ŀ
	// Atualiza a regua de progresso                                       
	//
	IncProc( OemToAnsi( STR0006 ) + ": " + SA1->A1_COD + "/" + SA1->A1_LOJA ) 	//"Cliente"
	
	//Ŀ
	// Mudanca de cliente                                                  
	//
	( cAlias )->( dbSkip() ) 
	
End

( cAlias )->( DBCloseArea() )

RestArea(aArea)
Return(.T.)

/*/


Ŀ
Funcao    FtUltVis   Autor  Eduardo Riera          Data 24.12.1999
Ĵ
Descrio Retorna a Ultima Visita do Cliente                          
Ĵ
Retorno    ExpD1: Ultima Visita do Cliente.                           
Ĵ
Parametros ExpC1: Codigo do Cliente                                   
           ExpC2: Loja do Cliente                                     
           ExpL3: Atualiza data de Ultima Visita                      
ٱ


/*/
Function FtUltVis(cCliente,cLoja,lAtualiza)

Local aArea    := GetArea()
Local aAreaAD7 := AD7->(GetArea())
Local aAreaSA1 := SA1->(GetArea())
Local cAlias   := ""
Local cQuery   := ""
Local dData    := SuperGetMv("MV_FTINVIS")

cQuery := "SELECT MAX(AD7_DATA) ULTVIS FROM "+RetSqlName("AD7")+" "
cQuery += "WHERE AD7_FILIAL='"+xFilial("AD7")+"' AND "
cQuery += "AD7_CODCLI='"+cCliente+"' AND "
cQuery += "AD7_LOJA='"+cLoja+"' AND "
cQuery += "D_E_L_E_T_=' '"

cQuery := ChangeQuery(cQuery)

cAlias := GetNextAlias()
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
TCSetField(Alias(),"ULTVIS","D")

dData := If(Empty(ULTVIS),dData,ULTVIS)

(cAlias)->( DBCloseArea() )

DbSelectArea("AD7")
        
//Ŀ
// Ponto de entrada para a definicao da ultima data de visita          
//
If ExistBlock("FTULTVIS")
	dData := ExecBlock("FTULTVIS",.F.,.F.,{dData})
EndIf

If ( lAtualiza )
	Begin Transaction
		DbSelectArea("SA1")
		dbSetOrder(1)
		If DbSeek(xFilial("SA1")+cCliente+cLoja)
			RecLock("SA1")
			Replace SA1->A1_ULTVIS With dData
			MsUnLock()
		EndIf
	End Transaction
EndIf
RestArea(aAreaSA1)
RestArea(aAreaAD7)
RestArea(aArea)
Return(dData)

/*/


Ŀ
Funcao    Ft320DVis  Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Exclusao    automatica das visitas aos clientes.            
Ĵ
Retorno   Verdadeiro                                                  
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft320DVis()

Local nOpcA := 0

Pergunte("FTA320",.F.)

FormBatch(OemToAnsi(STR0018),;			//"Exclusao de Visitas"
			{	OemToAnsi( STR0019),;	//" Este programa tem como objetivo excluir automaticamente "
				OemToAnsi( STR0020),;	//" as programaes de visita do representante comercial, "
				OemToAnsi( STR0021)},;	//" conforme os parametros definidos pelo usurio. "
			{  {5,.T.,{|o| Pergunte("FTA320",.T.)}},;
				{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}},;
				{2,.T.,{|o| o:oWnd:End()}}})

If ( nOpcA == 1 )

	Processa({|| Ft320DVis2()})

EndIf

Return(.T.)

/*/


Ŀ
Funcao    Ft320DVis2 Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Exclusao automatica das visitas aos clientes.               
Ĵ
Retorno   Verdadeiro                                                  
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
16/02/07  Hanna Caroline BOPS 119080 - Inclusao do PE para validar a 
                         permissao da exclusao da agenda             
14/08/08  Cleber Souza	 Valida exclusao da agenda no Exchange.	 
ٱ


/*/
Function Ft320dVis2()


Local cQuery 		:= ""
Local cAlias 		:= GetNextAlias()							//Alias do arquivo
Local lDeleta		:= .F.								//Valida se deve deletar o registro
Local lFt320Del		:= ExistBlock( "FT320DEL" )		//Valida existencia do PE, se deve ou nao excluir a agenda
Local lPermDel		:= .T.							//Valida se pode ser excluida a agenda  
Local lRetExg		:= .T.    
Local aInfoSinc 	:= {}  
Local cUserExg		:= ""
Local cSenhaExg		:= ""    

//Ŀ
//PE para validacao se o usuario pode ou nao excluir sua agenda
//
If lFt320Del
	lPermDel := ExecBlock( "FT320DEL", .F., .F. )
	If ValType(lPermDel) <> "L"
		lPermDel := .T.
	Endif
EndIf 

If lPermDel
	ProcRegua(SA1->(LastRec()))
		
	cQuery := "SELECT AD7.R_E_C_N_O_ AD7RECNO "
	cQuery += "FROM " + RetSqlName("AD7") + " AD7 "
	cQuery += "WHERE AD7.AD7_FILIAL='" + xFilial("AD7") + "' AND "
	cQuery += "AD7.AD7_CODCLI >= '"	+ MV_PAR01	+ "' AND "
	cQuery += "AD7.AD7_CODCLI <= '"	+ MV_PAR02	+ "' AND "
	cQuery += "AD7.AD7_VEND >= '"	+ MV_PAR03	+ "' AND "
	cQuery += "AD7.AD7_VEND <= '"	+ MV_PAR04	+ "' AND "
	cQuery += "AD7.AD7_DATA <= '"	+ Dtos(MV_PAR05) + "' AND "
	cQuery += "AD7.AD7_ORIGEM = '2' AND "	
	cQuery += "AD7.D_E_L_E_T_=' '"
	
	cQuery := ChangeQuery( cQuery )
		
	DBUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
			
	While (cAlias)->( !Eof() )
		//Ŀ
		//Efetua a Selecao da Agenda a ser excluida.                           
		//
		lDeleta:= .T.
	
		DbSelectArea("AD7")
		MsGoto((cAlias)->(AD7RECNO))
	
		If ( lDeleta ) .and. ( lRetExg )
			If lRetExg
				RecLock("AD7")
				dbDelete()
				MsUnLock()		
				FtUltVis(AD7->AD7_CODCLI,AD7->AD7_LOJA,.T.)
			EndIF	
		EndIf
		IncProc(STR0008 + ":" + SA1->A1_COD + "/" + SA1->A1_LOJA + " - " + Dtoc( SA1->A1_ULTVIS ) ) //"Oportunidades"
		(cAlias)->( DBSkip() )
	End
	
	(cAlias)->( DBCloseArea() )
	
	DbSelectArea( "AD7" ) 	

EndIf

Return(.T.)

/*/


Ŀ
Funcao    FtVisProsp Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Programacao das Visitas dos Representantes Comerciais aos   
          prospects.                                                  
Ĵ
Parametros ExpD1 : Data                                               
           ExpN1 : Tempo                                              
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Observacao O SUS deve estar posicionado                               
                                                                      
ٱ


/*/
Function FtVisProsp(dData,cTempo)

Local lContinua 	:= .T.
Local dUltData  
Local nTempo	
Local cHoraIni		:= ""
Local cHoraFim		:= ""
Local cVend     	:= SUS->US_VEND
Local cUserExg		:= ""
Local cSenhaExg		:= "" 
Local lRetExg		:= .T. 
Local aInfoSinc		:= {}   
Local cCodVend 		:= "" //vendedor logado
Local aNvlEstrut	:= {}

If ( nModulo == 73 )
  	cCodVend :=  CRMXRetVend()    
Else
	cCodVend := Ft320RpSel()
EndIf
If !Empty(cCodVend)
	aNvlEstrut := CRMXNvlEst(cCodVend)
EndIf


nTempo:= HoraToInt(cTempo) 

//Ŀ
//Verificar se a representante cadastrado                              
//
If ( !Empty(cVend) )
	DbSelectArea("SA3")
	dbSetOrder(1)
	If ( !DbSeek(xFilial("SA3")+cVend) )
		lContinua := .F.
	EndIf
Else
	lContinua := .F.
EndIf

If ( lContinua )
	//Ŀ
	//Verificar o Proximo horario disponivel do Vendedor                   
	//
	While ( lContinua )
		//Ŀ
		//Verifica a primeira hora disponivel para agendamento.                
		//
		cHoraIni := &(SuperGetMv("MV_FVHRINI"))
		//Ŀ
		//Verificar o Proximo horario disponivel do Vendedor                   
		//					
		DbSelectArea("AD7")
		dbSetOrder(1)
		DbSeek(xFilial("AD7")+SA1->A1_VEND+DTOS(dData))
		While ( !Eof() .AND. xFilial("AD7")==AD7->AD7_FILIAL .AND.;
							SA1->A1_VEND == AD7->AD7_VEND .AND.;
							dData == AD7->AD7_DATA )
			dUltData := dData
			cHoraFim := cHoraIni
			SomaDiaHor(@dUltData,@cHoraFim,nTempo)
			Do Case
				Case ( dUltData == dData .AND. cHoraFim < AD7->AD7_HORA1 )
					Exit
				Case ( dUltData <> dData )
					cHoraIni := cLimite
					Exit
				OtherWise
					If ( cHoraIni < AD7->AD7_HORA2 )
						cHoraIni := AD7->AD7_HORA2
					EndIf
			EndCase				
			
			DbSelectArea("AD7")
			dbSkip()
			
		End
		cHoraFim := cHoraIni
		dUltData := dData
		SomaDiaHor(@dUltData,@cHoraFim,nTempo)
		If ( dUltData <> dData .OR. cHoraFim > &(SuperGetMv("MV_FVHRFIM")) )
			dData++
		Else
			lContinua := .F.
		EndIf
	End
	//Ŀ
	//Nao se deve permitir mais de uma visita por dia.                     
	//
	DbSelectArea("AD7")
	dbSetOrder(3)
	If ( !DbSeek(xFilial("AD7")+SUS->US_COD+SUS->US_LOJA+DTOS(dData)) )
		RecLock("AD7",.T.)
		Replace AD7->AD7_FILIAL	With xFilial("AD7")
		Replace AD7->AD7_TOPICO With STR0022 //"Visita Programada"		
		Replace AD7->AD7_PROSPE	With SUS->US_COD
		Replace AD7->AD7_LOJPRO	With SUS->US_LOJA
		Replace AD7->AD7_VEND	With SUS->US_VEND
		Replace AD7->AD7_DATA	With dData
		Replace AD7->AD7_HORA1  With cHoraIni
		Replace AD7->AD7_HORA2  With cHoraFim
		Replace AD7->AD7_ORIGEM With "2"
		
		If	!Empty(aNvlEstrut)
		   	AD7->AD7_IDESTN  := aNvlEstrut[1]
		   	AD7->AD7_NVESTN  := aNvlEstrut[2] 
		EndIf
		
		MsUnLock()
		RecLock("SUS")
		Replace SUS->US_ULTVIS With dData
		MsUnLock()   
	EndIf
EndIf

Return(.T.)
      	

/*


Ŀ
Funcao    Ft320LOK1  Autor  Sergio Silveira        Data 18/01/2001
Ĵ
Descrio  LinOk das Tarefas do Representante Comercial               
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Observacao                                                            
Ĵ
	PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                         
Ĵ
Fernando      31/10/06811   BOPS11O808- P.E para validar a tarefas  
                            digitadas                               
-ٱ       


*/

Function Ft320LOK1()
  
Local lFt320Valid	:= ExistBlock("Ft320Valid")		// P.E. para validar as tarefas do representante                    
LOCAL lRet 			:= .T.
Local aAreaBKP		:= GetArea()   

If lFt320Valid
	lRet := ExecBlock( "Ft320Valid", .F., .F. )
	If (ValType(lRet) <> "L")
		Return(.F.)
	ElseIf !lRet
		Return(.F.)
	Endif 
Endif
                 
If !GdDeleted()                
	If Empty( GdFieldGet( "AD8_TOPICO" ) ) 
		Return(.F.)              
	EndIf 
	
	//Valida se Oportunidade informada e do Prospect ou Cliente indicado
	If !Empty(GdFieldGet( "AD8_NROPOR" ))
		dbSelectArea("AD1")
		AD1->(dbSetOrder(1))
		If AD1->(dbSeek(xFilial("AD1")+GdFieldGet( "AD8_NROPOR" )))
			If !Empty(GdFieldGet( "AD8_PROSPE" )) .And. GdFieldGet( "AD8_PROSPE" ) <> AD1->AD1_PROSPE
				lRet := .F.     
				MsgAlert(STR0090)
			ElseIf  !Empty(GdFieldGet( "AD8_CODCLI" )) .And. GdFieldGet( "AD8_CODCLI" ) <> AD1->AD1_CODCLI
				lRet := .F.    
				MsgAlert(STR0091)
			EndIf		
		ElseIf !Found() 
			lRet := .F.
			MsgAlert(STR0092) 
		EndIf
	EndIf
EndIf 

RestArea(aAreaBKP)

Return( lRet ) 

/*/


Ŀ
Funcao    Ft320Age   Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Manutencao da Agenda do Representante Tecnico.              
                                                                      
Ĵ
ParametrosExpD1: Data de Inicio                                       
          ExpO2: Objeto do calendario                                 
          ExpA3: Array da rotina automatica                           
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
Observacao                                                            
                                                                       
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
12/03/07  Fernando       Bops 119237 Alterao feita para usar o     
                         Walk Trhu									  
14/08/08  Cleber Souza	 Valida exclusao da agenda no Exchange.	 
ٱ


/*/
Function Ft320Age(dDataAtu, oCalend , aAutoCalend)

Local aArea         := GetArea() 
Local aAreaSX3      := {} 
Local aButtons      := {}
Local aUsButtons    := {}
Local aRotBack      := AClone( aRotina ) 
Local aRegistros    := {} 
Local lGravou		:= .F.
Local nCntFor		:= 0
Local nCntFor2		:= 0
Local nUsado		:= 0
Local nOpcA			:= 0
Local nPMemo        := 0
Local nSaveSx8      := GetSx8Len()
Local oDlg,oGetD   
Local aInfoSinc 	:= {}  
Local cUserExg		:= ""
Local cSenhaExg		:= ""   
Local lFt320BUT  	:= ExistBlock( "Ft320BUT" )
Local cCodVend		:= ""
Local aNvlEstrut	:= {}
Local cCaption      := ""
Local lPyme 		:= If( Type( "__lPyme" ) <> "U", __lPyme, .F. ) 
Local lRetExg	    := .T.

PRIVATE aCols		:= {}
PRIVATE aHeader		:= {}
PRIVATE dData       := dDataAtu          
PRIVATE aRegWork	:= {} 		//Variavel criada somente para trabalho da linhaok

If ( nModulo == 73 )
	cCodVend :=  CRMXRetVend() //vendedor logado  
Else
	cCodVend := Ft320RpSel()
EndIf
If !Empty(cCodVend)
	aNvlEstrut := CRMXNvlEst(cCodVend)
EndIf


aRotina := { { STR0010, "Ft320Age", 0, 4 } } //"Agenda"     

If Type("cCadastro") == "U"
	cCadastro := STR0111	//"Atividades"
EndIf
	
cCaption      := cCadastro + " - " + STR0010 // "Agenda"


If !lPyme
	aButtons:={{"COMPTITl",{||Ft320Apon()},STR0009, STR0052}} //"Apontamentos"###"Apont."

	//Ŀ
	// Avalia botoes do usuario                                     
	//
	If lFt320BUT
		If ValType( aUsButtons := ExecBlock( "Ft320BUT", .F., .F. ) ) == "A"
			AEval( aUsButtons, { |x| AAdd( aButtons, x ) } ) 	 	
		EndIf 	
	EndIf 	
EndIf

//Ŀ
//Montagem do aHeader                                                     
//
DbSelectArea("SX3")
dbSetOrder(1)
dbSeek("AD7")
While ( !Eof() .AND. SX3->X3_ARQUIVO=="AD7" )
	If ( X3Uso(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .AND.;
			!AllTrim(SX3->X3_CAMPO)=="AD7_VEND" .AND.;
			!AllTrim(SX3->X3_CAMPO)=="AD7_NOMVEN") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_NROPOR") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_PROSPE") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_LOJPRO") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_NOMPRO") .AND.;			
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_VENDAP") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_DATAAP") .AND.;
			!(lPyme .AND. AllTrim(SX3->X3_CAMPO)=="AD7_SEQAP")
		nUsado++
		aadd(aHeader,{ AllTrim(X3Titulo()),;
							SX3->X3_CAMPO,;
							SX3->X3_PICTURE,;
							SX3->X3_TAMANHO,;
							SX3->X3_DECIMAL,;
							SX3->X3_VALID,;
							SX3->X3_USADO,;
							SX3->X3_TIPO,;
							SX3->X3_ARQUIVO,;
							SX3->X3_CONTEXT } )
		If ( AllTrim(SX3->X3_CAMPO)$"AD7_MEMO" )
			nPMemo := nUsado
		EndIf
	EndIf
	DbSelectArea("SX3")
	dbSkip()
End
ADHeadRec("AD7",aHeader)
nUsado := Len(aHeader)	
//Ŀ
//Montagem do aCols                                                       
//
DbSelectArea("AD7")
dbSetOrder(1)
dbSeek(xFilial("AD7")+SA3->A3_COD+Dtos(dDataAtu))
While ( !Eof() .AND. xFilial("AD7") == AD7->AD7_FILIAL .AND.;
							AD7->AD7_VEND 	== SA3->A3_COD .AND.;
							AD7->AD7_DATA    == dDataAtu .AND. Iif(aAutoCalend <> Nil, AD7->AD7_HORA1 == aAutoCalend[1][3][2], .T.))
	If ( SoftLock("AD7" ) )
		aadd(aRegistros,RecNo())
		aadd(aCols,Array(nUsado+1))
		For nCntFor := 1 To nUsado
			If IsHeadAlias(aHeader[nCntFor,2])
			   	aCols[Len(aCols)][nCntFor] := "AD7"
			ElseIf IsHeadRec(aHeader[nCntFor,2])
			   	aCols[Len(aCols)][nCntFor] := AD7->(Recno())
			ElseIf ( aHeader[nCntFor][10] != "V" )
				aCols[Len(aCols)][nCntFor] := AD7->(FieldGet(FieldPos(aHeader[nCntFor][2])))
			Else
				aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor,2])
			EndIf
		Next nCntFor
		aCols[Len(aCols)][nUsado+1] := .F.
	EndIf
	DbSelectArea("AD7")
	dbSkip()
End
If ( Len(aCols) == 0 )
	aadd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor,2])
	 		aCols[Len(aCols)][nCntFor]:= 0 
	    ElseIf IsHeadAlias(aHeader[nCntFor,2])
			aCols[Len(aCols)][nCntFor] := "AD7"
		Else   
            aCols[1][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aCols[1][nUsado+1] := .F.
EndIf    

aRegWork := aClone(aRegistros)

//Ŀ
// Forca um Eof() para o correto funcionamento do campo memo              
//
AD7->( dbSetOrder( 1 ) ) 
AD7->( MsGoto(0) )      

If aAutoCalend == Nil
	DEFINE MSDIALOG oDlg TITLE cCaption FROM 9,0 TO 28,80 OF oMainWnd
	@ 016,010 SAY  RetTitle("A3_NOME") 	SIZE 040,009 	OF oDlg PIXEL
	@ 016,050 MSGET SA3->A3_NOME		SIZE 136,009 	OF oDlg PIXEL WHEN .F.

	oGetd:=MsGetDados():New(030,005,138,311,1,"Ft320LOK2","Ft320TOK","",.T.)

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()},,aButtons)
Else
	If MsGetDAuto(aAutoCalend,"Ft320Lok2",{|| .t.},/*aAutoCab*/,4)
		nOpcA := 1
	Else
		nOpcA := 0
	EndIf
EndIf    
aCols[1][3]:=STRTRAN(aCols[1][3],".",":")
If ( nOpcA == 1 )
	Begin Transaction
		For nCntFor := 1 To Len(aCols)
			If ( nCntFor > Len(aRegistros) )
				If ( !aCols[nCntFor][nUsado+1] )
					RecLock("AD7",.T.)
				EndIf
			Else
				AD7->(dbGoto(aRegistros[nCntFor]))
				RecLock("AD7",.F.)			
			EndIf
			If ( aCols[nCntFor][nUsado+1] ) .AND. nCntFor <= Len(aRegistros)
				If lRetExg
					If oCalend <> Nil
						oCalend:DelRestri( Day( AD7->AD7_DATA ) )			
					EndIf
					AD7->(dbDelete())      
					If ( !Empty(AD7->AD7_CODCLI) )
						FtUltVis(AD7->AD7_CODCLI,AD7->AD7_LOJA,.T.)
					EndIf							
					MSMM(AD7->AD7_CODMEM,,,,2)
				EndIf				
			Else
				If !( aCols[nCntFor][nUsado+1] ) 
		
					//Ŀ
					// Se alterou a data, retira a data anterior do calendario    
					//
					If GDFieldGet( "AD7_DATA", nCntFor ) <> AD7->AD7_DATA
						If oCalend <> Nil
							oCalend:DelRestri( Day( AD7->AD7_DATA ) )
						EndIf
					EndIf 					 
				
					For nCntFor2 := 1 To Len(aHeader)
						If ( aHeader[nCntFor2][10] <> "V" )
							FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2])
						EndIf					
					Next nCntFor2
					Replace AD7->AD7_FILIAL With xFilial("AD7")
					Replace AD7->AD7_VEND	With SA3->A3_COD
					If	!Empty(aNvlEstrut)
					   	AD7->AD7_IDESTN  := aNvlEstrut[1]
					   	AD7->AD7_NVESTN  := aNvlEstrut[2] 
					EndIf
					If ( !Empty(aCols[nCntFor][nPMemo]) )
						MSMM(AD7->AD7_CODMEM,,,aCols[nCntFor][nPMemo],1,,,"AD7","AD7_CODMEM")
					EndIf 
				EndIf					
			EndIf	
			MsUnLock()		
			lGravou := .T. 
		Next nCntFor
		EvalTrigger()
		While (GetSx8Len() > nSaveSx8)
			ConfirmSx8()
		End
	End Transaction
EndIf
While (GetSx8Len() > nSaveSx8)
	RollBackSx8()
End

If lGravou .AND. aAutoCalend == Nil
	Eval(oCalend:bChangeMes)
	Eval(oCalend:bChange) 	
EndIf

aRotina := aClone( aRotBack ) 

MsUnLockAll()
RestArea(aArea)
Return(lGravou)

/*


ͻ
Programa  Ft320TOK  Autor  Vendas / CRM         Data   11/10/09   
͹
Desc.     Funcao de validao na confirmao da inclusao da agenda.   
                                                                      
͹
Uso       FATA320                                                     
ͼ


*/
Function Ft320TOK()
Local lRet 		:= .T.
Local lFT320TDK := ExistBlock("FT320TDK")

If lFT320TDK
   lRet := Execblock("FT320TDK",.F.,.F.)
	If ValType(lRet) <> "L"
		lRet := .T. // Reinicializa a valiavel
	EndIf	
EndIf

Return ( lRet )

/*


Ŀ
Funcao    Ft320LOK2  Autor  Sergio Silveira        Data 18/01/2001
Ĵ
Descrio  LinOk da Agenda do Representante Comercial                 
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1: logico                                              
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
PROGRAM    DATA    BOPS   MOTIVO DA ALTERACAO                             
Ĵ
Hanna C.  19/01/06091152Inclusao do parametro MV_VLD_HOR, para vali-
                        dar ou nao a hora do agendamento            
Norbert W.05/03/07119308Alteracao da regra de validacao dos horarios
                        das visitas, para permitir que a hora de    
                        inicio de uma visita possa ser a mesma do   
                        termino da anterior.                        
ٱ


*/

Function Ft320LOK2()  
Local cCodCli		:= GdFieldGet("AD7_CODCLI")								// Codigo do Cliente
Local cLojCli		:= GdFieldGet("AD7_LOJA")								// Loja do cliente
Local cCodPro		:= GdFieldGet("AD7_PROSPE")								// Prospect
Local cLojPro		:= GdFieldGet("AD7_LOJPRO")								// Loja do Prospect
Local dData1		:= GdFieldGet("AD7_DATA")								// Data do Agendamento
Local cHora1		:= GdFieldGet("AD7_HORA1")								// Hora Inicial do Agendamento
Local cHora2		:= GdFieldGet("AD7_HORA2")								// Hora Final do agendamento
Local cTipoAg		:= ""													// Hora Final do agendamento
Local cEmails		:= ""													// Hora Final do agendamento
Local nPosDt1		:= aScan(aHeader,{|x| AllTrim(x[2])=="AD7_DATA"})		// Posicao da data do agendamento
Local nPosHr1		:= aScan(aHeader,{|x| AllTrim(x[2])=="AD7_HORA1"})		// Posicao da hora inicial do agendamento
Local nPosHr2		:= aScan(aHeader,{|x| AllTrim(x[2])=="AD7_HORA2"})		// Posicao da hora final do agendamento
Local nAuxFor		:= 0													// Variavel auxiliar para o For
Local lMV_VLD_HOR	:= SuperGetMV("MV_VLD_HOR",,.T.)						// Parametro que define se a validacao do horario deve existir ou nao
Local lRet 			:= .T. 															// Variavel com o retorno da funcao
Local lExistCp		:= .F.

//Ŀ
//Verifica se os campos de email foram criados na base
//
lExistCp	:= .T.
cTipoAg		:= GdFieldGet("AD7_AGEREU")
cEmails		:= GdFieldGet("AD7_EMAILP")

If !GdDeleted()                
	If Empty( GdFieldGet( "AD7_TOPICO" ) )						.OR. ;
			( (	!Empty( cCodCli ) .AND. Empty( cLojCli ) )		.OR. ;
			(	!Empty( cCodPro ) .AND. Empty( cLojPro ) ) )	.OR. ;
			(	 Empty( cCodPro ) .AND. Empty( cCodCli )		.AND.;
				 Empty( GDFieldGet( "AD7_NROPOR" ) ) )

		lRet := .F.                
		Help( "", 1, "OBRIGAT" ) 
	EndIf                                 
	
	If lRet
	   If !Empty( cCodCli ) .AND. !Empty( cCodPro )
			Aviso( STR0043, STR0044, { STR0026 }, 2 ) //"Atencao"###"Informe apenas o codigo do cliente ou prospect."###"Ok"
			lRet := .F. 
		EndIf 	
	EndIf 	       

	If lRet .AND. lExistCp
	   If Empty( cEmails ) .AND. cTipoAg == "R"
			Aviso( STR0043, STR0097, { STR0026 }, 2 ) //"Atencao"###"Informe apenas o codigo do cliente ou prospect."###"Ok"
			lRet := .F. 
		EndIf 	
	EndIf 

	//Ŀ
	//Verifica os interavalos das datas                                       
	//
	If !IsIncallStack("TMKA710")
			If lRet .AND. lMV_VLD_HOR
				For nAuxFor := 1 to Len( aCols )
		
					If !aCols[nAuxFor][Len( aHeader ) + 1] .AND. nAuxFor <> n						
				
						If	(   dData1 == aCols[nAuxFor][nPosDt1]		.AND. ;
							( ( cHora2 <= aCols[nAuxFor][nPosHr2]		.AND. ;
								cHora2 > aCols[nAuxFor][nPosHr1] )		.OR. ;
							(	cHora1 < aCols[nAuxFor][nPosHr2]		.AND. ;
								cHora1 >= aCols[nAuxFor][nPosHr1] ) ) )
							
							Aviso( STR0043, STR0053, { STR0026 }, 2 ) //"Atencao"###"O intervalo de horario informado ja esta alocado para este representante"###"Ok"
							lRet := .F.                           
							Exit
						Endif	
					Endif
				Next nAuxFor       
				
			Endif
				
		
		If lRet .AND. lMV_VLD_HOR
		
			//Ŀ
			//Verifica se o horario nao esta alocado na Base                          
			//
			DbSelectArea( "AD7" )
			dbSetOrder( 1 )
			DbSeek( xFilial( "AD7" ) + SA3->A3_COD + Dtos( dData1 ) )
			
			While !Eof() .AND. xFilial("AD7")		== AD7->AD7_FILIAL	.AND.;
								AD7->AD7_VEND		== SA3->A3_COD		.AND.;
								DToS(AD7->AD7_DATA)	== DToS( dData1 )
	
		 			If !cHora1 == AD7->AD7_HORA1  //Nao pode ser o mesmo item
						If	(	cHora2 <= AD7->AD7_HORA2	.AND.	;
								cHora2 >= AD7->AD7_HORA1 )	.OR.	;
							(	cHora1 <= AD7->AD7_HORA2 	.AND.	;
								cHora1 >= AD7->AD7_HORA1 )
	                   
							If ( aScan( aRegWork, AD7->( RecNo() ) ) == 0 )
								Aviso( STR0043, STR0053, { STR0026 }, 2 ) //"Atencao"###"O intervalo de horario informado ja esta alocado para este representante"###"Ok"
								lRet := .F.
								Exit
							Endif			
						Endif
					EndIf
				AD7->( dbSkip() )			
	
			End
	
		Endif
	EndIf
	
EndIf 

//Ŀ
// Forca um Eof() para o correto funcionamento do campo memo              
//
AD7->( dbSetOrder( 1 ) ) 
AD7->( MsGoto(0) ) 

Return( lRet ) 

/*/


Ŀ
Funcao    Ft320Vld   Autor  Eduardo Riera          Data 16.11.1999
Ĵ
Descrio Validacao da agenda do Representante Tecnico.               
                                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
Retorno   ExpL1: Indica se foi validado                               
Ĵ
Observacao                                                            
                                                                      
ٱ


/*/
Function Ft320Vld()

Local cCampo    := AllTrim(ReadVar())
Local cConteudo := &(ReadVar())
Local cChave    := ""

Local lRetorno  := .T.
Local lCliente  := .F.
Local lProspect := .F.
Local lOpor     := .F.
Local lSeek     := .F. 
Local lFt321Ativ:= IsInCallStack('FT321ATIV')
Local cCodCli	:= ""
Local cLoja	  	:= ""
Local cProspe	:= ""
Local cLojPro	:= ""
Local cOpor	  	:= 0

Local lPyme 	:= If( Type( "__lPyme" ) <> "U", __lPyme, .F. )
Local nPCodCli  := If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_CODCLI"}),0)
Local nPLoja    := If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJA"}),0)
Local nPProsp   := If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_PROSPE"}),0)
Local nPLojPro  := If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJPRO"}),0)
Local nPOpor    := If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_NROPOR"}),0)

If lFt321Ativ
	cCodCli	:= FwFldGet('AD7_CODCLI')
	cLoja		:= FwFldGet('AD7_LOJA')
	cProspe	:= FwFldGet('AD7_PROSPE')
	cLojPro	:= FwFldGet('AD7_LOJPRO')
	cOpor		:= FwFldGet('AD7_NROPOR')
Else
	cCodCli	:= aCols[n,nPCodCli]
	cLoja		:= aCols[n,nPLoja]
	cProspe	:= aCols[n,nPProsp]
	cLojPro	:= aCols[n,nPLojPro]
	cOpor		:= aCols[n,nPOpor]
Endif

Do Case
	Case ( "AD7_CODCLI"$cCampo .OR. "AD7_LOJA"$cCampo )
		cChave   := xFilial("SA1")
		lSeek := .T.
		If ( "AD7_CODCLI"$cCampo )
			If Empty( cConteudo ) 
				lSeek := .F.
				If !lFt321Ativ
					aCols[n, nPLoja ] := Space( Len( AD7->AD7_LOJA ) )
				Else
					FwFldPut('AD7_LOJA',Space( Len( AD7->AD7_LOJA ) ))
				Endif 
			Else 
				If Empty(cLoja)  
					cChave += cConteudo 
				Else			
					cChave += cConteudo+cLoja
				EndIf	
			EndIf 
		Else
			cChave += cCodCli+cConteudo
		EndIf
		If lSeek .AND. ( !SA1->(dbSeek(cChave)) )
			lRetorno := .F.            	
			Help(" ",1,"REGNOIS")
		EndIf  
		
		
	Case ( "AD7_PROSPE"$cCampo .OR. "AD7_LOJPRO"$cCampo )
		lSeek  := .T. 
		cChave := xFilial("SUS")
		If ( "AD7_PROSPE"$cCampo )
			If Empty( cConteudo ) 
				lSeek := .F.
				If !lFt321Ativ
					aCols[n, nPLojPro ] := Space( Len( AD7->AD7_LOJPRO ) ) 
				Else
					FwFldPut('AD7_LOJPRO',Space( Len( AD7->AD7_LOJPRO ) ))				
				Endif
			Else 
				If Empty(cLojPro)  
					cChave += cConteudo 
				Else			                                 	
					cChave += cConteudo+cLojPro
				EndIf	          
			EndIf 	
		Else
			If Empty(cLojPro)
				cChave += cConteudo 
				lSeek := .F.
			Else			                                 	
				cChave += cProspe+cConteudo
			EndIf
		EndIf
		If lSeek .AND. ( !SUS->(dbSeek(cChave)) )
			lRetorno := .F.
			Help(" ",1,"FT320VLD01")
		EndIf
	
	
	Case ( "AD7_CONTAT" $ cCampo )
	
	
		SU5->(dbSetOrder(1))
		If ( SU5->(dbSeek(xFilial("SU5")+cConteudo)) )
		      
			lCliente  := .F. 
			lProspect := .F. 		
			lOpor     := .F. 
		    
			//Ŀ
			// Verifica na tabela de entidades x contatos                             
			//
			AC8->( dbSetOrder( 2 ) ) 	
	
			//Ŀ
			// Pesquisa o cliente                                                     
			//
			If !Empty(cCodCli)
				lCliente := AC8->( dbSeek( xFilial( "AC8" ) + "SA1" + xFilial( "SA1" ) + ;
					PadR( cCodCli + cLoja , Len( AC8->AC8_CODENT ) ) + cConteudo ) ) 
			EndIf 	
			 		
			If !lPyme
				//Ŀ
				// Pesquisa o prospect                                                    
				//
				If !Empty(cProspe) 
					lProspect := AC8->( dbSeek( xFilial( "AC8" ) + "SUS" + xFilial( "SUS" ) + ;
						PadR( cProspe + cLojPro, Len( AC8->AC8_CODENT ) ) + cConteudo ) )
				EndIf

				//Ŀ
				// Pesquisa a oportunidade                                                
				//
				If !Empty(cOpor)
					lOpor := AD9->( dbSeek( xFilial( "AD9" ) + cOpor ) ) 
				EndIf
			EndIf
			
			If !lCliente .AND. !lProspect .AND. !lOpor
				lRetorno := .F.
				Help(" ",1,"FT320VLD02")
			EndIf 
			
		Else
			lRetorno := .F.
			Help(" ",1,"REGNOIS")
		EndIf
	
	Case ( "AD7_NROPOR" $ cCampo )

		AD1->(dbSetOrder(1))
		If ( AD1->(dbSeek(xFilial("AD1")+cConteudo)) )
			//Ŀ
			// Verifica se o vendedor esta na oportunidade ou no time de vendas       
			//
			AD2->( dbSetOrder( 1 ) ) 
			If ( AD1->AD1_VEND <> SA3->A3_COD ) .AND. !AD2->( dbSeek( xFilial("AD2") + AD1->AD1_NROPOR + AD1->AD1_REVISA + SA3->A3_COD ) )
				lRetorno := .F.
				Help(" ",1,"FT320VLD03")
			EndIf

			If lRetorno .AND. ! Empty(cCodCli)
				If AD1->AD1_CODCLI <> cCodCli .OR. AD1->AD1_LOJCLI <> cLoja
					Help(" ", 1, STR0043, , STR0112, 1)	//"Ateno" ## "Oportunidade selecionada no  do cliente/loja posicionado."
					lRetorno := .F.
				EndIf
			ElseIf lRetorno .AND. ! Empty(cProspe)
				If AD1->AD1_PROSPE <> cProspe .OR. AD1->AD1_LOJPRO <> cLojPro
					Help(" ", 1, STR0043, , STR0113, 1)	//"Ateno" ## "Oportunidade selecionada no  do prospect/loja posicionado."
					lRetorno := .F.
				EndIf
			EndIf
		Else
			lRetorno := .F.
			Help(" ",1,"REGNOIS")
		EndIf

EndCase

Return(lRetorno)

/*


Ŀ
Funcao    Ft320DtIni Autor  Sergio Silveira        Data 12/01/2001
Ĵ
Descrio  Inicializador padrao da data da agenda                     
Ĵ
Sintaxe    ExpD1 := Ft320DtIni()                                      
Ĵ
Retorno    ExpD1 -> Data da Agenda                                    
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


*/

Function Ft320DtIni()

Local dDtIni := dDataBase 

//Ŀ
// Se possui ddata definida, inicializa            
//
If Type( "dData" ) == "D"
	dDtIni := dData 
EndIf 

Return( dDtIni )         

/*


Ŀ
Funcao    LimSemana  Autor  Sergio Silveira        Data 12/01/2001
Ĵ
Descrio  Devolve o inicio / Fim da semana baseado numa data         
Ĵ
Sintaxe    ExpA1 := LimSemana( ExpD1 )                                
Ĵ
Retorno    ExpA1 -> 1 - Dia inicial / 2 - Dia Final                   
Ĵ
Parametros ExpD1 -> Data referencia                                   
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


*/

Function LimSemana( dData ) 

Local dDataIni                           
Local dDataFim 

Local nDia := Dow( dData )

dDataIni := dData - ( nDia - 1 ) 
dDataFim := dData + ( 7 - nDia ) 

Return( { dDataIni, dDataFim } ) 

/*


Ŀ
Funcao    Ft320VldHr Autor  Sergio Silveira        Data 17/01/2001
Ĵ
Descrio  Validacao das horas digitadas no agendamento               
Ĵ
Sintaxe    ExpL1 := Ft320VldHr( ExpC1, ExpC2 )                        
Ĵ
Retorno    ExpL1 -> Validacao                                         
Ĵ
Parametros ExpC1 -> Hora inicio / ExpC2 -> Hora fim                   
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
14/08/2007Cleber M.      -Bops 130776: Tratamento para inclusao auto-
                         matica (Portal Protheus), para nao verificar
                         os campos ainda nao preenchidos no aCols.   
ٱ


*/

Function Ft320VldHr( cHoraIni, cHoraFim ) 

Local cVar := AllTrim( ReadVar() )
Local lRet := .F.

Default cHoraIni := ""
Default cHoraFim := ""

Do Case                                        
Case "AD7_HORA1" $ cVar
 
    cHoraIni   := StrTran( cHoraIni, ".", ":" ) 
	lRet := AtVldHora( cHoraIni ) 
	
	If lRet                                                                     
	    cHoraFim   		:= STRTRAN(ALLTRIM(STR(SomaHoras(cHoraIni,"0.30"))),".",":")
	    cHoraIni   		:= StrTran( cHoraIni, ".", ":" )
	    M->AD7_HORA1	:= StrTran( M->AD7_HORA1, ".", ":" )
		cHoraIni   		:= StrTran( cHoraIni, ":", "" ) 
		
        If IsBlind()
        	cHoraFim	:=  ""
        EndIf
		lRet := If( Empty( cHoraFim ), .T., cHoraFim >= cHoraIni )     
		
		If !lRet 
			Aviso( STR0024, STR0025, { STR0026 }, 2 ) 		 //"Atencao !"###"A hora inicial deve ser igual ou inferior a final"###"Ok"
		EndIf 
		
	EndIf 	

Case "AD7_HORA2" $ cVar

    cHoraFim   := StrTran( cHoraFim, ".", ":" ) 
	lRet := AtVldHora( cHoraFim ) 
	
	If lRet 
		cHoraIni   := StrTran( cHoraIni, ":", "" ) 
		cHoraFim   := StrTran( cHoraFim, ":", "" ) 
		lRet := If( Empty( cHoraIni ), .T., cHoraFim >= cHoraIni ) 
		
		If !lRet 
			Aviso( STR0024, STR0027, { STR0026 }, 2 ) // "Atencao !"###"A hora final deve ser igual ou inferior a inicial"###"Ok"
		EndIf 
		
	EndIf 	    
	
Case "AD8_HORA1" $ cVar
 
    cHoraIni   := StrTran( cHoraIni, ".", ":" ) 
	lRet := AtVldHora( cHoraIni ) 
	
	If lRet                                                                     
	    cHoraFim   := STRTRAN(ALLTRIM(STR(SomaHoras(cHoraIni,"0.30"))),".",":")
		cHoraIni   := StrTran( cHoraIni, ".", ":" ) 
		M->AD8_HORA1	:= StrTran( M->AD8_HORA1, ".", ":" )
		cHoraIni   := StrTran( cHoraIni, ":", "" ) 
		
        If IsBlind()
        	cHoraFim	:=  ""
        EndIf
		lRet := If( Empty( cHoraFim ), .T., cHoraFim >= cHoraIni )     
		
		If !lRet 
			Aviso( STR0024, STR0025, { STR0026 }, 2 ) 		 //"Atencao !"###"A hora inicial deve ser igual ou inferior a final"###"Ok"
		EndIf 
		
	EndIf 	

Case "AD8_HORA2" $ cVar

    cHoraFim   := StrTran( cHoraFim, ".", ":" ) 
	lRet := AtVldHora( cHoraFim ) 
	
	If lRet 
		cHoraIni   := StrTran( cHoraIni, ":", "" ) 
		cHoraFim   := StrTran( cHoraFim, ":", "" ) 
		lRet := If( Empty( cHoraIni ), .T., cHoraFim >= cHoraIni ) 
		
		If !lRet 
			Aviso( STR0024, STR0027, { STR0026 }, 2 ) // "Atencao !"###"A hora final deve ser igual ou inferior a inicial"###"Ok"
		EndIf 
		
	EndIf 	
 
 
EndCase

Return( lRet ) 

   
/*


Ŀ
Funcao    Ft320AdCon Autor  Sergio Silveira        Data 26/07/2002
Ĵ
Descrio  Adiciona os contatos do cliente                            
Ĵ
Sintaxe    Ft320AdCon( ExpC1, ExpC2, @ExpA1 )                         
Ĵ
Retorno    .T.                                                        
Ĵ
Parametros ExpC1 -> Cod. Cliente / ExpC2 -> Loja / ExpA1 -> Array no  
           qual os contatos serao adicionados                         
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


*/

Function Ft320AdCon( cCodCli, cLoja, aContatos ) 

Local aArea		:= GetArea()	
Local cAliasQry	:= GetNextAlias()
Local cQuery    := ""
Local cCodEnt   := PadR( cCodCli + cLoja, Len( AC8->AC8_CODENT ) )

cQuery := "SELECT AC8_CODCON,U5_HORAV1,U5_HORAV2,U5_DIAVIS FROM "
cQuery += RetSqlName( "AC8" ) + " AC8," + RetSqlName( "SU5" ) + " SU5 "
cQuery += "WHERE "
cQuery += "AC8_FILIAL='" + xFilial( "AC8" ) + "' AND "
cQuery += "AC8_ENTIDA='SA1' AND "
cQuery += "AC8_FILENT='" + xFilial( "SA1" ) + "' AND "
cQuery += "AC8_CODENT='" + cCodEnt + "' AND "
cQuery += "AC8.D_E_L_E_T_=' ' AND " 
cQuery += "U5_CODCONT=AC8_CODCON AND "
cQuery += "U5_FILIAL='"  + xFilial( "SU5" ) + "' AND "
cQuery += "SU5.D_E_L_E_T_=' '" 					

cQuery := ChangeQuery( cQuery ) 

DBUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), cAliasQry, .F.,.T. ) 

While (cAliasQry)->( !Eof() )
	If !Empty((cAliasQry)->U5_DIAVIS)
		AAdd( aContatos, { (cAliasQry)->AC8_CODCON, (cAliasQry)->U5_HORAV1, (cAliasQry)->U5_HORAV2, (cAliasQry)->U5_DIAVIS } )
	EndIf
	(cAliasQry)->( DBSkip() )
End

(cAliasQry)->( DBCloseArea() )

RestArea( aArea )

Return( .T. ) 

/*/


Ŀ
Funcao    Ft320Fil   Autor  Kleber Dias Gomes     Data  16/09/2003
Ĵ
Descrio Filtro da consulta dos contatos somente do cliente          
          ou do prospect.                                             
Ĵ
ParametrosNenhum.                                                     
                                                                      
Ĵ
Retorno   ExpL1: Indica se e valido.                                  
Ĵ
Uso       FATA320                                                     
ٱ


/*/
Function Ft320Fil()

Local aArea		:= GetArea()
Local cTmpTable	:= GetNextAlias()
Local cAliasAC8	:= GetNextAlias()
Local aStruct	:= {}
Local aBrowse1	:= {}
Local aTitulo1	:= {}
Local aBrowse2	:= {}
Local aTitulo2	:= {}
Local aOrdem	:= {}
Local oDlg
Local oSeek
Local oCol1
Local oCol2
Local oBrowse1
Local oBrowse2 
Local oOrdem
Local cOrd
Local oBtn1
Local oBtn2
Local oBtn3
Local cArqTrab	:= ""
Local cInd1Trab := ""
Local cInd2Trab := ""
Local cSeek		:= Space(30)
Local cQuery	:= ""
Local lExport	:= .F.
Local i			:= 0
Local nSvRecBrw := 0
Local lFt321Ativ:= IsInCallStack('FT321ATIV')
Local cCodCli	:= ""
Local cLoja		:= ""
Local cProspe	:= ""
Local cLojPro	:= ""
Local cArqInd	:= ""
Local nIndex	:= 0
Local nPCodCli	:= If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_CODCLI"}),0)
Local nPLoja	:= If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJA"}),0)
Local nPProspe	:= If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_PROSPE"}),0)
Local nPLojPro	:= If(!lFt321Ativ,aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJPRO"}),0)
Local lPyme 	:= If( Type( "__lPyme" ) <> "U", __lPyme, .F. )
Local cRealName := ""

Private aRotina := { {STR0045,"A70Visual"	, 0 , 2} }	//"Visualizar"

If lFt321Ativ
	cCodCli	:= FwFldGet('AD7_CODCLI')
	cLoja		:= FwFldGet('AD7_LOJA') 
	cProspe	:= FwFldGet('AD7_PROSPE')
	cLojPro	:= FwFldGet('AD7_LOJPRO')
Else
	cCodCli	:= aCols[n,nPCodCli]
	cLoja		:= aCols[n,nPLoja]
	cProspe	:= aCols[n,nPProsp]
	cLojPro	:= aCols[n,nPLojPro]
Endif

DbSelectArea("AC8")
DBSetOrder(2)

If __oTable <> NIL
	cTmpTable := __oTable:GetAlias()
	//Se o alias no existir mais, exclui a tabela temporria para ser criada uma nova.
	If Select(cTmpTable) == 0
		__oTable:Delete()
		FreeObj(__oTable)
	EndIf
EndIf

If __oTable == NIL
	//Ŀ
	// Monta arquivo de trabalho para armazenar os SU5 que possuem keywords   
	//
	aAdd(aStruct,{"TRB_CODCON" ,"C",TamSX3("U5_CODCONT")[1],0})
	aAdd(aStruct,{"TRB_CONTAT" ,"C",TamSX3("U5_CONTAT") [1],0})
	aAdd(aStruct,{"TRB_RECNO"  ,"N",TamSX3("U5_CODCONT")[1],0})

	//-------------------------------------------------------------------
	// Define o objeto da tabela temporria.  
	//-------------------------------------------------------------------
	__oTable := FWTemporaryTable():New(cTmpTable)

	//-------------------------------------------------------------------
	// Atribui o  os ndices.  
	//-------------------------------------------------------------------
	__oTable:SetFields(aStruct)
		
	//-------------------------------------------------------------------
	// Define os ndices.  
	//-------------------------------------------------------------------
	__oTable:AddIndex("1", {"TRB_CODCON"})
	__oTable:AddIndex("2", {"TRB_CONTAT"})
		
	//-------------------------------------------------------------------
	// Cria a tabela.  
	//-------------------------------------------------------------------
	__oTable:Create()
Else
	cRealName := __oTable:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
EndIf

cQuery := ""
cQuery += "SELECT DISTINCT U5_CODCONT, U5_CONTAT, SU5.R_E_C_N_O_ SU5RECNO FROM "
cQuery += RetSqlName("SU5") + " SU5, " + RetSqlName( "AC8" ) + " AC8 "
cQuery += "WHERE "
cQuery += "AC8.AC8_FILIAL  = '" + xFilial("AC8") + "' AND "
cQuery += "(AC8.AC8_ENTIDA = 'SA1' AND "
cQuery += "AC8.AC8_CODENT  = '" + PadR(cCodCli+cLoja,Len(AC8->AC8_CODENT)) + "'"

If !lPyme
	cQuery += " OR AC8.AC8_ENTIDA  = 'SUS' AND "
	cQuery += "AC8.AC8_CODENT  = '" + PadR(cProspe+cLojPro,Len(AC8->AC8_CODENT)) + "')"
Else
	cQuery += ")"
EndIf

cQuery += " AND AC8.AC8_CODCON  = SU5.U5_CODCONT AND "
cQuery += "AC8.D_E_L_E_T_  = ' ' AND "
cQuery += "SU5.D_E_L_E_T_  = ' '"
cQuery += " ORDER BY U5_CODCONT"

cQuery := ChangeQuery(cQuery)

DBUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAC8,.T.,.T.)

While (cAliasAC8)->( !Eof() )
	
	Reclock(cTmpTable,.T.)
	Replace (cTmpTable)->TRB_CODCON With (cAliasAC8)->U5_CODCONT
	Replace (cTmpTable)->TRB_CONTAT With (cAliasAC8)->U5_CONTAT
	Replace (cTmpTable)->TRB_RECNO  With (cAliasAC8)->SU5RECNO
	(cTmpTable)->(MsUnLock())	

	(cAliasAC8)->( DBSkip() )
End

DbSelectArea(cTmpTable)
(cTmpTable)->(DBGoTop())

aAdd(aBrowse1,{"TRB_CODCON"})
aAdd(aBrowse1,{"TRB_CONTAT"})
aAdd(aBrowse2,{"TRB_CONTAT"})
aAdd(aBrowse2,{"TRB_CODCON"})

aAdd(aTitulo1,{STR0046}) //"Codigo"
aAdd(aTitulo1,{STR0047}) //"Nome"
aAdd(aTitulo2,{STR0047}) //"Nome"
aAdd(aTitulo2,{STR0046}) //"Codigo

aAdd(aOrdem,STR0048) //"Contato"
aAdd(aOrdem,STR0047) //"Nome"

DEFINE MSDIALOG oDlg FROM  62,15 TO 310,460 TITLE OemToAnsi(STR0049) PIXEL //"Consulta Padrao - Contatos"

oBrowse1 := VCBrowse():New(10,12,166,75,,,,oDlg,,,,{|| (cSeek := Space(30),oSeek:Refresh())},{ || lExport := .T., nSvRecBrw := Recno(), oDlg:End() }, , , , , , , , ,.T.)
oBrowse1 := oBrowse1:GetBrowse()
oBrowse1:lLineDrag	:= .T.

oBrowse2 := VCBrowse():New(10,12,166,75,,,,oDlg,,,,{|| (cSeek := Space(30),oSeek:Refresh())},{ || lExport := .T., nSvRecBrw := Recno(), oDlg:End() }, , , , , , , , ,.T.)
oBrowse2 := oBrowse2:GetBrowse()
oBrowse2:lLineDrag	:= .T.
oBrowse2:Hide()
    
For i:=1 To Len(aBrowse1)
	oCol1 := TCColumn():New( OemToAnsi(aTitulo1[i,1]), &("{ || "+aBrowse1[i,1]+"}"),,,, "LEFT",, .F., .F.,,,, .F., )
	oBrowse1:AddColumn(oCol1)
Next i

For i:=1 To Len(aBrowse2)
	oCol2 := TCColumn():New( OemToAnsi(aTitulo2[i,1]), &("{ || "+aBrowse2[i,1]+"}"),,,, "LEFT",, .F., .F.,,,, .F., )
	oBrowse2:AddColumn(oCol2)
Next i

@ 091, 14 SAY OemToAnsi(STR0050) SIZE 42, 7 OF oDlg PIXEL //"Pesquisa por:"
@ 090, 59 COMBOBOX oOrdem VAR cOrd ITEMS aOrdem ON CHANGE ;
							(If(oOrdem:nAt==1,(dbSetOrder(1),oBrowse2:Hide()),;
							(dbSetOrder(2),oBrowse2:Show())),;
							dbGoTop(),;
							oBrowse1:Refresh(),;
							oBrowse2:Refresh(),;	
							cSeek:=Space(30),;
							oSeek:Refresh()) SIZE 119, 42 OF oDlg PIXEL //"Pesquisar por:"
@ 104, 23 SAY OemToAnsi(STR0051) SIZE 32, 7 OF oDlg PIXEL //"Localizar"

@ 104, 58 MSGET oSeek VAR cSeek SIZE 120, 10 OF oDlg PIXEL

oSeek:bLostFocus := { || If(dbSeek(AllTrim(cSeek)),(oBrowse1:Refresh(),oBrowse2:Refresh()),)}

DEFINE SBUTTON oBtn1 FROM 09, 187 TYPE 1 ENABLE OF oDlg ACTION (lExport := .t., nSvRecBrw := Recno(), oDlg:End()) // DEFAULT
oBtn1:lAutDisable := .F.
DEFINE SBUTTON oBtn2 FROM 23, 187 TYPE 2 ENABLE OF oDlg ACTION oDlg:End()
DEFINE SBUTTON oBtn3 FROM 37, 187 TYPE 15 ENABLE ACTION (If(!Empty((cTmpTable)->TRB_RECNO),(DbSelectArea("SU5"),SU5->(DBGoTo((cTmpTable)->TRB_RECNO)),A70Visual("SU5",SU5->(Recno()),1),DbSelectArea(cTmpTable)),)) OF oDlg

cSeek := &(ReadVar())
If !Empty(cSeek)
	If !dbSeek(cSeek)
		dbGoTo(nSvRecBrw)
	EndIf
Else
	dbGoTop()
EndIf
cSeek := Space(30)

ACTIVATE MSDIALOG oDlg

If lExport
	SU5->(DBGoto((cTmpTable)->TRB_RECNO))
EndIf

(cAliasAC8)->( DBCloseArea() )

RestArea(aArea)

Return(lExport)

/*/


Ŀ
Funcao    Ft320Apon  Autor Kleber Dias Gomes       Data  20/10/03 
Ĵ
Descrio Apontamentos de Custos da Agenda                            
Ĵ
Retorno    Verdadeiro                                                 
Ĵ
Parametros Nenhum                                                     
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft320Apon()

Local aArea    := GetArea()
Local aAreaSA3 := SA3->(GetArea())
Local aRotSav  := aClone(aRotina)
Local aColsSav := aClone(aCols)
Local aHeaderS := aClone(aHeader)
Local aCampos  := {}
Local nSav     := n
Local nPNrOpor := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_NROPOR"})
Local nPData   := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_DATA"})
Local nPCodCli := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_CODCLI"})
Local nPLoja   := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJA"})
Local nPDataAP := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_DATAAP"})
Local nPVendAP := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_VENDAP"})
Local nPSeqAP  := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_SEQAP"})
Local nPProsp  := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_PROSPE"})
Local nPLjPro  := aScan(aHeader,{|x| AllTrim(x[2])=="AD7_LOJPRO"})

PRIVATE aRotina := {	{ "","",0,1},; //"Pesquisar"
						{ "","",0,2},; //"Visual"
						{ "","",0,3},; //"Incluir"
						{ "","",0,4},; //"Alterar"
						{ "","",0,5}}  //"Exclusao"
                                                   
DbSelectArea("AD5")
dbSetOrder(1)
If DbSeek(xFilial("AD5")+aCols[n,nPVendAP]+DtoS(aCols[n,nPDataAP])+aCols[n,nPSeqAP])
	aAdd(aCampos,{"AD5_VEND",""})
	aAdd(aCampos,{"AD5_NOMVEN",""})
	aAdd(aCampos,{"AD5_DATA",""})
	aAdd(aCampos,{"AD5_NROPOR",""})
	aAdd(aCampos,{"AD5_CODCLI",""})
	aAdd(aCampos,{"AD5_LOJA",""})

	Ft310SetAg(aCampos)
	FWExecView(STR0009, "FATA310", MODEL_OPERATION_UPDATE) //Apontamentos
		
	aRotina  := aClone(aRotSav)
	aCols    := aClone(aColsSav)
	aHeader  := aClone(aHeaderS)
	n        := nSav
Else
	If !Empty(aCols[n,nPNrOpor])
		aAdd(aCampos,{"AD5_VEND",SA3->A3_COD})
		aAdd(aCampos,{"AD5_NOMVEN",SA3->A3_NOME})
		aAdd(aCampos,{"AD5_DATA",aCols[n,nPData]})
		aAdd(aCampos,{"AD5_NROPOR",aCols[n,nPNrOpor]})
		aAdd(aCampos,{"AD5_CODCLI",aCols[n,nPCodCli]})
		aAdd(aCampos,{"AD5_LOJA",aCols[n,nPLoja]})
		aAdd(aCampos,{"AD5_PROSPE",aCols[n,nPProsp]})
		aAdd(aCampos,{"AD5_LOJPRO",aCols[n,nPLjPro]})

		Ft310SetAg(aCampos)
		FWExecView(STR0009, "FATA310", MODEL_OPERATION_INSERT) //Apontamentos

		aRotina  := aClone(aRotSav)
		aCols    := aClone(aColsSav)
		aHeader  := aClone(aHeaderS)
		n        := nSav
		aCols[n,nPVendAP]:= AD5->AD5_VEND
		aCols[n,nPDataAP]:= AD5->AD5_DATA
		aCols[n,nPSeqAP] := AD5->AD5_SEQUEN
	ElseIf ((!Empty(aCols[n,nPCodCli]) .AND. !Empty(aCols[n,nPLoja]))) .OR.;
		((!Empty(aCols[n,nPProsp]) .AND. !Empty(aCols[n,nPLjPro])))
		aAdd(aCampos,{"AD5_VEND",SA3->A3_COD})
		aAdd(aCampos,{"AD5_NOMVEN",SA3->A3_NOME})
		aAdd(aCampos,{"AD5_DATA",aCols[n,nPData]})
		aAdd(aCampos,{"AD5_CODCLI",aCols[n,nPCodCli]})
		aAdd(aCampos,{"AD5_LOJA",aCols[n,nPLoja]})
		aAdd(aCampos,{"AD5_NROPOR",""})
		aAdd(aCampos,{"AD5_PROSPE",aCols[n,nPProsp]})
		aAdd(aCampos,{"AD5_LOJPRO",aCols[n,nPLjPro]})

		Ft310SetAg(aCampos)
		FWExecView(STR0009, "FATA310", MODEL_OPERATION_INSERT) //Apontamentos
	
		aRotina  := aClone(aRotSav)
		aCols    := aClone(aColsSav)
		aHeader  := aClone(aHeaderS)
		n        := nSav
		aCols[n,nPVendAP]:= AD5->AD5_VEND
		aCols[n,nPDataAP]:= AD5->AD5_DATA
		aCols[n,nPSeqAP] := AD5->AD5_SEQUEN
	Else
		Help(" ",1,"FT320APON")
	EndIf
EndIf

RestArea(aAreaSA3)
RestArea(aArea)

Return(.T.)

/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()

Private aRotina := {	{ OemToAnsi(STR0002),"AxPesqui"		,0,1,0,.F.},;		// "Pesquisar"
						{ OemToAnsi(STR0003),"Ft320Work"	,0,2,0,.F.},;		// "WorkArea"
						{ OemToAnsi(STR0012),"Ft320Visit"	,0,3,0,.F.},;		// "Prg.Visita"
						{ OemToAnsi(STR0017),"Ft320DVis"	,0,5,0,.F.}}		// "Exc.Visita"

//Ŀ
//Chama a workarea antiga, caso a nova nao esteja habilitada
//

If ExistBlock("FT320MNU")
	ExecBlock("FT320MNU",.F.,.F.)
EndIf

Return(aRotina)


/*


ͻ
Programa  FilOrcam  Autor   Robson Nayland      Data  04/02/2008  
͹
Desc.     Carrega ORCAMENTOS/PROPOSTA do vendedor                     
                                                                      
͹
Uso                                                                   
ͼ


*/
Static Function FT320LoadSCJ(oProposta,lGoTop)

Local aArea		:= GetArea()
Local aAreaSCJ	:= SCJ->( GetArea() )
Local cFilSA1	:= SA1->(DbFilter())
Local cAlias	:= "AD1"
Local cNomeCli	:= ""
Local cNomePro	:= ""
Local cFilSCJ	:= xFilial("SCJ")
Local aProposta	:= {}
Local cFatProp	:= SuperGetMV("MV_FATPROP",,"O")		// Abre tela de Orcamento ou proposta Comercial 
Local nCont		:= 0
Local nLin		:= 0

Static nLstRecPro	:= 0

Default lGoTop		:= .T.

If lGoTop
	nLstRecPro	:= 0
Else
	aProposta	:= oProposta:aArray
	nLin		:= oProposta:nAt
EndIf


If 	cFatProp $ "P"
	SCJ->( dbSetOrder(5) )	//CJ_FILIAL+CJ_NROPOR+CJ_REVISA
Else
	SCJ->( dbSetOrder(1) )	//CJ_FILIAL+CJ_NUM+CJ_CLIENTE+CJ_LOJA
Endif

ADJ->(DbSetOrder(1))
AD1->(DbSetOrder(1))
	
DBSelectArea("TRBAD1")
DBGoTop()

If nLstRecPro > 0
	While !TRBAD1->( Eof() ) .AND. TRBAD1->( Recno()) <> nLstRecPro
		TRBAD1->(DbSkip())
	End
End

While !TRBAD1->( Eof() )
	
	nCont++
	
	AD1->(DbSeek(TRBAD1->(xFilial("AD1")+ADL_CODOPO)))
	
	If cFatProp == "O" .AND. (cAlias)->AD1_STATUS $ "13" .AND. !EMPTY((cAlias)->AD1_NUMORC) // Aberto ou Suspenso E COM ORCAMENTO
		If SCJ->(Dbseek(cFilSCJ+(cAlias)->AD1_NUMORC)) 

			cNomeCli	:= If(!Empty(SCJ->CJ_CLIENTE),Posicione("SA1",1,xFilial("SA1")+SCJ->CJ_CLIENTE+SCJ->CJ_LOJA,"A1_NOME"),"")
			cNomePro	:= If(!Empty(SCJ->CJ_PROSPE),Posicione("SUS",1,xFilial("SUS")+SCJ->CJ_PROSPE+SCJ->CJ_LOJPRO,"US_NOME"),"")
		
			aAdd(aProposta,{	SCJ->CJ_NUM,SCJ->CJ_PROSPE,SCJ->CJ_LOJPRO,cNomePro,SCJ->CJ_CLIENTE,;
								SCJ->CJ_LOJA,cNomeCli})
			
		EndIf
		
	ElseIf	cFatProp == "P" .AND. (cAlias)->AD1_STATUS $ "13" // Aberto ou Suspenso E COM ORCAMENTO

		If SCJ->(Dbseek( cFilSCJ + (cAlias)->(AD1_NROPOR+AD1_REVISA)))

			cNomeCli	:= If(!Empty(SCJ->CJ_CLIENTE),Posicione("SA1",1,xFilial("SA1")+SCJ->CJ_CLIENTE+SCJ->CJ_LOJA,"A1_NOME"),"")
			cNomePro	:= If(!Empty(SCJ->CJ_PROSPE),Posicione("SUS",1,xFilial("SUS")+SCJ->CJ_PROSPE+SCJ->CJ_LOJPRO,"US_NOME"),"")

			While !SCJ->(Eof()) 						.AND.;
				SCJ->CJ_FILIAL	== cFilSCJ 				.AND.;
				SCJ->CJ_NROPOR	== (cAlias)->AD1_NROPOR	.AND.;
				SCJ->CJ_REVISA	== (cAlias)->AD1_REVISA
				
				If aScan(aProposta,{|x| x[1] == SCJ->CJ_PROPOST }) == 0
					aAdd( aProposta, {	SCJ->CJ_PROPOST,SCJ->CJ_PROSPE,SCJ->CJ_LOJPRO ,;
				   						cNomePro,SCJ->CJ_CLIENTE,SCJ->CJ_LOJA,cNomeCli} )
				Endif         
				
				SCJ->(DbSkip())
				
			End

		EndIf
	
 	Endif     
 	
 	TRBAD1->( DBSkip() )
 	nLstRecPro	:= TRBAD1->(Recno())
 	
	If nCont >= NMAXPAGE
		Exit
	EndIf
	
Enddo	

If Len(aProposta) <= 0
	AAdd(aProposta,{"","","","","","",""})
EndIf
 
oProposta:SetArray(aProposta)
oProposta:bLine := {||{	aProposta[oProposta:nAt,1],;
						aProposta[oProposta:nAt,2],;
						aProposta[oProposta:nAt,3],;
						aProposta[oProposta:nAt,4],;
						aProposta[oProposta:nAt,5],;
						aProposta[oProposta:nAt,6],;
						aProposta[oProposta:nAt,7] } }
oProposta:GoTop()
oProposta:Refresh()

If nCont >= NMAXPAGE
	oProposta:bGoBottom := {||FT320LoadSCJ(@oProposta,.F.),oProposta:NAT := LEN(oProposta:AARRAY)}
	oProposta:bSkip		:= {|NSKIP, NOLD, nMax|nMax:=EVAL( oProposta:BLOGICLEN ),NOLD := oProposta:NAT, oProposta:NAT += NSKIP,;
												oProposta:NAT := MIN( MAX( oProposta:NAT, 1 ), nMax ),If(oProposta:nAt==nMax,FT320LoadSCJ(@oProposta,.F.),.F.),;
										 		oProposta:NAT - NOLD}	
EndIf

If nLin > 0
	oProposta:nAt := nLin
EndIf

RestArea( aAreaSCJ )
RestArea( aArea ) 

Return .T.

/*


ͻ
Programa  FT320Time Autor  Vendas Clientes      Data   26/02/08   
͹
Desc.     Monta vetor com o time de vendas sobre o vendedor passado no
          parametro                                                   
͹
Uso       FATA320                                                     
ͼ


*/
Function Ft320Time(cVend)

Local aGrupos 		:= {}
Local aTime 		:= {}
Local nI 			:= 0
Local aArea			:= GetArea()
Local aAreaSA3		:= SA3->(GetArea())
Local aAreaACA		:= ACA->(GetArea())
Local lProcTime		:= .T. 

DbSelectArea("SA3")
DbSetOrder(1)
DbSeek(xFilial("SA3")+cVend)

If (SA3->A3_GRPREP == cUltGrpPrc) .AND. (Len(aGrupo) > 0)
	aGrupos		:= aClone(aGrupo)
	If (cVend == cUltVndPrc) .AND. (Len(aGrupo) > 0)
		aTime		:= aClone(aTimeSTA)
		lProcTime	:= .F.
	EndIf
ElseIf (cVend == cUltVndPrc) .AND. (Len(aGrupo) == 0)
	aGrupos 	:= FtReprEst(cVend)
	aGrupo		:= aClone(aGrupos)
ElseIf (cVend == cUltVndPrc) .AND. (Len(aGrupo) > 0)
	aGrupos		:= aClone(aGrupo)
	aTime		:= aClone(aTimeSTA)
	lProcTime	:= .F.
Else
	aGrupos 	:= FtReprEst(cVend) 
	aGrupo		:= aClone(aGrupos)
	cUltVndPrc	:= cVend 
EndIf
     
cUltGrpPrc := SA3->A3_GRPREP

If lProcTime  

	DbSelectArea("SA3")
	SA3->(DbsetOrder(6)) //A3_FILIAL+A3_GRPREP
	For nI := 1 To Len(aGrupos)
		If SA3->(DbSeek(xFilial("SA3")+aGrupos[nI][1]))
			While !SA3->(Eof()) .And. xFilial("SA3")+aGrupos[nI][1] == SA3->(A3_FILIAL+A3_GRPREP)
				Aadd(aTime,SA3->A3_COD)
				SA3->(DbSkip())
			EndDo
		EndIf
	Next                   
	
	//Ŀ
	//Insere o vendedor da Workarea
	//
	If aScan(aTime,cVend) == 0
		AAdd(aTime,cVend)
	EndIf

	aTimeSTA := aClone(aTime)

EndIf

RestArea(aAreaSA3)
RestArea(aAreaACA)
RestArea(aArea)

Return(aTime)


/*

ͻ
Programa  Ft320Sup  Autor  Vendas Clientes      Data   26/02/08   
͹
Desc.     Monta vetor com o superior do vendedor                      
                                                                      
͹
Uso       FATA320                                                     
ͼ

*/
Function Ft320Sup(cVend,cCargo,cVendOri)
Local aArea		:= {}
Local aAreaSA3	:= {}
Local aAreaACA	:= {}
Local cGrupo	:= ""
Local cFilSA3	:= xFilial("SA3")
Local cFilACA	:= xFilial("ACA")
Local _aEstru	:= {} 
Local cFilterA3	:= SA3->(DbFilter())

DEFAULT cVendOri:= ""	//Vendedor original
DEFAULT cVend	:= ""	//Vendedor
DEFAULT cCargo	:= ""	//Cargo solicitado na funcao

If !Empty(cFilterA3)
	SA3->(DbClearFilter())
EndIf

aArea			:= GetArea()
aAreaSA3		:= SA3->(GetArea())
aAreaACA		:= ACA->(GetArea())

ACA->(DbSetOrder(1)) //ACA_FILIAL+ACA_GRPSUP
SA3->(DbSetOrder(1)) //A3_FILIAL+A3_COD

If Empty(cVendOri)
	cVendOri := cVend
Endif

If SA3->(DbSeek(cFilSA3+cVend))
	//Se pertence a um grupo e este grupo e superior a algum outro
	If !Empty(SA3->A3_GRPREP) .AND. ACA->(DbSeek(cFilACA+SA3->A3_GRPREP))
		cGrupo := ACA->ACA_GRPSUP
		If Alltrim(SA3->A3_CARGO) $ alltrim(cCargo)
			_aEstru := Ft320AchaSup(_aEstru,cGrupo,cVend,cCargo)
			If Len(_aEstru) = 0
				AADD(_aEstru,{cVend,cVend,Posicione("SA3",1,xFilial("SA3")+cVend,"A3_UNIDAD")})
			EndIf
			Return(_aEstru)
		EndIf
		
		DbSelectArea("SA3")
		SA3->(DbSetOrder(6)) //A3_FILIAL+A3_GRPREP
		If SA3->(DbSeek( cFilSA3 + cGrupo ))
		
			While !SA3->(Eof()) .And.cFilSA3+cGrupo == SA3->(A3_FILIAL+A3_GRPREP)
				If 	Alltrim(SA3->A3_CARGO) $ Alltrim(cCargo) .AND. (aScan(_aEstru,{|x|x[2]==SA3->A3_COD}) == 0)
					AADD(_aEstru,{cVendOri,SA3->A3_COD,SA3->A3_UNIDAD})
					Exit
				Else
					cVend := SA3->A3_COD
				EndIf
				SA3->(DbSkip())
			End
			
			If Len(_aEstru) == 0 .AND. Alltrim(cGrupo) <> "MAINGR"
				_aEstru := Ft320Sup(cVend,cCargo,cVendOri)
			Endif
		Endif
	EndIf
Endif

If Len(_aEstru) = 0
	AADD(_aEstru,{cVendOri,cVendOri,Posicione("SA3",1,xFilial("SA3")+cVendOri,"A3_UNIDAD")})
EndIf

If !Empty(cFilterA3)
	DbSelectArea("SA3")
	DbSetFilter({||&cFilterA3},cFilterA3)
EndIf

RestArea(aAreaSA3)
RestArea(aAreaACA)
RestArea(aArea)

Return(_aEstru)

/*

ͻ
Programa  Ft320AchaSupAutor  Vendas Clientes      Data   26/02/08   
͹
Desc.                                                                   
                                                                        
͹
Uso       FATA320                                                       
ͼ

*/
Static Function Ft320AchaSup(_aEstru,cGrupoSup,cVend,cCargo)

ACA->(DbSetOrder(1))
ADK->(DbSetOrder(1))
If ACA->(DbSeek(xFilial("ACA")+cGrupoSup))
	cGrupoSup := ACA->ACA_GRPSUP
	If !Empty(cGrupoSup)
		DbSelectArea("SA3")
		SA3->(DbSetOrder(6))
		If SA3->(DbSeek(xFilial("SA3")+cGrupoSup))
			While !SA3->(Eof()) .And. xFilial("SA3")+cGrupoSup == SA3->(A3_FILIAL+A3_GRPREP)
				If Alltrim(SA3->A3_CARGO) $ Alltrim(cCargo)
					AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
					Return(_aEstru)
				EndIf
				SA3->(DbSkip())
			EndDo
			If Len(_aEstru) = 0
				Ft320AchaSup(_aEstru,cGrupoSup,cVend,cCargo)
			EndIf
		Else
			DbSelectArea("SA3")
			SA3->(DbSetOrder(1))
			If SA3->(DbSeek(xFilial("SA3")+cVend))
				AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
				Return(_aEstru)
			EndIf
		EndIf
	Else
		DbSelectArea("SA3")
		SA3->(DbSetOrder(1))
		If SA3->(DbSeek(xFilial("SA3")+cVend))
			AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
			Return(_aEstru)
		EndIf
	EndIf
Else
	If ADK->(DbSeek(xFilial("ADK")+cGrupoSup))
		cGrupoSup := ADK->ADK_GRUNVE
		If !Empty(cGrupoSup)
			If ACA->(DbSeek(xFilial("ACA")+cGrupoSup))
				cGrupoSup := ACA->ACA_GRPSUP
				If !Empty(cGrupoSup)
					DbSelectArea("SA3")
					SA3->(DbSetOrder(6))
					If SA3->(DbSeek(xFilial("SA3")+cGrupoSup))
						While !SA3->(Eof()) .And. xFilial("SA3")+cGrupoSup == SA3->(A3_FILIAL+A3_GRPREP)
							If Alltrim(SA3->A3_CARGO) $ Alltrim(cCargo)
								AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
								Return(_aEstru)
							EndIf
							SA3->(DbSkip())
						EndDo
						If Len(_aEstru) = 0
							Ft320AchaSup(_aEstru,cGrupoSup,cVend,cCargo)
						EndIf
					Else
						DbSelectArea("SA3")
						SA3->(DbSetOrder(1))
						If SA3->(DbSeek(xFilial("SA3")+cVend))
							AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
							Return(_aEstru)
						EndIf
					EndIf
				Else
					DbSelectArea("SA3")
					SA3->(DbSetOrder(1))
					If SA3->(DbSeek(xFilial("SA3")+cVend))
						AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
						Return(_aEstru)
					EndIf
				EndIf
			EndIf
		Else
			DbSelectArea("SA3")
			SA3->(DbSetOrder(1))
			If SA3->(DbSeek(xFilial("SA3")+cVend))
				AADD(_aEstru,{cVend,SA3->A3_COD,SA3->A3_UNIDAD})
				Return(_aEstru)
			EndIf
		EndIf
	EndIf
		
EndIf
Return(_aEstru)


/*


ͻ
Programa  Ft320SomHrAutor  Vendas Clientes      Data   07/05/08   
͹
Desc.     Soma o tempo minimo entre as tarefas, apos informar a hora  
          inicial                                                     
͹
Uso       FATA320                                                     
ͼ


*/
Function Ft320SomHr(cHrs,cMin)

Default	cMin	:= "00:30"

Return AtSomaHora(cHrs,HoraToInt(cMin,2))

/*


ͻ
Programa  Ft320FilTbAutor  Vendas Clientes      Data   28/08/08   
͹
Desc.     Cria tabela temporaria para o alias informado, filtrando    
          registros via SQL.                                          
͹
Uso       FATA320                                                     
ͼ


*/
Function Ft320FilTb(aVend,cAlias,cFiltro,cArquivo,lSoQuery)

Local aArea			:= GetArea() 
Local aAreaSA3		:= SA3->(GetArea())
Local cArqTmp		:= ""
Local cQuery		:= ""   
Local cModelo		:= ""
Local cConcat		:= "" 

Default cFiltro		:= ""
Default lSoQuery	:= .F.

DbSelectArea("SA3")
DbSetOrder(1)
DbSeek(xFilial("SA3")+cVendAtu) 

//Ŀ
//Verifica se o vendedor atual possui subordinados
//
If lSuperior == Nil
	lSuperior := Len(aGrupo) >= 1
EndIf

//Ŀ
//Define o simbolo de concatenacao de acordo com o banco
//
If Upper(TcGetDb()) $ "ORACLE,POSTGRES,DB2,INFORMIX"
	cConcat := "||"
Else
	cConcat	:= "+"
Endif 
              
Do Case 
	
	//Ŀ
	//Filtro das oportunidades
	//
	Case cAlias == "AD1"
		
		AD1->(DbSetOrder(1))
		
		If IsInCallStack("FATA320")
			cModelo := aAcessACA[ 5, 2 ]
		Else
			cModelo := WRK_TODOS
		EndIf
        
		If cArquivo == Nil
			cArqTmp	:= "TRBAD1"
		Else
			cArqTmp	:= cArquivo
		EndIf
  		
  		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf
  		
		//Ŀ
		//Oportunidades onde foi envolvido no cabecalho
		//
		If (cModelo <> WRK_TODOS)                          
		
			cQuery	:= "SELECT DISTINCT ADL_CODOPO " 
			cQuery	+= " FROM " + RetSqlName("ADL") + " ADL "  

			//Cria o Join SOMENTE quando houver filtro
			If !Empty(cFiltro)
				cQuery	+= " INNER JOIN " + RetSqlName("AD1") + " AD1 "
				cQuery	+= " ON AD1.AD1_NROPOR"+cConcat+"AD1.AD1_REVISA = ADL.ADL_CODOPO AND AD1.AD1_FILIAL = '" + xFilial("AD1") + "'" 
			  	cQuery	+= " AND AD1.D_E_L_E_T_ = ' ' "
			EndIf
			
			cQuery	+= " WHERE ADL_FILIAL = '" + xFilial("ADL") + "' " 
			cQuery	+= " AND ADL_CODOPO <> ' ' "
			
			cQuery += " AND ADL.D_E_L_E_T_ = ' ' " 
			
			If lSuperior     
	   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
	   		Else
	   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
	   		EndIf
	
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf

		Else

			cQuery	:= "SELECT DISTINCT AD1_NROPOR"+cConcat+"AD1_REVISA ADL_CODOPO "
			cQuery	+= " FROM " + RetSqlName("AD1") + " AD1 "
			cQuery	+= " WHERE AD1_FILIAL = '" + xFilial("AD1") + "'"

			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf
	
			cQuery += " AND AD1.D_E_L_E_T_ = ' ' " 
			

		EndIf

		cQuery	+= " ORDER BY ADL_CODOPO "
		
		If !lSoQuery		
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)			
			(cArqTmp)->(DbGoTop())
		EndIf
    
	//Ŀ
	//Filtro de clientes
	//
	Case cAlias == "SA1"
		
		SA1->(DbSetOrder(1))
		
   		If IsInCallStack("FATA320") 
	   		cModelo := aAcessACA[ 1, 2 ]
   	 	Else
   			cModelo := WRK_TODOS
   	 	EndIf

		If cArquivo == Nil
			cArqTmp	:= "TRBSA1"
		Else
			cArqTmp	:= cArquivo
		EndIf
  		
  		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf   
  		
  		If cModelo <> WRK_TODOS  
  		
	  		cQuery	:= "SELECT DISTINCT ADL_CODENT,ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("ADL") + " ADL"
			
			//Cria o Join SOMENTE quando houver filtro
			If !Empty(cFiltro)
				cQuery	+= " INNER JOIN "+RetSqlName("SA1") + " SA1"
				cQuery	+= " ON SA1.A1_COD = ADL.ADL_CODENT AND SA1.A1_LOJA = ADL.ADL_LOJENT"
				cQuery	+= " AND SA1.A1_FILIAL = ADL.ADL_FILENT" 
			  	cQuery	+= " AND SA1.D_E_L_E_T_ = ' ' "
			EndIf
			
			cQuery	+= " WHERE ADL.ADL_FILIAL = '"+xFilial("ADL")+"'"
			
			If lSuperior     
	   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
	   		Else
	   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
	   		EndIf
	
			cQuery	+= " AND ADL.ADL_FILENT = '" + xFilial("SA1") + "'"
			cQuery	+= " AND ADL.ADL_ENTIDA = 'SA1' "

		  	cQuery	+= " AND ADL.D_E_L_E_T_ = ' ' "
		
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf

		Else
			cQuery	:= "SELECT DISTINCT SA1.A1_COD ADL_CODENT, SA1.A1_LOJA ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("SA1") + " SA1 "   
			cQuery	+= " WHERE SA1.A1_FILIAL = '"+xFilial("SA1")+"'"
		  	cQuery	+= " AND SA1.D_E_L_E_T_ = ' ' "
		  
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf
		  	
		EndIf

		If cModelo == WRK_REPINDF
		
			cQuery += " UNION "
			cQuery += " SELECT DISTINCT SA1B.A1_COD ADL_CODENT, SA1B.A1_LOJA ADL_LOJENT FROM " + RetSqlName("SA1") + " SA1B "
			cQuery += " WHERE SA1B.A1_FILIAL = '" + xFilial("SA1") + "' AND SA1B.A1_VEND = '' "
			
			cQuery += " AND SA1B.D_E_L_E_T_ = ' ' "
			
			If !Empty(cFiltro)               
				cFiltro := StrTran(cFiltro,"SA1.","SA1B.")
				cQuery += " " + cFiltro + " "
			EndIf
	
		EndIf

		cQuery	+= " ORDER BY ADL_CODENT,ADL_LOJENT "

		If !lSoQuery		
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)		
			(cArqTmp)->(DbGoTop())
		EndIf

	//Ŀ
	//Filtro de prospects
	//
	Case cAlias == "SUS"
		
		SUS->(DbSetOrder(1))
		           
		If IsInCallStack("FATA320")
	   		cModelo := aAcessACA[ 2, 2 ]  
		Else
			cModelo := WRK_TODOS
		EndIf
   		
		If cArquivo == Nil
			cArqTmp	:= "TRBSUS"
		Else
			cArqTmp	:= cArquivo
		EndIf
  		
  		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf
  		
		If cModelo <> WRK_TODOS 
		
	  		cQuery	:= "SELECT DISTINCT ADL_CODENT,ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("ADL") + " ADL"
			
			//Cria o Join SOMENTE quando houver filtro
			If !Empty(cFiltro)
				cQuery	+= " INNER JOIN "+RetSqlName("SUS") + " SUS"
				cQuery	+= " ON SUS.US_COD = ADL.ADL_CODENT AND SUS.US_LOJA = ADL.ADL_LOJENT"
				cQuery	+= " AND SUS.US_FILIAL = ADL.ADL_FILENT" 
			  	cQuery	+= " AND SUS.D_E_L_E_T_ = ' ' "
			EndIf
			
			cQuery	+= " WHERE ADL.ADL_FILIAL = '"+xFilial("ADL")+"'"
			
			If lSuperior     
	   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
	   		Else
	   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
	   		EndIf
	
			cQuery	+= " AND ADL.ADL_FILENT = '" + xFilial("SUS") + "'"
			cQuery	+= " AND ADL.ADL_ENTIDA = 'SUS' "
		  	cQuery	+= " AND ADL.D_E_L_E_T_ = ' ' "
		  
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf

		Else
			cQuery	:= "SELECT DISTINCT SUS.US_COD ADL_CODENT, SUS.US_LOJA ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("SUS") + " SUS "   
			cQuery	+= " WHERE SUS.US_FILIAL = '"+xFilial("SUS")+"'"
			cQuery	+= " AND SUS.D_E_L_E_T_ = ' ' "
		  
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf
		  	
		EndIf

		If cModelo == WRK_REPINDF
		
			cQuery += " UNION "
			cQuery += " SELECT DISTINCT SUSB.US_COD ADL_CODENT, SUSB.US_LOJA ADL_LOJENT FROM " + RetSqlName("SUS") + " SUSB "
			cQuery += " WHERE SUSB.US_FILIAL = '" + xFilial("SUS") + "' AND SUSB.US_VEND = '' "
			cQuery += " AND SUSB.D_E_L_E_T_ = ' ' "
		
			If !Empty(cFiltro)               
				cFiltro := StrTran(cFiltro,"SUS.","SUSB.")
				cQuery += " " + cFiltro + " "
			EndIf
	
		EndIf

		cQuery	+= " ORDER BY ADL_CODENT,ADL_LOJENT "

		If !lSoQuery		
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)		
			(cArqTmp)->(DbGoTop())
		EndIf

	//Ŀ
	//Filtro de suspects 
	//
	Case cAlias == "ACH"
		
		ACH->(DbSetOrder(1))
		
		If IsInCallStack("FATA320")
			cModelo := aAcessACA[ 2, 2 ]  
		Else
			cModelo := WRK_TODOS
		EndIf
		   		
		If cArquivo == Nil
			cArqTmp	:= "TRBACH"
		Else
			cArqTmp	:= cArquivo
		EndIf
  		
  		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf
  		
		If cModelo <> WRK_TODOS
		 
	  		cQuery	:= "SELECT DISTINCT ADL_CODENT,ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("ADL") + " ADL"
			
			//Cria o Join SOMENTE quando houver filtro
			If !Empty(cFiltro)
				cQuery	+= " INNER JOIN "+RetSqlName("ACH") + " ACH"
				cQuery	+= " ON ACH.ACH_CODIGO = ADL.ADL_CODENT AND ACH.ACH_LOJA = ADL.ADL_LOJENT"
				cQuery	+= " AND ACH.ACH_FILIAL = ADL.ADL_FILENT" 
			  	cQuery	+= " AND ACH.D_E_L_E_T_ = ' ' "
			EndIf
			
			cQuery	+= " WHERE ADL.ADL_FILIAL = '"+xFilial("ADL")+"'"
			
			If lSuperior     
	   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
	   		Else
	   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
	   		EndIf
	
			cQuery	+= " AND ADL.ADL_FILENT = '" + xFilial("ACH") + "'"
			cQuery	+= " AND ADL.ADL_ENTIDA = 'ACH' "

		  	cQuery	+= " AND ADL.D_E_L_E_T_ = ' ' "
		  	

			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf

		Else
			cQuery	:= "SELECT DISTINCT ACH.ACH_CODIGO ADL_CODENT, ACH.ACH_LOJA ADL_LOJENT"
			cQuery	+= " FROM " + RetSqlName("ACH") + " ACH "   
			cQuery	+= " WHERE ACH.ACH_FILIAL = '"+xFilial("ACH")+"'"
		  	cQuery	+= " AND ACH.D_E_L_E_T_ = ' ' "
		  	
			If !Empty(cFiltro)
				cQuery += " " + cFiltro + " "
			EndIf
		  	
		EndIf

		If cModelo == WRK_REPINDF
		
			cQuery += " UNION "
			cQuery += " SELECT DISTINCT ACHB.ACH_CODIGO ADL_CODENT, ACHB.ACH_LOJA ADL_LOJENT FROM " + RetSqlName("ACH") + " ACHB "
			cQuery += " WHERE ACHB.ACH_FILIAL = '" + xFilial("ACH") + "' AND ACHB.ACH_VEND = '' "
			cQuery += " AND ACHB.D_E_L_E_T_ = ' ' "
			
			If !Empty(cFiltro)               
				cFiltro := StrTran(cFiltro,"ACH.","ACHB.")
				cQuery += " " + cFiltro + " "
			EndIf
	
		EndIf

		cQuery	+= " ORDER BY ADL_CODENT,ADL_LOJENT "

		If !lSoQuery		
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)		
			(cArqTmp)->(DbGoTop())
		EndIf
				
	//Ŀ
	//Pedidos de venda
	//		
	Case cAlias == "SC5" 
	
		If cArquivo == Nil
			cArqTmp	:= "TRBSC5"
		Else
			cArqTmp	:= cArquivo
		EndIf
	
		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf
	
		SC5->(DbSetOrder(1))
		
		cQuery	:= "SELECT DISTINCT C5_NUM FROM " + RetSqlName("SC5") + " SC5 "  
        
		//Relaciona com o ADL
		cQuery	+= "INNER JOIN " + RetSqlName("ADL") + " ADL ON SC5.C5_CLIENTE = ADL.ADL_CODENT AND SC5.C5_LOJACLI = ADL.ADL_LOJENT "
		cQuery	+= " AND ADL.ADL_FILIAL = '" + xFilial("ADL") + "' AND ADL.ADL_ENTIDA = 'SA1' "

		If lSuperior     
   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
   		Else
   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
   		EndIf

		cQuery += " AND ADL.D_E_L_E_T_ = ' ' "
		
		//Where
		cQuery	+= "WHERE SC5.C5_FILIAL = '" + xFilial("SC5") + "'"

		If !Empty(cFiltro)               
			cQuery += " " + cFiltro + " "
		EndIf

		cQuery += " AND SC5.D_E_L_E_T_ = ' ' "

		cQuery	+= " ORDER BY C5_NUM" 
             
		If !lSoQuery
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)		
			(cArqTmp)->(DbGoTop())
		EndIf
	
	Case cAlias == "SUC"
	
		If cArquivo == Nil
			cArqTmp	:= "TRBSUC"
		Else
			cArqTmp	:= cArquivo
		EndIf
	
		If Select(cArqTmp) > 0
  			(cArqTmp)->(DbCloseArea())
  		EndIf
	
		SUC->(DbSetOrder(1))
		
		cQuery	:= "SELECT DISTINCT UC_CODIGO FROM " + RetSqlName("SUC") + " SUC " 

		//Relaciona com o ADL
		cQuery	+= "INNER JOIN " + RetSqlName("ADL") + " ADL ON SUC.UC_CHAVE = ADL.ADL_CODENT" + cConcat + "ADL.ADL_LOJENT "
		cQuery	+= " AND ADL.ADL_FILIAL = '" + xFilial("ADL") + "' AND SUC.UC_ENTIDAD = ADL.ADL_ENTIDA "

		If lSuperior     
   			cQuery += " AND ADL.ADL_NIVE"+StrZero(SA3->A3_NIVEL,2)+" = '" + AllTrim(SA3->A3_NVLSTR) + "' "
   		Else
   			cQuery += " AND ADL.ADL_VEND = '" + cVendAtu + "' "
   		EndIf

		cQuery += " AND ADL.D_E_L_E_T_ = ' ' "
		
		//Where
		cQuery	+= "WHERE SUC.UC_FILIAL = '" + xFilial("SUC") + "'"

		If !Empty(cFiltro)               
			cQuery += " " + cFiltro + " "
		EndIf
	
		cQuery += " AND SUC.D_E_L_E_T_ = ' ' "
		
		cQuery	+= " ORDER BY UC_CODIGO "

		If !lSoQuery
			cQuery	:= ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cArqTmp,.T.,.T.)
			(cArqTmp)->(DbGoTop())
		EndIf
		
EndCase

RestArea(aAreaSA3)
RestArea(aArea)

Return cQuery

/*


ͻ
Programa  Ft320Brw  Autor  Vendas CRM           Data   29/08/08   
͹
Desc.     MBrowse especifico para areas de trabalho temporarias TOP,  
          utilizando o aRotina de cada cadastro.                      
͹
Uso        AP                                                         
ͼ


*/
/*


ͻ
Programa  Ft320AFixeAutor  Vendas CRM           Data   29/08/08   
͹
Desc.     Monta a lista de campos a serem exibidos no browse          
                                                                      
͹
Uso        AP                                                         
ͼ


*/
Function Ft320AFixe(cAlias,aCposSim,aCposNao)

Local aFixe			:= {}
Local aArea			:= GetArea()
Local aAreaSX3  	:= SX3->(GetArea() )

Default aCposSim	:= {}
Default aCposNao	:= {}

/*
[n][1]=>Descrio do campo
[n][2]=>Nome do campo
[n][3]=>Tipo
[n][4]=>Tamanho
[n][5]=>Decimal
[n][6]=>Picture
*/

DbSelectArea("SX3")
DbSetOrder(1)
DbSeek(cAlias)

While !SX3->(Eof()) .AND. SX3->X3_ARQUIVO == cAlias

	If	(Len(aCposNao)>0) .AND. (aScan(aCposNao,{|x|AllTrim(Upper(x)) == AllTrim(SX3->X3_CAMPO) })<>0)
		SX3->(DbSkip())
		Loop		
	Endif

	If (SX3->X3_BROWSE = 'S' .AND. SX3->X3_CONTEXT <> "V" .AND. SX3->X3_TIPO <> "M") .OR.;
		(If((Len(aCposSim)>0),(aScan(aCposSim,{|x|AllTrim(Upper(x)) == AllTrim(SX3->X3_CAMPO) })<>0),.F.))
	
		AAdd(aFixe,{	SX3->X3_TITULO	,;
						SX3->X3_CAMPO	,;
						SX3->X3_TIPO	,;
						SX3->X3_TAMANHO	,;
						SX3->X3_DECIMAL	,;
						SX3->X3_PICTURE	})
	EndIf

	SX3->(DbSkip())

End

RestArea(aAreaSX3)
RestArea(aArea)

Return aFixe
