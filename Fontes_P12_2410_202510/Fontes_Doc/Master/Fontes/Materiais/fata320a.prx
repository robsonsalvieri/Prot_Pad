#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA320A.CH"

#DEFINE NMAXPAGE	60
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320TelaBบAutor  ณVendas CRM          บ Data ณ  01/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณTela de browse de registros, utilizando SQL para filtro das บฑฑ
ฑฑบ          ณinformacoes.                                                บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Nome do arquivo aberto a ser exibido                บฑฑ
ฑฑบ          ณExpA2 - Lista de campos a serem exibidos no browse          บฑฑ
ฑฑบ          ณExpC3 - Titulo da rotina                                    บฑฑ
ฑฑบ          ณExpA4 - Lista de rotinas disponibilizadas na lateral da telaบฑฑ
ฑฑบ          ณExpC5 - Nome real do alias exibido                          บฑฑ
ฑฑบ          ณExpA6 - Cores da legenda do browse                          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft320TelaB(cAlias		, aFixe		, cCadastro	, aRot	,;
					cRealAlias	, aCores	)

Local aArea		:= GetArea()
Local __aButton	:= {} 
Local aSize		:= MsAdvSize() 
Local aCampos	:= {} 
Local aPesqIdx	:= {}
Local aPesqOrd	:= {}

Local oDlg		:= Nil
Local oBtnPanel	:= Nil
Local oBmp		:= Nil 
Local oPesqPanel:= Nil
Local oPesqCbx	:= Nil
Local oPesqGet	:= Nil 

Local bExecBrow	:= Nil
Local bRefresh	:= {|oLstBx,nOpc,cRealAlias,cAlias,aFixe,cBLine|Ft320RefIt(@oLstBx,nOpc,cRealAlias,cAlias,aFixe,cBLine)}
Local bPosicione:= Nil
Local bNextReg	:= Nil

Local nX		:= 0
Local nCampos	:= Len(aFixe) 
Local nPosItem	:= 1
Local nWidth 	:= 40
Local nStart	:= 1
Local nTop,nLeft
Local nTotReg	:= 0
Local i

Local cBLine	:= "{||{"                                       
Local cSep		:= "" 
Local cPesqCampo:= Space(40)
Local cCapital	:= ""  
Local cRotina

Local lFlatMode := FlatMode()

Private aBitmaps	:= {}
Private aColors		:= {}
Private aRotina		:= aClone(aRot) 
Private oLstBx		:= Nil
Private oEnable		:= LoadBitmap( GetResources(), "ENABLE" )
Private oDisable	:= LoadBitmap( GetResources(), "DISABLE" )   

Default aCores	:= {}
                     
aColors := aClone(aCores)

If Len(aColors) > 0

	For i := 1 To Len(aColors)
		AADD(aBitmaps,LoadBitmap( GetResources(), aColors[i,2]))
	Next i  

	AAdd(aCampos," ")
	cBLine	+= "RetObjColor(aColors,oDisable,aBitMaps)"
	cSep	:= "," 
	nStart	:= 1 
	
EndIf

For nX := 1 to nCampos
	AAdd(aCampos,aFixe[nX][1])
Next nX

For nX := nStart to nCampos
	cBLine	+= cSep+"oLstBx:aArray[oLstBx:nAt,"+AllTrim(Str(nX))+"]"
	cSep	:= ","
Next nX                

cBLine	+= "}}"

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL  

nMbrWidth := oDlg:nWidth/2-43
nMbrHeight := oDlg:nHeight/2

@00,00 MSPANEL oBtnPanel PROMPT "" SIZE 50,15 OF oDlg
oBtnPanel:Align := CONTROL_ALIGN_LEFT

@ 000, 000 BITMAP oBmp RESOURCE "fw_degrade_menu.png" SIZE 000,000 OF oBtnPanel PIXEL NO BORDER
oBmp:LSTRETCH = .T.
oBmp:Align := CONTROL_ALIGN_ALLCLIENT

@ 000,000 MSPANEL oBackground PROMPT "" SIZE nMbrWidth,nMbrHeight OF oDlg
oBackground:Align := CONTROL_ALIGN_ALLCLIENT

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPesquisa de registrosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
AxPesqOrd(cRealAlias,@aPesqIdx,,.F.,@aPesqOrd)
cPesqOrd := aPesqOrd[1]

@00,00 MSPANEL oPesqPanel SIZE 15,15 OF oBackground
oPesqPanel:Align := CONTROL_ALIGN_TOP      

@02,02 COMBOBOX oPesqCbx VAR cPesqOrd ITEMS aPesqOrd SIZE 100,12 PIXEL OF oPesqPanel
oPesqCbx:cReadVar := ""

@02,103 MSGET oPesqGet VAR cPesqCampo SIZE 100,09 PIXEL OF oPesqPanel
oPesqGet:cReadVar := ""
oPesqGet:cSX1Hlp := "CCAMPO"

@02,205 BUTTON STR0001 SIZE 40,11 PIXEL OF oPesqPanel ;	//"Filtrar"
ACTION (If(Ft320Psq(cAlias	, cRealAlias	, cPesqCampo	, cPesqOrd	,;
					aPesqOrd, aPesqIdx		),;
					(oLstBx:aArray:={},nTotReg := Ft320Conta(cAlias),(cAlias)->(DbGoTop()),;
					Ft320LeBd(@oLstBx,aFixe,cAlias,cBLine,nTotReg,"oLstBx",cRealAlias)),.f.))


@02,250 BUTTON STR0002 SIZE 40,11 PIXEL OF oPesqPanel ;	//"Limpar Filtro"
ACTION (If(Ft320Psq(cAlias	, cRealAlias	, ""			, cPesqOrd	,;
					aPesqOrd, aPesqIdx		),;
					(oLstBx:aArray:={},nTotReg := Ft320Conta(cAlias),(cAlias)->(DbGoTop()),;
					Ft320LeBd(@oLstBx,aFixe,cAlias,cBLine,nTotReg,"oLstBx",cRealAlias),oLstBx:GoTop(),;
					oLstBx:Refresh()),.f.))

//ฺฤฤฤฤฤฤฤฟ
//ณRotinasณ
//ภฤฤฤฤฤฤฤู
bExecBrow := &("{|aArray,nPos,aSvRotina,lRunPopUp| aSvRotina := Aclone(aRotina), If(aArray <> NIL,aRotina := Aclone(aArray),),SetEnch(aRotina[nPos][1]), inclui := (aRotina[nPos][4] == 3), altera := (aRotina[nPos][4] == 4), Ft320AExBr(aRotina[nPos][2],nPos,'"+cRealAlias+"'),aRotina := Aclone(aSvRotina)}")
bPosicione:= {||(cRealAlias)->(DbGoTo(aTail(oLstBx:aArray[oLstBx:nAt])))}

For i:= 1 to Len(aRotina)

	If Upper(AllTrim(aRotina[i][2])) $ "AXPESQUI#PESQBRW"
		Loop
	EndIf

	If !("&"$aRotina[i,1])
		cRotina :=""
		For nX := 1 to Len(aRotina[i,1])
			If IsUpper(Subs(aRotina[i,1],nX,1))
				cRotina += "&"+Subs(aRotina[i,1],nX)
				cCapital += Lower(Subs(aRotina[i,1],nX,1))
				Exit
			Else
				cRotina += Subs(aRotina[i,1],nX,1)
			EndIf
		Next
	Else
		cRotina := aRotina[i,1]
	EndIf

	aRotina[i,1] := ButCapital(cRotina)
	BrwBtnPos(nPosItem,@nTop,@nLeft)
	
	If ValType(aRotina[i,2]) == "C"
		bBlock := &("{|| Eval(bPosicione),Eval(bExecBrow,,"+AllTrim(Str(i))+"),Eval(bRefresh,@oLstBx,"+Str(aRotina[i,4])+",'"+cRealAlias+"','"+cAlias+"',aFixe,cBLine)}")
		AADD(__aButton,TBrowseButton():New( nTop, nLeft, (aRotina[i,1]), oBtnPanel, bBlock, nWidth, 10,,oDlg:oFont, .F., .T., .F.,, .F.,,,))
	EndIf
	__aButton[Len(__aButton)]:Align := CONTROL_ALIGN_TOP
	If lFlatMode
		__aButton[Len(__aButton)]:SetCSS("#STYLE0024")
	EndIf
	
	nPosItem ++ 
Next 

//ฺฤฤฤฤฤฤฤฤฤฤฟ
//ณBotao sairณ
//ภฤฤฤฤฤฤฤฤฤฤู
BrwBtnPos(nPosItem,@nTop,@nLeft)
AADD(__aButton,TBrowseButton():New( nTop, nLeft, STR0003, oBtnPanel, {||oDlg:End()}, nWidth, 10,,oDlg:oFont, .F., .T.,.F.,,.F.,,,))//"Sair"
__aButton[Len(__aButton)]:Align := CONTROL_ALIGN_TOP
If lFlatMode
	__aButton[Len(__aButton)]:SetCSS("#STYLE0024")
EndIf

oLstBx := TWBrowse():New( 15,0, nMbrWidth-10, nMbrHeight-30,,aCampos,,oBackground,,,,,,,,,,,,,,.T. )

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณConta registros da tabelaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea(cAlias)
DbGoTop()
While !Eof() .AND. nTotReg <= NMAXPAGE
	nTotReg++
	DbSkip()
End

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCarrega dados para o ListBoxณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Ft320LeBd(@oLstBx,aFixe,cAlias,cBLine,nTotReg,"oLstBx",cRealAlias)

oLstBx:GoTop()
oLstBx:Refresh()

If (nTotReg > NMAXPAGE)
	bNextReg			:= {|oLstBx,aFixe|Ft320LeBd(@oLstBx,aFixe,cAlias,cBLine,nTotReg,'oLstBx',cRealAlias)}
	oLstBx:bGoBottom 	:= {||Eval(bNextReg,oLstBx,aFixe,cAlias,cBLine),oLstBx:NAT := LEN(oLstBx:AARRAY)}
	oLstBx:bSkip		:= {|NSKIP, NOLD, nMax|nMax:=EVAL( oLstBx:BLOGICLEN ),NOLD := oLstBx:NAT, oLstBx:NAT += NSKIP,;
							oLstBx:NAT := MIN( MAX( oLstBx:NAT, 1 ), nMax ),If(oLstBx:nAt==nMax,Eval(bNextReg,oLstBx,aFixe,cAlias,cBLine),.F.),;
							oLstBx:NAT - NOLD}
EndIf

ACTIVATE MSDIALOG oDlg CENTERED 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRoda a query novamente para eliminar qualquer filtro.ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Ft320FilTb(Nil,cRealAlias)

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320LeBd บAutor  ณVendas CRM          บ Data ณ  01/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para leitura do arquivo do banco de dados            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 - Objeto da listbox do browse                         บฑฑ
ฑฑบ          ณExpA2 - Lista de campos fixos                               บฑฑ
ฑฑบ          ณExpC3 - Alias da tabela a ser lida                          บฑฑ
ฑฑบ          ณExpC4 - Texto do codeblock a ser executado para cada linha  บฑฑ
ฑฑบ          ณExpN5 - Total de registros lidos                            บฑฑ
ฑฑบ          ณExpC6 - Nome do objeto da listbox passado no parametro      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft320LeBd(	oLBx	, aFixe		, cAlias	, cBLine	,;
					nTotReg	, cObjName	, cRealAlias)

Local nCont		:= 0 
Local nX		:= 0 
Local nCampos	:= Len(aFixe)            
Local nLinhas	:= Len(oLBx:aArray)
Local nLastRec	:= 0

Local aArray	:= {} 
Local aLinha	:= {}   

Local bNextReg	:= Nil

Local cChave	:= "" 
Local cChaveInd	:= ""
Local cSep		:= ""

If cObjName == Nil
	cObjName	:= "oLBx"
EndIf

bNextReg	:= &("{|"+cObjName+",aFixe|Ft320LeBd(@"+cObjName+",aFixe,cAlias,cBLine,nTotReg,'"+cObjName+"',cRealAlias)}")

If nLinhas > 0
	nLastRec := aTail(oLBx:aArray[nLinhas])
EndIf

DbSelectArea(cAlias)

If nLastRec == 0
	DbGoTop()	
EndIf

(cRealAlias)->(DbSetOrder(1))

For nX := 1 to (cAlias)->(FCount())
	cChaveInd	+= cSep + (cAlias)->(FieldName(nX))
	cSep	:= "+"
Next nX
                              
If (cAlias) == "TRBAD1"
	If nLastRec > 0 .And. nTotReg <> nLastRec
		DbSelectArea(cAlias)
		DbGoTop()
	Else 
		aArray := aClone(oLBx:aArray)  
    Endif 
Else 
	aArray := aClone(oLBx:aArray)      
Endif    

While !Eof() .AND. nCont < NMAXPAGE
	
	aLinha	:= {}  
	cChave	:= (cAlias)->&(cChaveInd)
	
	(cRealAlias)->(DbSeek(xFilial(cRealAlias)+cChave))
	
	For nX := 1 to nCampos
		If (cRealAlias)->(FieldPos(aFixe[nX][2])) == 0
			DbSelectArea("SX3")
			DbSetOrder(2)
			If DbSeek( aFixe[nX][2] ) .And. AllTrim(SX3->X3_CONTEXT) == "V"
				AAdd(aLinha,CriaVar(aFixe[nX][2]))
			Else
				AAdd(aLinha,"")
			EndIf			
			DbSelectArea(cAlias)			
		Else
			AAdd(aLinha,(cRealAlias)->&(aFixe[nX][2]))
		EndIf
	Next nX  
	AAdd(aLinha,(cRealAlias)->(Recno()))
	
	AAdd(aArray,aClone(aLinha))
	nCont++
	
	DbSkip()
	
End

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTrata casos onde nao ha registrosณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(aArray) == 0
	aLinha	:= {}
	For nX := 1 to nCampos
		AAdd(aLinha,CriaVar(aFixe[nX][2],.F.))
	Next nX
	AAdd(aLinha,0)
	AAdd(aArray,aClone(aLinha))
EndIf

oLBx:SetArray( aArray )
oLBx:bLine	:= &(cBLine)                               

If nLastRec == 0
	oLBx:GoTop()
EndIf

oLBx:Refresh()

If (nTotReg == Nil) .OR. (nTotReg > NMAXPAGE)
	
	cGoBottom	:= "{|| Eval(bNextReg,"+cObjName+",aFixe,cAlias,cBLine),"+cObjName+":NAT := Len("+cObjName+":aArray) }"
	cSkip		:= "{|NSKIP, NOLD, nMax|nMax:=Len("+cObjName+":aArray) ,NOLD := "+cObjName+":NAT, "+cObjName+":NAT += NSKIP,"+;
					cObjName+":NAT := MIN( MAX( "+cObjName+":NAT, 1 ), nMax ),If("+cObjName+":nAt==nMax,Eval(bNextReg,"+cObjName+",aFixe,cAlias,cBLine),.F.),"+;
					cObjName+":NAT - NOLD}"

	&(cObjName+":bGoBottom 	:= &(cGoBottom)")
	&(cObjName+":bSkip		:= &(cSkip)")
              
EndIf

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณButCapitalบAutor  ณVendas CRM          บ Data ณ  03/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para deixar o texto com as iniciais em maiusculo     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Texto a ser modificado                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ButCapital(cText)
Local ni
cText := LOWER(cText)
For ni:= 1 to Len(cText)
	If Subs(cText,ni,1) != "&"
		cText := SUbs(cText,1,ni-1)+Upper(Subs(cText,ni,1))+Subs(cText,ni+1)
		Exit
	EndIf
Next
Return cText

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณBrwBtnPos บAutor  ณVendas CRM          บ Data ณ  03/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine a posicao do botao de acordo com seu numero          บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpN1 - Numero do botao                                     บฑฑ
ฑฑบ          ณExpN2 - Coordenada superior                                 บฑฑ
ฑฑบ          ณExpN3 - Coordenada da esquerda                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function BrwBtnPos(nBtn,nTop,nLeft)
nTop := 1+((nBtn-1)*(10))
nLeft := 1
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320Psq  บAutor  ณVendas CRM          บ Data ณ  15/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de pesquisa na tabela temporaria                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Alias da tabela a ser pesquisada                    บฑฑ
ฑฑบ          ณExpC2 - Alias da tabela real (SX2)                          บฑฑ
ฑฑบ          ณExpC3 - Texto da pesquisa                                   บฑฑ
ฑฑบ          ณExpC4 - Ordem da pesquisa selecionada                       บฑฑ
ฑฑบ          ณExpA5 - Lista das ordens disponiveis                        บฑฑ
ฑฑบ          ณExpA6 - Lista dos indices das pesquisas                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320Psq(	cAlias	, cRealAlias	, cPesqCampo	, cPesqOrd,;
							aPesqOrd, aPesqIdx		)

Local cPesq		:= AllTrim(cPesqCampo)
Local cQueryAnd	:= ""  
Local cChvOrig	:= ""
Local cLenPsq	:= AllTrim(Str(Len(cPesq))) 
Local cPrefX	:= PrefixoCpo(cRealAlias)
Local cConcat	:= "+"
Local lFiltra	:= .T.

Local nChave	:= 0 
Local nLenChave	:= 0

cPesq	:= StrTran(cPesq,"'")
cPesq	:= StrTran(cPesq,'"')

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRetorna falso se nada foi solicitado para pesquisaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Empty(cPesq)
	lFiltra := .F.
EndIf                

If lFiltra
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDefine o simbolo de concatenacao de acordo com o bancoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Upper(TcGetDb()) $ "ORACLE,POSTGRES,DB2,INFORMIX"
		cConcat := "||"
	Endif 
	                     
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se a ordem selecionada eh validaณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (nChave	:= aScan(aPesqOrd,{|x| AllTrim(x) == AllTrim(cPesqOrd)})) == 0
		lFiltra := .F.
	Else             
		cChave		:= Upper((cRealAlias)->(IndexKey(nChave)))
		cChave 		:= StrTran(cChave,cPrefX+"_FILIAL+","") 
		cChvOrig	:= cChave
		cChave 		:= StrTran(cChave,cPrefX+"_",cRealAlias+"."+cPrefX+"_")
		cChave 		:= StrTran(cChave,"DTOS","")
		If cConcat <> "+"
			cChave 	:= StrTran(cChave,"+",cConcat)
		EndIf
	EndIf
	                                 
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se a chave de busca nao eh maior que o indiceณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nLenChave := Ft320TamCh(cChvOrig)    
	
	If nLenChave < Val(cLenPsq)
		cLenPsq := AllTrim(Str(nLenChave))
		cPesq	:= SubStr(cPesq,1,nLenChave)
	EndIf
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณConcatena a expressao do filtroณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lFiltra
		cQueryAnd := " AND SUBSTRING(" + cChave + ",1," + cLenPsq + ")= '"+cPesq+"'"
	EndIf
EndIf

Ft320FilTb(Nil,cRealAlias,cQueryAnd)

Return .T.     

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320TamChบAutor  ณVendas CRM          บ Data ณ  16/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna o tamanho maximo da pesquisa de acordo com a chave  บฑฑ
ฑฑบ          ณde pesquisa                                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Chave da pesquisa                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320TamCh(cChvOrig) 

Local nTam	:= 0
Local nX	:= 0
Local aCpos	:= {}

cChvOrig := StrTran(cChvOrig,"DTOS")
cChvOrig := StrTran(cChvOrig,"(")
cChvOrig := StrTran(cChvOrig,")")

aCpos := StrToKArr(cChvOrig,"+")

For nX := 1 to Len(aCpos)
	nTam += TamSX3(aCpos[nX])[1]
Next nX

Return nTam

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320ContaบAutor  ณVendas CRM          บ Data ณ  16/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConta os registros da tabela (limitado pelo valor NMAXPAGE).บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Alias da tabela a ser contada                       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320Conta(cAlias)

Local nTotReg := 0

DbSelectArea(cAlias)
DbGoTop()
While !Eof() .AND. nTotReg <= NMAXPAGE
	nTotReg++
	DbSkip()
End

Return nTotReg


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320PreF3บAutor  ณVendas CRM          บ Data ณ  24/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณPrepara as variaveis para execucao da consulta padrao em SQLบฑฑ
ฑฑบ          ณde acordo com o alias a ser filtrado                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Alias a ser listado para pesquisa                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFata320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft320PreF3(cAliasRef)

Local lRet		:= .F.
Local aCampos	:= {}
Local aIndices	:= {}
Local cWhere	:= ""
Local lWorkArea	:= IsInCallStack("FATA320")

Do Case
	
	Case cAliasRef == "SA1" 
		aCampos := {"A1_COD","A1_LOJA","A1_NOME","A1_CGC"}
		aIndices:=	{{"A1_FILIAL+A1_COD+A1_LOJA","A1_FILIAL+A1_NOME","A1_FILIAL+A1_CGC"},;
					{STR0004,STR0005,STR0006}} //"C๓digo+Loja"##"Nome"##"CPF/CNPJ"
	
	Case cAliasRef == "SUS"
		aCampos := {"US_COD","US_LOJA","US_NOME","US_CGC"}
		aIndices:=	{{"US_FILIAL+US_COD+US_LOJA","US_FILIAL+US_NOME","US_FILIAL+US_CGC"},;
					{STR0004,STR0005,STR0006}} //"C๓digo+Loja"##"Nome"##"CPF/CNPJ"  
	
	Case cAliasRef == "AD1"
		aCampos	:= {"AD1_NROPOR","AD1_DESCRI","AD1_VEND","AD1_PROSPE","AD1_LOJPRO"}
		aIndices:= {{"AD1_FILIAL+AD1_NROPOR"},{"Oportunidade"}}
	
EndCase

lRet := Ft320F3(cAliasRef,aCampos,aIndices,cWhere,lWorkArea) 

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320F3   บAutor  ณVendas CRM          บ Data ณ  24/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณConsulta padrao em SQL, respeitando regras da query forneci-บฑฑ
ฑฑบ          ณda                                                          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpC1 - Alias a ser considerado                             บฑฑ
ฑฑบ          ณExpA2 - Lista de campos exibidos                            บฑฑ
ฑฑบ          ณExpA3 - Lista de indices para pesquisa                      บฑฑ
ฑฑบ          ณExpC4 - Where da query (opcional)                           บฑฑ
ฑฑบ          ณExpL5 - Identifica se foi chamada a partir da WorkArea      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft320F3(	cAliasRef	, aCampos	, aIndices	, cWhere	,;
					lWorkarea	)

Local cCmbIndice:= ""
Local cPesq	 	:= Space(50)
Local oPesq
Local nX		:= 0
Local nRecno	:= 0
Local lRet		:= .F.
Local cTrbName	:= "TMPF3"+cAliasRef
Local cTitle	:= ""
Local cBLine	:= ""
Local cSep		:= "" 
Local bRet		:= {||lRet := .T.,nRecno := IIf(Len(oLstBx:aArray)>=oLstBx:nAt,aTail(oLstBx:aArray[oLstBx:nAt]),0),oDlg:End()}
Local oDlg
Local aDados	:= {}
Local aHeaders	:= {}
Local oLstBx       

Default lWorkarea	:= .F.
Default cWhere		:= ""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRemove o campo filial da listaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nX := 1 to Len(aCampos)
	If "_FILIAL" $ aCampos[nX]
		aDel(aCampos,nX)
		aSize(aCampos,Len(aCampos)-1) 
		Exit
	EndIf
Next nX 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณMonta header do listboxณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nX := 1 to Len(aCampos)
	AAdd(aHeaders,AllTrim(Capital(FWX3Titulo(aCampos[nX]))))
Next nX

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณNome da pesquisaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cTitle		:= Capital(AllTrim(FWX2Nome(cAliasRef)))
DEFINE MSDIALOG oDlg TITLE STR0007 + " " + cTitle FROM 268,260 TO 642,796 PIXEL //"Consulta"
    
	//Texto de pesquisa
	@ 017,002 MsGet oPesq Var cPesq Size 219,009 COLOR CLR_BLACK PIXEL OF oDlg

	//Interface para selecao de indice e filtro
	@ 003,228 Button STR0001 Size 037,012 PIXEL OF oDlg; //"Filtrar"
		ACTION (Ft320Lbx(	@oLstBx	, @aDados	, cWhere	, cTrbName	,;
							aCampos	, cAliasRef	, cCmbIndice, aIndices	,;
							@oDlg	, cPesq		, lWorkarea ))	
							
	@ 005,002 ComboBox cCmbIndice Items aIndices[2] Size 220,010 PIXEL OF oDlg;
		On Change (Ft320Lbx(	@oLstBx	, @aDados	, cWhere	, cTrbName	,;
								aCampos	, cAliasRef	, cCmbIndice, aIndices	,;
								@oDlg	, cPesq		, lWorkarea	))

    
	//ListBox
	oLstBx := TWBrowse():New(30,3,264,139,Nil,aHeaders,,oDlg,,,,,,,,,,,,,,.T.)
	oLstBx:bLDblClick := bRet
	
	//Botoes inferiores
	DEFINE SBUTTON FROM 172,002 TYPE 1	ENABLE OF oDlg Action(Eval(bRet))
	DEFINE SBUTTON FROM 172,035 TYPE 2	ENABLE OF oDlg Action(oDlg:End())
	DEFINE SBUTTON FROM 172,068 TYPE 15	ENABLE OF oDlg Action(Ft320Visua(@oLstBx,cAliasRef))
	
	//Carga dos dados
	Ft320Lbx(	@oLstBx	, @aDados	, cWhere	, cTrbName	,;
				aCampos	, cAliasRef	, cCmbIndice, aIndices	,;
				@oDlg	, cPesq		, lWorkarea	)

ACTIVATE MSDIALOG oDlg CENTERED 

If Select(cTrbName) > 0
	(cTrbName)->(DbCloseArea())
EndIf

If lRet
	DbSelectArea(cAliasRef)
	DbGoTo(nRecno)
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320Lbx  บAutor  ณVendas CRM          บ Data ณ  24/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCarga da ListBox de acordo com o filtro e parametros passa- บฑฑ
ฑฑบ          ณdos.                                                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO01 - Objeto da listbox                                  บฑฑ
ฑฑบ          ณExpA02 - Array contendo as linhas exibidas                  บฑฑ
ฑฑบ          ณExpC03 - Where a ser enviado para a query                   บฑฑ
ฑฑบ          ณExpC04 - Nome da tabela temporaria                          บฑฑ
ฑฑบ          ณExpA05 - Lista de campos a serem exibidos                   บฑฑ
ฑฑบ          ณExpC06 - Alias de referencia                                บฑฑ
ฑฑบ          ณExpC07 - Indice selecionado no combo                        บฑฑ
ฑฑบ          ณExpA08 - Lista de indices utilizados                        บฑฑ
ฑฑบ          ณExpO09 - Objeto da tela da consulta                         บฑฑ
ฑฑบ          ณExpC10 - Texto da pesquisa                                  บฑฑ
ฑฑบ          ณExpL11 - Indica se foi chamada a partir da Workarea         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320Lbx(	oLstBx	, aDados	, cWhere	, cTrbName	,;
							aCampos	, cAlias	, cCmbIndice, aIndices	,;
							oDlg	, cPesq		, lWorkarea	)

Local cQuery 	:= ""
Local cCpos		:= ""
Local cSep		:= ""
Local cChave	:= ""
Local nX		:= 0  
Local cPrefx	:= PrefixoCpo(cAlias)
Local cOrder	:= ""
Local bLine		:= Nil 
Local bNextReg	:= {|a,b,c,d,e|Ft530Pag(@a,b,c,d,e)}
Local nTotReg	:= 0 
Local nLenChave	:= 0
Local nInd		:= 0    
Local lFiltra	:= .F.
Local cConcat	:= "+"
Local cLenPsq	:= ""  
Local aCposAdd	:= aClone(aCampos)
Local cFiltro	:= ""

Default cPesq		:= ""  
Default	lWorkarea	:= .F.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณRemove espacos do texto pesquisadoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cPesq	:= AllTrim(cPesq)
cLenPsq	:= AllTrim(Str(Len(cPesq))) 

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica se deve ser feito algum filtroณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !Empty(cPesq)
	lFiltra := .T.
EndIf    

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณDefine a ordem utilizadaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
nInd := aScan(aIndices[2],cCmbIndice)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณFiltro de acordo com a pesquisaณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lFiltra
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDefine o simbolo de concatenacao de acordo com o bancoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If Upper(TcGetDb()) $ "ORACLE,POSTGRES,DB2,INFORMIX"
		cConcat := "||"
	Endif 
	                     
	cChave		:= Upper(aIndices[1][nInd])
	cChave 		:= StrTran(cChave,cPrefX+"_FILIAL+","") 
	cChvOrig	:= cChave
	cChave 		:= StrTran(cChave,cPrefX+"_",cAlias+"."+cPrefX+"_")
	cChave 		:= StrTran(cChave,"DTOS","")
	If cConcat <> "+"
		cChave 	:= StrTran(cChave,"+",cConcat)
	EndIf
	                                 
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se a chave de busca nao eh maior que o indiceณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nLenChave := Ft320TamCh(cChvOrig)    
	
	If nLenChave < Val(cLenPsq)
		cLenPsq := AllTrim(Str(nLenChave))
		cPesq	:= SubStr(cPesq,1,nLenChave)
	EndIf
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณConcatena a expressao do filtroณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If lFiltra
		cFiltro += " AND SUBSTRING(" + cChave + ",1," + cLenPsq + ")= '"+cPesq+"' "
	EndIf
	
EndIf
 
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณMonta lista de camposณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
SX3->(DbSetOrder(2)) 

cBLine	:= "{||{"

For nX := 1 to Len(aCampos)
	cBLine	+= cSep + "oLstBx:aArray[oLstBx:nAt]["+AllTrim(Str(nX))+"]"
	cSep	:= ","
Next nX 

cBLine	+= "}}" 
bLine	:= &(cBLine)
cSep := ""

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPrepara e executa a queryณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cQuery	:= Ft320FilTb(Nil,cAlias,cFiltro,cTrbName,.T.)

cQuery := ChangeQuery( cQuery )

If Select(cTrbName) > 0
	(cTrbName)->(DbCloseArea())
EndIf

DbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cTrbName,.T.,.T.)

(cTrbName)->(DbGoTop())

If (cTrbName)->(Eof())

	MsgStop(STR0008) //"Nenhum registro foi encontrado"

Else

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณConta registros da tabelaณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea(cTrbName)
	DbGoTop()
	While !Eof() .AND. nTotReg <= NMAXPAGE
		nTotReg++
		DbSkip()
	End	
	DbGoTop()

	aDados := Ft530Pag(Nil,cTrbName,aCposAdd,NMAXPAGE,cAlias) 
	
	oLstBx:SetArray(aDados)
	oLstBx:bLine := bLine
	oLstBx:GoTop()
	oLstBx:Refresh()
	oDlg:Refresh()
	
	If (nTotReg > NMAXPAGE)
		oLstBx:bGoBottom	:= {||Eval(bNextReg,oLstBx,cTrbName,aCposAdd,NMAXPAGE,cAlias),oLstBx:NAT := EVAL( oLstBx:BLOGICLEN ) }    
		oLstBx:bSkip		:= {|NSKIP, NOLD, nMax|nMax:=EVAL( oLstBx:BLOGICLEN ),NOLD := oLstBx:NAT, oLstBx:NAT += NSKIP,;
								oLstBx:NAT := MIN( MAX( oLstBx:NAT, 1 ), nMax ),If(oLstBx:nAt==nMax,Eval(bNextReg,oLstBx,cTrbName,aCposAdd,NMAXPAGE,cAlias),.F.),;
								oLstBx:NAT - NOLD}	
	EndIf	
EndIf

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320VisuaบAutor  ณVendas CRM          บ Data ณ  23/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVisualizacao do registro exibido na pesquisa padrao         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 - Objeto da listbox                                   บฑฑ
ฑฑบ          ณExpC2 - Alias da consulta                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320Visua(oLstBx,cAlias)
          
Local aArea	:= GetArea()
Local nReg	:= 0

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณTratamento para casos em que o filtro nใo exiba clientes no F3ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Len(oLstBx:aArray) >= oLstBx:nAt
	nReg := aTail(oLstBx:aArray[oLstBx:nAt])
                     
	SaveInter()
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCria um aRotina basico para evitar quaisquer problemasณ
	//ณcom a rotina diferente deste padrao                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aRotina := {{STR0009,"AxPesqui",0,1} ,;//"Pesquisar"
				{STR0010,"AxVisual",0,2}}	//"Visualizar"
	   
	DbSelectArea(cAlias)
	DbGoTo(nReg)
	
	AxVisual(cAlias,nReg,2)
	
	RestInter() 
EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320RefItบAutor  ณVendas CRM          บ Data ณ  30/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณAtualiza a listbox apos edicao de dados                     บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณExpO1 - Objeto da ListBox(referencia)                       บฑฑ
ฑฑบ          ณExpN2 - Opcao da rotina                                     บฑฑ
ฑฑบ          ณExpC3 - Alias real do browse                                บฑฑ
ฑฑบ          ณExpC4 - Alias utilizado no browse                           บฑฑ
ฑฑบ          ณExpA5 - Lista de campos fixos                               บฑฑ
ฑฑบ          ณExpC6 - Texto do bloco utilizado em cada linha              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320RefIt(	oLstBx	, nOpc		, cRealAlias	, cAlias	,;
							aFixe	, cBLine	)

Local nRecAtu	:= 0
Local nTotReg	:= 0
Local nPos		:= 0 
Local bNextReg	:= Nil

If nOpc <> 2

	nRecAtu := aTail(oLstBx:aArray[oLstBx:nAt])
	oLstBx:aArray := {}
	
	Ft320FilTb(Nil,cRealAlias)

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณConta registros da tabelaณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	DbSelectArea(cAlias)
	DbGoTop()
	While !Eof() .AND. nTotReg <= NMAXPAGE
		nTotReg++
		DbSkip()
	End
	DbGoTop()
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCarrega dados para o ListBoxณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	Ft320LeBd(	@oLstBx	, aFixe		, cAlias	, cBLine	,;
				nTotReg	, "oLstBx"	, cRealAlias)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณTenta posicionar no ultimo registro acessado, que pode terณ
	//ณsido excluido ou estar fora da pagina atual               ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If (nPos := aScan(oLstBx:aArray,{|x| aTail(x) == nRecAtu })) > 0
		oLstBx:nAt := nPos
	Else
		oLstBx:GoTop()
	EndIf
	oLstBx:Refresh()
	
	If (nTotReg > NMAXPAGE)
		bNextReg			:= {|oLstBx,aFixe|Ft320LeBd(@oLstBx,aFixe,cAlias,cBLine,nTotReg,'oLstBx',cRealAlias)}
		oLstBx:bGoBottom 	:= {||Eval(bNextReg,oLstBx,aFixe,cAlias,cBLine),oLstBx:NAT := LEN(oLstBx:AARRAY)}
		oLstBx:bSkip		:= {|NSKIP, NOLD, nMax|nMax:=EVAL( oLstBx:BLOGICLEN ),NOLD := oLstBx:NAT, oLstBx:NAT += NSKIP,;
								oLstBx:NAT := MIN( MAX( oLstBx:NAT, 1 ), nMax ),If(oLstBx:nAt==nMax,Eval(bNextReg,oLstBx,aFixe,cAlias,cBLine),.F.),;
								oLstBx:NAT - NOLD}
	EndIf
	
EndIf

Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt320AExBrบAutor  ณVendas CRM          บ Data ณ  18/11/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao responsavel pela execucao da rotina selecionada no   บฑฑ
ฑฑบ          ณmenu da mBrowse(Ft320TelaB)                                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA320A                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft320AExBr(cFuncao,nOpcao,cAlias)

Local cAliasBk	:= Alias()

DbSelectArea(cAlias)

&cFuncao.(cAlias,Recno(),nOpcao)

DbSelectArea(cAliasBk)

Return Nil
