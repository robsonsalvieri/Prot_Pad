#INCLUDE "PROTHEUS.CH" 
#INCLUDE "FWADAPTEREAI.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFISI010  บAutor  ณ Marcelo C. Coutinho  บ Data ณ  28/10/11   บฑฑ
ฑฑฬออออออออออุอออออออออออออสอออออออฯอออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบ Desc.    ณ Funcao de integracao com o adapter EAI para recebimento e    บฑฑ
ฑฑบ          ณ envio de informa็๕es do cadastro de municipios      (CC2)    บฑฑ
ฑฑบ          ณ utilizando o conceito de mensagem unica.                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ Param.   ณ cXML - Variavel com conteudo xml para envio/recebimento.     บฑฑ
ฑฑบ          ณ nTypeTrans - Tipo de transacao. (Envio/Recebimento)          บฑฑ
ฑฑบ          ณ cTypeMessage - Tipo de mensagem. (Business Type, WhoIs, etc) บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ Retorno  ณ aRet - Array contendo o resultado da execucao e a mensagem   บฑฑ
ฑฑบ          ณ        Xml de retorno.                                       บฑฑ
ฑฑบ          ณ aRet[1] - (boolean) Indica o resultado da execu็ใo da fun็ใo บฑฑ
ฑฑบ          ณ aRet[2] - (caracter) Mensagem Xml para envio                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ Uso      ณ FISA010                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function FISI010( cXML, nTypeTrans, cTypeMessage )

Local lRet     	:= .T.
Local cXMLRet  	:= ""
Local cError		:= ""
Local cWarning 	:= ""
Local nCount      := 0
Local aCab			:= {}
Local aItens		:= {}
Local nOpcx			:= 0
Local cLogErro 	:= ""
Local aErroAuto	:= {}
Local cEvent      := "upsert"
Local cCodEst     := ""
Local cCodMun     := "" 
Local aArea			:= GetArea()

Private oXmlA010		  := Nil
Private nCountA010	  := 0
Private lMsErroAuto    := .F.
Private lAutoErrNoFile := .T.

If nTypeTrans == TRANS_RECEIVE
	If cTypeMessage == EAI_MESSAGE_BUSINESS
		oXmlA010 := XmlParser(cXml, "_", @cError, @cWarning)
		
		If oXmlA010 <> Nil .And. Empty(cError) .And. Empty(cWarning)
			CC2->( dbSetOrder( 1 ) )
			If Upper(oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessEvent:_Event:Text) == "UPSERT"
				If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text" ) <> "U" 
				   If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text") <> "U"
				   	cCodMun := oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text
			  			If Len(cCodMun) == 7
			  				cCodMun := SubStr(cCodMun,3,7)
						EndIf
						If CC2->( MsSeek( xFilial('CC2')+oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text + cCodMun))
							nOpcx:= 4
						Else
							nOpcx:= 3
						EndIf
					EndIf
				Else
					nOpcx:= 3
				EndIf
			ElseIf Upper(oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessEvent:_Event:Text) == "DELETE" 
				If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text") <> "U"
					cCodMun := oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text
				  	If Len(cCodMun) == 7
				  		cCodMun := SubStr(cCodMun,3,7)
					 EndIf  
				EndIf
				If !CC2->( MsSeek( xFilial('CC2')+oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text + cCodMun ))
            	lRet := .F.
				EndIf
				nOpcx := 5
			EndIf
			
			If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text") <> "U"
				Aadd( aCab, { "CC2_EST", oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text, Nil })
			EndIf
			If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text") <> "U" 
			  cCodMun := oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text
			  If Len(cCodMun) == 7
			  		cCodMun := SubStr(cCodMun,3,7)
			  EndIf
				Aadd( aCab, { "CC2_CODMUN",  cCodMun , Nil })
			EndIf				
			
			If nOpcx <> 5
				If Type("oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_Description:Text") <> "U"
					Aadd( aCab, { "CC2_MUN", oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_Description:Text, Nil })
				EndIf
			EndIf

			If lRet
				MSExecAuto({|x,y| FISA010(x,y)},aCab,nOpcx)
			EndIf		

			If lMsErroAuto
				aErroAuto := GetAutoGRLog()
				For nCount := 1 To Len(aErroAuto)
					cLogErro += StrTran(StrTran(aErroAuto[nCount],"<",""),"-","") + (" ") 
				Next nCount
				// Monta XML de Erro de execu็ใo da rotina automatica.
				lRet := .F.
				cXMLRet := SubStr(EncodeUTF8( cLogErro ),1,750)
			Else
				// Monta xml com status do processamento da rotina autmotica OK.
				If lRet
					cXMLRet := "<OrderId>"+CC2->CC2_EST+CC2->CC2_MUN+"</OrderId>
   			Else
					cXMLRet := "<OrderId>"+oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_FederalUnit:Text +;
	                                   oXmlA010:_TOTVSMessage:_BusinessMessage:_BusinessContent:_OfficialCode:Text+"</OrderId>
					lRet := .T.
				EndIf
			EndIf
			
		Else
			// "Falha ao gerar o objeto XML"
			lRet := .F.
			cXMLRet := "Falha ao manipular o XML"
		EndIf

	ElseIf cTypeMessage == EAI_MESSAGE_RESPONSE
		cXMLRet := '<TAGX>TESTE DE RECEPCAO RESPONSE MESSAGE</TAGX>'
	ElseIf   cTypeMessage == EAI_MESSAGE_WHOIS
		cXMLRet := '1.0'
	EndIf
	
ElseIf nTypeTrans == TRANS_SEND
   
	If !Inclui .And. !Altera

		cEvent := 'delete'
		
	EndIf

	cCodEst := Tms120CdUf(CC2->CC2_EST,'1')
   cCodMun := Alltrim(cCodEst) + AllTrim(CC2->CC2_CODMUN)
   
	cXMLRet := '<BusinessEvent>'
	cXMLRet +=     '<Entity>City</Entity>'
	cXMLRet +=     '<Event>' + cEvent + '</Event>'
	cXMLRet +=     '<Identification>'
	cXMLRet +=         '<key name="FederalUnit">'  + AllTrim(CC2->CC2_EST)    + '</key>'
	cXMLRet +=         '<key name="OfficialCode">' + AllTrim(cCodMun)       + '</key>'
	cXMLRet +=     '</Identification>'
	cXMLRet += '</BusinessEvent>'
	cXMLRet += '<BusinessContent>'
	cXMLRet +=	  '<FederalUnit>'  + AllTrim(CC2->CC2_EST)    + '</FederalUnit>'
	cXMLRet +=    '<Description>'  + AllTrim(CC2->CC2_MUN)    + '</Description>'
	cXMLRet +=    '<OfficialCode>' + AllTrim(cCodMun)       + '</OfficialCode>'
	cXMLRet += '</BusinessContent>'
EndIf


RestArea( aArea )
Return { lRet, cXMLRet } 
