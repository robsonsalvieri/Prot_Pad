#INCLUDE 'PROTHEUS.CH'
#INCLUDE "TECA210.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

Static cUsaCtrMan	:= "0"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ TECA210  ³ Autor ³ Eduardo Riera         ³ Data ³ 20.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de PV via Contrato de Manutencao      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function TECA210()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()

Private cCadastro := STR0001  // "Contrato Manutencao"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse( 6, 1,22,75,"AAH")
dbSelectArea("AAH")
dbSetOrder(1)

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MenuDef   ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 08.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA210                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//³    6 - Alteracao sem inclusao de registro                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := {	{ STR0002, "AxPesqui"	, 0, 1, 0,.F.},; // "Pesquisar"
						{ STR0003, "At200Manut"	, 0, 2, 0,.T.},; // "Visualizar"
						{ STR0004, "At210Proc"	, 0, 3, 0,.T.} } // "Processa"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At210Proc ³ Autor ³ Eduardo Riera         ³ Data ³ 20.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento dos Contratos de Manutencao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At210Proc(cAlias,nReg,nOpc)

LOCAL dUlContr := SuperGetMV("MV_ULCONTR") 


Processa({|lEnd| At210GeraPv(cAlias,nReg,nOpc,dUlContr)}, STR0005 )  // "Gerando P.Vendas por Contrato"
Processa({|lEnd| At210GeraOs(cAlias,nReg,nOpc,dUlContr)}, STR0007 ) // "Processando O.S. dos contratos de manutenção"
dbSelectArea("SX6")
PutMV( "MV_ULCONTR", dDataBase ) 

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At210GeraP³ Autor ³ Eduardo Riera         ³ Data ³ 20.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento dos Contratos de Manutencao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At210GeraP( ExpC1, ExpN1, ExpN2, ExpD1 )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±³          ³ ExpD1 : Data do ultimo processamento                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Data      ³Programador³ BOPS  ³ Descricao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³04/10/05  ³Cleber M.  ³ 86528 ³ Corrigida a gravacao do campo C6_PEDCLI³±±
±±³          ³           ³       ³ conforme nr. do contrato (AAH_CONTRT)  ³±±
±±³17/02/06  ³Cleber M.  ³ 93484 ³ Forcar a liberacao do numero reservado ³±±
±±³          ³           ³       ³ c/ funcao FreeUsedCode(.T.)            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function At210GeraPv(cAlias,nReg,nOpc,dUlContr)

Local lPedido 	   := .F.
Local nUsado  	   := 0
Local nCntFor      := 0
Local nAcols       := 0
Local aTravas      := {}
Local lTravas      := .T.
Local aCondPag     := {}
Local nValor       := 0
Local nLoop        := 0 
Local nStack       := GetSX8Len() 
Local dProcesso  

Local aRecsAAH     := {}                
Local aDadosCFO    := {}

Local cCfo         := "" 
Local cEstado      := SuperGetMV( "MV_ESTADO" )  
Local cLocPad      := "01"

Local dUltRea      := CToD( "" ) 

Local lATA210PV    := ExistBlock( "ATA210PV" ) 
Local lATA210GP    := ExistBlock( "ATA210GP" ) 
Local lATA210CD    := ExistBlock( "ATA210CD" ) 
Local lGeraPV      := .T. 

Local nQtMesUltRea := 0
Local nDiaUltRea   := 0 
Local nQtdMeses    := 0 
Local nPosRecAAH   := 0 
Local nValorReaj   := 0 
Local nFatorReaj   := 0                            
Local cEventID     := ""                            	// Id do Evento a ser disparado pelo Event Viewer
Local cMensagem    := ""                             	// Mensagem que sera enviada por e-mail ou RSS pelo Event Viewer
Local lRateio 	   := .F.							 	// Define se o Pedido de Venda será rateado
Local aSocios 	   := {}   							 	// Array para armazenar os socios do grupo societario	
Local nX		   := 0              					// Incremento utilizado no laco for
Local nQtd		   := 1                               	// Quantidade Padrao
Local nQtdRst      := 1                               	// Quantidade Restante
Local nQtdRat	   := 0								 	// Quantidade Rateada
Local nVlrCtr	   := 0                               	// Valor do Contrato	
Local lContrtAtv   := .T.							 	// Verifica se o contrato esta ativo.
Local lBkpInclui   := If( Type('Inclui')=='L', Inclui, .F. )
Local lBkpAltera   := If( Type('Altera')=='L', Altera, .F. )

Local aStruField := {}
Local cField     := ""
Local nC         := 0

PRIVATE bCampo     := {|x| FieldName(x) }
PRIVATE aCols 	   := {}
PRIVATE aHeader    := {}
PRIVATE aColsFor   := {} 
 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Possibilita ou nao a geracao de pedido de venda para  ³
//³ contrato de manutencao cancelado.           		  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do SC6                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
aStruField := FWSX3Util():GetAllFields("SC6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		aRet   := FwTamSx3(cField)
		If (((X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			!( Trim(cField) == "C6_NUM" ) .AND.;
			Trim(cField) <> "C6_QTDEMP"  .AND.;
			Trim(cField) <> "C6_QTDENT") .AND.;
			GetSx3Cache(cField, "X3_NIVEL")) .OR.;
			Trim(cField)=="C6_NUMORC" .OR. ;
			Trim(cField)=="C6_NUMOP"  .OR. ;
			Trim(cField)=="C6_ITEMOP" .OR. ;
			Trim(cField)=="C6_OP" )
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							AllTrim(GetSx3Cache(cField,"X3_PICTURE")),;
							aRet[1],;
							aRet[2],;
							If(Trim(cField)=="C6_NUMORC",".F.",AllTrim(GetSx3Cache(cField,"X3_VALID"))),;
							GetSx3Cache(cField,"X3_USADO"),;
							aRet[3],;
							"SC6",;
							GetSx3Cache(cField,"X3_CONTEXT") } )
			nUsado ++
		EndIf
	Next nC
EndIf

dProcesso := If( ValType( dUlContr ) == "D", dUlContr, dDataBase ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Percorre todos os dias desde o ultimo processamento ate hoje            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While dProcesso <= dDataBase 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processamento dos Contratos                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("AAH")
	dbSetOrder(1)
	ProcRegua(LastRec())
	dbSeek(xFilial("AAH"))
	
	While ( !Eof() .AND. AAH->AAH_FILIAL == xFilial("AAH") ) 
		
		lContrtAtv	:= ( AAH->AAH_STATUS == "1" ) 
		
		If ( lContrtAtv .AND. dProcesso >= AAH->AAH_INICOB .AND.;
					If(AAH->AAH_TPCONT=="1",.T.,dProcesso<=AAH->AAH_FIMCOB) )
					
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Caso necessario, efetua o reajuste                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty( AAH->AAH_CODIND ) .AND. !Empty( AAH->AAH_PERREA ) 
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Calcula o numero de meses decorridos                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dUltRea := If( Empty( AAH->AAH_ULTREA ), AAH->AAH_INICOB, AAH->AAH_ULTREA ) 
				
				nQtMesUltRea := Year( dUltRea ) * 12 + Month( dUltRea ) 
				nDiaUltRea   := Day( dUltRea )  
		
				nQtdMeses    := Year( dProcesso ) * 12 + Month( dProcesso ) - nQtMesUltRea - If( Day( dProcesso ) < nDiaUltRea, 1, 0 )
				
				If nQtdMeses >= AAH->AAH_PERREA 
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Chama a funcao de calculo do reajuste e obtem um fator ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nFatorReaj := AtCalcReaj( 1, AAH->AAH_CODIND, dUltRea + 1, dProcesso ) 
	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Chama a funcao de reajuste de contratos de manutencao  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					AtReajCtr( nFatorReaj, dProcesso, .F. ) 
						
				EndIf 
		
			EndIf 		
					
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Pega o valor do contrato ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nValor := AAH->AAH_VALOR
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se o valor do contrato for zero      ³
			//³ Totaliza o valor pela base instalada ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( nValor == 0 )
				dbSelectArea("AA3")
				dbSetOrder(2)
				dbSeek(xFilial("AA3")+AAH->AAH_CONTRT)
				While ( !Eof() .AND. xFilial("AA3")	==	AA3->AA3_FILIAL .AND.;
											AAH->AAH_CONTRT==	AA3->AA3_CONTRT )
					nValor += AA3->AA3_VLRCTR
					dbSelectArea("AA3")
					dbSkip()
				EndDo
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Monta o array com os dados da cond. de pagto ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SE4")
			SE4->(dbSetOrder(1))
			SE4->(dbSeek(xFilial("SE4")+AAH->AAH_CONPAG ))
			
			aCondPag := Condicao(nValor,AAH->AAH_CONPAG,0,dProcesso-Day(dProcesso)+1)
			nVlrDif 	:= nValor
			For nCntFor := 1 To Len(aCondPag)
				nVlrDif -= aCondPag[nCntfor][2]
			Next nCntFor
			If Len(aCondPag) > 0
				aCondPag[Len(aCondPag)][2] += nVlrDif
			EndIf
			nValor := 0
			For nCntFor := 1 To Len(aCondPag)		
				If ( AAH->AAH_ULTEMI < aCondPag[nCntFor][1] .AND. nValor == 0 .AND. dProcesso == aCondPag[nCntFor][1] )
					nValor := aCondPag[nCntFor][2]
				EndIf
			Next nCntFor
			If TECAAHReco() .AND. AAH->AAH_NUMREC > 0 
				At210TitPr(1000,dProcesso,AAH->AAH_FILIAL,AAH->AAH_CONTRT,AAH->AAH_CPAGPV)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Se o valor do contrato for maior que zero ³
			//³ inicia a geracao do Ped. de Venda         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( nValor > 0 )
			
				lGeraPV := .T. 
	   			nVlrCtr := nValor
		
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ponto de entrada para permitir ou nao a geracao        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lAta210CD
					lGeraPV := ExecBlock( "ATA210CD",.F., .F., { dProcesso, nValor } )
				EndIf	
				
				If lGeraPV 
				     
				     Inclui := .T.
				     Altera := .F.
				     
				     DbSelectArea("AAS")
				     DbSetOrder(1)
				     
				     //AAS_FILIAL+AAS_PROPOS+AAS_PREVIS+AAS_CLIENT+AAS_LOJA
				     If DbSeek(xFilial("AAS")+AAH->AAH_PROPOS+AAH->AAH_REVPRO)
				         
				         lRateio := .T.
				         
				         While (AAS->(!EOF()) .AND. AAS->AAS_FILIAL == xFilial("AAS") .AND. ;
				         AAS->AAS_PROPOS == AAH->AAH_PROPOS .AND. AAS->AAS_PREVIS == AAH->AAH_REVPRO)   
				         	aAdd(aSocios,{AAS->AAS_CLIENT,AAS->AAS_LOJA,AAS->AAS_PERCEN})			 
				         AAS->(DbSkip()) 
				         End
				         
				     EndIf
					
					If !lRateio
				   		aAdd(aSocios,{AAH->AAH_CODCLI,AAH->AAH_LOJA})	
					End
					
					For nX := 1 To Len(aSocios)
				     
						If lRateio
							 
						 	If (nX == Len(aSocios))
						 		nQtd    := nQtdRst
								nValor	:= nQtd*nVlrCtr
					  		Else
						   		nQtdRat := Round((1/100)*aSocios[nX][3],1)
						   		nValor	:= nQtdRat*nVlrCtr
						   		nQtd	:= nQtdRat
						   		nQtdRst	-= nQtdRat
					   		EndIf
					   				
					   	EndIf 
				
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Posiciona Registros                                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("SA1")
						dbSetOrder(1)
							dbSeek(xFilial("SA1")+aSocios[nX][1]+aSocios[nX][2])
						If ( SoftLock("SA1") )
							aadd(aTravas,{ Alias() , RecNo() })
						Else
							lTravas := .F.
						EndIf
						
						dbSelectArea("SB1")
						dbSetOrder(1)
						dbSeek(xFilial("SB1")+AAH->AAH_CODPRO)  
						
						cLocPad := If( Empty( RetFldProd(SB1->B1_COD,"B1_LOCPAD") ), "01", RetFldProd(SB1->B1_COD,"B1_LOCPAD") ) 
			                     	
						dbSelectArea("SB2")
						dbSetOrder(1)
						If( dbSeek(xFilial("SB2")+AAH->AAH_CODPRO+cLocPad) )
							If ( SoftLock("SB2") )
								aadd(aTravas,{ Alias() , RecNo() })
							Else
								lTravas := .F.
							EndIf
						EndIf
						
						dbSelectArea("SF4")
						dbSetOrder(1)
						dbSeek(xFilial("SF4")+RetFldProd(SB1->B1_COD,"B1_TS"))
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Cria as Variaveis do Pedido de Venda                 ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						dbSelectArea("SC5")
						nMaxFor := FCount()
						For nCntFor := 1 To nMaxFor
							M->&(EVAL(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
						Next nCntFor
						M->C5_TIPO    := "N"
						M->C5_CLIENTE := aSocios[nX][1]
						M->C5_LOJACLI := aSocios[nX][2]
						M->C5_CLIENT  := Iif(!Empty(AAH->(FieldGet(FieldPos("AAH_CLIENT")))),AAH->AAH_CLIENT,AAH->AAH_CODCLI)     ///  Alterado para assumir cliente de entrega cadastrado no contrato
						M->C5_LOJAENT := Iif(!Empty(AAH->(FieldGet(FieldPos("AAH_LOJENT")))),AAH->AAH_LOJENT,AAH->AAH_LOJA)		  ///  Alterado para assumir Loja do cliente de entrega cadastrado no contrato
						M->C5_TIPOCLI := SA1->A1_TIPO
						M->C5_CONDPAG := AAH->AAH_CPAGPV
						M->C5_EMISSAO := dProcesso
						M->C5_TABELA  := "1"
						M->C5_ACRSFIN := SE4->E4_ACRSFIN
						M->C5_DESC1   := 0
						M->C5_DESC2   := 0
						M->C5_DESC3   := 0
						M->C5_DESC4   := 0
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Preenche aCols                                       ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aadd(aCols,Array(nUsado+1))
						nAcols := Len(aCols)
						aCols[nAcols,nUsado+1] := .F.   
						 
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Define o CFO                                         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aDadosCFO := {}
					 	Aadd(aDadosCfo,{"OPERNF","S"})
					 	Aadd(aDadosCfo,{"TPCLIFOR",SA1->A1_TIPO})					
					 	Aadd(aDadosCfo,{"UFDEST"  ,SA1->A1_EST})
					 	Aadd(aDadosCfo,{"INSCR"   ,SA1->A1_INSCR})				 	
						cCfo := MaFisCfo(,SF4->F4_CF,aDadosCfo)
						                   
					 	aColsFor := {} 									
						
						For nCntFor := 1 To nUsado
							Do Case
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_ITEM" )
									aCols[nAcols,nCntFor] := "01"
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_PRODUTO" )
									aCols[nAcols,nCntFor] := AAH->AAH_CODPRO
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_UM" )
									aCols[nAcols,nCntFor] := SB1->B1_UM
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_QTDVEN" )
										aCols[nAcols,nCntFor] := nQtd
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_PRCVEN" )
										aCols[nAcols,nCntFor] := nVlrCtr
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_VALOR" )
									aCols[nAcols,nCntFor] := nValor
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_TES" )
									aCols[nAcols,nCntFor] := RetFldProd(SB1->B1_COD,"B1_TS")
									If ( Empty(aCols[nAcols,nCntFor]) )
										lTravas := .F.
									EndIf
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_CODISS" )
									aCols[nAcols,nCntFor] := SB1->B1_CODISS
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_CF" )
									aCols[nAcols,nCntFor] := cCfo
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_SEGUM" )
									aCols[nAcols,nCntFor] := SB1->B1_SEGUM
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_LOCAL" )
									aCols[nAcols,nCntFor] := cLocPad
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_ENTREG" )
									aCols[nAcols,nCntFor] := M->C5_EMISSAO
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_PEDCLI" )
									//Grava o nr. do contrato corrente
									aCols[nAcols,nCntFor] := AAH->AAH_CONTRT
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_DESCRI" )
									aCols[nAcols,nCntFor] := SB1->B1_DESC
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_PRUNIT" )
									aCols[nAcols,nCntFor] := nVlrCtr
								Case ( AllTrim(aHeader[nCntFor,2]) == "C6_CONTRT" )
									aCols[nAcols,nCntFor]	:= AAH->AAH_CONTRT
								Case (AllTrim(aHeader[nCntFor,2]) == "C6_TPCONTR" )
									aCols[nAcols,nCntFor]	:= "3"
								OtherWise
									aCols[nAcols,nCntFor] := CriaVar(aHeader[nCntFor,2],.T.)
							EndCase
						Next nCntFor
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Efetua a Gravacao do Pedido de Venda                                    ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( lTravas )
							Begin Transaction
								
								a410Grava("SC5","SC6",NIL,NIL,NIL,NIL,NIL,nStack)          
						
								If Empty( nPosRecAAH := AScan( aRecsAAH, { |x| x[1] == AAH->AAH_CONTRT } ) )
									AAdd( aRecsAAH, { AAH->AAH_CONTRT, AAH->( RecNo() ), M->C5_NUM, M->C5_EMISSAO } ) 
								Else							
									aRecsAAH[ nPosRecAAH ] := { AAH->AAH_CONTRT, AAH->( RecNo() ), M->C5_NUM, M->C5_EMISSAO }
								EndIf 
								
								While GetSX8Len() > nStack 
									ConfirmSx8()
								EndDo
								EvalTrigger()
								
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Libera numeros reservados                            ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								FreeUsedCode(.T.) 
												
								If lATA210PV
									ExecBlock("ATA210PV",.F.,.F.)
								EndIf        
								
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Event Viewer - Envia e-mail ou RSS para    ³                   
								//| Geracao de Pedido de Venda - Field Service.³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

								cEventID  := "010" 
								cMensagem := SC5->C5_NUM +  SA1->A1_COD + "/" + SA1->A1_LOJA + "-" + SA1->A1_NOME + "-" + AAH->AAH_CONTRT				
								EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID, FW_EV_LEVEL_INFO,""/*cCargo */,STR0006,cMensagem)

								If TECAAWHiPV()
									CtrHistPV()
								EndIf
								
							End Transaction
						EndIf
						While GetSX8Len() > nStack 
							RollBackSx8()
						EndDo
						For nCntFor := 1 To Len(aTravas)
							dbSelectArea(aTravas[nCntFor][1])
							dbGoto(aTravas[nCntFor][2])
							MsUnLock()
						Next nCntFor
							
						aTravas := {}
						aCols   := {}
						lTravas := .T.
					
					Next nX
						
					If lTravas .And. (TECAAHReco() .AND. AAH->AAH_NUMREC > 0 )
					 	//Realiza a busca dos titulos provisorios para exclui-los
					 	At210TitPr(nValor,dProcesso,AAH->AAH_FILIAL,AAH->AAH_CONTRT,AAH->AAH_CPAGPV)
					EndIf

				     Inclui := lBkpInclui
				     Altera := lBkpAltera
					
				EndIf						
			EndIf
		EndIf
	    
		aSocios := {}
		lRateio := .F.
		nQtd	:= 1
	    nQtdRst := 1
	
		IncProc()
		dbSelectArea("AAH")
		dbSkip()
	EndDo
	
	dProcesso++ 	

EndDo 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Grava as datas e os numeros de pedido                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( aRecsAAH ) 
	For nLoop := 1 To Len( aRecsAAH )
	
		AAH->( dbGoto( aRecsAAH[ nLoop, 2 ] ) ) 
		
		RecLock("AAH", .F. )
		AAH->AAH_ULTPED := aRecsAAH[ nLoop, 3 ] 
		AAH->AAH_ULTEMI := aRecsAAH[ nLoop, 4 ] 
		
		AAH->( MsUnLock() )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto apos a gravacao                                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lATA210GP
			ExecBlock("ATA210GP",.F.,.F.)
		EndIf        
		 
	Next nLoop	
EndIf
		
Return(Nil)
	
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At210Auto ³ Autor ³ Eduardo Riera         ³ Data ³ 20.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento dos Contratos de Manutencao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At210Auto(cAlias,nReg,nOpc)

Local aArea    := GetArea()
Local dUlContr 

If ( SuperGetMV("MV_CONTRPV") )
	If ( dDataBase > ( dUlContr := SuperGetMV("MV_ULCONTR") ) )		
		Processa({|lEnd| At210GeraPv(cAlias,nReg,nOpc,dUlContr)}, STR0005 ) // "Gerando P.Vendas por Contrato"
		Processa({|lEnd| At210GeraOs(cAlias,nReg,nOpc,dUlContr)}, STR0007 ) // "Processando O.S. dos contratos de manutenção"
		dbSelectArea("SX6")
		PutMV( "MV_ULCONTR", dDataBase ) 
	EndIf
EndIf

RestArea(aArea)

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At210GeraOsºAutor ³Vendas CRM          º Data ³  07/06/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Gera as OSs para apontamentos dos contratos de manutencao   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA210                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function At210GeraOs(cAlias,nReg,nOpc,dUlContr)

Local cFilAAH	 := xFilial("AAH")
Local cFilAB6	 := xFilial("AB6")
Local cFilAB7	 := xFilial("AB7")
Local cFilAB8	 := xFilial("AB8")
Local cTipo		 := ""

Local aNumOS	 := {} 
Local aCabec	 := {}
Local aItem		 := {}
Local aItens	 := {}
Local aCond		 := {}

Local nX		 := 0

Local dProcesso  := If( ValType( dUlContr ) == "D", dUlContr, dDataBase )

Local lGeraOS	 := .F.
Local lContrtAtv := .T.
Local lFimContrt


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Possibilita ou nao a geracao de pedido de venda para  ³
//³ contrato de manutencao cancelado.           		  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

AB7->(dbSetOrder(1)) //AB7_FILIAL+AB7_NUMOS+AB7_ITEM  
AB8->(dbSetOrder(1)) //AB8_FILIAL+AB8_NUMOS+AB8_ITEM+AB8_SUBITE

dbSelectArea("AAH")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Percorre todos os dias desde o ultimo processamento ate hoje            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While dProcesso <= dDataBase 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processamento dos Contratos                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ProcRegua(LastRec())
	AAH->(dbSeek(cFilAAH))
	While ( !AAH->(Eof()) .AND. AAH->AAH_FILIAL == cFilAAH ) 
		
		lContrtAtv	:= ( AAH->AAH_STATUS == "1" )
		
		//Contrato Ativo gerado a partir de uma proposta
		If !Empty(AAH->AAH_PROPOS) .AND. lContrtAtv                                                                           
			
			lFimContrt	:= .F.			             
			lGeraOS		:= .F.
			aNumOS		:= {}
			
			AB6->(dbSetOrder(6)) //AB6_FILIAL+AB6_TPCONT+AB6_CONTRT+AB6_NUMOS
			AB6->(DbSeek(cFilAB6+"1"+AAH->AAH_CONTRT))
			
			//Busca as OS que estao em aberto, geradas pelo contrato e com mais de 30 dias
			While !AB6->(Eof()) 					.AND.;
				AB6->AB6_FILIAL == cFilAB6 			.AND.;
				AB6->AB6_TPCONT == "1" 				.AND.;
				AB6->AB6_CONTRT == AAH->AAH_CONTRT
				
				If AB6->AB6_STATUS $ "AB"
					AAdd(aNumOS, {AB6->AB6_NUMOS,AB6->AB6_EMISSA})
				End
				
				AB6->(DbSkip())				
			End
			
			//Ordena as OSs abertas por data, da mais recente para a mais antiga
			aSort(aNumOS,,,{|x,y| x[2] > y[2] })
			
			aCond	:= Condicao(100.00,AAH->AAH_CONPAG,0,dProcesso-Day(dProcesso)+1) //Valor fixo, apenas para obter datas da condicao
			
			//Verifica se o contrato esta encerrado			
			If (AAH->AAH_TPCONT == "2" .AND. dProcesso > AAH->AAH_FIMVLD)
				lFimContrt := .T.
			Else
				For nX := 1 To Len(aCond)		
					If (((Len(aNumOs) > 0 .AND. aNumOS[1,2] < aCond[nX][1]) .OR. Len(aNumOs) == 0) .AND. dProcesso == aCond[nX][1])
						lGeraOS	:= .T.
						Exit
					EndIf
				Next nX
			EndIf
			
			If lGeraOs .OR. lFimContrt

				//Encerra as OS abertas associadas ao contrato
				For nX := 1 to Len(aNumOs)
		
					aCabec	:= {}
					aItens	:= {}                            
					
					aAdd(aCabec,{"AB6_NUMOS",aNumOs[nX,1],Nil})
		                
	                AB7->(DbSeek(cFilAB7+aNumOs[nX,1]))
	                //Define o status final de cada item das OSs e as finaliza
	                While !AB7->(Eof()) .AND. AB7->AB7_NUMOS == aNumOs[nX,1]
	                	cTipo := ""                	
	                	If AB7->AB7_TIPO == '1'
	                		cTipo := "5"
						ElseIf AB7->AB7_TIPO $ '34' .AND. AB8->(dbSeek(cFilAB8+AB7->(AB7_NUMOS+AB7_ITEM)))
							cTipo := "2"
						EndIf
						
						If !Empty(cTipo)
							aItem := {}
							aAdd(aItem,{"LINPOS"	, "AB7_ITEM",AB7->AB7_ITEM})
							aAdd(aItem,{"AB7_TIPO"  , cTipo		,Nil})
							aAdd(aItens,aItem)		
						EndIf
					
	                	AB7->(DbSkip())
	                End
					     
					If Len(aItens) > 0
						TECA450(,aCabec,aItens,,4) 
					EndIf
				
				Next nX

				//Se o contrato estiver dentro da vigencia, gera nova OS
				If dProcesso >= AAH->AAH_INIVLD .AND. !lFimContrt
					aChaveAA3 := At210BaseCM(AAH->AAH_CONTRT)
					At240GerOs(	AAH->AAH_PROPOS	, aChaveAA3	, AAH->AAH_CPAGPV	, AAH->AAH_OCOROS	,;
								AAH->AAH_CONTRT	, Nil		, dProcesso			)
				EndIf
			EndIf			
		EndIf
	
		IncProc()
		AAH->(dbSkip())
	End 	
	dProcesso++ 	
End

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At210BaseCMºAutor ³Vendas CRM          º Data ³  08/06/2011 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Retorna a lista de bases de atendimento associadas a um con-º±±
±±º          ³trato de manutencao                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpC1 - Codigo do contrato de manutencao                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA210                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function At210BaseCM(cContrato)

Local aArea		:= GetArea()
Local aAreaAA3	:= AA3->(GetArea())
Local aBase		:= {}
Local cFilAA3	:= xFilial("AA3")

dbSelectArea("AA3")
dbSetOrder(2) //AA3_FILIAL+AA3_CONTRT+AA3_CODCLI+AA3_LOJA+AA3_CODPRO+AA3_NUMSER
dbSeek(cFilAA3+cContrato)

While !Eof() .AND. AA3->AA3_FILIAL == cFilAA3 .AND. AA3->AA3_CONTRT == cContrato
	AAdd(aBase,AA3->(AA3_FILIAL+AA3_CODFAB+AA3_LOJAFA+AA3_CODPRO+AA3_NUMSER))
	dbSkip()
End

RestArea(aAreaAA3)
RestArea(aArea)

Return aBase

//------------------------------------------------------------------------------
/*/{Protheus.doc} At210TitPr

@description Função para recuperar e deletar os titulos provisorios

@param nValor - Valor do pedido de venda
@param dProcesso - Data de processamento
@param cFilial - Filial do contrato
@param cContr - Codigo do Contrato
@param cCondPV - Condição de pagamento do PV

@author	Luiz Gabriel
@since	04/05/2020
/*/
//------------------------------------------------------------------------------
Static Function At210TitPr(nValor,dProcesso,cFilAAH,cContr,cCondPV,lDelete)
Local nCntFor	:= 0
Local cData 	:= ""
Local aCondPV	:= {}
Local nCondPv	:= 0
Local cAliasTXJ := GetNextAlias()
Local cRegDel	:= "%%"

Default lDelete := .T.

aCondPV := Condicao(nValor,cCondPV,0,dProcesso)
nCondPv := Len(aCondPV)

For nCntFor := 1 To nCondPv
	If nCntFor > 1
		cData += "','"
	EndIf
	cData += dToS(aCondPV[nCntFor][1])
Next nCntFor

If lDelete 
	cRegDel := "AND TXJ.D_E_L_E_T_ = '' AND SE1.D_E_L_E_T_ = ''"
	cRegDel := '%' + cRegDel + '%'	
EndIf 

BeginSQL Alias cAliasTXJ	
	SELECT 
		TXJ.R_E_C_N_O_ TXJ_RECNO,
		SE1.R_E_C_N_O_ SE1_RECNO
	FROM
		%Table:TXJ% TXJ
	INNER JOIN %Table:SE1% SE1 ON E1_FILIAL = TXJ.TXJ_FILE1
                            AND SE1.E1_NUM = TXJ.TXJ_NUM
                        	AND SE1.E1_PREFIXO = TXJ.TXJ_PREFIX
                            AND SE1.E1_PARCELA = TXJ.TXJ_PARCEL
                            AND SE1.E1_TIPO    = 'PR' 	
	WHERE 
		TXJ.TXJ_FILIAL = %xFilial:TXJ% AND 
		TXJ.TXJ_CTRMNT = %exp:cContr% AND 
		TXJ.TXJ_FILAAH = %exp:cFilAAH% AND 
		TXJ.TXJ_VENCTO IN (%Exp:cData%)
		%EXP:cRegDel% 

EndSQL

While !( cAliasTXJ )->( EOF() )
	// Posiciona nos itens que serão deletados
	TXJ->( DbGoTo( (cAliasTXJ)->TXJ_RECNO ) )
	TXJ->(RecLock("TXJ",.F.))
		If lDelete
			TXJ->(DbDelete())
		Else 
			TXJ->(DbRecall())
		EndIf 	 
	TXJ->(MsUnlock())

	SE1->( DbGoTo( (cAliasTXJ)->SE1_RECNO ) )
	SE1->(RecLock("SE1",.F.))
		If lDelete
			SE1->(DbDelete()) 
		Else 
			SE1->(DbRecall()) 
		EndIf 	
	SE1->(MsUnlock())

	( cAliasTXJ )->( DbSkip() )
EndDo
( cAliasTXJ )->( DbCloseArea() )

At200RecPr(aCondPV,dProcesso)

Return .T.

//--------------------------------------------------------------------------------
/*/{Protheus.doc} CtrHistPV

@description Cria o historico dos pedidos de venda do contrato de manutenção
@author Augusto Albuquerque
@since  13/01/2022
/*/
//--------------------------------------------------------------------------------
Static Function CtrHistPV()
Local aArea	:= GetArea()

dbSelectArea("AAW")
AAW->(dbSetOrder(1))

RecLock("AAW",.T.)
	REPLACE AAW_FILIAL 	With xFilial("AAW")
	REPLACE AAW_CODIGO 	With GetSXENum("AAW","AAW_CODIGO")
	REPLACE AAW_CONTRA 	With AAH->AAH_CONTRT
	REPLACE AAW_NUMPED 	With SC5->C5_NUM
	REPLACE AAW_DTEMIS 	With SC5->C5_EMISSAO
	REPLACE AAW_FILAAH	With AAH->AAH_FILIAL
MsUnlock()
ConfirmSX8()

RestArea(aArea)
Return

//--------------------------------------------------------------------------------
/*/{Protheus.doc} AT210VerPV

@description Exclui a tabela auxiliar do pedido de venda e volta a data do ultimo pedido
@author Augusto Albuquerque
@since  13/01/2022
/*/
//--------------------------------------------------------------------------------
Function AT210VerPV()
Local aArea		:= GetArea()
Local cAliasAAW	:= ""
Local lRet		:= .T. 

If UsaCTRMan()
	If TECAAWHiPV()
		dbSelectArea("AAW")
		AAW->(dbSetOrder(2))
		If AAW->(dbSeek(xFilial("AAW")+SC5->C5_NUM+DToS(SC5->C5_EMISSAO) ))
			cAliasAAW := GetNextAlias()
			BeginSQL Alias cAliasAAW
			SELECT
				AAW.AAW_DTEMIS
			FROM %Table:AAW% AAW
			WHERE
				AAW.AAW_FILIAL = %Exp:xFilial("AAW")%
				AND AAW.%NotDel% 
				AND AAW.AAW_CONTRA = %Exp:AAW->AAW_CONTRA%
				AND AAW.AAW_DTEMIS > %Exp:DToS(SC5->C5_EMISSAO)%
				ORDER BY AAW_DTEMIS DESC
			EndSQL

			If !(cAliasAAW)->(EOF())
				lRet := .F.
				Help(NIL, NIL, "AT210VerPV", NIL, STR0008 + DToC(SToD((cAliasAAW)->AAW_DTEMIS)) + STR0009 + DToC(SC5->C5_EMISSAO) + ")." , 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0010}) // "Existe pedido de venda com data de emissão superior(" ## ") ao que esta excluindo no momento(" ## "Para realizar a exclusão, por favor realize a exclusão dos outros pedidos de vendo com data mais recentes."
			EndIf
			(cAliasAAW)->(DbCloseArea())
		EndIf
	EndIf
EndIf

RestArea(aArea)
Return lRet

//--------------------------------------------------------------------------------
/*/{Protheus.doc} AT210ExcPV

@description Exclui a tabela auxiliar do pedido de venda e volta a data do ultimo pedido
@author Augusto Albuquerque
@since  13/01/2022
/*/
//--------------------------------------------------------------------------------
Function AT210ExcPV()
Local aArea		:= {}
Local cDtEmis	:= ""
Local cAliasAAW	:= ""
Local cFilAAH	:= ""
Local cNumPed	:= ""

If UsaCTRMan() .AND. TECAAWHiPV()
	dbSelectArea("AAW")
	AAW->(dbSetOrder(2))
	If AAW->(dbSeek(xFilial("AAW")+SC5->C5_NUM+DToS(SC5->C5_EMISSAO) ))
		cFilAAH := xFilial("AAH")
		aArea := GetArea()
		cAliasAAW := GetNextAlias()
		BeginSQL Alias cAliasAAW
		SELECT
			AAW.AAW_DTEMIS, AAW.AAW_NUMPED, AAW.AAW_FILAAH 
		FROM %Table:AAW% AAW
		WHERE
			AAW.AAW_FILIAL = %Exp:xFilial("AAW")%
			AND AAW.%NotDel% 
			AND AAW.AAW_CONTRA = %Exp:AAW->AAW_CONTRA%
			AND AAW.AAW_DTEMIS < %Exp:DToS(SC5->C5_EMISSAO)%
			ORDER BY AAW_DTEMIS DESC
		EndSQL

		If !(cAliasAAW)->(EOF())
			cDtEmis := (cAliasAAW)->AAW_DTEMIS
			cNumPed	:= (cAliasAAW)->AAW_NUMPED
			cFilAAH	:= (cAliasAAW)->AAW_FILAAH
		EndIf

		(cAliasAAW)->(DbCloseArea())
		RestArea(aArea)

		dbSelectArea("AAH")
		AAH->(dbSetOrder(1))
		If AAH->(dbSeek(cFilAAH+AAW->AAW_CONTRA))
			//Recupera o titulo provisorio deletado ao gerar o pedido de venda
			At210TitPr(1000,SC5->C5_EMISSAO,AAH->AAH_FILIAL,AAH->AAH_CONTRT,AAH->AAH_CPAGPV,.F.)
			AAH->(RecLock("AAH",.F.))
				AAH->AAH_ULTPED := cNumPed
				AAH->AAH_ULTEMI := SToD(cDtEmis)
			AAH->(MsUnlock())

			AAW->(RecLock("AAW", .F.))			
				AAW->(DbDelete())
			AAW->( MsUnlock() )
		EndIf
	EndIf
EndIf

Return

//--------------------------------------------------------------------------------
/*/{Protheus.doc} UsaCTRMan

@description Verifica se possui integração com field service
@author Augusto Albuquerque
@since  13/01/2022
/*/
//--------------------------------------------------------------------------------
Static Function UsaCTRMan()
Local cAliasAAH	:= ""
Local cSql 		:= ""

If cUsaCtrMan == '0'

	cSql += " SELECT 1 REC FROM " + RetSqlName( "AAH" ) + " AAH "
	cSql += " WHERE "
	cSql += " AAH.AAH_FILIAL = '" + xFilial("AAH") + "' AND "
	cSql += " AAH.D_E_L_E_T_ = ' ' "

	cSql := ChangeQuery(cSql)
	cAliasAAH := GetNextAlias()
	dbUseArea( .T., "TOPCONN", TCGENQRY(,,cSql),cAliasAAH, .F., .T.)

	If (cAliasAAH)->(EOF())
		cUsaCtrMan := '2'
	Else
		cUsaCtrMan := '1'
	EndIf
	(cAliasAAH)->(DbCloseArea())
EndIf

Return (cUsaCtrMan == '1')
