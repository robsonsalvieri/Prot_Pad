#INCLUDE "TBICONN.CH"
#INCLUDE "FATA310.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "CRMDEF.CH"

PUBLISH MODEL REST NAME FATA310 SOURCE FATA310

Static aCposAg := NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³FATA310   ³ Autor ³Eduardo Riera          ³ Data ³11.01.2000  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Cadastro de Apontamentos da Visita                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sergio Silveir³10/12/00³      ³  REVISAO GERAL - CRM.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FATA310(xAutoCab,xAutoItens,nOpcAuto,cFilDef,aAddFil,cVDefault)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aArea	   	:= GetArea()
Local aSavARot 	:= If(Type("aRotina")<>"U",aRotina,{})
Local cSavcCad 	:= If(Type("cCadastro")<>"U",cCadastro,"")

Local aAreaAD5   := {}
Local cFilAD5    := CriaVar("AD5_FILIAL")
Local cVendedor  := CriaVar("AD5_VEND")
Local dDataApont := CriaVar("AD5_DATA")
Local cSequencia := Criavar("AD5_SEQUEN")
Local nPosCampo  := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro	:= OemToAnsi(STR0001) //"Apontamento da Visita"
Private aRotina   	:= MenuDef()

Default cFilDef   := ""
Default aAddFil   := {}
Default cVDefault := ""

SaveInter()

If !Empty(xAutoCab)

	If ValType(xAutoItens) <> "A"
		xAutoItens := {}
	EndIf
	
	// Posicionar no registro a ser alterado/excluido
	If !(nOpcAuto == MODEL_OPERATION_INSERT)

		DbSelectArea("AD5")
		aAreaAD5 := AD5->(GetArea())

		nPosCampo := aScan(xAutoCab, {|aCampo| AllTrim(Upper(aCampo[1])) == "AD5_FILIAL"})
		If !Empty(nPosCampo)
			cFilAD5 := PadR(xAutoCab[nPosCampo][2], TamSX3("AD5_FILIAL")[1], Space(1))
		Else
			cFilAD5 := xFilial("AD5")
		Endif

		nPosCampo := aScan(xAutoCab, {|aCampo| AllTrim(Upper(aCampo[1])) == "AD5_VEND"})
		If !Empty(nPosCampo)
			cVendedor := PadR(xAutoCab[nPosCampo][2], TamSX3("AD5_VEND")[1], Space(1))
		Endif

		nPosCampo := aScan(xAutoCab, {|aCampo| AllTrim(Upper(aCampo[1])) == "AD5_DATA"})
		If !Empty(nPosCampo)
			dDataApont := xAutoCab[nPosCampo][2]
		Endif

		nPosCampo := aScan(xAutoCab, {|aCampo| AllTrim(Upper(aCampo[1])) == "AD5_SEQUEN"})
		If !Empty(nPosCampo)
			cSequencia := PadR(xAutoCab[nPosCampo][2], TamSX3("AD5_SEQUEN")[1], Space(1))
		Endif  

		AD5->(DbSetOrder(1))	// AD5_FILIAL + AD5_VEND + DToS(AD5_DATA) + AD5_SEQUEN 

		// Se houver dados para a chave completa tenta a pesquisa
		If (Empty(cVendedor) .Or. Empty(dDataApont) .Or. Empty(cSequencia)) ;
				.Or. ;
		   !(AD5->(DbSeek(cFilAD5 + cVendedor + DToS(dDataApont) + cSequencia)))
			HELP(" ", 1, STR0031, NIL, STR0032, 1, 1,,,,,,{STR0033})	//#"Inconsistência no Apontamento" #"O apontamento informado não foi encontrado na base de dados." #"Informe um vendedor, data de apontamento e sequência que juntos, identifiquem um Apontamento de Visitas no banco de dados."
			lMsErroAuto := .T.
		Endif
	EndIf
	
	If !(lMsErroAuto)
		FWMVCRotAuto(ModelDef(),"AD5",nOpcAuto,{{"AD5MASTER",xAutoCab},{"AD6DETAIL",xAutoItens}}, .F.)
	EndIf

	If (!Empty(aAreaAD5))
		RestArea(aAreaAD5)
	EndIf

Else
	//-------------------------------
	// Browse Apontamentos da Visita.
	//-------------------------------
	BrowseDef( /*oMBrowse*/, cFilDef, aAddFil, cVDefault )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura os dados de Entrada                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRotina  	:= aSavARot
cCadastro	:= cSavcCad
 
RestInter()
RestArea(aArea)

aSize(aAreaAD5, 0)
aAreaAD5 := Nil

Return(.T.)

//------------------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef
Browse de Cadastro de Apontamentos da Visita
@sample	BrowseDef( oMBrowse, cFilDef, aAddFil, cVDefault )
@param		oMBrowse	, Objeto	, Browse criado pelo Widget da Area de Trabalho.
			cFilDef	, Caracter	, Filtro padrao.
			aAddFil	, Array		, Filtros relacionados.
			cVDefault	, Caracter , Visao padrao.
@return	oMBrowse	, Objeto	, Retorna o objeto FWMBrowse.
@author	Anderson Silva
@since		05/12/2015
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function BrowseDef( oMBrowse, cFilDef, aAddFil, cVDefault )

Local cFiltraAD5 	:= ""
Local lFA310BRW		:= ExistBlock("FA310BRW")
Local oTableAtt		:= Nil
Local nX		  	:= 0
Local nScan			:= 0 
Local lWidget		:= .F.

Default oMBrowse	:= Nil
Default cFilDef		:= ""
Default aAddFil		:= {}
Default cVDefault	:= ""

DbSelectArea("AD5")
DbSetOrder(1)

If Empty( oMBrowse )
	oMBrowse := FWMBrowse():New()
	oMBrowse:SetAlias("AD5")
Else
	lWidget := .T.
EndIf

oMBrowse:SetCanSaveArea(.T.) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia de Filtros na mBrowse ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(cFilDef) .And. lFA310BRW
	cFiltraAD5 := ExecBlock("FA310BRW",.F.,.F.)
	If ValType(cFiltraAD5) <> "C"
		cFilDef := ""
	EndIf
	oMBrowse:AddFilter(STR0010,cFiltraAD5,.T.,.T.)
	oMBrowse:ExecuteFilter()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtro Default do Browse de Apontamentos. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty( cFilDef )
	oMBrowse:SetFilterDefault( cFilDef )
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Filtros adicionais do Browse de Apontamentos. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len( aAddFil )
	oMBrowse:DeleteFilter(aAddFil[nX][ADDFIL_ID])
	oMBrowse:AddFilter(	aAddFil[nX][ADDFIL_TITULO]		,;
							aAddFil[nX][ADDFIL_EXPR]			,;
							aAddFil[nX][ADDFIL_NOCHECK]		,;
							aAddFil[nX][ADDFIL_SELECTED]	,;
							aAddFil[nX][ADDFIL_ALIAS]		,;
							aAddFil[nX][ADDFIL_FILASK]		,;
							aAddFil[nX][ADDFIL_FILPARSER]	,;
							aAddFil[nX][ADDFIL_ID] )
	oMBrowse:ExecuteFilter()
Next nX
		
oMBrowse:SetDescription(STR0001) //"Apontamento da Visita"

If !lWidget 
	oTableAtt := TableAttDef()	
	oMBrowse:SetAttach( .T. ) //Habilita as visões do Browse
	oMBrowse:SetViewsDefault(oTableAtt:aViews)
	// selecionando a visão inicial 
	If !Empty( cVDefault ) 
		nScan := aScan( oTableAtt:aViews, { |x| x:cID == cVDefault } )
		If nScan > 0 
			oMBrowse:SetIDViewDefault( oTableAtt:aViews[nScan]:cID )
		EndIf
	EndIf
	//Se não for SIGACRM inibe a exibição do gráfico
	If nModulo <> 73
		oMBrowse:SetOpenChart( .F. )
	EndIf
	oMBrowse:SetMainProc("FATA310")
	oMBrowse:SetTotalDefault("AD5_FILIAL","COUNT",STR0015) // 'Total de Registros
	oMBrowse:Activate()		
EndIf
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} TableAttDef
Cria as visões e gráficos.
@sample	TableAttDef()
@param		Nenhum
@return	ExpO - Objetos com as Visoes e Gráficos.
@author	Cristiane Nishizaka
@since		28/04/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt 	:= FWTableAtt():New()
//Visões
Local oMyApont		:= Nil // Meus Apontamentos
Local aUserPaper 	:= CRMXGetPaper()
Local cCodUser 		:= If(SuperGetMv("MV_CRMUAZS",, .F.), CRMXCodUser(), RetCodUsr())  
Local cSeqPaper		:= " "

oTableAtt:SetAlias("AD5")

//----------
// Visões
//---------- 
	
// Meus Apontamentos
oMyApont := FWDSView():New()
oMyApont:SetName(STR0014) // "Meus Apontamentos"
oMyApont:SetID("MyApont") 
oMyApont:SetOrder(1) // AD5_FILIAL+AD5_VEND+DTOS(AD5_DATA)+AD5_SEQUEN
oMyApont:SetCollumns({"AD5_DATA",   "AD5_CODCLI", "AD5_LOJA",   "AD5_NROPOR", "AD5_EVENTO",;
                      "AD5_DESEVE", "AD5_PROSPE", "AD5_LOJPRO"})
oMyApont:SetPublic( .T. )
oMyApont:AddFilterRelation( 'AO4', 'AO4_CHVREG', 'AD5_FILIAL+AD5_VEND+AD5_DATA+AD5_SEQUEN' )

If !Empty( aUserPaper )
	cCodUser 	:= aUserPaper[USER_PAPER_CODUSR]
	cSeqPaper	:= aUserPaper[USER_PAPER_SEQUEN] + aUserPaper[USER_PAPER_CODPAPER]			
	oMyApont:AddFilter(STR0014, "AO4_CODUSR == '"+RetCodUsr()+"' .And. ( AO4_USRPAP == '" + cSeqPaper + "' .Or. AO4_USRPAP == ' ' ) .AND. AO4_CTRLTT == .T.","AO4") // "Meus Apontamentos"
Else
	oMyApont:AddFilter(STR0014, "AO4_CODUSR == '"+cCodUser+"' .AND. AO4_CTRLTT == .T.","AO4") // "Meus Apontamentos"
EndIf
		
oTableAtt:AddView(oMyApont)
Return (oTableAtt)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Local   nPos        := 0
Local   aEntRelac   := {}
Local   aAtiv       := {}
Local   aAnotac     := {}

Private aRotina     := {}

ADD OPTION aRotina Title STR0002 Action 'VIEWDEF.FATA310' OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina Title STR0003 Action 'VIEWDEF.FATA310' OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina Title STR0004 Action 'VIEWDEF.FATA310' OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina Title STR0005 Action 'VIEWDEF.FATA310' OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina Title STR0006 Action 'VIEWDEF.FATA310' OPERATION 5 ACCESS 0 //"Excluir"

If ExistBlock("FT310MNU")
	ExecBlock("FT310MNU",.F.,.F.)
EndIf

If nModulo == 73
	ADD OPTION aEntRelac TITLE STR0011 Action "CRMA200('AD5')" 		  OPERATION 8 ACCESS 0 //"Privilégios"
	
	aEntRelac := CRMXINCROT("AD5",aEntRelac)
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA190Con()",Nil) })
	If nPos > 0
		ADD OPTION aRotina TITLE aEntRelac[nPos][1] ACTION aEntRelac[nPos][2] OPERATION 8  ACCESS 0//"Conectar"
		Adel(aEntRelac,nPos)
		Asize(aEntRelac,Len(aEntRelac)-1)
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x|  IIF(ValType(x[2]) == "C", x[2] == "CRMA180()", Nil) })
	If nPos > 0
		ADD OPTION aAtiv   TITLE STR0016 ACTION "CRMA180(,,,3,,)" OPERATION 3  ACCESS 0//"Nova Atividade"
		ADD OPTION aAtiv   TITLE STR0017 ACTION "CRMA180()" OPERATION 8  ACCESS 0 //"Todas as ATividades"
		aEntRelac[nPos][2] := aAtiv
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA090()", Nil)})
	If nPos > 0
		ADD OPTION aAnotac   TITLE STR0018 ACTION "CRMA090(3)" OPERATION 3  ACCESS 0 //"Nova Anotação"
		ADD OPTION aAnotac   TITLE STR0019 ACTION "CRMA090()" OPERATION 8  ACCESS 0 //"Todas as Anotações"
		aEntRelac[nPos][2] := aAnotac
	EndIf

	aSort(aEntRelac,,,{ | x,y | y[1] > x[1] } )
	ADD OPTION aRotina TITLE STR0020 ACTION aEntRelac OPERATION 8  ACCESS 0//"Relacionadas"
EndIf
Return(aRotina) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ViewDef     ³ Autor ³Vendas & CRM           ³ Data ³ 08/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao da View                                          	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oView                                                       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ViewDef()

Local oModel		:= FWLoadModel( 'FATA310' )	// Cria um objeto de Modelo de dados baseado no ModelDef do fonte informado
Local oStruAD5	:= FWFormStruct( 2, 'AD5' )	// Cria as estruturas a serem usadas na View
Local oStruAD6	:= FWFormStruct( 2, 'AD6' ) // Cria as estruturas a serem usadas na View
Local oView
Local lFT310CAN	:= ExistBlock("FT310CAN")		// Interface de visualização construída
Local aUsrMemo	:= If( ExistBlock( "FT310MEM" ), ExecBlock( "FT310MEM", .F.,.F. ), {} ) //PE que retorna um array com os campos memo de usuario
Local nLoop		:= 1

If AD5->(FieldPos("AD5_IDESTN")) > 0
	oStruAD5:RemoveField("AD5_IDESTN")
	oStruAD5:RemoveField("AD5_NVESTN")
EndIf

oStruAD6:RemoveField("AD6_VEND")
oStruAD6:RemoveField("AD6_DATA")
oStruAD6:RemoveField("AD6_SEQUEN")

oView := FWFormView():New()								// Cria o objeto de View
oView:SetModel( oModel )								// Define qual Modelo de dados será utilizado

If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "AD5"
			oStruAD5:RemoveField(aUsrMemo[ nLoop, 2 ])
		ElseIf aUsrMemo[ nLoop, 1 ] == "AD6"
			oStruAD6:RemoveField(aUsrMemo[ nLoop, 2 ])
		EndIf
	Next nLoop
EndIf

oView:AddField( 'VIEW_AD5', oStruAD5, 'AD5MASTER' )	// Adiciona no nosso View um controle do tipo formulário (antiga Enchoice)
oView:AddGrid(  'VIEW_AD6', oStruAD6, 'AD6DETAIL' )	// Adiciona no nosso View um controle do tipo Grid (antiga Getdados)
oView:AddIncrementField( 'VIEW_AD6', 'AD6_ITEM' )	// Item Incremental do Grid

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para inclusao de botoes do usuario          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "FT310BUT" )
	Ft310AddBt(oView)
EndIf

oView := CRMXAddAct("AD5",oView) //Adcionar Rotinas no 'Ações relacionadas' do Formulário

If lFT310CAN
	oView:SetViewAction('BUTTONCANCEL', {|oView| Ft310BtCan(oView) })
EndIf

oView:CreateHorizontalBox( 'SUPERIOR'  , 50 )
oView:CreateHorizontalBox( 'INFERIOR'  , 50 )

// Relaciona o identificador (ID) da View com o "box" para exibição
oView:SetOwnerView( 'VIEW_AD5', 'SUPERIOR' )
oView:SetOwnerView( 'VIEW_AD6', 'INFERIOR' )
Return oView

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310BtCan  ³ Autor ³Vendas & CRM           ³ Data ³ 08/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Acionado ao Clicar em Cancelar.                            	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310BtCan(oMdl)

Local nOper	:= oMdl:GetOperation()
Local lRet		:= .T.

If nOper == MODEL_OPERATION_INSERT .OR. nOper == MODEL_OPERATION_UPDATE
	If ExistBlock("FT310CAN")
		lRet := ExecBlock("FT310CAN",.F.,.F.)
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310AddBt  ³ Autor ³Vendas & CRM           ³ Data ³ 08/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Adiciona Botoes de Usuario conforme ponto de entrada       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oView                                                       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ft310AddBt(oView)

Local aUsrButtons	:= {}
Local aOpc			:= {MODEL_OPERATION_VIEW,MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE,MODEL_OPERATION_DELETE}
Local nI

For nI := 1 To Len(aOpc)
	aUsrButtons	:= {}
	If ValType( aUsrButtons := ExecBlock( "FT310BUT", .F.,.F., { aOpc[nI] } ) ) == "A"
		AEval( aUsrButtons, { |x| oView:AddUserButton( x[3], x[1], x[2] ,NIL,NIL, {aOpc[nI]}) } )
	EndIf
Next nI

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ModelDef    ³ Autor ³Vendas & CRM           ³ Data ³ 08/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao do Model                                         	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ModelDef()

// Cria as estruturas a serem usadas no Modelo de Dados
Local oStruAD5	:= FWFormStruct( 1, 'AD5' )
Local oStruAD6	:= FWFormStruct( 1, 'AD6' )
Local aUsrMemo	:= If( ExistBlock( "FT310MEM" ), ExecBlock( "FT310MEM", .F.,.F. ), {} ) //Memos de Usuário
Local aMemoAD5	:= {}
Local aMemoAD6	:= {}
Local nLoop		:= 1
Local oModel // Modelo de dados construído

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New( 'FATA310' , /*{|oMdl| Ft310Pre(oMdl) }*/,{|oMdl| Ft310Pos(oMdl)},{|oMdl| Ft310Com(oMdl) },{|oMdl| Ft310BtCan(oMdl)})
oModel:SetVldActivate({|oMdl| Ft310Act(oMdl)})

If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "AD5"
			AAdd( aMemoAD5, { aUsrMemo[ nLoop, 2 ], aUsrMemo[ nLoop, 3 ] } )
			Ft310AddCam(oStruAD5,aUsrMemo[ nLoop, 2 ])
		ElseIf aUsrMemo[ nLoop, 1 ] == "AD6"
			AAdd( aMemoAD6, { aUsrMemo[ nLoop, 2 ], aUsrMemo[ nLoop, 3 ] } )
			Ft310AddCam(oStruAD6,aUsrMemo[ nLoop, 2 ])
		EndIf
	Next nLoop
EndIf

FWMemoVirtual( oStruAD5, aMemoAD5)

FWMemoVirtual( oStruAD6, aMemoAD6)

//Inicializa campos quando vier da workarea
oStruAD5:SetProperty('*'	,MODEL_FIELD_INIT,{|oMdl,cCampo| Ft310Init(oMdl,cCampo)})
If oModel:GetOperation() == MODEL_OPERATION_UPDATE .Or. oModel:GetOperation() == MODEL_OPERATION_INSERT
	oStruAD5:SetProperty('*'	,MODEL_FIELD_WHEN,{|oMdl,cCampo| Ft310When(oMdl,cCampo)})
EndIf

// Adiciona ao modelo um componente de formulário
oModel:AddFields( 'AD5MASTER', /*cOwner*/, oStruAD5)

// Adiciona ao modelo uma componente de grid
oModel:AddGrid( 'AD6DETAIL', 'AD5MASTER', oStruAD6 , NIL, {|oMdl| Ft310LOk(oMdl) })

//Deixa opcional adicionar itens ao ACZ
oModel:GetModel( 'AD6DETAIL'):SetOptional( .T. )

// Faz relacionamento entre os componentes do model
oModel:SetRelation( 'AD6DETAIL', { { 'AD6_FILIAL', 'xFilial( "AD6" )' }, { 'AD6_VEND', 'AD5_VEND' }, { 'AD6_DATA', 'AD5_DATA' }, { 'AD6_SEQUEN', 'AD5_SEQUEN' } }, AD6->( IndexKey( 1 ) ) )
	
// Adiciona a descrição do Modelo de Dados
oModel:SetDescription( STR0001 ) //"Apontamento de visita"

// Adicao do modelo da AO4 para evitar a validacao indevida do relacionamento SX9 antes da funcao CRMA200PAut
AO4GdModel("AD5MASTER", oModel, "AD5" )

// Retorna o Modelo de dados
Return oModel

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310AddCam ³ Autor ³Vendas & CRM           ³ Data ³ 09/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Adiciona o Campo na Estrutura                              	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ft310AddCam(oStru,cCampo)

Local aArea		:= GetArea()
Local aAreaSX3	:= SX3->(GetArea())  

DbSelectArea("SX3")
DbSetOrder(2)
If DbSeek(cCampo) .AND. !X3Usado(cCampo)
	oStru:AddField(X3Titulo(),; 	//CTITULO 
	               X3Descric(),; 	//CTOOLTIP
	               X3Titulo(),;		//CIDFIELD
	               TamSX3(cCampo)[3],;	//cTIPO
	               TamSX3(cCampo)[1],;	//Tamanho
	               TamSX3(cCampo)[2],;	//Decimal
	               {|| .T.},;		//Valid
	               NIL,;				//WHEN
	               NIL,;				//AValues
	               NIL,;				//Obrigat 
	               NIL,;				//Init
	               NIL,;				//KEY
	               NIL,;				//NOUPD
	               NIL,;				//LVIRTUAL
	               "")				//VIRTUAL
EndIf 
RestArea(aAreaSX3)
RestArea(aArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310Com    ³ Autor ³Vendas & CRM           ³ Data ³ 09/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Commit do Model                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310Com(oMdl) 

Local aArea	:= GetArea()
Local nOpc		:= oMdl:GetOperation()
Local lRet		:= .T.

FWFormCommit(oMdl,Nil,{|oMdl,cId,cAlias| lRet := Ft310CmtAft(oMdl,cId,cAlias) })

If ExistBlock("Fat310Gr")
	ExecBlock( "Fat310Gr", .F., .F., {nOpc-2})
EndIf
	
RestArea( aArea )

Return( lRet )

//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft310CmtAft
Bloco de transacao durante o commit do model. 
@sample	Ft310CmtAft(oModel,cId,cAlias)
@param		ExpO1 - Modelo de dados
			ExpC2 - Id do Modelo
			ExpC3 - Alias
@return	ExpL  - Verdadeiro / Falso
@author	Anderson Silva
@since		06/08/2014
@version	12               
/*/
//------------------------------------------------------------------------------
Static Function Ft310CmtAft(oModel,cId,cAlias)

Local nOperation	:= oModel:GetOperation()
Local cNrOpor 	:= ""
Local cEvento 	:= ""
Local cChave    	:= ""
Local aAutoAO4  	:= {}
Local lRetorno 	:= .T.
Local cCodUsr		:= ""
Local nOpcACZ		:= 0

If cId == "AD5MASTER"
	
	If nOperation == MODEL_OPERATION_INSERT .OR. nOperation == MODEL_OPERATION_UPDATE .OR. nOperation == MODEL_OPERATION_DELETE
		cNrOpor := oModel:GetValue("AD5_NROPOR")
		cEvento := oModel:GetValue("AD5_EVENTO")
		If ( !Empty(cNrOpor) .And. !Empty(cEvento) )
			If nOperation == MODEL_OPERATION_INSERT
				nOpcACZ := 1
			ElseIf nOperation == MODEL_OPERATION_UPDATE
				nOpcACZ := 2
			Else
				nOpcACZ := 3
			EndIf
			lRetorno := Ft310Reg(nOpcACZ,cNrOpor,cEvento,oModel)
		EndIf
	EndIf
	
	If lRetorno
		If !Empty(RetCodUsr())
			cCodUsr:= RetCodUsr() //Usuário do protheus
		Else
			cCodUsr:= UsrPrtErp() //Usuario do portal
		EndIf
				
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Adiciona ou Remove o privilegios deste registro.  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( nOperation == MODEL_OPERATION_INSERT .OR. nOperation == MODEL_OPERATION_DELETE )
			cChave 	:= PadR(xFilial("AD5")+oModel:GetValue("AD5_VEND")+dTos(oModel:GetValue("AD5_DATA"))+oModel:GetValue("AD5_SEQUEN"),TAMSX3("AO4_CHVREG")[1])
			aAutoAO4	:= CRMA200PAut(nOperation,"AD5",cChave,cCodUsr,/*aPermissoes*/,/*aNvlEstrut*/,/*cCodUsrCom*/,/*dDataVld*/)
			CRMA200Auto(aAutoAO4[1],aAutoAO4[2],nOperation)
		EndIf

		If ( lRetorno .And. nOperation == MODEL_OPERATION_DELETE ) 
			CRMA060("AD5", AD5->( RecNo() ) , MODEL_OPERATION_DELETE, .T. )	  
		EndIf
	EndIf
	
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310Act    ³ Autor ³Vendas & CRM           ³ Data ³ 08/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Validacao Activate Model                                   	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310Act(oMdl)

Local lRet			:= .T.
Local cCodCli		:= ""
Local cLojCli		:= ""
Local cHelpCli	:= ""
Local aArea		:= GetArea()
Local aAreaAD6	:= AD6->(GetArea())
Local cFilAD6		:= xFilial("AD6")

If ( oMdl:GetOperation() == MODEL_OPERATION_INSERT .Or. oMdl:GetOperation() == MODEL_OPERATION_UPDATE )
	
	If IsInCallStack("FATA320")
		If ( ADL->ADL_ENTIDA == "SA1" .And. ( oMdl:GetOperation() == MODEL_OPERATION_INSERT .Or. oMdl:GetOperation() == MODEL_OPERATION_UPDATE ) )
			cCodCli := ADL->ADL_CODENT
			cLojCli := ADL->ADL_LOJENT
			If  oMdl:GetOperation() == MODEL_OPERATION_INSERT
				cHelpCli := STR0025 //"O cliente está bloqueado para uso."
			Else
				cHelpCli := STR0026 //"O cliente deste apontamento está bloqueado para uso."
			EndIf
		EndIf
	ElseIf oMdl:GetOperation() == MODEL_OPERATION_UPDATE
		cCodCli 	:= AD5->AD5_CODCLI
		cLojCli 	:= AD5->AD5_LOJA
		cHelpCli	:= STR0026 //"O cliente deste apontamento está bloqueado para uso."
	EndIf
	
	If !Empty( cCodCli ) .And. !Empty( cLojCli )
		SA1->( DBSetOrder(1) )
		If ( SA1->(DBSeek(xFilial("SA1")+ cCodCli + cLojCli )) .And. SA1->A1_MSBLQL == "1" )
			Help("",,1,"FT310CLIBLQ",cHelpCli,2,,,,,,, {STR0028} ) //"Verifique a configuração do campo Status (A1_MSBLQL)."
			lRet := .F.
		EndIf
	EndIf
	
EndIf

If lRet
	If oMdl:GetOperation() == MODEL_OPERATION_UPDATE .OR. oMdl:GetOperation() == MODEL_OPERATION_DELETE
		DbSelectArea("AD6")
		If AD6->(DbSeek(cFilAD6+AD5->AD5_VEND+Dtos(AD5->AD5_DATA)+AD5->AD5_SEQUEN))
			While AD6->(! Eof()) .AND. AD6->AD6_FILIAL == cFilAD6 .AND. AD6->AD6_VEND == AD5->AD5_VEND .AND.;
			      AD6->AD6_DATA == AD5->AD5_DATA .AND. AD6->AD6_SEQUEN == AD5->AD5_SEQUEN
				If !Empty(AD6->AD6_NUMERO)
					lRet := .F.
					Help(" ",1,"FT310DESP")
					Exit
				EndIf
				DbSkip()
			EndDo
		EndIf
	Endif
EndIf

RestArea(aAreaAD6)
RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310Pos    ³ Autor ³Vendas & CRM           ³ Data ³ 09/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Pós-Valiação                                               	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310Pos(oMdl)

Local lRet		:= .T.
Local nOper	:= oMdl:GetOperation()

If nOper == MODEL_OPERATION_INSERT .OR.nOper == MODEL_OPERATION_UPDATE .OR. nOper == MODEL_OPERATION_DELETE
	lRet := Ft310TOk()
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310LOk    ³ Autor ³Vendas & CRM           ³ Data ³ 09/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Pós-Valiação                                               	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310LOk(oMdl)

Local lRet 		:= .T.							// Retorno da Pós-Validação
Local lFt310LOk	:= ExistBlock("FT310LOK") 	// Ponto de entrada para validacao da linha
Local nOper		:= oMdl:GetOperation()

If nOper == MODEL_OPERATION_UPDATE .OR. nOper == MODEL_OPERATION_INSERT .OR. nOper == MODEL_OPERATION_DELETE
	If !oMdl:IsDeleted()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se o Total bate com a quantidade e o valor unitario  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotal := NoRound(FwFldGet("AD6_QUANT")*FwFldGet("AD6_VLUNIT"),2)
		If ( nTotal <> NoRound(FwFldGet("AD6_TOTAL"),2) )
			Help(" ",1,"FT310LOK01")
			lRet := .F.
		EndIf
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Ponto de entrada na validacao da linha³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lRet )
			If lFt310LOk
				lRet := ExecBlock( "FT310LOK", .F.,.F.)
			Endif
		Endif
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310Init   ³ Autor ³Vendas & CRM           ³ Data ³ 10/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Carrega os Campos quando inicializar pela WorkArea          	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oModel e Campo                                              	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310Init(oMdl,cCampo)

Local aArea		:= GetArea()
Local aAreaSX3	:= SX3->(GetArea())
Local cRet		:= ""
Local cX3Relacao:= ""
Local cCodUser	:= RetCodUsr()
Local lOk		:= .T.

//Alteração necessario para não executar o X3_RELAÇÃO quando a chamada for do Portal
If !FunName() == "RPC"
	If ! Empty(( cX3Relacao := AllTrim(GetSX3Cache(cCampo,"X3_RELACAO")) ))
		cRet	:= &(cX3Relacao)
	EndIf
EndIf

If oMdl:GetOperation() == MODEL_OPERATION_INSERT
	aUserPaper	:= CRMXGetPaper() 
	
	If !Empty( aUserPaper )
		cCodVend := aUserPaper[USER_PAPER_CODVEND] 

		If !Empty( cCodVend )
			SA3->(DBSetOrder(1))
			lOk := SA3->( DBSeek( xFilial("SA3") + cCodVend ))
		EndIf
	ElseIf SA3->A3_CODUSR <> cCodUser
		SA3->( DBSetOrder(7) )
		lOk := SA3->( MSSeek(xFilial("SA3") + cCodUser ) )
		SA3->( DBSetOrder(1) )
	EndIf
	
	If IsInCallStack("FATA320")
	
		If ADL->ADL_ENTIDA == "SUS"
			If cCampo == "AD5_PROSPE"
				cRet := ADL->ADL_CODENT
			ElseIf cCampo == "AD5_LOJPRO"
				cRet := ADL->ADL_LOJENT
			EndIf
		ElseIf ADL->ADL_ENTIDA == "SA1"
			If cCampo == "AD5_CODCLI"
				cRet := ADL->ADL_CODENT
			ElseIf cCampo == "AD5_LOJA"
				cRet := ADL->ADL_LOJENT
			EndIf
		EndIf
		//Se chamada pela rotina de inclusao inicializa variaveis vendedor(codigo e nome)
		If cCampo == "AD5_VEND"
			cRet := SA3->A3_COD
		Elseif cCampo == "AD5_NOMVEN"
			cRet := SA3->A3_NOME
		Endif
	ElseIf IsInCallStack("CRMA290") .And. !IsInCallStack("CRMA290RFUN") .And. lOk
		If cCampo $ "AD5_PROSPE|AD5_LOJPRO|AD5_CODCLI|AD5_LOJA"
			
			aEntidad 	:= CRMA330Ent()
			
			If !Empty( aEntidad )
				If aEntidad[1] == "SA1"
					If cCampo == "AD5_CODCLI"
						cRet := aEntidad[2]
					ElseIf cCampo == "AD5_LOJA"
						cRet := aEntidad[3]
					EndIf
				ElseIf aEntidad[1] == "SUS"
					If cCampo == "AD5_PROSPE"
						cRet := aEntidad[2]
					ElseIf cCampo == "AD5_LOJPRO"
						cRet := aEntidad[3]
					EndIf
				EndIf 					
			EndIf 
		ElseIf  cCampo $ "AD5_VEND|AD5_NOMVEN" 
			//Se chamada pela rotina de inclusao inicializa variaveis vendedor(codigo e nome)
			If cCampo == "AD5_VEND"
				cRet := SA3->A3_COD
			ElseIf cCampo == "AD5_NOMVEN"
				cRet := SA3->A3_NOME
			EndIf
		
		EndIf			
	EndIf

	If ValType(aCposAg) == "A"
		nPos := aScan(aCposAg,{|x| AllTrim(x[1])==cCampo})
		If nPos > 0
			cRet := aCposAg[nPos,2]
		EndIf
	ElseIf lOk
		If cCampo == "AD5_VEND"
			cRet := SA3->A3_COD
		Elseif cCampo == "AD5_NOMVEN"
			cRet := SA3->A3_NOME
		ElseIf cCampo == "AD5_IDESTN" .AND. AD5->(FieldPos("AD5_IDESTN")) > 0 .AND. FindFunction("CRMXNVLEST")
			cRet:= CRMXNVLEST(SA3->A3_COD)[1]
		ElseIf cCampo == "AD5_NVESTN" .AND. AD5->(FieldPos("AD5_NVESTN")) > 0 .AND. FindFunction("CRMXNVLEST")
			cRet:= CRMXNVLEST(SA3->A3_COD)[2]
		EndIf
	EndIf
EndIf

RestArea(aAreaSX3)
RestArea(aArea)

Return cRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310When   ³ Autor ³Vendas & CRM           ³ Data ³ 10/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se pode editar o campo considerando a WorkArea     	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oModel e Campo                                              	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310When(oMdl,cCampo)

Local aArea		:= GetArea()
Local aAreaSX3	:= SX3->(GetArea())
Local cX3When		:= ""
Local nPos			:= 0
Local lRet			:= .T.

cX3When	:= AllTrim(GetSX3Cache(cCampo,"X3_WHEN"))
If ValType(aCposAg) == "A"
	If	( nPos := aScan(aCposAg,{|x| AllTrim(x[1]) == cCampo}) ) > 0
		lRet := .F.
	EndIf
EndIf
If lRet .AND. ! Empty(cX3When)
	lRet := &(cX3When)
EndIf

RestArea(aAreaSX3)
RestArea(aArea)

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310TOk  ³ Autor ³Vendas & CRM           ³ Data ³16.01.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³Funcao de validacao da Enchoice                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310TOk()

Local aArea		:= GetArea()
Local aAreaAD1	:= AD1->(GetArea())
Local lCamposCli	:= (AD1->(FieldPos("AD1_CODCLI")) > 0 .AND. AD1->(FieldPos("AD1_LOJCLI")) > 0)
Local lRetorno	:= .T.
Local lFT310TOK	:= ExistBlock("FT310TOK")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se ambos estao em branco                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Empty(FWFldGet("AD5_NROPOR")) .And. (Empty(FWFldGet("AD5_CODCLI")) .Or. Empty(FWFldGet("AD5_LOJA"))) )
	Help(" ",1,"FT310TUDOK")
	lRetorno := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se ambos estao preenchidos                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .AND. lCamposCli .AND.;
   (!Empty(FWFldGet("AD5_NROPOR")) .AND. ( !Empty(FWFldGet("AD5_CODCLI")) .Or. !Empty(FWFldGet("AD5_LOJA")) ))
	DbSelectArea("AD1")
	DbSetOrder(1)
	DbSeek(xFilial("AD1")+FWFldGet("AD5_NROPOR"))
	If !(FWFldGet("AD5_CODCLI") == AD1->AD1_CODCLI .AND. FWFldGet("AD5_LOJA") == AD1->AD1_LOJCLI)
		Help(" ",1,"FT310CLOP") // Nao e permitido especificar o cliente e oportunidade ao mesmo tempo
		lRetorno := .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//Verifica se o cliente e Propect estao preenchidos ao mesmo tempo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .AND. lCamposCli .AND.;
   (Empty(FWFldGet("AD5_NROPOR")) .AND. ( !Empty(FWFldGet("AD5_CODCLI")) .And. !Empty(FWFldGet("AD5_PROSPE")) ))
	Help(" ",1,"FT310TUDOK")
	lRetorno := .F.
EndIf

If lRetorno .AND. lFT310TOK
	lRetorno := ExecBlock("FT310TOK")
EndIf

RestArea(aAreaAD1)
RestArea(aArea)

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MyFata310 ³ Autor ³ Eduardo Riera         ³ Data ³19.09.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de teste da rotina automatica do programa FATA310     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar testes na rotina de    ³±±
±±³          ³apontamento                                                  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
/*
Function MyFata310()

Local aCabec := {}
Local aItens := {}
Local aLinha := {}
Local nX     := 0
Local nY     := 0
Local lOk    := .T.
PRIVATE lMsErroAuto := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Abertura do ambiente                                         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de 10 apontamentos de venda com 1 item cada",80))
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "FAT"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verificacao do ambiente para teste                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA3")
dbSetOrder(1)
If !SA3->(DbSeek(xFilial("SA3")+"V00001"))
	lOk := .F.
	ConOut("Cadastrar vendedor: V00001")
EndIf
If !SB1->(DbSeek(xFilial("SB1")+"PA001"))
	lOk := .F.
	ConOut("Cadastrar produto: PA001")
EndIf
dbSelectArea("SA1")
dbSetOrder(1)
If !SA1->(DbSeek(xFilial("SA1")+"CL000101"))
	lOk := .F.
	ConOut("Cadastrar cliente: CL000101")
EndIf
If lOk
	ConOut("Inicio: "+Time())
	For nY := 1 To 10
		aCabec := {}
		aItens := {}
		aadd(aCabec,{"AD5_VEND"   ,"V00001",Nil})
		aadd(aCabec,{"AD5_DATA"   ,dDataBase,Nil})
		aadd(aCabec,{"AD5_SEQUEN" ,StrZero(nY,2),Nil})
		aadd(aCabec,{"AD5_CODCLI",SA1->A1_COD,Nil})
		aadd(aCabec,{"AD5_LOJA",SA1->A1_LOJA,Nil})
		For nX := 1 To 30
			aLinha := {}
			aadd(aLinha,{"AD6_ITEM",StrZero(nX,2),Nil})
			aadd(aLinha,{"AD6_CODPRO",SB1->B1_COD,Nil})
			aadd(aLinha,{"AD6_QUANT",1,Nil})
			aadd(aLinha,{"AD6_VLUNIT",100,Nil})
			aadd(aLinha,{"AD6_TOTAL",100,Nil})
			aadd(aItens,aLinha)
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Teste de Inclusao                                            |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		FATA310(aCabec,aItens,3)
		If !lMsErroAuto
			ConOut("Incluido com sucesso! ")	
		Else
			ConOut("Erro na inclusao!")
		EndIf
	Next nY
	ConOut("Fim  : "+Time())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de alteracao                                           |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCabec := {}
	aItens := {}
	aadd(aCabec,{"AD5_VEND"   ,"V00001",Nil})
	aadd(aCabec,{"AD5_DATA"   ,dDataBase,Nil})
	aadd(aCabec,{"AD5_SEQUEN" ,StrZero(nY,2),Nil})
	aadd(aCabec,{"AD5_CODCLI",SA1->A1_COD,Nil})
	aadd(aCabec,{"AD5_LOJA",SA1->A1_LOJA,Nil})

	For nX := 1 To 30
		aLinha := {}
		aadd(aLinha,{"AD6_ITEM",StrZero(nX,2),Nil})
		aadd(aLinha,{"AD6_CODPRO",SB1->B1_COD,Nil})
		aadd(aLinha,{"AD6_QUANT",2,Nil})
		aadd(aLinha,{"AD6_VLUNIT",100,Nil})
		aadd(aLinha,{"AD6_TOTAL",200,Nil})
		aadd(aItens,aLinha)
	Next nX	
	ConOut(PadC("Teste de alteracao",80))
	ConOut("Inicio: "+Time())
	FATA310(aCabec,aItens,4)
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de Exclusao                                            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ConOut(PadC("Teste de exclusao",80))
	ConOut("Inicio: "+Time())
	FATA310(aCabec,aItens,5)
	If !lMsErroAuto
		ConOut("Exclusao com sucesso! ")	
	Else
		ConOut("Erro na exclusao!")
	EndIf
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
EndIf
RESET ENVIRONMENT
Return(.T.)
*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft310Reg  ³ Autor ³ Kleber Dias Gomes     ³ Data ³12.11.2003³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Verifica se existe regra do processo de venda.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nOpc: 	Opcao (1-Inclusao;2-Alteracao;3-Exclusao)         ³±±
±±³          ³ cNrOpor: Numero da Oportunidade de Venda                   ³±±
±±³          ³ cEvento: Codigo do Evento do Apontamento                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft310Reg(nOpc,cNrOpor,cEvento,oModel)

Local aArea    := GetArea()
Local aAreaAD1 := AD1->(GetArea())
Local aAreaAC2 := AC2->(GetArea())
Local aAreaACZ := ACZ->(GetArea())
Local aCab		 := {}
Local lRet		 := .T.

Default nOpc 		:= 0
Default cNrOpor	:= ""
Default cEvento	:= ""
Default oModel	:= Nil

PRIVATE lMsErroAuto    := .F.

dbSelectArea("AD1")
dbSetOrder(1)
DbSeek(xFilial("AD1")+cNrOpor)

dbSelectArea("ACZ")
dbSetOrder(2)
If dbSeek(xFilial("ACZ")+AD1->AD1_PROVEN+AllTrim(Str(nOpc))+cEvento)
	dbSelectArea("AC2")
	dbSetOrder(1)
	If ( ACZ->ACZ_ACAO == "2" )
		If !DBSeek(xFilial("AC2")+ACZ->ACZ_PROVEN+ACZ->ACZ_STAGE)
			If oModel <> Nil
				//"O estágio utilizado na regra do processo de venda desta Oportunidade de Venda não existe.", "Altere a regra do processo de venda utilizando um novo estágio." 
				oModel:GetModel():SetErrorMessage(,, oModel:GetId(),, "FT310REG",STR0021,STR0022)
			EndIf
			lRet := .F.
		EndIf
	Else
		If DBSeek(xFilial("AC2")+AD1->AD1_PROVEN+AD1->AD1_STAGE)
			If ( ACZ->ACZ_ACAO == "1" )
				AC2->(dbSkip())
			ElseIf ( ACZ->ACZ_ACAO == "3" )
				AC2->(dbSkip(-1))
			EndIf
		Else
			If oModel <> Nil
				//"O estágio utilizado na regra do processo de venda desta Oportunidade de Venda não existe.", "Altere a regra do processo de venda utilizando um novo estágio." 
				oModel:GetModel():SetErrorMessage(,, oModel:GetId(),, "FT310REG",STR0021,STR0022)
			EndIf
			lRet := .F.
		EndIf
	EndIf
	If ( !AC2->(Eof()) .And. !AC2->(Bof()) ) .And.;
			( AD1->AD1_PROVEN == ACZ->ACZ_PROVEN )
		
		aAdd(aCab,{"AD1_NROPOR", cNrOpor,})
		aAdd(aCab,{"AD1_STAGE",  AC2->AC2_STAGE})
		MSExecAuto({|x,y| FATA300(x,y)}, 4, aCab)
		
		If lMsErroAuto
			MostraErro()
			oModel:GetModel():SetErrorMessage(,, oModel:GetId(),, "FT310REG",STR0023,STR0024)
			lRet := .F.
		EndIf
	EndIf

EndIf

RestArea(aAreaAD1)
RestArea(aAreaAC2)
RestArea(aAreaACZ)
RestArea(aArea)

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³FT310OportºAutor  ³Vendas Clientes     º Data ³  23/04/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a oportunidade informada no campo AD5_NROPOR         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Apontamentos                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ft310Oport(cOportu)

Local lRet		:= .F.
Local lAchou	:= .F.
Local lVendOk	:= .T.
Local aArea		:= GetArea()
Local aAreaAD1	:= AD1->(GetArea())
Local aAreaSUS	:= SUS->(GetArea())

Default cOportu	:= &(ReadVar())

M->AD5_CODCLI	:= Space(TamSX3("AD5_CODCLI")[1])
M->AD5_LOJA	:= Space(TamSX3("AD5_LOJA")[1])
M->AD5_PROSPE	:= Space(TamSX3("AD5_PROSPE")[1])
M->AD5_LOJPRO	:= Space(TamSX3("AD5_LOJPRO")[1])

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a oportunidade esta preenchida e se esta existe³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cOportu)
	DbSelectArea("AD1")
	DbSetOrder(1)
	lRet := (lAchou	:= DbSeek(xFilial("AD1") + cOportu))
	If !lAchou .Or. (FT300VEND(.T.) <> AD1->AD1_VEND)
		lVendOk := .F.
	EndIf
Else
	lRet := .T.
EndIf

If lVendOk
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se encontrou a oportunidade e os campos AD5_PROSPE e³
	//³AD5_LOJPRO existem, preenche-os                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ 
	If lAchou .AND. (AD5->(FieldPos("AD5_PROSPE")) > 0) .AND. (AD5->(FieldPos("AD5_LOJPRO")) > 0)
		DbSelectArea("SUS")
		DbSetOrder(1)
		If DbSeek(xFilial("SUS")+AD1->AD1_PROSPE+AD1->AD1_LOJPRO) .AND. !Empty(SUS->US_CODCLI)
			M->AD5_CODCLI	:= SUS->US_CODCLI
			M->AD5_LOJA		:= SUS->US_LOJACLI
		EndIf
		M->AD5_PROSPE	:= AD1->AD1_PROSPE
		M->AD5_LOJPRO	:= AD1->AD1_LOJPRO
	EndIf
Else
	Help( " ", 1, "REGNOIS" )
	lRet := .F.
EndIf

RestArea(aAreaSUS)
RestArea(aAreaAD1)
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ft310ProspºAutor  ³Vendas Clientes     º Data ³  23/04/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida o prospect digitado no apontamento                   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Apontamentos                                           	  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ft310Prosp(cProspect,cLjProsp)

Local aArea		:= GetArea()
Local aAreaSUS	:= SUS->(GetArea())
Local aAreaAD1	:= AD1->(GetArea())
Local lRet			:= .F.
Local oModel
Local oModelAD5M	:= Nil

Default cProspect	:= ""
Default cLjProsp	:= M->AD5_LOJPRO

DbSelectArea("AD1")
AD1->(DbSetOrder(1))

lRet := Empty(cProspect)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Localiza o prospect ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lRet
	DbSelectArea("SUS")
	DbSetOrder(1)
	lRet := DbSeek( xFilial("SUS") + cProspect + cLjProsp )
	If lRet .AND. AD5->(FieldPos("AD5_IDESTN")) > 0 .AND. FindFunction("CRMXLibReg")
		lRet := CRMXLibReg("SUS",cProspect+cLjProsp,1)
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se encontrou o prospect, preenche os dados de prospect e ³
//³cliente, se houver registro de cliente para este prospect³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Trim(cProspect)=="" .OR. Trim(cLjProsp) == ""
	lRet := .T.
Else
	oModel := FwModelActive()
	If lRet
		oModelAD5M := oModel:GetModel("AD5MASTER")
		oModel:LoadValue("AD5MASTER", "AD5_CODCLI", SUS->US_CODCLI)
		oModel:LoadValue("AD5MASTER", "AD5_LOJA",   SUS->US_LOJACLI)
		   
	   // Verifica se a existe oportunidade adicionada,  se sim verifica se ela pertence ao
	   //Prospect indicado.
		If !Empty(oModelAD5M:GetValue("AD5_NROPOR")) .AND. AD1->(DbSeek(xFilial("AD1")+oModelAD5M:GetValue("AD5_NROPOR")))
			If oModelAD5M:GetValue("AD5_PROSPE") <> AD1->AD1_PROSPE
				oModel:LoadValue("AD5MASTER", "AD5_NROPOR", Space(TamSX3("AD5_NROPOR")[1]))
			EndIf
		EndIf
	Else
		oModel:LoadValue("AD5MASTER", "AD5_CODCLI", Space(TamSX3("AD5_CODCLI")[1]))
		oModel:LoadValue("AD5MASTER", "AD5_LOJA",   Space(TamSX3("AD5_LOJA")[1]))
	EndIf
EndIf

RestArea(aAreaAD1)
RestArea(aAreaSUS)
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ft310VlOp ºAutor  ³Vendas CRM          º Data ³  07/07/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Valida a oportunidade selecionada para o apontamento        º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³FATA310                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ft310VlOp()

Local lRet 		:= .T.
Local aArea		:= GetArea()
Local aAreaAD1	:= AD1->(GetArea())
Local cNumOp		:= &(ReadVar())

DbSelectArea("AD1")
DbSetOrder(1)
DbSeek(xFilial("AD1")+cNumOp)

If !Empty(M->AD5_PROSPE) .AND. ((AD1->AD1_PROSPE <> M->AD5_PROSPE ) .OR. (AD1->AD1_LOJPRO <> M->AD5_LOJPRO ))
	lRet := .F.
	MsgStop(STR0008) //"A oportunidade selecionada não está vinculada ao prospect deste apontamento"
ElseIf !Empty(M->AD5_CODCLI) .AND. ((AD1->AD1_CODCLI <> M->AD5_CODCLI ) .OR. (AD1->AD1_LOJCLI <> M->AD5_LOJA ))
	lRet := .F.
	MsgStop(STR0009) //"A oportunidade selecionada não está vinculada ao cliente deste apontamento"
EndIf

If lRet .AND. FunName() <> "FATA320"
	If Empty(M->AD5_PROSPE) .AND. !Empty(AD1->AD1_PROSPE)
		M->AD5_PROSPE := AD1->AD1_PROSPE
		M->AD5_LOJPRO := AD1->AD1_LOJPRO
	ElseIf Empty(M->AD5_CODCLI) .AND. !Empty(AD1->AD1_CODCLI)
		M->AD5_CODCLI := AD1->AD1_CODCLI
		M->AD5_LOJA	  := AD1->AD1_LOJCLI
	EndIf
EndIf
           
RestArea(aAreaAD1)
RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Ft310SetAgºAutor  ³Vendas CRM          º Data ³  13/01/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Utilizada para setar os valores que deverão ser preenchidos º±±
±±º          ³quando acessado pela workarea                               º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³FATA310                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ft310SetAg(aTmp) 

aCposAg := aTmp

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft310VdCli
Validacao dos campos AD5_CODCLI / AD5_LOJA.
@sample 	Ft310Cli(cCodCli,cLoja)
@param		ExpC1	Codigo do cliente
			ExpC2	Loja do cliente
@return   	ExpC	Expressao
@author	Anderson Silva
@since		08/11/2013
@version	11.90
/*/
//------------------------------------------------------------------------------ 
Function Ft310VdCli(cCodCli,cLoja)

Local lRetorno  := .T.
Default cCodCli := ""
Default cLoja   := ""

If FindFunction("CRMXLibReg")
	If !Empty( cCodCli ) .AND. Empty( cLoja )
		SA1->(DbSetOrder(1))
		If SA1->(!DbSeek(xFilial("SA1")+cCodCli))
			Help(NIL, NIL, "Ft310VdCli", NIL, STR0029 , 1, 0, NIL, NIL, NIL, NIL, NIL, { STR0030 }) // "Cliente inválido." ## "Verificar o código informado."
			lRetorno := .F.
		EndIf
	Else
		lRetorno := ( Vazio() .OR. ExistCpo("SA1",AllTrim(cCodCli+cLoja)) .AND. CRMXLibReg("SA1",AllTrim(cCodCli+cLoja),1) )
	EndIf	
EndIf

Return(lRetorno)

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} AO4GdModel

Cria um GridModel associado ao modelo informado no parãmetro, para evitar
a validação do SX9 da entidade principal do modelo informado com a AO4

@param, cIDModel, ID do modelo principal                              , String
@param, oModel  , Objeto do modelo a que o novo modelo serah associado, MPFormModel

@sample		AO4GdModel(cIDModel, oModel)

@return, Nil

@author		Squad CRM/Faturamento
@since		30/06/2021
@version	12.1.27
/*/
//----------------------------------------------------------------------------------
Static Function AO4GdModel(cIDMasterM, oModel, cAliasMast )
Local oStructAO4 := FWFormStruct(1,"AO4",/*bAvalCampo*/,/*lViewUsado*/)
Default cIDMasterM := ""
Default cAliasMast := ""

oModel:AddGrid("AO4CHILD",cIDMasterM,oStructAO4,/*bPreValid*/,/*bPosValid*/, , ,{|oGridModel, lCopy|LoadGdAO4(oGridModel, lCopy)})
oModel:SetRelation( "AO4CHILD" ,{ { "AO4_FILIAL", "FWxFilial( 'AO4' )" }, { "AO4_ENTIDA", cAliasMast }, { "AO4_CHVREG", ( cAliasMast )->( IndexKey( 1 ) ) }  }, AO4->( IndexKey( 1 ) ) )
oModel:GetModel("AO4CHILD"):SetOnlyView()
oModel:GetModel("AO4CHILD"):SetOnlyQuery()
oModel:GetModel("AO4CHILD"):SetOptional(.T.)
oModel:GetModel("AO4CHILD"):SetNoInsertLine(.T.)
oModel:GetModel("AO4CHILD"):SetNoUpdateLine(.T.)
oModel:GetModel("AO4CHILD"):SetNoDeleteLine(.T.)

Return Nil

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} LoadGdAO4 

Bloco de carga dos dados do submodelo.
Este bloco sera invocado durante a execução do metodo activate desta classe.
O bloco recebe por parametro o objeto de model do FormGrid(FWFormGridModel) e um 
valor lógico indicando se eh uma operação de copia.

@param, oGridModel, objeto de model do FormGrid, FWFormGridModel
@param, lCopy     , indica se eh uma operação de copia, Boolean

@sample	LoadGdAO4(oGridModel, lCopy)

@return, aLoad, array com os dados que serão carregados no objeto, 
                o array deve ter a estrutura abaixo:
					[n]
					[n][1] ExpN: Id do registro (RecNo)
					[n][2] Array com os dados, os dados devem seguir exatamente 
					       a mesma ordem da estrutura de dados submodelo

@author		Squad CRM/Faturamento
@since		30/06/2021
@version	12.1.27
/*/
//----------------------------------------------------------------------------------
Static Function LoadGdAO4(oGridModel, lCopy)
	
	Local aLoad      := {}
	Local oStructAO4 := FWFormStruct(1,"AO4",/*bAvalCampo*/,/*lViewUsado*/)
	Local aFields    := {}
	Local nField     := 0
	Local nQtFields  := 0
	Local xValue     := Nil
	Local cField     := ""
	Local cType      := ""
	Local nLen       := 0

	aFields   := oStructAO4:GetFields()
	nQtFields := Len(aFields)

	AAdd(aLoad, {0,{}})

	For nField := 1 To nQtFields
		
		cField := aFields[nField][3]
		
		If Alltrim(cField) == "AO4_FILIAL"
			xValue := XFilial("AO4")
			cType  := ""
		Else
			cType  := aFields[nField][4]
			nLen   := aFields[nField][5]	
		EndIf

		Do Case
			Case cType == "C"
				xValue := Space(nLen)
			Case cType == "N"
				xValue := 0
			Case cType == "L"
				xValue := .T.
			Case cType == "D"
				xValue := CToD("  /  /    ")
		End Case

		AAdd(aLoad[1][2], xValue)
	Next nField

	FwFreeObj(oStructAO4)
	FwFreeObj(aFields)

Return aLoad
