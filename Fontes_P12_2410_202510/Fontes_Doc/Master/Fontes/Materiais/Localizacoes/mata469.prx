#INCLUDE "MATA469.CH"
#INCLUDE "fivewin.ch"

/*/


Ŀ
Funo     MATA469   Autor  Lucas                  Data  10.03.00  
Ĵ
Descrio  Programa de Estornar/Desaprovar os Pedidos j Liberados.    
           Utilizado para Localizacoes ConeSul...					   
Ĵ
Sintaxe    Void MATA469(void)                                          
Ĵ
Uso        Generico                                                    
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                    
Ĵ
 Lucas        10/03/00      Inicio do desenvolvimento...             
 Lucas        27/08/00MelhorConsiderar todos os Pedidos, exceto os j
                            Facturados...                            
 Leonardo     14/05/01      Utilizacao da funcao a460estorno para con
                            templar todos os arquivos envolvidos.    
 Sergio F.    09/08/02059214Ajuste na funcao A469Desap().            
 Sergio F.    09/08/02059215Ajuste na rotina de geracao do arquivo   
                            temporario(TRB) - A469ProcC9().          
Luis Enrquez 10/01/17SERINN001-1055Se realiza merge para hacer modi-
                            ficacion de creacion de tablas temporales
                            CTREE.                                   
 Dora Vega    24/02/17MMI-153Realizacion del merge para la replica   
                             del llamado TWHSRT.Se agrega un Punto de
                             Entrada de Desaprobacion de Pedidos de  
                             Venta en la funcion A469Desap().(ARG)   
 Jos Glez    30/09/20DMINA- Se Valida a los campos del Browse tengan
                       9821  el uso del browse en la SX3 para poder  
                             mostrarlos (MEX)                        
ٱ



/*/
Function MATA469()

Local nX		:= 0
Local aCampos	:= {}
Private oTmpTable := Nil
Private aOrdem := {}

Private aRotina := { { OemToAnsi(STR0001),"A469Pesqui",0,1},;		//"Pesquisar"
							{ OemToAnsi(STR0002),"A469Visual",0,2},;		//"Visual"
							{ OemToAnsi(STR0003),"A469Estorn",0,3},;		//"Estornar"
	                        { OemToAnsi(STR0034),"A469Legend" ,0,3,0} }    //"Legenda"

//Ŀ
// Define o cabecalho da tela de atualizacoes           
//

Private cCadastro	:= OemToAnsi(STR0025) //"Desaprovacin de los Pedidos Liberados"
Private lFiltra		:= .F.
Private lAutomato := isBlind()  // Paso de Automatizacin

If Pergunte("MTA469",.T.)
	aCampos:={}
	AADD(aCampos,{"OK"	    ,"C",02,0})
	AADD(aCampos,{"PEDIDO"  ,"C",TamSX3("C9_PEDIDO")[1],0})
	AADD(aCampos,{"ITEMPED" ,"C",TamSX3("C9_ITEM")[1],0})
	AADD(aCampos,{"CLIENTE" ,"C",TamSX3("C9_CLIENTE")[1],0})
	AADD(aCampos,{"LOJA"    ,"C",TamSX3("C9_LOJA")[1],0})
	AADD(aCampos,{"PRODUTO" ,"C",TamSX3("C9_PRODUTO")[1],0})
	AADD(aCampos,{"QTDLIB"  ,"N",TamSX3("C9_QTDLIB")[1],2})
	AADD(aCampos,{"PRCVEN"  ,"N",TamSX3("C9_PRCVEN")[1],2})
	AADD(aCampos,{"TOTAL"   ,"N",TamSX3("C6_VALOR")[1],2})
	AADD(aCampos,{"TES"     ,"C",TamSX3("C6_TES")[1],0})
    AADD(aCampos,{"CF"      ,"C",TamSX3("C6_CF")[1],0})
	AADD(aCampos,{"DATALIB" ,"D",08,0})
	AADD(aCampos,{"SEQUEN"  ,"C",TamSX3("C9_SEQUEN")[1],0})
	AADD(aCampos,{"BLEST"   ,"C",TamSX3("C9_BLEST")[1],0})
	AADD(aCampos,{"BLCRED"  ,"C",TamSX3("C9_BLCRED")[1],0})
	AADD(aCampos,{"REMITO"  ,"C",TamSX3("C9_REMITO")[1],0})
	AADD(aCampos,{"NFISCAL" ,"C",TamSX3("C9_NFISCAL")[1],0})

	lFiltra := 	If( mv_par08==1,.T.,.F. )  // Filtra Pedidos j Entregues

	If Select("TRB") <> 0
		dbSelectArea("TRB")
		dbCloseArea()
	EndIf
    
    oTmpTable := FWTemporaryTable():New("TRB") 
	oTmpTable:SetFields( aCampos ) 

	aOrdem	:=	{"PEDIDO","ITEMPED","SEQUEN"}

	oTmpTable:AddIndex("IN1", aOrdem)

	oTmpTable:Create()

    Processa({|lEnd| lRet:= A469PrcC9()},,OemToAnsi(STR0005)) //"Preparando Pedidos..."

	//Ŀ
	// Endereca a funcao de BROWSE                          
	//
	aCampos := {}
  	Iif (GetSx3Cache("C9_PEDIDO"	,"X3_BROWSE") == "S",	AADD(aCampos,{"PEDIDO"    ,"",FWX3Titulo("C9_PEDIDO")	,X3Picture("C9_PEDIDO")})		,)	//"Pedido"
   	Iif (GetSx3Cache("C9_ITEM"		,"X3_BROWSE") == "S",	AADD(aCampos,{"ITEMPED"   ,"",FWX3Titulo("C9_ITEM")		,X3Picture("C9_ITEM")})			,)//"Item"
   	Iif (GetSx3Cache("C9_CLIENTE"	,"X3_BROWSE") == "S",	AADD(aCampos,{"CLIENTE"   ,"",FWX3Titulo("C9_CLIENTE")	,X3Picture("C9_CLIENTE")})		,)//"Cliente"
   	Iif (GetSx3Cache("C9_LOJA"		,"X3_BROWSE") == "S",	AADD(aCampos,{"LOJA"      ,"",FWX3Titulo("C9_LOJA")		,X3Picture("C9_LOJA")})			,)//"Loja"
    Iif (GetSx3Cache("C9_PRODUTO"	,"X3_BROWSE") == "S",	AADD(aCampos,{"PRODUTO"   ,"",FWX3Titulo("C9_PRODUTO")	,X3Picture("C9_PRODUTO")})		,)//"Producto"
   	Iif (GetSx3Cache("C9_QTDLIB"	,"X3_BROWSE") == "S",	AADD(aCampos,{"QTDLIB"    ,"",FWX3Titulo("C9_QTDLIB")	,X3Picture("C9_QTDLIB")})		,)//"Aprovadas"
   	Iif (GetSx3Cache("C9_PRCVEN"	,"X3_BROWSE") == "S",	AADD(aCampos,{"PRCVEN"    ,"",FWX3Titulo("C9_PRCVEN")	,X3Picture("C9_PRCVEN")})		,)//"Precio"
   	Iif (GetSx3Cache("C6_VALOR"		,"X3_BROWSE") == "S",	AADD(aCampos,{"TOTAL"     ,"",OemToAnsi(STR0014)		,X3Picture("C6_VALOR")})		,)//"Total"
   	Iif (GetSx3Cache("C6_TES"		,"X3_BROWSE") == "S",	AADD(aCampos,{"TES"       ,"",FWX3Titulo("C6_TES")		,X3Picture("C6_TES")})			,)//"TES"
   	Iif (GetSx3Cache("C6_CF"	 	,"X3_BROWSE") == "S",	AADD(aCampos,{"CF"        ,"",FWX3Titulo("C6_CF")		,X3Picture("C6_CF")})			,)//"CFO"
   	Iif (GetSx3Cache("C9_DATALIB"	,"X3_BROWSE") == "S",	AADD(aCampos,{"DATALIB"   ,"",OemToAnsi(STR0017)})										,)//"Fecha Lib."
   	Iif (GetSx3Cache("C9_SEQUEN"	,"X3_BROWSE") == "S",	AADD(aCampos,{"SEQUEN"    ,"",FWX3Titulo("C9_SEQUEN")	,X3Picture("C9_SEQUEN")})		,)//"Sequencia"
   	Iif (GetSx3Cache("C9_BLEST"		,"X3_BROWSE") == "S",	AADD(aCampos,{"BLEST"     ,"",FWX3Titulo("C9_BLEST")	,X3Picture("C9_BLEST")})		,)//"Bloq. Stock"
   	Iif (GetSx3Cache("C9_BLCRED"	,"X3_BROWSE") == "S",	AADD(aCampos,{"BLCRED"    ,"",FWX3Titulo("C9_BLCRED")	,X3Picture("C9_BLCRED")})		,)//"Bloq. Credito"
	Iif (GetSx3Cache("C9_REMITO"	,"X3_BROWSE") == "S",	AADD(aCampos,{"REMITO"    ,"",FWX3Titulo("C9_REMITO")	,X3Picture("C9_REMITO")})		,)//"Remito"
	Iif (GetSx3Cache("C9_NFISCAL"	,"X3_BROWSE") == "S",	AADD(aCampos,{"NFISCAL"   ,"",FWX3Titulo("C9_NFISCAL")	,X3Picture("C9_NFISCAL")})		,)//"Nota Fiscal"

	If !lAutomato
		MarkBrow("TRB","OK"   ,"TRB->REMITO+TRB->NFISCAL",aCampos,MV_PAR07==1,GetMark())
	Else
		dbSelectArea("TRB")
		A469Desap()
    EndIf
    
	dbSelectArea("TRB")
	dbCloseArea()
	
	If oTmpTable <> Nil  
		oTmpTable:Delete()  
		oTmpTable := Nil 
	EndIf 

EndIf
dbSelectArea("SC9")
dbSetOrder(1)  //C9_FILIAL+C9_PEDIDO+C9_ITEM+C9_SEQUEN+C9_PRODUTO
dbClearFilter()
Return

/*/


Ŀ
Funo    A469Pesqui Autor  Lucas                  Data  10.03.00  
Ĵ
Descrio  Programa de Estornar/Desaprovar os Pedidos j Liberados.    
           Utilizado para Localizacoes ConeSul...					   
Ĵ
Sintaxe    Void A469Pesqui()                                           
Ĵ
Uso        MATA469                                                     
Ĵ


/*/
Function A469Pesqui(cAlias,nReg,nOpc)
Local cCampo,oDlg,nOpca:=0
Local cFil:="",nChave:=1,dCampo,cOrd,oCbx,nOpt1,nI
Local aStruct
Local aOrd := {}

AADD(aOrd," "+STR0021)  // "Pedido de Venta+Item"
cOrd	:= STR0021 // "Pedido de Venta+Item"
oCBX	:= {STR0021} //"Pedido de Venta+Item"
cCampo	:= Space(49)

DEFINE MSDIALOG oDlg FROM 5, 5 TO 140, 350 PIXEL TITLE OemToAnsi(STR0022)         //"Bsqueda"
@ 5,5 COMBOBOX oCBX VAR cOrd ITEMS aOrd   SIZE 165,12 PIXEL ON CHANGE (nOpt1:=oCbx:nAt)  OF oDlg FONT oDlg:oFont
@ 22,5 MSGET     cCampo SIZE 165,10 PIXEL
DEFINE SBUTTON FROM 40,100 TYPE 1 ACTION (nOpca := 1,oDlg:End()) ENABLE OF oDlg
DEFINE SBUTTON FROM 40,135 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg

ACTIVATE MSDIALOG oDlg CENTERED
If nOpca == 0
	Return( Nil )
EndIf

nReg := RecNo()
dbSelectArea("TRB")
dbSetOrder(1)
dbSeek( AllTrim(cCampo),.T. )
If ! Found()
	dbGoto( nReg )
	Help(" ",1,"PESQ01")
EndIf
lRefresh := .t.
Return( Nil )

/*/


Ŀ
Funo    A469Visual Autor  Lucas                  Data  10.03.00  
Ĵ
Descrio  Programa de Estornar/Desaprovar os Pedidos j Liberados.    
           Utilizado para Localizacoes ConeSul...                      
Ĵ
Sintaxe    Void A469Visual()                                           
Ĵ
Uso        MATA469                                                     
Ĵ


/*/
Function A469Visual(cAlias,nReg,nOpc)
Local nOpcA		:= 0
Local bCampo	:= { |nCPO| Field(nCPO) }
Local aCpos2	:= {}
Local lContinua	:= .T.
Local lGrade	:= GetMV("MV_GRADE")
Local nUsado	:= 0
Local nTotPed	:= 0
Local nTotDes	:= 0
Local nCntFor	:= 0
Local nGetLin  	:= 0
Local aBackRot	:= aClone(aRotina)
Local oDlg
Local oGetd
Local oSAY1
Local oSAY2
Local oSAY3
Local oSAY4
Local cSeek		:= ""
Local cWhile	:= ""
Local aNoFields	:= {"C9_PEDIDO","C9_CLIENTE","C9_LOJA"}
Local aObjects 	:= {}                  
Local aSize    	:= {} 
Local aInfo    	:= {}
Local nNumDec  	:= TamSX3("C6_VALOR")[2]

Pergunte("MTA410",.F.)

If ( lGrade )
	aRotina[nOpc][4] := 6
EndIf

//Ŀ
// Inicializa a Variaveis Privates.                     
//
Private aTELA[0][0],aGETS[0]
Private cSavScrVT,;
cSavScrVP,;
cSavScrHT,;
cSavScrHP,;
CurLen,;
nPosAtu:=0,;
nPosAnt:=9999,;
nColAnt:=9999
Private aCols		:= {}
Private aHeader		:= {}
Private aColsGrade	:= {}
Private aHeadGrade	:= {}

//Ŀ
// Inicializa a Variaveis da Enchoice.                  
//
dbSelectArea("SC5")
dbSetOrder(1) //C5_FILIAL+C5_NUM
dbSeek(xFilial("SC5")+AllTrim(TRB->PEDIDO))

For nCntFor := 1 TO FCount()
	M->&(EVAL(bCampo,nCntFor)) := FieldGet(nCntFor)
Next nCntFor

//Ŀ
// Montagem do aHeader e aCols                           
//
cSeek	:= xFilial("SC9")+SC5->C5_NUM
cWhile	:= "SC9->C9_FILIAL+SC9->C9_PEDIDO"

FillGetDados(nOPc,"SC9",1,cSeek,{|| &cWhile },{||A469VldReg(@nTotPed,@nTotDes),.T.},aNoFields,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/)

//Ŀ
// Caso nao ache nenhum item , abandona rotina.         
//
If ( Len(aCols) == 0 )
	Help(" ",1,"A410SEMREG")
	lContinua := .F.
EndIf
If ( lContinua )
		//Ŀ
		// Faz o calculo automatico de dimensoes de objetos     
		//
		aSize := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 020, .t., .f. } )
	
		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
	
		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
			{{003,033,160,200,240,263}} )

		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL

		EnChoice( "SC5", nReg, nOpc, , , , ,aPosObj[1], {}, 3 )

		nGetLin := aPosObj[3,1]
		@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR Space(40)						SIZE 120,09 PICTURE "@!" OF oDlg PIXEL
		@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0014+" :")						SIZE 020,09 OF oDlg PIXEL	//"Total :"
		@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg	PIXEL
		@ nGetLin,aPosGet[1,5]  SAY OemToAnsi(STR0040)						SIZE 040,09 OF oDlg PIXEL	//"Desc./Acres.:"
		@ nGetLin,aPosGet[1,6]  SAY oSAY3 VAR 0 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec))		SIZE 060,09 OF oDlg	PIXEL RIGHT
		@ nGetLin+10,aPosGet[1,5] SAY OemToAnsi("=")							SIZE 020,09 OF oDlg PIXEL
		@ nGetLin+10,aPosGet[1,6] SAY oSAY4 VAR 0								SIZE 060,09 PICTURE TM(0,22,Iif(cPaisloc=="CHI" .And. M->C5_MOEDA == 1,NIL,nNumDec)) OF oDlg PIXEL RIGHT
		oDlg:Cargo	:= {|c1,n2,n3,n4| oSay1:SetText(c1),;
			oSay2:SetText(n2),;
			oSay3:SetText(n3),;
			oSay4:SetText(n4) }

		oGet := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,,,"")

		Ma410Rodap(oGet,nTotPed,nTotDes)

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nopca:=1,If(oGet:TudoOk(),.T.,nOpca :=0),oDlg:End()},{||oDlg:End()})

EndIf
aRotina := aClone(aBackRot)
dbSelectArea("TRB")
Return( nOpcA )

/*

Ŀ
Funcin    A469Estorn  Autor  Lucas/Diego Rivero   Data  10/03/00 
Ĵ
Descrip.   Desaprovacao de Pedidos Liberados.                         
Ĵ
Uso        SIGAFAT                                                    
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
 Lucas        10/03/00      Tratamiento del campo C6_OP cuando hay  
                            integracin con mdulo PCP, considerar  
                            tambin los Clientes Exportacin...     
ٱ


*/
Function A469Estorna(cAlias,nReg,nOpc)
Local lOk := MsgYesNo( OemToAnsi(STR0032), OemToAnsi(STR0024))  //"Desea Desaprobar los pedidos selecionados?"  ## "Confirmar"

If !lOk
   dbSelectArea("TRB")
Else
   Processa({|lEnd| A469Desap()})
EndIf
Return

/*


Ŀ
Funo     A469Desap Autor                         Data           
Ĵ
Descrio  Processamento                                              
Ĵ
 Uso       MATA469                                                    
ٱ


*/
Function A469Desap()

Local nCnt		:= 0
Local LM469ITEM	:= ExistBlock("M469ITEM")
Local lContinua	:= .T.
Local cMarca	:= ThisMark()
Local lInverte	:= ThisInv()
Local cSeq      := ""

If lAutomato
	cMarca		:= GetMark()
	lInverte	:= MV_PAR07==1
EndIf

dbSelectArea("TRB")
nTotalTRB := RecCount()
ProcRegua(nTotalTRB,21,04)

dbGoTop()

While !Eof()
   IncProc()
   If IsMark("OK", cMarca, lInverte) .And. Empty(TRB->REMITO+TRB->NFISCAL)
		dbSelectArea("SC9")
		dbSeek(xFilial("SC9")+TRB->PEDIDO+TRB->ITEMPED+TRB->SEQUEN)
		//Ponto de entrada para validar o processo
		IF lM469ITEM
			lContinua := ExecBlock("M469ITEM",.f.,.f.)
			If !lContinua
			   dbSelectArea("TRB")
			   MsgAlert(STR0037) //"Nenhum pedido foi desaprovado!"
				Return
			EndIf
		EndIf
		If Found()
	   	   If !Empty( C9_REMITO )
              MsgAlert( OemToAnsi(STR0026) + SC9->C9_PEDIDO +OemToAnsi(STR0027) ,OemToAnsi(STR0028)) //"El Pedido " ## " Tiene Items Remitidos!" ## "Error al Desaprobar"
      		  dbSelectArea("TRB")
      		  Return
   		   EndIf
			If !Empty( C9_NFISCAL )  .or. ( C9_BLEST == "10" .and. C9_BLCRED == "10")
				MsgAlert( OemToAnsi(STR0026) + SC9->C9_PEDIDO +OemToAnsi(STR0038) ,OemToAnsi(STR0028)) //"El Pedido " ## "Tiene Items Faturados!" ## "Error al Desaprobar"
				dbSelectArea("TRB")
				RecLock("TRB",.F.)
				dbDelete()
				MsUnLock()
				Return
			EndIf
			If !MsRlock()
				MsgAlert( OemToAnsi(STR0026) + SC9->C9_PEDIDO + OemToAnsi(STR0039) ,OemToAnsi(STR0028)) //"El Pedido " ## "Encontra se em uso por outra pessoa!" ## "Error al Desaprobar"
	        	dbSelectArea("TRB")
      		  	Return
      		EndIf  	
		EndIf
		//Ŀ
		// Verifica o Servico do WMS.                                   
		//
		If !LocIntDCF('SC9',.F.)
     		Return
		EndIf				
   EndIf
   dbSelectArea("TRB")
   dbSkip()
End

aRecnoSC5 := {}

dbSelectArea("TRB")
dbGoTop()
While !Eof()

   IncProc()                     	
   If IsMark("OK", cMarca, lInverte)  .And. Empty(TRB->REMITO+TRB->NFISCAL)
		dbSelectArea("SA1")
   		dbSetOrder(1) //A1_FILIAL+A1_COD+A1_LOJA
   		dbSeek( xFilial("SA1")+TRB->CLIENTE+TRB->LOJA )

		dbSelectArea("SC9")
		dbSeek( xFilial("SC9")+TRB->PEDIDO+TRB->ITEMPED+TRB->SEQUEN )
                             
		//Ŀ
		// Estorna o Servico do WMS (DCF)                           
		//
		LocIntDCF("SC9",.T.)
		//Ŀ
		//Efetua o estorno das movimentacoes internas                        
		//
		MaTrfLocal(SC6->C6_PRODUTO, , , ,SC6->C6_NUM,.T.,@cSeq)
		
		//Ŀ	
		//Punto de Entrada Desaprobacion de Pedidos de Venta                 
		//
		If ExistBlock("M469DESPV")
			ExecBlock("M469DESPV",.F.,.F.,{"TRB"})
		EndIf
		
		dbSelectArea("SC9")
		
		If a460Estorna(.f.)
	        MsUnlockAll()
	  		If lFiltra	  
	   			dbSelectArea("TRB")
				RecLock("TRB",.F.)
				dbDelete()
				MsUnLock()
			Else
				dbSelectArea("TRB")
				RecLock("TRB",.F.)
				Replace OK     With ""   // para o item nao ser reprocessado - Sergio Fuzinaka - 09.08.02
				Replace REMITO With "."  // eh feito isto para nao aparecer mais disponivel no browse (arq. trabalho)
				MsUnLock()
			EndIf
			nCnt++
		EndIf
			
	EndIf
	dbSelectArea("TRB")
	dbSkip()
EndDo

If nCnt > 0          
   MsgAlert( OemToAnsi(STR0029),OemToAnsi(STR0030)) //"Los Pedidos han sido Desaprobados." ## "Pedido Desaprobado"
Else 
   MsgAlert(STR0037) //"Nenhum pedido foi desaprovado!"
EndIf   
dbSelectArea("TRB")

Return

/*/


Ŀ
Funo     A469ProcC9  Autor  Jose Lucas           Data  08.05.99 
Ĵ
Descrio  Endereca rotinas para geracao dos arquivos...              
Ĵ
Uso        Mata469                                                    
ٱ


/*/
Function A469PrcC9(cAlias,cCampo,nOpce)

Local lCondSC9	:= .T.    
Local cMarca	:= ThisMark()
Local lInverte	:= (mv_par07==1)

//Ŀ
// Preparar arquivo TRB com dados a serem visualizados e selecionados pela   
// MarkBrowse, evitando  os  filtros na Indregua que tem se tornado lenta.   
// Geracao a partir do arquivo SC9 - Pedidos Liberados.                      
//LUCAS09/05/99
//Ŀ
// Controlar o Total de Registros para utilizar na ProcRegua...         
//LUCAS09/05/99
//Ŀ
// Varrer o SC9 respeitando filtrando com base nas pergunta...          
// temporaria.                                                          
//LUCAS09/05/99
dbSelectArea("SC9")
dbSetOrder(1) //C9_FILIAL+C9_PEDIDO+C9_ITEM+C9_SEQUEN+C9_PRODUTO
dbSeek(xFilial("SC9") + mv_par01,.T.)

nCount := RecCount()

nPedFim := 0
nPedIni := 0
If Upper(mv_par02) != "ZZZZZZ"
	nPedFim := Val(mv_par02)
EndIf
If ! Empty(mv_par01)
	nPedIni := Val(mv_par01)
EndIf

If (nPedFim-nPedIni) > 0
   ProcRegua(nPedFim-nPedIni,21,04)
Else
   ProcRegua(nCount,21,04)
EndIf

While !Eof() .and. C9_FILIAL == xFilial("SC9");
             .and. C9_PEDIDO <= mv_par02

	If  lFiltra .and. ( !Empty(C9_NFISCAL) .or. !Empty(C9_REMITO) .or.( C9_BLEST == "10" .and. C9_BLCRED == "10"))
      	dbSkip()
      	Loop
   	EndIf

   	If C9_PEDIDO < mv_par01 .or. C9_PEDIDO > mv_par02
      	dbSkip()
      	Loop
   	EndIf

   	If C9_CLIENTE < mv_par03 .or. C9_CLIENTE > mv_par04
      	dbSkip()
      	Loop
   	EndIf

   	If C9_LOJA < mv_par05 .or. C9_LOJA > mv_par06
      	dbSkip()
      	Loop
   	EndIf

   	//Ŀ
   	//  Posiciona o item do pedido no arquivo  -- SC6       
   	//
   	dbSelectArea("SC6")
   	dbSetOrder(1) //C6_FILIAL+C6_NUM+C6_ITEM+C6_PRODUTO
   	dbSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO)
   	dbSelectArea("SC9")
    
    lCondSC9  := (!Empty(C9_NFISCAL) .Or. !Empty(C9_REMITO) .Or. ( C9_BLEST == "10" .And. C9_BLCRED == "10"))
	dbSelectArea("TRB")
   	RecLock("TRB",.T.)
   	Replace OK		  With If(lInverte .And. !lCondSC9, cMarca, "  ")
  	Replace PEDIDO    With SC9->C9_PEDIDO
	Replace ITEMPED   With SC9->C9_ITEM
 	Replace CLIENTE   With SC9->C9_CLIENTE
   	Replace LOJA      With SC9->C9_LOJA
   	Replace PRODUTO   With SC9->C9_PRODUTO
   	Replace QTDLIB    With SC9->C9_QTDLIB
   	Replace PRCVEN    With SC9->C9_PRCVEN
  	Replace TES       With SC6->C6_TES
   	Replace CF        With SC6->C6_CF
   	Replace DATALIB   With SC9->C9_DATALIB
   	Replace SEQUEN    With SC9->C9_SEQUEN
	Replace BLEST     With SC9->C9_BLEST
	Replace BLCRED    With SC9->C9_BLCRED
	Replace REMITO	  With SC9->C9_REMITO
	Replace NFISCAL	  With SC9->C9_NFISCAL
	Replace TOTAL	  With (SC9->C9_QTDLIB*SC9->C9_PRCVEN)
	MsUnLock()
        
	IncProc()

   	dbSelectArea("SC9")
  	dbSkip()
Enddo

//Ŀ
// Verifica a existencia de registros em TRB.                       
//
dbSelectArea("TRB")
dbGotop()
If BOF() .and. EOF()
   	cMsg := OemToAnsi(STR0031) //"No hay Pedidos Liberados disponible para Desaprocacin..."
   	MsgStop( cMsg )
   	Return .F.
EndIf
Return( .T. )

/*/

Ŀ
Funo    A469LegendAutor   Fernando Machima       Data 27.05.2002 
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   Nenhum                                                       
Ĵ
Descrio Esta rotina monta uma dialog com a descricao das cores da    
          Mbrowse.                                                     
Ĵ
Uso        MATA469                                                     
ٱ


/*/
Function A469Legend()

Local cDescRem := GetDescRem()   //Descricao do Remito, cuja nomenclatura depende de cada pais

BrwLegenda(cCadastro,STR0034,{	{"ENABLE",STR0035},; 	//"Legenda"###"Pedido aprovado"                             
	       {"DISABLE",STR0036+cDescRem}})              //Pedido desaprovado/com
	
Return(.T.)  

/*/


Ŀ
Funo     A469VldReg  Autor  Vendas CRM           Data  28.05.14 
Ĵ
Descrio Funcao executada a partir da FillGetdados para validar cada 
          registro da tabela. Se retornar .T. FILLGETDADOS considera  
          o registro, se .F. despreza o registro.                     
Ĵ
Uso        Mata469                                                    
ٱ


/*/
Static Function A469VldReg(nTotPed,nTotDes)

Local lRet := .T.

dbSelectArea("SC6")
dbSetOrder(1) //C6_FILIAL+C6_NUM+C6_ITEM+C6_PRODUTO
dbSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO)
   	
//Ŀ
//Efetua a Somatoria do Rodape                                            
//
nTotPed	+= SC6->C6_VALOR
If ( SC6->C6_PRUNIT = 0 )
	nTotDes	+= SC6->C6_VALDESC
Else
	nTotDes += A410Arred((SC6->C6_PRUNIT*SC6->C6_QTDVEN),"C6_VALOR")-A410Arred((SC6->C6_PRCVEN*SC6->C6_QTDVEN),"C6_VALOR")
EndIf

Return(lRet)

/*/


Ŀ
Funo     NomeCampo  Autor  Vendas CRM            Data  18.07.14 
Ĵ
Descrio Funcao para retorno de nome de campo							 
Ĵ
Uso        Mata469                                                    
ٱ


/*/

Static Function NomeCampo( cCampo)
Local cTitulo := ""

dbSelectArea('SX3')
dbSetOrder(2)
If dbSeek( cCampo )
	cTitulo := Alltrim(X3Titulo())
EndIf

Return cTitulo
