#INCLUDE 'msobject.ch'
#INCLUDE 'topconn.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.com.purchaseinvoiceaccounting.ch'

namespace custom.compras.purchaseinvoiceaccounting.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} PurchaseInvoiceAccountingTReportsBusinessObject
Classe para creación del Objeto de Negocio de compras para SMARTVIEW
@author Leonardo Pereira
@since 31/10/2024
@version 1.0 
*/
//-------------------------------------------------------------------------------

@totvsFrameworkTReportsIntegratedProvider( active=.F., team='SIGACOM', tables='SF1,CT2,CV3,SE4,SA2', name='Facturas de Compras Conciliada', country='MEX', initialRelease='12.1.2410', customTables='SF1,CT2' )
class purchaseinvoiceaccountingTReportsBusinessObjectMEX from custom.compras.purchaseinvoiceaccounting.integratedprovider.purchaseinvoiceaccountingTReportsBusinessObject

	public method new() as object
	public method preData() as object
	public method processData() as object
	public method preSchema() as object

endclass

method new() class purchaseinvoiceaccountingTReportsBusinessObjectMEX
	_Super:new()
return self

method preData() as object class purchaseinvoiceaccountingTReportsBusinessObjectMEX
return self:oData

method processData() as object class purchaseinvoiceaccountingTReportsBusinessObjectMEX

	Local cQuery as character

	Local oExecMEX as object

	Local nX as numeric

	Local cAliasMEX as character

	aFieldsMEX := { 'F1_UUID','F1_FECTIMB','F1_USOCFDI','E4_MPAGSAT','F1_TPDOC','F1_FECANTF','F1_APROFOL','F1_VALADI' }
	aStructMEX := getStrutObj( aFieldsMEX, { } )

	cQuery := "SELECT SF1.F1_UUID,SF1.F1_FECTIMB,SF1.F1_USOCFDI,ISNULL(SE4.E4_MPAGSAT,'') AS E4_MPAGSAT,SF1.F1_TPDOC,SF1.F1_FECANTF,SF1.F1_APROFOL,SF1.F1_VALADI"
	cQuery += "FROM " + RetSqlName( "SF1" ) + " SF1 "
	cQuery += "LEFT JOIN  " + RetSqlName( "SE4" ) + " SE4 "
	cQuery += "   ON SE4.E4_FILIAL = ? "
	cQuery += "      AND SE4.E4_CODIGO = SF1.F1_COND "
	cQuery += "      AND SE4.D_E_L_E_T_ = ? "
	cQuery += "WHERE SF1.R_E_C_N_O_ = ? "

	cQuery := ChangeQuery( cQuery )

	oExecMEX := FwExecStatement():New( cQuery )

	oExecMEX:SetString( 1, FWxFilial( 'SE4' ) )
	oExecMEX:SetString( 2, ' ' )
	oExecMEX:SetString( 3, AllTrim( Str( self:nRecnoSF1 ) ) )

	cQuery := oExecMEX:getFixQuery( )

	cAliasMEX := oExecMEX:OpenAlias( )
	
	For nX := 1 To Len ( aStructMEX )
		If ( aStructMEX[nX][3] == 'date' )
			self:jItems[aStructMEX[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasMEX )->&( aStructMEX[nX][5] ) ) )
		Else
			self:jItems[aStructMEX[nX][1]] := ( cAliasMEX )->&( aStructMEX[nX][5] )
		EndIf
	Next

	// fecha tabela
	( cAliasMEX )->( DbCloseArea( ) )

	oExecMEX:Destroy( )
	oExecMEX := Nil

Return

method preSchema() as object class purchaseinvoiceaccountingTReportsBusinessObjectMEX

	Local nX as numeric

	aFieldsMEX := { 'F1_UUID','F1_FECTIMB','F1_USOCFDI','E4_MPAGSAT','F1_TPDOC','F1_FECANTF','F1_APROFOL','F1_VALADI'}
	aStructMEX := getStrutObj( aFieldsMEX, {} )
	
	For nX := 1 To Len(aStructMEX)
		self:addProperty( aStructMEX[nX][1], aStructMEX[nX][2], aStructMEX[nX][3], aStructMEX[nX][4], aStructMEX[nX][5] )
	Next

Return self:oSchema
