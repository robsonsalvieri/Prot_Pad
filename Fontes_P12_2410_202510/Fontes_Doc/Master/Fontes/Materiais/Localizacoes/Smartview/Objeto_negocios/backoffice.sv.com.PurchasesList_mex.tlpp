#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'tlpp-core.th'
#INCLUDE 'tlpp-rest.th'
#INCLUDE 'backoffice.sv.com.PurchasesList_mex.ch'

namespace custom.compras.PurchasesList.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} PurchasesListTReportsBusinessObject
Classe para creación del Objeto de Negocio de compras para TReports 
@author Laura Medina
@since 15/04/2023 
@version 1.0 
*/
//-------------------------------------------------------------------------------

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACOM", tables="SF1","SD1","SF2","SD2","SA2", name="Lista detallada de compras por proveedor - México", country="MEX", initialRelease="12.1.2310" )
class PurchasesListTReportsBusinessObjectMEX from custom.compras.PurchasesList.integratedprovider.PurchasesListTReportsBusinessObject

	public method new() as object
	public method preData() as object
	public method processData() as object
	public method preSchema() as object

	protected data aFields as array
	protected data aStruct as array

endclass

method new() class PurchasesListTReportsBusinessObjectMEX
	_Super:new()
return self

method preData() as object class PurchasesListTReportsBusinessObjectMEX
return self:oData

method processData() as object class PurchasesListTReportsBusinessObjectMEX

	Local aImpDoc := {}
	Local nX      := 0

	self:addProperty("IVA", STR0001, "number", "IVA", "IVA") // "Impuesto Valor Agregado"

	aImpDoc := IMPCOMPRAS(self:cAliasSD, self:cFilSD, self:cSerie, self:cDoc, self:cProv, self:cLojaP, self:cItemD)
	self:jItems["IVA"] := 0
	For nX:=1 To Len(aImpDoc)
		If aImpDoc[nX][1] $ "IVA|IVI|IV8"
			self:jItems["IVA"] := aImpDoc[nX][6]
		EndIf
	Next

return

method preSchema() as object class PurchasesListTReportsBusinessObjectMEX

	self:addProperty("IVA", STR0001, "number", "IVA", "IVA") // "Impuesto Valor Agregado"

return self:oSchema

/*/{Protheus.doc} IMPCOMPRAS
    Obtiene los impuestos por ítem de los ítems de Documentos de Compras.
    @type  Function
    @author luis.enriquez
    @since 27/05/2023
    @version version
    @param cAliasSD, caracter, Alias de la tabla "SD1"/"SD2"
    @param cFilSD, caracter, Filial de la tabla "SD2", si alias es "SD2", contrario es la Filial de la tabla "SD1"
    @param cSerie, caracter, Serie del Documento
    @param cDoc, caracter, Folio del Documento
    @param cCliPro, caracter, Código del Proveedor del Documento
    @param cLoja, caracter, Código de la Tienda del Documento
    @param cItem, caracter, Número de ítem del Documento
    @return aImpDet, arreglo, Impuestos del Detalle, donde:
    [1] = FB_CODIGO (Código del Impuesto)
    [2] = FB_DESCR (Descripción del Impuesto)
    [3] = FB_CPOLVRO  (Número de Campo de Libro Fiscal del Impuesto)
    [4] = FB_CLASSE (Clase del Impuesto)
    [5] = Base del Impuesto
    [6] = Valor del Impuesto
    @example
    (examples)
    @see (links_or_references)
    /*/
Function IMPCOMPRAS( cAliasSD, cFilSD, cSerie, cDoc, cCliPro, cLoja, cItem )

	Local cCampos   := ""
	Local cTablas   := ""
	Local cAliasImp := getNextAlias()
	Local cAliasDet := getNextAlias()
	Local aImpDet   := {}
	Local nX        := 0
	Local cBasImpSD := ""
	Local cValImpSD := ""
	Local cTesD     := ""

	Default cAliasSD := ""
	Default cSerie   := ""
	Default cDoc     := ""
	Default cCliPro  := ""
	Default cLoja    := ""

	// Impuestos de documento
	cQuery := 'SELECT SFB.FB_CODIGO, SFB.FB_CPOLVRO, SFB.FB_CLASSE, SFB.FB_IMPSAT, SFB.FB_DESCR, SFB.FB_ALIQ, SFC.FC_IMPOSTO, SFC.FC_INCDUPL '
	cQuery += 'FROM ' + RetSqlName( cAliasSD ) + " " + cAliasSD + " "
	cQuery += "INNER JOIN " + RetSqlName( 'SFC' ) + " SFC "
	cQuery += "   ON SFC.FC_FILIAL = ? "
	
	If ( cAliasSD == 'SD1' )
		cQuery += "      AND SFC.FC_TES = SD1.D1_TES "
	Else
		cQuery += "      AND SFC.FC_TES = SD2.D2_TES "
	EndIf

	cQuery += "      AND SFC.D_E_L_E_T_ = ? "
	cQuery += "INNER JOIN " + RetSqlName( 'SFB' ) + ' SFB '
	cQuery += "	  ON SFB.FB_FILIAL = ? "
	cQuery += "      AND SFB.FB_CODIGO = SFC.FC_IMPOSTO "
	cQuery += "      AND SFB.D_E_L_E_T_ = ? "
	cQuery += " WHERE "
	
	If ( cAliasSD == 'SD1' )
		cQuery += "SD1.D1_DOC = ? "
		cQuery += "   AND SD1.D1_SERIE = ? "
		cQuery += "   AND SD1.D1_FORNECE = ? "
		cQuery += "   AND SD1.D1_LOJA = ? "
		cQuery += "   AND SD1.D1_ITEM = ? "
		cQuery += "   AND SD1.D_E_L_E_T_ = ? "
	Else
		cQuery += "SD2.D2_DOC = ? "
		cQuery += "   AND SD2.D2_SERIE = ? "
		cQuery += "   AND SD2.D2_CLIENTE = ? "
		cQuery += "   AND SD2.D2_LOJA = ? "
		cQuery += "   AND SD2.D2_ITEM = ? "
		cQuery += "   AND SD2.D_E_L_E_T_ = ? "
	EndIf

	cQuery += 'GROUP BY ? '
	cQuery += 'ORDER BY ? '

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cQuery := ChangeQuery( cQuery )

	oQuery := FwExecStatement():New()

	//Define a consulta e os parâmetros
	oQuery:SetQuery( cQuery )

	oQuery:SetString( 1, FwxFilial( 'SFC' ) )
	oQuery:SetString( 2, ' ' )

	oQuery:SetString( 3, FwxFilial( 'SFB' ) )
	oQuery:SetString( 4, ' ' )

	oQuery:SetString( 5, cDoc )
	oQuery:SetString( 6, cSerie )
	oQuery:SetString( 7, cCliPro )
	oQuery:SetString( 8, cLoja )
	oQuery:SetString( 9, cItem )
	oQuery:SetString( 10, ' ' )

	oQuery:SetUnsafe( 11, 'SFB.FB_CODIGO, SFB.FB_CPOLVRO, SFB.FB_CLASSE, SFB.FB_IMPSAT, SFB.FB_DESCR, SFB.FB_ALIQ, SFC.FC_IMPOSTO, SFC.FC_INCDUPL' )
	oQuery:SetUnsafe( 12, 'SFB.FB_CODIGO, SFB.FB_CPOLVRO, SFB.FB_CLASSE, SFB.FB_IMPSAT, SFB.FB_DESCR, SFB.FB_ALIQ, SFC.FC_IMPOSTO, SFC.FC_INCDUPL' )

	cQuery := oQuery:getFixQuery( )

	// cria alias
	oQuery:OpenAlias( cAliasImp )

	While !( cAliasImp )->( Eof( ) )
		aAdd( aImpDet, { ( cAliasImp )->FB_CODIGO, ( cAliasImp )->FB_DESCR, ( cAliasImp )->FB_CPOLVRO, ( cAliasImp )->FB_CLASSE, 0, 0 } )
		( cAliasImp )->( Dbskip( ) )
	End

	( cAliasImp )->( DbCloseArea( ) )

	If cAliasSD == "SD1"
		cBasImpSD := "D1_BASIMP"
		cValImpSD := "D1_VALIMP"
		cTesD     := "D1_TES"
	Else
		cBasImpSD := "D2_BASIMP"
		cValImpSD := "D2_VALIMP"
		cTesD     := "D2_TES"
	EndIf

	If ( Len( aImpDet ) > 0 )
		For nX := 1 To Len( aImpDet )
			//Items de documento
			If ( cAliasSD == 'SD1' )
				cCampos	:= 'SD1.D1_COD, SD1.D1_ITEM, SD1.D1_QUANT, SD1.D1_UM, SD1.D1_VUNIT, SD1.D1_TOTAL, SD1.D1_VALDESC, D1_PEDISAT, SD1.D1_TES '
				cCampos += CAMPOSIMP( 'SD1' )
				cTablas	:= RetSqlName( 'SD1' ) + ' SD1 '
				cCond := "SD1.D1_FILIAL = ? "
				cCond += "   AND SD1.D1_DOC = ? "
				cCond += "   AND SD1.D1_SERIE = ? "
				cCond += "   AND SD1.D1_FORNECE = ? "
				cCond += "   AND SD1.D1_LOJA = ? "
				cCond += "   AND SD1.D1_ITEM = ? "
				cCond += "   AND SD1.D_E_L_E_T_ = ? "
			Else
				cCampos	:= 'SD2.D2_COD, SD2.D2_ITEM, SD2.D2_QUANT, SD2.D2_UM, SD2.D2_PRCVEN, SD2.D2_TOTAL, SD2.D2_DESCON, D2_PEDISAT, SD2.D2_TES, D2_VALFRE, D2_SEGURO, D2_DESPESA '
				cCampos += CAMPOSIMP( 'SD2' )
				cTablas	:= RetSqlName( 'SD2' ) + ' SD2 '
				cCond	:= "SD2.D2_FILIAL = ? "
				cCond	+= "   AND SD2.D2_DOC = ? "
				cCond	+= "   AND SD2.D2_SERIE = ? "
				cCond	+= "   AND SD2.D2_CLIENTE = ? "
				cCond	+= "   AND SD2.D2_LOJA = ? "
				cCond	+= "   AND SD2.D2_ITEM = ? "
				cCond	+= "   AND SD2.D_E_L_E_T_ = ? "
			EndIf

			cQuery := 'SELECT ' + cCampos
			cQuery += 'FROM ' + cTablas
			cQuery += 'WHERE ' + cCond

			// Executa a QUERY e cria uma tabela temporaria com os dados retornados
			cQuery := ChangeQuery( cQuery )

			oQuery := FwExecStatement():New()

			//Define a consulta e os parâmetros
			oQuery:SetQuery( cQuery )

			oQuery:SetString( 1, cFilSD )
			oQuery:SetString( 2, cDoc )
			oQuery:SetString( 3, cSerie )
			oQuery:SetString( 4, cCliPro )
			oQuery:SetString( 5, cLoja )
			oQuery:SetString( 6, cItem )
			oQuery:SetString( 7, ' ' )

			cQuery := oQuery:getFixQuery( )

			// cria alias
			oQuery:OpenAlias( cAliasDet )

			While !( cAliasDet )->( EOF( ) )
				//Valida si el impuesto existe en la TES o no tiene impuestos
				aResp := LxIMPTES(aImpDet[nX][1],(cAliasDet)->&(cTesD))
				If aResp[1]
					aImpDet[nX][5] += (cAliasDet)->(&(cBasImpSD + aImpDet[nX][3])) //Base del impuesto
					aImpDet[nX][6] += (cAliasDet)->(&(cValImpSD + aImpDet[nX][3])) //Valor del impuesto
				EndIf
				(cAliasDet)->(dbskip())
			End
			(cAliasDet)->(DBCloseArea())
		Next
	EndIf

Return aImpDet

/*/{Protheus.doc} CAMPOSIMP
    Obtiene los campos _ALQIMP, _BASIMP y _VALIMP que existen en el Diccionario para una tabla en específico.
    @type  Function
    @author luis.enriquez
    @since 27/05/2023
    @version version
    @param cTabla, caracter, Tabla de la cual se obtendrán los campos _ALQIMP, _BASIMP y _VALIMP
    @return cCPolvr, caracter, Cadena con los campos _ALQIMP, _BASIMP y _VALIMP separados por coma 
    @example
    (examples)
    @see (links_or_references)
    /*/
Function CAMPOSIMP(cTabla)

	Local aArea 	:= getArea()
	Local cCPolvr	:= ""

	dbSelectArea("SX3")
	SX3->(dbSetOrder(1)) // X3_ARQUIVO + X3_ORDEM

	If SX3->(dbSeek(cTabla))
		While SX3->(!eof())  .and. SX3->X3_ARQUIVO == cTabla
			If "_VALIMP" $ SX3->X3_CAMPO .OR. "_ALQIMP" $ SX3->X3_CAMPO .OR. "_BASIMP" $ SX3->X3_CAMPO
				cCPolvr	+= "," + X3_CAMPO
			EndIF
			SX3->(dbSkip())
		End
	EndIF

	RestArea(aArea)

Return cCPolvr
