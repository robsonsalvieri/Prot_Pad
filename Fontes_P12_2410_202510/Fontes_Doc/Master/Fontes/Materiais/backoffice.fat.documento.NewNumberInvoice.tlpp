#INCLUDE 'Protheus.ch'
#INCLUDE "FWBROWSE.CH"
#INCLUDE "BACKOFFICE.FAT.DOCUMENTO.NUMBERINVOICE.CH"

//-----------------------------------------------------------------------------------------------------
//- Variável de controle da chave de lockbyname
//-----------------------------------------------------------------------------------------------------
Static cKeyInvoiceInProcess as character
Static cKeyInvoiceInUse     as character

Static cDBMS  	      := TcGetDb()

Static __oQrySD9
Static __oQryAZZ

#define _nPosSerie     1
#define _nPosNameTable 2
#define _nPosEmp       3
#Define _nPosFil       4
#define _nPosModelo    5
#define _nPosTamDoc    6

Namespace backoffice.fat.documento

//------------------------------------------------------------------------------
//-  NumberInvoice()
//- 	Função responsável por retornar o próximo número do documento fiscal 
//- 	@type 		Function 
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@Param		cModDocumento - indica o modelo de documento fiscal 
//-                 cSerie        - Série do documento a ser fornecido a numeração
//- 	@return		Proximo numero do documento
//------------------------------------------------------------------------------
Function NumberInvoice(cModDocumento as character, cSerie as character) as character
	Local __cAlias       as character
	Local cNumberInvoice as character
	Local cModelo        as character
	Local nTamDocumento  as numeric
	Local nTamSerie      as numeric

	//-----------------------------------------------------------------------------------------------------
	//- Salva o Alias em uso
	//-----------------------------------------------------------------------------------------------------
	__cAlias := Alias()

	//-----------------------------------------------------------------------------------------------------
	//- efetua a validação da data real com a database, pois não é permitido
	//- a geração de notas retorativas
	//-----------------------------------------------------------------------------------------------------
	If MsDate() <> dDataBase 
		Help(" ",1,STR0001,,STR0002 + ' '+ dToc(dDataBase) + ' ' + STR0003 + ' '+ dToc(MsDate()) +; //"Data Inválida" ## "A data de entrada do sistema" ## "não corresponde a data real"
			'.',1,0,,,,,, {STR0004}) //"Ajuste a entrada correta da data."
		//-----------------------------------------------------------------------------------------------------
		//- força saída com um erro
		//-----------------------------------------------------------------------------------------------------
		UserException(STR0002+' '+dToc(dDataBase)+' '+STR0003+'.') //"A data de entrada do sistema" ## "não corresponde a data real"
	EndIF
	//-----------------------------------------------------------------------------------------------------
	//- Carrega o tamanho dos padrões de documentos
	//-----------------------------------------------------------------------------------------------------
	nTamDocumento := backoffice.fiscal.escrita.documentos.RetTypeDoc(cModDocumento,1,.T.)
	nTamSerie     := backoffice.fiscal.escrita.documentos.RetTypeDoc(cModDocumento,2,.T.)

	//-----------------------------------------------------------------------------------------------------
	//- Acha o código do documento fiscal
	//-----------------------------------------------------------------------------------------------------
	cModelo := backoffice.fiscal.escrita.documentos.RetTypeDoc(cModDocumento,3)

	//-----------------------------------------------------------------------------------------------------
	//- Ajusta o tamanho da Serie
	//-----------------------------------------------------------------------------------------------------
	cSerie := Padr(cSerie,nTamSerie)

	//-----------------------------------------------------------------------------------------------------
	//- montagem da chave de busca da numeração da nota fiscal
	//-	SERIE + NOME DA TABELA + EMPRESA + FILIAL + MODELO DOCUMENTO + NUMERO MAXIMO DE DIGITOS DA NF+NF
	//-----------------------------------------------------------------------------------------------------
	cKeyInvoiceInUse := cSerie+'|'+RetSqlName("SD9")+'|'+cEmpAnt+'|'+cFilAnt+'|'+cModelo+'|'+StrZero(nTamDocumento,2)

	//-----------------------------------------------------------------------------------------------------
	//- busca a numeração da nota fiscal
	//-----------------------------------------------------------------------------------------------------
	cNumberInvoice := NextInvoice(cSerie, cModelo)

	//-----------------------------------------------------------------------------------------------------
	//- devolve o alias em uso
	//-----------------------------------------------------------------------------------------------------
	If !Empty(__cAlias)
		dbSelectArea(__cAlias)
	EndIf

Return cNumberInvoice

//------------------------------------------------------------------------------
//-  NextInvoice()
//- 	Função resposável por validar a primeira carga do início, 
//-     ela será executada toda primeira vez da série ou quando o ls_number.val 
//-     for apagado do lado do license  
//- 	@type 		Function
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@Param		cSerie        - Serie do documento a ser fornecido a numeração
//-                 cModDocumento - código do modelo de documento fiscal 
//- 	@return		Proximo numero do documento
//------------------------------------------------------------------------------
Static Function NextInvoice(cSerie as character, cModelo as character) as character
	Local cNumberInvoice as character
	Local cQuery         as character
	Local cCnpj          as character
	Local cAlias         as character
	Local nPosQry   := 1 as numeric
	Local lOk            as logical


	//-----------------------------------------------------------------------------------------------------
	//- pega o CNPJ
	//-----------------------------------------------------------------------------------------------------
	If Select("SM0") > 0
		cCnpj := SM0->M0_CGC
	Else
		cCnpj := FWSM0Util():GetSM0Data(, , {"M0_CGC"})[1][2]
	EndIf

	//-----------------------------------------------------------------------------------------------------
	//-
	//- Efetua a busca de numero abertos
	//-
	//-----------------------------------------------------------------------------------------------------
	If __oQrySD9 == nil
		cQuery := "SELECT D9_DOC, D9_KEYNF, R_E_C_N_O_ RECSD9 FROM "
		cQuery += RetSqlName("SD9")
		//-----------------------------------------------------------------------------------------------------
		//D9_FILIAL+D9_CNPJ+D9_SERIE+D9_MODELO+D9_STATUS+DTOS(D9_DTUSO)
		//-----------------------------------------------------------------------------------------------------
		cQuery += " WHERE D9_FILIAL = ?"
		cQuery += " AND D9_CNPJ     = ?"
		cQuery += " AND D9_SERIE    = ?"
		cQuery += " AND D9_MODELO   = ?"
		cQuery += " AND D9_STATUS   = ?"
		cQuery += " AND D9_DTUSO    = ?"
		cQuery += " AND D_E_L_E_T_  = ?"
		cQuery += " ORDER BY D9_DOC "
		cQuery := ChangeQuery(cQuery)
		__oQrySD9 := FwExecStatement():New(cQuery)
	EndIf
	__oQrySD9:SetString(nPosQry++,xFilial('SD9'))
	__oQrySD9:SetString(nPosQry++,cCnpj)
	__oQrySD9:SetString(nPosQry++,cSerie)
	__oQrySD9:SetString(nPosQry++,cModelo)
	__oQrySD9:SetString(nPosQry++,'1')
	__oQrySD9:SetString(nPosQry++,dTos(dDataBase))
	__oQrySD9:SetString(nPosQry++,Space(1))

	cAlias := __oQrySD9:OpenAlias()
	//-----------------------------------------------------------------------------------------------------
	//- SD9                    5           10                     2       8             2               2                          15 = 44 + 6("|") = 50
	//-                         1          2                      3          4          5               6
	//- D9_KEYNF  - C -  44 (cSerie+'|'+RetSqlName("SD9")+'|'+cEmpAnt+'|'+cFilAnt+'|'+cModelo+'|'+StrZero(nTamDocumento,2)+'|'+NOTA FISCAL)
	//-----------------------------------------------------------------------------------------------------

	While (cAlias)->(!Eof())

		If LockByName((cAlias)->D9_KEYNF,.F.,.F.)
			SD9->(dbGoTo((cAlias)->RECSD9))
			//-----------------------------------------------------------------------------------------------------
			//- garante que o status ainda esteja liberado
			//- lembrando que a consulta é algo estático e
			//- o seu resultado pode já ter sido atualizado por
			//- outra thread
			//-----------------------------------------------------------------------------------------------------
			If SD9->D9_STATUS == '1'
				//-----------------------------------------------------------------------------------------------------
				//- Alimenta a variave de cache para uso da numeração
				//-----------------------------------------------------------------------------------------------------
				cKeyInvoiceInProcess := (cAlias)->D9_KEYNF
				cNumberInvoice       := (cAlias)->D9_DOC
				lOk := .T.
				Exit
			EndIf
			//-----------------------------------------------------------------------------------------------------
			//- destrava a chave pois não foi utilizada
			//-----------------------------------------------------------------------------------------------------
			UnLockByName((cAlias)->D9_KEYNF,.F.,.F.)
		EndIf
		(cAlias)->(dbSkip())
	EndDo
	(cAlias)->(dbCloseArea())

	If !lOk
		cNumberInvoice := GetSxeNum('SD9','D9_DOC',cKeyInvoiceInUse)
		ConfirmSx8()

		//-----------------------------------------------------------------------------------------------------
		//- Montagem da chave de controle
		//-----------------------------------------------------------------------------------------------------
		cKeyInvoiceInProcess := cKeyInvoiceInUse+'|'+cNumberInvoice

		//-----------------------------------------------------------------------------------------------------
		//- trava a chave
		//-----------------------------------------------------------------------------------------------------
		LockByName(cKeyInvoiceInProcess,.F.,.F.)

		//-----------------------------------------------------------------------------------------------------
		//- criação do novo registro referente a numeração da nota fiscal
		//-----------------------------------------------------------------------------------------------------
		RecLock("SD9",.T.)
		SD9->D9_FILIAL  := xFilial("SD9")
		SD9->D9_DOC     := cNumberInvoice
		SD9->D9_SERIE   := cSerie
		SD9->D9_DTUSO   := dDataBase
		SD9->D9_HORA    := Time()
		SD9->D9_USUARIO := cUserName
		SD9->D9_CNPJ    := cCnpj
		SD9->D9_FILORI  := xFilial('SD9')
		SD9->D9_KEYNF   := cKeyInvoiceInProcess
		SD9->D9_MODELO  := cModelo
		SD9->D9_STATUS  := '1' //- Liberado
		SD9->(MsUnLock())

	EndIf

Return cNumberInvoice

//------------------------------------------------------------------------------
//-  UnLockInvoice()
//- 	Função responsável por liberar numeração da nota fiscal.  
//- 	@type 		Function
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@return		Chave da numeração liberada
//------------------------------------------------------------------------------
Function UnLockInvoice()
	Local cQuery as character
	cQuery := "UPDATE "+RetSqlName("SD9")
	cQuery += " SET D9_STATUS = '2'"//- AJUSTA O ESTATUS PARA CONCLUÍDO
	cQuery += " WHERE D9_KEYNF = '"+cKeyInvoiceInProcess+"'"
	//-----------------------------------------------------------------------------------------------------
	//-
	//- verifica a execução
	//-
	//-----------------------------------------------------------------------------------------------------
	If !TcSqlExec(cQuery) == 0
		UserException(TcSqlError())
	EndIf
Return UnLockByName(cKeyInvoiceInProcess,.F.,.F.)

//- 
//------------------------------------------------------------------------------
//-  KeyInvoiceInUse()
//- 	FunÇão de retorno da chave em uso.
//- 	@type 		Function
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@return		cKeyInvoiceInUse chave em uso
//------------------------------------------------------------------------------
Function KeyInvoiceInUse()
Return cKeyInvoiceInUse

//------------------------------------------------------------------------------
//-  StartInvoice()
//- 	Função de validação inicial para atualizar o license.
//- 	@type 		Function
//- 	@author		Nilton Rodrigues
//- 	@since 		27/11/2024
//- 	@Param		lCampo, nTamanho  - Valor de passagem 
//- 	@return		Numeração sequencial
//------------------------------------------------------------------------------
Function StartInvoice(lCampo, nTamanho)
	Local cNumberInvoice as character
	Local cQuery         as character
	Local cCnpj          as character
	Local cAlias         as character
	Local cStatus        as character
	Local aKeyInvoice    as array
	Local oQrySD9Start   as Object
	Local nPosQry   := 1 as numeric

	//-----------------------------------------------------------------------------------------------------
	//- Converte a string no array de conversão de chave
	//-     1          2                      3          4          5               6
	//- cSerie+'|'+RetSqlName("SD9")+'|'+cEmpAnt+'|'+cFilAnt+'|'+cModelo+'|'+StrZero(nTamDocumento,2)
	//-----------------------------------------------------------------------------------------------------
	aKeyInvoice := STRTOKARR(cKeyInvoiceInUse,'|')
	//-----------------------------------------------------------------------------------------------------
	//- controle de retorno ao valor de passagem
	//-----------------------------------------------------------------------------------------------------
	lCampo      := .F.
	nTamanho    := Val(aKeyInvoice[_nPosTamDoc])

	//-----------------------------------------------------------------------------------------------------
	//- pega o CNPJ
	//-----------------------------------------------------------------------------------------------------
	If Select("SM0") > 0
		cCnpj := SM0->M0_CGC
	Else
		cCnpj := FWSM0Util():GetSM0Data(, , {"M0_CGC"})[1][2]
	EndIf

	//-----------------------------------------------------------------------------------------------------
	//- efetua a consulta da ultima nota fiscal
	//- aqui não se faz necessidade de controle do objeto, pois a
	//- passagem é uma única vez e apenas quando não existir o registro do modelo no license
	//=====================================================================================================
	//- faz a consulta para ver se tem numero livre
	//-----------------------------------------------------------------------------------------------------
	cQuery := "SELECT "
	If cDBMS == 'MSSQL'
		cQuery := " TOP 1  "
	EndIf
	cQuery += " D9_DTUSO, D9_DOC, D9_MODELO, D9_STATUS FROM "
	cQuery += RetSqlName("SD9")
	cQuery += " WHERE D9_FILIAL = ?"
	cQuery += " AND (D9_CNPJ     = ?"
	cQuery += " OR D9_CNPJ      = ?)"
	cQuery += " AND D9_SERIE    = ?"
	cQuery += " AND (D9_MODELO  = ?"
	cQuery += " OR   D9_MODELO  = ?)"
	cQuery += " AND D_E_L_E_T_  = ?"
	If cDBMS == 'ORACLE'
		cQuery += "AND ROWNUM = 1 "
	EndIf
	//-----------------------------------------------------------------------------------------------------
	//- Aqui a ordenação do maior para o menor tem um função muito importante
	//- pois aqui é que é descoberto dentro da última data o último registro usado
	//- com isso é possível entender o processamento da proxima numeração a ser utilizada
	//-----------------------------------------------------------------------------------------------------
	cQuery += " ORDER BY D9_DTUSO DESC, D9_DOC DESC"

	If cDBMS == 'POSTGRES'
		cQuery += " LIMIT 1 "
	EndIf
	cQuery := ChangeQuery(cQuery)
	oQrySD9Start := FwExecStatement():New(cQuery)

	oQrySD9Start:SetString(nPosQry++,xFilial('SD9'))
	oQrySD9Start:SetString(nPosQry++,cCnpj)
	oQrySD9Start:SetString(nPosQry++,Space(TamSx3('D9_CNPJ')[1]))
	oQrySD9Start:SetString(nPosQry++,aKeyInvoice[_nPosSerie])
	oQrySD9Start:SetString(nPosQry++,aKeyInvoice[_nPosModelo])
	oQrySD9Start:SetString(nPosQry++,Space(TamSx3('D9_MODELO')[1]))
	oQrySD9Start:SetString(nPosQry++,Space(1))

	cAlias := oQrySD9Start:OpenAlias()

	//-----------------------------------------------------------------------------------------------------
	//- Inicialização padrão
	//-----------------------------------------------------------------------------------------------------
	cStatus        := '2'
	cNumberInvoice := Replicate('0',nTamanho)

	If (cAlias)->(!Eof())
		cStatus := '1'
		//-----------------------------------------------------------------------------------------------------
		//- Esse cenário o status vazio indica que o registro já foi usado
		//- pelo modelo antigo, logo o status será como utilizado
		//-----------------------------------------------------------------------------------------------------
		If Empty((cAlias)->D9_STATUS) .or. (cAlias)->D9_STATUS == '2' .and. !(cAlias)->D9_DTUSO == Dtos(dDataBase)
			cStatus := '2'
		EndIf

		//-----------------------------------------------------------------------------------------------------
		//- efetua o ajuste de zero a esquerda, prevendo o aumento do campo em função do modelo
		//-----------------------------------------------------------------------------------------------------
		cNumberInvoice := StrZero(Val((cAlias)->D9_DOC),nTamanho)

	Else
		(cAlias)->(dbCloseArea())
		oQrySD9Start:Destroy()
		FwFreeObj(oQrySD9Start)
		//-----------------------------------------------------------------------------------------------------
		//- Efetua a busca da nota fiscal de saida e entrada (com formulário proprio)
		//-----------------------------------------------------------------------------------------------------
		cQuery := "SELECT MAX(DOC) DOC FROM ("
		cQuery += " SELECT MAX(F2_DOC) DOC FROM "
		cQuery += RetSqlName("SF2")
		cQuery += " WHERE F2_FILIAL = ?"
		cQuery += " AND F2_EMISSAO  = ?"
		cQuery += " AND F2_SERIE    = ?"
		cQuery += " AND D_E_L_E_T_  = ?"
		cQuery += " UNION ALL"
		cQuery += " SELECT MAX(F1_DOC) DOC FROM "
		cQuery += RetSqlName("SF1")
		cQuery += " WHERE F1_FILIAL = ?"
		cQuery += " AND F1_DTDIGIT  = ?"
		cQuery += " AND F1_SERIE    = ?"
		cQuery += " AND F1_FORMUL   = ?"
		cQuery += " AND D_E_L_E_T_  = ?"
		cQuery += " ) TEMP"
		cQuery := ChangeQuery(cQuery)
		oQrySD9Start := FwExecStatement():New(cQuery)
		nPosQry := 1
		oQrySD9Start:SetString(nPosQry++,xFilial('SF2'))
		//-----------------------------------------------------------------------------------------------------
		//****** aqui devemos entender a situação que pode ou não ser reinicido no mes, pois
		// poderá pegar numeração maior em função do mês anterior, pensar num parâmetro para
		//- indicar que haverá reinicio entre meses
		oQrySD9Start:SetString(nPosQry++,Dtos(dDataBase-30))
		oQrySD9Start:SetString(nPosQry++,aKeyInvoice[_nPosSerie])
		oQrySD9Start:SetString(nPosQry++,Space(1))
		oQrySD9Start:SetString(nPosQry++,xFilial('SF1'))
		//-----------------------------------------------------------------------------------------------------
		//****** aqui devemos entender a situação que pode ou não ser reinicido no mês, pois
		// poderá pegar numeração maior em função do mês anterior, pensar num parametro para
		//- indicar que haverá reinicio entre meses
		oQrySD9Start:SetString(nPosQry++,Dtos(dDataBase-30))
		oQrySD9Start:SetString(nPosQry++,aKeyInvoice[_nPosSerie])
		oQrySD9Start:SetString(nPosQry++,'S')
		oQrySD9Start:SetString(nPosQry++,Space(1))

		cAlias := oQrySD9Start:OpenAlias()

		If (cAlias)->(!Eof())
			//-----------------------------------------------------------------------------------------------------
			//- efetua o ajuste de zero a esquerda, prevendo o aumento do campo
			//-----------------------------------------------------------------------------------------------------
			cNumberInvoice := StrZero(Val((cAlias)->DOC),nTamanho)
		EndIf

	EndIf
	(cAlias)->(dbCloseArea())

	//-----------------------------------------------------------------------------------------------------
	//- Indica que a nota fiscal já foi utilizada, então soma 1
	//-----------------------------------------------------------------------------------------------------
	if cStatus == '2'
		//-----------------------------------------------------------------------------------------------------
		//- Aqui indica o limite atingido e reinicia
		//-----------------------------------------------------------------------------------------------------
		If cNumberInvoice == Replicate('9',nTamanho)
			cNumberInvoice := Replicate('0',nTamanho)
		EndIf
		cNumberInvoice := Soma1(cNumberInvoice)
	EndIf
	oQrySD9Start:Destroy()
	FwFreeObj(oQrySD9Start)
	FwFreeArray(aKeyInvoice)
Return cNumberInvoice

//------------------------------------------------------------------------------
//-  TypeSerInvoice()
//- 	Função resposável pela tela de seleção de espécie e série,  
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//- 	@Param		cSerie        - Série do documento a ser fornecido a numeração
//-                 cTypeDoc	  - Espécie do documento fiscal 
//- 	@return		lContinua	  - Indica se seleção foi confirmada ou cancelada
//------------------------------------------------------------------------------
Function TypeSerInvoice(cSerie as character, cTypeDoc as character) as logical

    Local oBrowse   As Object
    Local cQryAZZ   As Character
	Local lContinua	As Logical
	Local cAliasAZZ As Character
	Local oDlgTela		:= Nil
	Local aIndex		:= {"AZZ_ESPECI","AZZ_SERIE"}
	Local oColumn

	lContinua := .F.

	If Empty(cTypeDoc) //Se não foi informada a espécie abre a caixa de seleção

		If Empty(cSerie)
			If __oQryAZZ == Nil

				cQryAZZ := "SELECT AZZ_ESPECI, AZZ_SERIE FROM "
				cQryAZZ += RetSqlName("AZZ")
				cQryAZZ += " WHERE AZZ_FILIAL = ?"
				cQryAZZ += " AND AZZ_DEFAUL  = ?"
				cQryAZZ += " AND D_E_L_E_T_  = ?"
				cQryAZZ := ChangeQuery(cQryAZZ)

				__oQryAZZ := FwExecStatement():New(cQryAZZ)
			EndIf

			__oQryAZZ:SetString(1,xFilial('AZZ'))
			__oQryAZZ:SetString(2,'1')
			__oQryAZZ:SetString(3,Space(1))

			cAliasAZZ := GetNextAlias()
			
			DEFINE MSDIALOG oDlgTela TITLE STR0005 FROM 0,0 TO 400,400 PIXEL //"Seleção de Série"
			oBrowse := FWFormBrowse():New()
			oBrowse:SetDescription(STR0006 +' x '+ STR0007) // "Espécie x Série""
			oBrowse:SetAlias(cAliasAZZ)
			oBrowse:SetDataQuery(.T.)
			oBrowse:SetQuery(__oQryAZZ:getFixQuery())
			oBrowse:SetOwner(oDlgTela)
			oBrowse:SetDoubleClick({ || cTypeDoc := (cAliasAZZ)->AZZ_ESPECI, cSerie := (cAliasAZZ)->AZZ_SERIE, oDlgTela:End()})
			oBrowse:AddButton( OemTOAnsi(STR0008)	, {|| cTypeDoc := (cAliasAZZ)->AZZ_ESPECI, cSerie := (cAliasAZZ)->AZZ_SERIE, oDlgTela:End() } ,, 2 ) //"Confirmar"
			oBrowse:AddButton( OemTOAnsi(STR0009), {|| cTypeDoc := "", cSerie := "", lRet := .F., oDlgTela:End() } ,, 2 ) //"Cancelar"
			oBrowse:DisableDetails()
			oBrowse:SetQueryIndex(aIndex)
			
			ADD COLUMN oColumn DATA { ||  AZZ_ESPECI } TITLE STR0006 SIZE 15 OF oBrowse //"Espécie"
			ADD COLUMN oColumn DATA { ||  AZZ_SERIE  } TITLE STR0007 SIZE 15 OF oBrowse //"Série"

			oBrowse:Activate()
			
			ACTIVATE MSDIALOG oDlgTela CENTERED
				
			If !Empty(cTypeDoc)
				lContinua:=.T.
			EndIf
		Else
			DbSelectArea("AZZ")
			AZZ->(DbSetOrder(3)) //AZZ_FILIAL+AZZ_DEFAUL+AZZ_SERIE+AZZ_PDV
			If AZZ->( DBSeek(xFilial("AZZ")+"1"+cSerie) )
				lContinua:=.T.
				cTypeDoc  := AZZ->AZZ_ESPECI
			EndIf
		EndIf
	Else
		If Empty(cSerie)
			If Len(cTypeDoc) < 6
				cTypeDoc := PadR( cTypeDoc, TamSX3("AZZ_ESPECI")[1], " " ) //Complementa a espécie para garantir o resultado do Seek
			EndIf
			DbSelectArea("AZZ")
			AZZ->(DbSetOrder(2)) //AZZ_FILIAL+AZZ_ESPECI+AZZ_DEFAUL
			If AZZ->( DBSeek(xFilial("AZZ")+cTypeDoc+"1") )
				lContinua:=.T.
				cSerie  := AZZ->AZZ_SERIE
			EndIf
		EndIf
	EndIf

Return lContinua
