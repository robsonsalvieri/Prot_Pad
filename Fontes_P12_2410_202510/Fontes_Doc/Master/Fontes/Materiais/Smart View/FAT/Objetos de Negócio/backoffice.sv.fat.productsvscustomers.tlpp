#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.productsvscustomers.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.productsvscustomers.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA7,SA1", name="Produtos x Clientes", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} productsvscustomersSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Amarração Produtos x Clientes

@author FAT/CRM
@since 07/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class productsvscustomersSmartViewBusinessObject From Integratedprovider
    
    public method new() 		as object
    public method getData() 	as object
    public method getSchema() 	as object
	public method FatSV035CreateSQL()

    protected data aFields 		as array
    protected data aStruct 		as array
    protected data ojItems 		as json
    protected data cLocSelect 	as character
    protected data cLocWhere 	as character
	protected data cAliasQry	as character
 
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 07/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class productsvscustomersSmartViewBusinessObject
	
	_Super:new()
    self:appendArea(STR0001) 	 //"Faturamento"
    self:setDisplayName(STR0002) //"Produtos x Clientes"
    self:setDescription(STR0003) //"Relação de Produtos x Clientes"
    self:setIsLookUp(.T.)		 //Habilita o recurso de consulta padrao e ComboBox

	self:aFields	:= {}
	self:cAliasQry	:= ""
	self:cLocSelect := ""
	self:cLocWhere	:= ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 07/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class productsvscustomersSmartViewBusinessObject

	Local aPDFields		:= {} as array
	Local nX			:= 0 as numeric
	Local nScan         := 0 as numeric
	Local lObfuscated	:= .F. as logical

	MV_CLIINI 	:= ""
	MV_CLIFIM 	:= ""
	MV_LJINI 	:= ""
	MV_LJFIM 	:= ""
	MV_PRDINI 	:= ""
	MV_PRDFIM 	:= ""
	MV_PRCLINI 	:= ""
	MV_PRCLIFIM := ""
	MV_FIL 		:= ""
	MV_ORDER 	:= 1

	//metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "")

	//Ordenação com base no parâmetro MV_ORDER
	MV_ORDER := IIf(MV_ORDER == 0, 1, MV_ORDER)

	//Verifica se precisa fazer o tratamento para LGPD
	aPDFields 	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := len( aPDFields ) > 0

	//Cria a query
	self:FatSV035CreateSQL()

	DbSelectArea(self:cAliasQry)

	//Posiciona no registro inicial
	(self:cAliasQry)->(DbGoTop())

	While !(self:cAliasQry)->(Eof())

        self:ojItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)

			xValue := &("(self:cAliasQry)->"+self:aStruct[nX][5])

			If lObfuscated .and. ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf self:aStruct[nX][3] == "date"
				self:ojItems[self:aStruct[nX][1]] := IIF( ValType(xValue) == "C",;
														totvs.framework.treports.date.stringToTimeStamp( xValue ),;
														totvs.framework.treports.date.dateToTimeStamp( xValue ))
			Else
				self:ojItems[self:aStruct[nX][1]] := xValue	
			EndIf

        Next nX

		self:ProcessData(1)
        self:oData:appendData(self:ojItems)

		(self:cAliasQry)->(DbSkip())

	EndDo

    (self:cAliasQry)->(DBCloseArea())

    FwFreeArray(aPDFields)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class productsvscustomersSmartViewBusinessObject

	Local nX := 0 as numeric

	self:aFields := {"A7_FILIAL","A7_CLIENTE","A7_LOJA","A7_PRODUTO","A7_CODCLI","A7_DESCCLI","A7_PRECO01","A7_DTREF01","A7_PRECO02","A7_DTREF02",;
					"A7_PRECO03","A7_DTREF03","A7_PRECO04","A7_DTREF04","A7_PRECO05","A7_DTREF05","A7_PRECO06","A7_DTREF06","A7_PRECO07","A7_DTREF07",;
					"A7_PRECO08","A7_DTREF08","A7_PRECO09","A7_DTREF09","A7_PRECO10","A7_DTREF10","A7_PRECO11","A7_DTREF11","A7_PRECO12","A7_DTREF12",;
					"A7_SERVTIM","A7_LVLEMB","A7_LPSEMB","A7_PERCUB"}

    self:aStruct := FatTrGetStruct(self:aFields)

	For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

	self:oSchema:addParameter("MV_FIL"		,STR0015, "string", .T.,,,,,, FTSVHelp(,,,.T.)) 		// Filial
	self:oSchema:addParameter("MV_ORDER"	,STR0012, "number", .F.,,,,,, FTSVHelp(,,.T.))			// Ordem
	self:oSchema:addParameter("MV_CLIINI"	,STR0004, "string", .F.,,,,,, FTSVHelp("MTR681", "02"))	// Cliente de
	self:oSchema:addParameter("MV_CLIFIM"	,STR0005, "string", .F.,,,,,, FTSVHelp("MTR681", "03"))	// Cliente até
	self:oSchema:addParameter("MV_LJINI"	,STR0006, "string", .F.,,,,,, FTSVHelp("MTR770", "05"))	// Loja de
	self:oSchema:addParameter("MV_LJFIM"	,STR0007, "string", .F.,,,,,, FTSVHelp("MTR770", "06"))	// Loja até
	self:oSchema:addParameter("MV_PRDINI"	,STR0008, "string", .F.,,,,,, FTSVHelp("MTR681", "04"))	// Produto de
	self:oSchema:addParameter("MV_PRDFIM"	,STR0009, "string", .F.,,,,,, FTSVHelp("MTR681", "05"))	// Produto até
	self:oSchema:addParameter("MV_PRCLINI"	,STR0010, "string", .F.,,,,,, FTSVHelp("A7_CODCLI"))	// Cód.Prod.Cli.de
	self:oSchema:addParameter("MV_PRCLIFIM"	,STR0011, "string", .F.,,,,,, FTSVHelp("A7_CODCLI"))	// Cód.Prod.Cli.até

	If __lLookUp
		self:setCustomURL("MV_FIL"		,"api/framework/v1/genericLookupService/smartview/SM0", 2)              
		self:setCustomURL("MV_CLIINI"	,"api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_CLIFIM"	,"api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PRDINI"	,"api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PRDFIM"	,"api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PRCLINI"	,"api/framework/v1/genericQuery?tables=SA7&fields=a7_filial,a7_cliente,a7_loja,a7_produto,a7_codcli&DeletedFilter=true&Format=smartview&KeyProperty=a7_codcli", 2)
		self:setCustomURL("MV_PRCLIFIM"	,"api/framework/v1/genericQuery?tables=SA7&fields=a7_filial,a7_cliente,a7_loja,a7_produto,a7_codcli&DeletedFilter=true&Format=smartview&KeyProperty=a7_codcli", 2)
		self:setCustomURL("MV_ORDER"	,"api/faturamento/smartview/v1/options/fat/productsvscustomers/SVFTCliPrdPar/1", 1)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFTCliPrdPar
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Function SVFTCliPrdPar(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"		 
		aRet := { FatGetStrCpos( { 'A7_CLIENTE', 'A7_LOJA', 'A7_PRODUTO' } ) ,;
				  FatGetStrCpos( { 'A7_PRODUTO', 'A7_CLIENTE', 'A7_LOJA' } ) ,;
				  FatGetStrCpos( { 'A7_CLIENTE', 'A7_LOJA', 'A7_CODCLI' } ) }
	EndIf

Return aRet

//------------------------------------------------------------------
/*{Protheus.doc} FatSV035CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author FAT/CRM
@since 24/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV035CreateSQL() class productsvscustomersSmartViewBusinessObject

	Local aFiliais	   	    := {}	as array
	Local aOrder			:= {} 	as array
	Local cQuery			:= "" 	as character
	Local cFieldsQry		:= ""	as character
	Local cNameTmp		    := ""	as character
	Local nParam		    := 1 	as numeric
    Local oQuery 	 		:= Nil  as object
    Local oTempTableBranch	:= Nil  as object
    
	cFieldsQry := "A7_FILIAL,A7_CLIENTE,A7_LOJA,A7_PRODUTO,A7_CODCLI,A7_DESCCLI,A7_PRECO01,A7_DTREF01,A7_PRECO02,A7_DTREF02,"
	cFieldsQry += "A7_PRECO03,A7_DTREF03,A7_PRECO04,A7_DTREF04,A7_PRECO05,A7_DTREF05,A7_PRECO06,A7_DTREF06,A7_PRECO07,A7_DTREF07,"
	cFieldsQry += "A7_PRECO08,A7_DTREF08,A7_PRECO09,A7_DTREF09,A7_PRECO10,A7_DTREF10,A7_PRECO11,A7_DTREF11,A7_PRECO12,A7_DTREF12,"
	cFieldsQry += "A7_SERVTIM,A7_LVLEMB,A7_LPSEMB,A7_PERCUB"
	cFieldsQry += self:cLocSelect

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

    // Função que irá criar a tabela temporaria para fazer o Join de filiais
    oTempTableBranch := FatSVTableTempBranch(aFiliais, "SA7", "A7_FILIAL")
    cNameTmp := oTempTableBranch:GetTableNameForQuery()

    // Inclui campos customizados, se houver
    FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	// Define a ordenação com base no parâmetro MV_ORDER
	aOrder := 	{" SA7.A7_CLIENTE, SA7.A7_LOJA, SA7.A7_PRODUTO ",;
                 " SA7.A7_PRODUTO, SA7.A7_CLIENTE, SA7.A7_LOJA, A7.A7_CLIENTE, SA7.A7_LOJA, SA7.A7_CODCLI",;
				}

	cQuery := " SELECT ? "
	cQuery += "		FROM " 		+ RetSqlName("SA7") + " SA7 "
	cQuery += "	INNER JOIN ? TMPFIL "
	cQuery += " 	ON TMPFIL.TMP_FILIAL	= SA7.A7_FILIAL "
	cQuery += " LEFT JOIN " 	+ RetSqlName("SA1") + " SA1 "
	cQuery += " 	ON 	SA1.A1_FILIAL		= SA7.A7_FILIAL "   
	cQuery += " 	AND SA1.A1_COD			= SA7.A7_CLIENTE  "
	cQuery += " 	AND SA1.A1_LOJA 		= SA7.A7_LOJA "
	cQuery += " 	AND SA1.D_E_L_E_T_		= ? "
	cQuery += " WHERE SA7.A7_FILIAL 		= TMPFIL.TMP_FILIAL "
	cQuery += " 	AND SA7.A7_CLIENTE 		>= ? AND SA7.A7_CLIENTE <= ? "
	cQuery += " 	AND SA7.A7_LOJA 		>= ? AND SA7.A7_LOJA 	<= ? "
	cQuery += " 	AND SA7.A7_PRODUTO 		>= ? AND SA7.A7_PRODUTO <= ? "
	cQuery += " 	AND SA7.A7_CODCLI 		>= ? AND SA7.A7_CODCLI 	<= ? "
	cQuery += " ? "
	cQuery += " 	AND SA7.D_E_L_E_T_		= ? "
	cQuery += " ORDER BY ? "

	oQuery := FwExecStatement() :New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, MV_CLIINI)
	oQuery:SetString(nParam++, MV_CLIFIM)
	oQuery:SetString(nParam++, MV_LJINI)
	oQuery:SetString(nParam++, MV_LJFIM)
	oQuery:SetString(nParam++, MV_PRDINI)
	oQuery:SetString(nParam++, MV_PRDFIM)
	oQuery:SetString(nParam++, MV_PRCLINI)
	oQuery:SetString(nParam++, MV_PRCLIFIM)
	oQuery:SetUnsafe(nParam++, self:cLocWhere)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, IIf(MV_ORDER > 0, aOrder[MV_ORDER], aOrder[1]))

	self:cAliasQry := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	FwFreeArray(aFiliais)
	FwFreeArray(aOrder)

Return
