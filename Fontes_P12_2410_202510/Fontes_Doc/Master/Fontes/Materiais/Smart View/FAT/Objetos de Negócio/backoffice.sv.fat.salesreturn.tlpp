#include "protheus.ch" 
#include "totvs.ch"
#include "fwlibversion.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesreturn.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.salesreturn.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF1,SD1,SA1,SB1", name="Devoluções de Vendas", country="ALL")
//-------------------------------------------------------------------
/*/{Protheus.doc} salesreturnSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Devoluções de Vendas

@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class salesreturnSmartViewBusinessObject From IntegratedProvider

    public method new()          as object
    public method getData()      as object
    public method getSchema()    as object
    public method SVGetTitulos() as array

    public method FatSv007CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAliasTmpLoc as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data nPercIpiLoc  as numeric
    protected data nValIpiLoc   as numeric

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salesreturnSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001) //"Faturamento" 
    self:setDisplayName(STR0002) //"Devoluções de Vendas"
    self:setDescription(STR0003) //"Relação das Devoluções de Vendas"
    self:setIsLookUp(.T.)

    self:cAliasTmpLoc   := ""
    self:cSelectLoc     := ""
    self:cWhereLoc      := ""

	self:nPercIpiLoc    := 0
	self:nValIpiLoc     := 0
    self:aStruct        := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class salesreturnSmartViewBusinessObject

    Local cFilSF1       := "" as character
    Local cCliFor       := "" as character
    Local cNomeAnt      := "" as character
    Local cNFOri        := "" as character
    Local cSeriOri      := "" as character
    Local cQuebra       := "" as character
    Local cPrefixo      := "" as character
    Local cFilMV1DUPREF := Alltrim(SuperGetMv("MV_1DUPREF")) as character
    Local cId           := "" as character
    Local cRealName     := "" as character

    Local nX            := 0 as numeric
    Local nIPI          := 0 as numeric
    Local nValIpi       := 0 as numeric
    Local nScan         := 0 as numeric
    Local nDecs         := 0 as numeric
    Local nStruct       := 0 as numeric
    Local nTitulos      := 0 as numeric

    Local aTitulosVend  := {} as array
    Local aPDFields     := {} as array

    Local lDevolucao    := .F. as logical
    Local lObfuscated   := .F. as logical

    Private cAliasSF1   := ""  as character

    Private oQrySE1     := Nil as object

    Private MV_FIL 		:= "" as character
    Private MV_MOEDA    := 0  as numeric

	// Carrega os parametros SX1
    FatSetValueMVPAR(oFilter:getParameters(), "MTR770")

    //Traz todos os campos da estrutura
    self:aStruct := self:getStructFields()

    //Traz todos os campos do dicionário (campos usuário também)
    self:aFields := self:getArrayFields()

	// Criacao das Querys
    self:FatSv007CreateSQL()

    //Tratamento para permitir apenas o número de moedas utilizadas
    MV_MOEDA := Iif(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)
    nDecs := msdecimais(MV_MOEDA)

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, {})
    lObfuscated := Len(aPDFields) > 0

    nStruct := Len(self:aStruct)

    SF2->(dbSetOrder(1))

    //Posiciona no registro inicial
    (cAliasSF1)->(dbGoTop())

    While !(cAliasSF1)->(Eof())

        cCliFor  := (cAliasSF1)->F1_FORNECE
        cNomeAnt := (cAliasSF1)->A1_NOME
        cFilSF1  := (cAliasSF1)->F1_FILIAL

        If lObfuscated //Controle LGPD
            cNomeAnt := FwProtectedDataUtil():ValueAsteriskToAnonymize(cNomeAnt)
        EndIf

        cNFOri := (cAliasSF1)->D1_NFORI
        cSeriOri := ""

        If SF2->(MsSeek(cFilSF1+cNFOri+(cAliasSF1)->D1_SERIORI))
            If Empty(SF2->F2_PREFIXO)
                cPrefixo := cFilMV1DUPREF
                cPrefixo := &(cPrefixo)
            Else
                cPrefixo := SF2->F2_PREFIXO
            EndIf
            cSeriOri := cPrefixo
        EndIf

        lDevolucao := .F.
        cQuebra := (cAliasSF1)->D1_FILIAL+(cAliasSF1)->D1_DOC+(cAliasSF1)->D1_SERIE+(cAliasSF1)->D1_FORNECE+(cAliasSF1)->D1_LOJA

        While !(cAliasSF1)->(Eof()) .And. (cAliasSF1)->D1_FILIAL+(cAliasSF1)->D1_DOC+(cAliasSF1)->D1_SERIE+(cAliasSF1)->D1_FORNECE+(cAliasSF1)->D1_LOJA == cQuebra

            lDevolucao := .T.

            self:nPercIpiLoc := 0
            self:nValIpiLoc := 0

            //Tipo 2 realiza tratamento dos valores dos itens com cáculos localizados
            self:processData(2)

            nIpi    := self:nPercIpiLoc
            nValIpi := self:nValIpiLoc
            
            self:jItems := JsonObject():new()

            for nX := 1 To nStruct

                cId := self:aStruct[nX]:getName()
                cRealName := self:aStruct[nX]:getRealName()

                If !( "E1_" $ cRealName )

                    if lObfuscated .and. ( nScan := aScan(aPDFields, {|x| x[1] == cRealName } ) )  > 0  
                        self:jItems[cId] := aPDFields[nScan][2]
                    else
                        if cId == "TIPOREGISTRO"
                            self:jItems[cId] := STR0004
                        elseif cId == "PERCIPIPROD"
                            self:jItems[cId] := nIpi
                        elseif cId == "VLRIPIPROD"
                            self:jItems[cId] := nValIpi                
                        elseif cId == "CUSTO"
                            self:jItems[cId] := iif(MV_MOEDA==1,(cAliasSF1)->D1_CUSTO,&("D1_CUSTO"+ Str(IIF(MV_MOEDA > 5 , 1 , MV_MOEDA ) ,1) ))
                        elseIf cID  == 'VLRUNITPROD'
                            self:jItems[cId] := xMoeda((cAliasSF1)->D1_VUNIT,(cAliasSF1)->F1_MOEDA,MV_MOEDA,(cAliasSF1)->D1_DTDIGIT,nDecs,(cAliasSF1)->F1_TXMOEDA)
                        elseIf cID  == 'VLRTOTALPROD'
                            self:jItems[cId] := xMoeda(((cAliasSF1)->D1_TOTAL-(cAliasSF1)->D1_VALDESC),(cAliasSF1)->F1_MOEDA,MV_MOEDA,(cAliasSF1)->D1_DTDIGIT,nDecs,(cAliasSF1)->F1_TXMOEDA)
                        elseif UPPER(self:aStruct[nX]:GetType()) == "DATE"				
                            self:jItems[cId] := IIf( ValType((cAliasSF1)->&(cRealName)) == "C" , totvs.framework.treports.date.stringToTimeStamp( (cAliasSF1)->&(cRealName) ) ,  totvs.framework.treports.date.dateToTimeStamp( (cAliasSF1)->&(cRealName) ) )
                        else
                            self:jItems[cId] := (cAliasSF1)->&(cRealName)
                        endif
                    endif

                endif

            next nX

            self:processData(1)
            self:oData:appendData(self:jItems)

            (cAliasSF1)->(dbSkip())

        EndDo

        //Busca títulos referente a venda
        If lDevolucao

            aTitulosVend := self:SVGetTitulos(cFilSF1, cNFOri, cSeriOri)
            nTitulos := Len(aTitulosVend)

            For nX := 1 To nTitulos

                self:jItems := JsonObject():new()

                self:jItems["FILIAL"]           := cFilSF1
                self:jItems["TIPOREGISTRO"]     := STR0005  //"DUPLICATA NF SAÍDA"
                self:jItems["CODCLIFOR"]        := cCliFor
                self:jItems["NOMECLIFOR"]       := cNomeAnt
                self:jItems["NUMERONOTAORIG"]   := cNFOri
                self:jItems["SERIENOTAORIG"]    := cSeriOri
                self:jItems["PREFIXO"]          := aTitulosVend[nX][01]
                self:jItems["NUMTITULO"]        := aTitulosVend[nX][02]
                self:jItems["PARCELA"]          := aTitulosVend[nX][03]
                self:jItems["VENCIMENTO"]       := totvs.framework.treports.date.stringToTimeStamp(aTitulosVend[nX][04])
                self:jItems["SALDO"]            := aTitulosVend[nX][05]

                self:processData(1)
                self:oData:appendData(self:jItems)
            Next
        Endif
    EndDo

	(cAliasSF1)->(dbCloseArea())

    FreeObj(oQrySE1)

    aSize(aTitulosVend, 0)
    aSize(aPDFields,    0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class salesreturnSmartViewBusinessObject

	Local nX			:= 0 as numeric
    Local nPos          := 0 as numeric
    Local nStruct       := 0 as numeric
    Local nStructOrd    := 0 as numeric

	Local aFieldsSF1	:= {} as array
	Local aFieldsSD1	:= {} as array
   	Local aFieldsSB1	:= {} as array
    Local aFieldsSA1	:= {} as array
    Local aFieldsSE1	:= {} as array
    Local aStructCpo	:= {} as array
    Local aStructOrd	:= {} as array
	Local aStruct		:= {} as array
    Local aParameters   := {} as array

    //Estrutura campos ordenados para apresentação
    aStructOrd  := {    {"FILIAL",{}},;
                        {"TIPOREGISTRO",{"TIPOREGISTRO", STR0006, "string", STR0006, "TIPOREGISTRO"}},; //"Tipo Registro"
                        {"NUMERONOTADEV",{}},;
                        {"SERIENOTADEV",{}},;
                        {"DATADIGITA",{}},;
                        {"ITEM",{}},;
                        {"CODPRODUTO",{}},;
                        {"DSCPRODUTO",{}},;
                        {"QTDPROD",{}},;
                        {"UMPROD",{}},;
                        {"TIPOPROD",{}},;
                        {"GRUPOPROD",{}},;
                        {"VLRUNITPROD",{}},;
                        {"VLRDESCPROD",{}},;
                        {"VLRTOTALPROD",{}},;
                        {"PERCIPIPROD",{}},;
                        {"VLRIPIPROD",{}},;
                        {"PERCICMSPROD",{}},;
                        {"VLRICMSPROD",{}},;
                        {"VLRICMSRETPROD",{}},;
                        {"CODCLIFOR",{}},;
                        {"NOMECLIFOR",{}},;
                        {"TESENTRADA",{}},;
                        {"CUSTO",{}},;
                        {"NUMERONOTAORIG",{}},;
                        {"SERIENOTAORIG",{}},;
                        {"PREFIXO",{}},;
                        {"NUMTITULO",{}},;
                        {"PARCELA",{}},;
                        {"VENCIMENTO",{}},;
                        {"SALDO",{}}    }

    aStructCpo  := {    {"FILIAL","NUMERONOTADEV","SERIENOTADEV"},; //Estrutura campos SF1
                        {"DATADIGITA","ITEM","CODPRODUTO","QTDPROD","TIPOPROD","GRUPOPROD","VLRUNITPROD","VLRDESCPROD","VLRTOTALPROD","PERCIPIPROD","VLRIPIPROD","PERCICMSPROD","VLRICMSPROD","VLRICMSRETPROD","CODCLIFOR","TESENTRADA","CUSTO","NUMERONOTAORIG","SERIENOTAORIG"},; //Estrutura campos SD1
                        {"DSCPRODUTO","UMPROD"},; //Estrutura campos SB1
                        {"NOMECLIFOR"},; //Estrutura campos SA1
                        {"PREFIXO","NUMTITULO","PARCELA","VENCIMENTO","SALDO"}   } //Estrutura campos SE1

    aFieldsSF1  := {"F1_FILIAL","F1_DOC","F1_SERIE"}
    aStruct     := FatTrGetStruct(aFieldsSF1)
    nStruct     := Len(aStruct)

    For nX := 1 To nStruct
        If (nPos := aScan(aStructOrd, {|c| c[01] = aStructCpo[01][nX]})) > 0
            aStructOrd[nPos][02] := {aStructCpo[01][nX],aStruct[nX][2],aStruct[nX][3],aStruct[nX][2],aFieldsSF1[nX]}
        EndIf
    Next

    aFieldsSD1  := {"D1_DTDIGIT","D1_ITEM","D1_COD","D1_QUANT","D1_TP","D1_GRUPO","D1_VUNIT","D1_VALDESC","D1_TOTAL","D1_IPI","D1_VALIPI","D1_PICM","D1_VALICM","D1_ICMSRET","D1_FORNECE","D1_TES","D1_CUSTO","D1_NFORI","D1_SERIORI"}
    aStruct     := FatTrGetStruct(aFieldsSD1)
    nStruct     := Len(aStruct)
    For nX := 1 To nStruct
        If (nPos := aScan(aStructOrd, {|c| c[01] = aStructCpo[02][nX]})) > 0
            aStructOrd[nPos][02] := {aStructCpo[02][nX],aStruct[nX][2],aStruct[nX][3],aStruct[nX][2],aFieldsSD1[nX]}
        EndIf
    Next

    aFieldsSB1  := {"B1_DESC","B1_UM"}
    aStruct     := FatTrGetStruct(aFieldsSB1)
    nStruct     := Len(aStruct)
    For nX := 1 To nStruct
        If (nPos := aScan(aStructOrd, {|c| c[01] = aStructCpo[03][nX]})) > 0
            aStructOrd[nPos][02] := {aStructCpo[03][nX],aStruct[nX][2],aStruct[nX][3],aStruct[nX][2],aFieldsSB1[nX]}
        EndIf
    Next

    aFieldsSA1  := {"A1_NOME"}
    aStruct     := FatTrGetStruct(aFieldsSA1)
    nStruct     := Len(aStruct)
    If (nPos := aScan(aStructOrd, {|c| c[01] = aStructCpo[04][01]})) > 0
        aStructOrd[nPos][02] := {aStructCpo[04][01],aStruct[1][2],aStruct[1][3],aStruct[1][2],aFieldsSA1[01]}
    EndIf

    aFieldsSE1  := {"E1_PREFIXO","E1_NUM","E1_PARCELA","E1_VENCTO","E1_SALDO"}
    aStruct     := FatTrGetStruct(aFieldsSE1)
    nStruct     := Len(aStruct)
    For nX := 1 To nStruct
        If (nPos := aScan(aStructOrd, {|c| c[01] = aStructCpo[05][nX]})) > 0
            aStructOrd[nPos][02] := {aStructCpo[05][nX],aStruct[nX][2],aStruct[nX][3],aStruct[nX][2],aFieldsSE1[nX]}
        EndIf
    Next

    nStructOrd := Len(aStructOrd)
    For nX := 1 To nStructOrd
        self:oSchema:addProperty(aStructOrd[nX][02][01], aStructOrd[nX][02][02], aStructOrd[nX][02][03], aStructOrd[nX][02][04], aStructOrd[nX][02][05])
    Next

    // Busca no SX1 os parâmetros que serão utilizados
    aParameters := FtSVSetParam('MTR770',,,{'09'})

    // Adiciona os parâmetros do pergunte no Schema
	self:oSchema:addParameter("MV_FIL", STR0007 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

    self:oSchema:addParameter("MV_MOEDA", STR0008 + " ?", "number", .F.,,,,,, FTSVHelp("MTR770","09")) // Moeda

    // Validação para versão da Lib
    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SA1", 2) // Do Cliente ?
        self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SA1", 2) // Ate Cliente ?
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/13", 2)  // Do CFO ?
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/13", 2)  // Ate CFO ?
    EndIf

    aSize(aParameters, 0)
    aSize(aFieldsSF1, 0)
    aSize(aFieldsSD1, 0)
    aSize(aFieldsSB1, 0)
    aSize(aFieldsSA1, 0)
    aSize(aFieldsSE1, 0)
    aSize(aStructCpo, 0)
    aSize(aStructOrd, 0)
	aSize(aStruct, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVGetTitulos
Retorna array com títulos a pagar gerados na nota fiscal de saída
 
@return array: aTitulos
 
@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method SVGetTitulos(cFilialSE1 as character, cNFOri as character, cSerieOri as character) as array Class salesreturnSmartViewBusinessObject

    Local cAliasSE1 := "" as character
    Local aTitulos  := {} as array

	// Executa a SubQuery SE1
    oQrySE1:SetString(1, cFilialSE1)
    oQrySE1:SetString(2, cNFOri)
    oQrySE1:SetString(3, cSerieOri)
    oQrySE1:SetString(4, " ")

    cAliasSE1 := oQrySE1:OpenAlias()

    (cAliasSE1)->(dbGoTop())
	While !(cAliasSE1)->(Eof())

        aAdd(aTitulos,{(cAliasSE1)->E1_PREFIXO, (cAliasSE1)->E1_NUM, (cAliasSE1)->E1_PARCELA, (cAliasSE1)->E1_VENCTO,;
        xMoeda((cAliasSE1)->E1_SALDO,(cAliasSE1)->E1_MOEDA,MV_MOEDA,(cAliasSE1)->E1_EMISSAO) })

		(cAliasSE1)->(dbSkip())

	EndDo

    (cAliasSE1)->(dbCloseArea())

Return aTitulos

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv007CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 08/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv007CreateSQL() Class salesreturnSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuerySF1  := ""  as character
    Local cQuerySE1  := ""  as character
    Local cFieldsQry := ""  as character
    Local cPrefix    := ""  as character
    Local oQrySF1 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    For nX := 1 to Len(self:aFields)

        cPrefix := SubStr(self:aFields[nX],1,3)

        If cPrefix == 'F1_'
            cPrefix := 'SF1.'
        ElseIf cPrefix == 'D1_'
            cPrefix := 'SD1.'
        ElseIf cPrefix == 'B1_'
            cPrefix := 'SB1.'
         ElseIf cPrefix == 'A1_'
            cPrefix := 'SA1.'
        Else
            cPrefix := ''
        EndIf

        If !Empty(cPrefix)
            cFieldsQry += IIf(nX == 1 , ' ' , ', ') + cPrefix + self:aFields[nX]
        EndIf

    Next Nx

    //Campos necessários no select
    cFieldsQry += " ,SF1.F1_FORNECE, SF1.F1_LOJA, SF1.F1_TIPO, SF1.F1_MOEDA, SF1.F1_TXMOEDA, SD1.D1_FILIAL, SD1.D1_DOC "
    cFieldsQry += " ,SD1.D1_SERIE, SD1.D1_LOJA, SD1.D1_CUSTO2, SD1.D1_CUSTO3, SD1.D1_CUSTO4, SD1.D1_CUSTO5 "

    For nX := 1 To nNumFils

        cQuerySF1 += "SELECT ? "
        cQuerySF1 += " FROM "       + RetSqlName("SF1") + " SF1 "
        cQuerySF1 += "LEFT JOIN "   + RetSqlName("SD1") + " SD1 "
        cQuerySF1 += "  ON SD1.D1_FILIAL    = ? "
        cQuerySF1 += "  AND SD1.D1_DOC      = SF1.F1_DOC "
        cQuerySF1 += "  AND SD1.D1_SERIE    = SF1.F1_SERIE "
        cQuerySF1 += "  AND SD1.D1_FORNECE  = SF1.F1_FORNECE "
        cQuerySF1 += "  AND SD1.D1_LOJA     = SF1.F1_LOJA "
        cQuerySF1 += "  AND SD1.D1_CF BETWEEN ? AND ? "
        cQuerySF1 += "  AND SD1.D_E_L_E_T_  = ? "
        cQuerySF1 += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
        cQuerySF1 += "  ON SB1.B1_FILIAL    = ? "
        cQuerySF1 += "  AND SB1.B1_COD      = SD1.D1_COD "
        cQuerySF1 += "  AND SB1.D_E_L_E_T_  = ? "
        cQuerySF1 += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
        cQuerySF1 += "  ON SA1.A1_FILIAL    = ? "
        cQuerySF1 += "  AND SA1.A1_COD      = SF1.F1_FORNECE "
        cQuerySF1 += "  AND SA1.A1_LOJA     = SF1.F1_LOJA "
        cQuerySF1 += "  AND SA1.D_E_L_E_T_  = ? "
        cQuerySF1 += "WHERE SF1.F1_FILIAL   = ? "
        cQuerySF1 += "  AND SF1.F1_FORNECE BETWEEN ? AND ? "
        cQuerySF1 += "  AND SF1.F1_LOJA BETWEEN ? AND ? "
        cQuerySF1 += "  AND SF1.F1_TIPO     = ? "
        cQuerySF1 += "  AND SF1.F1_DTDIGIT BETWEEN ? AND ? "
        cQuerySF1 += "  AND NOT (?) "
        cQuerySF1 += "  ? "
        cQuerySF1 += "  AND SF1.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
            cQuerySF1 += " UNION ALL "
        EndIf

    Next nX

    // Monta SubQuery
    cQuerySE1 := "SELECT E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VENCTO,E1_SALDO,E1_MOEDA,E1_EMISSAO "
    cQuerySE1 += " FROM " + RetSqlName("SE1") + " SE1 "
    cQuerySE1 += "WHERE E1_FILIAL       = ? "
    cQuerySE1 += "  AND E1_NUM          = ? "
    cQuerySE1 += "  AND E1_PREFIXO      = ? "
    cQuerySE1 += "  AND SE1.D_E_L_E_T_  = ? "
    cQuerySE1 += " ORDER BY E1_PREFIXO, E1_NUM, E1_PARCELA "

    oQrySF1 := FwExecStatement():New(ChangeQuery(cQuerySF1))
    oQrySE1 := FwExecStatement():New(ChangeQuery(cQuerySE1))

    For nX := 1 To nNumFils

        oQrySF1:SetUnsafe(nParam++, cFieldsQry)
        oQrySF1:SetString(nParam++, FatSVFilial('SD1', 'D1_FILIAL', aFiliais[nX]))
        oQrySF1:SetString(nParam++, MV_PAR07)
        oQrySF1:SetString(nParam++, MV_PAR08)
        oQrySF1:SetString(nParam++, ' ')
        oQrySF1:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]))
        oQrySF1:SetString(nParam++, ' ')
        oQrySF1:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQrySF1:SetString(nParam++, ' ')
        oQrySF1:SetString(nParam++, FatSVFilial('SF1', 'F1_FILIAL', aFiliais[nX]))
        oQrySF1:SetString(nParam++, MV_PAR03)
        oQrySF1:SetString(nParam++, MV_PAR04)
        oQrySF1:SetString(nParam++, MV_PAR05)
        oQrySF1:SetString(nParam++, MV_PAR06)
        oQrySF1:SetString(nParam++, 'D')
        oQrySF1:SetString(nParam++, DTOS(MV_PAR01))
        oQrySF1:SetString(nParam++, DTOS(MV_PAR02))
        oQrySF1:SetUnsafe(nParam++, IsRemito(2, 'F1_TIPODOC'))
        oQrySF1:SetUnsafe(nParam++, self:cWhereLoc)
        oQrySF1:SetString(nParam++, ' ')

    Next nX

    self:cAliasTmpLoc := cAliasSF1 := oQrySF1:OpenAlias()

    FreeObj(oQrySF1)

    aSize(aFiliais,0)

Return
