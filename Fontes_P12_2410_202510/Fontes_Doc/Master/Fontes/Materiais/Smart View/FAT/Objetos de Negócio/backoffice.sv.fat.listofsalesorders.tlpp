#include "fwlibversion.ch"
#include "protheus.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofsalesorders.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofsalesorders.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SC5,SC6,SC9,SF4", name="Relação de Pedidos de Venda", country="ALL", customTables="SC5,SC6,SF4")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofsalesordersSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relacao de Pedidos de VEndas

@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class listofsalesordersSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV027CreateSQL()
 
    protected data aFields      as array
    protected data aStruct      as array
    protected data cLocSelect1  as character
    protected data cLocSelect2  as character
    protected data cLocSelect3  as character
    protected data cLocWhere1   as character
    protected data cLocWhere2   as character
    protected data cLocWhere3   as character
    protected data cLocGroupBy1 as character
    protected data cLocGroupBy2 as character
    protected data cLocAlias    as character
    protected data ojItems      as json

    protected data nItemLoc     as numeric
    protected data nValIPILoc   as numeric

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listofsalesordersSmartViewBusinessObject

    _Super:new()
    self:appEndArea(STR0001)        // "Faturamento"
    self:setDisplayName(STR0002)    // "Relação de Pedido de VEndas"
    self:setDescription(STR0003)    // "Listagem de Relação de Pedido de VEndas"
    self:setIsLookUp(.T.)  

    self:aFields := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listofsalesordersSmartViewBusinessObject

    Local aQuant 	    := {} as array
    Local aCampos	    := {} as array
    Local aTam   	    := {} as array
    Local aPDFields     := {} as array
    Local aImpostos     := MaFisRelImp("MTR700",{"SC5","SC6"}) as array

    Local cOP			:= "" as character
    Local cDescTab	    := "" as character
    Local cPedido       := "" as character
    Local cCampo        := "" as character
    Local cVEnds        := "" as character
    Local cLocal        := "" as character
    Local cTes          := "" as character
    Local cNome         := "" as character
    Local cForVen       := "" as character
    Local cItem      	:= "" as character
    Local cProduto   	:= "" as character
    Local cDescricao 	:= "" as character
    Local xValue	    := "" as character

    Local cTmpTable     := GetNextAlias() as character

    Local dC5Emissao    := dDataBase as date
    Local dEntreg		:= dDataBAse as date

    Local lContInt      := .T. as logical 
    Local lCabPed       := .T. as logical
    Local lBarra        := .F. as logical
    Local lImp 		    := .F. as logical
    Local lObfuscated   := .F. as logical
    Local lAlimArray    := .F. as logical

    Local nX	 	    := 1 as numeric
    Local nQtBloq	    := 0 as numeric
    Local nItem         := 0 as numeric    
    Local nC5Moeda      := 0 as numeric    
    Local nPos          := 0 as numeric
    Local nPrunit       := 0 as numeric
    Local nVldesc       := 0 as numeric
    Local nValIPI       := 0 as numeric 
    Local nAcresFin     := 0 as numeric
    Local nPacresFin    := 0 as numeric
    Local nQuant        := 0 as numeric
    Local nTotLocal	    := 0 as numeric
    Local nQtdVen		:= 0 as numeric
    Local nQtdEnt		:= 0 as numeric
    Local nQtLib		:= 0 as numeric
    Local nValDesc	    := 0 as numeric
    Local nPrcVen		:= 0 as numeric
    Local nImpLinha	    := 0 as numeric
    Local nTFat		    := 0 as numeric
    Local nScan         := 0 as numeric
    Local nTamPRCVEN    := FWSX3Util():GetFieldStruct("C6_PRCVEN")[3] as numeric
    Local nTamProd      := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric

    Private nTamBLEST   := FWSX3Util():GetFieldStruct("C9_BLEST")[3] as numeric
    Private nTamBLCRED  := FWSX3Util():GetFieldStruct("C9_BLCRED")[3] as numeric

    Private cAliasSC5   := "" as character
    Private cAliasSC6   := "" as character
    Private cAliasSC9   := "" as character

    Private aFieldsValue:= {} as array
    Private aCposCustom := self:getCustomFields() as array

    Private oTempTable  := Nil as json

    Private NORDER		:= 0   as numeric
    Private MV_FIL		:= ""  as character
    Private MV_PEDDE	:= ""  as character
    Private MV_PEDATE	:= ""  as character
    Private MV_PRODDE	:= ""  as character
    Private MV_PRODATE	:= ""  as character

    SA2->(DbSetOrder(1)) //A2_FILIAL, A2_COD, A2_LOJA
    SA1->(DbSetOrder(1)) //A1_FILIAL, A1_COD, A1_LOJA
    SB1->(DbSetOrder(1)) //B1_FILIAL, B1_COD

    //Metodo para retorno do json dos parâmetros
    FatSetValueMVPAR(oFilter:getParameters(), "MTR700")

    //Tratamento para permitir apenas o número de moedas utilizadas
    MV_PAR08 := IIf(MV_PAR08 <= 0 .Or. MV_PAR08 > ContaMoeda(), 1, MV_PAR08)

    NORDER := IIf(NORDER == 0, 1, NORDER)

    //Tratamento do tamanho do campo do parametro para mascara do produto
    MV_PAR05 := PADR(MV_PAR05, nTamProd)

    // Define array com base no SB2 e Monta arquivo de trabalho
    // para baixar estoque na listagem
    aTam := FWSX3Util():GetFieldStruct("B2_LOCAL")
    aAdd(aCampos, {"TB_LOCAL", "C", aTam[3], aTam[4]})

    aTam := FWSX3Util():GetFieldStruct("B2_COD")
    aAdd(aCampos, {"TB_COD",   "C", aTam[3], aTam[4]})

    aTam := FWSX3Util():GetFieldStruct("B2_QATU")
    aAdd(aCampos, {"TB_SALDO", "N", aTam[3], aTam[4]})

    //Instancia tabela temporária
    oTempTable := FWTemporaryTable():New(cTmpTable)

    //Atribui o  os índices
    oTempTable:SetFields( aCampos )
    oTempTable:AddIndex("1",{"TB_LOCAL","TB_COD"})

    //Criação da tabela
    oTempTable:Create()

    DbSelectArea("SC6")

    //Array para processamento
    aFieldsValue    := {;
                            {"FILIAL",      {"FILIAL",      "", '(cAliasSC6)->C5_FILIAL'}},;
                            {"NUMPEDIDO",   {"NUMPEDIDO",   "", '(cAliasSC6)->C5_NUM'}},;
                            {"CLIENTE",     {"CLIENTE",     "", '(cAliasSC6)->C5_CLIENTE'}},;
                            {"LOJA",        {"LOJA",        "", '(cAliasSC6)->C5_LOJACLI'}},;
                            {"TIPO",        {"TIPO",        "", 'cForVen'}},;
                            {"NOME",        {"NOME",        "", 'cNome'}},;
                            {"EMISSAO",     {"EMISSAO",     "", '(cAliasSC6)->C5_EMISSAO'}},;
                            {"TRANSP",      {"TRANSP",      "", '(cAliasSC6)->C5_TRANSP'}},;
                            {"CONDPAG",     {"CONDPAG",     "", '(cAliasSC6)->C5_CONDPAG'}},;
                            {"ITEM",        {"ITEM",        "", 'cItem'}},;
                            {"VENDEDOR",    {"VENDEDOR",    "", 'cVEnds'}},;
                            {"PRODUTO",     {"PRODUTO",     "", 'cProduto'}},;
                            {"DESCRICAO",   {"DESCRICAO",   "", 'cDescricao'}},;
                            {"ESTOQDISP",   {"ESTOQDISP",   "", 'nTotLocal'}},;
                            {"VENDIDO",     {"VENDIDO",     "", 'nQtdVen'}},;
                            {"ATENDIDO",    {"ATENDIDO",    "", 'nQtdEnt'}},;
                            {"SALDO",       {"SALDO",       "", 'nSaldo'}},;
                            {"QTDLIB",      {"QTDLIB",      "", 'nQtLib'}},;
                            {"QTDBLOQ",     {"QTDBLOQ",     "", 'nQtBloq'}},;
                            {"VALDESC",     {"VALDESC",     "", 'nValDesc'}},;
                            {"PRECOUNIT",   {"PRECOUNIT",   "", 'nPrcVen'}},;
                            {"SITPEDIDO",   {"SITPEDIDO",   "", 'cSitPed'}},;
                            {"VALENTREGAR", {"VALENTREGAR", "", 'nTFat'}},;
                            {"ENTREGA",     {"ENTREGA" ,    "", 'dEntreg'}},;
                            {"IMPOSTO",     {"IMPOSTO" ,    "", 'nImpLinha'}};
						} 

	//Irá montar as queries do objeto de negócio
    self:FatSV027CreateSQL()

    //Adiciono no Afields neste ponto para validar no tratametno do LGPD
    aadd(self:aFields,'A1_NOME')
    aadd(self:aFields,'A2_NOME')

    //VerIfica se precisa fazer o tratamento para LGPD
    aPDFields	:= FatGetfieldsLGPD(self:aFields, aCposCustom)
    lObfuscated	:= Len(aPDFields) > 0

    DbselectArea(cAliasSC6)

    While !( cAliasSC6 )->( Eof() )

        //Executa a validacao dos filtros do usuario e Parametros
        DbSelectArea( cAliasSC6 ) 

        If ValidMasc((cAliasSC6)->C6_PRODUTO,MV_PAR05)

            cItem      := (cAliasSC6)->C6_ITEM
            cProduto   := (cAliasSC6)->C6_PRODUTO
            cDescricao := (cAliasSC6)->C6_DESCRI

            If MV_PAR14 == 2
                If SB1->(MsSeek(FatSVFilial('SB1', 'B1_FILIAL', (cAliasSC6)->C6_FILIAL)+(cAliasSC6)->C6_PRODUTO))
                    cDescricao := SB1->B1_DESC
                EndIf
            EndIf

            cLocal  := (cAliasSC6)->C6_LOCAL
            cOp     := (cAliasSC6)->C6_OP
            cTes    := (cAliasSC6)->C6_TES
            nQtdven := (cAliasSC6)->C6_QTDVEN
            nQtdent := (cAliasSC6)->C6_QTDENT
            nPrunit := (cAliasSC6)->C6_PRUNIT
            nPrcven := (cAliasSC6)->C6_PRCVEN
            nVldesc := (cAliasSC6)->C6_VALDESC
            dEntreg := (cAliasSC6)->C6_ENTREG

            //---------------------------------------------------------------------------------------
            // VerIfica se o pedido de vEnda esta apto a faturar(nQtLib!=0)
            // ou com bloqueio(nQtBloq!=0) , conforme o parametro MV_PAR06
            // seleciona os reguistros a serem impressos.
            // Elementos do Array aQuant :
            // 1. Produto
            // 2. Quantidade Liberada
            // 3. Quantidade Bloqueada
            //---------------------------------------------------------------------------------------
            aQuant := {}
            nPos := Ascan(aQuant, {|x|x[1]== (cAliasSC9)->C9_PRODUTO})

            If (cAliasSC9)->C9_BLEST == space(nTamBLEST).And.(cAliasSC9)->C9_BLCRED == space(nTamBLCRED).And.(cAliasSC9)->C9_QTDLIB > 0

                IIf( MV_PAR06 <> 2 .And. nPos != 0, aQuant[nPos,2] += (cAliasSC9)->C9_QTDLIB, Aadd(aQuant,{(cAliasSC9)->C9_PRODUTO,(cAliasSC9)->C9_QTDLIB,0}) )

            ElseIf (cAliasSC9)->C9_BLEST <> space(nTamBLEST).Or.(cAliasSC9)->C9_BLCRED <> space(nTamBLCRED)

                IIf( MV_PAR06 <> 1 .And. nPos != 0, aQuant[nPos,3] += (cAliasSC9)->C9_QTDLIB, Aadd(aQuant,{(cAliasSC9)->C9_PRODUTO,0,(cAliasSC9)->C9_QTDLIB}) )

            EndIf

            //Varre o Array aQuant e alimenta as variaveis nQtLib e nQtBloq com o conteudo.
            For nX := 1 To Len(aQuant)
                lAlimArray  := MV_PAR06 == 2 .And. aQuant[1,2] > 0 .Or. MV_PAR06 == 1 .And. aQuant[1,3] > 0
                lContInt    := IIf( lAlimArray, .F., lContInt )
                nQtlib      += IIf( !lAlimArray, aQuant[nX,2], 0 )
                nQtBloq     += IIf( !lAlimArray, aQuant[nX,3], 0 )
            Next nX

            //Imprime o cabecalho do pedido no relatorio.
            If (lCabPed .And. lContInt .And. Len(aQuant)>0 .And. MV_PAR06 <> 3) .Or. (lCabPed .And. lContInt .And. MV_PAR06 == 3)

                DbSelectArea(cAliasSC5)
                MaFisSave()
                MaFisEnd()
                MaFisIni((cAliasSC5)->C5_CLIENTE,(cAliasSC5)->C5_LOJACLI,"C",(cAliasSC5)->C5_TIPO,(cAliasSC5)->C5_TIPOCLI,aImpostos,,,"SB1","MTR700")

                //Tipo 2 - Utilizar para países que o calculo de impostos depEnde da serie
                self:processData(2)

                For nX:= 1 To 5
                    cCampo := "C5_VEND"+STR(nX,1)
                    cCampo := (cAliasSC5)->(FieldGet(FieldPos(cCampo)))
                    cVEnds += IIf( !Empty(cCampo), IIf(lBarra,"/","") + cCampo, "" )
                    lBarra := IIf( !Empty(cCampo), .T., lBarra )
                Next nX

                cPedido     := (cAliasSC6)->C6_NUM
                nC5Moeda    := (cAliasSC5)->C5_MOEDA
                dC5Emissao  := (cAliasSC5)->C5_EMISSAO
                nPacresFin  := (cAliasSC5)->C5_ACRSFIN
                lCabPed     := .F.

            EndIf

            //---------------------------------------------------------------------------------------
            // o Skip dos dados Validos do C6 e dado antes da impressao dos itens do relatorio por
            // causa da compatibilizacao das logicas com Query e coDbase onde a disposicao dos dados
            // se deram de formas dIferentes.
            //---------------------------------------------------------------------------------------
            DbSelectArea(cAliasSC6)

            //Imprime os itens do pedido no relatorio
            If ( lContInt .And. Len(aQuant) > 0 .And. MV_PAR06 <> 3 ) .Or. ( lContInt .And. MV_PAR06 == 3 )

                nQuant      := IIf( (nQtLib+nQtBloq) <> 0, (nQtLib+nQtBloq), (nQtdven - nQtdent) )
                nTFat       := IIf( (nQtLib+nQtBloq) <> 0, (nQtLib+nQtBloq) * nPrcven, (nQtdven - nQtdent) * nPrcVen )
                nPrUnit     := IIf( nPrUnit == 0, NoRound(nTFat/nQuant,nTamPRCVEN), nPrUnit)
                nAcresFin   := A410Arred(nPrcVen*nPacresFin/100,"D2_PRCVEN")
                nTFat       += A410Arred(nQuant*nAcresFin,"D2_TOTAL")
                nValDesc    := a410Arred(nPrUnit*nQuant,"D2_DESCON")-nTFat
                nValDesc    := IIf(nVlDesc==0,nVlDesc,nValDesc)
                nValDesc    := Max(0,nValDesc)
                nPrUnit     += nAcresFin

                MaFisAdd(cProduto,cTes,(nQtLib+nQtBloq),nPrunit,nValdesc,,,,0,0,0,0,(nTFat+nValDesc),0,0,0)

                nItem 		+= 1
                lImp 		:= .T.
                nImpLinha   := 0

                //Atualizacao do saldo disponivel em estoque com base no SB2 atraves de arquivo de trab
                DbSelectArea(cTmpTable)

                IIf( MsSeek(cLocal+cProduto), (nTotLocal := (cTmpTable)->TB_SALDO, RecLock(cTmpTable,.F.)), 0 )

                If !MsSeek(cLocal+cProduto)
                    SB2->(MsSeek(FatSVFilial('SB2', 'B2_FILIAL', (cAliasSC6)->C6_FILIAL)+cProduto+cLocal))
                    nTotLocal := SB2->(SaldoSB2())
                    (cTmpTable)->(RecLock(cTmpTable,.T.))
                    (cTmpTable)->TB_COD	  := cProduto
                    (cTmpTable)->TB_LOCAL := cLocal
                    (cTmpTable)->TB_SALDO := nTotLocal
                EndIf

                If nQtLib <= 0
                    (cTmpTable)->TB_SALDO := (cTmpTable)->TB_SALDO - (nQtdven - nQtdent)
                EndIf

                (cTmpTable)->(MsUnLock())

                //Descricao SX5
                cDescTab := IIF(!Empty(cOp), FWGetSX5("E2",cOp)[1][4], "")  

                //Tipo 3 - Utilizar para calculo de impostos
                self:nItemLoc := nItem
                self:processData(3)

                nValIPI     := IIf( !Empty(self:nValIPILoc), self:nValIPILoc, 0 )
                nImpLinha   := nValIPI

                If MV_PAR13 == 2
                    self:nItemLoc := nItem
                    self:processData(4)

                    nImpLinha += IIf( !Empty(self:nValIPILoc), self:nValIPILoc, 0 )
                EndIf

                nTFat       += IIf( MV_PAR07 == 1, nValIPI, 0 )
                nValDesc    := xMoeda(nValDesc,nC5Moeda,MV_PAR08,IIf(MV_PAR12 == 1, dC5Emissao, dDataBase))
                nPrcVen     := xMoeda(nPrcVen ,nC5Moeda,MV_PAR08,IIf(MV_PAR12 == 1, dC5Emissao, dDataBase))
                nImpLinha   := xMoeda(nImpLinha,nC5Moeda,MV_PAR08,IIf(MV_PAR12 == 1, dC5Emissao, dDataBase)) 
                nTFat       := xMoeda(nTFat,nC5Moeda,MV_PAR08,IIf(MV_PAR12 == 1, dC5Emissao, dDataBase))

                If (cAliasSC6)->C5_TIPO $ "BD"
                    SA2->(MsSeek(FatSVFilial('SA2', 'A2_FILIAL', (cAliasSC6)->C6_FILIAL)+(cAliasSC6)->C5_CLIENTE+(cAliasSC6)->C5_LOJACLI))
                    cNome   := SA2->A2_NOME
                    cForVen := STR0010 //"Fornecedor"
                Else
                    SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', (cAliasSC6)->C6_FILIAL)+(cAliasSC6)->C5_CLIENTE+(cAliasSC6)->C5_LOJACLI))
                    cNome   := SA1->A1_NOME
                    cForVen := STR0009 //"Cliente"
                EndIf

                nSaldo  := nQtdVen-nQtdEnt
                cSitPed := cOP + "-" + cDescTab

				// Controle LGPD
				cNome := IIf( lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(cNome), cNome )

                self:ojItems := JsonObject():new()
                For nX := 1 To Len(self:aStruct)

                    //VerIfica se existe tratamento para valores do JSON
                    xValue  :=  IIf( (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0,;
                                    FatGetValueFields(aFieldsValue[nScan][1], "cAliasSC6", aFieldsValue[nScan][2]),;
                                    "(cAliasSC6)->"+self:aStruct[nX][5] )

                    //VerIficação para tratamento de LGPD e campos tipo Data antes informar no JSON
                    self:ojItems[self:aStruct[nX][1]]   := IIf( lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) ) > 0,;
                                                            aPDFields[nScan][2],;
                                                            IIf( UPPER(self:aStruct[nX][3]) == "DATE",;
                                                                IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
                                                                &(xValue) ) )

                Next nX

                self:processData(1) //Tratamento para commit localizado
                self:oData:appEndData(self:ojItems)

                nQtlib  := 0
                nQtBloq	:= 0

            EndIf

            (cAliasSC6)->(DbSkip())

            //Imprime o Rodape do pedido no relatorio.
            If (cAliasSC6)->C6_NUM  <> cPedido .And. lImp 

                nQtlib 	:= 0
                nQtBloq := 0
                nItem	:= 0
                cVEnds  := ""
                lCabPed := .T.
                lBarra  := .F.
                lImp    := .F.

                MaFisEnd()

            EndIf
            
        Else
            (cAliasSC6)->(DbSkip())
        EndIf

    EndDo

    //Limpeza do objeto/variável
    aSize(aTam,        0)
    aSize(aQuant,      0)
    aSize(aCampos,     0)
    aSize(aImpostos,   0)
    aSize(aPDFields,   0)
    aSize(aCposCustom, 0)
    aSize(aFieldsValue,0)

    If( ValType(oTempTable) == "O")
        oTempTable:Delete()
        FreeObj(oTempTable)
        oTempTable := Nil
    EndIf

    DbSelectArea(cAliasSC5)
    DbCloseArea()
    DbSelectArea("SC6")

Return self:oData
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listofsalesordersSmartViewBusinessObject

    Local aCposFake 	:= {} as array
    Local aParameters   := {} as array
    Local nx            := 0  as numeric

    self:aFields := {'FILIAL','NUMPEDIDO','CLIENTE','LOJA','TIPO','NOME','EMISSAO','TRANSP','CONDPAG','VENDEDOR','ITEM','PRODUTO','DESCRICAO',;
                    'ESTOQDISP','VENDIDO','ATENDIDO','SALDO','QTDLIB','QTDBLOQ','VALDESC','PRECOUNIT','SITPEDIDO','IMPOSTO','VALENTREGAR','ENTREGA'}

    aCposFake := {  {"FILIAL"	    ,STR0035    ,"string"	,STR0035    ,"FILIAL"},;        //"Filial"
                    {"NUMPEDIDO"	,STR0011    ,"string"	,STR0011    ,"NUMPEDIDO"},;     //"Numero"
                    {"CLIENTE"	    ,STR0009    ,"string"	,STR0009    ,"CLIENTE"},;       //"Cliente"
                    {"LOJA"	        ,STR0012	,"string"	,STR0012	,"LOJA"},;          //"Loja"
                    {"TIPO"	        ,STR0013	,"string"	,STR0013	,"TIPO"},;          //"Tipo"
                    {"NOME"			,STR0014	,"string"	,STR0014	,"NOME"},;	        //"Nome"
                    {"EMISSAO"		,STR0015	,"date"	    ,STR0015	,"EMISSAO"},;	    //"Emissao"
                    {"TRANSP"		,STR0016    ,"string"	,STR0016    ,"TRANSP"},;	    //"Transportadora"
                    {"CONDPAG"		,STR0017    ,"string"	,STR0017    ,"CONDPAG"},;	    //"Cond.Pagamento"
                    {"VENDEDOR"		,STR0018	,"string"	,STR0018	,"VENDEDOR"},;      //"Vendedor"
                    {"ITEM"			,STR0019	,"string"	,STR0019	,"ITEM"},;	        //"Item"
                    {"PRODUTO"		,STR0020	,"string"	,STR0020	,"PRODUTO"},;	    //"Produto"
                    {"DESCRICAO"	,STR0021	,"string"	,STR0021	,"DESCRICAO"},;     //"Descrição"
                    {"ESTOQDISP"	,STR0022	,"number"	,STR0022	,"ESTOQDISP"},;     //"Estoque Disponível"
                    {"VENDIDO"		,STR0023	,"number"	,STR0023	,"VENDIDO"},;	    //"Vendido"
                    {"ATENDIDO"		,STR0024	,"number"	,STR0024	,"ATENDIDO"},;      //"Atendido"
                    {"SALDO"		,STR0025	,"number"	,STR0025	,"SALDO"},;	        //"Saldo"
                    {"QTDLIB"		,STR0026	,"number"	,STR0026	,"QTDLIB"},;	    //"Qtd. Liberada"
                    {"QTDBLOQ"		,STR0027	,"number"	,STR0027	,"QTDBLOQ"},;	    //"Qtd. Bloqueada"
                    {"VALDESC"		,STR0028	,"number"	,STR0028	,"VALDESC"},;	    //"Vlr. Desconto"
                    {"PRECOUNIT"	,STR0029	,"number"	,STR0029	,"PRECOUNIT"},;     //"Preço Unitário"
                    {"SITPEDIDO"	,STR0030	,"string"	,STR0030	,"SITPEDIDO"},;     //"Situação"
                    {"IMPOSTO"		,STR0031	,"number"	,STR0031	,"IMPOSTO"},;	    //"Imposto"
                    {"VALENTREGAR"	,STR0032	,"number"	,STR0032	,"VALENTREGAR"},;   //"Valor a Entregar"
                    {"ENTREGA"		,STR0033	,"date"		,STR0033	,"ENTREGA"}}	    //"Entrega"

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake )

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:oSchema:addParameter("MV_FIL",     STR0035 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))           //Filial
    self:oSchema:addParameter("NORDER",     STR0034 + " ?", "number", .F.,,,,,, FTSVHelp(,,.T.))            //Ordem
    self:oSchema:addParameter("MV_PEDDE",   STR0036 + " ?", "string", .F.,,,,,, FTSVHelp('MTR700','01'))    //Pedido de
    self:oSchema:addParameter("MV_PEDATE",  STR0037 + " ?", "string", .F.,,,,,, FTSVHelp('MTR700','02'))    //Pedido ate
    self:oSchema:addParameter("MV_PRODDE",  STR0038 + " ?", "string", .F.,,,,,, FTSVHelp('MTR700','03'))    //Produto de
    self:oSchema:addParameter("MV_PRODATE", STR0039 + " ?", "string", .F.,,,,,, FTSVHelp('MTR700','04'))    //Produto ate

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('MTR700',,, {'01', '02', '03', '04'})

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nx][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",     "api/framework/v1/genericLookupService/smartview/SM0", 2)
        self:setCustomURL("MV_PEDDE",   "api/framework/v1/genericLookupService/smartview/SC5", 2)
        self:setCustomURL("MV_PEDATE",  "api/framework/v1/genericLookupService/smartview/SC5", 2)
        self:setCustomURL("MV_PRODDE",  "api/framework/v1/genericLookupService/smartview/SB1", 2)
        self:setCustomURL("MV_PRODATE", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("NORDER",     "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/1", 1)
        self:setCustomURL("MV_PAR06",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/2", 1)
        self:setCustomURL("MV_PAR07",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/3", 1)
        self:setCustomURL("MV_PAR09",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/4", 1)
        self:setCustomURL("MV_PAR12",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/5", 1)
        self:setCustomURL("MV_PAR13",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/6", 1)
        self:setCustomURL("MV_PAR14",   "api/faturamento/smartview/v1/options/fat/listofsalesorders/SVFLisOrdPr/7", 1)
	EndIf

    aSize(aCposFake, 0)
    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFLisOrdPr
Retorna as opçoes disponível para parametros do tipo combo.

@return array: Array com as opçoes.

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFLisOrdPr(cOpcao) 

    Local aRet := {} as  array

	If cOpcao == "1"
		aRet := { FatGetStrCpos( { 'C5_FILIAL', 'C5_NUM',     'C6_ITEM' } ) ,;
				  FatGetStrCpos( { 'C6_FILIAL', 'C6_PRODUTO', 'C6_NUM'  } ) ,;
				  FatGetStrCpos( { 'C6_FILIAL', 'C6_ENTREG',  'C5_NUM', 'C6_ITEM' } ) }
    ElseIf cOpcao == "2"
        aRet := FatGetCboX1('MTR700', 6)
    ElseIf cOpcao == "3"
        aRet := FatGetCboX1('MTR700', 7)
    ElseIf cOpcao == "4"
        aRet := FatGetCboX1('MTR700', 9)
    ElseIf cOpcao == "5"
        aRet := FatGetCboX1('MTR700', 12)
    ElseIf cOpcao == "6"
        aRet := FatGetCboX1('MTR700', 13)
    ElseIf cOpcao == "7"
        aRet := FatGetCboX1('MTR700', 14)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv027CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 21/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV027CreateSQL() class listofsalesordersSmartViewBusinessObject

    Local nX 	        := 0   as numeric
    Local nNumFils      := 0   as numeric
    Local nParam        := 1   as numeric
	Local cQuery        := ""  as character
    Local cFieldsQry    := ""  as character
    Local cFieldsQry2   := ""  as character
    Local cLocWhere     := ""  as character
    Local cGroupBy      := ""  as character
    Local oQuery 	    := Nil as object
    Local aFiliais	    := {}  as array
    Local aOrdem        := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    // Define a ordenação do resultado da query
    aOrdem := { " C5_FILIAL, C5_NUM, C6_ITEM ",;
                " C6_FILIAL, C6_PRODUTO, C6_NUM ",;
                " C6_FILIAL, C6_ENTREG, C5_NUM, C6_ITEM " }

    If MV_PAR06 == 3

        cFieldsQry := "SC5.C5_FILIAL,SC5.C5_NUM,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPO,SC5.C5_TIPOCLI,SC5.C5_TRANSP,SC5.C5_EMISSAO, "
        cFieldsQry += "SC5.C5_CONDPAG,SC5.C5_MOEDA,SC5.C5_VEND1,SC5.C5_VEND2,SC5.C5_VEND3,SC5.C5_VEND4,SC5.C5_VEND5, "
        cFieldsQry += "SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_PRODUTO,SC6.C6_DESCRI,SC6.C6_OP,SC6.C6_TES,SC6.C6_QTDVEN,SC6.C6_PRUNIT,SC6.C6_VALDESC, "
        cFieldsQry += "SC6.C6_VALOR,SC6.C6_ITEM,SC6.C6_PRCVEN,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_ENTREG,SC6.C6_LOCAL,SC6.C6_QTDENT,SC6.C6_BLQ, "
        cFieldsQry += "SC9.C9_FILIAL,SC9.C9_PEDIDO,SC9.C9_ITEM,SC9.C9_NFISCAL,SC9.C9_BLEST,SC9.C9_BLCRED,SC9.C9_PRODUTO, "
        cFieldsQry += "SUM(SC9.C9_QTDLIB) C9_QTDLIB,SF4.F4_FILIAL,SF4.F4_DUPLIC,SF4.F4_CODIGO,SC5.C5_ACRSFIN " + self:cLocSelect1

        cFieldsQry2 := "SC5.C5_FILIAL,SC5.C5_NUM,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPO,SC5.C5_TIPOCLI,SC5.C5_TRANSP,SC5.C5_EMISSAO, "
        cFieldsQry2 += "SC5.C5_CONDPAG,SC5.C5_MOEDA,SC5.C5_VEND1,SC5.C5_VEND2,SC5.C5_VEND3,SC5.C5_VEND4,SC5.C5_VEND5, "
        cFieldsQry2 += "SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_PRODUTO,SC6.C6_DESCRI,SC6.C6_OP,SC6.C6_TES,SC6.C6_QTDVEN,SC6.C6_PRUNIT,SC6.C6_VALDESC, "
        cFieldsQry2 += "SC6.C6_VALOR,SC6.C6_ITEM,SC6.C6_PRCVEN,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_ENTREG,SC6.C6_LOCAL,SC6.C6_QTDENT,SC6.C6_BLQ, "
        cFieldsQry2 += "' ' C9_FILIAL,' ' C9_PEDIDO,' ' C9_ITEM,' ' C9_NFISCAL,' ' C9_BLEST,' ' C9_BLCRED,' ' C9_PRODUTO,0 C9_QTDLIB, "
        cFieldsQry2 += "SF4.F4_FILIAL,SF4.F4_DUPLIC,SF4.F4_CODIGO,SC5.C5_ACRSFIN " + self:cLocSelect2

        cGroupBy := "SC5.C5_FILIAL,SC5.C5_NUM,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPO,SC5.C5_TIPOCLI,SC5.C5_TRANSP,SC5.C5_EMISSAO, "
        cGroupBy += "SC5.C5_CONDPAG,SC5.C5_MOEDA,SC5.C5_VEND1,SC5.C5_VEND2,SC5.C5_VEND3,SC5.C5_VEND4,SC5.C5_VEND5, "
        cGroupBy += "SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_PRODUTO,SC6.C6_DESCRI,SC6.C6_OP,SC6.C6_TES,SC6.C6_QTDVEN,SC6.C6_PRUNIT,SC6.C6_VALDESC, "
        cGroupBy += "SC6.C6_VALOR,SC6.C6_ITEM,SC6.C6_PRCVEN,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_ENTREG,SC6.C6_LOCAL,SC6.C6_QTDENT,SC6.C6_BLQ, "
        cGroupBy += "SC9.C9_FILIAL,SC9.C9_PEDIDO,SC9.C9_ITEM,SC9.C9_NFISCAL,SC9.C9_BLEST,SC9.C9_BLCRED,SC9.C9_PRODUTO, "
        cGroupBy += "SF4.F4_FILIAL,SF4.F4_DUPLIC,SF4.F4_CODIGO,SC5.C5_ACRSFIN " + self:cLocGroupBy1

        cLocWhere := self:cLocWhere1

        //Adiciona campos customizados (no array da estrutura)
        FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCposCustom, .T., .F., .T. )
        FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry2,aCposCustom, .T., .F., .F. )
        FatInjCposPerson(@aFieldsValue, @self:aStruct, @cGroupBy,   aCposCustom, .T., .F., .F. )

    Else

        cFieldsQry := "SC5.C5_FILIAL,SC5.C5_NUM,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPO,SC5.C5_TIPOCLI,SC5.C5_TRANSP,SC5.C5_EMISSAO, "
        cFieldsQry += "SC5.C5_CONDPAG,SC5.C5_MOEDA,SC5.C5_VEND1,SC5.C5_VEND2,SC5.C5_VEND3,SC5.C5_VEND4,SC5.C5_VEND5, "
        cFieldsQry += "SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_PRODUTO,SC6.C6_DESCRI,SC6.C6_OP,SC6.C6_TES,SC6.C6_QTDVEN,SC6.C6_PRUNIT,SC6.C6_VALDESC, "
        cFieldsQry += "SC6.C6_VALOR,SC6.C6_ITEM,SC6.C6_PRCVEN,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_ENTREG,SC6.C6_LOCAL,SC6.C6_QTDENT,SC6.C6_BLQ, "
        cFieldsQry += "SC9.C9_FILIAL,SC9.C9_PEDIDO,SC9.C9_ITEM,SC9.C9_NFISCAL,SC9.C9_BLEST,SC9.C9_BLCRED,SC9.C9_PRODUTO, "
        cFieldsQry += "SUM(SC9.C9_QTDLIB) C9_QTDLIB,SF4.F4_FILIAL,SF4.F4_DUPLIC,SF4.F4_CODIGO,SC5.C5_ACRSFIN " + self:cLocSelect3

        cGroupBy  := "SC5.C5_FILIAL,SC5.C5_NUM,SC5.C5_CLIENTE,SC5.C5_LOJACLI,SC5.C5_TIPO,SC5.C5_TIPOCLI,SC5.C5_TRANSP,SC5.C5_EMISSAO, "
        cGroupBy  += "SC5.C5_CONDPAG,SC5.C5_MOEDA,SC5.C5_VEND1,SC5.C5_VEND2,SC5.C5_VEND3,SC5.C5_VEND4,SC5.C5_VEND5, "
        cGroupBy  += "SC6.C6_FILIAL,SC6.C6_NUM,SC6.C6_PRODUTO,SC6.C6_DESCRI,SC6.C6_OP,SC6.C6_TES,SC6.C6_QTDVEN,SC6.C6_PRUNIT,SC6.C6_VALDESC, "
        cGroupBy  += "SC6.C6_VALOR,SC6.C6_ITEM,SC6.C6_PRCVEN,SC6.C6_CLI,SC6.C6_LOJA,SC6.C6_ENTREG,SC6.C6_LOCAL,SC6.C6_QTDENT,SC6.C6_BLQ, "
        cGroupBy  += "SC9.C9_FILIAL,SC9.C9_PEDIDO,SC9.C9_ITEM,SC9.C9_NFISCAL,SC9.C9_BLEST,SC9.C9_BLCRED,SC9.C9_PRODUTO, "
        cGroupBy  += "SF4.F4_FILIAL,SF4.F4_DUPLIC,SF4.F4_CODIGO,SC5.C5_ACRSFIN " + self:cLocGroupBy2

        cLocWhere := self:cLocWhere3

        //Adiciona campos customizados (no array da estrutura, )
        FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCposCustom, .T., .F., .T.)
        FatInjCposPerson(@aFieldsValue, @self:aStruct, @cGroupBy,   aCposCustom, .T., .F., .F. )

    EndIf

	// Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        // Explicacoes da Query:
        // O Campo C9_QTDLIB e somado por haver varios C9 para cada C6.
        //
        // Agrupamento de todos os campos comuns do SELECT para que nos relacionamentos com
        // C5,C6 e C9 com varios C9 para cada C6 gerem apenas um registro com o campo C9_QTDLIB
        // somado.
        //
        // Quando o MV_PAR06 ==3 ->"TODOS OS PEDIDOS" esse UNION acrescenta a Query os registros
        // do C6 que nao possuem C9.
        //
        // Se MV_PAR06 == 3
        // ATENCAO !!!! ao manipular os campos do SELECT ou a ordem da Clausula ORDER BY verIfi-
        // car a concordancia entre os mesmos !!!!!!!!!
        //
        // Para o uso de UNION a estrutura de SELECT deve ser igual a do SELECT anterior
        // note que a nomeclatura do C9 usa os mesmos nomes dos campos da TABELA, porem com o
        // uso de ' ' para nao fazer referencia a ela.

        // Imprime Ped.Vendas ? 3-Todos
        If MV_PAR06 == 3

            cQuery += "SELECT ? "
            cQuery += " FROM "      + RetSqlName("SC5") + " SC5 "
            cQuery += "INNER JOIN " + RetSqlName("SC6") + " SC6 "
            cQuery += " ON  SC6.C6_FILIAL    = ? "
            cQuery += " AND SC6.C6_NUM       = SC5.C5_NUM "
            cQuery += " AND SC6.C6_PRODUTO  >= ? "
            cQuery += " AND SC6.C6_PRODUTO  <= ? "
            cQuery += " AND SC6.C6_ENTREG   >= ? "
            cQuery += " AND SC6.C6_ENTREG   <= ? "
            cQuery += " AND SC6.C6_QTDVEN - SC6.C6_QTDENT > ? "
            cQuery += " AND SC6.C6_BLQ      <> ? "
            cQuery += " AND SC6.D_E_L_E_T_   = ? "
            cQuery += "INNER JOIN " + RetSqlName("SC9") + " SC9 "
            cQuery += " ON  SC9.C9_FILIAL    = ? "
            cQuery += " AND SC9.C9_PEDIDO    = SC6.C6_NUM "
            cQuery += " AND SC9.C9_ITEM      = SC6.C6_ITEM"
            cQuery += " AND SC9.C9_PRODUTO   = SC6.C6_PRODUTO "
            cQuery += " AND SC9.C9_NFISCAL   = ? "
            cQuery += " AND SC9.D_E_L_E_T_   = ? "
            cQuery += "LEFT JOIN " + RetSqlName("SF4") + " SF4 "
            cQuery += " ON  SF4.F4_FILIAL    = ? "
            cQuery += " AND SF4.F4_CODIGO    = SC6.C6_TES "

            //Quanto ao Tes ? 1-Gera Dupl. / 2-Nao Gera Dupl
            If MV_PAR09 == 1
                cQuery += " AND SF4.F4_DUPLIC = ? "
            ElseIf MV_PAR09 == 2
                cQuery += " AND SF4.F4_DUPLIC <> ? "
            EndIf

            cQuery += " AND SF4.D_E_L_E_T_   = ? "
            cQuery += "WHERE SC5.C5_FILIAL   = ? "
            cQuery += " AND SC5.C5_NUM      >= ? "
            cQuery += " AND SC5.C5_NUM      <= ? "
            cQuery += " ? " //Where que foi populado no objeto localizado
            cQuery += " AND SC5.D_E_L_E_T_   = ? "
            cQuery += "GROUP BY ? "
            cQuery += "UNION "
            cQuery += "SELECT ?" 
            cQuery += " FROM "      + RetSqlName("SC5") + " SC5 "
            cQuery += "INNER JOIN " + RetSqlName("SC6") + " SC6 "
            cQuery += " ON  SC6.C6_FILIAL    = ? "
            cQuery += " AND SC6.C6_NUM       = SC5.C5_NUM "
            cQuery += " AND SC6.C6_PRODUTO  >= ? "
            cQuery += " AND SC6.C6_PRODUTO  <= ? "
            cQuery += " AND SC6.C6_ENTREG   >= ? "
            cQuery += " AND SC6.C6_ENTREG   <= ? "
            cQuery += " AND SC6.C6_QTDVEN - SC6.C6_QTDENT > ? "
            cQuery += " AND SC6.C6_BLQ      <> ? "
            cQuery += " AND SC6.D_E_L_E_T_   = ? "
            cQuery += "LEFT JOIN " + RetSqlName("SF4") + " SF4 "
            cQuery += " ON  SF4.F4_FILIAL    = ? "
            cQuery += " AND SF4.F4_CODIGO    = SC6.C6_TES "

            // Quanto ao Tes ? 1-Gera Dupl. / 2-Nao Gera Dupl
            If MV_PAR09 == 1
                cQuery += " AND SF4.F4_DUPLIC  = ? "
            ElseIf MV_PAR09 == 2
                cQuery += " AND SF4.F4_DUPLIC <> ? "
            EndIf

            cQuery += " AND SF4.D_E_L_E_T_   = ? "
            cQuery += "WHERE SC5.C5_FILIAL   = ? "
            cQuery += " AND SC5.C5_NUM      >= ? "
            cQuery += " AND SC5.C5_NUM      <= ? "
            cQuery += " ? " //Where que foi populado no objeto localizado
            cQuery += " AND SC5.D_E_L_E_T_   = ? "
            cQuery += " AND NOT EXISTS ( SELECT SC9.C9_PEDIDO FROM " + RetSqlName("SC9") + " SC9 "
            cQuery += "     WHERE SC9.C9_FILIAL = ? "
            cQuery += "     AND SC9.C9_PEDIDO   = SC6.C6_NUM "
            cQuery += "     AND SC9.C9_ITEM     = SC6.C6_ITEM "
            cQuery += "     AND SC9.C9_NFISCAL  = ? "
            cQuery += "     AND SC9.C9_PRODUTO  = SC6.C6_PRODUTO"
            cQuery += "     AND SC9.D_E_L_E_T_  = ? ) "

        Else

            cQuery += "SELECT ? "
            cQuery += " FROM "      + RetSqlName("SC5") + " SC5 "
            cQuery += "INNER JOIN " + RetSqlName("SC6") + " SC6 "
            cQuery += " ON  SC6.C6_FILIAL   = ? "
            cQuery += " AND SC6.C6_NUM      = SC5.C5_NUM "
            cQuery += " AND SC6.C6_PRODUTO >= ? "
            cQuery += " AND SC6.C6_PRODUTO <= ? "
            cQuery += " AND SC6.C6_ENTREG  >= ? "
            cQuery += " AND SC6.C6_ENTREG  <= ? "
            cQuery += " AND SC6.C6_QTDVEN - SC6.C6_QTDENT > ? "
            cQuery += " AND SC6.C6_BLQ     <> ? "
            cQuery += " AND SC6.D_E_L_E_T_  = ? "
            cQuery += "INNER JOIN " + RetSqlName("SC9") + " SC9 "
            cQuery += " ON  SC9.C9_FILIAL   = ? "
            cQuery += " AND SC9.C9_PEDIDO   = SC6.C6_NUM "
            cQuery += " AND SC9.C9_ITEM     = SC6.C6_ITEM "
            cQuery += " AND SC9.C9_PRODUTO  = SC6.C6_PRODUTO "
            cQuery += " AND SC9.C9_NFISCAL  = ? "

            // Imprime Ped.Vendas ? 1-Aptos a Faturar / 2-Nao
            If MV_PAR06 == 1
                cQuery += " AND SC9.C9_BLEST  = ? "
                cQuery += " AND SC9.C9_BLCRED = ? "
                cQuery += " AND SC9.C9_QTDLIB > ? "
            ElseIf MV_PAR06 == 2
                cQuery += " AND ( SC9.C9_BLEST <> ? "
                cQuery += " OR SC9.C9_BLCRED <> ? ) "
            EndIf

            cQuery += " AND SC9.D_E_L_E_T_  = ? "

            cQuery += "LEFT JOIN " + RetSqlName("SF4") + " SF4 "
            cQuery += " ON  SF4.F4_FILIAL   = ? "
            cQuery += " AND SF4.F4_CODIGO   = SC6.C6_TES "

            //Quanto ao Tes ? 1-Gera Dupl. / 2-Nao Gera Dupl
            If MV_PAR09 == 1
                cQuery += " AND SF4.F4_DUPLIC = ? "
            ElseIf MV_PAR09 == 2
                cQuery += " AND SF4.F4_DUPLIC <> ? "
            EndIf

            cQuery += " AND SF4.D_E_L_E_T_ = ? "
            cQuery += "WHERE SC5.C5_FILIAL  = ? "
            cQuery += " AND SC5.C5_NUM     >= ? "
            cQuery += " AND SC5.C5_NUM     <= ? "
            cQuery += " ? " //Where que foi populado no objeto localizado
            cQuery += " AND SC5.D_E_L_E_T_  = ? "
            cQuery += " GROUP BY ? "

        EndIf

		If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, MV_PRODDE )
        oQuery:SetString(nParam++, MV_PRODATE )
        oQuery:SetString(nParam++, dtos(MV_PAR10) )
        oQuery:SetString(nParam++, dtos(MV_PAR11) )
        oQuery:SetString(nParam++, '0' )
        oQuery:SetString(nParam++, 'R' )
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SC9', 'C9_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, ' ' )

        If MV_PAR06 == 1
            oQuery:SetString(nParam++, Space(nTamBLEST) )
            oQuery:SetString(nParam++, Space(nTamBLCRED) )
            oQuery:SetString(nParam++, '0' )
        ElseIf MV_PAR06 == 2
            oQuery:SetString(nParam++, Space(nTamBLEST) )
            oQuery:SetString(nParam++, Space(nTamBLCRED) )
        EndIf

        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SF4', 'F4_FILIAL', aFiliais[nX]) )

        If MV_PAR09 == 1 .Or. MV_PAR09 == 2
            oQuery:SetString(nParam++, 'S' )
        EndIf

        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SC5', 'C5_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, MV_PEDDE )
        oQuery:SetString(nParam++, MV_PEDATE )
        oQuery:SetUnsafe(nParam++, cLocWhere )
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetUnsafe(nParam++, cGroupBy )

        If MV_PAR06 == 3

            oQuery:SetUnsafe(nParam++, cFieldsQry2)
            oQuery:SetString(nParam++, FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]) )
            oQuery:SetString(nParam++, MV_PRODDE )
            oQuery:SetString(nParam++, MV_PRODATE )
            oQuery:SetString(nParam++, dtos(MV_PAR10) )
            oQuery:SetString(nParam++, dtos(MV_PAR11) )
            oQuery:SetString(nParam++, '0' )
            oQuery:SetString(nParam++, 'R' )
            oQuery:SetString(nParam++, ' ' )
            oQuery:SetString(nParam++, FatSVFilial('SF4', 'F4_FILIAL', aFiliais[nX]) )

            If MV_PAR09 == 1 .Or. MV_PAR09 == 2
                oQuery:SetString(nParam++, 'S' )
            EndIf
            oQuery:SetString(nParam++, ' ' )
            oQuery:SetString(nParam++, FatSVFilial('SC5', 'C5_FILIAL', aFiliais[nX]) )
            oQuery:SetString(nParam++, MV_PEDDE )
            oQuery:SetString(nParam++, MV_PEDATE )
            oQuery:SetUnsafe(nParam++, self:cLocWhere2 )
            oQuery:SetString(nParam++, ' ' )
            oQuery:SetString(nParam++, FatSVFilial('SC9', 'C9_FILIAL', aFiliais[nX]) )
            oQuery:SetString(nParam++, ' ' )
            oQuery:SetString(nParam++, ' ' )

        EndIf

    Next nX

    oQuery:SetUnsafe(nParam++, aOrdem[NORDER] )

    cAliasSC9 := oQuery:OpenAlias()
    self:cLocAlias := cAliasSC9

    cAliasSC5 := cAliasSC6 := cAliasSC9

    FreeObj(oQuery)

    aSize(aFiliais, 0)
    aSize(aOrdem, 0)

Return
