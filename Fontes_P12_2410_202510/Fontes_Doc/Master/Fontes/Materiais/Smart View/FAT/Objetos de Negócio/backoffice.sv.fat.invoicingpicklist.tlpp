#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.invoicingpicklist.ch"

Static __LookUp	:= FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.invoicingpicklist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SD2,SB1,SB4", name="Pick-list de Faturamento", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} invoicingpicklistSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Pick-list de Faturamento.

@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class invoicingpicklistSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV022CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() class invoicingpicklistSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Pick-list de Faturamento"
    self:setDescription(STR0003)	//"Listagem Pick-list de Faturamento"
    self:setIsLookUp(.T.)   

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class invoicingpicklistSmartViewBusinessObject 

    Local xValue        := "" as character

    Local nX            := 0  as numeric
	Local nScan			:= 0  as numeric
    
    Local nTamProd      := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric

    Local lPrint	    := .T. as logical
    Local lLocalidade   := .F. as logical
    Local lObfuscated   := .F. as logical
    Local lLocaliz	    := .F. as logical

    Local aPDFields     := {}  as array

    Private aFieldsValue:= {}  as array
    Private aCposCustom := self:getCustomFields() as array

    Private MV_FIL		:= "" as character
    Private MV_NFDE     := "" as character
    Private MV_NFATE    := "" as character
    Private MV_SERIE    := "" as character
    Private MV_MASKP    := "" as character

    //----------------------------------------------------------
    // Tratativa para limpar o Cache do SuperGetMv() devido a 
    // execucao dos casos de teste de automacao.
    //----------------------------------------------------------
    SuperGetMV() //Realiza a limpeza do cache
    lLocaliz := SuperGetMV("MV_LOCALIZ") == "S"//Busca o valor do parametro

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR775")

    //Tratamento para respeitar o tamanho do campo do produto x mascara
    MV_MASKP := PADR(MV_MASKP, nTamProd)

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCposCustom )
    lObfuscated := Len( aPDFields ) > 0

    //Array para processamento
    aFieldsValue := {	{'B4_COD',     {'B4_COD',     "",'IIf( (self:cAlias)->D2_GRADE <> "S" .And. !lLocalidade, "", (self:cAlias)->B4_COD )'}},;
                        {'B4_COD',     {'B4_COD',     "",'IIf( (self:cAlias)->D2_GRADE <> "S" .And. !lLocalidade, "", (self:cAlias)->B4_COD )'}},;
                        {'D2_QUANT',   {'D2_QUANT',   "",'IIf( !lLocalidade,  (self:cAlias)->D2_QUANT, SDB->DB_QUANT )'}},;
                        {'DB_LOCALIZ', {'DB_LOCALIZ', "",'IIf( !lLocalidade, "", SDB->DB_LOCALIZ )'}};
                    }

    //Irá montar as queries do objeto de negócio
    self:FatSV022CreateSQL()

    dbSelectArea(self:cAlias)
	(self:cAlias)->(dbGoTop())

    While (self:cAlias)->(!Eof())

        //Valida o produto conforme a mascara
        If ValidMasc((self:cAlias)->D2_COD,MV_MASKP)

            lPrint := .T.
            lLocalidade := lLocaliz .And. Localiza((self:cAlias)->D2_COD)

            If lLocalidade
            
                SDB->(MsSeek((self:cAlias)->(D2_FILIAL+D2_COD+D2_LOCAL+D2_NUMSEQ+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA)))

                While SDB->(DB_FILIAL+DB_PRODUTO+DB_LOCAL+DB_NUMSEQ+DB_DOC+DB_SERIE+DB_CLIFOR+DB_LOJA) == ;
                        (self:cAlias)->(D2_FILIAL+D2_COD+D2_LOCAL+D2_NUMSEQ+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA)

                    lPrint := .F.

                    self:jItems := JsonObject():new()
                    For nX := 1 To Len(self:aStruct)

                        xValue := IIf( (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0,;
                                    FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2]),;
                                    IIf( 'DB_' $ self:aStruct[nX][1],;
                                        "SDB->"+self:aStruct[nX][5],;
                                        "(self:cAlias)->"+self:aStruct[nX][5] ) )

                        //Tratativa para tirar os espacos dos campos
                        xValue := IIF(ValType(&(xValue)) == "C", RTrim(&(xValue)), &(xValue))

                    	//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
				        self:jItems[self:aStruct[nX][1]] := IIf( lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) ) > 0,;
                                                                aPDFields[nScan][2],;
                                                                IIf( Alltrim(self:aStruct[nX][3]) == "date",;
                                                                    IIf( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C",;
                                                                        totvs.framework.treports.date.stringToTimeStamp(xValue),;
                                                                        totvs.framework.treports.date.dateToTimeStamp(xValue)),;
                                                                    xValue ))
                    Next nX

                    self:processData(1)
                    self:oData:appendData(self:jItems)

                    SDB->(DbSkip())

                EndDo

            EndIf

            If lPrint

                self:jItems := JsonObject():new()
                For nX := 1 To Len(self:aStruct)

                    xValue := IIf( (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0,;
                                FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2]),;
                                "(self:cAlias)->"+self:aStruct[nX][5] )

                    //Tratativa para tirar os espacos dos campos
                    xValue := IIF(ValType(&(xValue)) == "C", RTrim(&(xValue)), &(xValue))

                    self:jItems[self:aStruct[nX][1]] := IIf( lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) ) > 0,;
                                                            aPDFields[nScan][2],;
                                                            IIf( Alltrim(self:aStruct[nX][3]) == "date",;
                                                                IIf( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C", totvs.framework.treports.date.stringToTimeStamp(xValue), totvs.framework.treports.date.dateToTimeStamp(xValue) ),;
                                                                IIf( 'DB_' $ self:aStruct[nX][1], Nil, xValue ) ) )
                Next nX

                self:processData(1)
                self:oData:appendData(self:jItems)

            EndIf

        EndIf

        (self:cAlias)->(dbSkip())

    EndDo

    aSize(aPDFields,   0)
    aSize(aCposCustom, 0)
    aSize(aFieldsValue,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class invoicingpicklistSmartViewBusinessObject 

    Local nX := 0 as numeric

    self:aFields := {"D2_FILIAL","D2_COD","B1_DESC","B4_COD","B4_DESC","B1_UM","D2_QUANT","D2_LOCAL","DB_LOCALIZ","D2_LOTECTL","D2_NUMLOTE","D2_DTVALID","D2_POTENCI","D2_DOC"}
    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        If AllTrim(self:aStruct[nX][5]) == "B4_COD"
            self:addProperty(self:aStruct[nX][1], STR0004, self:aStruct[nX][3], STR0004, self:aStruct[nX][5])   //"Cód.Prod.Grade"
        ElseIf AllTrim(self:aStruct[nX][5]) == "B4_DESC"
            self:addProperty(self:aStruct[nX][1], STR0005, self:aStruct[nX][3], STR0005, self:aStruct[nX][5])   //"Desc.Grade"
        Else
            self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
        EndIf
    Next nX

    // Parâmetros para filtro do relatório
    self:oSchema:addParameter("MV_FIL",   STR0010 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))          //Filial ?
    self:oSchema:addParameter("MV_NFDE",  STR0006 + " ?", "string", .F.,,,,,, FTSVHelp('MTR775','01'))   //Da Nota Fiscal ?
    self:oSchema:addParameter("MV_NFATE", STR0007 + " ?", "string", .F.,,,,,, FTSVHelp('MTR775','02'))   //Ate a Nota Fiscal ?
    self:oSchema:addParameter("MV_SERIE", STR0008 + " ?", "string", .F.,,,,,, FTSVHelp('MTR775','03'))   //Da Serie ?
    self:oSchema:addParameter("MV_MASKP", STR0009 + " ?", "string", .F.,,,,,, FTSVHelp('MTR775','04'))   //Máscara do Produto ?

    If __LookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0",    2)
		self:setCustomURL("MV_NFDE",  "api/framework/v1/genericLookupService/smartview/SF2VEI", 2)
		self:setCustomURL("MV_NFATE", "api/framework/v1/genericLookupService/smartview/SF2VEI", 2)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv022CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 07/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV022CreateSQL() class invoicingpicklistSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
    Local nTamRef	 := Val(Substr(GetMv("MV_MASCGRD"),1,2)) as numeric
    Local cSubs      := IIf( Upper(TCGetDB()) $ "ORACLE|POSTGRES", "SUBSTR", "SUBSTRING" ) as character
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local cPrefix    := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)  

    For nX := 1 To Len(self:aFields)

        cPrefix := SubStr(self:aFields[nX],1,3)

        If cPrefix == 'D2_'
            cPrefix := 'SD2.'
        ElseIf cPrefix == 'B1_'
            cPrefix := 'SB1.'
        ElseIf cPrefix == 'B4_'
            cPrefix := 'SB4.'
        Else
            cPrefix := ""
        EndIf

        If !Empty(cPrefix)
            cFieldsQry += IIf(nX == 1, ' ', ', ') + cPrefix+self:aFields[nX]
        EndIf

    Next Nx

    cFieldsQry += ",SD2.D2_FILIAL, SD2.D2_GRADE, SD2.D2_SERIE, SD2.D2_NUMSEQ, SD2.D2_CLIENTE, SD2.D2_LOJA"
    cFieldsQry += self:cSelectLoc

    //Adiciona campos customizados (no array da estrutura, campos e query)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCposCustom, .T.,.F. ,.T. ,'DB_')

    // Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM "     + RetSqlName("SD2") + " SD2 "
        cQuery += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
        cQuery += " ON  SB1.B1_FILIAL   = ? "
        cQuery += " AND SB1.B1_COD      = SD2.D2_COD "
        cQuery += " AND SB1.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN " + RetSqlName("SB4") + " SB4 "
        cQuery += " ON  SB4.B4_FILIAL   = ? "
        cQuery += " AND SB4.B4_COD      = ? "
        cQuery += " AND SB4.D_E_L_E_T_  = ? "
        cQuery += "WHERE SD2.D2_FILIAL  = ? "

        If !Empty(MV_SERIE)
            cQuery += " AND ? = ? "
        Else
            cQuery += " AND SD2.D2_QUANT > ? "
        EndIf

        cQuery += " AND SD2.D2_DOC      >= ? "
        cQuery += " AND SD2.D2_DOC      <= ? "
        cQuery += " AND NOT (?) "
        cQuery += " ? "
        cQuery += " AND SD2.D_E_L_E_T_   = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY SD2.D2_FILIAL,SD2.D2_DOC,SD2.D2_SERIE,SD2.D2_CLIENTE,SD2.D2_LOJA,SD2.D2_COD,SD2.D2_LOTECTL,SD2.D2_NUMLOTE,SD2.D2_DTVALID "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry )
        oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SB4', 'B4_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, cSubs + "(SD2.D2_COD,1,"+Alltrim(Str(nTamRef)) + ")" )
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]) )

        If !Empty(MV_SERIE)
            oQuery:SetUnsafe(nParam++, SerieNfId("SD2",3,"D2_SERIE") )
            oQuery:SetString(nParam++, MV_SERIE )
        Else
            oQuery:SetString(nParam++, '0' )
        EndIf

        oQuery:SetString(nParam++, MV_NFDE )
        oQuery:SetString(nParam++, MV_NFATE )
        oQuery:SetUnsafe(nParam++, IsRemito(3,'SD2.D2_TIPODOC'))
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
