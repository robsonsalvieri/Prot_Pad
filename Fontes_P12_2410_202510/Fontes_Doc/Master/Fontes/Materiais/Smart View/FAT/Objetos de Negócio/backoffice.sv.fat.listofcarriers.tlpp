#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofcarriers.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofcarriers.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA4", name="Transportadoras", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofcarriersSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de transportadoras

@author Leonardo Kichitaro
@since 14/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofcarriersSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method FatSV002CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
    protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Leonardo Kichitaro
@since 19/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class listofcarriersSmartViewBusinessObject

	_Super:new()

    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Transportadoras"
    self:setDescription(STR0003)    //"Listagem de Transportadoras"
    self:setIsLookUp(.T.)

    self:cAlias := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Leonardo Kichitaro
@since 19/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object class listofcarriersSmartViewBusinessObject

    Local aPDFields     := {}   as array

    Local nX            := 0   as numeric

    Local lObfuscated   := .F. as logical

    Private aCpoCustom  := self:getCustomFields() as array

    Private MV_FIL		:= ""  as character
    Private CCODINI     := ""  as character
    Private CCODFIM     := ""  as character
    Private CNOMEINI    := ""  as character
    Private CNOMEFIM    := ""  as character
    Private CCGCINI     := ""  as character
    Private CCGCFIM     := ""  as character
    Private CECOINI     := ""  as character
    Private CECOFIM     := ""  as character

    Private NORDER      := 1   as numeric

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "")

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom)
    lObfuscated := Len(aPDFields) > 0

    //Irá montar as queries do objeto de negócio
	self:FatSV002CreateSQL()

    (self:cAlias)->(dbGotop())

    While !(self:cAlias)->(Eof())

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            self:jItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias)

        Next

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(DbSkip())

    EndDo

    (self:cAlias)->(DbCloseArea())

    //limpeza do objeto/variável
    aSize(aPDFields, 0)
    aSize(aCpoCustom,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author Leonardo Kichitaro
@since 19/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class listofcarriersSmartViewBusinessObject

    Local nX := 0 as numeric

    self:aFields := {"A4_FILIAL","A4_COD","A4_NOME","A4_NREDUZ","A4_EST","A4_MUN","A4_CGC","A4_INSEST","A4_ECSERVI"}

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:oSchema:addParameter("NORDER",     STR0012, "number", .F.,,,,,, FTSVHelp(,,.T.))           //Ordem
    self:oSchema:addParameter("MV_FIL",     STR0013, "string", .T.,,,,,, FTSVHelp(,,,.T.))          //Filial
	self:oSchema:addParameter("CCODINI",    STR0004, "string", .F.,,,,,, FTSVHelp("A4_COD"))	    //Código de
	self:oSchema:addParameter("CCODFIM",    STR0005, "string", .F.,,,,,, FTSVHelp("A4_COD"))	    //Código até
	self:oSchema:addParameter("CNOMEINI",   STR0006, "string", .F.,,,,,, FTSVHelp("A4_NOME"))	    //Nome de
	self:oSchema:addParameter("CNOMEFIM",   STR0007, "string", .F.,,,,,, FTSVHelp("A4_NOME"))	    //Nome até
	self:oSchema:addParameter("CCGCINI",    STR0008, "string", .F.,,,,,, FTSVHelp("A4_CGC"))	    //CPF/CNPJ de
	self:oSchema:addParameter("CCGCFIM",    STR0009, "string", .F.,,,,,, FTSVHelp("A4_CGC"))	    //CPF/CNPJ até
	self:oSchema:addParameter("CECOINI",    STR0010, "string", .F.,,,,,, FTSVHelp("A4_ECSERVI"))	//E-Commerce de
	self:oSchema:addParameter("CECOFIM",    STR0011, "string", .F.,,,,,, FTSVHelp("A4_ECSERVI"))    //E-Commerce até

    //Validação para versão da Lib
	If __lLookUp
		self:setCustomURL("MV_FIL",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
        self:setCustomURL("CCODINI", "api/framework/v1/genericLookupService/smartview/SA4", 2)
        self:setCustomURL("CCODFIM", "api/framework/v1/genericLookupService/smartview/SA4", 2)
		self:setCustomURL("NORDER",  "api/faturamento/smartview/v1/options/fat/listofcarriers/SVComboFATSV002/1", 1)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVComboFATSV002
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 18/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVComboFATSV002(cOpcao as character) as array

    Local aCposIndex := {"A4_COD","A4_NOME","A4_CGC","A4_ECSERVI"} as array
    Local aRet		 := {} as array
	Local cDescCombo := "" as character
	Local nX		 := 0 as numeric

	If cOpcao == "1"
		For nX := 1 To Len(aCposIndex)
			cDescCombo := FatGetStrCpos(StrTokArr(aCposIndex[nX],"+"))
			aAdd(aRet, cDescCombo)
		Next
	EndIf

	aSize(aCposIndex,0)

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv001CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 22/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV002CreateSQL() class listofcarriersSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array
    Local aOrdem     := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := "SA4.A4_FILIAL,SA4.A4_COD,SA4.A4_NOME,SA4.A4_NREDUZ,SA4.A4_EST,SA4.A4_MUN,SA4.A4_CGC,SA4.A4_INSEST,SA4.A4_ECSERVI"
	cFieldsQry += self:cSelectLoc

    // Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .F., .T.)

    // Define a ordenação do resultado da query
    aOrdem := { " A4_FILIAL, A4_COD ",;
                " A4_FILIAL, A4_NOME ",;
                " A4_FILIAL, A4_CGC ",;
                " A4_FILIAL, A4_ECSERVI "}

    // Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        cQuery  += " SELECT ? "
        cQuery  += "    FROM ? SA4 "
        cQuery  += " WHERE SA4.A4_FILIAL = ? "
        cQuery  += "    AND SA4.A4_COD BETWEEN ? AND ? "
        cQuery  += "    AND SA4.A4_NOME BETWEEN ? AND ? "
        cQuery  += "    AND SA4.A4_CGC BETWEEN ? AND ? "
        cQuery  += "    AND SA4.A4_ECSERVI BETWEEN ? AND ? "
        cQuery  += "    ? " //Where para SA4 que foi populado no objeto localizado
        cQuery  += " AND SA4.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetUnsafe(nParam++, RetSqlName('SA4'))
        oQuery:SetString(nParam++, FatSVFilial('SA4', 'A4_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, CCODINI)
        oQuery:SetString(nParam++, CCODFIM)
        oQuery:SetString(nParam++, CNOMEINI)
        oQuery:SetString(nParam++, CNOMEFIM)
        oQuery:SetString(nParam++, CCGCINI)
        oQuery:SetString(nParam++, CCGCFIM)
        oQuery:SetString(nParam++, CECOINI)
        oQuery:SetString(nParam++, CECOFIM)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, " ")

    Next nX

    oQuery:SetUnsafe(nParam++, IIf(NORDER > 0, aOrdem[NORDER], ""))

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais,0)
    aSize(aOrdem, 0)

Return
