#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofvisits.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.listofvisits.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1", name="Visitas", country="ALL", customTables="SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofvisitsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Visitas. 

@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class listofvisitsSmartViewBusinessObject From IntegratedProvider
    
    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV031CreateSQL()
 
    protected data aFields      as array
    protected data aStruct      as array
    protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data ojItems      as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 18/08/2023
@version 1.0*/
//-------------------------------------------------------------------   
Method new() Class listofvisitsSmartViewBusinessObject 
	
	_Super:new()
    self:appendArea(STR0001) 		//Faturamento
    self:setDisplayName(STR0002)	//Visitas
    self:setDescription(STR0003)	//Relação de Visitas
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class listofvisitsSmartViewBusinessObject

    Local aPDFields     := {} as array
    Local lObfuscated   := .F.as logical
    Local nX            := 0  as numeric

    Private aFieldsValue:= {} as array
    Private aCpoCustom  := self:getCustomFields() as array

    Private MV_FIL	    := "" as character
    Private CVENDDE     := "" as character
    Private CVENDATE    := "" as character
    Private NVENCIDA    := 1  as numeric

    // Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "")

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom)
    lObfuscated := len( aPDFields ) > 0

	//Irá montar as queries do objeto de negócio
    self:FatSV031CreateSQL()

    //Posiciona no registro inicial 
    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof()) .And. ( NVENCIDA <> 1 .Or. ((dDataBase - StoD((self:cAlias)->A1_ULTVIS )) > (self:cAlias)->A1_TEMVIS) )

        self:ojItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)

            self:ojItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias)

        Next

        self:processData(1)
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DBSkip())

    EndDo

    (self:cAlias)->(DBCloseArea())

    aSize(aPDFields,    0)
    aSize(aFieldsValue, 0)
    aSize(aCpoCustom,   0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class listofvisitsSmartViewBusinessObject

    Local nX := 0 as numeric

    self:aFields := {"A1_FILIAL","A1_VEND","A1_COD","A1_LOJA","A1_NOME","A1_ULTVIS","A1_TEMVIS","A1_CONTATO","A1_TEL"}
    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:oSchema:addParameter("MV_FIL",   STR0007 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))         //Filial
    self:oSchema:addParameter("CVENDDE" , STR0004 + " ?", "string", .F.,,,,,, FTSVHelp('MTR660','09'))  //Vendedor de
    self:oSchema:addParameter("CVENDATE", STR0005 + " ?", "string", .F.,,,,,, FTSVHelp('MTR660','10'))  //Vendedor ate
    self:oSchema:addParameter("NVENCIDA", STR0006 + " ?", "string", .F.,,,,,, FTSVHelp('MTR820','13'))  //Somente as Vencidas

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
        self:setCustomURL("CVENDDE",  "api/framework/v1/genericLookupService/smartview/SA3", 2)
        self:setCustomURL("CVENDATE", "api/framework/v1/genericLookupService/smartview/SA3", 2)
        self:setCustomURL("NVENCIDA", "api/faturamento/smartview/v1/options/fat/listofvisits/SVCBFATSV031/1", 1)
    EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVCBFATSV031
Retorna as opçoes de combobox do parametro de acordo com SX1
@return array: aRet
@author FAT/CRM
@since 15/05/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Function SVCBFATSV031(cOpcao) 

    Local aRet := {} as array

	If cOpcao == "1" 
		aRet := FatGetCboX1('MTR530', 3)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv029CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 18/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV031CreateSQL() Class listofvisitsSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := "A1_FILIAL, A1_COD, A1_LOJA, A1_NOME, A1_VEND, A1_ULTVIS, A1_TEMVIS, A1_TEL, A1_CONTATO"
    cFieldsQry +=  self:cSelectLoc

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .T., .T.)

	// Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM " + RetSqlName("SA1") + " SA1 "
        cQuery += "WHERE A1_FILIAL = ? "
        cQuery += " AND A1_TEMVIS  > ? "
        cQuery += " AND A1_VEND   >= ? "
        cQuery += " AND A1_VEND   <= ? "
        cQuery += " ? "     //Where que foi populado no objeto localizado
        cQuery += " AND D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY A1_VEND "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++ , cFieldsQry )
        oQuery:SetString(nParam++ , FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++, '0' )
        oQuery:SetString(nParam++ , CVENDDE )
        oQuery:SetString(nParam++ , CVENDATE )
        oQuery:SetUnsafe(nParam++, self:cWhereLoc )
        oQuery:SetString(nParam++, ' ' )

    Next nX

	self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
