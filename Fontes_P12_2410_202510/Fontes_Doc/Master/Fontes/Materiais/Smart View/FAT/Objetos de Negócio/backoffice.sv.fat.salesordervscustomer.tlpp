#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesordervscustomer.ch"

Static __lLookUp := FwLibVersion() >= "20231121"
Static nVend  := FA440CntVen()

namespace totvs.protheus.backoffice.sv.fat.salesordervscustomer.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SC5,SC6,SF4", name="Pedidos de Venda x Clientes", country="ALL", customTables="SA1,SC5")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesordervscustomerSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relacao de Pedidos de Venda x Clientes

@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class salesordervscustomerSmartViewBusinessObject from IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object

	public method FatSV005CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data cQuery		as character
    protected data cLocSelect	as character
    protected data nParam		as numeric
	protected data cAlias		as character
	protected data cWhereVend	as character
	protected data oQuery		as json
	protected data ojItems		as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salesordervscustomerSmartViewBusinessObject

	_Super:new()  					//Define a Área
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Pedidos de Venda x Clientes"
    self:setDescription(STR0003)	//"Relação de Pedidos de Venda x Clientes"
    self:setIsLookUp(.T.)  			//Define que terá consulta de LookUp

	self:aFields    := {}
	self:cLocSelect := ""
	self:cQuery 	:= ""
	self:nParam     := 1
	self:cAlias		:= ""
	self:cWhereVend	:= ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView

@return object: self:oData

@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class salesordervscustomerSmartViewBusinessObject

	Local aPDFields		:= {}  as array

	Local lObfuscated	:= .F. as logical

	Local nX			:= 0   as numeric

	Private aCpoCustom  := self:getCustomFields() as array

	Private MV_FIL		:= "" as character
	Private MV_DT_DE    := CToD("") as date
	Private MV_DT_ATE   := CToD("") as date
	Private MV_VEND_DE  := "" as character
	Private MV_VEND_ATE := "" as character
	Private MV_CLI_DE   := "" as character
	Private MV_CLI_ATE  := "" as character
	Private MV_MOEDA	:= 0  as numeric

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR600")

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_MOEDA := IIf(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields 	:= FatGetfieldsLGPD(self:aFields, aCpoCustom)
    lObfuscated := Len( aPDFields ) > 0

	//Irá montar as queries do objeto de negócio
	self:FatSV005CreateSQL()

	//Posiciona no registro inicial
	(self:cAlias)->(dbGoTop())

	While (self:cAlias)->(!Eof())

		self:ojItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)

			If Alltrim(self:aStruct[nX][5]) == "C6_VALOR"
				self:ojItems[self:aStruct[nX][1]] := xMoeda((self:cAlias)->C6_VALOR, (self:cAlias)->C5_MOEDA, MV_MOEDA, (self:cAlias)->C5_EMISSAO)
			Else
				self:ojItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias)
			EndIf

		Next

		self:processData(1)
		self:oData:appendData(self:ojItems)

		(self:cAlias)->(DbSkip())

	Enddo

	(self:cAlias)->(DbCloseArea())

	//Elimina da memória
	aSize(aPDFields, 0)
	aSize(aCpoCustom,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object class salesordervscustomerSmartViewBusinessObject

	Local cVend         := ""		as Character
	Local nX			:= 0		as numeric
	Local aParameters	:= {} 		as array
	Local cPergunte		:= "MTR600"	as character

    self:aFields := {"C5_FILIAL","A1_COD","A1_NOME","A1_MUN","C5_NUM","C6_VALOR"}

	For nX := 1 to nVend
        cVend := "C5_VEND" + AllTrim(Str(nX))
        If SC5->(ColumnPos(cVend)) > 0
            aAdd(self:aFields, cVend)
        EndIf
    Next

    self:aStruct := FatTrGetStruct(self:aFields)
	For nX := 1 To Len(self:aStruct)
		If 	"C5_VEND" $ AllTrim(self:aStruct[nX][5])
			cTituloCpo := STR0004 + " " + SubStr(AllTrim(self:aStruct[nX][5]), 8, 1) //"Vendedor" + Numero
			self:oSchema:addProperty(self:aStruct[nX][1], cTituloCpo, self:aStruct[nX][3], cTituloCpo, self:aStruct[nX][5])
		Else
			self:oSchema:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		EndIf
	Next

	//Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam(cPergunte,,, {'01', '02', '03', '04', '05', '06', '07', '08'} )

	self:oSchema:addParameter("MV_FIL",   	STR0011, "string", .T.,,,,,, FTSVHelp(,,, .T.))			// Filial ?
	self:oSchema:addParameter("MV_DT_DE", 	STR0005, "date",   .F.,,,,,, FTSVHelp(cPergunte,"01")) 	// A partir da Data ?
    self:oSchema:addParameter("MV_DT_ATE", 	STR0006, "date",   .F.,,,,,, FTSVHelp(cPergunte,"02")) 	// Até a Data ?
	self:oSchema:addParameter("MV_VEND_DE",	STR0007, "string", .F.,,,,,, FTSVHelp(cPergunte,"03")) 	// Vendedor de ?
	self:oSchema:addParameter("MV_VEND_ATE",STR0008, "string", .F.,,,,,, FTSVHelp(cPergunte,"04")) 	// Vendedor Até ?
	self:oSchema:addParameter("MV_CLI_DE", 	STR0009, "string", .F.,,,,,, FTSVHelp(cPergunte,"05")) 	// Cliente de ?
	self:oSchema:addParameter("MV_CLI_ATE", STR0010, "string", .F.,,,,,, FTSVHelp(cPergunte,"06")) 	// Cliente Até ?
	self:oSchema:addParameter("MV_MOEDA", 	STR0012, "number", .F.,,,,,, FTSVHelp(cPergunte,"08"))	// Moeda ?

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nx][5] )
	Next nX

	//Validação para versão da Lib
	If __lLookUp
		self:setCustomURL("MV_FIL", 	"api/framework/v1/genericLookupService/smartview/SM0", 2)	  				 	// Filial
		self:setCustomURL("MV_VEND_DE", "api/framework/v1/genericLookupService/smartview/SA3", 2) 					 	// Vendedor de
		self:setCustomURL("MV_VEND_ATE","api/framework/v1/genericLookupService/smartview/SA3", 2) 					 	// Vendedor Até
		self:setCustomURL("MV_CLI_DE",  "api/framework/v1/genericLookupService/smartview/SA1", 2) 					 	// Cliente de
		self:setCustomURL("MV_CLI_ATE", "api/framework/v1/genericLookupService/smartview/SA1", 2) 					 	// Cliente Até
		self:setCustomURL("MV_PAR09", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/1", 1) 	// Considera resíduo
		self:setCustomURL("MV_PAR12", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/2", 1) 	// Considera Agrega Valor
		self:setCustomURL("MV_PAR13", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/3", 1) 	// TES qto estoque
		self:setCustomURL("MV_PAR14", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/4", 1) 	// TES qto faturamento
		self:setCustomURL("MV_PAR15", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/5", 1) 	// Total por Clientes
		self:setCustomURL("MV_PAR16", 	"api/faturamento/smartview/v1/options/fat/salesordervscustomer/SVPedCli/6", 1) 	// Total Geral por
	EndIf

	aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVPedCli
Retorna as opçoes disponível para parametros do tipo combo.

@return array: Array com as opçoes

@author FAT/CRM
@since 14/02/2024
@version 1.0
*/
//-------------------------------------------------------------------   
Function SVPedCli(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"		 
		aRet := FatGetCboX1('MTR600' , 9 )
	ElseIf cOpcao == "2"
		aRet := FatGetCboX1('MTR600' , 12 )
	ElseIf cOpcao == "3"
		aRet := FatGetCboX1('MTR600' , 13 )
	ElseIf cOpcao == "4"
		aRet := FatGetCboX1('MTR600' , 14 )
	ElseIf cOpcao == "5"
		aRet := FatGetCboX1('MTR600' , 15 )
	ElseIf cOpcao == "6"
		aRet := FatGetCboX1('MTR600' , 16 )
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv005VendSC5
Retorna campos de Vendedores do Pedido de Venda (SC5)

@return Nil

@author FAT/CRM
@since 08/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Static Function FatSv005VendSC5()

	Local nX 	 := 0 as numeric
	Local cSuf 	 := "0" as character
	Local cVend  := "" as character
	Local cWhere := "" as character

	//Realiza condições para vendedores baseado nos parâmetros e quantidade de campos existentes na tabela SC5 para vendedores
	For nX := 1 to nVend

		cSuf := Soma1(cSuf)
		cVend := "C5_VEND"+cSuf

		If SC5->(ColumnPos(cVend)) > 0

			If !Empty(cWhere)
				cWhere += " OR "
			Endif

			cWhere += " ("+cVend+" <> ? and "+cVend+" >= ? and "+cVend+" <= ? ) "

		Endif

	Next
	
	If !Empty(cWhere)
		cWhere := "AND (" + cWhere + ") "
	EndIf

Return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv005CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 23/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV005CreateSQL() Class salesordervscustomerSmartViewBusinessObject

	Local nX 	     := 0   as numeric
	Local nY 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local cFieldsQry := ""  as character
    Local aFiliais	 := {}  as array

	//Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	//Adiciona Campos padroes e localizados
	cFieldsQry += ArrTokStr(self:aFields,",") + self:cLocSelect

    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCpoCustom)

	//-------------------------------------------------------------------
	// Verificar a quantidade de vendedores do Pedido de Venda (SC5)
	// para concatenar no Where da Query
	//-------------------------------------------------------------------
	self:cWhereVend := FatSv005VendSC5()

	//Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

		//---------------------------------------------------------------------------
		// Query Principal
		// BIND - Indice SC6 (G): C6_FILIAL+C6_NUM+C6_CLI+C6_LOJA
		// BIND - Indice SA1 (1): A1_FILIAL+A1_COD+A1_LOJA
		// BIND - Indice SC5 (B): C5_FILIAL+C5_CLIENTE+C5_LOJACLI+C5_EMISSAO+C5_TIPO
		//---------------------------------------------------------------------------
		self:cQuery += " SELECT SC5.C5_FILIAL, SC5.C5_MOEDA, SC5.C5_EMISSAO, "
		self:cQuery += "	? " //Campos padroes e localizados
		self:cQuery += " FROM " 		+ RetSqlName("SC5") + " SC5 "
		self:cQuery += " 	LEFT JOIN " + RetSqlName("SC6") + " SC6 " 
		self:cQuery += " 		ON  SC6.C6_FILIAL  	= SC5.C5_FILIAL "
		self:cQuery += " 		AND SC6.C6_NUM 	   	= SC5.C5_NUM "
		self:cQuery += " 		AND SC6.C6_CLI 	   	= SC5.C5_CLIENTE " 
		self:cQuery += " 		AND SC6.C6_LOJA    	= SC5.C5_LOJACLI "  
		self:cQuery += " 		AND SC6.D_E_L_E_T_ 	= ? "
		self:cQuery += " 	LEFT JOIN "	+ RetSqlName("SA1") + " SA1 "
		self:cQuery += " 		ON SA1.A1_FILIAL 	= ? "   
		self:cQuery += " 		AND SA1.A1_COD 		= SC5.C5_CLIENTE " 
		self:cQuery += " 		AND SA1.A1_LOJA 	= SC5.C5_LOJACLI " 
		self:cQuery += " 		AND SA1.D_E_L_E_T_ 	= ? "
		self:cQuery += " WHERE "
		self:cQuery += " 	SC5.C5_FILIAL = ? "
		self:cQuery += " 	AND SC5.C5_CLIENTE BETWEEN ? AND ? "
		self:cQuery += " 	AND SC5.C5_LOJACLI BETWEEN ? AND ? " 
		self:cQuery += " 	AND SC5.C5_EMISSAO BETWEEN ? AND ? "  
		self:cQuery += " 	AND SC5.C5_TIPO NOT IN (?) " 
		self:cQuery += self:cWhereVend //Vendedores
		self:cQuery += "		AND SC5.D_E_L_E_T_ = ?
		self:cQuery += " 	AND (	SELECT "
		self:cQuery += " 				SUM(C6_VALOR) " 
		self:cQuery += " 			FROM " +RetSqlName("SC6")+ " SC6 "
		self:cQuery += " 				INNER JOIN " +RetSqlName("SF4")+ " SF4 "
		self:cQuery += " 					ON SF4.F4_FILIAL = ? "
		self:cQuery += " 					AND SF4.F4_CODIGO = SC6.C6_TES "

		//--------------------------------------------------------------------------
		// Tratativa localizado (cPaisLoc = BRA)
		// Inclusao de complemento de Filtro do SubSelect da query SC6/SF4
		//---------------------------------------------------------------------------
		If MV_PAR12 == 1 //Considera Agrega Valor ?
			self:ProcessData(2, .F.)
		EndIf

		//TES qto estoque ?
		If MV_PAR13 == 1 .Or. MV_PAR13 == 2
			self:cQuery += " AND SF4.F4_ESTOQUE = ? " 
		EndIf

		//TES qto faturamento ?
		If MV_PAR14 == 1 .Or. MV_PAR14 == 2
			self:cQuery += " AND SF4.F4_DUPLIC = ? "
		EndIf

		//Considera resíduo ?
		If MV_PAR09 == 2
			self:cQuery += " AND SC6.C6_BLQ <> ? "
		Endif

		self:cQuery += " 					AND SF4.D_E_L_E_T_= ? "
		self:cQuery += " 			WHERE  "
		self:cQuery += " 				SC6.C6_FILIAL 		= ? "  
		self:cQuery += " 				AND SC6.C6_NUM 		= SC5.C5_NUM " 
		self:cQuery += " 				AND SC6.D_E_L_E_T_	= ? "
		self:cQuery += " 		) > ? "

		//---------------------------------------------------------------------------
		// Inclusao de complemento do Where da query - Localizado
		// Operacao 3 exclusivo para inclusao de novos filtros localizados
		//---------------------------------------------------------------------------
		self:processData(3, .F.)

		If nNumFils > 1 .And. nX < nNumFils
			self:cQuery += "  UNION ALL "
		EndIf

	Next nX

	//---------------------------------------------------------------------------
	// Acrescenta os dados da Query
	//---------------------------------------------------------------------------
	self:oQuery := FwExecStatement():New(ChangeQuery(self:cQuery))

	For nX := 1 To nNumFils

		self:oQuery:SetUnsafe(self:nParam++, cFieldsQry)		//Campos
		self:oQuery:SetString(self:nParam++, " ")				//DELETE SC6
		self:oQuery:SetString(self:nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))	//Filial SA1
		self:oQuery:SetString(self:nParam++, " ")				//DELETE SA1
		self:oQuery:SetString(self:nParam++, FatSVFilial('SC5', 'C5_FILIAL', aFiliais[nX]))	//Filial SC5
		self:oQuery:SetString(self:nParam++, MV_CLI_DE)			//Cliente De
		self:oQuery:SetString(self:nParam++, MV_CLI_ATE)		//Cliente Ate
		self:oQuery:SetString(self:nParam++, MV_PAR10)			//Loja De
		self:oQuery:SetString(self:nParam++, MV_PAR11)			//Loja Ate
		self:oQuery:SetString(self:nParam++, DTOS(MV_DT_DE))	//Emissao De
		self:oQuery:SetString(self:nParam++, DTOS(MV_DT_ATE))	//Emissao Ate
		self:oQuery:SetIn(self:nParam++, {'D','B'} )			//Tipo Pedido

		For nY := 1 to nVend
			self:oQuery:SetString(self:nParam++, ' ')			//Vendedor
			self:oQuery:SetString(self:nParam++, MV_VEND_DE)	//Vendedor De
			self:oQuery:SetString(self:nParam++, MV_VEND_ATE)	//Vendedor Ate
		Next nY
		
		self:oQuery:SetString(self:nParam++, " ")				//DELETE SC5
		self:oQuery:SetString(self:nParam++, FatSVFilial('SF4', 'F4_FILIAL', aFiliais[nX]))	 //Filial SF4

		//Considera Agrega Valor ?
		If MV_PAR12 == 1
			self:ProcessData(2, .T.) //Inclui conteudo do Where localizado
		EndIf

		//TES qto estoque ?
		If MV_PAR13 == 1              
			self:oQuery:SetString(self:nParam++, "S")
		ElseIf MV_PAR13 == 2
			self:oQuery:SetString(self:nParam++, "N")
		EndIf

		//TES qto faturamento ?         
		If MV_PAR14 == 1
			self:oQuery:SetString(self:nParam++, "S")
		ElseIf MV_PAR14 == 2
			self:oQuery:SetString(self:nParam++, "N")
		EndIf

		//Considera resíduo ?           
		If MV_PAR09 == 2
			self:oQuery:SetString(self:nParam++, "R ")
		Endif

		self:oQuery:SetString(self:nParam++, " ")				//DELETE SF4	
		self:oQuery:SetString(self:nParam++, FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]))	//Filial SC6
		self:oQuery:SetString(self:nParam++, " ") 				//DELETE SC6
		self:oQuery:SetUnsafe(self:nParam++, "0") 				//Valor 0

		//---------------------------------------------------------------------------
		// Inclusao de complemento do Where da query - Localizado
		// Operacao 3 exclusivo para inclusao de novos filtros localizados.
		//---------------------------------------------------------------------------
		self:processData(3, .T.)

	Next nX

    self:cAlias := self:oQuery:OpenAlias()

	FreeObj(self:oQuery)

	aSize(aFiliais, 0)

Return
