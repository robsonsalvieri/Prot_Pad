#INCLUDE "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.billingbycustomer.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.billingbycustomer.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF2,SA1", name="Faturamento por Cliente", country="ALL", customTables='SA1' )

//-------------------------------------------------------------------
/*/{Protheus.doc} billingbycustomerSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relatorio de Faturamento por Cliente

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class billingbycustomerSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method GravaDevR4()  as object
    public method GTrab1R4()    as object

    public method FatSV015CreateSQL()

    protected data aFields           as array
    protected data aStruct           as array
    protected data jItems            as json
    protected data cAlias            as character
    protected data cSelectSF2Loc     as character
    protected data cWhereSF2Loc      as character
    protected data nDecimaisLoc      as numeric
    protected data nVlSD2TotLoc      as numeric
    protected data nVlSD2TotLiqLoc   as numeric
    protected data nVlSD2TotDespLoc  as numeric
    protected data nVlSD2IcmLoc      as numeric
    protected data nVlSD2IpiLoc      as numeric
    protected data nVlSD2ImpIncLoc   as numeric
    protected data nVlSD2ImpNoIncLoc as numeric
    protected data nVlSF2AdiLoc      as numeric
    protected data nVlSD2AdiLoc      as numeric
    protected data nVlSD2ComplIpiLoc as numeric
    protected data nVlSD1TotLiqLoc   as numeric
    protected data nVlSD1IcmLoc      as numeric
    protected data nVlSD1IpiLoc      as numeric
    protected data nVlSD1ImpIncLoc   as numeric
    protected data nVlSD1ImpNoIncLoc as numeric

    protected data lValidSF2Loc      as logical
    protected data lComplIpiSF2Loc   as logical
    protected data lValidSF1Loc      as logical
    protected data lIncDevSF1Loc     as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class billingbycustomerSmartViewBusinessObject

    _Super:new()

    self:appendArea(STR0001)        //"Faturamento"
    self:setDisplayName(STR0002)    //"Faturamento por Cliente"
    self:setDescription(STR0003)    //"Listagem Faturamento por Cliente"
    self:setIsLookUp(.T.)

    self:aFields := {}
    self:aStruct := {}

    self:cAlias             := ""
    self:cSelectSF2Loc      := ""
    self:cWhereSF2Loc       := ""

    self:nDecimaisLoc       := 0
    self:nVlSD2TotLoc       := 0
    self:nVlSD2TotLiqLoc    := 0
    self:nVlSD2TotDespLoc   := 0
    self:nVlSD2IcmLoc       := 0
    self:nVlSD2IpiLoc       := 0
    self:nVlSD2ImpIncLoc    := 0
    self:nVlSD2ImpNoIncLoc  := 0
    self:nVlSF2AdiLoc       := 0
    self:nVlSD2AdiLoc       := 0
    self:nVlSD2ComplIpiLoc  := 0

    self:nVlSD1TotLiqLoc    := 0
    self:nVlSD1IcmLoc       := 0
    self:nVlSD1IpiLoc       := 0
    self:nVlSD1ImpIncLoc    := 0
    self:nVlSD1ImpNoIncLoc  := 0

    self:lValidSF2Loc       := .T.
    self:lComplIpiSF2Loc    := .F.
    self:lValidSF1Loc       := .F.

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class billingbycustomerSmartViewBusinessObject

    Local nZ            := 0 as numeric
    Local nTotal        := 0 as numeric
    Local nTotalLiq     := 0 as numeric
    Local nVlrSImp1     := 0 as numeric
    Local nVlrMerc2     := 0 as numeric
    Local nVlrTot3      := 0 as numeric
    Local nVALICM       := 0 as numeric
    Local nVALIPI	    := 0 as numeric
    Local nImpNoInc	    := 0 as numeric
    Local nImpInc  	    := 0 as numeric
    Local nTotDesp      := 0 as numeric
    Local nAdic 	    := 0 as numeric
    Local nComplIPI	    := 0 as numeric
    Local nIncDev       := 0 as numeric
    Local nEstoq        := 0 as numeric
    Local nDupli        := 0 as numeric
    Local nAbati        := 0 as numeric
    Local nNumRank      := 0 as numeric
    Local nTamCpo       := 0 as numeric
    Local nX            := 0 as numeric
    Local nValDescAdc   := 0 as numeric

    Local ojParams      := Nil as json

    Local aImpostos     := {} as array
    Local aFatCli       := {} as array
    Local aPDFields		:= {} as array

    Local lComplIPI     := .F. as logical
    Local lAvalTes      := .F. as logical
    Local lAbt          := .F. as logical
    Local lObfuscated	:= .F. as logical
    Local lCalcAdc  	:= .F. as logical

    Local dDtEmiCli     := CTOD("//") as date

    Local cDupli        := "" as character
    Local cEstoq        := "" as character
    Local cTitulo       := "" as character
    Local cCliente      := "" as character
    Local cLojaCli      := "" as character
    Local cNumDoc       := "" as character
    Local cSerie        := "" as character
    Local cMoeda        := "" as character
    Local cNomCli       := "" as character
    Local cLista        := "" as character
    Local cTextRank     := "" as character
    Local cChvList      := "" as character
    Local cTextList     := "" as character
    Local cTabela       := "" as character
    Local cPrefix       := "" as character
    Local cCodCli       := "" as character
    Local cLojCli       := "" as character

    Private aCustomFiels := self:getCustomFields() as array

    Private cFilSF2      := "" as character
    Private cCliDe       := "" as character
    Private cCliAte      := "" as character
    Private cEstDe       := "" as character
    Private cEstAte      := "" as character
    Private cDtEmiDe     := "" as character
    Private cDtEmiAte    := "" as character

    Private nDecs        := 0 as numeric
    Private nMoeda       := 0 as numeric
    Private nMoeDev      := 0 as numeric
    Private nDescAdc     := 0 as numeric
    Private nLista       := 0 as numeric
    Private nDeduzImp    := 0 as numeric

    Private nVALOR1      := 0 as numeric
    Private nVALOR2      := 0 as numeric
    Private nVALOR3      := 0 as numeric
    Private nVALOR4      := 0 as numeric
    Private nVALOR5      := 0 as numeric
    Private nVALOR6      := 0 as numeric
    Private nVALOR7      := 0 as numeric
    Private nVALOR8      := 0 as numeric
    Private nVALOR9      := 0 as numeric

    Private aCodCli      := {} as array

    Private MV_FIL	 := "" as character
	Private MV_MOEDA := 0  as numeric

    ojParams := oFilter:GetParameters() //metodo para retorno do json dos parametros
    FatSetValueMVPAR(ojParams,"MTR590")

	//Variaveis utilizadas para os parametros do pergunte
    If ojParams != Nil
        cDtEmiDe    := DTOS(mv_par01)   //Data de?						
        cDtEmiAte   := DTOS(mv_par02)   //Data ate?						
        cCliDe      := mv_par03         //Cliente de?					
        cCliAte     := mv_par04         //Cliente ate?					
        cEstDe      := mv_par05         //Estado de?						
        cEstAte     := mv_par06         //Estado ate?					
        nLista      := mv_par07         //Lista Por?						
        nMoeda      := MV_MOEDA         //Qual a moeda?					
        nIncDev     := mv_par09         //Inclui Devolucao?				
        nDupli      := mv_par10         //TES Qto Faturamento?			
        nEstoq      := mv_par11         //TES Qto Estoque?				
        nAbati      := mv_par12         //Abatimento?					
        nMoeDev     := mv_par13         //Converte Moeda da Devolucao?	
        nDescAdc    := mv_par14         //Desconsidera Adicionais?		
        nDeduzImp   := mv_par15         //Deduz PIS/COFINS e ICMS?		
    EndIf

    FreeObj(ojParams)//Elimina da memória a instância do objeto informado como parâmetro.

    cDupli      := IIf((nDupli == 1),"S",If((nDupli == 2),"N","SN"))
    cEstoq      := IIf((nEstoq == 1),"S",If((nEstoq == 2),"N","SN"))
    nDecs       := msdecimais(nMoeda)
    cMoeda      := GetMv("MV_MOEDA"+Ltrim(STR(IIF(nMoeda == 0 , 1 , nMoeda ))))
    cTextRank   := IIf(nLista<>3,STR0007,"")

    self:nDecimaisLoc   := nDecs

    If nLista == 1
        cLista := STR0008 //"Total do Cliente "
    ElseIf nLista == 2
        cLista := STR0009 //"Total por Ranking "
    ElseIf nLista == 3
        cLista := STR0010 //"Total por Estado de "
    Else
        cLista := STR0032 //"Total por Filial"
    EndIf

    If nLista = 1       //CODIGO
        cTitulo := STR0011+" "+OemToAnsi(STR0004)+" - "+ cMoeda //"Listagem de Faturamento por Cliente (Código) - REAL"
    ElseIf nLista == 2  //RANKING
        cTitulo := STR0011+" "+OemToAnsi(STR0005) +" - "+ cMoeda //"Listagem de Faturamento por Cliente (Ranking) - REAL"
    ElseIf nLista == 3  //ESTADO
        cTitulo := STR0011+" "+OemToAnsi(STR0006) +" - "+ cMoeda //"Listagem de Faturamento por Cliente (Estado) - REAL"
    Else                //FILIAL
        cTitulo := STR0011+" ("+OemToAnsi(STR0030) +") - "+ cMoeda //"Listagem de Faturamento por Cliente (Filial) - REAL"
    EndIf

    aadd( self:aFields , 'A1_NOME' )

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields	:= FatGetfieldsLGPD(self:aFields, aCustomFiels )
    lObfuscated	:= Len(aPDFields) > 0   

    SA1->(DbSetOrder(1))
    SF1->(DbSetOrder(1))
    SD1->(dbSetOrder(1))
    SF2->(DbSetOrder(1))
    SD2->(DbSetOrder(3))
    SF4->(DbSetOrder(1))

    //Irá montar as queries do objeto de negócio
	self:FatSV015CreateSQL()

    (self:cAlias)->(dbGotop())

    While !(self:cAlias)->(Eof())

        //Inicia a propriedade que valida se NF pode ser considerada ou não na listagem localizado
        self:lValidSF2Loc := .T.

        cFilSF2 := (self:cAlias)->F2_FILIAL

        If SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', cFilSF2)+(self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA))

            cCodCli   := SA1->A1_COD
            cLojCli   := SA1->A1_LOJA

            //Controle LGPD
            If lObfuscated
                cNomCli := FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_NOME)
            Else
                cNomCli := IIF(Empty(AllTrim(StrTran(SA1->A1_NOME,"'","")))	, " ", AllTrim(StrTran(SA1->A1_NOME,"'","")))
            EndIf

            self:processData(2)

            If self:lValidSF2Loc
                If SD2->(MsSeek(FatSVFilial('SD2', 'D2_FILIAL', cFilSF2)+(self:cAlias)->F2_DOC+(self:cAlias)->F2_SERIE))

                    lAvalTes    := .F.
                    nTotal 		:= 0
                    nTotalLiq	:= 0
                    nVALICM		:= 0
                    nVALIPI		:= 0
                    nImpNoInc	:= 0
                    nImpInc  	:= 0
                    nTotDesp    := 0
                    nAdic 		:= 0
                    nComplIPI	:= 0

                    //Inicia a propriedade do valor adicional para somar no valor total localizado
                    self:nVlSF2AdiLoc := 0
                    self:lComplIpiSF2Loc := .F.

                    //Tipo 3 realiza tratamento do valor total adicional(self:nVlSF2AdiLoc), no objeto localizado 
                    self:processData(3)
                    nAdic += self:nVlSF2AdiLoc

                    While SD2->(!Eof()) .And. cFilSF2 == SD2->D2_FILIAL .And. SD2->D2_DOC+SD2->D2_SERIE == (self:cAlias)->F2_DOC+(self:cAlias)->F2_SERIE

                        //Inicia propriedades para compor valores dos itens com cáculos localizados
                        self:nVlSD2TotLoc       := 0
                        self:nVlSD2TotLiqLoc    := 0
                        self:nVlSD2TotDespLoc   := 0
                        self:nVlSD2IcmLoc       := 0
                        self:nVlSD2IpiLoc       := 0
                        self:nVlSD2ImpIncLoc    := 0
                        self:nVlSD2ImpNoIncLoc  := 0
                        self:nVlSD2AdiLoc       := 0
                        self:nVlSD2ComplIpiLoc  := 0

                        SF4->(MsSeek(FatSVFilial('SF4', 'F4_FILIAL', cFilSF2)+SD2->D2_TES))
                        If AvalTes(SD2->D2_TES,cEstoq,cDupli)
                            lAvalTes := .T.

                            lComplIPI   := self:lComplIpiSF2Loc

                            //Tipo 4 realiza tratamento dos valores dos itens com cáculos localizados
                            self:processData(4)

                            nTotal  += IIF(self:nVlSD2TotLoc == 0, xMoeda(SD2->D2_TOTAL,(self:cAlias)->F2_MOEDA,nMoeda,(self:cAlias)->F2_EMISSAO,nDecs+1,(self:cAlias)->F2_TXMOEDA), self:nVlSD2TotLoc)
                
                            nAdic       += self:nVlSD2AdiLoc
                            nTotDesp    += self:nVlSD2TotDespLoc
                            nTotalLiq   += self:nVlSD2TotLiqLoc
                            nComplIPI   += self:nVlSD2ComplIpiLoc
                            nVALICM     += self:nVlSD2IcmLoc
                            nVALIPI     += self:nVlSD2IpiLoc
                            nImpInc     += self:nVlSD2ImpIncLoc
                            nImpNoInc   += self:nVlSD2ImpNoIncLoc
                        Endif

                        SD2->(dbSkip())
                    EndDo

                    If lAvalTes
                        nTotal += nAdic
                    Endif

                    If nTotal > 0

                        If cCliente <> (self:cAlias)->F2_CLIENTE .And. cLojaCli <> (self:cAlias)->F2_LOJA
                            cCliente := (self:cAlias)->F2_CLIENTE  
                            cLojaCli := (self:cAlias)->F2_LOJA     
                            cNumDoc := (self:cAlias)->F2_DOC      
                            cSerie := (self:cAlias)->F2_SERIE    
                        EndIf

                        cEstCli := SA1->A1_EST          
                        dDtEmiCli := (self:cAlias)->F2_EMISSAO  

                        //Busca Valores de Despesas Adicionais (mv_par14)
                        nValDescAdc := IIF(lAvalTes .And. nDescAdc == 2, 0, nAdic)
                        nVlrMerc2 := IIF((self:cAlias)->F2_TIPO == "P", 0, (nTotal-nValDescAdc-nTotDesp))

                        //Calcula Despesas Adicionais?
                        lCalcAdc := !lComplIPI .And. (nImpInc > 0 .Or. nImpNoInc > 0)

                        nVlrSImp1 := IIF(lCalcAdc,;
                                        nTotal-nImpNoInc,;
                                        IIF(nTotalLiq <> 0,;
                                            IIF(!lComplIPI, nTotalLiq-nVALICM, nTotalLiq-nVALICM-nComplIPI),;
                                            IIF(!lComplIPI, nTotal-nVALICM-nAdic-nTotDesp, nTotal-nVALICM-nAdic-nTotDesp-nComplIPI)))

                        nVlrTot3 := IIF(lCalcAdc,;
                                        IIF((self:cAlias)->F2_TIPO == "P",0,nTotal) + nImpInc + xMoeda((self:cAlias)->F2_FRETAUT,(self:cAlias)->F2_MOEDA,nMoeda,(self:cAlias)->F2_EMISSAO,nDecs+1,(self:cAlias)->F2_TXMOEDA),;
                                        IIF((self:cAlias)->F2_TIPO == "P",nComplIPI,nTotal-nTotDesp) + nVALIPI + IIF((self:cAlias)->F2_TIPO=="I" .And. (self:cAlias)->F2_ICMSRET > 0,xMoeda((self:cAlias)->F2_FRETAUT,1,nMoeda,(self:cAlias)->F2_EMISSAO),xMoeda((self:cAlias)->F2_ICMSRET+(self:cAlias)->F2_FRETAUT,1,nMoeda,(self:cAlias)->F2_EMISSAO)))

                        nVALOR1 := nVALOR1 + nVlrSImp1  //Replace nVALOR1  With nVALOR1 + nVlrSImp1
                        nVALOR2 := nVALOR2 + nVlrMerc2  //Replace nVALOR2  With nVALOR2 + nVlrMerc2
                        nVALOR3 := nVALOR3 + nVlrTot3   //Replace nVALOR3  With nVALOR3 + nVlrTot3

                        // Sergio Fuzinaka - 16.10.01
                        If Ascan( aCodCli, (self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA ) == 0
                            Aadd( aCodCli, (self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA )
                        Endif

                        //Grava Devolucao ref a Nota Fiscal posicionada
                        If nIncDev == 3
                            self:GravaDevR4((self:cAlias)->F2_DOC,(self:cAlias)->F2_SERIE,(self:cAlias)->F2_CLIENTE,(self:cAlias)->F2_LOJA,nMoeda,cEstoq,cDupli,StoD((self:cAlias)->F2_EMISSAO), cFilSF2)
                        Endif
                    Endif
                EndIf
            EndIf

        EndIf

        (self:cAlias)->(DBSkip())

        If nVALOR1+nVALOR2+nVALOR3 > 0 .And. cCodCli+cLojCli <> (self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA
        
            nVALOR4 := IIF(nVALOR4 > 0, nVALOR4*(-1), 0)
            nVALOR5 := IIF(nVALOR5 > 0, nVALOR5*(-1), 0)
            nVALOR6 := IIF(nVALOR6 > 0, nVALOR6*(-1), 0)

            nVALOR7 := nVALOR1+nVALOR4
            nVALOR8 := nVALOR2+nVALOR5
            nVALOR9 := nVALOR3+nVALOR6

            //Armazena os totais de faturamento por cliente no array
            Aadd(aFatCli,{  cFilSF2,;
                            cCodCli,;
                            cLojCli,;
                            cNomCli,;
                            nVALOR1,;
                            nVALOR2,;
                            nVALOR3,;
                            nVALOR4,;
                            nVALOR5,;
                            nVALOR6,;
                            nVALOR7,;
                            nVALOR8,;
                            nVALOR9,;
                            nNumRank,;
                            cEstCli})

            //Limpa as variaveis de totais
            nVALOR1 := nVALOR2 := nVALOR3 := nVALOR4 := nVALOR5 := nVALOR6 := nVALOR7 := nVALOR8 := nVALOR9 := nTotal := 0
        EndIf
    EndDo

    If !Empty(aFatCli)
        ASORT(aFatCli, , , { | x,y | x[5] > y[5] } )

        For nZ := 1 To Len(aFatCli)
            aFatCli[nZ][14] := nZ
        Next nZ

        If nLista == 1
            ASORT(aFatCli, , , { | x,y | x[2] < y[2] } )
        ElseIf nLista == 2
            ASORT(aFatCli, , , { | x,y | x[14] < y[14] } )
        ElseIf nLista == 3
            ASORT(aFatCli, , , { | x,y | x[15] < y[15] } )
        ElseIf nLista == 4
            ASORT(aFatCli, , , { | x,y | x[1] < y[1] } )
        EndIf

        nTamCpo := Len(Alltrim(Str(aFatCli[Len(aFatCli)][14])))

        For nZ := 1 To Len(aFatCli)
            If nIncDev == 1
                self:GTrab1R4(cEstoq,cDupli,nMoeda,aFatCli[nZ][2],aFatCli[nZ][3],aFatCli[nZ][1])

                aFatCli[nZ][8]  := IIF(nVALOR4 > 0, nVALOR4*(-1), 0)
                aFatCli[nZ][9]  := IIF(nVALOR5 > 0, nVALOR5*(-1), 0)
                aFatCli[nZ][10] := IIF(nVALOR6 > 0, nVALOR6*(-1), 0)

                aFatCli[nZ][11] := aFatCli[nZ][5] + aFatCli[nZ][8]
                aFatCli[nZ][12] := aFatCli[nZ][6] + aFatCli[nZ][9]
                aFatCli[nZ][13] := aFatCli[nZ][7] + aFatCli[nZ][10]

                //Limpa as variaveis de totais de Devolucao
                nVALOR4 := nVALOR5 := nVALOR6 := 0
            EndIf

            lAbt := (nLista == 1 .And. nAbati == 1 .And. (aFatCli[nZ][8]+aFatCli[nZ][9]+aFatCli[nZ][10])<0)

            If nLista == 1 .Or. nLista == 2
                cTextList := aFatCli[nZ][2]+aFatCli[nZ][3]+" "+IIf(lAbt,"(ABT)"," ")

                If nLista == 1
                    cChvList := aFatCli[nZ][2]+aFatCli[nZ][3]
                Else
                    cChvList := StrZero(aFatCli[nZ][14],nTamCpo)
                EndIf
            ElseIf nLista == 3
                cTextList := aFatCli[nZ][15]
                cChvList  := aFatCli[nZ][15]
            ElseIf nLista == 4
                cTextList := aFatCli[nZ][1]
                cChvList  := aFatCli[nZ][1]
            EndIf

            self:jItems := JsonObject():new()
            self:jItems["FILIAL"]       := aFatCli[nZ][1]
			self:jItems["cCLIENTE"]     := aFatCli[nZ][2]
			self:jItems["cLOJACLI"]     := aFatCli[nZ][3]
			self:jItems["cNOMCLI"]      := aFatCli[nZ][4]
			self:jItems["nVALOR1"]      := aFatCli[nZ][5]
			self:jItems["nVALOR2"]      := aFatCli[nZ][6]
			self:jItems["nVALOR3"]      := aFatCli[nZ][7]
			self:jItems["nVALOR4"]      := aFatCli[nZ][8]
			self:jItems["nVALOR5"]      := aFatCli[nZ][9]
			self:jItems["nVALOR6"]      := aFatCli[nZ][10]
			self:jItems["nVALOR7"]      := aFatCli[nZ][11]
			self:jItems["nVALOR8"]      := aFatCli[nZ][12]
			self:jItems["nVALOR9"]      := aFatCli[nZ][13]
			self:jItems["cTITULO"]      := cTitulo
			self:jItems["cLISTA"]       := cLista+cTextList
			self:jItems["cTEXTRANK"]    := cTextRank
			self:jItems["nNUMRANK"]     := aFatCli[nZ][14]
			self:jItems["cESTCLI"]      := aFatCli[nZ][15]
			self:jItems["cCHVLIST"]     := cChvList
            
            SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', cFilSF2)+aFatCli[nZ][2]+aFatCli[nZ][3]))
            //Estrutura para impressão apenas dos campos Custom
            For nX := 1 To Len(self:aStruct)
                
                cPrefix := SubStr( self:aStruct[nX][5], 1, 2 )
                cTabela := IIF(cPrefix $ 'A1', 'S'+cPrefix , self:cAlias )                                     

                self:jItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, cTabela)

            Next nX

            self:processData(1)
            self:oData:appendData(self:jItems)
        Next nZ
    EndIf

    //limpa todos os elementos do Array
    ASIZE(aFatCli,0)
    ASIZE(aCodCli,0)
    ASIZE(aImpostos,0)
    ASIZE(aPDFields,0)
    ASIZE(aCustomFiels,0)

    (self:cAlias)->(DbCloseArea())

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class billingbycustomerSmartViewBusinessObject

    Local nX          := 0  as numeric
    Local aParameters := {} as array

	self:oSchema:addProperty("FILIAL",      STR0030, "string", STR0030, "FILIAL")   //Codigo Filial
    self:oSchema:addProperty("cCLIENTE",    STR0012, "string", STR0012, "cCLIENTE") //Codigo Cliente
	self:oSchema:addProperty("cLOJACLI",    STR0013, "string", STR0013, "cLOJACLI") //Codigo Loja Cliente
	self:oSchema:addProperty("cNOMCLI",     STR0014, "string", STR0014, "cNOMCLI")  //Nome do Cliente
	self:oSchema:addProperty("nVALOR1",     STR0015, "number", STR0015, "nVALOR1")  //Vl.S/Imposto
	self:oSchema:addProperty("nVALOR2",     STR0016, "number", STR0016, "nVALOR2")  //Vl.Mercador.
	self:oSchema:addProperty("nVALOR3",     STR0017, "number", STR0017, "nVALOR3")  //Valor Total
	self:oSchema:addProperty("nVALOR4",     STR0018, "number", STR0018, "nVALOR4")  //Vl. S/ Imposto (DEV)
	self:oSchema:addProperty("nVALOR5",     STR0019, "number", STR0019, "nVALOR5")  //Vl.Mercador. (DEV)
	self:oSchema:addProperty("nVALOR6",     STR0020, "number", STR0020, "nVALOR6")  //Valor Total (DEV)
	self:oSchema:addProperty("nVALOR7",     STR0021, "number", STR0021, "nVALOR7")  //Total 1
	self:oSchema:addProperty("nVALOR8",     STR0022, "number", STR0022, "nVALOR8")  //Total 2
	self:oSchema:addProperty("nVALOR9",     STR0023, "number", STR0023, "nVALOR9")  //Total 3
	self:oSchema:addProperty("cTITULO",     STR0024, "string", STR0024, "cTITULO")  //Titulo do relatorio
	self:oSchema:addProperty("cLISTA",      STR0025, "string", STR0025, "cLISTA")   //Nome da Lista
	self:oSchema:addProperty("cTEXTRANK",   STR0026, "string", STR0026, "cTEXTRANK")//Texto Ranking
	self:oSchema:addProperty("nNUMRANK",    STR0027, "number", STR0027, "nNUMRANK") //Numero Ranking
	self:oSchema:addProperty("cESTCLI",     STR0028, "string", STR0028, "cESTCLI")  //Estado Cliente
	self:oSchema:addProperty("cCHVLIST",    STR0029, "string", STR0029, "cCHVLIST") //Chave de listagem

    // Busca no SX1 os parâmetros que serão utilizados reordenando e não passa o parametros 08
	aParameters := FtSVSetParam('MTR590',,,,{'07','01','02','03','04','05','06','09','10','11','12','13','14','15'})

    // Adiciona os parâmetros do pergunte no Schema
	self:oSchema:addParameter("MV_FIL", STR0030 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) //Filial

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

    self:oSchema:addParameter("MV_MOEDA", STR0031 + " ?", "number", .F.,,,,,, FTSVHelp("MTR590","08")) 	//Moeda

    // Validação para versão da Lib
    If __lLookUp

        self:setCustomURL("MV_FIL",	  "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SA1", 2) //Cliente de
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SA1", 2) //Cliente ate
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/12",  2) //Estado de
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/12",  2) //Estado ate

        self:setCustomURL("MV_PAR07", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/1", 1) //Lista Por
        self:setCustomURL("MV_PAR09", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/2", 1) //Inclui Devolução
        self:setCustomURL("MV_PAR10", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/3", 1) //TES Qto Faturamento
        self:setCustomURL("MV_PAR11", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/4", 1) //TES Qto Estoque
        self:setCustomURL("MV_PAR12", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/5", 1) //Abatimento
        self:setCustomURL("MV_PAR13", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/6", 1) //Converte Moeda da Devolucäo
        self:setCustomURL("MV_PAR14", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/7", 1) //Desconsidera Adicionais
        self:setCustomURL("MV_PAR15", "api/faturamento/smartview/v1/options/fat/billingbycustomer/SVCBFATSV015/8", 1) //Deduz PIS/COFINS e ICMS

	EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVCBFATSV015
Retorna as opçoes disponível para o parâmetro tipo combobox.

@return array: Array com as opçoes.

@author FAT/CRM
@since 25/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function SVCBFATSV015(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"
		aRet := FatGetCboX1('MTR590', 7)
        //Adiciona a opção de ordenação por filial
        aAdd(aRet, STR0030)
	ElseIf cOpcao == "2"
		aRet := FatGetCboX1('MTR590', 9)
	ElseIf cOpcao == "3"
		aRet := FatGetCboX1('MTR590', 10)
	ElseIf cOpcao == "4"
		aRet := FatGetCboX1('MTR590', 11)
	ElseIf cOpcao == "5"
		aRet := FatGetCboX1('MTR590', 12)
	ElseIf cOpcao == "6"
		aRet := FatGetCboX1('MTR590', 13)
	ElseIf cOpcao == "7"
		aRet := FatGetCboX1('MTR590', 14)
	ElseIf cOpcao == "8"
		aRet := FatGetCboX1('MTR590', 15)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} GravaDevR4
Grava item da devolucao ref a nota fiscal posicionada.

@return

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 
//Static Function GravaDevR4(cNumOri as character, cSerieOri as character, cClienteOri as character, cLojaOri as character, nMoeda2 as numeric, cEstoq2 as character, cDupli2 as character, dDtMoeDev as date)
method GravaDevR4(cNumOri as character, cSerieOri as character, cClienteOri as character, cLojaOri as character, nMoeda2 as numeric, cEstoq2 as character, cDupli2 as character, dDtMoeDev as date, cFilSD1 as character) as object class billingbycustomerSmartViewBusinessObject

    Local cNum        := "" as character
    Local cSerie      := "" as character
    Local cFornece    := "" as character
    Local cLoja       := "" as character
    Local cEstCli     := "" as character
    Local cNumDocNf   := "" as character

    Local nX          := 0  as numeric
    Local nTotal      := 0  as numeric
    Local nVALICM     := 0  as numeric
    Local nVALIPI     := 0  as numeric
    Local nImpNoInc   := 0  as numeric
    Local nImpInc     := 0  as numeric
    Local nVlrDev4    := 0  as numeric
    Local nVlrDev5    := 0  as numeric
    Local nVlrDev6    := 0  as numeric
    Local nAdic       := 0  as numeric
    Local nRecSD1     := 0  as numeric

    Local aImpostos   := {} as array
    Local aNotDev     := {} as array
    Local aArea       := GetArea() as array
    Local aAreaSD1    := SD1->(GetArea()) as array

    Local lAvalTes    := .F. as logical

    Local dDtEmiCli   := CTOD("//") as date

    dbSelectArea("SD1")
    SD1->(dbSetOrder(19))//D1_FILIAL, D1_NFORI, D1_SERIORI, D1_FORNECE, D1_LOJA
    If SD1->(MsSeek(cFilSD1+cNumOri+cSerieOri+cClienteOri+cLojaOri))
        nRecSD1 := SD1->(Recno())

        cNumDocNf := SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA
        Aadd( aNotDev, cNumDocNf )

        While !SD1->(Eof()) .And. cFilSD1 == SD1->D1_FILIAL .And.;
                cSerieOri+cNumOri+cClienteOri+cLojaOri == SD1->D1_SERIORI+SD1->D1_NFORI+SD1->D1_FORNECE+SD1->D1_LOJA

            If cNumDocNf <> SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA
                If Ascan( aCodCli, SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA) == 0
                    Aadd( aNotDev, SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA)
                    cNumDocNf := SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA
                Endif
            Endif
            SD1->(dbSkip())
        Enddo

        SD1->(dbGoTo(nRecSD1))

        SD1->(DbSetOrder(1))

        dbSelectArea("SF1")
        For nX :=1 to Len(aNotDev)

            cNum	    := SD1->D1_DOC
            cSerie	    := SD1->D1_SERIE
            cFornece    := SD1->D1_FORNECE
            cLoja		:= SD1->D1_LOJA

            SA1->(MsSeek(FWxFilial('SA1', cFilSD1) + cFornece + cLoja))

            If SF1->(MsSeek(aNotDev[nX]))

                self:lValidSF1Loc := .F.

                If SF1->F1_DTDIGIT < STOD(cDtEmiDe) .Or. SF1->F1_DTDIGIT > STOD(cDtEmiAte) .Or.;
                    SA1->A1_EST < cEstDe .Or. SA1->A1_EST > cEstAte .Or. SF1->F1_TIPO != "D"
                    Loop
                EndIf

                //Tipo 5 valida se esse registro da SF1 pode ser considerada na listagem
                self:processData(5)
                If !self:lValidSF1Loc

                    nTotal 		:= 0
                    nVALICM		:= 0
                    nVALIPI		:= 0
                    nImpNoInc	:= 0
                    nImpInc 	:= 0
                    lAvalTes    := .F.

                    dDtMoeDev  := IIF(nMoeDev == 1,SF1->F1_DTDIGIT,dDtMoeDev)

                    SD1->(MsSeek(aNotDev[nX]))
                    While !SD1->(Eof()) .and. cFilSD1 == SD1->D1_FILIAL .And. SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA == SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA .And. SD1->D1_TIPO == "D"

                        SF4->(MsSeek(FWxFilial('SF4', cFilSD1)+SD1->D1_TES))
                        If AvalTes(SD1->D1_TES,cEstoq2,cDupli2) .and. cSerieOri+cNumOri == SD1->D1_SERIORI+SD1->D1_NFORI
                            lAvalTes := .T.

                            self:nVlSD1IcmLoc       := 0
                            self:nVlSD1IpiLoc       := 0
                            self:nVlSD1ImpIncLoc    := 0
                            self:nVlSD1ImpNoIncLoc  := 0

                            nTotal += xMoeda((SD1->D1_TOTAL-SD1->D1_VALDESC),SF1->F1_MOEDA,nMoeda2,dDtMoeDev,nDecs+1,SF1->F1_TXMOEDA)

                            //Tipo 6 realiza tratamento dos valores dos itens com cáculos localizados
                            self:processData(6)
                            nVALICM     += self:nVlSD1IcmLoc
                            nVALIPI     += self:nVlSD1IpiLoc
                            nImpInc     += self:nVlSD1ImpIncLoc
                            nImpNoInc   += self:nVlSD1ImpNoIncLoc
                        Endif

                        SD1->(dbSkip())
                    EndDo

                    nAdic := 0

                    If lAvalTes 
                        nAdic := xMoeda(SF1->F1_FRETE+SF1->F1_SEGURO+SF1->F1_DESPESA,SF1->F1_MOEDA,nMoeda2,dDtMoeDev,nDecs+1,SF1->F1_TXMOEDA)
                        nTotal += nAdic
                    Endif

                    If nTotal > 0
                        cEstCli    := SA1->A1_EST 
                        dDtEmiCli  := dDtMoeDev   

                        If lAvalTes .And. nDescAdc == 2			
                            nVlrDev5 := nTotal
                        Else
                            nVlrDev5 := nTotal-nAdic
                        EndIf

                        nVlrDev4 := IIF(nImpNoInc <> 0 .Or. nImpInc <> 0, nTotal-nImpNoInc, nTotal-nVALICM-nAdic)
                        nVlrDev6 := IIF(nImpNoInc <> 0 .Or. nImpInc <> 0, nTotal+nImpInc, nTotal+nVALIPI+xMoeda(SF1->F1_ICMSRET,1,nMoeda2,dDtMoeDev))

                        nVALOR4 := nVALOR4+nVlrDev4
                        nVALOR5 := nVALOR5+nVlrDev5
                        nVALOR6 := nVALOR6+nVlrDev6
                    EndIf

                EndIf
            Endif
        Next nX
    EndIf

    RestArea(aAreaSD1)
    RestArea(aArea)

    aImpostos   := {}
    aNotDev     := {}
    aAreaSD1    := {}

    ASIZE(aImpostos,0)
    ASIZE(aNotDev,0)
    ASIZE(aAreaSD1,0)
    ASIZE(aArea,0)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} ³GTrab1R4
Gera arquivo de Trabalho para emissao de Estat.de Fatur.

@return .T.

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method GTrab1R4(cEstoq as character, cDupli as character, nMoeda as numeric, cCodCli as character, cCodLoj as character, cFilSD1 as character) as object class billingbycustomerSmartViewBusinessObject

	Local nTOTAL     := 0 as numeric
	Local nVALICM    := 0 as numeric
	Local nVALIPI    := 0 as numeric
	Local nImpNoInc  := 0 as numeric
	Local nImpInc    := 0 as numeric
	Local nVlrDev4   := 0 as numeric
	Local nVlrDev5   := 0 as numeric
	Local nVlrDev6   := 0 as numeric
	Local nAdic      := 0 as numeric

	Local aImpostos	 := {} as Array
    Local aArea      := GetArea() as array
    Local aAreaSF1   := SF1->(GetArea()) as array

	Local lAvalTes   := .F. as logical
	Local lDescInc   :=	.F. as logical

	Local dDtMoeDev := CTOD("//") as date

    //Tipo 7 valida se incluí desconto no cálculo ou não localizados
    self:lIncDevSF1Loc := .F.
    self:processData(7)
    lDescInc := self:lIncDevSF1Loc

	SF1->(dbSetOrder(2))//F1_FILIAL, F1_FORNECE, F1_LOJA, F1_DOC
	If SF1->(MsSeek(FWxFilial('SF1', cFilSD1)+cCodCli+cCodLoj))

		dDtMoeDev := SF1->F1_DTDIGIT

		While !SF1->(Eof()) .And. cFilSD1 == SF1->F1_FILIAL .And. SF1->F1_FORNECE == cCodCli
            self:lValidSF1Loc := .F.

			SA1->(MsSeek(FWxFilial('SA1', cFilSD1) + SF1->F1_FORNECE + SF1->F1_LOJA))

            //Tipo 8 valida se esse registro da SF1 pode ser considerada na listagem
            self:processData(8)

            If !self:lValidSF1Loc

                If !(SF1->F1_DTDIGIT < STOD(cDtEmiDe) .Or. SF1->F1_DTDIGIT > STOD(cDtEmiAte) .Or. SA1->A1_EST < cEstDe .Or. SA1->A1_EST > cEstAte .Or. SF1->F1_TIPO != "D")

                    If SD1->(MsSeek(cFilSD1+SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA))
                        nTOTAL 		:= 0.00
                        nTOTALLIQ	:= 0.00
                        nVALICM		:= 0.00
                        nVALIPI		:= 0.00
                        nImpNoInc	:= 0.00
                        nImpInc 	:= 0.00
                        lAvalTes    := .F.

                        While !SD1->(Eof()) .and. cFilSD1==SD1->D1_FILIAL .And. SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA ==SF1->F1_DOC+SF1->F1_SERIE+SF1->F1_FORNECE+SF1->F1_LOJA .And. SD1->D1_TIPO == "D"

                            SF4->(MsSeek(FWxFilial('SF4', cFilSD1)+SD1->D1_TES))
                            If AvalTes(SD1->D1_TES,cEstoq,cDupli)

                                //Verifica se existe a N.F original de Saida , se existir usa a data de emissao da NF de saida
                                //conforme o parametro MV_PAR13 se não encontrar usa o campo F1_DTDIGIT para converter a moeda da devolucao
                                dDtMoeDev := IIF(nMoeDev == 2,;
                                                IIF(SF2->(MsSeek(FWxFilial('SF2', cFilSD1)+SD1->D1_NFORI+SD1->D1_SERIORI+SD1->D1_FORNECE+SD1->D1_LOJA)), SF2->F2_EMISSAO, SF1->F1_DTDIGIT),;
                                                SF1->F1_DTDIGIT)

                                self:nVlSD1TotLiqLoc    := 0
                                self:nVlSD1IcmLoc       := 0
                                self:nVlSD1IpiLoc       := 0
                                self:nVlSD1ImpIncLoc    := 0
                                self:nVlSD1ImpNoIncLoc  := 0

                                lAvalTes := .T.
                                nTOTAL  +=xMoeda((SD1->D1_TOTAL-Iif(lDescInc,0,SD1->D1_VALDESC)),SF1->F1_MOEDA,nMoeda,dDtMoeDev,nDecs+1,SF1->F1_TXMOEDA)

                                //Tipo 9 realiza tratamento dos valores dos itens com cáculos localizados
                                self:processData(9)
                                nTOTALLIQ   += self:nVlSD1TotLiqLoc
                                nVALICM     += self:nVlSD1IcmLoc
                                nVALIPI     += self:nVlSD1IpiLoc
                                nImpInc     += self:nVlSD1ImpIncLoc
                                nImpNoInc   += self:nVlSD1ImpNoIncLoc
                            Endif

                            SD1->(dbSkip())
                        EndDo

                        nAdic := 0
                        If lAvalTes
                            nAdic := xMoeda(SF1->F1_FRETE+SF1->F1_SEGURO+SF1->F1_DESPESA,SF1->F1_MOEDA,nMoeda,dDtMoeDev)
                            nTOTAL += nAdic
                        Endif

                        If nTOTAL > 0
                            cEstCli    := SA1->A1_EST
                            dDtEmiCli  := SF1->F1_EMISSAO

                            nVlrDev5 := IIF(lAvalTes .And. nDescAdc == 2, nTotal, nTotal-nAdic)
                            nVlrDev4 := IIF(nImpNoInc <> 0 .Or. nImpInc <> 0, nTotal-nImpNoInc, IIF(nTOTALLIQ <> 0, nTOTALLIQ-nVALICM, nTotal-nVALICM-nAdic))
                            nVlrDev6 := IIF(nImpNoInc <> 0 .Or. nImpInc <> 0, nTotal+nImpInc, nTotal+nVALIPI+xMoeda(SF1->F1_ICMSRET,1,nMoeda,dDtMoeDev))

                            nVALOR4 := nVALOR4+nVlrDev4
                            nVALOR5 := nVALOR5+nVlrDev5
                            nVALOR6 := nVALOR6+nVlrDev6
                        Endif
                    EndIf

                EndIf

            EndIf

			SF1->(dbSkip())

		Enddo

	EndIf

    RestArea(aAreaSF1)
    RestArea(aArea)

    aImpostos   := {}
    aAreaSF1    := {}
    aArea       := {}

    ASIZE(aImpostos,0)
    ASIZE(aAreaSF1,0)
    ASIZE(aArea,0)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv015CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 28/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV015CreateSQL() class billingbycustomerSmartViewBusinessObject

    Local nX 	     := 0           as numeric
    Local nNumFils   := 0           as numeric
    Local nParam     := 1           as numeric
	Local cQuery     := ""          as character
    Local cFieldsQry := ""          as character
    Local oQuery 	 := Nil         as object
    Local aFiliais	 := {}          as array
    Local aF2Tipo    := {'B','D'}   as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := "F2_FILIAL,F2_CLIENTE,F2_LOJA,F2_DOC,F2_SERIE,F2_EMISSAO,F2_TIPO,F2_TIPODOC, "	
    cFieldsQry += "F2_MOEDA,F2_TXMOEDA,F2_FRETAUT,F2_ICMSRET,F2_SEGURO,F2_FRETE,F2_DESPESA,F2_SDOC"	
    cFieldsQry += self:cSelectSF2Loc

    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCustomFiels , .T., .F., .T.,'A1_|')

    For nX := 1 To nNumFils

        cQuery += " SELECT ? "
        cQuery += " FROM " + RetSqlName("SF2") + " SF2 "
        cQuery += " WHERE SF2.F2_FILIAL = ? "

        If !Empty(cDtEmiDe)
            cQuery += "AND SF2.F2_EMISSAO >= ? "
        EndIf

        If !Empty(cDtEmiAte)
            cQuery += "AND SF2.F2_EMISSAO <= ? "
        EndIf

        If !Empty(cCliDe)
            cQuery += "AND SF2.F2_CLIENTE >= ? "
        EndIf

        If !Empty(cCliAte)
            cQuery += "AND SF2.F2_CLIENTE <= ? "
        EndIf

        If !Empty(cEstDe)
            cQuery += "AND SF2.F2_EST >= ? "
        EndIf

        If !Empty(cEstAte)
            cQuery += "AND SF2.F2_EST <= ? "
        EndIf

        cQuery += " AND SF2.F2_TIPO NOT IN (?) "
        cQuery += " ? "     //Where para SF2 que foi populado no objeto localizado
        cQuery += "AND SF2.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
            cQuery += " UNION ALL "
        Else
            cQuery += " ORDER BY SF2.F2_FILIAL, SF2.F2_CLIENTE, SF2.F2_LOJA"
        EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++ , cFieldsQry)
        oQuery:SetString(nParam++ , FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]) )

        If !Empty(cDtEmiDe)
            oQuery:SetString(nParam++ , cDtEmiDe )
        EndIf

        If !Empty(cDtEmiAte)
            oQuery:SetString(nParam++ , cDtEmiAte )
        EndIf

        If !Empty(cCliDe)
            oQuery:SetString(nParam++ , cCliDe )
        EndIf

        If !Empty(cCliAte)
            oQuery:SetString(nParam++ , cCliAte )
        EndIf

        If !Empty(cEstDe)
            oQuery:SetString(nParam++ , cEstDe )
        EndIf

        If !Empty(cEstAte)
            oQuery:SetString(nParam++ , cEstAte )
        EndIf   

        oQuery:SetIn(nParam++ , aF2Tipo )
        oQuery:SetUnsafe(nParam++ , self:cWhereSF2Loc)
        oQuery:SetString(nParam++ , " " )

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais,0)

Return
