#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.realinvoicing.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.realinvoicing.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF1,SD1,SF2,SD2,SA1,SB1,SBM,SF4", name="Faturamento Real", country="ALL", customTables="SF1,SD1,SF2,SD2,SA1,SB1,SBM")

//-------------------------------------------------------------------
/*/{Protheus.doc} realinvoicingSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Faturamento 

@author FAT/CRM
@since 31/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class realinvoicingSmartViewBusinessObject From IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object

	public method FatSV013CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data ojItems		as json
    protected data cLocSelect1	as character
    protected data cLocSelect2	as character
    protected data cLocWhere1	as character
    protected data cLocWhere2	as character
    protected data cAliasSD1	as character
	protected data cAliasSD2	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 31/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class realinvoicingSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 //"Faturamento"
    self:setDisplayName(STR0002) //"Faturamento Real"
    self:setDescription(STR0003) //"Relação de Faturamento Real"
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual da relação
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 31/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class realinvoicingSmartViewBusinessObject

	Local cCpoStruct		:= ""	as character
	Local cDescSaida		:= ""	as character
	Local cDescEntra		:= ""	as character
	Local nX				:= 0	as numeric
	Local nScan				:= 0	as numeric
	Local aCposLGPD 		:= {}	as array
	Local aPDFields			:= {}	as array
	Local aStruFake			:= {}	as array
	Local lObfuscated		:= .F.	as logical
	Local xValue			:= Nil
	
    Private aCposCustom		:= self:getCustomFields() as array

	Private MV_FIL			:= ""			as character
	Private MV_MESFT		:= 1			as numeric
	Private MV_ANOFT		:= Year(Date())	as numeric

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters() ,"FATR120")

	cDescSaida := STR0004
	cDescEntra := STR0005

	aStruFake := {	{"FILIAL"	, "cFilQry"				, "cFilQry"					},;
					{"EMISSA"	, "D2_EMISSAO"			, "D1_EMISSAO"				},;
					{"ORIGEM"	, "'1-' + cDescSaida"	, "'2-' + cDescEntra"		},;
					{"CLIENT"	, "D2_CLIENTE"			, "D1_FORNECE"				},;
					{"NOME"		, "A1_NOME"				 							},;
					{"DOC"		, "D2_DOC"				, "D1_DOC"					},;
					{"DIGIT"	, "D2_EMISSAO"			, "D1_DTDIGIT"				},;
					{"SERIE"	, "D2_SERIE"			, "D1_SERIE"				},;
					{"ITEM"		, "D2_ITEM"				, "D1_ITEM"					},;
					{"COD"		, "D2_COD"				, "D1_COD"					},;
					{"PROD"		, "B1_DESC"											},;
					{"QUANT"	, "D2_QUANT"			, "D1_QUANT"				},;
					{"TOTAL"	, "D2_TOTAL"			, "(D1_TOTAL - D1_VALDESC)"	},;
					{"GRUPO"	, "BM_GRUPO"										},;
					{"GRDES"	, "BM_DESC"											},;
					{"CC"		, "D2_CCUSTO"			, "D1_CC"					},;
					{"ITEMCC"	, "D2_ITEMCC"			, "D1_ITEMCTA"				},;
					{"CLVL"		, "D2_CLVL"				, "D1_CLVL"					},;
					{"CONTA"	, "D2_CONTA"			, "D1_CONTA"				},;
					{"PAIS"		, "A1_PAIS"											} }

	aCposLGPD := {	"D1_FILIAL","D1_EMISSAO","D1_FORNECE","D1_DOC","D1_DTDIGIT","D1_SERIE","D1_ITEM","D1_COD",;
					"D1_QUANT","D1_TOTAL","D1_VALDESC","D1_CC","D1_ITEMCTA","D1_CLVL","D1_CONTA",;
					"D2_FILIAL","D2_EMISSAO","D2_CLIENTE","D2_DOC","D2_EMISSAO","D2_SERIE","D2_ITEM","D2_COD",;
					"D2_QUANT","D2_TOTAL","D2_CCUSTO","D2_ITEMCC","D2_CLVL","D2_CONTA",;
					"A1_NOME","A1_PAIS",;
					"B1_DESC",;
					"BM_GRUPO","BM_DESC" }

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields 	:= FatGetfieldsLGPD(aCposLGPD, aCposCustom )
    lObfuscated := Len(aPDFields) > 0

	//Irá montar as queries do objeto de negócio
	self:FatSV013CreateSQL()

	DbSelectArea(self:cAliasSD1)
	(self:cAliasSD1)->(DBGoTop())

	// Grava as informações do Documento de Entrada
	While !(self:cAliasSD1)->(Eof()) 

		cFilQry := (self:cAliasSD1)->D1_FILIAL

		self:ojItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)
			If !(SubStr(AllTrim(Upper(self:aStruct[nX][5])), 1, 3) $ "F2_")
				If AllTrim(Upper(self:aStruct[nX][5])) $ "FILIAL|ORIGEM"
					cCpoStruct	:= self:aStruct[nX][5]
					xValue		:= aStruFake[Iif(self:aStruct[nX][5] == "FILIAL", 1, 3)][3]
				ElseIf (nScan := aScan(aStruFake,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
					If Len(aStruFake[nScan]) > 2
						cCpoStruct	:= aStruFake[nScan][3]
						xValue		:= "(self:cAliasSD1)->"+aStruFake[nScan][3]
					Else
						cCpoStruct	:= aStruFake[nScan][2]
						xValue		:= "(self:cAliasSD1)->"+aStruFake[nScan][2]
					EndIf
				Else
					cCpoStruct	:= self:aStruct[nX][5]
					xValue		:= "(self:cAliasSD1)->"+self:aStruct[nX][5]
				EndIf

				If lObfuscated .And. ( nScan := aScan(aPDFields, {|x| x[1] == cCpoStruct } ) )  > 0
					self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
				ElseIf UPPER(self:aStruct[nX][3]) == "DATE"				
					self:ojItems[self:aStruct[nX][1]] := Iif(ValType((self:cAliasSD1)->&(cCpoStruct)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
				Else
					self:ojItems[self:aStruct[nX][1]] := &(xValue)
				EndIf
			EndIf
		Next nX
	
		self:processData(1)
		self:oData:appendData(self:ojItems)

		(self:cAliasSD1)->(DbSkip())

	EndDo

	(self:cAliasSD1)->(DbCloseArea())

	DbSelectArea(self:cAliasSD2)
	(self:cAliasSD2)->(DBGoTop())

	// Grava as informações do Documento de Saída
	While !(self:cAliasSD2)->(Eof()) 

		cFilQry := (self:cAliasSD2)->D2_FILIAL

		self:ojItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)
			If !(SubStr(AllTrim(Upper(self:aStruct[nX][5])), 1, 3) $ "F1_|D1_")
				If AllTrim(Upper(self:aStruct[nX][5])) $ "FILIAL|ORIGEM"
					cCpoStruct	:= self:aStruct[nX][5]
					xValue		:= aStruFake[Iif(self:aStruct[nX][5] == "FILIAL", 1, 3)][2]
				ElseIf (nScan := aScan(aStruFake,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
					cCpoStruct	:= aStruFake[nScan][2]
					xValue		:= "(self:cAliasSD2)->"+aStruFake[nScan][2]
				Else
					cCpoStruct	:= self:aStruct[nX][5]
					xValue		:= "(self:cAliasSD2)->"+self:aStruct[nX][5]
				EndIf

				If lObfuscated .And. ( nScan := aScan(aPDFields,  {|x| x[1] == cCpoStruct } ) )  > 0
					self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
				ElseIf UPPER(self:aStruct[nX][3]) == "DATE"				
					self:ojItems[self:aStruct[nX][1]] := Iif(ValType((self:cAliasSD2)->&(cCpoStruct)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ), totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
				Else
					self:ojItems[self:aStruct[nX][1]] := &(xValue)
				EndIf
			EndIf
		Next nX

		self:processData(2)
		self:oData:appendData(self:ojItems)

		(self:cAliasSD2)->(DbSkip())

	EndDo

	(self:cAliasSD2)->(DbCloseArea())

	aSize(aCposLGPD,  0)
	aSize(aCposCustom,0)
	aSize(aPDFields,  0)
	aSize(aStruFake,  0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 31/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class realinvoicingSmartViewBusinessObject

	Local nX := 0 as numeric

	self:aStruct := {	{"FILIAL",	STR0006,	"string",	STR0006,	"FILIAL"},;	//"Filial"
						{"EMISSA",	STR0007,	"date",		STR0007,	"EMISSA"},;	//"Emissao"
						{"ORIGEM",	STR0008,	"string",	STR0008,	"ORIGEM"},;	//"Origem"
						{"CLIENT",	STR0009,	"string",	STR0009,	"CLIENT"},;	//"Codigo do Cliente"
						{"NOME",	STR0010,	"string",	STR0010,	"NOME" 	},;	//"Nome do Cliente"
						{"DOC",		STR0011,	"string",	STR0011,	"DOC"	},;	//"Numero da Nota Fiscal"
						{"DIGIT",	STR0012,	"date",		STR0012,	"DIGIT" },;	//"Data da Digitacao"
						{"SERIE", 	STR0013,	"string",	STR0013,	"SERIE" },;	//"Serie da Nota Fiscal"
						{"ITEM",	STR0014,	"string",	STR0014,	"ITEM"  },;	//"Item da Nota Fiscal"
						{"COD",  	STR0015,	"string",	STR0015,	"COD"  	},;	//"Codigo do Produto"
						{"PROD",	STR0016,	"string",	STR0016,	"PROD"  },;	//"Descricao do Produto"
						{"QUANT",	STR0017,	"number",	STR0017,	"QUANT" },;	//"Quantidade"
						{"TOTAL",	STR0018,	"number",	STR0018,	"TOTAL" },;	//"Valor do Item"
						{"GRUPO",	STR0019,	"string",	STR0019,	"GRUPO" },;	//"Cod. Grupo de Produto"
						{"GRDES",	STR0020,	"string",	STR0020,	"GRDES" },;	//"Desc. Grupo de Produto"
						{"CC",		STR0021,	"string",	STR0021,	"CC"   	},;	//"Centro de Custo"
						{"ITEMCC",	STR0022,	"string",	STR0022,	"ITEMCC"},;	//"Item Contabil"
						{"CLVL",	STR0023,	"string",	STR0023,	"CLVL"	},;	//"Classe de Valor"
						{"CONTA",	STR0024,	"string",	STR0024,	"CONTA" },;	//"Conta contabil"
						{"PAIS",	STR0025,	"string",	STR0025,	"PAIS"  } }	//"País"

	For nX := 1 To Len(self:aStruct)
		self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][02], self:aStruct[nX][05])
	Next

	// Adiciona novo parametro
	self:oSchema:addParameter("MV_FIL",   STR0006 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial
    self:oSchema:addParameter("MV_MESFT", STR0026 + " ?", "number", .F.,,,,,, STR0028)  		 // Mês Faturamento
	self:oSchema:addParameter("MV_ANOFT", STR0027 + " ?", "number", .F.,,,,,, STR0029)  		 // Ano Faturamento

    If __lLookUp
        self:setCustomURL("MV_FIL",	  "api/framework/v1/genericLookupService/smartview/SM0", 2) 				// Filial ?
        self:setCustomURL("MV_MESFT", "api/faturamento/smartview/v1/options/fat/realinvoicing/SVFTReInvo/1", 1)	// Mês Faturamento ?
    EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFTReInvo
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 30/04/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFTReInvo( cOpcao ) 

	Local aRet 	:= {} as array
	Local aMes 	:= {} as array
	Local nX	:= 0 as numeric

	If cOpcao == "1"
		For nX := 1 To 12
			aAdd(aMes, MesExtenso(nX))
		Next nX
		aRet := aMes
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv015CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 01/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV013CreateSQL() class realinvoicingSmartViewBusinessObject

	Local nX 	    	:= 0  as numeric
	Local nNumFils  	:= 0  as numeric
	Local nParamSD1    	:= 1  as numeric
	Local nParamSD2    	:= 1  as numeric
    Local cFieldsQrySD1 := "" as character
	Local cFieldsQrySD2 := "" as character
	Local cQuerySD1 	:= "" as character
	Local cQuerySD2 	:= "" as character
	Local cTES 			:= "" as character
	Local cSerie 		:= "" as character
    Local aFiliais		:= {} as array
	Local dDateFrom		:= CToD("//") as date
	Local dDateTo		:= CToD("//") as date
	Local oQuerySD1		:= Nil as object
	Local oQuerySD2		:= Nil as object

	//Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	//Tratativa para limpar o valor dos parametros em cache, enquanto executados na automação
	SuperGetMv()

	cTES   := SuperGetMv("MV_FATTESX", .T., "")
	cSerie := SuperGetMv("MV_FATSCAN", .T., "CAN/DES/DEV")

	//Campos do select baseado nas devoluções
	cFieldsQrySD1 := " SF1.F1_DTDIGIT, SF1.F1_MOEDA, SF1.F1_EMISSAO, SD1.D1_TOTAL, SD1.D1_VALDESC, "
	cFieldsQrySD1 += " SD1.D1_FILIAL, SD1.D1_EMISSAO, SD1.D1_FORNECE, SD1.D1_DOC, SD1.D1_DTDIGIT, SD1.D1_SERIE, SD1.D1_ITEM, "
	cFieldsQrySD1 += " SD1.D1_COD, SD1.D1_QUANT, SD1.D1_CC, SD1.D1_ITEMCTA, SD1.D1_CONTA, SD1.D1_CLVL, "
	cFieldsQrySD1 += " SB1.B1_COD, SB1.B1_DESC, SB1.B1_GRUPO, SBM.BM_GRUPO, SBM.BM_DESC, SA1.A1_COD, "
	cFieldsQrySD1 += " SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_PAIS, SF4.F4_CODIGO "
	cFieldsQrySD1 += self:cLocSelect1

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, @cFieldsQrySD1, aCposCustom, .T., .F., .F., "F2_")

	//Campos do select baseado nas saídas
	cFieldsQrySD2 := " SD2.D2_FILIAL, SD2.D2_TOTAL, SD2.D2_EMISSAO, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_ITEM, SD2.D2_QUANT, SD2.D2_CCUSTO, SD2.D2_ITEMCC, SD2.D2_CONTA, "
	cFieldsQrySD2 += " SD2.D2_CLIENTE, SD2.D2_COD, SD2.D2_CLVL, SF2.F2_MOEDA, SF2.F2_EMISSAO, SB1.B1_COD, SB1.B1_DESC, SB1.B1_GRUPO, SBM.BM_GRUPO, "
	cFieldsQrySD2 += " SBM.BM_DESC, SA1.A1_COD, SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_PAIS, SF4.F4_CODIGO "
	cFieldsQrySD2 += self:cLocSelect2

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, @cFieldsQrySD2, aCposCustom, .T., .F., .T., "F1_|D1_")

	dDateFrom := CToD("01/" + PadL(MV_MESFT,2,"0") + "/" + cValToChar(MV_ANOFT))
	dDateTo	  := LastDay(dDateFrom)

	For nX := 1 To nNumFils

		// Documento de Entrada
		cQuerySD1 += "SELECT ? "
		cQuerySD1 += " 	FROM "		+ RetSqlName("SF1") + " SF1 "
		cQuerySD1 += "INNER JOIN "	+ RetSqlName("SD1") + " SD1 "
		cQuerySD1 += " 	ON  SD1.D1_FILIAL 	= ? "
		cQuerySD1 += " 	AND SD1.D1_DOC 		= SF1.F1_DOC " 
		cQuerySD1 += " 	AND SD1.D1_SERIE 	= SF1.F1_SERIE"
		cQuerySD1 += " 	AND SD1.D1_FORNECE	= SF1.F1_FORNECE "
		cQuerySD1 += " 	AND SD1.D1_LOJA		= SF1.F1_LOJA"
		cQuerySD1 += " 	AND SD1.D_E_L_E_T_ 	= ? " 
		cQuerySD1 += "INNER JOIN "	+ RetSqlName("SA1") + " SA1 "
		cQuerySD1 += " 	ON  SA1.A1_FILIAL 	= ? "
		cQuerySD1 += " 	AND SA1.A1_COD 		= SF1.F1_FORNECE 
		cQuerySD1 += " 	AND SA1.A1_LOJA 	= SF1.F1_LOJA
		cQuerySD1 += " 	AND SA1.D_E_L_E_T_ 	= ? "
		cQuerySD1 += "LEFT JOIN "	+ RetSqlName("SD2") + " SD2 "
		cQuerySD1 += " 	ON  SD2.D2_FILIAL 	= ? "
		cQuerySD1 += " 	AND SD2.D2_DOC 		= SD1.D1_NFORI "
		cQuerySD1 += " 	AND SD2.D2_SERIE 	= SD1.D1_SERIORI " 
		cQuerySD1 += " 	AND SD2.D2_CLIENTE 	= SD1.D1_FORNECE "
		cQuerySD1 += " 	AND SD2.D2_LOJA 	= SD1.D1_LOJA "
		cQuerySD1 += " 	AND SD2.D2_COD 		= SD1.D1_COD "
		cQuerySD1 += " 	AND (SD2.D2_ITEM 	= ? OR SD2.D2_ITEM = SD1.D1_ITEMORI) "
		cQuerySD1 += " 	AND SD2.D_E_L_E_T_ 	= ? "
		cQuerySD1 += "LEFT JOIN "	+ RetSqlName("SBM") + " SBM "
		cQuerySD1 += "	ON  SBM.BM_FILIAL 	= ? " 
		cQuerySD1 += "	AND SBM.BM_GRUPO 	= SD1.D1_GRUPO "
		cQuerySD1 += "	AND SBM.D_E_L_E_T_ 	= ? "
		cQuerySD1 += "LEFT JOIN "	+ RetSqlName("SB1") + " SB1 " 
		cQuerySD1 += " 	ON  SB1.B1_FILIAL 	= ? " 
		cQuerySD1 += " 	AND	SB1.B1_COD 		= SD1.D1_COD "
		cQuerySD1 += " 	AND SB1.D_E_L_E_T_ 	= ? "
		cQuerySD1 += "LEFT JOIN "	+ RetSqlName("SF4") + " SF4 " 
		cQuerySD1 += " 	ON 	SF4.F4_FILIAL 	= ? " 
		cQuerySD1 += " 	AND	SF4.F4_CODIGO 	= SD1.D1_TES "
		cQuerySD1 += " 	AND SF4.D_E_L_E_T_ 	= ? "
		cQuerySD1 += "WHERE SF1.F1_FILIAL 	= ? "

		// Filtra somente as sérias informada no parâmetro MV_FATSCAN
		If !Empty(cSerie)
			cQuerySD1 += " AND SF1.F1_SERIE IN (?)"
		Else
			cQuerySD1 += " AND SF1.F1_SERIE <> ? "
		EndIf

		cQuerySD1 += " AND SF1.F1_DTDIGIT BETWEEN ? AND ? "
		cQuerySD1 += " AND SF1.F1_TIPO 		= ? "
		cQuerySD1 += " AND SF1.F1_STATUS   != ? "
		cQuerySD1 += " ? "	// Where de localizados
		cQuerySD1 += " AND SF1.D_E_L_E_T_ 	= ? "
		
		// Documento de Saída 
		cQuerySD2 += "SELECT ? "
		cQuerySD2 += "	FROM "		+ RetSqlName("SD2") + " SD2 "
		cQuerySD2 += "INNER JOIN "	+ RetSqlName("SF2") + " SF2 "
		cQuerySD2 += "	ON  SF2.F2_FILIAL 	= ? " 
		cQuerySD2 += "	AND SF2.F2_DOC 		= SD2.D2_DOC " 
		cQuerySD2 += "	AND SF2.F2_SERIE 	= SD2.D2_SERIE "
		cQuerySD2 += "	AND SF2.F2_CLIENTE 	= SD2.D2_CLIENTE "
		cQuerySD2 += "	AND SF2.F2_LOJA 	= SD2.D2_LOJA "
		cQuerySD2 += "	AND SF2.D_E_L_E_T_ 	= ? "
		cQuerySD2 += "INNER JOIN "	+ RetSqlName("SA1") + " SA1 "
		cQuerySD2 += "	ON  SA1.A1_FILIAL 	= ? " 
		cQuerySD2 += "	AND SA1.A1_COD 		= SD2.D2_CLIENTE "
		cQuerySD2 += "	AND SA1.A1_LOJA 	= SD2.D2_LOJA "
		cQuerySD2 += "	AND SA1.D_E_L_E_T_ 	= ? "
		cQuerySD2 += "INNER JOIN "	+ RetSqlName("SB1") + " SB1 "
		cQuerySD2 += "	ON  SB1.B1_FILIAL 	= ? "
		cQuerySD2 += "	AND SB1.B1_COD 		= SD2.D2_COD "
		cQuerySD2 += "	AND SB1.D_E_L_E_T_ 	= ? "
		cQuerySD2 += "INNER JOIN "	+ RetSqlName("SF4") + " SF4 "
		cQuerySD2 += "	ON  SF4.F4_FILIAL 	= ? "
		cQuerySD2 += "	AND SF4.F4_CODIGO 	= D2_TES "
		cQuerySD2 += "	AND SF4.F4_DUPLIC  <> ? "
		cQuerySD2 += "	AND SF4.D_E_L_E_T_ 	= ? "
		cQuerySD2 += "LEFT JOIN "	+ RetSqlName("SBM") + " SBM "  
		cQuerySD2 += "	ON  SBM.BM_FILIAL 	= ? "
		cQuerySD2 += "	AND SBM.BM_GRUPO 	= SD2.D2_GRUPO " 
		cQuerySD2 += "	AND SBM.D_E_L_E_T_ 	= ? " 
		cQuerySD2 += "WHERE SD2.D2_FILIAL 	= ? "

		// Filtra somente as TES fora do parâmetro MV_FATTESX
		If !Empty(cTES)
			cQuerySD2 += " AND SD2.D2_TES NOT IN (?) "
		Else
			cQuerySD2 += " AND SD2.D2_TES <> ? "
		EndIf

		cQuerySD2 += "	AND SD2.D2_EMISSAO BETWEEN ? AND ? "
		cQuerySD2 += " ? "
		cQuerySD2 += "	AND SD2.D_E_L_E_T_ = ? "

		If nNumFils > 1 .And. nX < nNumFils
			cQuerySD1 += "  UNION ALL "
			cQuerySD2 += "  UNION ALL "
		Else
			cQuerySD1 += " ORDER BY SD1.D1_FILIAL, SD1.D1_EMISSAO "
			cQuerySD2 += " ORDER BY SD2.D2_FILIAL, SD2.D2_EMISSAO "
		EndIf

	Next nX

	oQuerySD1 := FwExecStatement():New(ChangeQuery(cQuerySD1))
	oQuerySD2 := FwExecStatement():New(ChangeQuery(cQuerySD2))

	For nX := 1 To nNumFils

		// Parametros da Query da SD1
		oQuerySD1:SetUnsafe(nParamSD1++, cFieldsQrySD1)
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SD1', 'D1_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, '' )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SBM', 'BM_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SF4', 'F4_FILIAL', aFiliais[nX]) )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetString(nParamSD1++, FatSVFilial('SF1', 'F1_FILIAL', aFiliais[nX]) )

		If !Empty(cSerie)
			oQuerySD1:SetIn(nParamSD1++, StrTokArr2(cSerie, "/"))
		Else
			oQuerySD1:SetString(nParamSD1++, ' ')
		EndIf

		oQuerySD1:SetString(nParamSD1++, DTOS(dDateFrom) )
		oQuerySD1:SetString(nParamSD1++, DTOS(dDateTo) )
		oQuerySD1:SetString(nParamSD1++, 'D' )
		oQuerySD1:SetString(nParamSD1++, ' ' )
		oQuerySD1:SetUnsafe(nParamSD1++, self:cLocWhere2 )
		oQuerySD1:SetString(nParamSD1++, ' ' )

		// Parametros da Query da SD2
		oQuerySD2:SetUnsafe(nParamSD2++, cFieldsQrySD2)
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]) )
		oQuerySD2:SetString(nParamSD2++, ' ' )
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]) )
		oQuerySD2:SetString(nParamSD2++, ' ' )
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]) )
		oQuerySD2:SetString(nParamSD2++, ' ' )
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SF4', 'F4_FILIAL', aFiliais[nX]) )
		oQuerySD2:SetString(nParamSD2++, 'N' )
		oQuerySD2:SetString(nParamSD2++, ' ' )
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SBM', 'BM_FILIAL', aFiliais[nX]) )
		oQuerySD2:SetString(nParamSD2++, ' ' )
		oQuerySD2:SetString(nParamSD2++, FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]) )

		If !Empty(cTES)
			oQuerySD2:SetIn(nParamSD2++, StrTokArr2(cTES, "/") )
		Else
			oQuerySD2:SetString(nParamSD2++, ' ')
		EndIf

		oQuerySD2:SetString(nParamSD2++, DTOS(dDateFrom) )
		oQuerySD2:SetString(nParamSD2++, DTOS(dDateTo) )
		oQuerySD2:SetUnsafe(nParamSD2++, self:cLocWhere1 )
		oQuerySD2:SetString(nParamSD2++, ' ' )

	Next nX

	self:cAliasSD1 := oQuerySD1:OpenAlias()
	self:cAliasSD2 := oQuerySD2:OpenAlias()

	FreeObj(oQuerySD1)
	FreeObj(oQuerySD2)

	aSize(aFiliais, 0)

Return
