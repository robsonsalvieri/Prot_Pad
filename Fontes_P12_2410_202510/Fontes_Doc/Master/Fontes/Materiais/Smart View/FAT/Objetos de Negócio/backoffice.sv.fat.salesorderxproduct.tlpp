#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesorderxproduct.ch"

Static __lLookUp  := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.salesorderxproduct.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SC5,SC6,SA3,SB1", name="Pedidos de Venda x Produtos", country="ALL")

/*/{Protheus.doc} salesorderxproductSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Pedidos de 
	@author Squad CRM/Faturamento
	@since 26/05/2023
	@version 12.1.2410
*/
Class salesorderxproductSmartViewBusinessObject From IntegratedProvider
    
    public method new() 		as object
    public method getData() 	as object
    public method getSchema() 	as object
	public method FatSv008CreateSQL()

	protected data cSelectLoc  as character
	protected data cLocWhere   as character
	protected data cAliasSC5   as character
	protected data jItems 	   as json
	protected data oQrySC6     as object

EndClass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 26/05/2023
	@version 12.1.2410
*/
Method new() Class salesorderxproductSmartViewBusinessObject
	
	_Super:new()
    self:appendArea(STR0001) 		//Faturamento
    self:setDisplayName(STR0002) 	//Pedidos de Venda x Produtos
    self:setDescription(STR0003) 	//Relação de Pedidos de Venda x Produtos
    self:setIsLookUp(.T.)  			//Define que terá consulta de LookUp

Return self
 
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 26/05/2023
	@version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object Class salesorderxproductSmartViewBusinessObject

	Local aVendProd		:= {} as array
	local aPDFields     := {} as array

	Local cCodVen		:= "" as character
	Local cDupli		:= "" as character
	Local cEstoq	 	:= "" as character
	Local cProdRef 		:= "" as character
	Local cAliasSC6		:= "" as character

	Local nPosVend		:= 0 as numeric
	Local nTotQtd  		:= 0 as numeric
	Local nTotEntr  	:= 0 as numeric
	Local nTotVal 		:= 0 as numeric
	Local nValor		:= 0 as numeric
	Local nQuant		:= 0 as numeric
	Local nEntr			:= 0 as numeric
	Local nI 			:= 0 as numeric
	Local nParam		:= 1 as numeric
	Local nTamRef		:= 0 as numeric
    Local nTamProd	    := 0 as numeric

	Local lObfuscated   := .F. as logical
	Local bVend			:= { |x| "C5_VEND"+Str(x,1) }

	MV_MOEDA    := 0  
	MV_FIL 		:= "" 
	MV_VENDE 	:= "" 
	MV_VENAT 	:= "" 
	MV_PRDDE 	:= "" 
	MV_PRDAT 	:= "" 
	MV_DATDE	:= Date() 
	MV_DATAT	:= Date()

	// Carrega os parametros
    FatSetValueMVPAR(oFilter:GetParameters() ,"MTR610")

	// Criacao das Querys
    self:FatSv008CreateSQL()

	// Carrega os dados necessarios apenas quando ha registro para processar na Query
	If (self:cAliasSC5)->(!Eof())

		nTamRef := Val(Substr(GetMv("MV_MASCGRD"),1,2))
    	nTamProd := GetSx3Cache("B1_COD", "X3_TAMANHO")

		//Tratamento para permitir apenas o número de moedas utilizadas.
    	MV_PAR11 := IIf(MV_PAR11 <= 0 .Or. MV_PAR11 > ContaMoeda(), 1, MV_PAR11)

		//Tratamento tamanho do campo mascara do produto
		MV_PAR07 := PADR(MV_PAR07, nTamProd)

		cDupli	:= IIf( (MV_PAR09 == 1),"S",IIf( (MV_PAR09 == 2),"N","SN" ) )
    	cEstoq	:= IIf( (MV_PAR10 == 1),"S",IIf( (MV_PAR10 == 2),"N","SN" ) )

		//Verifica se precisa fazer o tratamento para LGPD
		aPDFields   := FwProtectedDataUtil():UsrAccessPDField(__cUserID, {"A3_NOME"})
    	lObfuscated := Len( aPDFields ) != Len({"A3_NOME"})

		SA3->(DbSetOrder(1))//A3_FILIAL, A3_COD

		//Processa Faturamento
		(self:cAliasSC5)->(dbGoTop())

		While (self:cAliasSC5)->(!Eof())

			//Verifica se Vendedor devera ser impresso
			For nI := 1 To 5

				If (self:cAliasSC5)->&(EVAL(bVend,nI)) >= MV_VENDE .And.(self:cAliasSC5)->&(EVAL(bVend,nI)) <= MV_VENAT .And. !Empty((self:cAliasSC5)->&(EVAL(bVend,nI)))

					cCodVen	:= (self:cAliasSC5)->&(EVAL(bVend,nI))

					SA3->(MsSeek(FatSVFilial('SA3', 'A3_FILIAL', (self:cAliasSC5)->C5_FILIAL)+cCodVen))
					cDesVen := IIf( lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA3->A3_NOME), SA3->A3_NOME ) 

					nParam := 1
					self:oQrySC6:SetUnsafe(nParam++, FwJoinFilial("SB1", "SC6"))
					self:oQrySC6:SetString(nParam++, " ")
					self:oQrySC6:SetString(nParam++, (self:cAliasSC5)->C5_FILIAL)
					self:oQrySC6:SetString(nParam++, (self:cAliasSC5)->C5_NUM)
					self:oQrySC6:SetString(nParam++, MV_PRDDE)
					self:oQrySC6:SetString(nParam++, MV_PRDAT)
					self:oQrySC6:SetString(nParam++, " ")

					cAliasSC6 := self:oQrySC6:OpenAlias()

					While (cAliasSC6)->(!Eof()) .And. (MV_PAR12 != 2 .And. (cAliasSC6)->C6_BLQ != "R ")

						If AvalTes((cAliasSC6)->C6_TES,cEstoq,cDupli)
							If ValidMasc((cAliasSC6)->C6_PRODUTO,MV_PAR07)
								If (cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1

									cProdRef := Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef)
									nTotQtd  := 0
									nTotEntr := 0
									nTotVal  := 0
									nValor	 := 0
									nQuant	 := 0
									nEntr	 := 0

									While (cAliasSC6)->(!Eof()) .And. (self:cAliasSC5)->C5_FILIAL == (cAliasSC6)->C6_FILIAL .And.;
										cProdRef == Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef) .And. (cAliasSC6)->C6_NUM == (self:cAliasSC5)->C5_NUM .And.;
										!((cAliasSC6)->C6_PRODUTO < MV_PRDDE .Or. (cAliasSC6)->C6_PRODUTO > MV_PRDAT)
				
										nPosVend := aScan(aVendProd,{|x| x[1] == cCodVen+(cAliasSC6)->C6_PRODUTO+(self:cAliasSC5)->A1_EST})
										nValor	 := xMoeda((cAliasSC6)->C6_VALOR,(self:cAliasSC5)->C5_MOEDA,MV_PAR11,(self:cAliasSC5)->C5_EMISSAO)
										nQuant	 := (cAliasSC6)->C6_QTDVEN
										nEntr	 := (cAliasSC6)->C6_QTDENT

										IIf( nPosVend == 0, nTotVal  := nValor, nTotVal  += nValor)
										IIf( nPosVend == 0, nTotQtd  := nQuant, nTotQtd  += nQuant)
										IIf( nPosVend == 0, nTotEntr := nEntr,  nTotEntr += nEntr)
										IIf( nPosVend == 0,	aAdd(aVendProd,{cCodVen+(cAliasSC6)->C6_PRODUTO+(self:cAliasSC5)->A1_EST,;	//Chave
															cCodVen,;																//Codigo do Vendedor
															(self:cAliasSC5)->A1_EST,;													//Estado do Cliente
															(cAliasSC6)->C6_PRODUTO,;												//Codigo do Produto
															nTotVal,;																//Valor Total
															nTotQtd,;																//Quantidade do Pedido de Venda
															nTotEntr,;																//Quantidade Faturada
															(cAliasSC6)->C6_GRADE,;													//Possui grade?
															(self:cAliasSC5)->C5_MOEDA,;													//Moeda
															(cAliasSC6)->B1_DESC,;													//Desc Produto
															(cAliasSC6)->B1_UM,;													//UM Produto
															cDesVen,;																//Nome do Vendedor
															(cAliasSC6)->C6_FILIAL}), )												//Filial				

										(cAliasSC6)->(dbSkip())
										Loop

									EndDo

								EndIf

								nPosVend := aScan(aVendProd,{|x| x[1] == cCodVen+(cAliasSC6)->C6_PRODUTO+(self:cAliasSC5)->A1_EST})

								If nPosVend == 0
									aAdd(aVendProd,{cCodVen+(cAliasSC6)->C6_PRODUTO+(self:cAliasSC5)->A1_EST,;											//Chave
													cCodVen,;																							//Codigo do Vendedor
													(self:cAliasSC5)->A1_EST,;																			//Estado do Cliente
													(cAliasSC6)->C6_PRODUTO,;																			//Codigo do Produto
													IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotVal, xMoeda((cAliasSC6)->C6_VALOR, (self:cAliasSC5)->C5_MOEDA, MV_PAR11, (self:cAliasSC5)->C5_EMISSAO)),; //Valor Total
													IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotQtd, (cAliasSC6)->C6_QTDVEN),;			//Quantidade do Pedido de Venda
													IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotEntr, (cAliasSC6)->C6_QTDENT),;			//Quantidade Faturada
													(cAliasSC6)->C6_GRADE,;																				//Possui grade?
													(self:cAliasSC5)->C5_MOEDA,;																		//Moeda
													(cAliasSC6)->B1_DESC,;																				//Desc Produto
													(cAliasSC6)->B1_UM,;																				//UM Produto
													cDesVen,;																							//Nome do Vendedor
													(cAliasSC6)->C6_FILIAL} )																			//Filial																				
								Else
									aVendProd[nPosVend][5] := aVendProd[nPosVend][5] + IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotVal, xMoeda((cAliasSC6)->C6_VALOR, (self:cAliasSC5)->C5_MOEDA, MV_PAR11, (self:cAliasSC5)->C5_EMISSAO))
									aVendProd[nPosVend][6] := aVendProd[nPosVend][6] + IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotQtd, (cAliasSC6)->C6_QTDVEN)
									aVendProd[nPosVend][7] := aVendProd[nPosVend][7] + IIf((cAliasSC6)->C6_GRADE == "S" .And. MV_PAR08 == 1, nTotEntr, (cAliasSC6)->C6_QTDENT)
								EndIf
							EndIf
						EndIf

						(cAliasSC6)->(dbSkip())

					EndDo

					(cAliasSC6)->(DbCloseArea())

				EndIf

			Next nI

			(self:cAliasSC5)->(dbSkip())

		EndDo

		//Impressão do Relatorio
		If !Empty(aVendProd)

			For nI := 1 To Len(aVendProd)

				If aVendProd[nI][5] > 0

					self:jItems := JsonObject():new()
					self:jItems["FILIAL"] 		:= aVendProd[nI][13] //Filial
					self:jItems["CODIGO"] 		:= aVendProd[nI][2]	 //Codigo de Vendedor
					self:jItems["NOME"] 		:= aVendProd[nI][12] //Nome do Vendedor
					self:jItems["CODPRODUTO"] 	:= aVendProd[nI][4]	 //Codigo do Produto
					self:jItems["DESCPRODUTO"]	:= aVendProd[nI][10] //Descricao do Produto
					self:jItems["ESTADO"] 		:= aVendProd[nI][3]	 //Estado
					self:jItems["QTDPEDIDA"] 	:= aVendProd[nI][6]	 //Qtd. Pedida
					self:jItems["QTDFATURADA"]  := aVendProd[nI][7]	 //Qtd. Faturada
					self:jItems["VLRTOTAL"] 	:= aVendProd[nI][5]	 //Valor Total
					self:jItems["UM"] 			:= aVendProd[nI][11] //UM

					self:processData(1) //Commit para os campos localizados
					self:oData:appendData(self:jItems)

				EndIf

			Next nI

		EndIf

		(self:cAliasSC5)->(DbCloseArea())

		aSize(aVendProd,0)
		aSize(aPDFields,0)

	EndIf

	self:oQrySC6:Destroy()
    FreeObj(self:oQrySC6)
	self:oQrySC6:= Nil

Return self:oData

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 26/04/2023
	@version 12.1.2410
*/
Method getSchema() as object Class salesorderxproductSmartViewBusinessObject

	Local aParameters := {} as array
	Local nX := 0 as numeric

	self:oSchema:addProperty("FILIAL", 		STR0019, "string", STR0019, "FILIAL") 		//"Filial"
	self:oSchema:addProperty("CODIGO", 		STR0004, "string", STR0004, "CODIGO") 		//"Código"
	self:oSchema:addProperty("NOME", 		STR0005, "string", STR0005, "NOME") 		//"Nome"
	self:oSchema:addProperty("CODPRODUTO", 	STR0006, "string", STR0006, "CODPRODUTO") 	//"Código do Produto"
	self:oSchema:addProperty("DESCPRODUTO", STR0007, "string", STR0007, "DESCPRODUTO")	//"Descrição do Produto"
	self:oSchema:addProperty("ESTADO", 		STR0008, "string", STR0008 ,"ESTADO") 		//"Estado"
	self:oSchema:addProperty("QTDPEDIDA", 	STR0009, "number", STR0009, "QTDPEDIDA") 	//"Quantidade do Pedido"
	self:oSchema:addProperty("QTDFATURADA", STR0010, "number", STR0010, "QTDFATURADA") 	//"Quantidade Faturada"
	self:oSchema:addProperty("VLRTOTAL", 	STR0011, "number", STR0011, "VLRTOTAL") 	//"Valor Total"
	self:oSchema:addProperty("UM", 			STR0012, "string", STR0012, "UM") 			//"UM"

	// Adiciona parametros
	self:oSchema:addParameter("MV_FIL", 	STR0019 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) // Filial
	self:oSchema:addParameter("MV_DATDE", 	STR0017 + " ?", "date",   .F.,,,,,, FTSVHelp('MTR610','01'))
	self:oSchema:addParameter("MV_DATAT", 	STR0018 + " ?", "date",   .F.,,,,,, FTSVHelp('MTR610','02'))
    self:oSchema:addParameter("MV_VENDE", 	STR0013 + " ?", "string", .F.,,,,,, FTSVHelp('MTR610','03'))
	self:oSchema:addParameter("MV_VENAT", 	STR0014 + " ?", "string", .F.,,,,,, FTSVHelp('MTR610','04'))
	self:oSchema:addParameter("MV_PRDDE", 	STR0015 + " ?", "string", .F.,,,,,, FTSVHelp('MTR610','05'))
	self:oSchema:addParameter("MV_PRDAT",	STR0016 + " ?", "string", .F.,,,,,, FTSVHelp('MTR610','06'))

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('MTR610',,, {'01','02','03','04','05','06','11','13'})

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

	self:oSchema:addParameter("MV_MOEDA", STR0020 + " ?", "number", .F.,,,,,, FTSVHelp("MTR610","11")) // Moeda

	If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
		self:setCustomURL("MV_VENDE", "api/framework/v1/genericLookupService/smartview/SA3", 2)	// Vendedor de
		self:setCustomURL("MV_VENAT", "api/framework/v1/genericLookupService/smartview/SA3", 2)	// Vendedor ate
		self:setCustomURL("MV_PRDDE", "api/framework/v1/genericLookupService/smartview/SB1", 2)	// Produto de
		self:setCustomURL("MV_PRDAT", "api/framework/v1/genericLookupService/smartview/SB1", 2)	// Produto ate
		self:setCustomURL("MV_PAR08", "api/framework/treports/integratedprovider/v1/options/MTR610/MV_PAR08", 1) // Aglutina prod.Grade
		self:setCustomURL("MV_PAR09", "api/framework/treports/integratedprovider/v1/options/MTR610/MV_PAR09", 1) // TES Qto Faturamento
		self:setCustomURL("MV_PAR10", "api/framework/treports/integratedprovider/v1/options/MTR610/MV_PAR10", 1) // TES Qto Estoque
		self:setCustomURL("MV_PAR12", "api/framework/treports/integratedprovider/v1/options/MTR610/MV_PAR12", 1) // Considera Resíduos
	EndIf

	FwFreeArray(aParameters)

Return self:oSchema

/*{Protheus.doc} FatSv008CreateSQL
	Retorna todas as querys montadas
	@return Nil
	@author Squad CRM/Faturamento
	@since 08/10/2024
	@version 12.1.2410
*/
Method FatSv008CreateSQL() Class salesorderxproductSmartViewBusinessObject

    Local nParam     	:= 1   as numeric
	Local cNameTmpSC5   := ""  as character
	Local cFieldsQrySC5 := ""  as character
	Local cQuerySC5  	:= ""  as character
    Local cQuerySC6  	:= ""  as character
	Local aFiliais	 	:= {}  as array
    Local oQrySC5 := Nil as object
	Local oTempTableBranchSC5 := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranchSC5 := FatSVTableTempBranch(aFiliais, "SC5", "C5_FILIAL")
	
	cNameTmpSC5 := oTempTableBranchSC5:GetTableNameForQuery()

	cFieldsQrySC5 := " SC5.C5_FILIAL, SC5.C5_CLIENTE, SC5.C5_LOJACLI, SC5.C5_VEND1, SC5.C5_VEND2, SC5.C5_VEND3, SC5.C5_VEND4, "
	cFieldsQrySC5 += " SC5.C5_VEND5, SC5.C5_NUM, SC5.C5_MOEDA, SC5.C5_EMISSAO, SA1.A1_EST"

	cQuerySC5 := " SELECT ?"
	cQuerySC5 += "	? " //Campos Localizados
	cQuerySC5 += " FROM " + RetSqlName("SC5") + " SC5 "
	cQuerySC5 += " INNER JOIN ? TMPFIL"
	cQuerySC5 += " ON TMPFIL.TMP_FILIAL = SC5.C5_FILIAL"	
	cQuerySC5 += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
	cQuerySC5 += " AND SA1.A1_COD = SC5.C5_CLIENTE "
	cQuerySC5 += " AND SA1.A1_LOJA = SC5.C5_LOJACLI "
	cQuerySC5 += " AND SA1.D_E_L_E_T_ = ? "
	cQuerySC5 += " WHERE SC5.C5_FILIAL = TMPFIL.TMP_FILIAL "
	cQuerySC5 += " AND SC5.C5_EMISSAO BETWEEN ? AND ? "
	cQuerySC5 += " AND SC5.C5_TIPO <> ? "
	cQuerySC5 += " AND SC5.C5_TIPO <> ? "
	cQuerySC5 += " ? "
	cQuerySC5 += " AND SC5.D_E_L_E_T_ = ? "
	
	// Monta SubQuery
	cQuerySC6 := " SELECT C6_FILIAL, C6_NUM, C6_BLQ, C6_PRODUTO, C6_TES, C6_GRADE, C6_VALOR, C6_QTDVEN, C6_QTDENT, B1_DESC, B1_UM "
	cQuerySC6 += " FROM " + RetSqlName("SC6") + " SC6 "
	cQuerySC6 += " LEFT JOIN " + RetSqlName("SB1") + " SB1 ON ? "
	cQuerySC6 += " AND SB1.B1_COD = SC6.C6_PRODUTO"
	cQuerySC6 += " AND SB1.D_E_L_E_T_ = ? "
	cQuerySC6 += " WHERE SC6.C6_FILIAL = ? "
	cQuerySC6 += " AND SC6.C6_NUM = ? "
	cQuerySC6 += " AND SC6.C6_PRODUTO BETWEEN ? AND ? "
	cQuerySC6 += " AND SC6.D_E_L_E_T_ = ? "

	oQrySC5 := FwExecStatement():New(ChangeQuery(cQuerySC5))
	self:oQrySC6 := FwExecStatement():New(ChangeQuery(cQuerySC6))

	oQrySC5:SetUnsafe(nParam++, cFieldsQrySC5)
	oQrySC5:SetUnsafe(nParam++, self:cSelectLoc)
	oQrySC5:SetUnsafe(nParam++, cNameTmpSC5)
	oQrySC5:SetUnsafe(nParam++, FwJoinFilial("SA1", "SC5"))
	oQrySC5:SetString(nParam++, " ")
	oQrySC5:SetDate(nParam++, MV_DATDE)
	oQrySC5:SetDate(nParam++, MV_DATAT)
	oQrySC5:SetString(nParam++, 'B')
	oQrySC5:SetString(nParam++, 'D')
	oQrySC5:SetUnsafe(nParam++, self:cLocWhere)
	oQrySC5:SetString(nParam++, " ")

	self:cAliasSC5 := oQrySC5:OpenAlias()

    aSize(aFiliais,0)

	oTempTableBranchSC5:Delete()
    FWFreeObj(oTempTableBranchSC5)

    oQrySC5:Destroy()
    FreeObj(oQrySC5)
	oQrySC5:= Nil
    
Return
