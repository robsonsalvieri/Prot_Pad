#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.invoicesforcarriers.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.invoicesforcarriers.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF2,SD2,SA1,SA2,SA4", name="Notas Fiscais para Transporte", country="ALL", customTables="SA4,SF2")

//-------------------------------------------------------------------
/*/{Protheus.doc} invoicesforcarriersSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Notas Fiscais
para Transporte.

@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class invoicesforcarriersSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
       
    public method FatSV021CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
    protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data cGroupByLoc  as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class invoicesforcarriersSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 	 //Faturamento
    self:setDisplayName(STR0002) //Notas Fiscais para Transporte
    self:setDescription(STR0003) //Relação de Notas Fiscais para Transporte
    self:setIsLookUp(.T.)        //Define que terá consulta de LookUp

    self:cAlias       := ""
    self:cSelectLoc   := ""
    self:cWhereLoc    := ""
    self:cGroupByLoc  := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class invoicesforcarriersSmartViewBusinessObject 

    Local cCliFornec    := "" as character
    Local cMunicp       := "" as character
    Local cEstado       := "" as character
    Local xValue        := "" as character

    Local nQuant	    := 0 as numeric
    Local nX            := 0 as numeric
    Local nScan         := 0 as numeric

    Local lObfuscated   := .F. as logical

    Local aPDFields     := {} as array

    Private aFieldsValue := {} as array
    Private aCpoCustom   := self:getCustomFields() as array

    Private MV_FIL		 := "" as character
    Private MV_TRDE		 := "" as character
    Private MV_TRATE	 := "" as character
    Private MV_NFDE	     := "" as character
    Private MV_NFATE	 := "" as character
    Private MV_MOEDA     := 1  as numeric

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR650")

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_MOEDA := IIf(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)

    aFieldsValue:= {{'A1_NOME',     {'A1_NOME',     "", 'cCliFornec'}},;
                    {'A1_MUN',      {'A1_MUN',      "", 'cMunicp'}},;
                    {'A1_EST',      {'A1_EST',      "", 'cEstado'}},;
                    {'F2_VALBRUT',  {'F2_VALBRUT',  "", 'xMoeda((self:cAlias)->F2_VALBRUT,1,MV_MOEDA,(self:cAlias)->F2_EMISSAO)'}},;
                    {'F2_SERIE',    {'F2_SERIE',    "", 'Alltrim((self:cAlias)->&(SerieNfId("SF2",3,"F2_SERIE")))'}},;
                    {'D2_QUANT',    {'D2_QUANT',    "", 'nQuant'}}}

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := Len( aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
    self:FatSV021CreateSQL()

	//Posiciona no registro inicial
	(self:cAlias)->(dbGoTop())

    SA1->(DbSetOrder(1))
    SA2->(DbSetOrder(1))

	//Processa Faturamento
	While !(self:cAlias)->(Eof())
        nQuant := (self:cAlias)->TOTQUANT

        If (self:cAlias)->F2_TIPO $ "NCIP"
		    SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', (self:cAlias)->F2_FILIAL)+(self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA))
            cCliFornec  := SA1->A1_NOME
            cMunicp     := SA1->A1_MUN
            cEstado     := SA1->A1_EST
        Else
		    SA2->(MsSeek(FatSVFilial('SA2', 'A2_FILIAL', (self:cAlias)->F2_FILIAL)+(self:cAlias)->F2_CLIENTE+(self:cAlias)->F2_LOJA))
            cCliFornec  := SA2->A2_NOME
            cMunicp     := SA2->A2_MUN
            cEstado     := SA2->A2_EST
        EndIf

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)
            If (nScan := aScan(aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
                xValue := FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2])
            Else
               	xValue := "(self:cAlias)->"+self:aStruct[nX][5]
            EndIf

            If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                self:jItems[self:aStruct[nX][1]] := IIf(ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
             Else
                self:jItems[self:aStruct[nX][1]] := &(xValue)
            EndIf
		Next

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(dbSkip())

	EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields,0)
    aSize(aFieldsValue,0)
    aSize(aCpoCustom,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class invoicesforcarriersSmartViewBusinessObject

    Local cTituloCpo := "" as character
    Local nX         := 0  as numeric

    self:aFields := {"F2_FILIAL", "F2_TRANSP", "A4_NOME", "F2_DOC", "F2_SERIE", "F2_VOLUME1", "A1_NOME", "D2_QUANT", "F2_VALBRUT", "A1_MUN", "A1_EST", "F2_PBRUTO"}

    self:aStruct := FatTrGetStruct(self:aFields)

	For nX := 1 To Len(self:aStruct)
		If 	"A1_NOME" = AllTrim(self:aStruct[nX][5])
			cTituloCpo := STR0009
			self:oSchema:addProperty(self:aStruct[nX][1], cTituloCpo, self:aStruct[nX][3], cTituloCpo, self:aStruct[nX][5])
		ElseIf "A1_MUN" = AllTrim(self:aStruct[nX][5])
            cTituloCpo := STR0012
            self:oSchema:addProperty(self:aStruct[nX][1], cTituloCpo, self:aStruct[nX][3], cTituloCpo, self:aStruct[nX][5])
        ElseIf "A1_EST" = AllTrim(self:aStruct[nX][5])
            cTituloCpo := STR0013
            self:oSchema:addProperty(self:aStruct[nX][1], cTituloCpo, self:aStruct[nX][3], cTituloCpo, self:aStruct[nX][5])
        Else    
			self:oSchema:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		EndIf
	Next

    //Retorna do SX1 os parâmetros que serão utilizados, no array os que não serão utilizados
    aParameters := FtSVSetParam('MTR650',,,{'01','02','03','04','05'})

    // Adiciona os parametros
    self:oSchema:addParameter("MV_FIL",   STR0015 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))         //Filial ?
    self:oSchema:addParameter("MV_TRDE",  STR0016 + " ?", "string", .F.,,,,,, FTSVHelp("MTR650", "01")) //Da Transportadora ?
    self:oSchema:addParameter("MV_TRATE", STR0017 + " ?", "string", .F.,,,,,, FTSVHelp("MTR650", "02")) //Ate Transportadora ?
    self:oSchema:addParameter("MV_NFDE",  STR0019 + " ?", "string", .F.,,,,,, FTSVHelp("MTR650", "03")) //Da Nota Fiscal ?
    self:oSchema:addParameter("MV_NFATE", STR0020 + " ?", "string", .F.,,,,,, FTSVHelp("MTR650", "04")) //Ate a Nota Fiscal ?

    For nX := 1 To Len(aParameters)
        self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nX][5] )
    Next nX

    self:oSchema:addParameter("MV_MOEDA", STR0018 + " ?", "number", .F.,,,,,, FTSVHelp("MTR650", "05")) // Moeda ?

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                             // Filial ?
        self:setCustomURL("MV_TRDE",  "api/framework/v1/genericLookupService/smartview/SA4", 2)                             // Da Transportadora ?
        self:setCustomURL("MV_TRATE", "api/framework/v1/genericLookupService/smartview/SA4", 2)                             // Ate Transportadora ?
        self:setCustomURL("MV_PAR08", "api/faturamento/smartview/v1/options/fat/invoicesforcarriers/svfatCombo021/1", 1)    // Considerar NF Beneficiamento ?
        self:setCustomURL("MV_PAR09", "api/faturamento/smartview/v1/options/fat/invoicesforcarriers/svfatCombo021/2", 1)    // Considerar Dev.de Compra ?
    EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} svfatCombo021
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function svfatCombo021( cOpcao )

	Local aRet :=  {} as  array

	If cOpcao == "1"
		aRet :=  FatGetCboX1('MTR650', 8)
    ElseIf cOpcao == "2"
		aRet :=  FatGetCboX1('MTR650', 9)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv021CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV021CreateSQL() class invoicesforcarriersSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local cGroupBy   := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := " MIN(F2_FILIAL) F2_FILIAL, MIN(F2_TIPO) F2_TIPO, F2_DOC, "
    cFieldsQry += IIf( Alltrim(SerieNfId("SF2",3,"F2_SERIE")) <> "F2_SERIE", SerieNfId("SF2",3,"F2_SERIE") + ", ", ' ')
    cFieldsQry += " F2_SERIE, MIN(F2_EMISSAO) F2_EMISSAO, "
    cFieldsQry += " MIN(F2_CLIENTE) F2_CLIENTE, MIN(F2_LOJA) F2_LOJA, MIN(F2_TRANSP) F2_TRANSP, MIN(F2_VOLUME1) F2_VOLUME1, "
    cFieldsQry += " MIN(SF2.F2_VALBRUT) F2_VALBRUT, MIN(SF2.F2_PBRUTO) F2_PBRUTO, "
    cFieldsQry += " MIN(SA4.A4_NOME) A4_NOME, SUM(SD2.D2_QUANT) TOTQUANT " + self:cSelectLoc

    cGroupBy := " SF2.F2_FILIAL, SF2.F2_TRANSP, SF2.F2_DOC, SF2.F2_SERIE "
    cGroupBy += self:cGroupByLoc

    AEval( aCpoCustom, {|x| IIF("F2_" $ x[1], cGroupBy += ", SF2." + x[1], cGroupBy += ", SA4." + x[1])} )

    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .T., .T.)

    For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM "      + RetSqlName("SF2") + " SF2 "
        cQuery += "INNER JOIN " + RetSqlName("SD2") + " SD2 "
        cQuery += " ON  SD2.D2_FILIAL   = ? "
        cQuery += " AND SD2.D2_DOC      = SF2.F2_DOC "
        cQuery += " AND SD2.D2_SERIE    = SF2.F2_SERIE "
        cQuery += " AND SD2.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN "  + RetSqlName("SA4") + " SA4 "
        cQuery += " ON  SA4.A4_FILIAL   = ? "
        cQuery += " AND SA4.A4_COD      = SF2.F2_TRANSP "
        cQuery += " AND SA4.D_E_L_E_T_  = ? "
        cQuery += "WHERE SF2.F2_FILIAL  = ? "
        cQuery += " AND SF2.F2_TRANSP  >= ? "
        cQuery += " AND SF2.F2_TRANSP  <= ? "
        cQuery += " AND SF2.F2_DOC     >= ? "
        cQuery += " AND SF2.F2_DOC     <= ? "
        cQuery += " AND SF2.F2_EMISSAO >= ? "
        cQuery += " AND SF2.F2_EMISSAO <= ? "
        cQuery += " AND NOT (?)"

        If MV_PAR09 == 1
            cQuery += " AND SF2.F2_TIPO <> ? "
        EndIf

        If MV_PAR08 == 1
            cQuery += " AND SF2.F2_TIPO <> ? "
        EndIf
        
        cQuery += " ? " //Where para query de notas fiscais que foi populado no objeto localizado
        cQuery += " AND SF2.D_E_L_E_T_  = ? "
        cQuery += " GROUP BY ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += "  UNION ALL "
		Else
            cQuery += " ORDER BY F2_FILIAL, F2_TRANSP, F2_DOC, F2_SERIE "
        EndIf
    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SA4', 'A4_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, MV_TRDE)
        oQuery:SetString(nParam++, MV_TRATE)
        oQuery:SetString(nParam++, MV_NFDE)
        oQuery:SetString(nParam++, MV_NFATE)
        oQuery:SetString(nParam++, DTOS(MV_PAR06) )
        oQuery:SetString(nParam++, DTOS(MV_PAR07) )
        oQuery:SetUnsafe(nParam++, IsRemito(2, 'F2_TIPODOC'))

        If MV_PAR09 == 1
            oQuery:SetString(nParam++, 'D')
        EndIf

        If MV_PAR08 == 1
            oQuery:SetString(nParam++, 'B')
        EndIf

        oQuery:SetUnsafe(nParam++,  self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')
        oQuery:SetUnsafe(nParam++,  cGroupBy)

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
