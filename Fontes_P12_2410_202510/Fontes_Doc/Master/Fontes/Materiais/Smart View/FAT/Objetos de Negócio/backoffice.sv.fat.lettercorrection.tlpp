#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.lettercorrection.ch"

Static __LookUp	:= FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.lettercorrection.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF2,SF1,SA1,SA2", name="Impressão Carta de Correção", country="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} lettercorrectionSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Carta de Correção.

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class lettercorrectionSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV024CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class lettercorrectionSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Carta de Correção"
    self:setDescription(STR0003)	//"Impressão da Carta de Correção"
    self:setIsLookUp(.T.)  

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView

@return object: self:oData

@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object class lettercorrectionSmartViewBusinessObject

    Local cMes         := ""  as character
    Local cNome        := ""  as character
    Local cEndereco    := ""  as character
    Local cBairro      := ""  as character
    Local cCEP         := ""  as character
    Local cCidade      := ""  as character
    Local cEstado      := ""  as character

    Local lObfuscated  := .F. as logical

    Local aPDFields    := {}  as array
    Local aCposLGPD    := {}  as array
    Local aDadosSx5    := {}  as array
    Local aSM0Fileds   := {}  as array
    Local aSM0         := {}  as array

    Local nAno         := 0   as numeric
    Local nDia         := 0   as numeric
    Local nX		   := 0   as numeric
    Local ny		   := 0   as numeric
    Local nz		   := 0   as numeric

    Private CODDESC1   := ""  as character
    Private CODDESC2   := ""  as character
    Private CODDESC3   := ""  as character
    Private CODDESC4   := ""  as character
    Private CODDESC5   := ""  as character
    Private CORRECAO01 := ""  as character
    Private CORRECAO02 := ""  as character
    Private CORRECAO03 := ""  as character
    Private CORRECAO04 := ""  as character
    Private CORRECAO05 := ""  as character

    Private MV_FIL	   := ""  as character
    Private ENTRSAID   := 1   as numeric
    Private NUMERONF   := ""  as character
    Private SERIENF    := ""  as character

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR185")

    nDia    := Day(dDataBase)
    cMes    := MesExtenso(Month(dDataBase))
    nAno    := Year(dDataBase)

    aSM0Fileds     := { ;
        "M0_CIDENT",;    //Posição [1]
        "M0_CGC"   ,;    //Posição [2]
        "M0_NOMECOM",;   //Posição [3]
        "M0_ENDENT",;    //Posição [4]
        "M0_BAIRENT",;   //Posição [5]
        "M0_CEPENT",;    //Posição [6]
        "M0_ESTENT";     //Posição [7]
    }

    CODDESC1 := Iif(!Empty(CODDESC1), StrZero(CODDESC1, 2), "")
    CODDESC2 := Iif(!Empty(CODDESC2), StrZero(CODDESC2, 2), "")
    CODDESC3 := Iif(!Empty(CODDESC3), StrZero(CODDESC3, 2), "")
    CODDESC4 := Iif(!Empty(CODDESC4), StrZero(CODDESC4, 2), "")
    CODDESC5 := Iif(!Empty(CODDESC5), StrZero(CODDESC5, 2), "")

    aSM0      := FWSM0Util():GetSM0Data(,, aSM0Fileds)
    aDadosSx5 := FWGetSX5 ('CC')

	//Tratamento LGPD
	aCposLGPD   :=  {"A1_NOME","A1_END","A1_BAIRRO","A1_MUN","A1_CEP","A1_EST","A2_NOME","A2_END","A4_END","A2_BAIRRO","A2_CEP","A2_MUN","A2_EST"}

    aPDFields := FwProtectedDataUtil():UsrAccessPDField(__cUserID, aCposLGPD )
	lObfuscated := len( aPDFields ) != Len(aCposLGPD)

    //Irá montar as queries do objeto de negócio
    self:FatSV024CreateSQL()

    //Seta indices das tabelas para seek
    SA1->(DbSetOrder(1))
    SA2->(DbSetOrder(1))

 	DbSelectArea(self:cAlias)

	(self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())

        cNome		:= ""
        cEndereco	:= ""
        cBairro		:= ""
        cCep		:= ""
        cCidade		:= ""
        cEstado		:= ""

        nX++

        IF ( (self:cAlias)->TIPO $ "BD" .And. ENTRSAID == 2 ) .Or.  ( ENTRSAID == 1 .And. !(self:cAlias)->TIPO $ "BD" )
            //Se for Nota de entrada, beneficiamento ou devolução irá buscar pelo fornecedor
            IF SA2->(MsSeek( FatSVFilial("SA2", "A2_FILIAL", (self:cAlias)->FILIAL)+(self:cAlias)->DEST+(self:cAlias)->LOJA))
                cNome		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_NOME),SA2->A2_NOME)  
                cEndereco	:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_END),SA2->A2_END)
                cBairro		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_BAIRRO) ,SA2->A2_BAIRRO)
                cCep		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_CEP), SA2->A2_CEP)
                cCidade		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_MUN), SA2->A2_MUN)
                cEstado		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA2->A2_EST), SA2->A2_EST)
            EndIF
        Else
            IF SA1->(MsSeek( FatSVFilial("SA1", "A1_FILIAL", (self:cAlias)->FILIAL)+(self:cAlias)->DEST+(self:cAlias)->LOJA))
                cNome		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_NOME), SA1->A1_NOME ) 
                cEndereco	:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_END), SA1->A1_END)
                cBairro		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_BAIRRO), SA1->A1_BAIRRO) 
                cCep		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_CEP), SA1->A1_CEP) 
                cCidade		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_MUN), SA1->A1_MUN)  
                cEstado		:= IIF(lObfuscated, FwProtectedDataUtil():ValueAsteriskToAnonymize(SA1->A1_EST), SA1->A1_EST)                                      
            EndIF
        EndIF

        //Montagem dos dados
        self:jItems := JsonObject():new()
        self:jItems['CHAVE']        := Alltrim(Str(nx))
        self:jItems["LOCALDATA"]    := AllTrim(aSM0[1][2]) + ", " + StrZero(ndia,2) + " de " + AllTrim(cMes) + " de " + StrZero(nAno,4) //M0_CIDENT

        //Dados do Emissor
        self:jItems["EMISCNPJ"]     := TransForm(aSM0[2][2],"@!R NN.NNN.NNN/NNNN-99") //M0_CGC   
        self:jItems["EMISNOME"]     := AllTrim(aSM0[3][2]) //M0_NOMECOM
        self:jItems["EMISRUANUM"]   := AllTrim(aSM0[4][2]) //M0_ENDENT
        self:jItems["EMISBAIRRO"]   := AllTrim(aSM0[5][2]) //M0_BAIRENT
        self:jItems["EMISCEP"]      := TransForm(aSM0[6][2],"@R 99999-99") //M0_CEPENT
        self:jItems["EMISMUN"]      := AllTrim(aSM0[1][2]) //M0_CIDENT
        self:jItems["EMISUF"]       := AllTrim(aSM0[7][2]) //M0_ESTENT

        self:jItems["CLIFORNOME"]   := cNome    
        self:jItems["CLIFOREND"]    := cEndereco
        self:jItems["CLIFORBAIRRO"] := cBairro
        self:jItems["CLIFORCEP"]    := cCep
        self:jItems["CLIFORMUN"]    := cCidade
        self:jItems["CLIFOREST"]    := cEstado

        self:jItems["NFNUMERO"]     := Alltrim((self:cAlias)->DOC)
        self:jItems["NFSERIE"]      := Alltrim((self:cAlias)->SERIE)
        self:jItems["NFDATE"]       := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->EMISSAO)
        self:jItems["NFENTRADA"]    := IIF(ENTRSAID == 2, '', 'X')
        self:jItems["NFSAIDA"]      := IIF(ENTRSAID == 2, 'X', '')

        For nz := 1 to 5
            self:jItems["CCODDESC"+Alltrim(Str(nz))]   := &("CODDESC" + Alltrim(Str(nz)))
            self:jItems["CCORRECAO0"+Alltrim(Str(nz))] := DECODEUTF8(&('CORRECAO0' + Alltrim(Str(nz))))
        Next nz

        //Mantenho pois é necessário para impressão correta do objeto
        self:jItems["CODAJUST"]     :=  aDadosSX5[1][3]
        self:jItems["DESCAJUST"]    :=  aDadosSX5[1][4]

        self:processData(1)
        self:oData:appendData(self:jItems)

        For ny := 2 to Len(aDadosSx5)
            self:jItems := JsonObject():new()

            self:jItems['CHAVE']        := Alltrim(Str(nx))
            self:jItems["CODAJUST"]     := aDadosSX5[ny][3]
            self:jItems["DESCAJUST"]    := aDadosSX5[ny][4]

            For nz := 1 to 5
                self:jItems["CCODDESC"+Alltrim(Str(nz))]   := &("CODDESC" + Alltrim(Str(nz)))
                self:jItems["CCORRECAO0"+Alltrim(Str(nz))] := DECODEUTF8(&('CORRECAO0' + Alltrim(Str(nz))))
            Next nz

            self:processData(1)
            self:oData:appendData(self:jItems)
        Next ny

        (self:cAlias)->(DbSkip())

    EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aSm0,0)
    aSize(aSM0Fileds,0)
    aSize(aPDFields,0)
    aSize(aCposLGPD,0)
    aSize(aDadosSx5,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class lettercorrectionSmartViewBusinessObject 

    //Local e data
    self:oSchema:addProperty("LOCALDATA"   , STR0004, "string", STR0004, "LOCALDATA")    //"Local e data"

    //Tabela de códigos e suas descrições.
    self:oSchema:addProperty("CODAJUST"    , STR0015, "string", STR0015, "CODAJUST")     //"Códigos de Ajuste"
    self:oSchema:addProperty("DESCAJUST"   , STR0016, "string", STR0016, "DESCAJUST")    //"Descrição dos Códigos de Ajuste"

    //Dados do emissor da carta
    self:oSchema:addProperty("EMISCNPJ"    , STR0017, "string", STR0017, "EMISCNPJ")    //"CNPJ Emissor"
    self:oSchema:addProperty("EMISNOME"    , STR0018, "string", STR0018, "EMISNOME")    //"Emissor Nome"
    self:oSchema:addProperty("EMISRUANUM"  , STR0019, "string", STR0019, "EMISEND" )    //"Emissor Rua e Número"
    self:oSchema:addProperty("EMISBAIRRO"  , STR0020, "string", STR0020, "EMISBAIRRO" ) //"Emissor Bairro"
    self:oSchema:addProperty("EMISCEP"     , STR0021, "string", STR0021, "EMISBAIRRO" ) //"Emissor CEP"
    self:oSchema:addProperty("EMISMUN"     , STR0022, "string", STR0022, "EMISMUN" )    //"Emissor Municipio"
    self:oSchema:addProperty("EMISUF"      , STR0023, "string", STR0023, "EMISMUN" )    //"Emissor UF"

    //Dados da Nota Fiscal
    self:oSchema:addProperty("NFNUMERO"    , STR0024, "string", STR0024, "NFNUMERO")    //"NF Numero"
    self:oSchema:addProperty("NFSERIE"     , STR0025, "string", STR0025, "NFSERIE")     //"NF Série"
    self:oSchema:addProperty("NFDATE"      , STR0026, "date"  , STR0026, "NFDATE")      //"NF Emissão"
    self:oSchema:addProperty("NFENTRADA"   , STR0027, "string", STR0027, "NFENTRADA")   //"Entrada"
    self:oSchema:addProperty("NFSAIDA"     , STR0028, "string", STR0028, "NFSAIDA")     //"Saída"

    //Dados do cliente/fornecedor
    self:oSchema:addProperty("CLIFORNOME"  , STR0029, "string", STR0029, "CLIFORNOME")   //"CliFor Nome"
    self:oSchema:addProperty("CLIFOREND"   , STR0030, "string", STR0030, "CLIFOREND")    //"CliFor Endereço"
    self:oSchema:addProperty("CLIFORBAIRRO", STR0031, "string", STR0031, "CLIFORBAIRRO") //"CliFor Bairro"
    self:oSchema:addProperty("CLIFORCEP"   , STR0032, "string", STR0032, "CLIFORCEP")    //"CliFor CEP"
    self:oSchema:addProperty("CLIFORMUN"   , STR0033, "string", STR0033, "CLIFORMUN")    //"CliFor Município"
    self:oSchema:addProperty("CLIFOREST"   , STR0034, "string", STR0034, "CLIFOREST")    //"CliFor UF"

    self:oSchema:addProperty("CHAVE"       , STR0035 , "string", STR0035, "CHAVE")       //"CHAVE"

    //CORRECOES
    self:oSchema:addProperty("CCODDESC1"  , STR0010, "string", STR0010, "CCODDESC1")     //"Cod. Correção 01"
    self:oSchema:addProperty("CCODDESC2"  , STR0011, "string", STR0011, "CCODDESC2")     //"Cod. Correção 02"
    self:oSchema:addProperty("CCODDESC3"  , STR0012, "string", STR0012, "CCODDESC3")     //"Cod. Correção 03"
    self:oSchema:addProperty("CCODDESC4"  , STR0013, "string", STR0013, "CCODDESC4")     //"Cod. Correção 04"
    self:oSchema:addProperty("CCODDESC5"  , STR0014, "string", STR0014, "CCODDESC5")     //"Cod. Correção 05"

    self:oSchema:addProperty("CCORRECAO01", STR0005, "string", STR0005, "CCORRECAO01")   //"Cod. Correção 01"
    self:oSchema:addProperty("CCORRECAO02", STR0006, "string", STR0006, "CCORRECAO02")   //"Cod. Correção 02"
    self:oSchema:addProperty("CCORRECAO03", STR0007, "string", STR0007, "CCORRECAO03")   //"Cod. Correção 03"
    self:oSchema:addProperty("CCORRECAO04", STR0008, "string", STR0008, "CCORRECAO04")   //"Cod. Correção 04"
    self:oSchema:addProperty("CCORRECAO05", STR0009, "string", STR0009, "CCORRECAO05")   //"Cod. Correção 05"

    //Adiciona os parâmetros do relatório
    self:oSchema:addParameter("MV_FIL"    , STR0036 + " ?", "string", .F.,,,,,, FTSVHelp(,,,.T.))       //Filial ?
    self:oSchema:addParameter("ENTRSAID"  , STR0027 + "/" + STR0028 + " ?", "number", .F.,,,,,,STR0043) //Entrada/Saida ?
    self:oSchema:addParameter("NUMERONF"  , STR0037 + " ?", "string", .F.,,,,,, STR0041)                //Nummero da NF ?
    self:oSchema:addParameter("SERIENF"   , STR0038 + " ?", "string", .F.,,,,,, STR0042)                //Serie ?
    self:oSchema:addParameter("CODDESC1"  , STR0010 + " ?", "number", .F.,,,,,, STR0040 )               //Cod. Correção 01 ?
    self:oSchema:addParameter("CORRECAO01", STR0005 + " ?", "string", .F.,,,,,, STR0039 )               //Correção 01 ?
    self:oSchema:addParameter("CODDESC2"  , STR0011 + " ?", "number", .F.,,,,,, STR0040 )               //Cod. Correção 02 ?
    self:oSchema:addParameter("CORRECAO02", STR0006 + " ?", "string", .F.,,,,,, STR0039 )               //Correção 02 ?
    self:oSchema:addParameter("CODDESC3"  , STR0012 + " ?", "number", .F.,,,,,, STR0040 )               //Cod. Correção 03 ?
    self:oSchema:addParameter("CORRECAO03", STR0007 + " ?", "string", .F.,,,,,, STR0039 )               //Correção 03 ? 
    self:oSchema:addParameter("CODDESC4"  , STR0013 + " ?", "number", .F.,,,,,, STR0040 )               //Cod. Correção 04 ?
    self:oSchema:addParameter("CORRECAO04", STR0008 + " ?", "string", .F.,,,,,, STR0039 )               //Correção 04 ?
    self:oSchema:addParameter("CODDESC5"  , STR0014 + " ?", "number", .F.,,,,,, STR0040 )               //Cod. Correção 05 ?
    self:oSchema:addParameter("CORRECAO05", STR0009 + " ?", "string", .F.,,,,,, STR0039 )               //Correção 05 ?

	If __LookUp
        self:setCustomURL("MV_FIL",    "api/framework/v1/genericLookupService/smartview/SM0", 2)
        self:setCustomURL("ENTRSAID",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/1", 1)
        self:setCustomURL("CODDESC1",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/2", 1)
        self:setCustomURL("CODDESC2",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/2", 1)
        self:setCustomURL("CODDESC3",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/2", 1)
        self:setCustomURL("CODDESC4",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/2", 1)
        self:setCustomURL("CODDESC5",  "api/faturamento/smartview/v1/options/fat/lettercorrection/SV024Cbo/2", 1)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVComboFATSV024
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SV024Cbo(cOpcao as character) as array

	Local aRet := {} as array

	If cOpcao == "1"
		aRet := FatGetCboX1('MTR185', 3)
    ElseIf cOpcao == "2"
        aRet := FatGetX5('CC')
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv024CreateSQL
Retorna toda a query montada

@return Nil

@author FAT/CRM
@since 11/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV024CreateSQL() Class lettercorrectionSmartViewBusinessObject

    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object

    FatGetMVFiliais(MV_FIL)

    // Verifica se é documento de entrada ou saída para montar a query
    If ENTRSAID == 2

        cFieldsQry := "F2_FILIAL FILIAL,F2_DOC DOC," + SerieNfId("SF2", 3, "F2_SERIE") + " SERIE,F2_CLIENTE DEST,F2_LOJA LOJA,F2_EMISSAO EMISSAO,F2_TIPO TIPO,F2_SDOC SDOC"
        cFieldsQry += self:cSelectLoc

        cQuery += "SELECT ? "
        cQuery += " FROM " + RetSqlName("SF2") + " SF2 "
        cQuery += "WHERE SF2.F2_FILIAL  = ? "
        cQuery += " AND  SF2.F2_DOC     = ? "
        cQuery += " AND  ? = ? "
        cQuery += " ? "
        cQuery += " AND SF2.D_E_L_E_T_ = ? "

        oQuery := FwExecStatement():New(ChangeQuery(cQuery))

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial("SF2", "F2_FILIAL", FatGetMVFiliais(MV_FIL)[1]))
        oQuery:SetString(nParam++, NUMERONF)
        oQuery:SetUnsafe(nParam++, SerieNfId("SF2",3,"F2_SERIE") )
        oQuery:SetString(nParam++, SERIENF)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
		oQuery:SetString(nParam++, " ")

    Else

        cFieldsQry := 'F1_FILIAL FILIAL,F1_DOC DOC,' + SerieNfId("SF1", 3, "F1_SERIE") + " SERIE,F1_FORNECE DEST,F1_LOJA LOJA,F1_EMISSAO EMISSAO,F1_TIPO TIPO,F1_SDOC SDOC"
        cFieldsQry += self:cSelectLoc

        cQuery += "SELECT ? "
        cQuery += " FROM " + RetSqlName("SF1") + " SF1 "
        cQuery += "WHERE SF1.F1_FILIAL  = ? "
        cQuery += " AND  SF1.F1_DOC     = ? "
        cQuery += " AND  ? = ? "
        cQuery += " ? "
        cQuery += " AND SF1.D_E_L_E_T_ = ? "

        oQuery := FwExecStatement():New(ChangeQuery(cQuery))

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial("SF1", "F1_FILIAL", FatGetMVFiliais(MV_FIL)[1]))
        oQuery:SetString(nParam++, NUMERONF)
        oQuery:SetUnsafe(nParam++, SerieNfId("SF1",3,"F1_SERIE") )
        oQuery:SetString(nParam++, SERIENF)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
		oQuery:SetString(nParam++, " ")

    EndIf

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

Return
