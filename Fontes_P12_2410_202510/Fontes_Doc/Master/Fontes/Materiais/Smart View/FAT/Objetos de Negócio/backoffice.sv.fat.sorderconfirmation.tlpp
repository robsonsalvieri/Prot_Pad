#Include "protheus.ch"
#Include "FWLIBVERSION.CH"
#Include "TOTVS.CH"
#Include "MSOBJECT.CH"
#Include "TOTVS.FRAMEWORK.TREPORTS.INTEGRATEDPROVIDER.TH"
#Include "TLPP-REST.TH"
#Include "TLPP-CORE.TH"
#Include "BACKOFFICE.SV.FAT.SORDERCONFIRMATION.CH"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.sorderconfirmation.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SC5,SC6", name="Pré-Nota", country="ALL", customTables="SC5,SC6")

/*/{Protheus.doc} sorderconfirmationSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para a relação de pré-notas
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
class sorderconfirmationSmartViewBusinessObject from IntegratedProvider
    
    public method new() 		 as object
    public method getData() 	 as object
    public method getSchema() 	 as object
	public method SVImpCabecR4() as array
	public method SVMtr730Cli()
	public method SVFisGetInit()
	public method FatSV012CreateSQL()
 
    protected data aFields 		as array
    protected data aStruct 		as array
	protected data aCabPed		as array
	protected data aFisGet    	as array
	protected data aFisGetSC5 	as array
	protected data aPDFields 	as array
	protected data aCpoCustom 	as array
	protected data aSM0 		as array
	protected data cWhereLoc	as character
	protected data cAlias		as character
	protected data cAliasSC6	as character
	protected data cAliasLoc	as character
	protected data cSelectLoc   as character
	protected data cCNPJ		as character
	protected data cCGC			as character
	protected data cInscr		as character
	protected data lObfuscated  as logical
	protected data nValMerc		as numeric
	protected data nDesconto	as numeric
	protected data nPesoTot		as numeric
	protected data nFrete		as numeric
	protected data nValImp		as numeric
	protected data nItem		as numeric
	protected data jItems       as json
	protected data oQrySC6 	   as object
 
Endclass
 
/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
Method new() class sorderconfirmationSmartViewBusinessObject
	
	_Super:new()
    self:appendArea(STR0003) 	  //"Faturamento"
    self:setDisplayName(STR0001)  //"Pré-Nota"
    self:setDescription(STR0002)  //"Relação de Pré-Nota"
    self:setIsLookUp(.T.)  		  //Habilita o Lookup

	self:aCabPed  		:= {}
	self:aStruct  		:= {}
	self:aFisGet  		:= {}
	self:aFisGetSC5  	:= {}
	self:aPDFields  	:= {}
	self:aCpoCustom  	:= {}
	self:aSM0  			:= {}
	self:jItems  		:= Nil

Return self
 
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object class sorderconfirmationSmartViewBusinessObject

    Local aPedCli   	:= {} as array
    Local aC5Rodape  	:= {} as array
    Local aValMerc	 	:= {} as array
	Local aItemPed	 	:= {} as array
	Local aRodapR4	 	:= {} as array
	Local aCabecR4		:= {} as array
	Local aCabPV 		:= {} as array
	Local aCpoPers 		:= {} as array
    Local aRelImp   	:= MaFisRelImp("MT100",{"SF2","SD2"}) as array
	Local aFieldsLGPD	:= {"A1_NOME","A1_ENDENT","A1_END","A1_CEPE","A1_CEP","A1_CGC","A2_NOME","A2_END","A2_CGC","A3_NOME"} as array

    Local cPedido    	:= "" as character
    Local cCliEnt	 	:= "" as character
    Local cNfOri     	:= "" as character
    Local cSeriOri   	:= "" as character
	Local cChvList  	:= "" as character

    Local lLGPD	 		:= .F. as logical
    Local lDate	 		:= .F. as logical
	Local lnDescTP	 	:= .F. as logical
	Local lRndIss	 	:= .F. as logical
	Local lTpDpInd	 	:= .F. as logical

    Local nDesconto  	:= 0 as numeric
    Local nPesLiq    	:= 0 as numeric
    Local nG		 	:= 0 as numeric
    Local nFrete	 	:= 0 as numeric
    Local nSeguro	 	:= 0 as numeric
    Local nFretAut	 	:= 0 as numeric
    Local nDespesa	 	:= 0 as numeric
    Local nDescCab	 	:= 0 as numeric
    Local nPDesCab	 	:= 0 as numeric
    Local nY         	:= 0 as numeric
    Local nValMerc   	:= 0 as numeric
    Local nPrcLista  	:= 0 as numeric
    Local nAcresFin  	:= 0 as numeric
    Local nT		 	:= 0 as numeric
    Local nPesoTot	 	:= 0 as numeric
    Local nVlrPesoU	 	:= 0 as numeric
    Local nRecSD1SD2 	:= 0 as numeric
	Local nValImp	 	:= 0 as numeric
	Local nItem		 	:= 0 as numeric
	Local nScan		 	:= 0 as numeric
	Local nQuantPV		:= 0 as numeric
	Local nValToIPI 	:= 0 as numeric
	Local nSeq 			:= 1 as numeric
	Local nC 			:= 1 as numeric
	Local nTotDesc   	:= 0 as numeric
    Local nTamPrcVen 	:= TamSX3("C6_PRCVEN")[2] as numeric
	Local xValue	 	:= Nil

	MV_FIL := ""
	CPEDDE := ""
	CPEDATE := ""

	self:aCpoCustom := self:getCustomFields() 
	self:aSM0 := FWSM0Util():GetSM0Data(cEmpAnt,cFilAnt)
	self:lObfuscated := .F.

	SuperGetMv()//Limpa o cache dos parametro primeiro
	lnDescTP := SuperGetMv("MV_NDESCTP",.F.,.F.)
	lRndIss	 := SuperGetMv("MV_RNDISS",,.F.)
	lTpDpInd := SuperGetMv("MV_TPDPIND",.F.,.F.) == '1'

	SB1->(dbSetOrder(1)) //B1_FILIAL, B1_COD
	SD2->(dbSetOrder(3)) //D2_FILIAL, D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_COD, D2_ITEM
	SD1->(dbSetOrder(1)) //D1_FILIAL, D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, D1_COD, D1_ITEM

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR730")

    //Verifica se precisa fazer o tratamento para LGPD
    self:aPDFields   := FatGetfieldsLGPD(aFieldsLGPD, self:aCpoCustom )
    self:lObfuscated := len( self:aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
	self:FatSV012CreateSQL()

	DbSelectArea((self:cAlias))
    ((self:cAlias))->(DbGoTop())

    While !((self:cAlias))->(Eof())

		cCliEnt := IIf(!Empty(((self:cAlias))->(FieldGet(FieldPos("C5_CLIENT")))),((self:cAlias))->C5_CLIENT,((self:cAlias))->C5_CLIENTE)
        aCabPV := {}

        MaFisIni(cCliEnt,;									// 1-Codigo Cliente/Fornecedor
            	((self:cAlias))->C5_LOJACLI,;				// 2-Loja do Cliente/Fornecedor
            	IIf(((self:cAlias))->C5_TIPO$'DB',"F","C"),;// 3-C:Cliente , F:Fornecedor
            	((self:cAlias))->C5_TIPO,;					// 4-Tipo da NF
            	((self:cAlias))->C5_TIPOCLI,;				// 5-Tipo do Cliente/Fornecedor
            	aRelImp,;									// 6-Relacao de Impostos que suportados no arquivo
            	,;						   					// 7-Tipo de complemento
            	,;											// 8-Permite Incluir Impostos no Rodape .T./.F.
            	"SB1",;										// 9-Alias do Cadastro de Produtos - ("SBI" P/ Front Loja)
            	"MATA461",;									// 10-Nome da rotina que esta utilizando a funcao
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				Nil,;
				IIf(FindFunction("ChkTrbGen"), ChkTrbGen("SD2","D2_IDTRIB"), .F.))						
        
		//Cálculo de impostos Localizado
        self:processData(2)

        nFrete		:= ((self:cAlias))->C5_FRETE
        nSeguro		:= ((self:cAlias))->C5_SEGURO
        nFretAut	:= ((self:cAlias))->C5_FRETAUT
        nDespesa	:= ((self:cAlias))->C5_DESPESA
        nDescCab	:= ((self:cAlias))->C5_DESCONT
        nPDesCab	:= ((self:cAlias))->C5_PDESCAB

        aItemPed:= {}
        aCabPV := {	((self:cAlias))->C5_TIPO	,;
            		((self:cAlias))->C5_CLIENTE	,;
            		((self:cAlias))->C5_LOJACLI	,;
            		((self:cAlias))->C5_TRANSP	,;
            		((self:cAlias))->C5_CONDPAG	,;
            		((self:cAlias))->C5_EMISSAO	,;
            		((self:cAlias))->C5_NUM		,;
            		((self:cAlias))->C5_VEND1	,;
            		((self:cAlias))->C5_VEND2	,;
            		((self:cAlias))->C5_VEND3	,;
            		((self:cAlias))->C5_VEND4	,;
            		((self:cAlias))->C5_VEND5	,;
            		((self:cAlias))->C5_COMIS1	,;
            		((self:cAlias))->C5_COMIS2	,;
            		((self:cAlias))->C5_COMIS3	,;
            		((self:cAlias))->C5_COMIS4	,;
            		((self:cAlias))->C5_COMIS5	,;
            		((self:cAlias))->C5_FRETE	,;
            		((self:cAlias))->C5_TPFRETE	,;
            		((self:cAlias))->C5_SEGURO	,;
            		((self:cAlias))->C5_TABELA	,;
            		((self:cAlias))->C5_VOLUME1	,;
            		((self:cAlias))->C5_ESPECI1	,;
            		((self:cAlias))->C5_MOEDA	,;
            		((self:cAlias))->C5_REAJUST	,;
            		((self:cAlias))->C5_BANCO	,;
            		((self:cAlias))->C5_ACRSFIN	;
            }

        nTotQtd 	:= 0
        nTotVal 	:= 0
        nPesBru		:= 0
        nPesLiq		:= 0
        aPedCli		:= {}
        cPedido		:= ((self:cAlias))->C5_NUM
        aC5Rodape	:= {}
		nTotDesc	:= 0
        
        aadd(aC5Rodape,{((self:cAlias))->C5_PBRUTO,((self:cAlias))->C5_PESOL,((self:cAlias))->C5_DESC1,((self:cAlias))->C5_DESC2,;
            ((self:cAlias))->C5_DESC3,((self:cAlias))->C5_DESC4,((self:cAlias))->C5_MENNOTA})

        aPedCli := self:SVMtr730Cli()

        DbSelectArea((self:cAlias))
        For nY := 1 to Len(self:aFisGetSC5)
            If !Empty(&(self:aFisGetSC5[ny][2]))
            	MaFisAlt(self:aFisGetSC5[ny][1], IIF(self:aFisGetSC5[ny][1] == "NF_SUFRAMA", Iif(&(self:aFisGetSC5[ny][2]) == "1",.T.,.F.), &(self:aFisGetSC5[ny][2])), Len(aItemPed), .T.)	
            Endif
        Next nY

        While !(((self:cAliasSC6))->(Eof())) .And. (self:cAlias)->C5_FILIAL==((self:cAliasSC6))->C6_FILIAL .And. ((self:cAliasSC6))->C6_NUM == cPedido

            nRecSD1SD2 := 0
            nDesconto  := 0

            // Se o pedido for do tipo Complemento, posiciona na SD2, caso
            // contrário, posiciona na SD1
            If !Empty(((self:cAliasSC6))->C6_NFORI)

                If ((self:cAlias))->C5_TIPO == "C"
                    If SD2->(MsSeek((self:cAlias)->C5_FILIAL+((self:cAliasSC6))->C6_NFORI+((self:cAliasSC6))->C6_SERIORI+((self:cAliasSC6))->C6_CLI+((self:cAliasSC6))->C6_LOJA+;
                        ((self:cAliasSC6))->C6_PRODUTO+((self:cAliasSC6))->C6_ITEMORI))
                        nRecSD1SD2 := SD2->(RECNO())
                    Endif
                Else
                    If SD1->(MsSeek((self:cAlias)->C5_FILIAL+((self:cAliasSC6))->C6_NFORI+((self:cAliasSC6))->C6_SERIORI+((self:cAliasSC6))->C6_CLI+((self:cAliasSC6))->C6_LOJA+;
                        ((self:cAliasSC6))->C6_PRODUTO+((self:cAliasSC6))->C6_ITEMORI))
                        nRecSD1SD2 := SD1->(RECNO())
                    Endif
                Endif

                cNfOri := ((self:cAliasSC6))->C6_NFORI
                cSeriOri := ((self:cAliasSC6))->C6_SERIORI

            Endif

            DbSelectArea((self:cAliasSC6))

            // Calcula o preco de lista                     
            nValMerc  := ((self:cAliasSC6))->C6_VALOR
            nPrcLista := ((self:cAliasSC6))->C6_PRUNIT

            If ( nPrcLista == 0 )
                nPrcLista := NoRound(nValMerc/((self:cAliasSC6))->C6_QTDVEN,nTamPrcVen)
			ElseIf lnDescTP .And. ((self:cAliasSC6))->C6_VALDESC  == 0 //Tratamento para configuração do MV_NDESCTP como feito pela planilha.
				nPrcLista := A410Arred(nValMerc / ((self:cAliasSC6))->C6_QTDVEN ,"D2_TOTAL")
            Endif

            nAcresFin := A410Arred(((self:cAliasSC6))->C6_PRCVEN*((self:cAlias))->C5_ACRSFIN/100,"D2_PRCVEN")
            nValMerc  += A410Arred(((self:cAliasSC6))->C6_QTDVEN*nAcresFin,"D2_TOTAL")		
            nDesconto := a410Arred(nPrcLista*((self:cAliasSC6))->C6_QTDVEN,"D2_DESCON")-nValMerc
            nDesconto := IIf(nDesconto==0,((self:cAliasSC6))->C6_VALDESC,nDesconto)
            nDesconto := Max(0,nDesconto)
            nPrcLista += nAcresFin
            
			//Calcula o preco de lista Localizado
			self:nValMerc	:= nValMerc
			self:nDesconto	:= nDesconto
			self:processData(3)
			
			nValMerc := self:nValMerc
            
            MaFisAdd(((self:cAliasSC6))->C6_PRODUTO	,;	// 1-Codigo do Produto ( Obrigatorio )
                ((self:cAliasSC6))->C6_TES			,;	// 2-Codigo do TES ( Opcional )
                ((self:cAliasSC6))->C6_QTDVEN		,;	// 3-Quantidade ( Obrigatorio )
                nPrcLista						    ,;	// 4-Preco Unitario ( Obrigatorio )
                nDesconto						    ,;	// 5-Valor do Desconto ( Opcional )
                cNfOri							    ,;	// 6-Numero da NF Original ( Devolucao/Benef )
                cSeriOri						    ,;	// 7-Serie da NF Original ( Devolucao/Benef )
                nRecSD1SD2						    ,;	// 8-RecNo da NF Original no arq SD1/SD2
                0								    ,;	// 9-Valor do Frete do Item ( Opcional )
                0								    ,;	// 10-Valor da Despesa do item ( Opcional )
                0								    ,;	// 11-Valor do Seguro do item ( Opcional )
                0								    ,;	// 12-Valor do Frete Autonomo ( Opcional )
                nValMerc						    ,;	// 13-Valor da Mercadoria ( Obrigatorio )
                0								    ,;	// 14-Valor da Embalagem ( Opiconal )
                0								    ,;	// 15-RecNo do SB1
                0									,;	// 16-RecNo do SF4
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,;
				Nil									,; 
				((self:cAliasSC6))->C6_OPER 		,; //33-Tipo de Operação Fiscal
				)
        
            aadd(aItemPed,{ ((self:cAliasSC6))->C6_ITEM		,;
                			((self:cAliasSC6))->C6_PRODUTO	,;
                			((self:cAliasSC6))->C6_DESCRI	,;
                			((self:cAliasSC6))->C6_TES		,;
                			((self:cAliasSC6))->C6_CF		,;
                			((self:cAliasSC6))->C6_UM		,;
                			((self:cAliasSC6))->C6_QTDVEN	,;
                			((self:cAliasSC6))->C6_PRCVEN	,;
                			((self:cAliasSC6))->C6_NOTA		,;
                			((self:cAliasSC6))->C6_SERIE	,;
                			((self:cAliasSC6))->C6_CLI		,;
                			((self:cAliasSC6))->C6_LOJA		,;
               				((self:cAliasSC6))->C6_VALOR	,;
                			((self:cAliasSC6))->C6_ENTREG	,;
                			((self:cAliasSC6))->C6_DESCONT	,;
                			((self:cAliasSC6))->C6_LOCAL	,;
              	  			((self:cAliasSC6))->C6_QTDEMP	,;
                			((self:cAliasSC6))->C6_QTDLIB	,;
                			((self:cAliasSC6))->C6_QTDENT	,;
                		})							

			//String de Filtro
			cChvList := ((self:cAliasSC6))->C6_FILIAL+((self:cAliasSC6))->C6_NUM

            // Forca os valores de impostos que foram informados no SC6
            DbSelectArea((self:cAliasSC6))
            For nY := 1 to Len(self:aFisGet)
                If !Empty(&(self:aFisGet[ny][2]))
                    MaFisAlt(self:aFisGet[ny][1],&(self:aFisGet[ny][2]),Len(aItemPed))
                Endif
            Next nY

            // Calculo do ISS                              
			If ( ((self:cAlias))->C5_INCISS == "N" .And. ((self:cAlias))->C5_TIPO == "N")
				If ( (self:cAlias)->F4_ISS == "S" )

					nPrcLista := a410Arred(nPrcLista/(1-(MaAliqISS(Len(aItemPed))/100)),"D2_PRCVEN")

					If lRndIss	
						//Quando configurado para arredondar ISS, é nescessário calcular ISS por item
						//conforme realizado na função Ma410Impos (Planilha Financeira-MATA410) e MaPvPrcIt (MATA461)
						nValMerc  := a410Arred((nValMerc-nDesconto) / ((self:cAliasSC6))->C6_QTDVEN/(1-(MaAliqISS(Len(aItemPed))/100)) + nDesconto) * ((self:cAliasSC6))->C6_QTDVEN
						nValMerc  := a410Arred(nValMerc,"D2_PRCVEN")
					Else
						nValMerc  := (nValMerc-nDesconto)/(1-(MaAliqISS(Len(aItemPed))/100)) + nDesconto
					Endif
					
					MaFisAlt("IT_PRCUNI",nPrcLista,Len(aItemPed))
					MaFisAlt("IT_VALMERC",nValMerc,Len(aItemPed))

				Endif
			Endif

			If lTpDpInd .Or. (!lTpDpInd .And. (self:cAlias)->F4_DUPLIC == "S")
				nTotDesc += MaFisRet(Len(aItemPed),"IT_DESCONTO")
			Endif
            
            // Altera peso para calcular frete             
			If SB1->(MsSeek((self:cAliasSC6)->B1_FILIAL+(self:cAliasSC6)->C6_PRODUTO)) .And. SB1->B1_PESO > 0	
                MaFisAlt("IT_PESO",((self:cAliasSC6))->C6_QTDVEN*SB1->B1_PESO,Len(aItemPed))
                MaFisAlt("IT_PRCUNI",nPrcLista,Len(aItemPed))
            Endif

            aAdd(aValMerc,{nValMerc,Len(aItemPed)})

            // Somatória do Peso do Produto
            nVlrPesoU := ( ((self:cAliasSC6))->C6_QTDVEN*SB1->B1_PESO )
            nPesoTot += nVlrPesoU

			// Utilizado para inclusão dos campos personalizados
			For nY := 1 To Len(self:aCpoCustom)
				
				xValue 	:= self:aStruct[nY][5]
				lLGPD 	:= self:lObfuscated .and. (nScan := aScan(self:aPDFields,  {|x| x[1] == xValue } ) )  > 0
				lDate 	:= AllTrim(Upper(self:aStruct[nY][3])) == "DATE"

				xValue := IIF(lLGPD,; 
								self:aPDFields[nScan][2],;
								IIF(lDate,;
									IIF(ValType(&(xValue)) == "C",;
									totvs.framework.treports.date.stringToTimeStamp(((self:cAlias))->&(xValue)),;
									totvs.framework.treports.date.dateToTimeStamp(((self:cAlias))->&(xValue))),;
								((self:cAlias))->&(xValue)))

				aAdd(aCpoPers,{self:aStruct[nY][1], xValue})
				
			Next nY

            ((self:cAliasSC6))->(DbSkip())

        Enddo

		//Tratamento Localizado para o Peso e frete
        self:nPesoTot := nPesoTot
		self:nFrete := nFrete
		self:processData(4)
		       
        If nSeguro > 0
            MaFisAlt("NF_SEGURO"  ,nSeguro)
        Endif

        If nFretAut > 0
            MaFisAlt("NF_AUTONOMO",nFretAut)
        Endif

        If nDespesa > 0
            MaFisAlt("NF_DESPESA" ,nDespesa)
        Endif

        If nPDesCab > 0
			MaFisAlt("NF_DESCONTO",nPDesCab:=A410Arred((MaFisRet(,"NF_VALMERC")+MaFisRet(,'NF_DESCZF')-nTotDesc)*nPDesCab/100,"C6_VALOR")+MaFisRet(,"NF_DESCONTO"))
		Endif

        If nDescCab > 0
			MaFisAlt("NF_DESCONTO",Min(MaFisRet(,"NF_VALMERC")-0.01,nPDesCab+nTotDesc+nDescCab),/*nItem*/,/*lNoCabec*/,/*nItemNao*/,IIf(lTpDpInd,.F.,.T.))
		Endif

        If nFrete > 0 .Or. nFretAut > 0  .OR. nSeguro > 0 .Or. nDespesa > 0 .Or. nDescCab > 0 .Or. nPDesCab > 0 .Or. nPesoTot > 0
            For nT := 1 To Len(aValMerc)
                MaFisAlt("IT_VALMERC",aValMerc[nT][1],aValMerc[nT][2])
            Next nT
        Endif

        aSize(aValmerc,0)
	
		// Imprime informacoes de cabecalho			                 
        aCabecR4 := self:SVImpCabecR4(aPedCli,aCabPV,(self:cAlias)->C5_FILIAL)

		// Imprime informacoes de Item				                	 
        nItem := 0

		// Array referente ao sorderconfirmation.bra.tlpp
		self:aCabPed := aCabPV

        For nG := 1 To Len(aItemPed)

            nItem += 1
			// Imprimi o valor localizado do item
			self:nItem := nItem
			self:processData(5)
			nValImp := self:nValImp
			
			SB1->(MsSeek((self:cAlias)->B1_FILIAL+aItemPed[nItem][2]))

			nTotQtd += aItemPed[nItem][07]					//C6_QTDVEN
			nTotVal += aItemPed[nItem][13]+nValImp			//C6_VALOR
			nPesLiq	+= SB1->B1_PESO * aItemPed[nItem][07]	//C6_QTDVEN
			nPesBru += SB1->B1_PESBRU * aItemPed[nItem][07]	//C6_QTDVEN
			
			self:jItems  := Nil
			self:jItems  := JsonObject():new()

			// Imprime informacoes de cabecalho 	 		             	 
			self:jItems ["CNOMEMP"] 	:= aCabecR4[1][1]	//NOME DA EMPRESA
			self:jItems ["CENDCOB"] 	:= aCabecR4[1][2]	//ENDERECO DE COBRANCA
			self:jItems ["CTEL"] 		:= aCabecR4[1][3]	//TEL:
			self:jItems ["CCNPJ"] 		:= aCabecR4[1][4]	//CGC:
			self:jItems ["CNOMCF"] 		:= aCabecR4[1][5]	//NOME DO CLIENTE/FORNECEDOR
			self:jItems ["CENDENT"] 	:= aCabecR4[1][6]	//ENDERECO DE ENTREGA
			self:jItems ["CCEP"] 		:= aCabecR4[1][7]	//CEP
			self:jItems ["CCGC"] 		:= aCabecR4[1][8]	//CNPJ
			self:jItems ["DEMISSAO"] 	:= aCabecR4[1][9]	//DATA DA EMISSAO DO PEDIDO DE VENDA
			self:jItems ["CNUMPV"] 		:= aCabecR4[1][10]	//NUMERO DO PEDIDO DE VENDA
			self:jItems ["CITEMPV"] 	:= aCabecR4[1][11]	//ITEM DO PEDIDO DE VENDA
			self:jItems ["CTRANSP"] 	:= aCabecR4[1][12]	//CODIGO DA TRANSPORTADORA (TRASNP...:)
			self:jItems ["CNOMVEND"] 	:= aCabecR4[1][13]	//NOME DO VENDEDOR
			self:jItems ["CCOMISSA"] 	:= aCabecR4[1][14]	//COMISSAO DO VENDEDOR
			self:jItems ["CCONDPG"] 	:= aCabecR4[1][15]	//CONDICAO DE PAGAMENTO
			self:jItems ["CFRETE"] 		:= aCabecR4[1][16]	//FRETE (FRETE...:)
			self:jItems ["CSEGURO"] 	:= aCabecR4[1][17]	//SEGURO (SEGURO:)
			self:jItems ["CTABELA"] 	:= aCabecR4[1][18]	//TABELA (TABELA...:)
			self:jItems ["CVOLUME"] 	:= aCabecR4[1][19]	//VOLUME (VOLUME.:)
			self:jItems ["CESPECIE"] 	:= aCabecR4[1][20]	//ESPECIE (ESPECIA:)
			self:jItems ["CREAJUSTE"] 	:= aCabecR4[1][21]	//REAJUSTE (REAJUSTE)
			self:jItems ["CMOEDA"] 		:= aCabecR4[1][22]	//MOEDA (MOEDA:)
			self:jItems ["CBANCO"] 		:= aCabecR4[1][23]	//BANCO (BANCO:)
			self:jItems ["NACRESFIN"] 	:= aCabecR4[1][24] 	//ACRESCIMO FINANCEIRO (ACRES. FIN.:
			self:jItems ["CFILCAB"] 	:= aCabecR4[1][25]	//FILIAL

			// Imprime informacoes de Itens
			nQuantPV += aItemPed[nItem][7]
			nValToIPI += aItemPed[nItem][13]+nValImp

			self:jItems ["CITEM"]		:= aItemPed[nItem][1]			//ITEM
			self:jItems ["CPRODUTO"]	:= aItemPed[nItem][2]			//PRODUTO
			self:jItems ["CDESCRPRD"] 	:= aItemPed[nItem][3]			//DESCRICAO
			self:jItems ["CTES"]		:= aItemPed[nItem][4]			//TES
			self:jItems ["CCF"] 		:= aItemPed[nItem][5]			//CF
			self:jItems ["CUM"] 		:= aItemPed[nItem][6]			//UM
			self:jItems ["NQUANT"] 		:= aItemPed[nItem][7]			//QUANT.
			self:jItems ["NPRCUNIT"] 	:= aItemPed[nItem][8]			//PRC UNITARIO
			self:jItems ["NIPI"] 		:= MaFisRet(nItem,"IT_ALIQIPI")	//IPI
			self:jItems ["NICMS"] 		:= MaFisRet(nItem,"IT_ALIQICM")	//ICMS
			self:jItems ["NISS"] 		:= MaFisRet(nItem,"IT_ALIQISS")	//ISS
			self:jItems ["NVLTOTIPI"] 	:= aItemPed[nItem][13]+nValImp	//VL.TOT.C/IPI
			self:jItems ["DENTREGA"] 	:= totvs.framework.treports.date.stringToTimeStamp(aItemPed[nItem][14])	//ENTREGA
			self:jItems ["NDESCITEM"] 	:= aItemPed[nItem][15]			//%DESCONTO
			self:jItems ["CLOC"] 		:= aItemPed[nItem][16]			//LOC.
			self:jItems ["NQTDFAT"]		:= aItemPed[nItem][17]			//QTD.A FAT.
			self:jItems ["NSALDO"] 		:= aItemPed[nItem][18]			//SALDO
			self:jItems ["NULTFAT"] 	:= aItemPed[nItem][19]			//ULT.FAT.
			self:jItems ["NTOTQTD"] 	:= nTotQtd						//C6_QTDVEN
			self:jItems ["NTOTVAL"] 	:= nTotVal						//C6_VALOR
			self:jItems ["NPESLIQ"] 	:= nPesLiq						//C6_QTDVEN
			self:jItems ["NPESBRU"] 	:= nPesBru						//C6_QTDVEN
			self:jItems ["cCHVLIST"] 	:= cChvList						//Chave de Listagem dos Itens
			self:jItems ["nQUANTPV"] 	:= nQuantPV						//Quantidade Total
			self:jItems ["nVALTOIPI"] 	:= nValToIPI					//Valor Total + IPI

			aRodapR4 := SVImpRodapR4(nPesLiq,nPesBru,aC5Rodape,.T.)

			// Imprime informacoes de imposto
			self:jItems ["NBASEICM"]	:= MaFisRet(,"NF_BASEICM")	//"BASE ICMS
			self:jItems ["NVALICM"]		:= MaFisRet(,"NF_VALICM")	//"VALOR ICMS
			self:jItems ["NBASEIPI"]	:= MaFisRet(,"NF_BASEIPI")	//"BASE IPI
			self:jItems ["NVALIPI"]		:= MaFisRet(,"NF_VALIPI")	//"VALOR IPI
			self:jItems ["NBASESOL"]	:= MaFisRet(,"NF_BASESOL")	//"BASE RETIDO
			self:jItems ["NVALSOL"] 	:= MaFisRet(,"NF_VALSOL")	//"VALOR RETIDO
			self:jItems ["NTOTAL"] 		:= MaFisRet(,"NF_TOTAL")	//"VALOR TOTAL
			self:jItems ["NBASEISS"]	:= MaFisRet(,"NF_BASEISS")	//"BASE ISS
			self:jItems ["NVALISS"] 	:= MaFisRet(,"NF_VALISS")	//"VALOR ISS

			// Imprime informacoes de Rodape
			self:jItems ["CPESOBRU"]	:= aRodapR4[1][1]	//PESO BRUTO
			self:jItems ["CPESOLIQ"] 	:= aRodapR4[1][2]	//PESO LIQUIDO
			self:jItems ["NDESC1"] 		:= aRodapR4[1][3]	//VALOR 01 - DESCONTOS
			self:jItems ["NDESC2"] 		:= aRodapR4[1][4]	//VALOR 02 - DESCONTOS
			self:jItems ["NDESC3"] 		:= aRodapR4[1][5]	//VALOR 03 - DESCONTOS
			self:jItems ["NDESC4"] 		:= aRodapR4[1][6]	//VALOR 04 - DESCONTOS	
			self:jItems ["CMSGNOTA"] 	:= aRodapR4[1][7]	//MENSAGEM PARA NOTA FISCAL

			//Utilizado para inclusão dos campos personalizados
			If !Empty(aCpoPers)
				For nC := 1 To Len(self:aCpoCustom)
					self:jItems[aCpoPers[nSeq][1]] := aCpoPers[nSeq][2]
					nSeq++
				Next nC
			Endif

			self:processData(1)
			self:oData:appendData(self:jItems)
        Next

		nQuantPV := 0
		nValToIPI := 0
		nSeq := 1
		aSize(aRodapR4,0)
		aSize(aCabecR4,0)
		aSize(aCpoPers,0)

        MaFisEnd()
		((self:cAlias))->(DbSkip())

    Enddo

	((self:cAliasSC6))->(DbCloseArea())
	((self:cAlias))->(DbCloseArea())

	//Limpeza do objeto/variável
	FreeObj(self:oQrySC6)

	aSize(aPedCli,0)
    aSize(aC5Rodape,0)
    aSize(aRelImp,0)
	aSize(aFieldsLGPD,0)
    aSize(aItemPed,0)
    aSize(aCabPV,0)
	aSize(self:aPDFields,0)
	aSize(self:aFisGet,0)
    aSize(self:aFisGetSC5,0)
    aSize(self:aCpoCustom,0)
    aSize(self:aSM0,0)

Return self:oData


/*{Protheus.doc} SVImpCabecR4
	Emissao da Pre-Nota - Cabecalho
	
	@param aPedCli, Array com dados dos itens pedidos do cliente/fornecedor
	@param aCabPV, Array com dados do cabecalho do cliente/fornecedor

	//------------------------------------------------//
	//  array aCabPV                                 //             
	//  -------------                                 //             
	//  01 C5_TIPO      10 C5_VEND3    19 C5_TPFRETE  //     
	//  02 C5_CLIENTE   11 C5_VEND4    20 C5_SEGURO   //     
	//  03 C5_LOJACLI   12 C5_VEND5    21 C5_TABELA   //    
	//  04 C5_TRANSP    13 C5_COMIS1   22 C5_VOLUME1  //     
	//  05 C5_CONDPAG   14 C5_COMIS2   23 C5_ESPECI1  //     
	//  06 C5_EMISSAO   15 C5_COMIS3   24 C5_MOEDA    //     
	//  07 C5_NUM       16 C5_COMIS4   25 C5_REAJUST  //     
	//  08 C5_VEND1     17 C5_COMIS5   26 C5_BANCO    //   
	//  09 C5_VEND2     18 C5_FRETE    27 C5_ACRSFIN  //     
	//------------------------------------------------//
 
	@return .T.
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
Method SVImpCabecR4(aPedCli as array, aCabPV as array, cFilSC5 as character)  as array class sorderconfirmationSmartViewBusinessObject

	Local aCabec	:= {} as array

	Local cMoeda	:= "" as character
	Local cPedCli  	:= "" as character
	Local cNomEmp	:= "" as character
	Local cNomCom	:= "" as character
	Local cEndCob	:= "" as character
	Local cTel		:= "" as character
	Local cCNPJ		:= "" as character
	Local cNomCF	:= "" as character
	Local cEndEnt	:= "" as character
	Local cCEP		:= "" as character
	Local cCGC		:= "" as character
	Local cNumPV	:= "" as character
	Local cItemPV	:= "" as character
	Local cTransp	:= "" as character 
	Local cNomVend 	:= "" as character //"Vendedor" (STR0008)
	Local cCondPg	:= "" as character
	Local cFrete	:= "" as character
	Local cSeguro	:= "" as character
	Local cTabela	:= "" as character
	Local cVolume	:= "" as character
	Local cEspecie 	:= "" as character
	Local cReajuste := "" as character
	Local cBanco	:= "" as character
	Local cComissa 	:= "" as character //"Comissão" (STR0009)
	Local cFilCab	:= cFilSC5 as character

	//Variaveis Data
	Local dEmissao	:= CTOD("") as date

	//Variaveis Numerica
	Local nPed		:= 0 as numeric
	Local i        	:= 0 as numeric
	Local nV       	:= 1 as numeric
	Local nAcresFin := 0 as numeric

	aSort(aPedCli)

	If !Empty(self:aSM0)
		//  Informacoes do Quadro 1: Dados da Empresa                    
		cNomCom := Alltrim(self:aSM0[aScan(self:aSM0,  {|x| x[1] == "M0_NOMECOM" } )][2])
		cNomEmp := Alltrim(self:aSM0[aScan(self:aSM0,  {|x| x[1] == "M0_NOME" } )][2])+IIF(!Empty(cNomCom), ' - ' + rTrim(cNomCom),'')
		cEndCob	:= Alltrim(self:aSM0[aScan(self:aSM0,  {|x| x[1] == "M0_ENDCOB" } )][2])
		cTel	:= Alltrim(self:aSM0[aScan(self:aSM0,  {|x| x[1] == "M0_TEL" } )][2])
		cCNPJ 	:= Alltrim(self:aSM0[aScan(self:aSM0,  {|x| x[1] == "M0_CGC" } )][2])
	Endif

	If !(aCabPV[1]$"DB")//C5_TIPO
		//  Informacoes do Quadro 2: Dados do Cliente                    
		cNomCF 	:= (self:cAlias)->A1_COD+"/"+(self:cAlias)->A1_LOJA+" "+ SVFatLGPD((self:cAlias)->A1_NOME, "A1_NOME", self:lObfuscated, self:aPDFields)
		cEndEnt	:= IIf( !Empty((self:cAlias)->A1_ENDENT) .And. (self:cAlias)->A1_ENDENT # (self:cAlias)->A1_END,SVFatLGPD((self:cAlias)->A1_ENDENT, "A1_ENDENT", self:lObfuscated, self:aPDFields),;
					SVFatLGPD((self:cAlias)->A1_END, "A1_END", self:lObfuscated, self:aPDFields) )
		cCEP 	:= IIF( !Empty((self:cAlias)->A1_CEPE) .And. (self:cAlias)->A1_CEPE # (self:cAlias)->A1_CEP,SVFatLGPD((self:cAlias)->A1_CEPE, "A1_CEPE", self:lObfuscated, self:aPDFields),;
					SVFatLGPD((self:cAlias)->A1_CEP, "A1_CEP", self:lObfuscated, self:aPDFields) )+" "+AllTrim(IF( !Empty((self:cAlias)->A1_MUNE) .And.;
					(self:cAlias)->A1_MUNE # (self:cAlias)->A1_MUN,(self:cAlias)->A1_MUNE, (self:cAlias)->A1_MUN ))+" "+IIF( !Empty((self:cAlias)->A1_ESTE) .And.;
					(self:cAlias)->A1_ESTE # (self:cAlias)->A1_EST,(self:cAlias)->A1_ESTE, (self:cAlias)->A1_EST )

		//Tratamento para CGC localizado
		self:cInscr := (self:cAlias)->A1_INSCR
		self:processData(6)
		cCGC := SVFatLGPD(subs(transform((self:cAlias)->A1_CGC,PicPesFJ(RetPessoa((self:cAlias)->A1_CGC))),1,at("%",transform((self:cAlias)->A1_CGC,PicPes(RetPessoa((self:cAlias)->A1_CGC))))-1),"A1_CGC",;
		 		self:lObfuscated, self:aPDFields)+"  "+self:cCGC
	Else
		//  Informacoes do Quadro 2: Dados do Cliente                    
		cNomCF 	:= (self:cAlias)->A2_COD+"/"+(self:cAlias)->A2_LOJA+" "+ SVFatLGPD((self:cAlias)->A2_NOME,"A2_NOME", self:lObfuscated, self:aPDFields)
		cEndEnt	:= SVFatLGPD((self:cAlias)->A2_END, "A2_END", self:lObfuscated, self:aPDFields)
		cCEP 	:= (self:cAlias)->A2_CEP + " " + AllTrim((self:cAlias)->A2_MUN) + " " + (self:cAlias)->A2_EST

		//Tratamento para CGC localizado
		self:cInscr := (self:cAlias)->A2_INSCR
		self:processData(6)
		cCGC := SVFatLGPD(subs(transform((self:cAlias)->A2_CGC,PicPesFJ(RetPessoa((self:cAlias)->A2_CGC))),1,at("%",transform((self:cAlias)->A2_CGC,PicPes(RetPessoa((self:cAlias)->A2_CGC))))-1),"A2_CGC",;
			self:lObfuscated, self:aPDFields)+"  "+self:cCGC
	Endif 

	//  Informacoes do Quadro 3: Dados do Pedido                     
	dEmissao  := totvs.framework.treports.date.stringToTimeStamp(aCabPV[6])	// "Emissão: "(STR0005)
	cNumPV	  := aCabPV[7]		// "Pedido N: "(STR0006)

	//  Pedidos do Cliente                                           
	If Len(aPedCli) > 0	
		cPedCli := ""
		For nPed := 1 To Len(aPedCli)
			cPedCli += aPedCli[nPed]+Space(02)
			If nPed == Len(aPedCli)
				cItemPV := cPedCli
				cPedCli:=""
			Endif
		Next
	Endif

	//  Transportadora                                               
	cTransp	:= IIF(!Empty(aCabPV[4]), aCabPV[4]+" - "+(self:cAlias)->A4_NOME, "")// "Transp: " (STR0010)

	//  Vendedores                                                  
	For i := 8 to 12
		If SA3->(MsSeek((self:cAlias)->A3_FILIAL+aCabPV[i]))
			cNomVend += "("+Alltrim(Str(nV))+")" +" "+ aCabPV[i] + " - "+ Alltrim(SVFatLGPD(SA3->A3_NOME, "A3_NOME", self:lObfuscated, self:aPDFields));
						+Iif(!Empty(aCabPV[i+1]) .And. i<12," | ","")
			cComissa += "("+Alltrim(Str(nV))+")" +" "+ Alltrim(Transform(aCabPV[i+5], "@E 999,999,999.99"))+Iif(!Empty(aCabPV[i+1]) .And. i<12," | ","")
		Endif  
		nV++ 
	Next

	// Condicao de Pagto, Frete e Seguro                            
	cCondPg	:= aCabPV[5]+" - "+(self:cAlias)->E4_DESCRI//"Cond.Pgto: " (STR0011)
	cFrete	:= Alltrim(Transform(aCabPV[18],"@EZ 999,999,999.99")+SVTipoFrete(aCabPV[19]))//"Frete: " (STR0012)
	cSeguro	:= Alltrim(Transform(aCabPV[20],"@EZ 999,999,999.99"))//"Seguro: " (STR0013+)

	// Tabela, Volume e Especie                                     
	cTabela  := aCabPV[21]//"Tabela: " (STR0014)
	cVolume	 := Alltrim(Transform(aCabPV[22],"@EZ 999,999,999.99"))//"Volumes: " (STR0015)
	cEspecie := Alltrim(aCabPV[23])//"Espécie: " (STR0016)

	// Reajuste, Moeda, Banco e Acrescimo Financeiro                
	cMoeda	  := IIF(Strzero(aCabPV[24],1,0) < "2","1",Strzero(aCabPV[24],1,0))//"Moeda:" (STR0018) 
	cReajuste := aCabPV[25]//"Reajuste:" (STR0017)
	cBanco	  := aCabPV[26]//"Banco: " (STR0019)
	nAcresFin := aCabPV[27]//"Acres.Fin: " (STR0020)

	// Imprime informacoes de cabecalho			                 
	aAdd(aCabec,{	cNomEmp		,;	//NOME DA EMPRESA
					cEndCob		,;	//ENDERECO DE COBRANCA
					cTel		,;	//TEL:
					cCNPJ		,;	//CGC:
					cNomCF		,;	//NOME DO CLIENTE/FORNECEDOR
					cEndEnt		,;	//ENDERECO DE ENTREGA
					cCEP		,;	//CEP
					cCGC		,;	//CNPJ
					dEmissao	,;	//DATA DA EMISSAO DO PEDIDO DE VENDA
					cNumPV		,;	//NUMERO DO PEDIDO DE VENDA
					cItemPV		,;	//ITEM DO PEDIDO DE VENDA
					cTransp		,;	//CODIGO DA TRANSPORTADORA (TRASNP...:)
					cNomVend	,;	//NOME DO VENDEDOR
					cComissa	,;	//COMISSAO DO VENDEDOR
					cCondPg		,;	//CONDICAO DE PAGAMENTO
					cFrete		,;	//FRETE (FRETE...:)
					cSeguro		,;	//SEGURO (SEGURO:)
					cTabela		,;	//TABELA (TABELA...:)
					cVolume		,;	//VOLUME (VOLUME.:)
					cEspecie	,;	//ESPECIE (ESPECIA:)
					cReajuste	,;	//REAJUSTE (REAJUSTE)
					cMoeda		,;	//MOEDA (MOEDA:)
					cBanco		,;	//BANCO (BANCO:)
					nAcresFin	,;	//ACRESCIMO FINANCEIRO (ACRES. FIN.:)
					cFilCab		})	//FILIAL

Return aCabec

/*{Protheus.doc} SVImpRodapR4
	Emissao da Pre-Nota - Item
	@param aPedCli, Array com dados dos itens pedidos do cliente/fornecedor
	@param aCabPV, Array com dados do cabecalho do cliente/fornecedor
	@return aRodap
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
Static Function SVImpRodapR4(nPesLiq as numeric, nPesBru as numeric, aC5Rodape as array, lFinal as logical) as array

	Local aRodape	:= {} as array

	//  Imprime informacoes de cabecalho			                 
	aAdd(aRodape,{	STR(IIf(aC5Rodape[1][1] > 0,aC5Rodape[1][1],nPesBru)),;	//"Peso Bruto: " (STR0022)
					STR(IIf(aC5Rodape[1][2] > 0,aC5Rodape[1][2],nPesLiq)),;	//"Peso Líquido: " (STR0023)
					aC5Rodape[1][3],; 										//Valor 01 - Descontos
					aC5Rodape[1][4],; 										//Valor 02 - Descontos
					aC5Rodape[1][5],; 										//Valor 03 - Descontos
					aC5Rodape[1][6],; 										//Valor 04 - Descontos	
					AllTrim(aC5Rodape[1][7])})								//"Mensagem Para Nota Fiscal" (STR0029)

Return aRodape

/*{Protheus.doc} SVFisGetInit
	Inicializa as variaveis utilizadas no Programa
	@return .T.
	@author Squad CRM/Faturamento
	@since 26/04/2023
	@version 12.1.2410
*/
Method SVFisGetInit() class sorderconfirmationSmartViewBusinessObject  

	Local aCpoSX3		:= {} as array
	Local cCpoX3Vld     := "" as character
	Local cReferencia 	:= "" as character
	Local cMaFis 		:= "" as character
	Local nPosIni     	:= 0 as numeric
	Local nLen        	:= 0 as numeric
	Local nX        	:= 0 as numeric
	Local nI        	:= 0 as numeric
	Local nRegMaFis    	:= 0 as numeric

	If Empty(self:aFisGet)

		self:aFisGet	:= {}
		aCpoSX3	:= FWSX3Util():GetAllFields("SC6",.F.)

		For nX := 1 To Len(aCpoSX3)

			cCpoX3Vld := Alltrim(UPPER(GetSx3Cache(aCpoSX3[nX],"X3_VALID") + GetSx3Cache(aCpoSX3[nX],"X3_VLDUSER")))
			nRegMaFis := IIF('MAFISGET("' $ cCpoX3Vld .And. 'MAFISREF("' $ cCpoX3Vld, 2, 1)

			For nI := 1 To nRegMaFis

				cMaFis := IIF('MAFISGET("' $ cCpoX3Vld, 'MAFISGET("', IIF('MAFISREF("' $ cCpoX3Vld, 'MAFISREF("', ""))

				If !Empty(cMaFis)

					nPosIni := AT(cMaFis,cCpoX3Vld)+10
					nLen := IIF('MAFISGET("' $ cCpoX3Vld, AT('")',Substr(cCpoX3Vld,nPosIni,Len(cCpoX3Vld)-nPosIni))-1, AT('","MT410",',cCpoX3Vld)-nPosIni)
					cReferencia := Substr(cCpoX3Vld,nPosIni,nLen)
					aAdd(self:aFisGet,{cReferencia,aCpoSX3[nX],MaFisOrdem(cReferencia)})

				Endif

			Next nI

		Next nX

		aSort(self:aFisGet,,,{|x,y| x[3]<y[3]})

	Endif

	If Empty(self:aFisGetSC5)

		self:aFisGetSC5	:= {}
		aCpoSX3	:= FWSX3Util():GetAllFields("SC5",.F.)

		For nX := 1 To Len(aCpoSX3)

			cCpoX3Vld := Alltrim(UPPER(GetSx3Cache(aCpoSX3[nX],"X3_VALID") + GetSx3Cache(aCpoSX3[nX],"X3_VLDUSER")))
			nRegMaFis := IIF('MAFISGET("' $ cCpoX3Vld .And. 'MAFISREF("' $ cCpoX3Vld, 2, 1)

			For nI := 1 To nRegMaFis

				cMaFis := IIF('MAFISGET("' $ cCpoX3Vld, 'MAFISGET("', IIF('MAFISREF("' $ cCpoX3Vld, 'MAFISREF("', ""))

				If !Empty(cMaFis)

					nPosIni := AT(cMaFis,cCpoX3Vld)+10
					nLen := IIF('MAFISGET("' $ cCpoX3Vld, AT('")',Substr(cCpoX3Vld,nPosIni,Len(cCpoX3Vld)-nPosIni))-1, AT('","MT410",',cCpoX3Vld)-nPosIni)
					cReferencia := Substr(cCpoX3Vld,nPosIni,nLen)
					aAdd(self:aFisGetSC5,{cReferencia,aCpoSX3[nX],MaFisOrdem(cReferencia)})

				Endif

			Next nI

		Next nX

		aSort(self:aFisGetSC5,,,{|x,y| x[3]<y[3]})

	Endif

	MaFisEnd()

Return (.T.)

/*{Protheus.doc} SVMtr730Cli
	Método que retorna os pedidos do cliente 
	@return aPedidos
	@author Squad CRM/Faturamento
	@since 26/04/2023
	@version 12.1.2410
*/
Method SVMtr730Cli() class sorderconfirmationSmartViewBusinessObject

	Local aPedidos 	    := {} as array
	Local nParam 	    := 1 as numeric
	Local cAliasItemsC6 := ""
	
	self:oQrySC6:SetString(nParam++, ((self:cAlias)->C5_FILIAL ))
	self:oQrySC6:SetString(nParam++, ((self:cAlias)->C5_NUM ))
	self:oQrySC6:SetString(nParam++, ' ')
	self:oQrySC6:SetString(nParam++, ' ')

	cAliasItemsC6 := self:oQrySC6:OpenAlias()

	DbSelectArea(cAliasItemsC6)
    (cAliasItemsC6)->(DbGoTop())

    While !(cAliasItemsC6)->(Eof())

		If Ascan(aPedidos,(cAliasItemsC6)->C6_PEDCLI) == 0
			Aadd(aPedidos, (cAliasItemsC6)->C6_PEDCLI )
		Endif		

		(cAliasItemsC6)->(DbSkip())

	Enddo

	((cAliasItemsC6)->(DbCloseArea()))

Return(aPedidos)

/*{Protheus.doc} SVFatLGPD - Tratamento LGPD
	Realiza ofuscamento de uma variavel ou de um campo protegido.
	@return cCpoFat, retorna o valor ofuscado.
	@author Squad CRM/Faturamento
	@since 26/04/2023
	@version 12.1.2410
*/
Static Function SVFatLGPD(cConteud as character, cCpo as character, lObfuscated as logical, aPDFields as array) as character
    
	Local cCpoFat := "" as character

	If lObfuscated .and. aScan(aPDFields, cCpo) == 0
		cCpoFat :=	FwProtectedDataUtil():ValueAsteriskToAnonymize(cConteud)
	Else
		cCpoFat := cConteud
	Endif

Return cCpoFat

/*/{Protheus.doc} SVTipoFrete
    @description
    Texto do tipo de frete 
	@return cReturn (Sigla do tipo de frete)
    @author Squad CRM/Faturamento
    @since 17/12/2019
    @version 12.1.2410   
    @return lRet, Logico, Indica se o sistema trabalha com Dados Protegidos
/*/
Static Function SVTipoFrete(cTipofrete as character) as character

	Local cReturn := IIF(cTipofrete = 'C', '(CIF)',;
						IIF(cTipofrete = 'F', '(FOB)',;
							IIF(cTipofrete = 'T', '(TER)',;
								IIF(cTipofrete = 'R', '(REM)',;
									IIF(cTipofrete = 'D', '(DES)',;
										IIF(cTipofrete = 'S', '(SEM)', "")))))) as character
Return cReturn

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 26/04/2023
	@version 12.1.2410
*/
Method getSchema() as object Class sorderconfirmationSmartViewBusinessObject

	Local oObj := FWSX1Util():New() as object
	Local cGrpSX1 := "MTR730" as character
	
	oObj:AddGroup(cGrpSX1)
	oObj:SearchGroup()

	//VARIAVEIS DO CABECALHO
	self:oSchema:addProperty("CFILCAB",  	STR0103, "string", 	STR0103, "CFILCAB")		//"Filial"
	self:oSchema:addProperty("CNOMEMP",  	STR0030, "string", 	STR0030, "CNOMEMP")		//"Nome da Empresa"
	self:oSchema:addProperty("CENDCOB",  	STR0031, "string", 	STR0031, "CENDCOB") 	//"Endereço de Cobrança"
	self:oSchema:addProperty("CTEL",	 	STR0032, "string", 	STR0032, "CTEL") 		//"Telefone"
	self:oSchema:addProperty("CCNPJ", 	 	STR0033, "string", 	STR0033, "CCNPJ") 		//"CNPJ"
	self:oSchema:addProperty("CNOMCF", 	 	STR0034, "string", 	STR0034, "CNOMCF") 		//"Nome do Cliente/Fornecedor"
	self:oSchema:addProperty("CENDENT",  	STR0035, "string", 	STR0035, "CENDENT") 	//"Endereco de Entrega"
	self:oSchema:addProperty("CCEP", 	 	STR0036, "string", 	STR0036, "CCEP") 		//"CEP"
	self:oSchema:addProperty("CCGC", 	 	STR0037, "string", 	STR0037, "CCGC") 		//"CGC"
	self:oSchema:addProperty("DEMISSAO", 	STR0039, "date", 	STR0039, "DEMISSAO")	//"Emissão"
	self:oSchema:addProperty("CNUMPV", 	 	STR0040, "string", 	STR0040, "CNUMPV") 		//"Número Pedido de Venda"
	self:oSchema:addProperty("CITEMPV",  	STR0042, "string", 	STR0042, "CITEMPV") 	//"Item Pedido de Venda"
	self:oSchema:addProperty("CTRANSP",  	STR0043, "string", 	STR0043, "CTRANSP") 	//"Transportadora"
	self:oSchema:addProperty("CNOMVEND", 	STR0044, "string", 	STR0044, "CNOMVEND")	//"Nome Vendedores"
	self:oSchema:addProperty("CCOMISSA", 	STR0009, "string", 	STR0009, "CCOMISSA")	//"Comissao"
	self:oSchema:addProperty("CCONDPG",  	STR0046, "string", 	STR0046, "CCONDPG") 	//"Condição de Pagamento"
	self:oSchema:addProperty("CFRETE", 	 	STR0047, "string", 	STR0047, "CFRETE") 		//"Frete"
	self:oSchema:addProperty("CSEGURO",  	STR0048, "string", 	STR0048, "CSEGURO") 	//"Seguro"
	self:oSchema:addProperty("CTABELA",  	STR0049, "string", 	STR0049, "CTABELA") 	//"Tabela"
	self:oSchema:addProperty("CVOLUME",  	STR0050, "string", 	STR0050, "CVOLUME") 	//"Volume"
	self:oSchema:addProperty("CESPECIE", 	STR0051, "string", 	STR0051, "CESPECIE")	//"Espécie"
	self:oSchema:addProperty("CMOEDA", 	 	STR0052, "string", 	STR0052, "CMOEDA") 		//"Moeda"
	self:oSchema:addProperty("CREAJUSTE", 	STR0053, "string", 	STR0053, "CREAJUSTE") 	//"Reajuste"
	self:oSchema:addProperty("CBANCO", 	 	STR0054, "string", 	STR0054, "CBANCO") 		//"Banco"
	self:oSchema:addProperty("NACRESFIN", 	STR0055, "number", 	STR0055, "NACRESFIN") 	//"Acréscimo Financeiro"

	//VARIAVEIS DO ITEM DO PEDIDO DE VENDA
	self:oSchema:addProperty("CITEM",		STR0056, "string",	STR0056, "CITEM")		//"Item"
	self:oSchema:addProperty("CPRODUTO",	STR0057, "string",	STR0057, "CPRODUTO")	//"Produto"
	self:oSchema:addProperty("CDESCRPRD",	STR0058, "string",	STR0058, "CDESCRPRD")	//"Descrição"
	self:oSchema:addProperty("CTES",		STR0059, "string",	STR0059, "CTES")		//"TES"
	self:oSchema:addProperty("CCF", 		STR0060, "string",	STR0060, "CCF")			//"Código Fiscal"
	self:oSchema:addProperty("CUM", 		STR0061, "string",	STR0061, "CUM")			//"Unidade de Medida"
	self:oSchema:addProperty("NQUANT",		STR0062, "number",	STR0062, "NQUANT")		//"Quantidade"
	self:oSchema:addProperty("NPRCUNIT",	STR0063, "number",	STR0063, "NPRCUNIT")	//"Preço Unitário"
	self:oSchema:addProperty("NIPI",		STR0064, "number",	STR0064, "NIPI")		//"IPI"
	self:oSchema:addProperty("NICMS",		STR0065, "number",	STR0065, "NICMS")		//"ICMS"
	self:oSchema:addProperty("NISS",		STR0066, "number",	STR0066, "NISS")		//"ISS"
	self:oSchema:addProperty("NVLTOTIPI",	STR0067, "number",	STR0067, "NVLTOTIPI")	//"Valor Total Com IPI"
	self:oSchema:addProperty("DENTREGA", 	STR0068, "date",	STR0068, "DENTREGA")	//"Entrega"
	self:oSchema:addProperty("NDESCITEM",	STR0069, "number",	STR0069, "NDESCITEM")	//"%Desconto"
	self:oSchema:addProperty("CLOC",		STR0070, "string",	STR0070, "CLOC")		//"Armazém"
	self:oSchema:addProperty("NQTDFAT", 	STR0071, "number",	STR0071, "NQTDFAT")		//"Quantidade a Faturar"
	self:oSchema:addProperty("NSALDO",		STR0072, "number",	STR0072, "NSALDO")		//"Saldo"
	self:oSchema:addProperty("NULTFAT",		STR0073, "number",	STR0073, "NULTFAT")		//"Quantidade Entregue"
	self:oSchema:addProperty("NTOTQTD", 	STR0074, "number", 	STR0074, "NTOTQTD")		//"Quantidade vendida"
	self:oSchema:addProperty("NTOTVAL", 	STR0075, "number", 	STR0075, "NTOTVAL")		//"Valor do produto"
	self:oSchema:addProperty("NPESLIQ", 	STR0076, "number", 	STR0076, "NPESLIQ")		//"Peso Líquido"
	self:oSchema:addProperty("NPESBRU", 	STR0077, "number", 	STR0077, "NPESBRU")		//"Peso Bruto"
	self:oSchema:addProperty("nQUANTPV", 	STR0078, "number", 	STR0078, "nQUANTPV")	//"Quantidade Total"
	self:oSchema:addProperty("nVALTOIPI", 	STR0079, "number", 	STR0079, "nVALTOIPI")	//"Valor Total + IPI"

	//VARIAVEIS DE IMPOSTO
	self:oSchema:addProperty("NBASEICM", 	STR0080, "number", STR0080, "NBASEICM") 	//"Base ICMS"
	self:oSchema:addProperty("NVALICM", 	STR0081, "number", STR0081, "NVALICM") 		//"Valor ICMS"
	self:oSchema:addProperty("NBASEIPI", 	STR0082, "number", STR0082, "NBASEIPI") 	//"Base IPI"
	self:oSchema:addProperty("NVALIPI", 	STR0083, "number", STR0083, "NVALIPI") 		//"Valor IPI"
	self:oSchema:addProperty("NBASESOL", 	STR0084, "number", STR0084, "NBASESOL") 	//"Base RETIDO"
	self:oSchema:addProperty("NVALSOL", 	STR0085, "number", STR0085, "NVALSOL") 		//"Valor RETIDO"
	self:oSchema:addProperty("NTOTAL", 		STR0086, "number", STR0086, "NTOTAL") 		//"Valor TOTAL"
	self:oSchema:addProperty("NBASEISS", 	STR0087, "number", STR0087, "NBASEISS") 	//"Base ISS"
	self:oSchema:addProperty("NVALISS", 	STR0088, "number", STR0088, "NVALISS") 		//"Valor ISS"

	//VARIAVEIS DO RODAPE
	self:oSchema:addProperty("CPESOBRU", 	STR0090, "string", STR0090, "CPESOBRU") 	//"Peso Bruto Rodapé"
	self:oSchema:addProperty("CPESOLIQ", 	STR0091, "string", STR0091, "CPESOLIQ") 	//"Peso Líquido Rodapé"
	self:oSchema:addProperty("NDESC1",  	STR0097, "number", STR0097, "NDESC1") 		//"Desconto 01 Rodapé"
	self:oSchema:addProperty("NDESC2", 		STR0098, "number", STR0098, "NDESC2") 		//"Desconto 02 Rodapé"
	self:oSchema:addProperty("NDESC3", 		STR0099, "number", STR0099, "NDESC3") 		//"Desconto 03 Rodapé"
	self:oSchema:addProperty("NDESC4",   	STR0100, "number", STR0100, "NDESC4") 		//"Desconto 04 Rodapé"
	self:oSchema:addProperty("CMSGNOTA", 	STR0101, "string", STR0101, "CMSGNOTA")		//"Mensagem da Nota Fiscal"
	self:oSchema:addProperty("cCHVLIST", 	STR0102, "string", STR0102, "cCHVLIST")     //"Chave de listagem"

	self:oSchema:addParameter("MV_FIL"	,STR0103 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) 			   							//Filial
	self:oSchema:addParameter("CPEDDE" 	,Alltrim(oObj:GetGroup(cGrpSX1)[2][1]:CX1_PERGUNT), "string", .F.,,,,,, FTSVHelp(cGrpSX1,'01')) //Pedido De
	self:oSchema:addParameter("CPEDATE"	,Alltrim(oObj:GetGroup(cGrpSX1)[2][2]:CX1_PERGUNT), "string", .F.,,,,,, FTSVHelp(cGrpSX1,'02')) //Pedido Ate

	If __lLookUp
		self:setCustomURL("MV_FIL",	 "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial
		self:setCustomURL("CPEDDE",  "api/framework/v1/genericLookupService/smartview/SC5", 2) //Pedido De
		self:setCustomURL("CPEDATE", "api/framework/v1/genericLookupService/smartview/SC5", 2) //Pedido Ate
	Endif

Return self:oSchema

/*{Protheus.doc} FatSv012CreateSQL
	Retorna todas as querys montadas
	@return Nil
	@author Squad CRM/Faturamento
	@since 28/10/2024
	@version 12.1.2410
*/
Method FatSV012CreateSQL() class sorderconfirmationSmartViewBusinessObject

	Local nY 	     := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
	Local cQrySC6	 := ""  as character
    Local cFieldsQry := ""  as character
	Local cQryAd     := ""  as character
	Local cNameTmp   := ""  as character
    Local aFiliais	 := {}  as array
	Local oQuery 	 := Nil as object
	Local oTempTableBranch 	 := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SC5", "C5_FILIAL")

	// Filtragem do relatório
	self:SVFisGetInit(@self:aFisGet,@self:aFisGetSC5)

	For nY := 1 To Len(self:aFisGet)
		cQryAd += ","+self:aFisGet[nY][2]
	Next nY

	For nY := 1 To Len(self:aFisGetSC5)
		cQryAd += ","+self:aFisGetSC5[nY][2]
	Next nY

	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry += "C5_FILIAL,C5_NUM,C5_TIPO,C5_CLIENTE,C5_LOJACLI,C5_CLIENT,C5_LOJAENT,C5_TIPOCLI,C5_MOEDA,C5_FRETE, "
	cFieldsQry += "C5_SEGURO,C5_FRETAUT,C5_DESPESA,C5_DESCONT,C5_PDESCAB,C5_TRANSP,C5_CONDPAG,C5_EMISSAO,C5_VEND1, "
	cFieldsQry += "C5_VEND2,C5_VEND3,C5_VEND4,C5_VEND5,C5_COMIS1,C5_COMIS2,C5_COMIS3,C5_COMIS4,C5_COMIS5,C5_TPFRETE, "
	cFieldsQry += "C5_TABELA,C5_VOLUME1,C5_ESPECI1,C5_MOEDA,C5_REAJUST,C5_BANCO,C5_ACRSFIN,C5_PBRUTO,C5_PESOL,C5_DESC1, "
	cFieldsQry += "C5_DESC2,C5_DESC3,C5_DESC4,C5_MENNOTA,C5_INCISS,C6_FILIAL,C6_NUM,C6_NFORI,C6_SERIORI,C6_CLI,C6_LOJA, "
	cFieldsQry += "C6_PRODUTO,C6_ITEMORI,C6_VALOR,C6_PRUNIT,C6_QTDVEN,C6_PRCVEN,C6_VALDESC,C6_TES,C6_OPER,C6_ITEM,C6_DESCRI, "
	cFieldsQry += "C6_CF,C6_UM,C6_NOTA,C6_SERIE,C6_ENTREG,C6_DESCONT,C6_LOCAL,C6_QTDEMP,C6_QTDLIB,C6_QTDENT,B1_FILIAL,F4_ISS,F4_DUPLIC,"
	cFieldsQry += "A1_COD,A1_LOJA,A1_NOME,A1_ENDENT,A1_END,A1_CEPE,A1_CEP,A1_MUNE,A1_MUN,A1_ESTE,A1_EST,A1_INSCR,A1_CGC, "
	cFieldsQry += "A2_COD,A2_LOJA,A2_NOME,A2_END,A2_CEP,A2_MUN,A2_EST,A2_INSCR,A2_CGC, "
	cFieldsQry += "A3_FILIAL,A4_NOME,E4_DESCRI "

	cFieldsQry += cQryAd
	cFieldsQry += Self:cSelectLoc

	//Adiciona campos customizados
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:aCpoCustom, .T., .F., .T.)

	cQuery += "SELECT ? "
	cQuery += " FROM " + RetSqlName("SC5") + " SC5 "
	cQuery += " INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = SC5.C5_FILIAL"	
	cQuery += " INNER JOIN " + RetSqlName("SC6") + " SC6 ON ? "
	cQuery += " AND SC6.C6_NUM  = SC5.C5_NUM "
	cQuery += " AND SC6.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 ON ? "
	cQuery += " AND SB1.B1_COD  = SC6.C6_PRODUTO "
	cQuery += " AND SB1.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SF4") + " SF4 ON ? "
	cQuery += " AND SF4.F4_CODIGO = SC6.C6_TES "
	cQuery += " AND SF4.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
	cQuery += " AND SA1.A1_COD = SC5.C5_CLIENTE "
	cQuery += " AND SA1.A1_LOJA = SC5.C5_LOJACLI "
	cQuery += " AND SA1.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SA2") + " SA2 ON ? "
	cQuery += " AND SA2.A2_COD = SC5.C5_CLIENTE "
	cQuery += " AND SA2.A2_LOJA = SC5.C5_LOJACLI "
	cQuery += " AND SA2.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SA3") + " SA3 ON ? "
	cQuery += " AND SA3.A3_COD = SC5.C5_VEND1 "
	cQuery += " AND SA3.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SA4") + " SA4 ON ? "
	cQuery += " AND SA4.A4_COD = SC5.C5_TRANSP "
	cQuery += " AND SA4.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SE4") + " SE4 ON ? "
	cQuery += " AND SE4.E4_CODIGO = SC5.C5_CONDPAG "
	cQuery += " AND SE4.D_E_L_E_T_ = ? "
	cQuery += " WHERE SC5.C5_FILIAL = TMPFIL.TMP_FILIAL "

	If !Empty(CPEDDE) 
		cQuery += " AND SC5.C5_NUM >= ? "
	Endif

	If !Empty(CPEDATE)
		cQuery += " AND SC5.C5_NUM <=  ? "
	Endif

	cQuery += " ? "	//Tratamento para complemento de WHERE que foi populado no objeto localizado
	cQuery += " AND SC5.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY C5_FILIAL, C5_NUM, C6_ITEM "

	// Monta Subquery usada na função SVMtr730Cli
	cQrySC6 := " SELECT C6_FILIAL, C6_PEDCLI,C6_NUM "
	cQrySC6 += " FROM " + RetSqlName("SC6") + " SC6 "
	cQrySC6 += " WHERE SC6.C6_FILIAL = ? "
	cQrySC6 += " AND C6_NUM = ?"
	cQrySC6 += " AND C6_PEDCLI <> ? "
	cQrySC6 += " AND SC6.D_E_L_E_T_ = ? "
	cQrySC6 += " ORDER BY C6_FILIAL, C6_NUM " 

	self:oQrySC6 := FwExecStatement():New(ChangeQuery(cQrySC6))

    oQuery  := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SC6", "SC5"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "SC6"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SF4", "SC6"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA1", "SC5"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA2", "SC5"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA3", "SC5"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA4", "SC5"))
	oQuery:SetString(nParam++, " ")
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SE4", "SC5"))
	oQuery:SetString(nParam++, " ")

	If !Empty(CPEDDE) 
		oQuery:SetString(nParam++, CPEDDE)
	Endif

	If !Empty(CPEDATE)
		oQuery:SetString(nParam++, CPEDATE)
	Endif

	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++, " ")

	(self:cAlias) := oQuery:OpenAlias()
	(self:cAliasSC6) := oQuery:OpenAlias()
	(self:cAliasLoc) :=  (self:cAlias) 

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)

    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
