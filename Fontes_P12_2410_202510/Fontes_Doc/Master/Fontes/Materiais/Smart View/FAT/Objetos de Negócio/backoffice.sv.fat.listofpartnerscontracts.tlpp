#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofpartnerscontracts.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofpartnerscontracts.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="ADA,ADB,SC5,SC6,SD2,SA1,SB1", name="Contratos de Parcerias", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofpartnerscontractsSmartViewBusinessObject
Classe para criacao do Objeto de Negócio para Contratos de Parcerias
para embalagens

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class listofpartnerscontractsSmartViewBusinessObject From IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object

	public method FatSV026CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data ojItems		as json
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Metodo de instancia da classe

@return object: self

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class listofpartnerscontractsSmartViewBusinessObject 

	_Super:new()
    self:appEndArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Contratos de Parcerias"
    self:setDescription(STR0003)	//"Relação de Contratos de Parcerias"
    self:setIsLookUp(.T.)  

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negocio

@param nPage, numerico, indica a pagina atual do relatorio
@param oFilter, objeto, contÃ©m o filtro do SmartView

@return object: self:oData

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class listofpartnerscontractsSmartViewBusinessObject

	Local cStatus		 := ""	as character

	Local nX             := 0  as numeric
	Local nVlrTot        := 0  as numeric
	Local nPrcVen        := 0  as numeric
	Local nValDes        := 0  as numeric
	Local nScan			 := 0  as numeric

    Local lObfuscated    := .F. as logical

    Local aPDFields      := {} as array

	Private aFieldsValue := {} as array
	Private aCposCustom	 := self:getCustomFields() as array

	Private NORDER		 := 0  as numeric
	Private MV_FIL		 := "" as character
	Private CONTRATODE	 := "" as character
	Private CONTRATOATE  := "" as character
	Private CLIENTEDE    := "" as character
	Private CLIENTEATE   := "" as character
	Private CLIENTELJDE  := "" as character
	Private CLIENTELJATE := "" as character
	Private PRODUTODE	 := "" as character
	Private PRODUTOATE	 := "" as character

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "FTR030P9R1")

	//Tratamento para permitir apenas o número de moedas utilizadas.
    MV_PAR07 := IIf(MV_PAR07 <= 0 .Or. MV_PAR07 > ContaMoeda(), 1, MV_PAR07)

	NORDER := IIf(NORDER == 0, 1, NORDER)

	//Tratamento LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCposCustom)
    lObfuscated := Len(aPDFields) > 0

    aFieldsValue := {	{'ADA_STATUS', {'ADA_STATUS', "", 'cStatus'}},;
						{'ADB_TOTAL' , {'ADB_TOTAL' , "", 'nVlrTot'}},;
						{'ADB_PRCVEN', {'ADB_PRCVEN', "", 'nPrcVen'}},;
						{'ADB_VALDES', {'ADB_VALDES', "", 'nValDes'}};
                    }

	//Irá montar as queries do objeto de negócio
    self:FatSV026CreateSQL()

	dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		nVlrTot := xmoeda((self:cAlias)->ADB_TOTAL, (self:cAlias)->ADA_MOEDA,MV_PAR07,(self:cAlias)->ADA_EMISSA)
		nPrcVen := xmoeda((self:cAlias)->ADB_PRCVEN,(self:cAlias)->ADA_MOEDA,MV_PAR07,(self:cAlias)->ADA_EMISSA)
		nValDes := xmoeda((self:cAlias)->ADB_VALDES,(self:cAlias)->ADA_MOEDA,MV_PAR07,(self:cAlias)->ADA_EMISSA)
		cStatus := SVStatCont( (self:cAlias)->ADA_STATUS )

		self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

			xValue 	:= IIf( (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0,;
                			FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2]),;
							"(self:cAlias)->"+self:aStruct[nX][5] )

			self:ojItems[self:aStruct[nX][1]] 	:= IIf( lObfuscated .And. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0,;
				 									aPDFields[nScan][2],;
            										IIf( UPPER(self:aStruct[nX][3]) == "DATE",;
                										IIf( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C" , totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) )),;
														&(xValue) ) )

        Next nX

        self:processData(1) //Tratamento para commit localizado
        self:oData:appEndData(self:ojItems)

		(self:cAlias)->(dbSkip())

	EndDo

    //Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

    aSize(aPDFields,   0)
	aSize(aFieldsValue,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listofpartnerscontractsSmartViewBusinessObject

	Local nX 			:= 0 																		as numeric
	Local aParameters 	:= {} 																		as array
	Local aMV_Fili		:= {'MV_FIL', 		STR0024 + " ?", 'string', .T., FTSVHelp(,,,.T.)}		as array
	Local aMV_Orde		:= {'NORDER', 		STR0004 + " ?", 'number', .F., FTSVHelp(,,.T.)} 		as array
	Local aMV_ConD		:= {'CONTRATODE', 	STR0011 + " ?", 'string', .F., FTSVHelp('MTR125','01')} as array
	Local aMV_ConA		:= {'CONTRATOATE', 	STR0012 + " ?", 'string', .F., FTSVHelp('MTR125','02')} as array
	Local aMV_CliD		:= {'CLIENTEDE', 	STR0013 + " ?", 'string', .F., FTSVHelp('MTA266','03')} as array
	Local aMV_CliA		:= {'CLIENTEATE', 	STR0014 + " ?", 'string', .F., FTSVHelp('MTA266','04')} as array
	Local aMV_LojD		:= {'CLIENTELJDE', 	STR0015 + " ?", 'string', .F., FTSVHelp('MTA266','05')} as array
	Local aMV_LojA		:= {'CLIENTELJATE', STR0016 + " ?", 'string', .F., FTSVHelp('MTA266','06')} as array
	Local aMV_ProD		:= {'PRODUTODE', 	STR0017 + " ?", 'string', .F., FTSVHelp('MTA266','09')} as array
	Local aMV_ProA		:= {'PRODUTOATE', 	STR0018 + " ?", 'string', .F., FTSVHelp('MTA266','10')} as array

    self:aFields := {"ADA_FILIAL","ADA_NUMCTR","ADA_STATUS","ADB_NUMCTR","ADA_EMISSA","ADA_CODCLI","ADA_LOJCLI","A1_NOME","ADB_CODPRO","B1_DESC","ADB_ITEM","ADB_QUANT","TBNSALDO","ADB_PRCVEN",;
					 "ADB_TOTAL","ADB_VALDES","C6_CLI","C6_LOJA","C6_NUM","C5_EMISSAO","C6_QTDVEN","D2_DOC","D2_SERIE","D2_EMISSAO","D2_QUANT","ADA_MOEDA"}

    self:aStruct := FatTrGetStruct(self:aFields)

	// Cria a estrutura dos campos Padroes
	If !Empty(self:aStruct)
		For nX := 1 To Len(self:aStruct)
			If self:aStruct[nX][5] == "TBNSALDO"
				self:aStruct[nX] := {"TBNSALDO", STR0005, "number", STR0005, "TB_NSALDO"} 								//"Sld.Contrato"
			EndIf
			If self:aStruct[nX][5] == "C5_EMISSAO"
				self:aStruct[nX] := {self:aStruct[nX][1], STR0019, self:aStruct[nX][3],  STR0019, self:aStruct[nX][5]} 	//"Data de Emissão Pedido"
			EndIf
			If self:aStruct[nX][5] == "D2_EMISSAO"
				self:aStruct[nX] := {self:aStruct[nX][1], STR0020, self:aStruct[nX][3],  STR0020, self:aStruct[nX][5]} 	//"Data de Emissão NF"
			EndIf
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		Next nX
	EndIf

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array reordenando-os de acordo com o primeiro array
	aParameters := FtSVSetParam('FTR030P9R1',,,, {'MV_FIL','NORDER','02','03','01','CONTRATODE','CONTRATOATE','04','CLIENTEDE','CLIENTEATE','05','CLIENTELJDE','CLIENTELJATE','06','PRODUTODE','PRODUTOATE','07','08'},;
													{aMV_Fili, aMV_Orde, aMV_ConD, aMV_ConA, aMV_CliD, aMV_CliA, aMV_LojD, aMV_LojA, aMV_ProD, aMV_ProA} )

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

	If __lLookUp
		self:setCustomURL("MV_FIL",   		"api/framework/v1/genericLookupService/smartview/SM0", 2)
		self:setCustomURL("CONTRATODE",  	"api/framework/v1/genericLookupService/smartview/ADA", 2)
		self:setCustomURL("CONTRATOATE", 	"api/framework/v1/genericLookupService/smartview/ADA", 2)
		self:setCustomURL("CLIENTEDE",   	"api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("CLIENTEATE",  	"api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("CLIENTELJDE",   	"api/framework/v1/genericLookupService/smartview/CLJ", 2)
		self:setCustomURL("CLIENTELJATE",	"api/framework/v1/genericLookupService/smartview/CLJ", 2)
		self:setCustomURL("PRODUTODE",   	"api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("PRODUTOATE",  	"api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR01",  	 	"api/framework/v1/genericLookupService/smartview/ADA", 2)
		self:setCustomURL("MV_PAR04",    	"api/framework/v1/genericLookupService/smartview/CLI", 2)
		self:setCustomURL("MV_PAR05",    	"api/framework/v1/genericLookupService/smartview/CLJ", 2)
		self:setCustomURL("MV_PAR06",    	"api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("NORDER",      	"api/faturamento/smartview/v1/options/fat/listofpartnerscontracts/SVFTCtr001/1", 1)
		self:setCustomURL("MV_PAR08",    	"api/faturamento/smartview/v1/options/fat/listofpartnerscontracts/SVFTCtr001/2", 1)
	EndIf

	aSize(aParameters, 0)
	aSize(aMV_Fili, 0)
	aSize(aMV_Orde, 0)
	aSize(aMV_ConD, 0)
	aSize(aMV_ConA, 0)
	aSize(aMV_CliD, 0)
	aSize(aMV_CliA, 0)
	aSize(aMV_LojD, 0)
	aSize(aMV_LojA, 0)
	aSize(aMV_ProD, 0)
	aSize(aMV_ProA, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFTCtr001
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFTCtr001(cOpcao) 

	Local aRet := {} as array

	If cOpcao == "1" 
		aRet := { STR0021, STR0022, STR0023 }	//Número Contrato, Emissão Contrato, Cod. Cliente Contrato
	ElseIf cOpcao == "2"
		aRet := FatGetCboX1('FTR030P9R1', 8)	//Considerar contratos
	EndIf

Return aRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} SVStatCont
Retorna o texto com o status do contrato de parceria

@sample 	SVStatCont()
@param		ExpC	Codigo do status do contrato			
@return		ExpC 	Retorna o texto do status do contrato

@author	Serviços
@since		23/05/2018
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function SVStatCont(cStatus)

	Local cTxtStat := "" as character

	cTxtStat := IIf( cStatus == "E", STR0006,;						//"Encerrado Manualmente"
						IIf( cStatus == "C", STR0007,;				//"Parcialmente Entregue"
							IIf( cStatus == "A", STR0008,;			//"Contrato em Aberto"
								IIf( cStatus == "D", STR0009,;		//"Contrato Entregue"
									IIf( cStatus == "B", STR0010,;	//"Contrato Aprovado"
										cTxtStat ) ) ) ) )

Return cTxtStat

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv026CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 14/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV026CreateSQL() class listofpartnerscontractsSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array
	Local aOrdem     := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	// Define a ordenação do resultado da query
    aOrdem 		:= { 	" ADA.ADA_FILIAL, ADA.ADA_NUMCTR ",;
						" ADA.ADA_FILIAL, ADA.ADA_CODCLI, ADA.ADA_LOJCLI, ADA.ADA_NUMCTR ",;
               			" ADA_FILIAL, ADA.ADA_EMISSA, ADA.ADA_NUMCTR " }

	//Array para processamento
	For nX := 1 To Len(self:aFields)
		cFieldsQry += IIf( SubStr(self:aFields[nX], 1, 3 ) != 'TBN', self:aFields[nX] + ", ", "" )
	Next nX

	cFieldsQry += "ADB_QUANT-ADB_QTDEMP AS TB_NSALDO"
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados (no array da estrutura, campos e query)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCposCustom, .T., .F. , .T. , 'TBN' )

	// Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

	    cQuery += "SELECT ? "
		cQuery += "	FROM " 	   + RetSqlName("ADA") + " ADA "
		cQuery += "LEFT JOIN " + RetSqlName("ADB") + " ADB "
		cQuery += "	ON  ADB.ADB_FILIAL  = ? "
		cQuery += "	AND ADB.ADB_NUMCTR  = ADA.ADA_NUMCTR "

		// Considerar contratos ? 1-Aberto / 2-Enc. Fat. Total / 3-Enc Manualmente
		If MV_PAR08 == 1 .Or. MV_PAR08 == 3
			cQuery += "	AND ADB.ADB_QTDEMP <> ADB.ADB_QUANT "
		ElseIf MV_PAR08 == 2
			cQuery += "	AND ADB.ADB_QTDEMP = ADB.ADB_QUANT "
		EndIf

		cQuery += "	AND ADB.D_E_L_E_T_  = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
		cQuery += "	ON  SA1.A1_FILIAL   = ? "
		cQuery += "	AND SA1.A1_COD  	= ADB.ADB_CODCLI "
		cQuery += "	AND SA1.A1_LOJA  	= ADB.ADB_LOJCLI "
		cQuery += "	AND SA1.D_E_L_E_T_  = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
		cQuery += " ON  SB1.B1_FILIAL   = ? "
		cQuery += " AND SB1.B1_COD  	= ADB.ADB_CODPRO "
		cQuery += " AND SB1.D_E_L_E_T_  = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SC6") + " SC6 "
		cQuery += " ON  SC6.C6_FILIAL   = ? "
		cQuery += " AND SC6.C6_CONTRAT  = ADB.ADB_NUMCTR "
		cQuery += " AND SC6.C6_ITEMCON  = ADB.ADB_ITEM "
		cQuery += " AND SC6.D_E_L_E_T_  = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SC5") + " SC5 "
		cQuery += " ON  SC5.C5_FILIAL   = ? "
		cQuery += " AND SC5.C5_NUM      = SC6.C6_NUM "	
		cQuery += " AND SC5.D_E_L_E_T_  = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SD2") + " SD2 "
		cQuery += " ON  SD2.D2_FILIAL   = ? "
		cQuery += " AND SD2.D2_PEDIDO   = SC6.C6_NUM "
		cQuery += " AND SD2.D2_ITEMPV   = SC6.C6_ITEM "
		cQuery += " AND SD2.D_E_L_E_T_  = ? "
		cQuery += "WHERE ADA.ADA_FILIAL = ? "
		cQuery += "	AND ADA.ADA_EMISSA >= ? "
		cQuery += "	AND ADA.ADA_EMISSA <= ? "

		If !Empty(MV_PAR01)
			cQuery += " AND ( ADA.ADA_NUMCTR IN (?) " + " OR ADA.ADA_NUMCTR BETWEEN ? AND ? ) "
		ElseIf Empty(MV_PAR01)
			cQuery += " AND ( ADA.ADA_NUMCTR BETWEEN ? AND ? ) "
		EndIf

		If !Empty(MV_PAR04)
			cQuery += " AND ( ADA.ADA_CODCLI IN (?) " + " OR ADA.ADA_CODCLI BETWEEN ? AND ? ) "
		ElseIf Empty(MV_PAR04)
			cQuery += " AND ( ADA.ADA_CODCLI BETWEEN ? AND ? ) "
		EndIf

		If !Empty(MV_PAR05)
			cQuery += " AND ( ADA.ADA_LOJCLI IN (?) " + " OR ADA.ADA_LOJCLI BETWEEN ? AND ? ) "
		ElseIf Empty(MV_PAR05)
			cQuery += " AND ( ADA.ADA_LOJCLI BETWEEN ? AND ? ) "
		EndIf

		If !Empty(MV_PAR06)
			cQuery += " AND ( ADB.ADB_CODPRO IN (?) " + " OR ADB.ADB_CODPRO BETWEEN ? AND ? ) "
		ElseIf Empty(MV_PAR06)
			cQuery += " AND ( ADB.ADB_CODPRO BETWEEN ? AND ? ) "
		EndIf

		// Considerar contratos ? 1-Aberto / 3-Enc Manualmente
		If MV_PAR08 == 1
			cQuery += "	AND ADA.ADA_STATUS <> ? "
		ElseIf MV_PAR08 == 3
			cQuery += "	AND ADA.ADA_STATUS = ? "
		EndIf

		cQuery += " ? "	//Adição de condições para objeto localizado no where
		cQuery += "	AND ADA.D_E_L_E_T_ = ? "

		If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

		oQuery:SetUnsafe(nParam++, cFieldsQry )
		oQuery:SetString(nParam++, FatSVFilial('ADB', 'ADB_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SC5', 'C5_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('ADA', 'ADA_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, DtoS(MV_PAR02) )
		oQuery:SetString(nParam++, DtoS(MV_PAR03) )

		If !Empty(MV_PAR01)
			oQuery:SetIn(nParam++, StrTokArr(MV_PAR01, ";") )
			oQuery:SetString(nParam++, CONTRATODE )
			oQuery:SetString(nParam++, CONTRATOATE )
		ElseIf Empty(MV_PAR01)
			oQuery:SetString(nParam++, CONTRATODE )
			oQuery:SetString(nParam++, CONTRATOATE )
		EndIf

		If !Empty(MV_PAR04)
			oQuery:SetIn(nParam++, StrTokArr(MV_PAR04, ";") )
			oQuery:SetString(nParam++, CLIENTEDE )
			oQuery:SetString(nParam++, CLIENTEATE )
		ElseIf Empty(MV_PAR04)
			oQuery:SetString(nParam++, CLIENTEDE )
			oQuery:SetString(nParam++, CLIENTEATE )
		EndIf

		If !Empty(MV_PAR05)
			oQuery:SetIn(nParam++, StrTokArr(MV_PAR05, ";") )
			oQuery:SetString(nParam++, CLIENTELJDE )
			oQuery:SetString(nParam++, CLIENTELJATE )
		ElseIf Empty(MV_PAR05)
			oQuery:SetString(nParam++, CLIENTELJDE )
			oQuery:SetString(nParam++, CLIENTELJATE )
		EndIf

		If !Empty(MV_PAR06)
			oQuery:SetIn(nParam++, StrTokArr(MV_PAR06, ";") )
			oQuery:SetString(nParam++, PRODUTODE )
			oQuery:SetString(nParam++, PRODUTOATE )
		ElseIf Empty(MV_PAR06)
			oQuery:SetString(nParam++, PRODUTODE )
			oQuery:SetString(nParam++, PRODUTOATE )
		EndIf

		If MV_PAR08 == 1 .Or. MV_PAR08 == 3
			oQuery:SetString(nParam++, 'E')
		EndIf

		oQuery:SetUnsafe(nParam++, self:cWhereLoc )
		oQuery:SetString(nParam++, ' ')

    Next nX

	oQuery:SetUnsafe(nParam++, aOrdem[NORDER] )

	self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
