#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.pricelistbycustomer.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.pricelistbycustomer.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SA7,SB1", name="Lista de Precos por Clientes", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} pricelistbycustomerSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Lista de Precos por Clientes.

@author FAT/CRM
@since 28/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class pricelistbycustomerSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSv034CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAliasTmpLoc as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 28/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class pricelistbycustomerSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Lista de Preços por Clientes"
    self:setDescription(STR0003)	//"Relação de Lista de Preços por Clientes"
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 28/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class pricelistbycustomerSmartViewBusinessObject 

    Local cData         := "" as character

    Local nX            := 0  as numeric
    Local nP            := 0  as numeric
    Local nPreco        := 0  as numeric
    Local nScan         := 0  as numeric
    Local nTamProd      := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric

    Local aPDFields     := {}  as array

    Local lObfuscated	:= .F. as logical

    Local xValue		:= Nil

    Private cAliasSA7   := ""  as character

    Private aFieldsCusto:= self:getCustomFields() as array

    Private MV_FIL 		:= ""  as character

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR785")

    //Tratamento de tamanho do Produto x Mascara
    MV_PAR06 := PADR(MV_PAR06, nTamProd)

    // Criacao das Querys
    self:FatSv034CreateSQL()

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aFieldsCusto)
    lObfuscated := Len(aPDFields) > 0

	dbSelectArea(cAliasSA7)
	(cAliasSA7)->(dbGoTop())

    While (cAliasSA7)->(!Eof())

		//Valida o produto conforme a mascara
		If !ValidMasc((cAliasSA7)->A7_PRODUTO, MV_PAR06)
			(cAliasSA7)->(dbSkip())
			Loop
		Endif

		//Busca ultimo preco
		For nP := 12 to 1 Step -1
			nPreco := (cAliasSA7)->&("A7_PRECO"+StrZero(nP,2))
			cData  := (cAliasSA7)->&("A7_DTREF"+StrZero(nP,2))
			If !Empty(nPreco)
				Exit
			Endif
		Next

        self:jItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)
            xValue := Iif(self:aStruct[nX][5] == "A7_DTREF01",  "cData", Iif(self:aStruct[nX][5] == "A7_PRECO01",  "nPreco", "(cAliasSA7)->&(self:aStruct[nX][5])"))

            If lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp(&(xValue))
            Else
                self:jItems[self:aStruct[nX][1]] := &(xValue)
            EndIf
        Next
        self:processData(1)
        self:oData:appendData(self:jItems)

        (cAliasSA7)->(dbSkip())
    EndDo
    
    //Fechamento de tabelas
    (cAliasSA7)->(dbCloseArea())

    aSize(aPDFields,0)
    aSize(aFieldsCusto,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 28/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class pricelistbycustomerSmartViewBusinessObject

    Local nX := 0 as numeric
    Local aParameters := {} as array

    self:aFields := {"A7_FILIAL","A1_COD","A1_LOJA","A1_NOME","A1_END","A1_TEL","B1_COD","B1_DESC","A7_CODCLI","B1_PESO","B1_UM","B1_IPI","B1_POSIPI","A7_DTREF01","A7_PRECO01"}
    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        If self:aStruct[nX][5] == "A7_DTREF01"
            self:addProperty(self:aStruct[nX][1], STR0004, self:aStruct[nX][3], STR0004, self:aStruct[nX][5])  //"Último Reajuste"
        ElseIf self:aStruct[nX][5] == "A7_PRECO01"
            self:addProperty(self:aStruct[nX][1], STR0005, self:aStruct[nX][3], STR0005, self:aStruct[nX][5])  //"Preço de Venda"
        Else
            self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
        EndIf
    Next

	//Busca no SX1 os parâmetros que serão utilizados
    aParameters := FtSVSetParam('MTR785', {"MV_PAR05"})

    self:oSchema:addParameter("MV_FIL", STR0006 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))  // Filial

    For nX := 1 To Len(aParameters)
        self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
    Next

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SA1", 2) // Cliente de
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SA1", 2) // Cliente ate
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SB1", 2) // Produto de
        self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SB1", 2) // Produto ate
    EndIf

    aSize(aParameters,0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv034CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 06/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv034CreateSQL() Class pricelistbycustomerSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
    Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	//Monta campos do Select
    cFieldsQry := "SA7.A7_FILIAL,SA7.A7_PRODUTO,SA7.A7_CODCLI,SA7.A7_CLIENTE,SA7.A7_LOJA,"
    cFieldsQry += "SA7.A7_DTREF01,SA7.A7_DTREF02,SA7.A7_DTREF03,SA7.A7_DTREF04,SA7.A7_DTREF05,"
    cFieldsQry += "SA7.A7_DTREF06,SA7.A7_DTREF07,SA7.A7_DTREF08,SA7.A7_DTREF09,SA7.A7_DTREF10,"
    cFieldsQry += "SA7.A7_DTREF11,SA7.A7_DTREF12,SA7.A7_PRECO01,SA7.A7_PRECO02,SA7.A7_PRECO03,"
    cFieldsQry += "SA7.A7_PRECO04,SA7.A7_PRECO05,SA7.A7_PRECO06,SA7.A7_PRECO07,SA7.A7_PRECO08,"
    cFieldsQry += "SA7.A7_PRECO09,SA7.A7_PRECO10,SA7.A7_PRECO11,SA7.A7_PRECO12,"
    cFieldsQry += "SB1.B1_DESC,SB1.B1_PESO,SB1.B1_UM,SB1.B1_IPI,SB1.B1_POSIPI,SB1.B1_COD,SB1.B1_PRV1,"
    cFieldsQry += "SA1.A1_COD,SA1.A1_LOJA,SA1.A1_NOME,SA1.A1_END,SA1.A1_TEL"
	cFieldsQry += self:cSelectLoc

    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aFieldsCusto, .T., .T., .T.)

    For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM "      + RetSqlName("SA7") + " SA7 "
        cQuery += "LEFT JOIN "  + RetSqlName("SA1") + " SA1 "
        cQuery += " ON SA1.A1_FILIAL    = ? "
        cQuery += " AND SA1.A1_COD      = SA7.A7_CLIENTE "
        cQuery += " AND SA1.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN "  + RetSqlName("SB1") + " SB1 "
        cQuery += " ON SB1.B1_FILIAL    = ?"
        cQuery += " AND SB1.B1_COD      = SA7.A7_PRODUTO "
        cQuery += " AND SB1.D_E_L_E_T_  = ? "
        cQuery += "WHERE SA7.A7_FILIAL  = ? "
        cQuery += " AND SA7.A7_CLIENTE >= ? "
        cQuery += " AND SA7.A7_CLIENTE <= ? "
        cQuery += " AND SA7.A7_PRODUTO >= ? "
        cQuery += " AND SA7.A7_PRODUTO <= ? "
        cQuery += " ? " //Where que foi populado no objeto localizado
        cQuery += " AND SA7.D_E_L_E_T_ = ? "

		If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY A7_FILIAL, A7_CLIENTE, A7_LOJA, A7_PRODUTO "
		EndIf

	Next nX

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SA7', 'A7_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, MV_PAR01)
        oQuery:SetString(nParam++, MV_PAR02)
        oQuery:SetString(nParam++, MV_PAR03)
        oQuery:SetString(nParam++, MV_PAR04)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')

	Next nX

	self:cAliasTmpLoc := cAliasSA7 := oQuery:OpenAlias()

   	FreeObj(oQuery)

    aSize(aFiliais,0)

Return
