#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.ordersbyproduct.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.ordersbyproduct.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SC5,SC6,SB1,SF2,SD2,SD1", name="Pedidos por Produtos", country="ALL" )

//-------------------------------------------------------------------
/*/{Protheus.doc} ordersbyproductSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de Pedidos por Produtos

@author FAT/CRM
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
class ordersbyproductSmartViewBusinessObject from integratedProvider
    
    public method new() 		as object
    public method getData() 	as object
    public method getSchema() 	as object

	public method FatSV004CreateSQL()
 
    protected data aFields 		as array
    protected data aStruct 		as array
    protected data jItems 		as json
	protected data cAliasLoc	as character
    protected data cSelectLoc 	as character
    protected data cWhereLoc 	as character

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class ordersbyproductSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Pedidos por Produtos"
    self:setDescription(STR0003)	//"Listagem de Pedidos por Produtos"
    self:setIsLookUp(.T.)  			//Habilita o uso do Lookup

    self:cAliasLoc 	:= ""
    self:cSelectLoc := ""
    self:cWhereLoc 	:= ""

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 30/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class ordersbyproductSmartViewBusinessObject

	Local cAliasSC6	 := "" as character
	Local cAliasSD2	 := "" as character
	Local cAliasSD1	 := "" as character
	Local cProd		 := "" as character
	Local cDescProd	 := "" as character
	Local cUM 		 := "" as character
	Local cDupli	 := "" as character
	Local cEstoq	 := "" as character
	Local cFilSC6	 := "" as character

	Local nTamRef  	 := 0 as numeric
	Local nTamProd	 := 0 as numeric
	Local nTotQuant  := 0 as numeric
	Local nTotFat 	 := 0 as numeric
	Local nTotDesc   := 0 as numeric
	Local nTotVal 	 := 0 as numeric
	Local nParam	 := 1 as numeric
	Local nVlrDesc	 := 0 as numeric
	Local nX		 := 0 as numeric

	Local lNfOrig	 := .F. as logical
	Local lDtDev	 := .F. as logical

	Private aFiliais := {}  as array

	Private nNumFils := 0  as numeric

	Private MV_FIL	 := "" as character
	Private MV_PRDDE := "" as character
	Private MV_PRDAT := "" as character
	Private MV_DATDE := Date() as date
	Private MV_DATAT := Date() as date
	Private MV_MOEDA := 0  as numeric

	Private oQrySC6	 := Nil as object
	Private oQrySD2	 := Nil as object
	Private oQrySD1	 := Nil as object

    // Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR620")

	//-------------------------------------------------------------------
	// Criacao das Querys
	//-------------------------------------------------------------------
	self:FatSv004CreateSQL()

	For nX := 1 To nNumFils

		//-------------------------------------------------------------------
		// Executa a Query principal (SC6)
		//-------------------------------------------------------------------
		oQrySC6:SetUnsafe(nParam++ , self:cSelectLoc)								//Campos Select Localizado
		oQrySC6:SetString(nParam++ , FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]))	//Filial SB1
		oQrySC6:SetString(nParam++ , " ")											//Delete SB1
		oQrySC6:SetString(nParam++ , " ")											//Delete SC5
		oQrySC6:SetString(nParam++ , FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]))	//Filial SC6
		oQrySC6:SetString(nParam++ , MV_PRDDE )										//Produto De (SC6)
		oQrySC6:SetString(nParam++ , MV_PRDAT )										//Produto Ate (SC6)
		oQrySC6:SetUnsafe(nParam++ , self:cWhereLoc)								//Filtros Where Localizado
		oQrySC6:SetString(nParam++ , " " )											//Delete SC6

	Next nX

	self:cAliasLoc := cAliasSC6 := oQrySC6:OpenAlias()

	If (cAliasSC6)->(!Eof())

		nTamRef	 := Val(Substr(GetMv("MV_MASCGRD"),1,2))
		nTamProd := GetSx3Cache("B1_COD", "X3_TAMANHO")

		//Tratamento para permitir apenas o número de moedas utilizadas.
		MV_MOEDA := Iif(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)

		//Tratamento tamanho do campo mascara do produto
		mv_par06 := PADR(mv_par06, nTamProd)

		cDupli := IIf( (mv_par10 == 1),"S",IIf( (mv_par10 == 2),"N","SN" ) )
		cEstoq := IIf( (mv_par11 == 1),"S",IIf( (mv_par11 == 2),"N","SN" ) )	

		//Processa Faturamento
		(cAliasSC6)->(dbGoTop())
		While (cAliasSC6)->(!Eof()) .And. !( mv_par08 == 2 .And. ((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT == 0 .Or. Alltrim((cAliasSC6)->C6_BLQ) == "R"))

			If ValidMasc((cAliasSC6)->C6_PRODUTO,mv_par06)

				cFilSC6   := (cAliasSC6)->C6_FILIAL
				cProd 	  := (cAliasSC6)->C6_PRODUTO
				cDescProd := (cAliasSC6)->B1_DESC
				nTotQuant := nTotFat := nTotVal := nTotDesc := 0

				If (cAliasSC6)->C6_GRADE == "S" .And. mv_par07 == 1
					cProd := Substr((cAliasSC6)->C6_PRODUTO,1,nTamRef)
				Endif

				While !(cAliasSC6)->(Eof()) .And. IIf((cAliasSC6)->C6_GRADE == "S" .And. mv_par07 == 1, Substr((cAliasSC6)->C6_PRODUTO , 1, nTamRef), (cAliasSC6)->C6_PRODUTO) == cProd;
											.And. !( mv_par08 == 2 .And. ((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT == 0 .Or. Alltrim((cAliasSC6)->C6_BLQ)=="R") );
											.And. !(mv_par05 == 2 .And. Empty((cAliasSC6)->C6_SEGUM))

					cUM	:= IIf( mv_par05 == 1, (cAliasSC6)->C6_UM, (cAliasSC6)->C6_SEGUM )

					If ValidMasc((cAliasSC6)->C6_PRODUTO, mv_par06)
						
						If !AvalTes((cAliasSC6)->C6_TES, cEstoq, cDupli);//Avalia TES a Ser Impresso
							.Or. ((STOD((cAliasSC6)->C5_EMISSAO) < MV_DATDE .Or. STOD((cAliasSC6)->C5_EMISSAO) > MV_DATAT) .Or. (cAliasSC6)->C5_TIPO == "D")//Verifica se produto devera ser impresso
							(cAliasSC6)->(dbSkip())
							Loop
						Endif

						nTotQuant += IIf( mv_par05 == 1, (cAliasSC6)->C6_QTDVEN, (cAliasSC6)->C6_UNSVEN )
						nTotVal   += xMoeda((cAliasSC6)->C6_VALOR, (cAliasSC6)->C5_MOEDA, MV_MOEDA)
						nVlrDesc  := xMoeda((cAliasSC6)->C6_PRUNIT-(cAliasSC6)->C6_PRCVEN, (cAliasSC6)->C5_MOEDA, MV_MOEDA)*((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT)
						nTotDesc  += IIF(((cAliasSC6)->C6_QTDVEN-(cAliasSC6)->C6_QTDENT) = 0, (cAliasSC6)->C6_VALDESC, IIf(nVlrDesc < 0.00, 0, nVlrDesc))
					
					EndIf
					
					(cAliasSC6)->(dbSkip())

				EndDo

				If mv_par12 <> 3 // inclui devolucao

					//-------------------------------------------------------------------
					// Executa a Query de Devolucoes (SD1)
					//-------------------------------------------------------------------
					nParam := 1

					For nX := 1 To nNumFils

						oQrySD2:SetString(nParam++ , "D")												//Tipo Nota
						oQrySD2:SetString(nParam++ , " ")												//Delete SF2
						oQrySD2:SetString(nParam++ , FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]))	//Filial SD2
						oQrySD2:SetString(nParam++ , cProd )											//Codigo Produto De (SD2)
						oQrySD2:SetString(nParam++ , cProd )											//Codigo Produto Ate (SD2)
						oQrySD2:SetString(nParam++ , DTOS(MV_DATDE))									//Emissao De (SD2)
						oQrySD2:SetString(nParam++ , DTOS(MV_DATAT ))									//Emissao Ate (SD2)
						oQrySD2:SetString(nParam++ , " " )												//Delete SD2

					Next nX

					cAliasSD2 := oQrySD2:OpenAlias()

					While !(cAliasSD2)->(Eof())

						If AvalTes((cAliasSD2)->D2_TES,cEstoq,cDupli)

							nParam := 1

							For nX := 1 To nNumFils

								oQrySD1:SetString(nParam++ , FatSVFilial('SD1', 'D1_FILIAL', aFiliais[nX]) ) //Filial SD1
								oQrySD1:SetString(nParam++ , (cAliasSD2)->D2_COD ) 							 //Codigo Produto De (SD2)
								oQrySD1:SetString(nParam++ , "D" ) 											 //Tipo do Documento

								If mv_par12 == 1
									oQrySD1:SetString(nParam++ , (cAliasSD2)->D2_DOC ) //Numero do Documento/Nota 
									oQrySD1:SetString(nParam++ , (cAliasSD2)->D2_SERIE ) //Serie da Nota Fiscal     
								EndIf

								oQrySD1:SetString(nParam++ , " " ) //Delete SD1

							Next nX

							cAliasSD1 := oQrySD1:OpenAlias()

							While (cAliasSD1)->(!Eof())

								lNfOrig	:= mv_par12 == 1 .And. (cAliasSD2)->(D2_DOC+D2_SERIE) == (cAliasSD1)->D1_NFORI+(cAliasSD1)->D1_SERIORI //Nf Original
								lDtDev  := mv_par12 == 2 .And. STOD((cAliasSD1)->D1_EMISSAO) >= MV_DATDE .And. STOD((cAliasSD1)->D1_EMISSAO) <= MV_DATAT //Pela Data da Devolucao

								nTotFat -= IIF(lNfOrig .Or. lDtDev, (cAliasSD1)->D1_QUANT, 0)

								(cAliasSD1)->(dbSkip())

							EndDo

							If STOD((cAliasSD2)->D2_EMISSAO) <= MV_DATAT .And. STOD((cAliasSD2)->D2_EMISSAO) >= MV_DATDE .And. !(AllTrim((cAliasSD2)->D2_ESPECIE) $ "RFN#RFD")
								nTotFat += IIf( mv_par05 == 1, (cAliasSD2)->D2_QUANT, (cAliasSD2)->D2_QTSEGUM )
							EndIf

							(cAliasSD1)->(DbCloseArea())

						EndIf

						(cAliasSD2)->(dbSkip())

					EndDo

					(cAliasSD2)->(DbCloseArea())
				
				EndIf

				//Impressao do Relatorio
				If nTotQuant+nTotFat+nTotVal+nTotDesc <> 0

					self:jItems := JsonObject():new()
					self:jItems["FILIAL"] 		:= cFilSC6		//Codigo da Filial
					self:jItems["PRODUTO"] 		:= cProd		//Codigo do Produto
					self:jItems["NOME"] 		:= cDescProd	//Descricao do Produto
					self:jItems["QTDVENDIDA"] 	:= nTotQuant	//Qtd. Vendida
					self:jItems["QTDFATURADA"]	:= nTotFat		//Qtd. Faturada
					self:jItems["UM"] 			:= cUM			//UM
					self:jItems["DESCONTO"] 	:= nTotDesc		//Desconto
					self:jItems["VLRTOTAL"] 	:= nTotVal		//Valor Total
					self:processData(1)
					self:oData:appendData(self:jItems)

				EndIf

			Else
				DbSelectArea(cAliasSC6)
				(cAliasSC6)->(DbSkip())
			EndIf

		EndDo

		(cAliasSC6)->(DbCloseArea())

	EndIf

	FreeObj(oQrySC6)
	FreeObj(oQrySD1)
	FreeObj(oQrySD2)

	aSize(aFiliais, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class ordersbyproductSmartViewBusinessObject

	Local nX          := 0  as numeric
    Local aParameters := {} as array
	Local cPergunteMV := 'MTR620' as character

	self:oSchema:addProperty("FILIAL"		, STR0011, "string", STR0011, "C6_FILIAL")	//"Filial"
	self:oSchema:addProperty("PRODUTO"		, STR0004, "string", STR0004, "B1_COD")		//"Produto"
	self:oSchema:addProperty("NOME"			, STR0005, "string", STR0005, "B1_DESC")	//"Denominação"
	self:oSchema:addProperty("QTDVENDIDA"	, STR0006, "number", STR0006, "C6_QTDVEN")	//"Qtd.Vendida"
	self:oSchema:addProperty("QTDFATURADA"	, STR0007, "number", STR0007, "C6_QTDVEN")	//"Qtd.Faturada"
	self:oSchema:addProperty("UM"			, STR0008, "string", STR0008, "B1_UM")		//"UM"
	self:oSchema:addProperty("DESCONTO"		, STR0009, "number", STR0009, "C6_PRUNIT")	//"Desconto"
	self:oSchema:addProperty("VLRTOTAL"		, STR0010, "number", STR0010, "C6_VALOR")	//"Valor Total"

	// Busca no SX1 os parâmetros que serão utilizados
	aParameters := FtSVSetParam(cPergunteMV,,,{'01','02','03','04','09'})

    // Adiciona os parâmetros do pergunte no Schema
	self:oSchema:addParameter("MV_FIL",   	STR0011 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) 		 	//Filial
	self:oSchema:addParameter("MV_DATDE", 	STR0015 + " ?", "date",   .F.,,,,,, FTSVHelp(cPergunteMV,'01')) //Data de
	self:oSchema:addParameter("MV_DATAT", 	STR0016 + " ?", "date",   .F.,,,,,, FTSVHelp(cPergunteMV,'02')) //Data ate
	self:oSchema:addParameter("MV_PRDDE",	STR0013 + " ?", "string", .F.,,,,,, FTSVHelp(cPergunteMV,'03')) //Produto de
    self:oSchema:addParameter("MV_PRDAT",	STR0014 + " ?", "string", .F.,,,,,, FTSVHelp(cPergunteMV,'04')) //Produto até

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX
	self:oSchema:addParameter("MV_MOEDA", STR0012 + " ?", "number", .F.,,,,,, FTSVHelp(cPergunteMV,"09")) 	//Moeda

	// Validação para versão da Lib
	If __lLookUp
		self:setCustomURL("MV_FIL",	  "api/framework/v1/genericLookupService/smartview/SM0", 2)						//Filial
		self:setCustomURL("MV_PRDDE", "api/framework/v1/genericLookupService/smartview/SB1", 2)						//Produto De
		self:setCustomURL("MV_PRDAT", "api/framework/v1/genericLookupService/smartview/SB1", 2)						//Produto Ate
		self:setCustomURL("MV_PAR05", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/1", 1) 	//Qual Unid. Medida
		self:setCustomURL("MV_PAR07", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/2", 1) 	//Aglutina prod.Grade
		self:setCustomURL("MV_PAR08", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/3", 1) 	//Considera Entregues
		self:setCustomURL("MV_PAR10", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/4", 1) 	//TES Qto Faturamento
		self:setCustomURL("MV_PAR11", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/5", 1) 	//TES Qto ao Estoque
		self:setCustomURL("MV_PAR12", "api/faturamento/smartview/v1/options/fat/ordersbyproduct/SVCBFATSV004/6", 1) 	//Considera Devolucao
    EndIf

	aSize(aParameters, 0)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVCBFATSV004
Retorna as opçoes disponível para o parâmetro tipo combobox.

@return array: Array com as opçoes.

@author FAT/CRM
@since 25/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function SVCBFATSV004(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"
		aRet := FatGetCboX1('MTR620', 5)
	ElseIf cOpcao == "2"
		aRet := FatGetCboX1('MTR620', 7)
	ElseIf cOpcao == "3"
		aRet := FatGetCboX1('MTR620', 8)
	ElseIf cOpcao == "4"
		aRet := FatGetCboX1('MTR620', 10)
	ElseIf cOpcao == "5"
		aRet := FatGetCboX1('MTR620', 11)
	ElseIf cOpcao == "6"
		aRet := FatGetCboX1('MTR620', 12)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv004CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 08/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV004CreateSQL() class ordersbyproductSmartViewBusinessObject

	Local nX 	     := 0   as numeric
	Local cQuerySC6  := ""  as character
	Local cQuerySD1  := ""  as character
	Local cQuerySD2  := ""  as character

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	// Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

		//-------------------------------------------------------------------
		// Query Principal
		// BIND - Indice SB1 (1): B1_FILIAL, B1_COD
		// BIND - Indice SC5 (1): C5_FILIAL, C5_NUM
		// BIND - Indice SC6 (2): C6_FILIAL, C6_PRODUTO, C6_NUM, C6_ITEM
		//-------------------------------------------------------------------
		cQuerySC6 += " SELECT "
		cQuerySC6 += " 	C6_FILIAL,C6_QTDVEN,C6_QTDENT,C6_BLQ,C6_GRADE,C6_TES,C6_NUM,C6_SEGUM,"
		cQuerySC6 += " 	C6_UNSVEN,C6_VALOR,C6_PRUNIT,C6_PRCVEN,C6_VALDESC,C6_PRODUTO,C6_UM, "
		cQuerySC6 += " 	B1_DESC, "
		cQuerySC6 += " 	C5_TIPO, C5_EMISSAO, C5_MOEDA "
		cQuerySC6 += " 	? "
		cQuerySC6 += " FROM "+RetSqlName("SC6")+" SC6 "
		cQuerySC6 += " 	LEFT JOIN "+RetSqlName("SB1")+" SB1 "
		cQuerySC6 += " 		ON B1_FILIAL = ? "
		cQuerySC6 += " 		AND B1_COD = C6_PRODUTO "
		cQuerySC6 += " 		AND SB1.D_E_L_E_T_ = ? "
		cQuerySC6 += " 	LEFT JOIN "+RetSqlName("SC5")+" SC5 "
		cQuerySC6 += " 		ON C5_FILIAL = C6_FILIAL "
		cQuerySC6 += " 		AND C5_NUM = C6_NUM "
		cQuerySC6 += " 		AND SC5.D_E_L_E_T_ = ? "
		cQuerySC6 += " WHERE "
		cQuerySC6 += " 	C6_FILIAL = ? "
		cQuerySC6 += " 	AND C6_PRODUTO BETWEEN ? AND  ? "
		cQuerySC6 += " 	? "
		cQuerySC6 += " 	AND SC6.D_E_L_E_T_ = ? "

		If mv_par12 <> 3 // inclui devolucao?

			//------------------------------------------------------------------------------------------------
			// Query SD2
			// BIND - Indice SD2 (2): D2_FILIAL, D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_COD, D2_ITEM
			// BIND - Indice SD2 (M): D2_FILIAL, D2_COD, D2_EMISSAO
			// BIND - Indice SF2 (2): F2_FILIAL, F2_CLIENTE, F2_LOJA, F2_DOC, F2_SERIE, F2_TIPO, F2_ESPECIE
			//------------------------------------------------------------------------------------------------
			cQuerySD2 += " SELECT "
			cQuerySD2 += " 	D2_FILIAL, D2_COD, D2_EMISSAO, D2_TES, D2_DOC, D2_SERIE, D2_CLIENTE, D2_LOJA, D2_ESPECIE, D2_QUANT, D2_QTSEGUM, "
			cQuerySD2 += " 	F2_TIPO "
			cQuerySD2 += " FROM "+RetSqlName("SD2")+" SD2 "
			cQuerySD2 += " 	INNER JOIN "+RetSqlName("SF2")+" SF2 "
			cQuerySD2 += " 		ON F2_FILIAL = D2_FILIAL "
			cQuerySD2 += " 		AND F2_CLIENTE = D2_CLIENTE "
			cQuerySD2 += " 		AND F2_LOJA = D2_LOJA "
			cQuerySD2 += " 		AND F2_DOC = D2_DOC "
			cQuerySD2 += " 		AND F2_SERIE = D2_SERIE "
			cQuerySD2 += " 		AND F2_TIPO <> ? "
			cQuerySD2 += " 		AND SF2.D_E_L_E_T_ = ? "
			cQuerySD2 += " WHERE "
			cQuerySD2 += " 	D2_FILIAL = ? "
			cQuerySD2 += " 	AND D2_COD BETWEEN ? AND ? "
			cQuerySD2 += " 	AND D2_EMISSAO BETWEEN ? AND ? "
			cQuerySD2 += " 	AND SD2.D_E_L_E_T_ = ? "

			//---------------------------------------------------------------------------------------
			// Query SD1
			// BIND (X)
			//---------------------------------------------------------------------------------------
			cQuerySD1 += " SELECT "
			cQuerySD1 += " 	D1_FILIAL, D1_COD, D1_TIPO, D1_EMISSAO, D1_NFORI, D1_SERIORI, D1_QUANT "
			cQuerySD1 += " FROM "+RetSqlName("SD1")+" SD1 "
			cQuerySD1 += " WHERE "
			cQuerySD1 += " 	D1_FILIAL = ? "
			cQuerySD1 += " 	AND D1_COD = ? "
			cQuerySD1 += " 	AND D1_TIPO = ? "

			If mv_par12 == 1 //NF Original
				cQuerySD1 += " 	AND D1_DOC = ? "
				cQuerySD1 += " 	AND D1_SERIE = ? "
			EndIf

			cQuerySD1 += " 	AND SD1.D_E_L_E_T_ = ? "

			If nNumFils > 1 .And. nX < nNumFils
				cQuerySD2 += "  UNION ALL "
				cQuerySD1 += "  UNION ALL "
			EndIf

		EndIf

		If nNumFils > 1 .And. nX < nNumFils
			cQuerySC6 += "  UNION ALL "
		EndIf

	Next nX

	oQrySC6 := FwExecStatement():New(ChangeQuery(cQuerySC6))
	oQrySD1 := FwExecStatement():New(ChangeQuery(cQuerySD1))
	oQrySD2 := FwExecStatement():New(ChangeQuery(cQuerySD2))

Return
