#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salessummary.ch"

Static __LookUp	:= FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.salessummary.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SD1,SD2,SF1,SF2,SF4,SBM,SB1,SI1", name="Resumo de Vendas", country="ALL")

/*/{Protheus.doc} salessummarySmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Resumo de Vendas
	@author Squad CRM/Faturamento
	@since 20/04/2023
	@version 12.1.2410
*/
Class salessummarySmartViewBusinessObject From IntegratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object
	public method SVCalcDevR4()		as object
	public method FatSv011CreateSQL()

	protected data aQrySD1Loc   as array
	protected data aQrySD2Loc   as array
	protected data aCpoLoc      as array
	protected data cWhereSD1    as character
	protected data cWhereSD2    as character
	protected data cAliasLoc    as character
	protected data cAliasSD1    as character
	protected data cAliasSD2    as character
	protected data cAliasSD1Loc as character
	protected data cAliasSD2Loc as character
	protected data cFilLoc      as character
	protected data cCposSD2     as character
	protected data cSeNFIdSD2   as character
	protected data nDecs   		as numeric
	protected data nDevIpiLoc	as numeric
	protected data nValIPILoc	as numeric
	protected data nValIPI1Loc 	as numeric
	protected data jItems       as json

EndClass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 20/04/2023
	@version 12.1.2410
*/
Method new() Class salessummarySmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0003)		//"Faturamento"
    self:setDisplayName(STR0001)	//"Resumo de Vendas"
    self:setDescription(STR0002)	//"Relação de Resumo de Vendas"
    self:setIsLookUp(.T.)

	self:aQrySD1Loc := {}
	self:aQrySD2Loc := {}
	self:aCpoLoc	:= {}
	self:cSeNFIdSD2  := SerieNfId("SD2", 3, "D2_SERIE")
	self:nDecs := 0

Return self

/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 20/04/2023
	@version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object Class salessummarySmartViewBusinessObject

	Local oTempTable	:= Nil 	as object
	Local cColuna		:= "" 	 as character
	Local cAliasTemp 	:= "" 	 as character
	Local cArqSD1	 	:= "" 	 as character
	Local cAliasSF2		:= "SF2" as character
	Local cTexto		:= "" 	 as character
	Local cTotal		:= "" 	 as character
	Local cCampo		:= "" 	 as character
	Local cVend			:= "1" 	 as character
	Local cEstoq		:= "" 	 as character
	Local cDupli		:= "" 	 as character
	Local cVaria		:= "" 	 as character
	Local cVendedor		:= "" 	 as character
	Local cD2Tes		:= "" 	 as character
	Local cD2Tp 	 	:= "" 	 as character 
	Local cD2Grupo 		:= "" 	 as character
	Local cD2Conta 		:= "" 	 as character
	Local cD2Cod   		:= "" 	 as character 
	Local cIndex		:= "" 	 as character
	Local cD2ValImp		:= "" 	 as character
	Local cOrdemSel		:= "" 	 as character
	Local cCpo			:= ""    as character
	Local cFilTmp		:= ""    as character
	Local cValImpSuf	:= " 0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"	as character // Sufixos para os campos D2_VALIMP*

	Local nQuant	:= 0 as numeric
	Local nPrcVen	:= 0 as numeric
	Local nTotal	:= 0 as numeric
	Local nValIpi	:= 0 as numeric
	Local nQuant1	:= 0 as numeric
	Local nPrcVen1	:= 0 as numeric
	Local nTotal1	:= 0 as numeric
	Local nValIpi1	:= 0 as numeric
	Local nCntFor	:= 0 as numeric
	Local nVend		:= 0 as numeric
	Local nX		:= 0 as numeric
	Local nZ		:= 0 as numeric
	Local nDevQtd	:= 0 as numeric
	Local nDevVal	:= 0 as numeric
	Local nDevQtd1	:= 0 as numeric
	Local nDevVal1	:= 0 as numeric
	Local nIndex	:= 0 as numeric
	Local nDevIPI	:= 0 as numeric

	Local aCampos := {} as array
	Local aIndex  := {} as array
	Local aTam	  := {}	as array

	Local lVend		   := .F. as logical
	Local lObfuscated  := .F. as logical	 

	NORDER	 := 0 		
	MV_MOEDA := 0  		
	MV_FIL 	 := "" 	

	//Metodo para retorno do json dos parâmetros
	FatSetValueMVPAR(oFilter:getParameters(),"MTR660")

	SF1->(DbSetOrder(1))
	SD2->(dbSetOrder(3))
	SD1->(dbSetOrder(1))

	//Tratamento para permitir apenas o número de moedas utilizadas.
	MV_MOEDA := IIf(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)

	nVend	:= FA440CntVen()
	cEstoq  := IIf( (MV_PAR05 == 1),"S",IIf( (MV_PAR05 == 2),"N","SN" ) )
	cDupli  := IIf( (MV_PAR06 == 1),"S",IIf( (MV_PAR06 == 2),"N","SN" ) )
	self:nDecs   := MsDecimais(MV_MOEDA)	

	cOrdemSel := STR0001 + " - "
	cOrdemSel += IIf( NORDER == 1, STR0005,;				//"Por Tp/Saida + Produto"
					IIf( NORDER == 2, STR0006,;				//"Por Tipo"
						IIf( NORDER == 3, STR0007,;			//"Por Grupo"
							IIf( NORDER == 4, STR0008,;		//"P/Ct.Contab."
								IIf( NORDER == 5, STR0009,;	//"Por Produto"
									STR0010 ) ) ) ) )		//"Por Tp/Saida + Serie + Nota"

	cOrdemSel += " - " + GetMv("MV_MOEDA"+STR(MV_MOEDA,1))
	//Seleciona Indice da Nota Fiscal de Saida
	DbSelectArea(cAliasSF2)
	DbSetOrder(1)

	//Carrega os campos referentes aos impostos D2_VALIMP
	For nX := 1 to Len(cValImpSuf)
		cD2ValImp := AllTrim("D2_VALIMP"+Substr(cValImpSuf,nX,1))
		self:cCposSD2  += IIf( SD2->(ColumnPos(cD2ValImp)) > 0, ", " + cD2ValImp, "")
	Next nX

	// Monta as Querys SD1 e SD2
    self:FatSv011CreateSQL()

	DbSelectArea("SD2")

	Aadd(aCampos, {"D2_FILIAL", "", 0, 0})
	Aadd(aCampos, {"D2_COD",    "", 0, 0})
	Aadd(aCampos, {"D2_LOCAL",  "", 0, 0})
	Aadd(aCampos, {"D2_SERIE",  "", 0, 0})
	Aadd(aCampos, {"D2_TES",     "", 0, 0})
	Aadd(aCampos, {"D2_TP",      "", 0, 0})
	Aadd(aCampos, {"D2_GRUPO",   "", 0, 0})
	Aadd(aCampos, {"D2_CONTA",   "", 0, 0})
	Aadd(aCampos, {"D2_EMISSAO", "", 0, 0})
	Aadd(aCampos, {"D2_TIPO",    "", 0, 0})
	Aadd(aCampos, {"D2_DOC",     "", 0, 0})
	Aadd(aCampos, {"D2_QUANT",   "", 0, 0})
	Aadd(aCampos, {"D2_TOTAL",   "", 0, 0})
	Aadd(aCampos, {"D2_PRCVEN",  "", 0, 0})
	Aadd(aCampos, {"D2_ITEM",    "", 0, 0})
	Aadd(aCampos, {"D2_CLIENTE", "", 0, 0})
	Aadd(aCampos, {"D2_LOJA",    "", 0, 0})
	//Campos para guardar a moeda/taxa da nota para a conversao durante a impressao
	Aadd(aCampos, {"D2_MOEDA",   "", 0, 0})
	Aadd(aCampos, {"D2_TXMOEDA", "", 0, 0})

	// Campos de outras tabelas, para utilizar o LEFT JOIN na query
	Aadd(aCampos, {"F4_TEXTO", 	 "", 0, 0})
	Aadd(aCampos, {"BM_DESC", 	 "", 0, 0})
	Aadd(aCampos, {"I1_DESC", 	 "", 0, 0})
	Aadd(aCampos, {"B1_DESC", 	 "", 0, 0})

	//Tratamento para adição de campos localizados
	self:processData(2)
	If !Empty(self:aCpoLoc)
		For nZ := 1 to Len(self:aCpoLoc)
			Aadd(aCampos,self:aCpoLoc[nZ])
		Next nZ
	Endif

	For nX := 1 to Len(cValImpSuf)
		cD2ValImp	:= AllTrim("D2_VALIMP"+Substr(cValImpSuf,nX,1))
		If SD2->(ColumnPos(cD2ValImp)) > 0 .AND. aScan(aCampos, {|x| x[01] == cD2ValImp}) == 0
			Aadd(aCampos, {cD2ValImp, "", 0, 0})
		Endif
	Next nX

	For nX := 1 to Len(aCampos)
		aTam			:= If(aCampos[nX,01] == "D2_MOEDA",;
							TamSx3("F2_MOEDA"),;
							If(aCampos[nX,01] == "D2_TXMOEDA",;
								TamSx3("F2_TXMOEDA"),;
								TamSx3(aCampos[nX,01])))
		aCampos[nX,02]	:= aTam[3]
		aCampos[nX,03]	:= aTam[1]
		aCampos[nX,04]	:= aTam[2]
	Next nX

	cAliasTemp := GetNextAlias()
	oTempTable := FWTemporaryTable():New( cAliasTemp )
	oTempTable:SetFields( aCampos )

	cVaria := IIf( NORDER = 1 .Or. NORDER = 6, "D2_TES",; 	// Por Tes
				IIf( NORDER = 2, "D2_TP",;					// Por Tipo
					IIf( NORDER = 3, "D2_GRUPO",;			// Por Grupo
						IIf( NORDER = 4, "D2_CONTA",;		// Por Conta Contabil
						"D2_COD" ) ) ) )					// Por Produto

	cIndex := IIf( NORDER = 1 .Or. NORDER = 6, "D2_FILIAL+D2_TES+"+IIf( NORDER == 1,"D2_COD","D2_SERIE+D2_DOC" ),; 	// Por Tes
				IIf( NORDER = 2, SD2->(IndexKey(2)),;																// Por Tipo
					IIf( NORDER = 3, "D2_FILIAL+D2_GRUPO+D2_COD",;													// Por Grupo
						IIf( NORDER = 4, "D2_FILIAL+D2_CONTA+D2_COD",;												// Por Conta Contabil
						"D2_FILIAL+D2_COD+D2_LOCAL+D2_SERIE+D2_DOC" ) ) ) )											// Por Produto

	aIndex := StrTokArr(cIndex, "+")

	oTempTable:AddIndex("1", aIndex)
	oTempTable:Create()

	DbSelectArea(self:cAliasSD2)
	DbGoTop()

	While !(self:cAliasSD2)->(Eof())

		(cAliasSF2)->(MsSeek((self:cAliasSD2)->D2_FILIAL+(self:cAliasSD2)->D2_DOC+(self:cAliasSD2)->D2_SERIE+(self:cAliasSD2)->D2_CLIENTE+(self:cAliasSD2)->D2_LOJA))

		For nCntFor := 1 To nVend
			cVendedor := (cAliasSF2)->(FieldGet((cAliasSF2)->(ColumnPos("F2_VEND"+cVend))))
			If cVendedor >= MV_PAR09 .and. cVendedor <= MV_PAR10
				lVend := .T.
				Exit
			Endif
			cVend := Soma1(cVend,1)
		Next nCntFor

		cVend := "1"

		If lVend
			Reclock(cAliasTemp,.T.)
			Replace (cAliasTemp)->D2_FILIAL  With (self:cAliasSD2)->D2_FILIAL
			Replace (cAliasTemp)->D2_COD     With (self:cAliasSD2)->D2_COD
			Replace (cAliasTemp)->D2_LOCAL   With (self:cAliasSD2)->D2_LOCAL
			Replace (cAliasTemp)->D2_SERIE   With (self:cAliasSD2)->D2_SERIE
			Replace (cAliasTemp)->D2_TES     With (self:cAliasSD2)->D2_TES
			Replace (cAliasTemp)->D2_TP      With (self:cAliasSD2)->D2_TP
			Replace (cAliasTemp)->D2_GRUPO   With (self:cAliasSD2)->D2_GRUPO
			Replace (cAliasTemp)->D2_CONTA   With (self:cAliasSD2)->D2_CONTA
			Replace (cAliasTemp)->D2_EMISSAO With StoD((self:cAliasSD2)->D2_EMISSAO)
			Replace (cAliasTemp)->D2_TIPO    With (self:cAliasSD2)->D2_TIPO
			Replace (cAliasTemp)->D2_DOC     With (self:cAliasSD2)->D2_DOC
			Replace (cAliasTemp)->D2_QUANT   With (self:cAliasSD2)->D2_QUANT
			Replace (cAliasTemp)->F4_TEXTO   With (self:cAliasSD2)->F4_TEXTO
			Replace (cAliasTemp)->BM_DESC    With (self:cAliasSD2)->BM_DESC
			Replace (cAliasTemp)->I1_DESC    With (self:cAliasSD2)->I1_DESC
			Replace (cAliasTemp)->B1_DESC    With (self:cAliasSD2)->B1_DESC

			//Operação e/ou tratamento específico para localizado
			//Complemento de IPI e Aplicação do Diferimento do ICMS 
			//conforme ocorre no MATXFIS (issue DSERFIS1=8601)
			self:cAliasLoc		:= cAliasTemp
			self:self:cAliasSD2Loc	:= self:cAliasSD2
			self:processData(3)

			Replace (cAliasTemp)->D2_ITEM    With (self:cAliasSD2)->D2_ITEM
			Replace (cAliasTemp)->D2_CLIENTE With (self:cAliasSD2)->D2_CLIENTE
			Replace (cAliasTemp)->D2_LOJA    With (self:cAliasSD2)->D2_LOJA

			//Grava a moeda/taxa da nota para a conversao durante a impressao
			Replace (cAliasTemp)->D2_MOEDA   With (cAliasSF2)->F2_MOEDA
			Replace (cAliasTemp)->D2_TXMOEDA With (cAliasSF2)->F2_TXMOEDA
			(cAliasTemp)->(MsUnlock())

			lVend := .F.
		Endif

		(self:cAliasSD2)->(dbSkip())
	EndDo

	//Nota de Devolucao
	If MV_PAR04 == 2

		DbSelectArea(self:cAliasSD1)
		DbGoTop()

		While !(self:cAliasSD1)->(Eof())

			//Processamento para posicionamento da SF2 na nota de origem (localizado)
			self:cFilLoc	:= (self:cAliasSD1)->D1_FILIAL
			self:cAliasLoc	:= self:cAliasSD1
			self:processData(4)

			//Somente busca o vendedor, se realmente foi encontrado a nota fiscal de origem
			If (cAliasSF2)->(Found())
				For nCntFor := 1 To nVend
					cVendedor := AllTrim( (cAliasSF2)->(FieldGet((cAliasSF2)->(ColumnPos("F2_VEND"+cVend)))) )
					If cVendedor >= MV_PAR09 .and. cVendedor <= MV_PAR10
						lVend := .T.
						Exit
					Endif
					cVend := Soma1(cVend,1)
				Next nCntFor
				cVend := "1"
			Endif

			If lVend

				SF1->(MsSeek((self:cAliasSD1)->D1_FILIAL+(self:cAliasSD1)->D1_DOC+(self:cAliasSD1)->D1_SERIE+(self:cAliasSD1)->D1_FORNECE+(self:cAliasSD1)->D1_LOJA))
				
				Reclock(cAliasTemp,.T.)
				Replace (cAliasTemp)->D2_FILIAL	 With (self:cAliasSD1)->D1_FILIAL
				Replace (cAliasTemp)->D2_COD 	 With (self:cAliasSD1)->D1_COD
				Replace (cAliasTemp)->D2_LOCAL 	 With (self:cAliasSD1)->D1_LOCAL
				Replace (cAliasTemp)->D2_SERIE 	 With IIf( MV_PAR12==1, (self:cAliasSD1)->D1_SERIORI, (self:cAliasSD1)->D1_SERIE )
				Replace (cAliasTemp)->D2_TES 	 With (self:cAliasSD1)->D1_TES
				Replace (cAliasTemp)->D2_TP 	 With (self:cAliasSD1)->D1_TP
				Replace (cAliasTemp)->D2_GRUPO 	 With (self:cAliasSD1)->D1_GRUPO
				Replace (cAliasTemp)->D2_CONTA 	 With (self:cAliasSD1)->D1_CONTA
				Replace (cAliasTemp)->D2_EMISSAO With StoD((self:cAliasSD1)->D1_DTDIGIT)
				Replace (cAliasTemp)->D2_TIPO 	 With (self:cAliasSD1)->D1_TIPO
				Replace (cAliasTemp)->D2_DOC 	 With IIf(MV_PAR12==1,(self:cAliasSD1)->D1_NFORI,(self:cAliasSD1)->D1_DOC)
				Replace (cAliasTemp)->D2_QUANT 	 With -(self:cAliasSD1)->D1_QUANT
				Replace (cAliasTemp)->D2_TOTAL 	 With -((self:cAliasSD1)->D1_TOTAL-(self:cAliasSD1)->D1_VALDESC)
				Replace (cAliasTemp)->F4_TEXTO   With (self:cAliasSD1)->F4_TEXTO
				Replace (cAliasTemp)->BM_DESC    With (self:cAliasSD1)->BM_DESC
				Replace (cAliasTemp)->I1_DESC    With (self:cAliasSD1)->I1_DESC
				Replace (cAliasTemp)->B1_DESC    With (self:cAliasSD1)->B1_DESC

				//Localização para replace com valor de imposto
				self:cAliasLoc		:= cAliasTemp	
				self:cAliasSD1Loc	:= self:cAliasSD1
				self:processData(5)	

				Replace (cAliasTemp)->D2_PRCVEN  With (self:cAliasSD1)->D1_VUNIT
				Replace (cAliasTemp)->D2_ITEM 	 With (self:cAliasSD1)->D1_ITEM
				Replace (cAliasTemp)->D2_CLIENTE With (self:cAliasSD1)->D1_FORNECE
				Replace (cAliasTemp)->D2_LOJA 	 With (self:cAliasSD1)->D1_LOJA

				//Grava a moeda/taxa da nota para a conversao durante a impressao
				Replace (cAliasTemp)->D2_MOEDA   With SF1->F1_MOEDA
				Replace (cAliasTemp)->D2_TXMOEDA With SF1->F1_TXMOEDA
				(cAliasTemp)->(MsUnlock())

				lVend := .F.
			Endif

			(self:cAliasSD1)->(dbSkip())

		EndDo

	Endif

	//Busca descrições dos campos
	cD2Tes		:= AllTrim(FwX3Titulo("D2_TES"))
	cD2Tp		:= AllTrim(FwX3Titulo("D2_TP")) 
	cD2Grupo	:= AllTrim(FwX3Titulo("D2_GRUPO"))
	cD2Conta	:= AllTrim(FwX3Titulo("D2_CONTA"))
	cD2Cod		:= AllTrim(FwX3Titulo("D2_COD"))

	dbSelectArea(cAliasTemp)
	(cAliasTemp)->(dbGoTop())

	While !(cAliasTemp)->(Eof())

		cColuna := (cAliasTemp)->D2_DOC + "/" + Alltrim((cAliasTemp)->&(self:cSeNFIdSD2))
		cTexto	:= ""
		cTotal	:= ""
		cCpo	:= ""
		cFilTmp := (cAliasTemp)->D2_FILIAL

		cTexto 	:= IIf( NORDER = 1 .Or. NORDER = 6, cD2Tes + ": " + (cAliasTemp)->D2_TES + " - " + (cAliasTemp)->F4_TEXTO,;  // Por Tes
					IIf( NORDER = 2, cD2Tp + ": " + (cAliasTemp)->D2_TP,;													// Por Tipo
						IIf( NORDER = 3, cD2Grupo + ": " + (cAliasTemp)->D2_GRUPO + " - " + (cAliasTemp)->BM_DESC ,;		// Por Grupo
							IIf( NORDER = 4, cD2Conta + ": " + (cAliasTemp)->D2_CONTA + (cAliasTemp)->I1_DESC,;				// Por Conta Contabil
							cD2Cod + ": " + (cAliasTemp)->D2_COD + " " + (cAliasTemp)->B1_DESC ) ) ) )						// Por Produto

		cTotal 	:= IIf( NORDER = 1 .Or. NORDER = 6, (cAliasTemp)->D2_TES + " - " + (cAliasTemp)->F4_TEXTO,;		// Por Tes
					IIf( NORDER = 2, (cAliasTemp)->D2_TP,;														// Por Tipo
						IIf( NORDER = 3, (cAliasTemp)->D2_GRUPO + " - " + (cAliasTemp)->BM_DESC,;				// Por Grupo
							IIf( NORDER = 4, (cAliasTemp)->D2_CONTA,;											// Por Conta Contabil
							(cAliasTemp)->D2_COD ) ) ) )														// Por Produto

		cCpo  	:= IIf( NORDER = 1 .Or. NORDER = 6, (cAliasTemp)->D2_TES,; 	// Por Tes
					IIf( NORDER = 2, (cAliasTemp)->D2_TP,;					// Por Tipo
						IIf( NORDER = 3, (cAliasTemp)->D2_GRUPO,;			// Por Grupo
							IIf( NORDER = 4, (cAliasTemp)->D2_CONTA,;		// Por Conta Contabil
							(cAliasTemp)->D2_COD ) ) ) )					// Por Produto

		cCampo		:= "cCpo"
		nQuant		:= 0
		nTotal		:= 0
		nValIpi		:= 0
		nQuant1		:= 0
		nTotal1		:= 0
		nValIpi1	:= 0

		While &cCampo = &cVaria .And. !Eof()
			//Trato a Devolução de Vendas
			nDevQtd		:= 0
			nDevVal		:= 0
			nDevIPI		:= 0
			nDevQtd1	:= 0
			nDevVal1	:= 0

			If MV_PAR04 == 1  //Devolucao pela NF Original
				self:SVCalcDevR4(cDupli, cEstoq, cAliasTemp, cFilTmp, @nDevQtd, @nDevVal, @nDevIPI, @nDevQtd1, @nDevVal1)
				DbSelectArea(cAliasTemp)
			Endif

			If AvalTes((cAliasTemp)->D2_TES,cEstoq,cDupli)
				nQuant1		:= 0
				nPrcVen1	:= 0
				nTotal1		:= 0
				nValIpi		:= 0
				nQuant		:= (cAliasTemp)->D2_QUANT - nDevQtd
				cColuna		:= (cAliasTemp)->D2_DOC + "/" + Alltrim((cAliasTemp)->&(self:cSeNFIdSD2))
				nPrcVen 	:= xMoeda((cAliasTemp)->D2_PRCVEN,(cAliasTemp)->D2_MOEDA,MV_MOEDA,(cAliasTemp)->D2_EMISSAO,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA)
				nTotal		:= xMoeda((cAliasTemp)->D2_TOTAL,(cAliasTemp)->D2_MOEDA,MV_MOEDA,(cAliasTemp)->D2_EMISSAO,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA) - nDevVal

				//Localização para replace com valor de imposto
				self:cAliasLoc		:= cAliasTemp	
				self:nDevIpiLoc		:= nDevIpi
				self:nValIPILoc		:= nValIpi
				self:processData(6)	

				nValIpi := self:nValIPILoc

				//Commit dos campos
				self:jItems := JsonObject():new()

				self:jItems["FILIAL"]		:= cFilTmp
				self:jItems["CCPOCHAVE"]	:= cCpo
				self:jItems["CORDEMCAB"]	:= cTexto
				self:jItems["CORDEMTOT"]	:= RTrim(cTotal)
				self:jItems["CCOLUNA"]		:= cColuna
				self:jItems["CORDEMSEL"]	:= cOrdemSel
				self:jItems["CDOC"]		:= (cAliasTemp)->D2_DOC
				self:jItems["CSERIE"]		:= (cAliasTemp)->&(self:cSeNFIdSD2)
				self:jItems["NQUANT"]		:= nQuant
				self:jItems["NPRCVEN"]		:= nPrcVen
				self:jItems["NTOTAL"]		:= nTotal
				self:jItems["NVALIPI"]		:= nValIpi
				self:jItems["NQUANT1"]		:= nQuant1
				self:jItems["NPRCVEN1"]	:= nPrcVen1
				self:jItems["NTOTAL1"]		:= nTotal1
				self:jItems["NVALIPI1"]	:= nValIpi1

				self:processData(1) //Commit para campos Localizados
				self:oData:appendData(self:jItems)

			Else
				cColuna  := (cAliasTemp)->D2_DOC + "/" + Alltrim((cAliasTemp)->&(self:cSeNFIdSD2))
				nQuant1  := (cAliasTemp)->D2_QUANT - nDevQtd1
				nQuant	 := 0
				nPrcVen  := 0
				nTotal	 := 0
				nValIpi	 := 0
				nValIpi1 := 0
				nPrcVen1 := IIf( D2_TIPO <> "P", xMoeda((cAliasTemp)->D2_PRCVEN,(cAliasTemp)->D2_MOEDA,MV_MOEDA,(cAliasTemp)->D2_EMISSAO,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA), 0 ) 		 //Complemento de IPI
				nTotal1  := IIf( D2_TIPO <> "P", xMoeda((cAliasTemp)->D2_TOTAL,(cAliasTemp)->D2_MOEDA,MV_MOEDA,(cAliasTemp)->D2_EMISSAO,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA)- nDevVal1, 0 ) //Complemento de IPI

				//Localização para replace com valor de imposto
				self:cAliasLoc		:= cAliasTemp	
				self:nDevIpiLoc		:= nDevIpi
				self:nValIPI1Loc	:= nValIpi1
				self:processData(7)	

				nValIpi1 := self:nValIPI1Loc

				//Commit dos campos
				self:jItems := JsonObject():new()

				self:jItems["FILIAL"]		:= cFilTmp
				self:jItems["CCPOCHAVE"]	:= cCpo
				self:jItems["CORDEMCAB"]	:= cTexto
				self:jItems["CORDEMTOT"]	:= RTrim(cTotal)
				self:jItems["CCOLUNA"]		:= cColuna
				self:jItems["CORDEMSEL"]	:= cOrdemSel
				self:jItems["CDOC"]		:= (cAliasTemp)->D2_DOC
				self:jItems["CSERIE"]		:= (cAliasTemp)->&(self:cSeNFIdSD2)
				self:jItems["NQUANT"]		:= nQuant
				self:jItems["NPRCVEN"]		:= nPrcVen
				self:jItems["NTOTAL"]		:= nTotal
				self:jItems["NVALIPI"]		:= nValIpi
				self:jItems["NQUANT1"]		:= nQuant1
				self:jItems["NPRCVEN1"]	:= nPrcVen1
				self:jItems["NTOTAL1"]		:= nTotal1
				self:jItems["NVALIPI1"]	:= nValIpi1

				self:processData(1) //Commit para campos Localizados
				self:oData:appendData(self:jItems)
			Endif

			(cAliasTemp)->(dbSkip())

		EndDo

	EndDo

	If MV_PAR04 != 3
		(self:cAliasSD1)->(dbCloseArea())

		DbSelectArea("SD1")
		RetIndex("SD1")
		DbClearFilter()
		IIf( File(cArqSD1+OrdBagExt()), Ferase(cArqSD1+OrdBagExt()), )
	Endif

	(cAliasSF2)->(dbCloseArea())
	(self:cAliasSD2)->(dbCloseArea())

	If ValType(oTempTable) == "O"
		oTempTable:Delete()
		oTempTable := Nil
		FreeObj(oTempTable)
	Endif

	aSize(aTam,    0)
	aSize(aIndex,  0)
	aSize(aCampos, 0)

Return self:oData


/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 20/04/2023
	@version 12.1.2410
*/

Method getSchema() as object Class salessummarySmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local aParameters	:= {} as array

	self:oSchema:addProperty("FILIAL", 		STR0022, "string", STR0022, "FILIAL")		//Filial
	self:oSchema:addProperty("CCPOCHAVE", 	STR0011, "string", STR0011, "CCPOCHAVE")	//Chave
	self:oSchema:addProperty("CORDEMCAB", 	STR0012, "string", STR0012, "CORDEMCAB")	//Ordem
	self:oSchema:addProperty("CORDEMTOT", 	STR0013, "string", STR0013, "CORDEMTOT")	//Total
	self:oSchema:addProperty("CCOLUNA", 	STR0014, "string", STR0014, "CCOLUNA")      //Coluna
	self:oSchema:addProperty("CORDEMSEL",	STR0015, "string", STR0015, "CORDEMSEL")    //Ordem Relat
	self:oSchema:addProperty("CDOC", 		STR0016, "string", STR0016, "CDOC")       	//Nota Fiscal
	self:oSchema:addProperty("CSERIE", 		STR0017, "string", STR0017, "CSERIE")       //Série
	self:oSchema:addProperty("NQUANT", 		STR0018, "number", STR0018, "NQUANT")     	//Quantidade
	self:oSchema:addProperty("NPRCVEN", 	STR0019, "number", STR0019, "NPRCVEN")   	//Vlr.Unitario
	self:oSchema:addProperty("NTOTAL",  	STR0020, "number", STR0020, "NTOTAL")       //Vlr.Total
	self:oSchema:addProperty("NVALIPI", 	STR0021, "number", STR0021, "NVALIPI")      //Vlr.IPI
	self:oSchema:addProperty("NQUANT1", 	STR0018, "number", STR0018, "NQUANT1")   	//Quantidade
	self:oSchema:addProperty("NPRCVEN1", 	STR0019, "number", STR0019, "NPRCVEN1") 	//Vlr.Unitario
	self:oSchema:addProperty("NTOTAL1",  	STR0020, "number", STR0020, "NTOTAL1")     	//Vlr.Total
	self:oSchema:addProperty("NVALIPI1", 	STR0021, "number", STR0021, "NVALIPI1")     //Vlr.IPI

	self:oSchema:addParameter("MV_FIL", STR0022 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) 	// Filial
	self:oSchema:addParameter("NORDER", STR0012 + " ?", "number", .F.,,,,,, FTSVHelp(,,.T.,))	//Ordem

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('MTR660', {'MV_PAR03','MV_PAR07','MV_PAR08'} )

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter( aParameters[nX][1] , aParameters[nX][2] , aParameters[nX][3] , aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

	self:oSchema:addParameter("MV_MOEDA", STR0023 + " ?", "number", .F.,,,,,, FTSVHelp("MR780A","09")) // Moeda

	If __LookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) 					// Filial
		self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/SA3", 2)						// Do Vendedor
		self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/SA3", 2)						// Ate o Vendedor
		self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/SB1", 2)						// Do Produto
		self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/SB1", 2)						// Ate o Produto
		self:setCustomURL("NORDER",   "api/faturamento/smartview/v1/options/fat/salessummary/SVComboFATSV011/1", 1)	// Ordem
		self:setCustomURL("MV_PAR04", "api/framework/treports/integratedprovider/v1/options/MTR660/MV_PAR04", 1)	// Inclui Devolução
		self:setCustomURL("MV_PAR05", "api/framework/treports/integratedprovider/v1/options/MTR660/MV_PAR05", 1)	// TES Qto Estoque
		self:setCustomURL("MV_PAR06", "api/framework/treports/integratedprovider/v1/options/MTR660/MV_PAR06", 1)	// TES Qto Faturamento
		self:setCustomURL("MV_PAR11", "api/framework/treports/integratedprovider/v1/options/MTR660/MV_PAR11", 1)	// Considera Dev. Compras
		self:setCustomURL("MV_PAR12", "api/framework/treports/integratedprovider/v1/options/MTR660/MV_PAR12", 1)	// Imprime Documento
	Endif

	FwFreeArray(aParameters)

Return self:oSchema


/*{Protheus.doc} SVComboFATSV011
	Retorna as opçoes disponível para o parâmetro de ordem.
	@return array: Array com as opçoes.
	@author Squad CRM/Faturamento
	@since 14/11/2023
	@version 12.1.2410
*/
Function SVComboFATSV011(cOpcao as character) as array

	Local cDescCombo	:= ""	as character
	Local nX			:= 0	as numeric
	Local aCposIndex	:= {}	as array
	Local aRet			:= {}	as array

	aCposIndex := {	"D2_TES+D2_COD",;
					"D2_TP+D2_COD",;
					"D2_GRUPO+D2_COD",;
					"D2_CONTA+D2_COD",;
					"D2_COD+D2_LOCAL+D2_SERIE+D2_DOC",;
					"D2_TES+D2_SERIE+D2_DOC" }

	If cOpcao == "1"
		For nX := 1 To Len(aCposIndex)
			cDescCombo := FatGetStrCpos(StrTokArr(aCposIndex[nX],"+"))
			aAdd(aRet, cDescCombo)
		Next
	Endif

	aSize(aCposIndex,0)

Return aRet
/*{Protheus.doc} SVCalcDevR4
	Calculo de Devolucoes
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 20/04/2023
	@version 12.1.2410
*/
Method SVCalcDevR4(cDup as character, cEst as character, cAliasTemp as character, cFilSD1 as character, nDevQtd as numeric, nDevVal as numeric, nDevIPI as numeric, nDevQtd1 as numeric, nDevVal1 as numeric) as object class salessummarySmartViewBusinessObject

	If SD1->(MsSeek(cFilSD1 + (cAliasTemp)->D2_DOC + (cAliasTemp)->D2_SERIE + (cAliasTemp)->D2_CLIENTE + (cAliasTemp)->D2_LOJA + (cAliasTemp)->D2_COD + (cAliasTemp)->D2_ITEM))

		//Soma Devolucoes
		If !(SD1->D1_ORIGLAN == "LF")
			If AvalTes(SD1->D1_TES,cEst,cDup)
				If AvalTes(SD1->D1_TES,cEst) .And. (cEst == "S" .Or. cEst == "SN")
					nDevQtd += SD1->D1_QUANT
				Endif
				nDevVal += xMoeda((SD1->D1_TOTAL-SD1->D1_VALDESC),(cAliasTemp)->D2_MOEDA,MV_MOEDA,SD1->D1_DTDIGIT,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA)

				//Localização para replace com valor de imposto
				self:processData(8)
				nDevIpi := self:nDevIpiLoc
			Else
				If AvalTes(SD1->D1_TES,cEst) .And. (cEst == "S" .Or. cEst == "SN")
					nDevQtd1 += SD1->D1_QUANT
				Endif
				nDevVal1 += xMoeda((SD1->D1_TOTAL-SD1->D1_VALDESC),(cAliasTemp)->D2_MOEDA,MV_MOEDA,SD1->D1_DTDIGIT,self:nDecs+1,(cAliasTemp)->D2_TXMOEDA)
			Endif
		Endif

	Endif

Return self

/*{Protheus.doc} FatSv011CreateSQL
	Retorna todas as querys montadas
	@return Nil
	@author Squad CRM/Faturamento
	@since 01/12/2024
	@version 12.1.2410
*/
Method FatSv011CreateSQL() Class salessummarySmartViewBusinessObject

    Local nParam   := 1   as numeric
	Local cQuerySD1   := ""  as character
    Local cQuerySD2   := ""  as character
    Local cFldQrySD1  := ""  as character
	Local cFldQrySD2  := ""  as character
	Local cNameTmpSD1 := ""  as character
	Local cNameTmpSD2 := ""  as character
	Local cOrder 	  := ""  as character
	Local cSeNFIdSD1  := SerieNfId("SD1", 3, "D1_SERIE")   as character
	Local cOrNFIdSD1  := SerieNfId("SD1", 3, "D1_SERIORI") as character
	Local aFiliais := {}  as array
    Local oQrySD1 	  := Nil as object
    Local oQrySD2 	  := Nil as object
	Local oTempTableBranchSD1 := Nil as object
	Local oTempTableBranchSD2 := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranchSD1 := FatSVTableTempBranch(aFiliais, "SD1", "D1_FILIAL")
	oTempTableBranchSD2 := FatSVTableTempBranch(aFiliais, "SD2", "D1_FILIAL")

	cNameTmpSD1 := oTempTableBranchSD1:GetTableNameForQuery()
	cNameTmpSD2 := oTempTableBranchSD2:GetTableNameForQuery()

	cFldQrySD1 := "D1_FILIAL,D1_COD,D1_SERIORI,D1_NFORI,D1_ITEMORI,D1_FORNECE,D1_LOJA,D1_DOC,D1_SERIE,"
	cFldQrySD1 += "D1_LOCAL,D1_TES,D1_TP,D1_GRUPO,D1_CONTA,D1_DTDIGIT,D1_TIPO,D1_QUANT,D1_TOTAL,D1_SDOC,"
	cFldQrySD1 += "D1_VALDESC,D1_VALIPI,D1_ITEM,D1_VALIMP1,D1_VUNIT,F4_TEXTO,BM_DESC,I1_DESC,B1_DESC,D1_SDOCORI"
	cFldQrySD1 += IIf(Alltrim(cSeNFIdSD1) <> "D1_SERIE", (cSeNFIdSD1 + ","), "")
	cFldQrySD1 += IIf(Alltrim(cSeNFIdSD1) <> "D1_SERIE", (cOrNFIdSD1 + ","), "")
	cFldQrySD1 += IIf(Len(self:aQrySD1Loc) > 0, ","+ArrTokStr(self:aQrySD1Loc, ","), "") //Tratamento para campos adicionais localizados

	cQuerySD1 += " SELECT ? "
	cQuerySD1 += " FROM " + RetSqlName("SD1") + " SD1 "
	cQuerySD1 += " INNER JOIN ? TMPFIL"
	cQuerySD1 += " ON TMPFIL.TMP_FILIAL = SD1.D1_FILIAL"	
	cQuerySD1 += " LEFT JOIN " + RetSqlName("SF4") + " SF4 ON ? "
	cQuerySD1 += " AND SF4.F4_CODIGO = SD1.D1_TES "
	cQuerySD1 += " AND SF4.D_E_L_E_T_  = ? "
	cQuerySD1 += " LEFT JOIN " + RetSqlName("SBM") + " SBM ON ? "
	cQuerySD1 += " AND SBM.BM_GRUPO = SD1.D1_GRUPO "
	cQuerySD1 += " AND SBM.D_E_L_E_T_  = ? "
	cQuerySD1 += " LEFT JOIN " + RetSqlName("SI1") + " SI1 ON ? "
	cQuerySD1 += " AND SI1.I1_CODIGO = SD1.D1_CONTA "
	cQuerySD1 += " AND SI1.D_E_L_E_T_ = ? "
	cQuerySD1 += " LEFT JOIN " + RetSqlName("SB1") + " SB1 ON ? "
	cQuerySD1 += " AND SB1.B1_COD = SD1.D1_COD "
	cQuerySD1 += " AND SB1.D_E_L_E_T_ = ? "
	cQuerySD1 += " WHERE SD1.D1_FILIAL = TMPFIL.TMP_FILIAL "
	cQuerySD1 += " AND SD1.D1_TIPO = ? "
	cQuerySD1 += " AND SD1.D1_COD BETWEEN ? AND ?"
	cQuerySD1 += " AND SD1.D1_DTDIGIT BETWEEN ? AND ? "
	cQuerySD1 += " AND NOT (?) "
	cQuerySD1 += " ? "	//Tratamento para complemento de WHERE que foi populado no objeto localizado
	cQuerySD1 += " AND SD1.D_E_L_E_T_  = ? "
	cQuerySD1 += " ORDER BY D1_FILIAL, D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, D1_COD, D1_ITEM "

	oQrySD1 := FwExecStatement():New(ChangeQuery(cQuerySD1))
	oQrySD1:SetUnsafe(nParam++, cFldQrySD1)
	oQrySD1:SetUnsafe(nParam++, cNameTmpSD1)
	oQrySD1:SetUnsafe(nParam++, FwJoinFilial("SF4", "SD1"))
	oQrySD1:SetString(nParam++, ' ')
	oQrySD1:SetUnsafe(nParam++, FwJoinFilial("SBM", "SD1"))
	oQrySD1:SetString(nParam++, ' ')
	oQrySD1:SetUnsafe(nParam++,  FwJoinFilial("SI1", "SD1"))
	oQrySD1:SetString(nParam++, ' ')
	oQrySD1:SetUnsafe(nParam++, FwJoinFilial("SB1", "SD1"))
	oQrySD1:SetString(nParam++, ' ')
	oQrySD1:SetString(nParam++, 'D')
	oQrySD1:SetString(nParam++, MV_PAR13)
	oQrySD1:SetString(nParam++, MV_PAR14)
	oQrySD1:SetDate(nParam++, MV_PAR01)
	oQrySD1:SetDate(nParam++, MV_PAR02)
	oQrySD1:SetUnsafe(nParam++, IsRemito(2,"SD1.D1_TIPODOC"))
	oQrySD1:SetUnsafe(nParam++, self:cWhereSD1)
	oQrySD1:SetString(nParam++, ' ')

	self:cAliasSD1 := oQrySD1:OpenAlias()

	//Monta a query de itens de Nf de saída
	cFldQrySD2 := "D2_FILIAL,D2_COD,D2_LOCAL,"
	cFldQrySD2 += "D2_SERIE,D2_TES,D2_TP,D2_GRUPO,D2_CONTA,D2_EMISSAO,D2_TIPO,D2_DOC,D2_QUANT,D2_TOTAL,D2_VALIPI,"
	cFldQrySD2 += "D2_PRCVEN,D2_ITEM,D2_CLIENTE,D2_LOJA,F2_MOEDA,F2_TXMOEDA,F4_TEXTO,BM_DESC,I1_DESC,B1_DESC"
	cFldQrySD2 += self:cCposSD2
	cFldQrySD2 += IIf(Alltrim(self:cSeNFIdSD2) <> "D2_SERIE", (self:cSeNFIdSD2 + ","), "")
	cFldQrySD2 += IIf(Len(self:aQrySD2Loc) > 0, ","+ArrTokStr(self:aQrySD2Loc, ","), "") //Tratamento para campos adicionais localizados

	//Verifica se ha necessidade de Indexacao no SD2
	cOrder := IIf( NORDER = 1 .Or. NORDER = 6, "D2_FILIAL,D2_TES,"+IIf(NORDER == 1,"D2_COD","D2_SERIE,D2_DOC"),;	// Por Tes
				IIf(NORDER = 2, "D2_FILIAL,D2_TP,D2_COD",;															// Tipo do Produto, Codigo do Produto)
					IIf(NORDER = 3, "D2_FILIAL,D2_GRUPO,D2_COD",;													// Por Grupo
						IIf(NORDER = 4, "D2_FILIAL,D2_CONTA,D2_COD",;												// Por Conta Contabil
							"D2_FILIAL,D2_COD,D2_LOCAL,D2_SERIE,D2_DOC" ) ) ) )										// Por Produto


	cQuerySD2 += " SELECT ? "
	cQuerySD2 += " FROM " + RetSqlName("SD2") + " SD2 "
	cQuerySD2 += " INNER JOIN ? TMPFIL"
	cQuerySD2 += " ON TMPFIL.TMP_FILIAL = SD2.D2_FILIAL"	
	cQuerySD2 += " LEFT JOIN " + RetSqlName("SF2") + " SF2 ON ? "
	cQuerySD2 += " AND	SF2.F2_DOC = SD2.D2_DOC "
	cQuerySD2 += " AND	SF2.F2_SERIE = SD2.D2_SERIE "
	cQuerySD2 += " AND	SF2.F2_CLIENTE = SD2.D2_CLIENTE "
	cQuerySD2 += " AND	SF2.F2_LOJA = SD2.D2_LOJA "
	cQuerySD2 += " AND SF2.D_E_L_E_T_ = ? "
	cQuerySD2 += " LEFT JOIN " + RetSqlName("SF4") + " SF4 ON ? "
	cQuerySD2 += " AND SF4.F4_CODIGO = SD2.D2_TES "
	cQuerySD2 += " AND SF4.D_E_L_E_T_ = ? "
	cQuerySD2 += " LEFT JOIN " + RetSqlName("SBM") + " SBM ON ? "
	cQuerySD2 += " AND SBM.BM_GRUPO = SD2.D2_GRUPO "
	cQuerySD2 += " AND SBM.D_E_L_E_T_ = ? "
	cQuerySD2 += " LEFT JOIN " + RetSqlName("SI1") + " SI1 ON ? "
	cQuerySD2 += " AND SI1.I1_CODIGO = SD2.D2_CONTA "
	cQuerySD2 += " AND SI1.D_E_L_E_T_ = ? "
	cQuerySD2 += " LEFT JOIN " + RetSqlName("SB1") + " SB1 ON ? "
	cQuerySD2 += " AND SB1.B1_COD = SD2.D2_COD "
	cQuerySD2 += " AND SB1.D_E_L_E_T_ = ? "
	cQuerySD2 += " WHERE SD2.D2_FILIAL = TMPFIL.TMP_FILIAL "
	cQuerySD2 += " AND SD2.D2_EMISSAO BETWEEN ? AND ? "
	cQuerySD2 += " AND SD2.D2_COD BETWEEN ? AND ? "
	cQuerySD2 += " AND SD2.D2_ORIGLAN <> ? "
	cQuerySD2 += " AND D2_TIPO NOT IN (?) "
	cQuerySD2 += " AND NOT (?) "
	cQuerySD2 += " ? "	//Tratamento para complemento de WHERE que foi populado no objeto localizado	
	cQuerySD2 += " AND SD2.D_E_L_E_T_ = ? "
	cQuerySD2 += " ORDER BY ? "

	nParam := 1
    oQrySD2 := FwExecStatement():New(ChangeQuery(cQuerySD2))
	oQrySD2:SetUnsafe(nParam++, cFldQrySD2)
	oQrySD2:SetUnsafe(nParam++, cNameTmpSD2)
	oQrySD2:SetUnsafe(nParam++, FwJoinFilial("SF2", "SD2"))
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetUnsafe(nParam++, FwJoinFilial("SF4", "SD2"))
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetUnsafe(nParam++, FwJoinFilial("SBM", "SD2"))
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetUnsafe(nParam++, FwJoinFilial("SI1", "SD2"))
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetUnsafe(nParam++, FwJoinFilial("SB1", "SD2"))
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetDate(nParam++, MV_PAR01)
	oQrySD2:SetDate(nParam++, MV_PAR02)
	oQrySD2:SetString(nParam++, MV_PAR13)
	oQrySD2:SetString(nParam++, MV_PAR14)
	oQrySD2:SetString(nParam++, 'LF')

	If MV_PAR04 == 3 .Or. MV_PAR11 == 2
		oQrySD2:SetIn(nParam++, {'B','D','I'})
	Else
		oQrySD2:SetIn(nParam++, {'B','I'})
	Endif

	oQrySD2:SetUnsafe(nParam++, IsRemito(2,"SD2.D2_TIPODOC"))
	oQrySD2:SetUnsafe(nParam++, self:cWhereSD2)
	oQrySD2:SetString(nParam++, ' ')
	oQrySD2:SetUnsafe(nParam++, cOrder)

	self:cAliasSD2 := oQrySD2:OpenAlias()

	oTempTableBranchSD1:Delete()
	oTempTableBranchSD2:Delete()

    FWFreeObj(oTempTableBranchSD1)
    FWFreeObj(oTempTableBranchSD2)

    oQrySD1:Destroy()
    oQrySD2:Destroy()

	FreeObj(oQrySD1)
	FreeObj(oQrySD2)

	oQrySD1:= Nil
	oQrySD2:= Nil

    aSize(aFiliais,0)

Return
