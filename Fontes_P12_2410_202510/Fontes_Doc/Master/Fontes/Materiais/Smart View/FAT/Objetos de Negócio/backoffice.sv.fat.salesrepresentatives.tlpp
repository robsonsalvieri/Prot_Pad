#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesrepresentatives.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.salesrepresentatives.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA3", name="Vendedores", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesrepresentativesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para listagem de vendedores

@author FAT/CRM
@since 18/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class salesrepresentativesSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV003CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 18/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salesrepresentativesSmartViewBusinessObject

	_Super:new()

    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Vendedores"
    self:setDescription(STR0003)	//"Listagem de Vendedores"
    self:setIsLookUp(.T.)

    self:cSelectLoc := ""
    self:cWhereLoc  := ""
    self:cAlias     := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 18/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class salesrepresentativesSmartViewBusinessObject

    Local aPDFields     := {}   as array

    Local nX            := 0    as numeric

    Local lObfuscated   := .F.  as logical

    Private aCpoCustom    := self:getCustomFields() as array

    Private MV_FIL		:= ""   as character
    Private CCODINI     := ""   as character
    Private CCODFIM     := ""   as character
    Private CNOMEINI    := ""   as character
    Private CNOMEFIM    := ""   as character
    Private CCGCINI     := ""   as character
    Private CCGCFIM     := ""   as character
    Private CGERINI     := ""   as character
    Private CGERFIM     := ""   as character
    Private CSUPINI     := ""   as character
    Private CSUPFIM     := ""   as character
    Private CEQPINI     := ""   as character
    Private CEQPFIM     := ""   as character
    Private CUSRINI     := ""   as character
    Private CUSRFIM     := ""   as character
    Private CNVLINI     := ""   as character
    Private CNVLFIM     := ""   as character
    Private NORDER      := 1    as numeric

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "")
  
    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom)
    lObfuscated := Len( aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
	self:FatSV003CreateSQL()

    (self:cAlias)->(dbGotop())

    While !(self:cAlias)->(Eof())

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            self:jItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias)

        Next nX

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(DbSkip())

    EndDo

    (self:cAlias)->(dbCloseArea())

	aSize(aPDFields, 0)
    aSize(aCpoCustom, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 18/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class salesrepresentativesSmartViewBusinessObject

    Local nX := 0 as numeric

    self:aFields := {"A3_FILIAL","A3_COD","A3_NOME","A3_NREDUZ","A3_EST","A3_TIPO","A3_CGC","A3_GEREN","A3_SUPER","A3_GRPREP","A3_CODUSR","A3_NVLSTR"}

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

	self:oSchema:addParameter("NORDER",   STR0020, "number", .F.,,,,,, FTSVHelp(,,.T.))         //Ordem
    self:oSchema:addParameter("MV_FIL",   STR0021, "string", .T.,,,,,, FTSVHelp(,,,.T.))        //Filial
	self:oSchema:addParameter("CCODINI",  STR0004, "string", .F.,,,,,, FTSVHelp("A3_COD"))	    //Código de
	self:oSchema:addParameter("CCODFIM",  STR0005, "string", .F.,,,,,, FTSVHelp("A3_COD"))	    //Código até
	self:oSchema:addParameter("CNOMEINI", STR0006, "string", .F.,,,,,, FTSVHelp("A3_NOME"))	    //Nome de
	self:oSchema:addParameter("CNOMEFIM", STR0007, "string", .F.,,,,,, FTSVHelp("A3_NOME"))	    //Nome até
	self:oSchema:addParameter("CCGCINI",  STR0008, "string", .F.,,,,,, FTSVHelp("A3_CGC"))	    //CPF/CNPJ de
	self:oSchema:addParameter("CCGCFIM",  STR0009, "string", .F.,,,,,, FTSVHelp("A3_CGC"))	    //CPF/CNPJ até
	self:oSchema:addParameter("CGERINI",  STR0010, "string", .F.,,,,,, FTSVHelp("A3_GEREN"))	//Gerente de
	self:oSchema:addParameter("CGERFIM",  STR0011, "string", .F.,,,,,, FTSVHelp("A3_GEREN"))	//Gerente até
	self:oSchema:addParameter("CSUPINI",  STR0012, "string", .F.,,,,,, FTSVHelp("A3_SUPER"))	//Supervisor de
	self:oSchema:addParameter("CSUPFIM",  STR0013, "string", .F.,,,,,, FTSVHelp("A3_SUPER"))	//Supervisor até
	self:oSchema:addParameter("CEQPINI",  STR0014, "string", .F.,,,,,, FTSVHelp("A3_GRPREP"))	//Equipe de
	self:oSchema:addParameter("CEQPFIM",  STR0015, "string", .F.,,,,,, FTSVHelp("A3_GRPREP"))	//Equipe até
	self:oSchema:addParameter("CUSRINI",  STR0016, "string", .F.,,,,,, FTSVHelp("A3_CODUSR"))	//Cód.Usuário de
	self:oSchema:addParameter("CUSRFIM",  STR0017, "string", .F.,,,,,, FTSVHelp("A3_CODUSR"))	//Cód.Usuário até
	self:oSchema:addParameter("CNVLINI",  STR0018, "string", .F.,,,,,, FTSVHelp("A3_NVLSTR"))	//Nível Estr. de
	self:oSchema:addParameter("CNVLFIM",  STR0019, "string", .F.,,,,,, FTSVHelp("A3_NVLSTR"))	//Nível Estr. até

    //Validação para versão da Lib
	If __lLookUp
		self:setCustomURL("MV_FIL",  "api/framework/v1/genericLookupService/smartview/SM0", 2)
        self:setCustomURL("CCODINI", "api/framework/v1/genericLookupService/smartview/SA3", 2)
        self:setCustomURL("CCODFIM", "api/framework/v1/genericLookupService/smartview/SA3", 2)
		self:setCustomURL("NORDER",  "api/faturamento/smartview/v1/options/fat/salesrepresentatives/SVComboFATSV003/1", 1)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVComboFATSV003
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 18/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVComboFATSV003(cOpcao as character) as array

    Local aCposIndex	:= {} as array
	Local aRet			:= {} as array
	Local cDescCombo	:= "" as character
	Local nX			:= 0  as numeric

	aCposIndex := {	"A3_COD",;
					"A3_NOME",;
					"A3_CGC",;
                    "A3_GEREN",;
                    "A3_SUPER",;
                    "A3_GRPREP",;
                    "A3_CODUSR",;
					"A3_NVLSTR" }

	If cOpcao == "1"
		For nX := 1 To Len(aCposIndex)
			cDescCombo := FatGetStrCpos(StrTokArr(aCposIndex[nX],"+"))
			aAdd(aRet, cDescCombo)
		Next
	EndIf

	aSize(aCposIndex,0)

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv001CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 21/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV003CreateSQL() Class salesrepresentativesSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array
    Local aOrdem     := {}  as array

    //Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    //Monta campos do Select
    cFieldsQry := "SA3.A3_FILIAL,SA3.A3_COD,SA3.A3_NOME,SA3.A3_NREDUZ,SA3.A3_EST,SA3.A3_TIPO,SA3.A3_CGC,SA3.A3_GEREN,"
    cFieldsQry += "SA3.A3_SUPER,SA3.A3_GRPREP,SA3.A3_CODUSR,SA3.A3_NVLSTR"
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .F., .T.)

    // Define a ordenação do resultado da query
    aOrdem := { " SA3.A3_FILIAL,SA3.A3_COD",;
                " SA3.A3_FILIAL,SA3.A3_NOME",;
                " SA3.A3_FILIAL,SA3.A3_CGC",;
                " SA3.A3_FILIAL,SA3.A3_GEREN",;
                " SA3.A3_FILIAL,SA3.A3_SUPER",;
                " SA3.A3_FILIAL,SA3.A3_GRPREP",;
                " SA3.A3_FILIAL,SA3.A3_CODUSR",;
                " SA3.A3_FILIAL,SA3.A3_NVLSTR"}

    //Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        //-------------------------------------------------------------------------------------------------------
        // Query Principal
        // BIND - Indice SA3 (9): A3_FILIAL+A3_COD+A3_NOME+A3_CGC+A3_GEREN+A3_SUPER+A3_GRPREP+A3_CODUSR+A3_NVLSTR
        //-------------------------------------------------------------------------------------------------------
        cQuery  += " SELECT ? "
        cQuery  += "    FROM ? SA3 "
        cQuery  += " WHERE "
        cQuery  += "    SA3.A3_FILIAL = ? "
        cQuery  += "    AND SA3.A3_COD     BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_NOME    BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_CGC     BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_GEREN   BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_SUPER   BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_GRPREP  BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_CODUSR  BETWEEN ? AND ? "
        cQuery  += "    AND SA3.A3_NVLSTR  BETWEEN ? AND ? "
        cQuery  += "    ? " //Where para SA3 que foi populado no objeto localizado
        cQuery  += "    AND SA3.D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetUnsafe(nParam++, RetSqlName('SA3'))
        oQuery:SetString(nParam++, FatSVFilial('SA3', 'A3_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, CCODINI)
        oQuery:SetString(nParam++, CCODFIM)
        oQuery:SetString(nParam++, CNOMEINI)
        oQuery:SetString(nParam++, CNOMEFIM)
        oQuery:SetString(nParam++, CCGCINI)
        oQuery:SetString(nParam++, CCGCFIM)
        oQuery:SetString(nParam++, CGERINI)
        oQuery:SetString(nParam++, CGERFIM)
        oQuery:SetString(nParam++, CSUPINI)
        oQuery:SetString(nParam++, CSUPFIM)
        oQuery:SetString(nParam++, CEQPINI)
        oQuery:SetString(nParam++, CEQPFIM)
        oQuery:SetString(nParam++, CUSRINI)
        oQuery:SetString(nParam++, CUSRFIM)
        oQuery:SetString(nParam++, CNVLINI)
        oQuery:SetString(nParam++, CNVLFIM)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, " ")

    Next nX

    oQuery:SetUnsafe(nParam++, IIf(NORDER > 0, aOrdem[NORDER], ""))

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)
    aSize(aOrdem, 0)

Return
