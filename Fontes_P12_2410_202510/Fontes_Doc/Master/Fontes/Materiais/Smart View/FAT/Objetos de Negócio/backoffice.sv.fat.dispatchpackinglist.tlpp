#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.dispatchpackinglist.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.dispatchpackinglist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SA4,SB1,SC5,SC6,SC9,SF2", name="Romaneio de Despacho", country="ALL", customTables="SC5,SF2,SC6,SC9,SB1,SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} dispatchpackinglistSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Recibo para despacho.

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class dispatchpackinglistSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV020CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data ojItems      as json
    protected data cAlias       as character
	protected data cAliasTmpLoc as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class dispatchpackinglistSmartViewBusinessObject 
	
	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Romaneio para Despacho"
    self:setDescription(STR0003)	//"Relação de Romaneio para Despacho"
    self:setIsLookUp(.T.)  

    self:cAlias := ""

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class dispatchpackinglistSmartViewBusinessObject

    Local cNomeTransp   := ""               as character
    Local cVia          := ""               as character
    Local cTranspEnder  := ""               as character
    Local cTranspMunic  := ""               as character
    Local cTranspEstad  := ""               as character
    Local cMascara      := SuperGetMv("MV_MASCGRD") as character
    Local xValue        := ""               as character

    Local nTamRef       := Val(Substr(cMascara,1,2)) as numeric
    Local nTotLib       := 0                as numeric
    Local nQtdVlrTotal  := 0                as numeric
    Local nReg          := 0                as numeric
    Local nX            := 0                as numeric
    Local nTamProd	    := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric

    Local lObfuscated   := .F.              as logical

    Local aPDFields     := {}               as array

    Private aFieldsValue:= {}               as array
    Private aCpoCustom  := self:getCustomFields() as array

    Private MV_FIL		:= ""               as character
    Private MV_MOEDA    := 1                as numeric
    Private MV_AGLGR    := 1                as numeric

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"MTR790")

    //Tratamento para permitir apenas o número de moedas utilizadas
    MV_MOEDA := IIf(MV_MOEDA <= 0 .Or. MV_MOEDA > ContaMoeda(), 1, MV_MOEDA)

    //Tratamento tamanho do campo mascara do produto
    MV_PAR05 := PADR(MV_PAR05, nTamProd)

    //Array para processamento    
    aFieldsValue := {	{'A4_NREDUZ' ,{'A4_NREDUZ'  ,"",'cNomeTransp'}},;
						{'A4_VIA'    ,{'A4_VIA'     ,"",'cVia'}},;
						{'A4_END'    ,{'A4_END'     ,"",'cTranspEnder'}},;
						{'A4_MUN'    ,{'A4_MUN'     ,"",'cTranspMunic'}},;
                        {'A4_EST'    ,{'A4_EST'     ,"",'cTranspEstad'}},;
                        {'C9_PRCVEN' ,{'C9_PRCVEN'  ,"",'xMoeda((self:cAlias)->C9_PRCVEN,1,MV_MOEDA,(self:cAlias)->C5_EMISSAO)'}},;
                        {'C9_QTDLIB' ,{'C9_QTDLIB'  ,"",'nTotLib'}},;
                        {'B1_PICM'   ,{'B1_PICM'    ,"",'IIF(!Empty((self:cAlias)->B1_PICM), (self:cAlias)->B1_PICM, (self:cAlias)->B1_ALIQISS )'}},;
                        {'F2_VALBRUT',{'F2_VALBRUT' ,"",'xMoeda((nQtdVlrTotal*(self:cAlias)->C9_PRCVEN),1,MV_MOEDA,(self:cAlias)->C5_EMISSAO)'}},;
                        {'C9_SERIENF',{'C9_SERIENF' ,"",'(self:cAlias)->&(SerieNfId("SC9",3,"C9_SERIENF"))'}};
                    }

    //Irá montar as queries do objeto de negócio
    self:FatSV020CreateSQL()

	//Posiciona no registro inicial
	(self:cAlias)->(dbGoTop())

    //Tratamento LGPD
    aPDFields := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := len( aPDFields ) > 0

    SA4->(DbSetOrder(1))

	While !(self:cAlias)->(Eof())

        If ValidMasc((self:cAlias)->C9_PRODUTO, MV_PAR05)

            nTotLib      := (self:cAlias)->C9_QTDLIB
            nQtdVlrTotal := 0

            // Imprimi item com grade
            If (self:cAlias)->C6_GRADE == "S" .And. MV_AGLGR == 1

                cProdRef := Substr((self:cAlias)->C9_PRODUTO,1,nTamRef)
                cPedido  := (self:cAlias)->C9_PEDIDO
                nReg     := 0
                nTotLib  := 0

                While !Eof() .And. Substr((self:cAlias)->C9_PRODUTO,1,nTamRef) == cProdRef .And. (self:cAlias)->C9_PEDIDO == cPedido

                    nReg:=Recno()

                    If (Empty((self:cAlias)->C9_BLEST) .And. Empty((self:cAlias)->C9_BLCRED) .And. (self:cAlias)->C9_BLEST == "10" .And. (self:cAlias)->C9_BLCRED == "10");
                        .Or. (ValidMasc((self:cAlias)->C9_PRODUTO, MV_PAR05))

                        nTotLib += (self:cAlias)->C9_QTDLIB

                    EndIf

                    dbSkip()

                EndDo

                If nReg > 0
                    dbGoto(nReg)
                    nReg:=0
                EndIf

                nQtdVlrTotal := nTotLib

            Else

                nQtdVlrTotal := (self:cAlias)->C9_QTDLIB

            EndIf

            SA4->(MsSeek(FatSVFilial('SA4', 'A4_FILIAL', (self:cAlias)->C5_FILIAL)+(self:cAlias)->C5_TRANSP))

            cNomeTransp := SA4->A4_NREDUZ 
            cVia        := SA4->A4_VIA
            cTranspEnder:= SA4->A4_END
            cTranspMunic:= SA4->A4_MUN
            cTranspEstad:= SA4->A4_EST

            // Impressão do Relatório com componentes
            self:ojItems := JsonObject():new()

            For nX := 1 To Len(self:aStruct)

                If (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
                    xValue  := FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2])
                Else
                    xValue := "(self:cAlias)->"+self:aStruct[nX][5]
                EndIf

                If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                    self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
                ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                    self:ojItems[self:aStruct[nX][1]] := IIF( ValType((self:cAlias)->&(self:aStruct[nX][5])) == "C" , totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) ,  totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
                Else
                    self:ojItems[self:aStruct[nX][1]] := &(xValue)
                EndIf

            Next nX

            self:processData(1)
            self:oData:appendData(self:ojItems)

        EndIf

        (self:cAlias)->(DbSkip())

	EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields,0)
    aSize(aFieldsValue,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class dispatchpackinglistSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local aParameters   := {} as array

    self:aFields := {'C5_FILIAL','C5_NUM', 'C5_EMISSAO', 'C5_CLIENTE' , 'C5_LOJACLI' , 'A1_NOME' , 'A1_END',;
                     'A1_BAIRRO', 'A1_MUN', 'A1_EST', 'C5_TRANSP', 'A4_NREDUZ' ,'A4_VIA' , 'A4_END',;
                     'A4_MUN', 'A4_EST', 'C9_PRODUTO', 'C6_DESCRI' , 'C6_UM', 'C9_QTDLIB', 'C9_PRCVEN',;
                     'B1_IPI', 'B1_PICM', 'F2_VALBRUT', 'F2_EMISSAO', 'C6_LOCAL', 'C9_NFISCAL', 'C9_SERIENF'}

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    //Retorna do SX1 os parâmetros que serão utilizados, no array os que não serão utilizados
    aParameters := FtSVSetParam('MTR790',,,{'06','07','08'})

    self:oSchema:addParameter("MV_FIL", STR0005 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial ?

    For nX := 1 To Len(aParameters)
        self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
    Next nX

    // Adiciona novo parametro
    self:oSchema:addParameter("MV_AGLGR", STR0004 + " ?", "number", .F.,,,,,, FTSVHelp("MTR790","06"))  // Aglutina Prod. Grade ?
    self:oSchema:addParameter("MV_MOEDA", STR0006 + " ?", "number", .F.,,,,,, FTSVHelp("MTR790","07"))  // Moeda ?

    If __lLookUp
        self:setCustomURL("MV_FIL"  , "api/framework/v1/genericLookupService/smartview/SM0", 2)                         // Filial ?
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/CBL", 2)                         // Do Pedido ?
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/CBL", 2)                         // Até o Pedido ?
        self:setCustomURL("MV_AGLGR", "api/faturamento/smartview/v1/options/fat/dispatchpackinglist/SVFTDispPar/1", 1)  // Aglutina Prod. Grade ?
    EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFTDispPar
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFTDispPar( cOpcao )

	Local aRet :=  {} as  array

	If cOpcao == "1"
		aRet :=  FatGetCboX1('MTR790', 6)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv020CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV020CreateSQL() class dispatchpackinglistSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    For nX := 1 To Len(self:aFields)
        If 'C9_SERIENF' == self:aFields[nX]
            cFieldsQry += IIf( SerieNfId("SC9",3,"C9_SERIENF")<>"C9_SERIENF", SerieNfId("SC9",3,"C9_SERIENF")+"," ,"" )
        EndIf 
		cFieldsQry += IIF( !'A4_' $  self:aFields[nX], self:aFields[nX] + " ,", "")
	Next nX

    cFieldsQry += 'B1_ALIQISS, C9_FILIAL, C9_BLCRED, C9_BLEST, C9_PEDIDO, C9_CLIENTE, C9_LOJA, C6_GRADE, C9_ITEM, C9_SEQUEN'
    cFieldsQry += self:cSelectLoc

    //Adiciona campos customizados (no array da estrutura, campos e query)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCpoCustom )

    For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM "      + RetSqlName("SC9") + " SC9 "
        cQuery += "INNER JOIN " + RetSqlName("SC5") + " SC5 "
        cQuery += " ON  SC5.C5_FILIAL   = ? "
        cQuery += " AND SC5.C5_NUM      = SC9.C9_PEDIDO "
        cQuery += " AND NOT(SC5.C5_TIPO IN (?)) "
        cQuery += " AND SC5.D_E_L_E_T_  = ? "
        cQuery += "INNER JOIN " + RetSqlName("SC6") + " SC6 "
        cQuery += " ON  SC6.C6_FILIAL   = ? "
        cQuery += " AND SC6.C6_NUM      = SC9.C9_PEDIDO "
        cQuery += " AND SC6.C6_PRODUTO  = SC9.C9_PRODUTO "
        cQuery += " AND SC6.C6_ITEM     = SC9.C9_ITEM "
        cQuery += " AND SC6.D_E_L_E_T_  = ? "
        cQuery += "INNER JOIN " + RetSqlName("SF2") + " SF2 "
        cQuery += " ON  SF2.F2_FILIAL   = ? "
        cQuery += " AND SF2.F2_DOC      = SC9.C9_NFISCAL "
        cQuery += " AND SF2.F2_SERIE    = SC9.C9_SERIENF "
        cQuery += " AND SF2.F2_CLIENTE  = SC9.C9_CLIENTE "
        cQuery += " AND SF2.F2_LOJA     = SC9.C9_LOJA "
        cQuery += " AND SF2.F2_EMISSAO >= ? "
        cQuery += " AND SF2.F2_EMISSAO <= ? "
        cQuery += " AND SF2.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN "  + RetSqlName("SB1") + " SB1 "
        cQuery += " ON  SB1.B1_FILIAL   = ? "
        cQuery += " AND SB1.B1_COD      = SC9.C9_PRODUTO "
        cQuery += " AND SB1.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN "  + RetSqlName("SA1") + " SA1 "
        cQuery += " ON  SA1.A1_FILIAL   = ? "
        cQuery += " AND SA1.A1_COD      = SC9.C9_CLIENTE "
        cQuery += " AND SA1.A1_LOJA     = SC9.C9_LOJA  "
        cQuery += " AND SA1.D_E_L_E_T_  = ? "
        cQuery += "WHERE SC9.C9_FILIAL  = ? "
        cQuery += " AND SC9.C9_PEDIDO  >= ? "
        cQuery += " AND SC9.C9_PEDIDO  <= ? "
        cQuery += " AND (SC9.C9_BLEST   = ? OR SC9.C9_BLEST  = ? ) "
        cQuery += " AND (SC9.C9_BLCRED  = ? OR SC9.C9_BLCRED = ? ) "
        cQuery +=  " ? "
        cQuery += " AND SC9.D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
            cQuery += " ORDER BY SC9.C9_FILIAL, SC9.C9_PEDIDO, SC9.C9_PRODUTO, SC9.C9_CLIENTE, SC9.C9_LOJA, SC9.C9_ITEM, SC9.C9_SEQUEN "
        EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial('SC5', 'C5_FILIAL', aFiliais[nX]))
        oQuery:SetIn(nParam++, {'D','B'})
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SC6', 'C6_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, DtoS(MV_PAR03))
        oQuery:SetString(nParam++, DtoS(MV_PAR04))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SC9', 'C9_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, MV_PAR01)
        oQuery:SetString(nParam++, MV_PAR02)
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, '10')
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, '10')
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
