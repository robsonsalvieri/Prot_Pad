#include "totvs.ch"
#include "protheus.ch"
#include "msobject.ch"
#include "fwlibversion.ch"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.paymentstermslist.ch"
#include "totvs.framework.treports.integratedprovider.th"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.paymentstermslist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SE4", name="Condições de Pagamentos", country="ALL", customTables="SE4")

//-------------------------------------------------------------------
/*/{Protheus.doc} paymentstermslistSmartViewBusinessObject

Classe para criação do Objeto de Negócio para o cadastro das Condições de Pagamentos

@author FAT/CRM
@since 07/05/2024
@version 1.0
*/
//-------------------------------------------------------------------  
class paymentstermslistSmartViewBusinessObject from IntegratedProvider

	public method new()         as object
	public method getData()     as object
	public method getSchema()   as object
	public method FatSV039CreateSQL()

	protected data aFields      as array
	protected data aStruct      as array
	protected data cAlias    	as character
	protected data cSelectLoc 	as character
	protected data cWhereLoc 	as character
	protected data ojItems 		as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method new() class paymentstermslistSmartViewBusinessObject 

	_Super:new()
	self:aFields	:= {}
	self:aStruct	:= {}
	self:cAlias	    := ""
	self:cSelectLoc	:= ""
	self:cWhereLoc	:= ""

    self:appendArea(STR0001) 		// "Faturamento"
    self:setDisplayName(STR0002)	// "Condições de Pagamentos"
    self:setDescription(STR0003)	// "Lista de Condições de Pagamentos"
    self:setIsLookUp(.T.)  

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author FAT/CRM
@since 07/05/2024
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class paymentstermslistSmartViewBusinessObject

    Local aPDFields     := {} 	as array
    Local aParcela      := {} 	as array
    Local lObfuscated   := .F. 	as logical
    Local nX            := 0 	as numeric

    MV_FIL	 := ""
    MV_PTT03 := 1

    FatSetValueMVPAR(oFilter:GetParameters(), "")  // Método para retorno do JSON dos parâmetros

    // Verifica se precisa fazer o tratamento para LGPD   
    aPDFields := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

    // Chama o método que monta as queries SQL
    self:FatSV039CreateSQL()

    DbSelectArea(self:cAlias)

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())

        self:ojItems := JsonObject():new()

        // Preenche o objeto JSON com os dados da consulta
        For nX := 1 to Len(self:aStruct)

            self:ojItems[self:aStruct[nX][1]] := iif(lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                                                        aPDFields[nScan][2],;
                                                        (self:cAlias)->&(self:aStruct[nX][5]))

        Next nX

        self:processData(1) // Tratamento para commit localizado
        self:oData:appendData(self:ojItems)

        // Verifica se deve imprimir as parcelas, caso necessário
        If MV_PTT03 == 1 .and. MV_PAR04 > 0
            aParcela := Condicao(MV_PAR04, (self:cAlias)->E4_CODIGO, MV_PAR05, dDataBase, MV_PAR06)
            For nX := 1 to Len(aParcela)
                self:ojItems := JsonObject():new()
                self:ojItems['E4FILIAL']  := (self:cAlias)->E4_FILIAL
                self:ojItems['E4CODIGO']  := (self:cAlias)->E4_CODIGO
                self:ojItems['PARCELA']   := StrZero(nX, 2)
                self:ojItems['DTVENCTO']  := totvs.framework.treports.date.dateToTimeStamp(aParcela[nX][1])
                self:ojItems['VLTITULO']  := aParcela[nX][2]
                self:oData:appendData(self:ojItems)
            Next nX
        EndIf

        // Avança para o próximo registro na consulta
        (self:cAlias)->(DBSkip())

    EndDo

    // Fecha o alias e a área da tabela
    (self:cAlias)->(DBCloseArea())

    // Limpeza da memória
    FwFreeArray(aPDFields)
    FwFreeArray(aParcela)

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema
@author FAT/CRM
@since 07/05/2024
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class paymentstermslistSmartViewBusinessObject 

    Local nX            := 0 as numeric
    Local aParameters   := {} as array

	self:aFields := {"E4_FILIAL","E4_CODIGO", "E4_TIPO", "E4_DESCRI", "E4_COND", "E4_DDD",;
					 "E4_DESCFIN", "E4_DIADESC", "E4_ACRSFIN" }

	self:aStruct := FatTrGetStruct(self:aFields)

	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	self:addProperty("PARCELA"	, STR0004, "string"	, STR0004, "PARCELA")	// "Nº Parcela"
	self:addProperty("DTVENCTO"	, STR0005, "date"	, STR0005, "DTVENCTO")	// "Dt. Vencimento"
	self:addProperty("VLTITULO"	, STR0006, "number"	, STR0006, "VLTITULO") 	// "Vlr. Titulo"

	//Retorna do SX1 os parâmetros que serão utilizados, no array os que não serão utilizados
	aParameters := FtSVSetParam('MTR035',{'MV_PAR03'})

    self:oSchema:addParameter("MV_FIL", STR0011 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    self:oSchema:addParameter("MV_PTT03", STR0007, "number", .F.,,,,,, FTSVHelp('MTR035','03'))   // Imprime Exemplos ?

    //Validação para versão da Lib
	If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                     // Filial
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SE4", 2)                     // A partir do Código ?
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SE4", 2)                     // Ate o Código ?
		self:setCustomURL("MV_PTT03", "api/framework/treports/integratedprovider/v1/options/MTR035/MV_PAR03", 1)	// Imprime Exemplos?
	EndIf

	FwFreeArray(aParameters)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} buildQuery
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author FAT/CRM
@since 10/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method FatSV039CreateSQL() class paymentstermslistSmartViewBusinessObject

    Local cQuery     	    := ""   as character
    Local cFieldsQry        := ""	as character
    Local cNameTmp		    := ""	as character
    Local oQuery 	 		:= Nil  as object
    Local oTempTableBranch	:= Nil  as object
    Local aFiliais	   	    := {}	as array
    Local nParam		    := 1 	as numeric

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

    // Função que irá criar a tabela temporaria para fazer o Join de filiais
    oTempTableBranch := FatSVTableTempBranch(aFiliais, "SE4", "E4_FILIAL")
    cNameTmp := oTempTableBranch:GetTableNameForQuery()

    // Define os campos que serão utilizados na consulta
    cFieldsQry := "SE4.E4_FILIAL, SE4.E4_CODIGO, SE4.E4_TIPO, SE4.E4_DESCRI, SE4.E4_COND, SE4.E4_DDD, "
    cFieldsQry += "SE4.E4_DESCFIN, SE4.E4_DIADESC, SE4.E4_ACRSFIN"
    cFieldsQry += self:cSelectLoc

    // Inclui campos customizados, se houver
    FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM " + RetSqlName("SE4") + " SE4 "
    cQuery += "INNER JOIN ? TMPFIL "
    cQuery += " ON TMPFIL.TMP_FILIAL = SE4.E4_FILIAL "
    cQuery += "WHERE SE4.E4_FILIAL	 = TMPFIL.TMP_FILIAL "
    cQuery += "	AND SE4.E4_CODIGO	>= ? "
    cQuery += "	AND SE4.E4_CODIGO	<= ? "
    cQuery += " ? " // Adição de condição para objeto localizado no where
    cQuery += "	AND SE4.D_E_L_E_T_	 = ? "
    cQuery += "ORDER BY E4_FILIAL, E4_CODIGO "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++, ' ')

    self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	FwFreeArray(aFiliais)

Return 
