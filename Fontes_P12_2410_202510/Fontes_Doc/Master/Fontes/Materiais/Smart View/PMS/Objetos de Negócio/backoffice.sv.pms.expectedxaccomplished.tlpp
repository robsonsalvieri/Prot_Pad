#include "totvs.ch"
#include "fwlibversion.ch"
#include "protheus.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.pms.expectedxaccomplished.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.pms.expectedxaccomplished.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AFA,AE8,SB1,AFJ,AF8,AF9,AFB,AFN,SD1,AFS,SD2,AFI,SD3,AFU,AJC,AFR,SE2,SA6,AJE,SE5,FK5,FK7", name="Quantitativos Previstos x Realizado", country="ALL", customTables="AF8")
/*/{Protheus.doc} expectedxaccomplishedSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Projetos
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 1.0
*/
Class expectedxaccomplishedSmartViewBusinessObject from IntegratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object
	public method PMSSV001CreateSQL()
 
	protected data aFieldsValue		as array
    protected data aFields			as array
    protected data aStruct			as array
    protected data jItems			as json
    protected data cAliasAF8		as character
    protected data cSelectLoc		as character
	protected data cWhereLoc		as character

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self 
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 1.0
*/
Method new() class expectedxaccomplishedSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0003) 	 //Gestão de Projetos
    self:setDisplayName(STR0001) //Quantitativos Previstos x Realizado
    self:setDescription(STR0002) //Relação de Quantitativos Previstos x Realizado
    self:setIsLookUp(.T.)		 //Define que terá consulta de LookUp

	self:aFieldsValue 	:= {}
    self:cAliasAF8		:= ""
    self:cSelectLoc		:= ""
    self:cWhereLoc		:= ""

Return self


/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 29/05/2025
	@version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object class expectedxaccomplishedSmartViewBusinessObject

	Local cQueryAFU		:= "" as character
	Local cAliasAFU		:= "" as character
	Local cQueryAFA		:= "" as character
	Local cAliasAFA		:= "" as character
	Local cQueryAFJ		:= "" as character
	Local cAliasAFJ		:= "" as character
	Local cQueryAFS		:= "" as character
	Local cAliasAFS		:= "" as character
	Local cQueryAFI		:= "" as character
	Local cAliasAFI		:= "" as character
	Local cQueryAFN		:= "" as character
	Local cAliasAFN		:= "" as character
	Local cQueryAFB		:= "" as character
	Local cAliasAFB		:= "" as character
	Local cQueryAJC		:= "" as character
	Local cAliasAJC		:= "" as character
	Local cQueryAFR		:= "" as character
	Local cAliasAFR		:= "" as character
	Local cVersao		:= "" as character
	Local cTrunca		:= "" as character
	Local cCodigo		:= "" as character
	Local cTipo			:= "" as character
	Local cGrupo		:= "" as character
	Local cValue		:= "" as character
	Local cTipoReg		:= "" as character
	Local cCodGrpPrd	:= "" as character
	Local cDesGrpPrd	:= "" as character
	Local cCodProd		:= "" as character
	Local cDesProd		:= "" as character
	Local cUnProd		:= "" as character
	Local cCodTar		:= "" as character
	Local cDesTar		:= "" as character
	Local cTipoDet		:= "" as character
	Local cCodCliFor	:= "" as character
	Local cLojCliFor	:= "" as character
	Local cNomCliFor	:= "" as character
	Local cNumNota		:= "" as character
	Local cSerNota		:= "" as character
	Local cTipNota		:= "" as character
	Local cCodgRec		:= "" as character
	Local cDescRec		:= "" as character
	Local cCodTarDet	:= "" as character
	Local cIteTarDet	:= "" as character
	Local cDesTarDet	:= "" as character
	Local cBancoDet		:= "" as character
	Local cAgencDet		:= "" as character
	Local cContaDet		:= "" as character
	Local cNumTiDet		:= "" as character
	Local cProdProj		:= "" as character
	Local cArrayABC 	:= "" as character
	Local cAddMOV 		:= "" as character
	Local cObfNCli		:= "" as character
	Local cObfNFor		:= "" as character
	Local cObfNRecur	:= "" as character
	Local cMD5 			:= "" as character
	Local nX			:= 0 as numeric
	Local nY			:= 0 as numeric
	Local nZ			:= 0 as numeric
	Local nCustoSD3		:= 0 as numeric
	Local nQuantSD3		:= 0 as numeric
	Local nCustoReal	:= 0 as numeric
	Local nCustoPrj		:= 0 as numeric
	Local nPosABC		:= 0 as numeric
	Local nSinal		:= 0 as numeric
	Local nValConv		:= 0 as numeric
	Local nPercAcm		:= 0 as numeric
	Local nPercAcmB		:= 0 as numeric
	Local nPercAcm2		:= 0 as numeric
	Local nPercAcm2B	:= 0 as numeric
	Local nQuantAFA		:= 0 as numeric
	Local nValorAFB		:= 0 as numeric
	Local nTotEmp		:= 0 as numeric
	Local nTotRepo		:= 0 as numeric
	Local nCusRealRe	:= 0 as numeric
	Local nQtdeRepo		:= 0 as numeric
	Local nQtdEmp		:= 0 as numeric
	Local nVal			:= 0 as numeric
	Local nValSB1		:= 0 as numeric
	Local nValRepo		:= 0 as numeric
	Local nScan			:= 0 as numeric
	Local nQtdPrev      := 0 as numeric
	Local nVlrPrev      := 0 as numeric
	Local nPerPrPrev    := 0 as numeric
	Local nQtdEmpRea    := 0 as numeric
	Local nQtdAtuRea    := 0 as numeric
	Local nVlrRea       := 0 as numeric
	Local nPercRea      := 0 as numeric
	Local nSaldCusto    := 0 as numeric
	Local nQtdHoraRec	:= 0 as numeric
	Local nQuantidade	:= 0 as numeric
	Local nCustoDet		:= 0 as numeric
	Local nValorDet		:= 0 as numeric
	Local nPercPrDet 	:= 0 as numeric
	Local nPmsSd1Qt		:= 0 as numeric
	Local nPmsAfnQt		:= 0 as numeric
	Local nPmsAfnVl		:= 0 as numeric
	Local nDecCst		:= 0 as numeric
	Local nPosPrepAFA	:= 0 as numeric
	Local nPosPrepAFJ	:= 0 as numeric
	Local nPosPrepAFB	:= 0 as numeric
	Local nPosPrepAFN	:= 0 as numeric
	Local nPosPrepAFS	:= 0 as numeric
	Local nPosPrepAFI	:= 0 as numeric
	Local nPosPrepAFU	:= 0 as numeric
	Local nPosPrepAJC	:= 0 as numeric
	Local nPosPrepAFR	:= 0 as numeric
	Local nParam 		:= 0 as numeric

	Local dDataMov		:= CToD("") as date
	Local dDataEmi		:= CToD("") as date
	Local dDtNecDet		:= CToD("") as date
	Local dDataVen		:= CToD("") as date

	Local aArrayABC		:= {} as array
	Local aItemInfo		:= {} as array
	Local aAuxHand		:= {} as array
	Local aPDFields		:= {} as array
	Local aValuesAfn	:= {} as array
	Local aValuesSd1	:= {} as array

	Local aPrepaAFA 	:= {} as array
	Local aPrepaAFJ 	:= {} as array
	Local aPrepaAFB 	:= {} as array
	Local aPrepaAFN 	:= {} as array
	Local aPrepaAFS		:= {} as array
	Local aPrepaAFI		:= {} as array
	Local aPrepaAFU		:= {} as array
	Local aPrepaAJC		:= {} as array
	Local aPrepaAFR		:= {} as array

	Local lRecProd		:= .F. as logical	//caso o RECURSO nao tenha produto
	Local lObfuscated	:= .F. as logical
	Local lAdic			:= .T. as logical
	Local lAE8			:= .F. as logical
	Local lAgluSE5		:= .F. as logical
	Local lChkAJC		:= .F. as logical
	Local lContinue		:= .F. as logical

	Private nMoedaPMS := 0 	as numeric

	MV_FIL := ""
	MV_PREVI := ""
	MV_REALI := ""

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(),"PMR170") 

	self:aFieldsValue:= {{"AF8_REVISA", 		{"AF8_REVISA",		"", 'cVersao'}},;
						{"TIPOREGISTRO", 	    {"TIPOREGISTRO",	"", 'cTipoReg'}},;
						{"BM_GRUPO", 		    {"BM_GRUPO",		"", 'cCodGrpPrd'}},;
						{"BM_DESC", 		    {"BM_DESC",			"", 'cDesGrpPrd'}},;
						{"B1_COD", 			    {"B1_COD",			"", 'cCodProd'}},;
						{"B1_DESC",			    {"B1_DESC",			"", 'cDesProd'}},;
						{"B1_UM",			    {"B1_UM",			"", 'cUnProd'}},;
						{"AF9_TAREFA",		    {"AF9_TAREFA",		"", 'cCodTar'}},;
						{"AF9_DESCRI",		    {"AF9_DESCRI",		"", 'cDesTar'}},;
						{"QTDPREVIST", 		    {"QTDPREVIST",		"", 'nQtdPrev'}},;
						{"VLRPREVIST", 		    {"VLRPREVIST",		"", 'nVlrPrev'}},;
						{"PERCPRJPREV", 	    {"PERCPRJPREV",		"", 'nPerPrPrev'}},;
						{"PERCACUPREV", 	    {"PERCACUPREV",		"", 'nPercAcm'}},;
						{"QTDEMPREALIZ", 	    {"QTDEMPREALIZ",	"", 'nQtdEmpRea'}},;
						{"QTDATUREALIZ", 	    {"QTDATUREALIZ",	"", 'nQtdAtuRea'}},;
						{"VLRREALIZAD", 	    {"VLRREALIZAD",		"", 'nVlrRea'}},;
						{"PERCPRJREALIZ", 	    {"PERCPRJREALIZ",	"", 'nPercRea'}},;
						{"PERCACUREALIZ", 	    {"PERCACUREALIZ",	"", 'nPercAcm2'}},;
						{"SLDQUANT", 		    {"SLDQUANT",		"", 'nQtdeRepo'}},;
						{"SLDCUSTO", 		    {"SLDCUSTO",		"", 'nSaldCusto'}},;
						{"TIPODETALHE", 	    {"TIPODETALHE",		"", 'cTipoDet'}},;
						{"DTEMISSAO", 		    {"DTEMISSAO",		"", 'dDataEmi'}},;
						{"CODCLIFOR", 		    {"CODCLIFOR",		"", 'cCodCliFor'}},;
						{"LJCLIFOR", 		    {"LJCLIFOR",		"", 'cLojCliFor'}},;
						{"NOMECLIFOR", 		    {"NOMECLIFOR",		"", 'cNomCliFor'}},;
						{"AFN_DOC",			    {"AFN_DOC",			"", 'cNumNota'}},;
						{"AFN_SERIE", 		    {"AFN_SERIE",		"", 'cSerNota'}},;
						{"TIPO", 			    {"TIPO",			"", 'cTipNota'}},;
						{"AE8_RECURS", 		    {"AE8_RECURS",		"", 'cCodgRec'}},;
						{"AE8_DESCRI", 		    {"AE8_DESCRI",		"", 'cDescRec'}},;
						{"CODTAREFDET", 	    {"CODTAREFDET",		"", 'cCodTarDet'}},;
						{"ITEMTAREFDET", 	    {"ITEMTAREFDET",	"", 'cIteTarDet'}},;
						{"DESCTAREFDET", 	    {"DESCTAREFDET",	"", 'cDesTarDet'}},;
						{"QTDHRRECURSO", 	    {"QTDHRRECURSO",	"", 'nQtdHoraRec'}},;
						{"QUANTIDADE", 		    {"QUANTIDADE",		"", 'nQuantidade'}},;
						{"DTNECESSDET", 	    {"DTNECESSDET",		"", 'dDtNecDet'}},;
						{"A6_NOME", 		    {"A6_NOME",			"", 'cBancoDet'}},;
						{"FK5_AGENCI", 		    {"FK5_AGENCI",		"", 'cAgencDet'}},;
						{"FK5_CONTA", 		    {"FK5_CONTA",		"", 'cContaDet'}},;
						{"FK7_NUM", 		    {"FK7_NUM",			"", 'cNumTiDet'}},;
						{"FK5_DTDISP", 		    {"FK5_DTDISP",		"", 'dDataVen'}},;
						{"CUSTODET", 		    {"CUSTODET",		"", 'nCustoDet'}},;
						{"VALORDET", 		    {"VALORDET",		"", 'nValorDet'}},;
						{"PERCPRJDET", 		    {"PERCPRJDET",		"", 'nPercPrDet'}}}



	//Método que monta a Query principal AF8
	self:PMSSV001CreateSQL()

	(self:cAliasAF8)->(dbGoTop())

	If (self:cAliasAF8)->(!Eof())

		lChkAJC := PmsCHkAJC(.F.)
		nDecCst := FWSX3Util():GetFieldStruct("AF9_CUSTO")[4]
		cObfNCli := IIf(FTPDIsObfuscate("A1_NOME",,.T.),   FTPDObfuscate("CUSTOMMER NAME","A1_NOME",,.T.),  "") 
		cObfNFor := IIf(FTPDIsObfuscate("A2_NOME",,.T.),   FTPDObfuscate("SUPPLIER NAME" ,"A2_NOME",,.T.),  "")
		cObfNRecur := IIf(FTPDIsObfuscate("AE8_DESCRI",,.T.),FTPDObfuscate("RESOURCE NAME","AE8_DESCRI",,.T.),"")	

		//Tratamento para permitir apenas o número de moedas utilizadas
   		MV_PAR16  := IIf(MV_PAR16 <= 0 .Or. MV_PAR16 > ContaMoeda(), 1, MV_PAR16)
		nMoedaPMS := IIf( MV_PAR16 < 1, 1, MV_PAR16)

		//Verifica se precisa fazer o tratamento para LGPD
    	aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields() )
    	lObfuscated := Len( aPDFields ) > 0

		AFA->(dbSetOrder(1)) //AFA_FILIAL, AFA_PROJET, AFA_REVISA, AFA_TAREFA, AFA_ITEM, AFA_PRODUT, AFA_RECURS
		AE8->(dbSetOrder(1)) //AE8_FILIAL, AE8_RECURS, AE8_DESCRI
		SB1->(dbSetOrder(1)) //B1_FILIAL, B1_COD
		AF9->(dbSetOrder(1)) //AF9_FILIAL, AF9_PROJET, AF9_REVISA, AF9_TAREFA, AF9_ORDEM
		AFB->(dbSetOrder(1)) //AFB_FILIAL, AFB_PROJET, AFB_REVISA, AFB_TAREFA, AFB_ITEM
		AFN->(dbSetOrder(1)) //AFN_FILIAL, AFN_PROJET, AFN_REVISA, AFN_TAREFA, AFN_DOC, AFN_SERIE, AFN_FORNEC, AFN_LOJA, AFN_ITEM
		SD1->(dbSetOrder(1)) //D1_FILIAL, D1_DOC, D1_SERIE, D1_FORNECE, D1_LOJA, D1_COD, D1_ITEM
		SD2->(dbSetOrder(4)) //D2_FILIAL, D2_NUMSEQ
		SD3->(dbSetOrder(7)) //D3_FILIAL, D3_OP, D3_COD, D3_LOCAL
		AJC->(dbSetOrder(1)) //AJC_FILIAL, AJC_CTRRVS, AJC_PROJET, AJC_REVISA, AJC_TAREFA, AJC_DATA
		AFR->(dbSetOrder(1)) //AFR_FILIAL, AFR_PROJET, AFR_REVISA, AFR_TAREFA, AFR_PREFIX, AFR_NUM, AFR_PARCEL, AFR_TIPO, AFR_FORNEC, AFR_LOJA
		SE2->(dbSetOrder(1)) //E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA
		AFS->(dbSetOrder(1)) //AFS_FILIAL, AFS_PROJET, AFS_REVISA, AFS_TAREFA, AFS_COD, AFS_LOCAL, AFS_EMISSA, AFS_NUMSEQ
		AFI->(dbSetOrder(1)) //AFI_FILIAL, AFI_PROJET, AFI_REVISA, AFI_TAREFA, AFI_COD, AFI_LOCAL, AFI_EMISSA, AFI_NUMSEQ
		AFU->(dbSetOrder(1)) //AFU_FILIAL, AFU_CTRRVS, AFU_PROJET, AFU_REVISA, AFU_TAREFA, AFU_RECURS, AFU_DATA

		While !(self:cAliasAF8)->(Eof()) .And. ((self:cAliasAF8)->AF8_PROJET >= MV_PAR01 .Or. (self:cAliasAF8)->AF8_PROJET <= MV_PAR02) .And. ((self:cAliasAF8)->AF8_DATA >= DToS(MV_PAR03) .Or. (self:cAliasAF8)->AF8_DATA <= DToS(MV_PAR04))

			nPercAcm	:= 0
			nPercAcm2	:= 0
			nPercAcmB	:= 0
			nPercAcm2B	:= 0
			nTotRepo   	:= 0
			nPmsSd1Qt	:= 0
			nPmsAfnQt	:= 0
			nPmsAfnVl	:= 0
			aArrayABC  	:= {}
			aItemInfo  	:= {}
			aValuesAfn	:= {}
			aValuesSd1	:= {}
			cTrunca    	:= (self:cAliasAF8)->AF8_TRUNCA
			cVersao    	:= IIf( Empty(MV_PAR05), (self:cAliasAF8)->AF8_REVISA, MV_PAR05) // se a versao nao informada -> utiliza a ultima versao para o projeto / utiliza a versao informada
			aAuxHand   	:= PmsIniCOTP((self:cAliasAF8)->AF8_PROJET,cVersao,CTOD("31/12/2040"))
			nCustoPrj  	:= PmsTrunca(cTrunca,PmsRetCOTP(aAuxHand,2,Padr((self:cAliasAF8)->AF8_PROJET,Len(AFC->AFC_EDT)))[nMoedaPMS],nDecCst,)
			aAuxHand   	:= PmsIniCRTE((self:cAliasAF8)->AF8_PROJET,(self:cAliasAF8)->AF8_REVISA,CTOD("31/12/2040"))
			nCustoReal 	:= PmsTrunca(cTrunca,PmsRetCRTE(aAuxHand,2,(self:cAliasAF8)->AF8_PROJET+SPACE(2))[nMoedaPMS],nDecCst,)

			// Tabela AFA - Recursos do Projeto
			If AFA->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA))) //AFA_FILIAL, AFA_PROJET, AFA_REVISA, AFA_TAREFA, AFA_ITEM, AFA_PRODUT, AFA_RECURS

				cQueryAFA := " SELECT AFA_FILIAL, AFA_PROJET, AFA_REVISA, AFA_PRODUT, AFA_RECURS, AFA_TAREFA, AFA_CUSTD, AFA_MOEDA, R_E_C_N_O_"
				cQueryAFA += " FROM "+RetSqlName("AFA")+" AFA "
				cQueryAFA += " WHERE AFA.AFA_FILIAL = ? "	
				cQueryAFA += " AND AFA.AFA_PROJET = ? "		
				cQueryAFA += " AND AFA.AFA_REVISA = ? "		
				cQueryAFA += " AND AFA.AFA_PRODUT BETWEEN ? AND ? "	
				cQueryAFA += " AND AFA.D_E_L_E_T_  = ? "
				cQueryAFA += " ORDER BY AFA.AFA_FILIAL, AFA.AFA_PROJET, AFA.AFA_REVISA "

				cMD5 := MD5(cQueryAFA)

				If (nPosPrepAFA := Ascan(aPrepaAFA,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAFA,{FwExecStatement():New(ChangeQuery(cQueryAFA)),cMD5})
					nPosPrepAFA := Len(aPrepaAFA)
				Endif

				nParam := 1

				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, MV_PAR09)
				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, MV_PAR10)
				aPrepaAFA[nPosPrepAFA][1]:SetString(nParam++, ' ')

				cAliasAFA := aPrepaAFA[nPosPrepAFA][1]:OpenAlias()
				(cAliasAFA)->(dbGoTop())

				While !(cAliasAFA)->(Eof()) .And. ((self:cAliasAF8)->AF8_FILIAL+(self:cAliasAF8)->AF8_PROJET+(self:cAliasAF8)->AF8_REVISA == (cAliasAFA)->AFA_FILIAL+(cAliasAFA)->AFA_PROJET+(cAliasAFA)->AFA_REVISA)

					//tratamento para projetos que usam composicao aux	
					lRecProd := .F.
					aItemInfo := {}
					lContinue := .F.

					If !Empty((cAliasAFA)->AFA_PRODUT)
						
						SB1->(MsSeek((self:cAliasAF8)->AF8_FILIAL+(cAliasAFA)->AFA_PRODUT))
						nValSB1 := xMoedaPMS(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),nMoedaPMS,"",,,,,,cTrunca,)
						
						lContinue := .T.
						
					ElseIf MV_PREVI == 2 .and. Empty((cAliasAFA)->AFA_PRODUT)

						If AE8->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (cAliasAFA)->AFA_RECURS)) .And. !Empty(AE8->AE8_PRODUT) .And. (AE8->AE8_PRODUT > MV_PAR09 .Or. AE8->AE8_PRODUT < MV_PAR10)
		
							SB1->(MsSeek((self:cAliasAF8)->AF8_FILIAL+AE8->AE8_PRODUT))
							lRecProd := .T.
							nValSB1 := xMoedaPMS(RetFldProd(SB1->B1_COD,"B1_CUSTD"),Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")),nMoedaPMS,"",,,,,,cTrunca,)
							lContinue := .T.

						Endif

					Endif

					If lContinue

						// Tabela AFJ - Empenhos do Projeto
						cQueryAFJ := " SELECT AFJ_FILIAL, AFJ_PROJET, AFJ_COD, AFJ_QEMP, AFJ_QATU "
						cQueryAFJ += " FROM "+RetSqlName("AFJ")+ " AFJ "
						cQueryAFJ += " WHERE AFJ_FILIAL = ? "	
						cQueryAFJ += " AND AFJ_PROJET = ? "		
						cQueryAFJ += " AND AFJ_COD = ? "		
						cQueryAFJ += " AND D_E_L_E_T_  = ? "
						cQueryAFJ += " ORDER BY AFJ_FILIAL, AFJ_PROJET, AFJ_COD"

						cMD5 := MD5(cQueryAFJ)

						nParam := 1

						If (nPosPrepAFJ := Ascan(aPrepaAFJ,{|x| x[2] == cMD5})) == 0 
							Aadd(aPrepaAFJ,{FwExecStatement():New(ChangeQuery(cQueryAFJ)),cMD5})
							nPosPrepAFJ := Len(aPrepaAFJ)
						Endif

						aPrepaAFJ[nPosPrepAFJ][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
						aPrepaAFJ[nPosPrepAFJ][1]:SetString(nParam++, (cAliasAFA)->AFA_PROJET)
						aPrepaAFJ[nPosPrepAFJ][1]:SetString(nParam++, (cAliasAFA)->AFA_PRODUT)
						aPrepaAFJ[nPosPrepAFJ][1]:SetString(nParam++, ' ')

						cAliasAFJ := aPrepaAFJ[nPosPrepAFJ][1]:OpenAlias()
						(cAliasAFJ)->(dbGoTop())

						While !(cAliasAFJ)->(Eof()) .And. ((self:cAliasAF8)->AF8_FILIAL+(cAliasAFA)->AFA_PROJET+(cAliasAFA)->AFA_PRODUT == (cAliasAFJ)->AFJ_FILIAL+(cAliasAFJ)->AFJ_PROJET+(cAliasAFJ)->AFJ_COD)

							//Verifica o empenho do produto
							nQtdEmp += ((cAliasAFJ)->AFJ_QEMP - (cAliasAFJ)->AFJ_QATU)
							(cAliasAFJ)->(DbSkip())

						Enddo

						//Verifica a quantidade do produto
						AF9->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (cAliasAFA)->AFA_PROJET + (cAliasAFA)->AFA_REVISA + (cAliasAFA)->AFA_TAREFA))
						nQuantAFA := PmsPrvAFA((cAliasAFA)->R_E_C_N_O_,mv_par07,mv_par08,AF9->(RecNo()))

						// Visualizar Custo Previsto por Produto
						If MV_PREVI == 2 .and. !Empty((cAliasAFA)->AFA_PRODUT)
							cCodigo := (cAliasAFA)->AFA_PRODUT
							cTipo   := "SB1"
						ElseIf lRecProd
							cCodigo :=  SB1->B1_COD
							cTipo   := "SB1"
						ElseIf !Empty((cAliasAFA)->AFA_RECURS)
							cCodigo := (cAliasAFA)->AFA_RECURS
							cTipo   := "REC"
						Else
							cCodigo := (cAliasAFA)->AFA_PRODUT
							cTipo   := "SB1"
						Endif

						// Aglutina tarefas? Sim / Nao
						// se aglutina, verifica se jah existe no aArrayABC
						If MV_PAR12 == 1 .AND. (nPosABC := aScan(aArrayABC ,{|x| x[1]==cTipo .And. x[2]==cCodigo })) > 0
							aArrayABC[nPosABC ,03] += nQuantAFA
							aArrayABC[nPosABC ,04] += xMoedaPMS(nQuantAFA * (cAliasAFA)->AFA_CUSTD,(cAliasAFA)->AFA_MOEDA,nMoedaPMS,"",,,,,,cTrunca,)
						Else
							// Aglutina tarefas? Sim / Nao
							// se não aglutina, verifica se jah existe tipo, produto/recurso, projeto, revisao e tarefa no aArrayABC
							If MV_PAR12 == 2 .AND. (nPosABC := aScan(aArrayABC ,{|x| x[1]==cTipo .And. x[2]==cCodigo .And. x[11]==(cAliasAFA)->AFA_PROJET .AND. x[12]==(cAliasAFA)->AFA_REVISA .AND. x[13]==(cAliasAFA)->AFA_TAREFA }))>0
								aArrayABC[nPosABC ,03] += nQuantAFA
								aArrayABC[nPosABC ,04] += xMoedaPMS(nQuantAFA * (cAliasAFA)->AFA_CUSTD,(cAliasAFA)->AFA_MOEDA,nMoedaPMS,"",,,,,,cTrunca,)
							Else
								If cTipo == "REC"
									nCusRealRe:=CalcCusRec((cAliasAFA)->AFA_RECURS)
									If nCusRealRe = 0
										nCusRealRe := xMoedaPMS((cAliasAFA)->AFA_CUSTD,(cAliasAFA)->AFA_MOEDA,nMoedaPMS,"",,,,,,cTrunca,)
									Endif

									aAdd(aArrayABC ,{ cTipo,;
												(cAliasAFA)->AFA_RECURS,;
												nQuantAFA,;
												xMoedaPMS(nQuantAFA * (cAliasAFA)->AFA_CUSTD,(cAliasAFA)->AFA_MOEDA,nMoedaPMS,"",,,,,,cTrunca,),;
												0,;
												0,;
												0,;
												0,;
												{},;
												nCusRealRe,;
												(cAliasAFA)->AFA_PROJET,;
												(cAliasAFA)->AFA_REVISA,;
												(cAliasAFA)->AFA_TAREFA,;
												cGrupo })
								Else
									aAdd(aArrayABC ,{ cTipo,;
												SB1->B1_COD,;
												nQuantAFA,;
												xMoedaPMS(nQuantAFA * (cAliasAFA)->AFA_CUSTD,(cAliasAFA)->AFA_MOEDA,nMoedaPMS,"",,,,,,cTrunca,),;
												0,;
												0,;
												nQtdEmp,;
												0,;
												{},;
												nValSB1,;
												(cAliasAFA)->AFA_PROJET,;
												(cAliasAFA)->AFA_REVISA,;
												(cAliasAFA)->AFA_TAREFA,;
												SB1->B1_GRUPO } )
									nQtdEmp := 0
								Endif
							EndIf
						Endif
					Endif

					(cAliasAFA)->(dbSkip())

				Enddo

			Endif
			
			// Tabela AFB - Despesas do Projeto
			If AFB->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->AF8_PROJET + cVersao ))

				cQueryAFB := " SELECT AFB_FILIAL, AFB_PROJET, AFB_REVISA, AFB_TAREFA, AFB_ITEM, AFB_TIPOD, AFB_MOEDA, AFB.R_E_C_N_O_ AS AFB_RECNO," 
				cQueryAFB += " AF9.R_E_C_N_O_ AS AF9_RECNO "
				cQueryAFB += " FROM "+ RetSqlName("AFB")+" AFB "
				cQueryAFB += " INNER JOIN "+ RetSqlName("AF9")+" AF9 ON ?"
				cQueryAFB += " AND AF9_PROJET = AFB_PROJET "
				cQueryAFB += " AND AF9_REVISA = AFB_REVISA "
				cQueryAFB += " AND AF9_TAREFA = AFB_TAREFA "
				cQueryAFB += " AND AF9.D_E_L_E_T_ = ? "
				cQueryAFB += " WHERE AFB.AFB_FILIAL = ? "	
				cQueryAFB += " AND AFB.AFB_PROJET = ? "	
				cQueryAFB += " AND AFB.AFB_REVISA = ? "	
				cQueryAFB += " AND AFB.D_E_L_E_T_ = ? "
				cQueryAFB += " ORDER BY AFB.AFB_FILIAL, AFB.AFB_PROJET, AFB.AFB_REVISA "
				
				cMD5 := MD5(cQueryAFB)
				
				If (nPosPrepAFB := Ascan(aPrepaAFB,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAFB,{FwExecStatement():New(ChangeQuery(cQueryAFB)),cMD5})
					nPosPrepAFB := Len(aPrepaAFB)
				Endif

				nParam := 1

				aPrepaAFB[nPosPrepAFB][1]:SetUnsafe(nParam++, FwJoinFilial("AF9", "AFB"))
				aPrepaAFB[nPosPrepAFB][1]:SetString(nParam++, ' ')
				aPrepaAFB[nPosPrepAFB][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFB[nPosPrepAFB][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFB[nPosPrepAFB][1]:SetString(nParam++, cVersao)
				aPrepaAFB[nPosPrepAFB][1]:SetString(nParam++, ' ')

				cAliasAFB := aPrepaAFB[nPosPrepAFB][1]:OpenAlias()
				(cAliasAFB)->(dbGoTop())

				While !(cAliasAFB)->(Eof()) .And. ( (self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->AF8_PROJET + cVersao  == (cAliasAFB)->(AFB_FILIAL + AFB_PROJET + AFB_REVISA) )

					// verifica a despesa do produto
					nValorAFB := PmsPrvAFB((cAliasAFB)->AFB_RECNO, MV_PAR07, MV_PAR08, (cAliasAFB)->AF9_RECNO)[1]
					nVal := xMoedaPMS(nValorAFB, (cAliasAFB)->AFB_MOEDA, nMoedaPMS, "",,,,,,cTrunca,)

					//Adiciona informacoes sobre a despesa
					aItemInfo := {}
					aAdd(aItemInfo, "AFB")
					aAdd(aItemInfo, nVal )
					aAdd(aItemInfo, (cAliasAFB)->(AFB_FILIAL + AFB_PROJET + AFB_REVISA + AFB_TAREFA + AFB_ITEM))

					// Aglutina tarefas? Sim / Nao
					nPosABC := IIf( MV_PAR12 == 1, aScan(aArrayABC,{|x| x[1]=="DSP" .And. x[2]==(cAliasAFB)->AFB_TIPOD}),;
									aScan(aArrayABC,{|x| x[1]=="DSP" .And. x[2]==(cAliasAFB)->AFB_TIPOD .And. x[11]+x[12]+x[13]==(cAliasAFB)->(AFB_PROJET+AFB_REVISA+AFB_TAREFA) }))

					If nPosABC > 0
						aArrayABC[nPosABC ,04] += nVal
						aAdd(aArrayABC[nPosABC ,09], aItemInfo)
					Else
						aAdd(aArrayABC,{"DSP",(cAliasAFB)->AFB_TIPOD,0,nVal,0,0,0,0,{aItemInfo},0, (cAliasAFB)->AFB_PROJET,(cAliasAFB)->AFB_REVISA,(cAliasAFB)->AFB_TAREFA,cGrupo } )
					Endif

					(cAliasAFB)->(DbSkip())

				Enddo

			Endif

			// Tabela AFN - Projeto X NF de Entrada
			If AFN->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAFN := " SELECT AFN.AFN_FILIAL,AFN.AFN_PROJET, AFN.AFN_TAREFA, AFN.AFN_REVISA, AFN.AFN_DOC, AFN.AFN_SERIE, " 
				cQueryAFN += " AFN.AFN_FORNEC, AFN.AFN_LOJA, AFN.AFN_COD, AFN.AFN_TIPONF, AFN.AFN_QUANT, AFN.R_E_C_N_O_ AS AFN_RECNO, " 
				cQueryAFN += " SD1.D1_COD,SD1.D1_TIPO, SD1.D1_QUANT, SD1.D1_ORIGLAN, SD1.D1_DTDIGIT, SD1.D1_CUSTO, SD1.D1_EMISSAO, SD1.R_E_C_N_O_ AS SD1_RECNO, "
				cQueryAFN += " A2_NOME,B1_CUSTD,B1_MCUSTD "
				cQueryAFN += " FROM "+RetSqlName("AFN")+" AFN "
				cQueryAFN += " LEFT JOIN "+RetSqlName("SD1")+" SD1 ON ? "
				cQueryAFN += " AND SD1.D1_DOC = AFN.AFN_DOC "
				cQueryAFN += " AND SD1.D1_SERIE = AFN.AFN_SERIE "
				cQueryAFN += " AND SD1.D1_FORNECE = AFN.AFN_FORNEC "
				cQueryAFN += " AND SD1.D1_LOJA = AFN.AFN_LOJA "
				cQueryAFN += " AND SD1.D1_COD = AFN.AFN_COD "
				cQueryAFN += " AND SD1.D1_ITEM = AFN.AFN_ITEM "
				cQueryAFN += " AND SD1.D_E_L_E_T_ = ? "
				cQueryAFN += " LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ?"
				cQueryAFN += " AND SB1.B1_COD = SD1.D1_COD "
				cQueryAFN += " AND SB1.D_E_L_E_T_ = ? "
				cQueryAFN += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON ? "
				cQueryAFN += " AND SA2.A2_COD = AFN.AFN_FORNEC "
				cQueryAFN += " AND SA2.A2_LOJA = AFN.AFN_LOJA "
				cQueryAFN += " AND SA2.D_E_L_E_T_ = ? "
				cQueryAFN += " WHERE AFN.AFN_FILIAL = ? "	
				cQueryAFN += " AND AFN.AFN_PROJET = ? "	
				cQueryAFN += " AND AFN.AFN_REVISA = ? "
				cQueryAFN += " AND AFN.AFN_ESTOQU = ? "	
				cQueryAFN += " AND AFN.D_E_L_E_T_ = ? "
				cQueryAFN += " ORDER BY AFN.AFN_FILIAL, AFN.AFN_PROJET, AFN.AFN_REVISA "

				cMD5 := MD5(cQueryAFN)
				
				If (nPosPrepAFN := Ascan(aPrepaAFN,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAFN,{FwExecStatement():New(ChangeQuery(cQueryAFN)),cMD5})
					nPosPrepAFN := Len(aPrepaAFN)
				Endif

				nParam := 1

				aPrepaAFN[nPosPrepAFN][1]:SetUnsafe(nParam++, FwJoinFilial("SD1", "AFN"))
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, ' ')
				aPrepaAFN[nPosPrepAFN][1]:SetUnsafe(nParam++, FwJoinFilial("SB1", "SD1"))
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, ' ')
				aPrepaAFN[nPosPrepAFN][1]:SetUnsafe(nParam++, FwJoinFilial("SA2", "AFN"))
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, ' ')
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, '1')
				aPrepaAFN[nPosPrepAFN][1]:SetString(nParam++, ' ')

				cAliasAFN := aPrepaAFN[nPosPrepAFN][1]:OpenAlias()
				(cAliasAFN)->(DbGoTop())

				While (cAliasAFN)->(!Eof())

					If ((cAliasAFN)->D1_COD < MV_PAR09 .Or. (cAliasAFN)->D1_COD > MV_PAR10)
						(cAliasAFN)->(dbSkip())
						Loop
					Else
						nValSB1 := xMoedaPMS((cAliasAFN)->B1_CUSTD,Val((cAliasAFN)->B1_MCUSTD),nMoedaPMS,"",,,,,,cTrunca,)
					EndIf

					dDataMov :=	StoD((cAliasAFN)->D1_DTDIGIT)

					If dDataMov >= MV_PAR07 .And. dDataMov <= MV_PAR08

						aValuesAfn := { (cAliasAFN)->AFN_DOC, (cAliasAFN)->AFN_SERIE, (cAliasAFN)->AFN_FORNEC, (cAliasAFN)->AFN_LOJA, (cAliasAFN)->AFN_PROJET, (cAliasAFN)->AFN_REVISA, (cAliasAFN)->AFN_TAREFA, (cAliasAFN)->AFN_COD }
						aValuesSd1 := { (cAliasAFN)->D1_TIPO, (cAliasAFN)->D1_QUANT,  (cAliasAFN)->D1_ORIGLAN }
						nPmsSd1Qt  := PmsSD1QuantSV(aValuesAfn, aValuesSd1)
						nPmsAfnQt  := PmsAFNQUANT("QUANT")
						nPmsAfnVl  := PmsAFNQUANT("VALOR")
						nValConv   := xMoedaPMS((cAliasAFN)->D1_CUSTO,1,nMoedaPMS,"",,,,,,cTrunca,,"SD1")

						// Adiciona informacoes sobre o produto
						aItemInfo := {}
						aAdd(aItemInfo, "AFN")  
						aAdd(aItemInfo, (cAliasAFN)->AFN_FORNEC)
						aAdd(aItemInfo, IIf(Empty(cObfNFor),(cAliasAFN)->A2_NOME,cObfNFor))
						aAdd(aItemInfo, (cAliasAFN)->AFN_DOC)
						aAdd(aItemInfo, (cAliasAFN)->AFN_SERIE)
						aAdd(aItemInfo, (cAliasAFN)->AFN_TIPONF)
						aAdd(aItemInfo, (cAliasAFN)->AFN_QUANT)
						aAdd(aItemInfo, nPmsAfnVl*(nValConv/nPmsSd1Qt))
						aAdd(aItemInfo, (cAliasAFN)->D1_EMISSAO)
						aAdd(aItemInfo, (cAliasAFN)->AFN_RECNO)
						aAdd(aItemInfo, (cAliasAFN)->SD1_RECNO)

						// Aglutina tarefas? Sim / Nao
						nPosABC := IIf( MV_PAR12 == 1, aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFN)->D1_COD}),;
										aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFN)->D1_COD .And. x[11]+x[12]+x[13]==(cAliasAFN)->(AFN_PROJET+AFN_REVISA+AFN_TAREFA) }))

						If nPosABC > 0
							aArrayABC[nPosABC ,05] += IIf(aItemInfo[6]=="C",0,nPmsAfnQt)
							aArrayABC[nPosABC ,06] += nPmsAfnVl*(nValConv/nPmsSd1Qt)
							aAdd(aArrayABC[nPosABC ,09], aItemInfo)
						Else
							aAdd(aArrayABC,{"SB1",(cAliasAFN)->D1_COD,0,0,IIf(aItemInfo[6]=="C",0,nPmsAfnQt),;
												nPmsAfnVl*(nValConv/nPmsSd1Qt),;
												0,0,{aItemInfo},nValSB1,;
												(cAliasAFN)->AFN_PROJET, (cAliasAFN)->AFN_REVISA, (cAliasAFN)->AFN_TAREFA,cGrupo })
						EndIf

					EndIf

					(cAliasAFN)->(DbSkip())

				Enddo

			EndIf

			// Tabela AFS - Projeto X NF de Saída
			If AFS->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAFS := " SELECT AFS_FILIAL, AFS_PROJET, AFS_TAREFA, AFS_REVISA, AFS_NUMSEQ, AFS_MOVPRJ, AFS_QUANT, AFS_DOC, AFS_SERIE,AFS_COD,AFS.R_E_C_N_O_ AS AFS_RECNO, "
				cQueryAFS += " B1_CUSTD,B1_MCUSTD "
				cQueryAFS += " FROM "+RetSqlName("AFS")+" AFS "
				cQueryAFS += " LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ?"
				cQueryAFS += " AND SB1.B1_COD = AFS.AFS_COD "
				cQueryAFS += " AND SB1.D_E_L_E_T_ = ? "
				cQueryAFS += " WHERE AFS.AFS_FILIAL = ? "	
				cQueryAFS += " AND AFS.AFS_PROJET = ? "		
				cQueryAFS += " AND AFS.AFS_REVISA = ? "		
				cQueryAFS += " AND AFS.D_E_L_E_T_  = ? "
				cQueryAFS += " ORDER BY AFS.AFS_FILIAL, AFS.AFS_PROJET, AFS.AFS_REVISA"

				cMD5 := MD5(cQueryAFS)
				
				If (nPosPrepAFS := Ascan(aPrepaAFS,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAFS,{FwExecStatement():New(ChangeQuery(cQueryAFS)),cMD5})
					nPosPrepAFS := Len(aPrepaAFS)
				Endif

				nParam := 1
				
				aPrepaAFS[nPosPrepAFS][1]:SetUnsafe(nParam++, FwJoinFilial("SB1", "AFS"))
				aPrepaAFS[nPosPrepAFS][1]:SetString(nParam++, ' ')
				aPrepaAFS[nPosPrepAFS][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFS[nPosPrepAFS][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFS[nPosPrepAFS][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFS[nPosPrepAFS][1]:SetString(nParam++, ' ')

				cAliasAFS := aPrepaAFS[nPosPrepAFS][1]:OpenAlias()
			(cAliasAFS)->(DbGoTop())

				SD2->(dbSetOrder(4))

				While (cAliasAFS)->(!Eof()) .And. (self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->AF8_PROJET + (self:cAliasAF8)->AF8_REVISA == (self:cAliasAF8)->AF8_FILIAL + (cAliasAFS)->AFS_PROJET + (cAliasAFS)->AFS_REVISA

					If SD2->(MsSeek((self:cAliasAF8)->AF8_FILIAL+(cAliasAFS)->AFS_NUMSEQ))

						If (SD2->D2_COD < MV_PAR09 .Or. SD2->D2_COD > MV_PAR10)
							(cAliasAFS)->(dbSkip())
							Loop
						Else
							nValSB1 := xMoeda(PmsTrunca(cTrunca,(cAliasAFS)->B1_CUSTD,nDecCst,),Val((cAliasAFS)->B1_MCUSTD),nMoedaPMS)
							nValSB1 := xMoedaPMS((cAliasAFS)->B1_CUSTD,Val((cAliasAFS)->B1_MCUSTD),nMoedaPMS,"",,,,,,cTrunca,)
						EndIf

						dDataMov := IIf( !Empty(SD2->D2_DTDIGIT), SD2->D2_DTDIGIT, SD2->D2_EMISSAO )

						If dDataMov >= MV_PAR07 .And. dDataMov <= MV_PAR08

							nSinal	:=	IIf((cAliasAFS)->AFS_MOVPRJ $ '25',1,-1)

							nValConv := xMoedaPMS(SD2->D2_CUSTO1,1,nMoedaPMS,"",,,,,,cTrunca,,"SD2")

							// adiciona informacoes sobre o produto
							aItemInfo := {}
							aAdd(aItemInfo, "AFS")
							aAdd(aItemInfo, SD2->D2_CLIENTE)
							aAdd(aItemInfo, IIf(Empty(cObfNCli),FatSeekTable("SA1", 1, PadR(SubStr((self:cAliasAF8)->AF8_FILIAL,1,4), TamSx3("A1_FILIAL")[1] ) + SD2->(D2_CLIENTE+D2_LOJA), "A1_NOME", .T.),cObfNCli))
							aAdd(aItemInfo, (cAliasAFS)->AFS_DOC)
							aAdd(aItemInfo, (cAliasAFS)->AFS_SERIE)
							aAdd(aItemInfo, SD2->D2_TIPO)
							aAdd(aItemInfo, (cAliasAFS)->AFS_QUANT)
							aAdd(aItemInfo, nValConv*(PmsAFSQUANT()/SD2->D2_QUANT)*nSinal)
							aAdd(aItemInfo, dDataMov)
							aAdd(aItemInfo, (cAliasAFS)->AFS_RECNO)
							aAdd(aItemInfo, SD2->(Recno()))

							// Aglutina tarefas? Sim / Nao
							nPosABC := IIf( MV_PAR12 == 1,;
											aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFS)->AFS_COD}),;
												aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFS)->AFS_COD .And. x[11]+x[12]+x[13]==(cAliasAFS)->(AFS_PROJET+AFS_REVISA+AFS_TAREFA) }) )

							If nPosABC > 0
								aArrayABC[nPosABC ,05] += (cAliasAFS)->AFS_QUANT
								aArrayABC[nPosABC ,06] += nValConv*(PmsAFSQUANT()/SD2->D2_QUANT)*nSinal
								aAdd(aArrayABC[nPosABC ,09], aItemInfo)
							Else
								aAdd(aArrayABC,{	"SB1",;
													(cAliasAFS)->AFS_COD,;
													0,;
													0,;
													(cAliasAFS)->AFS_QUANT,;
													nValConv*(PmsAFSQUANT()/SD2->D2_QUANT)*nSinal,;
													0,;
													0,;
													{aItemInfo},;
													nValSB1,;
													(cAliasAFS)->AFS_PROJET, (cAliasAFS)->AFS_REVISA, (cAliasAFS)->AFS_TAREFA,cGrupo })
							Endif

						Endif

					Endif

					(cAliasAFS)->(DbSkip())

				Enddo

			Endif

			// QUERY AFI - PROJETO X MOVIMENTOS INTERNOS
			If AFI->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAFI := " SELECT AFI_FILIAL, AFI_PROJET, AFI_REVISA, AFI_TAREFA, AFI_COD, AFI_LOCAL, AFI_EMISSA, AFI_NUMSEQ, "
				cQueryAFI += " B1_CUSTD,B1_MCUSTD,B1_GRUPO,D3_ESTORNO,D3_CF,D3_EMISSAO,D3_DOC,D3_NUMSERI,D3_OP,D3_TIPO,D3_TM,D3_QUANT, "
				cQueryAFI += " D3_CUSFF1,D3_CUSTO1 "
				cQueryAFI += " FROM "+RetSqlName("AFI")+" AFI "
				cQueryAFI += " LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ?"
				cQueryAFI += " AND SB1.B1_COD = AFI.AFI_COD "
				cQueryAFI += " AND SB1.D_E_L_E_T_ = ? "
				cQueryAFI += " LEFT JOIN "+RetSqlName("SD3")+" SD3 ON ?"
				cQueryAFI += " AND SD3.D3_COD = AFI.AFI_COD "
				cQueryAFI += " AND SD3.D3_LOCAL = AFI.AFI_LOCAL  "
				cQueryAFI += " AND SD3.D_E_L_E_T_ = ? "
				cQueryAFI += " WHERE AFI.AFI_FILIAL = ? "	
				cQueryAFI += " AND AFI.AFI_PROJET = ? "		
				cQueryAFI += " AND AFI.AFI_REVISA = ? "		
				cQueryAFI += " AND AFI.D_E_L_E_T_  = ? "
				cQueryAFI += " ORDER BY AFI.AFI_FILIAL, AFI.AFI_PROJET, AFI.AFI_REVISA"

				cMD5 := MD5(cQueryAFI)
				
				If (nPosPrepAFI := Ascan(aPrepaAFI,{|x| x[2] == cMD5})) == 0
					Aadd(aPrepaAFI,{FwExecStatement():New(ChangeQuery(cQueryAFI)),cMD5})
					nPosPrepAFI := Len(aPrepaAFI)
				Endif

				nParam := 1

				aPrepaAFI[nPosPrepAFI][1]:SetUnsafe(nParam++, FwJoinFilial("SB1", "AFI"))
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, ' ')
				aPrepaAFI[nPosPrepAFI][1]:SetUnsafe(nParam++, FwJoinFilial("SD3", "AFI"))
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, ' ')
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFI[nPosPrepAFI][1]:SetString(nParam++, ' ')

				cAliasAFI := aPrepaAFI[nPosPrepAFI][1]:OpenAlias()
				(cAliasAFI)->(DbGoTop())

				While (cAliasAFI)->(!Eof()) .And. ( (cAliasAFI)->(AFI_FILIAL + AFI_PROJET + AFI_REVISA) == (self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->AF8_PROJET + (self:cAliasAF8)->AF8_REVISA )

					If !((cAliasAFI)->D3_ESTORNO == "S") .And. !((cAliasAFI)->D3_CF == "RE5")

						If StoD((cAliasAFI)->D3_EMISSAO) >= MV_PAR07 .And. StoD((cAliasAFI)->D3_EMISSAO) <= MV_PAR08

							If ((cAliasAFI)->AFI_COD < MV_PAR09 .Or. (cAliasAFI)->AFI_COD > MV_PAR10)
								(cAliasAFI)->(DbSkip())
								Loop
							Endif

							nValSB1 := xMoedaPMS((cAliasAFI)->B1_CUSTD,Val((cAliasAFI)->B1_MCUSTD),nMoedaPMS,"",,,,,,cTrunca,,"SB1")

							// adiciona informacao sobre o produto
							aItemInfo := {}
							aAdd(aItemInfo, "SD3")
							aAdd(aItemInfo, (cAliasAFI)->D3_EMISSAO)
							aAdd(aItemInfo, (cAliasAFI)->D3_DOC)
							aAdd(aItemInfo, (cAliasAFI)->D3_NUMSERI)
							aAdd(aItemInfo, (cAliasAFI)->D3_OP)
							aAdd(aItemInfo, (cAliasAFI)->D3_TIPO)
							aAdd(aItemInfo, (cAliasAFI)->D3_TM)

							nQuantSD3 := IIF((cAliasAFI)->D3_TM > "500",(cAliasAFI)->D3_QUANT, ((cAliasAFI)->D3_QUANT*-1))

							aAdd(aItemInfo, nQuantSD3)
							nCustoSD3 := IIF((self:cAliasAF8)->AF8_TPCUS == "2", (cAliasAFI)->D3_CUSFF1, (cAliasAFI)->D3_CUSTO1)
							aAdd(aItemInfo, xMoedaPMS(nCustoSD3,1,nMoedaPMS,"",,,,,,cTrunca,,"SD3") )

							aAdd(aItemInfo, SD3->(Recno()))

							If MV_PAR12 == 1 // Aglutina tarefas? Sim / Nao
								nPosABC := aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFI)->AFI_COD})
							Else
								nPosABC := aScan(aArrayABC,{|x| x[1]=="SB1" .And. x[2]==(cAliasAFI)->AFI_COD .And. x[11]+x[12]+x[13]==(cAliasAFI)->(AFI_PROJET+AFI_REVISA+AFI_TAREFA) })
							EndIf

							If nPosABC > 0

								aArrayABC[nPosABC ,05] += IIF(SD3->D3_TM > "500", (cAliasAFI)->D3_QUANT, ((cAliasAFI)->D3_QUANT*-1))
								nCustoSD3 := IIF((self:cAliasAF8)->AF8_TPCUS == "2", (cAliasAFI)->D3_CUSFF1, (cAliasAFI)->D3_CUSTO1)
								aArrayABC[nPosABC ,06] += IIF(SD3->D3_TM > "500",xMoedaPMS(nCustoSD3,1,nMoedaPMS,"",,,,,,cTrunca,,"SD3"),xMoedaPMS(nCustoSD3,1,nMoedaPMS,"",,,,,,cTrunca,,"SD3")*-1)

								aAdd(aArrayABC[nPosABC ,09], aItemInfo)

							Else

								nCustoSD3 := IIF((self:cAliasAF8)->AF8_TPCUS=="2", (cAliasAFI)->D3_CUSFF1, (cAliasAFI)->D3_CUSTO1)
								nCustoSD3 := IIF((cAliasAFI)->D3_TM > "500", xMoedaPMS(nCustoSD3,1,nMoedaPMS,"",,,,,,cTrunca,,"SD3"), xMoedaPMS(nCustoSD3,1,nMoedaPMS,"",,,,,,cTrunca,,"SD3")*-1)
								nQuantSD3 := IIF((cAliasAFI)->D3_TM > "500", (cAliasAFI)->D3_QUANT, ((cAliasAFI)->D3_QUANT*1))

								aAdd(aArrayABC,{"SB1", (cAliasAFI)->AFI_COD, 0, 0, nQuantSD3, nCustoSD3, 0, 0,{aItemInfo}, nValSB1, (cAliasAFI)->AFI_PROJET, (cAliasAFI)->AFI_REVISA, (cAliasAFI)->AFI_TAREFA, (cAliasAFI)->B1_GRUPO })

							Endif
						Endif
					Endif

					(cAliasAFI)->(DbSkip())
				Enddo

			Endif

			// QUERY AFU - APONTAMENTO
			If AFU->(MsSeek((self:cAliasAF8)->AF8_FILIAL + "1" + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAFU := " SELECT AFU_RECURS, AFU_PROJET, AFU_TAREFA, AFU_REVISA, AFU_DATA, "								
				cQueryAFU += " AFU_COD, AFU_CUSTO1, AFU_HORAI, AFU_HORAF, AFU_HQUANT, AFU.R_E_C_N_O_ AS AFU_RECNO, "
				cQueryAFU += " AE8_PRODUT,AE8_EQUIP,AE8_DESCRI,AED_DESCRI,AF9_DESCRI,AF9_DESCRI "								
				cQueryAFU += " FROM " + RetSqlName("AFU")+" AFU "
				cQueryAFU += " LEFT JOIN " + RetSqlName("AE8")+" AE8 ON ? "
				cQueryAFU += " AND AE8.AE8_RECURS = AFU_RECURS "
				cQueryAFU += " AND AE8.D_E_L_E_T_ = ? "
				cQueryAFU += " LEFT JOIN " + RetSqlName("AED")+" AED ON ? "
				cQueryAFU += " AND AED.AED_EQUIP = AE8.AE8_EQUIP "
				cQueryAFU += " AND AED.D_E_L_E_T_ = ? "
				cQueryAFU += " LEFT JOIN " + RetSqlName("AF9")+" AF9 ON ? "
				cQueryAFU += " AND AF9.AF9_PROJET = AFU.AFU_PROJET "
				cQueryAFU += " AND AF9.AF9_REVISA = AFU.AFU_REVISA "
				cQueryAFU += " AND AF9.AF9_TAREFA = AFU.AFU_TAREFA "
				cQueryAFU += " AND AFU.D_E_L_E_T_ = ? "
				cQueryAFU += " WHERE AFU.AFU_FILIAL = ? "			
				cQueryAFU += " AND AFU.AFU_PROJET = ? "				
				cQueryAFU += " AND AFU.AFU_REVISA = ? "				
				cQueryAFU += " AND AFU.AFU_DATA BETWEEN ? AND ? "	
				cQueryAFU += " AND AFU.AFU_COD BETWEEN ? AND ? "	
				cQueryAFU += " AND AFU.AFU_CTRRVS = ? "
				cQueryAFU += " AND AFU.D_E_L_E_T_  = ? "
				cQueryAFU += " ORDER BY AFU.AFU_FILIAL, AFU.AFU_CTRRVS, AFU.AFU_PROJET, AFU.AFU_REVISA, AFU.AFU_TAREFA, AFU.AFU_RECURS, AFU.AFU_DATA "

				cMD5 := MD5(cQueryAFU)
				
				If (nPosPrepAFU := Ascan(aPrepaAFU,{|x| x[2] == cMD5})) == 0
					Aadd(aPrepaAFU,{FwExecStatement():New(ChangeQuery(cQueryAFU)),cMD5})
					nPosPrepAFU := Len(aPrepaAFU)
				Endif

				nParam := 1

				aPrepaAFU[nPosPrepAFU][1]:SetUnsafe(nParam++, FwJoinFilial("AE8", "AFU"))
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, ' ')
				aPrepaAFU[nPosPrepAFU][1]:SetUnsafe(nParam++, FwJoinFilial("AED", "AE8"))
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, ' ')
				aPrepaAFU[nPosPrepAFU][1]:SetUnsafe(nParam++, FwJoinFilial("AF9", "AFU"))
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, ' ')
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, DTOS(MV_PAR07))
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, DTOS(MV_PAR08))
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, MV_PAR09)
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, MV_PAR10)
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, '1')
				aPrepaAFU[nPosPrepAFU][1]:SetString(nParam++, ' ')

				cAliasAFU := aPrepaAFU[nPosPrepAFU][1]:OpenAlias()
				(cAliasAFU)->(DbGoTop())

				While !(cAliasAFU)->(Eof())

					lAE8 := IIF(!Empty((cAliasAFU)->AFU_COD), .F., MV_REALI == 2 .And. !Empty((cAliasAFU)->AE8_PRODUT))
					lRecProd := lAE8

					cProdProj := IIF(!lAE8, (cAliasAFU)->AFU_COD, (cAliasAFU)->AE8_PRODUT)

					aItemInfo := {}
					aAdd(aItemInfo, "AFU")
					aAdd(aItemInfo, (cAliasAFU)->AFU_RECURS)
					aAdd(aItemInfo, IIf(Empty(cObfNRecur),(cAliasAFU)->AE8_DESCRI,cObfNRecur))
					aAdd(aItemInfo, (cAliasAFU)->AE8_EQUIP)
					aAdd(aItemInfo, (cAliasAFU)->AED_DESCRI)
					aAdd(aItemInfo, (cAliasAFU)->AFU_TAREFA)
					aAdd(aItemInfo, (cAliasAFU)->AF9_DESCRI)
					aAdd(aItemInfo, (cAliasAFU)->AFU_DATA)
					aAdd(aItemInfo, (cAliasAFU)->AFU_HORAI)
					aAdd(aItemInfo, (cAliasAFU)->AFU_HORAF)
					aAdd(aItemInfo, (cAliasAFU)->AFU_HQUANT)
					aAdd(aItemInfo, (cAliasAFU)->AFU_RECNO)

					// Visualizar Custo Realizado por Produto
					cCodigo := IIF(MV_REALI == 2 .and. !Empty((cAliasAFU)->AFU_COD), (cAliasAFU)->AFU_COD, IIF(lRecProd, (cAliasAFU)->AE8_PRODUT, IIF(!Empty((cAliasAFU)->AFU_RECURS), (cAliasAFU)->AFU_RECURS, (cAliasAFU)->AFU_COD)))
					cTipo 	:= IIF(MV_REALI == 2 .and. !Empty((cAliasAFU)->AFU_COD), "SB1", IIF(lRecProd, "SB1", IIF(!Empty((cAliasAFU)->AFU_RECURS), "REC", "SB1")))

					// Aglutina tarefas? Sim / Nao
					// se aglutina, verifica se jah existe no aArrayABC
					nValConv := xMoedaPMS((cAliasAFU)->AFU_CUSTO1,1,nMoedaPMS,"",,,,,,cTrunca,,"AFU")

					If MV_PAR12 == 1 .AND. (nPosABC := aScan(aArrayABC,{|x| x[1]==cTipo .And. x[2]==cCodigo}))>0
						aArrayABC[nPosABC ,05] += (cAliasAFU)->AFU_HQUANT
						aArrayABC[nPosABC ,06] += nValConv
						aAdd(aArrayABC[nPosABC ,09], aItemInfo)
					Else
						// Aglutina tarefas? Sim / Nao
						// se não aglutina, verifica se jah existe tipo, produto/recurso, projeto, revisao e tarefa no aArrayABC
						If MV_PAR12 == 2 .AND. (nPosABC := aScan(aArrayABC ,{|x| x[1]==cTipo .And. x[2]==cCodigo .And. x[11]==(cAliasAFU)->AFU_PROJET .AND. x[12]==(cAliasAFU)->AFU_REVISA .AND. x[13]==(cAliasAFU)->AFU_TAREFA }))>0
							aArrayABC[nPosABC ,05] += (cAliasAFU)->AFU_HQUANT
							aArrayABC[nPosABC ,06] += nValConv
							aAdd(aArrayABC[nPosABC ,09], aItemInfo)
						Else
							aAdd(aArrayABC,{IIF(cTipo == "REC", "REC", "SB1"),IIF(cTipo == "REC", (cAliasAFU)->AFU_RECURS, (cAliasAFU)->AE8_PRODUT),0,0,(cAliasAFU)->AFU_HQUANT,nValConv,0,0,{aItemInfo},0,(cAliasAFU)->AFU_PROJET, (cAliasAFU)->AFU_REVISA, (cAliasAFU)->AFU_TAREFA,cGrupo })
						Endif

					Endif
					
					(cAliasAFU)->(DbSkip())
					
				Enddo

			Endif

			// QUERY AFJ - EMPENHOS DO PROJETO
			If lChkAJC .And. AJC->(MsSeek((self:cAliasAF8)->AF8_FILIAL+"1"+(self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAJC := " SELECT AJC_FILIAL, AJC_CTRRVS, AJC_PROJET, AJC_REVISA, AJC_TAREFA, AJC_DATA, "
				cQueryAJC += " AJC_CUSTO1, AJC_TIPO, AJC_COD, AJC_TIPOD, AJC_DESCRI, AJC_QUANT, AJC.R_E_C_N_O_ AS AJC_RECNO, "
				cQueryAJC += " B1_CUSTD,B1_MCUSTD,B1_GRUPO,B1_DESC,AF9_DESCRI"
				cQueryAJC += " FROM "+RetSqlName("AJC")+" AJC "
				cQueryAJC += " LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ?"
				cQueryAJC += " AND SB1.B1_COD = AJC.AJC_COD "
				cQueryAJC += " AND SB1.D_E_L_E_T_ = ? "
				cQueryAJC += " LEFT JOIN " + RetSqlName("AF9")+" AF9 ON ? "
				cQueryAJC += " AND AF9.AF9_PROJET = AJC.AJC_PROJET "
				cQueryAJC += " AND AF9.AF9_REVISA = AJC.AJC_REVISA "
				cQueryAJC += " AND AF9.AF9_TAREFA = AJC.AJC_TAREFA "
				cQueryAJC += " AND AF9.D_E_L_E_T_ = ? "
				cQueryAJC += " WHERE AJC.AJC_FILIAL = ? "			
				cQueryAJC += " AND AJC.AJC_PROJET = ? "				
				cQueryAJC += " AND AJC.AJC_REVISA = ? "				
				cQueryAJC += " AND AJC.AJC_DATA BETWEEN ? AND ? "	           
				cQueryAJC += " AND AJC.AJC_COD BETWEEN ? AND ? "	        
				cQueryAJC += " AND AJC.AJC_CTRRVS = ? "	  
				cQueryAJC += " AND AJC.D_E_L_E_T_  = ? "
				cQueryAJC += " ORDER BY AJC.AJC_FILIAL, AJC.AJC_PROJET, AJC.AJC_REVISA "

				cMD5 := MD5(cQueryAJC)
				
				If (nPosPrepAJC := Ascan(aPrepaAJC,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAJC,{FwExecStatement():New(ChangeQuery(cQueryAJC)),cMD5})
					nPosPrepAJC := Len(aPrepaAJC)
				Endif

				nParam := 1
				aPrepaAJC[nPosPrepAJC][1]:SetUnsafe(nParam++, FwJoinFilial("SB1", "AJC"))
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, ' ')
				aPrepaAJC[nPosPrepAJC][1]:SetUnsafe(nParam++, FwJoinFilial("AF9", "AJC"))
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, ' ')
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, DTOS(MV_PAR07))
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, DTOS(MV_PAR08))
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, MV_PAR09)
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, MV_PAR10)
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, '1')
				aPrepaAJC[nPosPrepAJC][1]:SetString(nParam++, ' ')

				cAliasAJC := aPrepaAJC[nPosPrepAJC][1]:OpenAlias()
				(cAliasAJC)->(DbGoTop())
			
				While (cAliasAJC)->(!Eof()) .And. ((cAliasAJC)->(AJC_FILIAL + AJC_CTRRVS + AJC_PROJET + AJC_REVISA) == (self:cAliasAF8)->AF8_FILIAL + "1"+ (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)) .And.;
					((cAliasAJC)->AJC_DATA >= DTOS(MV_PAR07) .Or. (cAliasAJC)->AJC_DATA <= DTOS(MV_PAR08)) .And. (((cAliasAJC)->AJC_COD >= MV_PAR09 .Or. (cAliasAJC)->AJC_COD <= MV_PAR10))

					If (cAliasAJC)->AJC_TIPO == "1"
						nValSB1 := xMoedaPMS((cAliasAJC)->B1_CUSTD,Val((cAliasAJC)->B1_MCUSTD),nMoedaPMS,"",,,,,,cTrunca,)
					EndIf

					nValConv := xMoedaPMS((cAliasAJC)->AJC_CUSTO1,1,nMoedaPMS,"",,,,,,cTrunca,,"AJC")

					aItemInfo := {}
					aAdd(aItemInfo, "AJC")
					aAdd(aItemInfo, (cAliasAJC)->AJC_TIPO)
					aAdd(aItemInfo, IIf((cAliasAJC)->AJC_TIPO=="1",(cAliasAJC)->AJC_COD,(cAliasAJC)->AJC_TIPOD))
					aAdd(aItemInfo, IIf((cAliasAJC)->AJC_TIPO=="1",(cAliasAJC)->B1_DESC,(cAliasAJC)->AJC_DESCRI))
					aAdd(aItemInfo, (cAliasAJC)->AJC_TAREFA)
					aAdd(aItemInfo, (cAliasAJC)->AF9_DESCRI)
					aAdd(aItemInfo, nValConv )
					aAdd(aItemInfo, (cAliasAJC)->AJC_QUANT)
					aAdd(aItemInfo, (cAliasAJC)->AJC_RECNO)

					If MV_PAR12 == 1 // Aglutina tarefas? Sim / Nao
						nPosABC := IIf( (cAliasAJC)->AJC_TIPO=="1", aScan(aArrayABC,{|x| x[1]=="SB1".And.x[2]==(cAliasAJC)->AJC_COD}),;
										nPosABC := aScan(aArrayABC,{|x| x[1]=="DSP" .And.x[2]==(cAliasAJC)->AJC_TIPOD}) )
						If nPosABC > 0
							aArrayABC[nPosABC ,05] += (cAliasAJC)->AJC_QUANT
							aArrayABC[nPosABC ,06] += nValConv
							aAdd(aArrayABC[nPosABC ,09],aItemInfo)
							lAdic := .F.
						EndIf
					EndIf

					If lAdic
						If (cAliasAJC)->AJC_TIPO == "1"
							aAdd(aArrayABC,{"SB1",(cAliasAJC)->AJC_COD,0,0,(cAliasAJC)->AJC_QUANT,nValConv,0,0,{aItemInfo},nValSB1,;
													(cAliasAJC)->AJC_PROJET, (cAliasAJC)->AJC_REVISA, (cAliasAJC)->AJC_TAREFA,(cAliasAJC)->B1_GRUPO })
						Else
							aAdd(aArrayABC,{"DSP",(cAliasAJC)->AJC_TIPOD,0,0,(cAliasAJC)->AJC_QUANT,nValConv,0,0,{aItemInfo},0,;
													(cAliasAJC)->AJC_PROJET, (cAliasAJC)->AJC_REVISA, (cAliasAJC)->AJC_TAREFA,cGrupo })
						EndIf
					EndIf

					(cAliasAJC)->(dbSkip())

				EndDo
			EndIf

			// QUERY AFR - PROJETO X DESPESAS FINANCEIRAS
			IF AFR->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)))

				cQueryAFR := " SELECT AFR_FILIAL, AFR_PROJET, AFR_REVISA, AFR_TAREFA, AFR_PREFIX, AFR_NUM, AFR_PARCEL, "
				cQueryAFR += " AFR_TIPO, AFR_FORNEC, AFR_LOJA, AFR_DATA, AFR_VENREA, AFR_VALOR1, AFR_TIPOD, AFR.R_E_C_N_O_,A2_NOME "
				cQueryAFR += " FROM "+RetSqlName("AFR")+" AFR "
				cQueryAFR += " LEFT JOIN "+RetSqlName("SA2")+" SA2 ON ? "
				cQueryAFR += " AND SA2.A2_COD = AFR.AFR_FORNEC "
				cQueryAFR += " AND SA2.A2_LOJA = AFR.AFR_LOJA "
				cQueryAFR += " AND SA2.D_E_L_E_T_ = ? "
				cQueryAFR += " WHERE AFR.AFR_FILIAL = ? "	
				cQueryAFR += " AND AFR.AFR_PROJET = ? "		
				cQueryAFR += " AND AFR.AFR_REVISA = ? "		  
				cQueryAFR += " AND AFR.D_E_L_E_T_  = ? "
				cQueryAFR += " ORDER BY AFR.AFR_FILIAL, AFR.AFR_PROJET, AFR.AFR_REVISA "

				cMD5 := MD5(cQueryAFR)
				
				If (nPosPrepAFR := Ascan(aPrepaAFR,{|x| x[2] == cMD5})) == 0 
					Aadd(aPrepaAFR,{FwExecStatement():New(ChangeQuery(cQueryAFR)),cMD5})
					nPosPrepAFR := Len(aPrepaAFR)
				Endif

				nParam := 1

				aPrepaAFR[nPosPrepAFR][1]:SetUnsafe(nParam++,  FwJoinFilial("SA2", "AFR"))
				aPrepaAFR[nPosPrepAFR][1]:SetString(nParam++, ' ')
				aPrepaAFR[nPosPrepAFR][1]:SetString(nParam++, (self:cAliasAF8)->AF8_FILIAL)
				aPrepaAFR[nPosPrepAFR][1]:SetString(nParam++, (self:cAliasAF8)->AF8_PROJET)
				aPrepaAFR[nPosPrepAFR][1]:SetString(nParam++, (self:cAliasAF8)->AF8_REVISA)
				aPrepaAFR[nPosPrepAFR][1]:SetString(nParam++, ' ')

				cAliasAFR := aPrepaAFR[nPosPrepAFR][1]:OpenAlias()
				(cAliasAFR)->(DbGoTop())
			
				While (cAliasAFR)->(!Eof()) .And. (self:cAliasAF8)->AF8_FILIAL + (self:cAliasAF8)->(AF8_PROJET + AF8_REVISA)  == (cAliasAFR)->(AFR_FILIAL + AFR_PROJET + AFR_REVISA)

					If SE2->(MsSeek((self:cAliasAF8)->AF8_FILIAL + (cAliasAFR)->(AFR_PREFIX + AFR_NUM + AFR_PARCEL + AFR_TIPO + AFR_FORNEC + AFR_LOJA))) .And. SE2->E2_EMIS1 >= MV_PAR07 .And. SE2->E2_EMIS1 <= MV_PAR08

						//Se nao for abatimento
						nSinal := IIf( (SE2->E2_TIPO $ MVABATIM+"/"+MV_CPNEG), -1, 1 )

						aItemInfo := {}
						aAdd(aItemInfo, "AFR")
						aAdd(aItemInfo, (cAliasAFR)->AFR_FORNEC)
						aAdd(aItemInfo, IIf(Empty(cObfNFor),(cAliasAFR)->A2_NOME,cObfNFor))
						aAdd(aItemInfo, (cAliasAFR)->AFR_NUM)
						aAdd(aItemInfo, (cAliasAFR)->AFR_DATA)
						aAdd(aItemInfo, (cAliasAFR)->AFR_VENREA)
						aAdd(aItemInfo, xMoedaPMS((cAliasAFR)->AFR_VALOR1,1,nMoedaPMS,"",,,,,,cTrunca,,"AFR") *nSinal)
						aAdd(aItemInfo, (cAliasAFR)->R_E_C_N_O_)
						aAdd(aItemInfo, SE2->(Recno()))

						// Aglutina tarefas? Sim / Nao
						If MV_PAR12 == 1 .AND. (nPosABC := aScan(aArrayABC,{|x| x[1]=="DSP" .And. x[2]==(cAliasAFR)->AFR_TIPOD}))>0
							aArrayABC[nPosABC ,06] += xMoedaPMS((cAliasAFR)->AFR_VALOR1,1,nMoedaPMS,"",,,,,,cTrunca,,"AFR") * nSinal
							aAdd(aArrayABC[nPosABC ,09], aItemInfo)
						Else
							aAdd(aArrayABC,{"DSP",(cAliasAFR)->AFR_TIPOD,0,0,0,;
							xMoedaPMS((cAliasAFR)->AFR_VALOR1,1,nMoedaPMS,"",,,,,,cTrunca,,"AFR")*nSinal,;
							0,0, {aItemInfo},0, (cAliasAFR)->AFR_PROJET, (cAliasAFR)->AFR_REVISA, (cAliasAFR)->AFR_TAREFA,cGrupo })
						EndIf
					EndIf

					(cAliasAFR)->(dbSkip())

				EndDo

			EndIf

			If !(SE5->E5_SITUACA == "C") .And. (self:cAliasAF8)->FK5_RECPAG == "P" .And. SToD((self:cAliasAF8)->FK5_DATA) >= MV_PAR07 .And. SToD((self:cAliasAF8)->FK5_DATA) <= MV_PAR08

				nValConv := xMoedaPMS((self:cAliasAF8)->AJE_VALOR,1,nMoedaPMS,"",,,,,,cTrunca,,"AJE")

				aArrayABC := {}
				aItemInfo := {}

				aAdd(aItemInfo, "SE5")
				aAdd(aItemInfo, (self:cAliasAF8)->FK5_AGENCI)
				aAdd(aItemInfo, (self:cAliasAF8)->FK5_CONTA)
				aAdd(aItemInfo, nValConv )
				aAdd(aItemInfo, (self:cAliasAF8)->FK7_NUM)
				aAdd(aItemInfo, (self:cAliasAF8)->FK5_DTDISP)
				aAdd(aItemInfo, (self:cAliasAF8)->FK5_NUMCH)
				aAdd(aItemInfo, (self:cAliasAF8)->AJE_RECNO)
				aAdd(aItemInfo, (self:cAliasAF8)->SE5_RECNO)

				lAgluSE5 := MV_PAR12 == 1 .And. (nPosABC := aScan(aArrayABC,{|x| x[1]=="MOV"})) > 0
				cArrayABC := IIF(lAgluSE5,"aArrayABC[nPosABC ,06] += nValConv",".T.")
				cAddMOV := "aAdd(aArrayABC"+IIF(lAgluSE5,"[nPosABC ,09], aItemInfo)",",{'MOV','',0,0,0,nValConv,0,0, {aItemInfo},0,(self:cAliasAF8)->AJE_PROJET, (self:cAliasAF8)->AJE_REVISA, (self:cAliasAF8)->AJE_TAREFA,cGrupo })")
				&(cAddMOV)
				&(cArrayABC)

			EndIf

			// GRAVACAO DOS DADOS
			If Len(aArrayABC) > 0
			
				aArrayABC := aSort(aArrayABC,,,{|x,y| x[2] < y[2]})
				For nX := 1 to Len(aArrayABC)

					If !Empty(aArrayABC[nX,14])
						SBM->(MsSeek((self:cAliasAF8)->AF8_FILIAL+aArrayABC[nX,14]))
					EndIf

					If !AF9->(MsSeek((self:cAliasAF8)->AF8_FILIAL+aArrayABC[nX,11]+aArrayABC[nX,12]+aArrayABC[nX,13]))
						Loop
					EndIf

					nQtdHoraRec	:= 0
					nQuantidade	:= 0
					nCustoDet	:= 0
					nValorDet	:= 0
					nPercPrDet	:= 0
					cBancoDet	:= ""
					cAgencDet	:= ""
					cContaDet	:= ""
					cNumTiDet	:= ""
					cTipoDet	:= ""
					cCodCliFor	:= ""
					cNomCliFor	:= ""
					cLojCliFor	:= ""
					cNumNota	:= ""
					cSerNota	:= ""
					cTipNota	:= ""
					cCodgRec	:= ""
					cDescRec	:= ""
					cCodTarDet	:= ""
					cIteTarDet	:= ""
					cDesTarDet	:= ""
					dDtNecDet	:= CToD("")
					dDataVen	:= CToD("")
					cCodTar		:= AF9->AF9_TAREFA
					cDesTar		:= AF9->AF9_DESCRI
					cTipoReg	:= aArrayABC[nX,1]
					nQtdPrev	:= aArrayABC[nX,3]
					nVlrPrev	:= aArrayABC[nX,4]
					nQtdAtuRea	:= aArrayABC[nX,5]
					nVlrRea		:= aArrayABC[nX,6]
					cCodGrpPrd 	:= aArrayABC[nX,14]
					nPerPrPrev	:= ((nVlrPrev/nCustoPrj)*100)
					nPercRea	:= ((nVlrRea/nCustoReal)*100)
					nQtdEmpRea	:= IIf(aArrayABC[nX,7] < 0, 0, aArrayABC[nX,7])

					Do Case
						Case cTipoReg == "SB1"
							SB1->(MsSeek((self:cAliasAF8)->AF8_FILIAL+aArrayABC[nX,2]))
							nPercAcm  += aArrayABC[nX,4]/nCustoPrj*100
							nPercAcmB := nPercAcm

							If aArrayABC[nX,7] > 0
								nTotEmp += aArrayABC[nX,7]
							EndIf

							nPercAcm2  += aArrayABC[nX,6]/nCustoReal*100
							nPercAcm2B := nPercAcm2
							nQtdeRepo  := (aArrayABC[nX,3]-aArrayABC[nX,5])

							// Saldo com o calculo do Custo Previsto
							If MV_PAR11 == 1 //Custo Atual
								nValRepo := aArrayABC[nX,10]
								nTotRepo += nValRepo*nQtdeRepo
							EndIf

							dDataEmi	:= CToD("")
							cCodProd	:= Alltrim(SB1->B1_COD)
							cDesProd	:= Alltrim(SB1->B1_DESC)
							cUnProd		:= Alltrim(SB1->B1_UM)
							cDesGrpPrd 	:= Alltrim(IIf(!Empty(aArrayABC[nX,14]),SBM->BM_DESC,""))
							nSaldCusto	:= IIf(MV_PAR11 == 1, (aArrayABC[nX,10] * nQtdeRepo), (aArrayABC[nX,4]-aArrayABC[nX,6]))

							self:jItems := JsonObject():new()
							For nZ := 1 To Len(self:aStruct)

								If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
								Else
									cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
								EndIf

								If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
								ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
									self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
								ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
									self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
								Else
									self:jItems[self:aStruct[nZ][1]] := cValue
								EndIf

							Next nZ
							self:processData(1)
							self:oData:appendData(self:jItems)

							// os detalhes do apontamento sao armazenados no elemento
							// de indice 9 do aArrayABC
							If !Empty(aArrayABC[nX, 9])

								nQtdPrev 	:= 0
								nVlrPrev	:= 0
								nPerPrPrev	:= 0
								nQtdEmpRea	:= 0
								nQtdAtuRea	:= 0
								nVlrRea		:= 0
								nPercRea	:= 0
								nPercAcm	:= 0
								nPercAcm2	:= 0
								nQtdeRepo	:= 0
								nSaldCusto	:= 0

								For nY := 1 To Len(aArrayABC[nX, 9])

									Do Case
										// Nota Fiscal de Entrada
										Case aArrayABC[nX, 9, nY, 1] == "AFN"

											cLojCliFor	:= ""
											cTipoDet 	:= STR0004 //NF ENTRADA
											cCodCliFor	:= aArrayABC[nX, 9, nY, 2]
											cNomCliFor	:= aArrayABC[nX, 9, nY, 3]
											cNumNota	:= aArrayABC[nX, 9, nY, 4]
											cSerNota	:= aArrayABC[nX, 9, nY, 5]
											cTipNota	:= aArrayABC[nX, 9, nY, 6]
											nCustoDet	:= aArrayABC[nX, 9, nY, 8]
											nQuantidade := aArrayABC[nX, 9, nY, 7]
											dDataEmi	:= aArrayABC[nX, 9, nY, 9]									
											nPercPrDet	:= Round(nCustoDet / nCustoReal * 100, 2)

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf


												If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
												ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
													self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
												ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
													self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
												Else
													self:jItems[self:aStruct[nZ][1]] := cValue
												EndIf

											Next nZ
											self:processData(1)
											self:oData:appendData(self:jItems)

										// Nota Fiscal de Saida
										Case aArrayABC[nX, 9, nY, 1] == "AFS"
											AFS->(MsGoTo(aArrayABC[nX, 9, nY, 10]))
											SD2->(MsGoTo(aArrayABC[nX, 9, nY, 11]))

											cLojCliFor	:= ""
											cTipoDet 	:= STR0005 //NF SAIDA
											cCodCliFor	:= aArrayABC[nX, 9, nY, 2]
											cNomCliFor	:= aArrayABC[nX, 9, nY, 3]
											cNumNota	:= aArrayABC[nX, 9, nY, 4]
											cSerNota	:= aArrayABC[nX, 9, nY, 5]
											cTipNota	:= aArrayABC[nX, 9, nY, 6]
											nCustoDet	:= aArrayABC[nX, 9, nY, 8]
											nQuantidade := aArrayABC[nX, 9, nY, 7]
											dDataEmi	:= aArrayABC[nX, 9, nY, 9]
											nPercPrDet	:= Round(nCustoDet / nCustoReal * 100, 2)

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf

												self:jItems[self:aStruct[nZ][1]] := IIF(lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0,;
																						aPDFields[nScan][2],;
																							IIF(lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR",;
																								FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME))),;
																									IIF(UPPER(self:aStruct[nZ][3]) == "DATE",;
																										IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue)),;
																										cValue)))
											Next nZ

											self:processData(1)
											self:oData:appendData(self:jItems)

										// Movimentacoes Internas
										Case aArrayABC[nX, 9, nY, 1] == "SD3"
											SD3->(MsGoTo(aArrayABC[nX, 9, nY, 10]))

											cTipoDet 	:= STR0006 //MOV INTERNA
											dDataEmi	:= aArrayABC[nX, 9, nY, 2]
											cNumNota	:= aArrayABC[nX, 9, nY, 3]
											cSerNota	:= aArrayABC[nX, 9, nY, 4]
											cTipNota	:= aArrayABC[nX, 9, nY, 7]
											nCustoDet	:= aArrayABC[nX, 9, nY, 9]
											nQuantidade := aArrayABC[nX, 9, nY, 8]
											nPercPrDet	:= Round(aArrayABC[nX, 9, nY, 9] / nCustoReal * 100, 2)

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf

												If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
												ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
													self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
												ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
													self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
												Else
													self:jItems[self:aStruct[nZ][1]] := cValue
												EndIf

											Next nZ
											self:processData(1)
											self:oData:appendData(self:jItems)

										// Apontamento de Recursos
										Case aArrayABC[nX, 9, nY, 1] == "AFU"
											AFU->(MsGoTo(aArrayABC[nX, 9, nY, 12]))

											dDataEmi	:= CToD("")
											cTipoDet 	:= STR0007 //APT RECURSO
											cCodgRec	:= aArrayABC[nX, 9, nY, 2]
											cDescRec	:= aArrayABC[nX, 9, nY, 3]
											cCodTarDet	:= aArrayABC[nX, 9, nY, 6]
											cDesTarDet	:= aArrayABC[nX, 9, nY, 7]
											nQtdHoraRec	:= aArrayABC[nX, 9, nY, 11]

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf

												If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
												ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
													self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
												ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
													self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
												Else
													self:jItems[self:aStruct[nZ][1]] := cValue
												EndIf

											Next nZ
											self:processData(1)
											self:oData:appendData(self:jItems)
									EndCase
								Next nY
								nPercAcm 	:= nPercAcmB
								nPercAcm2 	:= nPercAcm2B
							EndIf

						Case cTipoReg == "DSP"
							cUnProd		:= ""
							dDataEmi	:= CToD("")
							cCodProd	:= Substr(aArrayABC[nX,2],1,20)
							cDesProd	:= FWGetSX5("FD", aArrayABC[nx,2])[1][4]
							nPercAcm  	+= aArrayABC[nX,4]/nCustoPrj*100
							nPercAcm2 	+= aArrayABC[nX,6]/nCustoReal*100
							nSaldCusto	:= (aArrayABC[nX,4]-aArrayABC[nX,6])

							self:jItems := JsonObject():new()
							For nZ := 1 To Len(self:aStruct)

								If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
								Else
									cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
								EndIf

								If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
								ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
									self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
								ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
									self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
								Else
									self:jItems[self:aStruct[nZ][1]] := cValue
								EndIf

							Next nZ
							self:processData(1)
							self:oData:appendData(self:jItems)

							// Saldo com o calculo do Custo Atual
							nTotRepo += IIF( MV_PAR11 == 1, aArrayABC[nX,4], 0)
						
							// impressao dos detalhes do apontamento
							If !Empty(aArrayABC[nX, 9])
								nQtdPrev 	:= 0
								nVlrPrev	:= 0
								nPerPrPrev	:= 0
								nQtdEmpRea	:= 0
								nQtdAtuRea	:= 0
								nVlrRea		:= 0
								nPercRea	:= 0
								nPercAcm	:= 0
								nPercAcm2	:= 0
								nQtdeRepo	:= 0
								nSaldCusto	:= 0

								For nY := 1 To Len(aArrayABC[nX, 9])
									Do Case
										// Despesas Financeiras
										Case aArrayABC[nX, 9, nY, 1] == "AFR"
											AFR->(MsGoTo(aArrayABC[nX, 9, nY, 8]))
											SE2->(MsGoTo(aArrayABC[nX, 9, nY, 9]))

											cUnProd		:= ""
											cLojCliFor	:= ""
											cTipoDet	:= STR0008 //DSP FINANCEIRA
											cCodCliFor	:= aArrayABC[nX, 9, nY, 2]
											cNomCliFor	:= aArrayABC[nX, 9, nY, 3]
											nValorDet	:= aArrayABC[nX, 9, nY, 7]										
											cDesProd	:= FWGetSX5("FD",aArrayABC[nx,2])[1][4]
											cCodProd	:= Substr(aArrayABC[nX,2],1,20)
											nPercPrDet	:= Round(nValorDet/nCustoReal*100, 2)
											dDtNecDet   := IIf( !Empty(aArrayABC[nx, 9, ny, 5]), aArrayABC[nx, 9, ny, 5], CToD(""))

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf


												If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
												ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
													self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
												ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
													self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
												Else
													self:jItems[self:aStruct[nZ][1]] := cValue
												EndIf

											Next nZ
											self:processData(1)
											self:oData:appendData(self:jItems)

										// Despesas da Tarefa
										Case aArrayABC[nX, 9, nY, 1] == "AFB"

											cUnProd		:= ""
											cTipoDet	:= STR0009 //DSP TAREFA
											cCodTarDet	:= AFB->AFB_TAREFA
											cIteTarDet  := AFB->AFB_ITEM
											cDesTarDet	:= AFB->AFB_DESCRI
											nValorDet	:= aArrayABC[nX, 9, nY, 2]
											cDesProd	:= FWGetSX5("FD", aArrayABC[nx,2])[1][4]
											cCodProd	:= Substr(aArrayABC[nX,2],1,20)
											nPercPrDet	:= Round(nValorDet/nCustoPrj* 100, 2)
											dDtNecDet   := IIf( !Empty(AFB->AFB_DATPRF), AFB->AFB_DATPRF, CTod(""))

											//AFB_FILIAL+AFB_PROJET+AFB_REVISA+AFB_TAREFA+AFB_ITEM
											If AFB->(MsSeek(aArrayABC[nX, 9, nY, 3]))

												self:jItems := JsonObject():new()
												For nZ := 1 To Len(self:aStruct)

													If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
														cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
													Else
														cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
													Endif

													If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
														self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
													ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
														self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
													ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
														self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
													Else
														self:jItems[self:aStruct[nZ][1]] := cValue
													EndIf

												Next nZ
												self:processData(1)
												self:oData:appendData(self:jItems)

											EndIf
									EndCase
								Next
								nPercAcm  := nPercAcmB
								nPercAcm2 := nPercAcm2B
							EndIf

						Case cTipoReg == "MOV"
							cCodProd	:= ""
							cUnProd		:= ""
							dDataEmi	:= CToD("")
							cDesProd	:= STR0010 //"Movimentos bancarios"
							nSaldCusto	:= (aArrayABC[nX,4]-aArrayABC[nX,6])
							nPercAcm 	+= aArrayABC[nX,4]/nCustoPrj*100
							nPercAcm2 	+= aArrayABC[nX,6]/nCustoReal*100

							self:jItems := JsonObject():new()
							For nZ := 1 To Len(self:aStruct)

								If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
								Else
									cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
								EndIf

								If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
								ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
									self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
								ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
									self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
								Else
									self:jItems[self:aStruct[nZ][1]] := cValue
								EndIf

							Next nZ
							self:processData(1)
							self:oData:appendData(self:jItems)

							// impressao dos detalhes do apontamento
							If !Empty(aArrayABC[nX, 9])
								nQtdPrev 	:= 0
								nVlrPrev	:= 0
								nPerPrPrev	:= 0
								nQtdEmpRea	:= 0
								nQtdAtuRea	:= 0
								nVlrRea		:= 0
								nPercRea	:= 0
								nPercAcm	:= 0
								nPercAcm2	:= 0
								nQtdeRepo	:= 0
								nSaldCusto	:= 0

								For nY := 1 To Len(aArrayABC[nX, 9])
									Do Case
										// movimento bancario
										Case aArrayABC[nX, 9, nY, 1] == "SE5"
											cCodProd	:= ""
											cUnProd		:= ""
											cDesProd	:= STR0010 //Movimentos bancarios
											cTipoDet	:= STR0011 //MOV BANCARIO
											cAgencDet	:= aArrayABC[nX, 9, nY, 2]
											cContaDet	:= aArrayABC[nX, 9, nY, 3]
											nValorDet	:= aArrayABC[nX, 9, nY, 4]
											cNumTiDet	:= aArrayABC[nX, 9, nY, 5]
											dDataVen	:= aArrayABC[nX, 9, nY, 6]
											nPercPrDet	:= Round(nValorDet/nCustoReal*100, 2)
											cBancoDet	:= FatSeekTable("SA6", 1, PadR(SubStr((self:cAliasAF8)->AF8_FILIAL,1,4), TamSx3("A1_FILIAL")[1] ) + (self:cAliasAF8)->FK5_BANCO + (self:cAliasAF8)->FK5_AGENCI + (self:cAliasAF8)->FK5_CONTA, "A6_NOME", .T.)

											self:jItems := JsonObject():new()
											For nZ := 1 To Len(self:aStruct)

												If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
												Else
													cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
												EndIf

												If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
													self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
												ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
													self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
												ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
													self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
												Else
													self:jItems[self:aStruct[nZ][1]] := cValue
												EndIf

											Next nZ
											self:processData(1)
											self:oData:appendData(self:jItems)
									EndCase
								Next nY
								nPercAcm 	:= nPercAcmB
								nPercAcm2 	:= nPercAcm2B
							EndIf

						Case cTipoReg == "REC"
							AE8->(MsSeek((self:cAliasAF8)->AF8_FILIAL+aArrayABC[nX,2]))

							cUnProd		:= "HR"
							dDataEmi	:= CToD("")
							cCodProd	:= AE8->AE8_RECURS
							nPercAcm 	+= aArrayABC[nX,4]/nCustoPrj*100
							nPercAcm2 	+= aArrayABC[nX,6]/nCustoReal*100
							nQtdeRepo 	:= (aArrayABC[nX,3]-aArrayABC[nX,5])
							cDesProd	:= IIf(Empty(cObfNRecur),AE8->AE8_DESCRI,cObfNRecur)
							nSaldCusto	:= IIf(MV_PAR11 == 1, (aArrayABC[nX,10]*nQtdeRepo), (aArrayABC[nX,4]-aArrayABC[nX,6]))
							
							nValRepo := IIF(MV_PAR11 == 1, aArrayABC[nX,10], nValRepo)
							nTotRepo += IIF(MV_PAR11 == 1, nValRepo*nQtdeRepo, 0)

							self:jItems := JsonObject():new()
							For nZ := 1 To Len(self:aStruct)

								If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasAF8", self:aFieldsValue[nScan][2]))
								Else
									cValue := (self:cAliasAF8)->(FieldGet(FieldPos(self:aStruct[nZ][5])))
								EndIf

								If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]})) > 0
									self:jItems[self:aStruct[nZ][1]] := aPDFields[nScan][2]
								ElseIf lObfuscated .and. self:aStruct[nZ][1] == "NOMECLIFOR"
									self:jItems[self:aStruct[nZ][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(Len(SA1->A1_NOME)))
								ElseIf UPPER(self:aStruct[nZ][3]) == "DATE"
									self:jItems[self:aStruct[nZ][1]] := IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue))
								Else
									self:jItems[self:aStruct[nZ][1]] := cValue
								EndIf

							Next nZ
							self:processData(1)
							self:oData:appendData(self:jItems)
					EndCase
				Next nX
			Endif

			(self:cAliasAF8)->(dbSkip())

		Enddo

	Endif

	(self:cAliasAF8)->(dbCloseArea())

	aSize(aAuxHand,    	0)
	aSize(aArrayABC,	0)
	aSize(aItemInfo,	0)
	aSize(aPDFields,   	0)
	aSize(aPrepaAFA, 	0)
	aSize(aPrepaAFJ, 	0)
	aSize(aPrepaAFB, 	0)
	aSize(aPrepaAFN, 	0)
	aSize(aPrepaAFS, 	0)
	aSize(aPrepaAFI, 	0)
	aSize(aPrepaAFU, 	0)
	aSize(aPrepaAJC, 	0)
	aSize(aPrepaAFR, 	0)
	aSize(aValuesAfn, 	0)
	aSize(aValuesSd1, 	0)
	FwFreeArray(self:aFieldsValue)

Return self:oData
 
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 28/05/2025
	@version 12.1.2410
*/

Method getSchema() as object class expectedxaccomplishedSmartViewBusinessObject

	Local nX		 	:= 0  as numeric
	Local aStruCpoFake  := {} as array
	Local aParameters	:= {} as array

	self:aFields := {"AF8_FILIAL","AF8_PROJET","AF8_DESCRI","AF8_REVISA","TIPOREGISTRO","BM_GRUPO","BM_DESC","B1_COD","B1_DESC","B1_UM","AF9_TAREFA","AF9_DESCRI",;
					 "QTDPREVIST","VLRPREVIST","PERCPRJPREV","PERCACUPREV","QTDEMPREALIZ","QTDATUREALIZ","VLRREALIZAD","PERCPRJREALIZ","PERCACUREALIZ","SLDQUANT","SLDCUSTO",;
					 "TIPODETALHE","DTEMISSAO","CODCLIFOR","LJCLIFOR","NOMECLIFOR","AFN_DOC","AFN_SERIE","TIPO","AE8_RECURS","AE8_DESCRI","CODTAREFDET","ITEMTAREFDET",;
					 "DESCTAREFDET","QTDHRRECURSO","QUANTIDADE","DTNECESSDET","A6_NOME","FK5_AGENCI","FK5_CONTA","FK7_NUM","FK5_DTDISP","CUSTODET","VALORDET","PERCPRJDET"}

	//Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"TIPOREGISTRO",	STR0016, "string" ,STR0016,"string"},;
					 {"QTDPREVIST",   	STR0024, "number" ,STR0024,"number"},;
					 {"VLRPREVIST", 	STR0025, "number" ,STR0025,"number"},;
					 {"PERCPRJPREV", 	STR0026, "number" ,STR0026,"number"},;
					 {"PERCACUPREV", 	STR0027, "number" ,STR0027,"number"},;
					 {"QTDEMPREALIZ", 	STR0028, "number" ,STR0028,"number"},;
					 {"QTDATUREALIZ", 	STR0029, "number" ,STR0029,"number"},;
					 {"VLRREALIZAD", 	STR0030, "number" ,STR0030,"number"},;
					 {"PERCPRJREALIZ", 	STR0031, "number" ,STR0031,"number"},;
					 {"PERCACUREALIZ", 	STR0032, "number" ,STR0032,"number"},;
					 {"SLDQUANT", 		STR0033, "number" ,STR0033,"number"},;
					 {"SLDCUSTO", 		STR0034, "number" ,STR0034,"number"},;
					 {"TIPODETALHE", 	STR0035, "string" ,STR0035,"string"},;
					 {"DTEMISSAO", 		STR0036, "date"   ,STR0036,"date"  },;
					 {"CODCLIFOR", 		STR0037, "string" ,STR0037,"string"},;
					 {"LJCLIFOR", 		STR0038, "string" ,STR0038,"string"},;
					 {"NOMECLIFOR", 	STR0039, "string" ,STR0039,"string"},;
					 {"TIPO", 			STR0042, "string" ,STR0042,"string"},;
					 {"CODTAREFDET", 	STR0045, "string" ,STR0045,"string"},;
					 {"ITEMTAREFDET", 	STR0046, "string" ,STR0046,"string"},;
					 {"DESCTAREFDET", 	STR0047, "string" ,STR0047,"string"},;
					 {"QTDHRRECURSO", 	STR0048, "number" ,STR0048,"number"},;
					 {"QUANTIDADE", 	STR0049, "number" ,STR0049,"number"},;
					 {"DTNECESSDET", 	STR0050, "date"   ,STR0050,"date"  },;
					 {"CUSTODET", 		STR0056, "number" ,STR0056,"number"},;
					 {"VALORDET", 		STR0057, "number" ,STR0057,"number"},;
					 {"PERCPRJDET", 	STR0058, "number" ,STR0058,"number"};
					 }

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][2], self:aStruct[nX][5])
    Next nX

	//Repassa para a funcao quais os paramentros que nao serao demonstrados.
	aParameters := FtSVSetParam('PMR170',{'MV_PAR06','MV_PAR13','MV_PAR14','MV_PAR15'})
	
	// Adiciona novo parametro do tipo number
	self:oSchema:addParameter("MV_FIL"   , STR0059 + " ?" ,"string", .T.,,,,,, FTSVHelp(,,,.T.)) //Filial  

	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX  

	self:oSchema:addParameter("MV_PREVI" , STR0060 + " ?" ,"string", .F.,,,,,,STR0061) //Previsto
	self:oSchema:addParameter("MV_REALI" , STR0062 + " ?" ,"string", .F.,,,,,,STR0063) //Realizado

	If __lLookUp
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AF82", 2)
		self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR11", "api/framework/treports/integratedprovider/v1/options/PMR170/MV_PAR11", 1)
		self:setCustomURL("MV_PAR12", "api/framework/treports/integratedprovider/v1/options/PMR170/MV_PAR12", 1)
		self:setCustomURL("MV_PREVI", "api/framework/treports/integratedprovider/v1/options/PMR170/MV_PAR13", 1)
		self:setCustomURL("MV_REALI", "api/framework/treports/integratedprovider/v1/options/PMR170/MV_PAR14", 1)
		self:setCustomURL("MV_FIL"	, "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif

	FwFreeArray(aStruCpoFake)
	FwFreeArray(aParameters)

Return self:oSchema


/*{Protheus.doc} PmsSD1QuantSV
	Tratamento feito para quando tipo da NF for Complemento de preco/frete
	@return array: Array com as opçoes.
	@author Squad CRM/Faturamento
	@since 02/04/2024
	@version 1.0
*/
Static Function PmsSD1QuantSV(aValuesAfn, aValuesSd1)

	Local nRet        	:= 0  as numeric
	Local nQtdeProj   	:= 0  as numeric
	Local nQtdeTot    	:= 0  as numeric
	Local aNFfrete		:= {} as array

	Default	aValuesAfn  := {}
	Default	aValuesSd1 	:= {}

	If !Empty(aValuesAfn) .And. !Empty(aValuesSd1)
		IIf(aValuesSd1[1] == "C" .AND. aValuesSd1[2] == 0,;
				nRet := 1,;
					IIf(aValuesSd1[3] == "DP" .Or. aValuesSd1[3] == "FR",;
						(aNFfrete := aValuesAfn, QTDEFRETE(aNFfrete , @nQtdeProj, @nQtdeTot), nRet := nQtdeTot),;
						nRet := aValuesSd1[2] );
		)

	Endif

Return nRet

/*{Protheus.doc} PMSSV001CreateSQL
	@author Squad CRM/Faturamento
	@since 21/05/2025
	@version 12.1.2410
	@return Nil
*/
Method PMSSV001CreateSQL() Class expectedxaccomplishedSmartViewBusinessObject 
    
	Local cFieldsQry 	:= "" as character
    Local cQryAF8    	:= "" as character
    Local cNameTmp   	:= "" as character
    Local nParam     	:= 1  as numeric
    Local aFiliais	 	:= {} as array
	Local oQuery     	:= Nil as object
	Local oTempTableBranch := Nil as object
	
	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AF8", "AF8_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := " AF8.AF8_FILIAL,AF8.AF8_TRUNCA,AF8.AF8_REVISA,AF8.AF8_PROJET,AF8.AF8_TPCUS,AF8.AF8_DESCRI,AF8.AF8_CLIENT,AF8.AF8_LOJA,AF8.AF8_CALEND, AF8.AF8_DATA, "
	cFieldsQry += " AJE.AJE_PROJET,AJE.AJE_REVISA,AJE.AJE_TAREFA,AJE.AJE_ID,AJE.AJE_VALOR,AJE.R_E_C_N_O_ AS AJE_RECNO,SE5.E5_SITUACA,SE5.R_E_C_N_O_ AS SE5_RECNO, "
	cFieldsQry += " FK5.FK5_DTDISP,FK5.FK5_BANCO,FK5.FK5_CONTA,FK5.FK5_AGENCI,FK5.FK5_NUMCH,FK5.FK5_RECPAG,FK5.FK5_DATA,FK7.FK7_NUM "

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

	cQryAF8 := " SELECT ? "
	cQryAF8 += " ? " //Select que pode ser populado no objeto localizado)
	cQryAF8 += " FROM " + RetSqlName("AF8") + " AF8 "
	cQryAF8 += " INNER JOIN ? TMPFIL "
	cQryAF8 += " ON TMPFIL.TMP_FILIAL = AF8.AF8_FILIAL "
	cQryAF8 += " LEFT JOIN  " + RetSqlName("AJE") + " AJE ON ? "
	cQryAF8 += " AND AJE.AJE_PROJET = AF8.AF8_PROJET "
	cQryAF8 += " AND AJE.AJE_REVISA = AF8.AF8_REVISA "
	cQryAF8 += " AND AJE.D_E_L_E_T_ = ? "
	cQryAF8 += " LEFT JOIN " + RetSqlName("SE5") + " SE5 ON ? "
	cQryAF8 += " AND SE5.E5_PROJPMS = AJE.AJE_ID "
	cQryAF8 += " AND SE5.D_E_L_E_T_ = ? "
	cQryAF8 += " LEFT JOIN " + RetSqlName("FK5") + " FK5 ON ? "
	cQryAF8 += " AND FK5.FK5_IDMOV  = SE5.E5_IDORIG "
	cQryAF8 += " AND FK5.D_E_L_E_T_ = ? "
	cQryAF8 += " LEFT JOIN " + RetSqlName("FK7") + " FK7 ON ? "
	cQryAF8 += " AND FK7.FK7_IDDOC  = SE5.E5_IDORIG "
	cQryAF8 += " AND FK7.D_E_L_E_T_ = ? "
	cQryAF8 += " WHERE "
	cQryAF8 += " AF8.AF8_FILIAL = TMPFIL.TMP_FILIAL "
	cQryAF8 += " AND AF8.AF8_DATA BETWEEN ? AND ? "
	cQryAF8 += " AND AF8.AF8_PROJET BETWEEN ? AND ? "
	cQryAF8 += " AND AF8.D_E_L_E_T_ = ? "
	cQryAF8 += " ? " //Where que foi populado no objeto localizado
	cQryAF8 += " ORDER BY AF8.AF8_FILIAL, AF8.AF8_PROJET, AF8.AF8_DATA "

	oQuery := FwExecStatement():New(ChangeQuery(cQryAF8))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++,  self:cSelectLoc ) //Select que pode ser populado no objeto localizado.
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++,  FwJoinFilial("AEJ", "AF8"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SE5", "AJE"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("FK5", "SE5"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("FK7", "SE5"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetDate(nParam++, MV_PAR03)
	oQuery:SetDate(nParam++, MV_PAR04)
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, self:cWhereLoc) //Where que foi populado no objeto localizado.

	self:cAliasAF8 := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
