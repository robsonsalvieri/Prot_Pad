#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.pms.projectresourcenotes.ch"

Static __lLookUp	:= FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.pms.projectresourcenotes.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AFU,AF8,AF9,AE8,AED", name="Apontamentos de Recursos do Projeto", country="ALL", customTables="ALL")
/*/{Protheus.doc} projectresourcenotesSmartViewBusinessObject
    Classe para criação do Objeto de Negócio para Apontamentos de Recursos do Projeto
    @author Squad CRM/Faturamento
    @since 23/05/2023
    @version 12.1.2410
*/
Class projectresourcenotesSmartViewBusinessObject from IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object
    public method PMSSV002CreateSQL()
 
    protected data aStruct      as array
    protected data aFields  	as array
    protected data aFieldsValue as array
    protected data jItems       as json
    protected data cAlias		as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
 
Endclass

/*{Protheus.doc} new
    Método de instância da classe
    @return object: self
    @author Squad CRM/Faturamento
    @since 23/05/2023
    @version 12.1.2410
*/
Method new() class projectresourcenotesSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)        //"Gestão de Projetos"
    self:setDisplayName(STR0002)    //"Apontamentos de Recursos do Projeto"
    self:setDescription(STR0003)    //"Listagem de Apontamentos de Recursos do Projeto"
    self:setIsLookUp(.T.)  

    self:aStruct    := {}
    self:aFields    := {}
    self:aFieldsValue := {}

Return self

/*{Protheus.doc} getData
    Retorna os dados do objeto de negócio
    @param nPage, numérico, indica a página atual do relatório
    @param oFilter, objeto, contém o filtro do TReports
    @return object: self:oData
    @author Squad CRM/Faturamento
    @since 23/05/2023
    @version 12.1.2410
*/
method getData(nPage as numeric, oFilter as object) as object class projectresourcenotesSmartViewBusinessObject

    Local nX            := 0    as numeric
	Local nScan         := 0    as numeric
    Local nCusto        := 0    as numeric
    Local aPDFields		:= {}   as array
    Local lObfuscated	:= .F.  as logical
    Local xValue		:= ""   as variant
    Local cTotalHora    := ""   as character

    MV_FIL	 := ""

    FatSetValueMVPAR(oFilter:getParameters(),"PMR250")

    aPDFields	:= FatGetfieldsLGPD({"AE8_DESCRI"}, self:getCustomFields())
    lObfuscated	:= Len(aPDFields) > 0

    self:aFieldsValue := { ;
                            {"AFU_CUSTO1", {"AFU_CUSTO1", "", "nCusto"}},;
                            {"TOTALHORAS", {"TOTALHORAS", "", "cTotalHora"}};
                        }
            
	self:PMSSV002CreateSQL()

    dbSelectArea(self:cAlias)
    dbGoTop()

	While !(self:cAlias)->(Eof())

        nCusto := (self:cAlias)->D3_CUSTO1

        If Empty(nCusto)
            nCusto := (self:cAlias)->AFU_CUSTO1
        EndIf

        cTotalHora := SVPmr250FmtHours(IntToHora((self:cAlias)->AFU_HQUANT, 5))

        self:jItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
                xValue  := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]))
			Else
                xValue  := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nX][5])))
            EndIf

			If lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
            Else
                Do Case


                    Case UPPER(self:aStruct[nX][3]) == "DATE"
					    self:jItems[self:aStruct[nX][1]] := IIf( ValType(xValue) == "C", ;
															 totvs.framework.treports.date.stringToTimeStamp(xValue), ;
															 totvs.framework.treports.date.dateToTimeStamp(xValue))

					Case UPPER(self:aStruct[nX][3]) == "STRING"
						self:jItems[self:aStruct[nX][1]] := Alltrim(xValue)

					Otherwise
						self:jItems[self:aStruct[nX][1]] := xValue
                EndCase
            EndIf

        Next


        self:processData(1)
        self:oData:appendData(self:jItems)

		(self:cAlias)->(dbSkip())

	EndDo

	(self:cAlias)->(dbCloseArea())

    aSize(aPDFields,0)
    FwFreeArray(self:aFieldsValue)

Return self:oData

/*{Protheus.doc} getSchema
    Retorna a estrutura dos campos
    @return object: self:oSchema
    @author Squad CRM/Faturamento
    @since 18/05/2023
    @version 12.1.2410
*/
   
Method getSchema() as object class projectresourcenotesSmartViewBusinessObject

	Local nX			:= 0  as numeric
    Local aStruCpoFake  := {} as array
    Local aParameters	:= {} as array

    self:aFields := {"AFU_FILIAL","AFU_RECURS","AE8_DESCRI","AFU_DATA","AFU_PROJET","AF8_DESCRI","AFU_TAREFA",;
                    "AF9_DESCRI","AED_EQUIP","AED_DESCRI","AFU_HORAI","AFU_HORAF","AFU_HQUANT","AFU_CUSTO1","TOTALHORAS"}

    aStruCpoFake  := {{"TOTALHORAS", STR0004, "string", STR0004, "TOTALHORAS"}} 

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

    For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	self:oSchema:addParameter("MV_FIL",	STR0005 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) 

    aParameters := FtSVSetParam('PMR250', {'MV_PAR07'})    
    
    For nX := 1 To Len(aParameters)
        self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
    Next nX

    If __lLookUp
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2) //Projeto De
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AF82", 2) //Projeto Ate
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/AE8", 2)  //Recurso De
        self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/AE8", 2)  //Recurso Ate
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/AED", 2)  //Equipe De
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/AED", 2)  //Equipe Ate
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)  //Filial
    EndIf

    FwFreeArray(aParameters)
    FwFreeArray(aStruCpoFake)

Return self:oSchema

/*/{Protheus.doc} SVPmr250FmtHours
    Converte as horas em valor.
    @sample 	Pmr250FmtHours() 
    @return		horas, numerico, horas em valor.
    @author		SQUAD CRM / FAT
    @since		09/05/18   
    @version	P12.1.17 
/*/
Static Function SVPmr250FmtHours(cHours as character) as character

	Local nPos		:= 0 as numeric
	Local nHours	:= 0 as numeric
	Local cMM		:= "" as character

    IIf( Empty(cHours), cHours := "00:00", cHours )

	nPos := At(":",cHours)

	nHours := Val( SubStr(cHours,1,nPos-1) )
	cMM	   := Substr(cHours,nPos,Len(cHours))
	cHours := Str(nHours,nHours) + cMM

Return cHours

/*{Protheus.doc} PMSSV002CreateSQL
    Método responsável por montar a query SQL.
    @return character: Retorna a query SQL montada.
    @author Squad CRM/Faturamento
    @since 05/06/2025
    @version 12.1.2410
*/
Method PMSSV002CreateSQL() class projectresourcenotesSmartViewBusinessObject
    
    Local cQuery     := "" as character
    Local cFieldsQry := "" as character
    Local cNameTmp   := "" as character
    Local cOrder     := "" as character
    Local nParam     := 1  as numeric
    Local aFiliais 	 := {} as array
    
    Local oQuery := Nil as object
    Local oTempTableBranch := Nil as object

	aFiliais := FatGetMVFiliais(MV_FIL)

	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AFU", "AFU_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "AFU.AFU_FILIAL,AFU.AFU_DATA,AFU.AFU_PROJET,AFU.AFU_TAREFA,AFU.AFU_RECURS,AFU.AFU_CUSTO1,AFU.AFU_HORAI,"
    cFieldsQry += "AFU.AFU_HORAF,AFU.AFU_HQUANT,AFU.AFU_TPREAL,AFU.AFU_COD,AFU.AFU_LOCAL,AFU.AFU_NUMSEQ,"
    cFieldsQry += "AF8.AF8_DESCRI,AF9.AF9_DESCRI,AE8.AE8_DESCRI,AED.AED_EQUIP,AED.AED_DESCRI,SD3.D3_CUSTO1"
	cFieldsQry += self:cSelectLoc

    cOrder := "AFU.AFU_RECURS, AFU.AFU_DATA, AFU.AFU_HORAI, AFU.AFU_PROJET, AFU.AFU_REVISA, AFU.AFU_TAREFA"

	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

	cQuery := " SELECT ? "
	cQuery += " FROM " +RetSqlName("AFU") + " AFU "
    cQuery += " INNER JOIN ? TMPFIL " 
    cQuery += "	ON TMPFIL.TMP_FILIAL = AFU.AFU_FILIAL "  
    cQuery += " INNER JOIN " +RetSqlName("AF8") + " AF8 ON ? " 
    cQuery += "	AND AF8.AF8_REVISA = AFU.AFU_REVISA "    
    cQuery += "	AND AF8.AF8_PROJET = AFU.AFU_PROJET "    
    cQuery += "	AND AF8.D_E_L_E_T_ = ? "   
    cQuery += " INNER JOIN " +RetSqlName("AF9") + " AF9 ON ? " 
    cQuery += " AND AF9.AF9_PROJET = AFU.AFU_PROJET"    
    cQuery += " AND AF9.AF9_REVISA = AFU.AFU_REVISA"    
    cQuery += "	AND AF9.AF9_TAREFA = AFU.AFU_TAREFA "    
    cQuery += "	AND AF9.D_E_L_E_T_ = ? "   
    cQuery += " INNER JOIN " +RetSqlName("AE8") + " AE8 ON ? " 
    cQuery += "	AND AE8.AE8_RECURS = AFU.AFU_RECURS "
    cQuery += "	AND AE8.D_E_L_E_T_ = ? "   
    cQuery += " LEFT JOIN " +RetSqlName("AED") + " AED ON ? "   
    cQuery += " AND AED.AED_EQUIP  = AE8.AE8_EQUIP"  
    cQuery += " AND AED.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " +RetSqlName("SD3") + " SD3 ON ? "
	cQuery += " AND SD3.D3_COD = AFU.AFU_COD "
	cQuery += " AND SD3.D3_LOCAL = AFU.AFU_LOCAL "
	cQuery += " AND SD3.D3_NUMSEQ = AFU.AFU_NUMSEQ "
	cQuery += " AND SD3.D_E_L_E_T_ = ? "
    cQuery += " WHERE AFU.AFU_FILIAL = TMPFIL.TMP_FILIAL "   
    cQuery += " AND AFU.AFU_CTRRVS = ?"    
    cQuery += " AND AFU.AFU_PROJET BETWEEN ? AND ? "
    cQuery += " AND AE8.AE8_RECURS BETWEEN ? AND ? "  
    cQuery += " AND AFU.AFU_DATA BETWEEN ? AND ? " 
    cQuery += " AND AE8.AE8_EQUIP BETWEEN ? AND ? "
    cQuery += " AND AFU.D_E_L_E_T_ = ? "    
    cQuery += self:cWhereLoc 
	cQuery += "ORDER BY ? "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    oQuery:SetUnsafe(nParam++, cFieldsQry)
    oQuery:SetUnsafe(nParam++, cNameTmp)
    oQuery:SetUnsafe(nParam++, FwJoinFilial("AF8", "AFU"))
	oQuery:SetString(nParam++, ' ')	
    oQuery:SetUnsafe(nParam++, FwJoinFilial("AF9", "AFU"))
	oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("AE8", "AFU"))
	oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("AED", "AE8"))	
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SD3", "AFU"))	
    oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, '1')
    oQuery:SetString(nParam++, MV_PAR01)
    oQuery:SetString(nParam++, MV_PAR02)
    oQuery:SetString(nParam++, MV_PAR03)
    oQuery:SetString(nParam++, MV_PAR04)
    oQuery:SetDate(nParam++, MV_PAR05)
    oQuery:SetDate(nParam++, MV_PAR06)
    oQuery:SetString(nParam++, MV_PAR08)
    oQuery:SetString(nParam++, MV_PAR09)
    oQuery:SetString(nParam++, ' ')
    oQuery:SetUnsafe(nParam++, cOrder)

    self:cAlias := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)

    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
