#Include "FWLIBVERSION.CH"
#Include "totvs.ch"
#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-rest.th"
#Include "tlpp-core.th"
#Include "backoffice.sv.pms.financialchronogram.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.pms.financialchronogram.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AF8,AFE,AFC,AF9,SA1", name="Cronograma Financeiro Previsto x Realizado", country="ALL", customTables="All")

/*{Protheus.doc} financialchronogramSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Cotação por Projeto
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
Class financialchronogramSmartViewBusinessObject from Integratedprovider
    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method PMSSV008CreateSQL()
	public method QueryEDT()
 
    protected data aFields		 as array
    protected data aStruct		 as array
    protected data aFieldsValue	 as array
    protected data jItems		 as json
	protected data cAliasPrj 	 as character
	protected data cAliasEDT 	 as character
	protected data cFieldsQryEdt as character
	protected data cQueryEDT 	 as character
    protected data cSelectLoc1	 as character
	protected data cSelectLoc2	 as character	
	protected data cWhereLoc1	 as character
	protected data cWhereLoc2	 as character
	protected data lContinue	 as logical
	protected data dDataAuxIni	 as date

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
	@since 10/05/2023
	@version 12.1.2410
*/
method new() class financialchronogramSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001) 	 // "Gestão de Projetos"
    self:setDisplayName(STR0002) // "Cronograma Financeiro"
    self:setDescription(STR0003) // "Cronograma Financeiro Previsto x Realizado"
    self:setIsLookUp(.T.)

	self:lContinue := .T.
	self:cFieldsQryEdt := ""
	self:cQueryEDT := ""
	self:dDataAuxIni := CtoD("  /  /  ")

Return self

/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do TReports
	@return object: self:oData
	@author Squad CRM/Faturamento
	@since 13/06/2025
	@version 12.1.2410
*/
method getData(nPage as numeric, oFilter as object) as object class financialchronogramSmartViewBusinessObject

	Local cValue		:= "" as character
	Local cCodigo		:= "" as character
	Local cDescri		:= "" as character
	Local cProdRec		:= "" as character
	Local cCodigoEdt	:= "" as character

	Local nX            := 0 as numeric
	Local nScan         := 0 as numeric
	Local nY            := 0 as numeric
	Local nPerc			:= 0 as numeric
	Local nPerc2		:= 0 as numeric
	Local nValAnt2		:= 0 as numeric
	Local nValAnt		:= 0 as numeric
	Local nVal2PrjAnt	:= 0 as numeric
	Local nValPrjAnt	:= 0 as numeric
	Local nVal2Prj		:= 0 as numeric
	Local nValPrj		:= 0 as numeric	

	Local dAuxFim		:= CtoD("  /  /  ") as date
	Local dDx			:= CtoD("  /  /  ") as date
			
    Local lObfuscated   := .F. as logical
	Local lProjeto	   	:= .F. as logical
	Local lEDT  		:= .F. as logical
	Local lTarefa	    := .F. as logical

    Local aPDFields     := {} as array
	Local aValAnt 		:= {} as array
	Local aHandle 		:= {} as array
	Local aHandle2 		:= {} as array	
	Local aDados 		:= {} as array
	
	MV_FIL := ""
	MV_NIVELDE	:= "" 
	MV_NIVELATE := ""

	//Metodo para retorno do json dos parametros
	FatSetValueMVPAR(oFilter:GetParameters(),"PMR270") 

	//Array para processamento
    self:aFieldsValue    := {{"TB_CODIGO", 	{"TB_CODIGO",	"", 'cCodigo'}},;
							 {"TB_TIPO", 	{"TB_TIPO",  	"", 'cDescri'}},;
							 {"TB_PRODREC", {"TB_PRODREC",  "", "cProdRec"}},;
							 {"TB_DATA",	{"TB_DATA" ,  	"", 'dDx'}},;
							 {"TB_VALOR",	{"TB_VALOR",  	"", "IIF( cProdRec == 'R', nVal,  nVal2 )"}},;
							 {"TB_PERCENT",	{"TB_PERCENT",  "", "IIF( cProdRec == 'R', nPerc, nPerc2 )"}};
							}

	//Verifica se precisa fazer o tratamento para LGPD   
	aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
	lObfuscated	:= Len(aPDFields) > 0

	//Método que monta a Query Principal
	self:PMSSV008CreateSQL()					

	((self:cAliasPrj)->(dbGoTop()))

	While !(self:cAliasPrj)->(Eof())

		//Query vai trazer toda a estrutura com EDT e Tarefas
		self:QueryEDT()

		self:lContinue := .F.

		(self:cAliasEDT)->(dbGoTop())

		If !(self:cAliasEDT)->(Eof())

			dDx := self:dDataAuxIni
			dAuxFim := MV_PAR07
			aValAnt := {}

			While dDx <= dAuxFim
				//Volto para o iníco do Alias para executar novamente para um novo dia.
				((self:cAliasEDT)->(dbGoTop()))

				aHandle	:= PmsIniCRTE((self:cAliasEDT)->AF8_PROJET,(self:cAliasEDT)->AFE_REVISA,dDx)
				aHandle2:= PmsIniCOTP((self:cAliasEDT)->AF8_PROJET,(self:cAliasEDT)->AFE_REVISA,dDx)

				While !(self:cAliasEDT)->(Eof())

					lProjeto	:= .F.
					lEDT		:= .F.
					lTarefa		:= .F.
					aDados		:= {}

					If (self:cAliasEDT)->AF8_PROJET == Alltrim( (self:cAliasEDT)->AFC_EDT )
						lProjeto	:= .T.
					ElseIf !Empty( (self:cAliasEDT)->AFC_EDT )  .And. cCodigoEdt <> (self:cAliasEDT)->AFC_EDT
						cCodigoEdt	:= (self:cAliasEDT)->AFC_EDT
						lEDT		:= .T.
						lTarefa		:= IIF( !Empty( (self:cAliasEDT)->AF9_TAREFA ) , .T. , .F. )
					ElseIf !Empty( (self:cAliasEDT)->AF9_TAREFA )
						lTarefa		:= .T.
					Endif

					nValPrj	 := PmsRetCRTE(aHandle,2,(self:cAliasEDT)->AFC_EDT)[1]
					nVal2Prj := PmsRetCOTP(aHandle2,2,(self:cAliasEDT)->AFC_EDT)[1]

					If lProjeto .Or. lEDT
						If lProjeto	
							//Quando For por Saldo
							If dDx > self:dDataAuxIni .And. MV_PAR09 == 2  .And. (nScan := aScan(aValAnt,  {|x| x[1] == "PRJ" .And.  x[2] ==   (self:cAliasEDT)->AFC_EDT } ) )  > 0
								nValPrjAnt	 := aValAnt[nScan][3]
								nVal2PrjAnt	 := aValAnt[nScan][4]

								aValAnt[nScan][3] := nValPrj
								aValAnt[nScan][4] := nVal2Prj

								nValPrj	 := nValPrj  - nValPrjAnt 
								nVal2Prj := nVal2Prj - nVal2PrjAnt
																
							ElseIf 	MV_PAR09 == 2 .And. ( nVal2Prj > 0 .or. nValPrj > 0 )
								aadd( aValAnt,  { "PRJ" , (self:cAliasEDT)->AFC_EDT , nValPrj , nVal2Prj } )
							Endif
						else
							nVal := PmsRetCRTE(aHandle,2,(self:cAliasEDT)->AFC_EDT)[1]
							nVal2 := PmsRetCOTP(aHandle2,2,(self:cAliasEDT)->AFC_EDT)[1]

							If dDx > self:dDataAuxIni .And. MV_PAR09 == 2 .And. (nScan := aScan(aValAnt,  {|x| x[1] == "EDT" .And.  x[2] == (self:cAliasEDT)->AFC_EDT } ) )  > 0
								nValAnt	 := aValAnt[nScan][3]
								nValAnt2 := aValAnt[nScan][4]

								aValAnt[nScan][3] := nVal
								aValAnt[nScan][4] := nVal2

								nVal := nVal  - nValAnt
								nVal2 := nVal2 - nValAnt2							

							ElseIf 	MV_PAR09 == 2 .And. ( nVal2 > 0 .Or. nVal > 0 ) 
								aadd( aValAnt,  { "EDT" , (self:cAliasEDT)->AFC_EDT , nVal , nVal2 } )
							Endif
						Endif
						If lProjeto
							nPerc := Round( ( nValPrj/ nValPrj ) * 100 , 2 )
							nPerc2 := Round( ( nVal2Prj/ nVal2Prj ) * 100 , 2 )
							aadd( aDados,{nValPrj,nVal2Prj,(self:cAliasEDT)->AF8_PROJET, STR0012,"P",nPerc,nPerc2} ) //Projeto
							aadd( aDados,{nValPrj,nVal2Prj,(self:cAliasEDT)->AF8_PROJET, STR0012,'R',nPerc,nPerc2} ) //Projeto
						else
							nPerc := Round( ( nVal/ nValPrj ) * 100 , 2)
							nPerc2 := Round( ( nVal2/ nVal2Prj ) * 100 , 2)
							aadd( aDados,{nVal,nVal2,(self:cAliasEDT)->AFC_EDT,STR0013,"P",nPerc,nPerc2} ) //EDT
							aadd( aDados,{nVal,nVal2,(self:cAliasEDT)->AFC_EDT,STR0013,'R',nPerc,nPerc2} ) //EDT
						Endif
					Endif
					If lTarefa
						nVal  := PmsRetCRTE(aHandle,1,(self:cAliasEDT)->AF9_TAREFA)[1]
						nVal2 := PmsRetCOTP(aHandle2,1,(self:cAliasEDT)->AF9_TAREFA)[1]

						If dDx > self:dDataAuxIni .And. MV_PAR09 == 2 .And. (nScan := aScan(aValAnt,  {|x| x[1] == "TRF" .And.  x[2] == (self:cAliasEDT)->AF9_TAREFA } ) )  > 0
							nValAnt	:= aValAnt[nScan][3]
							nValAnt2 := aValAnt[nScan][4]

							aValAnt[nScan][3] :=  nValAnt
							aValAnt[nScan][4] :=  nVal2

							nVal := nVal  - nValAnt
							nVal2 := nVal2 - nValAnt2
							
						ElseIf 	MV_PAR09 == 2 .And. ( nVal2 > 0 .OR. nVal > 0 )
							aadd( aValAnt,  { "TRF" , (self:cAliasEDT)->AF9_TAREFA , nVal ,nVal2 } )
						Endif

						nPerc  := Round( ( nVal/ nValPrj ) * 100 , 2)
						nPerc2 := Round( ( nVal2/ nVal2Prj ) * 100 , 2)
						aadd( aDados,{nVal,nVal2,(self:cAliasEDT)->AF9_TAREFA,STR0014,"P",nPerc,nPerc2} ) //Tarefa
						aadd( aDados,{nVal,nVal2,(self:cAliasEDT)->AF9_TAREFA,STR0014,"R",nPerc,nPerc2} ) //Tarefa
					
					Endif

					For nY := 1 to Len(aDados)

						nVal		:= aDados[nY][1]
						nVal2		:= aDados[nY][2]
						cCodigo 	:= aDados[nY][3]
						cDescri 	:= aDados[nY][4]
						cProdRec	:= aDados[nY][5]
						nPerc		:= aDados[nY][6]
						nPerc2		:= aDados[nY][7]

						self:jItems := JsonObject():new()
						For nX := 1 To Len(self:aStruct)
							//Verifica se existe tratamento para valores do JSON
							If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
								cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasEDT", self:aFieldsValue[nScan][2]))
							Else
								cValue := (self:cAliasEDT)->(FieldGet(FieldPos(self:aStruct[nX][5])))
							Endif

							//Verificação para tratamento de LGPD e campos tipo Data antes inFormar no JSON
							If lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
								self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
							ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
								self:jItems[self:aStruct[nX][1]] := Iif(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp( cValue ), totvs.framework.treports.date.dateToTimeStamp( cValue) )
							Else
								self:jItems[self:aStruct[nX][1]] := cValue
							Endif 
						Next nX

						self:processData(1) //Tratamento para commit localizado
						self:oData:appendData(self:jItems)

					Next nY
				
					(self:cAliasEDT)->(dbSkip())

				Enddo

				Do Case
					Case MV_PAR08 == 1
						dDx++
					Case MV_PAR08==2
						dDx+= 7
					Case MV_PAR08==3
						dDx+= 35
						dDx := CTOD("01/"+StrZero(MONTH(dDx),2,0)+"/"+StrZero(YEAR(dDx),4,0))-1
				EndCase
			Enddo
			(self:cAliasEDT)->(DBCloseArea())

		Endif
		(self:cAliasPrj)->(dbSkip())

	Enddo

	(self:cAliasPrj)->(DBCloseArea())

	aSize(aPDFields,0)
	aSize(aValAnt,0)
	aSize(aHandle,0)
	aSize(aHandle2,0)
	aSize(aDados,0)
	FwFreeArray(self:aFieldsValue)

Return self:oData
 
/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
	@return object: self:oSchema
	@author Squad CRM/Faturamento
	@since 13/06/2025
	@version 12.1.2410
*/
Method getSchema() as object class financialchronogramSmartViewBusinessObject

	Local nX 		  := 0  as  numeric	
	Local aCposFake	  := {} as  array
	Local aParameters := {} as  array

	self:aFields := {'AF8_FILIAL','AF8_PROJET','AF8_DESCRI','AF8_CLIENT','AF8_LOJA','A1_NOME',;
					 'AFE_REVISA','AFE_DATAF','AFE_HORAF','TB_CODIGO','TB_TIPO','AFC_DESCRI',;
					 'TB_PRODREC','TB_DATA','TB_VALOR','TB_PERCENT'}
	
	aCposFake := {{"TB_CODIGO"	, STR0006 , "string", STR0006 , "TB_CODIGO"},; //EDT/TAREFA/Projeto
				  {"TB_TIPO"    , STR0007 , "string", STR0007 , "TB_TIPO"},;  //Descrição
				  {"TB_PRODREC" , STR0008 , "string", STR0008 , "TB_PRODREC"},; //P/R
				  {"TB_DATA"  	, STR0009 , "date"	, STR0009 , "TB_DATA"},; //Dia
				  {"TB_VALOR"  	, STR0010 , "number", STR0010 , "TB_VALOR"},; //Valor
				  {"TB_PERCENT" , STR0011 , "number", STR0011 , "TB_PERCENT"}; //Percentual
				  }

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake )

	// Cria a estrutura dos campos Padroes                                   
	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	//Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('PMR270',,, {'05','11'} )

	self:oSchema:addParameter("MV_FIL",   STR0017 + " ?" ,"string",  .T.,,,,,, FTSVHelp(,,,.T.)) //Filial   

    For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	If __lLookUp
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AF82", 2)
		self:setCustomURL("MV_PAR08", "api/framework/treports/integratedprovider/v1/options/PMR270/MV_PAR08", 1)
		self:setCustomURL("MV_PAR09", "api/framework/treports/integratedprovider/v1/options/PMR270/MV_PAR09", 1)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
	Endif
	
	self:oSchema:addParameter("MV_NIVELDE" , STR0004 + " ?" ,"string", .F.,,,,,,STR0018) ////Nível de
	self:oSchema:addParameter("MV_NIVELATE" , STR0005 + " ?" ,"string", .F.,,,,,,STR0019) //Nível até

	FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema


/*{Protheus.doc} PMSSV008CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 13/06/2025
	@version 12.1.2410
	@return Nil
*/
Method PMSSV008CreateSQL() Class financialchronogramSmartViewBusinessObject 
    
	Local cFieldsQry := "" as character
    Local cQuery     := "" as character
    Local cNameTmp   := "" as character
    Local nParam     := 1  as numeric
    Local aFiliais	 := {} as array
    Local oQuery     := Nil as object
	Local oTempTableBranch := Nil as object
	
	// Função que verifica se há filiais inFormadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AF8", "AF8_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "AF8_FILIAL,AF8_PROJET "
	
	cQuery := " SELECT ? "
	cQuery += " ? "
	cQuery += " FROM " + RetSqlName("AF8") + " AF8 "
	cQuery += " INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = AF8.AF8_FILIAL"	
	cQuery += " WHERE AF8.AF8_FILIAL = TMPFIL.TMP_FILIAL "

	If !Empty(MV_PAR03)						
		cQuery += " AND AF8.AF8_DATA >= ? "
	Endif

	If !Empty(MV_PAR04)						
		cQuery += " AND AF8.AF8_DATA <= ? "
	Endif

	If !Empty(MV_PAR01)						
		cQuery += " AND AF8.AF8_PROJET >= ? "
	Endif

	If !Empty(MV_PAR02)						
		cQuery += " AND AF8.AF8_PROJET <= ? "
	Endif

	cQuery += " ? " //Where que foi populado no objeto localizado
	cQuery += " AND AF8.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY AF8.AF8_PROJET "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, self:cSelectLoc1)
	oQuery:SetUnsafe(nParam++, cNameTmp )

	If !Empty(MV_PAR03)	
		oQuery:SetDate(nParam++, MV_PAR03) 
	Endif

	If !Empty(MV_PAR04)	
		oQuery:SetDate(nParam++, MV_PAR04) 
	Endif

	If !Empty(MV_PAR01)	
		oQuery:SetString(nParam++, MV_PAR01 )
	Endif

	If !Empty(MV_PAR02)	
		oQuery:SetString(nParam++, MV_PAR02 )
	Endif

	oQuery:SetUnsafe(nParam++, self:cWhereLoc1)//Where que foi populado no objeto localizado
	oQuery:SetString(nParam++, " " )
		
	self:cAliasPrj := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 
Return

/*{Protheus.doc} QueryEDT
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 13/06/2025
	@version 12.1.2410
	@return Nil
*/
Method QueryEDT() Class financialchronogramSmartViewBusinessObject 
    Local cQuery	 := "" AS character
	Local nParam     := 1  as numeric
	Local nX 		 := 1  as numeric
    Local oQuery     := Nil as object

	If self:lContinue
		For nX := 1 To Len(self:aFields)
			self:cFieldsQryEdt += IIF( SubStr(self:aFields[nX],1,3) $ 'TB_', "" , self:aFields[nX] + "," )
		Next nX

		self:cFieldsQryEdt += 'AF8_FILIAL,AF8_REVISA,AFE_PROJET,AF9_TAREFA,AFC_EDT,AFC_NIVEL,AFC_EDTPAI'
		self:cFieldsQryEdt += self:cSelectLoc2

		//Adiciona campos customizados (no array da estrutura, )
		FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @self:cFieldsQryEdt, self:getCustomFields(), .T., .F., .T.)

		//Query vai trazer toda a estrutura com EDT e Tarefas
		cQuery := " SELECT ? "
		cQuery += " FROM " + RetSqlName("AF8") + " AF8  "
		cQuery += " INNER JOIN " + RetSqlName("AFE") + " AFE ON ? "
		cQuery += " AND AFE.AFE_PROJET = AF8.AF8_PROJET "

		If !Empty(MV_PAR10)	
			cQuery += "AND AFE.AFE_REVISA = ? "
		Else
			cQuery += "AND AFE.AFE_REVISA = AF8.AF8_REVISA "	
		Endif

		cQuery += "AND AFE.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("AFC") + " AFC ON ? "
		cQuery += "AND AFC.AFC_PROJET = AF8.AF8_PROJET "
		cQuery += "AND AFC.AFC_REVISA = AF8.AF8_REVISA "

		If !Empty(MV_NIVELDE)
			cQuery += "AND AFC.AFC_NIVEL >= ? "	
		Endif

		If !Empty(MV_NIVELATE)
			cQuery += "AND AFC.AFC_NIVEL <= ? "	
		Endif

		cQuery += "AND AFC.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("AF9") + " AF9 ON ? "
		cQuery += "AND AF9.AF9_PROJET = AFC.AFC_PROJET "
		cQuery += "AND AF9.AF9_REVISA = AFC.AFC_REVISA "
		cQuery += "AND AF9.AF9_EDTPAI = AFC.AFC_EDT "

		If !Empty(MV_NIVELDE)
			cQuery += "AND AF9.AF9_NIVEL >= ? "
		Endif

		If !Empty(MV_NIVELATE)
			cQuery += "AND AF9.AF9_NIVEL <= ? "	
		Endif

		cQuery += "AND AF9.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
		cQuery += "AND SA1.A1_COD = AF8.AF8_CLIENT "
		cQuery += "AND SA1.A1_LOJA = AF8.AF8_LOJA "
		cQuery += "AND SA1.D_E_L_E_T_ = ? "

		cQuery += "WHERE AF8.AF8_FILIAL = ? "

		If !Empty(MV_PAR03)	
			cQuery += "AND AF8.AF8_DATA >= ? "
		Endif

		If !Empty(MV_PAR04)	
			cQuery += "AND AF8.AF8_DATA <= ? "
		Endif
		
		If !Empty(MV_PAR01)	
			cQuery += "AND AF8.AF8_PROJET >= ? "
		Endif

		If !Empty(MV_PAR02)	
			cQuery += "AND AF8.AF8_PROJET <= ? "
		Endif

		cQuery += " ? " //Where que foi populado no objeto localizado
		cQuery += "AND AF8.D_E_L_E_T_ = ? "
		cQuery += "ORDER BY AF8_PROJET, AFE_REVISA, AFC_NIVEL, AFC_EDTPAI, AFC_EDT, AF9_TAREFA"

		Do Case
			Case MV_PAR08 == 1
				self:dDataAuxIni := MV_PAR06
			Case MV_PAR08 == 2
				self:dDataAuxIni := MV_PAR06
				If DOW(self:dDataAuxIni)<>1
					self:dDataAuxIni -= DOW(self:dDataAuxIni)-1
				Endif
			Case MV_PAR08 == 3
				self:dDataAuxIni := CTOD("01/"+StrZero(MONTH(MV_PAR06),2,0)+"/"+StrZero(YEAR(MV_PAR06),4,0))-1
		EndCase

		self:cQueryEDT := cQuery
	Endif
		
		oQuery := FwExecStatement():New(ChangeQuery(self:cQueryEDT))
		oQuery:SetUnsafe(nParam++, self:cFieldsQryEdt)
		oQuery:SetUnsafe(nParam++, FwJoinFilial("AFE", "AF8")) 

		If !Empty(MV_PAR10)
			oQuery:SetString(nParam++, MV_PAR10 )		
		Endif

		oQuery:SetString(nParam++, " " )
		oQuery:SetUnsafe(nParam++, FwJoinFilial("AFC", "AF8")) 

		If !Empty(MV_NIVELDE)
			oQuery:SetString(nParam++, MV_NIVELDE )
		Endif

		If !Empty(MV_NIVELATE)
			oQuery:SetString(nParam++, MV_NIVELATE )
		Endif

		oQuery:SetString(nParam++, " " )
		oQuery:SetUnsafe(nParam++, FwJoinFilial("AF9", "AFC")) 

		If !Empty(MV_NIVELDE)
			oQuery:SetString(nParam++, MV_NIVELDE )
		Endif

		If !Empty(MV_NIVELATE)
			oQuery:SetString(nParam++, MV_NIVELATE )
		Endif

		oQuery:SetString(nParam++, " " )
		oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "AF8")) 
		oQuery:SetString(nParam++, " " )
		oQuery:SetString(nParam++, (self:cAliasPrj)->AF8_FILIAL )

		If !Empty(MV_PAR03)	
			oQuery:SetDate(nParam++, MV_PAR03) 
		Endif

		If !Empty(MV_PAR04)	
			oQuery:SetDate(nParam++, MV_PAR04) 
		Endif

		If !Empty(MV_PAR01)	
			oQuery:SetString(nParam++, (self:cAliasPrj)->AF8_PROJET )
		Endif

		If !Empty(MV_PAR02)
			oQuery:SetString(nParam++, (self:cAliasPrj)->AF8_PROJET )
		Endif

		oQuery:SetUnsafe(nParam++, self:cWhereLoc2) //Where que foi populado no objeto localizado
		oQuery:SetString(nParam++, " " )

		self:cAliasEDT := oQuery:OpenAlias()

		oQuery:Destroy()
		FreeObj(oQuery)
		oQuery:= Nil
Return
