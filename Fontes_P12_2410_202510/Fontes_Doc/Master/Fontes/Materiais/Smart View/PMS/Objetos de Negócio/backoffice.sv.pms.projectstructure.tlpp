#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "pmsicons.ch"
#include "backoffice.sv.pms.projectstructure.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.pms.projectstructure.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AF8,AFE,SA1,AFC,AF9,AFA", name="Relação de Projetos", country="ALL", customTables="AF8,AFE,SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} projectstructureSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Projetos

@author FAT/CRM
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class projectstructureSmartViewBusinessObject from IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method PMSSV003CreateSQL()
 
    protected data aFields		as array
    protected data aStruct		as array
	protected data cAlias		as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
	protected data jItems		as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class projectstructureSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0003) 		//"Gestão de Projetos"
    self:setDisplayName(STR0001) 	//"Relação de Projetos"
    self:setDescription(STR0002) 	//"Relação de Projetos - Estrutura do Projeto"
    self:setIsLookUp(.T.)

	self:aFields := {}
	self:aStruct := {}

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class projectstructureSmartViewBusinessObject

	Local xValue        := ""	as  character
	Local nX			:= 0	as numeric
	Local nY			:= 0	as numeric
	Local nScan         := 0	as numeric
	Local aPDFields     := {}	as array
	Local lImpCols		:= .T.	as logical
	Local lObfuscated   := .F.	as logical
	Private aGetAF9		:= {}	as array
	Private aGetAFC		:= {}	as array

	MV_FIL	 := ""

	FatSetValueMVPAR(oFilter:GetParameters(),"PMR060")

	aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
	lObfuscated	:= Len(aPDFields) > 0
	
	AF9->(dbSetOrder(2))

	If Empty(MV_PAR07)
    	MV_PAR07 := dDataBase       
	EndIf

	self:PMSSV003CreateSQL()

	dbSelectArea(self:cAlias)
	(self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		//Verifica as versoes a serem impressas
		//Se estiver em branco so imprime a ultima versao (AF8_REVISA)
		If !Empty(MV_PAR05) .And. !PmrPertence((self:cAlias)->AF8_REVISA, MV_PAR05)
			(self:cAlias)->(dbSkip())
			Loop
		EndIf

		lImpCols	:= .T.
		aGetAFC		:= {}
		aGetAF9		:= {}

		SVPmr060_AFC((self:cAlias)->AF8_FILIAL, (self:cAlias)->AF8_PROJET, (self:cAlias)->AF8_REVISA, (self:cAlias)->AF8_PROJET, @lImpCols)

		// Detalhes AFC
		For nX := 1 To Len(aGetAFC)
			self:jItems := JsonObject():New()

			For nY := 1 To Len(self:aStruct)

				If ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nY][5]} ) ) > 0 
					self:jItems[self:aStruct[nY][1]] := aPDFields[nScan][2]
				Else
					xValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nY][5])))
				EndIf

				If lObfuscated .and. ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nY][5]} ) ) > 0 
					self:jItems[self:aStruct[nY][1]] := aPDFields[nScan][2]
				Else
					Do Case
						Case self:aStruct[nY][3] == "date"
							self:jItems[self:aStruct[nY][1]] := totvs.framework.treports.date.stringToTimeStamp(xValue)

						Case self:aStruct[nY][5] == "AFE_HORAF"
							self:jItems[self:aStruct[nY][1]] := Alltrim(xValue) + IIf(!Empty(AllTrim(xValue)), "h", "")

						Case ( nScan := aScan(aGetAFC[nX], {|x| x[1] == self:aStruct[nY][1]} ) ) > 0
							self:jItems[aGetAFC[nX][nScan][1]] := aGetAFC[nX][nScan][2]

						Case UPPER(self:aStruct[nY][3]) == "STRING"
							self:jItems[self:aStruct[nY][1]] := Alltrim(xValue)

						Otherwise
							self:jItems[self:aStruct[nY][1]] := xValue
					EndCase
				EndIf
			Next nY

			self:processData(1)
			self:oData:appendData(self:jItems)
		Next nX


		// Detalhes AF9
		For nX := 1 To Len(aGetAF9)
			self:jItems := JsonObject():New()

			For nY := 1 To Len(self:aStruct)
			
				If ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nY][5]} ) ) > 0 
					self:jItems[self:aStruct[nY][1]] := aPDFields[nScan][2]
				Else
					xValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nY][5])))
				EndIf

				If lObfuscated .and. ( nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nY][5]} ) ) > 0 
					self:jItems[self:aStruct[nY][1]] := aPDFields[nScan][2]
				Else
					Do Case
						Case self:aStruct[nY][3] == "date"
							self:jItems[self:aStruct[nY][1]] := totvs.framework.treports.date.stringToTimeStamp(xValue)

						Case self:aStruct[nY][5] == "AFE_HORAF"
							self:jItems[self:aStruct[nY][1]] := Alltrim(xValue) + IIf(!Empty(AllTrim(xValue)), "h", "")

						Case ( nScan := aScan(aGetAF9[nX], {|x| x[1] == self:aStruct[nY][1]} ) ) > 0
							self:jItems[aGetAF9[nX][nScan][1]] := aGetAF9[nX][nScan][2]

						Case UPPER(self:aStruct[nY][3]) == "STRING"
							self:jItems[self:aStruct[nY][1]] := Alltrim(xValue)

						Otherwise
							self:jItems[self:aStruct[nY][1]] := xValue
					EndCase
				EndIf
			Next nY

			self:processData(1)
			self:oData:appendData(self:jItems)
		Next nX

		dbSelectArea(self:cAlias)
		(self:cAlias)->(dbSkip())

	EndDo

	(self:cAlias)->(dbCloseArea())

	aSize(aPDFields,0)
    aSize(aGetAF9,  0)
    aSize(aGetAFC,  0)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method getSchema() as object class projectstructureSmartViewBusinessObject

	Local nX			:= 0	as numeric
	Local aParameters	:= {}   as array
	Local aCposFake		:= {}	as array

	aCposFake := {  {"CTIPOPRINT",	 	STR0004, "string", STR0004, "CTIPOPRINT"	},;	//"Tipo"
					{"CCODPRINT", 	 	STR0005, "string", STR0005, "CCODPRINT"		},;	//"Código"
					{"CDESCPRINT", 	 	STR0006, "string", STR0006, "CDESCPRINT"	},;	//"Descrição"
					{"CUMPRINT", 	 	STR0007, "string", STR0007, "CUMPRINT"		},;	//"Unidade Medida"
					{"CHRQTDPRINT",  	STR0008, "string", STR0008, "CHRQTDPRINT"	},;	//"Quantidade Previsto"
					{"CSTARTPRINT",  	STR0009, "string", STR0009, "CSTARTPRINT"	},;	//"Dt.Inic. Previsto"
					{"CFINISHPRINT", 	STR0010, "string", STR0010, "CFINISHPRINT"	},;	//"Dt.Final Previsto"
					{"CDTATUIPRINT", 	STR0011, "string", STR0011, "CDTATUIPRINT"	},;	//"Dt.Inic. Realizado"
					{"CDTATUFPRINT", 	STR0012, "string", STR0012, "CDTATUFPRINT"	},;	//"Dt.Final Realizado"
					{"CHDURACPRINT", 	STR0013, "string", STR0013, "CHDURACPRINT"	},;	//"Duração Previsto"
					{"CHDURAC1PRINT", 	STR0014, "string", STR0014, "CHDURAC1PRINT"	},;	//"Duração Realizado"
					{"CHRQTD1PRINT", 	STR0015, "string", STR0015, "CHRQTD1PRINT"	},;	//"Prog. Previsto"
					{"CHRQTD2PRINT", 	STR0016, "string", STR0016, "CHRQTD2PRINT"	},;	//"Quantidade Realizado"
					{"CHRQTD3PRINT", 	STR0017, "string", STR0017, "CHRQTD3PRINT"	} }	//"Real. Realizado"

	self:aFields := {"AF8_FILIAL","AF8_PROJET","AF8_DESCRI","AF8_REVISA","AF8_CLIENT","AF8_LOJA","A1_NOME","AFE_DATAF","AFE_HORAF",;
					 "CTIPOPRINT","CCODPRINT","CDESCPRINT","CUMPRINT","CHRQTDPRINT","CSTARTPRINT","CFINISHPRINT",;
					 "CDTATUIPRINT","CDTATUFPRINT","CHDURACPRINT","CHDURAC1PRINT","CHRQTD1PRINT","CHRQTD2PRINT","CHRQTD3PRINT"}

	self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX
	
	self:oSchema:addParameter("MV_FIL", STR0018 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial 

    aParameters := FtSVSetParam('PMR060', {}, .T.)
    For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
    Next nX

    If __lLookUp
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2)
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AF82", 2)
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/AE8", 2)
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/AE8", 2)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) // Filial 
    EndIf

	FwFreeArray(aCposFake)
	FwFreeArray(aParameters)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVPmr060_AFC - SmartView
Busca dos detalhes AFC
 
@return Nil
 
@author FAT/CRM
@since 10/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Static Function SVPmr060_AFC(cFilAtual as character, cProjeto as character, cRevisa as character, cEDT as character, lImpCols as logical)

	Local nNode			:= 0				as numeric
	Local nPercAtu		:= 0				as numeric

	Local aArea			:= FwGetArea()		as array
	Local aAreaAFC		:= AFC->(FwGetArea())	as array
	Local aAreaAF9		:= AF9->(FwGetArea())	as array
	Local aNodes		:= {}				as array
	Local aGetCpoAFC	:= {}				as array

	DEFAULT lImpCols 	:= .T.

	AFC->(dbSetOrder(1))
	AFC->(MsSeek(cFilAtual+cProjeto+cRevisa+cEDT))
	cProjeto := AFC->AFC_PROJET
	cRevisa  := AFC->AFC_REVISA
	cEDT     := AFC->AFC_EDT

	If PmrPertence(AFC->AFC_NIVEL,MV_PAR06) .And. PmsChkUser(AFC->AFC_PROJET,,AFC->AFC_EDT,AFC->AFC_EDTPAI,1,"ESTRUT",cRevisa)
		nPercAtu := PmsPOCAFC(AFC->AFC_PROJET,cRevisa,AFC->AFC_EDT,MV_PAR07)

		aAdd(aGetCpoAFC,{"CTIPOPRINT",   "EDT"})
		aAdd(aGetCpoAFC,{"CCODPRINT",    AFC->AFC_EDT})
		aAdd(aGetCpoAFC,{"CDESCPRINT",   Repli(".",Val(AFC->AFC_NIVEL)-1)+Substr(AFC->AFC_DESCRI,1,TamSX3("AFC_DESCRI")[1]-Val(AFC->AFC_NIVEL)-1)})
		aAdd(aGetCpoAFC,{"CUMPRINT",     AFC->AFC_UM})
		aAdd(aGetCpoAFC,{"CHRQTDPRINT",  AllTrim(Transform(AFC->AFC_QUANT, PesqPict("AFC","AFC_QUANT")))})
		aAdd(aGetCpoAFC,{"CSTARTPRINT",  DtoC(AFC->AFC_START)})
		aAdd(aGetCpoAFC,{"CFINISHPRINT", DtoC(AFC->AFC_FINISH)})
		aAdd(aGetCpoAFC,{"CDTATUIPRINT", DtoC(AFC->AFC_DTATUI)})
		aAdd(aGetCpoAFC,{"CDTATUFPRINT", DtoC(AFC->AFC_DTATUF)})
		aAdd(aGetCpoAFC,{"CHDURACPRINT", Transform(AFC->AFC_HDURAC ,"99999.99h")})
		aAdd(aGetCpoAFC,{"CHDURAC1PRINT",Transform(IIf(!Empty(AFC->AFC_DTATUI) .And. !Empty(AFC->AFC_DTATUF),;
						 PmsHrsItvl(AFC->AFC_DTATUI,AFC->AFC_HRATUI,AFC->AFC_DTATUF,AFC->AFC_HRATUF,AFC->AFC_CALEND,AFC->AFC_PROJET),0.00),"99999.99h")})
		aAdd(aGetCpoAFC,{"CHRQTD1PRINT", Transform(PmsPrvAFC(AFC->AFC_PROJET,cRevisa,AFC->AFC_EDT,MV_PAR07)/AFC->AFC_HUTEIS*100,"999.99%")})
		aAdd(aGetCpoAFC,{"CHRQTD2PRINT", AllTrim(Transform(AFC->AFC_QUANT*nPercAtu/100, PesqPict("AFC","AFC_QUANT")))})
		aAdd(aGetCpoAFC,{"CHRQTD3PRINT", Transform(nPercAtu,"999.99%")})
		aAdd(aGetAFC,aGetCpoAFC)

	EndIf

	IIf( lImpCols, lImpCols := .F., lImpCols )

	AF9->(MsSeek(cFilAtual+cProjeto+cRevisa+cEDT))
	While !AF9->(Eof()) .And. cFilAtual+cProjeto+cRevisa+cEDT == cFilAtual+AF9->AF9_PROJET+AF9->AF9_REVISA+AF9->AF9_EDTPAI
		aAdd(aNodes, {PMS_TASK,;
					AF9->(Recno()),;
					IIf(Empty(AF9->AF9_ORDEM), "000", AF9->AF9_ORDEM),;
					AF9->AF9_TAREFA})

		AF9->(dbSkip())
	EndDo

	AFC->(dbSetOrder(2))
	AFC->(MsSeek(cFilAtual+cProjeto+cRevisa+cEDT))
	While !AFC->(Eof()) .And. cFilAtual+cProjeto+cRevisa+cEDT == cFilAtual+AFC->AFC_PROJET+AFC->AFC_REVISA+AFC->AFC_EDTPAI
		aAdd(aNodes, {PMS_WBS,;
					AFC->(Recno()),;
					IIf(Empty(AFC->AFC_ORDEM), "000", AFC->AFC_ORDEM),;
					AFC->AFC_EDT})

		AFC->(dbSkip())
	EndDo

	aSort(aNodes, , , {|x,y| x[3]+x[4] < y[3]+y[4]})

	For nNode := 1 To Len(aNodes)
		If aNodes[nNode][1] == PMS_TASK
			// tarefa
			AF9->(dbGoto(aNodes[nNode][2]))
			SVPmr060_AF9(cFilAtual, AF9->AF9_PROJET, AF9->AF9_REVISA, AF9->AF9_TAREFA, @lImpCols)	
		Else
			// EDT
			AFC->(dbGoto(aNodes[nNode][2]))
			SVPmr060_AFC(cFilAtual, AFC->AFC_PROJET, AFC->AFC_REVISA, AFC->AFC_EDT, @lImpCols)	
		EndIf
	Next

	FwRestArea(aAreaAF9)
	FwRestArea(aAreaAFC)
	FwRestArea(aArea)

	aSize(aAreaAF9, 0)
	aSize(aAreaAFC, 0)
	aSize(aArea,    0)

Return

//-------------------------------------------------------------------
/*{Protheus.doc} SVPmr060_AF9 - SmartView
Busca dos detalhes AF9
 
@return logical
 
@author FAT/CRM
@since 04/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVPmr060_AF9(cFilAtual as character, cProjeto as character, cRevisa as character, cTarefa as character, lImpCols as logical)

	Local nPercAtu		:= 0					as numeric

	Local aArea			:= FwGetArea()			as array
	Local aAreaAF9		:= AF9->(FwGetArea())	as array
	Local aGetCpoAF9	:= {}					as array

	DEFAULT lImpCols 	:= .T.

	If PmrPertence(AF9->AF9_NIVEL,MV_PAR06).And.PmsChkUser(AF9->AF9_PROJET,AF9->AF9_TAREFA,,AF9->AF9_EDTPAI,1,"ESTRUT",cRevisa)
		If SVPMR060Rec(cFilAtual)
			nPercAtu := PmsPOCAF9(AF9->AF9_PROJET,cRevisa,AF9->AF9_TAREFA,MV_PAR07)

			aAdd(aGetCpoAF9,{"CTIPOPRINT","TRF"})
			aAdd(aGetCpoAF9,{"CCODPRINT",AF9->AF9_TAREFA})
			aAdd(aGetCpoAF9,{"CDESCPRINT",Repli(".",Val(AF9->AF9_NIVEL)-1)+Substr(AF9->AF9_DESCRI,1,TamSX3("AF9_DESCRI")[1]-Val(AF9->AF9_NIVEL)-1)})
			aAdd(aGetCpoAF9,{"CUMPRINT",AF9->AF9_UM})
			aAdd(aGetCpoAF9,{"CHRQTDPRINT",AllTrim(Transform(AF9->AF9_QUANT, PesqPict("AFC","AFC_QUANT")))})
			aAdd(aGetCpoAF9,{"CSTARTPRINT",DtoC(AF9->AF9_START)})
			aAdd(aGetCpoAF9,{"CFINISHPRINT",DtoC(AF9->AF9_FINISH)})
			aAdd(aGetCpoAF9,{"CDTATUIPRINT",DtoC(AF9->AF9_DTATUI)})
			aAdd(aGetCpoAF9,{"CDTATUFPRINT",DtoC(AF9->AF9_DTATUF)})
			aAdd(aGetCpoAF9,{"CHDURACPRINT",Transform(AF9->AF9_HDURAC,"99999.99h")})
			aAdd(aGetCpoAF9,{"CHDURAC1PRINT",Transform(IIf(!Empty(AF9->AF9_DTATUI) .And.;
											 !Empty(AF9->AF9_DTATUF), If(AF9->(FieldPos("AF9_HRATUI"))>0, ;
											 PmsHrsItvl(AF9->AF9_DTATUI,AF9->AF9_HRATUI,AF9->AF9_DTATUF,AF9->AF9_HRATUF,AF9->AF9_CALEND,AF9->AF9_PROJET), ;
											 PmsHrsItvl(AF9->AF9_DTATUI,"00:00",AF9->AF9_DTATUF,"24:00",AF9->AF9_CALEND,AF9->AF9_PROJET)) ,0.00),"99999.99h")})
			aAdd(aGetCpoAF9,{"CHRQTD1PRINT",Transform(PmsPrvAF9(AF9->AF9_PROJET,cRevisa,AF9->AF9_TAREFA,MV_PAR07)/AF9->AF9_HUTEIS*100,"999.99%")})
			aAdd(aGetCpoAF9,{"CHRQTD2PRINT",AllTrim(Transform(AF9->AF9_QUANT*nPercAtu/100, PesqPict("AFC","AFC_QUANT")))})
			aAdd(aGetCpoAF9,{"CHRQTD3PRINT",Transform(nPercAtu,"999.99%")})
			aAdd(aGetAF9,aGetCpoAF9)

			IIf( lImpCols, lImpCols := .F., lImpCols)

		Endif
	EndIf

	FwRestArea(aAreaAF9)
	FwRestArea(aArea)

	aSize(aAreaAF9,0)
	aSize(aArea,0)

Return

//-------------------------------------------------------------------
/*{Protheus.doc} SVPMR060Rec - SmartView
Verifica o recurso alocado no projeto

@return logical

@author FAT/CRM
@since 04/06/2025
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVPMR060Rec(cFilAtual as character) as logical

	Local lRet := .F.					  as logical
	Local nLen := Len(AllTrim(MV_PAR09))  as numeric

	If !Empty(MV_PAR08) .Or. Upper(AllTrim(MV_PAR09)) <> Replicate("Z", nLen)
		//Verifica se o projeto usa composicao aux
		AFA->(MsSeek(cFilAtual+AF9->AF9_PROJET+AF9->AF9_REVISA+AF9->AF9_TAREFA+MV_PAR08))
		lRet := IIf( cFilAtual+AFA->AFA_PROJET+AFA->AFA_REVISA+AFA->AFA_TAREFA == cFilAtual+AF9->AF9_PROJET+AF9->AF9_REVISA+AF9->AF9_TAREFA,;
			 	IIf(AFA->AFA_RECURS >= MV_PAR08 .AND. AFA->AFA_RECURS <= MV_PAR09, .T., .F.), .F. )
	Else
		lRet :=	.T.   	
	Endif

Return lRet
//-------------------------------------------------------------------
/*{Protheus.doc} PMSSV003CreateSQL
	Retorna todas as querys montadas
	@author FAT / CRM
	@since 03/06/2025
	@version 1.0
	@return Nil
*/
//-------------------------------------------------------------------
Method PMSSV003CreateSQL() Class projectstructureSmartViewBusinessObject

	Local cQuery		:= ""	as Character
	Local cFieldsQry	:= ""	as character
	Local nParam        := 1	as numeric
	Local aFiliais 		:= {}  as array
	Local oQuery 	    := Nil	as object
	Local oTempTableBranch := Nil as object

	aFiliais := FatGetMVFiliais(MV_FIL)

	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AF8", "AF8_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "AF8.AF8_FILIAL,AF8.AF8_DESCRI,AF8.AF8_PROJET,AF8.AF8_REVISA,AF8.AF8_CLIENT,AF8.AF8_LOJA,SA1.A1_NOME,"
	cFieldsQry += "AFE.AFE_FILIAL,AFE.AFE_PROJET,AFE.AFE_REVISA,AFE.AFE_DATAF,AFE.AFE_HORAF"
	cFieldsQry += Self:cSelectLoc

	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	cQuery := "SELECT ? "
	cQuery += "FROM "       + RetSqlName("AF8") + " AF8 "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = AF8.AF8_FILIAL "
	cQuery += "INNER JOIN " + RetSqlName("AFE") + " AFE "
	cQuery += "	ON ? "
	cQuery += "	AND AFE.AFE_PROJET    = AF8.AF8_PROJET "

	If Empty(MV_PAR05)
		cQuery += "AND AFE.AFE_REVISA = AF8.AF8_REVISA "
	Else
		cQuery += "AND AFE.AFE_REVISA = ? "
	EndIf

	cQuery += "	AND AFE.D_E_L_E_T_    = ? "
	cQuery += "LEFT JOIN "  + RetSqlName("SA1") + " SA1 "
	cQuery += "	ON ?"
	cQuery += "	AND SA1.A1_COD        = AF8.AF8_CLIENT "
	cQuery += "	AND SA1.A1_LOJA       = AF8.AF8_LOJA "
	cQuery += "	AND SA1.D_E_L_E_T_    = ? "
	cQuery += "	WHERE AF8.AF8_FILIAL  = TMPFIL.TMP_FILIAL "
	cQuery += "	AND AF8.AF8_PROJET   BETWEEN ? AND  ? "
	cQuery += "	AND AF8.AF8_DATA     BETWEEN ? AND  ? "
	cQuery += "	AND AF8.D_E_L_E_T_    = ? "
	cQuery += self:cWhereLoc
	cQuery += "ORDER BY AF8.AF8_PROJET "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AFE", "AF8"))
	If !Empty(MV_PAR05)
		oQuery:SetString(nParam++, MV_PAR05)
	EndIf
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "AF8"))
	oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, MV_PAR01)
    oQuery:SetString(nParam++, MV_PAR02)
    oQuery:SetDate(nParam++, MV_PAR03)
    oQuery:SetDate(nParam++, MV_PAR04)
	oQuery:SetString(nParam++, ' ')

    self:cAlias := oQuery:OpenAlias()

	oQuery:Destroy()
	FreeObj(oQuery)
	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)

Return
