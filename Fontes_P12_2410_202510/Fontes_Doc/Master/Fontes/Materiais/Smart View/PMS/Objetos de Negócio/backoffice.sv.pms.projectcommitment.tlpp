#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.pms.projectcommitment.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.pms.projectcommitment.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AFJ,AF8,AF9,SB1,SA1", name="Relação de Produtos Empenhados", country="ALL", customTables="ALL")

/*/{Protheus.doc} projectcommitmentSmartViewBusinessObject
    Classe para criação do Objeto de Negócio para Relação de Produtos Empenhados em Projetos
    @author Squad CRM/Faturamento
    @since 04/06/2025
    @version 12.1.2410
*/
  
Class projectcommitmentSmartViewBusinessObject from IntegratedProvider

    public method new()       as object
    public method getData()   as object
    public method getSchema() as object
    public method PMSSV004CreateSQL()

    protected data jItems       as json
    protected data aFields      as array
    protected data aStruct      as array
    protected data aFieldsValue as array
    protected data cAlias       as character
    protected data cSelectLoc	as character
    protected data cWhereLoc    as character
    protected data cFilialAFJ   as character

Endclass

/*{Protheus.doc} new
    Método de instância da classe
    @author Squad CRM/Faturamento
    @since 04/06/2025
    @version 12.1.2410
*/
Method new() class projectcommitmentSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)     //Gestão de Projetos
    self:setDisplayName(STR0002) //Produtos Empenhados dos Projetos
    self:setDescription(STR0003) //Relatório dos Produtos Empenhados dos Projetos
    self:setIsLookUp(.T.)        //Define que terá consulta de LookUp

Return self

/*{Protheus.doc} getData
    Retorna os dados do objeto de negócio
    @param nPage, numérico, indica a página atual do relatório
    @param oFilter, objeto, contém o filtro do TReports
    @return object: self:oData
    @author Squad CRM/Faturamento
    @since 04/06/2025
    @version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object class projectcommitmentSmartViewBusinessObject

    Local cValue        := ""  as character
    Local cOrigem       := ""  as character
    Local cNumero       := ""  as character
    Local nX            := 0   as numeric
    Local nScan         := 0   as numeric
    Local nSaldoEmp     := 0   as numeric
    Local lObfuscated   := .F. as logical
    Local aPDFields     := {}  as array
    
    MV_FIL := ""
    MV_ORDER := 0  

    FatSetValueMVPAR(oFilter:GetParameters(),"PMR360") 

    self:aFieldsValue:= {{"ORIGEM",      {"ORIGEM",      "", 'cOrigem'}},;
                         {"NUMERO",      {"NUMERO",      "", 'cNumero'}},;
                         {"AFJ_EMPEST",  {"AFJ_EMPEST",  "", 'nSaldoEmp'}};
                         }

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields() )
    lObfuscated := len( aPDFields ) > 0
    
    //Método que monta a Query
	self:PMSSV004CreateSQL()

	(self:cAlias)->(DbGoTop())

    AFG->(dbSetOrder(4))
    AFM->(dbSetOrder(4))
    AJ7->(dbSetOrder(4))

    While !(self:cAlias)->(Eof())
        cNumero     := ""
        cOrigem     := ""
        nSaldoEmp   := 0

        If ( MV_PAR03 == 1 .And. (self:cAlias)->(AFJ_QEMP-(AFJ_QATU+AFJ_EMPEST)) <= 0 ) .Or.; //Empenhos em Aberto
		   ( MV_PAR03 == 3 .And. (self:cAlias)->(AFJ_QEMP-(AFJ_QATU+AFJ_EMPEST)) != 0 )       //Empenhos Fechados
		    (self:cAlias)->(dbSkip())
		    Loop
	    EndIf

    Do Case
        Case (self:cAlias)->AFJ_ROTGER == "1"       //Solic. Compra
            AFG->(MsSeek( (self:cAlias)->AFJ_FILIAL + (self:cAlias)->AFJ_PROJET + (self:cAlias)->AF8_REVISA + (self:cAlias)->AFJ_TAREFA + (self:cAlias)->AFJ_TRT ))
            cNumero := AFG->AFG_NUMSC
            cOrigem := STR0005
        Case (self:cAlias)->AFJ_ROTGER == "2"   //Ord. Prod.
            AFM->(MsSeek( (self:cAlias)->AFJ_FILIAL + (self:cAlias)->AFJ_PROJET + (self:cAlias)->AF8_REVISA + (self:cAlias)->AFJ_TAREFA + (self:cAlias)->AFJ_TRT ))
            cNumero := AFM->AFM_NUMOP
            cOrigem := STR0006
        Case (self:cAlias)->AFJ_ROTGER == "3"   //Ped. Compra
            AJ7->(MsSeek( (self:cAlias)->AFJ_FILIAL + (self:cAlias)->AFJ_PROJET + (self:cAlias)->AFJ_TAREFA + (self:cAlias)->AFJ_TRT ))
            cNumero := AJ7->AJ7_NUMPC
            cOrigem := STR0007
        Case (self:cAlias)->AFJ_ROTGER == "4"   //Planejamento
            cNumero := (self:cAlias)->AFJ_PLANEJ
            cOrigem := STR0008
    EndCase

        nSaldoEmp :=  (self:cAlias)->AFJ_QEMP - ((self:cAlias)->AFJ_QATU + (self:cAlias)->AFJ_EMPEST)

        self:jItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
                cValue := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]))
            Else
                cValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nX][5])))
            Endif

            If lObfuscated .and. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
                self:jItems[self:aStruct[nX][1]] := IIF(!Empty(cValue), aPDFields[nScan][2] , cValue )
            Else
                self:jItems[self:aStruct[nX][1]] := IIF(UPPER(self:aStruct[nX][3]) == "DATE",;
                    IIf(ValType(cValue) == "C", totvs.framework.treports.date.stringToTimeStamp(cValue), totvs.framework.treports.date.dateToTimeStamp(cValue)),;
                        cValue)
            Endif
        Next nX

        self:processData()
        self:oData:appendData(self:jItems)

		(self:cAlias)->(DbSkip())

    Enddo

	(self:cAlias)->(DbCloseArea())

    FwFreeArray(self:aFieldsValue)
    aSize(aPDFields,    0)

Return self:oData


/*{Protheus.doc} getSchema
    Retorna a estrutura dos campos
    @return object: self:oSchema
    @author Squad CRM/Faturamento
    @since 04/06/2025
    @version 12.1.2410
*/
Method getSchema() as object class projectcommitmentSmartViewBusinessObject

	Local nX := 0  as numeric
    Local aStruCpoFake := {} as array
    Local aParameters := {}	as array

    self:aFields := {"AFJ_FILIAL","AFJ_PROJET", "AF8_DESCRI", "AF8_CLIENT", "AF8_LOJA", "A1_NOME", "AF9_TAREFA", "AF9_DESCRI", "B1_COD", "B1_DESC", "AFJ_QEMP", "AFJ_QATU", "ORIGEM", "NUMERO", "AFJ_EMPEST"}

    aStruCpoFake := {{"ORIGEM", STR0009, "string", STR0009},; //Origem
                     {"NUMERO", STR0010, "string", STR0010}}  //Número

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

    For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][04], self:aStruct[nX][05])
    Next nX

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam("PMR360",{"MV_VAR02"})

    self:oSchema:addParameter("MV_FIL",   STR0013 + " ?" ,"string",  .T.,,,,,, FTSVHelp(,,,.T.)) //Filial   
    self:oSchema:addParameter("MV_ORDER", STR0004 + " ?" ,"number",  .F.,,,,,, FTSVHelp(,,.T.))	 //Ordem
    self:oSchema:addParameter("MV_PAR02", STR0002 + " ?" ,"string",  .F.,,,,,, FTSVHelp("PMR360", "02")) //Produto

    For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2)                    
        self:setCustomURL("MV_ORDER", "api/faturamento/smartview/v1/options/pms/projectcommitment/SVPMSPrEmp/1", 1)
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SB1", 2)
        self:setCustomURL("MV_PAR03", "api/framework/treports/integratedprovider/v1/options/PMR360/MV_PAR03", 1)
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
    EndIf

    FwFreeArray(aStruCpoFake)

Return self:oSchema

/*{Protheus.doc} SVPMSPrEmp
    Retorna as opçoes disponível para o parâmetro de ordem
    @return array: Array com as opçoes  
    @author Squad CRM/Faturamento
    @since 04/06/2025
    @version 12.1.2410
*/
Function SVPMSPrEmp(cOpcao) 
	Local aRet := {} as array

	If cOpcao == "1"
        aRet := { FatGetStrCpos({'AFJ_PROJET', 'AFJ_TAREFA', 'AFJ_COD'   }),; 
                  FatGetStrCpos({'AFJ_COD'   , 'AFJ_PROJET', 'AFJ_TAREFA'}) } 
	EndIf

Return aRet


/*{Protheus.doc} PMSSV004CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 04/06/2025
	@version 12.1.2410
	@return Nil
*/
Method PMSSV004CreateSQL() Class projectcommitmentSmartViewBusinessObject 
    
	Local cFieldsQry := "" as character
    Local cQuery     := "" as character
    Local cNameTmp   := "" as character
    Local nParam     := 1  as numeric
    Local aFiliais	 := {} as array
    Local aOrder	 := {}  as array
    Local oQuery     := Nil as object
	Local oTempTableBranch := Nil as object
	
	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AFJ", "AFJ_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()
    
    cFieldsQry := "AFJ_FILIAL, AFJ_PROJET, AF8_DESCRI, AF8_CLIENT, AF8_LOJA, A1_NOME, AF9_TAREFA, AF9_DESCRI, B1_COD, B1_DESC, AFJ_QEMP, "
    cFieldsQry += "AFJ_QATU, AFJ_ROTGER, AFJ_EMPEST, AFJ_PLANEJ, AF8_REVISA, AFJ_TAREFA, AFJ_TRT "

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({@self:aFieldsValue}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .F., .T.)

    // Define a ordenação com base no parâmetro MV_ORDER
	aOrder := 	{ " AFJ_PROJET, AFJ_TAREFA, AFJ_COD ",;   
				  " AFJ_COD, AFJ_PROJET, AFJ_TAREFA "}

	cQuery := " SELECT ? "
    cQuery += " ? " //Ajuste de SELECT para objeto localizado
	cQuery += " FROM " + RetSqlName("AFJ") + " AFJ "
    cQuery += " INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = AFJ.AFJ_FILIAL"	
	cQuery += " INNER JOIN " + RetSqlName("AF8") + " AF8 ON ? "
    cQuery += " AND AF8.AF8_PROJET = AFJ.AFJ_PROJET "
    cQuery += " AND AF8.D_E_L_E_T_ = ? "
    cQuery += " INNER JOIN " + RetSqlName("AF9") + " AF9 ON ? "
	cQuery += " AND AF9.AF9_PROJET = AFJ.AFJ_PROJET "
	cQuery += " AND AF9.AF9_REVISA = AF8.AF8_REVISA "
    cQuery += " AND AF9.AF9_TAREFA = AFJ.AFJ_TAREFA "
    cQuery += " AND AF9.D_E_L_E_T_ = ? "
    cQuery += " INNER JOIN " + RetSqlName("SB1") + " SB1 ON ? "
	cQuery += " AND SB1.B1_COD = AFJ.AFJ_COD "
    cQuery += " AND SB1.D_E_L_E_T_ = ? "
    cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
	cQuery += " AND SA1.A1_COD = AF8.AF8_CLIENT "
    cQuery += " AND SA1.A1_LOJA = AF8.AF8_LOJA "
    cQuery += " AND SA1.D_E_L_E_T_ = ? "
	cQuery += " WHERE AFJ.AFJ_FILIAL = TMPFIL.TMP_FILIAL "
    cQuery += " AND AFJ.D_E_L_E_T_ = ? "
    
    If !Empty(MV_PAR01) 
        cQuery += " AND AFJ.AFJ_PROJET IN( ? )" 
    Endif
        
    If !Empty(MV_PAR02)  
        cQuery += " AND AFJ.AFJ_COD IN( ? )"   
    Endif

    cQuery += " ? " //Where que foi populado no objeto localizado
    cQuery += " ORDER BY ? " 

    oQuery := FwExecStatement():New(ChangeQuery(cQuery) )
	oQuery:SetUnsafe(nParam++ , cFieldsQry )
    oQuery:SetUnsafe(nParam++ , self:cSelectLoc )
    oQuery:SetUnsafe(nParam++ , cNameTmp)
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AF8", "AFJ"))
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("AF9", "AFJ"))
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SB1", "AFJ"))
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA1", "AF8"))
    oQuery:SetString(nParam++ , ' ')
    oQuery:SetString(nParam++ , ' ')

    If !Empty(MV_PAR01)
        oQuery:setIn(nParam++, StrTokArr2( MV_PAR01, ';' , .F.) )
    EndIf

    If !Empty(MV_PAR02)
        oQuery:setIn(nParam++, StrTokArr2( MV_PAR02, ';' , .F.) )
    EndIf

    oQuery:SetUnsafe(nParam++ ,self:cWhereLoc) //Adição de condições para objeto localizado no where
    oQuery:SetUnsafe(nParam++, IIf(MV_ORDER > 0, aOrder[MV_ORDER], aOrder[1]))

	self:cAlias := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)
    
    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 
	aSize(aOrder,0) 

Return
