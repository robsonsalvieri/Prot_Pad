#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.pms.listforquotationperproject.ch"
#include "pmsicons.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.pms.listforquotationperproject.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AF8,AF9,AFA,SB1,AE8", name="Lista para Cotação por Projeto", country="ALL", customTables="AF8,AF9,AFA")
//-------------------------------------------------------------------
/*/{Protheus.doc} listforquotationperprojectSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Cotação por Projeto

@author Squad CRM/Faturamento
@since 24/06/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------  
class listforquotationperprojectSmartViewBusinessObject from Integratedprovider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method PMSSV006CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
	protected data aFieldsValue	as array
	protected data cAlias		as character
    protected data jItems		as json
    protected data cSelectLoc	as character
	protected data cWhereLoc	as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Squad CRM/Faturamento
@since 10/05/2023
@version 12.1.2410
*/
//-------------------------------------------------------------------   
method new() class listforquotationperprojectSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001) 	 // "Gestão de Projetos"
    self:setDisplayName(STR0002) // "Cotação por Projeto"
    self:setDescription(STR0003) // "Relação para Cotação por Projeto"
    self:setIsLookUp(.T.)

	self:cSelectLoc 	:= ""
	self:cWhereLoc		:= ""
	self:aFieldsValue 	:= {}

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Squad CRM/Faturamento
@since 10/05/2023
@version 12.1.2410
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class listforquotationperprojectSmartViewBusinessObject
 
	Local xValue		:= ""   as variant	
	Local nX            := 0    as numeric
	Local nScan         := 0    as numeric				
    Local lObfuscated   := .F.  as logical
    Local aPDFields     := {}   as array	
	
	MV_NIVELDE	:= ""
	MV_NIVELATE := "" 
	MV_FIL	 	:= ""
			
    FatSetValueMVPAR(oFilter:GetParameters(),"PMR210")

	aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated	:= Len(aPDFields) > 0

    self:aFieldsValue := {  {"TB_TIPPRO", {"TB_TIPPRO"  , "", '"(" + IIF( !Empty((self:cAlias)->AFA_PRODUT), "P", "R" ) + ")" '}},;						 
                      	 	{"TB_QUANT1", {"TB_QUANT1"  , "", '((self:cAlias)->AFA_CUSTD*PmsAFAQuant((self:cAlias)->AFA_PROJET,(self:cAlias)->AFA_REVISA,(self:cAlias)->AFA_TAREFA,(self:cAlias)->AFA_PRODUT,(self:cAlias)->AF9_QUANT,(self:cAlias)->AFA_QUANT,(self:cAlias)->AF9_HDURAC)) / PmsAFAQuant((self:cAlias)->AFA_PROJET,(self:cAlias)->AFA_REVISA,(self:cAlias)->AFA_TAREFA,(self:cAlias)->AFA_PRODUT,(self:cAlias)->AF9_QUANT,(self:cAlias)->AFA_QUANT,(self:cAlias)->AF9_HDURAC)'}}}
		
	self:PMSSV006CreateSQL()

    (self:cAlias)->(dbGoTop())
	
	While !(self:cAlias)->(Eof())		
	
		self:jItems := JsonObject():new()
		For nX := 1 To Len(self:aStruct)
			
			If (nScan := aScan(self:aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } ) ) > 0
				xValue  := &(FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2]))
			Else
				xValue := (self:cAlias)->(FieldGet(FieldPos(self:aStruct[nX][5])))
			EndIf

			Do case
				Case lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0 
					self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]	

				Case UPPER(self:aStruct[nX][3]) == "DATE"
					IIf(ValType(xValue) == "C",;
						self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp(xValue),;
							self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(xValue))

				Case UPPER(self:aStruct[nX][3]) == "STRING"
					self:jItems[self:aStruct[nX][1]] := Alltrim(xValue)
				
				Otherwise
					self:jItems[self:aStruct[nX][1]] := xValue
			EndCase

		Next nX

		self:processData(1)
		self:oData:appendData(self:jItems)
	
		(self:cAlias)->(dbSkip())

	EndDo

	(self:cAlias)->(DBCloseArea())

	aSize(aPDFields,0)
	aSize(self:aFieldsValue,0)

return self:oData
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Squad CRM/Faturamento
@since 10/05/2023
@version 12.1.2410
*/
//-------------------------------------------------------------------   
method getSchema() as object class listforquotationperprojectSmartViewBusinessObject

	Local nX 			:= 0  as numeric	
	Local aParameters	:= {} as array
	Local aCposFake		:= {} as array

	self:aFields := {'AF8_FILIAL','AF8_PROJET', 'AF8_DESCRI','AF9_TAREFA','AF9_DESCRI','AFA_PRODUT',;
					 'AFA_RECURS','TB_TIPPRO','B1_DESC','AFA_QUANT','TB_QUANT1','AE8_DESCRI'}
	
	aCposFake := {{"TB_TIPPRO" , STR0006 ,"string" ,STR0006 ,"TB_TIPPRO"},; 
				  {"TB_QUANT1" , STR0007 ,"number" ,STR0007 ,"TB_QUANT1"}} 

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake )

	For nX := 1 To Len(self:aStruct)						
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX
	
	aParameters := FtSVSetParam('PMR210', {'MV_PAR03','MV_PAR09'} )	

	self:oSchema:addParameter("MV_FIL", STR0008 + " ?" , "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial    

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])                                     
	Next Nx	

	If __lLookUp
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/AF82", 2) 	//Projeto De
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/AF82", 2) 	//Projeto Ate
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SB1", 2) 	//Produto De
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SB1", 2) 	//Produto Ate
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SBM", 2) 	//Grupo de Produto Dee
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SBM", 2)		//Grupo de Produto Ate
		self:setCustomURL("MV_PAR08", "api/framework/treports/integratedprovider/v1/options/PMR210/mv_par08", 1) //Imprimir ?
		self:setCustomURL("MV_PAR10", "api/framework/treports/integratedprovider/v1/options/PMR210/mv_par10", 1) //Imprime Recurso sem prod. ?
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) 	//Filial
	EndIf

	self:oSchema:addParameter("MV_NIVELDE" , STR0004 ,"string", .F.,,,,,, STR0009) //"Nível de:"
    self:oSchema:addParameter("MV_NIVELATE", STR0005 ,"string", .F.,,,,,, STR0010) //"Nível até:"
	
	FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

return self:oSchema

//------------------------------------------------------------------
/*{Protheus.doc} PMSSV006CreateSQL
    Método responsável por montar a query SQL.

@return character: Retorna a query SQL montada.
 
@author Squad CRM/Faturamento
@since 13/06/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------
Method PMSSV006CreateSQL() class listforquotationperprojectSmartViewBusinessObject

	Local cQuery        := ""   as character
	Local cFieldsQry	:= ""	as character
	Local cNameTmp 		:= ""  as character
	Local nParam        := 1    as numeric
	Local nX            := 0    as numeric
	Local aFiliais 		:= {}  as array
	Local oQuery 		:= Nil  as object
	Local oTempTableBranch := Nil as object

	aFiliais := FatGetMVFiliais(MV_FIL)

	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AF8", "AF8_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	For nx := 1 To Len(self:aFields)
		cFieldsQry += IIF(SubStr(self:aFields[nx],1,3) $ 'AE8|TB_', "", self:aFields[nx] + "," ) 
	Next nx	

	cFieldsQry += 'B1_GRUPO, AF9_NIVEL, AFA_PROJET, AFA_REVISA, AFA_TAREFA, AF9_QUANT, AF9_HDURAC, AFA_CUSTD, AE8_DESCRI, AF8_FILIAL'
	cFieldsQry += self:cSelectLoc

	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := " SELECT ? "
	cQuery += " FROM " + RetSqlName("AF8") + " AF8 "
	cQuery += " INNER JOIN ? TMPFIL "
	cQuery += " 	ON TMPFIL.TMP_FILIAL = AF8.AF8_FILIAL "
	cQuery += " INNER JOIN " + RetSqlName("AFA") + " AFA "
	cQuery += "		ON ? "
	cQuery += "		AND AFA_PROJET 	= AF8_PROJET " 
	cQuery += "		AND AFA_REVISA 	= AF8_REVISA "
	cQuery += "		AND AFA_PRODUT 	BETWEEN ? AND ? "

	If MV_PAR10 == 2 //Imprime Recurso sem prod. ?
		cQuery += " AND AFA.AFA_PRODUT <> ? " 
	EndIf

	If MV_PAR08 == 2 //Imprimir ?                    
		cQuery += " AND AFA.AFA_CUSTD > ? "
	ElseIf MV_PAR08 == 3
		cQuery += " AND AFA.AFA_CUSTD = ? "
	Endif

	cQuery += "		AND AFA.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN "  + RetSqlName("AF9") + " AF9 "
	cQuery += "		ON ? "
	cQuery += "		AND AF9_PROJET = AFA_PROJET "
	cQuery += "		AND AF9_REVISA = AFA_REVISA "
	cQuery += "		AND AF9_TAREFA = AFA_TAREFA "

	If !Empty(MV_NIVELDE) .Or. !Empty(MV_NIVELATE)
		cQuery += " AND AF9_NIVEL BETWEEN ? AND ? " // Nivel de/Ate
	EndIf

	cQuery += "		AND AF9.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("SB1") + " SB1 "
	cQuery += "		ON ? "
	cQuery += "		AND B1_COD 		= AFA_PRODUT "
	cQuery += "		AND B1_GRUPO	BETWEEN ? AND ? "
	cQuery += "		AND SB1.D_E_L_E_T_ = ? "
	cQuery += " LEFT JOIN " + RetSqlName("AE8") + " AE8 " 
	cQuery += " 	ON ? "
	cQuery += " 	AND AE8_RECURS	= AFA_RECURS "
	cQuery += " 	AND AE8.D_E_L_E_T_	= ? "
	cQuery += " WHERE AF8_FILIAL 	= TMPFIL.TMP_FILIAL "
	cQuery += "		AND AF8_PROJET 	BETWEEN ? AND ? "
	cQuery += " 	? " //Where que foi populado no objeto localizado
	cQuery += " 	AND AF8.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY AF8.AF8_FILIAL, AF8.AF8_PROJET, AFA.AFA_TAREFA, AFA.AFA_PRODUT "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))
	
	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AFA", "AF8"))
	oQuery:SetString(nParam++, MV_PAR04) //Produto de
	oQuery:SetString(nParam++, MV_PAR05) //Produto até

	If MV_PAR10 == 2
		oQuery:SetString(nParam++, ' ')	//Imprime Recurso sem prod. ?
	EndIf 
	If MV_PAR08 == 2 .Or. MV_PAR08 == 3
		oQuery:SetString(nParam++, '0')  //Imprimir ?   
	EndIf

	oQuery:SetString(nParam++, ' ') 				
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AF9", "AFA"))

	If !Empty(MV_NIVELDE) .Or. !Empty(MV_NIVELATE)
		oQuery:SetString(nParam++, MV_NIVELDE)  // Nivel de
		oQuery:SetString(nParam++, MV_NIVELATE) // Nivel Até
	EndIf

	oQuery:SetString(nParam++, ' ') 					
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "AFA"))
	oQuery:SetString(nParam++, MV_PAR06) //Grupo de Produto de
	oQuery:SetString(nParam++, MV_PAR07) //Grupo de Produto Até
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AE8", "AFA"))
	oQuery:SetString(nParam++, ' ')					
	oQuery:SetString(nParam++, MV_PAR01) //Projeto de
	oQuery:SetString(nParam++, MV_PAR02) //Projeto Até
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)	
	oQuery:SetString(nParam++, ' ')	

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)
    oQuery:Destroy()
    FreeObj(oQuery)
    oQuery:= Nil

	aSize(aFiliais, 0)
	
Return
