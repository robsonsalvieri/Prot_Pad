#INCLUDE "TECXFUN.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "JPEG.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"
#INCLUDE 'FWMVCDEF.CH'
#DEFINE  CID_METRIC "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total"

Static __Cor 		:= NIl
Static aSX5Tab63 	:= NIL   //array multidimensional com 3 elementos cada linha: dia , mes , ano
Static lSabFeri  	:= NIL   //leitura parametro MV_SABFERI
Static lDomFeri 	:= NIL   //leitura parametro MV_DOMFERI

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECXFUN   ³ Autor ³ Eduardo Riera         ³ Data ³ 07.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Funcoes utilizadas no SigaTec                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT400GRAVA  ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava os Orcamentos                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void At400Grava(nExpN1,cExpC1)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nExpN1 : 1 - Inclusao / 2 - Alteracao / 3 - Excluir        ³±±
±±³          ³ nExpA1 : Numero do Chamado                                 ³±±
±±³          ³ nExpA2 : Numero do Atendimento                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400Grava(nOpcao,aChamado,aAtend)

Local aAreaAb1 := AB1->(GetArea())		//Guarda a area do arq. AB1
Local aAreaAb2 := AB2->(GetArea())		//Guarda a area do arq. AB2
Local aAreaAb3 := AB3->(GetArea())		//Guarda a area do arq. AB3
Local aAreaAb4 := AB4->(GetArea())		//Guarda a area do arq. AB4
Local aAreaAb5 := AB5->(GetArea())		//Guarda a area do arq. AB5
Local aArea    := GetArea()				//Guarda a area atual
Local aOs      := {}					//Array de Ordem de servico
Local aVazioAB8:= {} 					//Array vazio com estrutura do AB8
Local aAux     := {}					//Array auxiliar usado na copia p/ o aCols
Local bCampo   := {|x| FieldName(x) }	//Codeblock que retorna o nome do campo
Local cNumOS   := ""					//Nr. da OS
Local cTipoAnt := "0"					//Tipo do Item do orcamento
Local lGravou  := .F.					//Indica se gravou item do aCols
Local lStatus  := .T.					//Controla status do orcamento
Local lFoundAB3:= .F. 					//Indica que encontrou registro no AB3
Local nCntFor  := 0						//Usada em lacos For...Next
Local nCntFor2 := 0						//Usada em lacos For...Next
Local nCntFor3 := 0						//Usada em lacos For...Next
Local nPIteAB4 := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_ITEM"})	//Posicao do campo AB4_ITEM no aHeaderAB4
Local nPPrdAB4 := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_CODPRO"})	//Posicao do campo AB4_CODPRO no aHeaderAB4
Local nPMemoAB4:= aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_MEMO2"})	//Posicao do campo AB4_MEMO2 no aHeaderAB4
Local nPPrdAB5 := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODPRO"})	//Posicao do campo AB5_CODPRO no aHeaderAB5
Local nPIteAB5 := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})	//Posicao do campo AB5_SUBITE no aHeaderAB5
Local nPosMemo := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_MEMO2"})	//Posição do campo AB4_MEMO2 no aHeaderAB4.
Local nPosAhead:= 0														//Posicao encontrada em aHeader
Local nUsadoAB4:= Len(aHeaderAB4)		//Nr. de campo usados do AB4
Local nUsadoAB5:= Len(aHeaderAB5)		//Nr. de campo usados do AB5
Local nPosCampo:= 0						//Posicao do campo pesquisado
Local nOpcAB4  := 0  					//Opcao do AB4 (1=inclusao,2=alteracao)
Local nOpcOs   := 0						//Opcao da OS (1=inclusao,2=alteracao)
Local nRecnoAB3:= 0 					//Posicao do registro no AB3
Local uCampo   := ""					//Usada na criacao de variav. de memoria
Local cCampo   := ""					//Nome do campo
Local cParcela := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"	//Possiveis parcelas
Local cMv1Dup  := SuperGetMV( "MV_1DUP" )					//Define 1a parcela do titulo gerado
Local nParcelas:= SuperGetMV("MV_NUMPARC")					//Nr. maximo de parcelas
Local nMaxTipo9:= 26										//Nr. maximo de parcelas tipo 9
Local nPos1    := 0											//Posicao do campo no arquivo (FieldPos)
Local nValAB5  := 0											//Valor usado p/ conversao com xMoeda()
Local nMoeda   := 0											//Moeda a ser gravada no orcamento
Local nDecs	   := 0											//Nr. de casas decimais da Moeda
Local lAltTpOS := .T.										//Define se o tipo da ordem de serviço será alterado
Local cTipoOS  := ""										//Armazena o tipo da ordem de serviço

Private INCLUI       := ( nOpcao == 1 )
Private aColsAB7     := {}
Private aColsAB8     := {}
Private aHeaderAB7   := {}
Private aHeaderAB8   := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida a existencia da nova pergunta MV_PAR07 (Qual Moeda) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("AT400PROC")
	If Empty(MV_PAR07)
		nMoeda := IIf(!Empty(M->AB3_MOEDA), M->AB3_MOEDA, 1)
	Else
		nMoeda := MV_PAR07
	EndIf
Else
	If ( nOpcao <> 3 )
		nMoeda := IIf(!Empty(M->AB3_MOEDA), M->AB3_MOEDA, 1)
	Else
		nMoeda := 1
	EndIf
EndIf
nDecs := MsDecimais(nMoeda)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se pode estender a tipo 9 ate 36 parcelas                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len( cMv1Dup ) > 1 .AND. Len( cMv1Dup ) == Len( SE1->E1_PARCELA ) .AND. cMv1Dup == ( Replicate( "0", Len( SE1->E1_PARCELA ) - 1 ) + "1" )
	nMaxTipo9 := 36
EndIf

If nParcelas > nMaxTipo9
   nParcelas := nMaxTipo9
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ nOpcao: 1 - Inclusao de Registros                     ³
//³ nOpcao: 2 - Alteracao de Registros                    ³
//³ nOpcao: 3 - Exclusao de Registros                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nOpcao <> 3 ) // Inclusao e Alteracao
	aT450Monta()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se algum item do acols e valido                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lGravou := !Empty( AScan( aCols, { |x| !x[nUsadoAB4+1] .AND. !Empty(x[nPPrdAb4]) } ) )

	DbSelectArea("SA1")
	SA1->(DbSetOrder(1))
	SA1->(DbSeek(xFilial("SA1")+M->AB3_CODCLI+M->AB3_LOJA))
	DbSelectArea("AB3")
	AB3->(DbSetOrder(1))
	AB3->(DbSeek(xFilial("AB3")+M->AB3_NUMORC))

	lFoundAB3 := AB3->( Found() )

	If lFoundAB3
		nRecnoAB3 := AB3->( Recno() )
	EndIf

	If ( lGravou )
		RecLock("AB3",!Found())
		For nCntFor := 1 To FCount()
			If ( "FILIAL"$Field(nCntFor) )
				FieldPut(nCntFor,xFilial("AB3"))
			Else
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			EndIf
		Next nCntFor

		AB3->AB3_REGIAO := SA1->A1_REGIAO
		nRecnoAB3 := AB3->( Recno() )
		AB3->(MsUnLock())
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acerta o AcolsAB5                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Empty(aColsAB5) .OR. Len(aColsAB5)<=Len(aCols) )
		aadd(aAux,Array(nUsadoAB5+1))
		For nCntFor := 1 To nUsadoAB5
			If IsHeadRec(aHeaderAB5[nCntFor][2])
				aAux[1][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
				aAux[1][nCntFor] := "AB5"
			Else
				aAux[1][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2],.T.)
			Endif
		Next nCntFor
		aAux[1][nUsadoAB5+1] := .T.
		For nCntFor := 1 To Len(aCols)
			aadd(aColsAB5,aClone(aAux))
		Next nCntFor
	EndIf
	At400AB5()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao do Itens                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aCols)
		If ( !aCols[nCntFor][nUsadoAB4+1] .AND. !Empty(aCols[nCntFor][nPPrdAb4]) )
			DbSelectArea("AB4")
			AB4->(DbSetOrder(1))
			If AB4->(DbSeek(xFilial("AB4")+M->AB3_NUMORC+aCols[nCntFor][nPIteAB4]))
				nOpcAB4 := 2
				RecLock("AB4",.F.)
				cTipoAnt := AB4->AB4_TIPO
			Else
				nOpcAB4 := 1
				RecLock("AB4",.T.)
				cTipoAnt := "0"
				
			EndIf
			For nCntFor2 := 1 To nUsadoAB4
				If !Empty( nPosCampo := AB4->( FieldPos(aHeaderAB4[nCntFor2][2] ) ) )
					AB4->(FieldPut( nPosCampo ,aCols[nCntFor][nCntFor2]))
				EndIf
			Next nCntFor2

			// lGravou := .T.
			If nOpcAB4 == 1
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se for inclusao, posiciona pelo cliente                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AA3")
				AA3->(DbSetOrder(1))
				//AA3_FILIAL+AA3_CODCLI+AA3_LOJA+AA3_CODPRO+AA3_NUMSER+AA3_FILORI
				If !AA3->(DbSeek(xFilial('AA3')+ M->AB3_CODCLI + M->AB3_LOJA + AB4->AB4_CODPRO + AB4->AB4_NUMSER))
					If InTransaction()
						// "Problema com a integridade, não foi possivel localizar o produto " " com o serial " " na base instalada do cliente " " durante a gravacao do orcamento."
						Help( " ",1, "AT400INTEGRIDADE",, (STR0016 + Alltrim(AB4->AB4_CODPRO) + STR0017 + Alltrim(AB4->AB4_NUMSER) + STR0018 + M->AB3_CODCLI + STR0019), 1, 0 )
						ConOut(STR0016 + Alltrim(AB4->AB4_CODPRO) + STR0017 + Alltrim(AB4->AB4_NUMSER) + STR0018 + M->AB3_CODCLI + STR0019) 
						AB4->(MsUnLock())
						DisarmTransaction()
						lGravou := .F.
						RestArea(aAreaAb1)
						RestArea(aAreaAb2)
						RestArea(aAreaAb3)
						RestArea(aAreaAb4)
						RestArea(aAreaAb5)
						RestArea(aArea)
						Return(lGravou)
					EndIf
				EndIf

			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se nao, posiciona pelo fabricante + loja                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AA3")
				AA3->(DbSetOrder(4))
				AA3->(DbSeek(xFilial("AA3")+ AB4->AB4_CODFAB + AB4->AB4_LOJAFA + AB4->AB4_CODPRO + AB4->AB4_NUMSER))
			EndIf

			AB4->AB4_FILIAL   := xFilial("AB4")
			AB4->AB4_NUMORC   := M->AB3_NUMORC
			AB4->AB4_NRCHAM   := If(aChamado<>Nil,aChamado[nCntFor],AB4->AB4_NRCHAM)
			AB4->AB4_CODFAB   := AA3->AA3_CODFAB
			AB4->AB4_LOJAFA   := AA3->AA3_LOJAFA
			AB4->AB4_OSORIG   := If(aAtend<>Nil,aAtend[nCntFor],AB4->AB4_OSORIG)
			If ( AB4->AB4_TIPO == "1" )
				AB4->AB4_BXHORA := ""
				AB4->AB4_BXDATA := Ctod("")
			Else
				AB4->AB4_BXHORA := Time()
				AB4->AB4_BXDATA := dDataBase
			EndIf
			If ( lStatus .AND. AB4->AB4_TIPO=="1" )
				lStatus := .F.
			EndIf
			MSMM(AB4->AB4_MEMO,,,aCols[nCntFor][nPMemoAB4],1,,,"AB4","AB4_MEMO")
			AB4->(MsUnLock())
			AB4->( FKCommit() )
			
			If ( !Empty(AB4->AB4_NRCHAM) )
				DbSelectArea("AB2")
				AB2->(DbSetOrder(1))
				If (AB2->(DbSeek(xFilial("AB2")+AB4->AB4_NRCHAM)))
					RecLock("AB2")
					AB2->AB2_NUMORC   := AB4->AB4_NUMORC+AB4->AB4_ITEM
					AB2->(MsUnLock())
				EndIf
			EndIf
			If ( !Empty(AB4->AB4_OSORIG) ) .AND. ValType( aAtend ) == "A"
				DbSelectArea("AB9")
				AB9->(DbSetOrder(1))
				If AB9->(DbSeek(xFilial("AB9") + aAtend[nCntFor]))
					RecLock("AB9")
					AB9->AB9_NUMORC := M->AB3_NUMORC
					AB9->(MsUnLock())
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ordem de Servico - Itens                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( AB4->AB4_TIPO == "2" )
				DbSelectArea("AB7")
				AB7->(DbSetOrder(3))
				If AB7->(DbSeek(xFilial("AB7")+AB4->AB4_NUMORC+AB4->AB4_ITEM))
					cNumOS := AB7->AB7_NUMOS
					cTipoOS := AB7->AB7_TIPO
					lAltTpOS := .F.
				EndIf
				aadd(aColsAB7,Array(Len(aHeaderAB7)+1))
				For nCntFor2 := 1 To Len(aHeaderAB7)
					nPosAhead := aScan(aHeaderAB4,{|x| SubStr(AllTrim(x[2]),4)==;
														SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )
					If nCntFor2 == nPosMemo	//Alimenta o campo memo observação.
						aColsAB7[Len(aColsAB7)][nCntFor2] := aCols[nCntFor][nPMemoAB4]
					EndIf

					If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )
						If AB4->(FieldPos(aHeaderAB4[nPosAhead][2])) > 0
							aColsAB7[Len(aColsAB7)][nCntFor2] := AB4->(FieldGet(FieldPos(aHeaderAB4[nPosAhead][2])))
						EndIf
					Else
						If IsHeadRec(aHeaderAB7[nCntFor2][2])
							aColsAB7[Len(aColsAB7)][nCntFor2] := AB7->(RecNo())
						Elseif IsHeadAlias(aHeaderAB7[nCntFor2][2])
							aColsAB7[Len(aColsAB7)][nCntFor2] := "AB7"
						Else
							If Alltrim(aHeaderAB7[nCntFor2][2]) == "AB7_TIPO" .And. !lAltTpOS
								//Se o item já existe, mantém o Tipo já gravado
								aColsAB7[Len(aColsAB7)][nCntFor2] := cTipoOS
								lAltTpOS := .T.
							Else
								aColsAB7[Len(aColsAB7)][nCntFor2] := CriaVar(aHeaderAB7[nCntFor2][2])
							EndIf	
						Endif
					Endif
				Next nCntFor2
				aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .F.
				aadd(aOS,AB4->AB4_NUMORC+AB4->AB4_ITEM)
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Deletar item da Ordem de Servico   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( cTipoAnt=="2" )
					DbSelectArea("AB7")
					AB7->(DbSetOrder(3))
					If AB7->(DbSeek(xFilial("AB7")+AB4->AB4_NUMORC+AB4->AB4_ITEM))
						cNumOS := AB7->AB7_NUMOS
					EndIf
					aadd(aColsAB7,Array(Len(aHeaderAB7)+1))
					For nCntFor2 := 1 To Len(aHeaderAB7)
						nPosAhead := aScan(aHeaderAB4,{|x| SubStr(AllTrim(x[2]),4)==;
															SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )

						If nCntFor2 == nPosMemo	//Alimenta o campo memo observação.
							aColsAB7[Len(aColsAB7)][nCntFor2] := aCols[nCntFor][nPMemoAB4]
						EndIf

						If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )
							aColsAB7[Len(aColsAB7)][nCntFor2] := AB4->(FieldGet(FieldPos(aHeaderAB4[nPosAhead][2])))
						Else
							If IsHeadRec(aHeaderAB7[nCntFor2][2])
								aColsAB7[Len(aColsAB7)][nCntFor2] := AB7->(RecNo())
							Elseif IsHeadAlias(aHeaderAB7[nCntFor2][2])
								aColsAB7[Len(aColsAB7)][nCntFor2] := "AB7"
							Else
								aColsAB7[Len(aColsAB7)][nCntFor2] := CriaVar(aHeaderAB7[nCntFor2][2])
							Endif
						Endif
					Next nCntFor2
					aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .T. //Deletar
					aadd(aOS,AB4->AB4_NUMORC+AB4->AB4_ITEM)
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua a Gravacao dos Sub-Itens                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aAux := {}
			For nCntFor2 := 1 To Len(aColsAB5[nCntFor])
				If ( !aColsAB5[nCntFor][nCntFor2][nUsadoAB5+1] .AND. !Empty(aColsAB5[nCntFor][nCntFor2][nPPrdAB5]))
					DbSelectArea("AB5")
					AB5->(DbSetOrder(1))
					If AB5->(DbSeek(xFilial("AB5")+M->AB3_NUMORC+aCols[nCntFor][nPIteAB4]+aColsAB5[nCntFor][nCntFor2][nPIteAB5]))
						RecLock("AB5")
					Else
						RecLock("AB5",.T.)
					EndIf
					For nCntFor3 := 1 To nUsadoAB5
						If !Empty( nPosCampo := AB5->( FieldPos(aHeaderAB5[nCntFor3][2] ) ) )
							AB5->(FieldPut( nPosCampo,aColsAB5[nCntFor][nCntFor2][nCntFor3]))
						EndIf
					Next nCntFor3
					AB5->AB5_FILIAL := xFilial("AB5")
					AB5->AB5_NUMORC := M->AB3_NUMORC
					AB5->AB5_ITEM   := aCols[nCntFor][nPIteAB4]
					AB5->(MsUnLock())
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Ordem de Servico - Sub-Itens                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( AB4->AB4_TIPO == "2" )
						aadd(aAux,Array(Len(aHeaderAb8)+1))
						For nCntFor3 := 1 To Len(aHeaderAB8)
							nPosAhead := aScan(aHeaderAB5,{|x| SubStr(AllTrim(x[2]),4)==;
														SubStr(AllTrim(aHeaderAB8[nCntFor3][2]),4)} )
							If ( nPosAHead <> 0 )
								If	(cPaisLoc <> "BRA") .AND.;
									(Alltrim(aHeaderAB8[nCntFor3][2]) == "AB8_VUNIT" .OR.;
									Alltrim(aHeaderAB8[nCntFor3][2]) == "AB8_TOTAL" .OR.;
									Alltrim(aHeaderAB8[nCntFor3][2]) == "AB8_PRCLIS")

									nValAB5 := AB5->(FieldGet(FieldPos(aHeaderAB5[nPosAhead][2])))

									aAux[Len(aAux)][nCntFor3] := Round(xMoeda(nValAB5, AB3->AB3_MOEDA, nMoeda, dDataBase, nDecs+1, AB3->AB3_TXMOED), nDecs)
								Else
									aAux[Len(aAux)][nCntFor3] := AB5->(FieldGet(FieldPos(aHeaderAB5[nPosAhead][2])))
								EndIf
							Else
								If IsHeadRec(aHeaderAB8[nCntFor3][2])
									aAux[Len(aAux)][nCntFor3] := 0
								Elseif IsHeadAlias(aHeaderAB8[nCntFor3][2])
									aAux[Len(aAux)][nCntFor3] := "AB8"
								Else
									aAux[Len(aAux)][nCntFor3] := CriaVar(aHeaderAB8[nCntFor3][2])
								Endif
							EndIf
						Next nCntFor3

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Preenche o local padrao do produto                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If !Empty( nPosAhead := GDFieldPos( "AB8_LOCAL", aHeaderAB8 ) )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Obtem o local padrao do produto                     ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							SB1->( DbSetOrder( 1 ) )
							If SB1->( DbSeek( xFilial( "SB1" ) + AB5->AB5_CODPRO ) )
								aAux[ Len( aAux ), nPosAhead ] := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
							EndIf
						EndIf

						aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
					EndIf
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Sub-Itens                                           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					DbSelectArea("AB5")
					AB5->(DbSetOrder(1))
					If AB5->(DbSeek(xFilial("AB5")+M->AB3_NUMORC+aCols[nCntFor][nPIteAB4]+aColsAB5[nCntFor][nCntFor2][nPIteAB5]))
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Ordem de Servico - Sub-Itens                        ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( AB4->AB4_TIPO == "2" )
							aadd(aAux,Array(Len(aHeaderAb8)+1))
							For nCntFor3 := 1 To Len(aHeaderAB8)
								nPosAhead := aScan(aHeaderAB5,{|x| SubStr(AllTrim(x[2]),4)==;
															SubStr(AllTrim(aHeaderAB8[nCntFor3][2]),4)} )
								If ( nPosAHead <> 0 )
									aAux[Len(aAux)][nCntFor3] := AB5->(FieldGet(FieldPos(aHeaderAB5[nPosAhead][2])))
								Else
									If IsHeadRec(aHeaderAB8[nCntFor3][2])
										aAux[Len(aAux)][nCntFor3] := 0
									Elseif IsHeadAlias(aHeaderAB8[nCntFor3][2])
										aAux[Len(aAux)][nCntFor3] := "AB8"
									Else
										aAux[Len(aAux)][nCntFor3] := CriaVar(aHeaderAB8[nCntFor3][2])
									Endif
								EndIf
							Next nCntFor3
							aAux[Len(aAux)][Len(aHeaderAB8)+1] := .T. //Deletar
						EndIf
						RecLock("AB5")
						AB5->(DbDelete())
						AB5->(MsUnLock())
					EndIf
				EndIf
			Next nCntFor2

			AB5->( FKCommit() )
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ordem de Servico - Sub-Itens                        ³
			//³ O tamanho de aColsAB7 e aColsAB8 deve ser igual     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Len( aColsAB7 ) > Len( aColsAB8 )
				If Empty( aAux )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ quando o item do orcamento nao possui apontamento, deve criar o aux vazio ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Empty( aVazioAB8 )
						AAdd(aAux,Array(Len(aHeaderAb8)+1))
						For nCntFor3 := 1 To Len(aHeaderAB8)
							If IsHeadRec(aHeaderAB8[nCntFor3][2])
								aAux[Len(aAux),nCntFor3] := 0
							Elseif IsHeadAlias(aHeaderAB8[nCntFor3][2])
								aAux[Len(aAux),nCntFor3] := "AB8"
							Else
								aAux[Len(aAux),nCntFor3] := CriaVar(aHeaderAB8[nCntFor3][2])
							Endif
						Next nCntFor3
						aAux[Len(aAux),Len(aHeaderAB8)+1] := .F.
						aVazioAB8 := AClone( aAux )
					Else
						aAux := AClone( aVazioAB8 )
					EndIf
				EndIf
				AAdd(aColsAB8,aClone(aAux))
			EndIf
		Else
			DbSelectArea("AB4")
			AB4->(DbSetOrder(1))
			If AB4->(DbSeek(xFilial("AB4")+M->AB3_NUMORC+aCols[nCntFor][nPIteAB4]))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retira a amarracao com o Chamado Tecnico            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB2")
				AB2->(DbSetOrder(1))
				If AB2->(DbSeek(xFilial("AB2")+AB4->AB4_NRCHAM))
					RecLock("AB2")
					AB2->AB2_NUMORC := ""
					AB2->AB2_TIPO   := If( aChamado <> NIL, AB2->AB2_TIPO, "1" )
					AB2->AB2_BXDATA := Ctod("")
					AB2->AB2_BXHORA := ""
					AB2->(MsUnLock())
					AB2->( FKCommit() )
					

					DbSelectArea("AB1")
					AB1->(DbSetOrder(1))
					AB1->(DbSeek(xFilial("AB1")+AB2->AB2_NRCHAM))
					RecLock("AB1")
					AB1->AB1_STATUS   := If( aChamado <> NIL, AB1->AB1_STATUS, "A" )
					AB1->(MsUnLock())
				EndIf
				MSMM(AB4->AB4_MEMO,,,,2)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Retira a amarracao com o Atendimento Tecnico                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB9")
				AB9->(DbSetOrder(1))
				If AB9->( DbSeek(xFilial("AB9")+AB4->AB4_OSORIG) )
					RecLock("AB9")
					AB9->AB9_NUMORC := ""
					AB9->(MsUnLock())
					AB9->( FKCommit() )
					
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Deletar o subitem do Orcamento     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB5")
				AB5->(DbSetOrder(1))
				If AB5->(DbSeek(xFilial("AB5")+M->AB3_NUMORC+aCols[nCntFor][nPIteAB4]))
					While ( !Eof() .AND. AB5->AB5_FILIAL == xFilial("AB5") .AND.;
												AB5->AB5_NUMORC == M->AB3_NUMORC    .AND.;
												AB5->AB5_ITEM   == aCols[nCntFor][nPIteAB4])
						RecLock("AB5")
						AB5->(DbDelete())
						AB5->(DbSkip())
						AB5->(MsUnLock())
					EndDo
					AB5->( FKCommit() )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Ordem de Servico - Itens                            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( AB4->AB4_TIPO == "2" )
					DbSelectArea("AB7")
					AB7->(DbSetOrder(3))
					If AB7->(DbSeek(xFilial("AB7")+AB4->AB4_NUMORC+AB4->AB4_ITEM))
						cNumOS := AB7->AB7_NUMOS
					EndIf
					aadd(aColsAB7,Array(Len(aHeaderAB7)+1))
					For nCntFor2 := 1 To Len(aHeaderAB7)
						nPosAhead := aScan(aHeaderAB4,{|x| SubStr(AllTrim(x[2]),4)==;
															SubStr(AllTrim(aHeaderAB7[nCntFor2][2]),4)} )
						If ( nPosAhead<>0 .AND. AllTrim(aHeaderAB7[nCntFor2][2])<>"AB7_TIPO" )
							aColsAB7[Len(aColsAB7)][nCntFor2] := AB4->(FieldGet(FieldPos(aHeaderAB4[nPosAhead][2])))
						Else
							If IsHeadRec(aHeaderAB7[nCntFor2][2])
								aColsAB7[Len(aColsAB7)][nCntFor2] := 0
							Elseif IsHeadAlias(aHeaderAB7[nCntFor2][2])
								aColsAB7[Len(aColsAB7)][nCntFor2] := "AB7"
							Else
								aColsAB7[Len(aColsAB7)][nCntFor2] := CriaVar(aHeaderAB7[nCntFor2][2])
							Endif
						Endif
					Next nCntFor2
					aColsAB7[Len(aColsAB7)][Len(aHeaderAB7)+1] := .T. //Deletar
					aAux := {}
					aadd(aAux,Array(Len(aHeaderAb8)+1))
					aAux[Len(aAux)][Len(aHeaderAb8)+1] := .T. //Deletar
					aadd(aColsAB8,aClone(aAux))
					aadd(aOS,AB4->AB4_NUMORC+AB4->AB4_ITEM)
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Deletar o item do Orcamento        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("AB4",.F.)
				AB4->(DbDelete())
				
				AB4->(MsUnLock())
				AB4->( FKCommit() )
			EndIf
		EndIf
	Next nCntFor

	If lGravou

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava informacoes adicionais no header              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AB3->( MsGoto( nRecnoAB3 ) )
		Reclock('AB3',.F.)
		AB3->AB3_STATUS := If(lStatus,"E","A")
		AB3->( MsUnlock() )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ordem de Servico                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Len(aColsAB7) > 0 )
			If ( Empty(cNumOs) )
				DbSelectArea("AB6")
				For nCntFor := 1 To FCount()
					uCampo := FieldName(nCntFor)
					M->&(uCampo) := CriaVar(uCampo,.T.)
				Next nCntFor
				M->AB6_CODCLI := AB3->AB3_CODCLI
				M->AB6_LOJA   := AB3->AB3_LOJA
				M->AB6_TABELA := AB3->AB3_TABELA
				M->AB6_CONPAG := AB3->AB3_CONPAG
				M->AB6_MOEDA  := nMoeda

				If (AB3->AB3_MOEDA == nMoeda) .AND. (AB3->AB3_TXMOED > 0)
					M->AB6_TXMOED := AB3->AB3_TXMOED
				Else
					M->AB6_TXMOED := Recmoeda( dDatabase, M->AB6_MOEDA )
				EndIf

				If GetNewPar("MV_ORCOS",.F.)
					M->AB6_CONPAG := AB3->AB3_CONPAG
					M->AB6_DESC1  := AB3->AB3_DESC1
					M->AB6_DESC2  := AB3->AB3_DESC2
					M->AB6_DESC3  := AB3->AB3_DESC3
					M->AB6_DESC4  := AB3->AB3_DESC4
					M->AB6_TABELA := AB3->AB3_TABELA

					For nCntFor :=  1 To nParcelas
						cCampo := SubStr( cParcela, nCntFor, 1 )
						cCampo := "AB3_PARC"+cCampo
						nPos1 := AB3->(FieldPos(cCampo))
						If nPos1 > 0
							cCampo := "AB6"+SubStr(cCampo,4)
							M->&(cCampo) := AB3->(FieldGet(nPos1))
						EndIf
						cCampo := SubStr( cParcela, nCntFor, 1 )
						cCampo := "AB3_DATA"+cCampo
						nPos1 := AB3->(FieldPos(cCampo))
						If nPos1 > 0
							cCampo := "AB6"+SubStr(cCampo,4)
							M->&(cCampo) := AB3->(FieldGet(nPos1))
						EndIf
					Next nCntFor
				EndIf
			Else
				DbSelectArea("AB6")
				AB6->(DbSetOrder(1))
				AB6->(DbSeek(xFilial("AB6")+cNumOS))
				For nCntFor := 1 To FCount()
					uCampo := FieldName(nCntFor)
					M->&(uCampo) := AB6->(FieldGet(nCntFor))
				Next nCntFor
				M->AB6_TABELA := AB3->AB3_TABELA
				M->AB6_MOEDA  := nMoeda

				If (AB3->AB3_MOEDA == nMoeda) .AND. (AB3->AB3_TXMOED > 0)
					M->AB6_TXMOED := AB3->AB3_TXMOED
				Else
					M->AB6_TXMOED := Recmoeda( dDatabase, M->AB6_MOEDA )
				EndIf

				If GetNewPar("MV_ORCOS",.F.)
					M->AB6_CONPAG := AB3->AB3_CONPAG
					M->AB6_DESC1  := AB3->AB3_DESC1
					M->AB6_DESC2  := AB3->AB3_DESC2
					M->AB6_DESC3  := AB3->AB3_DESC3
					M->AB6_DESC4  := AB3->AB3_DESC4

					For nCntFor :=  1 To nParcelas
						cCampo := SubStr( cParcela, nCntFor, 1 )
						cCampo := "AB3_PARC"+cCampo
						nPos1 := AB3->(FieldPos(cCampo))
						If nPos1 > 0
							cCampo := "AB6"+SubStr(cCampo,4)
							M->&(cCampo) := AB3->(FieldGet(nPos1))
						EndIf
						cCampo := SubStr( cParcela, nCntFor, 1 )
						cCampo := "AB3_DATA"+cCampo
						nPos1 := AB3->(FieldPos(cCampo))
						If nPos1 > 0
							cCampo := "AB6"+SubStr(cCampo,4)
							M->&(cCampo) := AB3->(FieldGet(nPos1))
						EndIf
					Next nCntFor
				EndIf
			EndIf
			aHeader  := aClone(aHeaderAB7)
			aCols    := aClone(aColsAB7)
			DbSelectArea("AB6")
			AB6->(DbSetOrder(1))
			nOpcOs := If(AB6->(DbSeek(xFilial("AB6")+M->AB6_NUMOS)), 2, 1 )
			At450Grava(nOpcOs,,aOS)
		EndIf
	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ordem de Servico                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AB7")
		AB7->(DbSetOrder(3))
		If AB7->(DbSeek(xFilial("AB7")+AB3->AB3_NUMORC))
			DbSelectArea("AB6")
			AB6->(DbSetOrder(1))
			AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))
			At450Grava(3,,aOS)
		EndIf

		If lFoundAB3
			AB3->( MsGoto( nRecnoAB3 ) )
			RecLock("AB3")
			AB3->(DbDelete())
			AB3->(MsUnLock())
		EndIf
	EndIf

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ordem de Servico                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB7")
	AB7->(DbSetOrder(3))
	If AB7->(DbSeek(xFilial("AB7")+AB3->AB3_NUMORC))
		DbSelectArea("AB6")
		AB6->(DbSetOrder(1))
		AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))
		At450Grava(3,,aOs)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Exclusao dos subitens                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB5")
	AB5->(DbSetOrder(1))

	If AB5->(DbSeek(xFilial("AB5")+AB3->AB3_NUMORC))
		While ( !Eof() .AND. AB5->AB5_FILIAL   ==    xFilial("AB5") .AND.;
									AB5->AB5_NUMORC   ==    AB3->AB3_NUMORC )
			RecLock("AB5")
			AB5->(DbDelete())
			AB5->(MsUnLock())
			AB5->(DbSkip())
		EndDo

		AB5->( FKCommit() )
	EndIf

	DbSelectArea("AB4")
	AB4->(DbSetOrder(1))

	If AB4->(DbSeek(xFilial("AB4")+AB3->AB3_NUMORC))
		While ( !Eof() .AND. AB4->AB4_FILIAL   ==    xFilial("AB4") .AND.;
									AB3->AB3_NUMORC      ==    AB4->AB4_NUMORC )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retira a amarracao com o Chamado Tecnico            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB2")
			AB2->(DbSetOrder(1))
			If AB2->(DbSeek(xFilial("AB2")+AB4->AB4_NRCHAM))
				RecLock("AB2")
				AB2->AB2_NUMORC := ""
				AB2->AB2_TIPO   := "1"
				AB2->AB2_BXDATA := Ctod("")
				AB2->AB2_BXHORA := ""
				AB2->(MsUnLock())
				AB2->( FKCommit() )
				

				DbSelectArea("AB1")
				AB1->(DbSetOrder(1))
				AB1->(DbSeek(xFilial("AB1")+AB2->AB2_NRCHAM))
				RecLock("AB1")
				AB1->AB1_STATUS   := "A"
				AB1->(MsUnLock())	
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Retira a amarracao com o Atendimento da Ordem de Servico                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB9")
			AB9->(DbSetOrder(1))
			If AB9->(DbSeek(xFilial("AB9")+AB4->AB4_OSORIG))

				While !AB9->( Eof() ) .AND. AB9->AB9_FILIAL + AB9->AB9_NUMOS == ;
						xFilial("AB9")+AB4->AB4_OSORIG

		   		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Percorre o AB9 para identificar o atendimento que criou este orcamento ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If AB9->AB9_NUMORC == AB3->AB3_NUMORC
						RecLock("AB9")
						AB9->AB9_NUMORC := ""
						MsUnLock()
					EndIf
					AB9->( DbSkip() )
				EndDo

				AB9->( FKCommit() )

			EndIf
			MSMM(AB4->AB4_MEMO,,,,2)
			RecLock("AB4")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui a amarracao com os conhecimentos                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MsDocument( Alias(), RecNo(), 2, , 3 )

			AB4->(DbDelete())
			DbSelectArea("AB4")
			AB4->(DbSkip())
		EndDo

		AB4->( FKCommit() )
	EndIf

	DbSelectArea("AB3")
	RecLock("AB3")
	DbDelete()
	AB3->(MsUnLock())
EndIf
If ExistBlock("AT400GRV")
	Execblock("AT400GRV",.F.,.F.)
Endif

RestArea(aAreaAb1)
RestArea(aAreaAb2)
RestArea(aAreaAb3)
RestArea(aAreaAb4)
RestArea(aAreaAb5)
RestArea(aArea)
Return(lGravou)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400AB5  ³ Autor ³ Eduardo Riera         ³ Data ³ 30.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Esta funcao inicializa automaticamente os sub-itens dos    ³±±
±±³          ³ Orcamentos conforme a ocorrencia registrada.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Variaveis Privates                                         ³±±
±±³          ³ ExpN1 : Numero da Linha do AB4 a ser Processada            ³±±
±±³          ³         Se Nulo todas as linhas serao processadas.         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At400AB5(nLinAb4)

Local nCntFor := 0
Local nCntFor2:= 0
Local nPPrdAB4 := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_CODPRO"})
Local nPNsrAB4 := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_NUMSER"})
Local nPPrbAB4 := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_CODPRB"})
Local nPPrdAB5 := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODPRO"})
Local nPIteAB5 := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})
Local nUsadoAB4:= Len(aHeaderAB4)
Local nUsadoAB5:= Len(aHeaderAB5)
Local aAux     := {}
Local cItemAB5 := ""
Local nIniFor  := If(nLinAb4==Nil,1,nLinAb4)
Local nMaxFor  := If(nLinAb4==Nil,Len(aCols),nLinAb4)
Local lRdesc   := .F.

aAux := Array(nUsadoAB5+1)
For nCntFor := 1 To nUsadoAB5
	If IsHeadRec(aHeaderAB5[nCntFor][2])
		aAux[nCntFor] := 0
	Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
		aAux[nCntFor] := "AB5"
	Else
		aAux[nCntFor] := CriaVar(aHeaderAB5[nCntFor][2],.T.)
	Endif
Next nCntFor
aAux[nUsadoAB5+1] := .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a Ocorrencia/Problema eh do Tipo obsolescˆncia, caso       ³
//³ seja deve-se preencher automaticamente os sub-itens com os componentes ³
//³ a serem substituidos.                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := nIniFor To nMaxFor
	If ( !aCols[nCntFor][nUsadoAb4+1] )
		DbSelectArea("AAG")
		AAG->(DbSetOrder(1))
		If AAG->(DbSeek(xFilial("AAG")+aCols[nCntFor][nPPrbAB4]))
			If ( AAG->AAG_TIPPRB == "2" ) //Obsolescencia
				lRdesc   := .T.
				If ( Len(aColsAB5[nCntFor]) == 1 .AND. Empty(aColsAB5[nCntFor][1][nPPrdAb5]) )
					DbSelectArea("AA3")
					AA3->(DbSetOrder(1))
					If AA3->(DbSeek(xFilial("AA3")+M->AB3_CODCLI+M->AB3_LOJA+aCols[nCntFor][nPPrdAb4]+aCols[nCntFor][nPNsrAb4]))
						DbSelectArea("AAK")
						AAK->(DbSetOrder(1))
						If AAK->(DbSeek(xFilial("AAK")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO))
							While ( ! Eof() .AND. xFilial("AAK")   == AAK->AAK_FILIAL .AND.;
														AA3->AA3_CODFAB   == AAK->AAK_CODFAB .AND.;
														AA3->AA3_LOJAFA   == AAK->AAK_LOJAFA .AND.;
														AA3->AA3_CODPRO   == AAK->AAK_CODPRO )
								If ( ItIsObsol() )
									If ( !Empty(aColsAb5[nCntFor][1][nPPrdAB5]) )
										aadd(aColsAb5[nCntFor],aClone(aAux))
									Else
										cItemAb5 := "01"
									EndIf
									For nCntFor2 := 1 To Len(aHeaderAB5)
										Do Case
											Case AllTrim(aHeaderAB5[nCntFor2][2]) == "AB5_SUBITE"
												aColsAB5[nCntFor][Len(aColsAB5[nCntFor])][nCntFor2] := cItemAB5
												cItemAb5 := Soma1(cItemAb5)
											Case AllTrim(aHeaderAB5[nCntFor2][2]) == "AB5_CODPRO"
												aColsAB5[nCntFor][Len(aColsAB5[nCntFor])][nCntFor2] := AAK->AAK_CODCOM
											Case AllTrim(aHeaderAB5[nCntFor2][2]) == "AB5_DESPRO"
												DbSelectArea("SB1")
												DbSetOrder(1)
												DbSeek(xFilial("SB1")+AAK->AAK_CODCOM)
												aColsAB5[nCntFor][Len(aColsAB5[nCntFor])][nCntFor2] := SB1->B1_DESC
											Case AllTrim(aHeaderAB5[nCntFor2][2]) == "AB5_QUANT"
												aColsAB5[nCntFor][Len(aColsAB5[nCntFor])][nCntFor2] := 1
											Case AllTrim(aHeaderAB5[nCntFor2][2])$"AB5_PRCLIS#AB5_VUNIT#AB5_TOTAL"
												aColsAB5[nCntFor][Len(aColsAB5[nCntFor])][nCntFor2] := At400Tab(AAK->AAK_CODCOM,M->AB3_TABELA,1,M->AB3_CODCLI,M->AB3_LOJA,M->AB3_MOEDA)
										EndCase
									Next nCntFor2
									aColsAb5[nCntFor][Len(aColsAB5[nCntFor])][nUsadoAB5+1] := .F.
								EndIf
								DbSelectArea("AAK")
								DbSkip()
							EndDo
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
Next nCntFor
If lRdesc
	At400RDesc()
EndIf	
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450AB8  ³ Autor ³ Eduardo Riera         ³ Data ³ 30.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Esta funcao inicializa automaticamente os Sub-Itens da     ³±±
±±³          ³ Ordem de Servico conforme a ocorrencia registrada.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Variaveis Privates                                         ³±±
±±³          ³ ExpN1 : Numero da Linha do AB7 a ser Processada            ³±±
±±³          ³         Se Nulo todas as linhas serao processadas.         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At450AB8(nLinAb7)

Local nCntFor := 0
Local nCntFor2:= 0
Local nPPrdAB7 := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_CODPRO"})
Local nPNsrAB7 := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_NUMSER"})
Local nPPrbAB7 := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_CODPRB"})
Local nPPrdAB8 := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODPRO"})
Local nPIteAB8 := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})
Local nUsadoAB7:= Len(aHeaderAB7)
Local nUsadoAB8:= Len(aHeaderAB8)
Local aAux     := {}
Local cItemAB8 := ""
Local nIniFor  := If(nLinAB7==Nil,1,nLinAB7)
Local nMaxFor  := If(nLinAB7==Nil,Len(aCols),nLinAB7)

aAux := Array(nUsadoAB8+1)
For nCntFor := 1 To nUsadoAB8
	If IsHeadRec(aHeaderAB8[nCntFor][2])
		aAux[nCntFor] := 0
	Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
		aAux[nCntFor] := "AB8"
	Else
		aAux[nCntFor] := CriaVar(aHeaderAB8[nCntFor][2],.T.)
	Endif
Next nCntFor
aAux[nUsadoAB8+1] := .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a Ocorrencia/Problema eh do Tipo obsolescˆncia, caso       ³
//³ seja deve-se preencher automaticamente os sub-itens com os componentes ³
//³ a serem substituidos.                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := nIniFor To nMaxFor
	If ( !aCols[nCntFor][nUsadoAB7+1] )
		DbSelectArea("AAG")
		DbSetOrder(1)
		If ( DbSeek(xFilial("AAG")+aCols[nCntFor][nPPrbAB7]) )
			If ( AAG->AAG_TIPPRB == "2" ) //Obsolescencia
				If ( Len(aColsAB8[nCntFor]) == 1       .AND.;
					  Empty(aColsAB8[nCntFor][1][nPPrdAB8]) )
					DbSelectArea("AA3")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AA3")+M->AB6_CODCLI+M->AB6_LOJA+aCols[nCntFor][nPPrdAB7]+aCols[nCntFor][nPNsrAB7]) )
						DbSelectArea("AAK")
						DbSetOrder(1)
						If ( DbSeek(xFilial("AAK")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO) )
							While ( ! Eof() .AND. xFilial("AAK")   == AAK->AAK_FILIAL .AND.;
														AA3->AA3_CODFAB   == AAK->AAK_CODFAB .AND.;
														AA3->AA3_LOJAFA   == AAK->AAK_LOJAFA .AND.;
														AA3->AA3_CODPRO   == AAK->AAK_CODPRO )
								If ( ItIsObsol() )
									If ( !Empty(aColsAB8[nCntFor][1][nPPrdAB8]) )
										aadd(aColsAB8[nCntFor],aClone(aAux))
									Else
										cItemAB8 := StrZero(1,TamSX3("AB8_SUBITE")[1])
									EndIf

									nPosQtdeAB8 := GDFieldPos( "AB8_QUANT", aHeaderAB8 )

									For nCntFor2 := 1 To Len(aHeaderAB8)
										Do Case
											Case AllTrim(aHeaderAB8[nCntFor2][2]) == "AB8_SUBITE"
												aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nCntFor2] := cItemAB8
												cItemAB8 := Soma1(cItemAB8)
											Case AllTrim(aHeaderAB8[nCntFor2][2]) == "AB8_CODPRO"
												aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nCntFor2] := AAK->AAK_CODCOM
											Case AllTrim(aHeaderAB8[nCntFor2][2]) == "AB8_DESPRO"
												DbSelectArea("SB1")
												DbSetOrder(1)
												DbSeek(xFilial("SB1")+AAK->AAK_CODCOM)
												aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nCntFor2] := SB1->B1_DESC
											Case AllTrim(aHeaderAB8[nCntFor2][2]) == "AB8_QUANT"
												aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nCntFor2] := 1
											Case AllTrim(aHeaderAB8[nCntFor2][2])$"AB8_PRCLIS#AB8_VUNIT#AB8_TOTAL"
												aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nCntFor2] := At450Tab(AAK->AAK_CODCOM,M->AB6_TABELA,1,M->AB6_CODCLI,M->AB6_LOJA,M->AB6_MOEDA)
										EndCase
									Next nCntFor2
									aColsAB8[nCntFor][Len(aColsAB8[nCntFor])][nUsadoAB8+1] := .F.
								EndIf
								DbSelectArea("AAK")
								DbSkip()
							EndDo
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
Next nCntFor
At450Rdesc()
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450GRAVA  ³ Autor ³Eduardo Riera        ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as Ordens de Servicos                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT450Grava(ExpN1,ExpA2,ExpA3,ExpA4)                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : 1 - Inclusao / 2 - Alteracao / 3 - Excluir         ³±±
±±³          ³ ExpA2 : Numero do Chamado                                  ³±±
±±³          ³ ExpA3 : Numero do Orcamento                                ³±±
±±³          ³ ExpA4 : Numero do Help Desk                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Grava(nOpcx,aChamado,aOrcamento,aHelpDesk,lAtuEqLoc)

Local aAreaAb1   := AB1->(GetArea())				//Area do arquivo AB1
Local aAreaAb2   := AB2->(GetArea())				//Area do arquivo AB2
Local aAreaAb3   := AB3->(GetArea())				//Area do arquivo AB3
Local aAreaAb4   := AB4->(GetArea())				//Area do arquivo AB4
Local aAreaAb5   := AB5->(GetArea())				//Area do arquivo AB5
Local aAreaAb6   := AB6->(GetArea())				//Area do arquivo AB6
Local aAreaAb7   := AB7->(GetArea())				//Area do arquivo AB7
Local aAreaAb8   := AB8->(GetArea())				//Area do arquivo AB8
Local aArea      := (GetArea())           			//Area da tabela corrente
Local aInfoABL   := {}								//Array com o status da fila de help-desk
Local aPlano     := {}                    			//Array com os dados do Plano
Local aItGrPed   := {}      						//Array dos itens da OS que gerarao PV
Local aGerouPV   := {}       						//Array dos itens que gerou PV
Local aUserCpoPv := {}								//Array dos campos de usuario do PV
Local aDadosCFO  := {}								//Array com os dados do Cod. Fiscal de Operacao
Local aAux       := {}								//Array auxiliar
Local aHeadC6    := {}								//Array com o Header do SC6
Local aColsC6    := {}								//Array com as colunas/campos do SC6
Local aColsF     := {}								//Array com as colunas do PV do Fabricante
Local aFabric    := {}								//Array com os dados do Fabric.
Local bCampo     := {|x| FieldName(x) }				//Codeblock a ser executado
Local cTipoAnt   := ""								//Tipo anterior
Local cItSc6     := ""								//Item do SC6
Local cCond      := ""								//Cond. Pagto
Local cCfo       := ""								//Cod. CFO
Local cSeekAA3   := ""       						//Expressao (chave) de pesquisa no AA3
Local cEstado    := SuperGetMV("MV_ESTADO")			//Estado
Local cMenNota   := ""								//Mensagem da Nota
Local cQuery     := "" 								//Expressao da query a executar
Local lT450PVOS  := ExistBlock("T450PVOS")			//Indica se existe o PE T450PVOS
Local lPVtmp     := .F.
Local lPv        := .F.								//Indica se deve gerar o PV
Local lGravou    := .F.								//Indica se gravou OS
Local lTecXSC5   := (ExistBlock("TECXSC5"))			//Indica se existe o PE TECXSC5
Local lAtCpPvOs  := (ExistBlock("ATCPPVOS"))		//Indica se existe o PE ATCPPVOS
Local lAt410Grv  := (ExistBlock("AT410GRV"))		//Indica se existe o PE AT410GRV
Local lGerouPV   := .F.								//Indica se gerou o PV
Local lInclBack  := .F. 							//Backup da variav. INCLUI
Local lAT450GIT  := ExistBlock( "AT450GIT" ) 		//Indica se existe o PE AT450GIT
Local lFoundAB6  := .F. 							//Indica se encontrou registro no AB6
Local nCntFor    := 0								//Usada em lacos For...Next
Local nCntFor2   := 0								//Usada em lacos For...Next
Local nCntFor3   := 0								//Usada em lacos For...Next
Local nPIteAB7   := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})		//Posicao do campo AB7_ITEM
Local nPPrdAB7   := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_CODPRO"})	//Posicao do campo AB7_CODPRO
Local nPMem1AB7  := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_MEMO2"})		//Posicao do campo AB7_MEMO2
Local nPMem2AB7  := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_MEMO4"})		//Posicao do campo AB7_MEMO4
Local nPTipAB7   := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_TIPO"})		//Posicao do campo AB7_TIPO
Local nPPrdAB8   := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODPRO"})	//Posicao do campo AB8_CODPRO
Local nPIteAB8   := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})	//Posicao do campo AB8_SUBITE
Local nPSerAB8   := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODSER"})	//Posicao do campo AB8_CORSER
Local nPQtdAB8   := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_QUANT"})		//Posicao do campo AB8_QUANT
Local nUsadoAB7  := Len(aHeaderAB7)					//Tamanho do Header do AB7
Local nUsadoAB8  := Len(aHeaderAB8)					//Tamanho do Header do AB8
Local nRecAB7Ant := 0								//Nr. do registro do AB7 anterior
Local nOpcAB7    := 0								//Opcao do AB7 (1=inclusao/2=alteracao)
Local nPosUsrCpo := 0								//Posicao do campo de usuario no array
Local nAcolC6    := 0								//Indice do aCols do SC6
Local nItSc6     := 0								//Posicao do campo Item no aCols do SC6
Local nFabric    := 0								//Posicao do fabricante encontrada no array aFabric
Local nFieldPos  := 0								//Posicao do campo no arquivo
Local nRecnoAB6  := 0 								//Nr. do registro atual no AB6
Local cCampo   	:= ""										//Nome do campo
Local cParcela 	:= "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"	//Possiveis numeros de parcelas
Local cMv1Dup  	:= SuperGetMV( "MV_1DUP" )					//Primeira duplicata
Local nParcelas	:= SuperGetMV("MV_NUMPARC")					//Nr. de parcelas
Local nMaxTipo9	:= 26										//Nr. maximo do tipo 9
Local nPos1    	:= 0										//Posicao do campo no AB6
Local nAcrsFin 	:= 0										//Acrescimo financ. da cond. de pagto
Local nValor   	:= 0										//Valor a ser convertido pela xMoeda()
Local nMoeda	:= 0										//Moeda a ser gravada no PV
Local nDecs	  	:= 0										//Nr. de casas decimais da Moeda
Local cNrCham	:= ""										//Nr. do chamado tecnico
Local nValorPRC  := 0
Local cEventID      := ""                              // Id do Evento a ser disparado pelo Event Viewer
Local cMensagem     := ""                              // Mensagem que sera enviada por e-mail ou RSS pelo Event Viewer
Local lCarga	:= .T.                                     // Controla a carga do rateio
Local aSocios	:= {} 										// Array para armazenar os socios do grupo societario
Local aQtdXAB8 	:= {}                                      // Array que controla a quantidade dos itens da tabela AB8
Local lRateio 	:= .F.									   // Define se o Pedido de Venda será rateado
Local nX		:= 0									   // Incremento utilizado no laco for
Local nQuant	:= 0									   // Quantidade Rateada
Local lGSOpTri	:= SuperGetMv('MV_GSOPTRI',.F.,.F.) //Parametro para ativar a operação triangular
Local lSeekAA3	:= .F.
Local cAliasPesq:= ""										// Variável para setar a Tabela a ser pesquisada - Contrato
Local cTipoCont	:= "0"									// Definição do tipo de contrato para saber qual tabela usar 1- Manutençao (AAH) 2- Prestaçao de Servico (AAM)
Local lCancLib := IsInCallStack("At800CancLib")
Local lTecAtf := SuperGetMv('MV_TECATF', .F.,'N') == 'S'
Local oTecProvider	:= Nil
Local lSeekBase 	:= .F.
Local nPosB1 := 0
Local lChanged := .F.
Local lMVGsLocPV := SuperGetMV("MV_GSLOCPV",.f., .T. ) //envia Localização no Pedido de Venda
Local cRegiao	:= ""
Local cRegPe	:= ""
Local lNt450Aut := Type("lAt450Auto")=="U" .Or. !lAt450Auto
Local lLibAgenRH    :=  SuperGetMv("MV_GSAGTRH",.F.,.F.) // Define se verifica conflitos de RH para geração da agenda do Técnico (.F. = Não verifica, .T. = Verifica)

Local aStruField := {}
Local cField     := ""
Local nC         := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis para uso no tratamento dos campos especificos de ³
//³ descontos por itens, no Orcamento e na O.S.                ³
//³ (ver documentacao)                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lCposDesc := ( CpoUsado('AB5_VLDESC') .And. CpoUsado('AB5_PCDESC') .And. CpoUsado('AB8_VLDESC') .And.	 CpoUsado('AB8_PCDESC') )
Local aRetDescs := {}

Private INCLUI 	:= ( nOpcx == 1 ) 							//Indica se inclui

Default lAtuEqLoc := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida a existencia da nova pergunta MV_PAR08 (Qual Moeda) ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If IsInCallStack("AT450PROC")
	If Empty(MV_PAR08)
		nMoeda := IIf(!Empty(M->AB6_MOEDA), M->AB6_MOEDA, 1)
	Else
		nMoeda := MV_PAR08
	EndIf
Else
	If ( nOpcx <> 3 )
		nMoeda := IIf(!Empty(M->AB6_MOEDA), M->AB6_MOEDA, 1)
	Else
		nMoeda := 1
	EndIf
EndIf
nDecs := MsDecimais(nMoeda)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se pode estender a tipo 9 ate 36 parcelas                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len( cMv1Dup ) > 1 .AND. Len( cMv1Dup ) == Len( SE1->E1_PARCELA ) .AND. cMv1Dup == ( Replicate( "0", Len( SE1->E1_PARCELA ) - 1 ) + "1" )
	nMaxTipo9 := 36
EndIf

If nParcelas > nMaxTipo9
   nParcelas := nMaxTipo9
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ nOpcx: 1 - Inclusao de Registros                     ³
//³ nOpcx: 2 - Alteracao de Registros                    ³
//³ nOpcx: 3 - Exclusao de Registros                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nOpcx <> 3 ) // Inclusao e Alteracao

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se possui um item valido no acols                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 	lGravou := !Empty( AScan( aCols, { |x| !x[nUsadoAB7+1] .AND. !Empty(x[nPPrdAB7] ) } ) )

	DbSelectArea("SA1")
	DbSetOrder(1)
	DbSeek(xFilial("SA1")+M->AB6_CODCLI+M->AB6_LOJA)
	DbSelectArea("AB6")
	DbSetOrder(1)
	DbSeek(xFilial("AB6")+M->AB6_NUMOS)

	lFoundAB6 := AB6->( Found() )

	If lFoundAB6
		nRecnoAB6 := AB6->( Recno() )
	EndIf

	If ( lGravou )
		cRegiao := SA1->A1_REGIAO
		If ExistBlock("T450REGIAO")
			cRegPe := ExecBlock("T450REGIAO",.F.,.F.,{cRegiao})
			If ValType(cRegPe) == "C"
				cRegiao := cRegPe
			Endif
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua a inclusao do cabecalho da OS                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("AB6",nOpcx ==1)
		For nCntFor := 1 To FCount()
			If ( "FILIAL"$Field(nCntFor) )
				FieldPut(nCntFor,xFilial("AB6"))
			Else
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			EndIf
		Next nCntFor

		AB6->AB6_ATEND	:= Upper(AB6->AB6_ATEND)
		AB6->AB6_REGIAO := cRegiao
		AB6->AB6_CONPAG := If(Empty(AB6->AB6_CONPAG),cCond,AB6->AB6_CONPAG)

		nRecnoAB6	:= AB6->( Recno() )
		cOSAuto		:= AB6->AB6_NUMOS
		AB6->( FKCommit() )
		If FindFunction("TECTelMets") .And. (IsInCallStack("AT450Inclu") .Or. IsInCallStack("AT450Alter"))
			If !Empty(AB6->AB6_CONTRT)
				TECTelMets( "contrato_os", CID_METRIC )
			Endif
			If !Empty(AB6->AB6_CDORCS)
				TECTelMets( "codorc_os", CID_METRIC )
			Endif
		Endif
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Acerta o AcolsAB8                                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Empty(aColsAB8) .OR. Len(aColsAB8)<=Len(aCols) )
		aadd(aAux,Array(nUsadoAB8+1))
		For nCntFor := 1 To nUsadoAB8
			If IsHeadRec(aHeaderAB8[nCntFor][2])
				aAux[1][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
				aAux[1][nCntFor] := "AB8"
			Else
				aAux[1][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2],.T.)
			Endif
		Next nCntFor
		aAux[1][nUsadoAB8+1] := .T.
		For nCntFor := 1 To Len(aCols)
			aadd(aColsAB8,aClone(aAux))
		Next nCntFor
	EndIf
	At450Ab8()
	nCntFor := Len(aCols)+1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Insere os registros na tabela de saldo AAR ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	AAQ->(DbSetOrder(1))
	AAR->(DbSetOrder(1))
	For nCntFor2 := 1 To Len(aColsAB8[nCntFor-1])
		If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aColsAB8[nCntFor-1][nCntFor2][nPPrdAB8]))
			AAR->(DbSetOrder(1))
			If AAQ->AAQ_MODLIB == "1"
				If AAR->(!DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAB8[nCntFor-1][nCntFor2][nPPrdAB8])) .And.;
					aColsAB8[nCntFor-1][nCntFor2][nPSerAB8] == AtBuscServ(AA3->AA3_CONTRT,.T.)
					RecLock("AAR", .T.)
					AAR->AAR_FILIAL := xFilial("AAR")
					AAR->AAR_CONTRT := AAQ->AAQ_CONTRT
					AAR->AAR_CODPRO := AAQ->AAQ_CODPRO
					AAR->AAR_SALDO  := AAQ->AAQ_QTDLIB
					AAR->(MsUnLock())
				EndIf
			ElseIf AAQ->AAQ_MODLIB == "2"
				If AAR->(!DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAB8[nCntFor-1][nCntFor2][nPPrdAB8]+LEFT(DTOS(dDataBase),6))) .And.;
					aColsAB8[nCntFor-1][nCntFor2][nPSerAB8] == AtBuscServ(AA3->AA3_CONTRT,.T.)
					RecLock("AAR", .T.)
					AAR->AAR_FILIAL := xFilial("AAR")
					AAR->AAR_CONTRT := AAQ->AAQ_CONTRT
					AAR->AAR_CODPRO := AAQ->AAQ_CODPRO
					AAR->AAR_SALDO  := AAQ->AAQ_QTDLIB
					AAR->AAR_PERIOD := LEFT(DTOS(dDataBase),6)
					AAR->(MsUnLock())
				EndIf
			EndIf
			AAR->(FKCommit())
		EndIf
	Next nCntFor2


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao do Itens                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aCols)
		If ( !aCols[nCntFor][nUsadoAB7+1] .AND. !Empty(aCols[nCntFor][nPPrdAB7]) )
			DbSelectArea("AB7")
			DbSetOrder(1)
			If ( DbSeek(xFilial("AB7")+M->AB6_NUMOS+aCols[nCntFor][nPIteAB7]) )
				nOpcAB7 := 2
				RecLock("AB7",.F.)
				cTipoAnt := AB7->AB7_TIPO
				If cTipoAnt == aCols[nCntFor][nPTipAB7] .AND. !lChanged
					lAtuEqLoc := .F.
				Else
					lChanged := .T.
					lAtuEqLoc := .T.
				EndIf 
			Else
				nOpcAB7 := 1
				RecLock("AB7",.T.)
				cTipoAnt := "0"
			EndIf

			For nCntFor2 := 1 To nUsadoAB7
				If !Empty( nFieldPos := AB7->( FieldPos(aHeaderAB7[nCntFor2][2] ) ) ) .And. ValType(aCols[nCntFor][nCntFor2]) <> "U"
					AB7->(FieldPut( nFieldPos,aCols[nCntFor][nCntFor2]))
				EndIf
			Next nCntFor2
		
			DbSelectArea("AA3")
			DbSetOrder(1)
			If nOpcAB7 == 1

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se for inclusao do item, posiciona pelo cliente              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AA3")
				DbSetOrder(1)
				If AA3->(DbSeek(xFilial("AA3")+ M->AB6_CODCLI + M->AB6_LOJA + ;
					GDFieldGet( "AB7_CODPRO", nCntFor ) + ;
					GDFieldGet( "AB7_NUMSER", nCntFor )))
				
					lSeekBase := .T.
				EndIf
				
				If !lSeekBase
					AA3->(DbSetOrder(6))
					AA3->(DbSeek(xFilial("AA3")+GDFieldGet( "AB7_NUMSER", nCntFor )))
				EndIf

			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se nao, posiciona pelo fabricante + loja                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lCancLib
					DbSelectArea("AA3")
					AA3->(DbSetOrder(6))
					AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))
				Else
					DbSelectArea("AA3")
					AA3->(DbSetOrder(4))
					AA3->(DbSeek(xFilial("AA3")+ AB7->AB7_CODFAB + AB7->AB7_LOJAFA + AB7->AB7_CODPRO + AB7->AB7_NUMSER ))

				Endif

			EndIf

			AB7->AB7_FILIAL   := xFilial("AB7")
			AB7->AB7_NUMOS    := M->AB6_NUMOS
			AB7->AB7_NRCHAM   := If(aChamado<>Nil,aChamado[nCntFor],AB7->AB7_NRCHAM)
			AB7->AB7_NUMORC   := If(aOrcamento<>Nil,aOrcamento[nCntFor],AB7->AB7_NUMORC)
			AB7->AB7_NUMHDE   := If(ValType(aHelpDesk)=="A",aHelpDesk[nCntFor],AB7->AB7_NUMHDE)
			AB7->AB7_CODFAB   := AA3->AA3_CODFAB
			AB7->AB7_LOJAFA   := AA3->AA3_LOJAFA
			AB7->AB7_CODCLI   := M->AB6_CODCLI
			AB7->AB7_LOJA     := M->AB6_LOJA
			AB7->AB7_EMISSA   := M->AB6_EMISSA

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica quais itens gerarao pedidos de venda                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AB7->AB7_TIPO=="2".AND.cTipoAnt<>"2"
				AAdd( aItGrPed, AB7->AB7_ITEM )
				lPv := .T.
				AAdd( aGerouPV, { AB7->AB7_ITEM, .F., cTipoAnt } )
			EndIf

			MSMM(AB7->AB7_MEMO1,,,aCols[nCntFor][nPMem1AB7],1,,,"AB7","AB7_MEMO1") //campo de observação
			MSMM(AB7->AB7_MEMO3,,,aCols[nCntFor][nPMem2AB7],1,,,"AB7","AB7_MEMO3") //campo de solução
			IF AB7->( ColumnPos('AB7_APPSTA') )
				AB7->AB7_APPSTA := atStOS(AB7->AB7_TIPO , 'AB7')
			EndIf
			AB7->( FKCommit() )
			If FindFunction("TECTelMets") .And. (IsInCallStack("AT450Inclu") .Or. IsInCallStack("AT450Alter"))
				DbSelectArea("AAG")
				DbSetOrder(1)
				If DbSeek(xFilial("AAG")+AB7->AB7_CODPRB) .And. AAG->AAG_GERFNC == "1"
					TECTelMets( "fnc_os", CID_METRIC )
				Endif
			Endif
			//Quando for Integração TEC x ATF e for OS de inspeção.
			If lTecAtf .And. TecAtfSeek(AB7->AB7_NUMSER) .And. Empty(AB6->AB6_CDORCS) .And. Empty(AB6_ITORCS)
				If ValType(oTecProvider) <> 'O'
					oTecProvider := TECProvider():New()
				EndIf
								
				If nOpcAB7 == 1
					oTecProvider:InsertTWU( ,;					 //Cod. Movimentação
											AB7->AB7_NUMSER,;	 //Base de atendimento
											AB7->AB7_QTDSEP,;	 //Quantidade de equipamentos bloquados
											'2',;				 //Tipo 1=Reserva;2=Manutenção;3=Outros
											'1',;				 //Tipo OS 1=Sigatec;2=Sigamnt
											AB7->AB7_NUMOS,;	 //Numero da OS
											AB7->AB7_ITEM,;		 //Item da OS
											AB7->AB7_TIPO == '5')//Quando for encerrado realiza o desbloqueio de saldo
				Else
					If nOpcAB7 == 2 
						oTecProvider:DeleteTWU(,'1'+AB7->AB7_NUMOS+AB7->AB7_ITEM) //Tipo+NumOs+ItemOs

						oTecProvider:InsertTWU( ,;					 //Cod. Movimentação
												AB7->AB7_NUMSER,;	 //Base de atendimento
												AB7->AB7_QTDSEP,;	 //Quantidade de equipamentos bloquados
												'2',;				 //Tipo 1=Reserva;2=Manutenção;3=Outros
												'1',;				 //Tipo OS 1=Sigatec;2=Sigamnt
												AB7->AB7_NUMOS,;	 //Numero da OS
												AB7->AB7_ITEM,;		 //Item da OS
												AB7->AB7_TIPO == '5')//Quando for encerrado realiza o desbloqueio de saldo
					EndIf
				EndIf
			EndIf									
			
			If !(IsInCallStack("AT820GRV") .AND. IsInCallStack("AT450Inclu")) .AND. !(IsInCallStack("AT450Alter"))
				//Se veio do 820, nao grava novamente na OS
				AtEqStatus("TECA450",.F.)
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Efetua a Gravacao do Numero da Os no Chamado Tecnico e no Orcamento  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( !Empty(AB7->AB7_NRCHAM) )
				DbSelectArea("AB2")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB2")+AB7->AB7_NRCHAM) )
					RecLock("AB2")
					AB2->AB2_NUMOS := AB7->AB7_NUMOS+AB7->AB7_ITEM
				EndIf
			EndIf
			If ( !Empty(AB7->AB7_NUMORC) )
				DbSelectArea("AB4")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB4")+AB7->AB7_NUMORC) )
					RecLock("AB4")
					AB4->AB4_NUMOS := AB7->AB7_NUMOS+AB7->AB7_ITEM
					DbSelectArea("AB3")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AB3")+AB4->AB4_NUMORC) )
						cCond := AB3->AB3_CONPAG
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza o nr. do chamado tecnico no item da OS ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If !Empty(AB4->AB4_NRCHAM)
						cNrCham := AB4->AB4_NRCHAM
					Endif
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Grava a condicao de pagamento caso haja a mesma no orcamento³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AB6->AB6_CONPAG := If(Empty(AB6->AB6_CONPAG),cCond,AB6->AB6_CONPAG)
			AB6->( FKCommit() )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Efetua a Gravacao do Numero da Os na fila do Help Desk               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( !Empty(AB7->AB7_NUMHDE) )
				ABL->( DbSetOrder( 2 ) )
				If ABL->( DbSeek(xFilial("ABL")+AB7->AB7_NUMHDE ) )
					RecLock("ABL", .F. )
					ABL->ABL_NUMOS := AB7->AB7_NUMOS+AB7->AB7_ITEM
					ABL->( MsUnLock() )
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua a Gravacao dos Sub-Itens                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			For nCntFor2 := 1 To Len(aColsAB8[nCntFor])
				If ( !aColsAB8[nCntFor][nCntFor2][nUsadoAB8+1] .AND. !Empty(aColsAB8[nCntFor][nCntFor2][nPPrdAB8]))
					DbSelectArea("AB8")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AB8")+M->AB6_NUMOS+aCols[nCntFor][nPIteAB7]+aColsAB8[nCntFor][nCntFor2][nPIteAB8]) )
						RecLock("AB8")
					Else
						RecLock("AB8",.T.)
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza o saldo dos produtos que fazem parte do grupo de cobertura no contrato|
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

					AAQ->(DbSetOrder(1))
					AAR->(DbSetOrder(1))
					If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aColsAB8[nCntFor][nCntFor2][nPPrdAB8])) .And.;
						AtBuscServ(AA3->AA3_CONTRT,.T.) == aColsAB8[nCntFor][nCntFor2][nPSerAB8]
						If AAQ->AAQ_MODLIB == "1"
							If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAB8[nCntFor][nCntFor2][nPPrdAB8]))
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += AB8->AB8_QUANT
								AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aColsAB8[nCntFor][nCntFor2][nPQtdAB8])
								AAR->(MsUnlock())
								AAR->(FKCommit())
							EndIf
						ElseIf AAQ->AAQ_MODLIB == "2"
							If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAB8[nCntFor][nCntFor2][nPPrdAB8]+LEFT(DTOS(dDataBase),6)))
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += AB8->AB8_QUANT
								AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aColsAB8[nCntFor][nCntFor2][nPQtdAB8])
								AAR->(MsUnlock())
								AAR->(FKCommit())
							EndIf
						EndIf
					EndIf

					For nCntFor3 := 1 To nUsadoAB8
						If !Empty( nFieldPos := AB8->( FieldPos(aHeaderAB8[nCntFor3][2] ) ) )
							AB8->(FieldPut( nFieldPos,aColsAB8[nCntFor][nCntFor2][nCntFor3]))
						EndIf
					Next nCntFor3
					AB8->AB8_FILIAL   := xFilial("AB8")
					AB8->AB8_NUMOS := M->AB6_NUMOS
					If lNt450Aut
						AB8->AB8_ITEM  := aCols[nCntFor][nPIteAB7]
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Os campos abaixa sao redundantes na base para otimiza ³
					//³ cao de query's atraves de indices                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					AB8->AB8_CODCLI:= M->AB6_CODCLI
					AB8->AB8_LOJA  := M->AB6_LOJA
					AB8->AB8_CODPRD:= AB7->AB7_CODPRO
					AB8->AB8_NUMSER:= AB7->AB7_NUMSER
					AB8->AB8_TIPO  := AB7->AB7_TIPO

					DbSelectArea("AA5")
					DbSetOrder(1)
					DbSeek(xFilial("AA5")+AB8->AB8_CODSER)
					If FindFunction("TECTelMets") .And. (IsInCallStack("AT450Inclu") .Or. IsInCallStack("AT450Alter"))
						If AA5->AA5_ATUOS == "S"
							TECTelMets( "apont_atualiza_os", CID_METRIC )
						ElseIf AA5->AA5_ATUEST == "S"				
							TECTelMets( "apont_estoque_os", CID_METRIC )
						ElseIf AA5->AA5_ATUORC == "S"
							TECTelMets( "apont_orc_os", CID_METRIC )
						Endif
					Endif
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Cria o Almoxarifado se nao existir.                                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					DbSelectArea("SF4")
					DbSetOrder(1)
					If ( DbSeek(xFilial("SF4")+AA5->AA5_TES) .AND. SF4->F4_ESTOQUE=="S" )
						CriaSB2(AB8->AB8_CODPRO,AB8->AB8_LOCAL)
					EndIf
				Else
					DbSelectArea("AB8")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AB8")+M->AB6_NUMOS+aCols[nCntFor][nPIteAB7]+aColsAB8[nCntFor][nCntFor2][nPIteAB8]) )

						AAQ->(DbSetOrder(1))
						AAR->(DbSetOrder(1))
						If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+AB8->AB8_CODPRO))
							If AAQ->AAQ_MODLIB == "1"
								If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO)) .And.;
									AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
									RecLock("AAR",.F.)
									AAR->AAR_SALDO += AB8->AB8_QUANT
									AAR->(MsUnlock())
								EndIf
							ElseIf AAQ->AAQ_MODLIB == "2"
								If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO+LEFT(DTOS(dDataBase),6))) .And.;
									AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
									RecLock("AAR",.F.)
									AAR->AAR_SALDO += AB8->AB8_QUANT
									AAR->(MsUnlock())
								EndIf
							EndIf

						EndIf
						RecLock("AB8")
						DbDelete()
					EndIf
				EndIf
			Next nCntFor2

			AB8->( FKCommit() )
		Else
			DbSelectArea("AB8")
			DbSetOrder(1)
			If DbSeek(xFilial("AB8")+M->AB6_NUMOS+aCols[nCntFor][nPIteAB7])
				While ( !Eof() .AND. AB8->AB8_FILIAL   ==    xFilial("AB8") .AND.;
											AB8->AB8_NUMOS    ==    M->AB6_NUMOS   .AND.;
											AB8->AB8_ITEM     ==    aCols[nCntFor][nPIteAB7])
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Estorna tabela de saldo quando o atendimento for excluido  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

					AAQ->(DbSetOrder(1))
					AAR->(DbSetOrder(1))
					If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+AB8->AB8_CODPRO))
						If AAQ->AAQ_MODLIB == "1"
							If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO)) .And.;
								AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += AB8->AB8_QUANT
								AAR->(MsUnlock())
							EndIf
						ElseIf AAQ->AAQ_MODLIB == "2"
							If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO+LEFT(DTOS(dDataBase),6))) .And.;
								AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += AB8->AB8_QUANT
								AAR->(MsUnlock())
							EndIf
						EndIf
					EndIf

					RecLock("AB8")
					DbDelete()
					DbSelectArea("AB8")
					DbSkip()
				EndDo
				AB8->( FKCommit() )
			EndIf

			DbSelectArea("AB7")
			DbSetOrder(1)
			If ( DbSeek(xFilial("AB7")+M->AB6_NUMOS+aCols[nCntFor][nPIteAB7]) )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retirar amarracao com Chamado Tecnico                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB2")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB2")+AB7->AB7_NRCHAM) )
					RecLock("AB2")
					AB2->AB2_NUMOS  := ""
					AB2->AB2_TIPO   := If( aChamado <> NIL, AB2->AB2_TIPO, "1" )
					AB2->AB2_BXDATA := Ctod("")
					AB2->AB2_BXHORA := ""

					AB2->( FKCommit() )

					DbSelectArea("AB1")
					DbSetOrder(1)
					DbSeek(xFilial("AB1")+AB2->AB2_NRCHAM)
					RecLock("AB1")
					AB1->AB1_STATUS   := If( aChamado <> NIL, AB1->AB1_STATUS, "A" )
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retirar amarracao com Orcamento                             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("AB4")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB4")+AB7->AB7_NUMORC) )
					RecLock("AB4")
					AB4->AB4_NUMOS := ""
					AB4->AB4_TIPO  := "1"
					AB4->AB4_BXHORA:= ""
					AB4->AB4_BXDATA:= Ctod("")

					AB4->( FKCommit() )

					DbSelectArea("AB3")
					DbSetOrder(1)
					DbSeek(xFilial("AB3")+AB4->AB4_NUMORC)
					RecLock("AB3")
					AB3->AB3_STATUS   := "A"
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retirar amarracao com Plano de Manutencao Preventiva        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("ABE")
				DbSetOrder(5)

				AtExcOSPlan(AB7->AB7_NUMOS)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retirar amarracao com Projeto                               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				ABI->( DbSetOrder( 2 ) )
				If ABI->( DbSeek( xFilial( "ABI" ) + AB7->AB7_NUMOS + AB7->AB7_ITEM ) )
					RecLock( "ABI", .F. )
					ABI->ABI_NUMOS := CriaVar( "ABI_NUMOS", .F. )
					ABI->( MsUnLock() )
					ABI->( FKCommit() )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Retirar amarracao com item da fila de Help Desk             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty( AB7->AB7_NUMHDE )
					ABL->( DbSetOrder( 2 ) )
		            If ABL->( DbSeek( xFilial( "ABL" ) + AB7->AB7_NUMHDE ) )
						RecLock( "ABL", .F. )
						ABL->ABL_NUMOS  := CriaVar( "ABL_NUMOS", .F. )
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Retornar o status da fila                                   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aInfoABL := AtInfoABL()
						ABL->ABL_SITUAC := aInfoABL[ 1 ]
						ABL->( MsUnlock() )
						ABL->( FKCommit() )
					EndIf
				EndIf

				MSMM(AB7->AB7_MEMO1,,,,2)
				MSMM(AB7->AB7_MEMO3,,,,2)
				RecLock("AB7",.F.)
				DbDelete()
				MsUnLock()

				AB7->( FKCommit())

				AtEqStatus("TECA450",.T.)

				//Quando for Integração TEC x ATF e for OS de inspeção.
				If lTecAtf .And. TecAtfSeek(AB7->AB7_NUMSER) .And. Empty(AB6->AB6_CDORCS) .And. Empty(AB6_ITORCS)
					If ValType(oTecProvider) <> 'O'
						oTecProvider := TECProvider():New()
					EndIf
					oTecProvider:DeleteTWU(,'1'+AB7->AB7_NUMOS+AB7->AB7_ITEM) //Tipo+NumOs+ItemOs
				EndIf				
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada apos a gravacao do item ( AB7 )            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lAT450GIT
			ExecBlock( "AT450GIT", .F., .F., { nOpcx } )
		EndIf

	Next nCntFor

	If ( lGravou )

		AB6->( MsGoto( nRecnoAB6 ) )
		AB6->AB6_STATUS := AtOsStatus(AB6_NUMOS)
		IF AB6->( ColumnPos('AB6_APPSTA') )
			AB6->AB6_APPSTA := atStOS(AB6->AB6_STATUS , 'AB6')
		EndIf
		AtAtuTMK( AB6->AB6_NUMOS, AB6->AB6_STATUS )	// Atualiza item do TMK

	Else

		If lFoundAB6
			AB6->( MsGoto( nRecnoAB6 ) )
			RecLock("AB6")
			AB6->( DbDelete() )
		EndIF
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada apos a gravacao da OS que retorna se gera ou não pedido de vendas  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lT450PVOS
		lPVtmp := ExecBlock( "T450PVOS", .F., .F.,{lPV})
		lPV    := IIf(ValType(lPVtmp) == "L",lPVtmp,lPV)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta o Pedido de Vendas                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lPV )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta aHeader do SC6                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aStruField := FWSX3Util():GetAllFields("SC6")
		If Len(aStruField) > 0
			For nC := 1 To Len(aStruField)
				cField := aStruField[nC]
				aRet   := FwTamSx3(cField)
				If (((X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
					!( Trim(cField) == "C6_NUM" ) .AND.;
					Trim(cField) <> "C6_QTDEMP"  .AND.;
					Trim(cField) <> "C6_QTDENT") .AND.;
					GetSx3Cache(cField, "X3_NIVEL")) .OR.;
					Trim(cField)=="C6_NUMOS" .OR. ;
					Trim(cField)=="C6_NUMOSFA" .OR. ;
					Trim(cField)=="C6_NUMOP"  .OR. ;
					Trim(cField)=="C6_ITEMOP" .OR. ;
					Trim(cField)=="C6_OP" )
					aAdd(aHeadC6,{ AllTrim(FWX3Titulo(cField)),;
									cField,;
									AllTrim(GetSx3Cache(cField,"X3_PICTURE")),;
									aRet[1],;
									aRet[2],;
									If(Trim(cField)$"C6_NUMOS#C6_NUMOSFA",".F.",AllTrim(GetSx3Cache(cField,"X3_VALID"))),;
									GetSx3Cache(cField,"X3_USADO"),;
									aRet[3],;
									"SC6",;
									GetSx3Cache(cField,"X3_CONTEXT") } )
					If ( AllTrim(cField)=="C6_ITEM" )
						nItSc6 := Len(aHeadC6)
					EndIf
				EndIf
			Next nC
		EndIf

		If AB6->AB6_TPCONT == '1'

			DbSelectArea("AAH")
		    DbSetOrder(1)

		    If DbSeek(xFilial("AAH")+AB6->AB6_CONTRT)

				DbSelectArea("AAS")
				DbSetOrder(1)

				//AAS_FILIAL+AAS_PROPOS+AAS_PREVIS+AAS_CLIENT+AAS_LOJA
				If DbSeek(xFilial("AAS")+AAH->AAH_PROPOS+AAH->AAH_REVPRO)

					lRateio := .T.

					While (AAS->(!EOF()) .AND. AAS->AAS_FILIAL == xFilial("AAS") .AND. ;
					AAS->AAS_PROPOS == AAH->AAH_PROPOS .AND. AAS->AAS_PREVIS == AAH->AAH_REVPRO)
						aAdd(aSocios,{AAS->AAS_CLIENT,AAS->AAS_LOJA,AAS->AAS_PERCEN})
					AAS->(DbSkip())
					End

				EndIf

			EndIf

		ElseIf AB6->AB6_TPCONT == '2'

			DbSelectArea("AAM")
			DbSetOrder(1)

			If DbSeek(xFilial("AAM")+AB6->AB6_CONTRT)

				DbSelectArea("AAS")
				DbSetOrder(1)

				//AAS_FILIAL+AAS_PROPOS+AAS_PREVIS+AAS_CLIENT+AAS_LOJA
				If DbSeek(xFilial("AAS")+AAM->AAM_PROPOS+AAM->AAM_REVPRO)

					lRateio := .T.

					While (AAS->(!EOF()) .AND. AAS->AAS_FILIAL == xFilial("AAS") .AND. ;
					AAS->AAS_PROPOS == AAM->AAM_PROPOS .AND. AAS->AAS_PREVIS == AAM->AAM_REVPRO)
						aAdd(aSocios,{AAS->AAS_CLIENT,AAS->AAS_LOJA,AAS->AAS_PERCEN})
					AAS->(DbSkip())
					End

				EndIf

			EndIf

		EndIf


		If !lRateio
			aAdd(aSocios,{AB6->AB6_CODCLI,AB6->AB6_LOJA})
		EndIf


		DbSelectArea("AB8")
		DbSetOrder(1)
		DbSeek(xFilial("AB8")+AB6->AB6_NUMOS)

		For nX := 1  To Len(aSocios)

		    aAreaAB6_ := AB6->(GetArea())
		    aAreaAB8_ := AB8->(GetArea())

			While ( !Eof() .AND. AB8->AB8_FILIAL == xFilial("AB8") .AND.;
									AB8->AB8_NUMOS== AB6->AB6_NUMOS )

				lGerouPV := .F.

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se deve gerar pedidos deste item            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty( AScan( aItGrPed, AB8->AB8_ITEM ) )

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Posiciona Registros                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					DbSelectArea("SA1")
					DbSetOrder(1)
					DbSeek(xFilial("SA1")+aSocios[nX][1]+aSocios[nX][2])
					RecLock("SA1")

					DbSelectArea("SB1")
					DbSetOrder(1)
					DbSeek(xFilial("SB1")+AB8->AB8_CODPRO)
					nPosB1 := SB1->(RECNO())
					
					DbSelectArea("SB2")
					DbSetOrder(1)
					DbSeek(xFilial("SB2")+AB8->AB8_CODPRO+RetFldProd(SB1->B1_COD,"B1_LOCPAD"))

					DbSelectArea("AA5")
					DbSetOrder(1)
					DbSeek(xFilial("AA5")+AB8->AB8_CODSER)

					DbSelectArea("SF4")
					DbSetOrder(1)
					DbSeek(xFilial("SF4")+AA5->AA5_TES)

					DbSelectArea("SE4")
					DbSetOrder(1)
					If DbSeek(xFilial("SE4")+AB6->AB6_CONPAG)
						nAcrsFin := SE4->E4_ACRSFIN
					Endif

					DbSelectArea("AA3")
					DbSetOrder(1)
					DbSeek(xFilial("AA3")+AB8->AB8_CODCLI+AB8->AB8_LOJA+AB8->AB8_CODPRD+AB8->AB8_NUMSER)
					
					If lMVGsLocPV
						DbSelectArea("AB9")
						DbSetOrder(1)
						DbSeek(xFilial("AB9")+AB7->AB7_NUMOS+AB7->AB7_ITEM)
						
						DbSelectArea("ABA")
						DbSetOrder(1)
						DbSeek(xFilial("ABA")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)
					EndIf
					If ( AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"C", .T.) > 0 )
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Preenche o aCols do SC6                              ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aadd(aColsC6,Array(Len(aHeadC6)+1))
						nAcols := Len(aColsC6)
						aColsC6[nAcols,Len(aHeadC6)+1] := .F.

						lGerouPV := .T.

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Define o CFO                                         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aDadosCFO := {}
					 	Aadd(aDadosCfo,{"OPERNF","S"})
					 	Aadd(aDadosCfo,{"TPCLIFOR",SA1->A1_TIPO})
					 	Aadd(aDadosCfo,{"UFDEST"  ,SA1->A1_EST})
					 	AAdd(aDadosCfo,{"INSCR"   ,SA1->A1_INSCR})
						cCfo := MaFisCfo(,SF4->F4_CF,aDadosCfo)

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Alimenta os campos definidos pelo usuario            ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aUserCpoPv := {}

						If lATCPPVOS
							aUserCpoPv := ExecBlock( "ATCPPVOS", .F., .F., { "1" } )
						EndIf

						nValorPRC := 0

						If lRateio

							cItInt := AB8->AB8_ITEM+AB8->AB8_SUBITE

							If lCarga
								aAdd(aQtdXAB8,{cItInt,AB8->AB8_QUANT})
							EndIf

							If (nX == Len(aSocios))
								nY	   := aScan(aQtdXAB8,{|x| x[1] == cItInt})
								nQuant := aQtdXAB8[nY][2]
							Else
								nQuant := Round((AB8->AB8_QUANT/100) * aSocios[nX][3],TAMSX3("AB8_QUANT")[2])
								nY 	   := aScan(aQtdXAB8,{|x| x[1] == cItInt })
				   				aQtdXAB8[nY][2] -= nQuant
							EndIf

						Else
							nQuant := AB8->AB8_QUANT
						EndIf
						For nCntFor := 1 To Len(aHeadC6)
							Do Case
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_ITEM" )
									aColsC6[nAcols,nCntFor] := "01"
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRODUTO" )
									aColsC6[nAcols,nCntFor] := AB8->AB8_CODPRO
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CODISS" )
									aColsC6[nAcols,nCntFor] := SB1->B1_CODISS
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_UM" )
									If !EMPTY(nPosB1)
										SB1->(DbGoTo(nPosB1))
									EndIf
									aColsC6[nAcols,nCntFor] := SB1->B1_UM
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_QTDVEN" )
									aColsC6[nAcols,nCntFor] := nQuant
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRCVEN" )
									If cPaisLoc <> "BRA"
										If lCposDesc //-> Trata desconto por item. (ver documentacao.)
											nValorPRC := AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"C", .T.)/AB8->AB8_QUANT
										Else
											nValorPRC := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"C", .T.)/AB8->AB8_QUANT
										EndIf
										nValorPRC := Round(xMoeda(nValorPRC, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
									Else
										If lCposDesc //-> Trata desconto por item. (ver documentacao.)
											nValorPRC := Round(AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"C", .T.)/AB8->AB8_QUANT,nDecs)
										Else
											nValorPRC := Round(AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"C", .T.)/AB8->AB8_QUANT,nDecs)
										EndIf
									EndIf
									aColsC6[nAcols,nCntFor] := nValorPRC
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_VALOR" )
									If cPaisLoc <> "BRA"
										If lCposDesc //-> Trata desconto por item. (ver documentacao.)
											nValor := IIf(nValorPRC > 0,nValorPRC * nQuant,AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"C", .T.))
										Else
											nValor := IIf(nValorPRC > 0,nValorPRC * nQuant,AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"C", .T.))
										EndIf
										nValor := Round(xMoeda(nValor, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
									Else
										If lCposDesc //-> Trata desconto por item. (ver documentacao.)
											nValor := IIf(nValorPRC > 0,nValorPRC * nQuant,AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"C", .T.))
										Else
											nValor := IIf(nValorPRC > 0,nValorPRC * nQuant,AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"C", .T.))
										EndIf
									EndIf
									aColsC6[nAcols,nCntFor] := nValor
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_TES" )
									aColsC6[nAcols,nCntFor] := AA5->AA5_TES
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CF" )
									aColsC6[nAcols,nCntFor] := cCfo
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CLASFIS" )
									aColsC6[nAcols,nCntFor] := (Substr(SB1->B1_ORIGEM,1,1)+SF4->F4_SITTRIB)
	 							Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_SEGUM" )
									aColsC6[nAcols,nCntFor] := SB1->B1_SEGUM
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_LOCAL" )
									aColsC6[nAcols,nCntFor] := AB8->AB8_LOCAL
								Case ( lMVGsLocPV .AND. AllTrim(aHeadC6[nCntFor,2]) == "C6_LOCALIZ" ) 
									aColsC6[nAcols,nCntFor] := ABA->ABA_LOCALI
								Case (lMVGsLocPV .AND. AllTrim(aHeadC6[nCntFor,2]) == "C6_NUMSERI" )
									aColsC6[nAcols,nCntFor] := ABA->ABA_NUMSER
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_ENTREG" )
									aColsC6[nAcols,nCntFor] := dDataBase
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PEDCLI" )
									aColsC6[nAcols,nCntFor] := AB8->AB8_NUMOS
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_DESCRI" )
									aColsC6[nAcols,nCntFor] := AB8->AB8_DESPRO
								Case ( lMVGsLocPV .AND. AllTrim(aHeadC6[nCntFor,2]) == "C6_LOTECTL" )
									aColsC6[nAcols,nCntFor] := ABA->ABA_LOTECT
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRUNIT" )
								    If cPaisLoc <> "BRA"
								       	nValor := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_PRCLIS,"C", .T.,AllTrim(aHeadC6[nCntFor,2]))
								       	nValor := Round(xMoeda(nValor, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
								    Else
								    	nValor := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_PRCLIS,"C", .T.,AllTrim(aHeadC6[nCntFor,2]))
								    EndIf
									aColsC6[nAcols,nCntFor] := nValor
								Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_NUMOS" )
									aColsC6[nAcols,nCntFor] := AB8->AB8_NUMOS+AB8->AB8_ITEM+AB8->AB8_SUBITE
								Case ( lCposDesc .And. AllTrim(aHeadC6[nCntFor,2]) == "C6_DESCONT" ) //-> Trata desconto por item. (ver documentacao.)
									aColsC6[nAcols,nCntFor] := AB8->AB8_PCDESC
								Case ( lCposDesc .And. AllTrim(aHeadC6[nCntFor,2]) == "C6_VALDESC" ) //-> Trata desconto por item. (ver documentacao.)
									aColsC6[nAcols,nCntFor] := AB8->AB8_VLDESC
								Case IsHeadRec(aHeadC6[nCntFor,2])
									aColsC6[nAcols,nCntFor] := AB8->(Recno())
								Case IsHeadAlias(aHeadC6[nCntFor,2])
									aColsC6[nAcols,nCntFor] := "AB8"
								OtherWise
									aColsC6[nAcols,nCntFor] := CriaVar(aHeadC6[nCntFor,2],.T.)
							EndCase

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Preenche os campos de usuario                        ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If !Empty( nPosUsrCpo := aScan( aUserCpoPV, { |x| AllTrim( x[1] ) == AllTrim(aHeadC6[nCntFor,2] ) } ) )
								aColsC6[nAcols,nCntFor] := aUserCpoPV[ nPosUsrCpo, 2 ]
							EndIf

						Next nCntFor
					EndIf

					If lCarga

						If ( AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"F",.T.) > 0 .AND. !Empty(AA3->AA3_CODFAB))
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Preenche o aCols do SC6                              ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							aColsF := {}
							aadd(aColsF,Array(Len(aHeadC6)+1))
							nAcols := Len(aColsF)
							aColsF[nAcols,Len(aHeadC6)+1] := .F.

							lGerouPV := .T.

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Posiciona Registros                                  ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							SA1->( DbSetOrder( 1 ) )
							SA1->( DbSeek(xFilial("SA1") + AA3->AA3_CODFAB+AA3->AA3_LOJAFA ) )

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Define o CFO                                         ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							aDadosCFO := {}
						 	Aadd(aDadosCfo,{"OPERNF","S"})
						 	Aadd(aDadosCfo,{"TPCLIFOR",SA1->A1_TIPO})
						 	Aadd(aDadosCfo,{"UFDEST"  ,SA1->A1_EST})
							cCfo := MaFisCfo(,SF4->F4_CF,aDadosCfo)

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Alimenta os campos definidos pelo usuario            ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							aUserCpoPv := {}

							If lATCPPVOS
								aUserCpoPv := ExecBlock( "ATCPPVOS", .F., .F., { "2" } )
							EndIf

							nValorPRC := 0

							For nCntFor := 1 To Len(aHeadC6)
								Do Case
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_ITEM" )
										aColsF[nAcols,nCntFor] := "01"
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRODUTO" )
										aColsF[nAcols,nCntFor] := AB8->AB8_CODPRO
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CODISS" )
										aColsF[nAcols,nCntFor] := SB1->B1_CODISS
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_UM" )
										aColsF[nAcols,nCntFor] := SB1->B1_UM
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_QTDVEN" )
										aColsF[nAcols,nCntFor] := AB8->AB8_QUANT
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRCVEN" )
										If cPaisLoc <> "BRA"
											If lCposDesc //-> Trata desconto por item. (ver documentacao.)
												nValorPRC := AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"F", .T.)/AB8->AB8_QUANT
											Else
												nValorPRC := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"F", .T.)/AB8->AB8_QUANT
											EndIf
											nValorPRC := Round(xMoeda(nValorPRC, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
										Else
											If lCposDesc //-> Trata desconto por item. (ver documentacao.)
												nValorPRC := Round(AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"F", .T.)/AB8->AB8_QUANT , nDecs)
											Else
												nValorPRC := Round(AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"F", .T.)/AB8->AB8_QUANT , nDecs)
											EndIf
										EndIf
										aColsF[nAcols,nCntFor] := nValorPRC
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_VALOR" )
										If cPaisLoc <> "BRA"
											If lCposDesc //-> Trata desconto por item. (ver documentacao.)
												nValor := IIf(nValorPRC > 0,nValorPRC * AB8->AB8_QUANT,AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"F", .T.))
											Else
												nValor := IIf(nValorPRC > 0,nValorPRC * AB8->AB8_QUANT,AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"F", .T.))
											EndIF
											nValor := Round(xMoeda(nValor, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
										Else
											If lCposDesc //-> Trata desconto por item. (ver documentacao.)
												nValor := IIf(nValorPRC > 0,nValorPRC * AB8->AB8_QUANT,AtVlrPagto(AB8->AB8_CODSER,AB8->(AB8_TOTAL - AB8_VLDESC),"F", .T.))
											Else
												nValor := IIf(nValorPRC > 0,nValorPRC * AB8->AB8_QUANT,AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_TOTAL,"F", .T.))
											EndIf
										EndIf
										aColsF[nAcols,nCntFor] := nValor
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_TES" )
										aColsF[nAcols,nCntFor] := AA5->AA5_TES
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CF" )
										aColsF[nAcols,nCntFor] := cCfo
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_CLASFIS" )
										aColsF[nAcols,nCntFor] := (Substr(SB1->B1_ORIGEM,1,1)+SF4->F4_SITTRIB)
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_SEGUM" )
										aColsF[nAcols,nCntFor] := SB1->B1_SEGUM
									Case ( lMVGsLocPV .AND. AllTrim(aHeadC6[nCntFor,2]) == "C6_LOCAL" )
										aColsF[nAcols,nCntFor] := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_LOCALIZ" )
										aColsF[nAcols,nCntFor] := ABA->ABA_LOCALI
									Case ( lMVGsLocPV .AND. AllTrim(aHeadC6[nCntFor,2]) == "C6_NUMSERI" )
										aColsF[nAcols,nCntFor] := ABA->ABA_NUMSER
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_ENTREG" )
										aColsF[nAcols,nCntFor] := dDataBase
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PEDCLI" )
										aColsF[nAcols,nCntFor] := AB8->AB8_NUMOS
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_DESCRI" )
										aColsF[nAcols,nCntFor] := AB8->AB8_DESPRO
									Case ( lMVGsLocPV .AND.  AllTrim(aHeadC6[nCntFor,2]) == "C6_LOTECTL" )
										aColsF[nAcols,nCntFor] := ABA->ABA_LOTECT
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_PRUNIT" )
										If cPaisLoc <> "BRA"
											nValor := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_PRCLIS,"F", .T.,AllTrim(aHeadC6[nCntFor,2]))
											nValor := Round(xMoeda(nValor, AB6->AB6_MOEDA, nMoeda, dDataBase, nDecs+1, AB6->AB6_TXMOED), nDecs)
										Else
											nValor := AtVlrPagto(AB8->AB8_CODSER,AB8->AB8_PRCLIS,"F", .T.,AllTrim(aHeadC6[nCntFor,2]))
										EndIf
										aColsF[nAcols,nCntFor] := nValor
									Case ( AllTrim(aHeadC6[nCntFor,2]) == "C6_NUMOSFA" )
										aColsF[nAcols,nCntFor] := AB8->AB8_NUMOS+AB8->AB8_ITEM+AB8->AB8_SUBITE
									Case ( lCposDesc .And. AllTrim(aHeadC6[nCntFor,2]) == "C6_DESCONT" ) //-> Trata desconto por item. (ver documentacao.)
										aColsF[nAcols,nCntFor] := AB8->AB8_PCDESC
									Case ( lCposDesc .And. AllTrim(aHeadC6[nCntFor,2]) == "C6_VALDESC" ) //-> Trata desconto por item. (ver documentacao.)
										aColsF[nAcols,nCntFor] := AB8->AB8_VLDESC
									Case IsHeadRec(aHeadC6[nCntFor,2])
										aColsF[nAcols,nCntFor] := AB8->(Recno())
									Case IsHeadAlias(aHeadC6[nCntFor,2])
										aColsF[nAcols,nCntFor] := "AB8"
									OtherWise
										aColsF[nAcols,nCntFor] := CriaVar(aHeadC6[nCntFor,2],.T.)
								EndCase

								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Preenche os campos de usuario                        ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If !Empty( nPosUsrCpo := aScan( aUserCpoPV, { |x| AllTrim( x[1] ) == AllTrim(aHeadC6[nCntFor,2] ) } ) )
									aColsF[nAcols,nCntFor] := aUserCpoPV[ nPosUsrCpo, 2 ]
								EndIf

							Next nCntFor
							nFabric := aScan(aFabric,{|x| x[1]==AA3->AA3_CODFAB+AA3->AA3_LOJAFA})
							If ( nFabric == 0 )
								aadd(aFabric,{ AA3->AA3_CODFAB+AA3->AA3_LOJAFA , aClone(aColsF) })
							Else
								aadd(aFabric[nFabric,2],aColsF[1])
							EndIf
						EndIf
		            EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Cria um array por item                               ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Empty( nScanIt := AScan( aGerouPV, { |x| x[1] == AB8->AB8_ITEM } ) )
						AAdd( aGerouPV, { AB8->AB8_ITEM, .F., "5" } )
					Else
						aGerouPV[ nScanIt, 2 ] := If( aGerouPV[ nScanIt, 2 ], .T., lGerouPV )
					EndIf

				EndIf

				DbSelectArea("AB8")
				DbSkip()
			EndDo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Prepara para grava PV do Cliente                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Len(aColsC6) > 0 )
				aHeader := aHeadC6
				aCols   := aColsC6
				cItSc6  := "01"
				For nCntFor := 1 To Len(aCols)
					aCols[nCntFor][nItSc6] := cItSc6
					cItSc6 := Soma1(cItSc6,2)
				Next nCntFor
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica a existencia de contrato para OS            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ


			   cAliasPesq = "AAH"
			   cTipoCont = "1"

	      	 	If AB6->AB6_TPCONT=="2"
                                  cAliasPesq = "AAM"
                                  cTipoCont = "2"
 		       Endif

  	 	           (cAliasPesq)->(DbSetOrder(1),DbSeek(xFilial(cAliasPesq)+AB6->AB6_CONTRT))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava Pedido para o Cliente                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("SA1")
				DbSetOrder(1)
				DbSeek(xFilial("SA1")+aSocios[nX][1]+aSocios[nX][2])
				RecLock("SA1")
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria as Variaveis do Pedido de Venda                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("SC5")
				For nCntFor := 1 To FCount()
					M->&(EVAL(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
				Next nCntFor
				M->C5_TIPO    := "N"
				M->C5_CLIENTE := aSocios[nX][1]
				M->C5_LOJACLI := aSocios[nX][2]
				M->C5_CLIENT  := Iif(val(cTipoCont)>0,(Iif(cTipoCont="1",Iif(!Empty(AAH->AAH_CLIENT),AAH->AAH_CLIENT,AB6->AB6_CODCLI),IIF(!Empty(AAM->AAM_CLIENT),AAM->AAM_CLIENT,AB6->AB6_CODCLI))),AB6->AB6_CODCLI)        ///  Alterado para assumir cliente de entrega cadastrado no contrato
				M->C5_LOJAENT := Iif(val(cTipoCont)>0,(Iif(cTipoCont="1",Iif(!Empty(AAH->AAH_LOJENT),AAH->AAH_LOJENT,AB6->AB6_LOJA  ),IIF(!Empty(AAM->AAM_LOJENT),AAM->AAM_LOJENT,AB6->AB6_LOJA))),AB6->AB6_LOJA)		 	 ///  Alterado para assumir Loja do cliente de entrega cadastrado no contrato
				M->C5_TIPOCLI := SA1->A1_TIPO
				M->C5_CONDPAG := AB6->AB6_CONPAG
				M->C5_ACRSFIN := nAcrsFin

				For nCntFor :=  1 To nParcelas
					cCampo := SubStr( cParcela, nCntFor, 1 )
					cCampo := "AB6_PARC"+cCampo
					nPos1 := AB6->(FieldPos(cCampo))
					If nPos1 > 0
						cCampo := "C5"+SubStr(cCampo,4)
						M->&(cCampo) := AB6->(FieldGet(nPos1))
					EndIf
					cCampo := SubStr( cParcela, nCntFor, 1 )
					cCampo := "AB6_DATA"+cCampo
					nPos1 := AB6->(FieldPos(cCampo))
					If nPos1 > 0
						cCampo := "C5"+SubStr(cCampo,4)
						M->&(cCampo) := AB6->(FieldGet(nPos1))
					EndIf
				Next nCntFor

				M->C5_TABELA  := AB6->AB6_TABELA
				M->C5_MOEDA   := nMoeda

				If (AB6->AB6_MOEDA == nMoeda) .AND. (AB6->AB6_TXMOED > 0)
					M->C5_TXMOEDA := AB6->AB6_TXMOED
				Else
					M->C5_TXMOEDA := Recmoeda( dDatabase, M->C5_MOEDA )
				EndIf

				//-> Trata desconto por item. (ver documentacao.)
				If lCposDesc .And. ( AB6->(AB6_DESC1 + AB6_DESC2 + AB6_DESC3 + AB6_DESC4) = 0  )

					aRetDescs := TecAtDsOrc(AB6->AB6_NUMOS+'01')
					M->C5_DESC1   := aRetDescs[1]
					M->C5_DESC2   := aRetDescs[2]
					M->C5_DESC3   := aRetDescs[3]
					M->C5_DESC4   := aRetDescs[4]
				Else
					M->C5_DESC1   := AB6->AB6_DESC1
					M->C5_DESC2   := AB6->AB6_DESC2
					M->C5_DESC3   := AB6->AB6_DESC3
					M->C5_DESC4   := AB6->AB6_DESC4
				EndIf

				cMenNota      := Formula( SA1->A1_MENSAGE )
				M->C5_MENNOTA := If( ValType( cMenNota ) == "C", cMenNota, "" )

				lInclBack  := INCLUI
				INCLUI     := .T.

				If lAt410Grv
					Execblock("AT410GRV",.F.,.F.,{1})
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava o Pedido de Venda (SC5/SC6) ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				a410Grava(.F.,.F.)
				If ( __lSx8 )
					ConfirmSx8()
				EndIf
				EvalTrigger()

				INCLUI := lInclBack

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Event Viewer - Envia e-mail ou RSS para    ³
				//| Geracao de Pedido de Venda - Field Service.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				cEventID   := "010"
				cMensagem  := SC5->C5_NUM + SA1->A1_COD + "/" + SA1->A1_LOJA + "-" + AllTrim(SA1->A1_NOME)+ " " + AB6->AB6_NUMOS
				EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID, FW_EV_LEVEL_INFO,""/*cCargo*/,STR0020,cMensagem)

			EndIf

			RestArea(aAreaAB6_)
			RestArea(aAreaAB8_)
			lCarga 	:= .F.
			aColsC6 := {}

		Next nX

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Prepara para gravar o PV do Fabricante                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Len(aFabric) > 0 )
			aHeader := aHeadC6
			For nCntFor := 1 To Len(aFabric)
				aCols   := aFabric[nCntFor][2]
				cItSc6  := "01"
				For nCntFor2 := 1 To Len(aCols)
					aCols[nCntFor2][nItSc6] := cItSc6
					cItSc6 := Soma1(cItSc6,2)
				Next nCntFor2
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava Pedido para o Cliente                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("SA1")
				DbSetOrder(1)
				DbSeek(xFilial("SA1")+aFabric[nCntFor][1])
				RecLock("SA1")
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria as Variaveis do Pedido de Venda                 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("SC5")
				For nCntFor := 1 To FCount()
					M->&(EVAL(bCampo,nCntFor)) := CriaVar(FieldName(nCntFor),.T.)
				Next nCntFor
				M->C5_TIPO    := "N"
				M->C5_CLIENTE := SA1->A1_COD
				M->C5_LOJAENT := SA1->A1_LOJA
				M->C5_LOJACLI := SA1->A1_LOJA
				M->C5_TIPOCLI := SA1->A1_TIPO
				M->C5_CONDPAG := AB6->AB6_CONPAG
				M->C5_ACRSFIN := nAcrsFin

				For nCntFor :=  1 To nParcelas
					cCampo := SubStr( cParcela, nCntFor, 1 )
					cCampo := "AB6_PARC"+cCampo
					nPos1 := AB6->(FieldPos(cCampo))
					If nPos1 > 0
						cCampo := "C5"+SubStr(cCampo,4)
						M->&(cCampo) := AB6->(FieldGet(nPos1))
					EndIf
					cCampo := SubStr( cParcela, nCntFor, 1 )
					cCampo := "AB6_DATA"+cCampo
					nPos1 := AB6->(FieldPos(cCampo))
					If nPos1 > 0
						cCampo := "C5"+SubStr(cCampo,4)
						M->&(cCampo) := AB6->(FieldGet(nPos1))
					EndIf
				Next nCntFor

				M->C5_TABELA  := AB6->AB6_TABELA
				M->C5_MOEDA   := nMoeda

				If (AB6->AB6_MOEDA == nMoeda) .AND. (AB6->AB6_TXMOED > 0)
					M->C5_TXMOEDA := AB6->AB6_TXMOED
				Else
					M->C5_TXMOEDA := Recmoeda( dDatabase, M->C5_MOEDA )
				EndIf

				M->C5_DESC1   := AB6->AB6_DESC1
				M->C5_DESC2   := AB6->AB6_DESC2
				M->C5_DESC3   := AB6->AB6_DESC3
				M->C5_DESC4   := AB6->AB6_DESC4

				cMenNota      := Formula( SA1->A1_MENSAGE )
				M->C5_MENNOTA := If( ValType( cMenNota ) == "C", cMenNota, "" )

				lInclBack  := INCLUI
				INCLUI     := .T.

				If lAt410Grv
					Execblock("AT410GRV",.F.,.F.,{2})
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Grava o Pedido de Venda (SC5/SC6) ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				a410Grava(.F.,.F.)
				If ( __lSx8 )
					ConfirmSx8()
				EndIf
				EvalTrigger()

				INCLUI := lInclBack

		        //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		        //³ Event Viewer - Envia e-mail ou RSS para    ³
		   		//| Geracao de Pedido de Venda - Field Service.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				cEventID  := "010"
		   		cMensagem := SC5->C5_NUM + SA1->A1_COD + "/" + SA1->A1_LOJA + "-" + AllTrim(SA1->A1_NOME)+ " " + AB6->AB6_NUMOS
				EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID, FW_EV_LEVEL_INFO,""/*cCargo*/,STR0020,cMensagem)

			Next nCntFor
		EndIf
		If ( lTECXSC5 )
			ExecBlock("TECXSC5",.F.,.F.)
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Prepara para efetuar a alocacao da ordem de servico                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lGravou .AND. !lPV )
		If ( SuperGetMV("MV_ALOCTEC") .AND. AB6->AB6_STATUS == "A" )
			AtAlocTec(AB6->AB6_NUMOS)
		EndIf
	Else
		If ( !lPV )
			AtRefazOs(M->AB6_NUMOS,.T.,,.T.)
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Altera o Status para anterior ou encerrado se nao gerou nenhum PV      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lPv
		nRecAB7Ant := AB7->( RecNo() )
		For nCntFor := 1 To Len( aGerouPV )
			If !aGerouPV[ nCntFor, 2 ]
				AB7->( DbSetOrder( 1 ) )
				If AB7->( DbSeek( xFilial( "AB7" ) + M->AB6_NUMOS + aGerouPV[ nCntFor, 1 ] ) )
					If !IsInCallStack('AT450Efet')

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Refaz o status do Item                                                 ³
						//³ Caso o item estivesse com status 1 ( OS ) ou 0 ( Incluido agora )      ³
						//³ coloca status 5 ( encerrado ).Caso contrario,retorna o status original ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ						
						RecLock( "AB7", .F. )
						AB7->AB7_TIPO := If( aGerouPV[ nCntFor, 3 ] == "1" .OR. aGerouPV[ nCntFor, 3 ] == "0",;
										 "5", aGerouPV[ nCntFor, 3 ] )
						AB7->( MsUnLock() )
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Refaz o status da OS                                                   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						AB6->( MsGoto( nRecnoAB6 ) )
						RecLock( "AB6", .F. )
						AB6->AB6_STATUS := AtOsStatus(M->AB6_NUMOS)
						AB6->( MsUnLock() )
					
					Else
						RecLock( "AB7", .F. )
						AB7->AB7_TIPO := IIF( aGerouPV[ nCntFor, 3 ] == "1" .OR. aGerouPV[ nCntFor, 3 ] == "4",;
										"5", aGerouPV[ nCntFor, 3 ])
						AB7->( MsUnLock() )
						
						If AB6->AB6_STATUS == "E" .And. AB7->AB7_TIPO == "3"
							RecLock( "AB7", .F. )
							AB7->AB7_TIPO := "4"
							AB7->( MsUnLock() )
						EndIf 
					EndIf
				EndIf

			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Caso tenha gerado pedido atualiza Situacao do Chamado Tecnico ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AB7->( DbSetOrder( 1 ) )
				If AB7->( DbSeek( xFilial( "AB7" ) + M->AB6_NUMOS + aGerouPV[ nCntFor, 1 ] ) )
                	DbSelectArea("AB2")
                	DbSetOrder(1)
					If DbSeek( xFilial("AB2") + cNrCham )
						RecLock("AB2",.F.)
						AB2->AB2_TIPO := "5" 		//Encerrado
						AB2->( MsUnLock() )
                	Endif
				Endif

			EndIf
		Next nCntFor
		AB7->( dbGoto( nRecAB7Ant ) )
	EndIf

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Efetua a Exclusao das Ordens de Servico Alocadas ( ABB )                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AtRefazOs(AB6->AB6_NUMOS,.T.,,.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Exclusao dos Itens                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB8")
	DbSetOrder(1)
	If DbSeek(xFilial("AB8")+AB6->AB6_NUMOS)
		While ( !Eof() .AND. AB8->AB8_FILIAL   ==    xFilial("AB8") .AND.;
									AB8->AB8_NUMOS ==    AB6->AB6_NUMOS )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Estorna tabela de saldo quando o atendimento for excluido  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			AAQ->(DbSetOrder(1))
			AAR->(DbSetOrder(1))
			If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+AB8->AB8_CODPRO))
				If AAQ->AAQ_MODLIB == "1"
					If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO)) .And.;
						AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
						RecLock("AAR",.F.)
						AAR->AAR_SALDO += AB8->AB8_QUANT
						AAR->(MsUnlock())
					EndIf
				ElseIf AAQ->AAQ_MODLIB == "2"
					If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+AB8->AB8_CODPRO+LEFT(DTOS(dDataBase),6))) .And.;
						AB8->AB8_CODSER == AtBuscServ(AA3->AA3_CONTRT,.T.)
						RecLock("AAR",.F.)
						AAR->AAR_SALDO += AB8->AB8_QUANT
						AAR->(MsUnlock())
					EndIf
				EndIf
			EndIf

			RecLock("AB8")
			DbDelete()
			DbSelectArea("AB8")
			DbSkip()
		EndDo
		AB8->( FKCommit() )
	EndIf

	DbSelectArea("AB7")
	DbSetOrder(1)
	If DbSeek(xFilial("AB7")+AB6->AB6_NUMOS)
		While ( !Eof() .AND. AB7->AB7_FILIAL   ==    xFilial("AB7") .AND.;
									AB6->AB6_NUMOS    ==    AB7->AB7_NUMOS )
			DbSelectArea("AB2")
			DbSetOrder(1)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retirar amarracao com Chamado Tecnico                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( DbSeek(xFilial("AB2")+AB7->AB7_NRCHAM) )
				RecLock("AB2")
				AB2->AB2_NUMOS := ""
				AB2->AB2_TIPO     := "1"
				AB2->AB2_BXDATA := Ctod("")
				AB2->AB2_BXHORA := ""

				AB2->( FKCommit() )

				DbSelectArea("AB1")
				DbSetOrder(1)
				DbSeek(xFilial("AB1")+AB2->AB2_NRCHAM)
				RecLock("AB1")
				AB1->AB1_STATUS   := "A"
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retirar amarracao com Orcamento                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB4")
			DbSetOrder(1)
			If ( DbSeek(xFilial("AB4")+AB7->AB7_NUMORC) )
				RecLock("AB4")
				AB4->AB4_NUMOS := ""
				AB4->AB4_TIPO  := "1"
				AB4->AB4_BXHORA:= ""
				AB4->AB4_BXDATA:= Ctod("")

				AB4->( FKCommit() )

				DbSelectArea("AB3")
				DbSetOrder(1)
				DbSeek(xFilial("AB3")+AB4->AB4_NUMORC)
				RecLock("AB3")
				AB3->AB3_STATUS   := "A"
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retirar amarracao com Plano de Manutencao Preventiva        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("ABE")
			DbSetOrder(5)

			AtExcOSPlan(AB7->AB7_NUMOS)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retirar amarracao com Projeto                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			ABI->( DbSetOrder( 2 ) )
			If ABI->( DbSeek( xFilial( "ABI" ) + AB7->AB7_NUMOS + AB7->AB7_ITEM ) )
				RecLock( "ABI", .F. )
				ABI->ABI_NUMOS := CriaVar( "ABI_NUMOS", .F. )
				ABI->( MsUnLock() )
				ABI->( FKCommit() )
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Retirar amarracao com item da fila de Help Desk             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty( AB7->AB7_NUMHDE )
				ABL->( DbSetOrder( 2 ) )
				If ABL->( DbSeek( xFilial( "ABL" ) + AB7->AB7_NUMHDE ) )
					RecLock( "ABL", .F. )
					ABL->ABL_NUMOS  := CriaVar( "ABL_NUMOS", .F. )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Retornar o status da fila                                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aInfoABL := AtInfoABL()
					ABL->ABL_SITUAC := aInfoABL[ 1 ]
					ABL->( MsUnlock() )
					ABL->( FKCommit() )
				EndIf
			EndIf

			MSMM(AB7->AB7_MEMO1,,,,2)
			MSMM(AB7->AB7_MEMO3,,,,2)

			If lGSOpTri .OR. IsInCallStack("AT450EXCLU")
				lSeekAA3 := AtPosAA3(cFilAnt+AB7->AB7_NUMSER, AB7->AB7_CODPRO)
			Else
				DbSelectArea("AA3")
				DbSetOrder(6)
				lSeekAA3 := AA3->( DbSeek( xFilial( "AA3" ) + AB7->AB7_NUMSER ) )
			EndIf
			
			If lSeekAA3 
				AtEqStatus("TECA450",.T.)
			EndIf

			RecLock("AB7")
			DbDelete()

			//Quando for Integração TEC x ATF e for OS de inspeção.
			If lTecAtf .And. TecAtfSeek(AB7->AB7_NUMSER) .And. Empty(AB6->AB6_CDORCS) .And. Empty(AB6_ITORCS)
				If ValType(oTecProvider) <> 'O'
					oTecProvider := TECProvider():New()
				EndIf
				oTecProvider:DeleteTWU(,'1'+AB7->AB7_NUMOS+AB7->AB7_ITEM) //Tipo+NumOs+ItemOs
			EndIf

			DbSelectArea("AB7")
			DbSkip()
		EndDo

		AB7->( FKCommit() )
	EndIf

	RecLock("AB6")
	DbDelete()
EndIf
If ExistBlock("AT450GRV")
	Execblock("AT450GRV",.F.,.F.)
Endif
TecDestroy(oTecProvider)

RestArea(aAreaAb1)
RestArea(aAreaAb2)
RestArea(aAreaAb3)
RestArea(aAreaAb4)
RestArea(aAreaAb5)
RestArea(aAreaAb6)
RestArea(aAreaAb7)
RestArea(aAreaAb8)
RestArea(aArea)
Return(lGravou)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsGar   ³ Autor ³ Eduardo Riera         ³ Data ³ 07.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Produto esta em garantia                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se o Produto esta em garantia               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Fabricante                               ³±±
±±³          ³ ExpC2 : Loja do Fabricante                                 ³±±
±±³          ³ ExpC3 : Codigo do Produto                                  ³±±
±±³          ³ ExpC4 : Numero de Serie                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsGar(cCodFab,cLojaFab,cProduto,cNumSer)

Local aArea := GetArea()
Local lGarantia := .F.
Local cCodCli:=""
Local cLojaCli:=""

#IFDEF TOP
	Local cQuery  := ""
#ENDIF

If IsInCallStack("AT460INCLU")
	DbSelectArea("AB7")
	DbSetOrder(1)
	If DbSeek(xFilial("AB7")+M->AB9_NUMOS)
		cProduto :=  AB7_CODPRO
		cNumSer	 :=	 AB7_NUMSER
	Endif
Endif

DbSelectArea("AA3")
DbSetOrder(4)
If ( DbSeek(xFilial("AA3")+cCodFab+cLojaFab+cProduto+cNumSer) )
	cCodCli := AA3->AA3_CODCLI
	cLojaCli:= AA3->AA3_LOJA
	If ( AA3->AA3_DTGAR >= dDataBase )
		lGarantia := .T.
	Else
		#IFDEF TOP

			cQuery := "SELECT COUNT(*) GARANTIA FROM "+RetSqlName("AB8")+" "
			cQuery += "WHERE AB8_FILIAL='"+xFilial("AB8")+"' AND "
			cQuery += "AB8_CODCLI='"+cCodCli+"' AND "
			cQuery += "AB8_LOJA='"+cLojaCli+"' AND "
			cQuery += "AB8_CODPRO='"+cProduto+"' AND "
			cQuery += "AB8_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB8_DTGAR>='"+Dtos(dDataBase)+"' AND "
			cQuery += "D_E_L_E_T_=' '"

			cQuery := ChangeQuery(cQuery)

			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),GetNextAlias(),.T.,.T.)
				If ( GARANTIA > 0 )
				lGarantia := .T.
			EndIf
			dbCloseArea()
			DbSelectArea("AA3")

		#ELSE
			DbSelectArea("AB8")
			DbSetOrder(2)
			DbSeek(xFilial("AB8")+cCodCli+cLojaCli+cProduto+cNumSer+Dtos(dDataBase),.T.)
			While ( !Eof() .AND. xFilial("AB8")  == AB8->AB8_FILIAL .AND.;
										cCodCli         == AB8->AB8_CODCLI .AND.;
										cLojaCli        == AB8->AB8_LOJA   .AND.;
										cProduto        == AB8->AB8_CODPRO .AND.;
										cNumSer         == AB8->AB8_NUMSER .AND.;
										dDataBase       <= AB8->AB8_DTGAR  .AND.;
										!lGarantia)
				lGarantia := .T.
				DbSelectArea("AB8")
				DbSkip()
			EndDo
		#ENDIF
	EndIf
EndIf
If ( !lGarantia )
	DbSelectArea("AA4")
	DbSetOrder(3)
	If ( DbSeek(xFilial("AA4")+cCodFab+cLojaFab+cProduto+cNumSer) )
		While ( !Eof() .AND. xFilial("AA4") == AA4->AA4_FILIAL .AND.;
							cCodFab == AA4->AA4_CODFAB .AND.;
							cLojafab == AA4->AA4_LOJAFA .AND.;
							cProduto== AA4->AA4_CODPRO .AND.;
							cNumSer == AA4->AA4_NUMSER .AND.;
							!lGarantia)
			If ( AA4->AA4_DTGAR >= dDataBase )
				lGarantia := .T.
			EndIf
			DbSelectArea("AA4")
			DbSkip()
		EndDo
	EndIf
EndIf
RestArea(aArea)
Return(lGarantia)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsCtrMan³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Produto possui contrato de manutencao        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se o Produto possui contrato de manutencao  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Fabricante                               ³±±
±±³          ³ ExpC2 : Loja do Fabricante                                 ³±±
±±³          ³ ExpC3 : Codigo do Produto                                  ³±±
±±³          ³ ExpC4 : Numero de Serie                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsCtrMan(cCodFab,cLojaFa,cCodPro,cNumSer)

Local aArea    := GetArea()
Local lContrato := .F.

DbSelectArea("AA3")
DbSetOrder(4)
If ( DbSeek(xFilial("AA3")+cCodFab+cLojaFa+cCodPro+cNumSer) )
	If ( !Empty(AA3->AA3_CONTRT) )
		DbSelectArea("AAH")
		DbSetOrder(1)
		If ( DbSeek(xFilial("AAH")+AA3->AA3_CONTRT) )
			If ( dDataBase >= AAH->AAH_INIVLD .AND.;
				 (dDataBase <= AAH->AAH_FIMVLD .OR. AAH->AAH_TPCONT=="1" ))
				lContrato := .T.
			EndIf
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return(lContrato)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsCtrPre³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Indica se ha preventiva a ser efetuada.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se ha preventiva a ser efetuada.            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Fabricante                               ³±±
±±³          ³ ExpC2 : Loja do Fabricante                                 ³±±
±±³          ³ ExpC3 : Codigo do Produto                                  ³±±
±±³          ³ ExpC4 : Numero de Serie                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsCtrPre(cCodFab,cLojaFa,cCodPro,cNumSer)
Local aArea       := GetArea()
Local lPreventiva := .F.

DbSelectArea("AA3")
DbSetOrder(4)
If ( DbSeek(xFilial("AA3")+cCodFab+cLojaFa+cCodPro+cNumSer) )
	If ( !Empty(AA3->AA3_CTAPRE) )
		DbSelectArea("AAJ")
		DbSetOrder(1)
		If ( DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE) )
			While ( !Eof() .AND. xFilial("AAJ")  == AAJ->AAJ_FILIAL .AND.;
										AA3->AA3_CTAPRE == AAJ->AAJ_CTAPRE .AND. !lPreventiva)
				If ( AAJ->AAJ_DTPREV < dDataBase .AND. Empty(AAJ->AAJ_DTREAL) )
					lPreventiva := .T.
				EndIf
				DbSelectArea("AAJ")
				DbSkip()
			EndDo
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return(lPreventiva)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsNumPre³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Indica o numero de preventivas atrasadas                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 : Indica o numero de preventivas atrasadas           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Fabricante                               ³±±
±±³          ³ ExpC2 : Loja do Fabricante                                 ³±±
±±³          ³ ExpC3 : Codigo do Produto                                  ³±±
±±³          ³ ExpC4 : Numero de Serie                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsNumPre(cCodFab,cLojaFa,cCodPro,cNumSer)
Local aArea       := GetArea()
Local nPreventiva := 0

DbSelectArea("AA3")
DbSetOrder(4)
If ( DbSeek(xFilial("AA3")+cCodFab+cLojaFa+cCodPro+cNumSer) )
	If ( !Empty(AA3->AA3_CTAPRE) )
		DbSelectArea("AAJ")
		DbSetOrder(1)
		If ( DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE) )
			While ( !Eof() .AND. xFilial("AAJ")  == AAJ->AAJ_FILIAL .AND.;
										AA3->AA3_CTAPRE == AAJ->AAJ_CTAPRE )
				If ( AAJ->AAJ_DTPREV < dDataBase .AND. Empty(AAJ->AAJ_DTREAL) )
					nPreventiva ++
				EndIf
				DbSelectArea("AAJ")
				DbSkip()
			EndDo
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return(nPreventiva)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsChamad³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Cliente possui um chamado em aberto          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se o Cliente possui um chamado em aberto    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Cliente                                  ³±±
±±³          ³ ExpC2 : Loja                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsChamado(cCodCli,cLoja)
Local aArea    := GetArea()
Local aAreaAB1 := AB1->( GetArea() )

Local lChamado := .F.

DbSelectArea("AB1")
DbSetOrder(4)
If ( DbSeek(xFilial("AB1")+"A"+cCodCli+cLoja) )
	lChamado := .T.
EndIf

RestArea(aAreaAB1)
RestArea(aArea)
Return(lChamado)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsOrc   ³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Cliente possui um Orcamento em aberto        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se o Cliente possui um Orcamento em Aberto  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Cliente                                  ³±±
±±³          ³ ExpC2 : Loja                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsOrc(cCodCli,cLoja)
Local aArea      := GetArea()
Local lOrcamento := .F.

DbSelectArea("AB3")
DbSetOrder(4)
If ( DbSeek(xFilial("AB3")+"A"+cCodCli+cLoja) )
	lOrcamento := .T.
EndIf
RestArea(aArea)
Return(lOrcamento)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsOS    ³ Autor ³ Eduardo Riera         ³ Data ³ 22.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o Cliente possui uma OS em Aberto              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 : Indica se o Cliente possui uma OS em Aberto        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Cliente                                  ³±±
±±³          ³ ExpC2 : Loja                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsOS(cCodCli,cLoja)
Local aArea := GetArea()
Local lOS := .F.

DbSelectArea("AB6")
DbSetOrder(4)
If ( DbSeek(xFilial("AB6")+"A"+cCodCli+cLoja) )
	lOS := .T.
EndIf
RestArea(aArea)
Return(lOS)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³X3Combo   ³ Autor ³ Eduardo Riera         ³ Data ³ 07.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna a Descricao de um campo ComboBox                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1: Descricao do ComboBox                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Campo do SX3                                        ³±±
±±³          ³ ExpC2: Conteudo do Combo                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function X3Combo(cCampo,cCombo)

Local aArea    := GetArea()		//Guarda a area atual
Local cDescri  := ""			//Descricao do item selecionado no Combo
Local cBox     := ""			//Conteudo do ComboBox
Local aBox     := {}			//Array com os dados do Combo
Local nPosicao1:= 0				//Posicao 1 no array aBox
Local nPosicao2:= 0				//Posicao do "=" na string

DbSelectArea("SX3")
DbSetOrder(2)
If ( DbSeek(cCampo) )
	cBox  := X3CBox()
	While ( !Empty(cBox) )
		nPosicao1   := At(";",cBox)
		If ( nPosicao1 == 0 )
			nPosicao1 := Len(cBox)+1
		EndIf
		nPosicao2   := At("=",cBox)
		aadd(aBox,{ StrTran(SubStr(cBox,1,nPosicao2-1),"&"),SubStr(cBox,nPosicao2+1,nPosicao1-nPosicao2-1) })
		cBox := SubStr(cBox,nPosicao1+1)
	EndDo
	nPosicao1 := aScan(aBox,{|x| AllTrim(x[1]) == AllTrim(cCombo) })
	If ( nPosicao1 <> 0 )
		cDescri := AllTrim(aBox[nPosicao1][2])
	EndIf
EndIf
DbSelectArea("SX3")
DbSetOrder(1)
RestArea(aArea)
Return(cDescri)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtVlrPagto³ Autor ³ Eduardo Riera         ³ Data ³ 08.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o Valor a Ser Pago pelo Servico ou Orcamento       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Valor a Ser Pago                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Servico                                  ³±±
±±³          ³ ExpN1 : Valor do Servico                                   ³±±
±±³          ³ ExpC1 : "C" para Cliente / "F" para Fabricante             ³±±
±±³          ³ ExpL1 : "T" qdo for gravacao de Pedidos                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtVlrPagto(cServico,nValor,cTipo, lGravar, cCampo)

Local aArea  := GetArea()	//Guarda a area atual
Local nDec   := IIF(Empty(cCampo),2,TamSX3(cCampo)[2])

lGravar := If( ValType( lGravar ) <> "L", .F., lGravar )
DbSelectArea("AA5")
DbSetOrder(1)
If( DbSeek(xFilial("AA5")+cServico) )
	DbSelectArea("SF4")
	DbSetOrder(1)
	If ( DbSeek(xFilial("SF4")+AA5->AA5_TES) )
		// Na exibicao dos totais na tela, considera o TES, mas na gravacao dos
		// Pedidos nao considerar pois a geracao da NF fara o tratamento
		If ( SF4->F4_DUPLIC=="S" ) .OR. lGravar
			If ( cTipo $ "cC" )
				nValor *= (AA5->AA5_PRCCLI/100)
			Else
				nValor *= (AA5->AA5_PRCFAB/100)
			EndIf
		Else
			nValor := 0
		EndIf
		nValor := Round(nValor,nDec)
	Else
		nValor := 0
	EndIf
Else
	nValor := 0
EndIf
RestArea(aArea)

Return(nValor)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³VldHora   ³ Autor ³ Eduardo Riera         ³ Data ³ 23.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a Hora Informada                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AtVldHora( ExpC1, [ ExpL1 ] )                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: String da Hora                                      ³±±
±±³          ³ ExpL1: Indica se valida apenas os minutos                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtVldHora(cHorario,lMinutos)

Local cHora    := SubStr(cHorario,1,2)
Local cMinuto  := ""

Local lRetorno := .F.

Local nAchou :=  At( ":", cHorario )  //Verificar se a hora foi passada no formato 99:99 ou 9999
Local nPosMin := If(nAchou>0,nAchou+1,3)
cMinuto  := SubStr(cHorario,nPosMin,2)
lMinutos := If( ValType( lMinutos ) == "L", lMinutos, .F. )

If ( If( lMinutos, .T., ( cHora >= "00" .AND. cHora < "24" ) ) .AND.;
		cMinuto >= "00" .AND. cMinuto < "60" )
	lRetorno := .T.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Horario e positivo                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	lRetorno := Val( StrTran( cHorario, ":", "" ) ) >= 0
EndIf

Do Case

Case ( !lRetorno) .AND. !lMinutos
	Help(" ",1,"VLDHORA")
Case ( !lRetorno) .AND. lMinutos
	Help(" ",1,"VLDMIN")
EndCase
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtInfoABL ³ Autor ³ Sergio Silveira       ³ Data ³07/08/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna informacoes para o Item de Help Desk               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1 : Situacao do Help Desk ( Fila )                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ABL deve estar posicionado                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtInfoABL()

Local aArea      := GetArea()
Local aAreaABK   := ABK->(GetArea())
Local cStatus    := ""
Local lExisteABK := .F.
Local nTempo     := 0

#IFDEF TOP
	Local cQuery  := ""
#ENDIF

#IFDEF TOP

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Commita para garantir a atualizacao dos dados                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ABK->( dbCommit() )

	cQuery := ""
	cQuery += "SELECT ABK_FILIAL,ABK_NRCHAM,ABK_SITUAC,ABK_TEMPO FROM " + RetSqlName( "ABK" ) + " "
	cQuery += "WHERE ABK_FILIAL='" + xFilial( "ABK" ) + "' AND "
	cQuery += "ABK_NRCHAM='" + ABL->ABL_NRCHAM + "' AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), "TRABABK", .F., .T. )

	If Alias() == "TRABABK"
		nTempo := 0
		While !TRABABK->( Eof() )
			lExisteABK := .T.
			If ( TRABABK->ABK_SITUAC=="1" ) //Encerrado
				cStatus := "3"
			ElseIf cStatus <> "3"
				cStatus := "2"
			EndIf
			nTempo += HoraToInt( TRABABK->ABK_TEMPO, 4 )
			TRABABK->( DbSkip() )
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Exclui a area de trabalho da Query                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		TRABABK->( dbCloseArea() )
		DbSelectArea( "ABK" )
	EndIf

#ELSE
	ABK->( DbSetOrder( 1 ) )
	cSeekABK := xFilial("ABK")+ABL->ABL_NRCHAM
	If ABK->( DbSeek( cSeekABK ) )
		nTempo := 0
		While ( !ABK->( Eof() ) .AND. cSeekABK == ABK->ABK_FILIAL + ABK->ABK_NRCHAM )
			lExisteABK := .T.
			If ( ABK->ABK_SITUAC=="1" ) //Encerrado
				cStatus := "3"
			ElseIf cStatus <> "3"
				cStatus := "2"
			EndIf
			nTempo += HoraToInt( ABK->ABK_TEMPO, 4 )
			ABK->( DbSkip() )
		EndDo
	EndIf
#ENDIF

If ( Empty(cStatus) )
	cStatus := "1"
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura os dados de entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaABK)
RestArea(aArea)
Return( { cStatus, IntToHora( nTempo, 4 ), lExisteABK } )


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtTipoAB7 ³ Autor ³ Eduardo Riera         ³ Data ³ 23.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica a Situacao Correta do Item de Orcamento           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 : Situacao da OS                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ AB7 deve estar posicionado                                 ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtTipoAB7()

Local aArea    := GetArea()
Local aAreaAB8 := AB8->(GetArea())
Local aAreaAB9 := AB9->(GetArea())
Local cStatus  := ""

#IFDEF TOP

	cAliasQry := "ATTIPOAB7"

	cQuery := ""

	AB9->( dbCommit() )

	cQuery += "SELECT AB9_TIPO FROM " + RetSqlName( "AB9" ) + " "
	cQuery += "WHERE "
	cQuery += "AB9_FILIAL='" + xFilial( "AB9" ) + "' AND "
	cQuery += "AB9_NUMOS='"  + AB7->AB7_NUMOS + AB7->AB7_ITEM + "' AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T. , "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )

	If cAliasQry == Alias()
		While !Eof()
			cStatus := "3"

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica a Situacao 4 - Atendido                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AB9_TIPO == "1"
				cStatus := "4"
				Exit
			EndIf
			DbSkip()
		EndDo
		dbCloseArea()
		DbSelectArea( "AB9" )
	EndIf

	cAliasQry := "ATTIPOAB8"

	cQuery := ""

	AB8->( dbCommit() )

	cQuery += "SELECT COUNT(*) LINHAS FROM " + RetSqlName( "AB8" ) + " "
	cQuery += "WHERE "
	cQuery += "AB8_FILIAL='"  + xFilial( "AB8" ) + "' AND "
	cQuery += "AB8_NUMOS='"   + AB7->AB7_NUMOS   + "' AND "
	cQuery += "AB8_ITEM='"    + AB7->AB7_ITEM    + "' AND "
	cQuery += "(AB8_NUMPV<>'"  + Space( Len( AB8->AB8_NUMPV ) ) + "' OR "
	cQuery += " AB8_NUMPVF<>'" + Space( Len( AB8->AB8_NUMPVF ) )+ "') AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T. , "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )

	If cAliasQry == Alias()
		cStatus := If( Empty( LINHAS ), cStatus, "2" )
		dbCloseArea()
		DbSelectArea( "AB8" )
	EndIf

#ELSE

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a Situacao 4 - Atendido                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB9")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AB9")+AB7->AB7_NUMOS+AB7->AB7_ITEM) )
		cStatus := "3"
		While ( !Eof() .AND. xFilial("AB9") == AB9_FILIAL .AND.;
									AB9->AB9_NUMOS == AB7->AB7_NUMOS+AB7->AB7_ITEM )
			If ( AB9->AB9_TIPO=="1" ) //Encerrado
				cStatus := "4"
				Exit
			EndIf

			DbSelectArea("AB9")
			DbSkip()
		EndDo
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica a Situacao quanto a OS.                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB8")
	DbSetOrder(1)
	DbSeek(xFilial("AB8")+AB7->AB7_NUMOS+AB7->AB7_ITEM)
	While ( !Eof() .AND. AB8->AB8_FILIAL == xFilial("AB8") .AND.;
								AB8->AB8_NUMOS  == AB7->AB7_NUMOS .AND.;
								AB8->AB8_ITEM   == AB7->AB7_ITEM )
		If ( !Empty(AB8->AB8_NUMPV) ) .OR. ( !Empty( AB8->AB8_NUMPVF ) )
			 cStatus := "2"
			 Exit
		EndIf
		DbSelectArea("AB8")
		DbSkip()
	EndDo

#ENDIF

If ( Empty(cStatus) )
	cStatus := "1"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura os dados de entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaAB8)
RestArea(aAreaAB9)
RestArea(aArea)
Return(cStatus)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ATOSSTATUS³ Autor ³ Eduardo Riera         ³ Data ³ 27.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna o Status da OS ( Campo AB6_STATUS )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 : Status da OS ('A' Aberto/'E' Encerrada/'B' Atendida³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Numero da OS                                       ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtOsStatus(cNumOs)

Local aArea    := GetArea()
Local aAreaAb7 := AB7->(GetArea())
Local lTipo2   := .F.
Local lTipo3   := .F.
Local lTipo4   := .F.
Local lTipo5   := .F.
Local cStatus  := "A"

DbSelectArea("AB7")
DbSetOrder(1)
DbSeek(xFilial("AB7")+cNumOs)

While ( !Eof() .AND. xFilial("AB7") == AB7_FILIAL .AND.;
							cNumOs         == AB7->AB7_NUMOS )
	Do Case
		Case ( AB7->AB7_TIPO == "2" ) // Pedido Gerado
			lTipo2 := .T.
		Case ( AB7->AB7_TIPO $ "13" ) // Em Atendimento
			lTipo3 := .T.
		Case ( AB7->AB7_TIPO == "4" ) // Atendido
			lTipo4 := .T.
		Case ( AB7->AB7_TIPO == "5" ) // Encerrado
			lTipo5 := .T.
	EndCase
	DbSelectArea("AB7")
	DbSkip()
EndDo
Do Case
	Case ( lTipo3 )
		cStatus := "A"
	Case ( lTipo4 )
		cStatus := "B"
	Case ( lTipo5 .OR. lTipo2 )
		cStatus := "E"
	OtherWise
		cStatus := "A"
EndCase

RestArea(aAreaAB7)
RestArea(aArea)

Return(cStatus)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtTotHora ³ Autor ³ Eduardo Riera         ³ Data ³ 27.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o Total de Horas entre duas datas e duas horas     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 : Total de Horas                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1 : Data Inicial                                       ³±±
±±³          ³ ExpC1 : Hora Inicial                                       ³±±
±±³          ³ ExpD2 : Data Final                                         ³±±
±±³          ³ ExpC2 : Hora Final                                         ³±±
±±³          ³ ExpC3 : Picture a ser retornada                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ATTotHora(	dDtIni,	cHrIni,	dDtFim,	cHrFim,;
					cPicture )

Local nHoras   := Val(SubStr(cHrFim,1,2)) - Val(SubStr(cHrIni,1,2))
Local nMinutos := Val(SubStr(cHrFim,4,2)) - Val(SubStr(cHrIni,4,2))
Local nDias    := dDtFim - dDtIni
Local cRetorno := ""
Local nTamDias := 0

cPicture := If(cPicture==Nil,"999D 99:99",cPicture)
nTamDias := At("D",cPicture)-1

If ( nMinutos < 0 )
	nHoras--
	nMinutos += 60
EndIf
If ( nHoras < 0 )
	nDias --
	nHoras += 24
EndIf

IF nHoras == 0 .AND. cHrFim == cHrIni
	nHoras := 24
EndIf

cRetorno += StrZero(nDias,nTamDias)
cRetorno += StrZero(nHoras,2)
cRetorno += StrZero(nMinutos,2)
cRetorno := TransForm(Val(cRetorno),cPicture)
If ( nDias < 0 )
	cRetorno := ""
EndIf
Return(cRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³HoraToInt ³ Autor ³ Eduardo Riera         ³ Data ³ 28.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Converte Horas em Inteiro.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1: Inteiro                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: String no Formato "HH:MM"                           ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function HoraToInt(cHora,nDigitos)

Local nHoras
Local nMinutos
Local nExtDigit

nExtDigit := If( ValType( nDigitos ) == "N", nDigitos - 2, 0 )

nHoras    := Val(SubStr(cHora,1,2 + nExtDigit ))
nMinutos  := Val(SubStr(cHora,4 + nExtDigit,2))/60

Return(nHoras+nMinutos)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³IntToHora ³ Autor ³ Eduardo Riera         ³ Data ³ 28.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Converte Inteiro em Horas                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1: String no Formato "HH:MM"                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1: Numero de Horas                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function IntToHora(nHora,nDigitos,lCptZro)

Local nHoras    := 0
Local nMinutos  := 0
Local cHora     := ""
Local lNegativo := .F.

Default lCptZro := .F. //Indica se irá preencher com zeros a esquerda, horas dentro do intervalo de 99 - 1000, em relação ao nDigitos

lNegativo := ( nHora < 0 )

nHora     := ABS( nHora )

nHoras    := Int(nHora)
nMinutos  := (nHora-nHoras)*60

nDigitos := If( ValType( nDigitos )=="N", nDigitos, Len(cValtoChar(Int(nHora))) ) - If( lNegativo, 1, 0 )

If nHoras > 99 .And. nHoras < 1000 .AND. !lCptZro
	cHora := If( lNegativo, "-", "" ) + StrZero( nHoras, 3 )+":"+StrZero( nMinutos, 2 )
Else
	cHora := If( lNegativo, "-", "" ) + StrZero( nHoras, Iif(nDigitos<2,2,nDigitos))+":"+StrZero( nMinutos, 2 )
Endif

Return(cHora)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SubtHoras ³ Autor ³ Eduardo Riera         ³ Data ³ 28.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o Numero de Horas entre dois tempos.               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1: Numero de Horas entre duas datas                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1: Data Inicial                                        ³±±
±±³          ³ ExpH1: Hora Inicial                                        ³±±
±±³          ³ ExpD2: Data Final                                          ³±±
±±³          ³ ExpH2: Hora Final                                          ³±±
±±³          ³ ExpL5: Horario Continuo                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim,lHrCont)

Local nDias
Local nHoras

//Horario Continuo. Ex. 10/10/2010 08:00 até 11/10/2010 17:00 como 32 horas. Quando .F. Considera apenas 8 hs dia 10 + 8hs dia 11 = 16hs
Default lHrCont := .T. //Considera horario continuo.
Default dDataIni := Date()
Default cHoraIni := ""
Default dDataFim := Date()
Default cHoraFim := ""

nDias := dDataFim - dDataIni
nHoras:= HoraToInt(cHoraFim)-HoraToInt(cHoraIni)

If nDias > 0 .AND. lHrCont == .F.
	nHoras	:= nHoras+(nHoras * nDias)
Else
	nHoras := nHoras+(nDias*24)
EndIf

Return(nHoras)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SubtHorasC ³ Autor ³ Vendas & CRM         ³ Data ³ 28.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o Numero de Horas entre dois tempos por periodo.     ³±±
±±³        ³ Ex. do dia 01 ao 04 das 08:00 as 18:00                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1: Numero de Horas entre duas datas                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1: Data Inicial                                        ³±±
±±³          ³ ExpH1: Hora Inicial                                        ³±±
±±³          ³ ExpD2: Data Final                                          ³±±
±±³          ³ ExpH2: Hora Final                                          ³±±
±±³          ³ ExpL5: Horario Continuo                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±³ Uso      ³ Gatilhos AB9 - Atendimento da O.S.                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SubtHorasC(dDataIni,cHoraIni,dDataFim,cHoraFim,lHrCont)
Return SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim,.F.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtSomaHora³ Autor ³ Sergio Silveira       ³ Data ³24/07/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma dois Horarios ( funcao para chamada externa da        ³±±
±±³          ³ somahoras )                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1: Horario Final                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Hora Inicial                                        ³±±
±±³          ³ ExpN2: nHoras                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtSomaHora(cHora,nHora)

Return( SomaHoras(cHora,nHora) )


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SomaHoras ³ Autor ³ Eduardo Riera         ³ Data ³ 24.12.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma dois Horarios.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1: Horario Final                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Hora Inicial                                        ³±±
±±³          ³ ExpN2: nHoras                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function SomaHoras(cHora,nHora)

Return( IntToHora(HoraToInt(cHora)+nHora) )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SomaDiaHor³ Autor ³ Eduardo Riera         ³ Data ³ 22.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma Dias e Horas                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1 : Data Inicial - Este vai ser modificado             ³±±
±±³          ³ ExpC1 : Hora Inicial - Este vai ser modificado             ³±±
±±³          ³ ExpN1 : Horas a serem Somadas                              ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SomaDiaHor(dData,cHora,nSomaHs)

Local nDias := 0

nSomaHs  += HoraToInt(cHora)
nDias    := Int(nSomaHs/24)
dData    += nDias
cHora    := IntToHora(nSomaHs-(nDias*24))

Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³SubtDiaHor³ Autor ³ Danilo Dias           ³ Data ³ 22.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrica‡…o ³ Subtrai Dias e Horas                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpD1 : Data Inicial - Este vai ser modificado             ³±±
±±³          ³ ExpC1 : Hora Inicial - Este vai ser modificado             ³±±
±±³          ³ ExpN1 : Horas a serem Subtraidas                           ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function SubtDiaHor( dData, cHora, nTempo )

Local nHoras := 0

nHoras = HoraToInt( cHora ) - nTempo

While nHoras < 0

	nHoras = 24 - Abs( nHoras )
	dData -= 1

EndDo

cHora := IntToHora( nHoras )

Return Nil


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtIsObsol ³ Autor ³ Eduardo Riera         ³ Data ³ 29.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se existe algum item em obsolecencia.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Codigo do Fabricante                                ³±±
±±³          ³ ExpC2: Loja do Fabricante                                  ³±±
±±³          ³ ExpC3: Codigo do Produto.                                  ³±±
±±³          ³ ExpC4: Numero de Serie do Produto                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtIsObsol(cCodFab,cLojaFa,cCodPro,cNumSer)

Local aArea    := GetArea()
Local aAreaAA3 := AA3->(GetArea())
Local lisObsol := .F.

DbSelectArea("AA3")
DbSetOrder(4)
If ( DbSeek(xFilial("AA3")+cCodFab+cLojaFa+cCodPro+cNumSer) )
	DbSelectArea("AAK")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AAK")+cCodFab+cLojaFa+cCodPro) )
		While ( !Eof() .AND. xFilial("AAK") == AAK->AAK_FILIAL .AND.;
									cCodFab        == AAK->AAK_CODFAB .AND.;
									cLojaFa        == AAK->AAK_LOJAFA .AND.;
									cCodPro        == AAK->AAK_CODPRO .AND.;
									!lIsObsol)
			lIsObsol := ItIsObsol()
			DbSelectArea("AAK")
			DbSkip()
		EndDo
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna os dados de Entrada                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaAA3)
RestArea(aArea)

Return(lIsObsol)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ItIsObsol ³ Autor ³ Eduardo Riera         ³ Data ³ 30.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se o item esta em Obsolescencia                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ AA3 e AAK Posicionados                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function ItIsObsol()

Local aArea    := GetArea()
Local nAcumul  := AA3->AA3_ACUMUL
Local dDtInst  := If(Empty(AA3->AA3_DTINST),AA3->AA3_DTVEND,AA3->AA3_DTINST)
Local lisObsol := .F.
Local bTmpAcu  := {|| If(AAK->AAK_TIPTMP=="D",1,If(AAK->AAK_TIPTMP=="M",30,360))}

DbSelectArea("AAL")
DbSetOrder(1)
If ( DbSeek(xFilial("AAL")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER+AAK->AAK_CODCOM) )
	nAcumul -= AAL->AAL_ACUMUL
	dDtInst := AAL->AAL_ACUTMP
EndIf
If ( nAcumul >= AAK->AAK_ACUMUL .AND. AAK->AAK_ACUMUL<>0 )
	lIsObsol := .T.
EndIf
If ( dDataBase > dDtInst+(AAK->AAK_ACUTMP*Eval(bTmpAcu)) .AND. AAK->AAK_ACUTMP <> 0 )
	lIsObsol := .T.
EndIf

RestArea(aArea)

Return(lIsObsol)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtAlocTec ³ Autor ³ Eduardo Riera         ³ Data ³23.12.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Alocacacao automatica dos Tecnicos.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Numero da Ordem de Servico.                          ³±±
±±³          ³ExpN1: Numero de Horas a ser alocada [ Opcional ]           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtAlocTec(cNumOs,nHorasAloc)

Local aArea			:= GetArea()				// Salva a area atual
Local aTecnico		:= {}						// Lista de técnicos
Local aNivelTec		:= {}						// Nível dos técnicos
Local aOcorrencia	:= {}
Local aAuxiliar		:= {}
Local aBestTec		:= {}
Local aOwnerTec		:= {}
Local aBestHor		:= {}
Local aTravas		:= {}						// Lista com os registros travados
Local aRes          := {} 
Local cRegra		:= SuperGetMV("MV_REGRAAL")
Local cHora0		:= ""
Local lITmpStd		:= SuperGetMV("MV_ITMPSTD")
Local lRegiao		:= SubStr(cRegra,1,1)=="1"
Local lPrioridade	:= SubStr(cRegra,2,1)=="1"
Local lHabilidade	:= SubStr(cRegra,3,1)=="1"
Local lNivel		:= SubStr(cRegra,4,1)=="1"
Local lNivelTec		:= .F.
Local lContinua		:= .T.
Local lGravTec      := .T.
Local nNivel		:= 0
Local nTecnico		:= 0
Local nOcorrencia	:= 0
Local nCntFor		:= 0						// Contador
Local nCntFor1		:= 0						// Contador
Local nCntFor2		:= 0						// Contador
Local nHoras		:= 0
Local nMenorTempo	:= 0
Local nVisitas		:= 0
lOCAL nX1           := 0
Local dData0		:= Nil
Local lAT450Alc		:= ExistBlock('AT450Alc')
Local lAt450Age		:= ExistBlock('AT450AGE')
Local lT450Regiao	:= ExistBlock("T450REGIAO")
Local lPe450Age		:= .F.
Local lLibAgenRH    :=  SuperGetMv("MV_GSAGTRH",.F.,.F.) // Define se verifica conflitos de RH para geração da agenda do Técnico (.F. = Não verifica, .T. = Verifica)
Local nLoop			:= 0						// Contador
Local aHabilReq		:= {}                      	// Lista com as Habilidades requeridas para atender a O.S.
Local cAliasQry		:= ""						// Alias da Query
Local cQuery		:= ""						// Query do SQL
Local cGrpAte		:= ""						// Grupo final de técnico
Local lNoRotAuto	:= ( Type("lAt450Auto")=="U" .OR. !lAt450Auto ) .AND. ( Type("lAt400Auto")=="U" .OR. !lAt400Auto ) .AND.   ( Type("lAt300Auto")=="U" .OR. !lAt300Auto  )	// Se esta sendo rodado apartir de uma rotina automática
Local cRegiao		:= ""
Local cRegPe		:= ""

dData0      := dDataBase+SuperGetMV("MV_ATDIAS")
If ( dData0 == dDataBase )
	cHora0   := SomaHoras(Time(),SuperGetMV("MV_ATHORAS"))
Else
	cHora0   := "00:00"
EndIf
nHorasAloc  := If(nHorasAloc==Nil,0,nHorasAloc)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Refaz a alocacao de uma OS                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (! AtRefazOs(cNumOs,.T.,@nHorasAloc,.F.) )
	If lNoRotAuto
		Help(" ",1,"ATALOCTEC1") // Problemas de travamento do Tecnico
	EndIf
	lContinua := .F.
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona Registros.                                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB6")
	DbSetOrder(1)
	DbSeek(xFilial("AB6")+cNumOs)

	DbSelectArea("SA1")
	DbSetOrder(1)
	DbSeek(xFilial("SA1")+AB6->AB6_CODCLI+AB6->AB6_LOJA)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Deve-se varrer todos os itens da Ordem de Servico para alocacao.        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AB7")
	DbSetOrder(1)
	DbSeek(xFilial("AB7")+cNumOs)
	While ( !Eof() .AND. xFilial("AB7") == AB7->AB7_FILIAL .AND.;
								cNumOs         == AB7->AB7_NUMOS )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Registros                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("AAG")
		DbSetOrder(1)
		DbSeek(xFilial("AAG")+AB7->AB7_CODPRB)

		cGrpAte := Space( Len( AAP->AAP_GRPATE ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se este equipamento possui um grupo de atendimento especifico  ³
		//³ Isto ocorre quando o equipamento possui contrato e se o contrato esta   ³
		//³ amarrado a um grupo de atendimento. Nesse caso, utiliza-se o AA7        ³
		//³ especifico do grupo de atendimento                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AA3->( DbSetOrder( 1 ) )
		If AA3->( DbSeek( xFilial( "AA3" ) + AB6->AB6_CODCLI + AB6->AB6_LOJA + AB7->AB7_CODPRO + AB7->AB7_NUMSER ) )

			If !Empty( AA3->AA3_CONTRT )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Obtem o grupo de atendimento associado a este contrato                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cGrpAte := AtRetGrpAt( AA3->AA3_CONTRT, .T. )
			EndIf

		EndIf

		DbSelectArea("AA7")
		DbSetOrder(1)
		DbSeek(xFilial("AA7")+AB7->AB7_CODPRO+AB7->AB7_CODPRB+cGrpAte)

		If !AA7->( Found() )
			DbSeek(xFilial("AA7")+AB7->AB7_CODPRO+CriaVar("AA7_CODPRB",.F.)+cGrpAte)

			If !AA7->( Found() )
				DbSeek(xFilial("AA7")+CriaVar("AA7_CODPRO",.F.)+AB7->AB7_CODPRB+cGrpAte)
			EndIf

		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Aqui eh escolhido o Tecnico a ser alocado considerando:                 ³
		//³      - Regiao                                                          ³
		//³      - Habilidade                                                      ³
		//³      - Nivel de Conhecimento da Habilidade.                            ³
		//³      - Nivel de Conhecimento exigido para a execucao do Servico        ³
		//³      - Prazo de Atendimento                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa Todos os Tecnicos Disponiveis conforme a regiao, habilidade e  ³
		//³nivel de conhecimento.                                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calcula as horas necessaria para alocacao.                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( nHorasAloc == 0 )
			nHoras := SubtHoras(dDataBase,"00:00",dDataBase,AA7->AA7_TMPSTD)
			nHoras += SubtHoras(dDataBase,"00:00",dDataBase,SA1->A1_TMPSTD)
		EndIf
		If ( nHoras > 0 )
			cRegiao := SA1->A1_REGIAO
			If lT450Regiao
				cRegPe := ExecBlock("T450REGIAO",.F.,.F.,{cRegiao})
				If ValType(cRegPe) == "C"
					cRegiao := cRegPe
				Endif
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Obtem todas as habilidades necessarias para este produto/ocorrencia ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aHabilReq   := {}
			AtHabilReq( @aHabilReq )

			#IFDEF TOP

				cAliasQry := "ATALOCTEC"

				cQuery := "SELECT AA1_FILIAL,AA1_TIPO,AA1_ALOCA,AA1_CODTEC,AA1_REGIAO FROM "
				cQuery += RetSqlName( "AA1" ) + " "
				cQuery += "WHERE "
				cQuery += "AA1_FILIAL='" + xFilial( "AA1" ) + "' AND "
				cQuery += "AA1_TIPO IN ( '1', '3' ) AND "
				cQuery += "AA1_ALOCA='1' AND "

				If lRegiao

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se do tecnico e desta regiao ou de todas as regioes        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cQuery += " AA1_REGIAO IN ( '" + cRegiao + "', '" + ;
							Replicate( "*", Len( AA1->AA1_REGIAO ) ) + "' ) AND "

				EndIf

				cQuery += "D_E_L_E_T_=' '"

				cQuery := ChangeQuery( cQuery )

				DbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasQry, .F., .T. )

				While !( cAliasQry )->( Eof() )

					lNivelTec   := .T.
					nNivel      := 0

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se deve considerar habilidade e se existem habilidades     ³
					//³ necessarias para este produto x ocorrencia                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( lHabilidade ) .AND. !Empty( aHabilReq )
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Caso avalie nivel, verifica se o tecnico tem nivel para atender a todas³
						//³ as habilidades                                                         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						For nLoop := 1 to Len( aHabilReq )
							If lNivelTec
								AA2->( DbSetOrder(2) )
								If AA2->( DbSeek(xFilial("AA2")+( cAliasQry )->AA1_CODTEC + aHabilReq[nLoop,1] ) )
									If ( lNivel )
										nNivel    := AA2->AA2_NIVEL
										lNivelTec := ( nNivel>=aHabilReq[nLoop,2] )
									Else
										lNivelTec := .T.
									EndIf
								Else
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Se nao encontrou, rejeita este tecnico                              ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									lNivelTec := .F.
								EndIf
							Else
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Caso um item nao tenha sido atendido, sai                           ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								Exit
							EndIf
						Next nLoop
					Else
						lNivelTec := .T.
					EndIf


					If ( lNivelTec )
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Adiciona-se as habilidades que o tecnico efetivamente possui        ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If Empty( aHabilReq )
							AAdd(aNivelTec,{ ( cAliasQry )->AA1_CODTEC,CriaVar("AAC_HABIL",.F.)})
						Else
							AEval(aHabilReq,;
								{ |x| AAdd(aNivelTec,{ ( cAliasQry )->AA1_CODTEC,x[1] } ) } )
						EndIf

						nTecnico := aScan(aTecnico,{|x| x[1] == ( cAliasQry )->AA1_CODTEC } )
						If ( nTecnico > 0 )
							aTecnico[nTecnico][2]++
						Else
							aadd(aTecnico,{ ( cAliasQry )->AA1_CODTEC,1})
						EndIf
					EndIf

					( cAliasQry )->( DbSkip() )

				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Apaga a area de trabalho da query                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				DbSelectArea( cAliasQry )
				dbCloseArea()

				DbSelectArea( "AA1" )

			#ELSE

				DbSelectArea("AA1")
				DbSetOrder(2)
				DbSeek(xFilial("AA1")+If(lRegiao,cRegiao,""))

				While ( !Eof() .AND. xFilial("AA1") == AA1_FILIAL .AND.;
											If(lRegiao,cRegiao == AA1->AA1_REGIAO,.T.) )

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se o tecnico e Field Service ou Ambos e se esta disponivel    ³
					//³ para alocacao                                                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If AA1->AA1_TIPO $ "13" .AND. AA1->AA1_ALOCA == "1"

						lNivelTec   := .T.
						nNivel      := 0
						nHabilTot   := 0

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica se deve considerar habilidade e se existem habilidades     ³
						//³ necessarias para este produto x ocorrencia                          ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( lHabilidade ) .AND. !Empty( aHabilReq )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Caso avalie nivel, verifica se o tecnico tem nivel para atender a todas³
							//³ as habilidades                                                         ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							For nLoop := 1 to Len( aHabilReq )
								If lNivelTec
									AA2->( DbSetOrder(2) )
									If AA2->( DbSeek(xFilial("AA2")+AA1->AA1_CODTEC+aHabilReq[nLoop,1] ) )
										If ( lNivel )
											nNivel    := AA2->AA2_NIVEL
											lNivelTec := ( nNivel>=aHabilReq[nLoop,2] )
										Else
											lNivelTec := .T.
										EndIf
									Else
										//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
										//³ Se nao encontrou, rejeita este tecnico                              ³
										//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
										lNivelTec := .F.
									EndIf
								Else
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Caso um item nao tenha sido atendido, sai                           ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									Exit
								EndIf
							Next nLoop
						Else
							lNivelTec := .T.
						EndIf

						If ( lNivelTec )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Adiciona-se as habilidades que o tecnico efetivamente possui        ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If Empty( aHabilReq )
								AAdd(aNivelTec,{AA1->AA1_CODTEC,CriaVar("AAC_HABIL",.F.)})
							Else
								AEval(aHabilReq,;
									{ |x| AAdd(aNivelTec,{ AA1->AA1_CODTEC,x[1] } ) } )
							EndIf

							nTecnico := aScan(aTecnico,{|x| x[1] == AA1->AA1_CODTEC})
							If ( nTecnico > 0 )
								aTecnico[nTecnico][2]++
							Else
								aadd(aTecnico,{AA1->AA1_CODTEC,1})
							EndIf
						EndIf

					EndIf

					DbSelectArea("AA1")
					DbSkip()
				EndDo

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica os tecnicos que atendem a todas as regioes                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lRegiao

					cSeekAA1 := xFilial( "AA1" ) + Replicate( "*", Len( AA1->AA1_REGIAO ) )

					AA1->( DbSetOrder( 2 ) )
					If AA1->( DbSeek( cSeekAA1 ) )
		
						While ( !AA1->( Eof() ) .AND. xFilial( "AA1" ) + AA1->AA1_REGIAO == cSeekAA1 ) 
													
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Verifica se o tecnico e Field Service ou Ambos e se esta disponivel    ³
							//³ para alocacao                                                          ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If AA1->AA1_TIPO $ "13" .AND. AA1->AA1_ALOCA == "1"

								lNivelTec   := .T.
								nNivel      := 0
								nHabilTot   := 0

								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Verifica se deve considerar habilidade e se existem habilidades     ³
								//³ necessarias para este produto x ocorrencia                          ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If ( lHabilidade ) .AND. !Empty( aHabilReq )

									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Caso avalie nivel, verifica se o tecnico tem nivel para atender a todas³
									//³ as habilidades                                                         ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									For nLoop := 1 to Len( aHabilReq )
										If lNivelTec
											AA2->( DbSetOrder(2) )
											If AA2->( DbSeek(xFilial("AA2")+AA1->AA1_CODTEC+aHabilReq[nLoop,1] ) )
												If ( lNivel )
													nNivel    := AA2->AA2_NIVEL
													lNivelTec := ( nNivel>=aHabilReq[nLoop,2] )
												Else
													lNivelTec := .T.
												EndIf
											Else
												//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
												//³ Se nao encontrou, rejeita este tecnico                              ³
												//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
												lNivelTec := .F.
											EndIf
										Else
											//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
											//³ Caso um item nao tenha sido atendido, sai                           ³
											//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
											Exit
										EndIf
									Next nLoop
								Else
									lNivelTec := .T.
								EndIf

								If ( lNivelTec )
									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Adiciona-se as habilidades que o tecnico efetivamente possui        ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									If Empty( aHabilReq )
										AAdd(aNivelTec,{AA1->AA1_CODTEC,CriaVar("AAC_HABIL",.F.)})
									Else
										AEval(aHabilReq,;
											{ |x| AAdd(aNivelTec,{ AA1->AA1_CODTEC,x[1] } ) } )
									EndIf

									If !Empty(nTecnico := aScan(aTecnico,{|x| x[1] == AA1->AA1_CODTEC} ) )
										aTecnico[nTecnico,2]++
									Else
										AAdd(aTecnico,{AA1->AA1_CODTEC, 1 })
									EndIf
								EndIf

							EndIf

							DbSelectArea("AA1")
							DbSkip()
						EndDo

					EndIf

				EndIf

			#ENDIF

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ O array aocorrencia possui uma chave composta de ocorrencia + produto+    ³
			//³ grupo de atendimento, que podem possuir habilidades requeridas diferentes ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( aScan(aOcorrencia,{|x| x[1]==AB7->AB7_CODPRB + AB7->AB7_CODPRO + cGrpAte } ) == 0 )
				If Empty( aHabilReq )
					AAdd(aOcorrencia,{AB7->AB7_CODPRB + AB7->AB7_CODPRO + cGrpAte,nHoras,CriaVar("AAC_HABIL",.F.)})
				Else
					AEval(aHabilReq,{|x|AAdd(aOcorrencia,{AB7->AB7_CODPRB + AB7->AB7_CODPRO + cGrpAte,nHoras,x[1]})})
				EndIf
			EndIf

		EndIf
		DbSelectArea("AB7")
		DbSkip()
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Nesta rotina sao testadas combinacoes de tecnicos a fim de encontrar   ³
	//³ o menor conjunto de tecnicos que resolvam todas as ocorrencias da OS   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Os Tecnicos que solucionarem o maior numero de ocorrencia em uma visita ³
	//³sao os mais indicados para efetuarem a visita.                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aTecnico := aSort(aTecnico,,,{|x,y| x[2] > y[2] })
	For nCntFor := 1 To Len(aTecnico)
		aOwnerTec   := {}
		aAuxiliar   := {}
		nOcorrencia    := 0
		aadd(aBestTec,{})
		nVisitas := Len(aBestTec)
		For nCntFor1 := nCntFor To Len(aTecnico)
			nHoras   := 0
			For nCntFor2 := 1 To Len(aOcorrencia)
				If ( aScan(aNivelTec,{|x|  x[1]==aTecnico[nCntFor1][1].AND.;
													If (Empty(x[2]),.T.,x[2]==aOcorrencia[nCntFor2][3])}) <> 0 )

					If ( aScan(aAuxiliar,{|x| x[1]==aOcorrencia[nCntFor2][1] .AND. x[2]==aOcorrencia[nCntFor2][3] } ) == 0 )
						If Empty( aScan( aAuxiliar,{|x| x[1]==aOcorrencia[nCntFor2,1] } ) )
							nHoras += aOcorrencia[nCntFor2][2]
						EndIf
						AAdd(aAuxiliar,{aOcorrencia[nCntFor2,1],aOcorrencia[nCntFor2,3]})
					EndIf

				EndIf
			Next nCntFor2
			If ( nOcorrencia < Len(aAuxiliar) .AND. nHoras > 0 )
				aadd(aBestTec[nVisitas],{ aTecnico[nCntFor1][1] , nHoras })
				If ( aTecnico[nCntFor1][1] == aTecnico[nCntFor][1] )
					aadd(aOwnerTec,{ aClone(aBestTec[nVisitas]),aClone(aAuxiliar) })
				EndIf
			EndIf
			nOcorrencia := Len(aAuxiliar)
			If ( nOcorrencia == Len(aOcorrencia) )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Apos achar um conjunto de tecnicos validos, tenta verificar se nao ha   ³
				//³mais conjuntos de tecnicos validos do mesmo ponto de partida.           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				aadd(aBestTec,Aclone(aOwnerTec[1][1]))
				aAuxiliar 	:= Aclone(aOwnerTec[1][2])
				nVisitas 	:= Len(aBestTec)
				nOcorrencia := 0
			EndIf
		Next nCntFor1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Aqui sao retirados o conjunto de tecnicos que nao solucionam as ocorren-³
		//³cias em questao.                                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Len(aAuxiliar) <> Len(aOcorrencia) )
			aBestTec := aDel(aBestTec,Len(aBestTec))
		EndIf
	Next nCntFor
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retirar os elementos nulos do Array aBestTec                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Aqui a Matriz aBestTec e Avaliada para verificar o menor numero de      ³
	//³visitas a ser efetuada.                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAuxiliar   := aBestTec
	aBestTec    := {}
	nVisitas    := 0
	For nCntFor := 1 To Len(aAuxiliar)
		If ( aAuxiliar[nCntFor] <> Nil )
			aadd(aBestTec,aAuxiliar[nCntFor])
			nVisitas := If(Len(aAuxiliar[nCntFor]) < nVisitas .OR. nVisitas==0,Len(aAuxiliar[nCntFor]),nVisitas)
		EndIf
	Next nCntFor
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Retirar os elementos com maior numero de Visitas                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aAuxiliar := AClone( aBestTec )
	aBestTec  := {}
	For nCntFor := 1 To Len(aAuxiliar)
		If ( Len(aAuxiliar[nCntFor]) == nVisitas )
			aadd(aBestTec,aAuxiliar[nCntFor])
		EndIf
	Next nCntFor
	If ( Len(aBestTec) == 0 )
		If lNoRotAuto
			Help(" ",1,"ATALOCTEC2",,RetTitle("AB6_NUMOS")+":"+AB6->AB6_NUMOS,4) // Hedit2
		EndIf
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula-se o numero de horas que o grupo de tecnicos atende o Cliente   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aBestTec)
		aadd(aBestHor,{})
		For nCntFor1 := 1 To Len(aBestTec[nCntFor])
			aAuxiliar   := AtNextHora(aBestTec[nCntFor][nCntFor1][1],aBestTec[nCntFor][nCntFor1][2],SA1->A1_PRIOR,dData0,cHora0,.T.)
			aadd(aBestHor[nCntFor], AClone(aAuxiliar) )
		Next nCntFor1
	Next nCntFor

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula-se o Grupo que atende com maior rapidez o Cliente               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTecnico    := 0
	nMenorTempo := 0  
		
		For nCntFor := 1 To Len(aBestHor)
			nHoras := 0
			For nCntFor1 := 1 To Len(aBestHor[nCntFor])
				nCntFor2 := Len(aBestHor[nCntFor][nCntFor1])
				If ( nCntFor2 <> 0 )
					nHoras   += SubtHoras(dData0,cHora0,aBestHor[nCntFor][nCntFor1][nCntFor2][1],aBestHor[nCntFor][nCntFor1][nCntFor2][2])
				EndIf
				IF lLibAgenRH .And. INCLUI
					nX1++
			        aRes :=  ChkCfltAlc(dData0,aBestHor[nCntFor][nCntFor1][nCntFor2][1],aBestTec[nX1][1][1],cHora0,aBestHor[nCntFor][nCntFor1][nCntFor2][2])
				    If  !aRes[1]  .And. !aRes[2] .And. !aRes[3] .And. !aRes[6] 	
				    	lGravTec := .T. 
			    	Else 	
					    lGravTec := .F. 
						nHoras := 0
				    Endif	
				Endif	
			Next nCntFor1

			If ( nHoras < nMenorTempo .OR. nMenorTempo==0 )
				nMenorTempo := nHoras
				If lGravTec
					nTecnico    := nCntFor
				Endif   
			EndIf
		Next nCntFor

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Aloca-se o Grupo Escolhido                                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( nTecnico <> 0 )
		For nCntFor := 1  To Len(aBestTec[nTecnico])
			DbSelectArea("AA1")
			DbSetOrder(1)
			DbSeek(xFilial("AA1")+aBestTec[nTecnico][nCntFor][1])
			lContinua := SoftLock("AA1")
			If ( lContinua )
				aadd(aTravas,{ Alias() , RecNo()})
			Else
				Exit
			EndIf
		Next nCntFor
	Else
		If lNoRotAuto
			Help(" ",1,"ATALOCTEC3",,RetTitle("AB6_NUMOS")+":"+AB6->AB6_NUMOS,4) // Hedit2
		EndIf
	EndIf
	If ( lContinua )
		If lAt450Age
			Begin Transaction
				lPe450Age := ExecBlock("AT450Age",.F.,.F.,{aBestTec, aBestHor, nTecnico , cNumOs})
				If lPe450Age
					AtRefazOs(cNumOs,.F.,,.F.)
				EndIf	
			End Transaction
		ElseIf ( nTecnico <> 0 )
			Begin Transaction
			For nCntFor := 1  To Len(aBestTec[nTecnico])
				For nCntFor1 := 1 To Len(aBestHor[nTecnico][nCntFor])
					RecLock("ABB",.T.)
					ABB->ABB_FILIAL   := xFilial("ABB")
					ABB->ABB_CODTEC   := aBestTec[nTecnico][nCntFor][1]
					ABB->ABB_NUMOS    := cNumOs
					ABB->ABB_DTINI    := aBestHor[nTecnico][nCntFor][nCntFor1][1]
					ABB->ABB_HRINI    := aBestHor[nTecnico][nCntFor][nCntFor1][2]
					ABB->ABB_DTFIM    := aBestHor[nTecnico][nCntFor][nCntFor1][3]
					ABB->ABB_HRFIM    := aBestHor[nTecnico][nCntFor][nCntFor1][4]
					ABB->ABB_HRTOT    := AtTotHora(ABB->ABB_DTINI,ABB->ABB_HRINI,ABB->ABB_DTFIM,ABB->ABB_HRFIM)
					ABB->ABB_OBSERV   := STR0001 //"ALOCADO AUTOMATICAMENTE"
					ABB->ABB_SACRA    := "N"
					ABB->ABB_CODIGO	  := AtABBNumCd()
					ABB->ABB_ENTIDA	  := "AB6"
					ABB->ABB_CHAVE	  := cNumOs
					ABB->ABB_CHEGOU   := "N"
        			ABB->ABB_ATENDE   := "2"
				Next nCntFor1
			Next nCntFor
			AtRefazOs(cNumOs,.F.,,.F.)
			End Transaction
		EndIf
	Else
		If lNoRotAuto
			Help(" ",1,"ATALOCTEC1") // Problemas de travamento do Tecnico
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de entrada executado no final da alocação do técnico.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lAT450Alc
	ExecBlock("AT450Alc",.F.,.F.)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna ao Estado de Entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To Len(aTravas)
	DbSelectArea(aTravas[nCntFor][1])
	dbGoto(aTravas[nCntFor][2])
	MsUnLock()
Next nCntFor
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtNextHora³ Autor ³ Eduardo Riera         ³ Data ³ 23.12.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Indicar o Proximo Horario Disponivel para Alocacao de um   ³±±
±±³          ³ Tecnico.                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpA1: [1] Data Inicial                                     ³±±
±±³          ³ExpA2: [2] Hora Final                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cCodTec    : Codigo do Tecnico                             ³±±
±±³          ³ nHoras     : Tempo a ser Alocado em Horas                  ³±±
±±³          ³ cPriorCli  : Prioridade do Cliente                         ³±±
±±³          ³ dData0     : Data a ser Considerada para alocacao          ³±±
±±³          ³ cHora0     : Hora a ser Considerada para alocacao          ³±±
±±³          ³ lAlocados  : Considera alocados                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtNextHora(	cCodTec,	nHoras,		cPriorCli,		dData0,;
						cHora0,		lAlocados	)

Local aArea       := GetArea()
Local aAreaAA1    := AA1->(GetArea())
Local aAreaSA1    := SA1->(GetArea())
Local aAreaABB    := ABB->(GetArea())
Local aAreaAB7    := AB7->(GetArea())
Local aAreaAB6    := AB6->(GetArea())
Local aAreaAAG    := AAG->(GetArea())
Local aNextHora   := { }
Local aAuxiliar   := { }
Local aAlocados   := { }
Local aTabPadrao  := { }
Local aCalend     := { }
Local cHoraIni1   := ""
Local cHoraFim1   := ""
Local cHoraIni2   := ""
Local cHoraFim2   := ""
Local dDataIni1   := dData0
Local dDataFim1   := dData0
Local dDataIni2   := dData0
Local dDataFim2   := dData0
Local dDataFinal  := dData0
Local cHoraFinal  := ""
Local cHoraInicial:= ""
Local dDataInicial:= dData0
Local nCntFor     := 0
Local cRegra      := SuperGetMV("MV_REGRAAL")
Local lPrioridade := SubStr(cRegra,2,1)=="1"
Local nTotalAloc  := 0
Local nIntervalo  := 0
Local nDifStd     := SubtHoras(dDataBase,"00:00",dDataBase,SuperGetMV("MV_DIFSTD"))
Local cSacraOs    := ""
Local dData1      := dData0+1 //Verifica a Proxima data
Local dProximo
Local lNoRotAuto  := ( Type("lAt450Auto")=="U" .OR. !lAt450Auto ) .AND. ( Type("lAt400Auto")=="U" .OR. !lAt400Auto ) .AND.   ( Type("lAt300Auto")=="U" .OR. !lAt300Auto  )	// Se esta sendo rodado apartir de uma rotina automática
Local lErrAutBkp  := .F.
Local cPriorida	:= " "
Local cSeqTur		:= ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Armazena o valor atual de lMsErroAuto, caso a fTabTurno de erro         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lNoRotAuto .AND. Type("lMsErroAuto")=="L"
	lErrAutBkp := lMsErroAuto
EndIf

cHora0    := SubStr(cHora0,1,5)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Preenche a Tabela aTabPadrao                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( fTabTurno(@aTabPadrao) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Altera a Filial da Tabela Padrao para compatibilizar com o SigaPon      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aTabPadrao)
		aTabPadrao[nCntFor][1] := xFilial("SRA") // Falar com Mauro Testoni
	Next nCntFor
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona Registros                                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AA1")
	DbSetOrder(1)
	DbSeek(xFilial("AA1")+cCodTec)
	//Verifica se há sequencia de turno senão utiliza o padrão 01
	If !Empty(AA1->AA1_SEQTUR)
		cSeqTur := AA1->AA1_SEQTUR
	Else 
		cSeqTur := "01"
	EndIf 	
	If ( CriaCalend(dData0,dData0,AA1->AA1_TURNO,cSeqTur,aTabPadrao,@aCalend,cFilAnt) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica o Horario de Trabalho do Tecnico extraido do Ponto Eletronico  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		While ( Len(aCalend) < 4 )
			If Len(aCalend) == 2
				If !( aCalend[1][6]=="S" ) // Dia não Trabalhado
					dData0++
					aCalend := { }
					CriaCalend(dData0,dData0,AA1->AA1_TURNO,cSeqTur,aTabPadrao,@aCalend,cFilAnt)
				Else
					Exit
				EndIf 
			EndIf	
		EndDo
		
		If Len(aCalend) == 4 		
			cHoraIni1   := StrZero(Int(aCalend[1][3]),2)+":"+StrZero(((aCalend[1][3]-Int(aCalend[1][3]))*100),2)
			cHoraFim1   := StrZero(Int(aCalend[2][3]),2)+":"+StrZero(((aCalend[2][3]-Int(aCalend[2][3]))*100),2)
			cHoraIni2   := StrZero(Int(aCalend[3][3]),2)+":"+StrZero(((aCalend[3][3]-Int(aCalend[3][3]))*100),2)
			cHoraFim2   := StrZero(Int(aCalend[4][3]),2)+":"+StrZero(((aCalend[4][3]-Int(aCalend[4][3]))*100),2)
			dDataIni1   := dData0
			dDataFim1   := dData0
			dDataIni2   := dData0
			dDataFim2   := dData0
			
		Do Case
			Case ( cHoraFim1 < cHoraIni1 )
				dDataFim1 := dData0+1
				dDataIni2 := dDataFim1
				dDataFim2 := dDAtaIni2
			Case ( cHoraIni2 < cHoraFim1 )
				dDataIni2 := dData0+1
				dDataFim2 := dDataIni2
			Case ( cHoraFim2 < cHoraIni1 )
				dDataFim2 := dData0+1
		EndCase
			
		ElseIf Len(aCalend) == 2
			cHoraIni1   := StrZero(Int(aCalend[1][3]),2)+":"+StrZero(((aCalend[1][3]-Int(aCalend[1][3]))*100),2)
			cHoraFim2   := StrZero(Int(aCalend[2][3]),2)+":"+StrZero(((aCalend[2][3]-Int(aCalend[2][3]))*100),2)
			cHoraIni2   := "00:00"
			cHoraFim1   := "00:00"
			dDataIni1   := dData0
			dDataFim1   := dData0
			dDataIni2   := dData0
			dDataFim2   := dData0
			
			If cHoraFim2 < cHoraIni1 
				dDataFim2 := dData0+1
			EndIf
			
		EndIf

		nIntervalo     := SubtHoras(dDataFim1,cHoraFim1,dDataIni2,cHoraIni2)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica a Proxima data                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dData1 := dData0+1
		While ( Empty(dProximo) )
			CriaCalend(dData1,dData1,AA1->AA1_TURNO,cSeqTur,aTabPadrao,@aCalend,cFilAnt)
			If ( aCalend[1][6]=="S" ) // Dia Trabalhado
				dProximo := aCalend[1][1]
			EndIf
			dData1++
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se ha Ordens de Servico Sacramentadas                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("ABB")
		DbSetOrder(5)
		DbSeek(xFilial("ABB")+cCodTec+"S"+Dtos(dData0)+cHora0,.T.)
		If (  xFilial("ABB") == ABB->ABB_FILIAL .AND.;
				cCodTec        == ABB->ABB_CODTEC )
			cSacraOs := "S"
		Else
			cSacraOs := "N"
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica as Ordens de Servico alocadas no Dia dData0                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("ABB")
		DbSetOrder(1)
		DbSeek(xFilial("ABB")+cCodTec+Dtos(dData0)+cHora0,.T.)
		While ( !Eof() .AND. xFilial("ABB") == ABB->ABB_FILIAL .AND.;
									cCodTec        == ABB->ABB_CODTEC .AND.;
									dData0         == ABB->ABB_DTINI  .AND.;
									cHora0         <= ABB->ABB_HRINI  .AND.;
									lAlocados)
			TxSeekCli(ABB->ABB_NUMOS,"A1_PRIOR","",@cPriorida," ")
			aadd(aAlocados,{  cPriorida,;
									cSacraOs,;
									ABB->ABB_DTINI,;
									ABB->ABB_HRINI,;
									ABB->ABB_DTFIM,;
									ABB->ABB_HRFIM,;
									ABB->ABB_NUMOS })
			cPriorida := " "
			DbSelectArea("ABB")
			DbSkip()
		EndDo
		DbSelectArea("ABB")
		DbSetOrder(2)
		DbSeek(xFilial("ABB")+cCodTec+Dtos(dData0)+cHora0,.T.)
		While ( !Eof() .AND. xFilial("ABB") == ABB->ABB_FILIAL .AND.;
									cCodTec        == ABB->ABB_CODTEC .AND.;
									dData0         == ABB->ABB_DTFIM  .AND.;
									cHora0         <= ABB->ABB_HRFIM  .AND.;
									lAlocados)
			TxSeekCli(ABB->ABB_NUMOS,"A1_PRIOR","",@cPriorida," ")
			If ( aScan(aAlocados,{|x| x[7] == ABB->ABB_NUMOS })==0 )
				aadd(aAlocados,{  cPriorida,;
										cSacraOs,;
										ABB->ABB_DTINI,;
										ABB->ABB_HRINI,;
										ABB->ABB_DTFIM,;
										ABB->ABB_HRFIM,;
										ABB->ABB_NUMOS })
			EndIf
			cPriorida := " "
			DbSelectArea("ABB")
			DbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Ordena o Array de itens alocados.                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aSort(aAlocados,,,{|x,y| Dtos(x[3])+x[4]+Dtos(x[5])+x[6] < Dtos(y[3])+y[4]+Dtos(y[5])+y[6]})
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa o Proximo Horario Disponivel.                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Caso a Prioridade do Cliente seja Considerada.                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lPrioridade )
			For nCntFor := 1 To Len(aAlocados)
				If ( aAlocados[nCntFor][2]<>"S" .AND. cPriorCli < aAlocados[nCntFor][1] )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula a Hora Inicial                                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( nCntFor == 1                      .AND. ;
						 cHora0   <  aAlocados[nCntFor][4]  .AND. ;
						 dData0   >= aAlocados[nCntFor][3] )
						cHoraInicial := cHora0
						dDataInicial := dData0
					Else
						cHoraInicial := aAlocados[nCntFor][4]
						dDataInicial := aAlocados[nCntFor][3]
					EndIf
					If ( cHoraInicial < cHoraIni1 .AND. dDataInicial==dData0 )
						cHoraInicial := cHoraIni1
						dDataInicial := dDataIni1
					EndIf
					If ( SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1) <= 0 .AND.;
							SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2) >= 0 )
						cHoraInicial := cHoraIni2
						dDataInicial := dDataIni2
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Quando a horario de inicio for diferente de um intervalor deve-se con-  ³
					//³siderar o parametro MV_DIFSTD.                                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If (  cHoraInicial <> cHoraIni1 .AND. dDataInicial == dDataIni1 .AND.;
							cHoraInicial <> cHoraIni2 .AND. dDataInicial == dDataIni2)
						SomaDiaHor(@dDataInicial,@cHoraInicial,nDifStd)
					EndIf
					If (  SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2)<=0 )
						nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim2,cHoraFim2)
						nIntervalo  := 0
					Else
						nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1)
						If ( nTotalAloc >= nHoras )
							nIntervalo  := 0
						EndIf
						nTotalAloc  += SubtHoras(dDataIni2,cHoraIni2,dDataFim2,cHoraFim2)
					EndIf
					nTotalAloc  := If(nTotalAloc<0,0,nTotalAloc)
					If ( nTotalAloc >= nHoras )
						dDataFinal  := dDataInicial
						cHoraFinal  := cHoraInicial
						SomaDiaHor(@dDataFinal,@cHoraFinal,nIntervalo+nHoras)
						aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
					Else
						If ( nTotalAloc > 0 )
							cHoraFinal  := cHoraFim2
							dDataFinal  := dDataFim2
							nHoras      -= nTotalAloc
							aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
						EndIf
						aAuxiliar   := AtNextHora(cCodTec,nHoras,cPriorCli,dProximo,"00:00",.F.)
					EndIf
					nCntFor := Len(aAlocados)+1
				EndIf
			Next nCntFor
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Caso nao achou nenhum para realocar,aloca no fim                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Empty(aNextHora) .AND. Empty(aAuxiliar) )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Caso tenha itens alocados no dia solicitado.                            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nCntfor := Len(aAlocados)
			If ( nCntFor <> 0 )
				If ( aAlocados[nCntFor][5] == dData0 )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Calcula a Hora Inicial                                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cHoraInicial := aAlocados[nCntFor][6]
					dDataInicial := aAlocados[nCntFor][5]
					If ( cHoraInicial < cHoraIni1 .AND. dDataInicial==dData0 )
						cHoraInicial := cHoraIni1
						dDataInicial := dDataIni1
					EndIf
					If ( SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1) <= 0 .AND.;
							SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2) >= 0 )
						cHoraInicial := cHoraIni2
						dDataInicial := dDataIni2
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Quando a horario de inicio for diferente de um intervalor deve-se con-  ³
					//³siderar o parametro MV_DIFSTD.                                          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If (  cHoraInicial <> cHoraIni1 .AND. dDataInicial == dDataIni1 .AND.;
							cHoraInicial <> cHoraIni2 .AND. dDataInicial == dDataIni2)
						SomaDiaHor(@dDataInicial,@cHoraInicial,nDifStd)
					EndIf
					If (  SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2)<=0 )
						nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim2,cHoraFim2)
						nIntervalo  := 0
					Else
						nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1)
						If ( nTotalAloc >= nHoras )
							nIntervalo  := 0
						EndIf
						nTotalAloc  += SubtHoras(dDataIni2,cHoraIni2,dDataFim2,cHoraFim2)
					EndIf
					nTotalAloc  := If(nTotalAloc<0,0,nTotalAloc)
					If ( nTotalAloc >= nHoras )
						dDataFinal  := dDataInicial
						cHoraFinal  := cHoraInicial
						SomaDiaHor(@dDataFinal,@cHoraFinal,nIntervalo+nHoras)
						aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
					Else
						If ( nTotalAloc > 0 )
							cHoraFinal  := cHoraFim2
							dDataFinal  := dDataFim2
							nHoras      -= nTotalAloc
							aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
						EndIf
						aAuxiliar   := AtNextHora(cCodTec,nHoras,cPriorCli,dProximo,"00:00",lAlocados)
					EndIf
				Else
					aAuxiliar   := AtNextHora(cCodTec,nHoras,cPriorCli,dProximo,"00:00",lAlocados)
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Caso nao tenha itens alocados no Dia                                    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Calcula a Hora Inicial                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cHoraInicial := cHora0
				If ( cHoraInicial < cHoraIni1 .AND. dDataInicial<=dData0 )
					cHoraInicial := cHoraIni1
					dDataInicial := dDataIni1
				EndIf
				If ( SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1) <= 0 .AND.;
						SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2) >= 0 )
					cHoraInicial := cHoraIni2
					dDataInicial := dDataIni2
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Quando a horario de inicio for diferente de um intervalor deve-se con-  ³
				//³siderar o parametro MV_DIFSTD.                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (  cHoraInicial <> cHoraIni1 .AND. dDataInicial == dDataIni1 .AND.;
						cHoraInicial <> cHoraIni2 .AND. dDataInicial == dDataIni2)
					SomaDiaHor(@dDataInicial,@cHoraInicial,nDifStd)
				EndIf
				If (  SubtHoras(dDataInicial,cHoraInicial,dDataIni2,cHoraIni2)<=0 )
					nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim2,cHoraFim2)
					nIntervalo  := 0
				Else
					nTotalAloc  += SubtHoras(dDataInicial,cHoraInicial,dDataFim1,cHoraFim1)
					If ( nTotalAloc >= nHoras )
						nIntervalo  := 0
					EndIf
					nTotalAloc  += SubtHoras(dDataIni2,cHoraIni2,dDataFim2,cHoraFim2)
				EndIf
				nTotalAloc  := If(nTotalAloc<0,0,nTotalAloc)
				If ( nTotalAloc >= nHoras )
					dDataFinal  := dDataInicial
					cHoraFinal  := cHoraInicial
					SomaDiaHor(@dDataFinal,@cHoraFinal,nIntervalo+nHoras)
					aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
				Else
					If ( nTotalAloc > 0 )
						cHoraFinal  := cHoraFim2
						dDataFinal  := dDataFim2
						nHoras      -= nTotalAloc
						aNextHora   := {{ dDataInicial,cHoraInicial,dDataFinal,cHoraFinal }}
					EndIf
					aAuxiliar   := AtNextHora(cCodTec,nHoras,cPriorCli,dProximo,"00:00",lAlocados)
				EndIf
			EndIf
		EndIf
	EndIf
EndIf
For nCntFor := 1 To Len(aAuxiliar)
	aadd(aNextHora,aAuxiliar[nCntFor])
Next nCntFor

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura o valor atual de lMsErroAuto                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lNoRotAuto .AND. Type("lMsErroAuto")=="L"
	lMsErroAuto := lErrAutBkp
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna ao Estado de Entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaAA1)
RestArea(aAreaSA1)
RestArea(aAreaABB)
RestArea(aAreaAB7)
RestArea(aAreaAB6)
RestArea(aAreaAAG)
RestArea(aArea)

Return(aNextHora)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtRefazOs ³Autor  ³Eduardo Riera          ³ Data ³04.12.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Refaz a Alocacao de uma OS.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica se a alocacao foi refeita                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Numero da OS                                         ³±±
±±³          ³ExpL2: Indica se ha Operacao e pelo inicio (.T.) ou fim(.F.)³±±
±±³          ³ExpN3: Horas Alocadas do item deletado.                     ³±±
±±³          ³ExpL4: Indicas se a operacao solicitante e de exclusao.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta funcao nao reavalia a melhor alocacao possivel quando  ³±±
±±³          ³esta sendo feita a movimentacao de horarios, pois tal       ³±±
±±³          ³processamento demanda muito tempo de CPU e em alguns casos  ³±±
±±³          ³pode deixar a processo de alocacao em Loop infinito.        ³±±
±±³          ³Esta funcao considera os itens sacramentados ramentados     ³±±
±±³          ³visto que numa exclusao o sistema deve mover os itens aloca-³±±
±±³          ³dos independentemente de estar sacramentado ou nao.         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtRefazOs(cNumOS,lInicio,nHorasAloc,lExclui)

Local aArea    := GetArea()
Local aAreaABB := ABB->(GetArea())
Local aAreaAA1 := AA1->(GetArea())
Local cTecnico := ""
Local aTravas  := { }
Local nCntFor  := 0
Local nCntFor1 := 0
Local aTecnicos:= { }
Local nTecnicos:= 0
Local lContinua:= .T.
Local aRegABB  := {}
Local cQuebra  := ""
Local aAuxiliar:= { }
Local nHoras   := 0
Local cOsAnt   := ""
Local lSacra   := .F.
Local aOrdem   := {}
Local nOrdem   := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Registros                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("ABB")
DbSetOrder(3)
DbSeek(xFilial("ABB")+cNumOs)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os tecnicos envolvidos, calcula a data e hora de referencia pa-³
//³pesquisa e a data e a hora de inicio da alocacao. Deleta a alocacao da  ³
//³ordem de servico, se solicitado pelo parametro lExclui ou se for pelo   ³
//³inicio.                                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Begin Transaction
	While ( !Eof() .AND. xFilial("ABB") == ABB->ABB_FILIAL .AND.;
								cNumOs         == ABB->ABB_NUMOS  .AND.;
								lContinua )
		cTecnico := ABB->ABB_CODTEC
		DbSelectArea("AA1")
		DbSetOrder(1)
		DbSeek(xFilial("AA1")+cTecnico)
		If ( RecLock("AA1") )
			aadd(aTravas,{ Alias() , RecNo() })
			If ( ! lExclui )
				nTecnicos   := aScan(aTecnicos,{|x| x[1] == cTecnico})
				If ( nTecnicos == 0 )
					aadd(aTecnicos,{ cTecnico , , , , })
					nTecnicos   := Len(aTecnicos)
				EndIf
				If ( lInicio )
					If ( Empty(aTecnicos[nTecnicos][2]) .OR. aTecnicos[nTecnicos][2] > ABB->ABB_DTINI )
						aTecnicos[nTecnicos][2] := ABB->ABB_DTINI
					EndIf
					If ( Empty(aTecnicos[nTecnicos][3]) .OR. aTecnicos[nTecnicos][3] > ABB->ABB_HRINI ) .AND.;
						aTecnicos[nTecnicos][2] == ABB->ABB_DTINI
						aTecnicos[nTecnicos][3] := ABB->ABB_HRINI
					EndIf
				Else
					If ( Empty(aTecnicos[nTecnicos][2]) .OR. aTecnicos[nTecnicos][2] > ABB->ABB_DTFIM )
						aTecnicos[nTecnicos][2] := ABB->ABB_DTFIM
					EndIf
					If ( Empty(aTecnicos[nTecnicos][3]) .OR. aTecnicos[nTecnicos][3] > ABB->ABB_HRFIM ) .AND.;
						aTecnicos[nTecnicos][2] == ABB->ABB_DTFIM
						aTecnicos[nTecnicos][3] := ABB->ABB_HRFIM
					EndIf
				EndIf
				If ( Empty(aTecnicos[nTecnicos][4]) .OR. aTecnicos[nTecnicos][4] > ABB->ABB_DTINI )
					aTecnicos[nTecnicos][4] := ABB->ABB_DTINI
				EndIf
				If ( Empty(aTecnicos[nTecnicos][5]) .OR. aTecnicos[nTecnicos][5] > ABB->ABB_HRINI ) .AND.;
					aTecnicos[nTecnicos][4] == ABB->ABB_DTINI
					aTecnicos[nTecnicos][5] := ABB->ABB_HRINI
				EndIf
				If ( lInicio )
					If ( ABB->ABB_SACRA<>"S" )
						RecLock("ABB")
						DbDelete()
					Else
						nHorasAloc += SubtHoras(ABB->ABB_DTINI,ABB->ABB_HRINI,ABB->ABB_DTFIM,ABB->ABB_HRFIM)
						lSacra := .T.
					EndIf
				EndIf
			Else
				RecLock("ABB")
				DbDelete()
			EndIf
		Else
			lContinua := .F.
		EndIf
		DbSelectArea("ABB")
		DbSkip()
	EndDo
End Transaction
If ( !lExclui )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se ha itens sacramentados                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nCntFor := 1 To Len(aTecnicos)
		DbSelectArea("ABB")
		DbSetOrder(5)
		DbSeek(xFilial("ABB")+aTecnicos[nCntFor][1]+"S"+Dtos(aTecnicos[nCntFor][4])+aTecnicos[nCntFor][5],.T.)
		If (  xFilial("ABB")          == ABB->ABB_FILIAL .AND.;
				aTecnicos[nCntFor][1]   == ABB->ABB_CODTEC )
			lSacra := .T.
			nCntFor := Len(aTecnicos)+1
		EndIf
	Next nCntFor
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Move os itens alocacados para o novo horario                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lContinua .AND. !lSacra .AND. !lExclui)
	For nCntFor := 1 To Len(aTecnicos)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Guarda os registros processados pois a chave sera alterada              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("ABB")
		DbSetOrder(4)
		DbSeek(xFilial("ABB")+aTecnicos[nCntFor][1]+Dtos(aTecnicos[nCntFor][4])+aTecnicos[nCntFor][5],.T.)
		While ( !Eof() .AND. xFilial("ABB")       == ABB->ABB_FILIAL .AND.;
									aTecnicos[nCntFor][1]== ABB->ABB_CODTEC .AND.;
									aTecnicos[nCntFor][4]<= ABB->ABB_DTINI )
			aadd(aOrdem,ABB->(RecNo()))
			DbSelectArea("ABB")
			DbSkip()
		EndDo
		For nOrdem := 1 To Len(aOrdem)
			DbSelectArea("ABB")
			dbGoto(aOrdem[nOrdem])
			cQuebra  := ABB->ABB_NUMOS
			cOsAnt   := ABB->ABB_NUMOS
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Soma as Horas Alocadas                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( cNumOs <> ABB->ABB_NUMOS )
				aadd(aRegABB,ABB->(RecNo()))
				nHoras   += AtHoraTec(ABB->ABB_CODTEC,ABB->ABB_DTINI,ABB->ABB_HRINI,ABB->ABB_DTFIM,ABB->ABB_HRFIM)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica o Proximo Registro                                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("ABB")
			If ( nOrdem+1 <= Len(aOrdem) )
				dbGoto(aOrdem[nOrdem+1])
			Else
				dbGoto(LastRec()+1)
			EndIf
			If ( Empty(cQuebra) .OR. ABB->ABB_NUMOS <> cQuebra )
				If ( Len(aRegABB) <> 0 )
					Begin Transaction
						aAuxiliar:= AtNextHora(aTecnicos[nCntFor][1],nHoras," ",aTecnicos[nCntFor][2],aTecnicos[nCntFor][3],.F.)
						For nCntFor1 := Len(aAuxiliar)+1 To Len(aRegABB)
							DbSelectArea("ABB")
							dbGoto(aRegABB[nCntFor1])
							RecLock("ABB")
							DbDelete()
						Next nCntFor1
						For nCntFor1 := 1 To Len(aAuxiliar)
							If ( nCntFor1 <= Len(aRegABB) )
								DbSelectArea("ABB")
								dbGoto(aRegABB[nCntFor1])
								RecLock("ABB")
							Else
								RecLock("ABB",.T.)
							EndIf
							ABB->ABB_FILIAL   := xFilial("ABB")
							ABB->ABB_CODTEC   := aTecnicos[nCntFor][1]
							ABB->ABB_NUMOS    := cOsAnt
							ABB->ABB_DTINI    := aAuxiliar[nCntFor1][1]
							ABB->ABB_HRINI    := aAuxiliar[nCntFor1][2]
							ABB->ABB_DTFIM    := aAuxiliar[nCntFor1][3]
							ABB->ABB_HRFIM    := aAuxiliar[nCntFor1][4]
							ABB->ABB_HRTOT    := AtTotHora(ABB->ABB_DTINI,ABB->ABB_HRINI,ABB->ABB_DTFIM,ABB->ABB_HRFIM)
							ABB->ABB_OBSERV   := STR0001 //"ALOCADO AUTOMATICAMENTE"
							ABB->ABB_SACRA    := If(Empty(ABB->ABB_SACRA),"N",ABB->ABB_SACRA)
						Next nCntFor1
					End Transaction
					aRegABB  := {}
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Determina o novo horario de inicio da alocacao.                         ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aTecnicos[nCntFor][2]   := ABB->ABB_DTFIM
					aTecnicos[nCntFor][3]   := ABB->ABB_HRFIM
				EndIf
			EndIf
			If ( Len(aRegABB) == 0 )
				nHoras := 0
			EndIf
		Next nOrdem
	Next nCntFor
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna ao Estado de Entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To Len(aTravas)
	DbSelectArea(aTravas[nCntFor][1])
	dbGoto(aTravas[nCntFor][2])
	MsUnLock()
Next nCntFor

RestArea(aAreaAA1)
RestArea(aAreaABB)
RestArea(aArea)

Return(lContinua)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtOSTempo ³ Autor ³ Eduardo Riera         ³ Data ³ 09.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calcula o Tempo decorrido desde o Chamado ate o Atendimento.³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³[1] Nr.de Horas decorridas                                  ³±±
±±³          ³[2] Nr.de Horas ate a alocacao                              ³±±
±±³          ³[3] Indica se o tecnico compareceu no local.                ³±±
±±³          ³[4] Nr.de Horas alocadas.                                   ³±±
±±³          ³[5][1] Data do Chamado                                      ³±±
±±³          ³[5][2] Hora do Chamado                                      ³±±
±±³          ³[6][1] Data da Alocacao - Primeira                          ³±±
±±³          ³[6][2] Hora da Alocacao - Primeira                          ³±±
±±³          ³[7][1] Data da Alocacao - Termino                           ³±±
±±³          ³[7][2] Hora da Alocacao - Termino                           ³±±
±±³          ³[8] Indica se foi confirmado o atendimento                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³cNumOs : Numero da OS.                                      ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtOSTempo(cNumOs)

Local aArea    := GetArea()
Local aAreaABB := ABB->(GetArea())
Local aAreaAB9 := AB9->(GetArea())
Local aAreaAB6 := AB6->(GetArea())
Local aAreaAB7 := AB7->(GetArea())
Local aAreaAB3 := AB3->(GetArea())
Local aAreaAB4 := AB4->(GetArea())
Local aAreaAB2 := AB2->(GetArea())
Local aAreaAB1 := AB1->(GetArea())
Local dData0
Local cHora0
Local dDataI   := Ctod("")
Local cHoraI   := ""
Local dDataFAl := Ctod("")
Local cHoraFAl := ""
Local dDataF   := Ctod("")
Local cHoraF   := ""
Local nAteAloc := 0
Local nHorasAl := 0
Local nHoras   := 0
Local lChegou  := .F.
Local nDecorrid:= 0
Local lAtendeu := .F.

Local bWhileAB7:= ""
Local bWhileAB9:= ""

Local cQuery   := ""
Local cAliasAB7:= ""
Local cAliasAB9:= ""
Local cAliasABB:= ""
Local lQueryAB9:= .F.
Local lQueryABB:= .F.
Local lQueryAB7:= .F.

lQueryAB7 := .F.


lQueryAB7 := .T.

cAliasAB7 := "ATOSTMPAB7"

cQuery := "SELECT AB7_NRCHAM, AB7_NUMORC, AB7_TIPO, AB7_NUMOS, AB7_ITEM FROM "
cQuery += RetSqlName( "AB7" ) + " AB7 "
cQuery += "WHERE "
cQuery += "AB7_FILIAL='" + xFilial( "AB7" ) + "' AND "
cQuery += "AB7_NUMOS='"  + cNumOs           + "' AND "
cQuery += "AB7.D_E_L_E_T_=' '"

cQuery := ChangeQuery( cQuery )

DbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB7, .F., .T. )

bWhileAB7 := { || !( cAliasAB7 )->( Eof() ) }

While Eval( bWhileAB7 )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula a data de Inicio                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Empty(dData0) )
		Do Case
			Case !Empty( ( cAliasAB7 )->AB7_NRCHAM )
				DbSelectArea("AB2")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB2")+( cAliasAB7 )->AB7_NRCHAM) )
					DbSelectArea("AB1")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AB1")+AB2->AB2_NRCHAM) )
						dData0 := AB1->AB1_EMISSA
						cHora0 := AB1->AB1_HORA
					EndIf
				EndIf
			Case !Empty( ( cAliasAB7 )->AB7_NUMORC )
				DbSelectArea("AB4")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB4")+( cAliasAB7 )->AB7_NUMORC) )
					DbSelectArea("AB3")
					DbSetOrder(1)
					If ( DbSeek(xFilial("AB3")+AB4->AB4_NUMORC) )
						dData0 := AB3->AB3_EMISSA
						cHora0 := AB3->AB3_HORA
					EndIf
				EndIf
		EndCase
		If ( Empty(dData0) )

			DbSelectArea("AB6")
			DbSetOrder(1)
			DbSeek(xFilial("AB6")+cNumOs)

			dData0 := AB6->AB6_EMISSA
			cHora0 := AB6->AB6_HORA
		EndIf
	EndIf
	If ( ( cAliasAB7 )->AB7_TIPO $ "42" )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calcula o Termino                                                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		lQueryAB7 := .F.

		#IFDEF TOP

			lQueryAB9 := .T.

			cAliasAB9 := "ATOSTMPAB9"

		  	cQuery := "SELECT AB9_DTFIM, AB9_HRFIM FROM "
			cQuery += RetSqlName( "AB9" ) + " AB9 "
		  	cQuery += "WHERE "
		  	cQuery += "AB9_FILIAL='" + xFilial( "AB9" ) + "' AND "
		  	cQuery += "AB9_NUMOS='"  + ( cAliasAB7 )->AB7_NUMOS + ( cAliasAB7 )->AB7_ITEM + "' AND "
		  	cQuery += "AB9.D_E_L_E_T_=' '"

		  	cQuery := ChangeQuery( cQuery )

		  	DbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB9, .F., .T. )

		  	TcSetField( cAliasAB9, "AB9_DTFIM", "D", 8, 0 )

		  	bWhileAB9 := { || !( cAliasAB9 )->( Eof() ) }

		#ELSE

			lQueryAB9 := .F.

			cAliasAB9 := "AB9"

			DbSelectArea("AB9")
			DbSetOrder(1)

			DbSeek(xFilial("AB9")+( cAliasAB7 )->AB7_NUMOS + ( cAliasAB7 )->AB7_ITEM  )
		   bWhileAB9 := { || !AB9->( Eof() ) .AND. xFilial("AB9") == AB9->AB9_FILIAL .AND.;
									( cAliasAB7 )->AB7_NUMOS + ( cAliasAB7 )->AB7_ITEM == AB9->AB9_NUMOS }
		#ENDIF

		While Eval( bWhileAB9 )

			dDataF := Max(dDataF,( cAliasAB9 )->AB9_DTFIM)
			If ( ( cAliasAB9 )->AB9_DTFIM == dDataF )
				cHoraF := If(cHoraF< ( cAliasAB9 )->AB9_HRFIM, ( cAliasAB9 )->AB9_HRFIM,cHoraF)
			EndIf

			( cAliasAB9 )->( DbSkip() )
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Exclui a area de trabalho da query                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP
			DbSelectArea( cAliasAB9 )
			dbCloseArea()
			DbSelectArea( "AB9" )
		#ENDIF

	Else
		dDataF := dDataBase
		If FindFunction("TecBisTir") .AND. TecBisTir()
			cHoraF := "00:00:00"
		Else
			cHoraF := Time()
		EndIf
	EndIf

	( cAliasAB7 )->( DbSkip() )

EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exclui a area de trabalho da query                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	DbSelectArea( cAliasAB7 )
	dbCloseArea()
	DbSelectArea( "AB7" )
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula quanto tempo decorreu-se ate a primeira alocacao                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

lQueryABB := .F.

#IFDEF TOP

	lQueryABB := .T.

	cAliasABB := "ATOSTMPABB"

  	cQuery := "SELECT * FROM "
	cQuery += RetSqlName( "ABB" ) + " ABB "
  	cQuery += "WHERE "
  	cQuery += "ABB_FILIAL='" + xFilial( "ABB" ) + "' AND "
  	cQuery += "ABB_NUMOS='"  + cNumOs           + "' AND "
  	cQuery += "ABB.D_E_L_E_T_=' '"

  	cQuery := ChangeQuery( cQuery )

  	DbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasABB, .F., .T. )

  	TcSetField( cAliasABB, "ABB_DTINI", "D", 8, 0 )
  	TcSetField( cAliasABB, "ABB_DTFIM", "D", 8, 0 )

  	bWhileABB := { || !( cAliasABB )->( Eof() ) }

#ELSE

	lQueryABB := .F.

	cAliasABB := "ABB"

	DbSelectArea("ABB")
	DbSetOrder(3)
	DbSeek(xFilial("ABB")+cNumOs)

   bWhileABB := { || ( !ABB->( Eof() ) .AND. xFilial("ABB") == ABB->ABB_FILIAL .AND.;
													cNumOs == ABB->ABB_NUMOS )	}

#ENDIF

While Eval( bWhileABB )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Obtem a data / hora inicial da primeira alocacao                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Empty(dDataI) .OR. ( ( DToS( dDataI ) + cHoraI ) > ( DToS( ( cAliasABB )->ABB_DTINI ) + ( cAliasABB )->ABB_HRINI ) ) )
		dDataI := ( cAliasABB )->ABB_DTINI
		cHoraI := ( cAliasABB )->ABB_HRINI
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Obtem a data / hora final e status da ultimo alocacao                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( ( DToS( dDataFAl ) + cHoraFAl ) < ( DToS( ( cAliasABB )->ABB_DTFIM ) + ( cAliasABB )->ABB_HRFIM ) )
		dDataFAl := ( cAliasABB )->ABB_DTFIM
		cHoraFAl := ( cAliasABB )->ABB_HRFIM

		lChegou  := ( ( cAliasABB )->ABB_CHEGOU == "S" )
		lAtendeu := ( ( cAliasABB )->ABB_ATENDE == "1" )
	EndIf
	nHoras   := SubtHoras(dData0,cHora0,( cAliasABB )->ABB_DTINI,( cAliasABB )->ABB_HRINI)
	nHoras   -= AtHrNUtil(dData0,( cAliasABB )->ABB_DTINI)
	nAteAloc := If(nAteAloc<>0,Min(nHoras,nAteAloc),nHoras)
	nHorasAl += SubtHoras(( cAliasABB )->ABB_DTINI,( cAliasABB )->ABB_HRINI,( cAliasABB )->ABB_DTFIM,( cAliasABB )->ABB_HRFIM)
	nHorasAl -= AtHrNUtil(( cAliasABB )->ABB_DTINI,( cAliasABB )->ABB_DTFIM)

	( cAliasABB )->( DbSkip() )

EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exclui a area de trabalho da query                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	DbSelectArea( cAliasABB )
	dbCloseArea()
	DbSelectArea( "ABB" )
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o numero de horas decorridas                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nDecorrid := SubtHoras(dData0,cHora0,dDataF,cHoraF)
nDecorrid -= AtHrNUtil(dData0,dDataF)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna ao Estado de Entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaAB1)
RestArea(aAreaAB2)
RestArea(aAreaAB3)
RestArea(aAreaAB4)
RestArea(aAreaAB6)
RestArea(aAreaAB7)
RestArea(aAreaAB9)
RestArea(aAreaABB)
RestArea(aArea)

Return({ nDecorrid,;
			nAteAloc ,;
			lChegou ,;
			nHorasAl,;
			{ dData0, cHora0},;
			{ dDataI, cHoraI},;
			{ dDataFAl, cHoraFAl}, lAtendeu })

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtHrNUtil ³ Autor ³ Eduardo Riera         ³ Data ³10.01.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calcula o numero de horas que devem ser desconsideradas do  ³±±
±±³          ³periodo.                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Numero de Dias                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³dDtIni: Data Inicial                                        ³±±
±±³          ³dDtFim: Data Final                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtHrNUtil(dDtIni,dDtFim)

Local nHrNUtil := SuperGetMV("MV_ATHRNUT")
Local nHoras   := 0

Default dDtIni := Date()
Default dDtFim := Date()

While ( dDtIni <= dDtFim )
	While ( dDtIni <> DataValida(dDtIni,.T.) )
		nHoras += nHrNUtil
		dDtIni++
	EndDo
	dDtIni++
EndDo
Return(nHoras)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtHoraTec ³ Autor ³ Eduardo Riera         ³ Data ³ 18.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Soma o numero de horas alocadas conforme o horario de tra- ³±±
±±³          ³ balho do tecnico.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Retorna o numero de horas alocadas conforme tabela   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do Tecnico                                    ³±±
±±³          ³ExpD1: Data Inicial                                         ³±±
±±³          ³ExpC1: Hora Inicial                                         ³±±
±±³          ³ExpD2: Data Final                                           ³±±
±±³          ³ExpC2: Hora Final                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtHoraTec(	cCodTec,	dDataIni,	cHoraIni,	dDataFim,;
							cHoraFim	)

Local aArea       := GetArea()
Local aAreaAA1    := AA1->(GetArea())
Local nHoras      := 0
Local aTabPadrao  := {}
Local aCalend     := {}
Local cHoraIni1   := ""
Local cHoraIni2   := ""
Local cHoraFim1   := ""
Local cHoraFim2   := ""
Local dDataIni1   := dDataIni
Local dDataIni2   := dDataIni
Local dDataFim1   := dDataIni
Local dDataFim2   := dDataIni
Local nIntervalo  := 0
Local cSeqTur	  := "" 

If ( fTabTurno(@aTabPadrao) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Altera a Filial da Tabela Padrao para compatibilizar com o SigaPon      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aTabPadrao[1][1] := xFilial("SRA") // Falar com Mauro Testoni
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona Registros                                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AA1")
	DbSetOrder(1)
	DbSeek(xFilial("AA1")+cCodTec)
	//Verifica se há sequencia de turno senão utiliza o padrão 01
	If !Empty(AA1->AA1_SEQTUR)
		cSeqTur := AA1->AA1_SEQTUR
	Else 
		cSeqTur := "01"
	EndIf
	If ( CriaCalend(dDataIni,dDataIni,AA1->AA1_TURNO,cSeqTur,aTabPadrao,@aCalend,cFilAnt) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica o Horario de Trabalho do Tecnico extraido do Ponto Eletronico  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Len(aCalend) == 4 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica o Horario de Trabalho do Tecnico extraido do Ponto Eletronico  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cHoraIni1   := StrZero(Int(aCalend[1][3]),2)+":"+StrZero(((aCalend[1][3]-Int(aCalend[1][3]))*100),2)
			cHoraFim1   := StrZero(Int(aCalend[2][3]),2)+":"+StrZero(((aCalend[2][3]-Int(aCalend[2][3]))*100),2)
			cHoraIni2   := StrZero(Int(aCalend[3][3]),2)+":"+StrZero(((aCalend[3][3]-Int(aCalend[3][3]))*100),2)
			cHoraFim2   := StrZero(Int(aCalend[4][3]),2)+":"+StrZero(((aCalend[4][3]-Int(aCalend[4][3]))*100),2)
			dDataIni1   := dDataIni
			dDataFim1   := dDataIni
			dDataIni2   := dDataIni
			dDataFim2   := dDataIni
			Do Case
				Case ( cHoraFim1 < cHoraIni1 )
					dDataFim1 := dDataIni+1
					dDataIni2 := dDataFim1
					dDataFim2 := dDAtaIni2
				Case ( cHoraIni2 < cHoraFim1 )
					dDataIni2 := dDataIni+1
					dDataFim2 := dDataIni2
				Case ( cHoraFim2 < cHoraIni1 )
					dDataFim2 := dDataIni+1
			EndCase
			nIntervalo     := SubtHoras(dDataFim1,cHoraFim1,dDataIni2,cHoraIni2)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Calcula o numero de horas no intervalo                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nHoras := SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se o intervalo esta somado                                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( SubtHoras(dDataFim1,cHoraFim1,dDataIni,cHoraIni) <= 0 .AND.;
					SubtHoras(dDataIni2,cHoraIni2,dDataFim,cHoraFim) >= 0 )
					nHoras -= SubtHoras(dDataFim1,cHoraFim1,dDataIni2,cHoraIni2)
			EndIf
		Else
			nHoras := SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim)
		EndIf
	Else
		nHoras := SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim)
	EndIf
Else
	nHoras := SubtHoras(dDataIni,cHoraIni,dDataFim,cHoraFim)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna ao Estado de Entrada                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aAreaAA1)
RestArea(aArea)

Return(nHoras)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtCalcMTBF³ Autor ³Marco Aurelio          ³ Data ³14.09.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³Calcula o Tempo Medio Entre Falhas de um determinado        ³±±
±±³          ³equipamento ( Mean Time Between Failure )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Resultado do calculo efetuado                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtCalcMTBF()

LOCAL nValRet   := 0    // Valor calculado a ser retornado
LOCAL nDiasOper := 0    // Dias de operacao do equipamento

If !( Empty( AA3->AA3_DTREFH ) .AND. Empty( AA3->AA3_DTINST ) )

	nDiasOper := dDataBase - If( !Empty(AA3->AA3_DTREFH),;
													AA3->AA3_DTREFH ,; // Data Ref. Hist.
													AA3->AA3_DTINST )  // Data Instalacao do equipamento

	nValRet := ( AA3->AA3_HOROPH + ( ( nDiasOper * (AA3->AA3_DIAOPE/7) ) * AA3->AA3_HORDIA )) /;
											 ( AA3->AA3_QTFALH + AA3->AA3_QTFAL )
EndIf

Return( nValRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtMTBFGer ³ Autor ³Marco Aurelio          ³ Data ³15.09.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³Filtra produtos e equipamentos conforme parametros passados ³±±
±±³          ³e Calcula o Tempo Medio Entre Falhas dos mesmos             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nil                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³aParam - Array com dados dos parametros definidos no SX1    ³±±
±±³          ³                                                            ³±±
±±³          ³Elementos: 01 - Produto DE                                  ³±±
±±³          ³           02 - Produto ATE                                 ³±±
±±³          ³           03 - Tipo de Produto DE                          ³±±
±±³          ³           04 - Tipo de Produto ATE                         ³±±
±±³          ³           05 - Grupo de Produto DE                         ³±±
±±³          ³           06 - Grupo de Produto ATE                        ³±±
±±³          ³           07 - Fabricante do Produto DE                    ³±±
±±³          ³           08 - Loja do Fabricante do Produto DE            ³±±
±±³          ³           09 - Fabricante do Produto ATE                   ³±±
±±³          ³           10 - Loja do Fabricante do Produto ATE           ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtMTBFGer(aParam)

LOCAL cIndSB1    ,; // Nome do arquivo indice temporario - SB1
		cIndAA3    ,; // Nome do arquivo indice temporario - AA3
		cFiltro    ,; // Condicao para filtro de registros
		cKeyAB7    ,; // Chave para Loop no AB7 - Itens OS
		nOrdSB1    ,; // Numero de ordem do indice temporario - SB1
		nOrdAA3    ,; // Numero de ordem do indice temporario - AA3
		nCntFal    ,; // Contador de falhas
		nRegSB1    ,; // Numero de registro filtrados em SB1
		nHrOpGer   ,; // Total geral de horas operacionais
		nDiasOper  ,; // Dias de operacao do equipamento
		nFalGer       // Total geral de falhas

LOCAL aMTBF := {}

DbSelectArea("SB1")
DbSetOrder(1)

cFiltro := 'B1_FILIAL == "'+ XFILIAL("SB1") + '" .AND. '+;
			  'B1_COD    >= "'+ aParam[1]   + '" .AND. '+;
			  'B1_COD    <= "'+ aParam[2]   + '" .AND. '+;
			  'B1_TIPO   >= "'+ aParam[3]   + '" .AND. '+;
			  'B1_TIPO   <= "'+ aParam[4]   + '" .AND. '+;
			  'B1_GRUPO  >= "'+ aParam[5]   + '" .AND. '+;
			  'B1_GRUPO  <= "'+ aParam[6]   + '"'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Geracao de indice temporario de produtos - SB1                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cIndSB1 := CriaTrab(,.F.)

IndRegua("SB1",cIndSB1,IndexKey(),,cFiltro) // "Selecionando Registros..."

nOrdSB1 := RetIndex("SB1") + 1

#IFNDEF TOP
	DbSetIndex( cIndSB1 + OrdBagExt() )
#ENDIF

cFiltro := 'AA3_FILIAL == "'+ XFILIAL("AA3") + '" .AND. '+;
		  'AA3_CODFAB+AA3_LOJAFA >= "'+ aParam[7]+aParam[09] + '" .AND. '+;
		  'AA3_CODFAB+AA3_LOJAFA <= "'+ aParam[8]+aParam[10] + '"'


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Geracao de indice temporario de equipamentos(AA3)                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cIndAA3 := CriaTrab(,.F.)

IndRegua("AA3",cIndAA3,"AA3_CODPRO + AA3_NUMSER + AA3_CODFAB + AA3_LOJAFA",,cFiltro) // "Selecionando Registros..."

nOrdAA3 := RetIndex("AA3") + 1

#IFNDEF TOP
	DbSetIndex( cIndAA3 + OrdBagExt() )
#ENDIF

DbSetOrder(nOrdAA3)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Produto(SB1)                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SB1")
DbSetOrder(nOrdSB1)

nRegSB1 := LastRec()

ProcRegua(nRegSB1)

DbGoTop()

While !SB1->( EOF() )

	IncProc()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Posiciona Equipamento(AA3)                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	DbSelectArea("AA3")

	nHrOpGer := 0
	nFalGer  := 0

	DbSeek( SB1->B1_COD )

	While ( AA3->AA3_CODPRO == SB1->B1_COD )  .AND. !AA3->(EOF())

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Itens da OS(AB7) para apuracao de falhas ocorridas no equip. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		DbSelectArea("AB7")
		DbSetOrder(5)

		cKeyAB7 := xFilial( "AB7" ) + AA3->AA3_CODFAB + AA3->AA3_LOJAFA + ;
			AA3->AA3_CODPRO + AA3->AA3_NUMSER

		nCntFal := 0

		DbSeek( cKeyAB7 )

		While !AB7->( Eof() ) .AND. (AB7->AB7_FILIAL+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+AB7->AB7_NUMSER) == (cKeyAB7)

			aMTBF := AtFalItOS()
			nCntFal += aMTBF[ 1 ]

			DbSelectArea("AB7")
			DbSkip()

		EndDo

		DbSelectArea("AA3")

		RecLock("AA3",.f.)

		AA3->AA3_QTFAL  := nCntFal
		AA3->AA3_MTBF   := ATCalcMTBF()
		AA3->AA3_DTMTBF := dDataBase

		MsUnLock()

		If !( Empty( AA3->AA3_DTREFH ) .AND. Empty( AA3->AA3_DTINST ) )
			nDiasOper := dDataBase - If( !Empty(AA3->AA3_DTREFH),;
															AA3->AA3_DTREFH ,; // Data Ref. Hist.
															AA3->AA3_DTINST )  // Data Instalacao do equipamento
			nHrOpGer += ( ( AA3->AA3_HOROPH + ( ( nDiasOper * (AA3->AA3_DIAOPE/7) ) * AA3->AA3_HORDIA )) )
			nFalGer  += ( AA3->AA3_QTFALH + AA3->AA3_QTFAL )
		EndIf

		DbSkip()

	EndDo

	If aParam[11] == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava o MTBF geral para o produto                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		RecLock("SB1",.F.)
		SB1->B1_MTBF := ( nHrOpGer / nFalGer )
		SB1->( MsUnLock() )
	EndIf

	SB1->( DbSkip() )

EndDo

RetIndex("SB1")

Ferase( cIndSB1 + OrdBagExt() )

RetIndex("AA3")

Ferase( cIndAA3 + OrdBagExt() )

Return( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtCalcMTTR³ Autor ³Marco Aurelio          ³ Data ³14.09.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³Calcula o Tempo Medio para Reparo de um determinado         ³±±
±±³          ³equipamento ( Mean Time To Repair )                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Resultado do calculo efetuado                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtCalcMTTR()

LOCAL nValRet  // Valor calculado a ser retornado

nValRet := ( AA3->AA3_HORRPH + AA3->AA3_HORRP ) /;
			  ( AA3->AA3_QTREPH + AA3->AA3_QTREP )

Return( nValRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtMTTRGer ³ Autor ³Marco Aurelio          ³ Data ³26.09.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³Filtra produtos e equipamentos conforme parametros passados ³±±
±±³          ³e Calcula o Tempo Medio Para Reparo                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nil                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³aParam - Array com dados dos parametros definidos no SX1    ³±±
±±³          ³                                                            ³±±
±±³          ³Elementos: 01 - Produto DE                                  ³±±
±±³          ³           02 - Produto ATE                                 ³±±
±±³          ³           03 - Tipo de Produto DE                          ³±±
±±³          ³           04 - Tipo de Produto ATE                         ³±±
±±³          ³           05 - Grupo de Produto DE                         ³±±
±±³          ³           06 - Grupo de Produto ATE                        ³±±
±±³          ³           07 - Fabricante do Produto DE                    ³±±
±±³          ³           08 - Loja do Fabricante do Produto DE            ³±±
±±³          ³           09 - Fabricante do Produto ATE                   ³±±
±±³          ³           10 - Loja do Fabricante do Produto ATE           ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtMTTRGer(aParam)

LOCAL cIndSB1    ,; // Nome do arquivo indice temporario - SB1
		cIndAA3    ,; // Nome do arquivo indice temporario - AA3
		cFiltro    ,; // Condicao para filtro de registros
		cKeyAB7    ,; // Chave para Loop no AB7 - Itens OS
		nOrdSB1    ,; // Numero de ordem do indice temporario - SB1
		nOrdAA3    ,; // Numero de ordem do indice temporario - AA3
		nHorRp     ,; // Quantidade total de horas de reparo
		nRegSB1    ,; // Numero de registro filtrados em SB1
		nHRepGer   ,; // Total geral de horas de reparos
		nRepGer       // Total geral de Reparos

LOCAL aMTTR := {}

DbSelectArea("SB1")
DbSetOrder(1)

cFiltro := 'B1_FILIAL == "'+ XFILIAL("SB1") + '" .AND. '+;
			  'B1_COD    >= "'+ aParam[1]   + '" .AND. '+;
			  'B1_COD    <= "'+ aParam[2]   + '" .AND. '+;
			  'B1_TIPO   >= "'+ aParam[3]   + '" .AND. '+;
			  'B1_TIPO   <= "'+ aParam[4]   + '" .AND. '+;
			  'B1_GRUPO  >= "'+ aParam[5]   + '" .AND. '+;
			  'B1_GRUPO  <= "'+ aParam[6]   + '"'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Geracao de indice temporario de produtos - SB1                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cIndSB1 := CriaTrab(,.F.)

IndRegua("SB1",cIndSB1,IndexKey(),,cFiltro) // "Selecionando Registros..."

nOrdSB1 := RetIndex("SB1") + 1

#IFNDEF TOP
	DbSetIndex( cIndSB1 + OrdBagExt() )
#ENDIF

cFiltro := 'AA3_FILIAL == "'+ XFILIAL("AA3") + '" .AND. '+;
			'AA3_CODFAB+AA3_LOJAFA >= "'+ aParam[7]+aParam[09] + '" .AND. '+;
			'AA3_CODFAB+AA3_LOJAFA <= "'+ aParam[8]+aParam[10] + '"'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Geracao de indice temporario de equipamentos(AA3)                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cIndAA3 := CriaTrab(,.F.)

IndRegua("AA3",cIndAA3,"AA3_CODPRO + AA3_NUMSER + AA3_CODFAB + AA3_LOJAFA",,cFiltro) // "Selecionando Registros..."

nOrdAA3 := RetIndex("AA3") + 1

#IFNDEF TOP
	DbSetIndex( cIndAA3 + OrdBagExt() )
#ENDIF

DbSetOrder(nOrdAA3)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Produto(SB1)                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbSelectArea("SB1")
DbSetOrder(nOrdSB1)

nRegSB1 := LastRec()

ProcRegua(nRegSB1)

DbGoTop()

While !SB1->( Eof() )

	IncProc()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Equipamento(AA3)                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	DbSelectArea("AA3")

	nHRepGer := 0
	nRepGer  := 0

	DbSeek( SB1->B1_COD )

	While ( AA3->AA3_CODPRO == SB1->B1_COD )  .AND. !AA3->(EOF())

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Itens da OS(AB7) para apuracao de falhas ocorridas no equip. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		DbSelectArea("AB7")
		DbSetOrder(5)

		cKeyAB7 := xFilial( "AB7" ) + AA3->AA3_CODFAB + AA3->AA3_LOJAFA + ;
			AA3->AA3_CODPRO + AA3->AA3_NUMSER

		nHorRp := 0
		nQtRep := 0

		DbSeek( cKeyAB7 )

		While !AB7->( Eof() ) .AND. (AB7->AB7_FILIAL+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+AB7->AB7_NUMSER) == (cKeyAB7)

			aMTTR := AtStaItOs()

			nQtRep += aMTTR[ 1 ]
			nHorRp += aMTTR[ 2 ]

			DbSelectArea("AB7")

			AB7->( DbSkip() )

		EndDo

		DbSelectArea("AA3")

		RecLock("AA3",.f.)

		AA3->AA3_QTREP  := nQtRep
		AA3->AA3_HORRP  := nHorRp
		AA3->AA3_MTTR   := ATCalcMTTR()
		AA3->AA3_DTMTTR := dDataBase

		MsUnLock()

		nHRepGer += ( AA3->AA3_HORRPH + AA3->AA3_HORRP )
		nRepGer  += ( AA3->AA3_QTREPH + AA3->AA3_QTREP )

		AA3->( DbSkip() )

	EndDo

	If aParam[11] == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava o MTTR geral para o produto                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea("SB1")
		RecLock("SB1",.f.)
		SB1->B1_MTTR :=  ( nHRepGer / nRepGer )
		MsUnLock()
	EndIf

	SB1->( DbSkip() )

EndDo

RetIndex("SB1")

#IFNDEF TOP
	Ferase( cIndSB1 + OrdBagExt() )
#ENDIF

RetIndex("AA3")

#IFNDEF TOP
	Ferase( cIndAA3 + OrdBagExt() )
#ENDIF

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtStaItOs ³ Autor ³ Sergio Silveira       ³ Data ³14/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Retorna a quantidade, tempo de reparo e tempo de falhas    ³±±
±±³          ³ para uma OS + Item                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := AtStatItOs()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aStat -> 1 - Qtde reparos / 2 - Horas reparos / 3-Qt.Falhas³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtStaItOs()

Local aArea    := GetArea()            //Variavel de restauracao do ambiente
Local aAreaAB9 := AB9->(GetArea())    //Variavel de restauracao do ambiente da tabela AB9

LOCAL lFalha   := .F.

LOCAL nQtRep   := 0
LOCAL nHorRp   := 0
LOCAL nCntFal  := 0

#IFDEF TOP
	Local cQuery  := ""
#ENDIF

#IFDEF TOP

	AB9->( dbCommit() )

	cQuery += "SELECT AB9_DTINI,AB9_HRINI,AB9_DTFIM,AB9_HRFIM,AAG_TIPPRB FROM "
	cQuery += RetSqlName( "AB9" ) + " AB9," + RetSqlName( "AAG" ) + " AAG WHERE "
	cQuery += "AB9_NUMOS='" + AB7->AB7_NUMOS + AB7->AB7_ITEM + "' AND "
	cQuery += "AB9_CODPRB=AAG_CODPRB AND "
	cQuery += "AB9_FILIAL='" + xFilial("AB9") + "' AND "
	cQuery += "AAG_FILIAL='" + xFilial("AAG") + "' AND "
	cQuery += "AB9.D_E_L_E_T_=' ' AND "
	cQuery += "AAG.D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), "TRAB", .F., .T. )

	TcSetField( "TRAB", "AB9_DTINI", "D", 8, 0 )
	TcSetField( "TRAB", "AB9_DTFIM", "D", 8, 0 )

	If Alias() == "TRAB"
		While !TRAB->( Eof() )
			If TRAB->AAG_TIPPRB == "1"
				If !Empty(TRAB->AB9_HRFIM)
					nQtRep++
	 				nHorRp += SubtHoras(TRAB->AB9_DTINI,TRAB->AB9_HRINI,TRAB->AB9_DTFIM,TRAB->AB9_HRFIM)
					If !lFalha
						nCntFal++
						lFalha := .T.
					EndIf
				EndIf
			EndIf
	 		TRAB->( DbSkip() )
		EndDo
		TRAB->( dbCloseArea() )
		DbSelectArea( "AB9" )
	EndIf

#ELSE
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisa AB9 para saber se houve apontamento tecnico                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	cKeyAB9 := xFilial( "AB9" ) + AB7->AB7_NUMOS + AB7->AB7_ITEM

	AB9->( DbSetOrder( 1 ) )
	If AB9->( DbSeek( cKeyAB9 ) )

		While !AB9->( Eof() ) .AND. AB9->AB9_FILIAL+AB9->AB9_NUMOS == cKeyAB9

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Pesquisa Tipo de Ocorrencia(AAG) para verificar se campo AAG_TIPPRB = 1³
			//³(Problema) - o que caracteriza ocorrencia como falha                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			AAG->( DbSetOrder( 1 ) )
			If AAG->( DbSeek( xFilial("AAG") + AB9->AB9_CODPRB ) )

				If AAG->AAG_TIPPRB == "1"
					If !Empty(AB9->AB9_HRFIM)
						nQtRep++
		 				nHorRp += SubtHoras(AB9->AB9_DTINI,AB9->AB9_HRINI,AB9->AB9_DTFIM,AB9->AB9_HRFIM)
						If !lFalha
							nCntFal++
							lFalha := .T.
						EndIf
					EndIf
				EndIf

			EndIf

			DbSelectArea("AB9")

			AB9->( DbSkip() )

		EndDo
	EndIf

#ENDIF

RestArea( aAreaAB9 )
RestArea( aArea )

Return( { nQtRep, nHorRp, nCntFal } )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtFalItOS ³ Autor ³ Sergio Silveira       ³ Data ³14/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Retorna a quantidade de falhas para a OS + Item            ³±±
±±³          ³ Atencao - o AB7 deve estar posicionado                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := AtFalItOS()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1 -> 1 - Quantidade de falhas                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtFalItOS()

LOCAL aArea   := GetArea()

#IFNDEF TOP
	LOCAL cKeyAB9 := ""
#ENDIF

LOCAL nCntFal := 0

#IFDEF TOP
	Local cQuery  := ""
#ENDIF

#IFDEF TOP

	AB9->( dbCommit() )

   cQuery := ""
	cQuery += "SELECT AAG_TIPPRB FROM "
	cQuery += RetSqlName( "AB9" ) + " AB9," + RetSqlName( "AAG" ) + " AAG WHERE "
	cQuery += "AB9_NUMOS='" + AB7->AB7_NUMOS + AB7->AB7_ITEM + "' AND "
	cQuery += "AB9_CODPRB=AAG_CODPRB AND "
	cQuery += "AB9_FILIAL='" + xFilial("AB9") + "' AND "
	cQuery += "AAG_FILIAL='" + xFilial("AAG") + "' AND "
	cQuery += "AB9.D_E_L_E_T_=' ' AND "
	cQuery += "AAG.D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), "TRAB", .F., .T. )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisa Tipo de Ocorrencia(AAG) para verificar se campo AAG_TIPPRB = 1³
	//³(Problema) - o que caracteriza ocorrencia como falha                   ³
	//³ Abandona o loop na primeira ocorrencia que caracterizar falha         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Alias() == "TRAB"
		While !TRAB->( Eof() )
			If TRAB->AAG_TIPPRB == "1"
				nCntFal++
				Exit
			EndIf
	 		TRAB->( DbSkip() )
		EndDo
		TRAB->( dbCloseArea() )
		DbSelectArea( "AB9" )
	EndIf

#ELSE
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pesquisa AB9 para saber se houve apontamento tecnico                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	cKeyAB9 :=  xFilial("AB9") + AB7->AB7_NUMOS + AB7->AB7_ITEM

	AB9->( DbSetOrder( 1 ) )
	If AB9->( DbSeek( cKeyAB9 ) )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Pesquisa Tipo de Ocorrencia(AAG) para verificar se campo AAG_TIPPRB = 1³
		//³(Problema) - o que caracteriza ocorrencia como falha                   ³
		//³ Abandona o loop na primeira ocorrencia que caracterizar falha         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		While !AB9->( Eof() ) .AND. cKeyAB9==AB9->AB9_FILIAL+AB9->AB9_NUMOS
			If AAG->( DbSeek( xFilial("AAG") + AB9->AB9_CODPRB ) )
	  			If AAG->AAG_TIPPRB == "1"
					nCntFal++
					Exit
				EndIf
			EndIf
   		AB9->( DbSkip() )
		EndDo

	EndIf

#ENDIF

RestArea( aArea )

Return( { nCntFal } )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ATCustoEqto ³ Autor ³Eduardo Riera        ³ Data ³ 03.12.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo do Custo de Manutencao por equipamento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpA1:= AtCustoEqto(ExpC1,ExpC2,ExpC3,ExpC4,ExpN5,ExpL1)    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1: Array com dois elementos:                           ³±±
±±³          ³        1 - Custo Atual ( Valor presente )                  ³±±
±±³          ³        2 - Custo Historico                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Codigo do Fabricante.                               ³±±
±±³          ³ ExpC2: Loja do Fabricante                                  ³±±
±±³          ³ ExpC3: Produto                                             ³±±
±±³          ³ ExpC4: Numero de Serie                                     ³±±
±±³          ³ ExpL5: Considera OS em Garantia.                           ³±±
±±³          ³ ExpD6: Data Limite para Apuracao do Custo                  ³±±
±±³          ³ ExpD7: Data Inicio para Apuracao do Custo                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECXFUN                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtCustoEqto(	cCodFab,	cLojaFa,	cCodPro,	cNumSer,;
						dData,		dDataIni	)

Local aArea     := GetArea()
Local aAreaSM0
Local aAreaAA1  := AA1->(GetArea())
Local aAreaSB1  := SB1->(GetArea())
Local aAreaAB7  := AB7->(GetArea())
Local aAreaAB9  := AB9->(GetArea())
Local aAreaABA  := ABA->(GetArea())
Local aAreaABC  := ABC->(GetArea())
Local aCusto    := {}
Local aCustUser := {}
Local aFiliais  := {}

Local bFilial   := { |x| If( FWModeAccess( x, 3 ) == "C", xFilial( x ), aFiliais[ nLoop ] ) }

Local cQuery    := ""
Local cAlias    := "AB7"
Local cNumOS    := ""

Local lGarantia := SuperGetMV("MV_ATGARCS")

Local nCustAtu  := 0
Local nCustHis  := 0
Local nMoeda    := 0
Local nLoop     := 0
Local lGarAcess := !Empty( ABA->( FieldPos( "ABA_GARANT" ) ) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa o Array de filiais                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If SuperGetMV( "MV_ATCUSGE" )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array para todas as filiais desta empresa                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	aAreaSM0 := SM0->( GetArea() )
	cCodigo  := SM0->M0_CODIGO

	SM0->( DbSetOrder( 1 ) )
	If SM0->( DbSeek( cCodigo ) )

		While !SM0->( Eof() ) .AND. SM0->M0_CODIGO == cCodigo
			AAdd( aFiliais, FWGETCODFILIAL )
			SM0->( DbSkip() )
		EndDo

	EndIf

	RestArea( aAreaSM0 )

Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o array apenas para a filial corrente                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aFiliais := { cFilAnt }
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa parametros da rotina                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dData       := If(dData==Nil,dDataBase,dData)
dDataIni    := If(ValType(dDataIni)=="D",dDataIni,CToD("") )

For nLoop := 1 to Len( aFiliais )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processamento da Rotina para TopConnect                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	#IFDEF TOP

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calculo do custo pelo metodo ON-LINE ( ATUAL )                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o custo hora homem.                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuery := "SELECT AB9.AB9_TOTFAT,AA1.AA1_CUSTO "
			cQuery += "FROM "+RetSqlName("AB7")+" AB7,"+;
								RetSqlName("AB9")+" AB9,"+;
								RetSqlName("AA1")+" AA1 "
			cQuery += "WHERE AB7.AB7_FILIAL='"+ Eval(bFilial,"AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "
			If ( !lGarantia )
				cQuery += "AB9.AB9_GARANT='N' AND "
			EndIf
			cQuery += "AB9.D_E_L_E_T_=' ' AND "
			cQuery += "AA1.AA1_FILIAL='"+Eval( bFilial, "AA1")+"' AND "
			cQuery += "AA1.AA1_CODTEC=AB9.AB9_CODTEC AND "
			cQuery += "AA1.D_E_L_E_T_=' ' "

			cAlias := CriaTrab(,.F.)
			cQuery := ChangeQuery(cQuery)
			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

			While ( !Eof() )
				nCustAtu += (HoraToInt(AB9_TOTFAT)*AA1_CUSTO)
				DbSkip()
			EndDo

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula o Custo com troca de pecas                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuery := "SELECT SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD,SUM(ABA.ABA_QUANT) ABA_QUANT "
			cQuery += "FROM "+RetSqlName("AB7")+" AB7,"+;
								RetSqlName("AB9")+" AB9,"+;
								RetSqlName("ABA")+" ABA, "+;
								RetSqlName("SB1")+" SB1 "
			cQuery += "WHERE AB7.AB7_FILIAL='"+Eval( bFilial, "AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "

			If ( !lGarantia )
				cQuery += "AB9.AB9_GARANT='N' AND "
			EndIf
			cQuery += "AB9.D_E_L_E_T_=' ' AND "
			cQuery += "ABA.ABA_FILIAL='"+Eval( bFilial, "ABA")+"' AND "
			cQuery += "ABA.ABA_NUMOS=AB9.AB9_NUMOS AND "
			cQuery += "ABA.ABA_CODTEC=AB9.AB9_CODTEC AND "
			cQuery += "ABA.ABA_SEQ=AB9.AB9_SEQ AND "
			cQuery += "ABA.D_E_L_E_T_=' ' AND "
			cQuery += "SB1.B1_FILIAL='"+Eval( bFilial, "SB1")+"' AND "
			cQuery += "SB1.B1_COD=ABA.ABA_CODPRO AND "
			cQuery += "SB1.D_E_L_E_T_=' ' "
			cQuery += "GROUP BY SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD "
			cQuery += "ORDER BY SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD "

			cAlias := CriaTrab( ,.F. )
			cQuery := ChangeQuery(cQuery)
			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

			While ( !Eof() )

				nMoeda := If(Val(RetFldProd((cAlias)->B1_COD,"B1_MCUSTD",cAlias))==0,1,Val(RetFldProd((cAlias)->B1_COD,"B1_MCUSTD",cAlias)))
				nCustAtu += (ABA_QUANT*xMoeda(RetFldProd((cAlias)->B1_COD,"B1_CUSTD",cAlias),nMoeda,1))

				DbSelectArea(cAlias)
				DbSkip()
			EndDo

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula o custo das despesas financeiras                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			cQuery := "SELECT SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD,AB6.AB6_MOEDA,SUM(ABC.ABC_QUANT) ABC_QUANT,"
			cQuery += "SUM(ABC.ABC_VALOR) ABC_VALOR "
			cQuery += "FROM "+RetSqlName("AB6")+" AB6,"+;
								RetSqlName("AB7")+" AB7,"+;
								RetSqlName("AB9")+" AB9,"+;
								RetSqlName("ABC")+" ABC,"+;
								RetSqlName("SB1")+" SB1 "
			cQuery += "WHERE AB7.AB7_FILIAL='"+Eval( bFilial, "AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB6.AB6_FILIAL='"+Eval( bFilial, "AB6")+"' AND "
			cQuery += "AB6.AB6_NUMOS=AB7.AB7_NUMOS AND "
			cQuery += "AB6.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "
			If ( !lGarantia )
				cQuery += "AB9.AB9_GARANT='N' AND "
			EndIf
			cQuery += "AB9.D_E_L_E_T_=' ' AND "
			cQuery += "ABC.ABC_FILIAL='"+Eval( bFilial, "ABC")+"' AND "
			cQuery += "ABC.ABC_NUMOS=AB9.AB9_NUMOS AND "
			cQuery += "ABC.ABC_CODTEC=AB9.AB9_CODTEC AND "
			cQuery += "ABC.ABC_SEQ=AB9.AB9_SEQ AND "
			cQuery += "ABC.D_E_L_E_T_=' ' AND "
			cQuery += "SB1.B1_FILIAL='"+Eval( bFilial, "SB1")+"' AND "
			cQuery += "SB1.B1_COD=ABC.ABC_CODPRO AND "
			cQuery += "SB1.D_E_L_E_T_=' ' "
			cQuery += "GROUP BY SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD,AB6.AB6_MOEDA "
			cQuery += "ORDER BY SB1.B1_COD,SB1.B1_CUSTD,SB1.B1_MCUSTD,AB6.AB6_MOEDA "

			cAlias := CriaTrab( , .F. )
			cQuery := ChangeQuery(cQuery)

			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

			While ( !Eof() )

				nMoeda := If(Val(RetFldProd((cAlias)->B1_COD,"B1_MCUSTD",cAlias))==0,1,Val(RetFldProd((cAlias)->B1_COD,"B1_MCUSTD",cAlias)))

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se o custo Standard esta vazio, calcula pelo valor da despesa         ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Empty( RetFldProd(SB1->B1_COD,"B1_CUSTD") )
					nCustAtu += xMoeda(ABC_VALOR,AB6_MOEDA,1)
				Else
					nCustAtu += (ABC_QUANT*xMoeda(RetFldProd((cAlias)->B1_COD,"B1_CUSTD",cAlias),nMoeda,1))
				EndIf

				DbSelectArea(cAlias)
				DbSkip()
			EndDo

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calculo do custo pelo metodo HISTORICO                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o custo hora homem.                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			cQuery := "SELECT SUM(AB9.AB9_CUSTO) AB9_CUSTO "
			cQuery += "FROM "+RetSqlName("AB7")+" AB7,"
			cQuery += RetSqlName("AB9")+" AB9 "
			cQuery += "WHERE AB7.AB7_FILIAL='"+Eval( bFilial, "AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "
			If ( !lGarantia )
				cQuery += "AB9.AB9_GARANT='N' AND "
			EndIf
			cQuery += "AB9.D_E_L_E_T_=' '"

			cAlias := CriaTrab(,.F.)
			cQuery := ChangeQuery(cQuery)

			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona ao custo                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			nCustHis += If( Valtype( AB9_CUSTO ) == "N", AB9_CUSTO, 0 )

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o Custo com troca de pecas                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			cQuery := "SELECT SUM(ABA.ABA_CUSTO) ABA_CUSTO "
			cQuery += "FROM "+RetSqlName("AB7")+" AB7,"+;
								RetSqlName("AB9")+" AB9,"+;
								RetSqlName("ABA")+" ABA "
			cQuery += "WHERE AB7.AB7_FILIAL='"+Eval( bFilial, "AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se considera garantia do equipamento e do acessorio          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( !lGarantia )
				If lGarAcess
					cQuery += "( ( AB9.AB9_GARANT='N' AND ABA.ABA_GARANT=' ' ) OR ABA.ABA_GARANT='N' ) AND "
				Else
					cQuery += "AB9.AB9_GARANT='N' AND "
				EndIf
			EndIf

			cQuery += "AB9.D_E_L_E_T_=' ' AND "
			cQuery += "ABA.ABA_FILIAL='"+Eval( bFilial, "ABA")+"' AND "
			cQuery += "ABA.ABA_NUMOS=AB9.AB9_NUMOS AND "
			cQuery += "ABA.ABA_CODTEC=AB9.AB9_CODTEC AND "
			cQuery += "ABA.ABA_SEQ=AB9.AB9_SEQ AND "
			cQuery += "ABA.D_E_L_E_T_=' '"

			cAlias := CriaTrab( , .F. )
			cQuery := ChangeQuery(cQuery)

			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona ao custo                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			nCustHis += If( Valtype( ABA_CUSTO ) == "N", ABA_CUSTO, 0 )

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o Custo das despesas financeiras                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuery := "SELECT SUM(ABC.ABC_CUSTO) ABC_CUSTO "
			cQuery += "FROM "+RetSqlName("AB7")+" AB7,"+;
								RetSqlName("AB9")+" AB9,"+;
								RetSqlName("ABC")+" ABC "
			cQuery += "WHERE AB7.AB7_FILIAL='"+Eval( bFilial, "AB7")+"' AND "
			cQuery += "AB7.AB7_CODFAB='"+cCodFab+"' AND "
			cQuery += "AB7.AB7_LOJAFA='"+cLojaFa+"' AND "
			cQuery += "AB7.AB7_CODPRO='"+cCodPro+"' AND "
			cQuery += "AB7.AB7_NUMSER='"+cNumSer+"' AND "
			cQuery += "AB7.AB7_EMISSA<='"+Dtos(dData)   +"' AND "
			cQuery += "AB7.AB7_EMISSA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "AB7.D_E_L_E_T_=' ' AND "
			cQuery += "AB9.AB9_FILIAL='"+Eval( bFilial, "AB9")+"' AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,1,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)))+")=AB7.AB7_NUMOS AND "
			cQuery += "SUBSTRING(AB9.AB9_NUMOS,"+ALLTRIM(STR(LEN(AB7->AB7_NUMOS)+1))+","+ALLTRIM(STR(LEN(AB7->AB7_ITEM)))+")=AB7.AB7_ITEM AND "
			If ( !lGarantia )
				cQuery += "AB9.AB9_GARANT='N' AND "
			EndIf
			cQuery += "AB9.D_E_L_E_T_=' ' AND "
			cQuery += "ABC.ABC_FILIAL='"+Eval( bFilial, "ABC")+"' AND "
			cQuery += "ABC.ABC_NUMOS=AB9.AB9_NUMOS AND "
			cQuery += "ABC.ABC_CODTEC=AB9.AB9_CODTEC AND "
			cQuery += "ABC.ABC_SEQ=AB9.AB9_SEQ AND "
			cQuery += "ABC.D_E_L_E_T_=' ' "

			cAlias := CriaTrab( , .F. )
			cQuery := ChangeQuery(cQuery)

			DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Adiciona ao custo                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nCustHis += If( Valtype( ABC_CUSTO ) == "N", ABC_CUSTO, 0 )

			DbSelectArea(cAlias)
			dbCloseArea()
			DbSelectArea("AB7")


	#ELSE
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Processamento para ISAM                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DbSelectArea(cAlias)
		DbSetOrder(5)
		DbSeek(Eval( bFilial, "AB7")+cCodFab+cLojaFa+cCodPro+cNumSer+DToS(dDataIni), .T. )

		DbSelectArea(cAlias)

		While ( !Eof() .AND.Eval( bFilial, "AB7") == AB7->AB7_FILIAL .AND.;
							cCodFab == AB7->AB7_CODFAB .AND.;
							cLojaFa == AB7->AB7_LOJAFA .AND.;
							cCodPro == AB7->AB7_CODPRO .AND.;
							cNumSer == AB7->AB7_NUMSER .AND.;
							dData   >= AB7->AB7_EMISSA .AND.;
							dDataIni<= AB7->AB7_EMISSA )


			DbSelectArea("AB6")
			DbSetOrder(1)
			DbSeek(Eval(bFilial,"AB6")+AB7->AB7_NUMOS)

			cNumOs := AB7->AB7_NUMOS+AB7->AB7_ITEM

			DbSelectArea("AB9")
			DbSetOrder(1)
			DbSeek(Eval( bFilial, "AB9")+cNumOs)

			While ( !Eof() .AND.Eval( bFilial, "AB9") == AB9->AB9_FILIAL .AND.;
								cNumOs == AB9->AB9_NUMOS )

				If ( (!lGarantia .AND. AB9->AB9_GARANT=="N") .OR. lGarantia )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o custo hora homem                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					DbSelectArea("AA1")
					DbSetOrder(1)
					DbSeek(Eval( bFilial, "AA1")+AB9->AB9_CODTEC)

					nCustAtu += (HoraToInt(AB9->AB9_TOTFAT)*AA1->AA1_CUSTO)
					nCustHis += AB9->AB9_CUSTO

				EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o custo das pecas trocadas                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea("ABA")
				DbSetOrder(1)
				DbSeek(Eval( bFilial, "ABA")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)

				While ( !Eof() .AND. Eval( bFilial, "ABA") == ABA->ABA_FILIAL .AND.;
									AB9->AB9_NUMOS  == ABA->ABA_NUMOS .AND.;
									AB9->AB9_CODTEC == ABA->ABA_CODTEC .AND.;
									AB9->AB9_SEQ    == ABA->ABA_SEQ )

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Verifica se considera garantia do equipamento e do acessorio          ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lGarAcess
						lCusto := (lGarantia.OR.(!lGarantia.AND.((AB9->AB9_GARANT=="N".AND.ABA->ABA_GARANT==" ").OR.ABA->ABA_GARANT=="N" ) ) )
					Else
						lCusto := ( lGarantia .OR. (!lGarantia .AND. AB9->AB9_GARANT=="N") )
					EndIf

					If lCusto

						DbSelectArea("SB1")
						DbSetOrder(1)
						DbSeek(Eval( bFilial, "SB1")+ABA->ABA_CODPRO)

						nMoeda := If(Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))==0,1,Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")))

						nCustAtu += (ABA->ABA_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),nMoeda,1))
						nCustHis += ABA->ABA_CUSTO

					EndIf

					DbSelectArea("ABA")
					DbSkip()
				EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula os custos financeiros.                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				DbSelectArea("ABC")
				DbSetOrder(1)
				DbSeek(Eval( bFilial, "ABC")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)

				While ( !ABC->( Eof() ) .AND. Eval( bFilial, "ABC") == ABC->ABC_FILIAL .AND.;
									AB9->AB9_NUMOS  == ABC->ABC_NUMOS .AND.;
									AB9->AB9_CODTEC == ABC->ABC_CODTEC .AND.;
									AB9->AB9_SEQ    == ABC->ABC_SEQ )

					DbSelectArea("SB1")
					DbSetOrder(1)
					DbSeek(Eval( bFilial, "SB1")+ABC->ABC_CODPRO)

					nMoeda := If(Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))==0,1,Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se o custo Standard esta vazio, calcula o custo atual pelo valor da despesa  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

					If Empty( RetFldProd(SB1->B1_COD,"B1_CUSTD") )
						nCustAtu += xMoeda(ABC->ABC_VALOR,AB6->AB6_MOEDA,1)
					Else
						nCustAtu += (ABC->ABC_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),nMoeda,1))
					EndIf

					nCustHis += ABC->ABC_CUSTO

					DbSelectArea("ABC")
					DbSkip()

				EndDo

				DbSelectArea("AB9")
				DbSkip()
				EndDo
			DbSelectArea(cAlias)
			DbSkip()
		EndDo

	#ENDIF

Next nLoop

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para a atribuicao de custo do usuario ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "ATCUSTUS" )
	aCustUser := ExecBlock( "ATCUSTUS",.F.,.F. )
	nCustAtu += aCustUser[ 1 ]
	nCustHis += aCustUser[ 2 ]
EndIf

RestArea(aAreaAA1)
RestArea(aAreaSB1)
RestArea(aAreaAB7)
RestArea(aAreaAB9)
RestArea(aAreaABA)
RestArea(aAreaABC)
RestArea(aArea)

aCusto := { nCustAtu, nCustHis }

Return( aCusto )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ATEqStatus  ³ Autor ³Eduardo Riera        ³ Data ³ 17.12.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Manutencao do Controle de Status do Equipamento             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³AtEqStatus (ExpC1,ExpL2,ExpC3)                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Rotina Chamadora.                                   ³±±
±±³          ³ ExpL2: Estorno                                             ³±±
±±³          ³ ExpC3: Forca um Status de Equipamento ( RdMake )           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O Equipamento deve estar posicionado                       ³±±
±±³          ³ Se houver OS o AB7 deve estar posicionado                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECXFUN                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtEqStatus(cRotina,lEstorno,cStatus)

Local aArea    := GetArea()
Local aAreaSB5  := SB5->(GetArea())
Local cCliente := SuperGetMV("MV_ATCLPAD")
Local lAt800Grv := IsInCallStack("At820Grv")
Local lCancLib  := IsInCallStack("At800CancLib")
Local lSepCan  	:= IsInCallStack("At800SepCan")

lEstorno := If( ValType( lEstorno ) <> "L", .F., lEstorno )

If ( lEstorno )
	If ( !Empty(AA3->AA3_STAANT) ) .And. Empty(AB6->AB6_ORCAME)
		RecLock("AA3")
		AA3->AA3_STATUS := AA3->AA3_STAANT
		AA3->AA3_STAANT := ""
		AA3->(MsUnLock())
	ElseIf !Empty(AB6->AB6_ORCAME)
		RecLock("AA3")
		AA3->AA3_STATUS := "01" //Em Cliente
		AA3->AA3_STAANT := ""
		AA3->(MsUnLock())
	Else
		RecLock("AA3")
		AA3->AA3_STATUS := ""
		AA3->(MsUnLock())
	EndIf
Else
	Do Case
		Case !Empty(cStatus) .AND. AA3->AA3_STATUS != cStatus
			RecLock("AA3")
			AA3->AA3_STAANT := AA3->AA3_STATUS
			AA3->AA3_STATUS := cStatus
			AA3->(MsUnLock())
		Case cRotina $ "MATA100|MATA103" .AND. IIF(AA3->AA3_CODCLI == cCliente .AND. AA3->AA3_LOJA == "00", AA3->AA3_STATUS != "02", AA3->AA3_STATUS != "04")
			If ( AA3->AA3_CODCLI == cCliente .AND. AA3->AA3_LOJA=="00" )
				RecLock("AA3")
				AA3->AA3_STAANT := AA3->AA3_STATUS
				AA3->AA3_STATUS := "02" //Em Estoque
				AA3->(MsUnLock())
			Else
				RecLock("AA3")
				AA3->AA3_STAANT := AA3->AA3_STATUS
				AA3->AA3_STATUS := "04" //Em Nosso Poder
				AA3->(MsUnLock())
			EndIf
		Case cRotina == "MATA460" .AND. AA3->AA3_STATUS != "01"
			RecLock("AA3")
			AA3->AA3_STAANT := AA3->AA3_STATUS
			AA3->AA3_STATUS := "01" //Em Cliente
			AA3->(MsUnLock())
		Case cRotina == "TECA450"
			If !lAt800Grv .And. !lCancLib .And. !lSepCan
				If ( AB7->AB7_TIPO $ "245" )
					RecLock("AA3")
					AA3->AA3_STATUS := AA3->AA3_STAANT
					AA3->AA3_STAANT := ""
					AA3->(MsUnLock())
				Else
					If AA3->AA3_STATUS <> "03"
						RecLock("AA3")
						AA3->AA3_STAANT := AA3->AA3_STATUS
						AA3->AA3_STATUS := "03" //Em Manutencao
						AA3->(MsUnLock())
					EndIf
				EndIf
			Endif
		Case cRotina == "TECA040"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamento do Status de transferencia              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock( "ATTRSTAT" )
				RecLock("AA3")
				AA3->AA3_STAANT := AA3->AA3_STATUS
				AA3->AA3_STATUS := ExecBlock( "ATTRSTAT", .f., .f. )
				AA3->(MsUnLock())
			EndIf
	EndCase
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tratamento de Excecoes                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Empty(AA3->AA3_STATUS) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Quando o Cliente for igual ao parametro MV_ATCLPAD, ³
		//³assumir que o equipamento esta em estoque.          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		 If ( AA3->AA3_CODCLI == cCliente .AND. AA3->AA3_LOJACLI=="00" )
			RecLock("AA3") 
			AA3->AA3_STATUS := "02" //Em Estoque
			AA3->(MsUnLock())
		Else
			RecLock("AA3")
			AA3->AA3_STATUS := "01" //Em Cliente
			AA3->(MsUnLock())
		EndIf
	EndIf
EndIf
//Tratamento para quando for Granel
DbSelectArea("SB5")
SB5->(DbSetOrder(1))
If SB5->(DbSeek(xFilial("SB5")+AA3->AA3_CODPRO))
	If SB5->B5_ISIDUNI == "2"
		If RecLock("AA3",.F.)
			AA3->AA3_STATUS := "08" //Granel
			AA3->AA3_STAANT := "02" //Em Estoque
			AA3->(MsUnLock())
		Endif
	Endif
Endif

RestArea(aAreaSB5)
RestArea(aArea)
Return(.T.)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtServRet ³ Autor ³ Sergio Silveira       ³ Data ³ 30/11/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve o codigo de servico do produto conforme o grupo      ³±±
±±³          ³ de cobertura.                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtServRet( ExpC2, ExpC3 )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC2 -> Codigo do produto                                   ³±±
±±³          ³ ExpC3 -> Grupo de cobertura                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Codigo de Servico                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtServRet( cProduto, cCodGrp )

LOCAL aArea     := GetArea()
LOCAL aAreaSB1  := SB1->( GetArea() )
LOCAL aAreaAAB  := AAB->( GetArea() )
LOCAL aAreaAAA  := AAA->( GetArea() )

LOCAL cCodServ  := CriaVar( "AA5_CODSER", .F. )
LOCAL cTipo
LOCAL cGrupo
LOCAL cProdFill := Space( Len( CriaVar( "B1_COD"     ) ) )
LOCAL cGrupFill := Space( Len( CriaVar( "B1_GRUPO"   ) ) )
Local lContinua := .T.
Local dDataAAR	:= If(ReadVar() <> "M->AB9_CODPRO",dDataBase,M->AB9_DTFIM)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa o produto  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

SB1->( DbSetOrder( 1 ) )

If SB1->( DbSeek( xFilial( "SB1" ) + cProduto ) )

	cTipo   := SB1->B1_TIPO
	cGrupo  := SB1->B1_GRUPO
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Retorna o codigo do servico do cabecalho do grupo de cobertura caso o saldo esteja zero  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If AAQ->(DbSeek(xFilial("AAQ")+AAH->AAH_CONTRT+cProduto))
		lContinua := .F.
		AAR->(DbSetOrder(1))
		If AAQ->AAQ_MODLIB == "1"
			If AAR->(dbSeek(xFilial("AAR")+AAH->AAH_CONTRT+cProduto))
				If AAR->AAR_SALDO == 0
					AAA->( DbSetOrder( 1 ) )
					If AAA->( DbSeek( xFilial( "AAA" ) + cCodGrp ) )
						cCodServ := AAA->AAA_CODSER
					EndIf
				ElseIf AAR->AAR_SALDO > 0
					If AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProduto ) )
						cCodServ := AAB->AAB_CODSER
					EndIf
				EndIf
			ElseIf AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProduto ) )
				cCodServ := AAB->AAB_CODSER
			EndIf
		ElseIf AAQ->AAQ_MODLIB == "2"
			If AAR->(dbSeek(xFilial("AAR")+AAH->AAH_CONTRT+cProduto+LEFT(DTOS(dDataAAR),6)))
				If AAR->AAR_SALDO == 0
					AAA->( DbSetOrder( 1 ) )
					If AAA->( DbSeek( xFilial( "AAA" ) + cCodGrp ) )
						cCodServ := AAA->AAA_CODSER
					EndIf
				ElseIf AAR->AAR_SALDO > 0
					If AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProduto ) )
						cCodServ := AAB->AAB_CODSER
					EndIf
				EndIf
			ElseIf AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProduto ) )
				cCodServ := AAB->AAB_CODSER
			EndIf
		EndIf
	EndIf


	If lContinua
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pesquisa pelo tipo / grupo / produto   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProduto ) )
			cCodServ := AAB->AAB_CODSER
		Else

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Pesquisa pelo tipo / grupo ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			If AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupo + cProdFill ) )
				cCodServ := AAB->AAB_CODSER
			Else

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Pesquisa pelo tipo ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If AAB->( DbSeek( xFilial( "AAB" ) + cCodGrp + cTipo + cGrupFill + cProdFill ) )
					cCodServ := AAB->AAB_CODSER
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Se nao encontrar utiliza o codigo de servico padrao ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					AAA->( DbSetOrder( 1 ) )
					If AAA->( DbSeek( xFilial( "AAA" ) + cCodGrp ) )
						cCodServ := AAA->AAA_CODSER
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna as areas anteriores ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RestArea( aAreaSB1 )
RestArea( aAreaAAB )
RestArea( aAreaAAA )

RestArea( aArea )

Return( cCodServ )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtBaseGrup³ Autor ³ Sergio Silveira       ³ Data ³ 09/10/2001 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve o codigo do grupo de cobertura da base instalada     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtBaseServ( ExpC2, ExpC3, ExpC4, ExpC5, ExpC6 )     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC2 -> Codigo do Fabricante                                ³±±
±±³          ³ ExpC3 -> Loja do Fabricante                                  ³±±
±±³          ³ ExpC4 -> Codigo do produto                                   ³±±
±±³          ³ ExpC5 -> Numero de serie                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Codigo do grupo de cobertura                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtBaseGrup( cCodFab, cLojaFa, cCodPro, cNumSer )

LOCAL cCodGrp     := CriaVar( "AAA_CODGRP", .F. )

LOCAL dInicioAA3  := CTOD( "" )

LOCAL lCodServAA3 := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pesquisa pelo equipamento na base instalada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AA3->( DbSetOrder( 4 ) )
If AA3->( DbSeek( xFilial( "AA3" ) + cCodFab + cLojaFa + cCodPro + cNumSer ) )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Obtem o codigo de servico pelo AA3 ( Caso exista o campo ) ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	lCodServAA3 := .F.

	If !Empty( AA3->( FieldPos( "AA3_CODGRP" ) ) )
		dInicioAA3 := If( Empty( AA3->AA3_DTINST ), AA3->AA3_DTVEND, AA3->AA3_DTINST )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se esta no periodo de garantia ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty( AA3->AA3_CODGRP ) .AND. 	dDataBase >= dInicioAA3 .AND. dDataBase <= AA3->AA3_DTGAR
			lCodServAA3 := .T.
			cCodGrp := AA3->AA3_CODGRP
		EndIf

	EndIf

	If !lCodServAA3
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pesquisa pelo contrato ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AAH->( DbSetOrder( 1 ) )
		If AAH->( DbSeek( xFilial( "AAH" ) + AA3->AA3_CONTRT ) )
			If ( dDataBase >= AAH->AAH_INIVLD .AND.;
				 (dDataBase <= AAH->AAH_FIMVLD .OR. AAH->AAH_TPCONT=="1" ))
				cCodGrp := AAH->AAH_CODGRP
			EndIf
		EndIf

	EndIf

EndIf

Return( cCodGrp )



/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtBaseServ³ Autor ³ Sergio Silveira       ³ Data ³ 13/01/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Devolve o codigo de servico do item da base instalada de     ³±±
±±³          ³ acordo com o contrato / grupo de contratos                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtBaseServ( ExpC2, ExpC3, ExpC4, ExpC5, ExpC6 )     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC2 -> Codigo do Fabricante                                ³±±
±±³          ³ ExpC3 -> Loja do Fabricante                                  ³±±
±±³          ³ ExpC4 -> Codigo do produto                                   ³±±
±±³          ³ ExpC5 -> Numero de serie                                     ³±±
±±³          ³ ExpC6 -> Codigo de produto do apontamento                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Codigo de Servico                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtBaseServ(cCodFab,	cLojaFa,	cCodPro,	cNumSer,;
					cProduto, cCodGrp )
LOCAL cCodServ := CriaVar( "AA5_CODSER", .F. )
Default cCodGrp:= ""

cCodGrp  := AtBaseGrup( cCodFab, cLojaFa, cCodPro, cNumSer )

If !Empty( cCodGrp )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Retorna o codigo de servico para o grupo / produto ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCodServ := AtServRet( cProduto, cCodGrp )
EndIf

Return( cCodServ )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtProdServ³ Autor ³ Sergio Silveira       ³ Data ³ 13/01/2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Validacao do codigo do produto nos apontamentos              ³±±
±±³          ³ Alimenta o campo codigo de servico nas GetDados ou retorna   ³±±
±±³          ³ codigo de servico para os gatilhos                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpX1 := AtProdServ( )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpX1 -> Logico ou Codigo de Servico ( Gatilho )             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtProdServ()

LOCAL cProduto := &( ReadVar() )
LOCAL cCampo   := ReadVar()
LOCAL cCodSer

LOCAL xRet     := .T.

Do Case
Case "ABA_CODPRO" $ cCampo

	xRet := CriaVar( "AA5_CODSER", .F.  )
	AB7->( DbSetOrder( 1 ) )

	If AB7->( DbSeek( xFilial( "AB7" ) + M->AB9_NUMOS ) )

		cCodSer := AtBaseServ( AB7->AB7_CODFAB, AB7->AB7_LOJAFA, AB7->AB7_CODPRO, AB7->AB7_NUMSER, cProduto )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o codigo de servico conforme a rotina chamadora ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		xRet := cCodSer

	EndIf

Case "ABC_CODPRO" $ cCampo

	xRet := CriaVar( "AA5_CODSER", .F.  )

	AB7->( DbSetOrder( 1 ) )

	If AB7->( DbSeek( xFilial( "AB7" ) + M->AB9_NUMOS ) )

		cCodSer := AtBaseServ( AB7->AB7_CODFAB, AB7->AB7_LOJAFA, AB7->AB7_CODPRO, AB7->AB7_NUMSER, cProduto )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o codigo de servico conforme a rotina chamadora ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		xRet := cCodSer

	EndIf

Case "AB8_CODPRO" $ cCampo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza o codigo de servico conforme a rotina chamadora ³
	//³ A Base instalada deve estar posicionada                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	cCodSer := AtBaseServ( AA3->AA3_CODFAB, AA3->AA3_LOJAFA, AA3->AA3_CODPRO,;
			AA3->AA3_NUMSER, cProduto )
	xRet := cCodSer

Case "ABG_CODPRO" $ cCampo

	xRet := CriaVar( "AA5_CODSER", .F.  )
	AB7->( DbSetOrder( 1 ) )
	If AB7->( DbSeek( xFilial( "AB7" ) + M->ABF_NUMOS + M->ABF_ITEMOS ) )
		cCodSer := AtBaseServ( AB7->AB7_CODFAB, AB7->AB7_LOJAFA, AB7->AB7_CODPRO,;
			AB7->AB7_NUMSER, cProduto )
		xRet := cCodSer
	EndIf

EndCase

Return( xRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ TECAGRETC  ³ Autor ³ Sigatec		       ³ Data ³ 04.08.14   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Retorna a cor (codigo)										      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ TECAGRETC( )                                     			   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Cod COR                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±³          ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function TECAGRETC()
Return __Cor

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AtCorWh  ³ Autor ³ Sergio Silveira       ³ Data ³ 22.09.98   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibe ListBox para a escolha de cores - Condicao When        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtCorWh( ExpO1, ExpC1 )                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Campo que recebera o numero da Cor                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±³          ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtCorWh( oGet, cCampoCor )

LOCAL aListBox := {}
LOCAL aBitmaps := {}

LOCAL cMarrom
LOCAL cVar  := AllTrim( StrTran( ReadVar(), "M->", "" ) )

LOCAL nOpca := 0
LOCAL nList

LOCAL oDlgDisp
LOCAL oList
LOCAL oButAdic
LOCAL oButBaix
Local lRet := .F.
DEFAULT oGet := Nil

cAl := Left( cCampoCor, At( "_",  cCampoCor ) - 1 )

If !EMPTY(cCampoCor)
	nList := Val( ( cAl )->( FieldGet( FieldPos( cCampoCor ) ) ) )
EndIf

aBitmaps :=  {  { "BR_PRETO"   , STR0003,  },;  // "Preto"
				{ "BR_CINZA"   , STR0004,  },;  // "Cinza"
				{ "BR_MARRON"  , STR0005,  },;  // "Marrom"
				{ "BR_PINK"    , STR0006,  },;  // "Rosa"
				{ "BR_VERMELHO", STR0007,  },;  // "Vermelho"
				{ "BR_LARANJA" , STR0008,  },;  // "Laranja"
				{ "BR_AMARELO" , STR0009,  },;  // "Amarelo"
				{ "BR_VERDE"   , STR0010,  },;  // "Verde"
				{ "BR_AZUL"    , STR0011,  } }  // "Azul"

If cCampoCor $ cVar .AND. Len( cVar ) == 7

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Alimenta no aListBox os Handles dos Bitmaps ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	AEval( aBitmaps, { |x,y| aBitmaps[ y, 3 ] := LoadBitmap( GetResources(), x[ 1 ] ) } )
	AEval( aBitmaps, { |x| AAdd( aListBox, { x[ 3 ], x[ 2 ] } ) } )

	DEFINE MSDIALOG oDlgDisp TITLE STR0013 From 0, 0 To 10, 40 OF oMainWnd // Cores

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Permite ou nao a inclusao de novos itens ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@  7,  8 LISTBOX oList VAR cVar Fields HEADER " ", STR0012 SIZE 110, 60 ON ;
	DBLCLICK ( nOpca := 1, nList := oList:nAt, oDlgDisp:End() )  Of oDlgDisp PIXEL // "Cor"

	oList:SetArray( aListBox )
	oList:bLine := { || { aListBox[oList:nAT,1], aListBox[oList:nAT,2] } }

	DEFINE SBUTTON oBut1 FROM 07, 123 TYPE 1 ACTION ( nOpca := 1, nList := oList:nAt,;
	oDlgDisp:End() ) ENABLE of oDlgDisp

	DEFINE SBUTTON oBut2 FROM 27, 123 TYPE 2 ACTION ( nOpca := 0,;
	oDlgDisp:End() ) ENABLE of oDlgDisp

	ACTIVATE MSDIALOG oDlgDisp CENTERED

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se confirmou, inicializa o campo Cor     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If nOpca == 1
		__Cor := Str( nList, 1 )
		If oGet <> Nil
			oGet:lModified := .T.
		Endif
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Se confirmou, inicializa o campo Cor     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		lRet := .T.
	EndIf

EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AtCorVal ³ Autor ³ Sergio Silveira       ³ Data ³ 03/12/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a Cor Selecionada                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AtCorVal( ExpC1 )                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Campo que deve receber a descricao da Cor.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> logico ( Validacao )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±³          ³                                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtCorVal( cCampoDes )

LOCAL cConteudo := &( ReadVar() )
LOCAL lRet      := .F.

If Val( cConteudo ) >= 1 .AND. Val( cConteudo ) <= 10
	lRet := .T.

	M->&( cCampoDes ) := AtCorIni( cConteudo, cCampoDes )

EndIf

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AtCorIni ³ Autor ³ Sergio Silveira       ³ Data ³ 03/12/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Devolve a descricao da cor conforme o numero                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtCorIni( ExpC2, ExpC3 )                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC2 -> Numero da Cor / ExpC3 -> Nome do Campo descricao Cor³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Descricao da Cor                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ TECA010                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtCorIni( cCor, cCampoDes )

LOCAL aCores   := { STR0003, ;  // "Preto"
				STR0004, ;  // "Cinza"
				STR0005, ;  // "Marrom"
				STR0006, ;  // "Rosa"
				STR0007, ;  // "Vermelho"
				STR0008, ;  // "Laranja"
				STR0009, ;  // "Amarelo"
					  STR0010, ;  // "Verde"
					  STR0011  }  // "Azul"

LOCAL cCores

cCores := If( ValType( cCor ) == "C", If( Empty( cCor ),CriaVar( cCampoDes, .F. ),;
			aCores[ Val( cCor ) ] ), CriaVar( cCampoDes, .F. ) )

Return( cCores )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtExpPlano³ Autor ³ Sergio Silveira       ³ Data ³ 16/09/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Expande o plano de manutencao preventiva                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtExpPlano( ExpC1 , ExpD2 )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Codigo do Plano de Manutencao                     ³±±
±±³          ³ ExpD2 -> Data para referencia da expancao do plano         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observ.   ³ O AA3 deve estar posicionado                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtExpPlano( cPlano , dData, lAutomato )

LOCAL cSeekAA9
LOCAL cUnidInic
LOCAL cUnidInter
LOCAL cDateNew
LOCAL cDataBase

Local dDateNew
LOCAL dDataIni

LOCAL lExp := .F.
Local lBlq := .F.
Local cQuery := ""

LOCAL nQuantOcor := 0
LOCAL nMesAtu    := 0
LOCAL nAnoAtu    := 0
LOCAL nDiaNew    := 0
LOCAL nQtdInic   := 0
LOCAL nQtdInter  := 0
LOCAL nLoop      := 0
Local nFator1    := 0
Local nFator2    := 0
Local nAnoNew    := 0
Local nMesNew    := 0
Local lAT620GRV := ExistBlock("AT620GRV")

Default lAutomato := .F.

dData := If ( dData == Nil, dDataBase, dData )

Begin Transaction

AA8->( DbSetOrder( 1 ) )

If AA8->( DbSeek( xFilial( "AA8" ) + cPlano ) )

	cSeekAA9 := xFilial( "AA9" ) + AA8->AA8_PLANO

	AA9->( DbSetOrder( 1 ) )
	AA9->( DbSeek( cSeekAA9 ) )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Percorre os itens e cria os movimentos relativos ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	While !AA9->( Eof() ) .AND. cSeekAA9 == AA9->AA9_FILIAL + AA9->AA9_PLANO

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua se o item estiver ativo ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		If AA9->AA9_ATIVO == "1"

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula a data de inicio       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			nQuantOcor := AA9->AA9_QUANT

			cUnidInic  := AA9->AA9_UNIINI
			nQtdInic   := AA9->AA9_INICIO

			cUnidInter := AA9->AA9_UNIINT
			nQtdInter  := AA9->AA9_INTERV

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Trata para unidade de tempo mensal, diario ou anual          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			Do Case
			Case cUnidInic == "1"

				dDataIni :=  dData + nQtdInic

			Case cUnidInic == "2"

				nMesAtu := Month( dData )
				nAnoAtu := Year( dData )

				nFator1 := Int( nQtdInic / 12 )
				nFator2 := If( 12 - nMesAtu < nQtdInic, 1, 0 )

				nAnoNew := Year(MonthSum(dData,nQtdInic))
				nMesNew := nMesAtu + nQtdInic - 12 * ( nFator2 )

				nDiaNew := Day( dData )

				While .T.

					cDateNew := Str( nDiaNew, 2 ) + "/" + Str( nMesNew, 2 ) + ;
							"/" + Str( nAnoNew, 4 )

					dDateNew := CToD( cDateNew )

					If !Empty( dDateNew )
						Exit
					EndIf

					nDiaNew--

				EndDo

				dDataIni := dDateNew

			Case cUnidInic == "3"

				cDataBase := DToC( dData )
				nAnoNew   := Year( dData ) + nQtdInic
				dDataIni  := CToD( Left( cDataBase, 6 ) + Str( nAnoNew, 4 ) )

			EndCase

			If cPaisLoc == "MEX"
				dDateNew := TxDtValid( dDataIni )
			Else
				dDateNew := DataValida( dDataIni )
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cria os movimentos             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

			For nLoop := 1 To nQuantOcor

				lExp := .T.

				#IFDEF TOP

					lBlq := .F.

					cQuery := ""
					cQuery += "SELECT COUNT(*) TOTAL FROM " + RetSqlName("ABE") + " ABE "
					cQuery += " WHERE ABE.ABE_FILIAL = '" + xFilial("ABE") + "' AND "
					cQuery += "	   ABE.ABE_CODFAB = '" + AA3->AA3_CODFAB + "' AND "
					cQuery += "	   ABE.ABE_LOJAFA = '" + AA3->AA3_LOJAFA + "' AND "
					cQuery += "	   ABE.ABE_CODPRO = '" + AA3->AA3_CODPRO + "' AND "
					cQuery += "	   ABE.ABE_NUMSER = '" + AA3->AA3_NUMSER + "' AND "
					cQuery += "	   ABE.ABE_PLANO  = '" + AA8->AA8_PLANO  + "' AND "
					cQuery += "	   ABE.ABE_ITEM   = '" + AA9->AA9_ITEM   + "' AND "
					cQuery += "	   ABE.ABE_SEQUEN =  " + StrZero(nLoop,4)+ "  AND "
					cQuery += "	   ABE.ABE_DATA   = '" + Dtos(dDateNew)  + "' AND "
					cQuery += "	   ABE.D_E_L_E_T_ = ' ' "

					cQuery := ChangeQuery(cQuery)

					dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"TRBMOV",.F.,.T.)

					If TRBMOV->TOTAL > 0
						lBlq := .T.
					EndIf

					TRBMOV->(dbCloseArea())

				#ENDIF

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Cria o Movimento no ABE        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !lBlq
					RecLock( "ABE", .T. )

					ABE->ABE_FILIAL := xFilial( "ABE" )
					ABE->ABE_CODFAB := AA3->AA3_CODFAB
					ABE->ABE_LOJAFA := AA3->AA3_LOJAFA
					ABE->ABE_CODPRO := AA3->AA3_CODPRO
					ABE->ABE_NUMSER := AA3->AA3_NUMSER
					ABE->ABE_PLANO  := AA8->AA8_PLANO
					ABE->ABE_ITEM   := AA9->AA9_ITEM
					ABE->ABE_SEQUEN := nLoop
					ABE->ABE_DATA   := dDateNew
					ABE->ABE_DTORIG := ABE->ABE_DATA
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					// Ponto de entrada para gravar campos de usuários           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lAT620GRV
						Execblock("AT620GRV",.F.,.F.)
					Endif

					ABE->( MsUnLock() )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Trata para unidade de tempo diario, mensal ou anual          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

				Do Case
				Case cUnidInter == "1"

					dDateNew := dDateNew + nQtdInter

				Case cUnidInter == "2"

					nMesAtu := Month( dDateNew )
					nAnoAtu := Year( dDateNew  )

					nFator1 := Int( nQtdInter / 12 )
					nFator2 := If( 12 - nMesAtu < nQtdInter, 1, 0 )

					nAnoNew := nAnoAtu + nFator1 + nFator2
					nMesNew := nMesAtu + nQtdInter - 12 * ( nFator2 )

					nDiaNew := Day( dDateNew )

					While .T.

						cDateNew := Str( nDiaNew, 2 ) + "/" + Str( nMesNew, 2 ) + ;
								"/" + Str( nAnoNew, 4 )

						dDateNew := CToD( cDateNew )

						If !Empty( dDateNew )
							Exit
						EndIf

						nDiaNew--

					EndDo

				Case cUnidInter == "3"

					cDateNew  := DToC( dDateNew )
					nAnoNew   := Year( dDateNew ) + nQtdInter
					dDateNew  := CToD( Left( cDateNew, 6 ) + Str( nAnoNew, 4 ) )

				EndCase

				If cPaisLoc == "MEX"
					dDateNew := TxDtValid( dDateNew )
				Else
					dDateNew := DataValida( dDateNew )
				EndIf

			Next nLoop

		EndIf

		AA9->( DbSkip() )

	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava o plano expandido na base instalada ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If lExp

		RecLock( "AA3", .F. )

		AA3->AA3_PLANO := cPlano
		AA3->AA3_DTEXP := dData

		AA3->( MsUnLock() )

	Else
		If !lAutomato
			Help( " ", 1, "ATMOVPLANO" ) // Nenhum movimento foi gerado !
		EndIf
	Endif

EndIf

End Transaction

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtEstPlano³ Autor ³ Sergio Silveira       ³ Data ³ 16/09/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Estorna o a expansao do plano de manutencao preventiva     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtExpPlano( ExpC1 )                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Codigo do Plano de Manutencao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtEstPlano( cPlano )

LOCAL cSeekABE

Begin Transaction

AA8->( DbSetOrder( 1 ) )

If AA8->( DbSeek( xFilial( "AA8" ) + cPlano ) )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Percorre os itens e estorna os movimentos ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	ABE->( DbSetOrder( 1 ) )

	cSeekABE := xFilial( "ABE" ) + AA3->AA3_CODFAB + AA3->AA3_LOJAFA + ;
		AA3->AA3_CODPRO + AA3->AA3_NUMSER

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Exclui todos os movimentos para este cliente / plano ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If ABE->( DbSeek( cSeekABE + cPlano ) )

		ABE->( DbEval( { || RecLock( "ABE", .F., .T. ), ABE->( DbDelete() ),;
			ABE->( MsUnlock() ) },{|| Empty(ABE->ABE_NUMOS) }, { || cSeekABE + cPlano == ABE_FILIAL+ABE_CODFAB+;
			ABE_LOJAFA + ABE_CODPRO + ABE_NUMSER + ABE_PLANO },, , .T. ) )

	EndIf
	If !ABE->( DbSeek( cSeekABE ) )
		RecLock("AA3")
		AA3->AA3_PLANO := ""
		AA3->AA3_DTEXP := Ctod("")
		MsUnLock()
	Else
		RecLock("AA3")
		AA3->AA3_PLANO := ABE->ABE_PLANO
		MsUnLock()
	EndIf

EndIf

End Transaction

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ EB()     ³ Autor ³ Sergio Silveira       ³ Data ³ 16/09/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Faz a chamada da funcao ExecBlock com menos bytes para uso ³±±
±±³          ³ com X3_WHEN                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ EB( ExpC1, ExpL1, ExpL2 )                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Executam as mesmas funcoes de ExecBlock                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ SIGATEC                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function EB( cRotina, cL1, cL2 )

Return( ExecBlock( cRotina, cL1, cL2 ) )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³          ³ Autor ³ Sergio Silveira       ³ Data ³ 05/03/99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Identifica Linha excluida na Getdados                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := GDDeleted( [ ExpN1] )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> ExpN1 -> Linha do Acols                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1 -> Item excluido ou nao                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtFilterAA3()

LOCAL cFunName := AllTrim( FunName() )
LOCAL cFilter  := ""

Do Case
Case "TECA300" $ cFunName
	cFilter := "AA3_FILIAL==xFilial('AA3') .AND. AA3_CODCLI==@#M->AB1_CODCLI .AND. AA3_LOJA==@#M->AB1_LOJA"
Case "TECA450" $ cFunName
	cFilter := "AA3_FILIAL==xFilial('AA3') .AND. AA3_CODCLI==@#M->AB6_CODCLI .AND. AA3_LOJA==@#M->AB6_LOJA"
Case "TECA460" $ cFunName
	cFilter := "AA3_FILIAL==xFilial('AA3') .AND. AA3_CODCLI==@#M->AB9_CODCLI .AND. AA3_LOJA==@#M->AB9_LOJA"
EndCase

Return( cFilter )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtHabilReq³ Autor ³ Sergio Silveira       ³ Data ³01/08/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inclui a habilidade requerida para o Produto x Ocorrencia  ³±±
±±³          ³ no array passado por referencia                            ³±±
±±³          ³ O AA7 deve estar posicionado.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtHabilReq( @ExpA1 )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 -> Array de Habilidades -> 1-Habilidade / 2-Nivel    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtHabilReq( aReqHabil )

LOCAL aArea    := GetArea()

LOCAL cSeekAAC := xFilial( "AAC" ) + AA7->AA7_CODPRO + AA7->AA7_CODPRB + AA7->AA7_GRPATE
LOCAL cQuery   := ""

#IFDEF TOP

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento para SQL                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cQuery := ""
	cQuery += "SELECT * FROM " + RetSqlName( "AAC" ) + " "
	cQuery += "WHERE AAC_FILIAL='" + xFilial( "AAC" ) + "' AND "
	cQuery += "AAC_CODPRO='" + AA7->AA7_CODPRO + "' AND "
	cQuery += "AAC_CODPRB='" + AA7->AA7_CODPRB + "' AND "
	cQuery += "AAC_GRPATE='" + AA7->AA7_GRPATE + "' AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )

	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), "TRBAAC", .F., .T. )

	If Alias() == "TRBAAC"
		While !TRBAAC->( Eof() )
			If Empty( nScan := AScan( aReqHabil, { |x| x[1] == TRBAAC->AAC_HABIL } ) )
				AAdd( aReqHabil, { TRBAAC->AAC_HABIL, TRBAAC->AAC_NIVEL } )
			Else
				If TRBAAC->AAC_NIVEL > aReqHabil[ nScan, 2 ]
					aReqHabil[ nScan, 2 ] := TRBAAC->AAC_NIVEL
				EndIf
			EndIf
			TRBAAC->( DbSkip() )
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Exclui a area de trabalho da query                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		TRBAAC->( dbCloseArea() )
		DbSelectArea( "AAC" )
	EndIf

#ELSE

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processamento para ISAM                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If AAC->( DbSeek( cSeekAAC ) )
		While !AAC->( Eof() ) .AND. cSeekAAC == AAC->AAC_FILIAL + ;
					AAC->AAC_CODPRO + AAC->AAC_CODPRB + AAC->AAC_GRPATE

			If Empty( nScan := AScan( aReqHabil, { |x| x[1] == AAC->AAC_HABIL } ) )
				AAdd( aReqHabil, { AAC->AAC_HABIL, AAC->AAC_NIVEL } )
			Else
				If AAC->AAC_NIVEL > aReqHabil[ nScan, 2 ]
					aReqHabil[ nScan, 2 ] := AAC->AAC_NIVEL
				EndIf
			EndIf
			AAC->( DbSkip() )
		EndDo
	EndIf

#ENDIF

RestArea( aArea )

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtCalcReaj ³ Autor ³ Sergio Silveira      ³ Data ³17/08/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao para calculo do reajuste                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := AtCalcReaj( ExpN2, ExpC1, ExpD1, ExpD2 )          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 := Valor reajustado                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN2 = Valor atual                                        ³±±
±±³          ³ ExpC1 = Codigo do Indice                                   ³±±
±±³          ³ ExpD1 = Data Inicial                                       ³±±
±±³          ³ ExpD2 = Data Final                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Function AtCalcReaj( nValorAtu, cCodIndice, dInicio, dFinal )

LOCAL aStruct    := {}
LOCAL aArea      := GetArea()

LOCAL bWhile     := { || .T. }

LOCAL cSeekAAE   := ""
LOCAL cQuery     := ""
LOCAL cAlias     := ""

LOCAL nValorReaj := nValorAtu
LOCAL nFator     := 1
LOCAL nLoop      := 0

AAD->( DbSetOrder( 1 ) )

If AAD->( DbSeek( xFilial( "AAD" ) + cCodIndice ) )

	#IFDEF TOP

		aStruct := AAE->( dbStruct() )

		cAlias := "ATCALCREAJ"

		cQuery := ""

		cQuery += "SELECT * FROM " + RetSqlName( "AAE" ) + " "
		cQuery += "WHERE "
		cQuery += "AAE_FILIAL='" + xFilial( "AAE" ) + "' AND "
		cQuery += "AAE_CODIND='" + cCodIndice       + "' AND "
		cQuery += "AAE_DATA>='"  + DToS( dInicio ) + "' AND "
		cQuery += "AAE_DATA<='"  + DToS( dFinal )   + "' AND "
		cQuery += "D_E_L_E_T_=' '"

		cQuery := ChangeQuery( cQuery )

		DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAlias, .F., .T. )

		For nLoop := 1 To Len( aStruct )
			If aStruct[ nLoop, 2 ] <> "C"
				TcSetField( cAlias, aStruct[ nLoop, 1 ],aStruct[ nLoop, 2 ],aStruct[ nLoop, 3 ],aStruct[ nLoop, 4 ] )
			EndIf
		Next nLoop

		bWhileAAE := { || !( cAlias )->( Eof() ) }


	#ELSE

		cAlias := "AAE"

		AAE->( DbSetOrder( 1 ) )

		cSeekAAE := xFilial( "AAE" ) + cCodIndice
		AAE->( DbSeek( cSeekAAE + DToS( dInicio ), .T. ) )

		bWhileAAE := { || !AAE->( Eof() ) .AND. AAE->AAE_FILIAL + AAE->AAE_CODIND == cSeekAAE .AND. AAE->AAE_DATA <= dFinal }

	#ENDIF

	While Eval( bWhileAAE )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Considera taxa composta ou nominal                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If AAD->AAD_TPTAXA == "1"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Considera variacao positiva ou negativa              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( cAlias )->AAE_SINAL == "1"
				nFator *= ( 1 + ( cAlias )->AAE_INDICE / 100 )
			Else
				nFator *= ( 1 - ( cAlias )->AAE_INDICE / 100 )
			EndIf
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Considera variacao positiva ou negativa              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( cAlias )->AAE_SINAL == "1"
				nFator += ( cAlias )->AAE_INDICE / 100
			Else
				nFator -= ( cAlias )->AAE_INDICE / 100
			EndIf
		EndIf
		( cAlias )->( DbSkip() )
	EndDo

EndIf

#IFDEF TOP
	If !EMPTY(cAlias)
		( cAlias )->( dbCloseArea() )
	EndIf
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Multiplica pelo fator                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nValorReaj *= nFator

RestArea( aArea )

Return( nValorReaj )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtReajCtr ³ Autor ³ Eduardo Riera         ³ Data ³26.03.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Rotina de reajuste de contratos de Manutencao.              ³±±
±±³          ³Esta rotina reajusta o valor do contrato e atualiza o perio-³±±
±±³          ³do de validade e cobranca para o periodo posterior ao cadas-³±±
±±³          ³trado no contrato de Manutencao. Ex. Se o periodo de valida ³±±
±±³          ³de for: 01/01/99 a 31/12/99 o novo periodo sera: 01/01/2000 ³±±
±±³          ³a 31/12/2000.                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 = AtReajCtr( ExpN1, ExpD1, ExpL2 )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 = Sempre .T.                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Multiplicador do Reajuste                            ³±±
±±³          ³ExpD1: Data de referencia do reajuste                       ³±±
±±³          ³ExpL2: Indica se atualiza datas de validade / cobranca      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA200                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O contrato deve estar posicionado.                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtReajCtr(nMultiplicador,dReajuste,lAtuValid)

Local aArea		:= GetArea()
Local aAreaAA3	:= AA3->(GetArea())
Local nDias		:= 0

DEFAULT dReajuste := dDataBase
DEFAULT lAtuValid := .T.

RecLock("AAH") // Trava o Contrato de Manutencao
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Reajuste do valor pelos itens contidos no Ctr. de Manutencao  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( AAH->AAH_VALOR == 0 )
	DbSelectArea("AA3")
	DbSetOrder(2)
	DbSeek(xFilial("AA3")+AAH->AAH_CONTRT)
	While ( !Eof() .AND. xFilial("AA3") == AA3->AA3_FILIAL .AND.;
			AA3->AA3_CONTRT == AAH->AAH_CONTRT )
		RecLock("AA3")
		AA3->AA3_VLRCTR *= nMultiplicador
		MsUnLock()
		DbSelectArea("AA3")
		DbSkip()
	EndDo
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reajuste do valor contido no proprio contrato de manutencao   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AAH->AAH_VALOR *= nMultiplicador
EndIf

If lAtuValid
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reajuste do periodo de validade                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( AAH->AAH_TPCONT == "2" )
		nDias := AAH->AAH_FIMVLD-AAH->AAH_INIVLD
		AAH->AAH_INIVLD := AAH->AAH_FIMVLD + 1
		AAH->AAH_FIMVLD += nDias + 1
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reajuste do periodo de cobranca                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nDias := AAH->AAH_FIMCOB-AAH->AAH_INICOB
	AAH->AAH_INICOB := AAH->AAH_FIMCOB + 1
	AAH->AAH_FIMCOB += nDias + 1
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza a data do ultimo reajuste                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAH->AAH_ULTREA := dReajuste

MsUnLock()

RestArea(aAreaAA3)
RestArea(aArea)
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtVldDiaHr³ Autor ³ Sergio Silveira       ³ Data ³05/09/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Rotina de Validacao de Data / Hora inicio Data / Hora fim  ³±±
±±³          ³ ( Verifica se a data / hora fim e maior ou igual que a     ³±±
±±³          ³ data / hora inicio )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AtVldDiaHr( ExpD1, ExpD2, ExpC1, ExpC2 )          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 := Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpD1: Data inicial                                         ³±±
±±³          ³ExpD2: Data final                                           ³±±
±±³          ³ExpC1: Hora inicial ( Formato HH:MM )                       ³±±
±±³          ³ExpC2: Hora Final   ( Formato HH:MM )                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGATEC                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtVldDiaHr( dInicio, dFinal, cHrInicio, cHrFinal )

Local cHrVazio := "  :  |" + Space( 5 )
Local lRetorno := .T.
Local cCpoAux  := ReadVar()

If "TECA460" $ AllTrim(FunName()) .AND. !Empty( dInicio ) .AND. !Empty( dFinal ) .AND. ;
		( ("AB9_DTSAID" $ cCpoAux) .OR. ("AB9_DTFIM" $ cCpoAux) )
	If dFinal < dInicio
		lRetorno := .F.
    EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida apenas se todos os itens estiverem preenchidos        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .AND. !Empty( dInicio ) .AND. !Empty( dFinal ) .AND. ;
		 !( cHrInicio $ cHrVazio ) .AND. !( cHrFinal $ cHrVazio )

	lRetorno := ( DToS( dFinal ) + Str( HoraToInt( cHrFinal ), 5, 2 ) ) >= ;
			( DToS( dInicio ) + Str( HoraToInt( cHrInicio ), 5, 2 ) )

EndIf

Return( lRetorno )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtRetGrpAt³ Autor ³ Sergio Silveira       ³ Data ³06/06/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o grupo de atendimento do contrato de manutencao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtRetGrpAt( ExpC2, [ ExpL1 ] )                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 := Grupo de atendimento                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC2: Codigo do contato                                    ³±±
±±³          ³ExpL1: Indica se considera vigencia                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGATEC                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtRetGrpAt( cContrato, lDtVig )

Local aArea   := GetArea()
Local bValid  := { || .T. }
Local cGrpAte := Space( Len( AAP->AAP_GRPATE ) )

lDtVig := If( ValType( lDtVig ) == "L", lDtVig, .F. )

If lDtVig
	bValid := { || ( dDataBase >= AAH->AAH_INIVLD .AND.;
				 dDataBase <= AAH->AAH_FIMVLD ) .OR. AAH->AAH_TPCONT=="1" }
EndIf

AAH->( DbSetOrder( 1 ) )
If AAH->( DbSeek( xFilial( "AAH" ) + cContrato ) )

	If Eval( bValid )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Obtem o grupo de atendimento associado a este contrato                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty( AAH->AAH_GRPATE )
			cGrpAte := AAH->AAH_GRPATE
		EndIf
	EndIf
EndIf

RestArea( aArea )

Return( cGrpAte )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtCanDelEq³ Autor ³ Sergio Silveira       ³ Data ³09/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de validacao da exclusao do Equipamento da Base Inst³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := AtCanDelEq( ExpC1, ExpC2, ExpC3, ExpC4, [ ExpL2 ])³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 := Validacao                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do fabricante                                 ³±±
±±³          ³ExpC2: Loja do fabricante                                   ³±±
±±³          ³ExpC3: Codigo do produto                                    ³±±
±±³          ³ExpC4: Numero de serie                                      ³±±
±±³          ³ExpL2: Exibe mensagens                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGATEC                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtCanDelEq( cCodFab, 	cLojaFab, 	cCodPro, 	cNumSer,;
					 lMessages, cFilOri	)

Local aHelps := {}
Local aArea  := GetArea()

Local lRet   := .T.

lMessages := If( ValType( lMessages ) == "L", lMessages, .F. )

AA3->( DbSetOrder( 4 ) )
AA3->( DbSeek( xFilial( "AA3" ) + cCodFab + cLojaFab + cCodPro + cNumSer ) )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Checa se Amarracao tem chamado tecnico              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AB2->( DbSetOrder( 3 ) )
If AB2->( DbSeek( xFilial("AB2") + cCodFab + cLojaFab + cCodPro + cNumSer ) )
	AAdd( aHelps, "AT040DEL01" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Checa se a Amarracao tem orcamento                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AB4->( DbSetOrder( 4 ) )
If AB4->( DbSeek( xFilial("AB4") + cCodFab + cLojaFab + cCodPro + cNumSer ) )
	AAdd( aHelps, "AT040DEL02" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Checa se Amarracao tem Ordem de Servico             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AB7->( DbSetOrder( 5 ) )
If AB7->( DbSeek(xFilial("AB7") + cCodFab + cLojaFab + cCodPro + cNumSer ) )
	AAdd( aHelps, "AT040DEL03" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se ha contrato de manutencao                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !Empty(AA3->AA3_CONTRT) )
	AAdd( aHelps, "AT040DEL04" )
	lRet := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se ha Preventiva                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !Empty(AA3->AA3_CTAPRE) )
	AAdd( aHelps, "AT040DEL05" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Checa se existem pendencias para o equipamento      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ABD->( DbSetOrder( 1 ) )
If ABD->( DbSeek(xFilial("ABD") + cCodFab + cLojaFab + cCodPro + cNumSer ) )
	AAdd( aHelps, "AT040DEL06" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Checa se existem movimentos de manutencao preventiva  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ABE->( DbSetOrder( 1 ) )
If ABE->( DbSeek(xFilial("ABE") + cCodFab + cLojaFab + cCodPro + cNumSer ) )
	AAdd( aHelps, "AT040DEL07" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³  Checa se existe relação de base x ATF  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TWH->( DbSetOrder( 1 ) )
If TWH->( DbSeek(xFilial("TWH") + cNumSer +  cFilOri) )
	AAdd( aHelps, "AT040DEL08" )
	lRet := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Dispara validacao do usuario                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	If ExistBlock( "ATVDELEQ" )
		lRet := ExecBlock( "ATVDELEQ", .F., .F. )
	EndIf
EndIf

If lMessages
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Processa os helps                                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AEval( aHelps, { |x| Help( "", 1, x ) } )
EndIf

RestArea( aArea )

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtDiscar   ³ Autor ³ Kleber Dias Gomes    ³ Data ³09/08/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Exibe o telefone virtual e executa a discagem              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := AtDiscar( ExpC2 )                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Numero efetivamente discado                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC2 -> Numero do Telefone                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtDiscar(cTel)

Local aBuffer	:= {}
Local oDlg
Local oTel
Local oCursor
Local cTitle	:= ""
Local nTipSol	:= 24
Local cCti		:= ""
Local nImage	:= 0
Local nRet		:= -1
Local oBtn		:= Array(13)


DEFINE MSDIALOG oDlg FROM  300,20 TO 570,285 TITLE cTitle  PIXEL OF oMainWnd

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria o cursor para as imagens do telefone virtual.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE CURSOR oCursor HAND

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Objetos para a criacao do telefone virtual.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 22,75 MSGET oTel VAR cTel SIZE 44 ,12 OF oDlg PIXEL PICTURE "@S" WHEN RIGHT READONLY ON CHANGE NOVSCROLL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria o fundo com a imagem de um telefone com gancho.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 00,00  JPEG RESOURCE "TEL_TELEFONE" SIZE 220,150 OF oDlg PIXEL

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria as imagens com os caracteres 1..0 , C e ","³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ 37,85  JPEG oBtn[1]   RESOURCE "TEL_DISCAR" 	SIZE 25,25 OF oDlg PIXEL ON CLICK (IIf(!Empty(cTel),nRet:= ATDiskNumber(nTipSol,cTel,cCTI),"")) BORDER TOOLTIP CURSOR oCursor
@ 55,65  JPEG oBtn[2]   RESOURCE "TEL_1"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("1",@cTel,oTel) BORDER CURSOR oCursor//"1"
@ 55,85  JPEG oBtn[3]   RESOURCE "TEL_2"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("2",@cTel,oTel) BORDER CURSOR oCursor//"2"
@ 55,105 JPEG oBtn[4]   RESOURCE "TEL_3"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("3",@cTel,oTel) BORDER CURSOR oCursor//"3"
@ 75,65  JPEG oBtn[5]   RESOURCE "TEL_4"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("4",@cTel,oTel) BORDER CURSOR oCursor//"4"
@ 75,85  JPEG oBtn[6]   RESOURCE "TEL_5"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("5",@cTel,oTel) BORDER CURSOR oCursor//"5"
@ 75,105 JPEG oBtn[7]   RESOURCE "TEL_6"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("6",@cTel,oTel) BORDER CURSOR oCursor//"6"
@ 95,65  JPEG oBtn[8]   RESOURCE "TEL_7"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("7",@cTel,oTel) BORDER CURSOR oCursor//"7"
@ 95,85  JPEG oBtn[9]   RESOURCE "TEL_8"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("8",@cTel,oTel) BORDER CURSOR oCursor//"8"
@ 95,105 JPEG oBtn[10]  RESOURCE "TEL_9"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("9",@cTel,oTel) BORDER CURSOR oCursor//"9"
@115,65  JPEG oBtn[11]  RESOURCE "TEL_C"        SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("C",@cTel,oTel) BORDER CURSOR oCursor//"C"
@115,85  JPEG oBtn[12]  RESOURCE "TEL_0"   		SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel("0",@cTel,oTel) BORDER CURSOR oCursor//"0"
@115,105 JPEG oBtn[13]  RESOURCE "TEL_VIRGULA"  SIZE 25,25 OF oDlg PIXEL ON CLICK ATKeyTel(",",@cTel,oTel) BORDER CURSOR oCursor//","
If !isBlind()
	ACTIVATE MSDIALOG oDlg CENTER
EndIf
Return( cTel )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtKeyTel   ³ Autor ³Kleber Dias Gomes     ³ Data ³09/08/02  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Monta o numero do telefone atraves dos botoes pressionados  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Caracter do botao pressionado                     ³±±
±±³          ³ ExpC2 -> Numero do telefone a ser montado                  ³±±
±±³          ³ ExpU1 -> Get com o telefone a ser discado                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtKeyTel(cKey,cTel,oTel)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza o numero do telefone. Caso pressionou "C" limpa o numero a ser discado.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cKey == "C")
	cTel:= ""
	cTel:=Space(80)
Else
	cTel:= AllTrim(cTel) + cKey
	cTel+= Space(80 - Len(cTel))
Endif

oTel:Refresh()

Return Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtDiskNumber ³ Autor ³Kleber Dias Gomes      ³ Data ³09/08/02  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Executa a comunicacao com CTI/MODEM para a discagem            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 -> Numero da Solicitacao do Recurso                     ³±±
±±³          ³ ExpC1 -> Numero do Telefone a ser discado                     ³±±
±±³          ³ ExpC2 -> Modelo da CTI					                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtDiskNumber(nTipSol,cTel,cCTI)
Local nRet    := 0
Local aBuffer := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia os dados para a CTI/MODEM executar a discagem.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cTel:= AllTrim(cTel)
nRet:= SGSendCti( nTipSol, {cTel}, @aBuffer)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Fecha a comunicacao com o MODEM.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nRet == 0 .AND. Empty(cCTI)
	SGSendCti( 25, {}, @aBuffer)
Endif

Return(nRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtRetKit   ³ Autor ³ Sergio Silveira      ³ Data ³17/09/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna o kit de atendimento para o produto / ocorrencia   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpA1 := AtRetKit( ExpC1, ExpC2 )                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpA1 -> Array com a seguinte estrutura :                  ³±±
±±³          ³        1 -> Codigo do produto                              ³±±
±±³          ³        2 -> Descricao do produto                           ³±±
±±³          ³        3 -> Quantidade                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Codigo do produto                                 ³±±
±±³          ³ ExpC2 -> Codigo da ocorrencia / problema                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AtRetKit( cCodPro, cCodPrb )

LOCAL aArea     := GetArea()
LOCAL aKit      := {}

LOCAL cProdNulo := Space( Len( AA6->AA6_CODPRO ) )
LOCAL cPrbNulo  := Space( Len( AA6->AA6_CODPRB ) )

LOCAL lContinua := .T.

#IFDEF TOP
	LOCAL cQuery    := ""
	LOCAL cAliasQry := ""
#ELSE
	LOCAL cSeekAA6  := ""
#ENDIF

#IFDEF TOP

	DbSelectArea( "AA6" )

	If !Empty( cCodPro ) .AND. !Empty( cCodPrb )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto / ocorrencia             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cAliasQry := GetNextAlias()

		cQuery := "SELECT AA6_PRODUT,AA6_QUANT,AA6_ITEM,B1_DESC FROM "
		cQuery += RetSqlName( "AA6" ) + " AA6," + RetSqlName( "SB1" ) + " SB1 "
		cQuery += "WHERE "
		cQuery += "AA6_FILIAL='" + xFilial( "AA6" ) + "' AND "
		cQuery += "AA6_CODPRO='" + cCodPro + "' AND "
	 	cQuery += "AA6_CODPRB='" + cCodPrb + "' AND "
	 	cQuery += "AA6.D_E_L_E_T_=' ' AND "
		cQuery += "B1_FILIAL='" + xFilial( "SB1" ) + "' AND "
		cQuery += "B1_COD=AA6_PRODUT AND "
	 	cQuery += "SB1.D_E_L_E_T_=' ' ORDER BY AA6_ITEM"

	 	cQuery := ChangeQuery( cQuery )

	 	DbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .f., .t. )

	 	TcSetField( cAliasQry, "AA6_QUANT", "N", 18, 2 )

	 	While !( cAliasQry )->( Eof() )
	 		AAdd( aKit, { ( cAliasQry )->AA6_PRODUT,( cAliasQry )->B1_DESC,( cAliasQry )->AA6_QUANT } )
	 		( cAliasQry )->( DbSkip() )
	 	EndDo

		( cAliasQry )->( dbCloseArea() )

		DbSelectArea( "AA6" )

	EndIf

	lContinua := Empty( aKit )

	If lContinua .AND. !Empty( cCodPro )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto prenchido / ocorrencia branco ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cAliasQry := GetNextAlias()

		cQuery := "SELECT AA6_PRODUT,AA6_QUANT,AA6_ITEM,B1_DESC FROM "
		cQuery += RetSqlName( "AA6" ) + " AA6," + RetSqlName( "SB1" ) + " SB1 "
		cQuery += "WHERE "
		cQuery += "AA6_FILIAL='" + xFilial( "AA6" ) + "' AND "
		cQuery += "AA6_CODPRO='" + cCodPro  + "' AND "
	 	cQuery += "AA6_CODPRB='" + cPrbNulo + "' AND "
	 	cQuery += "AA6.D_E_L_E_T_=' ' AND "
		cQuery += "B1_FILIAL='" + xFilial( "SB1" ) + "' AND "
		cQuery += "B1_COD=AA6_PRODUT AND "
	 	cQuery += "SB1.D_E_L_E_T_=' ' ORDER BY AA6_ITEM"

	 	cQuery := ChangeQuery( cQuery )

	 	DbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .f., .t. )

	 	TcSetField( cAliasQry, "AA6_QUANT", "N", 18, 2 )

	 	While !( cAliasQry )->( Eof() )
	 		AAdd( aKit, { ( cAliasQry )->AA6_PRODUT,( cAliasQry )->B1_DESC,( cAliasQry )->AA6_QUANT } )
	 		( cAliasQry )->( DbSkip() )
	 	EndDo

		( cAliasQry )->( dbCloseArea() )

		DbSelectArea( "AA6" )

	EndIf

	lContinua := Empty( aKit )

	If lContinua .AND. !Empty( cCodPrb )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto branco / ocorrencia preenchida³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cAliasQry := GetNextAlias()

		cQuery := "SELECT AA6_PRODUT,AA6_QUANT,AA6_ITEM,B1_DESC FROM "
		cQuery += RetSqlName( "AA6" ) + " AA6," + RetSqlName( "SB1" ) + " SB1 "
		cQuery += "WHERE "
		cQuery += "AA6_FILIAL='" + xFilial( "AA6" ) + "' AND "
		cQuery += "AA6_CODPRO='" + cProdNulo + "' AND "
	 	cQuery += "AA6_CODPRB='" + cCodPrb   + "' AND "
	 	cQuery += "AA6.D_E_L_E_T_=' ' AND "
		cQuery += "B1_FILIAL='" + xFilial( "SB1" ) + "' AND "
		cQuery += "B1_COD=AA6_PRODUT AND "
	 	cQuery += "SB1.D_E_L_E_T_=' ' ORDER BY AA6_ITEM"

	 	cQuery := ChangeQuery( cQuery )

	 	DbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .f., .t. )

	 	TcSetField( cAliasQry, "AA6_QUANT", "N", 18, 2 )

	 	While !( cAliasQry )->( Eof() )
	 		AAdd( aKit, { ( cAliasQry )->AA6_PRODUT,( cAliasQry )->B1_DESC,( cAliasQry )->AA6_QUANT } )
	 		( cAliasQry )->( DbSkip() )
	 	EndDo

		( cAliasQry )->( dbCloseArea() )

		DbSelectArea( "AA6" )

 	EndIf

#ELSE

	If !Empty( cCodPro ) .AND. !Empty( cCodPrb )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto / ocorrencia             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AA6->( DbSetOrder( 1 ) )

		cSeekAA6 := xFilial( "AA6" ) + cCodPro + cCodPrb
		If AA6->( DbSeek( cSeekAA6 ) )
		 	While !AA6->( Eof() ) .AND. cSeekAA6 == AA6->AA6_FILIAL + AA6->AA6_CODPRO + AA6->AA6_CODPRB
				If SB1->( DbSeek( xFilial( "SB1" ) + AA6->AA6_PRODUT ) )
			 		AAdd( aKit, { AA6->AA6_PRODUT, SB1->B1_COD, AA6->AA6_QUANT } )
				EndIf
				AA6->( DbSkip() )
			EndDo
		EndIf

	EndIf

	lContinua := Empty( aKit )

	If lContinua .AND. !Empty( cCodPro )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto prenchido / ocorrencia branco ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AA6->( DbSetOrder( 1 ) )

		cSeekAA6 := xFilial( "AA6" ) + cCodPro + cPrbNulo
		If AA6->( DbSeek( cSeekAA6 ) )
		 	While !AA6->( Eof() ) .AND. cSeekAA6 == AA6->AA6_FILIAL + AA6->AA6_CODPRO + AA6->AA6_CODPRB
				If SB1->( DbSeek( xFilial( "SB1" ) + AA6->AA6_PRODUT ) )
			 		AAdd( aKit, { AA6->AA6_PRODUT, SB1->B1_COD, AA6->AA6_QUANT } )
				EndIf
				AA6->( DbSkip() )
			EndDo
		EndIf

 	EndIf

	lContinua := Empty( aKit )

	If lContinua .AND. !Empty( cCodPrb )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Traz o kit para o produto branco / ocorrencia preenchida³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		AA6->( DbSetOrder( 1 ) )

		cSeekAA6 := xFilial( "AA6" ) + cProdNulo + cPrbNulo
		If AA6->( DbSeek( cSeekAA6 ) )
		 	While !AA6->( Eof() ) .AND. cSeekAA6 == AA6->AA6_FILIAL + AA6->AA6_CODPRO + AA6->AA6_CODPRB
				If SB1->( DbSeek( xFilial( "SB1" ) + AA6->AA6_PRODUT ) )
			 		AAdd( aKit, { AA6->AA6_PRODUT, SB1->B1_COD, AA6->AA6_QUANT } )
				EndIf
				AA6->( DbSkip() )
			EndDo
		EndIf

	EndIf

#ENDIF

RestArea( aArea )

Return( aKit )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AtSchedTec³ Autor ³ Henry Fila            ³ Data ³22.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Traz os horarios ddas alocacoes                              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ExpN1 := AtSchedTec(ExpA1)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1 - Array mnemonico os dados a serem considerados        ³±±
±±³          ³        {TECN_FROM,Codigo do Tecnico Inicial}                ³±±
±±³          ³        {TECN_TO  ,Codigo do Tecnico Final  }                ³±±
±±³          ³        {DATA_FROM,Data Inicial}                             ³±±
±±³          ³        {DATA_TO  ,Data Final  }                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpA1 - .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo retornar os horarios das aloca ³±±
±±³          ³coes dos tecnicos de acordo com parametros                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Field Service                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtSchedTec( aParam )

Local aTecnico   := {}
Local aItens     := {}
Local cQuery     := ""
Local cIndABB    := ""
Local cKeyABB    := ""
Local cTecFrom   := Space(Len(AA1->AA1_CODTEC))
Local cTecTo     := Replicate( "Z", Len( AA1->AA1_CODTEC ) )
Local cTecnico   := ""
Local dDataFrom  := Ctod("01/01/80")
Local dDataTo    := Ctod("31/12/20")

Local cAliasABB := "ABB"
Local cAliasAA1 := "AA1"

Local cMostr	:= ""
Local cVtDe	:= ""
Local cVtAte	:= ""
Local cNumDe 	:= ""
Local cNumAte 	:= ""
Local cCliDe 	:= ""
Local cCliAte 	:= ""
Local cContrtDe	:= ""
Local cContrtAte:= ""


Local lQuery  := .F.

Local nIndABB := 0
Local nLoop   := 0

DEFAULT aParam := {}

For nLoop := 1 To Len( aParam )

	cTipo    := aParam[ nLoop, 1 ]

	Do Case
	Case cTipo == "TECN_FROM"
		cTecFrom    := aParam[ nLoop, 2 ]
	Case cTipo == "TECN_TO"
		cTecTo      := aParam[ nLoop, 2 ]
	Case cTipo == "DATA_FROM"
		dDataFrom 	:= aParam[ nLoop, 2 ]
	Case cTipo == "DATA_TO"
		dDataTo		:= aParam[ nLoop, 2 ]
	Case cTipo == "CLIE_DE"
		cCliDe		:= aParam[ nLoop, 2 ]
	Case cTipo == "CLIE_ATE"
		cCliAte		:= aParam[ nLoop, 2 ]
	Case cTipo == "CONTR_DE"
		cContrtDe	:= aParam[ nLoop, 2 ]
	Case cTipo == "CONTR_ATE"
		cContrtAte	:= aParam[ nLoop, 2 ]
	Case cTipo == "NUM_DE"
		cNumDe		:= aParam[ nLoop, 2 ]
	Case cTipo == "NUM_ATE"
		cNumAte		:= aParam[ nLoop, 2 ]
	Case cTipo == "VT_DE"
		cVtDe		:= aParam[ nLoop, 2 ]
	Case cTipo == "VT_ATE"
		cVtAte		:= aParam[ nLoop, 2 ]
	Case cTipo == "TP_ENT"
		cMostr		:= aParam[ nLoop, 2 ]
		If ValType(cMostr) == "N"
			cMostr := AllTrim(Str(cMostr))
		EndIf
	EndCase

Next nLoop

If Empty(cNumAte)
	cNumAte := "ZZZZZZ"
EndIf

If Empty(cVtAte)
	cVtAte := "ZZZZZZ"
EndIf

DbSelectArea("ABB")
#IFDEF TOP

	If Len(aParam) > 10

		lQuery := .T.

		cAliasABB := "QRYABB"
		cAliasAA1 := "QRYABB"

		IF cMostr == "1" .or. cMostr == "2"

		    cQuery := "SELECT ABB.*, AA1_NOMTEC FROM "
			cQuery += RetSqlName("ABB") + " ABB "
			cQuery += "LEFT JOIN "+RetSqlName("AA1") + " AA1 ON "

			cQuery += "AA1_FILIAL ='"+xFilial("AA1")+"' AND "
			cQuery += "AA1_CODTEC = ABB_CODTEC AND "
			cQuery += "AA1.D_E_L_E_T_ = ' ' "

			cQuery += "LEFT JOIN "+RetSqlName("AB6") + " AB6 ON "

			cQuery += "AB6_FILIAL ='" + xFilial( "AB6" ) + "' AND "
			cQuery += "AB6_NUMOS = ABB_NUMOS AND "
			If !Empty(cCliDe) .And. !Empty(cCliAte)
				cQuery += "AB6_CODCLI >= '" + cCliDe + "' AND "
				cQuery += "AB6_CODCLI <= '" + cCliAte + "' AND "
			EndIf

			If !Empty(cContrtDe) .And. !Empty(cContrtAte)
				cQuery += "AB6_CONTRT >= '" + cContrtDe + "' AND "
				cQuery += "AB6_CONTRT <= '" + cContrtAte + "' AND "
			EndIf

			cQuery += "AB6_TPCONT >= ' ' AND "
			cQuery += "AB6_TPCONT <= '2' AND "

			cQuery += "AB6.D_E_L_E_T_ = ' ' "

			cQuery += "LEFT JOIN "+RetSqlName("AAT") + " AAT ON "
			cQuery += "AAT_FILIAL ='"+xFilial("AAT")+"' AND AAT_CODVIS = ABB_CHAVE AND AAT.D_E_L_E_T_ = ' '"

			cQuery += " WHERE "

			cQuery += "ABB_FILIAL ='"+xFilial("ABB")+"' AND "
			cQuery += "ABB_CODTEC >= '"+cTecFrom+"' AND "
			cQuery += "ABB_CODTEC <= '"+cTecTo+"' AND "
			cQuery += "ABB_DTINI  >= '"+Dtos(dDataFrom)+"' AND "
			cQuery += "ABB_DTFIM  <= '"+Dtos(dDataTo)+"' AND "

			If cMostr == "1"
				cQuery += "ABB_NUMOS BETWEEN '" + cNumDe + "' AND '" + cNumAte + "' "
				cQuery += "AND ABB_ENTIDA <> 'AAT' "
			EndIf

			If cMostr == "2"
		    	cQuery += "ABB_CHAVE BETWEEN '" + cVtDe + "' AND '" + cVtAte + "' "
		    	cQuery += "AND ABB_ENTIDA = 'AAT' "
			EndIf

			cQuery += "AND ABB.D_E_L_E_T_ = ' ' "

			cQuery += "ORDER BY AA1_CODTEC"

		ElseIf cMostr == "3"

			cQuery := "SELECT ABB.*, AA1_NOMTEC FROM "
			cQuery += RetSqlName("ABB") + " ABB, "
			cQuery += RetSqlName("AA1") + " AA1, "
			cQuery += RetSqlName("AB6") + " AB6 "
			cQuery += " WHERE "

			cQuery += "ABB_FILIAL ='"+xFilial("ABB")+"' AND "
			cQuery += "ABB_CODTEC >= '"+cTecFrom+"' AND "
			cQuery += "ABB_CODTEC <= '"+cTecTo+"' AND "
			cQuery += "ABB_DTINI  >= '"+Dtos(dDataFrom)+"' AND "
			cQuery += "ABB_DTFIM  <= '"+Dtos(dDataTo)+"' AND "

			cQuery += "ABB_NUMOS BETWEEN '" + cNumDe + "' AND '" + cNumAte + "' AND "
			cQuery += "ABB_ENTIDA <> 'AAT' AND "

			cQuery += "ABB.D_E_L_E_T_ = ' ' AND "

			cQuery += "AA1_FILIAL ='"+xFilial("AA1")+"' AND "
			cQuery += "AA1_CODTEC = ABB_CODTEC AND "
			cQuery += "AA1.D_E_L_E_T_ = ' ' AND "

			cQuery += "AB6_FILIAL ='" + xFilial( "AB6" ) + "' AND "
			cQuery += "AB6_NUMOS = ABB_NUMOS AND "
			If !Empty(cCliDe) .And. !Empty(cCliAte)
				cQuery += "AB6_CODCLI >= '" + cCliDe + "' AND "
				cQuery += "AB6_CODCLI <= '" + cCliAte + "' AND "
			EndIf

			If !Empty(cContrtDe) .And. !Empty(cContrtAte)
				cQuery += "AB6_CONTRT >= '" + cContrtDe + "' AND "
				cQuery += "AB6_CONTRT <= '" + cContrtAte + "' AND "
			EndIf


			cQuery += "AB6_TPCONT >= ' ' AND "
			cQuery += "AB6_TPCONT <= '2' AND "

			cQuery += "AB6.D_E_L_E_T_ = ' ' "

		    cQuery += "UNION "

		    cQuery += "SELECT ABB.*, AA1_NOMTEC FROM "
			cQuery += RetSqlName("ABB") + " ABB, "
			cQuery += RetSqlName("AA1") + " AA1, "
			cQuery += RetSqlName("AAT") + " AAT "
			cQuery += " WHERE "

			cQuery += "ABB_FILIAL ='"+xFilial("ABB")+"' AND "
			cQuery += "ABB_CODTEC >= '"+cTecFrom+"' AND "
			cQuery += "ABB_CODTEC <= '"+cTecTo+"' AND "
			cQuery += "ABB_DTINI  >= '"+Dtos(dDataFrom)+"' AND "
			cQuery += "ABB_DTFIM  <= '"+Dtos(dDataTo)+"' AND "

			cQuery += "ABB_CHAVE BETWEEN '" + cVtDe + "' AND '" + cVtAte + "' AND "
		    cQuery += "ABB_ENTIDA = 'AAT' AND "

			cQuery += "ABB.D_E_L_E_T_ = ' ' AND "

			cQuery += "AA1_FILIAL ='"+xFilial("AA1")+"' AND "
			cQuery += "AA1_CODTEC = ABB_CODTEC AND "
			cQuery += "AA1.D_E_L_E_T_ = ' ' AND "

			cQuery += "AAT_FILIAL ='"+xFilial("AAT")+"' AND "
			cQuery += "AAT_CODVIS = ABB_CHAVE AND "
			cQuery += "AAT.D_E_L_E_T_ = ' ' "

	    EndIf

		cQuery := ChangeQuery(cQuery)
	    DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABB,.T.,.T.)

		TcSetField( cAliasABB, "ABB_DTINI", "D", 8, 0 )
		TcSetField( cAliasABB, "ABB_DTFIM", "D", 8, 0 )

    Else

	    lQuery := .T.

		cAliasABB := "QRYABB"
		cAliasAA1 := "QRYABB"

		cQuery := "SELECT ABB.*, AA1_NOMTEC FROM "
		cQuery += RetSqlName("ABB") + " ABB, "
		cQuery += RetSqlName("AA1") + " AA1, "
		cQuery += RetSqlName("AB6") + " AB6 "
		cQuery += " WHERE "

		cQuery += "ABB_FILIAL ='"+xFilial("ABB")+"' AND "
		cQuery += "ABB_CODTEC >= '"+cTecFrom+"' AND "
		cQuery += "ABB_CODTEC <= '"+cTecTo+"' AND "
		cQuery += "ABB_DTINI  >= '"+Dtos(dDataFrom)+"' AND "
		cQuery += "ABB_DTFIM  <= '"+Dtos(dDataTo)+"' AND "
		If !Empty(cNumDe) .And. !Empty(cNumAte)
			cQuery += "ABB_NUMOS >= '" + cNumDe + "' AND "
			cQuery += "ABB_NUMOS <= '" + cNumAte + "' AND "
		EndIf
		cQuery += "ABB.D_E_L_E_T_ = ' ' AND "

		cQuery += "AA1_FILIAL ='"+xFilial("AA1")+"' AND "
		cQuery += "AA1_CODTEC = ABB_CODTEC AND "
		cQuery += "AA1.D_E_L_E_T_ = ' ' AND "

		cQuery += "AB6_FILIAL ='" + xFilial( "AB6" ) + "' AND "
		cQuery += "AB6_NUMOS = ABB_NUMOS AND "
		If !Empty(cCliDe) .And. !Empty(cCliAte)
			cQuery += "AB6_CODCLI >= '" + cCliDe + "' AND "
			cQuery += "AB6_CODCLI <= '" + cCliAte + "' AND "
		EndIf

		If !Empty(cContrtDe) .And. !Empty(cContrtAte)
			cQuery += "AB6_CONTRT >= '" + cContrtDe + "' AND "
			cQuery += "AB6_CONTRT <= '" + cContrtAte + "' AND "
		EndIf

		cQuery += "AB6_TPCONT >= ' ' AND "
		cQuery += "AB6_TPCONT <= '2' AND "


		cQuery += "AB6.D_E_L_E_T_ = ' ' "

		cQuery += "ORDER BY AA1_CODTEC"

		cQuery := ChangeQuery(cQuery)
	    DbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasABB,.T.,.T.)

		TcSetField( cAliasABB, "ABB_DTINI", "D", 8, 0 )
		TcSetField( cAliasABB, "ABB_DTFIM", "D", 8, 0 )

    EndIf

#ELSE

	DbSelectArea("ABB")
	DbSetOrder(1)
	cIndABB := CriaTrab(NIL,.F.)
	cKeyABB := IndexKey()

	cCond := 'ABB_FILIAL == "'+xFilial("ABB")+'" .AND.'
	cCond += 'ABB_CODTEC >= "'+cTecFrom+'" .AND. ABB_CODTEC <= "'+cTecTo+'" .AND. '
	cCond += 'Dtos(ABB_DTINI) >= "'+Dtos(dDataFrom)+'" .AND. Dtos(ABB_DTFIM) <= "'+Dtos(dDataTo)+'" '

	IndRegua("ABB",cIndABB,cKeyABB,,cCond,"Selecionando Registros ...")
	nIndABB := RetIndex("ABB")+1

	#IFNDEF TOP
		DbSetIndex(cIndABB+OrdBagExT())
	#ENDIF

	DbSetOrder(nIndABB)
	DbGoTop()

#ENDIF

While (cAliasABB)->(!Eof())

	cTecnico := (cAliasABB)->ABB_CODTEC

	If !lQuery
		cQuery += "AB6_CODCLI >= '" + cCliDe + "' AND "
		cQuery += "AB6_CODCLI <= '" + cCliAte + "' AND "
		cQuery += "AB6_CONTRT >= '" + cContrtDe + "' AND "
		cQuery += "AB6_CONTRT <= '" + cContrtAte + "' AND "

		cQuery += "AB6_TPCONT >= '1' AND "
		cQuery += "AB6_TPCONT <= '2' AND "

        DbSelectArea( "AB6" )
		AB6->( DbSetOrder( 1 ) )
		If AB6->( !DbSeek( xFilial( "AB6" ) + ABB->ABB_NUMOS ) )
			(cAliasABB)->(DbSkip())
			Loop
		EndIf

		// Filtra o cliente
		If AB6->AB6_CODCLI < cCliDe .OR. AB6->AB6_CODCLI > cCliAte
			(cAliasABB)->(DbSkip())
			Loop
		EndIf

		// Filtra o contrato

		If AB6->AB6_CONTRT < cContrtDe .OR. AB6->AB6_CONTRT > cContrtAte
			(cAliasABB)->(DbSkip())
			Loop
		EndIf

		// Filtra tipo do contrato

		If !( AB6->AB6_TPCONT $ "1*2" )
			(cAliasABB)->(DbSkip())
			Loop
		EndIf

        DbSelectArea( "AA1" )
		AA1->(DbSetOrder(1))
		AA1->(DbSeek(xFilial("AA1")+ABB->ABB_CODTEC))
	Endif

	Aadd(aTecnico,{	(cAliasABB)->ABB_CODTEC, Capital( (cAliasAA1)->AA1_NOMTEC ),}	)

	aItens := {}

	While (cAliasABB)->(!Eof()) .AND. (cAliasABB)->ABB_CODTEC == cTecnico

		Aadd(aItens,{(cAliasABB)->ABB_NUMOS,;
					(cAliasABB)->ABB_DTINI,;
					(cAliasABB)->ABB_HRINI,;
					(cAliasABB)->ABB_DTFIM,;
					(cAliasABB)->ABB_HRFIM,;
					(cAliasABB)->ABB_CHAVE})

		(cAliasABB)->(DbSkip())

	EndDo

	aTecnico[Len(aTecnico)][3] := aClone(aItens)
Enddo

aTecnico := aSort(aTecnico,,,{|x,y| x[1] < y[1] })

If lQuery
	DbSelectArea(cAliasABB)
	dbCloseArea()
Endif

Return(aTecnico)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TECLoad()³ Autor ³ Sergio Silveira       ³ Data ³22.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Dispara as rotinas de processamento do Field Service         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³TECLoad                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Field Service                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function TECLoad()
Local lGsteclo := SuperGetMv("MV_GSTECLO",,.F.)
Local cUsrCtServ := GetNewPar( "MV_ATUSCTS", "" )
Local lExistMv := GetMv( "MV_GSTECLO" ,.T., )   
Local lSemOs := SuperGetMv("MV_GSGEROS",,'1') == '2'


If lSemOs
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Criacao de um template de proposta comercial ³
	//³para integracao,com Microsoft Word.          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	R600TWrd()

	If !lGsteclo
		//-- Trecho para popular os campos TFF_TABXML
		DbSelectArea('TFF')
		If TFF->( ColumnPos('TFF_TABXML') ) > 0
			At740FLXML()
		EndIf

		ABQITEM()
		If ExistFunc("At581UpTFF")
			At581UpTFF()
		EndIf
		If lExistMv
			TecXSemOs()
			PUTMV("MV_GSTECLO", .T.)
		EndIf	

	EndIf

Else
	At210Auto()

	At250Auto( cUsrCtServ )
EndIf

If FindFunction("TecBMetrics")
	TecBMetrics()
EndIf

Return( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtTravaReg³ Autor ³ Conrado Q. Gomes      ³ Data ³ 19.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Travamento dos regristros durante a criação do aCols       ³±±
±±³          ³ feito através do FillGetDados.                             ³±±
±±³          ³ Utilizada em conjunto com a função AtDestravaReg.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Lógico, onde:                                              ³±±
±±³          ³ .T.: Conseguiu fazer o travamento do registro.             ³±±
±±³          ³ .F.: Não conseguiu fazer o travamento do registro.         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ cAlias: Alias da tabela do registro que será travado.      ³±±
±±³Parametros³ aTravas, onde:                                             ³±±
±±³          ³ [n,1]: Alias da tabela com o registro travado.             ³±±
±±³          ³ [n,2]: Número do registro travado.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³12/02/2007³Conrado Q.     ³-BOPS 115729: Faz a verificação se o alias  ³±±
±±³          ³               ³aberto é o mesmo do qual se deseja fazer o  ³±±
±±³          ³               ³bloqueio.                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtTravaReg( cAlias, aTravas )

// Se o Alias atual for diferente do requisitado para bloqueio. (Necessário quando não há nenhum registro posicionado no cAlias)
If ( Alias() == cAlias )
	If ( SoftLock(cAlias) )
		Aadd(aTravas,{ cAlias , RecNo() })
	Else
		Return(.F.)
	Endif
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtDestravaReg³ Autor ³ Conrado Q. Gomes   ³ Data ³ 19.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Destravamento dos registros travados durante a criação do  ³±±
±±³          ³ aCols feito através do FillGetDados.                       ³±±
±±³          ³ Utilizada em conjunto com a função AtTravaReg.     s        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aTravas, onde:                                             ³±±
±±³          ³ [n,1]: Alias da tabela com o registro travado.             ³±±
±±³          ³ [n,2]: Número do registro travado.                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AtDestravaReg( aTravas )

Local aArea	:= GetArea()	// Salva a area atual
Local nCntFor	:= 0			// Contador para o destravamento

For nCntFor := 1 To Len(aTravas)
	DbSelectArea(aTravas[nCntFor][1])
	dbGoto(aTravas[nCntFor][2])
	MsUnLock()
Next nCntFor

RestArea(aArea)

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AtAtuTMK  ºAutor  ³Vendas CRM          º Data ³  25/10/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Atualiza o item do chamado TMK                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³Gield Service/Call Center                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtAtuTMK(cNumOS,cStatus)

Local aArea		:= GetArea()
Local cFilSUD	:= xFilial("SUD")
Local cNumTMK	:= ""
Local lFinaliza	:= .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Finaliza os chamados cuja OS tenha sido encerrada³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cStatus == "E"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza itens do chamado associados a OS³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("SUD")
	DbSetOrder(5) //UD_FILIAL+UD_NUMOS+UD_CODIGO+UD_ITEM
	If DbSeek(cFilSUD + cNumOS)

		cNumTMK := SUD->UD_CODIGO

		While !SUD->(Eof())	 .AND. SUD->UD_FILIAL == cFilSUD .AND. SUD->UD_NUMOS == cNumOS
			RecLock("SUD",.F.)
			SUD->UD_STATUS := "2" //Encerrado
			MsUnLock()
			DbSkip()
		End
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se todos os itens foram encerrados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cNumTMK)
		SUD->(DbSetOrder(1))
		SUD->(DbSeek(cFilSUD+cNumTMK))

		While !SUD->(Eof()) .AND. SUD->UD_FILIAL == cFilSUD .AND. SUD->UD_CODIGO = cNumTMK .AND. lFinaliza
			If SUD->UD_STATUS <> "2"
				lFinaliza := .F.
			EndIf
			SUD->(DbSkip())
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Finaliza o chamado, caso todos os itens estejam encerrados³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lFinaliza
			DbSelectArea("SUC")
			DbSetOrder(1)
			If DbSeek(xFilial("SUC")+cNumTMK)
				RecLock("SUC",.F.)
				SUC->UC_STATUS := "3"
				MsUnLock()
			EndIf
		EndIf

	EndIf

EndIf

RestArea(aArea)

Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TecSegCli ºAutor  ³Totvs               º Data ³  15/04/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Opcao de pesquisa e multiselecao de segmentos              º±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºUso       ³ VENDAS/CRM                                                 º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TecSegCli()
Local cTitulo	:= ""
Local MvPar		:= ""
Local MvParDef	:= ""
Local cDesc		:= ""
Local aArea 	:= GetArea()
Local nReg	    := 0
Local nSit		:= 0
Local nX		:= 0
Local aSit		:= {}
Local aRetSX5	:= {}
Local nTamChave	:= TamSX3( "X5_CHAVE" )[1]

Private MvRet	:= AllTrim( ReadVar() )

If !IsBlind()
	aSit	 := {}
	cTitulo  := STR0021 //" Selecione os Segmentos "

	aRetSX5 := FWGetSX5("T3")
	For nX := 1 To Len(aRetSX5)
		cDesc := aRetSX5[nX,4]
	   	aAdd( aSit, cDesc )
	   	MvParDef += aRetSX5[nX,3]
	Next nX

	IF AdmOpcoes( @MvPar, cTitulo, aSit, MvParDef,,, .F., nTamChave, Len( aSit ), .T. )
		nSit := 1
		&MvRet	:= AllTrim( &MvRet )
		For nReg := 1 To len(mvpar) Step nTamChave  // Acumula as filiais num vetor
			If SubSTR(mvpar, nReg, nTamChave) <> Replicate("*",nTamChave) .AND. !Empty( SubSTR(mvpar, nReg, nTamChave) )
				&MvRet += SubStr( mvpar, nReg, nTamChave ) + ";"
			EndIf

			nSit++
		next

		If Empty( MvRet )
 	  		Help( " ",1, "TECXSEG",, STR0022, 1, 0 )	//"Por favor selecionar ao menos um registro"
		EndIF
	Endif
Else
	&MvRet := ""
EndIf

//Guarda numa variavel private o retorno da função
&MvRet := AllTrim( &MvRet )
cRetSX1 := &MvRet

RestArea( aArea )

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³TecTracker³ Autor ³ Totvs                 ³ Data ³16/05/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TecTracker( cCodigo )
Local aArea		:= GetArea()
Local aAreaAD1	:= AD1->( GetArea() )
Local aEnt		:= {}
Local cQuery	:= ""
Local cAliasQry	:= ""
Local lErro		:= .T.

Default cCodigo	:= ""

If !Empty( cCodigo )
	cQuery += "SELECT AD1_NROPOR FROM " + RetSqlName( "AD1" ) + " "
	cQuery += "WHERE "
	cQuery += "AD1_FILIAL='" + xFilial( "AD1" ) + "' AND "
	cQuery += "AD1_PROPOS='" + cCodigo          + "' AND "
	cQuery += "D_E_L_E_T_=''"
	cQuery := ChangeQuery( cQuery )

	cAliasQry := GetNextAlias()
	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasQry, .F., .T. )
	While ( cAliasQry )->( !Eof() )
		lErro := .F.

		aAdd( aEnt, { "AD1", ( cAliasQry )->AD1_NROPOR } )
		MaTrkShow( aEnt ) 		// Dispara o system tracker

		( cAliasQry )->( DbSkip() )
	End

	( cAliasQry )->( DbCloseArea() )
EndIf

If lErro
	Help( " ", 1, "NOTRACKER",, STR0023, 4 ) //"Não há propostas para este registro!"
EndIf

RestArea( aAreaAD1 )
RestArea( aArea )
Return .T.


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TxRelease   ºAutor  ³Vendas CRM          º Data ³  25/10/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verificação de Release,Campos e Tabelas para o GS           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGATEC	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TxRelease()
Local lRetorno	:= .F.

lRetorno := .T.

Return (lRetorno)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TxSeekCli   ºAutor  ³Vendas CRM          º Data ³  25/10/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Seek no cliente ou prospect da entidade de agendamento      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGATEC	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TxSeekCli(cNumOs,cCampoSA1,cCampoSUS,xVariavel,xDefVar)
Local lRet := .F.

xVariavel := xDefVar

If !Empty(cNumOS)
	DbSelectArea("AB7")
	DbSetOrder(1)
	DbSeek(xFilial("AB7")+cNumOs)
	DbSelectArea("SA1")
	DbSetOrder(1)
	DbSeek(xFilial("SA1")+AB7->AB7_CODCLI+AB7->AB7_LOJA)
	If !Empty(cCampoSA1) //Cliente
		xVariavel := SA1->&(cCampoSA1)
	EndIf
Else
	If !EMPTY(ABB->ABB_ENTIDA) .AND. !EMPTY(ABB->ABB_CHAVE)
		If TxSeekEnt(ABB->ABB_ENTIDA,ABB->ABB_CHAVE)
			If ABB->ENTIDA == "AAT"
				If AAT->AAT_ENTIDA == "1" .AND. !Empty(cCampoSA1) //Cliente
					DbSelectArea("SA1")
					DbSetOrder(1)
					If (lRet := DbSeek(xFilial("SA1")+AAT->AAT_CODENT+AAT->AAT_LOJENT))
						xVariavel := SA1->&(cCampoSA1)
					EndIf
				ElseIf AAT->AAT_ENTIDA == "2" .AND. !Empty(cCampoSUS) //Prospect
					DbSelectArea("SUS")
					DbSetOrder(1)
					If (lRet := DbSeek(xFilial("SUS")+AAT->AAT_CODENT+AAT->AAT_LOJENT))
						xVariavel := SUS->&(cCampoSUS)
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³TxGatHr   ºAutor  ³Vendas CRM          º Data ³  04/02/13 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Função do Gatilho dos campos ABB_HRINI e ABB_HRFIM   		º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ SIGATEC	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TxGatHr()

Local cRetorno 	:= ""
Local lNot500Auto	:= Type("lAt500Auto")=="U" .Or. !lAt500Auto	// Váriável com informação se é ou não rotina automática

If lNot500Auto
	cRetorno := AtTotHora(FwFldGet("ABB_DTINI"),FwFldGet("ABB_HRINI"),FwFldGet("ABB_DTFIM"),FwFldGet("ABB_HRFIM"))
Else
	cRetorno := AtTotHora(M->ABB_DTINI,M->ABB_HRINI,M->ABB_DTFIM,M->ABB_HRFIM)
EndIf

Return(cRetorno)

//------------------------------------------------------------------------------
/*/{Protheus.doc} TxDtValid
Calcula a data valida, verifica a configuração dos parametros
MV_SABFERI e MV_DOMFERI e varre a tabela 63 do SX5

@sample 	TxDtValid(dData,lProxima)

@param		ExpD 	Data para iniciar o calculo da função
@param		ExpL 	.T. posterga a data recebida para proximo dia util
					.F. retrocede data recebida para dia util anterior

@return		ExpD 	Data valida

@author	Serviços
@since		13/11/2014
@version	P118
/*/
//------------------------------------------------------------------------------
Function TxDtValid(dData,lProxima)
Local cAlias	:= Alias()
Local cData		:= "", cAno := ""
Local dFeriado	:= dDatabase, dDataAnt, nAno
Local cPulaDia	:= ''
Local aFeriados := {}
Local aRetSX5	:= {}
Local nX		:= 0

lProxima:= Iif(lProxima==Nil,.T.,lProxima)
If	Empty(aFeriados)

	If lSabFeri == NIL   //variavel static para ler apenas uma unica vez
		//MV_SABFERI = S, considera o sabado como feriado e MV_SABFERI = N, considera sabado com dia util
		If( Alltrim(SuperGetMV("MV_SABFERI", Nil, "S" )) $ "S" )
			lSabFeri := .T.
		Else
			lSabFeri := .F.
		EndIf
	Endif

	If lDomFeri == NIL  //variavel static para ler apenas uma unica vez
		//MV_DOMFERI = S ou vazio, considera o domingo como feriado e MV_DOMFERI = N, considera domingo com dia util
		If ( Alltrim(SuperGetMV("MV_DOMFERI", Nil, "S" )) $ "N" )
			lDomFeri := .F.
		Else
			lDomFeri := .T.
		EndIf
	EndIf

	If ( ValType( dData ) # "D" ) .Or. ( Empty( dData ) )
		dData := dDataBase
	Endif

	If ValType( lProxima ) # "L"
		lProxima := .T.
	Endif

	If aSX5Tab63 == NIL  //variavel static para ler tabela 63 uma unica vez
		aSX5Tab63 := {}
		aRetSX5 := FWGetSX5("63")
		For nX := 1 To Len(aRetSX5)
			aAdd(aSX5Tab63, { Subst(aRetSX5[nX,4],1,2), Subst(aRetSX5[nX,4],4,2), Subst(aRetSX5[nX,4],7,2) } )
		Next nX
	EndIf

	//Varre a tabela de feriados para verificar se a data
	//base ‚ feriado.
	For nX := 1 TO Len(aSX5Tab63)
		cData := aSX5Tab63[nX,1] + '/' + aSX5Tab63[nX,2]
		cAno  := aSX5Tab63[nX,3]
		If Empty(cAno)
			nAno := Year(dData)
			cAno := Subs(StrZero(nAno,4,0),3,2)
			dFeriado := ctod( cdata+"/"+cAno,"DDMMYY" )
			aAdd(aFeriados,dTos(dFeriado))
			nAno -= 1
			cAno := Subs(StrZero(nAno,4,0),3,2)
			dFeriado := ctod( cdata+"/"+cAno,"DDMMYY" )
			aAdd(aFeriados,dTos(dFeriado))
			nAno += 2
			cAno := Subs(StrZero(nAno,4,0),3,2)
			dFeriado := ctod( cdata+"/"+cAno,"DDMMYY" )
			aAdd(aFeriados,dTos(dFeriado))
		Else
			cData += ("/" + cAno)
			dFeriado := ctod( cdata,"DDMMYY" )
			aAdd(aFeriados,dTos(dFeriado))
		EndIf
	Next nX
	aFeriados:=aSort(aFeriados)
EndIf

If lSabFeri
	cPulaDia += "7"
EndIf
If lDomFeri
	cPulaDia += "1"
EndIf

dDataAnt := StoD("")
While dDataAnt != dData
	dDataAnt := dData
	If StrZero(Dow(dData),1,0) $ cPulaDia .Or. aScan(aFeriados,DTOS(dData)) > 0
		If lProxima
			dData++
		Else
			dData--
		EndIf
	EndIf
EndDo

If !Empty( cAlias )
	dbSelectArea( cAlias )
EndIf
Return( dData )


//-------------------------------------------------------------------
/*/{Protheus.doc} TxcBox
Função para formar as opções.

@author Kaique Schiller
@since 10/05/2016
@version P12.1.11
/*/
//-------------------------------------------------------------------
Function TxcBox(cCampo)
Local cRet 	   := ""
Default cCampo := ""

If cCampo == "TFI_APUMED"
	cRet := STR0026 //"1=Inicio(Locação)/Fim(Locação);2=Agenda.(Entrega)/Coleta(Equip);3=Inicio(Locação)/Coleta(Equip);4=Agenda.(Entrega)/Fim(Locação);5=Dt. Nota de Remessa do Item./Fim(Locação)"
Endif

Return cRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} TECXFPOPUP
Função para criar uma view flutuante em determinado modelo

@sample 	TECXFPOPUP(oModel,oSubView, cTitle, nOper, nRed, aButtons )

@param		oModel 		Modelo ativo no momento de chamar a janela
@param		oSubView	SubView criada para ser executada
@param		cTitle		Titulo da janela
@param		nOper		Operação que será executada, não pode ser inclusão
@param		nRed		Percentual de redução da janela
@param		aButtons	Botões que serão apresentados na janela

@return		Nil

@author		filipe.goncalves
@since		15/06/2016
@version	P12.1.12
/*/
//------------------------------------------------------------------------------
Function TECXFPOPUP(oModel, oSubView, cName, nOper, nRedu , aButtons, cTitle, bSetonOk, bSetOK )
Local aArea			:= GetArea()
Local aSaveLines	:= FwSaveRows()
Local oExecView		:= FWViewExec():New()

Default cTitle		:= STR0029 //"View Pop-Up"
Default	nOper		:= MODEL_OPERATION_UPDATE
Default nRedu 		:= 70
Default oSubView	:= FwViewActive()
Default oModel		:= FwModelActive()
Default aButtons	:= {	{.F.,Nil},;			//- Copiar
							{.F.,Nil},;			//- Recortar
							{.F.,Nil},;			//- Colar
							{.F.,Nil},;			//- Calculadora
							{.F.,Nil},;			//- Spool
							{.F.,Nil},;			//- Imprimir
							{.T.,"OK"},;			//- Confirmar
							{.F.,"Cancelar"},;	//- Cancelar
							{.F.,Nil},;			//- WalkThrough
							{.F.,Nil},;			//- Ambiente
							{.F.,Nil},;			//- Mashup
							{.F.,Nil},;			//- Help
							{.F.,Nil},;			//- Formulário HTML
							{.F.,Nil};				//- ECM
						}
Default bSetonOk    := {|| .T. }					
Default bSetOK		:= {|| .T. }

//Tratativa para se o usuário informar a operação como inclusão a janela sempre ser tratada como alteração, para não apagar as informações que foram colocadas
If nOper == MODEL_OPERATION_INSERT
	nOper := MODEL_OPERATION_UPDATE
EndIf

//Proteção para execução com View ativa.
If oModel != Nil .And. oModel:isActive()
	oExecView:setModel(oModel)
	oExecView:setView(oSubView)
	oExecView:setTitle(cTitle)
	oExecView:setOperation(nOper)
	oExecView:setReduction(nRedu)
	oExecView:setButtons(aButtons)
	oExecView:setCloseOnOK(bSetonOk)
	oExecView:setOK(bSetOK)
	oExecView:setModal(.F.)
	If ValType(oSubView) == "O"
		oSubView:SetAfterViewActivate( {|oSubView|oSubView:Refresh()} )
	EndIf
	If !isBlind()
		oExecView:openView(.F.)
	EndIf
EndIf

FWRestRows(aSaveLines)
RestArea(aArea)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} TCXINIBRW
Inicializador de browse para colocar o campo da STJ no Browse de Reserva e da Separação

@param		cCampo	Campo que será chamado o gatilho
@return		dData	Informação que será retornada

@author		filipe.goncalves
@since		17/06/2016
@version	P12.1.12
/*/
//------------------------------------------------------------------------------
Function TCXINIBRW(cCampo)
Local aArea			:= GetArea()
Local aSaveLines	:= FWSaveRows()
Local dData			:= STOD(" / / ")

If cCampo == "TEW_DTPFIM"
	AA3->(dbSetOrder(6))
	If AA3->(dbSeek(xFilial("AA3") + TEW->TEW_BAATD))
		If (STJ->( ColumnPos('TJ_DTMPFIM')) > 0 )
			STJ->(dbSetOrder(15))
			If STJ->(dbSeek(xFilial("STJ") + "B" + AA3->AA3_CODBEM))
				dData := STJ->TJ_DTMPFIM
			EndIf
		EndIf
	EndIf
Else
	TEW->(dbSetOrder(7))
	IF TEW->(dbSeek(xFilial("TEW") + TFI->TFI_COD + TFI->TFI_PRODUT))
		AA3->(dbSetOrder(6))
		If AA3->(dbSeek(xFilial("AA3") + TEW->TEW_BAATD))
			If (STJ->( ColumnPos('TJ_DTMPFIM')) > 0 )
				STJ->(dbSetOrder(15))
				If STJ->(dbSeek(xFilial("STJ") + "B" + AA3->AA3_CODBEM))
					dData := STJ->TJ_DTMPFIM
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

FWRestRows( aSaveLines )
RestArea(aArea)
Return dData

//=======================================================================================
/*/{Protheus.doc} TECBlView(oModel,lDelLine,lInsLine,lUpdLine)
Bloqueio de Grids pela View.

@param		oModel		- Objeto:	Modelo(Grid) que sofrerá alteração.
			lDelLine	- Lógico: Será possível excluir linha.
			lInsLine	- Lógico: Será possível inserir linha.
			lUpdLine	- Lógico: Será possível alterar linha.

@return	Nil
@author	Israel.Escorizza
@since		23/06/2016
@version 	12
/*/
//=======================================================================================
Function TECBlView(oModel,lDelLine,lInsLine,lUpdLine)
Default lDelLine := oModel:CanDeleteLine()
Default lInsLine := oModel:CanInsertLine()
Default lUpdLine := oModel:CanUpdateLine()

oModel:SetNoDeleteLine(!lDelLine)
oModel:SetNoInsertLine(!lInsLine)
oModel:SetNoUpdateLine(!lUpdLine)

Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ABQITEM
Ajuste na Tabela ABQ caso o ABQ_ITEM tenha aumentado.

@Return		Sem Retorno
@since		28/11/2019
@author		Augusto Albuquerque
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function ABQITEM()
Local aItem		:= {} 
Local cNewValor	:= ""
Local cAliasABQ	:= GetNextAlias()
Local cAliasABB := GetNextAlias()
Local nTamABQ	:= TamSX3("ABQ_ITEM")[1]
Local nTotal 	:= 0
Local nAlter	:= 0
Local nMeter
Local oDlg
Local oMeter
Local oSayMtr
Local cLen := ""

If TcGetDb() $ "ORACLE,POSTGRES,DB2,INFORMIX,MSSQL"

 	cLen := IIF(Trim(Upper(TcGetDb())) $ "ORACLE,POSTGRES,DB2,INFORMIX","LENGTH","LEN") + "(ABQ_ITEM) != " + Str(nTamABQ)
	cLen := "%"+cLen+"%"
	BeginSql alias cAliasABB
		SELECT COUNT(*) AS CNT
		  FROM %table:ABB% ABB
		 WHERE ABB.%NotDel% 
	EndSql
	
	nTotal := (cAliasABB)->CNT
	(cAliasABB)->(dbCloseArea())
	
	If TamSX3("ABQ_ITEM")[1] > 2 .AND. TamSX3("ABB_IDCFAL")[1] > 20 
		Begin Transaction
			BeginSql alias cAliasABQ
				SELECT ABQ.R_E_C_N_O_ RECNO, ABQ.ABQ_ITEM, ABQ.ABQ_CONTRT, ABQ.ABQ_ORIGEM
				  FROM %table:ABQ% ABQ
				 WHERE ABQ.%NotDel% 
				 	AND (ABQ.ABQ_ITEM LIKE "% %"
				 	OR %Exp:cLen% )
				 ORDER BY ABQ.ABQ_CONTRT, ABQ.ABQ_ITEM, ABQ.ABQ_ORIGEM
			EndSql
			nTamABQ := 0
			While !(cAliasABQ)->(Eof())
				nTamABQ	:= Len( AllTrim((cAliasABQ)->ABQ_ITEM))
				If nTamABQ <> TamSX3("ABQ_ITEM")[1]
					cNewValor := Replicate("0", TamSX3("ABQ_ITEM")[1] - nTamABQ)
					AADD(aItem, {(cAliasABQ)->ABQ_CONTRT,;
								 (cAliasABQ)->ABQ_ITEM,;
								 (cAliasABQ)->ABQ_ORIGEM,;
								 Nil})
					DBSelectArea("ABQ")
					ABQ->(DBGoTo((cAliasABQ)->RECNO))
					RecLock("ABQ", .F.)
						ABQ->ABQ_ITEM := cNewValor + AllTrim((cAliasABQ)->ABQ_ITEM)
					(cAliasABQ)->(MsUnLock())
					aItem[Len(aItem)][4] := ABQ->ABQ_ITEM 
				EndIf
				(cAliasABQ)->(dBSkip())
			EndDo
			If !Empty(aItem)
				If IsBlind()
					ABQProce( aItem )
				Else 
					DEFINE MSDIALOG oDlg FROM 0,0 TO 5,60 TITLE STR0030 // "Configurando o Gestão de Serviços"
						nMeter := 0
						oSayMtr := tSay():New(10,10,{||STR0031},oDlg,,,,,,.T.,,,220,20) // "Essa operação pode demorar varios minutos..."
						oMeter  := tMeter():New(20,10,{|u|if(Pcount()>0,nMeter:=u,nMeter)},nTotal,oDlg,220,10,,.T.) // cria a régua
						
					ACTIVATE MSDIALOG oDlg CENTERED ON INIT ( nAlter := ABQProce( aItem, oDlg, oMeter, oSayMtr, nTotal ) )
				EndIf
			EndIf
			(cAliasABQ)->(dbCloseArea())
		End Transaction
	EndIf
EndIf
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} ABQProce
Ajuste na Tabela ABB caso o ABQ_ITEM tenha aumentado, e atualiza o ABB_IDCFAL
com o valor certo do ABQ_ITEM.

@Return		nAlter - numero de registros alterados.
@since		28/11/2019
@author		Augusto Albuquerque
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function ABQProce( aItem, oDlg, oMeter, oSayMtr, nTotal )
Local aIDCFAL	:= { Nil, Nil, Nil}
Local cAliasABB	:= GetNextAlias()
Local nABQ		:= Len(aItem)
Local nReg		:= 0
Local nAlter 	:= 0
Local nAux
Local nX

Default oDlg	:= Nil
Default oMeter	:= Nil
Default oSayMtr	:= Nil
Default nTotal	:= 0

DbSelectArea("ABB")
DbGoTop()

While !(ABB->(Eof()))
	cIdcFal := ABB->ABB_IDCFAL
	cidCTR := LEFT(cIdcFal, TamSX3("ABQ_CONTRT")[1] )
	cIdSemCTR := SUBSTR(cIdcFal, TamSX3("ABQ_CONTRT")[1]+1)
	If (nAux := ASCAN(aItem, {|a| a[1] == cidCTR .AND. Alltrim(a[2]) $ cIdSemCTR .AND. a[3] $ cIdSemCTR})) > 0  
		RecLock("ABB", .F.)
			ABB->ABB_IDCFAL := aItem[nAux][1] + aItem[nAux][4] + aItem[nAux][3]
		ABB->(MsUnLock())
		nAlter++
	EndIf
	
	If !IsBlind()
		oMeter:Set(++nReg)
		oSayMtr:SetText(STR0032 + cValToChar(nReg) + STR0033 + cValToChar(nTotal)) // "Processando: " ## " de " 
		oMeter:Refresh()
		oSayMtr:CtrlRefresh()
		SysRefresh()
	EndIf

	ABB->(DbSkip())
EndDo

If !IsBlind()
	oDlg:End()
EndIf

Return nAlter
//--------------------------------------------------------------------
/*/{Protheus.doc} TecXSemOs
@author Mateus Boiani
@since 15/07/2020

@description Executa o ajuste das ABBs dentro de um Progress bar
/*/
//--------------------------------------------------------------------
Static Function TecXSemOs()

Local aArea := GetArea()
Local lBenenv
Local lAdienv
Local lItapur
Local lMPonto
Local cSql := ""
Local cAliasTmp := ""
Local cSpcItaPur := SPACE(TamSX3("AB9_ITAPUR")[1])
Local nTotal := 0
Local oDlg := nil
Local oSayMtr := nil
Local nMeter := 0

DbSelectArea("ABB")

lBenenv := ABB->(ColumnPos("ABB_BENENV")) > 0
lAdienv := ABB->(ColumnPos("ABB_ADIENV")) > 0
lItapur := ABB->(ColumnPos("ABB_ITAPUR")) > 0
lMPonto := ABB->(ColumnPos("ABB_MPONTO")) > 0

If lBenenv .AND. lAdienv .AND. lItapur .AND. lMPonto
	cSql := " SELECT ABB.R_E_C_N_O_ REC "
	cSql += ", ABB.ABB_BENENV "
	cSql += ", ABB.ABB_ADIENV "
	cSql += ", ABB.ABB_ITAPUR "
	cSql += ", ABB.ABB_MPONTO "
	cSql += ", AB9.AB9_BENENV "
	cSql += ", AB9.AB9_ADIENV "
	cSql += ", AB9.AB9_ITAPUR "
	cSql += ", AB9.AB9_MPONTO "
	cSql += " FROM " + RetSqlName( "ABB" ) + " ABB "
	cSql += " INNER JOIN " + RetSqlName( "AB9" ) + " AB9 ON "
	cSql += FWJoinFilial("ABB" , "AB9" , "ABB", "AB9", .T.) + " AND "
	cSql += " AB9.AB9_ATAUT = ABB.ABB_CODIGO AND AB9.D_E_L_E_T_ = ' ' "
	cSql += " AND AB9.AB9_CODTEC = ABB.ABB_CODTEC "
	cSql += " WHERE "
	cSql += " ABB.D_E_L_E_T_ = ' ' AND "
	cSql += " ( "
		cSql += " (ABB.ABB_BENENV = 'F' AND AB9.AB9_BENENV = 'T' ) OR "
		cSql += " (ABB.ABB_ADIENV = 'F' AND AB9.AB9_ADIENV = 'T' ) OR "
		cSql += " (ABB.ABB_ITAPUR = '"+cSpcItaPur+"' AND AB9.AB9_ITAPUR != '" + cSpcItaPur + "' ) OR "
		cSql += " (ABB.ABB_MPONTO = 'F' AND AB9.AB9_MPONTO = 'T' ) "
	cSql += " ) "
	cSql += " AND ABB.ABB_CHEGOU = 'S' AND ABB.ABB_ATENDE = '1' "
	cSql := ChangeQuery(cSql)
	cAliasTmp := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry(,,cSql), cAliasTmp, .T., .T.)

	If !(cAliasTmp)->(EOF())
		If isBlind()
			ABBsemOS(cAliasTmp)
		Else
			While (cAliasTmp)->(!EOF())
				nTotal++
				(cAliasTmp)->(DbSkip())
			End
			(cAliasTmp)->(DbGoTop())

			DEFINE MSDIALOG oDlg FROM 0,0 TO 5,60 TITLE STR0034 //"Verificando registros..."
				oSayMtr := tSay():New(10,10,{||STR0035},oDlg,,,,,,.T.,,,220,20) //"Processando, aguarde..."
				oMeter  := tMeter():New(20,10,{|u|if(Pcount()>0,nMeter:=u,nMeter)},nTotal,oDlg,220,10,,.T.)
				
			ACTIVATE MSDIALOG oDlg CENTERED ON INIT (ABBsemOS(cAliasTmp,@oDlg,@oMeter))

		EndIf
	EndIf
	(cAliasTmp)->(DbCloseArea())	
EndIf

RestArea(aArea)

Return
//--------------------------------------------------------------------
/*/{Protheus.doc} ABBsemOS
@author Mateus Boiani
@since 15/07/2020

@description Migra os registros para o modo Sem O.S.
/*/
//--------------------------------------------------------------------
Static Function ABBsemOS(cAliasTmp,oDlg,oMeter)
Local lLoadBar := .F.
Local nCount := 0

Default oDlg := nil
Default oMeter := nil

lLoadBar := !isBlind() .AND. oMeter != nil .AND. oDlg != nil

BEGIN TRANSACTION
	While !(cAliasTmp)->(EOF())
		ABB->(DbGoTo( (cAliasTmp)->REC ))
		RecLock("ABB", .F.)
			If (cAliasTmp)->AB9_BENENV == 'T' .AND. (cAliasTmp)->ABB_BENENV == 'F'
				ABB->ABB_BENENV := .T.
			EndIf
			If (cAliasTmp)->AB9_ADIENV == 'T' .AND. (cAliasTmp)->ABB_ADIENV == 'F'
				ABB->ABB_ADIENV := .T.
			EndIf
			If !EMPTY((cAliasTmp)->AB9_ITAPUR) .AND. EMPTY((cAliasTmp)->ABB_ITAPUR)
				ABB->ABB_ITAPUR := (cAliasTmp)->AB9_ITAPUR
			EndIf
			If (cAliasTmp)->AB9_MPONTO == 'T' .AND. (cAliasTmp)->ABB_MPONTO == 'F'
				ABB->ABB_MPONTO := .T.
			EndIf
		ABB->(MsUnLock())
		(cAliasTmp)->(DbSkip())
		If lLoadBar
			oMeter:Set(++nCount)
			oMeter:Refresh()
		EndIf
	End
END TRANSACTION

If lLoadBar
	oDlg:End()
EndIf

Return


//--------------------------------------------------------------------
/*/{Protheus.doc} AtDscOrc
@author Alexandre Alves
@since 18/07/2020

@description Resgata percentuais de desconto do cabeçalho do Orçamento.
/*/
//--------------------------------------------------------------------
Function TecAtDsOrc(cNumOs) 

Local aArea   := GetArea()
Local cArqDsc := GetNextAlias()
Local aRetDsc := {0,0,0,0}

BeginSql alias cArqDsc
	SELECT AB4.AB4_FILIAL
		 , AB4.AB4_NUMORC
		 , AB4.AB4_NUMOS
		 , AB3.AB3_DESC1
		 , AB3.AB3_DESC2
		 , AB3.AB3_DESC3
		 , AB3.AB3_DESC4
	FROM %table:AB4% AB4
	INNER JOIN %table:AB3% AB3 ON (AB3_FILIAL = AB4_FILIAL AND AB3_NUMORC = AB4_NUMORC)
	WHERE AB4.%notDel% AND
		  AB3.%notDel% AND
		  AB4.AB4_NUMOS = %Exp:cNumOs% AND
		  AB4.AB4_NUMORC <> %Exp:''%
EndSql

dbSelectArea(cArqDsc)
If (cArqDsc)->(!Eof())
	aRetDsc[1] := (cArqDsc)->AB3_DESC1
	aRetDsc[2] := (cArqDsc)->AB3_DESC2
	aRetDsc[3] := (cArqDsc)->AB3_DESC3
	aRetDsc[4] := (cArqDsc)->AB3_DESC4
EndIf

(cArqDsc)->(dbCloseArea())

RestArea(aArea)

Return(aRetDsc)

//--------------------------------------------------------------------
/*/{Protheus.doc} AtExcOSPlan
@author Luiz Gabriel
@since 22/06/2021

@description Exclui a amarração da O.S com o Plano de manutenção
/*/
//--------------------------------------------------------------------
Function AtExcOSPlan(cCodOs)
Local cQry := GetNextAlias()

BeginSQL Alias cQry
	SELECT ABE.R_E_C_N_O_ ABERECNO
	  	FROM %Table:ABE% ABE
	 	WHERE ABE.ABE_FILIAL = %xFilial:ABE%
			AND ABE.ABE_NUMOS = %Exp:cCodOs%
	   		AND ABE.%NotDel%
EndSQL

While (cQry)->( !Eof() )
	// Posiciona nos itens que sofrerão as atualizações
	ABE->( DbGoTo( (cQry)->ABERECNO ) )
	ABE->(RecLock("ABE",.F.))
		ABE->ABE_NUMOS := ''
	ABE->(MsUnlock())
	(cQry)->(DbSkip())
EndDo

(cQry)->(DbCloseArea())
ABE->( DbGoTo(0) )

Return


//--------------------------------------------------------------------
/*/{Protheus.doc} atStOS
@author Diego Bezerra
@param cTipo, string, código da situaç?o da ordem de serviço
@param cAlias, string, pode ser 'AB7' ou 'AB6' (Item da ordem de serviço ou ordem de serviço, respectivamente)
@description Retorna o código do status no app, baseado na situaç?o da ordem de serviço, no Protheus.
/*/
//--------------------------------------------------------------------
Function atStOS(cTipo, cAlias)

Local cStatus := '2'

If (cAlias == 'AB7')
	Do Case
		Case ( cTipo $ "245" ) // Encerrado
			cStatus := '3'
		Case ( cTipo == "3" ) // Em Atendimento
			cStatus := '2'
		Case ( cTipo == '1')
			cStatus := '1'
	EndCase
Else 
	Do Case
		Case ( cTipo == "A")
			cStatus := "1"
		Case ( cTipo $ "BE" )
			cStatus := "3"
	EndCase
EndIf
Return cStatus
