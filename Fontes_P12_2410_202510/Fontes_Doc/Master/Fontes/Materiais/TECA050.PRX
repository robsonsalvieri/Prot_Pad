#INCLUDE "TECA050.CH"
#INCLUDE "PROTHEUS.CH"
#DEFINE MAXITENS 500
/*/


Ŀ
Program   TECA050    Autor  Eduardo Riera          Data  25.09.98 
Ĵ
Descrio  Manutencao do FAQ ( Frequenty Asked Question)              
Ĵ
Retorno    Nenhum                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function TECA050()

Private cCadastro := STR0001 //"FAQ - Frequently Asked Question"
Private aRotina := MenuDef()

//Ŀ
// Endereca a funcao de BROWSE                                  
//
dbSelectArea("AAI")
dbSetOrder(1)
mBrowse( 6, 1,22,75,"AAI")

dbSelectArea("AAI")
dbSetOrder(1)

Return(Nil)

/*/

Ŀ
Funo    MenuDef    Autor  Conrado Q. Gomes       Data  08.12.06 
Ĵ
Descrio  Definio do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA050                                                    
ٱ


/*/
Static Function MenuDef()  
//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//    6 - Alteracao sem inclusao de registro                    
//
Local aRotina	:=	{	{ STR0002	,"AxPesqui"  	,0	,1	,0	,.F.	}	,;	//"Pesquisar"
						{ STR0003	,"At050Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
						{ STR0004	,"At050Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
						{ STR0005	,"At050Alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
						{ STR0006	,"At050Delet"	,0	,5	,0	,.T.	}	}	//"Excluir"           

Local aRotinaMnu := {}							

If ExistBlock("AT050MNU")
	aRotinaMnu := ExecBlock("AT050MNU",.F.,.F.,{aRotina})
	If ValType(aRotinaMnu) == "A"
		aRotina := aClone(aRotinaMnu)
    EndIf
EndIf
							
Return(aRotina)

/*/


Ŀ
Funcao    At050Inclu Autor  Eduardo Riera          Data 25.09.98  
Ĵ
Descrio  Inclusao  do FAQ                                           
Ĵ
Retorno                                                               
Ĵ
Parametros ExpC1 : Alias                                              
           ExpN1 : Registro                                           
           ExpN2 : Opcao                                              
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
19/12/2006 Conrado Q.    Bops 115730: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
09/02/2007 Cleber M.     Bops 115730: Retirada a variav. Private N.  
ٱ


/*/
Function At050Inclu(cAlias,nReg,nOpc)

Local lGravou		:= .F.
Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local aGet			:= {}
Local nSaveSX8		:= GetSX8Len()
Local aNoFields		:= {}			// Campos que no sero adicionados ao aHeader
Local aCoors         := FWGetDialogSize( oMainWnd )

Private aCols		:= {}
Private aHeader		:= {}
Private CAT050FAQ	:= CriaVar("AAI_CODFAQ")
Private CAT050GRUP	:= CriaVar("AAI_GRUPO")
Private CAT050PROD	:= CriaVar("AAI_CODPRO")
Private aSize		:= MsAdvSize()

//Ŀ
//Montagem do Array do Cabecalho                                          
//
Aadd(aGet,{FWX3Titulo("AAI_CODFAQ"),PesqPict("AAI","AAI_CODFAQ"),})
Aadd(aGet,{FWX3Titulo("AAI_GRUPO" ),PesqPict("AAI","AAI_GRUPO" ),"SBM"})
Aadd(aGet,{FWX3Titulo("AAI_CODPRO"),PesqPict("AAI","AAI_CODPRO"),"SB1"})
 
//Ŀ
//Montagem aHeader, aCols
//
aNoFields := {	"AAI_CODPRO"	,"AAI_CODFAQ"	,"AAI_GRUPO"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AAI"		,1				,/*cSeek*/		,;
					/*{||cWhile}*/	,{|| .T. }	,aNoFields		,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
Endif

aCols[1][GdFieldPos("AAI_ITEM")] := "01"

DEFINE MSDIALOG oDlg TITLE cCadastro  FROM aCoors[1],aCoors[2] TO aCoors[3],aCoors[4] PIXEL

@ 035,010 SAY aGet[1][1] 	SIZE 040,011 	OF oDlg PIXEL 
@ 035,035 MSGET CAT050FAQ 	SIZE 040,011 	OF oDlg PIXEL ;		
				PICTURE "@!" ;
	         	VALID CheckSX3('AAI_CODFAQ') ;
				WHEN VisualSX3('AAI_CODFAQ')
				
@ 035,115 SAY aGet[2][1] 	SIZE 040,011 	OF oDlg PIXEL				
@ 035,140 MSGET CAT050GRUP SIZE 040,011 	OF oDlg PIXEL ;
				PICTURE "@!" ;
				VALID CheckSX3('AAI_GRUPO') ;
				WHEN VisualSX3('AAI_GRUPO') ;
				F3 aGet[2][3]
				
@ 035,210 SAY aGet[3][1] 	SIZE 040,011 	OF oDlg PIXEL				
@ 035,250 MSGET CAT050PROD SIZE 060,011 	OF oDlg PIXEL ;
				PICTURE "@!" ;
				VALID CheckSX3('AAI_CODPRO') ;
				WHEN VisualSX3('AAI_CODPRO') ;
				F3 aGet[3][3]

oGetd := MsGetDados():New(054,005,aSize[4],aSize[3],nOpc,"AT050LINOK","AT050TUDOK","+AAI_ITEM",.T.)

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})

If ( nOpcA == 1 )
	Begin Transaction
		lGravou := aT050Grava(1)
		If ( lGravou )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			EndDo
		EndIf
	End Transaction
EndIf

While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
EndDo

Return(lGravou)

/*/


Ŀ
Funcao    At050Alter Autor  Eduardo Riera          Data 25.09.98  
Ĵ
Descrio  Alteracao do FAQ                                           
Ĵ
Retorno                                                               
Ĵ
Parametros ExpC1 : Alias                                              
           ExpN1 : Registro                                           
           ExpN2 : Opcao                                              
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
19/12/2006 Conrado Q.    Bops 115730: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


/*/

Function At050Alter(cAlias,nReg,nOpc)

Local lGravou		:= .F.
Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local aGet			:= {}
Local nSaveSX8		:= GetSX8Len()
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aNoFields		:= ""			// Campos que no sero adicionados ao aHeader
Local aTravas		:= {}			// Campos que foram travados
Local lTravas		:= .T.			// Se todos os campos foram travados com sucesso
Local aCoors         := FWGetDialogSize( oMainWnd )

Private aCols		:= {}
Private aHeader		:= {}
Private CAT050FAQ	:= AAI->AAI_CODFAQ
Private CAT050GRUP	:= AAI->AAI_GRUPO
Private CAT050PROD	:= AAI->AAI_CODPRO
Private aSize		:= MsAdvSize()

//Ŀ
//Montagem do Array do Cabecalho                                          
//
Aadd(aGet,{FWX3Titulo("AAI_CODFAQ"),PesqPict("AAI","AAI_CODFAQ"),})
Aadd(aGet,{FWX3Titulo("AAI_GRUPO" ),PesqPict("AAI","AAI_GRUPO" ),"SBM"})
Aadd(aGet,{FWX3Titulo("AAI_CODPRO"),PesqPict("AAI","AAI_CODPRO"),"SB1"})

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial("AAI")+M->CAT050FAQ
cWhile	:= "AAI->AAI_FILIAL+AAI->AAI_CODFAQ"
aNoFields := {	"AAI_CODPRO"	,"AAI_CODFAQ"	,"AAI_GRUPO"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	If !FillGetDados(	nOpc			,"AAI"		,1				,cSeek									,;
		  				{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/							,; 
						/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/								,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AAI", aTravas) }	)
		lTravas := .F.
	Endif
Endif

If Empty(aCols[1][GdFieldPos("AAI_ITEM")])
	aCols[1][GdFieldPos("AAI_ITEM")] := "01"
Endif
        
If ( lTravas )
	DEFINE MSDIALOG oDlg TITLE cCadastro  FROM aCoors[1],aCoors[2] TO aCoors[3],aCoors[4] PIXEL
	
	@ 035,010 SAY aGet[1][1] 	SIZE 040,011 	OF oDlg PIXEL 
	@ 035,035 MSGET CAT050FAQ 	SIZE 040,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_CODFAQ') ;
					WHEN .F.
					
	@ 035,115 SAY aGet[2][1] 	SIZE 040,011 	OF oDlg PIXEL				
	@ 035,140 MSGET CAT050GRUP SIZE 040,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_GRUPO') ;
					WHEN VisualSX3('AAI_GRUPO') ;
					F3 aGet[2][3]
					
	@ 035,210 SAY aGet[3][1] 	SIZE 040,011 	OF oDlg PIXEL				
	@ 035,250 MSGET CAT050PROD SIZE 060,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_CODPRO') ;
					WHEN VisualSX3('AAI_CODPRO') ;
					F3 aGet[3][3]
					
	oGetd:=MsGetDados():New(054,005,aSize[4],aSize[3],nOpc,"AT050LINOK","AT050TUDOK","+AAI_ITEM",.T.)
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})
	
	If ( nOpcA == 1 )
		Begin Transaction
			lGravou := aT050Grava(2)
			If ( lGravou )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSx8()
				EndDo
			EndIf
		End Transaction
	EndIf
EndIf

While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
EndDo

//Ŀ
//Efetua o destravamento dos registros travados.
//
AtDestravaReg( aTravas )

Return(lGravou)

/*/


Ŀ
Funcao    At050Delet Autor  Eduardo Riera          Data 25.09.98  
Ĵ
Descrio  Exclusao do FAQ                                            
Ĵ
Retorno                                                               
Ĵ
Parametros ExpC1 : Alias                                              
           ExpN1 : Registro                                           
           ExpN2 : Opcao                                              
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
19/12/2006 Conrado Q.    Bops 115730: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


/*/

Function At050Delet(cAlias,nReg,nOpc)

Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local aGet			:= {}
Local nSaveSX8		:= GetSX8Len()
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aNoFields		:= ""			// Campos que no sero adicionados ao aHeader
Local aTravas		:= {}			// Campos que foram travados
Local lTravas		:= .T.			// Se todos os campos foram travados com sucesso
Local aCoors         := FWGetDialogSize( oMainWnd )

Private aCols		:= {}
Private aHeader		:= {}
Private CAT050FAQ	:= AAI->AAI_CODFAQ
Private CAT050GRUP	:= AAI->AAI_GRUPO
Private CAT050PROD	:= AAI->AAI_CODPRO
Private aSize		:= MsAdvSize()

//Ŀ
//Montagem do Array do Cabecalho                                          
//
Aadd(aGet,{FWX3Titulo("AAI_CODFAQ"),PesqPict("AAI","AAI_CODFAQ"),})
Aadd(aGet,{FWX3Titulo("AAI_GRUPO" ),PesqPict("AAI","AAI_GRUPO" ),"SBM"})
Aadd(aGet,{FWX3Titulo("AAI_CODPRO"),PesqPict("AAI","AAI_CODPRO"),"SB1"})

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial("AAI")+M->CAT050FAQ
cWhile	:= "AAI->AAI_FILIAL+AAI->AAI_CODFAQ"
aNoFields := {	"AAI_CODPRO"	,"AAI_CODFAQ"	,"AAI_GRUPO"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	If !FillGetDados(	nOpc			,"AAI"		,1				,cSeek									,;
			  			{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/							,; 
						/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/								,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AAI", aTravas) }	)					
		lTravas := .F.
	Endif
Endif

If ( lTravas )
	DEFINE MSDIALOG oDlg TITLE cCadastro  FROM aCoors[1],aCoors[2] TO aCoors[3],aCoors[4] PIXEL
	
	@ 035,010 SAY aGet[1][1] 	SIZE 040,011 	OF oDlg PIXEL
	@ 035,035 MSGET CAT050FAQ 	SIZE 040,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_CODFAQ') ;
					WHEN .F.
					
	@ 035,115 SAY aGet[2][1] 	SIZE 040,011 	OF oDlg PIXEL				
	@ 035,140 MSGET CAT050GRUP SIZE 040,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_GRUPO') ;
					WHEN .F.
					
	@ 035,210 SAY aGet[3][1] 	SIZE 040,011 	OF oDlg PIXEL				
	@ 035,250 MSGET CAT050PROD SIZE 060,011 	OF oDlg PIXEL ;
					PICTURE "@!" ;
					VALID CheckSX3('AAI_CODPRO') ;
					WHEN .F.
					
	oGetd:=MsGetDados():New(054,005,aSize[4],aSize[3],nOpc,"AllwaysTrue","AllwaysTrue","",.F.)
	
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()})
	
	If ( nOpcA == 1 )
		Begin Transaction
			aT050Grava(3)
	        EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			EndDo
		End Transaction
	EndIf
Endif
	
While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
EndDo

//Ŀ
//Efetua o destravamento dos registros travados.
//
AtDestravaReg( aTravas )

Return(.T.)

/*/


Ŀ
Funcao    At050Visua Autor  Eduardo Riera          Data 25.09.98  
Ĵ
Descrio  Visualizacao do FAQ                                        
Ĵ
Retorno                                                               
Ĵ
Parametros ExpC1 : Alias                                              
           ExpN1 : Registro                                           
           ExpN2 : Opcao                                              
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
19/12/2006 Conrado Q.    Bops 115730: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


/*/

Function At050Visua(cAlias,nReg,nOpc)

Local nOpcA			:= 0
Local oDlg			:= Nil
Local oGetD			:= Nil
Local aGet			:= {}
Local cSeek			:= ""			// Seek para montagem da aCols
Local cWhile		:= ""			// While para montagem da aHeader
Local aNoFields		:= ""			// Campos que no sero adicionados ao aHeader
Local aCoors         := FWGetDialogSize( oMainWnd )

Private aCols		:= {}
Private aHeader		:= {}
Private CAT050FAQ	:= AAI->AAI_CODFAQ
Private CAT050GRUP	:= AAI->AAI_GRUPO
Private CAT050PROD	:= AAI->AAI_CODPRO
Private aSize		:= MsAdvSize()

//Ŀ
//Montagem do Array do Cabecalho                                          
//
Aadd(aGet,{FWX3Titulo("AAI_CODFAQ"),PesqPict("AAI","AAI_CODFAQ"),})
Aadd(aGet,{FWX3Titulo("AAI_GRUPO" ),PesqPict("AAI","AAI_GRUPO" ),"SBM"})
Aadd(aGet,{FWX3Titulo("AAI_CODPRO"),PesqPict("AAI","AAI_CODPRO"),"SB1"})

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial("AAI")+M->CAT050FAQ
cWhile	:= "AAI->AAI_FILIAL+AAI->AAI_CODFAQ"
aNoFields := {	"AAI_CODPRO"	,"AAI_CODFAQ"	,"AAI_GRUPO"	}

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AAI"		,1				,cSeek			,;
					{|| &cWhile }	,{|| .T. }	,aNoFields		,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		)
Endif

DEFINE MSDIALOG oDlg TITLE cCadastro  FROM aCoors[1],aCoors[2] TO aCoors[3],aCoors[4] PIXEL

@ 035,010 SAY aGet[1][1] 	SIZE 040,011 	OF oDlg PIXEL 
@ 035,035 MSGET CAT050FAQ 	SIZE 040,011 	OF oDlg PIXEL ;
				PICTURE "@!" ;
				VALID CheckSX3('AAI_CODFAQ') ;
				WHEN .F.
				
@ 035,115 SAY aGet[2][1] 	SIZE 040,011 	OF oDlg PIXEL				
@ 035,140 MSGET CAT050GRUP SIZE 040,011 	OF oDlg PIXEL ;
				PICTURE "@!" ;
				VALID CheckSX3('AAI_GRUPO') ;
				WHEN .F. ;
				F3 aGet[2][3]
				
@ 035,210 SAY aGet[3][1] 	SIZE 040,011 	OF oDlg PIXEL				
@ 035,250 MSGET CAT050PROD SIZE 060,011 	OF oDlg PIXEL ;
				PICTURE "@!" ;
				VALID CheckSX3('AAI_CODPRO') ;
				WHEN .F. ;
				F3 aGet[3][3]
				
oGetd:=MsGetDados():New(054,005,aSize[4],aSize[3],nOpc,"AllwaysTrue","AllwaysTrue","",.F.)

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})

Return(.T.)


/*/


Ŀ
Funcao    At050Grava Autor  Eduardo Riera          Data  25.09.98 
Ĵ
Descrio  Gravacao do FAQ                                            
Ĵ
Retorno    Indica se a gravacao foi efetuada ou nao                   
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function At050Grava(nOpcao)

Local nCntFor := 0
Local nUsado  := Len(aHeader)
Local lGravou := .F.
Local nCntFor2:= 0
Local nPosItem:= aScan(aHeader,{|x| AllTrim(x[2])=="AAI_ITEM" })
Local cMemo	  := ""

Do Case
	Case ( nOpcao <> 3 )
		For nCntFor := 1 To Len(aCols)
			dbSelectArea("AAI")
			dbSetOrder(1)
			If ( dbSeek(xFilial("AAI")+M->CAT050FAQ+aCols[nCntFor][nPosItem]) )
				RecLock("AAI")
			Else
				RecLock("AAI",.T.)
			EndIf
			If ( !aCols[nCntFor][nUsado+1] )
				For nCntFor2 := 1 To nUsado
					If ( aHeader[nCntFor2][10] <> "V" )
						AAI->(FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2]))
					EndIf
					If ( AllTrim(aHeader[nCntFor2][2])=="AAI_MEMO2" )
						cMemo := aCols[nCntFor][nCntFor2]
					EndIf
				Next nCntFor2
				If (!Empty(M->CAT050PROD) )
					dbSelectArea("SB1")
					dbSetOrder(1)
					dbSeek(xFilial("SB1")+M->CAT050PROD)
					M->CAT050GRUP := SB1->B1_GRUPO
				EndIf
				
				AAI->AAI_FILIAL 	:= xFilial("AAI")
				AAI->AAI_CODFAQ 	:= M->CAT050FAQ
				AAI->AAI_GRUPO		:= M->CAT050GRUP
				AAI->AAI_CODPRO		:= M->CAT050PROD
				lGravou := .T.
				If ( !Empty(cMemo) )
					MSMM(AAI->AAI_MEMO,,,cMemo,1,,,"AAI","AAI_MEMO")
				EndIf
			Else
				dbDelete()
			EndIf
		Next nCntFor
	OtherWise
		For nCntFor := 1 To Len(aCols)
			dbSelectArea("AAI")
			dbSetOrder(1)
			If ( dbSeek(xFilial("AAI")+M->CAT050FAQ+aCols[nCntFor][nPosItem]) )
				RecLock("AAI")
				dbDelete()
			EndIf
		Next
EndCase
Return(lGravou)

/*/


Ŀ
Funcao    At050LinOk Autor  Eduardo Riera          Data  25.09.98 
Ĵ
Descrio  Validacao da LinhaOk                                       
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At050LinOk()

Local nUsado 	:= Len(aHeader)
Local lRetorno := .T.
Local nPosPrb  := aScan(aHeader,{|x| AllTrim(x[2])=="AAI_CODPRB"})
Local nPosSoluc:= aScan(aHeader,{|x| AllTrim(x[2])=="AAI_SOLUC"})

If ( !aCols[n][nUsado+1] )
	If ( (Empty(aCols[n][nPosPrb]) .And. Len(aCols)==1) .Or.;
			Empty(aCols[n][nPosSoluc]) )
		Help(" ",1,"AT050LIN01")
		lRetorno := .F.
	EndIf
EndIf

Return(lRetorno)

/*/


Ŀ
Funcao    At050TudOk Autor  Eduardo Riera          Data  25.09.98 
Ĵ
Descrio  Validacao da TudoOk                                        
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At050TudOk()
Return(.T.)
