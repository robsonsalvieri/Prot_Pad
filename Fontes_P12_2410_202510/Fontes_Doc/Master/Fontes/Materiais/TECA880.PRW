#INCLUDE "TECA880.CH"
#INCLUDE "TOTVS.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE itARMA     "1"
#DEFINE itCOLETE   "2"
#DEFINE itMUNICAO  "3"

#DEFINE eoLOCINTER   '1'
#DEFINE eoLOCATEND   '2'
#DEFINE edCLIENTE    '1'
#DEFINE edEMPRESA    '2'

#DEFINE stAGENDADA   '1'
#DEFINE stEFETIVADA  '2'
#DEFINE stANULADA    '3'

#DEFINE mtIMPLANTACAO    '1'
#DEFINE mtMANUTENCAO     '2'
#DEFINE mtDESCARTE       '3'
#DEFINE mtREFORCO        '4'
#DEFINE mtENCERRAMENTO   '5'

Static cTab		   := ""
Static aCmpBco	   := {} //Array com os locais de alocação da munição
Static oModel      := Nil
Static oView       := Nil
Static cArmaLocal  := ""
Static cLocTFF     := ""
Static cCodigoTFL  := ""
Static cCodCOL     := ""
Static cCodMun     := ""
Static cCodSB1     := ""

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} TECA880	

Cadastro de Movimentação - TFQ

@author 	Serviços
@since 		20/08/13
@version 	P11 R9
@param		cFilDefault, Caracter, código de filtro para restrição forçada da exibição dos dados na tabela/browse
/*/
//----------------------------------------------------------------------------------------------------------------------
Function TECA880(cFilDefault)
Local oBrowse	:= Nil
Local nX		:= 0
Local aCores	:= {	{ 'TFQ->TFQ_STATUS=="1"' , 'BR_AMARELO',  STR0002 },; //"Agendada"
						{ 'TFQ->TFQ_STATUS=="2"' , 'BR_VERDE',    STR0003 },; //"Efetivada"
						{ 'TFQ->TFQ_STATUS=="3"' , 'BR_VERMELHO', STR0004 } } //"Cancelada"

Default cFilDefault := ''

Private aRotina	:= MenuDef()
Private cCadastro	:= STR0001 //"Movimentação"
 
oBrowse := FWMBrowse():New()
oBrowse:SetAlias('TFQ')

// Adiciona as legendas.
For nX := 1 To Len(aCores)
	oBrowse:AddLegend(aCores[nX][1],aCores[nX][2],aCores[nX][3]) 
Next nX   

If !Empty(cFilDefault)
	oBrowse:SetFilterDefault(cFilDefault)
EndIf

oBrowse:SetDescription(STR0001) //"Movimentação"
oBrowse:Activate()

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef

Definição do MenuDef

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpO:aRotina
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function MenuDef()

LOCAL aRotina := {}

ADD OPTION aRotina TITLE STR0084 ACTION 'PesqBrw'			OPERATION 1 ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0085 ACTION 'VIEWDEF.TECA880'	OPERATION 2 ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0086 ACTION 'VIEWDEF.TECA880'	OPERATION 3 ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0087 ACTION 'VIEWDEF.TECA880'	OPERATION 4 ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0088 ACTION 'VIEWDEF.TECA880'	OPERATION 5 ACCESS 0 //"Excluir"
ADD OPTION aRotina TITLE STR0106 ACTION 'TC880EST'			OPERATION 4 ACCESS 0 //"Estornar"
ADD OPTION aRotina TITLE STR0005 ACTION 'MSDOCUMENT'		OPERATION 7 ACCESS 0 //"Bco. Conhecimento"

Return aRotina

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ModelDef

Definição do Model 

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpO:oModel
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function ModelDef()

Local oStruTFQ  := FWFormStruct(1,'TFQ', {|cCampo| AT880GFLD('CABECALHO',cCampo)})		//Cabeçalho de Movimentação
Local oStruTFR  := FWFormStruct(1,'TFR')		//Tabela de Funcionarios da ocorrencia	
Local oStruTFOA := FWFormStruct(1,'TFO', {|cCampo| AT880GFLD('ARMAS'   , cCampo)})		//Item de Movimentação - Armas
Local oStruTFOM := FWFormStruct(1,'TFO', {|cCampo| AT880GFLD('MUNICOES', cCampo)})		//Item de Movimentação - Munições
Local oStruTFOC := FWFormStruct(1,'TFO', {|cCampo| AT880GFLD('COLETES' , cCampo)})		//Item de Movimentação - Coletes

Local aAux			:= {}

Local bPosVld		:= {|oModel|At880PosTFQ(oModel)}
Local bPosValidacao	:= {|oModel|At880PosValid(oModel)}
Local bCommit		:= {|oModel|At880Commit(oModel)}
Local bActivate		:= {|oModel|At880Active(oModel)}
Local bPosTRF		:= {|oModel|At880PosTRF(oModel)}
Local bLnPsTFOA 	:= {|oModel,nLine,cAcao,cCampo|At880LPArm(oModel,nLine,cAcao,cCampo)}
Local bLnPsTFOC 	:= {|oModel,nLine,cAcao,cCampo|At880LPCol(oModel,nLine,cAcao,cCampo)}
Local bLnPsTFOM		:= {|oModel,nLine,cAcao,cCampo|At880LPMun(oModel,nLine,cAcao,cCampo) .And. At880VldQt(oModel:GetModel())} 
Local bLnPrTFOA		:= {|oModel,nLine,cAcao,cCampo, xValue, xOldValue|At880PRArm(oModel,nLine,cAcao,cCampo, xValue, xOldValue)}
Local bLnPrTFOC		:= {|oModel,nLine,cAcao,cCampo, xValue, xOldValue|At880PRCol(oModel,nLine,cAcao,cCampo, xValue, xOldValue)}
Local bLnPrTFOM		:= {|oModel,nLine,cAcao,cCampo, xValue, xOldValue|At880PRMun(oModel,nLine,cAcao,cCampo, xValue, xOldValue)}
Local bLocalVld		:= {||}
Local cAtuProp		:= ""
Local aLoadFilter	:= {}
Local lGsOrcArma	:= FindFunction("TecGsArma") .And. TecGsArma()
Local lExstRev      := TFQ->(ColumnPos("TFQ_CONREV")) > 0 .And. TFQ->(ColumnPos("TFQ_POSTO")) > 0

//Criação de Gatilhos para os campos virtuais
aAux := FwStruTrigger("TFQ_ORIGEM","TFQ_DSCLOC","At880LDsc('ORIGEM')",.F.,Nil,Nil,Nil)
oStruTFQ:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4]) 

aAux := FwStruTrigger("TFQ_ORIGEM","TFQ_ORIGEM","At880Clear()",.F.,Nil,Nil,Nil)
oStruTFQ:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4]) 

aAux := FwStruTrigger("TFQ_DESTIN","TFQ_DDESTI","At880LDsc('DESTINO')",.F.,Nil,Nil,Nil)
oStruTFQ:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

aAux := FwStruTrigger("TFQ_RESTRA","TFQ_DRESP","At880Resp()",.F.,Nil,Nil,Nil)
oStruTFQ:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

aAux := FwStruTrigger("TFR_CODTEC","TFR_NOMTEC","At880Atend()",.F.,Nil,Nil,Nil)
oStruTFR:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

//Gatilhos para as descrições dos armamentos
aAux := FwStruTrigger("TFO_ITCOD","TFO_DESCR","At880IniArma('"+itARMA+"',FwFldGet('TFO_ITCOD'))",.F.,Nil,Nil,Nil)
oStruTFOA:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

aAux := FwStruTrigger("TFO_ITCOD","TFO_DESCR","At880IniArma('"+itMUNICAO+"',FwFldGet('TFO_ITCOD'))",.F.,Nil,Nil,Nil)
oStruTFOM:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

aAux := FwStruTrigger("TFO_ITCOD","TFO_DESCR","At880IniArma('"+itCOLETE+"',FwFldGet('TFO_ITCOD'))",.F.,Nil,Nil,Nil)
oStruTFOC:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

If lGsOrcArma
	aAux := FwStruTrigger("TFO_ITCOD","TFO_CODTXQ","AT880LTFF()",.F.,Nil,Nil,Nil)
	oStruTFOA:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

	aAux := FwStruTrigger("TFO_ITCOD","TFO_CODTXQ","AT880Col()",.F.,Nil,Nil,Nil)
	oStruTFOC:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])
	If lExstRev
		aAux := FwStruTrigger("TFQ_POSTO","TFQ_CODTFL","AT880TFL()",.F.,Nil,Nil,Nil)
		oStruTFQ:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])
	EndIf
	aAux := FwStruTrigger("TFO_ITCOD","TFO_CODTXQ","AT880Mun(1)",.F.,Nil,Nil,Nil)
	oStruTFOM:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

	aAux := FwStruTrigger("TFO_ITCOD","TFO_PRODUT","AT880Mun(2)",.F.,Nil,Nil,Nil)
	oStruTFOM:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])

	oStruTFOA:SetProperty('TFO_CODTXQ',MODEL_FIELD_WHEN,{|oModel|.F.})
	oStruTFOC:SetProperty('TFO_CODTXQ',MODEL_FIELD_WHEN,{|oModel|.F.})
	oStruTFOM:SetProperty('TFO_CODTXQ',MODEL_FIELD_WHEN,{|oModel|.F.})
Endif

xAux := FwStruTrigger( 'TFQ_MOTIVO', 'TFQ_ENTORI', 'At880Mot(FwFldGet("TFQ_MOTIVO"))', .F., Nil, Nil, Nil)
oStruTFQ:AddTrigger( xAux[1], xAux[2], xAux[3], xAux[4])

oStruTFQ:SetProperty('TFQ_ENTORI', MODEL_FIELD_WHEN, {|| !FwFldGet("TFQ_MOTIVO") $ '1|5'})

// Permitir informar campos Origem e Destino
oStruTFQ:SetProperty('TFQ_ORIGEM',MODEL_FIELD_WHEN,{|oModel|.T.})
oStruTFQ:SetProperty('TFQ_DESTIN',MODEL_FIELD_WHEN,{|oModel|.T.})

//Aplicar os inicializadores padrão
oStruTFOA:SetProperty('TFO_ITMOV',MODEL_FIELD_INIT,{|| itARMA })
oStruTFOM:SetProperty('TFO_ITMOV',MODEL_FIELD_INIT,{|| itMUNICAO })
oStruTFOC:SetProperty('TFO_ITMOV',MODEL_FIELD_INIT,{|| itCOLETE })

// Pré Validação de Edição - Master
oStruTFQ:SetProperty("TFQ_CONTRT",MODEL_FIELD_WHEN,{ || (FwFldGet("TFQ_ENTDES") == edCLIENTE .And. !Empty(FwFldGet("TFQ_DESTIN")) .Or. lGsOrcArma) } )

// Pós Validação de Edição - Master
oStruTFQ:SetProperty("TFQ_ORIGEM",MODEL_FIELD_VALID,{ |oModel|At880VldOri(oModel,'TFQ_ORIGEM')})
oStruTFQ:SetProperty("TFQ_ENTORI",MODEL_FIELD_VALID,{ |oModel|At880VlEnt(oModel,'TFQ_ENTORI') })
oStruTFQ:SetProperty("TFQ_ENTDES",MODEL_FIELD_VALID,{ |oModel|At880VlEnt(oModel,'TFQ_ENTDES') })
oStruTFQ:SetProperty("TFQ_MOTIVO",MODEL_FIELD_VALID,{ |oModel|At880VlEnt(oModel,'TFQ_MOTIVO') .And. At880VldTp() })
oStruTFQ:SetProperty("TFQ_CONTRT",MODEL_FIELD_VALID,{ |oModel|At880VlCont(oModel,FwFldGet("TFQ_DESTIN"),oModel:GetValue("TFQ_CONTRT")) })
oStruTFQ:SetProperty("TFQ_DMOVIM",MODEL_FIELD_VALID,{ |oModel|At880VDtMv(oModel) })

// Obrigatoriedade de preenchimento - Armas
oStruTFOA:SetProperty('TFO_LGUIA',MODEL_FIELD_OBRIGAT,.T.)

// Pré Validação de Edição - Armas
oStruTFOA:SetProperty('TFO_ITMOV' ,MODEL_FIELD_WHEN,{|oModel| .F.})
oStruTFOA:SetProperty('TFO_NRGUIA',MODEL_FIELD_WHEN,{|oModel| oModel:GetValue('TFO_LGUIA') == '1'})
oStruTFOA:SetProperty('TFO_DTRET' ,MODEL_FIELD_WHEN,{|oModel| oModel:GetValue('TFO_LRET') == '1'})
// Pós Validação de Edição - Armas
oStruTFOA:SetProperty("TFO_ITCOD",MODEL_FIELD_VALID,{ |oModel| At880VIMV(oModel) })
oStruTFOA:SetProperty("TFO_LGUIA",MODEL_FIELD_VALID,{ |oModel| Pertence('12') .and. At880NGuia(oModel) })
oStruTFOA:SetProperty("TFO_LRET" ,MODEL_FIELD_VALID,{ |oModel| Pertence('12') .and. At880DtPrv(oModel) })
oStruTFOA:SetProperty("TFO_DTRET",MODEL_FIELD_VALID,{ |oModel| At880VDtRt(oModel) })

// Obrigatoriedade de preenchimento - Munições
oStruTFOM:SetProperty('TFO_LGUIA',MODEL_FIELD_OBRIGAT,.T.)
oStruTFOM:SetProperty('TFO_QTDE' ,MODEL_FIELD_OBRIGAT,.T.)

// Pré Validação de Edição - Munições
oStruTFOM:SetProperty('TFO_ITMOV' ,MODEL_FIELD_WHEN,{|oModel| .F.})
oStruTFOM:SetProperty('TFO_NRGUIA',MODEL_FIELD_WHEN,{|oModel| oModel:GetValue('TFO_LGUIA') == '1'})
oStruTFOM:SetProperty('TFO_DTRET' ,MODEL_FIELD_WHEN,{|oModel| oModel:GetValue('TFO_LRET') == '1'})

// Pós Validação de Edição - Munições
oStruTFOM:SetProperty("TFO_LGUIA",MODEL_FIELD_VALID,{ |oModel| Pertence('12') .and. At880NGuia(oModel) })
oStruTFOM:SetProperty("TFO_LRET" ,MODEL_FIELD_VALID,{ |oModel| Pertence('12') .and. At880DtPrv(oModel) })
oStruTFOM:SetProperty("TFO_DTRET",MODEL_FIELD_VALID,{ |oModel| At880VDtRt(oModel) })

// monta a validação para o campo TFO_PRODUT
cAtuProp := RTrim( GetSX3Cache("TFO_PRODUT","X3_VALID") ) + " .And. At880VldAloc('3')"
If !Empty( Alltrim(cValUser := GetSX3Cache("TFO_PRODUT","X3_VLDUSER") ) )
	cAtuProp += " .And. " + cValUser
EndIf
bLocalVld := FwBuildFeature( STRUCT_FEATURE_VALID, cAtuProp )

oStruTFOM:SetProperty("TFO_PRODUT", MODEL_FIELD_VALID, bLocalVld )

// Pré Validação de Edição - Coletes
oStruTFOC:SetProperty('TFO_ITMOV',MODEL_FIELD_WHEN,{|oModel|.F.})
oStruTFOC:SetProperty('TFO_DTRET',MODEL_FIELD_WHEN,{|oModel|oModel:GetValue('TFO_LRET') == '1'})

// Pós Validação de Edição - Coletes
oStruTFOC:SetProperty("TFO_ITCOD",MODEL_FIELD_VALID,{ |oModel| At880VIMV(oModel) })
oStruTFOC:SetProperty("TFO_LRET" ,MODEL_FIELD_VALID,{ |oModel| Pertence('12') .and. At880DtPrv(oModel) })
oStruTFOC:SetProperty("TFO_DTRET",MODEL_FIELD_VALID,{ |oModel| At880VDtRt(oModel) })

//Instancia o cadastro de Ocorrencia
oModel := MPFormModel():New('TECA880',/*bPreValidacao*/,bPosVld,bCommit,/*bCancel*/)

//Instancia o cabeçalho da ocorrencia
If lGsOrcArma
	oModel:AddFields('TFQMASTER',/*cOwner*/,oStruTFQ, {|oMdlTFQ,cAction,cField,xValue| PrelinTFQ(oMdlTFQ,cAction,cField,xValue)},bPosValidacao,/*bCarga*/,/*bFieldAbp*/)
else
	oModel:AddFields('TFQMASTER',/*cOwner*/,oStruTFQ,/*bPreValidacao*/,bPosValidacao,/*bCarga*/,/*bFieldAbp*/)
Endif		

//Instancia os Grids para cadastro de ocorrencia
oModel:AddGrid("TFRFUNC","TFQMASTER",oStruTFR,/*bLinePre*/,/*bLinePost*/,/*bPreValidacao*/,bPosTRF,/*bLoadPrd*/)
oModel:AddGrid("TFOARMA","TFQMASTER",oStruTFOA,bLnPrTFOA,bLnPsTFOA,/*bPreTFOA*/,/*bPosTFOA*/,/*bLoadPrd*/)
oModel:AddGrid("TFOMUNI","TFQMASTER",oStruTFOM,bLnPrTFOM,bLnPsTFOM,/*bPreValidacao*/,/*bPosValidacao*/,/*bLoadPrd*/)
oModel:AddGrid("TFOCOLE","TFQMASTER",oStruTFOC,bLnPrTFOC,bLnPsTFOC,/*bPreValidacao*/,/*bPosValidacao*/,/*bLoadPrd*/)

//Realiza o relacionamento entre o cabeçalho e os grids Funcionario/Pessoas - Armas - Munições - Coletes
oModel:SetRelation("TFRFUNC",{{"TFR_FILIAL","xFilial('TFR')"},{"TFR_CODMOV","TFQ_CODIGO"}},TFR->( IndexKey(1)))  
oModel:SetRelation("TFOARMA",{{"TFO_FILIAL","xFilial('TFO')"},{"TFO_CDMOV","TFQ_CODIGO"}},TFO->( IndexKey(1)))  
oModel:SetRelation("TFOMUNI",{{"TFO_FILIAL","xFilial('TFO')"},{"TFO_CDMOV","TFQ_CODIGO"}},TFO->( IndexKey(1)))  
oModel:SetRelation("TFOCOLE",{{"TFO_FILIAL","xFilial('TFO')"},{"TFO_CDMOV","TFQ_CODIGO"}},TFO->( IndexKey(1)))  

//Determina a obrigatoriedade de preenchimento dos Grid's
oModel:GetModel('TFOARMA'):SetOptional(.T.)
oModel:GetModel('TFOMUNI'):SetOptional(.T.)
oModel:GetModel('TFOCOLE'):SetOptional(.T.)

//Configura o Grid para não duplicar a linha
oModel:GetModel("TFRFUNC"):SetUniqueLine({"TFR_CODTEC"})
oModel:GetModel("TFOARMA"):SetUniqueLine({"TFO_ITMOV","TFO_ITCOD"})
oModel:GetModel("TFOMUNI"):SetUniqueLine({"TFO_ITMOV","TFO_PRODUT"})
oModel:GetModel("TFOCOLE"):SetUniqueLine({"TFO_ITMOV","TFO_ITCOD"})

//Aplicar filtro de dados aos Grid's
aLoadFilter := { { "TFO_ITMOV", "'"+itARMA+"'", MVC_LOADFILTER_EQUAL } }
oModel:GetModel("TFOARMA"):SetLoadFilter( aLoadFilter, /*cLoadFilter*/ )
aLoadFilter := { { "TFO_ITMOV", "'"+itCOLETE+"'", MVC_LOADFILTER_EQUAL } }
oModel:GetModel("TFOCOLE"):SetLoadFilter( aLoadFilter, /*cLoadFilter*/ )
aLoadFilter := { { "TFO_ITMOV", "'"+itMUNICAO+"'", MVC_LOADFILTER_EQUAL } }
oModel:GetModel("TFOMUNI"):SetLoadFilter( aLoadFilter, /*cLoadFilter*/ )

//Valida o Model para Ativa-lo
oModel:SetVldActivate(bActivate)

oModel:SetPrimaryKey( {} )

Return(oModel)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ViewDef

Definição da View 

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpO:oView
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function ViewDef()

Local oModel   	 := FWLoadModel('TECA880')
Local oStruTFQ	 := FWFormStruct(2,'TFQ', {|cCampo| AT880GFLD('CABECALHO',cCampo)})		//Cabeçalho de Movimentação
Local oStruTFR	 := FWFormStruct(2,'TFR')		//Tabela de Funcionarios da ocorrencia	
Local oStruTFOA	 := FWFormStruct(2,'TFO', {|cCampo| AT880GFLD('ARMAS'   , cCampo)})		//Item de Movimentação - Armas
Local oStruTFOM	 := FWFormStruct(2,'TFO', {|cCampo| AT880GFLD('MUNICOES', cCampo)})		//Item de Movimentação - Munições
Local oStruTFOC	 := FWFormStruct(2,'TFO', {|cCampo| AT880GFLD('COLETES' , cCampo)})		//Item de Movimentação - Coletes
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()
Local lExstRev   := TFQ->(ColumnPos("TFQ_CONREV")) > 0 .And. TFQ->(ColumnPos("TFQ_POSTO")) > 0 

oView:= FWFormView():New()
oView:SetModel(oModel)

oView:AddUserButton(STR0005, 'CLIPS',{|oView|MsDocument('TFQ',TFQ->(RECNO()),oModel:GetOperation() )}) //"Bco. Conhecimento"

//Adiciona o Link para Emissão da Guia
oView:AddUserButton(STR0048,"",{|oView|At880Guia(oModel)},,,) //Guia de Trafego

oStruTFQ:RemoveField("TFQ_ULTSTA")
If lGsOrcArma
	oStruTFQ:RemoveField("TFQ_CODTFL")
else
	oStruTFQ:RemoveField("TFQ_CONTRT")
	If lExstRev
		oStruTFQ:RemoveField("TFQ_CONREV")
		oStruTFQ:RemoveField("TFQ_POSTO")
	EndIf
Endif
oStruTFQ:RemoveField("TFQ_ENTIDA")
oStruTFR:RemoveField("TFR_CODMOV")

oView:AddField('VIEW_CAB',oStruTFQ,'TFQMASTER')
oView:AddGrid("VIEW_GRID_TFR",oStruTFR,"TFRFUNC")
oView:AddGrid("VIEW_GRID_TFOA",oStruTFOA,"TFOARMA")
oView:AddGrid("VIEW_GRID_TFOM",oStruTFOM,"TFOMUNI")
oView:AddGrid("VIEW_GRID_TFOC",oStruTFOC,"TFOCOLE")

oView:CreateHorizontalBox('SUPERIOR',65)
oView:CreateHorizontalBox('INFERIOR',35)

// Criação da pasta para conter as abas da parte inferior
oView:CreateFolder( "ABAS", "INFERIOR" )

// Criação das Abas
oView:AddSheet( "ABAS", "ABA01", STR0089 )	// 'Destinatários'
oView:AddSheet( "ABAS", "ABA02", STR0090 )			// 'Armas'
oView:AddSheet( "ABAS", "ABA03", STR0091 )			// 'Munição'
oView:AddSheet( "ABAS", "ABA04", STR0092 )			// "Coletes"

// Criação das áreas de exibição dos dados nas abas
oView:CreateHorizontalBox( "ID_ABA01", 100,,, "ABAS", "ABA01" )
oView:CreateHorizontalBox( "ID_ABA02", 100,,, "ABAS", "ABA02" )
oView:CreateHorizontalBox( "ID_ABA03", 100,,, "ABAS", "ABA03" )
oView:CreateHorizontalBox( "ID_ABA04", 100,,, "ABAS", "ABA04" )

// Relacionando o componente da interface com o componente de dados.
oView:SetOwnerView( 'VIEW_CAB', 'SUPERIOR' )			// Cabeçalho da Movimentação		
oView:SetOwnerView( 'VIEW_GRID_TFR', 'ID_ABA01' )	// Grid Destinatários 
oView:SetOwnerView( 'VIEW_GRID_TFOA', 'ID_ABA02' )	// Grid Armas
oView:SetOwnerView( 'VIEW_GRID_TFOM', 'ID_ABA03' )	// Grid Munição
oView:SetOwnerView( 'VIEW_GRID_TFOC', 'ID_ABA04' )	// Grid Coletes

// Adicionando Consulta F3
If lGsOrcArma
	oStruTFOA:SetProperty('TFO_ITCOD',MVC_VIEW_LOOKUP,'ARMCON')
	oStruTFOC:SetProperty('TFO_ITCOD',MVC_VIEW_LOOKUP,'COLCON')
	oStruTFOM:SetProperty('TFO_ITCOD',MVC_VIEW_LOOKUP,'MUNCOD')
else
	oStruTFOA:SetProperty('TFO_ITCOD',MVC_VIEW_LOOKUP,'SB1ARM')
	oStruTFOC:SetProperty('TFO_ITCOD',MVC_VIEW_LOOKUP,'SB1COL')
	oStruTFOM:SetProperty('TFO_PRODUT',MVC_VIEW_LOOKUP,'SB1MUN')
Endif	

oView:AddIncrementField( 'VIEW_GRID_TFR', 'TFR_ITEM' )

// Aba Armas
oStruTFOA:SetProperty('TFO_ITMOV', MVC_VIEW_ORDEM, '01')
oStruTFOA:SetProperty('TFO_ITCOD', MVC_VIEW_ORDEM, '02')
oStruTFOA:SetProperty('TFO_DESCR', MVC_VIEW_ORDEM, '03')

//Aba Coletes
oStruTFOC:SetProperty('TFO_ITMOV', MVC_VIEW_ORDEM, '01')
oStruTFOC:SetProperty('TFO_ITCOD', MVC_VIEW_ORDEM, '02')
oStruTFOC:SetProperty('TFO_DESCR', MVC_VIEW_ORDEM, '03')

// Aba Munições
If lGsOrcArma
	oStruTFOM:SetProperty('TFO_ITMOV' , MVC_VIEW_ORDEM, '01')
	oStruTFOM:SetProperty('TFO_ITCOD' , MVC_VIEW_ORDEM, '02')
	oStruTFOM:SetProperty('TFO_PRODUT', MVC_VIEW_ORDEM, '03')
	oStruTFOM:SetProperty('TFO_DESCR' , MVC_VIEW_ORDEM, '04')
Else
	oStruTFOM:SetProperty('TFO_ITMOV' , MVC_VIEW_ORDEM, '01')
	oStruTFOM:SetProperty('TFO_PRODUT', MVC_VIEW_ORDEM, '02')
	oStruTFOM:SetProperty('TFO_DSCMUN', MVC_VIEW_ORDEM, '03')
EndIf

oStruTFQ:SetProperty("TFQ_ORIGEM" ,MVC_VIEW_ORDEM, Soma1(oStruTFQ:GetProperty('TFQ_ENTORI' , MVC_VIEW_ORDEM)))
oStruTFQ:SetProperty("TFQ_DSCLOC" ,MVC_VIEW_ORDEM, Soma1(oStruTFQ:GetProperty('TFQ_ORIGEM' , MVC_VIEW_ORDEM)))
oStruTFQ:SetProperty('TFQ_ORIGEM', MVC_VIEW_CANCHANGE, .T.)

If lGsOrcArma
	oStruTFQ:SetProperty('TFQ_CONTRT', MVC_VIEW_ORDEM, '04')
	If lExstRev
		oStruTFQ:SetProperty('TFQ_CONREV', MVC_VIEW_ORDEM, '05')
	EndIf

Endif

If FindFunction("TecMtFlArm")
	If TFQ->(ColumnPos("TFQ_FILORI")) > 0
		If !TecMtFlArm()
			oStruTFQ:RemoveField("TFQ_FILORI")
		Else
			oStruTFQ:SetProperty("TFQ_FILORI" ,MVC_VIEW_ORDEM, '07')
		Endif
	EndIf
	If TFQ->(ColumnPos("TFQ_FILDES")) > 0
		If !TecMtFlArm()
			oStruTFQ:RemoveField("TFQ_FILDES")
		Else
			oStruTFQ:SetProperty("TFQ_FILDES" ,MVC_VIEW_ORDEM, '10')
		Endif
	Endif
Endif
If  oModel:GetOperation() == MODEL_OPERATION_VIEW .AND. ExistBlock("Tec880ImpProt")
	oView:AddUserButton(STR0093,"",{|oModel| At800RIm(oModel ,oView)},,,) // "Reimprimir protocolo"
	
EndIf

oView:SetCloseOnOk({|| .T.} )

Return(oView)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Active

Verifica se o Status da Movimentação está Efetivada ou cancelada e não permite a sua edição
@author Serviços
@since 17/10/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880Active(oMdl)
Local nOperation 	:= oMdl:GetOperation()
Local lRet			:= .T.

If (nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_DELETE) .And. TFQ->TFQ_STATUS <> stAGENDADA
	Help( ,, "At880Active",, STR0007, 1, 0 )//"Movimentação",'Não é permitido alterar ou excluir a Movimentação Efetivada / Cancelada.'
	lRet := .F.
EndIf

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PosValid

Pos-Validação do cadastro de Movimentação
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PosValid(oMdl)
Local lRet	:= .T.
Local oMdlAux := nil
Local nQtMin	:= 0
Local nA      := 0
Local nQtdTXQ := 0
local nMaxArma   := 0
local nMaxCole   := 0
local nMaxMuni   := 0						
local nMinMuni := 0
local nQtmov     := 0
Local nQtdAPO    := 0
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()
Local lAT880VldMun := ExistBlock("AT880VLDMUN")
Local lRetPE := .F.
Local lArmMultFil := TecMtFlArm()

If lRet 
	// Grade Armas
	oMdlAux := oMdl:GetModel():GetModel('TFOARMA')
	
	// Verifica a quantidade de itens válidos
	nQtMin += At880SumIt(oMdlAux) 

	//Verifica se foi preenchido o Numero da Guia
	lRet := At880VldGuia(oMdlAux)

	If lRet
		//Verifica as datas de Retorno e da movimentação
		lRet := At880Status(oMdlAux)
	EndIf	

	If 	lRet .And. lGsOrcArma .And. !Empty(oMdlAux:GetValue("TFO_ITCOD")) // Grade Armas
		Begin Transaction
			TE0->(DbSetorder(1))
			If  FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'1'|'3'|'4'|"  
					For nA := 1 to oMdlAux:Length()
						oMdlAux:Goline(nA)
						If !oMdlAux:Isdeleted()
							iF TE0->(MsSeek( FwXFilial("TE0") + oMdlAux:GetValue("TFO_ITCOD")))
								If (TE0->TE0_SITUA == '1') .Or. ( lArmMultFil .And. TE0->TE0_SITUA == '2' )
									If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
										nQtdTXQ := TXQ->TXQ_QTDVEN
										nMaxArma := TXQ->TXQ_QTDAPO + 1
										If nMaxArma <= nQtdTXQ
											RecLock("TXQ",.F.) 
												TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO + 1
											TXQ->(MsUnlock())
										Else
											DisarmTransaction()
											lRet := .F.
											Help(,,STR0097,,STR0098,1,0) //"Quantidade excedida para movimentação"###"A quantidade de armas foi excedida. Verifique o contrato"###
											Exit
										Endif				
									Endif
								else
									lRet := .F.
									Help( ,, "At880Posvalid",, STR0101, 1, 0 ) //"O armamento nao esta localizado na origem informada"
									Exit
								Endif		
							Endif		
						Endif	
					Next nA
			Elseif FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'2'|'5'|" 
				For nA := 1 to oMdlAux:Length()
					oMdlAux:Goline(nA)
					If !oMdlAux:Isdeleted()	
						iF TE0->(MsSeek(FwXFilial("TE0")+oMdlAux:GetValue("TFO_ITCOD")))
							If (TE0->TE0_SITUA == '2') .Or. ( lArmMultFil .And. TE0->TE0_SITUA == '1' )
								If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
									RecLock("TXQ",.F.) 
										TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO - 1
									TXQ->(MsUnlock())			
								Endif
							else
								lRet := .F.
									Help( ,, "At880Posvalid",, STR0101, 1, 0 ) //"O armamento nao esta localizado na origem informada"
								Exit
							Endif	
						Endif	
					Endif	
				Next nA
			Endif
		End Transaction	
	Endif	
EndIf	
 
If lRet	
	// Grade de Munições
	oMdlAux := oMdl:GetModel():GetModel('TFOMUNI') 
	nQtMin += At880SumIt(oMdlAux)

	//Verifica se foi preenchido o Numero da Guia
	lRet := At880VldGuia(oMdlAux)

	If lRet
		//Verifica as datas de Retorno e da movimentação	
		lRet := At880Status (oMdlAux,.T.)	
	EndIf

	If 	lRet .And. lGsOrcArma .And. !Empty(oMdlAux:GetValue("TFO_ITCOD")) // Grade de munições
		Begin Transaction
		TE2->(DbSetorder(4))
			If  FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'1'|'3'|'4'|"  
					For nA := 1 to oMdlAux:Length()
						oMdlAux:Goline(nA)
						If !oMdlAux:Isdeleted()
							If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
								nQtdTXQ := TXQ->TXQ_QTDVEN
								nQtmov := oMdlAux:GetValue("TFO_QTDE") //Quantidade solicitada
								nMaxMuni := TXQ->TXQ_QTDAPO + nQtmov
								If	nMaxMuni <= nQtdTXQ
									RecLock("TXQ",.F.) 
										TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO +  nQtmov
									TXQ->(MsUnlock())
								Else
									DisarmTransaction()
									lRet := .F.
									Help(,,STR0097,,STR0098,1,0) //"Quantidade excedida para movimentação"###"A quantidade de armas foi excedida. Verifique o contrato"###
									Exit
								Endif				
							Endif	
						Endif		
					Next nA
			Elseif FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'2'|'5'|" .And. !Empty(oMdlAux:GetValue("TFO_ITCOD"))
				For nA := 1 to oMdlAux:Length()
					oMdlAux:Goline(nA)
					If !oMdlAux:Isdeleted()
						If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
							nQtdTXQ  := TXQ->TXQ_QTDVEN // Quantidade do Orçamento
							nQtmov  := oMdlAux:GetValue("TFO_QTDE") //Quantidade solicitada na movimentaçaõ
							nQtdAPO  := TXQ->TXQ_QTDAPO // Quantidade consumuda no orçamento
							nMinMuni := nQtdAPO - nQtmov	// Quantidade apontada - quantidade solicitada 
							iF nQtdAPO >= nQtmov
								If nMinMuni <= nQtdTXQ
									RecLock("TXQ",.F.) 
										TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO - nQtmov
									TXQ->(MsUnlock())	
								Endif	
							Endif		
						Endif
					Endif			
				Next nA
			Endif
		End Transaction	
	Endif
EndIf

If lRet 
	// Grade de Coletes
	oMdlAux := oMdl:GetModel():GetModel('TFOCOLE') // Grade de Coletes

	nQtMin += At880SumIt(oMdlAux)

	If 	lRet .And. lGsOrcArma .And. !Empty(oMdlAux:GetValue("TFO_ITCOD"))
		Begin Transaction
		TE1->(DbSetorder(1))
		If FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'1'|'3'|'4'|"  
			For nA := 1 to oMdlAux:Length()
				oMdlAux:Goline(nA)
				If !oMdlAux:Isdeleted()
					iF TE1->(MsSeek(FwXFilial("TE1")+oMdlAux:GetValue("TFO_ITCOD")))
						If (TE1->TE1_SITUA == '1') 
							If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
								nQtdTXQ := TXQ->TXQ_QTDVEN
								nMaxCole := TXQ->TXQ_QTDAPO + 1
								If nMaxCole <= nQtdTXQ
									RecLock("TXQ",.F.) 
										TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO + 1
									TXQ->(MsUnlock())
								Else
									DisarmTransaction()
									lRet := .F.
									Help(,,STR0097,,STR0098,1,0) //"Quantidade excedida para movimentação"###"A quantidade de armas foi excedida. Verifique o contrato"###
									Exit
								Endif				
							Endif
						else
							lRet := .F.
							Help( ,, "At880Posvalid",, STR0101, 1, 0 ) //"O armamento nao esta localizado na origem informada"
							Exit
						Endif		
					Endif		
				Endif	
			Next nA
		Elseif  FwFldGet("TFQ_STATUS") == '2' .And. FwFldGet("TFQ_MOTIVO") $ "|'2'|'5'|"  .And. !Empty(oMdlAux:GetValue("TFO_ITCOD"))
				For nA := 1 to oMdlAux:Length()
					oMdlAux:Goline(nA)
					If !oMdlAux:Isdeleted()
						iF TE1->(MsSeek(FwXFilial("TE1")+oMdlAux:GetValue("TFO_ITCOD")))
							If (TE1->TE1_SITUA == '2') .Or. ( lArmMultFil .And. TE1->TE1_SITUA == '1' )
								If TXQ->(MsSeek(xFilial("TXQ")+oMdlAux:GetValue("TFO_CODTXQ")))
									RecLock("TXQ",.F.) 
										TXQ->TXQ_QTDAPO := TXQ->TXQ_QTDAPO - 1
									TXQ->(MsUnlock())			
								Endif
							Endif	
						Endif		
					Endif	
				Next nA
			Endif
		End Transaction	
	Endif	
Endif	

If lRet
	//Verifica as datas de Retorno e da movimentação
	lRet := At880Status(oMdlAux)
EndIf

// PE com prazo de validade para permitir validação personalizadas
If lAT880VldMun .And. !lGsOrcArma .And. dDataBase <= SToD("31/12/2023")
	lRetPE := ExecBlock("AT880VLDMUN", .F., .F.)
EndIf

If !lRetPE .And. Empty(nQtMin) .AND. lRet
	Help( ,, "At880PosValid",, STR0063, 1, 0 ) //'Alocação''Nenhum item de armamento foi selecionado!'
	lRet := .F.
EndIf

// Validação os campos da manutenção. 
If lRet 
	lRet := At880MntCmp()
EndIf
	
Return( lRet )

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880LPArm
	Pos-Validação da linha do cadastro de Movimentação - Grid Armas

@param oModel,object,Objeto Master do formulário de movimentação
@param nLine,numerico,Linha da grade na edição atual

@sample At880LPArm(oMdl,nLine)

@author Serviços
@since 01/06/2015
@version P12

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880LPArm(oMdl,nLine,cAcao,cCampo) 
Local lRet        := .T.
Local nOperation  := oMdl:GetOperation()
Local lGsOrcArma  := FindFunction("TecGsArma") .And. TecGsArma()
Local lArmMultFil := TecMtFlArm()
Local oMdlTFQ     := oMdl:GetModel():GetModel('TFQMASTER')
Local oStruTFQ    := Nil

If nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_INSERT

	If Empty(oMdl:GetValue('TFO_ITCOD')) .OR. ( lGsOrcArma .And. Empty(oMdl:GetValue('TFO_CODTXQ')) .And. IIf( !Empty( oMdlTFQ:GetValue('TFQ_CONTRT') ), .T., !lArmMultFil ) )
		Help( ,, "At880LPArm",, STR0102, 1, 0 ) //"O campo Armamento não esta preenchido. Verifique e tente novamente"
		lRet := .F.
	Endif

	If oMdl:GetValue('TFO_LGUIA') == '1' .AND. Empty(oMdl:GetValue('TFO_NRGUIA'))
		Help( ,, "At880LPArm",, STR0012, 1, 0 ) //"Preencha o Numero da Guia"
		lRet := .F.
	EndIf
	
	If lRet .And. lGsOrcArma .And. lArmMultFil
		oStruTFQ := oMdlTFQ:GetStruct()
		If Empty( oMdlTFQ:GetValue('TFQ_CONTRT') )
			oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .F. )
		Else
			If !oStruTFQ:GetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT )
				oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .T. )
			EndIf
		EndIf
	EndIf

EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880LPCol
	Pos-Validação da linha do cadastro de Movimentação - Grid Coletes

@param oModel,object,Objeto Master do formulário de movimentação
@param nLine,numerico,Linha da grade na edição atual

@sample At880LPCol(oMdl,nLine,cAcao,cCampo)

@author Serviços
@since 06/03/2023
@version P12

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880LPCol(oMdl,nLine,cAcao,cCampo) 
Local lRet := .T.
Local nOperation := oMdl:GetOperation()
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()
Local lArmMultFil := TecMtFlArm()
Local oMdlTFQ := oMdl:GetModel():GetModel('TFQMASTER')
Local oStruTFQ := Nil

If nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_INSERT

	If Empty(oMdl:GetValue('TFO_ITCOD')) .OR. ( lGsOrcArma .And. Empty(oMdl:GetValue('TFO_CODTXQ')) .And. IIf( !Empty( oMdlTFQ:GetValue('TFQ_CONTRT') ), .T., !lArmMultFil ) )
		Help( ,, "At880LPCol",, STR0103, 1, 0 ) //"O campo Armamento(Colete) não esta preenchido. Verifique e tente novamente"
		lRet := .F.
	Endif

	If lRet .And. lGsOrcArma .And. lArmMultFil
		oStruTFQ := oMdlTFQ:GetStruct()
		If Empty( oMdlTFQ:GetValue('TFQ_CONTRT') )
			oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .F. )
		Else
			If !oStruTFQ:GetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT )
				oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .T. )
			EndIf
		EndIf
	EndIf

EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880LPMun
	Pos-Validação da linha do cadastro de Movimentação - Grid Munições

@param oModel,object,Objeto Master do formulário de movimentação
@param nLine,numerico,Linha da grade na edição atual

@sample At880LPMun(oMdl,nLine,cAcao,cCampo)

@author Serviços
@since 08/07/2015
@version P12

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880LPMun(oMdl,nLine,cAcao,cCampo)
Local lRet := .T.
Local nOperation := oMdl:GetOperation()
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()
Local lAT880VldMun := ExistBlock("AT880VLDMUN")
Local lRetPE := .F.
Local lArmMultFil := TecMtFlArm()
Local oMdlTFQ := oMdl:GetModel():GetModel('TFQMASTER')
Local oStruTFQ := Nil

If nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_INSERT

	If oMdl:GetValue('TFO_LGUIA') == '1' .AND. Empty(oMdl:GetValue('TFO_NRGUIA'))
		Help( ,, "At880LPMun",, STR0012, 1, 0 ) //'Alocação','Preencha o Numero da Guia!'
		lRet := .F.
	EndIf

	// PE com prazo de validade para permitir validação personalizadas
	If lAT880VldMun .And. !lGsOrcArma .And. dDataBase <= SToD("31/12/2023")
		lRetPE := ExecBlock("AT880VLDMUN", .F., .F.)
	EndIf

	If !lRetPE .And. (Empty(oMdl:GetValue('TFO_PRODUT')) .OR. ( lGsOrcArma .And. Empty(oMdl:GetValue('TFO_CODTXQ'))) .And. IIf( !Empty( oMdlTFQ:GetValue('TFQ_CONTRT') ), .T., !lArmMultFil ) )
		Help( ,, "At880LPMun",, STR0104, 1, 0 ) //"O Codigo do produto nao esta preenchido."
		lRet := .F.
	EndIf

	If oMdl:GetValue('TFO_QTDE') <= 0
		Help( ,, "At880LPMun",, STR0105, 1, 0 ) //"Quantidade nao esta preenchida."
		lRet := .F.
	EndIf

	If lRet .And. lGsOrcArma .And. lArmMultFil
		oStruTFQ := oMdlTFQ:GetStruct()
		If Empty( oMdlTFQ:GetValue('TFQ_CONTRT') )
			oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .F. )
		Else
			If !oStruTFQ:GetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT )
				oStruTFQ:SetProperty( 'TFQ_CONTRT', MODEL_FIELD_OBRIGAT, .T. )
			EndIf
		EndIf
	EndIf

EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PosTRF

Pos-Validação do cadastro de Movimentação - Grid Reponsaveis
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PosTRF(oModel)
Local lRet			:= .T.
Local nI			:= 0
Local aArea		:= GetArea()

For nI := 1 to oModel:Length()
	oModel:GoLine(nI)
	//Verifica se o campo CNV está preenchido
	If (oModel:IsUpdated() .Or. oModel:IsInserted())
		DbSelectArea("AA1")
		AA1->(DbSetOrder(1))
		
		If AA1->(DbSeek(xFilial("AA1") + FwFldGet("TFR_CODTEC")))
			If Empty(AA1->AA1_CNV)
				Help( ,, "At880PosTRF",, STR0058 + cValToChar(nI) + STR0059, 1, 0 )//"O Atendente selecionado na linha: "##" não está com a CNV preenchida"
				lRet:= .F.	
			EndIf
		EndIf
	EndIf
	
Next nI

RestArea(aArea)

Return(lRet)


//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PosTFQ

Pos-Validação do cadastro de Movimentação - Geral

@author 	Serviços
@since 		28/08/13
@version 	P11 R9

@return 	lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PosTFQ(oModel)

Local lRet			:= .T.
Local aArea		:= GetArea()
Local nI 			:= 0
Local oTFOMuni		:= oModel:GetModel('TFOMUNI')
Local aRows 		:= FwSaveRows()

If Empty(FwFldGet("TFQ_CONTRT")) .And. FwFldGet("TFQ_ENTDES") == edCLIENTE 
	lRet := At680Perm( Nil, __cUserId, "007" ) // Pode movimentar armamento fora do contrato?
	If !lRet
		Help( ,, 'AT880ACE',, STR0060, 1, 0) // "Usuário sem permissão para movimentação fora do contrato!"		
	EndIf
EndIf

If lRet 
	For nI := 1 To oTFOMuni:Length()		
		oTFOMuni:GoLine(nI)
		If !oTFOMuni:IsDeleted() .And. !Empty(oTFOMuni:GetValue('TFO_PRODUT'))
			lRet := At880VldQt(oModel)
			If !lRet
				Exit
			EndIf	
		EndIf	
	Next nI	
EndIf

RestArea(aArea)
FwRestRows( aRows )
Return(lRet)


//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PRArm
Pre-Validação da linha do cadastro de Movimentação - Grid Armas
@author flavio.vicco
@since 01/08/2023
@return	lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PRArm(oMdl, nLine, cAcao, cCampo, xValue, xOldValue)
Local lGsOrcArma:= FindFunction("TecGsArma") .And. TecGsArma()
Local lOk       := .T.
Local lArmMultFil := TecMtFlArm()
If cAcao == 'SETVALUE' .And. xValue <> xOldValue
	If cCampo == 'TFO_ITCOD'
		// Validação dos Armamentos (munição) no Orçamento
		If lGsOrcArma .And. !lArmMultFil .And. !Empty(FwFldGet("TFQ_CONTRT"))
			If Empty(AT880LTFF(xValue))
				Help(,,"At880PPRMun",,"Código da Arma não localizado no Contrato.",1,0)
				lOk := .F.
			EndIf
		EndIf
	EndIf
EndIf
Return lOk

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PRCol
Pre-Validação da linha do cadastro de Movimentação - Grid Coletes
@author flavio.vicco
@since 01/08/2023
@return	lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PRCol(oMdl, nLine, cAcao, cCampo, xValue, xOldValue)
Local lGsOrcArma:= FindFunction("TecGsArma") .And. TecGsArma()
Local lOk       := .T.
Local lArmMultFil := TecMtFlArm()
If cAcao == 'SETVALUE' .And. xValue <> xOldValue
	If cCampo == 'TFO_ITCOD'
		// Validação dos Armamentos (munição) no Orçamento
		If lGsOrcArma .And. !lArmMultFil .And. !Empty( FwFldGet("TFQ_CONTRT") )
			If Empty(AT880Col(xValue))
				Help(,,"At880PPRMun",,"Código da Arma não localizado no Contrato.",1,0)
				lOk := .F.
			EndIf
		EndIf
	EndIf
EndIf
Return lOk

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880PRMun
Pre-Validação da linha do cadastro de Movimentação - Grid Muniçoes
@author flavio.vicco
@since 01/08/2023
@return	lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880PRMun(oMdl, nLine, cAcao, cCampo, xValue, xOldValue)
Local lGsOrcArma:= FindFunction("TecGsArma") .And. TecGsArma()
Local lOk       := .T.
Local lArmMultFil := TecMtFlArm()
If cAcao == 'SETVALUE' .And. xValue <> xOldValue
	If cCampo == 'TFO_ITCOD'
		// Validação dos Armamentos (munição) no Orçamento
		If lGsOrcArma .And. !lArmMultFil .And. !Empty( FwFldGet("TFQ_CONTRT") )
			If Empty(AT880Mun(1,xValue))
				Help(,,"At880PPRMun",,"Código da Munição não localizado no Contrato.",1,0)
				lOk := .F.
			EndIf
		EndIf
	EndIf
EndIf
Return lOk

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Commit

Realiza a gravação da movimentação e alteração de Status dos armamentos
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880Commit(oMdl)
Local lRet := .T.

lRet := At880Cmt23(oMdl)

Retur lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Cmt23

Realiza a gravação da movimentação e alteração de Status dos armamentos (versão 23 ou superior)
@author Serviços
@since 18/12/2018
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Cmt23(oMdl)

Local lRet 		:= .T.
Local nOperation 	:= oMdl:GetOperation()
Local oMdlAux		:= nil
Local cTpArma		:= ''
Local cItArma		:= ''
Local dRetorno	:= CToD('')
Local nI			:= 0
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()

If nOperation == MODEL_OPERATION_INSERT .Or. nOperation == MODEL_OPERATION_UPDATE

	Begin Transaction
		Do Case
			Case FwFldGet("TFQ_MOTIVO") == mtIMPLANTACAO  .Or. FwFldGet("TFQ_MOTIVO") == mtREFORCO //Implantação ou Reforço

				//Verifica se os campos de manutenção estão preenchidos, senão realiza a sua limpeza
				If !(Empty(FwFldGet("TFQ_CDRESP"))) .Or. !(Empty(FwFldGet("TFQ_TPMNT"))) .Or. !(Empty(FwFldGet("TFQ_MOTMNT")))
					At880MntClean()
				EndIf

				//Altera a situação do Armamento		
				If FwFldGet("TFQ_STATUS") == stEFETIVADA

					// Instancia Grid Armas			
					oMdlAux	:= oMdl:GetModel('TFOARMA')
					cTpArma := itARMA
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Implantada
							EndIf
						Next nI
					EndIf

					// Instancia Grid Coletes
					oMdlAux	:= oMdl:GetModel('TFOCOLE')
					cTpArma := itCOLETE
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Implantada
							EndIf
						Next nI
					EndIf

					// Instancia Grid Munição
					oMdlAux	:= oMdl:GetModel('TFOMUNI')
					cTpArma	:= itMUNICAO
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							//cItArma:= oMdlAux:getValue('TFO_ITCOD')
							cItArma:= oMdlAux:getValue('TFO_PRODUT')
							nQtd	:= oMdlAux:GetValue('TFO_QTDE')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880IncTFP(cItArma,nQtd)				//Altera o Saldo da Munição
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Municao para Implantada
							EndIf
						Next nI
					EndIf
				EndIf

			Case FwFldGet("TFQ_MOTIVO") == mtMANUTENCAO //Manutenção

				If FwFldGet("TFQ_STATUS") == stEFETIVADA

					// Instancia Grid Armas			
					oMdlAux	:= oMdl:GetModel('TFOARMA')
					cTpArma := itARMA
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Ativa
								//Gera o registro de manutenção
								At880IncManut(oMdl,cTpArma,cItArma)
							EndIf
						Next nI
					EndIf

					// Instancia Grid Coletes
					oMdlAux	:= oMdl:GetModel('TFOCOLE')
					cTpArma := itCOLETE
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Ativa
								//Gera o registro de manutenção
								If !(lRet := At880IncManut(oMdl,cTpArma,cItArma))
									Exit
								EndIf
							EndIf
						Next nI
					EndIf

					// Instancia Grid Munição
					oMdlAux	:= oMdl:GetModel('TFOMUNI')
					cTpArma	:= itMUNICAO
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma:= oMdlAux:getValue('TFO_PRODUT')
							nQtd	:= oMdlAux:GetValue('TFO_QTDE')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880IncTFP(cItArma,nQtd)				//Altera o Saldo da Munição
							EndIf
						Next nI
					EndIf

					If lRet
						ApMsgInfo( STR0041, STR0042 ) //'Manutenção incluida com sucesso.','ATENÇÃO'
					EndIf
				EndIf

			Case FwFldGet("TFQ_MOTIVO") == mtDESCARTE //Descarte

				If FwFldGet("TFQ_STATUS") == stEFETIVADA

					// Instancia Grid Armas			
					oMdlAux	:= oMdl:GetModel('TFOARMA')
					cTpArma := itARMA
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Ativa
							EndIf
						Next nI
					EndIf

					// Instancia Grid Coletes
					oMdlAux	:= oMdl:GetModel('TFOCOLE')
					cTpArma := itCOLETE
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:getValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880SitArma(cTpArma,cItArma,dRetorno)	//Altera o Status da Arma para Ativa
							EndIf
						Next nI
					EndIf

					// Instancia Grid Munição
					oMdlAux	:= oMdl:GetModel('TFOMUNI')
					cTpArma	:= itMUNICAO
					If	!( oMdlAux:IsEmpty() )
						For nI := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma:= oMdlAux:getValue('TFO_PRODUT')
							nQtd	:= oMdlAux:GetValue('TFO_QTDE')
							If !oMdlAux:IsDeleted() .AND. !Empty(cItArma)
								At880IncTFP(cItArma,nQtd)				//Altera o Saldo da Munição
							EndIf
						Next nI
					EndIf
				Endif

			Case FwFldGet("TFQ_MOTIVO") == mtENCERRAMENTO //	Encerramento

				//	Verifica se os campos de manutenção estão preenchidos, senão realiza a sua limpeza
				If !(Empty(FwFldGet("TFQ_CDRESP"))) .Or. !(Empty(FwFldGet("TFQ_TPMNT"))) .Or. !(Empty(FwFldGet("TFQ_MOTMNT")))
					At880MntClean()
				EndIf

				//	Altera a situação do Armamento		
				If FwFldGet("TFQ_STATUS") == stEFETIVADA 

					//	Instancia Grid Armas
					oMdlAux := oMdl:GetModel('TFOARMA')
					If	!(oMdlAux:IsEmpty())
						cTpArma := itARMA
						For nI  := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:GetValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .And. !Empty(cItArma)
								At880SitArma(cTpArma, cItArma, dRetorno)	//	Altera o Status da Arma para Implantada
							EndIf
						Next
					EndIf

					//	Instancia Grid Coletes
					oMdlAux := oMdl:GetModel('TFOCOLE')
					If	!(oMdlAux:IsEmpty())
						cTpArma := itCOLETE
						For nI  := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma  := oMdlAux:GetValue('TFO_ITCOD')
							dRetorno := oMdlAux:GetValue('TFO_DTRET')
							If !oMdlAux:IsDeleted() .And. !Empty(cItArma)
								At880SitArma(cTpArma, cItArma, dRetorno)	//	Altera o Status da Arma para Implantada
							EndIf
						Next
					EndIf

					//	Instancia Grid Munição
					oMdlAux := oMdl:GetModel('TFOMUNI')
					If	!(oMdlAux:IsEmpty())
						cTpArma := itMUNICAO
						For nI  := 1 To oMdlAux:Length()
							oMdlAux:GoLine(nI)
							cItArma := oMdlAux:GetValue('TFO_PRODUT')
							nQtd    := oMdlAux:GetValue('TFO_QTDE')
							If !oMdlAux:IsDeleted() .And. !Empty(cItArma)
								At880IncTFP(cItArma, nQtd)                 //	Altera o Saldo da Munição
								At880SitArma(cTpArma, cItArma, dRetorno)   //	Altera o Status da Municao para Implantada
							EndIf
						Next
					EndIf
				EndIf

		EndCase

		// Zera o campo de contrato quando a movimentação for interna
		If FwFldGet("TFQ_ENTDES") == edEMPRESA .And. !lGsOrcArma
			oMdl:LoadValue( "TFQMASTER", "TFQ_CONTRT",  "" )
			If TFQ->(ColumnPos("TFQ_CONREV")) > 0
				oMdl:LoadValue( "TFQMASTER", "TFQ_CONREV",  "" )
			EndIf
		EndIf

	End Transaction

EndIf

//Imprime o Protocolo
At880ImpPr(oMdl)

FWModelActive( oMdl )
FWFormCommit( oMdl )
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FA880F3

Chama a Consulta Padrão conforme o que foi selecionado no campo TFQ_ITMOV
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function FA880F3(cTpArma)
Local lRet			:= .T.
Local aArea		:= GetArea()
Local oMdl		:= FwModelActive()
Default cTpArma	:= "" 

If Empty(cTpArma)  
	cTpArma := oMdl:GetValue("TFOARMA","TFO_ITMOV")
EndIf

SaveInter()
Do Case
	Case cTpArma == "1"
		lRet := Conpad1( NIL,NIL,NIL,"TE0")
		
	Case cTpArma == "2"
		lRet := ConPad1(Nil,Nil,Nil,"TE1")
		
	Case cTpArma == "3"
		lRet := ConPad1(Nil,Nil,Nil,"TE2")
									
EndCase
RestInter()
RestArea(aArea)
Return (lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880DscArma

Realiza o preenchimento da descrição do Armamento
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880DscArma()
Local cDesc		:= ""
Local cLocal		:= ""
Local cDscLocal	:= ""
Local aArea		:= GetArea()
Local cArma		:= ""

Do Case
 	Case FwFldGet("TFO_ITMOV") == "1"
		
		DbSelectArea("TE0")
		TE0->(DbSetOrder(1))
		
		If TE0->(DbSeek(xFilial("TE0") + FwFldGet("TFO_ITCOD")))		
			cArma := TE0->TE0_CODPRO
			cDesc := Posicione("SB1",1,xFilial("SB1") + cArma,"SB1->B1_DESC")
			cLocal		:= TE0->TE0_LOCAL
			cDscLocal	:= TE0->TE0_CLIDES
			
			//Atribiu o valor para os campos de origem
			At880DscOrigem(cLocal,cDscLocal)
			
	 	EndIf
	 Case FwFldGet("TFO_ITMOV") == "2"
		
		DbSelectArea("TE1")
		TE1->(DbSetOrder(1))
		
		If TE1->(DbSeek(xFilial("TE1") + FwFldGet("TFO_ITCOD")))		
			cArma := TE1->TE1_CODPRO
			cDesc := Posicione("SB1",1,xFilial("SB1") + cArma,"SB1->B1_DESC")
			cLocal		:= TE1->TE1_LOCAL
			cDscLocal	:= TE1->TE1_CLIDES
			
			//Atribiu o valor para os campos de origem
			At880DscOrigem(cLocal,cDscLocal)
	 	EndIf
	
	Case FwFldGet("TFO_ITMOV") == "3"
		
		DbSelectArea("TE2")
		TE2->(DbSetOrder(1))
		
		If TE2->(DbSeek(xFilial("TE2") + FwFldGet("TFO_PRODUT") ))		
			cArma := TE2->TE2_CODPRO
			cDesc := Posicione("SB1",1,xFilial("SB1") + cArma,"SB1->B1_DESC")
			cLote		:= TE2->TE2_LOTE
			
			//Atribiu o valor para o campo de Lote
			At880DscOrigem(Nil,Nil,cLote)
	 	EndIf		
EndCase
RestArea(aArea)
Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880DscOrigem

Seta os valores para os campos de Origem e descrição da Origem
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880DscOrigem(cLocal,cDesc,cLote)
Local oModel	:= FWModelActive()

Default cLocal	:= ""
Default cDesc		:= ""
Default cLote		:= ""

If !Empty(cLocal)
	oModel:SetValue("TFQMASTER","TFQ_ORIGEM", cLocal)
EndIf

If !Empty(cDesc)
	oModel:SetValue("TFQMASTER","TFQ_DSCLOC", cDesc)	
EndIf

If !Empty(cLote)
	oModel:SetValue("TFQMASTER","TFO_LOTE", cLote)
EndIf

Return(.T.)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Agend

Verifica se a arma escolhida possui Movimentação Agendada

@param cItMov,caractere,Tipo de Armamento
@param cItCod,caractere,Código do Armamento
@sample At880Agend(cItMov,cItCod)
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880Agend(cItMov,cItCod)
	Local lRet		:= .T.
	Local cTabTMP	:= GetNextAlias()
	Local cStatus	:= stAGENDADA
	Local aArea	:= GetArea()

	BeginSQL alias cTabTMP

		column NCONT as Numeric(3,0)
				
		SELECT COUNT(TFO.TFO_ITCOD) AS NCONT
		
		FROM %Table:TFQ% TFQ
		
		INNER JOIN %Table:TFO% TFO ON
		TFO.TFO_FILIAL = TFQ.TFQ_FILIAL
		AND TFO.TFO_CDMOV = TFQ.TFQ_CODIGO
		AND TFO.%NotDel%
		
		WHERE
		TFQ.TFQ_FILIAL = %xfilial:TFQ%
		AND TFO.TFO_ITMOV = %exp:cItMov%
		AND TFO.TFO_ITCOD = %exp:cItCod%
		AND TFQ.TFQ_STATUS = %exp:cStatus%
		AND TFQ.%NotDel%
		
	EndSQL
	
	While (cTabTMP)->(!Eof())
		lRet := (cTabTMP)->NCONT > 0
		(cTabTMP)->(dbSkip())
	EndDo

	(cTabTMP)->(dbCloseArea())
RestArea(aArea)
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldAloc

Verifica se a arma escolhida possui alocação na origem

@param cItMov,caractere,Item de Armamento
@param cItCod,caractere,Código do armamento

@sample At880VldAloc(cItMov,cItCod)
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldAloc(cItMov,cItCod,lVldLaXLo)
Local lRet		:= .T.
Local aArea	:= GetArea()	
Local cTab		:= ''
Local cEnt		:= ''
Local cCdEnt	:= '' 	
Local lMultFil	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm() .And. !isBlind()
Local cFilLoc	:= ""

Default cItMov	:= ''
Default cItCod	:= ''
Default lVldLaXLo	:= .F.

Do Case
	Case cItMov == itARMA
		cTab := "TE0"
	Case cItMov == itCOLETE
		cTab := "TE1"
	Case cItMov == itMUNICAO
		cTab := "TE2"
EndCase

If cTab $ "TE0;TE1"
	DbSelectArea(cTab)
	(cTab)->(DbSetOrder(1))
			
	If (cTab)->(DbSeek(xFilial(cTab) + cItCod))
		cEnt	:= (cTab)->&(cTab + '_ENTIDA')
		cEnt	:= If( cEnt == 'TER', '1' , '2' )
		cCdEnt	:= (cTab)->&(cTab + '_LOCAL')
		If lMultFil
			cFilLoc	:= (cTab)->&(cTab + '_FILLOC')			
		Endif					
		If Empty(cCdEnt)
				lRet := .F.
		ElseIf lVldLaXLo
			If lMultFil
				lRet := (cEnt == FWFLDGET('TFQ_ENTORI')) .and. (cCdEnt == FWFLDGET('TFQ_ORIGEM')) .and. (cFilLoc == FWFLDGET('TFQ_FILORI'))
			Else
				lRet := (cEnt == FWFLDGET('TFQ_ENTORI')) .and. (cCdEnt == FWFLDGET('TFQ_ORIGEM')) 
			EndIf
		EndIf
	EndIf

ElseIf cTab == "TE2"
	
	DbSelectArea("TFP")	// HISTORICO MOVIMENTACAO MUNICAO
	If FwFldGet("TFQ_ENTORI") == "1"
		TFP->(DbSetOrder(7)) //TFP_FILIAL+TFP_ENTIDA+TFP_CODINT+TFP_PRODUT+TFP_FILLOC
	Else
		TFP->(DbSetOrder(8)) //TFP_FILIAL+TFP_ENTIDA+TFP_CODLOC+TFP_PRODUT+TFP_FILLOC
	EndIf
	If !TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + FwFldGet('TFO_PRODUT')+ Iif(lMultFil,FwFldGet('TFQ_FILORI'),"")))
		Help( ,, "At880VIMV",, STR0072, 1, 0 )//'Alocação','Esse armamento não está alocado na Origem informada!'
		lRet := .F.
	EndIf

EndIf

RestArea(aArea)
	 
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldGuia

Verifica se o Numero da Guia ou a justificativa foram preenchidas
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880VldGuia(oMdl)
Local lRet	:= .T.
Local nI := 0

Default oMdl := nil

If !Empty(oMdl)
	For nI := 1 To oMdl:Length()
		oMdl:GoLine(nI)

		//Verifica se o campo Numero da Guia está preenchido
		If		!oMdl:IsDeleted() .and. oMdl:GetValue('TFO_LGUIA') == '1' .and. Empty(oMdl:GetValue('TFO_NRGUIA'))
			Help( ,, "At880VldGuia",, STR0012, 1, 0 ) //'Alocação','Preencha o Numero da Guia'
			lRet	:= .F.
			//Verifica se o ocampo Justificativa está preenchido
		ElseIf	!oMdl:IsDeleted() .and. oMdl:GetValue('TFO_LGUIA') == '2' .and. Empty(FwFldGet("TFQ_JUSTIF"))
			Help( ,, "At880VldGuia",, STR0013, 1, 0 ) //'Alocação','Preencha a Justificativa'
			lRet	:= .F.
		EndIf
	
		// Havendo um caso, abandona.
		If !lRet
			Exit	
	EndIf	
	Next nI
EndIf
		
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldStat

Verifica o Status da Arma ou Colete e verifica se é possivel a sua movimentação

@param cItMov,caractere,Tipo do Armamento
@param cItCod,caractere,Codigo do armamento
@param cMotivo,caractere,Motivo da movimentação

@sample At880VldStat(cItMov,cItCod,cMotivo)

@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880VldStat(cItMov, cItCod, cMotivo)
Local lRet  := .F.
Local aArea := GetArea()

Default cItCod := FwFldGet("TFQ_ITARMA")

If Empty(cMotivo)
	Help( ,, "At880VldStat",, STR0064, 1, 0 ) //'Alocação','Informe o Motivo para a Movimentação!'

Else
	Do Case
		Case cItMov == itARMA
			DbSelectArea("TE0")
			TE0->(DbSetOrder(1))
			If TE0->(DbSeek(xFilial("TE0") + cItCod))

				//Verifica o tipo do motivo e verifica o Status		
				If cMotivo $ mtIMPLANTACAO + mtMANUTENCAO + mtREFORCO + mtENCERRAMENTO
					lRet := TE0->TE0_SITUA $ "1,2,8"
				ElseIf cMotivo == mtDESCARTE
					lRet := TE0->TE0_SITUA $ "1,2,8,9"
				EndIf
				If !lRet
					Help( ,, "At880VldStat",, STR0014, 1, 0 ) //'Alocação','O Status do Armamento não permite movimentação!'
				EndIf
			EndIf

		Case cItMov == itCOLETE
			DbSelectArea("TE1")
			TE1->(DbSetOrder(1))
			If TE1->(DbSeek(xFilial("TE1") + cItCod))

				//Verifica o tipo do motivo e verifica o Status		
				If cMotivo $ mtIMPLANTACAO + mtMANUTENCAO + mtREFORCO + mtENCERRAMENTO
					lRet := TE1->TE1_SITUA $ "1,2,8"
				ElseIf cMotivo == mtDESCARTE
					lRet := TE1->TE1_SITUA $ "1,2,8,9"
				EndIf
				If !lRet
					Help( ,, "At880VldStat",, STR0014, 1, 0 )//'Alocação','O Status do Armamento não permite movimentação!'
				EndIf
			EndIf

		Case cItMov == itMUNICAO
			DbSelectArea("TE2")
			TE2->(DbSetOrder(1))
			lRet := TE2->(DbSeek(xFilial("TE2") + cItCod))
	EndCase
EndIf
RestArea(aArea)

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldSaldo
	Verifica o Saldo do local de origem informado
@param oMdl,objeto,Objeto de dados sendo editado.

@sample At880VldSaldo(oMdl)
@author Serviços
@since 28/08/13
@version P11 R9

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880VldSaldo(oMdl)
Local lRet			:= .T.
Local aArea		:= GetArea()
Local cItCod		:= oMdl:GetValue('TFO_ITCOD')
Local nQtde		:= oMdl:GetValue('TFO_QTDE')
Local cLote := ""

If FwFldGet("TFQ_ENTORI") == "1"
	//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
	DbSelectArea("TFP")
	TFP->(DbSetOrder(2))
Else
	//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
	DbSelectArea("TFP")
	TFP->(DbSetOrder(4))
EndIf

//Verifica se a quantidade informada é maior que o saldo do blister	
If TE2->(DbSeek(xFilial("TE2") + cItCod))	
	If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cLote))
		If TFP->TFP_SALDO < nQtde
			Help( ,, "At880VldSaldo",, STR0018, 1, 0 )//'Alocação',"A quantidade informada é maior do que está alocado ou é superior ao saldo disponivel para movimentação no Blister relacionado."
			lRet := .F.
		EndIf
	EndIf
EndIf

RestArea(aArea)			

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AtVldLocMun

Valida se o local informado realmente existe para a munição informada
@sample AtVldLocMun(cCodigo) 
@author Serviços
@since 28/08/13
@version P11 R9
@Param cCodigo, Codigo do Local informado no campo TFQ_ORIMUN

@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Function AtVldLocMun(cCodigo)
Local lRet		:= .T.
Local nY		:= 0
Local aArea	:= GetArea()

aCmpBco := At880QryLoc()
	
If FwFldGet("TFQ_ENTORI") == "1"
		//Faz a Busca para Locais Internos
	nY := aScan(aCmpBco,{|x| AllTrim(x[3]) == Alltrim(cCodigo)})
		
	If nY == 0
		Help( ,, "AtVldLocMun",, STR0020, 1, 0 )//"Codigo",'O codigo informado não está relacionado'
		lRet := .F.
	EndIf
Else
		//Faz a Busca para Locais de Atendimento
	nY := aScan(aCmpBco,{|x| AllTrim(x[4]) == Alltrim(cCodigo)})
		
	If nY == 0
		Help( ,, "AtVldLocMun",, STR0020, 1, 0 )//"Codigo",'O codigo informado não está relacionado'
		lRet := .F.
	EndIf
		
EndIf

RestArea(aArea)

Return(lRet)
//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldLoc

Verifica se o local de destino não é igual ao de origem
@author Matheus.Gonçalves
@since 12/02/2021
@version P12 

@return lRet Retorna .F. se houver inconsistência 
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldLoc()
Local lRet 	:= .T.
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local lVldEnt	:= Alltrim(FwFldGet("TFQ_ENTORI")) == "1" .And. Alltrim(FwFldGet("TFQ_ENTDES")) == "1" .Or.;
				   Alltrim(FwFldGet("TFQ_ENTORI")) == "2" .And. Alltrim(FwFldGet("TFQ_ENTDES")) == "2"

If !lVldEnt
	If lMultFil 
		If Alltrim(FwFldGet("TFQ_FILDES")) == Alltrim(FwFldGet("TFQ_FILORI")) .And.; 
		   Alltrim(FwFldGet("TFQ_DESTIN")) == Alltrim(FwFldGet("TFQ_ORIGEM")) 
		   	Help( ,, "At880VldLoc",, STR0021, 1, 0 )//'Alocação','Local de Origem é o mesmo de Destino'
			lRet := .F.	
		EndIf	
	Else
		If Alltrim(FwFldGet("TFQ_DESTIN")) == Alltrim(FwFldGet("TFQ_ORIGEM")) 
		   	Help( ,, "At880VldLoc",, STR0021, 1, 0 )//'Alocação','Local de Origem é o mesmo de Destino'
			lRet := .F.
		EndIf	
	EndIf
EndIf                         

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldCons

Validação do campo de codigo das Armas conforme o que foi selecionado no campo TEU_TPARMA
@sample 	At880VldCons() 
@since		11/09/2013 
@version P11 R9
     
@return 	lRet, Retorno do ExistCPO de acordo com o que foi selecionado no campo TEU_TPARMA
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldCons()
Local lRet	:= .F.
Do Case
	Case FwFldGet("TFO_ITMOV") == "1"
		lRet := ExistCpo("TE0")

	Case FwFldGet("TFO_ITMOV") == "2"
		lRet := ExistCpo("TE1")
		
	Case FwFldGet("TFO_ITMOV") == "3"
		lRet := ExistCpo("TE2")
	
EndCase
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FA880ENT

Chama a Consulta Padrão conforme o que foi selecionado no campo TFQ_ENTDES
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function FA880ENT(cChave)
Local oModel		:= FwModelActive()
Local oModelTFQ 	:= oModel:GetModel('TFQMASTER')
Local cTpEntOri	:= oModelTFQ:GetValue("TFQ_ENTORI") 
Local cTpEntDes	:= oModelTFQ:GetValue("TFQ_ENTDES")
Local lRet			:= .T.
Local aArea		:= GetArea()
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()
Local lInclui		:= oModelTFQ:GetOperation() == MODEL_OPERATION_INSERT
Default cChave	:= ""

SaveInter()

If cChave == 'ORIGEM'
	If lMultFil .And. oModelTFQ:GetValue("TFQ_FILORI") <> cFilAnt
		cFilAnt := oModelTFQ:GetValue("TFQ_FILORI")
	Endif
	If cTpEntOri == eoLOCATEND
		If lGsOrcArma
			lRet := ConsRES("LOCTXQ","C",FwfldGet("TFQ_CONTRT"),FwfldGet("TFQ_ENTORI"),FwfldGet("TFQ_ORIGEM"),FwfldGet("TFQ_ENTDES"),FwfldGet("TFQ_DESTIN"))
	  	else
			lRet := Conpad1( NIL,NIL,NIL,"ABS")			
		Endif	
	ElseIf cTpEntOri == eoLOCINTER	
		lRet := ConPad1(Nil,Nil,Nil,"TER")
	EndIf
ElseIF lGsOrcArma .And. cChave == 'ARMA' 
	lRet := ConsRES("ARMA","",FwfldGet("TFQ_CONTRT"),FwfldGet("TFQ_ENTORI"),FwfldGet("TFQ_ORIGEM"),FwfldGet("TFQ_ENTDES"),FwfldGet("TFQ_DESTIN"))
ElseIF lGsOrcArma .And.  cChave == 'COLETE' 
	lRet := ConsRES("COLETE","",FwfldGet("TFQ_CONTRT"),FwfldGet("TFQ_ENTORI"),FwfldGet("TFQ_ORIGEM"),FwfldGet("TFQ_ENTDES"),FwfldGet("TFQ_DESTIN"))
ElseIF lGsOrcArma .And. cChave == 'MUNI'
	lRet := ConsRES("MUNI","",FwfldGet("TFQ_CONTRT"),FwfldGet("TFQ_ENTORI"),FwfldGet("TFQ_ORIGEM"),FwfldGet("TFQ_ENTDES"),FwfldGet("TFQ_DESTIN"),lInclui)
Else
	If lMultFil .And. oModelTFQ:GetValue("TFQ_FILDES") <> cFilAnt
		cFilAnt := oModelTFQ:GetValue("TFQ_FILDES")
	Endif
	If cTpEntDes == edCLIENTE
		If	lGsOrcArma
			lRet := ConsRES("LOCTXQ","C",FwfldGet("TFQ_CONTRT"),FwfldGet("TFQ_ENTORI"),FwfldGet("TFQ_ORIGEM"),FwfldGet("TFQ_ENTDES"),FwfldGet("TFQ_DESTIN"))
		else
			lRet := Conpad1( NIL,NIL,NIL,"ABS")
		Endif		
	ElseIf	cTpEntDes == edEMPRESA	
		lRet := ConPad1(Nil,Nil,Nil,"TER")
	EndIf
EndIf
If lMultFil .And. cFilBkp <> cFilAnt
	cFilAnt := cFilBkp
Endif

RestInter()
RestArea(aArea)
Return (lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880RetEnt

Retorna a variavel da memoria do model, para a consulta especifica
@sample 	At880RetEnt() 
@since		11/09/2013
@version P11 R9
     
@return 	cCodigo, CHARACTER, codigo do Armamento.
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880RetEnt(cChave)
Local oModel		:= FwModelActive()
Local oModelTFQ 	:= oModel:GetModel('TFQMASTER')
Local cTpEntOri	:= oModelTFQ:GetValue("TFQ_ENTORI") 
Local cTpEntDes	:= oModelTFQ:GetValue("TFQ_ENTDES")
Local cCodigo	:= ""	
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()	

Default cChave	:= ""

If cChave == 'ORIGEM'
	If		cTpEntOri == eoLOCATEND 
		If  lGsOrcArma
			cCodigo := cArmaLocal
		Else
			cCodigo := ABS->ABS_LOCAL
		Endif
	ElseIf	cTpEntOri == eoLOCINTER	
		cCodigo := TER->TER_CODIGO
	EndIf
Else
	If		cTpEntDes == edCLIENTE  
		cCodigo := ABS->ABS_LOCAL
	ElseIf	cTpEntDes == edEMPRESA	
		cCodigo := TER->TER_CODIGO
	EndIf
EndIf


If (lGsOrcArma .And. !Empty(cArmaLocal) .And. cChave != 'ORIGEM' .And. cTpEntDes != '2' ) .Or. cChave $ "|ARMA|COLETE|MUNI|"
	cCodigo := cArmaLocal
Elseif 	lGsOrcArma .And. !Empty(cArmaLocal) .And. cChave != 'ORIGEM' .And. cTpEntDes == '2'
	cCodigo := TER->TER_CODIGO
Elseif 	lGsOrcArma .And. !Empty(cArmaLocal) .And. cChave == 'ORIGEM' .And. cTpEntDes == '1'	
	cCodigo := cArmaLocal
Endif

Return(cCodigo)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880LDsc

Realiza o preenchimento da descrição do Local

@param	cLocal,caractere,Tipo do Local de Atendimento

@sample At880LDsc(cLocal)
@author Serviços
@since 20/08/13
@version P11 R9
@return cRet,caractere,Retorna a descrição do Local selecionado
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880LDsc(cLocal)
Local cRet		:= ""
Local aArea		:= GetArea()
Local cCampo	:= ''
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt
Local oMdl		:= FWModelActive()
Local lGsOrcArma:= FindFunction("TecGsArma") .And. TecGsArma()

Default cLocal:= ""

If cLocal == 'ORIGEM'
	If lMultFil .And. oMdl:GetValue("TFQMASTER","TFQ_FILORI") <> cFilAnt
		cFilAnt := oMdl:GetValue("TFQMASTER","TFQ_FILORI")
	Endif
	cCampo	:= 'TFQ_ORIGEM'
	If FwFldGet('TFQ_ENTORI') == "1"
		cTab := 'TER'	// Locais Internos - Empresa
	ElseIf	FwFldGet('TFQ_ENTORI') == "2"
		cTab := 'ABS'	// Locais de Atendimento - Cliente
	EndIf
Else
	If lMultFil .And. oMdl:GetValue("TFQMASTER","TFQ_FILDES") <> cFilAnt
		cFilAnt := oMdl:GetValue("TFQMASTER","TFQ_FILDES")
	Endif
	cCampo	:= 'TFQ_DESTIN'
	If FwFldGet('TFQ_ENTDES') == "1"
		cTab := 'ABS'	// Locais de Atendimento - Cliente
	ElseIf	FwFldGet('TFQ_ENTDES') == "2"
		cTab := 'TER'	// Locais Internos - Empresa
	EndIf
EndIf
cRet := Alltrim(Posicione(cTab,1,xFilial(cTab)+FwFldGet(cCampo),cTab+"_DESCRI"))
If lMultFil .And. cFilBkp <> cFilAnt
	cFilAnt := cFilBkp
Endif

If lGsOrcArma .And. cTab == "ABS"
	//Ajuste do TFQ_CODTFL caso a variavel statica esteja vazia (valor digitado na mão sem uso do F3)
	If Empty(cCodigoTFL)
		AT880TFLdg(oMdl,cCampo)
	EndIf
EndIf

RestArea(aArea)
Return(cRet)
		
//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldOri
	 
Validação do campo de codigo de origem
@sample 	At880VldOri() 
@since		08/07/2015 
@version P11 R9
		
@return 	lRet, Retorno do ExistCPO de acordo com a configuração do campo TFQ_ENTORI e com o código informado no TFQ_ORIGEM
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldOri(oMdl,cIdCall)
Local lRet	:= .F.
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt

If lMultFil .And. oMdl:GetValue("TFQ_FILORI") <> cFilAnt
	cFilAnt := oMdl:GetValue("TFQ_FILORI")
Endif

Do Case
	Case FwFldGet("TFQ_ENTORI") == eoLOCINTER
		lRet := ExistCpo("TER",oMdl:GetValue(cIdCall))

	Case FwFldGet("TFQ_ENTORI") == eoLOCATEND
		lRet := ExistCpo("ABS",oMdl:GetValue(cIdCall))

EndCase

If lMultFil .And. cFilBkp <> cFilAnt
	cFilAnt := cFilBkp
Endif
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldEnt

Validação do campo de codigo de destino
@sample 	At780VldEnt() 
@since		11/09/2013 
@version P11 R9
     
@return 	lRet, Retorno do ExistCPO de acordo com o que foi selecionado no campo TEU_TPARMA
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldEnt()
Local lRet	:= .F.
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt
Local oMdl		:= FWModelActive()

If lMultFil .And. oMdl:GetValue("TFQMASTER","TFQ_FILDES") <> cFilAnt
	cFilAnt := oMdl:GetValue("TFQMASTER","TFQ_FILDES")
Endif
Do Case
	Case FwFldGet("TFQ_ENTDES") == edCLIENTE
		lRet := ExistCpo("ABS")

	Case FwFldGet("TFQ_ENTDES") == edEMPRESA
		lRet := ExistCpo("TER")
	
EndCase
If lMultFil .And. cFilBkp <> cFilAnt
	cFilAnt := cFilBkp
Endif
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Resp

Realiza o preenchimento da descrição do Responsavel
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Resp()
Local cDesc		:= ""
Local aArea		:= GetArea()

cDesc := Alltrim(Posicione("AA1",1,xFilial("AA1")+FwFldGet("TFQ_RESTRA"),"AA1_NOMTEC"))

RestArea(aArea)

Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Atend

Realiza o preenchimento do nome e dados do atendente responsavel pelo armamento
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Atend()
Local cDesc		:= ""
Local aArea		:= GetArea()
Local oModel		:= Nil

DbSelectArea("AA1")
AA1->(DbSetOrder(1))

If AA1->(DbSeek(xFilial("AA1")+FwFldGet("TFR_CODTEC")))
	oModel := FWModelActive()

	oModel:setValue("TFRFUNC","TFR_FUNCAO" 	,Posicione("SRJ",1,xFilial("SRJ")+AA1->AA1_FUNCAO,"RJ_DESC"))
	oModel:setValue("TFRFUNC","TFR_CDFUNC"	,AA1->AA1_CDFUNC)
	oModel:setValue("TFRFUNC","TFR_TURNO"	,AA1->AA1_TURNO)

	cDesc := AA1->AA1_NOMTEC
	
EndIf

RestArea(aArea)

Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IniArma 

Realiza a Inicialização padrão do armamento
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpC:Retorna a descrição da Arma
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880IniArma(cItem,cArma)
Local aArea := GetArea()
Local cProd := ""
Local cDesc := ""
Local nTamD := FwTamSx3("TFO_DESCR")[1]

Default cItem := ""
Default cArma := ""

If !Empty(cItem) .And. !Empty(cArma)
	Do Case
		Case cItem == "1"
			// Arma
			cProd := Posicione("TE0",1,xFilial("TE0") + cArma ,"TE0->TE0_CODPRO")	
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
		Case cItem == "2"
			// Colete
			cProd := Posicione("TE1",1,xFilial("TE1") + cArma ,"TE1->TE1_CODPRO")
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
		Case cItem == "3"
			// Municao
			cProd := Posicione("TE2",1,xFilial("TE2") + cArma ,"TE2->TE2_CODPRO")
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
	EndCase
	cDesc := Padr(cDesc,nTamD)
EndIf

RestArea(aArea)
Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IniDscLoc 

Realiza a Inicialização padrão da descrição do local 
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpC:Retorna a descrição do local de origem
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880IniDscLoc(cEntida,cLocal)
Local cDesc		:= ""
Local aArea		:= GetArea()
Local lModoInc	:= .F.
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt

Default cEntida		:= ""
Default cLocal		:= ""

If Type('INCLUI') == 'L'
	lModoInc := INCLUI
EndIf 

If !lModoInc .And. !Empty(cEntida) .And. !Empty(cLocal)
	If lMultFil .And. TFQ->TFQ_FILORI <> cFilAnt
		cFilAnt := TFQ->TFQ_FILORI
	Endif
	Do Case
		Case cEntida == eoLOCINTER
			cDesc := Alltrim(Posicione("TER",1,xFilial("TER")+ cLocal ,"TER_DESCRI"))

		Case cEntida == eoLOCATEND
			cDesc := Alltrim(Posicione("ABS",1,xFilial("ABS")+ cLocal ,"ABS_DESCRI"))
	
	EndCase
	If lMultFil .And. cFilBkp <> cFilAnt
		cFilAnt := cFilBkp
	Endif
EndIf

RestArea(aArea)

Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IniEnt 

Realiza a Inicialização padrão da descrição do local destino
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpC:Retorna a descrição do Local de destino
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880IniEnt(cEntida,cLocal)
Local cDesc		:= ""
Local aArea		:= GetArea()
Local lModoInc	:= .F.
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()
Local cFilBkp	:= cFilAnt

Default cEntida		:= ""
Default cLocal		:= ""

If Type('INCLUI') == 'L'
	lModoInc := INCLUI
EndIf 

If !lModoInc .And. !Empty(cEntida) .And. !Empty(cLocal)
	If lMultFil .And. TFQ->TFQ_FILDES <> cFilAnt
		cFilAnt := TFQ->TFQ_FILDES
	Endif
	Do Case
		Case cEntida == edCLIENTE
			cDesc := Alltrim(Posicione("ABS",1,xFilial("ABS")+ cLocal ,"ABS_DESCRI"))

		Case cEntida == edEMPRESA
			cDesc := Alltrim(Posicione("TER",1,xFilial("TER")+ cLocal ,"TER_DESCRI"))
	
	EndCase
	If lMultFil .And. cFilBkp <> cFilAnt
		cFilAnt := cFilBkp
	Endif
EndIf
RestArea(aArea)
Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880WMnt

Habilita ou desabilita os campos de manutenção 
@sample 	At880WMnt() 
@since		11/09/2013 
@version P11 R9
     
@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880WMnt()                                                   
Local lRet := .F.

If INCLUI .and. (FwFldGet('TFQ_MOTIVO') == mtMANUTENCAO)
	lRet := .T.
EndIf

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880ConsLoc

Realiza a Consulta especifica para os Locais de origem da Munição
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880ConsLoc()
Local lRet			:= .T.
Local cCodigo		:= ""
Local cEntida		:= ""
Local cLocal		:= ""
Local lOk			:= .F.
Local aArea		:= GetArea()

Local oModel				//Modelo atual
Local oDlgCmp				//Dialog
Local oPanel 				//Objeto Panel
Local oFooter				//Rodapé
Local oListBox			//Grid campos
Local oOk					//Objeto Confirma 
Local oCancel				//Objeto Cancel

aCmpBco := At880QryLoc()

If !Empty(aCmpBco)

	//	Cria a tela para a pesquisa dos campos e define a area a ser utilizada na tela 
	Define MsDialog oDlgCmp FROM 000, 000 To 350, 550 Pixel
			
	// Cria o panel principal
	@ 000, 000 MsPanel oPanel Of oDlgCmp Size 250, 340 // Coordenada para o panel
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT //Indica o preenchimento e alinhamento do panel (nao necessita das coordenadas)
		
	// Criação do grid para o panel	
	oListBox := TWBrowse():New( 40,05,204,100,,{STR0019,STR0022,STR0023,STR0024,STR0025,STR0026,STR0027},,oPanel,,,,,,,,,,,,.F.,,.T.,,.F.,,,) // "Codigo","Entidade","Codigo Interno","Codigo do Local", "Movimentação", "Saldo", "Lote"
	oListBox:SetArray(aCmpBco) // Atrela os dados do grid com a matriz
	oListBox:bLine := { ||{aCmpBco[oListBox:nAT][1],aCmpBco[oListBox:nAT][2],aCmpBco[oListBox:nAT][3],aCmpBco[oListBox:nAT][4],aCmpBco[oListBox:nAT][5],aCmpBco[oListBox:nAT][6],aCmpBco[oListBox:nAT][7]}} // Indica as linhas do grid
	oListBox:bLDblClick := { ||Eval(oOk:bAction), oDlgCmp:End()} // Duplo clique executa a ação do objeto indicado
	oListBox:Align := CONTROL_ALIGN_ALLCLIENT //Indica o preenchimento e alinhamento do browse
		
	// Cria o panel para os botoes	
	@ 000, 000 MsPanel oFooter Of oDlgCmp Size 000, 010 // Corrdenada para o panel dos botoes (size)
	oFooter:Align   := CONTROL_ALIGN_BOTTOM //Indica o preenchimento e alinhamento do panel (nao necessita das coordenadas)
			
	// Botoes para o grid auxiliar	
	@ 000, 000 Button oCancel Prompt STR0028  Of oFooter Size 030, 000 Pixel //Cancelar
	oCancel:bAction := { || lOk := .F., oDlgCmp:End() }
	oCancel:Align   := CONTROL_ALIGN_RIGHT
		
	@ 000, 000 Button oOk     Prompt STR0029 Of oFooter Size 030, 000 Pixel //Confirmar
	oOk:bAction     := { || lOk := .T.,cCodigo:=aCmpBco[oListBox:nAT][3],cEntida:=aCmpBco[oListBox:nAT][2],cLocal:=aCmpBco[oListBox:nAT][4],oDlgCmp:End() } // Acao ao clicar no botao
	oOk:Align       := CONTROL_ALIGN_RIGHT // Alinhamento do botao referente ao panel
		
		// Ativa a tela exibindo conforme a coordenada
	Activate MsDialog oDlgCmp Centered
			
	//Utilizar o modelo ativo para substituir os valores das variaves de memoria		
	oModel	:= FWModelActive()
		
	If lOk
		If cEntida == "1"
			cTab	:= "TER"
			oModel:SetValue("TFQMASTER","TFQ_ENTIDA", cEntida)
			oModel:SetValue("TFQMASTER","TFQ_ORIMUN", cCodigo)
		ElseIf cEntida == "2"
			cTab := "ABS"
			oModel:SetValue("TFQMASTER","TFQ_ENTIDA", cEntida)
			oModel:SetValue("TFQMASTER","TFQ_ORIMUN", cLocal)
		EndIf
	EndIf
Else
	Help( ,, "At880ConsLoc",, STR0030, 1, 0 )//"Alocação,'Não há alocação para essa munição, selecione outro lote'
EndIf

RestArea(aArea)

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880RetLoc

Retorna a variavel da memoria do model, para a consulta especifica
@sample 	AT840RetLoc() 
@since		11/09/2013
@version P11 R9
     
@return 	cCodigo, CHARACTER, conteudo da variavel de memoria.
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880RetLoc()
Return (FwFldGet("TFQ_ORIMUN"))

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880QryLoc

Realiza a Query para a consulta especifica do Local de Alocação da Munição
@sample 	At780Query() 
@since		11/09/2013 
@version P11 R9
     
@return 	aRet, Array com os Responsaveis
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880QryLoc()
Local aRet		:= {}
Local cAlias	:= GetNextAlias()
Local cCond	:= FwFldGet("TFQ_ITARMA")	//Codigo do Armamento
Local cEntida	:= FwFldGet("TFQ_ENTORI") 	//1=Local Interno, 2=Local de Atendimento
Local cMovim	:= ""							//1=Saldo Cofre,2=Saldo no Cliente(Local de Atendimento)	

If Select("cAlias") > 0 
	(cAlias)->(DbCloseArea())
Endif

If !Empty(cEntida)
	
	If cEntida == "1"
		cMovim := "1"
	ElseIf cEntida == "2"
		cMovim := "2"
	EndIf
	
	BeginSQL alias cAlias		
		SELECT TFP.*
		FROM
			%Table:TFP% TFP
		WHERE
			TFP.TFP_FILIAL = %xfilial:TFP%
			AND
			TFP.TFP_ENTIDA = %Exp:cEntida% 
			AND
			TFP.TFP_MOVIM = %Exp:cMovim% 
			AND
			TFP.TFP_SALDO > 0
			AND
			TFP.TFP_CODMUN = %Exp:cCond%
			AND 
			TFP.%NotDel%  	
	EndSQL	
	
	While (cAlias)->(!Eof())
	
		AAdd( aRet, { 	(cAlias)->TFP_CODIGO,;
							(cAlias)->TFP_ENTIDA,;
							(cAlias)->TFP_CODINT,;
							(cAlias)->TFP_CODLOC,;
							(cAlias)->TFP_MOVIM,;
							(cAlias)->TFP_SALDO,;
							(cAlias)->TFP_LOTE } )
		
		(cAlias)->(DbSkip())
		
	EndDo
	(cAlias)->(DbCloseArea())
EndIf

Return(aRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880LocMun

Realiza o preenchimento da descrição do Local
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880LocMun()
Local cDescLoc 	:= ""
Local aArea		:= GetArea()

If FwFldGet("TFQ_ENTORI") == eoLOCINTER
	
	cDescLoc	:= Posicione("TER",1,xFilial("TER") + FwFldGet("TFQ_ORIMUN"),"TER->TER_DESCRI")
	
ElseIf FwFldGet("TFQ_ENTORI") == eoLOCATEND
	
	cDescLoc	:= Posicione("ABS",1,xFilial("ABS") + FwFldGet("TFQ_ORIMUN"),"ABS->ABS_DESCRI")
	
EndIf

RestArea(aArea)

Return(cDescLoc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldMov

Limpa os campos de descrição codigo dependendo da seleção do campo TFQ_ITMOV
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldMov()
Local lRet			:= .T.
Local oModel		:= FWModelActive()

If 	FwFldGet("TFQ_ITMOV") == itARMA .Or. FwFldGet("TFQ_ITMOV") == itCOLETE
	
	//Limpa o codigo da arma quando ela for alterada
	If !Empty(FwFldGet("TFQ_ITARMA"))
		oModel:LoadValue("TFQMASTER","TFQ_ITARMA", "")
		oModel:LoadValue("TFQMASTER","TFQ_DITEM", "")
	EndIf
	
	//Limpa a Origem da Arma
	If !Empty(FwFldGet("TFQ_ORIGEM")) .OR. !Empty(FwFldGet("TFQ_DSCLOC"))
		oModel:LoadValue("TFQMASTER","TFQ_ORIGEM", "")
		oModel:LoadValue("TFQMASTER","TFQ_DSCLOC", "")
	EndIf
	
	//Tira o valor do campo lote quando for selecionado Arma ou colete
	If !Empty(FwFldGet("TFQ_LOTE"))
		oModel:SetValue("TFQMASTER","TFQ_LOTE", "")
	EndIf
	
	If !Empty(FwFldGet("TFQ_ENTORI"))
		oModel:SetValue("TFQMASTER","TFQ_ENTORI", "")
	EndIf
	
	If !Empty(FwFldGet("TFQ_ORIMUN"))
		oModel:SetValue("TFQMASTER","TFQ_ORIMUN", "")
	EndIf
	
	If !Empty(FwFldGet("TFQ_DSCMUN"))
		oModel:LoadValue("TFQMASTER","TFQ_DSCMUN", "")
	EndIf
	
	If !Empty(FwFldGet("TFQ_QTDMUN"))
		oModel:LoadValue("TFQMASTER","TFQ_QTDMUN", 0)
	EndIf
	
ElseIf	FwFldGet("TFQ_ITMOV") == itMUNICAO 
	
	//Limpa o codigo da arma quando ela for alterada
	If !Empty(FwFldGet("TFQ_ITARMA"))
		oModel:LoadValue("TFQMASTER","TFQ_ITARMA", "")
		oModel:LoadValue("TFQMASTER","TFQ_DITEM", "")
	EndIf
	
	//Limpa a Origem da Arma
	If !Empty(FwFldGet("TFQ_ORIGEM")) .OR. !Empty(FwFldGet("TFQ_DSCLOC"))
		oModel:LoadValue("TFQMASTER","TFQ_ORIGEM", "")
		oModel:LoadValue("TFQMASTER","TFQ_DSCLOC", "")
	EndIf
	
	//Tira o valor do campo Origem e descrição da origem quando for selecionado munição
	If !Empty(FwFldGet("TFQ_ORIGEM"))
		oModel:SetValue("TFQMASTER","TFQ_ORIGEM", "")
	EndIf
	
	If !Empty(FwFldGet("TFQ_DSCLOC"))
		oModel:SetValue("TFQMASTER","TFQ_DSCLOC", "")
	EndIf	
		
EndIf
	
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880CleanEnt

Limpa os campos de descrição codigo dependendo da seleção do campo TFQ_ENTDES
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880CleanEnt()
Local lRet			:= .T.
Local oModel		:= FWModelActive()

//Antes de Limpar verifica se é manutenção, onde só será permitida
//a movimentação para a empresa
If FwFldGet("TFQ_ENTDES") == edCLIENTE .And. (FwFldGet("TFQ_MOTIVO") == mtMANUTENCAO .Or. FwFldGet("TFQ_MOTIVO") == mtDESCARTE .Or. FwFldGet("TFQ_MOTIVO") == mtENCERRAMENTO)
	Help( ,, "At880CleanEnt",, STR0031, 1, 0 )//'Para movimentação de Manutenção, Descarte e Encerramento, só será possivel selecionar a Empresa'
	lRet := .F.	
EndIf

If 	lRet .And. (FwFldGet("TFQ_ENTDES") == edCLIENTE .Or. FwFldGet("TFQ_ENTDES") == edEMPRESA)
	
	//Limpa o codigo de destino e sua descrição	
	If !Empty(FwFldGet("TFQ_DESTIN"))
		oModel:LoadValue("TFQMASTER","TFQ_DESTIN", "")
		oModel:LoadValue("TFQMASTER","TFQ_DDESTI", "")
	EndIf
			
EndIf
	
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880ClnOri

Limpa os campos de descrição codigo dependendo da seleção do campo TFQ_ENTORI
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880ClnOri()
Local lRet		:= .T.
Local oModel	:= FWModelActive()

If (FwFldGet("TFQ_ENTORI") == eoLOCINTER .Or. FwFldGet("TFQ_ENTORI") == eoLOCATEND)
	
	//Limpa o codigo de Origem e sua descrição	
	If !Empty(FwFldGet("TFQ_ORIMUN"))
		oModel:LoadValue("TFQMASTER","TFQ_ORIMUN", "")
		oModel:LoadValue("TFQMASTER","TFQ_DSCMUN", "")
	EndIf
			
EndIf
	
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880MntClean

Limpa os campos de descrição codigo dependendo da seleção do campo TFQ_ENTDES
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880MntClean()
Local lRet			:= .T.
Local oModel		:= FWModelActive()

oModel:LoadValue("TFQMASTER","TFQ_CDRESP", "")
oModel:LoadValue("TFQMASTER","TFQ_TPMNT" , "")
oModel:LoadValue("TFQMASTER","TFQ_MOTMNT", "")

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880SumIt
	Efetua uma contagem dos itens não deletados da grade.
	
@author Serviços
@param oMdl,objeto,Grade de Armamentos
@sample	At880SumIt(oMdl)
@since 20/08/13
@version P11 R9
@return nRet,numerico,A quantidade de itens não deletados.
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880SumIt(oMdl)
Local nRet	:= 0
Local nI	:= 0
Local cField := ""

If oMdl:HasField('TFO_ITCOD')
	cField := 'TFO_ITCOD'
ElseIf oMdl:HasField('TFO_PRODUT')
	cField := 'TFO_PRODUT'
EndIf

If !Empty(oMdl) .And. !Empty(cField)
	If	!( oMdl:IsEmpty() )
		For nI := 1 To oMdl:Length()
			oMdl:GoLine( nI )
			If !oMdl:IsDeleted() .and. !Empty(oMdl:GetValue(cField))
				nRet++
			EndIf

		Next nI
	EndIf
EndIf


Return nRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Status

Verifica a Data de movimentação para realizar a efetivação da movimentação
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Status(oMdl,lMuni)
Local lRet	:= .T.
Local nI := 0

Default oMdl := nil
Default lMuni := .F.

If FwFldGet("TFQ_STATUS") == stAGENDADA
	//Verifica se a data de movimentação é menor ou igual a database
	If FwFldGet("TFQ_DMOVIM") < DDataBase
		Help( ,, "At880Status",, STR0033, 1, 0 )//'Não é possivel agendar uma movimentação com data menor que a database'
		lRet := .F.
	EndIf
EndIf

If FwFldGet("TFQ_STATUS") == stEFETIVADA
	//Verifica se a data de movimentação é menor ou igual a database
	If FwFldGet("TFQ_DMOVIM") > DDataBase
		Help( ,, "At880Status",, STR0034, 1, 0 )//'A data de movimentação não pode ser maior que a database para ser efetivada'
		lRet := .F.
	EndIf	
EndIf

If lRet .and. !Empty(oMdl)
	For nI := 1 To oMdl:Length()

		oMdl:GoLine( nI )
		
		If !oMdl:IsDeleted()
			//Valida se a data de retorno foi preenchida quando foi selecionado previsão de retorno
			If oMdl:GetValue("TFO_LRET") == "1" .and. Empty(oMdl:GetValue("TFO_DTRET")) 
				Help( ,, "At880Status",, STR0035, 1, 0 ) //'Informe uma data de retorno'
				lRet := .F.	
			EndIf

			// Verifica a Data de Validade dos Armamentos
			If lRet .And. !lMuni
				lRet := At880VlArm(oMdl:GetValue("TFO_ITMOV"),oMdl:GetValue("TFO_ITCOD"),FwFldGet("TFQ_ENTORI"),FwFldGet("TFQ_ENTDES"))
			EndIf
		EndIf
				
		If !lRet
			Exit	// Havendo um caso, abandona.
		EndIf
		
	Next nI
EndIf

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880DtMovim

Valida a data de retorno e de movimentação
@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. quando houve sucesso na operação
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880DtMovim()
Local lRet	:= .T.

If !Empty(FwFldGet("TFQ_DTRETO"))
	If !FwFldGet("TFQ_DTRETO") > dDataBase
		Help( ,, "At880DtMovim",, STR0036, 1, 0 )
		lRet := .F.	
	EndIf
EndIf

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880SitArma

Função para alterar o Status das Armas
@sample 	At780SitArma() 
@since		11/09/2013 
@version P11 R9

@Param lIniciada,lFinalizado,Indica se o Status voltará ao antigo ou par Em Manutenção
     
@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880SitArma(cTpArma,cItArma,dRetorno)
Local aArea		:= GetArea()
Local cSubLoc		:= ""			//Codigo do SubLocal
Local cDscSub		:= ""			//Descrição do SubLocal
Local cStatus		:= ""			//Status de Armamento
Local oMdl			:= FwModelActive()
Local lMultFil 		:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()

Default cTpArma	:= FwFldGet("TFQ_ITMOV")
Default cItArma	:= FwFldGet("TFQ_ITARMA")
Default dRetorno	:= FwFldGet("TFQ_DTRETO")  

Do Case	
	Case oMdl:GetId() == "TECA880" .AND. cTpArma == itARMA
		
		DbSelectArea("TE0")
		TE0->(DbSetOrder(1))
							
		If TE0->(DbSeek(xFilial("TE0")+ cItArma))
			
			//Altera o Status para Implantado	
			RecLock("TE0",.F.)
			
			If FwFldGet("TFQ_ENTDES") == edCLIENTE
				REPLACE TE0_SITUA WITH "2"
			
				//Altera o local de alocação
				If lMultFil
					REPLACE TE0_FILLOC 	With FwFldGet("TFQ_FILDES")					
				Endif
				REPLACE TE0_LOCAL 	WITH FwFldGet("TFQ_DESTIN")
				REPLACE TE0_CLIDES 	WITH FwFldGet("TFQ_DDESTI")
					
				//Verifica se a origem é sublocal e grava o local
				cSubLoc := At880SubLoc(FwFldGet("TFQ_DESTIN"),@cDscSub)
				
				If !Empty(cSubLoc)
					REPLACE TE0_POSTAL 	WITH cSubLoc
					REPLACE TE0_DSCPOS	WITH cDscSub
				Else
					REPLACE TE0_POSTAL 	WITH ""
					REPLACE TE0_DSCPOS	WITH ""
				EndIf	
				
				REPLACE TE0_ENTIDA WITH "ABS"
				REPLACE TE0_DTALOC WITH FwFldGet("TFQ_DMOVIM")
				REPLACE TE0_PRVRET WITH dRetorno
				REPLACE TE0_CODMOV 	With FwFldGet("TFQ_CODIGO")
				
				//Grava o registro de Alteração do FREAR
				At880Hist(cItArma,Nil,.T.,.F.,"2")
				
			ElseIf FwFldGet("TFQ_ENTDES") == edEMPRESA
				
				If FwFldGet("TFQ_MOTIVO") == mtDESCARTE //Descarte
					//Retorna para Ativo
					REPLACE TE0_SITUA WITH "A"
					cStatus := "A"							
				Else
					//Retorna para Ativo
					REPLACE TE0_SITUA WITH "1"
					cStatus := "1"	
				EndIf
				
				//Altera o local de alocação
				If lMultFil
					REPLACE TE0_FILLOC 	With FwFldGet("TFQ_FILDES")					
				Endif
				REPLACE TE0_LOCAL 	WITH FwFldGet("TFQ_DESTIN")
				REPLACE TE0_CLIDES 	WITH FwFldGet("TFQ_DDESTI")
				REPLACE TE0_POSTAL 	WITH ""
				REPLACE TE0_DSCPOS	WITH ""	
				REPLACE TE0_ENTIDA WITH "TER"
				REPLACE TE0_DTALOC WITH FwFldGet("TFQ_DMOVIM")
				REPLACE TE0_PRVRET	WITH dRetorno
				REPLACE TE0_CODMOV 	With FwFldGet("TFQ_CODIGO")
				
				//Grava o registro de Alteração do FREAR
				At880Hist(cItArma,Nil,.T.,.F.,cStatus)
				
			EndIf
			
			MsUnLock()	
					
		EndIf
	Case oMdl:GetId() == "TECA880" .AND. cTpArma == itCOLETE
	
		DbSelectArea("TE1")
		TE1->(DbSetOrder(1))
							
		If TE1->(DbSeek(xFilial("TE1")+cItArma))
			
			//Altera o Status para Implantado	
			RecLock("TE1",.F.)
			
			If FwFldGet("TFQ_ENTDES") == edCLIENTE
				REPLACE TE1_SITUA WITH "2"
				
				//Altera o local de alocação
				If lMultFil
					REPLACE TE1_FILLOC 	With FwFldGet("TFQ_FILDES")					
				Endif
				REPLACE TE1_LOCAL 	WITH FwFldGet("TFQ_DESTIN")
				REPLACE TE1_CLIDES 	WITH FwFldGet("TFQ_DDESTI")
						
				//Verifica se a origem é sublocal e grava o local
				cSubLoc := At880SubLoc(FwFldGet("TFQ_DESTIN"),@cDscSub)
					
				If !Empty(cSubLoc)
					REPLACE TE1_POSTAL 	WITH cSubLoc
					REPLACE TE1_DSCPOS	WITH cDscSub
				Else
					REPLACE TE1_POSTAL 	WITH ""
					REPLACE TE1_DSCPOS	WITH ""
				EndIf	
					
				REPLACE TE1_ENTIDA WITH "ABS"
				REPLACE TE1_DTALOC WITH FwFldGet("TFQ_DMOVIM")
				REPLACE TE1_PRVRET WITH dRetorno
				REPLACE TE1_CODMOV 	With FwFldGet("TFQ_CODIGO")
				//Grava o registro de Alteração do FREAR
				At880Hist(Nil,cItArma,.F.,.T.,"2")
					
			ElseIf FwFldGet("TFQ_ENTDES") == edEMPRESA
					
				If FwFldGet("TFQ_MOTIVO") == mtDESCARTE //Descarte
					//Retorna para Ativo
					REPLACE TE1_SITUA WITH "A"
					cStatus := "A"							
				Else
					//Retorna para Ativo
					REPLACE TE1_SITUA WITH "1"
					cStatus := "1"	
				EndIf
				
				//Altera o local de alocação
				If lMultFil
					REPLACE TE1_FILLOC 	With FwFldGet("TFQ_FILDES")					
				Endif
				REPLACE TE1_LOCAL 	WITH FwFldGet("TFQ_DESTIN")
				REPLACE TE1_CLIDES 	WITH FwFldGet("TFQ_DDESTI")
				REPLACE TE1_POSTAL 	WITH ""
				REPLACE TE1_DSCPOS	WITH ""	
				REPLACE TE1_ENTIDA WITH "TER"
				REPLACE TE1_DTALOC WITH FwFldGet("TFQ_DMOVIM")
				REPLACE TE1_PRVRET	WITH dRetorno
				REPLACE TE1_CODMOV 	With FwFldGet("TFQ_CODIGO")
					
				//Grava o registro de Alteração do FREAR
				At880Hist(Nil,cItArma,.F.,.T.,cStatus)
				
			EndIf
				
			MsUnLock()	
						
		EndIf
	
	Case cTpArma == itMUNICAO
		//Atualiza o cadastro da munição
		RecLock("TE2",.F.)
		TE2->TE2_LOCAL 	:= oMdl:GetValue("TFQMASTER","TFQ_DESTIN")
		TE2->TE2_CLIDES := oMdl:GetValue("TFQMASTER","TFQ_DDESTI")
		MsUnlock()
					
EndCase
RestArea(aArea)
Return(.T.)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Hist

Atualiza a Tabela TIO para armazenar toda troca de Status dos Armamentos

@author Serviços
@since 20/08/13
@version P11 R9

@param cArma,Codigo da Arma
@Param cColete,Codigo do Colete
@Param lArma,Indica se deve Atualizar a Arma
@Param lColete,Indica se deve atualizar o Colete
@Param cStatus,Indica o Status que foi alterado

@return ExpL:Retorna .T. 
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880Hist(cArma, cColete, lArma, lColete, cStatus)
Local cMotivo	:= ""
Local aArea		:= GetArea()
Local cNum		:= GetSXENum("TIO","TIO_CODIGO")

Default cArma	:= ""
Default cColete	:= ""
Default lArma	:= .F.	
Default lColete	:= .F.
Default cStatus	:= ""

If cStatus == "2"
	//Concatena o Movimentação mais o codigo
	cMotivo	:= STR0037 + FwFldGet("TFQ_CODIGO") //"Movimentação para implantação, Codigo: "
ElseIf cStatus == "1" .Or. cStatus == "A"
	//Concatena Retorno mais o codigo
	cMotivo	:= STR0038 + FwFldGet("TFQ_CODIGO") //"Retorno para Empresa, Codigo: "		
EndIf

//Grava o TIO para controle do FREAR
DbSelectArea("TIO")
RecLock("TIO",.T.)
	REPLACE TIO_FILIAL 	With xFilial("TIO")
	REPLACE TIO_CODIGO 	With cNum
	REPLACE TIO_CODMOV 	With FwFldGet("TFQ_CODIGO")
	REPLACE TIO_DTALT 	With dDataBase		
	If lArma
		REPLACE TIO_ARMA 	With cArma
		REPLACE TIO_MOTIVO 	With cMotivo
		REPLACE TIO_FREAR	With cStatus 
	ElseIf lColete
		REPLACE TIO_COLETE 	With cColete
		REPLACE TIO_MOTIVO 	With cMotivo
		REPLACE TIO_FREAR	With cStatus
	EndIf
	REPLACE TIO_CODUSU 	With __cUserId
TIO->(MsUnlock())
TIO->(ConfirmSX8())

RestArea(aArea)
	
Return(.T.)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880SubLoc

Verifica se o destino possui sublocal para preencher
@sample 	At880SubLoc() 
@since		11/09/2013 
@version P11 R9

@Param cLocal, Codigo do Local de Atendimento
     
@return cCodLoc, Codigo do SubLocal
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880SubLoc(cLocal,cDesc)
Local cCodLoc	:= ""
Local aArea	:= GetArea()

DbSelectArea("ABS")
ABS->(DbSetOrder(1))
				
If ABS->(DbSeek(xFilial("ABS") + cLocal))
	If !Empty(ABS->ABS_LOCPAI)
		cCodLoc	:= ABS->ABS_LOCPAI
		If ABS->(DbSeek(xFilial("ABS") + cCodLoc))
			cDesc		:= ABS->ABS_DESCRI	
		EndIf	
	EndIf
EndIf

RestArea(aArea)

Return(cCodLoc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IncManut

Realiza a Inclusão da manutenção
@sample 	At880IncManut() 
@since		11/09/2013 
@version P11 R9

@Param cArma, Codigo do Armamento
     
@return lRet, Retorna .T. se houve sucesso
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880IncManut(oMdl,cTpArma,cItArma,nQtd)
Local lRet 	:= .T.
Local aCampos	:= {}
Local cCDRESP	:= oMdl:GetModel('TFQMASTER'):GetValue("TFQ_CDRESP")
Local dDMOVIM	:= oMdl:GetModel('TFQMASTER'):GetValue("TFQ_DMOVIM")
Local cTPMNT	:= oMdl:GetModel('TFQMASTER'):GetValue("TFQ_TPMNT")
Local cMOTMNT	:= oMdl:GetModel('TFQMASTER'):GetValue("TFQ_MOTMNT")
Local cDESTIN := oMdl:GetModel('TFQMASTER'):GetValue("TFQ_DESTIN")
Local cDDESTI	:= oMdl:GetModel('TFQMASTER'):GetValue("TFQ_DDESTI")
Local aArea	:= GetArea()
Local cCodMun	:= ""

Default cTpArma := ""
Default cItArma := ""
Default nQtd    := 0

Private lMsHelpAuto := .T.			// Variavel de controle interno do ExecAuto
Private lMsErroAuto := .F.	

If !Empty(cTpArma) .and. !Empty(cItArma)

	If cTpArma <> itMUNICAO
		//Monta o Array para inclusão da manutenção
		aCampos:= 	{{"TEU_CODIGO"		,GetSXENum("TEU","TEU_CODIGO")			,NIL},; //Codigo
					{"TEU_TPARMA"     	,cTpArma     								,NIL},; //Tipo da Arma
					{"TEU_CDARM"      	,cItArma					     			,NIL},; //Codigo da Arma
					{"TEU_CDRESP"			,cCDRESP					      			,NIL},; //Responsavel
					{"TEU_RESPON"			,At780DescRes(cCDRESP)					,NIL},; //Nome do Responsavel
					{"TEU_DTABER"			,dDMOVIM				        			,NIL},; //Data de Abertura
					{"TEU_TPMNT"			,cTPMNT				         			,NIL},; //Tipo de Manutenção
					{"TEU_LOCAL" 			,"2"         								,NIL},; //Manutenção no Local, 2 = Não
					{"TEU_MOTIVO"    		,cMOTMNT 									,NIL},;  //Motivo da Manutenção
					{"TEU_ORIGEM"    		,"TECA880" 								,NIL}}  //Origem da Manutenção
	ElseIf cTpArma == itMUNICAO .and. !Empty(nQtd)
		//Monta o Array para inclusão da manutenção
		
		TE2->(DbSetOrder(4))				
		TE2->(DbSeek(xFilial("TE2") + cItArma))
		cCodMun := TE2->TE2_CODMUN
		
		
		aCampos:= 	{{"TEU_CODIGO"		,GetSXENum("TEU","TEU_CODIGO")	,NIL},; //Codigo
					{"TEU_TPARMA"     	,cTpArma     					,NIL},; //Tipo da Arma
					{"TEU_CDARM"      	,cCodMun						,NIL},; //Codigo da Arma
					{"TEU_ENTORI"      	,"1"						    ,NIL},; //Entidade do local de Manutenção
					{"TEU_CODLOC"      	,cDESTIN     					,NIL},; //Codigo do Local de Manutenção
					{"TEU_DSCLOC"      	,cDDESTI					    ,NIL},; //Descrição do Local
					{"TEU_QTDMUN"      	,nQTD						    ,NIL},; //Quantidade de Munições
					{"TEU_CDRESP"		,cCDRESP					    ,NIL},; //Responsavel
					{"TEU_RESPON"		,At780DescRes(cCDRESP)			,NIL},; //Nome do Responsavel
					{"TEU_DTABER"		,dDMOVIM				        ,NIL},; //Data de Abertura
					{"TEU_TPMNT"		,cTPMNT				         	,NIL},; //Tipo de Manutenção
					{"TEU_LOCAL" 		,"2"         					,NIL},; //Manutenção no Local, 2 = Não
					{"TEU_MOTIVO"    	,cMOTMNT				 		,NIL},;  //Motivo da Manutenção
					{"TEU_ORIGEM"    	,"TECA880" 						,NIL}}  //Origem da Manutenção
		
	EndIf		
	
	If !Empty(aCampos)
		Processa( { || TECA780(aCampos,3) },STR0039,STR0040,.F.) //'Aguarde','Processando...'
			
		If lMsErroAuto
			If !isBlind()
				MostraErro()
			EndIf
			DisarmTransaction()
			lRet := .F.
		EndIf
			
		ConfirmSX8()
	EndIf
	
EndIf

RestArea(aArea)
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880MntCmp

Verifica se os campos de Manutenção estão preenchidos
@sample 	At880MntCmp() 
@since		11/09/2013 
@version P11 R9

@Param cArma, Codigo do Armamento
     
@return cCodLoc, Codigo do SubLocal
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880MntCmp()
Local lRet	:= .T.

// Se a movimentação é de manutenção...
If FwFldGet("TFQ_MOTIVO") == mtMANUTENCAO
 
	//Verifica se o campo de responsavel está preenchido
	If Empty(FwFldGet("TFQ_CDRESP"))
		Help( ,, "At880MntCmp",, STR0043, 1, 0 ) //'O campo de responsavel pela manutenção não está preenchido'
		lRet := .F.
	EndIf
	
	//Verifica se o campo tipo da manutenção está preenchido
	If lRet .And. Empty(FwFldGet("TFQ_TPMNT"))
		Help( ,, "At880MntCmp",, STR0044, 1, 0 ) //'O campo tipo da manutenção não está preenchido'
		lRet := .F.
	EndIf
	
	//Verifica se o campo de motivo da manutenção está preenchido
	If lRet .And. Empty(FwFldGet("TFQ_MOTMNT"))
		Help( ,, "At880MntCmp",, STR0045, 1, 0 )//'O campo motivo da manutenção não está preenchido'
		lRet := .F.
	EndIf

EndIf

Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880MntAbe

Verifica se a arma selecionada possui alguma manutenção em aberto
@sample 	At880MntAbe() 
@since		11/09/2013 
@version P11 R9

@Param cArma, Codigo do Armamento
     
@return cCodLoc, Codigo do SubLocal
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880MntAbe(cITMov,cCDMov)
Local lRet		:= .T.
Local aRet		:= {}
Local cAlias	:= GetNextAlias()
Local cArma	:= cCDMov
Local cTipo	:= cITMov
Local cStatus	:= "1"

If Select("cAlias") > 0 
	(cAlias)->(DbCloseArea())
Endif

BeginSQL alias cAlias		
	SELECT TEU.TEU_CODIGO, TEU.TEU_TPARMA, TEU.TEU_CDARM, TEU.TEU_CDRESP, TEU.TEU_DTABER 
	  FROM %Table:TEU% TEU
	 WHERE TEU.TEU_FILIAL = %xfilial:TEU%
	   AND TEU.TEU_TPARMA = %Exp:cTipo%
	   AND TEU.TEU_CDARM = %Exp:cArma%
	   AND TEU.TEU_STATUS = %Exp:cStatus%
	   AND TEU.%NotDel%	   	
EndSQL	

While (cAlias)->(!Eof())

	AAdd( aRet, { 	(cAlias)->TEU_CODIGO,;
						(cAlias)->TEU_CDARM } )
	
	(cAlias)->(DbSkip())
	
EndDo

(cAlias)->(DbCloseArea())

If Len(aRet) > 0
	Help( ,, "At880MntAbe",, STR0046, 1, 0 )	//'A Arma selecionada possui uma manutenção em aberto, Exclua ou encerre a manutenção'
	lRet := .F.
EndIf

Return(lRet)



//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880RetDados

Retorna os dados da Arma
@sample 	At880RetDados(cArma,cDesc,aCarac)
@since		11/09/2013 
@version P11 R9

@Param cArma, Codigo do Armamento
     
@return cCodLoc, Codigo do SubLocal
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880RetDados(cTpArma,cArma,cDesc,aCarac)
Local aArea		:= GetArea()
Local cMarca		:= ""
Local cEspecie	:= ""
Local cProd		:= ""

Do Case
	Case cTpArma == itARMA
		DbSelectArea("TE0")
		TE0->(DbSetOrder(1))
		
		If TE0->(DbSeek(xFilial("TE0") + cArma ))
			cProd := TE0->TE0_CODPRO	
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
			cMarca	:= Posicione("SX5",1,xFilial("SX5")+"79"+TE0->TE0_MARCA,"SX5->X5_DESCRI")
			
			If TE0->TE0_ESPEC == "1"
				cEspecie := "Revolver"
			Else
				cEspecie := "Pistola"
			EndIf
			
			AAdd( aCarac, TE0->TE0_ATIVO )
			AAdd( aCarac, cEspecie )
			AAdd( aCarac, cMarca )
			AAdd( aCarac, TE0->TE0_CALIBR )
			AAdd( aCarac, TE0->TE0_MODELO )
			AAdd( aCarac, TE0->TE0_NUMREG )
			AAdd( aCarac, TE0->TE0_SINARM )
		EndIf
	Case cTpArma == itCOLETE
		DbSelectArea("TE1")
		TE1->(DbSetOrder(1))
		
		If TE1->(DbSeek(xFilial("TE1") + cArma ))
			cProd := TE1->TE1_CODPRO	
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
			
			AAdd( aCarac, TE1->TE1_MARCA )
			AAdd( aCarac, TE1->TE1_VALIDA )
			AAdd( aCarac, TE1->TE1_NUMCOL )
			AAdd( aCarac, TE1->TE1_NUMSER )
			AAdd( aCarac, TE1->TE1_SINARM )
			AAdd( aCarac, TE1->TE1_PLCDIA )
			AAdd( aCarac, TE1->TE1_PLCTRA )
		EndIf
		
	Case cTpArma == itMUNICAO
		DbSelectArea("TE2")
		
		TE2->(DbSetOrder(4))
		
		If TE2->(DbSeek(xFilial("TE2") + cArma ))
			cProd := TE2->TE2_CODPRO	
			cDesc := Posicione("SB1",1,xFilial("SB1") + cProd ,"SB1->B1_DESC")
			
			If TE2->TE2_ESPEC == "1"
				cEspecie := "Revolver"
			ElseIf TE2->TE2_ESPEC == "2"
				cEspecie := "Pistola"
			Else
				cEspecie := "Espingarda"
			EndIf	

			AAdd( aCarac, cProd )
			
			AAdd( aCarac, TE2->TE2_VALIDA )
			AAdd( aCarac, cEspecie )
			AAdd( aCarac, TE2->TE2_MARCA )
			AAdd( aCarac, TE2->TE2_CALIBR )
			AAdd( aCarac, TE2->TE2_MODELO )
			AAdd( aCarac, TE2->TE2_SINARM )
	
		EndIf
EndCase
RestArea(aArea)
Return(.T.)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880RetResp

Retorna os responsaveis pela alocação
@sample 	At880RetResp() 
@since		11/09/2013 
@version P11 R9
     
@return aARet, Array com o retorno
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880RetResp()
Local aRet			:= {}
Local oModel 		:= FWModelActive()
Local oModelTFR	:= oModel:GetModel('TFRFUNC')	//Model do Grid Pai
Local nX			:= 0

For nX := 1 To oModelTFR:Length()
	oModelTFR:GoLine(nX)
	AAdd( aRet, FwFldGet("TFR_NOMTEC") )
Next nX
Return(aRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Guia

Realiza a abertura do Link para verificação do C.A do Fornecedor
@author Serviços
@since 20/08/13
@version P11 R9

@Param oModel,Model do Cadastro
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Guia(oModel)
Local oDlg		:= Nil
Local aAdvSize	:=	{}
Local cUrl		:= SuperGetMV("MV_TECGUIA",,"")
Local oWebEngine := NIl
Local oWebChannel := Nil
Local nPort		 := 00000

aAdvSize	:=	MsAdvSize()

oMainWnd:CoorsUpdate() // Atualiza as corrdenadas da Janela MAIN
nMyWidth  := oMainWnd:nClientWidth  - 10
nMyHeight := oMainWnd:nClientHeight - 30

oWebChannel := TWebChannel():New()
nPort       := oWebChannel:connect() // Efetua conexão e retorna a porta do WebSocket

DEFINE DIALOG oDlg TITLE STR0047 From aAdvSize[7],00 To nMyHeight,nMyWidth PIXEL //"Movimentação"
	
// Cria componente
oWebEngine := TWebEngine():New(oDlg, 05, 05, nMyHeight-250, nMyWidth-820, cUrl, nPort)
oWebEngine:navigate(cUrl)
oWebEngine:Align := CONTROL_ALIGN_ALLCLIENT

ACTIVATE DIALOG oDlg CENTERED


Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IncTFP

Atualiza a Tabela TFP e registra a movimentação

@param cItArma,caractere,Código do Armamento
@param nQtd,numerico,Quantidade movimentada

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpL:Retorna .T. 
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880IncTFP(cItArma,nQtd)
Local lRet		:= .T.
Local lContinua	:= .F.
Local aAreaTFP	:= TFP->(GetArea())
Local aAreaTE2	:= TE2->(GetArea())
Local cLote		:= ""
Local cEntDes	:= "" 		//Variavel com a Entidade de Destino
Local cCodMun	:= ""
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm()

Default cItArma	:= FwFldGet("TFQ_ITARMA")
Default nQtd	:= FwFldGet("TFQ_QTDMUN")

Do Case
	Case FwFldGet("TFQ_ENTDES") == edCLIENTE
		If FwFldGet("TFQ_ENTORI") == "1"
			//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
			DbSelectArea("TFP")
			TFP->(DbSetOrder(7))
			
		Else
			//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
			DbSelectArea("TFP")
			
			TFP->(DbSetOrder(8))
			
		EndIf
		
		DbSelectArea("TE2")
		
		TE2->(DbSetOrder(4))
		
		If TE2->(DbSeek(xFilial("TE2") + cItArma))
		
			If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILORI"),"")))
				cLote	:= TFP->TFP_LOTE
				cCodMun := TFP->TFP_CODMUN
				
				RecLock("TFP",.F.)
					REPLACE TFP_SALDO	WITH (TFP->TFP_SALDO - nQtd)
				MsUnlock()
	
				lContinua	:= .T.	
			EndIf
		EndIf
		
		//Inclui local de alocação da munição
		If lContinua
			If FwFldGet("TFQ_ENTDES") == edEMPRESA
				//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
				
				TFP->(DbSetOrder(7))
				
				cEntDes := "1"
			Else
				//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
				TFP->(DbSetOrder(8))
			
				cEntDes := "2"
			EndIf
				
			//Se Local de Destino já existe Atualiza os dados e soma o saldo
			If TFP->(DbSeek(xFilial("TFP") + cEntDes + FwFldGet("TFQ_DESTIN") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILDES") ,"")))
				RecLock("TFP",.F.)
				REPLACE TFP_SALDO		With (TFP->TFP_SALDO + nQtd)
			Else
				RecLock("TFP",.T.)
				REPLACE TFP_SALDO		With nQtd
			Endif
					
			REPLACE TFP_FILIAL 	With xFilial("TFP")
			REPLACE TFP_CODIGO 	With GetSXENum("TFP","TFP_CODIGO")
			REPLACE TFP_ENTIDA 	With "2" 								//1=Empresa, 2= Cliente
			If lMultFil
				REPLACE TFP_FILLOC 	With FwFldGet("TFQ_FILDES")				
			Endif
			REPLACE TFP_CODLOC 	With FwFldGet("TFQ_DESTIN")		
			REPLACE TFP_MOVIM 	With "2"								//Saldo Cliente
			REPLACE TFP_CODMUN	With cCodMun
			REPLACE TFP_LOTE	With cLote
			REPLACE TFP_STATUS	With "1"								//Efetivada
			REPLACE TFP_DTHIST	With FwFldGet("TFQ_DMOVIM")
			REPLACE TFP_CODMOV	With FwFldGet("TFQ_CODIGO")
			REPLACE TFP_PRODUT	With cItArma
			MsUnlock()
			ConfirmSX8()
		EndIf
	
	Case FwFldGet("TFQ_ENTDES") == edEMPRESA
		
		Do Case
			Case FwFldGet("TFQ_MOTIVO") == mtIMPLANTACAO
				If FwFldGet("TFQ_ENTORI") == "1"
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(7))
					
				Else
					//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(8))
					
				EndIf
				
				DbSelectArea("TE2")
				
				TE2->(DbSetOrder(4))
				
				If TE2->(DbSeek(xFilial("TE2") + cItArma))
				
					If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILORI") ,"")))
						cLote	:= TFP->TFP_LOTE
						cCodMun := TFP->TFP_CODMUN
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO - nQtd)
						MsUnlock()
						lContinua	:= .T.	
					EndIf
				EndIf
				
				//Inclui local de alocação da munição
				If lContinua
					If FwFldGet("TFQ_ENTDES") == edEMPRESA
						//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
						
						TFP->(DbSetOrder(7))
						
						cEntDes := "1"
					Else
						//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
						
						TFP->(DbSetOrder(8))
						
						cEntDes := "2"
					EndIf
						
					//Se Local de Destino já existe Atualiza os dados e soma o saldo
					If TFP->(DbSeek(xFilial("TFP") + cEntDes + FwFldGet("TFQ_DESTIN") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILDES"),"")))
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO + nQtd)
					Else
						RecLock("TFP",.T.)
						REPLACE TFP_SALDO		With nQtd
					EndIf	
								
					REPLACE TFP_FILIAL 	With xFilial("TFP")
					REPLACE TFP_CODIGO 	With GetSXENum("TFP","TFP_CODIGO")
					REPLACE TFP_ENTIDA 	With "1" 								//1=Empresa, 2= Cliente
					If lMultFil
						REPLACE TFP_FILLOC 	With FwFldGet("TFQ_FILDES")				
					Endif
					REPLACE TFP_CODINT 	With FwFldGet("TFQ_DESTIN")		
					REPLACE TFP_MOVIM 	With "1"								//Saldo Empresa					
					REPLACE TFP_CODMUN	With  cCodMun
					REPLACE TFP_LOTE	With cLote
					REPLACE TFP_STATUS	With "1"								//Efetivada
					REPLACE TFP_DTHIST	With FwFldGet("TFQ_DMOVIM")
					REPLACE TFP_CODMOV	With FwFldGet("TFQ_CODIGO")
					REPLACE TFP_PRODUT	With cItArma		
					MsUnlock()
					ConfirmSX8()
				EndIf
			Case FwFldGet("TFQ_MOTIVO") == mtMANUTENCAO //Manutenção
				If FwFldGet("TFQ_ENTORI") == "1"
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(7))
					
				Else
					//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
					DbSelectArea("TFP")
				
					TFP->(DbSetOrder(8))
					
				EndIf
				
				DbSelectArea("TE2")
				
				TE2->(DbSetOrder(4))
				
				
				If TE2->(DbSeek(xFilial("TE2") + cItArma))
				
					If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILORI"),"")))
						cLote	:= TFP->TFP_LOTE
						cCodMun := TFP->TFP_CODMUN
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO - nQtd)
						MsUnlock()
						lContinua	:= .T.	
					EndIf
				EndIf
				
				//Inclui local de alocação da munição
				If lContinua
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					
					TFP->(DbSetOrder(7))
					
					cEntDes := "1"
						
					//Se Local de Destino já existe Atualiza os dados e soma o saldo
					If aT730PosTFP(xFilial("TFP"),cEntDes,FwFldGet("TFQ_DESTIN"),cItArma,Iif(lMultFil,FwFldGet("TFQ_FILDES"),""))
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO + nQtd)
					Else
						RecLock("TFP",.T.)
						REPLACE TFP_SALDO		With nQtd
					EndIf	
		
					REPLACE TFP_FILIAL 	With xFilial("TFP")
					REPLACE TFP_CODIGO 	With GetSXENum("TFP","TFP_CODIGO")
					REPLACE TFP_ENTIDA 	With "1" 								//1=Empresa, 2= Cliente
					If lMultFil
						REPLACE TFP_FILLOC 	With FwFldGet("TFQ_FILDES")				
					Endif
					REPLACE TFP_CODINT 	With FwFldGet("TFQ_DESTIN")		
					REPLACE TFP_MOVIM 	With "1"								//Saldo Empresa							
					REPLACE TFP_CODMUN	With cCodMun					
					REPLACE TFP_LOTE	With cLote
					REPLACE TFP_STATUS	With "1"								//Efetivada
					REPLACE TFP_DTHIST	With FwFldGet("TFQ_DMOVIM")
					REPLACE TFP_CODMOV	With FwFldGet("TFQ_CODIGO")
					REPLACE TFP_PRODUT	With cItArma
					MsUnlock()
					ConfirmSX8()
				EndIf	
			Case FwFldGet("TFQ_MOTIVO") == mtDESCARTE //Descarte
				 If FwFldGet("TFQ_ENTORI") == "1"
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(7))
					
				Else
					//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(8))
					
				EndIf
				
				DbSelectArea("TE2")
				
				TE2->(DbSetOrder(4))
				
				If TE2->(DbSeek(xFilial("TE2") + cItArma))
					If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILORI"),"")))
						cLote	:= TFP->TFP_LOTE
						cCodMun := TFP->TFP_CODMUN
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO With (TFP->TFP_SALDO - nQtd)
						MsUnlock()
						lContinua	:= .T.	
					EndIf
				EndIf
				
				//Inclui local de alocação da munição
				If lContinua
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					TFP->(DbSetOrder(2))
					cEntDes := "1"
						
					RecLock("TFP",.T.)
						
					REPLACE TFP_FILIAL 	With xFilial("TFP")
					REPLACE TFP_CODIGO 	With GetSXENum("TFP","TFP_CODIGO")
					REPLACE TFP_ENTIDA 	With "1" 								//1=Empresa, 2= Cliente
					If lMultFil
						REPLACE TFP_FILLOC 	With FwFldGet("TFQ_FILDES")				
					Endif
					REPLACE TFP_CODINT 	With FwFldGet("TFQ_DESTIN")		
					REPLACE TFP_MOVIM 	With "8"								//Saldo Empresa		
					REPLACE TFP_SALDO	With nQtd					
					REPLACE TFP_CODMUN	With cCodMun					
					REPLACE TFP_LOTE	With cLote
					REPLACE TFP_STATUS	With "1"								//Efetivada
					REPLACE TFP_DTHIST	With FwFldGet("TFQ_DMOVIM")
					REPLACE TFP_CODMOV	With FwFldGet("TFQ_CODIGO")	
					REPLACE TFP_PRODUT	With cItArma
					MsUnlock()
					ConfirmSX8()
				EndIf	
			Case FwFldGet("TFQ_MOTIVO") == mtENCERRAMENTO //Encerramento
				If FwFldGet("TFQ_ENTORI") == "1"
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
					DbSelectArea("TFP")
				
					TFP->(DbSetOrder(7))
					
				Else
					//Atualiza o Saldo do Local de Alocação Selecionada para a Movimentação
					DbSelectArea("TFP")
					
					TFP->(DbSetOrder(8))
					
				EndIf
				
				DbSelectArea("TE2")
				
				TE2->(DbSetOrder(4))
				
				If TE2->(DbSeek(xFilial("TE2") + cItArma))
				
					If TFP->(DbSeek(xFilial("TFP") + FwFldGet("TFQ_ENTORI") + FwFldGet("TFQ_ORIGEM") + cItArma +IIF(lMultFil,FwFldGet("TFQ_FILORI"),"")))
						cLote	:= TFP->TFP_LOTE
						cCodMun := TFP->TFP_CODMUN
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO - nQtd)
						MsUnlock()
						lContinua	:= .T.	
					EndIf
				EndIf
	
				//Inclui local de alocação da munição
				If lContinua
					//Atualiza o Saldo do Local Interno Selecionada para a Movimentação
		
					TFP->(DbSetOrder(7))
					
					cEntDes := "1"
						
					//Se Local de Destino já existe Atualiza os dados e soma o saldo
					If aT730PosTFP(xFilial("TFP"),cEntDes,FwFldGet("TFQ_DESTIN"),cItArma,Iif(lMultFil,FwFldGet("TFQ_FILDES"),""))
						RecLock("TFP",.F.)
						REPLACE TFP_SALDO		With (TFP->TFP_SALDO + nQtd)
					Else
						RecLock("TFP",.T.)
						REPLACE TFP_SALDO		With nQtd
					EndIf	
		
					REPLACE TFP_FILIAL 	With xFilial("TFP")
					REPLACE TFP_CODIGO 	With GetSXENum("TFP","TFP_CODIGO")
					REPLACE TFP_ENTIDA 	With "1" 								//1=Empresa, 2= Cliente
					If lMultFil
						REPLACE TFP_FILLOC 	With FwFldGet("TFQ_FILDES")				
					Endif
					REPLACE TFP_CODINT 	With FwFldGet("TFQ_DESTIN")		
					REPLACE TFP_MOVIM 	With "1"								//Saldo Empresa		
					REPLACE TFP_CODMUN	With cCodMun
					REPLACE TFP_LOTE	With cLote
					REPLACE TFP_STATUS	With "1"								//Efetivada
					REPLACE TFP_DTHIST	With FwFldGet("TFQ_DMOVIM")
					REPLACE TFP_CODMOV	With FwFldGet("TFQ_CODIGO")
					REPLACE TFP_PRODUT	With cItArma
					MsUnlock()
					ConfirmSX8()
				EndIf	
		EndCase				
EndCase
RestArea(aAreaTFP)
RestArea(aAreaTE2)
Return(lRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IniPad

Inicializador do local de origem da munição

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpC:Descrição do Local de origem
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880IniPad(cEntOri,cOriMun)
Local cDesc 	:= ""
Local aArea	:= GetArea()
Local lModoInc	:= .F.

If Type('INCLUI') == 'L'
	lModoInc := INCLUI
EndIf 

If !lModoInc .And. !Empty(cENTORI) .And. !Empty(cORIMUN)
	Do Case
		Case cEntOri == "2"
			cDesc := Alltrim(Posicione("ABS",1,xFilial("ABS")+ cOriMun ,"ABS_DESCRI"))

		Case cEntOri == "1"
			cDesc := Alltrim(Posicione("TER",1,xFilial("TER")+ cOriMun ,"TER_DESCRI"))
	
	EndCase
EndIf
RestArea(aArea)
Return(cDesc)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880IniLote

Inicializador do Lote da munição

@author Serviços
@since 20/08/13
@version P11 R9

@return ExpC:Descrição do Lote
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880IniLote(cITMOV,cCdARMA)
Local cDesc 	:= ""
Local aArea	:= GetArea()

If !Inclui .And. !Empty(cITMOV) .And. !Empty(cCdARMA)
	If cITMOV == itMUNICAO
		cDesc := Posicione("TE2",1,xFilial("TE2") + cCdARMA ,"TE2->TE2_LOTE") 
	EndIf	
EndIf
RestArea(aArea)
Return(cDesc)

//----------------------------------------------------------
/*/{Protheus.doc} At880VlCont
Efetua a validação do campo de contrato

@Return 	lRet,, define se bloqueia ou nao

@author 	Serviços
@since 		28/04/2014
/*/
//----------------------------------------------------------
Function At880VlCont( oModel, cLocal, cContrato )

Local lRet := (FwFldGet("TFQ_ENTDES") == edEMPRESA)
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()

If !lRet
	If Empty(cContrato)
		lRet := At680Perm( Nil, __cUserId, "007" ) // Pode movimentar armamento fora do contrato?		
		If !lRet
			oModel:GetModel():SetErrorMessage(oModel:GetId(),"TFQ_CONTRT",oModel:GetModel():GetId(),"TFQ_CONTRT",'AT880ACE',; 
				STR0060, "" ) // "Usuário sem permissão para movimentação fora do contrato!"			
		EndIf
	Else 
		IF lGsOrcArma
			lRet := .T.
		Else	
			lRet := At880FilCont( cLocal, cContrato )
			If !lRet
				oModel:GetModel():SetErrorMessage(oModel:GetId(),"TFQ_CONTRT",oModel:GetModel():GetId(),"TFQ_CONTRT",'AT880ACE2',; 
					STR0061, "" ) // "Contrato invalido para o local escolhido!"	
			EndIf 
		Endif	
	EndIf
EndIf

Return(lRet)

//----------------------------------------------------------
/*/{Protheus.doc} At880FilCont
Efetua o filtro dos contratos do local

@param 		oModel

@Return 	lRet,, define se bloqueia ou nao

@author 	Serviços
@since 		28/04/2014
/*/
//----------------------------------------------------------
Function At880FilCont(cCodLocal, cContrato, cRevisao )

Local aArea:= GetArea()
Local lRet := .F.

Default cRevisao := ""

If CN9->CN9_SITUAC == "05"
	DbSelectArea("TFL")
	TFL->(dbSetOrder(3)) //TFL_FILIAL+TFL_LOCAL
	If TFL->( dbSeek( xFilial("TFL") + cCodLocal ) )
		While TFL->(!Eof()) .And. TFL->TFL_LOCAL == cCodLocal		
			If cContrato == TFL->TFL_CONTRT .And. ;
			   (Empty(cRevisao) .Or. cRevisao == TFL->TFL_CONREV)
				lRet := .T.
				Exit
			EndIf			
			TFL->(dbSkip())		
		EndDo
	EndIf
EndIf
RestArea(aArea)
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880QtdMov
Verifica a quantidade de movimentação do armamento

@param 		oModel

@Return 	lRet,, define se bloqueia ou nao

@author 	Serviços
@since 		28/04/2014
/*/
//----------------------------------------------------------
Function At880QtdMov( cTipo, cLocal, cContrato )
 
Local lRet 	  := (cTipo == "3" .Or. Empty(cContrato)) 
Local cAliasTFF := GetNextAlias()
Local cAliasTFQ := GetNextAlias()
Local nNumTFF   := 0
Local nNumTFQ   := 0
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm() .And. !isBlind()
Local oMdl		:= FWModelActive()
Local cWhere	:= "%%"

If lMultFil
	cWhere := "% (TFQ.TFQ_FILDES = '" + oMdl:GetValue("TFQMASTER","TFQ_FILDES") + "' AND TFQ.TFQ_DESTIN = '" + cLocal + "' OR "
	cWhere += "TFQ.TFQ_FILORI = '" + oMdl:GetValue("TFQMASTER","TFQ_FILORI") + "' AND TFQ.TFQ_ORIGEM = '" + cLocal + "' ) AND %"
Else
	cWhere :=  "% (TFQ.TFQ_DESTIN = '" + cLocal + "' OR TFQ.TFQ_ORIGEM = '" + cLocal + "') AND %"
Endif

If !lRet

 	lRet := At680Perm( Nil, __cUserId, "006" ) // Pode alocar mais armamentos que a quantidade definida?	
		
	If !lRet 
	
		// Verifica a quantidade definida no contrato para o numero de armamentos
		BeginSql Alias cAliasTFF	
		
			SELECT 
				TFL.TFL_LOCAL, TFL.TFL_CONTRT, 
				SUM(TFF.TFF_NARMA) MOV_ARM, SUM(TFF.TFF_NCOLE) MOV_COL
			FROM 
				%table:TFL% TFL
			JOIN %table:TFF% TFF ON
				TFF.TFF_FILIAL = %xFilial:TFF% AND
				TFF.TFF_CODPAI = TFL.TFL_CODIGO AND 
				TFF.%NotDel%
			WHERE 
				TFL.TFL_FILIAL = %xFilial:TFL% AND 
				TFL.TFL_LOCAL = %Exp:cLocal% AND 
				TFL.TFL_CONTRT = %Exp:cContrato% AND 
				TFL.%NotDel%
			GROUP BY 
				TFL.TFL_LOCAL, TFL.TFL_CONTRT	
		
		EndSql	
		
		If cTipo == itARMA				// Arma
			nNumTFF := (cAliasTFF)->MOV_ARM
		ElseIf	cTipo == itCOLETE				// Colete
			nNumTFF := (cAliasTFF)->MOV_COL 
		EndIf
		
		// Verifica a quantidade movimentada de armamentos		
		BeginSql Alias cAliasTFQ		
		
			SELECT 
				TFO.TFO_ITMOV, TFQ_ENTDES
			FROM 
				%table:TFQ% TFQ
			INNER JOIN %table:TFO% TFO ON 
					TFQ.TFQ_FILIAL = TFO.TFO_FILIAL 
				AND TFQ.TFQ_CODIGO = TFO.TFO_CDMOV
			WHERE 
				TFQ.TFQ_FILIAL = %xFilial:TFQ% AND
				TFQ.TFQ_CONTRT = %Exp:cContrato% AND 
				%Exp:cWhere%
				TFQ.%NotDel%
		
		EndSql	
		
		nNumTFQ := 0
		
		While (cAliasTFQ)->(!Eof())
		
			If (cAliasTFQ)->TFO_ITMOV <> cTipo
				(cAliasTFQ)->(dbSkip())
				Loop			
			EndIf
			
			If (cAliasTFQ)->TFQ_ENTDES == "1"
				nNumTFQ++
			Else 
				nNumTFQ--
			EndIf
		
			(cAliasTFQ)->(dbSkip())
		
		EndDo	
		
		lRet := (nNumTFF >= (nNumTFQ+1))
				
		If !lRet 
			Help( ,, "AT880MOVARM",, STR0062, 1, 0) // "Usuário sem permissão para movimentar equipamentos adicionais no contrato!"			
		EndIf			
	
	EndIf
	
EndIf 
Return(lRet)

//----------------------------------------------------------
/*/{Protheus.doc} AT880GFLD
	Realiza a classificação de campos baseado num determinado contexto 

@param 		cTipo,caractere,Constante que determina se a estrutura a ser construida: ARMAS - MUNICOES - COLETES
@param		cCampo,caractere,Nome do campo para validação de apresentação na interface.

@sample	AT880GFLD(cTipo, cCampo)
@return 	lRet,, define se apresenta ou nao

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function AT880GFLD(cTipo, cCampo)
Local lRet := .F.
Local cHeader	:= ''
Local lGsOrcArma := FindFunction("TecGsArma") .And. TecGsArma()


cHeader += 'TFQ_FILIAL;TFQ_CODIGO;TFQ_ORIGEM;TFQ_DSCLOC;TFQ_ENTORI;TFQ_MOTIVO;TFQ_ENTDES;TFQ_DESTIN;'
cHeader += 'TFQ_DDESTI;TFQ_JUSTIF;TFQ_DMOVIM;TFQ_RESTRA;TFQ_DRESP;TFQ_STATUS;TFQ_USUAR;TFQ_DATA;TFQ_HORA;'
If lGsOrcArma
	cHeader += 'TFQ_ENTIDA;TFQ_ULTSTA;TFQ_CDRESP;TFQ_TPMNT;TFQ_MOTMNT;TFQ_CONTRT;TFQ_FILORI;TFQ_FILDES;TFQ_CODTFL'
else
	cHeader += 'TFQ_ENTIDA;TFQ_ULTSTA;TFQ_CDRESP;TFQ_TPMNT;TFQ_MOTMNT;TFQ_CONTRT;TFQ_FILORI;TFQ_FILDES'	
Endif
	If TFQ->(ColumnPos("TFQ_CONREV")) > 0 .And. TFQ->(ColumnPos("TFQ_POSTO")) > 0
		cHeader += ';TFQ_CONREV;TFQ_POSTO'
	EndIf
cCampo := AllTrim(cCampo)

If cTipo == 'CABECALHO'
	lRet := cCampo $ cHeader
ElseIf	cTipo == 'ARMAS'
	If lGsOrcArma
		lRet := cCampo $ 'TFO_ITCOD;TFO_ITMOV;TFO_DESCR;TFO_LGUIA;TFO_NRGUIA;TFO_LRET;TFO_DTRET;TFO_CODTXQ'
	Else
		lRet := cCampo $ 'TFO_ITCOD;TFO_ITMOV;TFO_DESCR;TFO_LGUIA;TFO_NRGUIA;TFO_LRET;TFO_DTRET'
	Endif
ElseIf	cTipo == 'MUNICOES'
	If lGsOrcArma
		lRet := cCampo $ 'TFO_PRODUT;TFO_ITMOV;TFO_DESCR;TFO_LOTE;TFO_QTDE;TFO_LGUIA;TFO_NRGUIA;TFO_LRET;TFO_DTRET;TFO_CODTXQ;TFO_ITCOD' //TFO_CARGA;
	else
		lRet := cCampo $ 'TFO_PRODUT;TFO_ITMOV;TFO_DSCMUN;TFO_LOTE;TFO_QTDE;TFO_LGUIA;TFO_NRGUIA;TFO_LRET;TFO_DTRET' //TFO_CARGA;
	Endif
ElseIf	cTipo == 'COLETES'
	If lGsOrcArma
		lRet := cCampo $ 'TFO_ITCOD;TFO_ITMOV;TFO_DESCR;TFO_LRET;TFO_DTRET;TFO_CODTXQ'
	else
		lRet := cCampo $ 'TFO_ITCOD;TFO_ITMOV;TFO_DESCR;TFO_LRET;TFO_DTRET'	
	EndIf
Endif

Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880NGuia
	Realiza consistência no preenchimento do Nr da Guia.  
@Param		oMdl.objeto,Modelo de dados em edição

@sample	At880NGuia(oMdl)
@return 	lRet,, Define se valida a edição

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880NGuia(oMdl)
Local lRet := .T.
Local nOperation := oMdl:GetOperation()

If nOperation == MODEL_OPERATION_UPDATE .Or. nOperation == MODEL_OPERATION_INSERT
	If oMdl:GetValue('TFO_LGUIA') == '2' .AND. !Empty(oMdl:GetValue('TFO_NRGUIA'))
		oMdl:LoadValue('TFO_NRGUIA',Space(TamSX3('TFO_NRGUIA')[1]))
	EndIf
EndIf
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VDtMv
	Realiza a validação da data do movimento versus a data de retorno do armamento.  

@param		oMdl,OObjeto,Modelo de dados Master
@sample	At880VDtMv(oMdl)
@return 	lRet,logico,Determina a validação da edição

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VDtMv(oMdl)
Local lRet			:= .T.
Local oMdlTFO		:= nil
Local dMov			:= oMdl:GetValue('TFQ_DMOVIM')
Local dRet			:= CToD('')
Local nJ			:= 0
Local nI			:= 0

For nI := 1 To 3
	If		nI == 1 
		//Verifica as datas de retorno do grupo armas
		oMdlTFO	:= oMdl:GetModel():GetModel('TFOARMA')
	ElseIf nI == 2
		//Verifica as datas de retorno do grupo Munições
		oMdlTFO	:= oMdl:GetModel():GetModel('TFOMUNI')
	ElseIf nI == 3
		//Verifica as datas de retorno do grupo Coletes
		oMdlTFO	:= oMdl:GetModel():GetModel('TFOCOLE')
	EndIf
	
	For nJ := 1 To oMdlTFO:Length()
		oMdlTFO:GoLine(nJ)
		If !oMdlTFO:IsDeleted()
			dRet := oMdlTFO:GetValue('TFO_DTRET')
			If !Empty(dMov) .and. !Empty(dRet)
				If !(lRet := dRet >= dMov)
					Exit		
				EndIf
			EndIf		
		EndIf
	Next nJ
	
	If !lRet
		Help( ,, "AT880VLDTRT",, STR0065, 1, 0) // "Data de retorno deve ser maior ou igual que a data da movimentação!"
		Exit
	EndIf
Next nI
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VlArm
	Realiza a validação da data de validade do armamento.  

@param		cItMov,caractere,Tipo do armamento
@param		cItCod,caractere,Código do armamento

@sample	At880VlArm(cItMov,cItCod)
@return 	lRet,logico,Determina a data de validade é igual ou superior à data da movimentação

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VlArm(cItMov,cItCod,cOri,cDes)
Local lRet		:= .T.
Local aArea		:= GetArea()
Local dMov		:= FwFldGet('TFQ_DMOVIM')
Local dValida	:= CToD('')

Default cItMov := ""
Default cItCod := ""
Default cOri := ""
Default cDes := ""

//Valida apenas se o destino nao for igual a 2: Empresa
//Movimentacoes com destino igual a empresa podem acontecer mesmo quando o armamento esta vencido por conta de operacoes de descarte
If alltrim(cDes) != "2"
If !Empty(cItMov) .and. !Empty(cItCod)
	Do Case
		Case cItMov == itARMA
			DbSelectArea("TE0");	TE0->(DbSetOrder(1))
			If TE0->(DbSeek(xFilial("TE0") + cItCod))
				dValida := TE0->TE0_VALIDA
			EndIf
		Case cItMov == itCOLETE
			DbSelectArea("TE1");TE1->(DbSetOrder(1))
			If TE1->(DbSeek(xFilial("TE1") + cItCod))
				dValida := TE1->TE1_VALIDA
			EndIf
	EndCase
	If !Empty(dValida)
		lRet := dValida >= dMov
	EndIf
	If !lRet
		Help( ,, "At880VlArm",, STR0066, 1, 0) // "Data de Validade do Armamento expirou!"
	EndIf
EndIf
EndIf

RestArea(aArea)
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VDtRt
	Realiza a validação da data prevista de retorno do armamento.  

@param		oMdl,OObjeto,Modelo de dados em edição
@sample	At880VDtRt(oMdl)
@return 	lRet,logico,Determina a validação da edição

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VDtRt(oMdl)
Local lRet	:= .T.
Local dRet	:= oMdl:GetValue('TFO_DTRET')
Local dMov	:= FwFldGet('TFQ_DMOVIM')

If !Empty(dRet) .and. !Empty(dMov)
	If dRet < dMov
		Help( ,, "AT880VLDTRT",, STR0067, 1, 0) // "Data de retorno deve ser maior ou igual que a data da movimentação!"
		lRet := .F.		
	EndIf
EndIf
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880DtPrv
	Realiza consistência no preenchimento da Previsão de retorno do armamento.  

@param		oMdl,OObjeto,Modelo de dados em edição
@sample	At880DtPrv(oMdl)
@return 	lRet,Logico,Define se valida a edição

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880DtPrv(oMdl)
Local lRet := .T.

If oMdl:GetValue('TFO_LRET') == '2' .AND. !Empty(oMdl:GetValue('TFO_DTRET'))
	oMdl:LoadValue('TFO_DTRET',CToD(''))
EndIf
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VlEnt
	Valida as entidades de Origem e Destino da Movimentação bem como o motivo para a movimentação entre elas.  
@param		oModel,object,Objeto de dados Movimentação de Armamentos
@param		cIdCall,caractere,Identificação do campo que está sendo editado
@sample	At880VlEnt(oMdlcIdCall)
@return 	lRet,, Determina se os critérios para movimentação entre as entidades foram cumpridos ou não

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VlEnt(oMdl,cIdCall)
Local lRet			:= .T.
Local cEntDest	:= oMdl:GetValue('TFQ_ENTDES')
Local cMotivo		:= oMdl:GetValue('TFQ_MOTIVO') 

If lRet
	// Limpa os campos dependentes do tipo de entidade: Origem ou Destino
	If		cIdCall == 'TFQ_ENTORI'
		oMdl:LoadValue('TFQ_ORIGEM',Space(TAMSX3('TFQ_ORIGEM')[1]))
		oMdl:LoadValue('TFQ_DSCLOC',Space(TAMSX3('TFQ_DSCLOC')[1]))
	ElseIf	cIdCall == 'TFQ_ENTDES'
		oMdl:LoadValue('TFQ_DESTIN',Space(TAMSX3('TFQ_DESTIN')[1]))
		oMdl:LoadValue('TFQ_DDESTI',Space(TAMSX3('TFQ_DDESTI')[1]))
	EndIf
EndIf 

// Valida o motivo da movimentação versus o destino
// 1-Implantações tem sempre como destino o Local de Atendimento de Cliente.
// 2-Manutenções  tem sempre como destino a Empresa prestadora do serviço.
// 3-Descartes    tem sempre como destino a Empresa prestadora do serviço.
// 4-Reforço      tem sempre como destino o Local de Atendimento de Cliente. 

If lRet .and. cEntDest == edCLIENTE .And. (cMotivo == mtMANUTENCAO .Or. cMotivo == mtDESCARTE .Or. cMotivo == mtENCERRAMENTO)
	Help( ,, "At880VlEnt",, STR0031, 1, 0 )//'Para movimentação de Manutenção, Descarte e Encerramento, só será possivel selecionar a Empresa como destino'
	lRet := .F.	
EndIf

//Para a movimentação de reforco só será possivel selecionar o cliente
If lRet .and. cEntDest == edEMPRESA .And. cMotivo == mtREFORCO
	Help( ,, "At880VlEnt",, STR0032, 1, 0 )//'Para movimentação de Reforço ou Implantação, só será possivel selecionar o cliente como destino'
	lRet := .F.	
EndIf
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VMXA
	verifica se a arma vinculada à municação está relacionada no Grid de Armas.  
@param		oModel,object,Objeto de dados Movimentação de Armamentos
@sample	At880VMXA(oMdl)
@return 	lRet,logico,determinando se tem um cadastro respectivo ou não.

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VMXA(oMdl)
Local lRet			:= .F.
Local oMdlArma	:= oModel:GetModel('TFOARMA')
Local cCdArma		:= oMdl:GetValue('TFO_ARMA')
Local nI			:= 0

If !Empty(cCdArma)
	For nI := 1 to oMdlArma:Length()
		oMdlArma:GoLine(nI)
		If !oMdlArma:IsDeleted()
			If lRet := oMdlArma:GetValue('TFO_ITCOD') == cCdArma
				Exit
			EndIf
		EndIf
	Next nI
Else
	lRet := .T.
EndIf

If !lRet
	Help( ,, "At880VMXA",,STR0069, 1, 0 )//'Não há registro com este código na Aba de Armas!'
EndIf

Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880PosGA
	  
@param		oMdl,object,Objeto Master do formulário de movimentação
@param		nLine,numerico,Linha da grade na edição atual
@param		cAcao,caractere,Mode de Edição: ISENABLE,CANSETVALUE,SETVALUE,ADDLINE,DELETE,UNDELETE, etc.
@param		cCampo,caractere,Identificador do campo com foco de edição

@sample	At880PosGA(oMdl,nLine,cAcao,cCampo)
@return 	lRet,logico,

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880PosGA(oMdl,nLine,cAcao,cCampo)
Local lRet := .T.
Return lRet

//----------------------------------------------------------
/*/{Protheus.doc} At880VIMV
	Validação dos Itens de Movimentação
	  
@param		oModel,object,Objeto relativo a Armamento

@sample	At880VIMV(oMdl)
@return 	lRet,logico,Determina se o item pode ser movimentado.

@author 	Serviços
@since 		02/06/2015
/*/
//----------------------------------------------------------
Function At880VIMV(oMdl)
Local lRet		:= .T.
Local cItMov	:= ''
Local cItCod	:= ''
Local cMotivo	:= ''
Local nOperation:= oMdl:GetOperation()

cItMov := oMdl:GetValue('TFO_ITMOV')
cItCod := oMdl:GetValue('TFO_ITCOD')
cMotivo := FwFldGet('TFQ_MOTIVO')

//Verifico se já existe um agendamento para o armamento selecionado

If lRet .and. cItMov == itARMA .and. nOperation == MODEL_OPERATION_INSERT .And. At880Agend(cItMov,cItCod)
	Help( ,, "At880VIMV",, STR0009, 1, 0 )//'Alocação','Já existe um agendamento para esse armamento'
	lRet := .F.	
EndIf

If lRet .and. cItMov == itCOLETE .and. nOperation == MODEL_OPERATION_INSERT .And. At880Agend(cItMov,cItCod)
	Help( ,, "At880VIMV",, STR0009, 1, 0 )//'Alocação','Já existe um agendamento para esse armamento'
	lRet := .F.	
EndIf

//Verifica se o armamento está alocado no local de Origem
If lRet
	 //Verificar se o local de origem está preenchido
	If lRet .and. Empty(FwFldGet('TFQ_ORIGEM'))
		Help( ,, "At880VIMV",, STR0071, 1, 0 )//'Alocação','A Origem da Movimentação deve ser informada!'
		lRet := .F.
	EndIf
	 
	If lRet .and. !At880VldAloc(cItMov,cItCod,(cItMov <> itMUNICAO))
		Help( ,, "At880VIMV",, STR0072, 1, 0 )//'Alocação','Esse armamento não está alocado na Origem informada!'
		lRet := .F.
	EndIf
EndIf

If lRet .AND. cItMov <> itMUNICAO
	//Verifica o Status das armas baseado do motivo
	lRet := At880VldStat(cItMov,cItCod,cMotivo)
EndIf

// Verifica a quantidade de movimentação do armamento no local de atendimento
If lRet .And. FwFldGet("TFQ_ENTDES") == edCLIENTE
	lRet := At880QtdMov(cItMov, FwFldGet("TFQ_DESTIN"), FwFldGet("TFQ_CONTRT"))
EndIf

//Verifica se a arma selecionada possui manutenção em aberto
If lRet
	lRet := At880MntAbe(cItMov,cItCod)
EndIf

//Verifica a Data de Validade do armamento
If lRet .and. FwFldGet("TFQ_ENTDES") == edCLIENTE
	lRet := At880VlArm(cItMov,cItCod,FwFldGet("TFQ_ENTORI"),FwFldGet("TFQ_ENTDES"))
EndIf

If lRet .AND. oMdl:GetValue("TFO_ITMOV") == itMUNICAO 
	lRet := ExistCpo("TE2",cItCod)
Endif

If lRet .AND. oMdl:GetValue("TFO_ITMOV") == itCOLETE
	lRet := ExistCpo("TE1",cItCod)
Endif

//Verifica se a arma esta desativada e indo para um cliente
If lRet .and. FwFldGet("TFQ_ENTDES") == edCLIENTE .and. oMdl:GetValue("TFO_ITMOV") == itARMA
	If  alltrim(upper(Posicione("TE0",1,xFilial('TE0') + oMdl:GetValue('TFO_ITCOD'),"TE0_SITUA"))) == "9" .or. ;
		alltrim(upper(Posicione("TE0",1,xFilial('TE0') + oMdl:GetValue('TFO_ITCOD'),"TE0_SITUA"))) == "A"
		lRet := .F.
		Help( ,,,STR0074,STR0075, 1, 0 ) //"Arma Descartada ou Desativada" //"O armamento escolhido tem situação igual a Desativada ou Descartada"
	EndIf
EndIf

//Verifica se o colete esta desativado e indo para um cliente
If lRet .and. FwFldGet("TFQ_ENTDES") == edCLIENTE .and. oMdl:GetValue("TFO_ITMOV") == itCOLETE
	If  alltrim(upper(Posicione("TE1",1,xFilial('TE1') + oMdl:GetValue('TFO_ITCOD'),"TE1_SITUA"))) == "9" .or. ;
		alltrim(upper(Posicione("TE1",1,xFilial('TE1') + oMdl:GetValue('TFO_ITCOD'),"TE1_SITUA"))) == "A"
		lRet := .F.
		Help( ,,,STR0076,STR0077, 1, 0 ) //"Colete Descartado ou Desativado" //"O colete escolhido tem situação igual a Desativado ou Descartado"
	EndIf
EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880GtSld

Valida a quantidade de saldo de um local para movimentação

@since 18/10/2017
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880GtSld(cCodPro,cEntida,cLocal)
Local nRet  := 0
Local cTemp := GetNextAlias()
Local lMultFil 	:= TFQ->(ColumnPos("TFQ_FILORI")) .And. TFQ->(ColumnPos("TFQ_FILDES")) .And. FindFunction("TecMtFlArm") .And. TecMtFlArm() .And. !isBlind()
Local oMdl		:= FWModelActive()
Local cWhere	:= "%%"

If lMultFil
	cWhere := "% TFP.TFP_FILLOC = '" + oMdl:GetValue("TFQMASTER","TFQ_FILORI") + "' AND %"
Endif

If cEntida == '1'
	BeginSQL Alias cTemp
		
		SELECT 
			TFP_SALDO
		FROM 	
			%Table:TFP% TFP
		WHERE 
			TFP.TFP_FILIAL = %Exp:xFilial("TFP")% AND
			TFP.TFP_PRODUT = %Exp:cCodPro% AND
			TFP.TFP_CODINT = %Exp:cLocal% AND
			TFP.TFP_MOVIM = '1' AND
			%Exp:cWhere%
			TFP.%NotDel%
	EndSql
	
	nRet := ( cTemp )->( TFP_SALDO ) 
	
	(cTemp)->(DbCloseArea())
	
ElseIf cEntida == '2'
	
	BeginSQL Alias cTemp
		SELECT 
				TFP_SALDO
			FROM 	
				%Table:TFP% TFP
			WHERE 
				TFP.TFP_FILIAL = %Exp:xFilial("TFP")% AND
				TFP.TFP_PRODUT = %Exp:cCodPro% AND
				TFP.TFP_CODLOC = %Exp:cLocal% AND
				TFP.TFP_MOVIM = '2' AND
				%Exp:cWhere%
				TFP.%NotDel%
	EndSql
	
	nRet := ( cTemp )->( TFP_SALDO ) 
	
	(cTemp)->(DbCloseArea())
					
EndIf

Return nRet 

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldQt

Valida a quantidade da munição

@since 18/10/2017
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldQt(oModel) 
Local lRet := .T.
Local oTFQMaster := oModel:GetModel('TFQMASTER')
Local oTFODetail := oModel:GetModel('TFOMUNI')
Local nQtd 		:= oTFODetail:GetValue('TFO_QTDE')
Local cCodPro	:=	 oTFODetail:GetValue('TFO_PRODUT')
Local cEntida	:= oTFQMaster:GetValue('TFQ_ENTORI')
Local cLocal	:= oTFQMaster:GetValue('TFQ_ORIGEM')

If !Empty(cEntida) .And. !Empty(cLocal) .And. !Empty(cCodPro)
	lRet := At880GtSld(cCodPro,cEntida,cLocal) >= nQtd
EndIf	  

If !lRet
	Help( ,, "At880Qtd",, STR0081 + Alltrim(cCodPro) + STR0082, 1, 0 ) //"Saldo do produto "###" indisponível para movimentação"
EndIf

Return lRet


//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880VldTp

Valida o tipo de movimentação

@since 18/10/2017
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880VldTp()
Local oModel 		:= FwModelActive()
Local oTFQMaster 	:= oModel:GetModel('TFQMASTER')
Local oTFODetail 	:= oModel:GetModel('TFOMUNI')
Local lRet 			:= .T.
Local aRows 		:= FwSaveRows()
Local nI    		:= 0

If oTFQMaster:GetValue('TFQ_MOTIVO') == mtMANUTENCAO
	If !oTFODetail:IsEmpty()
		For nI := 1 To oTFODetail:Length()
			oTFODetail:GoLine(nI)
			oTFODetail:DeleteLine()
		Next nI 
		
	EndIf
	
	oTFODetail:SetNoDeleteLine(.T.)
	oTFODetail:SetNoInsertLine(.T.)
	oTFODetail:SetNoUpdateLine(.T.)
Else
	oTFODetail:SetNoDeleteLine(.F.)
	oTFODetail:SetNoInsertLine(.F.)
	oTFODetail:SetNoUpdateLine(.F.)
EndIf

FwRestRows( aRows )

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At800RIm

Reimpressão de protocolo de recebimento de arma

@since 18/10/2017
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At800RIm(oModel, oView)
Local oMdlAux := NIL
Local aFolder :=  {{STR0090, "TFOARMA" }, {STR0091, "TFOMUNI"}, {STR0092, "TFOCOLE"}} //"Armas"##"Munição"##"Coletes"
Local nPos := 0
Local aFldSel := {}
Local cCampoGet := "TFO_ITCOD"
local nQtde := 0
	
If (FwFldGet("TFQ_STATUS") == stEFETIVADA .And. FwFldGet("TFQ_ENTDES") == edCLIENTE) .OR.;
	FwFldGet("TFQ_STATUS") == stAGENDADA
	
	aFldSel :=  oView:GetFolderActive("ABAS", 2)
	
	If Len(aFldSel) >= 2 .AND. !Empty(aFldSel[2])
		nPos := aScan(aFolder,  {|l| l[1] == aFldSel[2]})
	EndIf

	If nPos > 0
		oMdlAux := oModel:GetModel(aFolder[nPos, 2])
		If !oMdlAux:IsEmpty() .AND. !oMdlAux:IsDeleted() 
			If aFolder[nPos, 2] == "TFOMUNI" 
				cCampoGet := "TFO_PRODUT"
				nQtde := oMdlAux:GetValue("TFO_QTDE")
			EndIf
								
			If !Empty(oMdlAux:getValue(cCampoGet))
				ExecBlock("Tec880ImpProt", .F., .F., {FwFldGet("TFQ_CODIGO"),oMdlAux:GetValue("TFO_ITMOV"),oMdlAux:GetValue(cCampoGet),FwFldGet("TFQ_DRESP"),nQtde,FwFldGet("TFQ_DDESTI")})
			EndIf
			
		EndIf
	Else
		Help( ,, "At800ImpPr",, STR0094, 1, 0 ) //"Para reimpressão de protocolo que selecione um item do grid de Armas, Coletes ou Munição"
	EndIf

Else
	Help( ,, "At800ImpPr",, STR0095, 1, 0 ) //"Movimentação não permite reimpressao de protocolo"
EndIf

Return



//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880ImpPr

Impressão de protocolo de recebimento de arma

@since 18/10/2017
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At880ImpPr(oMdl)

Local oMdlAux 	:= NIL //Models das armas
Local cCampoGet := "TFO_ITCOD" //Campo do Codigo do armamento/prod. munição
Local aModels 	:= {"TFOARMA", "TFOMUNI", "TFOCOLE"} //Models das armas
Local nI 		:= 0 //Contador de Linhas do Grid
Local nC 		:= 0 //Contador de Models
Local nQtde 	:= 0 //Quantidade
Local lProtocolo := ExistBlock("Tec880ImpProt") //RdMake de Protocolo Impresso


If lProtocolo .AND. ;
	((FwFldGet("TFQ_STATUS") == stEFETIVADA .And. FwFldGet("TFQ_ENTDES") == edCLIENTE) .OR.;
	(FwFldGet("TFQ_STATUS") == stAGENDADA .AND. MsgYesNo(STR0011)) )	//"Deseja Imprimir o Protocolo de Entrega?"
	
	For nC := 1 to len(aModels)
		oMdlAux := oMdl:GetModel(aModels[nC])
		cCampoGet := "TFO_ITCOD"
		nQtde := 0
	
		If aModels[nC] == "TFOMUNI"
			cCampoGet := "TFO_PRODUT"		
		EndIf
		// Instancia Grid Armas/Coletes ou Munições			
		oMdlAux	:= oMdl:GetModel(aModels[nC])
		If	! oMdlAux:IsEmpty()
			For nI := 1 To oMdlAux:Length()
				oMdlAux:GoLine(nI)
				If aModels[nC] == "TFOMUNI"
					nQtde := oMdlAux:GetValue("TFO_QTDE")
				EndIf
				If !oMdlAux:IsDeleted() .AND. !Empty(oMdlAux:GetValue(cCampoGet))
					ExecBlock("Tec880ImpProt", .F., .F., {FwFldGet("TFQ_CODIGO"),oMdlAux:GetValue("TFO_ITMOV"),oMdlAux:GetValue(cCampoGet),FwFldGet("TFQ_DRESP"),nQtde,FwFldGet("TFQ_DDESTI")})
				EndIf
			Next nI
		EndIf
	
	Next nC

EndIf

Return 

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Ret

Preencher a variavel para atualizar a consulta padrao
Author - Vitor kwon
@since 03/02/2022
@version P12
@return Nil
/*/
//--------------------------------------------------------------------------------------------------------------------

Function At880Ret(cRetArm,cDest,cCodTFL,cCodMuni)

cArmaLocal  := cRetArm
cLocTFF     := cDest
cCodigoTFL  := cCodTFL
cCodCOL     := cDest
cCodMun     := cDest
cCodSB1     := cCodMuni

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} PrelinTFQ

Função de Prevalidacao dos fields de alocação TFQ

@author Vitor kwon
@since 03/02/2023
/*/
//------------------------------------------------------------------------------

Function PrelinTFQ(oMdlTFQ,cAction,cField,xValue)

local lRet := .T.
local lCont := .T.

if VALTYPE(oMdlTFQ) == 'O' .AND. oMdlTFQ:GetId() == "TFQMASTER"

	If cAction == "SETVALUE"
		If cField $ "|TFQ_ORIGEM|" .And. FwFldGet("TFQ_ENTORI") == '2'
            lCont := At880Local(FwFldGet("TFQ_CONTRT"),Alltrim(xValue),"1")
			If !lCont .And. !Empty(xValue)
		  		lRet := .F.
				Help( " ", 1, "PrelinTFQ", Nil,STR0099  , 1 ) //"Este local nao pertence a este contrato. Verifique o numero do contrato"
			Endif	
		Endif
		If cField $ "|TFQ_DESTIN|" .And. FwFldGet("TFQ_ENTDES") == '1' 
			iF FwFldGet("TFQ_ENTORI") == '2'
				lRet := .T.
			else
				lCont := At880Local(FwFldGet("TFQ_CONTRT"),Alltrim(xValue),"1")
				If !lCont .And. !Empty(xValue)
					lRet := .F.
					Help( " ", 1, "PrelinTFQ", Nil,STR0099 , 1 ) //"Este local nao pertence a este contrato. Verifique o numero do contrato"
				Endif
			Endif			
		Endif
		If cField $ "|TFQ_CONTRT|"  
		    iF Empty(xValue) .or. (oMdlTFQ:GetValue("TFQ_CONTRT") <> xValue)
				oMdlTFQ:LoadValue("TFQ_ORIGEM", "")
				oMdlTFQ:LoadValue("TFQ_DESTIN", "")
				oMdlTFQ:LoadValue("TFQ_DDESTI", "")
				oMdlTFQ:LoadValue("TFQ_DSCLOC", "")
				oMdlTFQ:LoadValue("TFQ_RESTRA", "")
				oMdlTFQ:LoadValue("TFQ_ENTORI", "")
				oMdlTFQ:LoadValue("TFQ_MOTIVO", "")
				oMdlTFQ:LoadValue("TFQ_ENTDES", "")
				oMdlTFQ:LoadValue("TFQ_STATUS", "")
				oMdlTFQ:LoadValue("TFQ_DRESP", "")
				oMdlTFQ:LoadValue("TFQ_JUSTIF", "")
			Endif	
            lCont := At880Local(FwFldGet("TFQ_CONTRT"),Alltrim(xValue),"2")
			If  !lCont .And. !Empty(xValue)
		  		lRet := .F.
				Help( " ", 1, "PrelinTFQ", Nil,STR0100  , 1 ) //"Este local nao pertence a este contrato. Verifique o numero do contrato"
			Endif  
		Endif
		If cField $ "|TFQ_MOTIVO|"  
			iF Empty(xValue) .or. (oMdlTFQ:GetValue("TFQ_MOTIVO") <> xValue) 
				oMdlTFQ:LoadValue("TFQ_ORIGEM", "")
				oMdlTFQ:LoadValue("TFQ_DESTIN", "")
				oMdlTFQ:LoadValue("TFQ_DDESTI", "")
				oMdlTFQ:LoadValue("TFQ_DSCLOC", "")
				oMdlTFQ:LoadValue("TFQ_RESTRA", "")
				oMdlTFQ:LoadValue("TFQ_ENTORI", "")
				oMdlTFQ:LoadValue("TFQ_ENTDES", "")
				oMdlTFQ:LoadValue("TFQ_STATUS", "")
				oMdlTFQ:LoadValue("TFQ_DRESP", "")
				oMdlTFQ:LoadValue("TFQ_JUSTIF", "")
			Endif
		Endif
	Endif
Endif

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Local

Verifica se existe o local de atendimento no contrato
@author Vitor Kwon
@param cContrato
@param cLocal 
@since 03/02/2023
	
@return	lRet- valor true quando tudo ocorrer de acordo
/*/
//--------------------------------------------------------------------------------------------------------------------

Function At880Local(cContrato,cDestino,cTipo)

Local lRet := .F.
Local cAlias := ""
Local cCodUser := PswRet(1)[1,1]
Local cQry := ""
Local oQry := Nil

Default cContrato := ""
Default cDestino := ""
Default cTipo := ""

If cTipo == '1'
	cQry := " SELECT 1 "
	cQry += " FROM ? TFL "
	cQry += " WHERE TFL_FILIAL =  ? AND "
	cQry += " TFL_CONTRT = ? AND "
	cQry += " TFL_LOCAL  = ? AND "
	cQry += " TFL.D_E_L_E_T_ = ' ' "
	oQry := FwExecStatement():New(cQry)
	oQry:SetUnsafe( 1, RetSQLName("TFL") )
	oQry:SetString(  2, xFilial("TFL") )
	oQry:SetString(  3, cContrato )
	oQry:SetString(  4, cDestino )
ElseIF cTipo == '2'
	cQry := " SELECT  DISTINCT TFJ_CONTRT, TFJ_CONREV "
	cQry += " FROM ? TFJ "
	cQry += " INNER JOIN ? TFL ON TFL_FILIAL = ? AND TFL_CODPAI = TFJ_CODIGO AND TFL.D_E_L_E_T_ = ' ' "
	cQry += " INNER JOIN ? TFF ON TFF_FILIAL = ? AND TFF_CODPAI = TFL_CODIGO AND TFF.D_E_L_E_T_ = ' ' "
	cQry += " INNER JOIN ? TXQ ON TXQ_FILIAL = ? AND TXQ_CODTFF = TFF_COD    AND TXQ.D_E_L_E_T_ = ' ' "
	cQry += " WHERE TFJ_FILIAL = ? AND "
	cQry += " TFJ_CONTRT = ? AND "
	cQry += " TFJ_STATUS = '1' AND TFJ.D_E_L_E_T_ = ' ' "
	If !At680Perm( Nil, cCodUser, "074" ) // Define regras de restrição
		cQry += " AND TFF_CODSUB = ' ' "
	EndIf
	oQry := FwExecStatement():New(cQry)
	oQry:SetUnsafe( 1, RetSQLName("TFJ") )
	oQry:SetUnsafe( 2, RetSQLName("TFL") )
	oQry:SetString(  3, xFilial("TFL") )
	oQry:SetUnsafe( 4, RetSQLName("TFF") )
	oQry:SetString(  5, xFilial("TFF") )
	oQry:SetUnsafe( 6, RetSQLName("TXQ") )
	oQry:SetString(  7, xFilial("TXQ") )
	oQry:SetString(  8, xFilial("TFJ") )
	oQry:SetString(  9, cDestino )
EndIf

cAlias := oQry:OpenAlias()

If ((cAlias)->(!Eof()))
	lRet := .T.
EndIF
(cAlias)->(dbCloseArea())
oQry:Destroy()
FwFreeObj(oQry)

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880LTFF

Gatilho do campo TFO_ITCOD 
@author Vitor Kwon
@since 03/02/2023
@return	cLocTFF - retorna o local de atendimento da arma
/*/
//--------------------------------------------------------------------------------------------------------------------
Function AT880LTFF(cItCod)
Local cContrt := FwfldGet("TFQ_CONTRT")
Local cItMov  := "1" // Armas
Local cSitua  := IIf( FwfldGet("TFQ_ENTORI") == "2" .And. FwfldGet("TFQ_ENTDES") == "2", "2", "1" )
Local cAlias := ""
Local cQuery := ""
Local nOrdem := 1
Local oStatement := Nil
Local lArmMultFil := TecMtFlArm()

Default cItCod  := FwFldGet("TFO_ITCOD")

If !Empty(cItCod)
	cLocTFF := ""

	cQuery := " SELECT TXQ_CODIGO "
	cQuery += " FROM ? TE0 "
	cQuery += " INNER JOIN ? TXQ ON TXQ.TXQ_FILIAL = ? "
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		cQuery += " AND TXQ.TXQ_CONTRT = ? "
	EndIf
	cQuery += " AND TXQ.TXQ_ITEARM = ? "
	cQuery += " AND TXQ.TXQ_CODPRD = TE0.TE0_CODPRO "
	cQuery += " AND TXQ.TXQ_CODSUB = ' ' "
	cQuery += " AND TXQ.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE TE0.TE0_FILIAL = ? "
	cQuery += " AND TE0.TE0_COD = ? "
	cQuery += " AND TE0_SITUA = ? "
	cQuery += " AND TE0.D_E_L_E_T_ = ' ' "

	oStatement := FwExecStatement():New( cQuery )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TE0") )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TXQ") )
	oStatement:SetString( nOrdem++, FwxFilial( "TXQ" ) )
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		oStatement:SetString( nOrdem++, cContrt )
	EndIf
	oStatement:SetString( nOrdem++, cItMov )
	oStatement:SetString( nOrdem++, FwxFilial( "TE0" ) )
	oStatement:SetString( nOrdem++, cItCod )
	oStatement:SetString( nOrdem++, cSitua )

	cAlias := oStatement:OpenAlias()
	
	If (cAlias)->( !Eof() )
		cLocTFF := (cAlias)->TXQ_CODIGO
	EndIf

	(cAlias)->(DbCloseArea())
	oStatement:Destroy()
	FwFreeObj( oStatement )
EndIf

Return cLocTFF

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880TFL

Gatilho do campo TFQ_DESTIN --> TFQ_CODTFL
@author Vitor Kwon
@since 03/02/2023
@return	cCodigoTFL - retorna o código da TFL
/*/
//--------------------------------------------------------------------------------------------------------------------


Function AT880TFL()

Return cCodigoTFL

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880Col

Gatilho do campo 
@author Vitor Kwon
@since 03/02/2023
@return	cCodigoTFL - retorna o código da TXQ do colete
/*/
//--------------------------------------------------------------------------------------------------------------------

Function AT880Col(cItCod)
Local cContrt := FwfldGet("TFQ_CONTRT")
Local cItMov  := "2" // Coletes
Local cSitua  := IIf( FwfldGet("TFQ_ENTORI") == "2" .And. FwfldGet("TFQ_ENTDES") == "2", "2", "1" )
Local cAlias := ""
Local cQuery := ""
Local nOrdem := 1
Local oStatement := Nil
Local lArmMultFil := TecMtFlArm()

Default cItCod  := FwFldGet("TFO_ITCOD")

If !Empty(cItCod)
	cCodCOL := ""

	cQuery := " SELECT TXQ_CODIGO "
	cQuery += " FROM ? TE1 "
	cQuery += " INNER JOIN ? TXQ ON TXQ.TXQ_FILIAL = ? "
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		cQuery += " AND TXQ.TXQ_CONTRT = ? "
	EndIf
	cQuery += " AND TXQ.TXQ_ITEARM = ? "
	cQuery += " AND TXQ.TXQ_CODPRD = TE1.TE1_CODPRO "
	cQuery += " AND TXQ.TXQ_CODSUB = ' ' "
	cQuery += " AND TXQ.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE TE1.TE1_FILIAL = ? "
	cQuery += " AND TE1.TE1_CODCOL = ? "
	cQuery += " AND TE1_SITUA = ? "
	cQuery += " AND TE1.D_E_L_E_T_ = ' ' "

	oStatement := FwExecStatement():New( cQuery )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TE1") )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TXQ") )
	oStatement:SetString( nOrdem++, FwxFilial( "TXQ" ) )
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		oStatement:SetString( nOrdem++, cContrt )
	EndIf
	oStatement:SetString( nOrdem++, cItMov )
	oStatement:SetString( nOrdem++, FwxFilial( "TE1" ) )
	oStatement:SetString( nOrdem++, cItCod )
	oStatement:SetString( nOrdem++, cSitua )

	cAlias := oStatement:OpenAlias()
	
	If (cAlias)->( !Eof() )
		cCodCOL := (cAlias)->TXQ_CODIGO
	EndIf

	(cAlias)->(DbCloseArea())
	oStatement:Destroy()
	FwFreeObj( oStatement )
EndIf

Return cCodCOL

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880Mun

Gatilho do campo 
@author Vitor Kwon
@since 22/02/2023
@Param nOpc, 1 retorna codigo municao Orc e 2 retorna codigo produto
@return	cCodMun - retorna o código TXQ da munição
/*/
//--------------------------------------------------------------------------------------------------------------------

Function AT880Mun(nOpc,cItCod)
Local cContrt := FwfldGet("TFQ_CONTRT")
Local cItMov  := "3" // Munição
Local cCodTXQ := ""
Local cAlias := ""
Local cQuery := ""
Local nOrdem := 1
Local oStatement := Nil
Local lArmMultFil := TecMtFlArm()

Default nOpc := 1
Default cItCod  := FwFldGet("TFO_ITCOD")

If !Empty(cItCod)
	cCodTXQ := ""

	cQuery := " SELECT TXQ_CODIGO, TXQ_CODPRD "
	cQuery += " FROM ? TE2 "
	cQuery += " INNER JOIN ? TXQ ON TXQ.TXQ_FILIAL = ? "
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		cQuery += " AND TXQ.TXQ_CONTRT = ? "
	EndIf
	cQuery += " AND TXQ.TXQ_ITEARM = ? "
	cQuery += " AND TXQ.TXQ_CODPRD = TE2.TE2_CODPRO "
	cQuery += " AND TXQ.TXQ_CODSUB = ' ' "
	cQuery += " AND TXQ.D_E_L_E_T_ = ' ' "
	cQuery += " WHERE TE2.TE2_FILIAL = ? "
	cQuery += " AND TE2.TE2_CODMUN = ? "
	cQuery += " AND TE2.D_E_L_E_T_ = ' ' "

	oStatement := FwExecStatement():New( cQuery )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TE2") )
	oStatement:SetUnsafe( nOrdem++, RetSQLName("TXQ") )
	oStatement:SetString( nOrdem++, FwxFilial( "TXQ" ) )
	If !lArmMultFil .Or. ( lArmMultFil .And. !Empty( cContrt ) )
		oStatement:SetString( nOrdem++, cContrt )
	EndIf
	oStatement:SetString( nOrdem++, cItMov )
	oStatement:SetString( nOrdem++, FwxFilial( "TE1" ) )
	oStatement:SetString( nOrdem++, cItCod )

	cAlias := oStatement:OpenAlias()
	
	If (cAlias)->( !Eof() )
		If nOpc == 1
			cCodTXQ := (cAlias)->TXQ_CODIGO
		Else
			cCodTXQ := (cAlias)->TXQ_CODPRD
		EndIf
	EndIf

	(cAlias)->(DbCloseArea())
	oStatement:Destroy()
	FwFreeObj( oStatement )
EndIf

Return cCodTXQ

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880Prd

Gatilho do campo 
@author Vitor Kwon
@since 22/02/2022
@return	cCodSB1 - retorna o código do produto da SB1 
/*/
//--------------------------------------------------------------------------------------------------------------------


Function AT880prd()

Return cCodSB1

/*/{Protheus.doc} TC880EST
	Estorna movimentações canceladas/efetivadas
	@author Roberto Santiago
	@since 06/08/2024
	@return
/*/
Function TC880EST()

	FWMsgRun(Nil, {|| T880EProc()}, "Aguarde...", "Estornando movimentação...")

Return Nil

/*/{Protheus.doc} T880EProc
	Estorna movimentações canceladas/efetivadas
	@author Roberto Santiago
	@since 06/08/2024
	@return
/*/
Static Function T880EProc()

	Local cTabArma     := "TE0"
	Local cTabColete   := "TE1"
	Local cTabMunicao  := "TE2"
	Local cCodigo      := TFQ->TFQ_CODIGO
	Local aCdArmamento := Array(0)
	Local nArmamento   := 0
	Local cTabela      := ""
	Local aUltDados    := Array(0)
	Local aCurDados    := Array(0)

	Begin Transaction
		DBSelectArea("TFO")
		TFO->(DBGoTop())
		While TFO->(!EoF())
			If TFO->TFO_CDMOV == cCodigo
				aAdd(aCdArmamento,;
						{;
							cCodigo,;
							TFO->TFO_ITCOD,;
							IIF(TFO->TFO_ITMOV == "1", cTabArma, IIF(TFO->TFO_ITMOV == "2", cTabColete, cTabMunicao)),;
							TFQ->TFQ_MOTIVO,;
							TFO->TFO_CODTXQ,;
							IIF(TFO->TFO_QTDE == 0, 1, TFO->TFO_QTDE);
						})
				RecLock("TFO", .F.)
					TFO->(DBDelete())
				TFO->(MsUnlock())
			EndIf
			TFO->(DBSkip())
		EndDo

		DBSelectArea("TFR")
		TFR->(DBGoTop())
		While TFR->(!EoF())
			If TFR->TFR_CODMOV == cCodigo
				RecLock("TFR", .F.)
					TFR->(DBDelete())
				TFR->(MsUnlock())
			EndIf
			TFR->(DBSkip())
		EndDo

		For nArmamento := 1 To Len(aCdArmamento)
			cTabela := aCdArmamento[nArmamento][3]

			DBSelectArea("TXQ")
			TXQ->(DBSetOrder(1)) //TXQ_FILIAL+TXQ_CODIGO
			If TXQ->(DBSeek(xFilial("TXQ") + aCdArmamento[nArmamento][5]))
				RecLock("TXQ", .F.)
					TXQ->TXQ_QTDAPO := (TXQ->TXQ_QTDAPO - aCdArmamento[nArmamento][6])
				TXQ->(MsUnlock())
			EndIf

			DBSelectArea(cTabela)	
			(cTabela)->(DBSetOrder(1))
			(cTabela)->(DBSeek(xFilial() + aCdArmamento[nArmamento][2]))
			If cTabela $ "TE0.TE1"
				RecLock(cTabela, .F.)
					(cTabela)->&(aCdArmamento[nArmamento][3] + "_SITUA") := "1"
				(cTabela)->(MsUnlock())
			ElseIf cTabela == "TE2"
				aUltDados := GLstRecTFP(aCdArmamento, nArmamento)
				If Len(aUltDados) == 2
					DBSelectArea("TFP")
					TFP->(DBGoTo(aUltDados[2]))
					RecLock("TFP", .F.)
						TFP->(DBDelete())
					TFP->(MsUnlock())

					aUltDados := GLstRecTFP(aCdArmamento, nArmamento)
					If Len(aUltDados) == 2
						aCurDados := GLstRecTFP(aCdArmamento, nArmamento)
						If Len(aCurDados) == 2
							TFP->(DBGoTo(aCurDados[2]))
							RecLock("TFP", .F.)
								TFP->TFP_SALDO += aUltDados[1]
							TFP->(MsUnlock())
						EndIf
					EndIf
				EndIf
			EndIf
		Next nArmamento

		RecLock("TFQ", .F.)
			TFQ->(DBDelete())
		TFQ->(MsUnlock())
	End Transaction

Return Nil

/*/{Protheus.doc} GLstRecTFP
	Retorna saldo das movimentações de municoes
	@author Roberto Santiago
	@since 06/08/2024
	@return
/*/
Static Function GLstRecTFP(aCdArmamento, nArmamento)

	Local cQueryTFP    := ""
	Local cAliasTFP    := ""
	Local aLastDados   := Array(0)
	Local aArea        := GetArea()
	Local oQuery       := Nil

	cQueryTFP += "SELECT TFP.TFP_SALDO, TFP.R_E_C_N_O_ RECNO "
	cQueryTFP += "FROM ? "
	cQueryTFP += "WHERE 1 = 1 "
	cQueryTFP += "	AND " + %Exp:RetSQLCond("TFP")%
	cQueryTFP += "	AND TFP_CODMUN = ? "
	cQueryTFP += "ORDER BY TFP.TFP_CODIGO DESC"

	oQuery := FwExecStatement():New(cQueryTFP)
	oQuery:SetUnsafe(1, RetSQLTab("TFP"))
	oQuery:SetString(2, aCdArmamento[nArmamento][2])

	cAliasTFP := oQuery:OpenAlias()

	(cAliasTFP)->(DBGoTop())

	If (cAliasTFP)->(!EoF())
		aLastDados := {(cAliasTFP)->TFP_SALDO , (cAliasTFP)->RECNO}
	EndIf

	(cAliasTFP)->(DBCloseArea())
	oQuery:Destroy()
	FwFreeObj(oQuery)
	RestArea(aArea)

Return aLastDados

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Clear

Limpa o grid de armamento ao mudar a origem da movimentação
@author jack junior
@since 07/10/2024
@Param 
@return	
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Clear()
Local oModel	:= FwModelActive()
Local oMdlArma	:= oModel:GetModel('TFOARMA')
Local oView 	:= FwViewActive()

If oModel:GetOperation() == MODEL_OPERATION_INSERT
	If oMdlArma:CanClearData()
		oMdlArma:ClearData(.T.,.T.)
		oView:Refresh( "VIEW_GRID_TFOA" )
	EndIf
EndIf

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT880TFLdg

Popula o campo TFQ_CODTFL caso não tenha sido populado pelo F3 do TFQ_DESTIN
@author jack junior
@since 27/11/2024
@Param oMdl - Modelo Field TFQ
	   cCampo - 'TFQ_ORIGEM' ou 'TFQ_DESTIN'
@return	
/*/
//--------------------------------------------------------------------------------------------------------------------
Function AT880TFLdg(oMdl,cCampo)
Local cAliasTFL := ""
Local cCodABS	:= ""
Local cCodTFL	:= ""
Local cContrato	:= ""
Local cRevisao  := ""
Local cQryTFL   := ""
Local oQryTFL   := Nil
Local lExstRev  := TFQ->(ColumnPos("TFQ_CONREV")) > 0

If Empty(oMdl:GetValue("TFQMASTER","TFQ_CODTFL"))
	cContrato := oMdl:GetValue("TFQMASTER","TFQ_CONTRT")
	If lExstRev
		cRevisao  := oMdl:GetValue("TFQMASTER","TFQ_CONREV")
	EndIf
	If cCampo == "TFQ_ORIGEM"
		cCodABS	:= oMdl:GetValue("TFQMASTER","TFQ_ORIGEM")
	ElseIf cCampo == "TFQ_DESTIN"
		cCodABS	:= oMdl:GetValue("TFQMASTER","TFQ_DESTIN")
	EndIf

	If !Empty(cContrato) .And. !Empty(cCodABS) 
		cQryTFL += " SELECT TFL.TFL_CODIGO "
		cQryTFL += " FROM ? TFL "
		cQryTFL += " WHERE TFL.TFL_FILIAL = ? "
		cQryTFL += " AND TFL.TFL_CONTRT = ? "
		cQryTFL += " AND TFL.TFL_LOCAL = ? "
		If lExstRev
			cQryTFL += " AND TFL.TFL_CONREV = ? "
		EndIf
		cQryTFL += " AND TFL.D_E_L_E_T_ = ' ' "

		//Prepara a query:
		oQryTFL := FwExecStatement():New(cQryTFL)

		//SELECT
		oQryTFL:SetUnsafe( 1, RetSQLName("TFL") )
		//WHERE
		oQryTFL:SetString( 2, xFilial('TFL') )
		oQryTFL:SetString( 3, cContrato )
		oQryTFL:SetString( 4, cCodABS )
		If lExstRev
			oQryTFL:SetString( 5, cRevisao )
		EndIf

		cAliasTFL := oQryTFL:OpenAlias()

		If !(cAliasTFL)->(EOF())
			cCodTFL := (cAliasTFL)->TFL_CODIGO							
		EndIf

		(cAliasTFL)->(dbCloseArea())
		oQryTFL:Destroy()
		FwFreeObj(oQryTFL)

		If !Empty(cCodTFL)
			cCodigoTFL  := cCodTFL
			oMdl:GetModel("TFQMASTER"):LoadValue("TFQ_CODTFL",cCodTFL)
		EndIf
	EndIf
EndIf

Return
//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Mot

TRIGGER Controla WHEN do campo TFQ_ENTORI
@author Roberto Santiago
@since 02/01/2025
@Param cMotivo - Motivo da Movimentação (TFQ)
	   cCampo - 'TFQ_MOVIVO
@return	TFQ_ENTORI
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Mot(cMotivo) as character
Return IIF(cMotivo == '1', '1', IIF(cMotivo == '5', '2', ''))

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At880Posto

Controla WHEN do campo TFQ_POSTO
@author Breno.gomes
@since 27/03/2025
@return	.T. / .F.
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At880Posto() as logical
return (!EMPTY(FwFldGet("TFQ_CONTRT")) .And. (!EMPTY(FwFldGet("TFQ_ORIGEM")) .Or. !EMPTY(FwFldGet("TFQ_DESTIN"))))
