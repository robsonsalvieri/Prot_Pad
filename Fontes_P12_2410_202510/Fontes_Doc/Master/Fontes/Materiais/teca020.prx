#Include 'Protheus.ch'
#INCLUDE "TECA020.CH"
#INCLUDE 'FWMVCDEF.CH'
#Include 'TBICONN.ch'

//Itens Funcionarios
#DEFINE P_FILIAL			1
#DEFINE P_MAT				2
#DEFINE P_NOME				3
#DEFINE P_CARGO			4
#DEFINE P_DESCARG			5
#DEFINE P_FUNCAO			6
#DEFINE P_TURNO			7
#DEFINE P_CC				8
#DEFINE P_DESFUNC			9

PUBLISH MODEL REST NAME TECA020 source TECA020


Static dPonIni		:= Stod("")
Static dPomFim		:= SToD("")
Static oMdlTrans	:= Nil

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³FunCAo    ³ TECA020  ³ Autor ³ Vendas e CRM          ³ Data ³ 13/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Programa de Manutencao no Cadastro de Tecnicos             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function TECA020(nOpcAuto,xRotAuto)
Local oBrowse

Default nOpcAuto := 0
Default xRotAuto := Nil

If ValType(xRotAuto) <> "A"
	If FindFunction("TecExecNPS")
		TecExecNPS()
	EndIf
	oBrowse := FWMBrowse():New()
	oBrowse:SetAlias('AA1')
	oBrowse:SetDescription(STR0001) // Cadastro de Tecnicos
	oBrowse:DisableDetails()
	
	//Legendas para o browse
	oBrowse:Addlegend( "AT020Legen() == 1" 	, "RED" 	, STR0082 ) // "Alocação Disponível"
	oBrowse:Addlegend( "AT020Legen() == 2"	, "BLUE"	, STR0083 ) // "Intermitente"
	oBrowse:Addlegend( "AT020Legen() == 3"	, "GREEN"	, STR0081 ) // "Alocação Indisponível"
	oBrowse:Activate()
Else
	If TYPE("aRotina") != 'A'
		aRotina := MenuDef()	
	EndIf
	FWMVCRotAuto(ModelDef(),"AA1",nOpcAuto,{{"AA1MASTER",xRotAuto}})
EndIf

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MenuDef   ³ Autor ³ Vendas CRM		    ³ Data ³ 13/02/2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³Funcao de definicao do aRotina                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³aRotina   retorna a array com lista de aRotina               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³TECA020                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
Local aMenu:={}

aAdd(aMenu,{ STR0015, 'VIEWDEF.TECA020', 0 , 2, 0, .T. } ) // 'Visualizar'
aAdd(aMenu,{ STR0016, 'VIEWDEF.TECA020', 0, 3, 0, .T. } ) // 'Incluir'
aAdd(aMenu,{ STR0017, 'VIEWDEF.TECA020', 0, 4, 0, .T. } ) // 'Alterar'
aAdd(aMenu,{ STR0018, 'VIEWDEF.TECA020', 0, 5, 0, .T. } ) // 'Excluir'
aAdd(aMenu,{ STR0019, 'VIEWDEF.TECA020', 0, 8, 0, .T. } ) // 'Imprimir'
aAdd(aMenu,{ STR0020, 'VIEWDEF.TECA020', 0, 9, 0, .T. } ) // 'Copiar'
aAdd(aMenu,{ STR0021,'At020ImpFnc', 0, 3, 0, .F.})//"Importação de Funcionários"

If SuperGetMv("MV_GSCAROL",,"1") == "2"
	aAdd(aMenu,{ STR0116,'attToCarol', 0, 3, 0, .F.})//"Envio de fotos para Carol"
	aAdd(aMenu,{ STR0117,'tecInvUsers', 0, 3, 0, .F.})//"Criar usuários Carol"
EndIf
If SuperGetMV("MV_GSXINT",,"2") == "2"
	aAdd( aMenu, { STR0084 , 'At020Convo', 0, 4, 0, .F.}) // "Convocações"
EndIf
If SuperGetMV("MV_TECXRH",,".F.")
	aAdd(aMenu,{ STR0094,'AT020RAAte', 0, 2, 0, .F.})// "Relatorio de Agendas"
EndIf


Return aMenu

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ModelDef  º Autor ³ Vendas CRM         º Data ³ 13/02/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Define o modelo de dados (MVC)                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
 
Static Function ModelDef()
Static oModel
Local oStruAA1		:= FWFormStruct(1,'AA1',/*bAvalCampo*/,/*lViewUsado*/)
Local bCommit		:= {|oMdl|At020VlMVC(oMdl)}		//Funcao a ser executada no momento da exclusao
Local bPosValidacao	:= {||At020VdVis()}
Local aAux			:= {}
Local aAux1			:= {}
Local aAux2			:= {}
Local aAux3			:= {}
Local aAux4			:= {}
Local aAux5			:= {}
Local cGSxInt		:= SuperGetMV("MV_GSXINT",,"2")
Local aCpoLoad		:= At020LdVis(cGSxInt)
Local nC			:= 0

If cGSxInt == "2" .AND. !IsInCallStack("AutoInclude")
	aAux := FwStruTrigger("AA1_CDFUNC","AA1_TURNO","At020Desc()",.F.,Nil,Nil,Nil)
	oStruAA1:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])
	
	aAux1 := FwStruTrigger("AA1_CDFUNC","AA1_CC","At020DesCC()",.F.,Nil,Nil,Nil)
	oStruAA1:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])
	
	aAux2 := FwStruTrigger("AA1_CDFUNC","AA1_FUNFIL","At020Filial()",.F.,Nil,Nil,Nil)
	oStruAA1:AddTrigger(aAux2[1],aAux2[2],aAux2[3],aAux2[4])

	aAux2 := FwStruTrigger("AA1_CDFUNC","AA1_CODTEC","FwFldGet('AA1_FUNFIL')+Posicione('SRA',1,FwFldGet('AA1_FUNFIL')+FwFldGet('AA1_CDFUNC'),'RA_MAT')",.F.,Nil,Nil,Nil)
	oStruAA1:AddTrigger(aAux2[1],aAux2[2],aAux2[3],aAux2[4])

	aAux3 := FwStruTrigger("AA1_CDFUNC","AA1_NOMTEC","At020Nome()",.F.,Nil,Nil,Nil)
		oStruAA1:AddTrigger(aAux3[1],aAux3[2],aAux3[3],aAux3[4])
	
	aAux4 := FwStruTrigger("AA1_CDFUNC","AA1_SEQTUR","At020SeqTr()",.F.,Nil,Nil,Nil)
		oStruAA1:AddTrigger(aAux4[1],aAux4[2],aAux4[3],aAux4[4])
		
	aAux5 := FwStruTrigger("AA1_CDFUNC","AA1_FUNCAO","At020Funcao()",.F.,Nil,Nil,Nil)
		oStruAA1:AddTrigger(aAux5[1],aAux5[2],aAux5[3],aAux5[4])
ElseIF cGSxInt <> "2"
	oStruAA1:SetProperty( 'AA1_FUNCAO' , MODEL_FIELD_OBRIGAT, .F.)
	For nC := 1 to Len(aCpoLoad)
		oStruAA1:SetProperty( aCpoLoad[nC] , MODEL_FIELD_WHEN, { || Empty(FwFldGet("AA1_CDFUNC")) })
	Next nC
	oStruAA1:SetProperty( 'AA1_CC' , MODEL_FIELD_WHEN, { || !Empty(FwFldGet("AA1_CDFUNC")) })
EndIf

oModel := MPFormModel():New('TECA020',/*bPreValidacao*/,bPosValidacao,bCommit,/*bCancel*/)
oModel:AddFields('AA1MASTER',/*cOwner*/,oStruAA1,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
oModel:SetDescription(STR0001)
If nModulo == 28 .Or. IsInCallStack("AT020INCAA1") //Tira a obrigatoriedade dos campos somente quando for executado o SIGATEC.
	oStruAA1:SetProperty( 'AA1_EMINFI' , MODEL_FIELD_OBRIGAT, .F.)
	oStruAA1:SetProperty( 'AA1_FUNPRO' , MODEL_FIELD_OBRIGAT, .F.)
	oStruAA1:SetProperty( 'AA1_LIBOSV' , MODEL_FIELD_OBRIGAT, .F.)
	oStruAA1:SetProperty( 'AA1_REQPEC' , MODEL_FIELD_OBRIGAT, .F.)
	oStruAA1:SetProperty( 'AA1_CODVEN' , MODEL_FIELD_OBRIGAT, .F.)
	oStruAA1:SetProperty( 'AA1_NOMUSU' , MODEL_FIELD_OBRIGAT, .F.)
EndIf
Return(oModel)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ViewDef   º Autor ³ Vendas CRM         º Data ³  13/02/2012 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Define a interface para cadastro em MVC.                    º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TECA020')
Local oStruAA1 := FWFormStruct(2,'AA1')

oStruAA1:SetProperty('AA1_FUNFIL', MVC_VIEW_ORDEM, Soma1(oStruAA1:GetProperty('AA1_CDFUNC', MVC_VIEW_ORDEM)))

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField('VIEW_AA1', oStruAA1,'AA1MASTER')
oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_AA1','TELA')
oView:AddUserButton("Histórico Disciplinar", 'CLIPS',{|oView| At020Disci(FwFldGet("AA1_CODTEC"))}) 
If SuperGetMv("MV_GSCAROL",,"1") == "2"
	oView:AddUserButton(STR0119, 'CLIPS',{|| attToCarol(.T.,FwFldGet("AA1_CODTEC"))}) //"Exportar foto para Carol"
	oView:AddUserButton(STR0118, 'CLIPS',{|| tecInvUsers(.T.,FwFldGet("AA1_CODTEC"))}) //"Criar conta Carol"
EndIf
If SuperGetMv("MV_FINDME",,1) == 2
	oView:AddUserButton("Integrar FindMe", 'CLIPS',{|| intFindMe()}) //"Integrar Findme"
EndIf
oStruAA1:RemoveField("AA1_TIPMAR")

Return(oView)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At020VlDel³ Autor ³VENDAS E CRM           ³ Data ³ 13/02/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao da Exclusao do Tecnico                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At020VlMVC(oMdl,lAutomato)
 
local lRetorno := .T.
Local nOpc := oMdl:GetOperation() 
Local cFuncio   := FwFldGet("AA1_CDFUNC")
Local cVistor   := FwFldGet("AA1_VISTOR")
Local cLocalA	:= FwFldGet("AA1_LOCAL")
Local cLocaZB	:= FwFldGet("AA1_LOCLZB")
Local cLocaZR	:= FwFldGet("AA1_LOCLZR")
Local nValors	:= FwFldGet("AA1_VALOR")
Local cRateio	:= FwFldGet("AA1_RATE")
Default lAutomato := .F.
Default oMdl	:=	Nil

If !lAutomato
	FWFormCommit( oMdl )
EndIf
If nOpc == MODEL_OPERATION_DELETE 	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica na Amarracao Cliente X Eqto                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno )
		dbSelectArea("AA3")
		dbSetOrder(5)
		If ( dbSeek(xFilial("AA3")+AA1->AA1_CODTEC) )
			Help(" ",1,"AT020DEL01")
			lRetorno := .F.
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica no Apontamento Tecnico                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno )
		dbSelectArea("AB9")
		dbSetOrder(3)
		If ( dbSeek(xFilial("AB9")+AA1->AA1_CODTEC) )
			Help(" ",1,"AT020DEL02")
			lRetorno := .F.
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica na Agenda do Tecnico                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno )
		dbSelectArea("ABB")
		dbSetOrder(1)
		If ( dbSeek(xFilial("ABB")+AA1->AA1_CODTEC) )
			Help(" ",1,"AT020DEL03")
			lRetorno := .F.
		EndIf
	EndIf
	     
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica nas habilidades do tecnico                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno )
		dbSelectArea("AA2")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AA2")+AA1->AA1_CODTEC) )
			Help(" ",1,"HELP", , STR0002, 3, 1 )  //"Nao e possivel a exclusao de um tecnico vinculado a uma habilidade"
			lRetorno := .F.		
	    Endif
	Endif	

	If  lRetorno .And. FindFunction("TECTelMets")
	    TECTelMets("exclui_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
	Endif		

Elseif nOpc == MODEL_OPERATION_UPDATE .And. FindFunction("TECTelMets") 
    TECTelMets("altera_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
EndIf

If  FindFunction("TECTelMets") .And. !Empty(cFuncio) .And. nOpc != MODEL_OPERATION_DELETE 
	TECTelMets( "cdfunc_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif
If  FindFunction("TECTelMets") .And. !Empty(cVistor) .And. nOpc != MODEL_OPERATION_DELETE
	TECTelMets( "vistor_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif
If FindFunction("TECTelMets") .And. !Empty(cLocalA) .And. nOpc != MODEL_OPERATION_DELETE 
	TECTelMets( "local_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif	
If FindFunction("TECTelMets") .And. !Empty(cLocaZB)  .And. nOpc != MODEL_OPERATION_DELETE
	TECTelMets( "local_boa_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif	
If FindFunction("TECTelMets") .And. !Empty(cLocaZR)  .And. nOpc != MODEL_OPERATION_DELETE
	TECTelMets( "local_ruim_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif	
If FindFunction("TECTelMets") .And. !Empty(nValors)  .And. nOpc != MODEL_OPERATION_DELETE
	TECTelMets( "valor_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
Endif 
If FindFunction("TECTelMets") .And. !Empty(cRateio)  .And. nOpc != MODEL_OPERATION_DELETE
	TECTelMets( "cust_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
EndIF

If isInCallStack("AT020GFUN")
	oMdl:DeActivate()
EndIf

Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At020VlDel³ Autor ³ Eduardo Riera         ³ Data ³ 04.11.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao da Exclusao do Tecnico                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Logico                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At020VlDel()


local lRetorno := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica na Amarracao Cliente X Eqto                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AA3")
	dbSetOrder(5)
	If ( dbSeek(xFilial("AA3")+AA1->AA1_CODTEC) )
		Help(" ",1,"AT020DEL01")
		lRetorno := .F.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica no Apontamento Tecnico                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AB9")
	dbSetOrder(3)
	If ( dbSeek(xFilial("AB9")+AA1->AA1_CODTEC) )
		Help(" ",1,"AT020DEL02")
		lRetorno := .F.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica na Agenda do Tecnico                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("ABB")
	dbSetOrder(1)
	If ( dbSeek(xFilial("ABB")+AA1->AA1_CODTEC) )
		Help(" ",1,"AT020DEL03")
		lRetorno := .F.
	EndIf
EndIf
     
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica nas habilidades do tecnico                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno )
	dbSelectArea("AA2")
	dbSetOrder(1)
	If ( dbSeek(xFilial("AA2")+AA1->AA1_CODTEC) )
		Help(" ",1,"HELP", , STR0002, 3, 1 )  //"Nao e possivel a exclusao de um tecnico vinculado a uma habilidade"
		lRetorno := .F.
	EndIf
EndIf

Return(lRetorno)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020VdVisº Autor ³ Vendas CRM         º Data ³ 11/07/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se o vistoriador esta associado a um usuario do    º±±
±±º			 ³Sistema.                           						  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At020VdVis() 
   
Local lRetorno	:= .T.
Local cCodUsr	:= FwFldGet("AA1_CODUSR")
Local cVistor	:= FwFldGet("AA1_VISTOR")


If cVistor == "1" .AND. Empty(cCodUsr)
	Help("",1,"AT020VUSR") 
	lRetorno := .F.
EndIf 

Return(lRetorno)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020VdVisº Autor ³ Vendas CRM         º Data ³ 06/08/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Verifica se o vistoriador esta associado a um usuario do    º±±
±±º			 ³Sistema.(Formulario nao MVC)		                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At020VdVst() 
   
Local lRetorno	:= .T.

If M->AA1_VISTOR == "1" .AND. Empty(M->AA1_CODUSR)
	Help("",1,"AT020VUSR") 
	lRetorno := .F.
EndIf 

Return(lRetorno)

/*                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020Desc   º Autor ³ Vendas CRM     º Data ³ 19/10/12º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Gatilho do Turno									         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At020Desc()
Local cCodTur	:= ""
Local aArea	:= GetArea()

cCodTur := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_TNOTRAB") )

RestArea(aArea)

Return( cCodTur )

/*                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020Desc   º Autor ³ Vendas CRM     º Data ³ 19/10/12º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Gatilho do Centro de Custo						         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At020DesCC()
Local cCodCC	:= ""
Local aArea	:= GetArea()

cCodCC := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_CC") )

RestArea(aArea)

Return( cCodCC )

/*                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020VLFun   º Autor ³ Vendas CRM    º Data ³ 19/10/12º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação dos campos AA1_TURNO e AA1_CC 			     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At020VLFun(nOption) 
Local cCodCC		:= ""
Local cCodTur		:= ""
Local lRetorno 	:= .F.

If !Empty(FwFldGet("AA1_CDFUNC"))

	If nOption == 1
		cCodCC := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_CC") )
		If Alltrim(FwFldGet("AA1_CC")) == cCodCC
			lRetorno := .T.
		EndIf 
	Else
		cCodTur := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_TNOTRAB") )
		If Alltrim(FwFldGet("AA1_TURNO")) == cCodTur
			lRetorno := .T.
		EndIf 
	EndiF
Else
	lRetorno := .T.	
EndIf

If !lRetorno .AND. nOption == 1
	HELP(' ',1,'AT020CC')
ElseIf !lRetorno .AND. nOption == 2
	HELP(' ',1,'AT020TURNO')	
EndIf	


Return( lRetorno )

/*                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020VLFun   º Autor ³ Vendas CRM    º Data ³ 19/10/12º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Validação do campo AA1_MPONTO						º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At020VLMarc()

Local cConteudo	:= &(ReadVar())		// Variavel corrente que esta sendo editada.
Local lRet		:= .T. 					// Retorno da validacao.

If !Empty(FwFldGet("AA1_MPONTO"))
	If  ( FwFldGet("AA1_MPONTO") == "1" .AND. Empty(FwFldGet("AA1_CDFUNC")) )
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³	 Problema: Não será possível marcar ponto sem a seleção de funcionario. ³	
		//³	 Solucao: Selecione um funcionario para o atendente.                    ³	
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Help("",1,"AT020MPONTO")
		lRet := .F.
	EndIf	
EndIf		                                                       
		 

Return ( lRet )       

/*                               
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At020Desc º Autor ³ Vendas CRM      º Data ³ 19/10/12 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Gatilho do Centro de Custo						    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA020                                               º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At020Filial()
Local cCodFil	:= ""
Local aArea	:= GetArea()

cCodFil := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_FILIAL") )

RestArea(aArea)

Return( cCodFil )       

//------------------------------------------------------------------------------
/*/{Protheus.doc} At020Disci()
Rotina Abre a Tela do Histórico Disciplina do Atendente Posicionado

@author arthur.colado
@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------

Function At020Disci(cCodTec)
Local oPanel            := Nil
Local oBrowse           := Nil

DEFINE MSDIALOG oPanel TITLE "Histórico Disciplinar" FROM 050,050 TO 500,800 PIXEL//"Histórico Disciplinar"

oBrowse:= FWmBrowse():New()
oBrowse:SetOwner( oPanel )   
oBrowse:SetDescription( "Histórico Discilinar") //"Histórico Discilinar"
oBrowse:SetAlias( "TIT" )   
oBrowse:DisableDetails() 
oBrowse:SetWalkThru(.F.)
oBrowse:SetAmbiente(.F.)
oBrowse:SetProfileID("01") 
oBrowse:SetMenuDef( "  " )
oBrowse:SetFilterDefault( "TIT_CODTEC = '" + cCodTec + "' " ) 
oBrowse:Activate() 

//bloco de codigo para duplo click - deve ficar após o activate, senao o FWMBrowse ira sobreescrever com o bloco padrao
oBrowse:BlDblClick := {||At020VisDisci()}  
oBrowse:Refresh()

ACTIVATE MSDIALOG oPanel CENTERED

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} At020Disci()
Rotina Realiza a abertura da tela de disciplina

@author arthur.colado
@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------

Function At020VisDisci()
Local aArea       := GetArea()
                 
DbSelectArea("TIT")
TIT->(DbSetOrder(1))
      
If TIT->(DbSeek(xFilial("TIT")+TIT->TIT_CODIGO))
      FWExecView(Upper("Visualizar Disciplina"),"VIEWDEF.TECA440",MODEL_OPERATION_VIEW,/*oDlg*/,/*bCloseOnOk*/,/*bOk*/,/*nPercReducao*/)//"Visualizar Disciplina"      
EndIf

RestArea(aArea)

Return (.T.)

//------------------------------------------------------------------------------
/*/{Protheus.doc} At020ImpFnc

Criação do Browse para importar funcionários

@sample 	At020ImpFnc() 
@since		12/12/2017      
@version 	P12
@return 	lOk
/*/
//------------------------------------------------------------------------------
Function At020ImpFnc(cTab,nRecno,nOpc,lAuto,cMat )

Local oMrkBrowse	:= FWMarkBrowse():New()
Local oGSTmpTb		:= Nil
Local lOk 			:= .T.
Local aFuncionar	:= {}
Local aStruct		:= {}
Local aIdx			:= {}
Local aColumns    	:= {}
Local aSeek			:= {}
Local aInsertTmp	:= {}
Local nStepCmmIns	:= 900 //Quantidade do lote de regsitros a serem gravados na tabela temporária a cada INSERT do objeto GsTmpTable
Local nX			:= 1
Local nY			:= 1
Local lPeAt020Fu	:= ExistBlock("At020IFunc") 
Local cGSxInt		:= SuperGetMV("MV_GSXINT",,"2")
Local cMarca		:= IIF(cGSxInt == "3", "RM", "")
Local cMsgErro		:= ""
Local cFiltro 		:= ""
Local lConsole		:= IsBlind()
Local aNoCols		:= At020NoCols(cGSxInt)

Default cMat	:= ''
Default lAuto	:= .F.

If !lPeAt020Fu
	If Empty(cMarca)
		aFuncionar := At850Func()//Carrega lista dos funcionários 
	Else
		
		cFiltro := At020FilRM(lConsole)
		If !lConsole
			MsgRun ( STR0075, STR0076, { || aFuncionar := At020ItG(cMarca, @cMsgErro, cFiltro) } ) //"Consultando Atendentes" #"Aguarde"
		Else
			aFuncionar := At020ItG(cMarca, @cMsgErro, cFiltro) 
		EndIf
		
		If !Empty(cMsgErro)
			Help(,, "At020ImpFnc",,cMsgErro ,1, 0)
			lOk := .F.
		Else
			lOk = Len(aFuncionar) > 0
		EndIf
		
	EndIf
Else
	aFuncionar := ExecBlock('At020IFunc', .f., .f.)
	If Valtype(aFuncionar) <> "A"
		aFuncionar := {}
		Help(" ",1,"HELP", , STR0073, 3, 1 ) //"Problemas no retorno do Ponto de Entrada At020IFunc"
		lOk := .F.
	EndIf
EndIf

If lOk
	//Cria estrutura e tabela tmp com os campos necessarios
	Aadd(aStruct, {"OK"        , "C", 1 , 0})
	Aadd(aStruct, {"RA_FILIAL"	, "C", TamSX3("RA_FILIAL")[1]	, TamSX3("RA_FILIAL")[2]})
	Aadd(aStruct, {"RA_MAT"		, "C", TamSX3("RA_MAT")[1]		, TamSX3("RA_MAT")[2]})
	Aadd(aStruct, {"RA_NOME"	, "C", TamSX3("RA_NOME")[1]		, TamSX3("RA_NOME")[2]})
	Aadd(aStruct, {"RA_CARGO"	, "C", TamSX3("RA_CARGO")[1]	, TamSX3("RA_CARGO")[2]})
	Aadd(aStruct, {"RA_DCARGO"	, "C", TamSX3("Q3_DESCSUM")[1]	, TamSX3("Q3_DESCSUM")[2]})
	Aadd(aStruct, {"RA_CODFUN"	, "C", TamSX3("RA_CODFUNC")[1]	, TamSX3("RA_CODFUNC")[2]})
	Aadd(aStruct, {"RA_DESCFU"	, "C", TamSX3("RJ_DESC")[1]		, TamSX3("RJ_DESC")[2]})
	Aadd(aStruct, {"RA_CC"		, "C", TamSX3("RA_CC")[1]		, TamSX3("RA_CC")[2]})
	Aadd(aStruct, {"RA_TNOTRAB"	, "C", TamSX3("RA_TNOTRAB")[1]	, TamSX3("RA_TNOTRAB")[2]})    

	//Cria indices para a tabela temporária 
	Aadd(aIdx, {"I1",{ 'RA_MAT' }})
	Aadd(aIdx, {"I2",{ 'RA_CARGO'}})
	Aadd(aIdx, {"I3",{ 'RA_CODFUN'}})
	Aadd(aIdx, {"I4",{ 'RA_CC'}})
	Aadd(aIdx, {"I5",{ 'RA_TNOTRAB'}})
	Aadd(aIdx, {"I6",{ 'RA_NOME'}})
	
	
		
	//Cria array da busca de acordo com os indices da tabela temporária
	aAdd(aSeek, {TxDadosCpo('RA_MAT')[1]	,{{'','C',TamSX3('RA_MAT')[1],TamSX3('RA_MAT')[2],TxDadosCpo('RA_MAT')[1],PesqPict('SRA','RA_MAT'),NIL}},1, aScan(aNoCols, {|c| c == "RA_MAT" }) = 0})
	aAdd(aSeek, {TxDadosCpo('RA_CARGO')[1]	,{{'','C',TamSX3('RA_CARGO')[1],TamSX3('RA_CARGO')[2],TxDadosCpo('RA_CARGO')[1],PesqPict('SRA','RA_CARGO'),NIL}},2, aScan(aNoCols, {|c| c == "RA_CARGO" }) = 0})
	aAdd(aSeek, {TxDadosCpo('RA_CODFUNC')[1],{{'','C',TamSX3('RA_CODFUNC')[1],TamSX3('RA_CODFUNC')[2],TxDadosCpo('RA_CODFUNC')[1],PesqPict('SRA','RA_CODFUNC'),NIL}},3, aScan(aNoCols, {|c| c == "RA_CODFUN" }) = 0})
	aAdd(aSeek, {TxDadosCpo('RA_CC')[1]		,{{'','C',TamSX3('RA_CC')[1],TamSX3('RA_CC')[2],TxDadosCpo('RA_CC')[1],PesqPict('SRA','RA_CC'),NIL}},4, aScan(aNoCols, {|c| c == "RA_CC" }) = 0})
	//
	aAdd(aSeek, {TxDadosCpo('RA_TNOTRAB')[1],{{'','C',TamSX3('RA_TNOTRAB')[1],TamSX3('RA_TNOTRAB')[2],TxDadosCpo('RA_TNOTRAB')[1],PesqPict('SRA','RA_TNOTRAB'),NIL}},5, aScan(aNoCols, {|c| c == "RA_TNOTRAB" }) = 0})
	aAdd(aSeek, {TxDadosCpo('RA_NOME')[1],{{'','C',TamSX3('RA_NOME')[1],TamSX3('RA_NOME')[2],TxDadosCpo('RA_NOME')[1],PesqPict('SRA','RA_NOME'),NIL}},6, aScan(aNoCols, {|c| c == "RA_NOME" }) = 0})
	
	
	//Instancia o método NEW para criação da tabela temporária
	oGSTmpTb := GSTmpTable():New('TRBAA1',aStruct, aIdx, {}, nStepCmmIns )
	cRetTab  := 'TRBAA1'
	
	//Validação para a criação da tabela temporária
	If !oGSTmpTb:CreateTMPTable()
		oGSTmpTb:ShowErro()
	Else
		//Preenche Tabela temporária com as informações filtradas
		For nX := 1 To Len(aFuncionar)
			aInsertTmp :={}
			Aadd(aInsertTmp, {'RA_FILIAL'	,aFuncionar[nX][1]})
			Aadd(aInsertTmp, {'RA_MAT'		,aFuncionar[nX][2]})
			Aadd(aInsertTmp, {'RA_NOME'		,At020CharE(aFuncionar[nX][3])})
			Aadd(aInsertTmp, {'RA_CARGO'	,aFuncionar[nX][4]})
			Aadd(aInsertTmp, {'RA_DCARGO'	,aFuncionar[nX][5]})
			Aadd(aInsertTmp, {'RA_CODFUN'	,aFuncionar[nX][6]})
			Aadd(aInsertTmp, {'RA_DESCFU'	,aFuncionar[nX][9]})
			Aadd(aInsertTmp, {'RA_CC'		,aFuncionar[nX][8]})
			Aadd(aInsertTmp, {'RA_TNOTRAB'	,aFuncionar[nX][7]})

			If oGSTmpTb:Insert(aInsertTmp)
				lOk := oGSTmpTb:Commit()
			Else
				lOk := .F.
				Exit
			EndIf
		Next nX
		
		//MarkBrowse
		For nY := 1 To Len(aStruct)
			If aStruct[nY][1] <> "OK" .AND. aStruct[nY][1] <> "RA_FILIAL"  .AND. aScan(aNoCols, {|c| c == aStruct[nY][1] }) = 0
				AAdd(aColumns,FWBrwColumn():New())
				aColumns[Len(aColumns)]:SetData( &("{||"+aStruct[nY][1]+"}") )
				aColumns[Len(aColumns)]:SetTitle(RetTitle(aStruct[nY][1]))
				aColumns[Len(aColumns)]:SetSize(aStruct[nY][3])
				aColumns[Len(aColumns)]:SetDecimal(aStruct[nY][4])
				aColumns[Len(aColumns)]:SetPicture(PesqPict(cRetTab,aStruct[nY][1]))
			EndIf
		Next nY
	
		If !lAuto
			DEFINE MSDIALOG oDlg TITLE STR0004 From 300,0 To 700,1000 PIXEL //"Funcionários sem relacionamento com Atendentes:"
			oMrkBrowse:SetOwner(oDlg)
			oMrkBrowse:DisableFilter()
		EndIf
		
		oMrkBrowse:SetDescription(STR0003) //"Importação de Funcionários"
		oMrkBrowse:SetTemporary(.T.)     	
		oMrkBrowse:AddButton(STR0031,{||At020GFun(cRetTab,oMrkBrowse,oGSTmpTb),oDlg:End()},,3,)		//"Gerar"
		oMrkBrowse:AddButton(STR0007,{||At850CaAtd(, 3, "INCLUIR"),oDlg:End()},,3,,.F., 1)	//"Cad. Atendente"
		oMrkBrowse:SetFieldMark("OK")
		oMrkBrowse:SetAlias(cRetTab) //Seta o arquivo temporario para exibir a seleção dos dados
		oMrkBrowse:SetSeek(.T., aSeek) 
		oMrkBrowse:SetAllMark( { || oMrkBrowse:AllMark() } )
		oMrkBrowse:SetColumns(aColumns)
		oMrkBrowse:DisableReport()
		oMrkBrowse:SetMenuDef("")
		
		If !lAuto
			oMrkBrowse:Activate()
			ACTIVATE MSDIALOG oDlg CENTERED	
		Else 
			At020Mark(oMrkBrowse,cRetTab,lAuto, cMat)
			If !At020GFun(cRetTab,oMrkBrowse,oGSTmpTb)
				lOk := .F.
			EndIf
		EndIf
	     
		oGSTmpTb:Close()
		TecDestroy(oGSTmpTb)	    
	EndIf
EndIf

Return lOk
//------------------------------------------------------------------------------
/*/{Protheus.doc} At020Nome()
Gatilho Nome do Atendente

@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------
Function At020Nome()
Local cCodNom	:= ""
Local aArea	:= GetArea()

cCodNom := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_NOME") )

RestArea(aArea)

Return( cCodNom )    

//------------------------------------------------------------------------------
/*/{Protheus.doc} At020SeqTr()
Gatilho Sequencia do Turno

@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------
Function At020SeqTr()
Local cSeqTur	:= ""
Local aArea	:= GetArea()

cSeqTur := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_SEQTURN") )

RestArea(aArea)

Return( cSeqTur )    

//------------------------------------------------------------------------------
/*/{Protheus.doc} At020Funcao()
Gatilho da Função

@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------
Function At020Funcao()
Local cFuncao	:= ""
Local aArea	:= GetArea()

cFuncao := Alltrim( Posicione("SRA",1,xFilial("SRA")+FwFldGet("AA1_CDFUNC"),"RA_CODFUNC") )

RestArea(aArea)

Return( cFuncao )    


//------------------------------------------------------------------------------
/*/{Protheus.doc} At020AltRH()
Função para alteração das informações do RH

@since 18/03/2014
@version 1.0
/*/
//------------------------------------------------------------------------------
Function At020AltRH(cFilDe, cMat, aCampos, lPos, cFilAtend, cFilAte, cEmpAnt, cEmpAte, aNewAA1)
Local aRetAgend		:= {}
Local lRet			:= .T.
Local nI			:= 0
Local lSIXAA1 		:= checkSIX("AA1", 7)
Local lGpea180		:= IsInCallStack("GPEA180") //Verifica se a chamada foi feita para a transferencia de funcionario
Local lNewAtend 	:= .F.
Local cCodAtend		:= ""
Local lIncAA1		:= .T.
Local nPosTecAtu	:= 0

Default cFilDe 		:= ""
Default cMat		:= ""
Default aCampos		:= {}	
Default lPos		:= .T.
Default cFilAtend	:= ""
Default cFilAte		:= ""
// Default cEmpAnt		:= "" //debito tec.
Default cEmpAte		:= ""
Default aNewAA1		:= {}

//Verifica se existe o indice 7 para poder realizar a replica.
If !lSIXAA1
	Return(.F.)
EndIf

//Define se o registro já está posicionado
If !lPos
	DbSelectArea("AA1")
	AA1->(DbSetOrder(7))
			
	If !AA1->(DbSeek(cFilDe + cMat + cFilAtend))
		Return(.F.)
	EndIf	
EndIf

//Verifica se há campo e valor a serem alterados
If !Empty(aCampos)

	//Estrutura do aCampo
	//[N][1] - Nome do Campo
	//[N][2] - Valor alterado 

	//Campos que podem ser Alterados
	// RA_NOME
	// RA_CC
	// RA_TNOTRAB
	// RA_SEQTURN
	// RA_CODFUNC
	// RA_MAT
	//RA_FILIAL
	BEGIN TRANSACTION
	
		RecLock("AA1" , .F.)
		
		If !lGpea180 
			For nI := 1 To Len(aCampos)
					
					Do Case
						Case aCampos[nI][1] == "RA_NOME"			
							REPLACE AA1_NOMTEC With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_CC"
							REPLACE AA1_CC With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_TNOTRAB"
							REPLACE AA1_TURNO With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_SEQTURN"
							If AA1->(FieldPos("AA1_SEQTUR")) > 0
								REPLACE AA1_SEQTUR With aCampos[nI][2]
							EndIF	
						Case aCampos[nI][1] == "RA_CODFUNC"
							REPLACE AA1_FUNCAO With aCampos[nI][2]	
						Case aCampos[nI][1] == "RA_MAT" 
							REPLACE AA1_CDFUNC With aCampos[nI][2]		
						Case aCampos[nI][1] == "RA_FILIAL"
							REPLACE AA1_FUNFIL With aCampos[nI][2]		
					EndCase
			
			Next nI
		Else
			If cFilDe <> cFilAte .OR. cEmpAnt <> cEmpAte
				REPLACE AA1_ALOCA With "2" //Deixa o Atendente como Indisponível
				lNewAtend := .T.
				cCodAtend := AA1->AA1_CODTEC
			Else
				For nI := 1 To Len(aCampos)
					Do Case
						Case aCampos[nI][1] == "RA_NOME"			
							REPLACE AA1_NOMTEC With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_CC"
							REPLACE AA1_CC With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_TNOTRAB"
							REPLACE AA1_TURNO With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_SEQTURN"
							If AA1->(FieldPos("AA1_SEQTUR")) > 0
								REPLACE AA1_SEQTUR With aCampos[nI][2]
							EndIF
						Case aCampos[nI][1] == "RA_MAT" 
							REPLACE AA1_CDFUNC With aCampos[nI][2]
						Case aCampos[nI][1] == "RA_CODFUNC"
							REPLACE AA1_FUNCAO With aCampos[nI][2]			
					EndCase
				Next nI
			EndIf
		EndIf
		
		AA1->(MsUnlock())

		If lNewAtend .And. Len(aNewAA1) > 1
			lIncAA1 := At020IncAA1(aNewAA1) 
			If lIncAA1 
				If Empty(dPonIni) .OR. Empty(dPomFim)
					PerAponta(@dPonIni , @dPomFim , Nil, .F.)
				EndIF
				aRetAgend := At020ExcEft( cCodAtend, dPonIni, dPomFim)
				nPosTecAtu := aScan(aNewAA1, {|x| x[1] == "AA1_CODTEC"})
				ExecAloca( aNewAA1[nPosTecAtu][2], aRetAgend, dPonIni, dPomFim, cCodAtend )
				If lGpea180
					ExecUnif( cFilDe, cCodAtend )
				EndIf
			Else 
				DisarmTransacation()
			EndIf
		EndIf 

	END TRANSACTION
	
EndIf

Return (lRet)


//------------------------------------------------------------------------------
/*/{Protheus.doc} At020VTCA
Função para validação do tamanho do campo AA1_CODTEC e suporte ao gatilho sequencia 2 do campo AA1_CDFUNC 

@sample 	At020VTCA()
@since		13/05/2015
@version	12.1.6
/*/
//------------------------------------------------------------------------------

Function At020VTCA()
Local lRet := .T.
Local aArea
Local cAliasQry	:= GetNextAlias()

If SuperGetMV("MV_GSXINT",,"2") == "2"
	lRet := TamSX3('AA1_CODTEC')[1] >= (TamSX3('RA_MAT')[1] + TamSX3('AA1_FILIAL')[1])
	If !lRet
		Help(" ",1,"AT020VTCA")	//###O tamanho do campo Código do Atendente tem tamanho insuficiente para receber o novo código que referencia o código do funcionário. ### Informe o ocorrido ao Administrador.###
	Else
		If !IsInCallStack("AutoInclude")
			lRet := (Empty(FwFldGet("AA1_CDFUNC")) .OR. ExistCPO("SRA", FwFldGet("AA1_CDFUNC"),1))
		EndIf
		
	EndIf
Else
	aArea := GetArea()
	BeginSql Alias cAliasQry
		Select 1 Contar
		  From %Table:AA1% AA1
		 Where AA1.AA1_FILIAL=%xFilial:AA1%
		   And AA1.AA1_CDFUNC=%Exp:FwFldGet("AA1_CDFUNC")%
		   And AA1.%notdel%
	EndSql
	If (cAliasQry)->(!EOF())
		lRet := .F.
		Help(,,"AT020CDFUNC",,STR0074,1,0) //"Código do funcionário (AA1_CDFUNC) já utilizado em outro atendente."
	EndIf
	(cAliasQry)->(dbCloseArea())
	RestArea(aArea)
EndIf

Return lRet
//-------------------------------------------------------------------------------------
/*/{Protheus.doc} At020VldFu
Valida para que nao seja possivel incluir o funcionario da SRA mais de uma vez como Atendente 
na AA1
@author  TOTVS SP - Equipe de Projetos Piloto do Segmento de Servicos e Juridico
@param   cCdFunc		Caracter - Código do funcionário no SIGAGPE
@param   cCdFilFun	Caracter - Código da filial do funcionário no SIGAGPE
@return  Nil
@since 	 01/12/2015
@version P12
/*/
//-------------------------------------------------------------------------------------
Function At020VldFu( cCdFunc, cCdFilFun )
Local lRet := .T.
Local aArea := AA1->(GetArea())
	
dbSelectArea('AA1')
AA1->(dbSetOrder(7)) // AA1_FILIAL+AA1_CDFUNC+AA1_FUNFIL
If AA1->(dbSeek(xFilial('AA1') + PadR(cCdFunc,TamSx3("AA1_CDFUNC")[1]) + PadR(cCdFilFun,TamSx3("AA1_FUNFIL")[1])))
	lRet := .F.
	Help(" ",1,"HELP", , STR0029 + Alltrim(AA1->AA1_CODTEC), 3, 1 ) //"Este funcionario ja esta matriculado como atendente pelo codigo nro: "
EndIf
RestArea(aArea)
	
Return lRet

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} At020VlDtC
Alerta para vencimento do curso do atendente
@param  aCodAtend	Array   contendo Codigo(s) do atendente(s)
@param  lShowPerg	Boolean se deve perguntar se deseja continuar?
@sample At020VlDtC(aCodAtend,lShowPerg)
@since  28/08/2015
@return lRet Boolean
/*/
//-------------------------------------------------------------------------------------
Function At020VlDtC(aCodAtend,lShowPerg)

Local aAreaAtu	:= GetArea()
Local aAreaAA1	:= AA1->(GetArea())
Local aAreaSRA	:= SRA->(GetArea())
Local aAreaRAL	:= RAL->(GetArea())
Local cAliasQry	:= GetNextAlias()
Local lRetorno	:= .T.
Local nInd
Local aAtendente	:= {}  //Atendente com curso vencido
Local cMensagem	:= STR0025+Space(1)//"Funcionário"
Local cSeparador	:= " - "
Local lTemCursoVenc
Local cFilAA1		:= xFilial("AA1")
Local cFilRAL		:= xFilial("RAL")
Default lShowPerg	:= .T.

AA1->(dbsetorder(1))//AA1_FILIAL+AA1_CODTEC
SRA->(dbsetorder(1))//RA_FILIAL+RA_MAT
RAL->(dbsetorder(1))//RAL_FILIAL+RAL_FUNCAO+RAL_CURSO

aSort(aCodAtend)
For nInd:=1 To Len(aCodAtend)

	If !( AA1->(dbseek(cFilAA1+aCodAtend[nInd])) .And. SRA->(dbseek(AA1->(AA1_FUNFIL+AA1_CDFUNC))) .And. RAL->(dbSeek(cFilRAL+SRA->RA_CODFUNC)) )
		Loop
	EndIf

	//Verificar primeiro sem data de validade ( sem prazo de vencimento )
	BeginSql Alias cAliasQry
		Select Count(1) Contar
		  From %Table:RA4% RA4,%Table:RAL% RAL
		 Where RA4_FILIAL=%xFilial:RA4%
		   And RA4.RA4_MAT=%Exp:SRA->RA_MAT%
		   And RA4.RA4_VALIDA=' '
		   And RA4.%notdel%
		   And RAL.RAL_FILIAL=%xFilial:RAL%
		   And RAL.RAL_CURSO=RA4.RA4_CURSO
		   And RAL.RAL_FUNCAO=%Exp:SRA->RA_CODFUNC%
		   And RAL.%notdel%
	EndSql

	lTemCursoVenc:=( (cAliasQry)->Contar== 0)

	(cAliasQry)->(dbCloseArea())
	If lTemCursoVenc

		//Verificar se há curso vencido
		BeginSql Alias cAliasQry
			COLUMN RA4_VALIDA AS DATE
			Select Max(RA4_VALIDA) RA4_VALIDA
			  From %Table:RA4% RA4,%Table:RAL% RAL
			 Where RA4_FILIAL=%xFilial:RA4%
			   And RA4.RA4_MAT=%Exp:SRA->RA_MAT%
			   And RA4.RA4_VALIDA<>' '
			   And RA4.%notdel%
			   And RAL.RAL_FILIAL=%xFilial:RAL%
			   And RAL.RAL_CURSO=RA4.RA4_CURSO
			   And RAL.RAL_FUNCAO=%Exp:SRA->RA_CODFUNC%
			   And RAL.%notdel%
		EndSql

		If Empty((cAliasQry)->RA4_VALIDA)
			lTemCursoVenc:=.F.
		Else
			lTemCursoVenc:=(cAliasQry)->RA4_VALIDA<dDataBase
		EndIf
		(cAliasQry)->(dbCloseArea())

	EndIf

	If lTemCursoVenc
		Aadd(aAtendente,AA1->AA1_CODTEC)
		cMensagem+="#"+AllTrim(Str(Len(aAtendente)))+"[Atendente]#"+cSeparador
	EndIf

Next nInd

If Len(aAtendente)>0
	lRetorno:=.F.
	If lShowPerg
		cMensagem:=Left(cMensagem,Len(cMensagem)-Len(cSeparador))+Space(1)+STR0026+CRLF+STR0027 //com curso vencido+CRLF+Deseja continuar?
		lRetorno:=MsgYesNo(I18N( cMensagem,aAtendente, 1, 0 ),STR0028 )	//Atenção
	EndIf
EndIf

RestArea(aAreaAA1)
RestArea(aAreaSRA)
RestArea(aAreaRAL)
RestArea(aAreaAtu)
Return lRetorno

/*/{Protheus.doc} at020ValUs
	Validação para usuario, o usuario pode estar atrelado apenas a um atendente
	
	@author 	Joni Lima do Carmo
	@since 		05/08/2016
	@version 	12
	@param		Nil
	@return 	lRet Boolean

/*/
Function at020ValUs()

	Local aArea	:= GetArea()
	Local aAreaAA1	:= AA1->(GetArea())
	
	Local lRet := .T.
	
	Local oModel := FWModelActive()
	Local oMdlAA1 := nil
	
	If ValType(oModel)=='O' .and. oModel:IsActive() .and. oModel:GeTID() == 'TECA020' 
	
		oMdlAA1 := oModel:GetModel('AA1MASTER')
		
		cCodUser := oMdlAA1:GetValue('AA1_CODUSR')
		
		dbSelectArea('AA1')
		AA1->(dbSetOrder(4))
		
		If AA1->(dbSeek(xFilial('AA1') + PadR(cCodUser,TamSx3("AA1_CODUSR")[1])))
			lRet := .F.
			Help(" ",1,"HELP", , STR0030 + Alltrim(AA1->AA1_CODTEC), 3, 1 ) //"Este usuario ja esta matriculado como atendente pelo codigo nro: "
		EndIf
		
	EndIf
	
	RestArea(aAreaAA1)
	RestArea(aArea)
	
Return lRet
//-------------------------------------------------------------------------------------
/*/{Protheus.doc} At020Mark

Faz gravação no campo OK com a marcação.

@return	lRet
@author	rebeca.asuncao
@since 		12/12/2017
@version	P12.1.17 
/*/
//-------------------------------------------------------------------------------------
Function At020Mark(oMrkBrowse,cRetTab,lAuto, cMat)
Local aArea		:= GetArea()
Local cMarca	:= ""
 
Default lAuto	:= .F.
Default cMat	:= ''

DbSelectArea(cRetTab)

If !lAuto
	cMarca := oMrkBrowse:Mark()
	While !(cRetTab)->(Eof()) 
		RecLock(cRetTab, .F.)
			
		If (cRetTab)->OK <> cMarca
			(cRetTab)->OK := ' '
		Else
			(cRetTab)->OK := cMarca
		EndIf
		MsUnlock()
		(cRetTab)->(DbSkip())
	End
Else
	cMarca := "C" 
	(cRetTab)->(DbGoTop())
	While !(cRetTab)->(Eof()) 
		RecLock(cRetTab, .F.)
		If (cRetTab)->RA_MAT == cMat
			(cRetTab)->OK := cMarca
		Else
			(cRetTab)->OK := ' '
		EndIf
		MsUnlock()
		(cRetTab)->(DbSkip())
	End
EndIf

RestArea(aArea)

If !lAuto
	oMrkBrowse:Refresh()
EndIf

Return 

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020GFun
Gera atendentes com os funcionários que não possuem relacionamento com atendentes e que foram marcados no Browse.

@return	lRet
@author	rebeca.asuncao
@since 		12/12/2017
@version	P12.1.17 
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At020GFun(cRetTab,oMrkBrowse,oGSTmpBk)

Local aMarkFun		:= {}
Local lAuto			:= IsBlind()
Local lRet			:= .T.
Local aAreaSRA		:= SRA->(GetArea())

Default cRetTab := ""
Default oMrkBrowse := Nil
Default oGSTmpBk := Nil

If lAuto
	(cRetTab)->(DbGoTop())
	While !(cRetTab)->(EOF())
			If (cRetTab)->OK == "C"
				aadd(aMarkFun, {Posicione("SRA", 1, (cRetTab)->RA_FILIAL + (cRetTab)->RA_MAT, "RA_NOME"),;
								(cRetTab)->RA_CODFUN,;
								(cRetTab)->RA_MAT,;
								(cRetTab)->RA_FILIAL,;
								(cRetTab)->RA_CC,;
								(cRetTab)->RA_TNOTRAB }) 							
			EndIf
		(cRetTab)->( DbSkip() )
	EndDo
Else
	(cRetTab)->(DbGoTop())
	
	While (cRetTab)->(!EOF())
		If oMrkBrowse:IsMark()
			aadd(aMarkFun, {(cRetTab)->RA_NOME,;
							(cRetTab)->RA_CODFUN,;
							(cRetTab)->RA_MAT,;
							(cRetTab)->RA_FILIAL,;
							(cRetTab)->RA_CC,;
							(cRetTab)->RA_TNOTRAB})
		EndIf	
		(cRetTab)->( DbSkip() )
	EndDo
EndIf

If !lAuto
	MsgRun( STR0032,STR0028,{|| lRet := AutoInclude(aMarkFun,cRetTab,oMrkBrowse,oGSTmpBk) })// 'Atenção' ### "Aguarde! Gerando Atendentes ..."]
	If FindFunction("TECTelMets")
		TECTelMets( "import_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
	EndIF
Else
	lRet := AutoInclude(aMarkFun,cRetTab,oMrkBrowse,oGSTmpBk)
EndIf

RestArea(aAreaSRA)

Return lRet


//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020Atua
Atualiza a tabela temporária e limpa o array aMarkFun

@return	lRet
@author	rebeca.asuncao
@since 		12/12/2017
@version	P12.1.17 
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020Atua(cRetTab,aMarkFun,oMrkBrowse,oGSTmpBk)
Local nX	:= 1
Local lRet	:= .T.
Local aRet := {}

Default cRetTab := ""
Default aMarkFun := {}
Default oMrkBrowse := Nil 
Default oGSTmpBk := Nil

If Len(aMarkFun) > 0
	For nX:= 1 To Len(aMarkFun)
	 	aAdd(aRet, {'RA_MAT', aMarkFun[nX][3]} ) 
		If oGSTmpBk:Seek(aRet)
			If oGSTmpBk:Delete()
				oGSTmpBk:Commit()
			EndIf 
			aRet:= {}
		EndIf
	Next 							
	aMarkFun := {} //limpa o array	
Else 
	lRet := .F.
EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AutoInclude
Função utilizada na inclusão automática de atendentes

@return	lRet, bool, retorna .T. se não ocorrer erros
@author	Mateus Boiani
@since 		27/12/2018
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function AutoInclude(aMarkFun,cRetTab,oMrkBrowse,oGSTmpBk)
Local oModel
Local aRotAuto := {}
Local nX
Local nY
Local nCont
Local lOK := .T.
Local aErrors := {}
Local oModelAA1
Local cMsg := ""
Local aErroMVC := {}
Local lRet	:= .T.
Local aCountErr := {}
Local nAux := 0 
Local nSuccess := 0
Local nFail := 0
Local cGSxInt		:= SuperGetMV("MV_GSXINT",,"2")
Local aCpoLoad		:= At020LdVis(cGSxInt)

If Len(aMarkFun) > 0
	oModel := FwLoadModel("TECA020")
	For nCont := 1 To Len(aMarkFun)
		lOK := .T.
		
		aAdd(aRotAuto,{"AA1_NOMTEC"	,aMarkFun[nCont][1]	,Nil})
		aAdd(aRotAuto,{"AA1_FUNCAO"	,aMarkFun[nCont][2]	,Nil})
		aAdd(aRotAuto,{"AA1_FUNFIL"	,aMarkFun[nCont][4]	,Nil})
		If (FWModeAccess("AA1",3) == FWModeAccess("CTT",3) .AND. FWModeAccess("AA1",2) == FWModeAccess("CTT",2) .AND. FWModeAccess("AA1",1) == FWModeAccess("CTT",1));
				.OR. (xFilial("CTT") == aMarkFun[nCont][4]) .OR.;
				(FWModeAccess("AA1",3) == "E" .AND. FWModeAccess("AA1",2) == "E" .AND. FWModeAccess("AA1",1) == "E")
			aAdd(aRotAuto,{"AA1_CC"		,aMarkFun[nCont][5]	,Nil})
		EndIf
		aAdd(aRotAuto,{"AA1_CDFUNC"	,aMarkFun[nCont][3]	,Nil})
		aAdd(aRotAuto,{"AA1_TURNO"	,aMarkFun[nCont][6]	,Nil})

		oModel:SetOperation( MODEL_OPERATION_INSERT )
		oModel:Activate()
		oModelAA1 := oModel:GetModel("AA1MASTER")

		For nX := 1 to LEN(aRotAuto)
			If !EMPTY(aRotAuto[nX][2])
				If aScan(aCpoLoad, {|c| c == aRotAuto[nX][1]}) = 0
					If !(oModelAA1:SetValue(aRotAuto[nX][1],aRotAuto[nX][2]))
						lOK := .F.
						Exit
					EndIf
				ElseIf !(oModelAA1:LoadValue(aRotAuto[nX][1],aRotAuto[nX][2]))
						lOK := .F.
						Exit
				EndIf
			EndIf
		Next
		
		Begin Transaction
		
			If !lOK .OR. !oModel:VldData() .OR. !oModel:CommitData()
				nFail++
				aErroMVC := oModel:GetErrorMessage()
				AADD(aErrors, {"AA1_NOMTEC: " 	+ aMarkFun[nCont][1],;
								 "AA1_FUNCAO: " 	+ aMarkFun[nCont][2],;
								 "AA1_CDFUNC: " 	+ aMarkFun[nCont][3],;
								 "AA1_FUNFIL: " 	+ aMarkFun[nCont][4],;
								 "AA1_CC: " 		+ aMarkFun[nCont][5],;
								 "AA1_TURNO: " 	+ aMarkFun[nCont][6],;
								 STR0041 + ' [' + AllToChar( aErroMVC[1] ) + ']',; // "Id do formulário de origem:"
								 STR0042 + ' [' + AllToChar( aErroMVC[2] ) + ']',; //"Id do campo de origem:"
								 STR0043 + ' [' + AllToChar( aErroMVC[3] ) + ']',; //"Id do formulário de erro:"
								 STR0044 + ' [' + AllToChar( aErroMVC[4] ) + ']',; //"Id do campo de erro:"
								 STR0045 + ' [' + AllToChar( aErroMVC[5] ) + ']',; //"Id do erro:"
								 STR0046 + ' [' + AllToChar( aErroMVC[6] ) + ']',; //"Mensagem do erro:"
								 STR0047 + ' [' + AllToChar( aErroMVC[7] ) + ']',; //"Mensagem da solução:"
								 STR0048 + ' [' + AllToChar( aErroMVC[8] ) + ']',; //"Valor atribuído:"
								 STR0049 + ' [' + AllToChar( aErroMVC[9] ) + ']'; //"Valor anterior:"
								 })
				lOK := .F.
				DisarmTransacation()
				oModel:DeActivate()
			EndIf
		End Transaction
		//limpa o array
		For nX := 1 to LEN(aRotAuto)
			ASIZE(aRotAuto[nX],0)
		Next
		aRotAuto := {}
		
		If lOK
			nSuccess++
			At020Atua(cRetTab,{aMarkFun[nCont]},oMrkBrowse,oGSTmpBk) //Atualiza tabela temporária
		EndIf
	Next nCont
EndIf

If !EMPTY(aErrors)
	cMsg += STR0068 + " " + cValToChar(( nCont - 1 )) + CRLF //"Total de funcionários processados:" 
	cMsg += STR0069 + " " + cValToChar(nSuccess) + CRLF //"Atendentes inseridos:" 
	cMsg += STR0070 + " " + cValToChar(nFail) + CRLF + CRLF //"Atendentes não inseridos (falhas):"
	cMsg += STR0038 + CRLF + CRLF //"Os atendentes abaixo não foram inseridos: "
	If LEN(aErrors) <= 50 //Não há necessidade de exibir um log kilométrico. Caso maior que 50, a informação será agrupada.
		For nX := 1 To LEN(aErrors)
			For nY := 1 To LEN(aErrors[nX])
				cMsg += aErrors[nX][nY] + CRLF
			Next
			cMsg += CRLF + REPLICATE("-",30) + CRLF
		Next
	Else
		For nX := 1 To LEN(aErrors)
			If (nAux := ASCAN(aCountErr, {|z| z[1] == aErrors[nX][12]})) == 0
				AADD(aCountErr, {aErrors[nX][12], 1, aErrors[nX]})
			Else
				aCountErr[nAux][2]++
			EndIf
		Next
		
		For nX := 1 To LEN(aCountErr)
			cMsg += STR0071 + " " + cValToChar(aCountErr[nX][2]) + " " + STR0072 + " " + CRLF + CRLF //"A ocorrência abaixo repetiu-se em" ## "funcionários:"
			For nY := 1 TO LEN(aCountErr[nX][3])
				cMsg += aCountErr[nX][3][nY] + CRLF
			Next
			cMsg += CRLF + REPLICATE("-",30) + CRLF
		Next
	EndIf
	cMsg += CRLF + STR0039 //"Verifique se os dados inseridos são válidos e se é possível inserir atendentes com os valores listados acima."
	If !ISBlind()
		AtShowLog(cMsg,STR0040,/*lVScroll*/,/*lHScroll*/,/*lWrdWrap*/,.F.) //"Importação de Atendentes"
	EndIf
	lRet := .F.
EndIf

If !ISBlind()
	oMrkBrowse:Refresh(.T.)
EndIf

If Len(aMarkFun) <> 0
	lRet := .F.
EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020LdVis
Retorna quais os campos estão com a propriedade Visualizar
@param cGSxInt - parametro MV_GSXINT
@return	aRet, Array, campos com propriedade de visualizar
@author	fabiana.silva
@since 		08/08/2019
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020LdVis(cGSxInt)
Local aRet := {}
If cGSxInt <> "2"
	aRet :=  {"AA1_EMAIL", "AA1_NOMTEC", "AA1_FONE", "AA1_CDFUNC"}
EndIf
Return aRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020NoCols
Retorna quais os campos não estarão na tela de seleção de funcionários para importação
@param cGSxInt - parametro MV_GSXINT
@return	aRet, Array, campos não visualizados
@author	fabiana.silva
@since 		08/08/2019
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020NoCols(cGSxInt)
Local aRet := {}
If cGSxInt <> "2"
	aRet := {"RA_CARGO", "RA_DCARGO", "RA_CODFUN", "RA_CC", "RA_TNOTRAB"}
EndIf
Return aRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020ItG
Retorna quais os funcionários disponíveis para importação
@param cMarca - Marca do Produto (ex RM)
@param cMsg - Retorno da mensagem de Erro
@param cFiltro - Expressão de Filtro do serviço
@return	aRet, Array, contendo os funcionários a serem inseridos
@author	fabiana.silva
@since 		08/08/2019
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020ItG(cMarca, cMsgErro, cFiltro)
Local oXML := NIL
Local oWS := {}
Local cXML := ""
Local lRet := .T.
Local cError := ""
Local aFunc := {}
Local nC := 0
Local nTamCDFunc := TamSX3("AA1_CDFUNC")[1]
Local aFuncImp	:= {}
Local cMatr := ""
Local nTamNome := TamSX3("AA1_NOMTEC")[1]
Local cWarning := ""
Local nTamDFun := TamSX3("RA_DESCFUN")[1]
Local cCodFilRM := ""
Local cCodEmpRM := ""
Local oNodeFunc := NIL

Default cMarca := IIF(SuperGetMV("MV_GSXINT",,"2") == "3", "RM", "")

If cMarca == "RM"
	oWS :=  GSItRMWS(cMarca, .F., @cMsgErro, @cCodFilRM, @cCodEmpRM)
	
	If oWS <> NIL
		oWS:cFiltro := "PFUNC.CODFILIAL = '" + AllTrim(cCodFilRM) +"'" + " AND PFUNC.CODCOLIGADA = '" + AllTrim(cCodEmpRM) +"'" + IIF(!Empty(cFiltro), " AND "+cFiltro, "")
		oWS:cDataServerName := "FopFuncData"
		If oWS:ReadView()
			cXML:= oWS:cReadViewResult
			
			If !Empty(oWS:cReadViewResult)
				oXML := XmlParser(cXML, "_", @cError, @cWarning)
				
				If Empty(cError)
					oNodeFunc := XmlChildEx ( oXML:_NEWDATASET, "_PFUNC")
					If Valtype(oNodeFunc) == "O" 
						aAdd(aFunc, oNodeFunc)
					ElseIf Valtype(oNodeFunc) == "A"
						aFunc := aClone(oNodeFunc)
					EndIf
				EndIf
				If !Empty(cError)
					cMsgErro := STR0077 + cError//"Problemas no xml de retorno mensagem ReadView "
					lRet := .F.
				EndIf
				
				AA1->(DbSetOrder(07)) //AA1_FILILA+AA1_CDFUNC+AA1_FUNFIL
				Do While lRet .AND. ( (nC := nC + 1) <= Len(aFunc))
					//Verifica se o funcionário deve ser adicionado
					If !Empty(aFunc[nC]:_CHAPA:TEXT) .and. !Empty(aFunc[nC]:_NOME) 
					 	cMatr := PadR(aFunc[nC]:_CHAPA:Text, nTamCDFunc)
						If !AA1->(DbSeek(xFilial("AA1")+ cMatr+cFilAnt))
						
							aAdd( aFuncImp , {cFilAnt,;
												cMatr,;
												PadR(aFunc[nC]:_NOME:TEXT,nTamNome),;
												"",;
												"",;
												"",;
												"",;
												"",;
												PadR(aFunc[nC]:_NOME_FUNCAO:TEXT, nTamDFun)})
						EndIf
					Endif
				EndDo

				If Valtype(oNodeFunc) == "O" 
					FreeObj(oNodeFunc) 
				ElseIf Valtype(oNodeFunc) == "A"
					aEval(oNodeFunc, { |o| FreeObj(o)})
					oNodeFunc := NIL
				EndIf				
				FreeObj(oXML)

			Else
				cMsgErro := STR0078 + "ReadView"//"XML em branco"
				Help(,, "GSItEmpFil",, ,1, 0)
			EndIf

		Else
			cMsgErro := STR0079 + "ReadView"//"Problemas ao consumir o método:"
		EndIf
	EndIf
	
	If oWS <> NIL
		FreeObj(oWS)		
		oWS := NIL
	EndIf
	
	oWS := NIL

EndIf

Return aFuncImp

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020FilRM
Retorna a expressão do filtro do Serviço RM
@param lConsole - Execução no Console (job)
@return	cFiltro, String, campos não visualizados
@author	fabiana.silva
@since 		08/08/2019
/*/
//--------------------------------------------------------------------------------------------------------------------

Static Function At020FilRM(lConsole)
Local cFiltro := ""

If TecHasPerg("MV_PAR01", "TECA020RM")
	If Pergunte("TECA020RM", !lConsole)
		If !Empty(MV_PAR01) .AND. !Empty(MV_PAR02)
			cFiltro := "(PFUNC.NOME BETWEEN '" + AllTrim(MV_PAR01)  + "' AND '" + AllTrim(MV_PAR02) + "')"
		ElseIf !Empty(MV_PAR01)	
			cFiltro := "(PFUNC.NOME >=  '" + AllTrim(MV_PAR01) + "')"
		EndIf
		
		If !Empty(MV_PAR03) .AND. !Empty(MV_PAR04)
			cFiltro += IIF(!Empty(cFiltro), " AND ", "") +  "(PFUNC.DATAADMISSAO BETWEEN '" + Left(FWTimeStamp(3, MV_PAR03, "00:00:00"), 10)  + "' AND '" + Left(FWTimeStamp(3, MV_PAR04, "00:00:00"), 10) + "')"
		ElseIf !Empty(MV_PAR03)	
			cFiltro += IIF(!Empty(cFiltro), " AND ", "") + "(PFUNC.DATAADMISSAO  >=  '" + Left(FWTimeStamp(3, MV_PAR03, "00:00:00"), 10)+ "')"
		EndIf
		
		If MV_PAR05 = 1 //Todos os funcionários admitidos/Ativos na data base atual
			cFiltro += IIF(!Empty(cFiltro), " AND ", "") + "(PFUNC.DATADEMISSAO IS NULL)"
		Else //Somente os desligados na data base atual
			cFiltro += IIF(!Empty(cFiltro), " AND ", "") + "(PFUNC.DATADEMISSAO IS NOT NULL)"
		EndIf
	Else
		cFiltro := "1=2"
	EndIf
EndIf
Return cFiltro

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020Convo
Verifica se o atendente possui vinculo com RH-Protheus e se é do tipo de contrato intermitente.
@author	Augusto Albuquerque
@since 		05/09/2019
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At020Convo()
Local cMat		:= ""

If AA1->AA1_ALOCA == "1"
	cMat := AA1->AA1_CDFUNC
	If !Empty(cMat)
		DbSelectArea("SRA")
		SRA->(DbSetOrder(1))
		If SRA->(DbSeek(xFilial("SRA")+cMat))
			If SRA->RA_TPCONTR == "3"
				FWExecView(OemToAnsi(STR0084), "GPEA018", MODEL_OPERATION_UPDATE ,,{||.T.}) // "Convocacoes"
				If FindFunction("TECTelMets")
					TECTelMets( "convoc_atend", "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total")
				EndIF
			Else
				Help(" ",1,"AT020CONVO", , STR0085, 3, 1 ) // "Opção disponível apenas para funcionários com contrato intermitente(legenda azul)."
			EndIf
		Else
			Help(" ",1,"AT020CONVO", , STR0086, 3, 1 ) // "Atendente não encontrado no RH(SRA)"
		EndIf
	Else
		Help(" ",1,"AT020CONVO", , STR0087, 3, 1 ) // "Atendente não possui vinculo com o RH(Codigo de Matricula)"
	EndIf
Else
	Help(" ",1,"AT020CONVO", , STR0088, 3, 1 ) // "O Funcionario não esta disponivel para alocação"
EndIf
Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT020Legen
Retorna o numero correspondente para legenda do atendente
@return	nRet - Numero da legenda 1-Indiponivel, 2-Intermintente e 3-Disponivel
@author	Augusto Albuquerque
@since 		05/09/2019
/*/
//--------------------------------------------------------------------------------------------------------------------
Function AT020Legen()
Local nRet		:= 3 // Disponivel
Local aAreaAA1	:= AA1->(GetArea())
Local aAreaSRA	:= SRA->(GetArea())

If AA1->AA1_ALOCA == "1"
	If !Empty(AA1->AA1_CDFUNC)	
		DbSelectArea("SRA")
		SRA->(DbSetOrder(1))
		If SRA->(DbSeek(AA1->AA1_FUNFIL+AA1->AA1_CDFUNC)) .And. SRA->RA_TPCONTR == "3"
			nRet	:= 2 // Intermitente
		Endif
	Endif
Else
	nRet := 1 // Indisponivel
EndIf

RestArea(aAreaSRA)
RestArea(aAreaAA1)

Return (nRet)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AT020RAAte
Monta a tela com a data inicial e final para o relatorio
@author	Augusto Albuquerque
@since 		17/03/2020
/*/
//--------------------------------------------------------------------------------------------------------------------
Function AT020RAAte()
Local aFilPesq := {}
Local aSM0 		:= FWArrFilAtu()
Local cCompE	:= FWModeAccess("ABB",1)
Local cCompU	:= FWModeAccess("ABB",2)
Local cCompF	:= FWModeAccess("ABB",3)
Local cFilial	:= "IN ("
Local dGetDtDe := dDataBase
Local dGetDtAte := dDataBase
Local nX
Local oDlgSelect
Local oDataDe
Local oDataAte
Local oRefresh
Local oExit

If TecHasPerg("MV_PAR07","TECR050")
	If cCompU == 'E'
		aFilPesq := FWAllFilial(aSM0[SM0_EMPRESA],aSM0[SM0_UNIDNEG])
	ElseIf cCompE == 'E'
		aFilPesq := FWAllUnitBusiness(aSM0[SM0_EMPRESA])
	EndIf

	For nX := 1 To Len(aFilPesq)
		If nX > 1
			cFilial += ","
		EndIf

		If cCompF == 'E'
			cFilial +=  "'" + aSM0[SM0_EMPRESA]+aSM0[SM0_UNIDNEG]+aFilPesq[nX] + "'"
		ElseIf cCompU == 'E'
			cFilial +=  "'" + aSM0[SM0_EMPRESA]+aSM0[SM0_UNIDNEG]+Space(Len(aFilPesq[nX])) + "'"
		ElseIf cCompE == 'E'
			cFilial +=  "'" + aSM0[SM0_EMPRESA]+Space(Len(aSM0[SM0_UNIDNEG]))+Space(Len(aSM0[SM0_FILIAL])) + "'"
		EndIf
	Next nX

	cFilial := cFilial + ")"
EndIf

DEFINE MSDIALOG oDlgSelect FROM 0,0 TO 100,300 PIXEL TITLE STR0089 // Atendentes relacionados
	@ 5, 9 SAY STR0090 SIZE 50, 19 PIXEL // Data de Início
	@ 5, 80 SAY STR0091 SIZE 50, 19 PIXEL // Data de Término
	
	oDataDe := TGet():New( 015, 009, { | u | If( PCount() == 0, dGetDtDe, dGetDtDe := u ) },oDlgSelect, ;
						060, 010, "@D",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"dGetDtDe",,,,.T.)

	oDataAte := TGet():New( 015, 080, { | u | If( PCount() == 0, dGetDtAte, dGetDtAte := u ) },oDlgSelect, ;
						060, 010, "@D",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"dGetDtAte",,,,.T.)

	oExit := TButton():New( 035, 080, STR0092,oDlgSelect,{|| oDlgSelect:End() }, 30,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // Sair

	oRefresh := TButton():New( 035, 010, STR0093,oDlgSelect,{|| Tecr050( dGetDtDe, dGetDtAte, AA1->AA1_CODTEC, cFilial ), oDlgSelect:End()}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. ) // Buscar

ACTIVATE MSDIALOG oDlgSelect CENTER

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020IncAA1
Realiza a inclusão do novo atendente em uma nova filial
@author	Luiz Gabriel
@since 		20/12/2021
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020IncAA1(aNewAA1)  
Local oModelAA1	:= Nil 
Local nX 		:= 0
Local lOk 		:= .T.
Local oGsLog	:= GsLog():New()
Local cNomTec	:= AA1->AA1_NOMTEC

If VALTYPE(oMdlTrans) <> 'O'
	oMdlTrans := FwLoadModel("TECA020")
EndIf

oMdlTrans:SetOperation( MODEL_OPERATION_INSERT )

If oMdlTrans:Activate()
	oModelAA1 := oMdlTrans:GetModel("AA1MASTER")

	For nX := 1 To Len(aNewAA1)
		If  !(oModelAA1:LoadValue(aNewAA1[nX][1],aNewAA1[nX][2]))
			lOK := .F.
			Exit
		EndIf
	Next nX

	If !lOK .OR. !oMdlTrans:VldData() .OR. !oMdlTrans:CommitData()
		aErroMVC := oMdlTrans:GetErrorMessage()
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0095 ) //"parametros recebidos"
		oGsLog:addLog("TransfFunc "+ cNomTec, "------------------") 
		For nX := 1 To Len(aNewAA1)
			oGsLog:addLog("TransfFunc "+ cNomTec, aNewAA1[nX][1] + " - " + AllToChar(aNewAA1[nX][2]) ) 
		Next nX 
		oGsLog:addLog("TransfFunc "+ cNomTec, "------------------") 
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0041 + ' [' + AllToChar( aErroMVC[1] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0042 + ' [' + AllToChar( aErroMVC[2] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0043 + ' [' + AllToChar( aErroMVC[3] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0044 + ' [' + AllToChar( aErroMVC[4] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0045 + ' [' + AllToChar( aErroMVC[5] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0046 + ' [' + AllToChar( aErroMVC[6] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0047 + ' [' + AllToChar( aErroMVC[7] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0048 + ' [' + AllToChar( aErroMVC[8] ) + ']')
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0049 + ' [' + AllToChar( aErroMVC[9] ) + ']') 
		oGsLog:addLog("TransfFunc "+ cNomTec, "------------------") 
		oGsLog:printLog("TransfFunc "+ cNomTec) 
		lOK := .F.
		oMdlTrans:DeActivate()
	Else 
		oMdlTrans:DeActivate()
	EndIf
EndIf

Return lOk

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020ExcEft
Realiza a exclusão das agendas de efetivo do atendente
@author	Luiz Gabriel
@since 		20/12/2021
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At020ExcEft(cCodAtend, dPonIni, dPomFim)
Local aManut	:= {}
Local aDados	:= {}
Local aTGY		:= {}
Local cAliQry 	:= GetNextAlias()
Local cSelect	:= "%%" 
Local cCodTFF	:= ""
Local cHorsTFF	:= ""
Local cWhereABB	:= "%%" 
Local cWhereABQ	:= "%%"
Local cWhereTCU	:= "%%"
Local lHorFlex	:= TecXHasEdH()
Local lPrHora	:= TecABBPRHR()
Local nPosTGY	:= 0	

cSelect := "%"

If lHorFlex
	cSelect += ", TGY.TGY_ENTRA1"
	cSelect += ", TGY.TGY_SAIDA1"
	cSelect += ", TGY.TGY_ENTRA2"
	cSelect += ", TGY.TGY_SAIDA2"
	cSelect += ", TGY.TGY_ENTRA3"
	cSelect += ", TGY.TGY_SAIDA3"
	cSelect += ", TGY.TGY_ENTRA4"
	cSelect += ", TGY.TGY_SAIDA4"
EndIf

cSelect += "%"

If ABB->(ColumnPos("ABB_CODTW1") > 0)
	cWhereABB := "% AND ABB.ABB_CODTW1 = '' %"
EndIf

cWhereABQ := "% AND " + FWJoinFilial("ABQ" , "ABB" , "ABQ", "ABB", .T.) + " %"

cWhereTCU := "% AND " + FWJoinFilial("ABB" , "TCU" , "ABB", "TCU", .T.) + " %"

BeginSql Alias cAliQry
	 Column ABB_DTINI as Date
	 Column ABB_DTFIM as Date
	 Column ABB_DTREF as Date

	SELECT TDV.TDV_DTREF AS ABB_DTREF,
		TDV.TDV_SEQTRN,
		TDV.TDV_TPDIA,
		ABB.ABB_CODIGO,
		ABB.ABB_DTINI,
		ABB.ABB_DTFIM,
		ABB.ABB_CODTEC,
		ABB.ABB_HRINI,
		ABB.ABB_HRFIM,
		ABB.ABB_ATENDE,
		ABB.ABB_CHEGOU,
		ABB.ABB_MANUT,
		ABB.ABB_IDCFAL,
		ABB.ABB_FILIAL,
		ABB.R_E_C_N_O_ AS ABB_RECABB,
		TGY.TGY_FILIAL,
		TGY.TGY_ESCALA,
		TGY.TGY_CODTDX,
		TGY.TGY_DTINI,
		TGY.TGY_DTFIM,
		TGY.TGY_GRUPO,
		TGY.TGY_CODTFF,
		TGY.TGY_ULTALO,
		TGY.TGY_TIPALO,
		ABQ.ABQ_FILTFF,
		ABQ.ABQ_CODTFF,
		TCU.TCU_ALOCEF
		%Exp:cSelect%
	FROM %Table:TDV% TDV
		INNER JOIN %Table:ABB% ABB ON ABB.ABB_CODIGO = TDV.TDV_CODABB
			AND TDV.TDV_FILIAL = ABB.ABB_FILIAL
			AND ABB.ABB_CODTEC = %Exp:cCodAtend%
			AND ABB.ABB_CHEGOU = 'N'
			AND ABB.ABB_ATENDE = '2'
			%Exp:cWhereABB%
			AND ABB.%NotDel%
		INNER JOIN %Table:ABQ% ABQ ON ABQ.ABQ_CONTRT||ABQ.ABQ_ITEM||ABQ.ABQ_ORIGEM = ABB.ABB_IDCFAL
			%Exp:cWhereABQ%
			AND ABQ.%NotDel%
		LEFT JOIN %Table:TGY% TGY ON TGY.TGY_ATEND = ABB.ABB_CODTEC
			AND TGY.TGY_CODTFF = ABQ.ABQ_CODTFF
			AND TGY.TGY_FILIAL = ABQ.ABQ_FILTFF
			AND TGY.%NotDel%
		INNER JOIN %Table:TCU% TCU ON TCU.TCU_COD = ABB.ABB_TIPOMV
			%Exp:cWhereTCU%
			AND TCU.%NotDel%
	WHERE 
		TDV.TDV_DTREF >= %exp:dPonIni%
		AND TDV.%NotDel%
	ORDER BY TDV.TDV_DTREF,
			ABB.ABB_DTINI,
			ABB.ABB_HRINI,
			ABB.ABB_DTFIM

EndSql

Do While ! (cAliQry)->(Eof())

	If cCodTFF <> (cAliQry)->ABQ_CODTFF
		cCodTFF := (cAliQry)->ABQ_CODTFF
		If lPrHora
			cHorsTFF := Posicione("TFF", 1, (cAliQry)->ABQ_FILTFF+(cAliQry)->ABQ_CODTFF, "TFF_QTDHRS")
		EndIf
	EndIf

	If (cAliQry)->ABB_MANUT == '1' .OR. (cAliQry)->TCU_ALOCEF == '2' .OR. (lPrHora .AND. !Empty(cHorsTFF) )
		AADD(aManut, {(cAliQry)->ABB_CODIGO,;
					(cAliQry)->ABB_FILIAL,;
					(cAliQry)->ABB_CODTEC,;
					(cAliQry)->ABB_IDCFAL,;
					(cAliQry)->ABB_DTINI,;
					(cAliQry)->ABB_HRINI,;
					(cAliQry)->ABB_DTFIM,;
					(cAliQry)->ABB_HRFIM,;
					(cAliQry)->ABB_ATENDE,;
					(cAliQry)->ABB_CHEGOU,;
					(cAliQry)->ABB_DTREF,;
					(cAliQry)->ABB_RECABB,;
					(cAliQry)->TDV_SEQTRN,;
					(cAliQry)->TCU_ALOCEF == '2',;
					(cAliQry)->ABB_MANUT == '1',;
					(cAliQry)->TDV_TPDIA == 'S',;
					IIF(lPrHora, !Empty(cHorsTFF), .F.);
				})
	EndIf
	AADD(aDados, {(cAliQry)->ABB_CODIGO,;
				(cAliQry)->ABB_FILIAL,;
				(cAliQry)->ABB_CODTEC,;
				(cAliQry)->ABB_IDCFAL,;
				(cAliQry)->ABB_DTINI,;
				(cAliQry)->ABB_HRINI,;
				(cAliQry)->ABB_DTFIM,;
				(cAliQry)->ABB_HRFIM,;
				(cAliQry)->ABB_ATENDE,;
				(cAliQry)->ABB_CHEGOU,;
				(cAliQry)->ABB_DTREF,;
				(cAliQry)->ABB_RECABB,;
				(cAliQry)->TDV_SEQTRN;
			})
	If !lPrHora .OR. (lPrHora .AND. Empty(cHorsTFF))
		If Empty(aTGY)
			AADD(aTGY, {(cAliQry)->TGY_FILIAL,;
					(cAliQry)->TGY_ESCALA,;
					(cAliQry)->TGY_CODTDX,;
					(cAliQry)->TGY_GRUPO,;
					(cAliQry)->TGY_DTINI,;
					(cAliQry)->TGY_DTFIM,;
					(cAliQry)->TGY_ULTALO,;
					(cAliQry)->TGY_CODTFF,;
					(cAliQry)->TGY_TIPALO,;
					(cAliQry)->ABQ_FILTFF,;
					{};
				})

			If lHorFlex
				aTGY[Len(aTGY)][11] := { {(cAliQry)->TGY_ENTRA1, (cAliQry)->TGY_SAIDA1},;
										{(cAliQry)->TGY_ENTRA2, (cAliQry)->TGY_SAIDA2},;
										{(cAliQry)->TGY_ENTRA3, (cAliQry)->TGY_SAIDA3},;
										{(cAliQry)->TGY_ENTRA4, (cAliQry)->TGY_SAIDA4} }
			EndIf
		Else
			nPosTGY := ASCAN(aTGY, {|z| z[1] == (cAliQry)->TGY_FILIAL .and.;
									z[2] == (cAliQry)->TGY_ESCALA .and.;
									z[3] == (cAliQry)->TGY_CODTDX .and.;
									z[4] == (cAliQry)->TGY_GRUPO .and.;
									z[5] == (cAliQry)->TGY_DTINI .and.;
									z[6] == (cAliQry)->TGY_DTFIM .and.;
									z[7] == (cAliQry)->TGY_ULTALO .and.;
									z[8] == (cAliQry)->TGY_CODTFF .AND.;
									z[9] == (cAliQry)->TGY_TIPALO .AND.;
									z[10] == (cAliQry)->ABQ_FILTFF })
			If nPosTGY == 0
				AADD(aTGY, {(cAliQry)->TGY_FILIAL,;
					(cAliQry)->TGY_ESCALA,;
					(cAliQry)->TGY_CODTDX,;
					(cAliQry)->TGY_GRUPO,;
					(cAliQry)->TGY_DTINI,;
					(cAliQry)->TGY_DTFIM,;
					(cAliQry)->TGY_ULTALO,;
					(cAliQry)->TGY_CODTFF,;
					(cAliQry)->TGY_TIPALO,;
					(cAliQry)->ABQ_FILTFF,;
					{};
				})
				If lHorFlex
					aTGY[Len(aTGY)][11] := { {(cAliQry)->TGY_ENTRA1, (cAliQry)->TGY_SAIDA1},;
											{(cAliQry)->TGY_ENTRA2, (cAliQry)->TGY_SAIDA2},;
											{(cAliQry)->TGY_ENTRA3, (cAliQry)->TGY_SAIDA3},;
											{(cAliQry)->TGY_ENTRA4, (cAliQry)->TGY_SAIDA4} }
				EndIf
			EndIf 						
		EndIf	
	EndIf
	(cAliQry)->(DbSkip())

EndDo

(cAliQry)->(DbCloseArea())

If Len(aDados) > 0
	LogPrAtend( aManut, cCodAtend )
	at190dELoc(aDados, .F., .F., .T., .T.)
EndIf

Return { aTgy, aDados }

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ExecAloca
Realiza aas chamadas das alocaç~pes do atendente.
@author	Augusto Albuquerque
@since 		28/12/2021
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function ExecAloca(cCodTec, aAgendas, dPonIni, dPomFim, cAntCodTec)

AlocEfetiv( cCodTec, aAgendas[1], aAgendas[2], dPonIni, dPomFim )
//realiza a exclusão da rota
At020ExcRt(cAntCodTec,dPonIni,cCodTec)

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020ExcRt
Realiza a exclusão das agendas de rota do atendente
@author	Luiz Gabriel
@since 		20/12/2021
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At020ExcRt(cCodTec,dPonIni, cNewCodTec)
Local cTmpAgenda	:= GetNextAlias()
Local aCodRot 		:= {}
Local aDtIniFim		:= {}
Local cWhereABB		:= "%%"
Local cWhereABQ		:= "%%"
Local cWhereTW1		:= "%%"
Local cBkpFil		:= cFilAnt
Local nX 			:= 0
Local dDtIniRt
Local dDtFimRt
Local dDtIniTGZ
Local dDtFimTGZ

If ABB->(ColumnPos("ABB_CODTW1") > 0)
	cWhereABB := "% AND TW1.TW1_COD = ABB.ABB_CODTW1 %"
EndIf

cWhereABQ := "% AND " + FWJoinFilial("ABQ" , "ABB" , "ABQ", "ABB", .T.) + " %"

cWhereTW1 := "% AND " + FWJoinFilial("TW1" , "ABQ" , "TW1", "ABQ", .T.) + " %"

BeginSql Alias cTmpAgenda
	SELECT 	TW0.TW0_COD,
			TW0.TW0_FILIAL
		FROM %table:ABB% ABB
			INNER JOIN %table:ABQ% ABQ ON ABQ.%NotDel%
									%Exp:cWhereABQ%
									AND ABB.ABB_IDCFAL = ABQ.ABQ_CONTRT || ABQ.ABQ_ITEM || ABQ.ABQ_ORIGEM
			INNER JOIN %table:TDV% TDV ON ABB.ABB_CODIGO = TDV.TDV_CODABB
									AND TDV.TDV_FILIAL = ABB.ABB_FILIAL
									AND TDV.%NotDel%
									AND TDV.TDV_DTREF >= %exp:dPonIni%
			INNER JOIN %table:TW1% TW1 ON TW1.TW1_FILTFF = ABQ.ABQ_FILTFF
									%Exp:cWhereTW1%
									AND TW1.TW1_CODTFF = ABQ.ABQ_CODTFF
									%Exp:cWhereABB%
									AND TW1.%NotDel%
			INNER JOIN %table:TW0% TW0 ON TW0.TW0_FILIAL = TW1.TW1_FILIAL
									AND TW0.TW0_COD = TW1.TW1_CODTW0
									AND TW0.%NotDel%
		WHERE ABB.%NotDel%
		AND ABB.ABB_MANUT  = '2'
		AND ABB.ABB_ATENDE = '2'
		AND ABB.ABB_CHEGOU = 'N'
		AND ABB.ABB_CODTEC = %Exp:cCodTec%
		Group BY	TW0_COD,
					TW0.TW0_FILIAL
EndSql

While (cTmpAgenda)->(! Eof())
	If	aScan(aCodRot, {|x| x == (cTmpAgenda)->TW0_COD}) == 0
		aAdd(aCodRot,{(cTmpAgenda)->TW0_COD, (cTmpAgenda)->TW0_FILIAL})
	EndIf
	(cTmpAgenda)->(DbSkip())
EndDo

(cTmpAgenda)->(DbCloseArea())

For nX := 1 To Len(aCodRot)
	cFilAnt := aCodRot[nX][2]
	aDtIniFim := At581DtCob(aCodRot[nX][1],cCodTec)
	dDtIniRt  := aDtIniFim[1]
	dDtFimRt  := aDtIniFim[2]
	dDtIniTGZ := aDtIniFim[1]
	dDtFimTGZ := aDtIniFim[2]

	If dPonIni < dDtIniRt
		dPonIni := dDtIniRt
	EndIf 

	At581Eftv("TR", aCodRot[nX][1], dDatabase, cNewCodTec, .T., dPonIni, dDtFimRt, dDtIniTGZ, dDtFimTGZ, Posicione("AA1", 1, xFilial("AA1")+cCodTec, "AA1_NOMTEC"))
Next nX	

cFilAnt := cBkpFil

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} AlocEfetiv
Aloca o atendente na nova filial
@author	Augusto Albuquerque
@since 		23/12/2021
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function AlocEfetiv( cCodTec, aTGY, aDados, dPonIni, dPomFim )
Local cSeq		:= ""
Local dDtIni	:= SToD("")
Local lHorFlex	:= TecXHasEdH()
Local nAux		:= 1 
local nX, nY
Local oAloc 	:= Nil//GsAloc():New()
Local oGsLog	:= GsLog():New()
Local cMsg		:= ""

Default aTGY := {}

For nX := 1 To Len(aTGY)

	oAloc := GsAloc():New()
	If nX == 1
		dDtIni := dPonIni
	Else
		dDtIni := SToD(aTGY[nX][5])
	EndIf
	oAloc:defFil(aTGY[nX][10])
	oAloc:defEscala(aTGY[nX][2])
	oAloc:defPosto(aTGY[nX][8])
	oAloc:defTec(cCodTec)
	oAloc:defGrupo(aTGY[nX][4])
	oAloc:defConfal(aTGY[nX][3])
	oAloc:defDate( dDtIni, SToD(aTGY[nX][7]))
	For nY := nAux To Len( aDados )
		cSeq := aDados[nY][13]
		If aDados[nY][11] >= dDtIni
			nAux := nY
			Exit
		EndIf
	Next nY
	oAloc:defSeq(cSeq)
	oAloc:defTpAlo(aTGY[nX][9])
	If lHorFlex
		oAloc:defGeHor( aTGY[nX][11] )
	EndIf

	oAloc:ProjAloc()
	If !oAloc:GravaAloc()
		cMsg := oAloc:defMessage()
	EndIf

	oAloc:destroy()
	If !Empty(cMsg)
		oGsLog:addLog("TransfFunc "+ cCodTec, STR0113 ) //"Projeção e Gravação"
		oGsLog:addLog("TransfFunc "+ cCodTec, "------------------") 
		oGsLog:addLog("TransfFunc "+ cCodTec, STR0114 + cMsg ) //"Mensagem de Erro: "
		oGsLog:addLog("TransfFunc "+ cCodTec, "------------------")
		oGsLog:printLog("TransfFunc "+ cCodTec)
		cMsg := ""
	EndIf 	 

Next nX

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} LogPrAtend
Constroi o log para avisar sobre manutenção de agenda e ou alocação por FT/Hora
@author	Augusto Albuquerque
@since 		03/01/2022
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function LogPrAtend( aAgenda, cCodAtend )
Local aArea		:= GetArea()
Local cQryMAN	:= ""
Local cNomTec	:= AA1->AA1_NOMTEC
Local nX
Local oGsLog	:= Nil //GsLog():New()

Default aAgenda := {}

If !Empty(aAgenda)
	oGsLog	:= GsLog():New()
	oGsLog:addLog("TransfFunc "+ cNomTec, STR0096 ) //"Agendas Com Manutenção ou Alocação FT"
	oGsLog:addLog("TransfFunc "+ cNomTec, "------------------") 

	For nX := 1 To Len(aAgenda)
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0097 + cCodAtend ) //"Codigo antigo do atendente: "
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0098 + cNomTec ) //"Nome do Antendente: "
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0099 + DToC(aAgenda[nX][5]) ) //"Data inicio da agenda: " 
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0100 + aAgenda[nX][6] ) //"Horario Inicio da agenda: "
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0101 +  DToC(aAgenda[nX][7]) ) //"Data fim da agenda: "
		oGsLog:addLog("TransfFunc "+ cNomTec, STR0102 + aAgenda[nX][8] ) //"Horario fim da agenda: "

		If aAgenda[nX][15]
			cQryMAN := GetNextAlias()
			
			BeginSQL Alias cQryMAN
				SELECT ABR.ABR_MOTIVO, ABR.ABR_HRINI, ABR.ABR_HRFIM, ABN.ABN_TIPO, ABN.ABN_DESC, ABR.ABR_CODSUB, AA1.AA1_NOMTEC
				FROM %Table:ABR% ABR
				INNER JOIN %table:ABN% ABN ON ABN.ABN_CODIGO = ABR.ABR_MOTIVO
					AND ABN.ABN_FILIAL = %Exp:xFilial("ABN", aAgenda[nX][2])%
					AND ABN.%NotDel%
				LEFT JOIN %table:AA1% AA1 ON AA1_CODTEC = ABR.ABR_CODSUB
					AND AA1.%NotDel%
					AND AA1.AA1_FILIAL = %Exp:xFilial("AA1", aAgenda[nX][2])%
				WHERE ABR.ABR_FILIAL = %Exp:aAgenda[nX][2]%
				AND ABR.%NotDel%
				AND ABR.ABR_AGENDA = %Exp:aAgenda[nX][1]%
			EndSQL

			If (cQryMAN)->(!EOF())
				oGsLog:addLog("TransfFunc "+ cNomTec, STR0103 + (cQryMAN)->ABN_TIPO + " - " + (cQryMAN)->ABN_DESC ) //"Manutenção do Tipo: "
				oGsLog:addLog("TransfFunc "+ cNomTec, STR0104 + (cQryMAN)->ABR_HRINI ) //"Horario inicial: "
				oGsLog:addLog("TransfFunc "+ cNomTec, STR0105 + (cQryMAN)->ABR_HRFIM )  //"Horario Final: "
				If !Empty((cQryMAN)->ABR_CODSUB)
					oGsLog:addLog("TransfFunc "+ cNomTec, STR0106 + (cQryMAN)->ABR_CODSUB ) //"Atendente Substituto Codigo: "
					oGsLog:addLog("TransfFunc "+ cNomTec, STR0107 + (cQryMAN)->AA1_NOMTEC ) //"Atendente Substituto Nome: "
				EndIf
			EndIf

			(cQryMAN)->(DbCloseArea())
		EndIf

		If aAgenda[nX][17]
			oGsLog:addLog("TransfFunc "+ cNomTec, STR0108) //"Agenda Gerada na Alocação Por Hora"
		ElseIf aAgenda[nX][14]
			oGsLog:addLog("TransfFunc "+ cNomTec, STR0109) //"Agenda Gerada por FT"
		EndIf

		oGsLog:addLog("TransfFunc "+ cNomTec, STR0110 + IIF(aAgenda[nX][16], STR0111, STR0112) ) //"Tipo do Dia: " ## "Trabalhado" ## "Não Trabalhado"
		oGsLog:addLog("TransfFunc "+ cNomTec, "------------------") 

	Next nX 

	oGsLog:printLog("TransfFunc "+ cNomTec) 
EndIf

RestArea( aArea )

Return

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At020CharE
Verifica a existencia de aspas simples no nome do técnico e substitui para não dar erro de
sintaxe SQL na criação da tabela temporária do browse de importação de funcionários.
@author	Luiz Gabriel, Jack Junior - Serviços 
@since 		07/11/2022
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function At020CharE(cExp)
Local cCarac    := "'"
Local cNomeTec	:= ""

If ValType(cExp) == "C"
    cNomeTec := cExp
	If At(cCarac,cExp) > 0
		cNomeTec := StrTran(cExp, cCarac, CHR(96))
	EndIf
EndIf

Return cNomeTec

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} intFindMe
Responsável por criar usuário na plataforma findme, com os dados do atendente
@author	Diego Bezerra
@since 		12/09/2023
/*/
//--------------------------------------------------------------------------------------------------------------------

Static Function intFindMe()
	
	Local lRet := .F.
	Local aTec := {}
	Local oFindMe := authFindMe()
	Local aRet := {}

	If oFindMe:lAuth
		If !Empty(AA1->AA1_EMAIL) .AND. !Empty(AA1->AA1_FONE)
			If !Empty(AA1->AA1_CDFUNC)
				AADD(aTec,AA1->AA1_CODTEC)
				AADD(aTec,AA1->AA1_FILIAL)
				AADD(aTec,'01')
				AADD(aTec,RTRIM(AA1->AA1_NOMTEC))
				AADD(aTec,SuperGetMv("MV_FDROLE",,'coordinator'))
				AADD(aTec,RTRIM(AA1->AA1_EMAIL))
				AADD(aTec,RTRIM(AA1->AA1_CDFUNC))
				AADD(aTec,SuperGetMv('MV_FDPSW',,'1234'))
				AADD(aTec,'pt-BR')
				AADD(aTec,RTRIM(AA1->AA1_FONE))
				AADD(aRet,aTec)
				// Realiza a inclusão de usuário na plataforma findMe, conforme dados do atendente
				if !usrFindMe(aRet, oFindMe)
					Help(,,STR0120,,STR0121,1,0) //"Atendente FindMe" # "Nâo foi possível incluir o atendente selecionado, pois ele já está cadastrado na FindMe."
				EndIf
			Else
				Help(,,STR0122,,STR0123,1,0) //"Atendente não é funcionário" # "O atendente deve ser um funcionário nessa integração"
			EndIf
		Else
			// Alerta de email obrigatório
			Help(,,STR0124,,STR0125,1,0) //"Campos obrigatórios" #"Campo telefone e email são obrigatórios para essa integração"
		EndIf
	Else
		// Alerta de erro na autenticação com a findme
		Help(,,STR0126,,STR0127,1,0) //"Autenticação findme" # "Erro ao autenticar na plataforma findme" 
	EndIf

Return lRet

/*/{Protheus.doc} ExecUnif
	(long_description)
	@type Static Function
	@author Anderson F. Gomes
	@since 26/11/2024
	@param cFilOrig, Character, Filial de Origem
	@param cCodAtend, Character, Código do Atendente
	@return Nil, Nil, Nulo
/*/
Static Function ExecUnif( cFilOrig, cCodAtend )
	Local lRet As Logical
	Local lRestFil As Logical
	Local oStatement As Object
	Local oMdlUnif As Object
	Local oMdlTXD As Object
	Local cQuery As Character
	Local cAliasQry As Character
	Local cCompFSCP	As Character
	Local cFilBkp	As Character
	Local nX As Numeric

	lRet := .T.
	lRestFil := .F.
	cCompFSCP := FWModeAccess( "SCP", 3 )

	cQuery := "SELECT DISTINCT TXD_CODSA "
	cQuery += "FROM ? "
	cQuery += "WHERE TXD_FILIAL = ? "
	cQuery += "AND TXD_CODTEC = ? "
	cQuery += "AND D_E_L_E_T_ = ?"

	oStatement := FWPreparedStatement():New( cQuery )
	oStatement:SetNumeric( 1, RetSqlName( "TXD" ) )
	oStatement:SetString( 2, FWxFilial( "TXD" ) )
	oStatement:SetString( 3, cCodAtend )
	oStatement:SetString( 4, ' ' )

	cQuery := ChangeQuery( oStatement:GetFixQuery() )
	cAliasQry := GetNextAlias()

	MPSysOpenQuery( cQuery, cAliasQry )
	While (cAliasQry)->( !Eof() )
		SCP->( DbSetOrder(1) )
		If SCP->( MSSeek( IIf( cCompFSCP == "E", cFilOrig, FWxFilial( "SCP" ) ) + (cAliasQry)->TXD_CODSA ) )
			If A185BtVd() .Or. A185BtAm()
				If !Empty( cFilOrig ) .And. cFilOrig <> cFilAnt
					cFilBkp := cFilAnt
					cFilAnt := cFilOrig
					lRestFil := .T.
				EndIf
				TXC->( DbSetOrder(1) )//TXC_FILIAL+TXC_CODTEC
				If TXC->( MsSeek( FWxFilial( "TXC" ) + cCodAtend ) )
					oMdlUnif := FwLoadModel( "TECA894" )
					oMdlUnif:SetOperation( MODEL_OPERATION_DELETE )
					oMdlUnif:Activate()
					oMdlTXD := oMdlUnif:GetModel( "TXDItens" )
					For nX := 1 To oMdlTXD:Length()
						At894Auto(	nX,;
									oMdlTXD:GetValue( "TXD_CODPRO", nX ),;
									Posicione( "SB1", 1, FWxFilial( "SB1" ) + oMdlTXD:GetValue( "TXD_CODPRO", nX ), "B1_UM" ),;
									oMdlTXD:GetValue( "TXD_QTDE", nX ),;
									dDataBase,;
									oMdlUnif:GetModel( "TXCMaster" ):GetValue( "TXC_ARMDEV" ),;
									oMdlTXD:GetValue( "TXD_CODSA", nX ),;
									5,;
									oMdlTXD )
					Next nX
					oMdlUnif:CommitData()
				EndIf
				If lRestFil
					cFilAnt := cFilBkp
				EndIf
			EndIf
		EndIf
		(cAliasQry)->( DbSkip() )
	EndDo

	(cAliasQry)->(DbCloseArea())
	oStatement:Destroy()
	FwFreeObj( oStatement )

Return lRet
