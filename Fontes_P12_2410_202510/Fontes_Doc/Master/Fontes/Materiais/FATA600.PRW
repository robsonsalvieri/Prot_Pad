#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA600.CH"
#INCLUDE "DBTREE.CH"
#INCLUDE "FWMVCDEF.CH"

PUBLISH MODEL REST NAME FATA600 SOURCE FATA600

Static aTipo09		:= {}
Static aCronoFin	:= {}
Static aConfigAlo 	:= {}	// Configuracao da alocacao de recurso.
Static _aTFFCOD_   	:= {} 	// Armazena os codigos gerados para os itens do RH
Static _lVistoria_	:= .F.	// Armazena se a chamada do Orcamento e via Vistoria
Static _oMdlOpor	:= Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} FATA600
Proposta Comercial (Convertida em MVC)

@author luiz.jesus
@since 18/03/2014
@version 12
/*/
//------------------------------------------------------------------
Function FATA600(oMdlFt300, nOperation, aADYMaster, aADZProduto, aADZAcessor)

Local aArea		:= GetArea()
Local oBrowse	:= Nil															//Objeto do Browse
Local nX		:= 0															//Contador
Local aSincProp	:= {}															//Campo virtual para ilutrar a proposta sincronizada
Local nOpcADJ	:= SuperGetMv("MV_FATMNTP",, 1)								//Tratamento do dado na ADJ
Local cFilter	:= ""															//Filtro do Browse
Local aCores	:= {}															//Cores da legenda
Local oTableAtt	:= Nil
Local lFT600FIL	:= ExistBlock("FT600FIL")
Local aCoresNew	:= {}
Local lRet		:= .T.
Local aAutoRot	:= {}
Local nSeekOpt	:= 0
Local aFT600CL	:= {}

Default nOperation	:= MODEL_OPERATION_INSERT
Default aADYMaster	:= {}
Default aADZProduto	:= {}
Default aADZAcessor	:= {}

Private cCadastro	:= STR0001 	//"Proposta Comercial"
Private aRotina		:= MenuDef(oMdlFt300)

If Len( aADYMaster ) == 0
	// Tratamento efetuado para quando chamado a proposta, da visualização de uma oportunidade.
	If ValType(oMdlFt300) == "O"
		nOperation	:= oMdlFt300:GetOperation()
		If nOperation == MODEL_OPERATION_VIEW .And. nOpcADJ <> 4
			// Se tiver proposta
			DbSelectArea( "ADJ" )
			DbSetOrder( 1 ) //ADJ_FILIAL+ADJ_NROPOR+ADJ_REVISA
			If !( Empty(FwFldGet("ADJ_PROPOS")) ) .AND. ( DbSeek(xFilial("ADJ")+FwFldGet("AD1_NROPOR")) )
				//Liberado a opção visualizar da proposta, não possivel realizar as outras opções de menu.
				aRotina := {}
				ADD OPTION aRotina TITLE STR0023 ACTION "VIEWDEF.FATA600"	OPERATION 2 ACCESS 0 	// "Visualizar"
				AAdd( aRotina, { STR0026, 'A600Impr' 	, 0, 6, 0, NIL } ) 	//"Impressão"
			Else
				HELP(" ",1,"REGNOIS")
				lRet	:= .F.
			EndIf
		EndIf
	EndIf

	If lRet
		If ValType(oMdlFt300) == "O"
			_oMdlOpor := oMdlFt300
		EndIf

		aCores 	:= FT600Legenda()

		cFilter := "ADY_FILIAL == '" + xFilial("ADY") + "' .AND. ADY_OPORTU == '" + AD1->AD1_NROPOR + "'"
		If lFT600FIL
			cFilter += " .AND. "+ExecBlock("FT600FIL",.F.,.F.,{cFilter})
		EndIf
		oBrowse := FWMBrowse():New()
		oBrowse:SetAlias('ADY')

		//Adiciona as Legendas no Browse
		For nX := 1 To Len(aCores)
			oBrowse:AddLegend( aCores[nX][1], aCores[nX][2], aCores[nX][3] )
		Next nX

		//------------------------------------------
		// Ponto de entrada que adiciona coluna ao Browse
		//------------------------------------------
		If ExistBlock("FT600CL")
			aFT600CL := ExecBlock("FT600CL",.F.,.F.)
			If ValType(aFT600CL) == "A"
				For nX := 1 to len(aFT600CL)
					oBrowse:AddColumn(aFT600CL[nX])
				Next nX
			EndIf
			aFT600CL	:= {}
		Endif

		oBrowse:SetFilterDefault(cFilter)

		//Adiciona no browse o campo Sincronizar para o parametro MV_FATMNTP igual a 1
		If cValToChar( nOpcADJ ) $ "1|4"
			aSincProp:= { STR0192, {|| IIF(ADY->ADY_SINCPR,"LBOK","LBNO")}, "C", "@BMP", 0, 1, 0, .F., {||}, .T.,,,,, .F. } //Sincronizar
			oBrowse:AddColumn(aSincProp)
		EndIf

		oBrowse:SetCacheView( .F. )
		//Habilita as visões na proposta comercial
		oBrowse:SetAttach(.T.)
		oTableAtt := TableAttDef()
		If oTableAtt <> Nil
			oBrowse:SetViewsDefault(oTableAtt:aViews)
			oBrowse:SetChartsDefault(oTableAtt:aCharts)

			oBrowse:SetIDChartDefault( "PorDtEmis" )
		EndIf

		//Se não for SIGACRM inibe a exibição do gráfico
		If nModulo <> 73
			oBrowse:SetOpenChart( .F. )
		EndIf

		oBrowse:SetCanSaveArea(.T.)
		oBrowse:SetTotalDefault('ADY_PROPOS','COUNT',STR0281) // "Total de Registros"
		oBrowse:SetChgAll(.F.)
		oBrowse:SetSeeAll(.F.)
		oBrowse:DisableDetails(.F.)
		oBrowse:SetDescription(cCadastro)
		oBrowse:SetMenuDef("FATA600")
		oBrowse:SetMainProc("FATA600")
		oBrowse:Activate()
	EndIf
Else
	//------------------------------------------
	// Cabeçalho - Proposta
	//------------------------------------------
	aAdd( aAutoRot, { "ADYMASTER", aADYMaster } )

	//------------------------------------------
	// Itens - Proposta (Folder Produtos)
	//------------------------------------------
	aAdd( aAutoRot, { "ADZPRODUTO", aADZProduto } )

	//------------------------------------------
	// Itens - Proposta (Folder Acessórios)
	//------------------------------------------
	aAdd( aAutoRot, { "ADZACESSOR", aADZAcessor } )

	//-------------------------------------------------------------
	// Posiciona na oportunidade caso  o modelo não esteja ativo.
	//-------------------------------------------------------------
	nSeekOpt := aScan( aADYMaster, { |x| Alltrim( x[1] ) == "ADY_OPORTU" } )

	If nSeekOpt > 0
		DbSelectArea( "AD1" )
		DbSetOrder( 1 ) //AD1_FILIAL+AD1_NROPOR+AD1_REVISA
		If DbSeek( xFilial("AD1") + aADYMaster[nSeekOpt][2] )
			_oMdlOpor := FwLoadModel( 'FATA300' )
			_oMdlOpor:SetOperation( MODEL_OPERATION_UPDATE )
			_oMdlOpor:Activate()

			FwMvcRotAuto( ModelDef(), "ADY", nOperation, aAutoRot, /*lSeek*/, .T. )
		Else
			Help( ,, 'A600EXECAUTO1',, STR0323, 1, 0 )//"O número da oportunidade informado não foi encontrado, sendo assim, a execução da rotina automática não será efetuada."
		EndIf
	Else
		Help( ,, 'A600EXECAUTO2',, STR0324, 1, 0 )//"Não foi informado o número da Oportunidade de Vendas, sendo assim, a execução da rotina automática não será efetuada."
	EndIf

EndIf

_oMdlOpor := Nil

RestArea( aArea )
Return Nil

//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as visões e gráficos padrão.

@sample	TableAttDef()

@param		Nenhum

@return	ExpA - Array de Objetos com as Visoes e Gráficos.

@author	Aline Kokumai
@since		02/05/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt		:= FWTableAtt():New()
Local oPorStatus	:= Nil // Pizza: Proposta Comercial por Status
Local oPorDtEmis	:= Nil // Colunas: Proposta Por Data de Emissão
Local oPropSinc		:= Nil // Visão: Oportunidades Sincronizadas

oTableAtt:SetAlias("ADY")

// Proposta Sincronizada
oPropSinc := FWDSView():New()
oPropSinc:SetName(STR0260)	//"Proposta Sincronizada"
oPropSinc:SetID("PropSinc")
oPropSinc:SetOrder(1) // ADY_FILIAL+ADY_PROPOS+ADY_PREVIS
oPropSinc:SetCollumns({	"ADY_PROPOS", "ADY_PREVIS", "ADY_OPORTU", "ADY_REVISA", "ADY_ENTIDA", "ADY_CODIGO",;
					 	"ADY_LOJA", "ADY_DESENT","ADY_TABELA", "ADY_STATUS", "ADY_VEND", "ADY_MSBLQL"})
oPropSinc:SetPublic( .T. )
oPropSinc:AddFilter(STR0260, "ADY_SINCPR == .T.")
oTableAtt:AddView(oPropSinc)

// GRAFICOS
// Pizza: Proposta Comercial por Status
oPorStatus := FWDSChart():New()
oPorStatus:SetName(STR0261)	//"Proposta Comercial por Status"
oPorStatus:SetTitle(STR0261)
oPorStatus:SetID("PorStatus")
oPorStatus:SetType("PIECHART")
oPorStatus:SetSeries({ {"ADY", "ADY_PROPOS", "COUNT"} })
oPorStatus:SetCategory( { {"ADY", "ADY_STATUS"} } )
oPorStatus:SetPublic( .T. )
oPorStatus:SetLegend( CONTROL_ALIGN_BOTTOM ) //Inferior
oPorStatus:SetTitleAlign( CONTROL_ALIGN_CENTER )
oTableAtt:AddChart(oPorStatus)

// Colunas: Proposta Por Data de Emissão
oPorDtEmis := FWDSChart():New()
oPorDtEmis:SetName(STR0264)	//"Proposta Por Data de Emissão"
oPorDtEmis:SetTitle(STR0264)
oPorDtEmis:SetID("PorDtEmis")
oPorDtEmis:SetType("BARCOMPCHART")
oPorDtEmis:SetSeries({ {"ADY", "ADY_PROPOS", "COUNT"} })
oPorDtEmis:SetCategory( { {"ADY", "ADY_DATA"} } )
oPorDtEmis:SetPublic( .T. )
oPorDtEmis:SetLegend( CONTROL_ALIGN_BOTTOM ) //Inferior
oPorDtEmis:SetTitleAlign( CONTROL_ALIGN_CENTER )
oTableAtt:AddChart(oPorDtEmis)

Return (oTableAtt)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} MenuDef
Definição do MenuDef

@return ExpO, Array, aRotina

@author luiz.jesus
@since 18/03/2014
@version 12
/*/
//--------------------------------------------------------------------------------------------------------------------
Static Function MenuDef(oMdlFt300)

Local aRotina 	:= {}
Local nPos		:= 0
Local aAtiv		:= {}
Local aAnotac	:= {}
Local aEntRelac	:= {}
Local lFT600MNU	:= ExistBlock("FT600MNU")
Local aMenuUser	:= {}
Local n1		:= 0

ADD OPTION aRotina TITLE  STR0023  ACTION  "VIEWDEF.FATA600" OPERATION 2 ACCESS 0 // "Visualizar"
ADD OPTION aRotina TITLE  STR0024  ACTION  "VIEWDEF.FATA600" OPERATION 3 ACCESS 0 // "Incluir"
ADD OPTION aRotina TITLE  STR0025  ACTION  "VIEWDEF.FATA600" OPERATION 4 ACCESS 0 // "Alterar"
ADD OPTION aRotina TITLE  STR0335  ACTION  "VIEWDEF.FATA600" OPERATION 5 ACCESS 0 // "Excluir"
ADD OPTION aRotina TITLE  STR0191  ACTION  "VIEWDEF.FATA600" OPERATION 9 ACCESS 0 // "Copiar"

AAdd( aRotina, { STR0026, 'A600Impr' 	, 0, 6, 0, NIL } ) //"Impressão"
aAdd( aRotina, { STR0190, 'A600CompPr' , 0, 7, 0, NIL } ) //"Comparar Propostas"

If nModulo == 28
	AAdd( aRotina, { STR0219, "A600SerRes", 0, 4,, .T. }  ) // "Reservar Equip."
EndIf

If nModulo == 73

	aEntRelac := CRMXINCROT( "ADY", aEntRelac )

	ADD OPTION aEntRelac TITLE STR0235   ACTION "CRMA200()"	 OPERATION  8 ACCESS 0  //"Privilégios"

	nPos := ASCAN(aEntRelac, { |x|  x[2] == "CRMA190Con()" })
	If nPos > 0
		ADD OPTION aRotina TITLE aEntRelac[nPos][1] ACTION aEntRelac[nPos][2] OPERATION 8  ACCESS 0//"Conectar"
		Adel(aEntRelac,nPos)
		Asize(aEntRelac,Len(aEntRelac)-1)
	EndIf

	nPos := ASCAN(aEntRelac, { |x|  IIF(ValType(x[2]) == "C", x[2] == "CRMA180()", Nil) })
	If nPos > 0
		ADD OPTION aAtiv   TITLE STR0231 ACTION "CRMA180(,,,3,,)" OPERATION 3  ACCESS 0 //"Nova Atividade"
		ADD OPTION aAtiv   TITLE STR0232 ACTION "CRMA180()" OPERATION 8  ACCESS 0 //"Todas as ATividades"
		aEntRelac[nPos][2] := aAtiv
	EndIf

	nPos := ASCAN(aEntRelac, { |x|  IIF(ValType(x[2]) == "C", x[2] == "CRMA090()", Nil) })
	If nPos > 0
		ADD OPTION aAnotac   TITLE STR0233 ACTION "CRMA090(3)" OPERATION 3  ACCESS 0 //"Nova Anotação"
		ADD OPTION aAnotac   TITLE STR0234 ACTION "CRMA090()" OPERATION 8  ACCESS 0 //"Todas as Anotações"
		aEntRelac[nPos][2] := aAnotac
	EndIf
EndIf

//copia é desativada enquanto o novo fata600 da totvs12 nao descer para o padrão
If nModulo == 28
	nPos := ASCAN(aRotina, { |x|  x[1] == "Copiar" })
	If nPos > 0
		Adel(aRotina,nPos)
		Asize(aRotina,Len(aRotina)-1)
	EndIf
EndIf

Asort(aEntRelac,,,{ | x,y | y[1] > x[1] } )

ADD OPTION aRotina TITLE  STR0266 ACTION aEntRelac 	    OPERATION 8  ACCESS 0	//"Relacionadas"

If lFT600MNU
	aMenuUser := ExecBlock("FT600MNU",.F.,.F.,{aRotina,oMdlFt300})
	If ValType(aMenuUser) == "A"
		For n1 := 1 to Len(aMenuUser)
			ADD OPTION aRotina TITLE  aMenuUser[n1,1] ACTION aMenuUser[n1,2] OPERATION aMenuUser[n1,3]  ACCESS 0 
		Next n1
	EndIf
EndIf

Return aRotina

//-------------------------------------------------------------------
/*/{Protheus.doc} ModelDef
Definição do modelo de Dados

@author luiz.jesus
@since 18/03/2014
@version 12
/*/
//-------------------------------------------------------------------
Static Function ModelDef()

Local oModel
Local nTamTot		:= GetSX3Cache("ADZ_TOTAL","X3_TAMANHO")		// Tamanho do campo ADZ_TOTAL.
Local nDecTot		:= GetSX3Cache("ADZ_TOTAL","X3_DECIMAL")		// Numero de decimais do campo ADZ_TOTAL.
Local lFta600MDL	:= ExistBlock("FATA600MDL")
Local lVldArmazem	:= (SuperGetMv("MV_RESTARM",,"0") <> "0")

Local oStructFke	:= FWFormModelStruct():New()

Local oStrADY		:= FWFormStruct(1,'ADY')			//Cabeçalho da Proposta Comercial
Local oStrADZPro	:= FWFormStruct(1,'ADZ')			//Itens da Proposta Comercial
Local oStrADZAce	:= FWFormStruct(1,'ADZ')			//Itens da Proposta Comercial
Local aADY1         := {}
Local aADY2         := {}
Local aADY3         := {}
Local aADY4         := {}
Local aADY5         := {}
Local aADY6         := {}
Local aADY7         := {}

//Blocos de validação
Local bLinePost		:= {|oModel, nLine| At600LOk(oModel, nLine)}  	//Pré-Validação da linha
Local bPosVldLin		:= {|oModel| At600VPsLn(oModel)} 	//Pós-Validação da linha
Local bTudoOk			:= {|oModel| At600TOk(oModel)}	 	//Pós-Validação do Model
Local bCommit			:= {|oModel| At600Commit(oModel)} 	//Commit do Model
Local bCancel			:= {|oModel| At600Cancel(oModel)} 	//Cancelamento da Proposta
Local bLoadCro		:= {|| {}}
Local bActive			:= {|oModel| Ft600MdlAct(oModel)}	//Load do Grid
Local cValidUser		:= ""
Local bVldActive		:= {|oModel| FT600VldActivate(oModel)}

oStructFke:AddTable("ZYY",{},STR0267) //"Cronograma"

//----------------Estrutura para criação do campo-----------------------------
// [01] C Titulo do campo
// [02] C ToolTip do campo
// [03] C identificador (ID) do Field
// [04] C Tipo do campo
// [05] N Tamanho do campo
// [06] N Decimal do campo
// [07] B Code-block de validação do campo
// [08] B Code-block de validação When do campo
// [09] A Lista de valores permitido do campo
// [10] L Indica se o campo tem preenchimento obrigatório
// [11] B Code-block de inicializacao do campo
// [12] L Indica se trata de um campo chave
// [13] L Indica se o campo pode receber valor em uma operação de update.
// [14] L Indica se o campo é virtual

oStructFke:AddField(STR0268, STR0269, "ZYY_FILIAL", "C", FwSizeFilial(), 0)								//"Filial"//"Filial do Sistema"
oStructFke:AddField(STR0270, STR0270, "ZYY_PARCE",  "C", GetSX3Cache("E1_PARCELA","X3_TAMANHO"), 0)	//Parcela
oStructFke:AddField(STR0271, STR0271, "ZYY_VENC",   "D", 8,              0)								//"Vencimento"
oStructFke:AddField(STR0271, STR0271, "ZYY_TOTAL",  "N", nTamTot,        nDecTot)						//"Vencimento"

If lVldArmazem
	oStrADZPro:AddField(AllTrim(FWX3Titulo("CK_LOCAL")),"","ADZ_ARMAZE","C",GetSX3Cache("CK_LOCAL","X3_TAMANHO"),0,/*bValid*/,/*bWhen*/,/*aValues*/,/*lObrigat*/,/*bInit*/,/*lKey*/,/*LNoUpd*/,.T.,/*cValid*/)
EndIF

aADY1 := FwStruTrigger("ADY_CONDPG" /*cDom*/,;
                       "ADY_CONDPG" /*cCDom*/,;
                       "A600TFGrid(1)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY1[1],aADY1[2],aADY1[3],aADY1[4]) 

aADY2 := FwStruTrigger("ADY_DESCON" /*cDom*/,;
                       "ADY_DESCON" /*cCDom*/,;
                       "A600TFGrid(2)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY2[1],aADY2[2],aADY2[3],aADY2[4])

aADY3 := FwStruTrigger("ADY_TES" /*cDom*/,;
                       "ADY_TES" /*cCDom*/,;
                       "A600TFGrid(3)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY3[1],aADY3[2],aADY3[3],aADY3[4])

aADY4 := FwStruTrigger("ADY_TPPROD" /*cDom*/,;
                       "ADY_TPPROD" /*cCDom*/,;
                       "A600TFGrid(4)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY4[1],aADY4[2],aADY4[3],aADY4[4])

aADY5 := FwStruTrigger("ADY_LOCAL" /*cDom*/,;
                       "ADY_LOCAL" /*cCDom*/,;
                       "A600TFGrid(5)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY5[1],aADY5[2],aADY5[3],aADY5[4])


aADY6 := FwStruTrigger("ADY_TABELA" /*cDom*/,;
                       "ADY_TABELA" /*cCDom*/,;
                       "A600TFGrid(6)" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY6[1],aADY6[2],aADY6[3],aADY6[4])

aADY7 := FwStruTrigger("ADY_TABELA" /*cDom*/,;
                       "ADY_TABELA" /*cCDom*/,;
                       "A600TFGrid(2)," /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;  
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStrADY:AddTrigger(aADY7[1],aADY7[2],aADY7[3],aADY7[4])




Ft600TAddADZ( oStrADZPro, "ADZPRODUTO" ) 
Ft600TAddADZ( oStrADZAce, "ADZACESSOR" )

//Para modulo Gestão de Serviço campo não é Necessario Ser Obrigatorio
If nModulo == 28

	oStrADY:SetProperty('ADY_TPCONT', MODEL_FIELD_INIT, {|| '4'} )
	oStrADY:SetProperty('ADY_TABELA' ,MODEL_FIELD_OBRIGAT,.F.)
	oStrADY:SetProperty('ADY_TABELA' ,MODEL_FIELD_WHEN   , {|| .F. } )

	oStrADY:SetProperty('ADY_CONDPG' ,MODEL_FIELD_OBRIGAT,.F.)
	oStrADY:SetProperty('ADY_CONDPG' ,MODEL_FIELD_WHEN   , {|| .T. } )

	oStrADY:SetProperty('ADY_TES' ,MODEL_FIELD_OBRIGAT,.F.)
	oStrADY:SetProperty('ADY_TES' ,MODEL_FIELD_WHEN   , {|| .T.} )
EndIf

oStrADZPro:SetProperty("ADZ_FOLDER",MODEL_FIELD_INIT	,FwBuildFeature(STRUCT_FEATURE_INIPAD,"'1'")) // Produto(s)
oStrADZAce:SetProperty("ADZ_FOLDER",MODEL_FIELD_INIT	,FwBuildFeature(STRUCT_FEATURE_INIPAD,"'2'")) // Acessorio(s)

cValidUser := FT600VldUser( "ADZ_PRODUT" )
oStrADZPro:SetProperty("ADZ_PRODUT",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Ft600VProd('ADZPRODUTO') .And. " + cValidUser ) )
oStrADZAce:SetProperty("ADZ_PRODUT",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Ft600VProd('ADZACESSOR') .And. " + cValidUser ) )

cValidUser := FT600VldUser( "ADZ_PRDALO" )
oStrADZPro:SetProperty("ADZ_PRDALO",MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"At600VdAlo(1) .And. " + cValidUser ) )
oStrADZAce:SetProperty("ADZ_PRDALO",MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"At600VdAlo(2) .And. " + cValidUser ) )

cValidUser := FT600VldUser( "ADZ_CONDPG" )
oStrADZPro:SetProperty("ADZ_CONDPG",MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID," ExistCpo('SE4') .AND. A600VldCPg('ADZPRODUTO') .And. " + cValidUser + ".And. A600VTP09 ('ADZPRODUTO')") )
oStrADZAce:SetProperty("ADZ_CONDPG",MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID," ExistCpo('SE4') .AND. A600VldCPg('ADZACESSOR') .And. " + cValidUser + ".And. A600VTP09 ('ADZACESSOR')") )

If lVldArmazem
	oStrADZPro:SetProperty("ADZ_ARMAZE",MODEL_FIELD_INIT,FwBuildFeature(STRUCT_FEATURE_INIPAD,"Ft600VArmaze('ADZPRODUTO')")) 
	oStrADZPro:SetProperty("ADZ_ARMAZE",MODEL_FIELD_VALID,FwBuildFeature(STRUCT_FEATURE_VALID,"Ft600VArmaze('ADZPRODUTO',.T.)")) 
EndIf

oStrADZPro:SetProperty("ADZ_PRCVEN",MODEL_FIELD_OBRIGAT,.F.)
oStrADZPro:SetProperty('ADZ_TOTAL'	,MODEL_FIELD_OBRIGAT,.F.)
oStrADZAce:SetProperty("ADZ_PRCVEN",MODEL_FIELD_OBRIGAT,.F.)
oStrADZAce:SetProperty('ADZ_TOTAL'	,MODEL_FIELD_OBRIGAT,.F.)

//----------------------------------------------------------------------------------------------------------------------
// Retira a obrigatoriedade da tabela quando é utilizado agrupador, pois neste caso, será utilizado a tabela de preços
// das regras de comercialização por item
//----------------------------------------------------------------------------------------------------------------------
If SuperGetMv( "MV_FATMNTP",, 1 ) == 4
	oStrADY:SetProperty( "ADY_TABELA", MODEL_FIELD_OBRIGAT, .F. )
EndIf

oModel	:= MPFormModel():New('FATA600' /*<cID>*/,;
      	                     /*<bPre>*/,;
      	                     bTudoOk /*<bPost>*/,;
      	                     bCommit /*<bCommit>*/,;
      	                     bCancel /*<bCancel>*/)

oModel:addFields('ADYMASTER',,oStrADY,)

oModel:addGrid('ADZPRODUTO' /*<cId>*/,;
               'ADYMASTER' /*<cOwner>*/,;
               oStrADZPro /*<oModelStruct>*/,;
               {|oModel,nLine,cAction,cField| Ft600LinePre(oModel,nLine,cAction,cField,1)} /*<bLinePre>*/,;
               bLinePost /*<bLinePost>*/,;
               /*<bPreVal>*/,;
               bPosVldLin /*<bPost>*/,;
               /*<bLoadGrid>*/)
oModel:addGrid('ADZACESSOR' /*<cId>*/,;
               'ADYMASTER' /*<cOwner>*/,;
               oStrADZAce /*<oModelStruct>*/,;
               {|oModel,nLine,cAction,cField| Ft600LinePre(oModel,nLine,cAction,cField,2)} /*<bLinePre>*/,;
               bLinePost /*<bLinePost>*/,;
               /*<bPreVal>*/,;
               bPosVldLin /*<bPost>*/,;
               /*<bLoadGrid>*/)
oModel:addGrid('CRONOFIN' /*<cId>*/,;
               'ADYMASTER' /*<cOwner>*/,;
               oStructFke /*<oModelStruct>*/,;
               /*<bLinePre>*/,;
               /*<bLinePost>*/,;
               /*<bPreVal>*/,;
               /*<bPost>*/,;
               bLoadCro /*<bLoadGrid>*/)

oModel:GetModel('ADYMASTER'):SetDescription(STR0252)	//"Cabeçalho da Proposta"
oModel:GetModel('ADZPRODUTO'):SetDescription(STR0236)	//"Produto"
oModel:GetModel('ADZACESSOR'):SetDescription(STR0237)	//'Acessorio'

//Realiza o relacionamento
oModel:SetRelation('ADZPRODUTO',{{'ADZ_FILIAL','xFilial("ADZ")'},{"ADZ_PROPOS","ADY_PROPOS"},{"ADZ_REVISA","ADY_PREVIS"},{"ADZ_FOLDER","'1'"}},ADZ->(IndexKey(3)))
oModel:SetRelation('ADZACESSOR',{{'ADZ_FILIAL','xFilial("ADZ")'},{"ADZ_PROPOS","ADY_PROPOS"},{"ADZ_REVISA","ADY_PREVIS"},{"ADZ_FOLDER","'2'"}},ADZ->(IndexKey(3)))

// --------------+
// TOTALIZADORES |
// --------------+
//-- Total Geral de Produto
oModel:AddCalc('CALC' /*cId*/,;
               'ADYMASTER' /*cOwner*/,;
               'ADZPRODUTO' /*cIdForm*/,;
               'ADZ_TOTAL' /*cIdField*/,;
               'ADZ__TOTPRD' /*cIdCalc*/,;
               'FORMULA' /*cOperation*/,;
               {||.T.} /*bCondition*/,;
                /*bInitValue*/,;
               STR0283 /*cTitle*/,; // 'Total Geral de Produto'
               {|oMdlCalc,nTotal,xValor,lSum| A600RdpTot(oMdlCalc,nTotal,xValor,lSum,'ADZPRODUTO','ADZ__TOTPRD') },;
               /*nTamanho*/,;
               /*nDecimal*/)
//-- Total Geral de Acessorio
oModel:AddCalc('CALC' /*cId*/,;
               'ADYMASTER' /*cOwner*/,;
               'ADZACESSOR' /*cIdForm*/,;
               'ADZ_TOTAL' /*cIdField*/,;
               'ADZ__TOTACE' /*cIdCalc*/,;
               'FORMULA' /*cOperation*/,;
               {||.T.} /*bCondition*/,;
               /*bInitValue*/,;
               STR0285 /*cTitle*/,; // 'Total Geral de Acessorio'
               {|oMdlCalc,nTotal,xValor,lSum| A600RdpTot(oMdlCalc,nTotal,xValor,lSum,'ADZACESSOR','ADZ__TOTACE') },;
               /*nTamanho*/,;
               /*nDecimal*/)
//-- Total Geral da Proposta
oModel:AddCalc('CALC' /*cId*/,;
               'ADYMASTER' /*cOwner*/,;
               "ADZPRODUTO" /*cIdForm*/,;
               "ADZ_TOTAL" /*cIdField*/,;
               "ADZ__TOTPRO" /*cIdCalc*/,;
               "FORMULA" /*cOperation*/,;
               {||.T.} /*bCondition*/,;
               /*bInitValue*/,;
               STR0286 /*cTitle*/,; // "( A+B )"#"Total Geral da Proposta"
               {|oModel| oModel:GetValue("CALC",'ADZ__TOTPRD')+oModel:GetValue("CALC",'ADZ__TOTACE')} /*bFormula*/,;
               nTamTot /*nTamanho*/,;
               nDecTot /*nDecimal*/)


oModel:GetModel('CALC'):AddEvents('CALC',"ADZ__TOTPRO","ADZ__TOTACE",{|| .T. })	//"Total" 'Acessorio'


//---------------------------------------------------+
// MODULO SIGAGTP - GESTÃO TRANSPORTE DE PASSAGEIROS |
//---------------------------------------------------+
If nModulo == 88 .AND. FindFunction("GTPX600MDL")

	//Atribui os subModelos p/ Model (GTP)
	GTPX600Mdl(oModel, oStrADZPro)

EndIf

//Utilização da grid antiga (GetDados())
oModel:GetModel("ADZPRODUTO"):SetUseOldGrid( .T. )

//Permite o Grid sem dados
oModel:GetModel("ADZACESSOR"):SetOptional(.T.)
oModel:GetModel("CRONOFIN"):SetOptional(.T.)

//Utilização da grid antiga (GetDados())
oModel:GetModel("ADZACESSOR"):SetUseOldGrid( .T. )

//Desabilita o Cronograma financeiro para edição e no commit
oModel:GetModel('CRONOFIN'):SetNoInsertLine( .T. )
oModel:GetModel('CRONOFIN'):SetNoDeleteLine( .T. )
oModel:GetModel('CRONOFIN'):SetNoUpdateLine( .T. )

//Desabilita o model para o commit
oModel:GetModel('CRONOFIN'):SetOnlyQuery(.T.)

oModel:SetVldActivate( bVldActive )

//Inicializa cronograma financeiro
oModel:SetActivate(bActive)

// ---------------------------------------------------------+
// Ponto de entada para adição de totalizadores customizados|
// ---------------------------------------------------------+
If lFta600MDL
	ExecBlock("FATA600MDL" ,.F. ,.F. ,{oModel})
EndIF
Return oModel

//-------------------------------------------------------------------
/*/{Protheus.doc} FT600VldActivate
Valida o activate do model.
@author luiz.jesus
@since 18/03/2014
@version 12
/*/
//-------------------------------------------------------------------
Static Function FT600VldActivate( oModel )

Local lRet 			:= .T.
Local nOperation	:= oModel:GetOperation()
Local oStructADY	:= Nil

If ( !Empty( _oMdlOpor ) .And. _oMdlOpor:IsActive() .And. _oMdlOpor:GetOperation() == MODEL_OPERATION_VIEW )
	If nOperation == MODEL_OPERATION_INSERT
		Help(,,"FT600VLDACT",, STR0336 + STR0024 + STR0337, 1, 0 ) //"Não é possível #Incluir# proposta devido a operação da oportunidade ser visualização."
		lRet := .F.
	ElseIf nOperation == MODEL_OPERATION_UPDATE
		Help(,,"FT600VLDACT",, STR0336 + STR0025 + STR0337, 1, 0 ) //"Não é possível #Alterar# proposta devido a operação da oportunidade ser visualização."
		lRet := .F.
	ElseIf nOperation == MODEL_OPERATION_DELETE
		Help(,,"FT600VLDACT",, STR0336 + STR0335 + STR0337, 1, 0 ) //"Não é possível #Excluir# proposta devido a operação da oportunidade ser visualização."
		lRet := .F.
	EndIf
EndIf

If	lRet .AND. nOperation <> MODEL_OPERATION_VIEW .AND. nOperation <> MODEL_OPERATION_INSERT .AND. ADY->ADY_STATUS == "F"
	Help( ,, "FT600VLDACT",, STR0213, 1, 0 )	//Proposta Bloqueada!
	lRet := .F.
EndIf

//Inicializa a revisão quando é inclusão
If lRet .AND. nOperation == MODEL_OPERATION_INSERT
	oStructADY := oModel:GetModel("ADYMASTER"):GetStruct()
	oStructADY:SetProperty("ADY_PREVIS",MODEL_FIELD_INIT,FwBuildFeature(STRUCT_FEATURE_INIPAD,"StrZero(1,GetSX3Cache('ADY_PREVIS','X3_TAMANHO'),0)"))	// Revisão
	If Empty(GetSX3Cache("ADY_TABELA","X3_RELACAO"))// Tratativa para respeitar primeiro o Inicializador Padrão da SX3 do campo ADY_TABELA, caso houver conteudo.
		oStructADY:SetProperty("ADY_TABELA",MODEL_FIELD_INIT,FwBuildFeature(STRUCT_FEATURE_INIPAD,"AD1->AD1_TABELA")) // tabela de preços da oportunidade
	EndIf
EndIf

If lRet .AND. nOperation == MODEL_OPERATION_UPDATE
	oModel:GetModel('ADZPRODUTO'):SetOnlyQuery(.T.)
	oModel:GetModel('ADZACESSOR'):SetOnlyQuery(.T.)
EndIf
Return( lRet )

//-------------------------------------------------------------------
/*/{Protheus.doc} ViewDef
Definição do interface

@author luiz.jesus
@since 18/03/2014
@version 12
/*/
//-------------------------------------------------------------------
Static Function ViewDef()

Local oView
Local oModel		:= FwLoadModel("FATA600")
Local oStrADY		:= FWFormStruct(2, 'ADY')
Local oStrADZPro	:= FWFormStruct(2, 'ADZ')
Local oStrADZAce	:= FWFormStruct(2, 'ADZ')
Local oStructFke	:= FWFormViewStruct():New()
Local oStr4			:= FWCalcStruct(oModel:GetModel('CALC'))
Local lMultVist	 	:= SuperGetMv("MV_MULVIST",,.F.)   				// Multipla Vistorias
Local nOpcADJ 		:= SuperGetMv("MV_FATMNTP",,1) 					//Tratamento do dado na ADJ
Local lFT600BUT		:= ExistBlock("FT600BUT") 						//Ponto de entrada para adicionar botoes na EnchoiceBar
Local lFT600APRV	:= ExistBlock("FT600APRV")						//Ponto de entrada para indicar se serao exibidos as funcoes de aprovacao da proposta
Local lPyme			:= IIF(Type("__lPyme") <> "U",__lPyme,.F.)					// Serie 3 do Protheus
Local lAprova		:= .F.                                                                                //Indica se as opções de aprovação devem ser exibidas no Ações Relacionadas
Local aButtons		:= {}
Local oMdlPROD		:= oModel:GetModel('ADZPRODUTO')
Local oMdlACES		:= oModel:GetModel('ADZACESSOR')
Local lGeraOrc		:= SuperGetMv("MV_CRMGORC",,.T.) // O pedido de vendas não será mais gerado de forma automática ao desabilitar o parâmetro. (Inicialmente criado para atender à TDI para não gravar mais CJ e CK, ao desabilitá-lo).
Local lOrcSimp	 	:= SuperGetMV('MV_ORCSIMP',, '2') == '1' .AND. HasOrcSimp()
Local lVldArmazem	:= (SuperGetMv("MV_RESTARM",,"0") <> "0")

//----------------Estrutura para criação do campo-----------------------------
// [01] C Nome do Campo
// [02] C Ordem
// [03] C Titulo do campo
// [04] C Descrição do campo
// [05] A Array com Help
// [06] C Tipo do campo
// [07] C Picture
// [08] B Bloco de Picture Var
// [09] C Consulta F3
// [10] L Indica se o campo é evitável
// [11] C Pasta do campo
// [12] C Agrupamento do campo
// [13] A Lista de valores permitido do campo (Combo)
// [14] N Tamanho Maximo da maior opção do combo
// [15] C Inicializador de Browse
// [16] L Indica se o campo é virtual
// [17] C Picture Variável

oStructFke:AddField("ZYY_PARCE","01",STR0270,STR0270,{STR0270},"C","@!",Nil,Nil,.F.,Nil)	//"Parcela"
oStructFke:AddField("ZYY_VENC","02",STR0271,STR0271,{STR0271},"D",,Nil,Nil,.F.,Nil)	//"Vencimento"
oStructFke:AddField("ZYY_TOTAL","03",STR0272,STR0272,{STR0272},"N",PesqPict("ADZ","ADZ_TOTAL"),Nil,Nil,.F.,Nil)	//"Total"

If lVldArmazem
	oStrADZPro:AddField("ADZ_ARMAZE","15",AllTrim(FWX3Titulo("CK_LOCAL")),"",{GetHlpSoluc("CK_LOCAL")[1]},"C",GetSX3Cache("CK_LOCAL","X3_PICTURE"),/*bPictVar*/,"NNR"/*cLookup*/,/*lCanChange*/,/*cFolder*/,"24"/*cGroup*/,/*aComboValues*/,/*nMaxLenCombo*/,/*cIniBrow*/,.T./*lVirtual*/,/*cPictVar*/,/*lInsertLine*/)
EndIf

oView := FWFormView():New()
oView:SetModel(oModel)

//Adiciona os botões em ações relacionadas
oView:AddUserButton(STR0010,"",{|| A600Servicos(oModel) },,,{MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE}) //"Serviços (PMS)"
oView:AddUserButton(STR0058,"",{|| Ft600Impos() },,,{MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE} )			//"Impostos"
oView:AddUserButton(STR0017,"",{|| A600TimeV() },,,{MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE} )			//"Time de vendas"
If nOpcADJ <> 4
	oView:AddUserButton(STR0291,"",{|| FT600Cat(oModel)},,VK_F4,{MODEL_OPERATION_INSERT,MODEL_OPERATION_VIEW,MODEL_OPERATION_UPDATE})		//'Categoria'
Else
	oView:AddUserButton(STR0319,"",{|| FT600AGrup(oModel),oView:Refresh() },,VK_F4,{MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE})		//'Agrupadores' ### "Informe a tabela de preços"
EndIf

If nModulo == 28

	oStrADY:SetProperty( "ADY_TPCONT", MVC_VIEW_CANCHANGE, .F. )
	oStrADY:SetProperty( "ADY_TPPROD", MVC_VIEW_CANCHANGE, .F. )
	oStrADY:SetProperty( "ADY_DESCON", MVC_VIEW_CANCHANGE, .F. )
	oStrADY:SetProperty( "ADY_LOCAL", MVC_VIEW_CANCHANGE, .F. )

	oView:AddUserButton(STR0208,"",{|oView|At600OrcView()},,,{MODEL_OPERATION_VIEW} ) 							//'Vis. Orcçam. Serv.'
	oView:AddUserButton(STR0194,"",{|oView|AT600COrc()},,,{MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE} ) //'Atualiza Orc. Serviços'
	oView:AddUserButton(STR0195,"",{|oView|At600SeExc()},,,{MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE} ) //'Remover Orc. Serviços'
	//oView:AddUserButton(STR0176,"",{||TECA350(A600LdPROD( oModel ))},,, { MODEL_OPERATION_INSERT, MODEL_OPERATION_UPDATE } ) //"Beneficios"
	//oView:AddUserButton(STR0280,"",{|oView|TECA340(oView,oMdlPROD,oMdlACES,aConfigAlo)},,,{MODEL_OPERATION_INSERT,MODEL_OPERATION_UPDATE} ) //'Conf. de Alocação'

	If !lPyme
		If	!( IsInCallStack("At270VProp") )
			// Se a execução da VIEW não for a partir da rotina de Visualização da Vistoria Técnica, exibe os botões da Vistoria Técnica
			If !lMultVist .And. !lOrcSimp
				oView:AddUserButton(STR0145,"",{|| A600VsVist(/*cCodVis*/,oModel)})	//"Visualizar Vistoria"
				oView:AddUserButton(STR0144,"",{|| TECA280(/*cCodVis*/,oModel)},,,{ MODEL_OPERATION_INSERT, MODEL_OPERATION_UPDATE }) 		//"Vis.Tec x Proposta"
				oView:AddUserButton(STR0146,"",{|| A600ImpVis(/*cCodVis*/,oModel)},,,{ MODEL_OPERATION_INSERT, MODEL_OPERATION_UPDATE }) 					//"Importar Vistoria Tecnica"
			ElseIf !lOrcSimp
				oView:AddUserButton(STR0147,"",{|| A600ConVis(oModel)}) 					//"Vistoria(s) Técnica"
			EndIf
		EndIf
	EndIf

EndIf

If lFT600APRV
	lAprova := ExecBlock("FT600APRV", .F., .F., {oModel})
	If ( ValType(lAprova) <> 'L' )
		lAprova := .F.
	Endif
EndIf

If ( SCJ->CJ_STATUS == 'F' .Or. lAprova ).And. lGeraOrc
   	oView:AddUserButton( STR0015, '', { |oView| Ft600Aprv( .T., oView ) },,,{ MODEL_OPERATION_VIEW } ) //"Aprovar"
    oView:AddUserButton( STR0046, '', { |oView| Ft600Aprv( .F., oView ) },,,{ MODEL_OPERATION_VIEW } ) //"Reprovar"
Endif

oView := CRMXAddAct("ADY",oView) //Adcionar Rotinas no 'Ações relacionadas' do Formulário

If ( lFT600BUT )
	aButtons := ExecBlock( "FT600BUT", .F.,.F., {oView} )
	If ( ValType( aButtons ) == 'A' .And. Len( aButtons ) > 0 )
		AEval( aButtons, { |x| oView:AddUserButton( x[1], x[2], x[3], x[4], x[5], x[6] ) } )
	EndIf
EndIf

aSort(oView:aUserButtons,,,{ | x,y | y[1] > x[1] } )

oView:AddField('FORM1' , oStrADY,'ADYMASTER' )
oView:AddGrid('Produt' , oStrADZPro,'ADZPRODUTO' )
oView:AddGrid('Acess' , oStrADZAce,'ADZACESSOR' )
oView:AddGrid('CROFIN' , oStructFke,'CRONOFIN' )

oView:AddField('FORM9', oStr4,'CALC')

If ( nModulo <> 28 .OR. lPyme .OR. lMultVist )
	oStrADY:RemoveField( 'ADY_CODVIS' )
	oStrADY:RemoveField( 'ADY_SITVIS' )
	oStrADY:RemoveField( 'ADY_VISTEC' )
EndIf

If !( cValToChar( nOpcADJ ) $ "1|4" )
	oStrADY:RemoveField( 'ADY_SINCPR' )
EndIf

oStrADY:RemoveField( 'ADY_HREMIS' )
oStrADY:RemoveField( 'ADY_USREMI' )
oStrADY:RemoveField( 'ADY_DTUPL'  )
oStrADY:RemoveField( 'ADY_HRUPLO' )
oStrADY:RemoveField( 'ADY_USRUPL' )
oStrADY:RemoveField( 'ADY_DTAPRP' )
oStrADY:RemoveField( 'ADY_HRAPRP' )
oStrADY:RemoveField( 'ADY_USRPRP' )
oStrADY:RemoveField( 'ADY_DTPDV'  )
oStrADY:RemoveField( 'ADY_HRPDV'  )
oStrADY:RemoveField( 'ADY_USRPDV' )
oStrADY:RemoveField( 'ADY_DTFAT'  )
oStrADY:RemoveField( 'ADY_HRFAT'  )
oStrADY:RemoveField( 'ADY_USRFAT' )
oStrADY:RemoveField( 'ADY_DTREPR' )
oStrADY:RemoveField( 'ADY_HRREPR' )
oStrADY:RemoveField( 'ADY_USREPR' )
oStrADY:RemoveField( 'ADY_MTREPR' )
oStrADY:RemoveField( 'ADY_OBSREP' )
oStrADY:RemoveField( 'ADY_DTAPRO' )
oStrADY:RemoveField( 'ADY_HRAPRO' )
oStrADY:RemoveField( 'ADY_USAPRO' )
oStrADY:RemoveField( 'ADY_OBSAPR' )
oStrADY:RemoveField( 'ADY_USAPRP' )
oStrADY:RemoveField( 'ADY_USRAPR' )

oStrADZPro:RemoveField("R_E_C_N_O_")
oView:CreateHorizontalBox( 'BOXFORM1', 50)
oView:CreateHorizontalBox( 'MIDDLE', 34)

oView:CreateFolder('ABAS','MIDDLE')  // Pasta que conterá cada uma das guias
oView:AddSheet( 'ABAS', 'ABA01', STR0236 )	//'Produto'
oView:AddSheet( 'ABAS', 'ABA02', STR0237 )	//'Acessorio'
oView:AddSheet( 'ABAS', 'ABA03', STR0238 )	//'Cronograma Financeiro'

oView:CreateHorizontalBox( 'BOXFORM11', 100, /*owner*/, /*lUsePixel*/, 'ABAS', 'ABA03')
oView:SetOwnerView('CROFIN','BOXFORM11')

oView:CreateHorizontalBox( 'ID_ABA01' , 100,,, 'ABAS', 'ABA01' )

oView:CreateHorizontalBox( 'ID_ABA02' , 100,,, 'ABAS', 'ABA02' )
oView:CreateHorizontalBox( 'BOXFORM9', 16)
oView:SetOwnerView('FORM9','BOXFORM9')

oView:SetOwnerView('FORM1','BOXFORM1')

oView:SetOwnerView( 'Produt' , 'ID_ABA01')

oView:SetOwnerView( 'Acess' , 'ID_ABA02')

oView:AddIncrementField('Acess' , 'ADZ_ITEM' )

oView:AddIncrementField('Produt' , 'ADZ_ITEM' )

//----------------+
// MODULO SIGAGTP |
//----------------+
If nModulo == 88 .AND. FindFunction( "GTPX600VIEW" )

	//Atribui os subModelos p/ View (GTP)
	GTPX600View(oView,oStrADY,oStrADZPro)

EndIf

//Refaz cronograma financeiro ao trocar de Aba
oView:SetVldFolder({|| Ft600Folcg()})

oView:ShowInsertMessage(.F.)
oView:ShowUpdateMessage(.F.)
oView:SetCloseOnOk({|| .T.})	//Realizar o fechamento da Proposta após as confirmação
Return oView

//-------------------------------------------------------------------
/*/{Protheus.doc} At600Tipo9
Cria a Tela para preenchimento das parcelas

@author luiz.jesus

@since 18/03/2014
@version 1.0
/*/
//-------------------------------------------------------------------
Function At600Tipo9(cCodTp9,oModel,cFolder,cE4_COND)

Local aArea		:= GetArea()				//Salva a area anterior
Local nOpcA		:= 0						//Opcao de escolha  OK - CANCELA
Local aTipo9		:= {}						//Array com as parcelas
Local nP			:= 0
Local nNAnt		:= N						//Salva o N
Local nOpAux		:= oModel:GetOperation()
Local nOpetarion	:= Iif((nOpAux==3).OR.(nOpAux==4),3,1)
Local cUsado		:= GetSX3Cache("L4_VALOR", "X3_USADO")
Local lAlterCron	:= SuperGetMv("MV_ALTERCR",,.F.)	// Parametro que define se a condição de pagamento parte do ADZ_DT1VEN

//Objetos da Tela
Local oDlgModal	:= Nil
Local oGetTotal	:= Nil
Local oPanelMain	:= Nil
Local oLayMain	:= Nil
Local oDlgLayParc	:= Nil
Local oDlgLayCalc	:= Nil
Local oDlgLayCab	:= NIL
Local oGetProd	:= NIL
Local oGetDProd	:= NIL

//variaveis da tela
Local cProd		:= oModel:GetModel(cFolder):GetValue("ADZ_PRODUT")
Local cDProd		:= oModel:GetModel(cFolder):GetValue("ADZ_DESCRI")

//variaveis compartilhadas
Private nTotal	:= oModel:GetModel(cFolder):GetValue("ADZ_TOTAL") //Valor total do Item
Private nSaldo	:= nTotal
Private nSalPer	:= (nSaldo / nTotal) * 100
Private oGetSaldo
Private oGetParc
Private oGetSalPer

//Variaveis usada na getDados
Private lRefresh
Private aHeader	:= {}
Private aCols		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta cabecalho                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aHeader,{STR0270,;								//01 - X3TITULO()            //"Parcela"
              "TEMP_ITEM",;							//02 - X3_CAMPO
              "@E 999",;							//03 - X3_PICTURE
              3,;									//04 - X3_TAMANHO
              0,;									//05 - X3_DECIMAL
              "NaoVazio()",;						//06 - X3_VALID
              cUsado,;								//07 - X3_USADO
              "C",;									//08 - X3_TIPO
              "",;									//09 - X3_ARQUIVO
              ""})									//10 - X3_CONTEXT

AADD(aHeader,{STR0007,;								//01 - X3TITULO()            //"Valor"
              "L4_VALOR",;							//02 - X3_CAMPO
              GetSX3Cache("L4_VALOR","X3_PICTURE"),;//03 - X3_PICTURE
              10,;									//04 - X3_TAMANHO
              2,;									//05 - X3_DECIMAL
              "Positivo()",;						//06 - X3_VALID
              cUsado,;								//07 - X3_USADO
              "N",;									//08 - X3_TIPO
              "",;									//09 - X3_ARQUIVO
              ""})									//10 - X3_CONTEXT

AADD(aHeader,{STR0274,;								//01 - X3TITULO()		//"Percentual"
              "L4_COMP",;							//02 - X3_CAMPO
              "@E 999.99",; 						//03 - X3_PICTURE
              8,;									//04 - X3_TAMANHO
              2,;									//05 - X3_DECIMAL
              "Positivo()",;						//06 - X3_VALID
              cUsado,;								//07 - X3_USADO
              "N",;									//08 - X3_TIPO
              "",;									//09 - X3_ARQUIVO
              ""})									//10 - X3_CONTEXT

AADD(aHeader,{STR0275,;								//01 - X3TITULO()		//"Data"
              "L4_DATA",;							//02 - X3_CAMPO
              "",;									//03 - X3_PICTURE
              8,;									//04 - X3_TAMANHO
              0,;									//05 - X3_DECIMAL
              "NaoVazio()",;						//06 - X3_VALID
              cUsado,;								//07 - X3_USADO
              "D",;									//08 - X3_TIPO
              "",;									//09 - X3_ARQUIVO
              ""})									//10 - X3_CONTEXT

aTipo9 := Ft600GetTipo09()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se as parcelas devem ser recalculadas  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If	Len(aTipo9) > 0
	For nP := 1 To Len(aTipo9)
		If	Alltrim(aTipo9[nP,5]) == Alltrim( oModel:GetModel(cFolder):GetValue('ADZ_ITEM') ) .And.;
			Alltrim(aTipo9[nP,6]) == Alltrim( oModel:GetModel(cFolder):GetValue('ADZ_FOLDER') )
			aadd(aCols, {StrZero(len(aCols)+1,3),;							//Item
			             aTipo9[nP,3],;										//Valor
			             NoRound( (aTipo9[nP,3] / nTotal) * 100 , 4 ),;	//Percentual
			             aTipo9[nP,2],;										//Data
			             .F.})												//Deletado
		Endif
	Next nP
Endif

If	Len(aCols) == 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o Array com 1 elemento vazio³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols			:= Array(1, Len(aHeader) + 1)
	aCols[1][1]	:= StrZero(1,3)
	aCols[1][2]	:= 0
	aCols[1][3]	:= 0
	aCols[1][4]	:= If(lAlterCron .AND. ! Empty(oModel:GetModel(cFolder):GetValue("ADZ_DT1VEN")), oModel:GetModel(cFolder):GetValue("ADZ_DT1VEN"), dDatabase) ///CTOD("")
	aCols[1][5]	:= .F.		//Linha de controle de DELECAO
EndIf

n	:= Len(aCols)

//Se executado via área de trabalho
If Empty(aRotina)
	aRotina := Menudef()
EndIf

oDlgModal	:= FWDialogModal():New()
oDlgModal:SetTitle(STR0054) //"Valor da Parcela e Vencimento"
oDlgModal:SetBackground(.F.)
oDlgModal:SetEscClose(.F.)
oDlgModal:SetSize(270,248)
oDlgModal:EnableFormBar(.T.)
oDlgModal:SetCloseButton(.F.)
oDlgModal:CreateDialog()
oDlgModal:CreateFormBar()

oDlgModal:AddButton(STR0395,{|| (nOpcA := 1,IIF(At600Tp9Tot(aCols,cCodTp9,oModel,cFolder),oDlgModal:DeActivate(),nOpcA:=0)) },STR0395,,.T.,.F.,.T.)	//"Salvar" ## "Salvar"
oDlgModal:AddButton(STR0396,{|| At600Tp9GP()},STR0396,,.T.,.F.,.T.)																								//"Parcelar" ## "Parcelar"
oDlgModal:AddButton(STR0397,{|| At600Tp9LP()},STR0397,,.T.,.F.,.T.)																								//"Limpar" ## "Limpar"
oDlgModal:AddButton(STR0066,{|| oDlgModal:DeActivate()},STR0066,,.T.,.F.,.T.)																					//"Cancelar" ## "Cancelar"

oPanelMain	:= oDlgModal:GetPanelMain()

oLayMain	:= FWLayer():New()
oLayMain:Init(oPanelMain, .F.)

oLayMain:AddLine("LINE_CABEC",  25, .T.)
oLayMain:AddLine("LINE_PARCEL", 55, .T.)
oLayMain:AddLine("LINE_CALC",   20, .T.)

oLayMain:AddCollumn("COL_CABEC", 100, .T., "LINE_CABEC")
oLayMain:AddCollumn("COL_PARCEL", 100, .T., "LINE_PARCEL")
oLayMain:AddCollumn("COL_CALC",   100, .T., "LINE_CALC")

oLayMain:AddWindow("COL_CABEC",  "WIN_CABEC",  STR0407, 100, .F., .F.,, "LINE_CABEC")	//"Produto"
oLayMain:AddWindow("COL_PARCEL", "WIN_PARCEL", STR0398, 100, .F., .F.,, "LINE_PARCEL")	//Parcelas
oLayMain:AddWindow("COL_CALC",   "WIN_CALC",   STR0272, 100, .F., .F.,, "LINE_CALC")	//Total

oDlgLayCab		:= oLayMain:GetWinPanel("COL_CABEC", "WIN_CABEC", "LINE_CABEC")
@ 04,02 SAY STR0407 SIZE 25,10 PIXEL OF oDlgLayCab  //"Produto"
@ 02,35 MSGET oGetProd  VAR cProd  SIZE 105,10 WHEN .F. PICTURE "@!" PIXEL OF oDlgLayCab
@ 18,02 SAY STR0408 SIZE 25,10 PIXEL OF oDlgLayCab  //"Descrição"
@ 16,35 MSGET oGetDProd VAR cDProd SIZE 200,10 WHEN .F. PICTURE "@!" PIXEL OF oDlgLayCab

oDlgLayParc				:= oLayMain:GetWinPanel("COL_PARCEL", "WIN_PARCEL", "LINE_PARCEL")
oGetParc					:= MSGetDados():New(1,1,105,237,nOpetarion,Nil,"AlwaysTrue","+TEMP_ITEM",.T.,,,.F.,999,"At600Tp9FOk",,,,oDlgLayParc)
oGetParc:oBrowse:bDelete	:= {|| aCols[n,Len(aCols[n])]	:= !aCols[n,Len(aCols[n])], At600Tp9Rf(), oGetParc:oBrowse:Refresh(.F.)}
oGetParc:Refresh(.T.)

oDlgLayCalc	:= oLayMain:GetWinPanel( "COL_CALC", "WIN_CALC", "LINE_CALC" )
@ 05,05 SAY STR0392 SIZE 20,10 PIXEL OF oDlgLayCalc  //Saldo:
@ 03,25 MSGET oGetSaldo VAR nSaldo SIZE 54,10 WHEN .F. PICTURE "@E 999,999,999.99" PIXEL  OF oDlgLayCalc

@ 05,85 SAY STR0393 SIZE 23,10 PIXEL OF oDlgLayCalc //Saldo %:
@ 03,110 MSGET oGetSalPer VAR nSalPer SIZE 50,10 WHEN .F. PICTURE "@E 999.9999" PIXEL  OF oDlgLayCalc

@ 05,165 SAY STR0394 SIZE 20,10 PIXEL OF oDlgLayCalc //Total:
@ 03,180 MSGET oGetTotal VAR nTotal SIZE 54,10 WHEN .F. PICTURE "@E 999,999,999.99" PIXEL  OF oDlgLayCalc

At600Tp9Rf()
oDlgModal:Activate()

N := nNAnt

RestArea(aArea)
Return(nOpcA == 1)

//------------------------------------------------------------------------------
/*/{Protheus.doc} At600Tp9GP
@description	Funcao Gera Parcelas da tela em At600Tipo9.
@sample		At600Tp9GP()
@author		CRM e Serviços
@since		11/07/2017
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function At600Tp9GP()

Local oDialog 		:= Nil
Local oPanel			:= Nil
Local cQtdParc		:= StrZero(1,3)
Local dVenc			:= dDatabase
Local nI				:= 0
Local nX				:= 0
Local nVlrParcela		:= 0
Local nDiaVenc		:= 0
Local nMesVenc		:= 0
Local nAnoVenc		:= 0
Local nTtParcelado	:= 0
Local nQtdParc		:= 0
Local nParcMan		:= 0
Local lContinua		:= .F.
Local aDelet			:= {}
Local nLimiteParc		:= SuperGetMv("MV_NUMPARC",,999)
Local bConfirmar		:= {|| Iif(val(cQtdParc)>nLimiteParc .Or. val(cQtdParc)==0,;
                                Help( ,, 'A600TP9GP',, STR0399 + Str(nLimiteParc), 1, 0 )  ,; //"O numero de parcelas deve ser maior que 0 e menor que "
                                (lContinua := .T., oDialog:DeActivate()) )  }
Local nColItm			:= aScan(aHeader,{|x|Alltrim(x[2])=="TEMP_ITEM" }) //coluna de valores
Local nColVlr			:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_VALOR" }) //coluna de valores
Local nColPer			:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_COMP" }) //coluna de valores
Local nColData		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_DATA" }) //coluna de valores
Local nColDel			:= Len(aHeader) +1 //coluna de delecao

//-------------------------------------------------------------------
// valida Saldo
//-------------------------------------------------------------------
If nSaldo <= 0
	Help(,1,"A600TP9GP",,STR0400,1) //"Não há saldo para parcelar"
	Return
EndIf

//-------------------------------------------------------------------
// Monta o janela pesquisa.
//-------------------------------------------------------------------
oDialog := FWDialogModal():New()
oDialog:SetTitle( STR0401 )    //"Gerar Parcelas"
oDialog:SetBackGround( .F. )
oDialog:SetEscClose( .T. )
oDialog:SetSize( 100, 150 )
oDialog:EnableFormBar( .T. )
oDialog:SetCloseButton(.F.)
oDialog:CreateDialog()
oDialog:CreateFormBar()
oDialog:AddButton( STR0065, bConfirmar, STR0065 , , .T., .F., .T., )			//"OK"
oDialog:AddButton( STR0066,{|| oDialog:DeActivate()  },STR0066,,.T.,.F.,.T.)	//"Cancelar"

//-------------------------------------------------------------------
// Recupera o container.
//-------------------------------------------------------------------
oPanel := oDialog:GetPanelMain()

//-------------------------------------------------------------------
// Insere as opções de tela.
//-------------------------------------------------------------------
@ 15,05 SAY STR0402  SIZE 70,10 PIXEL	OF oPanel  //"Quantidade de Parcelas:"
@ 25,05 MSGET cQtdParc  SIZE 60,10 PICTURE "@999999" PIXEL HASBUTTON OF oPanel
@ 15,85 SAY STR0403  SIZE 70,10 PIXEL OF oPanel  //"Primeiro Vencimento:"
@ 25,85 MSGET dVenc  SIZE 60,10 PIXEL HASBUTTON OF oPanel

//-------------------------------------------------------------------
// Exibe a janela.
//-------------------------------------------------------------------
oDialog:Activate()

If lContinua

	nQtdParc		:= Val(cQtdParc)
	nVlrParcela	:= NoRound( nSaldo / nQtdParc , 2)

	nDiaVenc		:= Day(dVenc)
	nMesVenc		:= Month(dVenc)
	nAnoVenc		:= Year(dVenc)

	//-------------------------------------------------------------------
	// Retira parcelas invalidas inseridas manualmente
	//-------------------------------------------------------------------
	For nI := 1 to len(aCols)
		If aCols[nI][nColDel] .Or. aCols[nI][nColVlr] <= 0
			aAdd( aDelet , aCols[nI][nColItm] )
		EndIf
	Next nI

	For nI := 1 to len(aDelet)
		Adel(aCols, aScan(aCols,{|x|Alltrim(x[nColItm])==aDelet[nI] }) )
		ASize(aCols,Len(aCols)-1)
	Next nI

	//-------------------------------------------------------------------
	// Geracao das parcelas
	//-------------------------------------------------------------------
	nQtdParc += nParcMan := Len(aCols)
	For nI := 1 to nQtdParc

		//-------------------------------------------------------------------
		// Nao altera parcelas inseridas manualmente
		//-------------------------------------------------------------------
		If nParcMan >= nI
			aCols[nI][nColItm] := StrZero(nI,3)
			Loop
		EndIf

		//-------------------------------------------------------------------
		// Nao incrementa data na primeira parcela automatica
		//-------------------------------------------------------------------
		If nI > (1 + nParcMan)
			If nMesVenc == 12
				nMesVenc := 1
				nAnoVenc++
			Else
				nMesVenc++
			EndIf
		EndIf

		//-------------------------------------------------------------------
		// Calcula diferenca para a ultima parcela
		//-------------------------------------------------------------------
		If nI == nQtdParc
			nTtParcelado := 0
			For nX := 1 to len(aCols)
				If !aCols[nX][nColDel]
					nTtParcelado += aCols[nX][nColVlr]
				EndIf
			Next nX

			nVlrParcela := nTotal - nTtParcelado
		EndIf

		//-------------------------------------------------------------------
		// Grava parcela
		//-------------------------------------------------------------------
		ASize(aCols,nI)
		aCols[nI] := Array(5)
		aCols[nI][nColItm] := StrZero(nI,3)
		aCols[nI][nColVlr] := nVlrParcela
		aCols[nI][nColPer] := NoRound( (nVlrParcela / nTotal) * 100 , 4 )
		aCols[nI][nColData] := STOD(StrZero(nAnoVenc,4) + StrZero(nMesVenc,2) + StrZero(nDiaVenc,2))
		aCols[nI][nColDel] := .F.			//Linha de controle de DELECAO

	Next nI

	//-------------------------------------------------------------------
	// Atualiza Saldo na tela
	//-------------------------------------------------------------------
	nTtParcelado := 0
	For nX := 1 to len(aCols)
		If !aCols[nX][nColDel]
			nTtParcelado += aCols[nX][nColVlr]
		EndIf
	Next nX

	nSaldo := nTotal - nTtParcelado
	oGetSaldo:Refresh()

	nSalPer := ( nSaldo / nTotal) * 100
	oGetSalPer:Refresh()

EndIf

Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} At600Tp9LP
Funcao Limpa Parcelas da tela em At600Tipo9.
@sample		At600Tp9LP()
@author		CRM e Serviços
@since		11/07/2017
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function At600Tp9LP()

Local nColItm		:= aScan(aHeader,{|x|Alltrim(x[2])=="TEMP_ITEM" }) //coluna de valores
Local nColVlr		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_VALOR" }) //coluna de valores
Local nColPer		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_COMP" }) //coluna de valores
Local nColData	:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_DATA" }) //coluna de valores
Local nColDel		:= Len(aHeader) +1 //coluna de delecao

//-------------------------------------------------------------------
// Limpa objeto da GetDados
//-------------------------------------------------------------------
aCols					:= {}
aCols					:= Array(1, Len(aHeader) + 1)
aCols[1][nColItm]		:= StrZero(1,3)
aCols[1][nColVlr]		:= 0
aCols[1][nColPer]		:= 0
aCols[1][nColData]	:= dDatabase
aCols[1][nColDel]		:= .F.			//Linha de controle de DELECAO

//-------------------------------------------------------------------
// Atualiza Saldo na tela
//-------------------------------------------------------------------
nSaldo := nTotal
oGetSaldo:Refresh()

nSalPer := ( nSaldo / nTotal) * 100
oGetSalPer:Refresh()
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} At600Tp9Rf
Funcao Refresh dos objetos de tela em At600Tipo9.
@sample		At600Tp9Rf()
@author		CRM e Serviços
@since		11/07/2017
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function At600Tp9Rf()

Local aColsParc	:= aClone(aCols)
Local nSomaItens	:= 0
Local nI			:= 0
Local nColItm		:= aScan(aHeader,{|x|Alltrim(x[2])=="TEMP_ITEM"}) //coluna de valores
Local nColVlr		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_VALOR"}) //coluna de valores
Local nColPer		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_COMP"}) //coluna de valores
Local nColData	:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_DATA"}) //coluna de valores
Local nColDel		:= Len(aHeader) +1 //coluna de delecao

//-------------------------------------------------------------------
// Soma dos itens, ignora itens deletados
//-------------------------------------------------------------------
For nI := 1 to len(aColsParc)
	If !aColsParc[nI][nColDel]
		nSomaItens += aColsParc[nI][nColVlr]
	EndIf
Next nI

//-------------------------------------------------------------------
// Atualiza Saldo na tela
//-------------------------------------------------------------------
nSaldo := nTotal - nSomaItens
oGetSaldo:Refresh()

nSalPer := ( nSaldo / nTotal) * 100
oGetSalPer:Refresh()
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} At600Tp9FOk
Funcao field ok do getDados em At600Tipo9.
@sample		At600Tp9FOk()
@author		CRM e Serviços
@since		11/07/2017
@version	P12
/*/
//------------------------------------------------------------------------------
Function At600Tp9FOk()

Local nLinPosNow	:= oGetParc:oBrowse:nAt //linha alterada
Local nColPosNow	:= oGetParc:oBrowse:nColPos  //coluna alterada
Local nSomaItens	:= 0
Local nI			:= 0
Local nVlrNew		:= &(ReadVar()) //novo valor do campo alterado

Local nColItm		:= aScan(aHeader,{|x|Alltrim(x[2])=="TEMP_ITEM" }) //coluna de valores
Local nColVlr		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_VALOR" }) //coluna de valores
Local nColPer		:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_COMP" }) //coluna de valores
Local nColData	:= aScan(aHeader,{|x|Alltrim(x[2])=="L4_DATA" }) //coluna de valores
Local nColDel		:= Len(aHeader) +1 //coluna de delecao

//-------------------------------------------------------------------
// Atualiza valores na aCols
//-------------------------------------------------------------------
If nColPosNow == nColVlr
	aCols[nLinPosNow][nColVlr] := nVlrNew
	aCols[nLinPosNow][nColPer] := NoRound( (nVlrNew / nTotal) * 100 , 4 )
ElseIf nColPosNow == nColPer
	aCols[nLinPosNow][nColVlr] :=  NoRound( ( nVlrNew / 100 ) * nTotal , 2)
EndIf

//-------------------------------------------------------------------
// Soma dos itens, ignora itens deletados
//-------------------------------------------------------------------
For nI:=1 to len(aCols)
	If !aCols[nI][nColDel]
		nSomaItens += aCols[nI][nColVlr]
	EndIf
Next nI

//-------------------------------------------------------------------
// Atualiza Saldo na tela
//-------------------------------------------------------------------
nSaldo := nTotal - nSomaItens
oGetSaldo:Refresh()

nSalPer := ( nSaldo / nTotal) * 100
oGetSalPer:Refresh()
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} A600CompPr

Função que faz a comparação das propostas.

@sample		A600CompPr()

@author		CRM e Serviços
@since		29/04/2013
@version	P12
/*/
//------------------------------------------------------------------------------
Function A600CompPr()

Local aArea		:= GetArea()
Local aAreaADY	:= ADY->(GetArea())
Local oDlgProp	:= Nil
Local aListBox	:= {}
Local aRet     	:= {}
Local oChkMar  	:= Nil
Local oLbx     	:= Nil
Local oDlg			:= Nil
Local oOk      	:= LoadBitmap(GetResources(), 'LBOK')
Local oNo      	:= LoadBitmap(GetResources(), 'LBNO')
Local lChk     	:= .F.
Local lTeveMarc	:= .F.
Local cVar     	:= ""
Local oMdlOpor	:= FT600MdlOport()
Local oMdlAD1		:= oMdlOpor:GetModel("AD1MASTER")
Local lRetorno 	:= .T.
Local cFilADY		:= xFilial("ADY")

DbSelectArea("ADY")
ADY->(DbSetOrder(2))	//ADY_FILIAL+ADY_OPORTU+ADY_REVISA+ADY_PROPOS
If ADY->(DbSeek(cFilADY+oMdlAD1:GetValue("AD1_NROPOR")))
	While ( ADY->(!Eof()) .AND. ADY->ADY_FILIAL == cFilADY .AND. ADY->ADY_OPORTU == oMdlAD1:GetValue("AD1_NROPOR") )
		aAdd(aListBox,{IIF(ADY->ADY_SINCPR,.T.,.F.),ADY->ADY_PROPOS,dToc(ADY->ADY_DATA)})
		ADY->(DbSkip())
	EndDo
EndIf

If Len(aListBox) > 0

	aSort(aListBox,,,{ |x,y| x[2] < y[2] } )

	DEFINE FONT oBold NAME "Arial" SIZE 0, -12 BOLD
	DEFINE MSDIALOG  oDlgProp Title STR0128 From 0, 0 To 255, 450 Pixel
	oDlgProp:cToolTip := STR0244 //"Tela para Seleção de Propostas"
	oDlgProp:cTitle := STR0245	//"Selecione as Propostas Comerciais para comparação"
	@ 10, 10 Listbox oLbx Var cVar Fields Header ' ', STR0246, STR0247 Size 210, 095 Of oDlg Pixel //"Proposta"	"Data de Emissão"
	oLbx:SetArray(aListBox)
	oLbx:bLine := {|| {IIf( aListBox[oLbx:nAt, 1], oOk, oNo ),;
	                   aListBox[oLbx:nAt, 2], ;
	                   aListBox[oLbx:nAt, 3]}}
	oLbx:BlDblClick := {|| aListBox[oLbx:nAt, 1] := !aListBox[oLbx:nAt, 1], Ft600VerTodos( aListBox, @lChk, oChkMar ), oChkMar:Refresh(), oLbx:Refresh()}
	oLbx:cToolTip   := oDlgProp:cTitle
	oLbx:lHScroll   := .F. // NoScroll
	@ 112, 10 CheckBox oChkMar Var  lChk Prompt STR0248 Message STR0249 Size 40, 007 Pixel Of oDlgProp; //'Todos'###'Marca / Desmarca Todos'
	                                     on Click Ft600Marca(lChk, @aListBox, oLbx)
	DEFINE SButton From 111, 155 Type 1 Action ( IIf(FT600RetProp(@aRet, aListBox),(Ft600CR("ADY",aListBox[oLbx:nAt,2],aRet),oDlgProp:End()), MsgStop(STR0253)) ) OnStop STR0250 Enable Of oDlgProp //'Confirma a Seleção' "Não há Propostas Marcadas."
	DEFINE SButton From 111, 188 Type 2 Action ( IIf(lTeveMarc, aRet := aMarcadas, .T.), oDlgProp:End() )                                                         OnStop STR0251 Enable Of oDlgProp //'Abandona a Seleção'
	ACTIVATE MSDIALOG oDlgProp CENTERED

Else
	lRetorno := .F.
	Aviso(STR0241,STR0242,{STR0243})	//"Atençao"##"Não há Propostas a serem comparadas!"##"Ok"
EndIf

RestArea(aAreaADY)
RestArea(aArea)
Return(lRetorno)

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600CR

Programa de comparacao das revisoes da Proposta Comercial.

@sample  Ft600CR(cCodVSup)

@Param   cCodVSup - Codigo do supervisor

@return  lAchou -  Variavel lógica com flag informando se achou ou não

@author  Serviços/CRM
@since   29/04/2014
@version P12
/*/
//-----------------------------------------------------------------------------------------

Function Ft600CR(cAlias,cPropos,aPropos)

Local aLstHeader	:= {}
Local aLstCols	:= {}
Local cLine		:= "{||}"
Local aCmp			:= {}
Local cVar     	:= ""
Local nOpca		:= 0
Local oDlg
Local oBold
Local oFont1
Local oBmp
Local oList
Local oOk			:= LoadBitmap( GetResources(), "LBOK" )
Local oNo			:= LoadBitmap( GetResources(), "LBNO" )
Local lRet			:= .T.

Default cAlias 	:= "ADY"
Default cPropos	:= ""
Default aPropos	:= {}

DbSelectArea(cAlias)
DbSetOrder(1)

If !Empty(cPropos)
	Dbseek(xFilial(cAlias) + cPropos)
EndIf

lRet := Ft600Rev((cAlias)->ADY_PROPOS, @aLstHeader, @aLstCols, @cLine,(cAlias)->ADY_OPORTU,aPropos)

If Len(aLstCols) > 1

	DEFINE DIALOG oDlg TITLE STR0063 FROM 0,-300 TO 470,600 OF oMainWnd PIXEL //"Comparador das revisões"

	DEFINE FONT oBold 	NAME "Arial"   		SIZE 0, -13 BOLD
	DEFINE FONT oFont1	NAME "Courier New" 	SIZE 7,15
	@  0, 0  BITMAP oBmp RESNAME "PROJETOAP" oF oDlg SIZE 30, 1000 NOBORDER WHEN .F. PIXEL ADJUST
	@ 03, 40 SAY STR0064 FONT oBold PIXEL //"Selecione as revisões que deseja comparar"
	@ 14, 30 TO 16 ,450 LABEL '' OF oDlg   PIXEL
	@ 40, 30 LISTBOX oList VAR cVar Fields SIZE 420, 170 ON DBLCLICK (Ft600Click(@aLstCols,oList:nAt)) OF oDlg PIXEL FONT oFont1
	oList:aHeaders := aLstHeader
	oList:SetArray(aLstCols)
	oList:bLine := &(cLine)
	oList:nFreeze := 1
	@ 22 , 412 BUTTON STR0023  SIZE 039, 013 OF oDlg PIXEL Action(Ft600Vis(aLstCols,oList:nAt))//"Visualizar"
	@ 218, 369 BUTTON STR0065  SIZE 039, 013 OF oDlg PIXEL Action(iIf(Ft600ROk(aLstCols,@aCmp),(nOpca := 1,oDlg:End()),)) //"OK"
	@ 218, 412 BUTTON STR0066  SIZE 039, 013 OF oDlg PIXEL Action( nOpca := 0, oDlg:End()) //"Cancelar"

	ACTIVATE MSDIALOG oDlg CENTERED

	If nOpca == 1
		Processa({||Ft600Cmp(aCmp)}, STR0067, STR0068,.F.)	//"Processando"###"Comparando as revisões da Proposta comercial..."
	EndIf
Else
	Aviso(STR0069,STR0070,{STR0065})	//Proposta selecionada não possui revisões!
	lRet := .F.
EndIf

Return lRet

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600Cmp

Programa de comparacao das revisoes da Proposta Comercial.

@sample  Ft600Cmp

@author  Serviços/CRM
@since   29/04/2014
@version P12
/*/
//-----------------------------------------------------------------------------------------

Static Function Ft600Cmp(aCmp)

Local oDlg
Local oTree
Local oTree2
Local oMenu
Local oMenu2
Local aOpVComp := {}
Local aOrigem  := {}
Local aDestino := {}
Local aButtons := {}
Local aObjects := {}
Local aPosObj  := {}
Local aInfo    := {}
Local aSize    := MsAdvSize(.T.)

/*
ESTRUTURA DO RETORNO DA Ft600Tree
[1] - Alias
[2] - Chave
[3] - Descricao
[4] - Cargo
[5] - Cargo Pai
[6] - Tipo Diferenca (N - Normal, C - Change, E - Deleted, I - Inserted)
[7] - E Recurso ? (Truee,False)
[8] - Indica tipo do produto(1-Produto, 2-Acessorio)
*/

//Monta um array com a estrutura do tree de oportunidade de venda
//que sera utilizado como base na comparacao.
aOrigem := Ft600Tree(aCmp[1])

//Monta um array com a estrutura do tree de oportunidade de venda
//que sera utilizado como base na comparacao.
aDestino:= Ft600Tree(aCmp[2])


//Monta um array com a estrutura do tree de oportunidade de venda
//da comparacao entre as versoes.
aOpVComp:= Ft600Compara(aOrigem,aDestino)

//Monta a  tela com o tree da versao base e com o tree da versao
//resultado da comparacao.
aAdd( aObjects, { 100, 100, .T., .T., .F. } )
aAdd( aObjects, { 100, 100, .T., .T., .F. } )
aInfo  := { aSize[1],aSize[2],aSize[3],aSize[4],3,3 }
aPosObj:= MsObjSize( aInfo, aObjects, .T.,.T. )

DEFINE MSDIALOG oDlg TITLE STR0254 FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL	//"Comparação das Revisões"

	MENU oMenu POPUP
		MENUITEM STR0023 ACTION Ft600VisDet(@oTree,aOrigem)	//"Visualizar"
	ENDMENU

	MENU oMenu2 POPUP
		MENUITEM STR0023 ACTION Ft600VisDet(@oTree2,aOpVComp)	//"Visualizar"
		MENUITEM STR0255 ACTION Ft600Item(oTree,oTree2,aOrigem,aOpVComp,aCmp[1],aCmp[2])	//"Comparar"
	ENDMENU

	oTree:= dbTree():New(aPosObj[1,1], aPosObj[1,2],aPosObj[1,3],aPosObj[1,4], oDlg,,,.T.)
	oTree:bRClicked := {|o,x,y|  oMenu:Activate(x-100,y-200,oTree) } // Posição x,y em relação a Dialog
	oTree:bChange   := {|| Ft600CtrMenu(1,oMenu,oTree)}
	oTree:lShowHint := .F.
	Ft600MontaTree(@oTree,aOrigem)

	oTree2:= dbTree():New(aPosObj[2,1], aPosObj[2,2],aPosObj[2,3],aPosObj[2,4], oDlg,,,.T.)
	oTree2:bRClicked := {|o,x,y|  oMenu2:Activate(x-800,y-200,oTree2) } // Posição x,y em relação a Dialog
	oTree2:bChange   := {|| Ft600CtrMenu(2,oMenu2,oTree2)}
	oTree2:lShowHint := .F.
	Ft600MontaTree(@oTree2,aOpVComp)

	AAdd(aButtons, {"DBG09",       {|| Ft600Inf()}, STR0256})	//"Legenda"
   	AAdd(aButtons, {"PMSSETADOWN", {|| Ft300Nav(1,aOpVComp,@oTree,@oTree2)}, STR0257, STR0257})	//"Proxima Diferenca"
   	AAdd(aButtons, {"PMSSETAUP",   {|| Ft300Nav(2,aOpVComp,@oTree,@oTree2)}, STR0258, STR0258})	//"Diferenca Anterior"

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg, {||oDlg:End()} ,{||oDlg:End()},,aButtons)

Return Nil

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} A600DesbP

Interface de desbloqueio de proposta

@sample     A600DesbP(  )

@param		 ExpC - Codigo do Vendedor superior

@return     ExpL: Verdadeiro

@author     Victor Bitencourt
@since      08/09/2014
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function A600DesbP(cCodVSup)

Local oGrpProp      		//Objeto Grupo Box
Local oGrpPesq	   			//Objeto Grupo Box
Local oBtn1         		//Objeto Button Pesquisar
Local oBtn2    				//Objeto Button Proximo
Local oBtn3    				//Objeto Button Visualizar
Local oBtn4   				//Objeto Button Aprovar
Local oBtn5    				//Objeto Button Sair
Local oBtn6                 //Objeto Button Reprovar
Local oBtn7   				//Objeto Button Sair
Local oGet1    				//Objeto Get
Local oPanel1  				//Objeto Panel
Local oPanel2 				//Objeto Panel
Local oBrwPBlq 			    //Objeto Browse Proposta bloqueadas
Local cGet1 := Space(60)    //Espaco na caixa de texto
Local aPropBlq := A600BProp(cCodVSup)
Static oDlg 				//Objeto MSDialog Proposta(s) bloqueada(s)

If Len(aPropBlq) > 0

	DEFINE MSDIALOG oDlg TITLE STR0088 FROM 000, 000  TO 340,900 PIXEL  //Proposta(s) Bloqueada(s)

	oPanel1 := TPanel():New(00,00,/*cText*/,oDlg,/*oFont*/,/*lCentered*/,;
	                        /*uParam7*/,/*nClrText*/,/*nClrBack*/,;
	                        350,497,/*lLowered*/,/*lRaised*/)

	oPanel2 := TPanel():New(00,00,/*cText*/,oDlg,/*oFont*/,/*lCentered*/,;
	                        /*uParam7*/,/*nClrText*/,/*nClrBack*/,;
	                        350,026,/*lLowered*/,/*lRaised*/)

	@ 001, 001 GROUP oGrpProp TO 143, 400 PROMPT STR0089 OF oPanel1 PIXEL  //"Proposta bloqueadas criadas pelos vendedores em sua célula de vendas"
	@ 000, 001 GROUP oGrpPesq TO 024, 200 PROMPT STR0090 OF oPanel2 PIXEL //"Pesquisa"
	@ 008, 004 MSGET oGet1 VAR cGet1 SIZE 093, 010 OF oPanel2 PIXEL
	@ 008, 108 BUTTON oBtn1 PROMPT STR0091 SIZE 037, 012 OF oPanel2 PIXEL ACTION IIF(A600Pesq(oBrwPBlq,cGet1, .T. ),oGet1:SetColor(CLR_BLACK,CLR_WHITE),oGet1:SetColor(CLR_WHITE,CLR_HRED))//"&Pesquisar"
	@ 008, 156 BUTTON oBtn2 PROMPT STR0092 SIZE 037, 012 OF oPanel2 PIXEL ACTION IIF(A600Pesq(oBrwPBlq,cGet1, .F. ),oGet1:SetColor(CLR_BLACK,CLR_WHITE),oGet1:SetColor(CLR_WHITE,CLR_HRED))//"Proximo"
	@ 008, 214 BUTTON oBtn3 PROMPT STR0093 SIZE 037, 012 OF oPanel2 PIXEL ACTION VisualProp(oBrwPBlq:aArray[oBrwPBlq:nAt,5])//"&Visualizar"
	@ 008, 262 BUTTON oBtn4 PROMPT STR0125 SIZE 037, 012 OF oPanel2 PIXEL ACTION IIF(A600RProp(oBrwPBlq,cCodVSup),oDlg:End(),Nil) //"Atualizar"
	@ 008, 310 BUTTON oBtn5 PROMPT STR0094 SIZE 037, 012 OF oPanel2 PIXEL ACTION IIF(A600AProp(oBrwPBlq),oDlg:End(),Nil)//"Aprovar"
	@ 008, 358 BUTTON oBtn6 PROMPT STR0046 SIZE 037, 012 OF oPanel2 PIXEL ACTION IIF(A600Reprov(oBrwPBlq),IIF(A600RProp(oBrwPBlq,cCodVSup),oDlg:End(),Nil) ,Nil)//"Reprovar"
	@ 008, 406 BUTTON oBtn7 PROMPT STR0097 SIZE 037, 012 OF oPanel2 PIXEL ACTION oDlg:End()//"Sair"
	oBrwPBlq := FWBrwProp(oPanel1,aPropBlq) 

	// Don't change the Align Order
	oPanel1:Align := CONTROL_ALIGN_ALLCLIENT
	oPanel2:Align := CONTROL_ALIGN_BOTTOM

	ACTIVATE MSDIALOG oDlg CENTERED

Else
	AVISO(STR0096,STR0098,{STR0099},1)  //"Não há proposta(s) bloqueada(s) para aprovação"//"Fechar"
EndIf

Return(.T.)

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} FwBrwProp

Browse para marcacao da(s) proposta(s) bloqueada(s)

@sample     FwBrwProp(  )

@param		 ExpO1 - Objeto oPanel1
@param		 ExpA2 - Proposta(s) bloqueada(s)

@return     ExpL: Verdadeiro/Falso

@author     Victor Bitencourt
@since      08/09/2014
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Static Function FwBrwProp(oPanel1,aPropBlq)

Local oBrowse   						   					//Objeto Browse
Local aBrowse  	:= aPropBlq        					  	//Array com a(s) proposta(s) bloqueada(s)
Local nRow 		:= 09  		   						  	//coordenada vertical.
Local nCol      	:= 03 	   		   						//coordenada horizontal.
Local nWidth    	:= 392			   						//largura em pixels do objeto.
Local nHeight   	:= 130		  	    	 				//altura em pixels do objeto.
Local aHeaders  	:={STR0100,STR0101,STR0102,STR0103}    //título dos campos no cabeçalho. ##"Proposta"#"Data"#"Entidade"#"Nome"##
Local aColSizes 	:={30,30,35,142}           				// largura das colunas.
Local bLDblClick 	:= {|| }								//bloco de codigo que será executado quando clicar duas vezes no objeto.
Local uParam20 	:= .F.    								//Compatibilidade.
Local lPixel    	:=.T.    							    //Considera as coordenadas passadas em pixels.
Local uParam24  	:=.F.    							    //Compatibilidade.
Local lNCliObf		:= .F.


oBrowse := TCBrowse():New(nRow,nCol,nWidth,nHeight,/*bLine*/,aHeaders,aColSizes,;
                          oPanel1,/*cField*/,/*uValue1*/,/*uValue2*/,/*bChange*/,bLDblClick,;
                          /*bRClick*/,/*oFont*/,/*oCursor*/,/*nClrFore*/,/*nClrBack*/,/*cMsg*/,;
                          uParam20,/*cAlias*/,lPixel,/*bWhen*/,uParam24,/*bValid*/,/*lHScroll*/,/*lVScroll*/)

oBrowse:SetArray(aBrowse)
oBrowse:bLine := {|| {;
aBrowse[oBrowse:nAt,1],;
aBrowse[oBrowse:nAt,2],;
aBrowse[oBrowse:nAt,3],;
aBrowse[oBrowse:nAt,4];
}}

If FATPDActive() .And. FTPDUse(.T.)
	lNCliObf := FATPDIsObfuscate("A1_NOME",Nil,.T.) .Or. FATPDIsObfuscate("US_NOME",Nil,.T.)  
	oBrowse:aObfuscatedCols := {.F.,.F.,.F.,lNCliObf}    
EndIf 


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Troca a imagem no duplo click do mouse.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oBrowse:bLDblClick := {||VisualProp(aBrowse[oBrowse:nAt,5]),oBrowse:DrawSelect()}

// Scroll type
oBrowse:nScrollType := 0

Return(oBrowse)

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} A600TimeV

Exibicao do time de vendas associado a oportunidade da proposta atual.

@sample  A600TimeV

@author  Serviços/CRM
@since   29/04/2014
@version P12
/*/
//-----------------------------------------------------------------------------------------

Static Function A600TimeV()

Local aArea		:= GetArea()							// Armazena a area atual
Local aAreaSA3		:= SA3->(GetArea())					// Armazena o posicionamento do SA3
Local aAreaAD2		:= AD2->(GetArea())					// Armazena o posicionamento do AD2
Local aAreaSUM		:= SUM->(GetArea())					// Armazena o posicionamento do SUM
Local aVend		:= {}									// Lista de vendedores
Local oMdlOpor	:= FT600MdlOport()
Local oMdlAD1 		:= oMdlOpor:GetModel("AD1MASTER")
Local cOportun		:= oMdlAD1:GetValue("AD1_NROPOR")
Local cRevisa	 	:= oMdlAD1:GetValue("AD1_REVISA")
Local cFilAD2		:= xFilial("AD2")						// Filial da tabela AD2
Local cFilSA3		:= xFilial("SA3")						// Filial da tabela SA3
Local cFilSUM		:= xFilial("SUM")						// Filial da tabela SUM
Local oDlg			:= Nil  								// Objeto da tela
Local oLBox			:= Nil									// Objeto da Listbox
Local lNVendObf		:= .F.
Local lCargoObf		:= .F.

Default cRevisa := ""

//Seleciona o indice utilizado para cada tabela
DbSelectArea("SA3")
DbSetOrder(1)	//A3_FILIAL+A3_COD

DbSelectArea("SUM")
DbSetOrder(1)	//A3_FILIAL+A3_COD

DbSelectArea("AD2")
DbSetOrder(1)	//AD2_FILIAL+AD2_NROPOR+AD2_REVISA+AD2_VEND
DbSeek(cFilAD2+cOportun+cRevisa)

	//Percorre o time de vendas da oportunidade associada
	While !AD2->(Eof()) 			.AND.;
		AD2->AD2_FILIAL	== cFilAD2	.AND.;
		AD2->AD2_NROPOR	== cOportun	.AND.;
		AD2->AD2_REVISA	== cRevisa

		SA3->(DbSeek(cFilSA3+AD2->AD2_VEND))	//Posiciona Vendedor
		SUM->(DbSeek(cFilSUM+SA3->A3_CARGO))	//Localiza o cargo do vendedor


		//Armazena os dados
		AAdd(aVend,{AD2->AD2_VEND,AllTrim(Capital(SA3->A3_NOME)),AllTrim(Capital(SUM->UM_DESC))})

		AD2->(DbSkip())

	End

	//Se encontrou vendedores para a oportunidade, monta a tela
	//com a listbox para exibicao do time
	If Len(aVend) > 0

		DEFINE MSDIALOG oDlg TITLE STR0017 + " - " + STR0042 + cOportun FROM 0,0 TO 300,665 PIXEL  //"Time de vendas"###"Oportunidade:"

		//Label
		@ 03,03 TO 145,295 LABEL STR0017 PIXEL OF oDlg

		//Listbox
		@ 12,08 ListBox oLBox Fields HEADER STR0043,STR0044,STR0045;  //"Codigo"###"Nome"###"Cargo"
		Size 280,126 Of oDlg Pixel

		//Atribuicao do conteudo da listbox
		oLBox:SetArray(aVend)
		oLBox:bLine := {||{	aVend[oLBox:nAt,01],;
					 		aVend[oLBox:nAT,02],;
							aVend[oLBox:nAT,03]}}
		If FATPDActive() .And. FTPDUse(.T.)
			lNVendObf	:= FATPDIsObfuscate("A3_NOME",Nil,.T.)
			lCargoObf	:= FATPDIsObfuscate("UM_DESC",Nil,.T.)
			oLBox:aObfuscatedCols := {.F.,lNVendObf,lCargoObf}  
		EndIf 

		//Botoes
		DEFINE SBUTTON FROM 005,300 TYPE 1 ENABLE OF oDlg Action (oDlg:End())

		FWPDLogUser("A600TIMEV") 
		ACTIVATE MSDIALOG oDlg CENTERED  

	Else
		Help("",1,"FT600TIMVEN",,STR0041,1) //"Time de vendas não encontrado para a oportunidade desta proposta"
	EndIf

RestArea(aAreaSA3)
RestArea(aAreaAD2)
RestArea(aAreaSUM)
RestArea(aArea)

Return Nil

//-----------------------------------------------------------------------------------------
/*/	{Protheus.doc} Ft600Impos

Esta funcao efetua os calculos de impostos (ICMS,IPI,ISS,etc) com base nas funcoes fiscais,
a fim de possibilitar ao usuario o valor de desembolso financeiro.

@sample  Ft600Impos

@author  Serviços/CRM
@since   29/04/2014
@version P12
/*/
//-----------------------------------------------------------------------------------------
Static Function Ft600Impos()

Local aArea		 := GetArea()
Local aAreaSA1	 := SA1->(GetArea())
Local aAreaADZ   := ADZ->(GetArea())
Local aTitles    := {STR0058}	//"Impostos"
Local nX         := 0
Local nPrcLista  := 0
Local nValMerc   := 0
Local nDesconto  := 0
Local nAcresFin  := 0
Local nQtdPeso   := 0
Local nItem      := 0
Local oDlg			:= NIL
Local oFolder		:= NIL
Local nZ         := 0
Local aCols		 := {}
Local oModel	 := FWModelActive()
Local nOperation := oModel:GetOperation()
Local oMdlADZ1    := oModel:GetModel("ADZPRODUTO")
Local oStructADZ1 := oMdlADZ1:GetStruct()
Local aCamposADZ := oStructADZ1:GetFields()
Local oMdlADZ2    := oModel:GetModel("ADZACESSOR")
Local cF600Cla	  := ""
Local nBkp		  := IIf (Type("N")<>"U", N, Nil)
Local nCodPro    := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_PRODUT"})	//Posicao do campo ADZ_PRODUT
Local nQtdVen    := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_QTDVEN"})	//Posicao do campo ADZ_QTDVEN
Local nTes       := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_TES"})	//Posicao do campo ADZ_TES
Local nValor     := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_TOTAL"})  //Posicao do campo ADZ_VALOR
Local nPrUnit    := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_PRCTAB"}) //Posicao do campo ADZ_PRCTAB
Local nPrcVen    := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_PRCVEN"}) //Posicao do campo ADZ_PRCVEN
Local nValDes    := aScan(aCamposADZ,{|x| AllTrim(x[3])=="ADZ_VALDES"}) //Posicao do campo ADZ_VALDES
Local cFilSB1		:= xFilial("SB1")
Local cFilSB2		:= xFilial("SB2")
Local cFilSF4		:= xFilial("SF4")
Local lDescSai	:= Iif(GetNewPar('MV_DESCSAI','1') == "2",.T.,.F.)
Local nNFFrete		:= 0
Local nNFSeguro		:= 0
Local nNFDesconto	:= 0
Local nNFAutonomo	:= 0
Local nNFDespesa	:= 0
Local nNFBaseDup	:= 0
Local nNFTotal		:= 0

//Posiciona Registros
DbSelectArea("SA1")
DbSetOrder(1)

MsSeek(xFilial("SA1")+If(!Empty(M->ADY_CLIENT),M->ADY_CLIENT,M->ADY_CLIENT)+M->ADY_LOJENT)
//Inicializa a funcao fiscal
MaFisSave()
MaFisEnd()

MaFisIni(Iif(Empty(M->ADY_CLIENT),M->ADY_CODIGO,M->ADY_CLIENT),; 							// 1-Codigo Cliente/Fornecedor
         Iif(!Empty(M->ADY_LOJENT),M->ADY_LOJENT,M->ADY_LOJA),; 							// 2-Loja do Cliente/Fornecedor
         "C",; 							// 3-C:Cliente , F:Fornecedor
         "N",; 							// 4-Tipo da NF
         Nil,;
         Nil,;
         Nil,;
         Nil,;
         Nil,;
         "MATA461",; 							// 10-Nome da rotina que esta utilizando a funcao
         Nil,;
         Nil,;
         If(SubsTr(M->ADY_ENTIDA,1,1)<> "1" .AND. Empty(M->ADY_CLIENT),M->ADY_CODIGO + M->ADY_LOJA,""),; // 13- Codigo e Loja do Prospect
		 Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,Nil,;
		 IIf(FindFunction("ChkTrbGen"),ChkTrbGen("SD2","D2_IDTRIB"),.F.))	// 33-Flag para indicar se os tributos genéricos devem ou não ser calculados

	//Na argentina o calculo de impostos depende da serie.
	If cPaisLoc == 'ARG'
		MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
	Endif

	//Agrega os itens para a funcao fiscal
	DbSelectArea("ADZ")
	DbGotop()

	For nZ := 1 TO 2
		If nZ == 1
			aCols := oMdlADZ1:ACOLS
		Else
			aCols := oMdlADZ2:ACOLS
		EndIf

		For nX := 1 to Len(aCols)
			//Verifica se a linha foi deletada
			If ( !oMdlADZ1:IsDeleted() .And. !Empty(aCols[nX][nCodPro]))
				//Posiciona Registros
				SB1->(dbSetOrder(1))
				If SB1->(MsSeek(cFilSB1+aCols[nX][nCodPro]))
					nQtdPeso := aCols[nX][nQtdVen]*SB1->B1_PESO
				EndIf
				SB2->(dbSetOrder(1))
				SB2->(MsSeek(cFilSB2+aCols[nX][nCodPro]+SB1->B1_LOCPAD))
				SF4->(dbSetOrder(1))
				SF4->(MsSeek(cFilSF4+aCols[nX][nTes]))

				//Calcula o preco de lista
				nValMerc  := aCols[nX][nValor]
				nPrcLista := aCols[nX][nPrUnit]
				nQtdPeso  := 0
				nItem++
				If ( nPrcLista == 0 )
					nPrcLista := A410Arred(nValMerc/aCols[nX][nQtdVen],"ADZ_PRCVEN")
				EndIf
				nAcresFin := A410Arred(aCols[nX][nPrcVen]*SE4->E4_ACRSFIN/100,"D2_PRCVEN")
				nValMerc  += A410Arred(nAcresFin*aCols[nX][nQtdVen],"D2_TOTAL")
				nDesconto := A410Arred(nPrcLista*aCols[nX][nQtdVen],"D2_DESCON")-nValMerc
				nDesconto := IIf(nDesconto==0,aCols[nX][nValDes],nDesconto)
				nDesconto := Max(0,nDesconto)
				nPrcLista += nAcresFin

				//Para os outros paises, este tratamento e feito no programas que calculam os impostos.
				If cPaisLoc=="BRA" .or. lDescSai
					nValMerc  += nDesconto
				Endif

				//Agrega os itens para a funcao fiscal
				MaFisAdd((aCols[nX][nCodPro]),;   	// 1-Codigo do Produto ( Obrigatorio )
				         (aCols[nX][nTes]),;	   			// 2-Codigo do TES ( Opcional )
				         (aCols[nX][nQtdVen]),;    			// 3-Quantidade ( Obrigatorio )
				         nPrcLista,;		   					// 4-Preco Unitario ( Obrigatorio )
				         nDesconto,;    						// 5-Valor do Desconto ( Opcional )
				         "",;	   		   					// 6-Numero da NF Original ( Devolucao/Benef )
				         "",;								// 7-Serie da NF Original ( Devolucao/Benef )
				         0,;				   					// 8-RecNo da NF Original no arq SD1/SD2
				         0,;				   					// 9-Valor do Frete do Item ( Opcional )
				         0,;									// 10-Valor da Despesa do item ( Opcional )
				         0,;				   					// 11-Valor do Seguro do item ( Opcional )
				         0,;				   					// 12-Valor do Frete Autonomo ( Opcional )
				         nValMerc,;							// 13-Valor da Mercadoria ( Obrigatorio )
				         0)				   					// 14-Valor da Embalagem ( Opiconal )

				SB1->(dbSetOrder(1))
				If SB1->(MsSeek(cFilSB1+aCols[nX][nCodPro]))
					nQtdPeso := aCols[nX][nQtdVen]*SB1->B1_PESO
				Endif

				//Altera peso para calcular frete
				MaFisAlt("IT_PESO",nQtdPeso,nItem)
				MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
				MaFisAlt("IT_VALMERC",nValMerc,nItem)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Tratamento efetuado para atender ao calculo  ³
				//³do FCI.									    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				N := nX		//Incialização manual da variável N para verificar o aCols
				cF600Cla := XFciGetOrigem(aCols[nX][nCodPro],M->ADY_DATA)[1]	//1 - Codigo da Origem
				If !Empty(cF600Cla)
					MaFisAlt("IT_CLASFIS", cF600Cla,nX)
				EndIf
			EndIf
			dbSelectArea("ADZ")
			dbSkip()
		Next nX

	Next nY

//Monta a tela de exibicao dos valores fiscais
DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0062) FROM 09,00 TO 28,80 //"Planilha Financeira"
oFolder := TFolder():New(001,001,aTitles,{"HEADER"},oDlg,,,, .T., .F.,315,140)

//Folder 1
MaFisRodape(1,oFolder:aDialogs[1],,{005,001,310,60},Nil,.T.)

nNFFrete    := MaFisRet(,"NF_FRETE")
nNFSeguro   := MaFisRet(,"NF_SEGURO")
nNFDesconto := MaFisRet(,"NF_DESCONTO")
nNFAutonomo := MaFisRet(,"NF_AUTONOMO")
nNFDespesa  := MaFisRet(,"NF_DESPESA")
nNFBaseDup  := MaFisRet(,"NF_BASEDUP")
nNFTotal    := MaFisRet(,"NF_TOTAL")

@ 070,005 SAY RetTitle("F2_FRETE")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,105 SAY RetTitle("F2_SEGURO")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,205 SAY RetTitle("F2_DESCONT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,005 SAY RetTitle("F2_FRETAUT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,105 SAY RetTitle("F2_DESPESA")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 085,205 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
@ 070,050 MSGET nNFFrete		PICTURE PesqPict("SF2","F2_FRETE",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 070,150 MSGET nNFSeguro  		PICTURE PesqPict("SF2","F2_SEGURO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 070,250 MSGET nNFDesconto		PICTURE PesqPict("SF2","F2_DESCONTO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,050 MSGET nNFAutonomo		PICTURE PesqPict("SF2","F2_FRETAUT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,150 MSGET nNFDespesa		PICTURE PesqPict("SF2","F2_DESPESA",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 085,250 MSGET nNFBaseDup		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[1]
@ 110,005 SAY OemToAnsi(STR0060)   SIZE 40,10 PIXEL OF oFolder:aDialogs[1] //"Total da Nota"
@ 110,050 MSGET nNFTotal		PICTURE PesqPict("SF2","F2_VALBRUT",16,2) 	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
@ 110,270 BUTTON OemToAnsi(STR0061)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[1] PIXEL	//"Sair"

ACTIVATE MSDIALOG oDlg CENTERED

MaFisEnd()
MaFisRestore()

N := nBkp

RestArea(aAreaADZ)
RestArea(aAreaSA1)
RestArea(aArea)

Return

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600GetCrono
@description	Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
             	Cronograma Financeiro para passar para os outros fontes que compõem o FATA600,
             	evitando assim, a declaração de variáveis Private.
@sample		Ft600Crono( )
@return		aCronoFin: Array do Cronograma (Retorna a variável statica do cronograma financeiro.)
@author		Thamara Villa Jacomo
@since			09/01/2014
@version		P12
/*/
//-----------------------------------------------------------------------------------------
Function Ft600GetCrono()

Return( aCronoFin )

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600GetAloc

Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
Cronograma Financeiro para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.

@sample     Ft600GetAloc( )

@return     aConfigAlo: Configuracao da alocação

@author     Thamara Villa Jacomo
@since      09/01/2014
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function Ft600GetAloc()

Return( aConfigAlo )

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600SetAloc
Seta a configuração da alocação.
@sample     Ft600SetAloc( aCfgAlo )
@return     Nenhum
@author     Thamara Villa Jacomo
@since      09/01/2014
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function Ft600SetAloc( aXX )

Default	aXX	:= {}

aConfigAlo := aXX
Return

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} FT600GetBen()

Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
Beneficio para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.

@sample     FT600GetBen( )

@return     _aTFFCOD_: Array dos codigos do item do RH

@author     totvs
@since      01/06/2015
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function FT600GetBen()

Return( _aTFFCOD_ )

Function FT600SetBen( aBenef )
	_aTFFCOD_ := aBenef
Return

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} FT600GetVis()

Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
Beneficio para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.

@sample     FT600GetVis( )

@return     _lVistoria_: Logico

@author     totvs
@since      08/06/2015
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function FT600GetVis()

Return( _lVistoria_ )

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} FT600SetVis()

Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
Beneficio para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.

@sample     FT600SetVis( )

@return

@author     totvs
@since      08/06/2015
@version    P12
/*/
//-----------------------------------------------------------------------------------------
Function FT600SetVis( lVistoria )

	_lVistoria_ := lVistoria
Return

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600GetTipo09
Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
array aTipo09 para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.
@sample     Ft600GetTipo09( )
@return	ExpA = Array com o conteúdo da variável aTipo09
@author     Thamara Villa
@since      13/01/2014
@version    P12.1.7
/*/
//-----------------------------------------------------------------------------------------
Function Ft600GetTipo09()

Return( aTipo09 )

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600SetTipo09
Devido ao fracionamento dos fontes, é necessário guardar o conteúdo da variável estática do
array aTipo09 para passar para os outros fontes que compõem o FATA600,
evitando assim, a declaração de variáveis Private.
@sample     Ft600SetTipo09( )
@param 		ExpA = Array com o conteúdo da variável aTipo09
@return	 Nil
@author     Thamara Villa
@since      13/01/2014
@version    P12.1.7
/*/
//-----------------------------------------------------------------------------------------
Function Ft600SetTipo09(aRef)

aTipo09	:= aClone(aRef)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600TAddADZ
Centraliza a inclusão dos gatilhos no model.
@sample 	Ft600TAddADZ( oStruct, cMdlDetail )
@param		oStruct		, Objeto	, Estrutura dos campos
			cMdlDetail		, Caracter	, Nome do ModelGrid que o gatilho foi disparado.
@Return   	Nenhum
@author	Anderson Silva
@since		10/03/2016
@version	12.1.7
/*/
//------------------------------------------------------------------------------
Static Function Ft600TAddADZ( oStruct, cMdlDetail )

Local aAux1 		:= {}
Local aTrigAD1		:= oStruct:GetTriggers()
Local aCloneTrig	:= aClone(aTrigAD1)
Local nX			:= 0
Local lVldArmazem	:= (SuperGetMv("MV_RESTARM",,"0") <> "0")

//Criação de Gatilhos para preencher os campos de Produto
aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_DESCRI" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_DESCRI','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)

oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_UM"     /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_UM','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_MOEDA"  /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_MOEDA','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_TES"    /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_TES','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_QTDVEN" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_QTDVEN','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_PRCTAB" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_PRCTAB','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_PRCVEN" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_PRCVEN','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_CONDPG" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_CONDPG','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       "FT600CdTrg('ADZ_PRODUT','ADZ_CONDPG','" + cMdlDetail + "')" /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_TPPROD" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_TPPROD','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
                       "ADZ_DESCON" /*cCDom*/,;
                       "FT600Trigger('ADZ_PRODUT','ADZ_DESCON','" + cMdlDetail + "')" /*cRegra*/,;
                       .F. /*lSeek*/,;
                       Nil /*cAlias*/,;
                       0   /*nOrdem*/,;
                       Nil /*cChave*/,;
                       Nil /*cCondic*/,;
                       Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

If lVldArmazem
	aAux1 := FwStruTrigger("ADZ_PRODUT" /*cDom*/,;
						"ADZ_ARMAZE" /*cCDom*/,;
						"FT600Trigger('ADZ_PRODUT','ADZ_ARMAZE','" + cMdlDetail + "')" /*cRegra*/,;
						.F. /*lSeek*/,;
						Nil /*cAlias*/,;
						0   /*nOrdem*/,;
						Nil /*cChave*/,;
						Nil /*cCondic*/,;
						Nil /*cSequen*/)
	oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])
EndIf

aAux1 := FwStruTrigger("ADZ_QTDVEN" /*cDom*/,;
                        "ADZ_TOTAL"  /*cCDom*/,;
                        "FT600Trigger('ADZ_QTDVEN','ADZ_TOTAL','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_MOEDA"  /*cDom*/,;
                        "ADZ_PRCTAB" /*cCDom*/,;
                        "FT600Trigger('ADZ_MOEDA','ADZ_PRCTAB','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_MOEDA"  /*cDom*/,;
                        "ADZ_PRCVEN" /*cCDom*/,;
                        "FT600Trigger('ADZ_MOEDA','ADZ_PRCVEN','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

If	ADZ->(ColumnPos("ADZ_TABAGR")) > 0
	// Quanto o campo ADZ_TABAGR existir no dicionário de dados, leva em consideração a regra de sua aplicação
	aAux1 := FwStruTrigger("ADZ_TABAGR" /*cDom*/,;
	                        "ADZ_PRCTAB" /*cCDom*/,;
	                        "FT600Trigger('ADZ_TABAGR','ADZ_PRCTAB','" + cMdlDetail + "')" /*cRegra*/,;
	                        .F. /*lSeek*/,;
	                        Nil /*cAlias*/,;
	                        0   /*nOrdem*/,;
	                        Nil /*cChave*/,;
	                        Nil /*cCondic*/,;
	                        Nil /*cSequen*/)
	oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

	aAux1 := FwStruTrigger("ADZ_TABAGR" /*cDom*/,;
	                        "ADZ_PRCVEN" /*cCDom*/,;
	                        "FT600Trigger('ADZ_TABAGR','ADZ_PRCVEN','" + cMdlDetail + "')" /*cRegra*/,;
	                        .F. /*lSeek*/,;
	                        Nil /*cAlias*/,;
	                        0   /*nOrdem*/,;
	                        Nil /*cChave*/,;
	                        Nil /*cCondic*/,;
	                        Nil /*cSequen*/)
	oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

	aAux1 := FwStruTrigger("ADZ_TABAGR" /*cDom*/,;
	                        "ADZ_TOTAL"  /*cCDom*/,;
	                        "FT600Trigger('ADZ_TABAGR','ADZ_TOTAL','" + cMdlDetail + "')" /*cRegra*/,;
	                        .F. /*lSeek*/,;
	                        Nil /*cAlias*/,;
	                        0   /*nOrdem*/,;
	                        Nil /*cChave*/,;
	                        Nil /*cCondic*/,;
	                        Nil /*cSequen*/)
	oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])
EndIf

aAux1 := FwStruTrigger("ADZ_PRCVEN" /*cDom*/,;
                        "ADZ_TOTAL"  /*cCDom*/,;
                        "FT600Trigger('ADZ_PRCVEN','ADZ_TOTAL','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_DESCON" /*cDom*/,;
                        "ADZ_DESCON" /*cCDom*/,;
                        "FT600Trigger('ADZ_DESCON','ADZ_DESCON','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1 := FwStruTrigger("ADZ_VALDES" /*cDom*/,;
                        "ADZ_VALDES" /*cCDom*/,;
                        "FT600Trigger('ADZ_VALDES','ADZ_VALDES','" + cMdlDetail + "')" /*cRegra*/,;
                        .F. /*lSeek*/,;
                        Nil /*cAlias*/,;
                        0   /*nOrdem*/,;
                        Nil /*cChave*/,;
                        Nil /*cCondic*/,;
                        Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1  := FwStruTrigger("ADZ_TOTAL" /*cDom*/,;
                         "ADZ_TOTAL" /*cCDom*/,;
                         "A600CroFinance()" /*cRegra*/,;
                         .F. /*lSeek*/,;
                         Nil /*cAlias*/,;
                         0   /*nOrdem*/,;
                         Nil /*cChave*/,;
                         Nil /*cCondic*/,;
                         Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1  := FwStruTrigger("ADZ_DT1VEN" /*cDom*/,;
                         "ADZ_DT1VEN" /*cCDom*/,;
                         "A600CroFinance()" /*cRegra*/,;
                         .F. /*lSeek*/,;
                         Nil /*cAlias*/,;
                         0   /*nOrdem*/,;
                         Nil /*cChave*/,;
                         Nil /*cCondic*/,;
                         Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

aAux1  := FwStruTrigger("ADZ_QTDVEN" /*cDom*/,;
                         "ADZ_QTDVEN" /*cCDom*/,;
                         "FT600Trigger('ADZ_QTDVEN','ADZ_QTDVEN','" + cMdlDetail + "')" /*cRegra*/,;
                         .F. /*lSeek*/,;
                         Nil /*cAlias*/,;
                         0   /*nOrdem*/,;
                         Nil /*cChave*/,;
                         Nil /*cCondic*/,;
                         Nil /*cSequen*/)
oStruct:AddTrigger(aAux1[1],aAux1[2],aAux1[3],aAux1[4])

If Len(aCloneTrig) > 0
	For nX := 1 to Len(aCloneTrig)
		oStruct:AddTrigger(aCloneTrig[nX][1],aCloneTrig[nX][2],aCloneTrig[nX][3],aCloneTrig[nX][4])
	Next nX
EndIf

aSize(aCloneTrig,0)

Return Nil

//------------------------------------------------------------------------------
/*/	{Protheus.doc} FATA600AGR

Sobrescreve produtos do item da proposta com os produtos selecionados no
agrupador.

@sample	FATA600AGR()

@param	ExpO1 - ModelGrid dos Produtos da Oportunidade

@return	ExpA - Grid atualizada

@author	Cleyton F.Alves
@since		30/01/2014
@version	12.5.6
/*/
//------------------------------------------------------------------------------
Function FATA600AGR(oMdlADZ)

Local cFilADZ		:= xFilial("ADZ")
Local lDel			:= .F.

ADZ->(dbSetOrder(3))
ADZ->(dbSeek(cFilADZ+ADY->(ADY_PROPOS+ADY_PREVIS)))

WHile ADZ->(!Eof()) .AND. ADZ->ADZ_FILIAL == cFilADZ .AND. ADZ->ADZ_PROPOS == ADY->ADY_PROPOS .AND. ADZ->ADZ_REVISA == ADY->ADY_PREVIS

	If oMdlADZ:SeekLine({{"R_E_C_N_O_" , ADZ->(Recno())}},.F.)
		lDel := .F.
	Else
		lDel := .T.
	EndIf

	//Elimina os registros forçados na deleção da manipulação da grid
	//antes do commit padrão do form, que ajustará a gravação da tabela.
	If lDel
		RecLock("ADZ",.F.)
		dbDelete()
		MsUnLock()
	EndIf
	ADZ->(dbSkip())
EndDo

Return()

//-------------------------------------------------------------------
/*/{Protheus.doc} AT600COrc
Carga do Orçamento de Serviços

@author guilherme.pimentel
@since 01/02/2017
@version 12
/*/
//------------------------------------------------------------------
Function AT600COrc()

MsgRun(STR0378,STR0379,{|| At600SeAtu() })	//"Realizando carga do Orçamento" ## "Aguarde"

Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT600MdlOport
Retorna o model da Oportunidade de Venda.
@sample 	FT600MdlOport( oMdlProp )
@param		oMdlProp , Objeto ,Model da Proposta Comercial
@Return   	oMdlOpor , Objeto ,Model da Oportunidade de Venda
@author	Anderson Silva
@since		10/03/2016
@version	12.1.7
/*/
//------------------------------------------------------------------------------
Function FT600MdlOport(oMdlProp)

Local oMdlAD1		:= Nil
Local oMdlADY		:= Nil
Local lLoadModel	:= .T.

Default oMdlProp := FwModelActive()

If oMdlProp:GetId() == "FATA600"

	oMdlADY := oMdlProp:GetModel("ADYMASTER")

	If ( _oMdlOpor <> Nil .And. _oMdlOpor:GetId() == "FATA300" .And. _oMdlOpor:IsActive() .And. oMdlProp:IsActive() )
		oMdlAD1 := _oMdlOpor:GetModel("AD1MASTER")
		If ( oMdlAD1:GetValue("AD1_NROPOR") == oMdlADY:GetValue("ADY_OPORTU") .And.;
		     oMdlAD1:GetValue("AD1_REVISA") == oMdlADY:GetValue("ADY_REVISA") )
			lLoadModel := .F.
		EndIf
	EndIf

	If lLoadModel
		AD1->(DBSetOrder(1))
		If AD1->(DBSeek(xFilial("AD1") + oMdlADY:GetValue("ADY_OPORTU") + oMdlADY:GetValue("ADY_REVISA")))
			If _oMdlOpor == Nil
				_oMdlOpor := FwLoadModel("FATA300")
				If IsInCallStack("A416Desbl") .Or. FunName() == "CRMA801" .Or. IsInCallStack("CRMA090")
					_oMdlOpor:SetOperation(MODEL_OPERATION_VIEW)
				Else
					_oMdlOpor:SetOperation(MODEL_OPERATION_UPDATE)
				EndIf
			Else
				//Desativa o model e ativa novamente para carregar o model da proxima oportunidade de venda.
				_oMdlOpor:DeActivate()
			EndIf
			_oMdlOpor:Activate()
			FwModelActive(oMdlProp)
			//Valida se o model da oportunidade está ativo.
			If !_oMdlOpor:IsActive()
				aError := _oMdlOpor:GetErrorMessage()
				If !Empty(aError)
					oMdlProp:SetErrorMessage(,,aError[3],,aError[5],aError[6],aError[7])
				EndIf
			EndIf
		EndIf
	EndIf

EndIf
Return(_oMdlOpor)

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT600VldUser
Retorna a validação de usuario no SX3
@sample 	FT600VldUser( cField )
@param		cField	, caracter, Campo do SX3
@Return   	cRet	, caracter, Valid do campo
@author	Anderson Silva
@since		18/03/2016
@version	12.1.7
/*/
//------------------------------------------------------------------------------
Static Function FT600VldUser( cField )

Local cRet := ".T."

If !Empty( cField )
	cRet := GetSx3Cache( cField, "X3_VLDUSER" )
	If Empty( cRet )
		cRet := ".T."
	EndIf
EndIf
Return( cRet )

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT600CbxStatus
Retorna os status da proposta comercial no X3_CBOX.
@sample 	FT600CbxStatus()
@param		Nenhum
@Return   	cRet	, caracter, Status da proposta comercial.
@author	Anderson Silva
@since		18/03/2016
@version	12.1.7
/*/
//------------------------------------------------------------------------------
Function FT600CbxStatus()

Local cCombo 			:= STR0353 //"A=Proposta em aberto;B=Proposta fechada;C=Proposta cancelada;D=Proposta não aprovada;E=Proposta Aprovada;F=Proposta bloqueada;G=Upload Pendente;H=Aguardando Analise;I=Contrato Gerado;J=Pedido de Venda Gerado;K=Nota Fiscal Gerada"
Local lFT600NSta		:= ExistBlock("FT600NSTA")
Local cRetStatus		:= ""

If lFT600NSta
	cRetStatus := ExecBlock("FT600NSTA")
	If ValType(cRetStatus) == "C" .AND. !(Empty(cRetStatus))
		cRetStatus	:= AllTrim(cRetStatus)
		cCombo		+= If(Left(cRetStatus,1) <> ";", ";", "") + cRetStatus
	EndIf
EndIf
Return( cCombo )

//------------------------------------------------------------------------------
/*/{Protheus.doc} Ft600Inf
Legenda do comparador da Proposta Comercial
@sample 	Ft600Inf()
@param		Nenhum
@Return   	lRet, lógico, .T.
@author	CRM/Faturamento
@since		30/09/2003
@version
/*/
//------------------------------------------------------------------------------
Static Function Ft600Inf()

Local oDlg
Local oBmp1
Local oBmp2
Local oBmp3
Local oBmp4
Local oBmp5
Local oBmp6
Local oBmp7
Local oBmp8
Local oBmp9
Local oBmp10
Local oBut1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Cria tela com os bitmaps utilizados no tree para correta identificacao.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DEFINE MSDIALOG oDlg Title STR0075 Of oMainWnd Pixel From 0,0 To 175,550 //"Legenda"

@ 002,003 To 085,273 LABEL STR0075 Pixel //"Legenda"

@ 013,010 BITMAP oBmp1 RESNAME "BPMSEDT1"   SIZE 16,16 NOBORDER Pixel
@ 013,025 Say STR0001 + STR0084                         Of oDlg Pixel //"Proposta Comercial"###" - Modificada"

@ 013,150 BITMAP oBmp2 RESNAME "BPMSEDT4"   SIZE 16,16 NOBORDER Pixel
@ 013,165 Say STR0001 + STR0083                         Of oDlg Pixel //"Proposta Comercial"###" - Não Alterada"

@ 025,010 BITMAP oBmp3 RESNAME "EXCLUIR"    SIZE 16,16 NOBORDER Pixel
@ 025,025 Say STR0085 + STR0081                         Of oDlg Pixel //"Produto"###" - Excluido"

@ 025,150 BITMAP oBmp7 RESNAME "SDUDRPTBL"  SIZE 16,16 NOBORDER Pixel
@ 025,165 Say STR0086 + STR0081                         Of oDlg Pixel //"Acessorio"###" - Excluido"

@ 040,010 BITMAP oBmp4 RESNAME "NOTE"       SIZE 16,16 NOBORDER Pixel
@ 040,025 Say STR0085 + STR0080                         Of oDlg Pixel //"Produto"###" - Modificado"

@ 040,150 BITMAP oBmp8 RESNAME "S4WB005N"   SIZE 16,16 NOBORDER Pixel
@ 040,165 Say STR0086 + STR0080                         Of oDlg Pixel //"Acessorio"###" - Modificado"

@ 055,010 BITMAP oBmp5 RESNAME "BMPINCLUIR" SIZE 16,16 NOBORDER Pixel
@ 055,025 Say STR0085 + STR0082                         Of oDlg Pixel //"Produto"###" - Incluido"

@ 055,150 BITMAP oBmp9 RESNAME "SDUSETDEL"  SIZE 16,16 NOBORDER Pixel
@ 055,165 Say STR0086 + STR0082                         Of oDlg Pixel //"Acessorio"###" - Incluido"

@ 073,010 BITMAP oBmp6 RESNAME "PMSTASK4"   SIZE 16,16 NOBORDER Pixel
@ 073,025 Say STR0085 + STR0083                         Of oDlg Pixel //"Produto"###" - Não Alterado"

@ 073,150 BITMAP oBmp10 RESNAME "PMSTASK2"  SIZE 16,16 NOBORDER Pixel
@ 073,165 Say STR0086 + STR0083                         Of oDlg Pixel //"Acessorio"###" - Não Alterado"

DEFINE SBUTTON oBut1 From 072,244 Type 1 ACTION (oDlg:End()) ENABLE Of oDlg
Activate MSDIALOG oDlg Centered
Return(.T.)

//------------------------------------------------------------------------------
/*/{Protheus.doc} FT600Legenda
Retorna array com cores e legendas da proposta comercial
@sample 	FT600Legenda()
@param		Nenhum
@Return   	aRet	, array, Dados da legenda
@author	Eder Oliveira
@since		04/04/2017
@version	12.1.7
/*/
//------------------------------------------------------------------------------
Function FT600Legenda()

Local lFT600COR	:= ExistBlock("FT600COR")
Local aRet			:= {}
Local aCoresNew	:= {}

aRet 	:= {{'ADY->ADY_STATUS=="A"', 'BR_VERDE',    STR0184},;		//"Proposta em aberto"
    	    {'ADY->ADY_STATUS=="B"', 'BR_VERMELHO', STR0185},;		//"Proposta fechada"
    	    {'ADY->ADY_STATUS=="C"', 'BR_PRETO',    STR0186},;		//"Proposta cancelada"
    	    {'ADY->ADY_STATUS=="D"', 'BR_AMARELO',  STR0187},;		//"Proposta não aprovada"
    	    {'ADY->ADY_STATUS=="E"', 'BLUE',        STR0032},;		//"Proposta Aprovada"
    	    {'ADY->ADY_STATUS=="F"', 'BR_CINZA',    STR0189},;		//"Proposta bloqueada"
    	    {'ADY->ADY_STATUS=="G"', 'BR_MARROM',   STR0348},;		//"Upload Pendente"
    	    {'ADY->ADY_STATUS=="H"', 'ORANGE',      STR0349},;		//"Aguardando Analise"
    	    {'ADY->ADY_STATUS=="I"', 'PINK',        STR0350},;		//"Contrato Gerado"
    	    {'ADY->ADY_STATUS=="J"', 'WHITE',       STR0351},;		//"Pedido de Venda Gerado"
    	    {'ADY->ADY_STATUS=="K"', 'BR_VIOLETA',  STR0352}}		//"Nota Fiscal Gerada"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lFT600COR
	aCoresNew := ExecBlock("FT600COR",.F.,.F.,{aRet})
	If ValType( aCoresNew ) == "A"
		aRet := aClone(aCoresNew)
	EndIf
Endif
Return(aRet)

//------------------------------------------------------------------------------
/*/{Protheus.doc} A600RdpTot
Retorna array com cores e legendas da proposta comercial
@sample 	A600RdpTot(oModel, 1401, 301, .F., 'ADZPRODUTO','ADZ__TOTPRD')
@param		oModel, Objeto, Modelo de dados da proposta
			nTotal, Numerico, Valor total da proposta
			xValor, Numerico, valor atual da linha posicionada
			lSum, Lógico, Se verdadeiro soma xValor ao nTotal, se não, subtrai
			cSubModel, Caracter, Id do submodelo (Produto/Acessório)
			cTotalType, Caracter, Id do campo virtual de total (Produto ou Acessório)
@Return   	nTotal, Numerico, Valor total da proposta
@author	Squad CRM/Faturamento
@since		30/05/2018
@version	12.1.17+
/*/
//------------------------------------------------------------------------------
Static Function A600RdpTot( oModel, nTotal, xValor, lSum, cSubModel,cTotalType )
	Local oModelADZ 	:= oModel:GetModel(cSubModel)
	Local oMdlCalPrd  	:= oModel:GetModel("CALC")
	Local nMoeda		:= Val( oModelADZ:GetValue('ADZ_MOEDA') )
	Local nTotAtu	 	:= 0

	Default nTotal	   	:= 0
	Default xValor		:= 0
	Default lSum		:= .T.
	Default cSubModel	:= 'ADZPRODUTO'
	Default cTotalType	:= 'ADZ__TOTPRD'

	If  nMoeda > 1
		nTotAtu := xMoeda( oModelADZ:GetValue('ADZ_TOTAL'),  nMoeda, 1 ,dDatabase , GetSX3Cache("ADZ_TOTAL","X3_DECIMAL") )
	Else
		nTotAtu := oModelADZ:GetValue('ADZ_TOTAL')
	EndIf

	If lSum
		nTotal := oMdlCalPrd:GetValue(cTotalType) + nTotAtu
	Else
		nTotal := oMdlCalPrd:GetValue(cTotalType) - nTotAtu
	EndIf

Return nTotal

//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDIsObfuscate
    @description
    Verifica se um campo deve ser ofuscado, esta função deve utilizada somente após 
    a inicialização das variaveis atravez da função FATPDLoad.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cField, Caractere, Campo que sera validado
    @param cSource, Caractere, Nome do recurso que buscar dados protegidos.
    @param lLoad, Logico, Efetua a carga automatica do campo informado
    @return lObfuscate, Lógico, Retorna se o campo será ofuscado.
    @example FATPDIsObfuscate("A1_CGC",Nil,.T.)
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDIsObfuscate(cField, cSource, lLoad)
    
	Local lObfuscate := .F.

    If FATPDActive()
		lObfuscate := FTPDIsObfuscate(cField, cSource, lLoad)
    EndIf  

Return lObfuscate



//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDActive
    @description
    Função que verifica se a melhoria de Dados Protegidos existe.

    @type  Function
    @sample FATPDActive()
    @author Squad CRM & Faturamento
    @since 17/12/2019
    @version P12    
    @return lRet, Logico, Indica se o sistema trabalha com Dados Protegidos
/*/
//-----------------------------------------------------------------------------
Static Function FATPDActive()

    Static _lFTPDActive := Nil
  
    If _lFTPDActive == Nil
        _lFTPDActive := ( GetRpoRelease() >= "12.1.027" .Or. !Empty(GetApoInfo("FATCRMPD.PRW")) )  
    Endif

Return _lFTPDActive  


