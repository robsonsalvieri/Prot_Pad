#INCLUDE "PROTHEUS.CH"
#INCLUDE "TECA040.CH"
#INCLUDE "FWMVCDEF.CH"

#DEFINE CADASTRO STR0007 //"Base Instalada"
#DEFINE NUMITENS 9999

Static cRetF3 := ""
/*/


Ŀ
Funo     TECA040   Autor  Vendas CRM             Data  16/09/98 
Ĵ
Descrio  Base Instalada ( Amarracao Cliente x Equipamento )         
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function TECA040(cRotina,xAutoCab,xAutoItens,nOpcAuto)

Local cRoda    := ""
Local xRet     := Nil
Local aCores   := At040Cores()

Private aRotina 	:= MenuDef()
Private cCadastro	:= CADASTRO

If IsIncallStack("FWMILEAUTO") .AND. ValType( xAutoItens ) <> "A" .AND. ValType( nOpcAuto ) <> "N"
	nOpcAuto 	:= xAutoItens
	xAutoItens	:= {}
EndIf

If ValType( cRotina ) == "C"
	//Ŀ
	// Faz tratamento para chamada por outra rotina             
	//
	If !Empty( nScan := AScan( aRotina, { |x| Upper( AllTrim(x[2]) ) == Upper( AllTrim(cRotina) ) } ) )
		cRoda := cRotina + "( 'AA3', AA3->( Recno() ), " + Str(nScan,2) + ")"
		xRet  := Eval( { || &( cRoda ) } )
	EndIf
Else
	//Ŀ
	// Endereca a funcao de BROWSE                                  
	//
	If nOpcAuto == Nil
		mBrowse( 6, 1,22,75,"AA3",,,,,,aCores)
	Else
		Private lFS040Auto	:= .T.
		Private aAutoCab	:= xAutoCab
		Private aAutoItens	:= xAutoItens
		MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"AA3")
	EndIf
EndIf

dbSelectArea("AA4")
dbSetOrder(1)
dbSelectArea("AA3")
dbSetOrder(1)

Return( xRet )

/*/

Ŀ
Funo    MenuDef    Autor  Conrado Q. Gomes       Data  08.12.06 
Ĵ
Descrio  Definio do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Static Function MenuDef()
//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//    6 - Alteracao sem inclusao de registro                    
//
Local aRotHorim := {{ STR0043, "VIEWDEF.TECA970", 0, 4, 0, .T.},;	//"Lanamento das marcaes"
                    { STR0044, "At970AtSep",      0, 4, 0, .T.},;	//"Atualizao para separao"
                    { STR0045, "At970AtRet",      0, 4, 0, .T.}}	//"Atualizao para retorno"
Local aRotina   := {{ STR0001, "AxPesqui",     0, 1, 0, .F.},;	//"Pesquisar"
                    { STR0002, "At040Visua",   0, 2, 0, .T.},;	//"Visualizar"
                    { STR0003, "At040Inclu",   0, 3, 0, .T.},;	//"Incluir"
                    { STR0004, "At040Alter",   0, 4, 0, .T.},;	//"Alterar"
                    { STR0005, "At040Delet",   0, 5, 0, .T.},;	//"Excluir"
                    { STR0006, "At040Trans",   0, 2, 0, .T.},;	//"Tranfer"
                    { STR0008, "MsDocument",   0, 4, 0, .T.},;	//"Conhecimento"
                    { STR0009, "At040Gera",    0, 2, 0, .T.},;	//"Gera Base"
                    { STR0028, "At040LocBens", 0, 3, 0, .T.},;	//"Importar Bens SIGAMNT"
                    { STR0046, aRotHorim,      0, 4, 0, .T.},; 	//"Horimetro"
                    { STR0040, "At040Legend",  0, 2, 0, .T.}}	//"Legenda"
Local aRotUser := {}		// Rotinas adicionadas pelo usurio

//Ŀ
// Adiciona rotinas de usuario no arotina                       
//
If ExistBlock( "AT040ROT" )
	If ValType( aRotUser := ExecBlock( "AT040ROT", .f., .f. ) ) == "A"
		AEval( aRotUser, { |x| AAdd( aRotina, x ) } )
	EndIf
EndIf
Return(aRotina)


/*/


Ŀ
Funo    AT040Inclu Autor  Vendas CRM             Data  16/09/98 
Ĵ
Descrio  Programa de inclusao de Clientes x Equipamentos            
Ĵ
Sintaxe e  Void AT040Inclu(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Inclu(cAlias,nReg,nOpc)

Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local nOpcA 	:= 0
Local nSaveSX8	:= GetSX8Len()
Local oDlg		:= Nil
Local aCmpoView		:= {}
Local lGSLE := !ExistFunc("GSGetIns") .OR.  GSGetIns("LE")
Local lRotAuto := ( Type("lFs040Auto") <> "U" .AND. lFs040Auto )
Local cAbaLoc := IIF( !lGSLE,At040LoAb(), "3")
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private oGetd		:= Nil
Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aCols 		:= {}

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//Ŀ
// Cria Variaveis de Memoria da Enchoice                
//

aStruField := FWSX3Util():GetAllFields("AA3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField)
		If (lGSLE .Or. AllTrim(GetSx3Cache(cField,"X3_FOLDER")) <> cAbaLoc)
			If X3USO(GetSx3Cache(cField,"X3_USADO"))
				aAdd(aCmpoView, AllTrim(cField))
			EndIf
		EndIf
	Next nC
EndIf

//Ŀ
//Montagem aHeader, aCols
//
If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AA4"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,;
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
EndIf

If  !lRotAuto
	aSize    := MsAdvSize()
	aObjects := {}
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )
	If lGSLE
		aCmpoView := NIL
	EndIf
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
	EnChoice( "AA3", nReg, nOpc, , , , aCmpoView, aPosObj[1], , 3)
	oGetd     := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At040LinOk","At040TudOk","",.T.,,,,NUMITENS)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk().AND.At040IncOk(),If(!Obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()})
Else
	If Len(aGets)==0
		EnchAuto("AA3",aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4])
	EndIf

	If EnchAuto("AA3",aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4])
		If MsGetDAuto(aAutoItens,"At040LinOk",{|| At040TudOk() .AND. At040IncOk()},aAutoCab,aRotina[nOpc][4])
			nOpcA := 1
		EndIf
	EndIf
EndIf

If ( nOpcA == 1 )
	Begin Transaction

	aT040Grava(1)
	EvalTrigger()
	If ( "TECA040"$FunName() )
		While ( GetSX8Len() > nSaveSx8 )
			ConfirmSX8()
		End
	EndIf
	// quando possui bem vinculado
	If !Empty(AA3->AA3_CDBMFL) .And. !Empty(AA3->AA3_CODBEM) .And. AA3->AA3_HMEATV == "1"
		DBSelectArea("ST9")
		ST9->(DbSetOrder(1))  // T9_FILIAL+T9_CODBEM
		// bem tem informao de contador associada
		If Posicione("ST9", 1, AA3->AA3_CDBMFL+AA3->AA3_CODBEM, "T9_TEMCONT") <> "N"
			At040LeSTP()
		EndIf
	EndIf

	End Transaction

EndIf

If ( "TECA040"$FunName() )
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	End
EndIf
dbSelectArea("AA3")

Return nOpcA

/*/


Ŀ
Funo    At040Alter Autor  Vendas CRM             Data  16/09/98 
Ĵ
Descrio  Programa de alterao amarrao Cliente x Equipamento      
Ĵ
Sintaxe    Void At040Alter(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada no menu                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Alter(cAlias,nReg,nOpc)

Local aPosObj	:= {}
Local aObjects	:= {}
Local aSize		:= {}
Local nOpcA		:= 0
Local oDlg		:= Nil
Local nSaveSX8	:= GetSX8Len()
Local cSeek		:= ""			// Seek para montagem do aCols
Local cWhile	:= ""			// While para montagem do aCols
Local aTravas	:= {}			// Campos que foram travados
Local lTravas	:= .T.			// Se todos os campos foram travados com sucesso
Local aCmpoView		:= {}
Local lGSLE := !ExistFunc("GSGetIns") .OR. GSGetIns("LE")
Local lRotAuto := ( Type("lFs040Auto") <> "U" .AND. lFs040Auto )
Local cAbaLoc := IIF( !lGSLE, At040LoAb(), "3")
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private oGetd		:= Nil
Private aTela		:= {}
Private aGets		:= {}
Private aCols 		:= {}
Private CurLen		:= Nil
Private nPosAtu		:= 0
Private nPosAnt		:= 9999
Private nColAnt		:= 9999
Private cSavScrVT	:= Nil
Private cSavScrVP	:= Nil
Private cSavScrHT	:= Nil
Private cSavScrHP	:= Nil
Private aHeader		:= {}
Private aAA4		:= {{,}}

aRotina := MenuDef()

//Ŀ
// Monta a Enchoice                                     
//
dbSelectArea(cAlias)

If ( !AtTravaReg(cAlias, aTravas) )
	lTravas := .F.
EndIf

aStruField := FWSX3Util():GetAllFields("AA3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AA3->(FieldGet(FieldPos(cField)))
		EndIf
		If (lGSLE .Or. AllTrim(GetSx3Cache(cField,"X3_FOLDER")) <> cAbaLoc)
			If X3USO(GetSx3Cache(cField,"X3_USADO"))
				aAdd(aCmpoView, AllTrim(cField))
			EndIf
		EndIf
	Next nC
EndIf

//Ŀ
// Atualiza campos Virtuais                             
//
AT040Virt()

If ( lTravas )
	//Ŀ
	//Montagem aHeader, aCols
	//

	cSeek := xFilial("AA4")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
	cWhile:= "AA4->AA4_FILIAL+AA4->AA4_CODCLI+AA4->AA4_LOJA+AA4->AA4_CODPRO+AA4->AA4_NUMSER"
	n := 1
	If Len(aHeader) == 0 .AND. Len(aCols) == 0
		If !FillGetDados(	nOpc			,"AA4"			,1				,cSeek								,;
			   				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/						,;
			   				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/							,;
			   				/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AA4", aTravas) }	)
			lTravas := .F.
		EndIf
		If lTravas
			aAA4 := aClone(aTravas)
		EndIf
	EndIf
EndIf

If ( lTravas )

	If  !lRotAuto
		aSize    := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )

		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )

		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

		If lGSLE
			aCmpoView := NIL
		EndIf
		EnChoice(  "AA3", nReg, nOpc, , , ,aCmpoView , aPosObj[1], , 3, , , , , ,.T. )
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AT040LinOk","AT040TudOk",,.T.,,,,NUMITENS)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk().AND. At040IncOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()})
	Else
		If EnchAuto("AA3",aAutoCab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .AND. MsGetDAuto(aAutoItens,"At040LinOk",{|| At040TudOk() },aAutoCab,aRotina[nOpc][4])
			nOpcA := 1
		EndIf
	EndIf

	If ( nOpcA == 1 )
		Begin Transaction
         At040Grava(2)
			EvalTrigger()
			If ( "TECA040"$FunName() )
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSx8()
				End
			EndIf

			// quando possui bem vinculado
			If !Empty(AA3->AA3_CDBMFL) .And. !Empty(AA3->AA3_CODBEM) .And. AA3->AA3_HMEATV == "1"
				DBSelectArea("ST9")
				ST9->(DbSetOrder(1))  // T9_FILIAL+T9_CODBEM
				DbSelectArea("TWT")
				TWT->(DbSetOrder(1))  // TWT_FILIAL+TWT_CODAA3+TWT_ITEM+TWT_NUMHOR
				// no encontra registro j existente na TWT &&;
				// bem tem informao de contador associada
				If !(TWT->(DbSeek(xFilial("TWT")+AA3->AA3_NUMSER))) .And. ;
					Posicione("ST9", 1, AA3->AA3_CDBMFL+AA3->AA3_CODBEM, "T9_TEMCONT") <> "N"

					At040LeSTP()
				EndIf
			EndIf

		End Transaction
	EndIf

EndIf

If ( "TECA040"$FunName() )
	While ( GetSX8Len() > nSaveSx8 )
		RollBackSx8()
	End
EndIf

//Ŀ
//Efetua o destravamento dos registros travados.
//
AtDestravaReg( aTravas )

Return (nOpcA)

/*/


Ŀ
Funo    At040Delet Autor  Vendas CRM             Data  16/09/98 
Ĵ
Descrio  Programa de exclusao  amarrao Cliente x Equipamento      
Ĵ
Sintaxe    Void At040Delet(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada no menu                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Delet(cAlias,nReg,nOpc)

Local aPosObj	:= {}
Local aObjects	:= {}
Local aSize		:= {}
Local nOpcA		:= 0
Local oDlg		:= Nil
Local lTravas	:= .T.
Local aTravas 	:= {}
Local lContinua	:= .T.
Local cSeek		:= "" 	// Seek para montagem do aCols
Local cWhile	:= ""	// While para montagem do aCols
Local aCmpoView		:= {}
Local lGSLE :=  !ExistFunc("GSGetIns") .OR. GSGetIns("LE")
Local lRotAuto := ( Type("lFs040Auto") <> "U" .AND. lFs040Auto )
Local cAbaLoc := IIF( !lGSLE, At040LoAb(), "3")
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private oGetd		:= Nil
Private aTela		:= {}
Private aGets		:= {}
Private aCols		:= {}
Private CurLen		:= Nil
Private nPosAtu		:= 0
Private nPosAnt		:= 9999
Private nColAnt		:= 9999
Private cSavScrVT	:= Nil
Private cSavScrVP	:= Nil
Private cSavScrHT	:= Nil
Private cSavScrHP	:= Nil
Private aHeader		:= {}
n := 1

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//Ŀ
// Monta a Enchoice                                     
//
dbSelectArea(cAlias)

If ( !AtTravaReg(cAlias, aTravas) )
	lTravas := .F.
EndIf

If ( lTravas )
	aStruField := FWSX3Util():GetAllFields("AA3")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			If GetSx3Cache(cField,"X3_CONTEXT")=="V"
				M->&(cField) := CriaVar(cField)
			Else
				M->&(cField) := AA3->(FieldGet(FieldPos(cField)))
			EndIf
			If (lGSLE .Or. AllTrim(GetSx3Cache(cField,"X3_FOLDER")) <> cAbaLoc)
				If X3USO(GetSx3Cache(cField,"X3_USADO"))
					aAdd(aCmpoView, AllTrim(cField))
				EndIf
			EndIf
		Next nC
	EndIf
EndIf

//Ŀ
// Atualiza campos Virtuais                             
//
AT040Virt()

If ( lTravas )
	//Ŀ
	//Montagem aHeader, aCols
	//
	cSeek := xFilial("AA4")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
	cWhile:= "AA4->AA4_FILIAL+AA4->AA4_CODCLI+AA4->AA4_LOJA+AA4->AA4_CODPRO+AA4->AA4_NUMSER"

	If Len(aHeader) == 0 .AND. Len(aCols) == 0
		If !FillGetDados(	nOpc			,"AA4"		,1					,cSeek								,;
			   				{|| &cWhile }	,{|| .T. }	,/*aNoFields*/		,/*aYesFields*/						,;
							/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/		,/*lEmpty*/							,;
							/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AA4", aTravas) }	)
			lTravas := .F.
		EndIf
	EndIf
EndIf

If ( lTravas )
	//Ŀ
	// Verifica se e possivel excluir o equipamento         
	//
	lContinua := AtCanDelEq( AA3->AA3_CODFAB, AA3->AA3_LOJAFA, AA3->AA3_CODPRO, AA3->AA3_NUMSER, .T., AA3->AA3_FILORI )

EndIf

//-----------------------------------------------------
//  Verifica se o equipamento j vinculado a uma alocao
// caso esteja, no permite a excluso dele
If lTravas .And. lContinua .And. AA3->AA3_EQALOC == '1'

	DbSelectArea('TEW')
	TEW->( DbSetOrder( 3 ) ) // TEW_FILIAL+TEW_BAATD

	If TEW->( DbSeek( xFilial('TEW')+AA3->AA3_NUMSER ) )
		lContinua := .F.
		Help(,,'AT040DELEQ',, STR0025,1,0) // 'No  permitido excluir um equipamento para locaa j vinculado a uma alocao'
	EndIf
EndIf

If ( lTravas .AND. lContinua )

	If  !lRotAuto
		aSize    := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )

		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		If lGSLE
			aCmpoView := NIL
		EndIf

		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
		EnChoice( "AA3", nReg, nOpc, , , , aCmpoView, aPosObj[1], , 3, , , , , ,.T. )
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AT040LinOk","AT040TudOk","",.T.)

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()})
	Else
		nOpcA := 1
	EndIf

	If ( nOpcA == 1 )
		Begin Transaction
			If AA3->AA3_HMEATV == "1"
				At040DelTWT()
			EndIf
			At040Grava(3)
		End Transaction
	EndIf

EndIf

//Ŀ
//Efetua o destravamento dos registros travados.
//
AtDestravaReg( aTravas )

Return (nOpcA)

/*/


Ŀ
Funo    At040Visua Autor  Vendas CRM             Data  16/09/98 
Ĵ
Descrio  Programa de Visualizacao da Amarracao ClientexEquipto      
Ĵ
Sintaxe    Void At040Visua(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada no menu                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Visua(cAlias,nReg,nOpc)

Local aPosObj	:= {}
Local aObjects	:= {}
Local aSize		:= {}
Local nOpcA 	:= 0
Local oDlg      := Nil
Local cSeek		:= ""	// Seek para montagem do aCols
Local cWhile	:= ""	// While para montagem do aCols
Local aCmpoView		:= {}
Local lGSLE :=  !ExistFunc("GSGetIns") .OR. GSGetIns("LE")
Local cAbaLoc := IIF( !lGSLE, At040LoAb(), "3")
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private oGetd		:= Nil
Private aTela		:= {}
Private aGets		:= {}
Private aCols		:= {}
Private CurLen		:= Nil
Private nPosAtu		:= 0
Private nPosAnt		:= 9999
Private nColAnt		:= 9999
Private cSavScrVT	:= Nil
Private cSavScrVP	:= Nil
Private cSavScrHT	:= Nil
Private cSavScrHP	:= Nil
Private aHeader		:= {}
n := 1

If Type('aRotina')=='U'
	aRotina := MenuDef()
EndIf

//Ŀ
// Monta a Enchoice                                     
//
aStruField := FWSX3Util():GetAllFields("AA3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AA3->(FieldGet(FieldPos(cField)))
		EndIf
		If (lGSLE .Or. AllTrim(GetSx3Cache(cField,"X3_FOLDER")) <> cAbaLoc)
			If X3USO(GetSx3Cache(cField,"X3_USADO"))
				aAdd(aCmpoView, AllTrim(cField))
			EndIf
		EndIf
	Next nC
EndIf

//Ŀ
// Atualiza campos Virtuais                             
//
AT040Virt()

//Ŀ
//Montagem aHeader, aCols
//

cSeek := xFilial("AA4")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
cWhile:= "AA4->AA4_FILIAL+AA4->AA4_CODCLI+AA4->AA4_LOJA+AA4->AA4_CODPRO+AA4->AA4_NUMSER"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"AA4"		,1				,cSeek			,;
		  				{|| &cWhile }	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,;
						/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,/*lEmpty*/		)
EndIf

aSize    := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100, 100, .t., .t. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )

If Type('cCadastro') == 'U'
	cCadastro := CADASTRO
EndIf
If lGSLE
	aCmpoView := NIL
EndIf

DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
EnChoice( "AA3", nReg, nOpc, , , ,aCmpoView , aPosObj[1], , 3, , , , , ,.T. )
oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AT040LinOk","AT040TudOk","")
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()})

Return (nOpcA)


/*/


Ŀ
Funo    At040Trans Autor  Vendas CRM             Data  04/11/98 
Ĵ
Descrio  Programa de Transferencia da amarrao Cliente x Equipto   
Ĵ
Sintaxe    Void At040Trans(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada no menu                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Trans(cAlias,nReg,nOpc)

Local aObjects	:= {}
Local aPosObj	:= {}
Local aSize		:= {}
Local nOpcA 	:= 0
Local oDlg		:= Nil
Local lTravas	:= .T.
Local aTravas 	:= {}
Local nSaveSX8	:= GetSX8Len()
Local cSeek		:= ""			// Seek para montagem do aCols
Local cWhile	:= ""			// While para montagem do aCols
Local lAT040Tra	:= ExistBlock("AT040TRA")	// Ponto de entrada para permitir ou proibir a execuo da transferncia
Local lAT040ETr := ExistBlock("AT040ETR")	// Ponto de entrada executado aps a transferncia da base instalada.
Local lRet		:= .T.						// Retorno do ponto de entrada
Local aCmpoView		:= {}
Local lGSLE :=  ExistFunc("GSGetIns") .AND. GSGetIns("LE")
Local cAbaLoc := IIF( !lGSLE, At040LoAb(), "3")
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private oGetd		:= Nil
Private aTela		:= {}
Private aGets		:= {}
Private aCols		:= {}
Private CurLen		:= Nil
Private nPosAtu		:= 0
Private	nPosAnt		:= 9999
Private nColAnt		:= 9999
Private cSavScrVT	:= Nil
Private cSavScrVP	:= Nil
Private cSavScrHT	:= Nil
Private cSavScrHP	:= Nil
Private aHeader		:= {}
Private aAA4		:= {}
n := 1

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//Ŀ
//Ponto de entrada para permitir ou no a continuao da
//execuo da transferncia da base instalada.          
//
If lAT040Tra
	lRet := ExecBlock("AT040TRA", .F. ,.F.)
	If ValType(lRet) == "L"
		If !lRet
			Return nOpcA
		EndIf
	EndIf
EndIf

If ( AA3->AA3_EQALOC == '1' )
	lRet := .F.
	Help(,,'AT040LOC',, STR0024,1,0) //"No  possvel realizar transferncia em itens de locao"
EndIf

If lRet
	//Ŀ
	// Monta a Enchoice                                     
	//
	dbSelectArea(cAlias)

	If ( !AtTravaReg(cAlias, aTravas) )
		lTravas := .F.
	EndIf

	If ( lTravas )
		aStruField := FWSX3Util():GetAllFields("AA3")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				If GetSx3Cache(cField,"X3_CONTEXT")=="V"
					M->&(cField) := CriaVar(cField)
				Else
					M->&(cField) := AA3->(FieldGet(FieldPos(cField)))
				EndIf
				If (lGSLE .Or. AllTrim(GetSx3Cache(cField,"X3_FOLDER")) <> cAbaLoc)
					If X3USO(GetSx3Cache(cField,"X3_USADO"))
						aAdd(aCmpoView, AllTrim(cField))
					EndIf
				EndIf
			Next nC
		EndIf
	EndIf

	//Ŀ
	// Atualiza campos Virtuais                             
	//
	AT040Virt()

	If ( lTravas )
		//Ŀ
		//Montagem aHeader, aCols
		//
		cSeek := xFilial("AA4")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER
		cWhile:= "AA4->AA4_FILIAL+AA4->AA4_CODCLI+AA4->AA4_LOJA+AA4->AA4_CODPRO+AA4->AA4_NUMSER"

		If Len(aHeader) == 0 .AND. Len(aCols) == 0
			If !FillGetDados(	nOpc			,"AA4"			,1				,cSeek								,;
						  		{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/						,;
						  		/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/							,;
						  		/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| AtTravaReg("AA4", aTravas) }	)
				lTravas := .F.
			EndIf
			If lTravas
				aAA4 := aClone(aTravas)
			EndIf
		EndIf
	EndIf

	If ( lTravas )

		aSize    := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )

		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )
		If lGSLE
			aCmpoView := NIL
		EndIf
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME
		EnChoice( "AA3", nReg, nOpc, , , ,aCmpoView , aPosObj[1], , 3, , , , , ,.T. )
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"AT040LinOk","AT040TudOk","",.T.,,,,NUMITENS)

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()})

		If ( nOpcA == 1 )
			If ( Pergunte("ATA040",.T.) )
				Begin Transaction
					At040Grava(4,MV_PAR01,MV_PAR02)
					EvalTrigger()
					If ( "TECA040"$FunName() )
						While ( GetSX8Len() > nSaveSx8 )
							ConfirmSx8()
						End
					EndIf
				End Transaction
			Else
				nOpcA := 0
			EndIf
		EndIf

	EndIf

	If ( "TECA040"$FunName() )
		While ( GetSX8Len() > nSaveSx8 )
			RollBackSx8()
		End
	EndIf

	//Ŀ
	//Efetua o destravamento dos registros travados.
	//
	AtDestravaReg( aTravas )

	If lAT040ETr
		ExecBlock("AT040ETr", .F. ,.F., {If(nOpcA == 1,.T., .F.)})
	EndIf
EndIf

Return (nOpcA)

/*/


Ŀ
Funo    At040LinOk Autor  Henry Fila             Data  07/04/98 
Ĵ
Descrio  Validacao da Linha                                         
Ĵ
Sintaxe    Void At040LinOk()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040LinOk()

Local nPosProd	:= aScan( aHeader,{|x| Trim(x[2]) == "AA4_PRODAC" })
Local nPosSrAc	:= aScan( aHeader,{|x| Trim(x[2]) == "AA4_NSERAC" })
Local nCntFor	:= 0
Local lRet		:= .T.
Local nUsado	:= Len(aHeader)

//Ŀ
//Valida a existncia da varivel privada aAA4.
//
If Type("aAA4") != "A"
	aAA4 := {{,}}
EndIf

//Ŀ
//Verifica se a Linha nao esta deletada                                   
//
If ( lRet .AND. aCols[n][nUsado+1] )
	Return(.T.)
EndIf
//Ŀ
//Valida numero de Linhas                                                 
//
If ( Empty(aCols[n][nPosProd]) .AND. N<>1 .AND. lRet )
	lRet := .F.
	Help(" ",1,"AT040LIN01") // Linhas em branco
EndIf
//Ŀ
//Valida Produto e Numero de Serie                                        
//
If ( lRet )
    If ( !Empty(aCols[n][nPosProd]) .AND. Empty(aCols[n][nPosSrAc]) )
		HELP(" ",1 ,"AT040LIN01")
		lRet := .F.
	EndIf
EndIf
If ( lRet )
	For nCntFor:= 1 to Len(aCols)
		If ( !aCols[nCntFor][nUsado+1] )
			If ( nCntFor <> N )
				If ( (aCols[n][nPosProd] == aCols[nCntFor][nPosProd] .AND.;
						aCols[n][nPosSrAc] == aCols[nCntFor][nPosSrAc]) .OR.;
					  (aCols[n][nPosProd] == M->AA3_CODPRO .AND.;
						aCols[n][nPosSrAc] == M->AA3_NUMSER) )
				  HELP(" ",1 ,"AT040LIN02")
					lRet := .F.
				EndIf
			EndIf
		EndIf
	Next nCntFor
EndIf

If ( lRet )
	//Ŀ
	//Pesquisa se nao encontra amarracao com o mesmo numero de serie        
	//
	DbSelectArea("AA4")
	DbSetOrder(2)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+aCols[n][nPosProd]+aCols[n][nPosSrAc]) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
			lRet := .F.
			Help(" ",1,"AT040LIN02")
		EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+aCols[n][nPosProd]+aCols[n][nPosSrAc]) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
		HELP(" ",1,"AT040LIN02")
	EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(2)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+M->AA3_CODPRO+M->AA3_NUMSER) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
			Help(" ",1,"AT040LIN02")
		EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+M->AA3_CODPRO+M->AA3_NUMSER) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
			Help(" ",1,"AT040LIN02")
		EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(4)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODFAB+M->AA3_LOJAFA+aCols[n][nPosProd]+aCols[n][nPosSrAc])  )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
			Help(" ",1,"AT040SKSER")
		EndIf
	EndIf
EndIf
Return( lRet )

/*/


Ŀ
Funo    At040TudOk Autor  Henry Fila             Data  07/04/98 
Ĵ
Descrio  Validacao Geral                                            
Ĵ
Sintaxe    Void At040TudOk()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040TudOk()
Local lOk := .T.
Local aArea := GetArea()
Local aAreaST9 := {}
Local aAreaAA3 := {}
Local lIntTecMnt := ExistFunc('At040ImpST9') .And. ExistFunc('At800OsxTec') .And. (TEW->( ColumnPos('TEW_TPOS')) > 0 ) .And. (AA3->(ColumnPos('AA3_CODBEM')) > 0)

If lIntTecMnt .And. !Empty(M->AA3_CODBEM) .and. !(IsInCallStack("ATFA060"))

	DbSelectArea("ST9")
	aAreaST9 := ST9->(GetArea())
	ST9->(DbSetOrder(1)) //T9_FILIAL+T9_CODBEM

	aAreaAA3 := AA3->(GetArea())
	AA3->(DbSetOrder( 1 ) ) // AA3_FILIAL+AA3_CDBMFL+AA3_CODBEM

	If ST9->(DbSeek(M->AA3_CDBMFL+M->AA3_CODBEM))

		If Alltrim(ST9->T9_CODESTO) <> Alltrim(M->AA3_CODPRO)  // Produto do estoque diferente
			lOk := .F.
			Help(,,"AT040PRDBEM",, STR0029,1,0) // "Os cdigos de produtos no cadastro de Bem e da Base de Atendimento esto diferentes entre si."
		ElseIf ST9->T9_SITBEM <> "A" // Equipamento bloqueado
			lOk := .F.
			Help(,,"AT040SITBEM",, STR0030,1,0) // "O Bem no est ativo no sistema. Campo T9_SITBEM como Inativo."
		ElseIf AA3->( DbSeek(xFilial("AA3")+M->AA3_CDBMFL+M->AA3_CODBEM ) )  // Bem j vinculado a base de atendimento
			lOk := .F.
			Help(,,"AT040BEMEX",, STR0031,1,0) // "O Bem j est vinculado a outra Base de Atendimento no sistema."
		EndIf
	Else
		lOk := .F.
		Help(,,"AT040BEMEX",,STR0049,1,0) // "Bem Inexistente na filial de destino. Nao localizado na tabela ST9"
	EndIf


	RestArea(aAreaAA3)
	RestArea(aAreaST9)
	RestArea(aArea)
EndIf

If lOk
	AA3->( DbSetOrder( 6 ) ) // AA3_FILIAL + AA3_NUMSER + AA3_FILORI
	If M->AA3_EQALOC  == '1' .And. !Empty( M->AA3_NUMSER )  .And. (AA3->( ColumnPos('AA3_MSBLQL')) == 0 .Or. M->AA3_MSBLQL == '2')  .And. At040HasID(M->AA3_NUMSER, M->AA3_FILORI)
		MsgAlert(STR0047,STR0048)//#"Numero de serie(Id.Unico), J utilizado em outra base de atendimento, para que o produto seja um produto disponivel para locao o numero de serie(Id.Unico) devera ser unico" #"Favor Verificar ID.Unico"
		lOk := .F.
	EndIf
EndIf


If lOk
	lOk := At040FilOk( M->AA3_FILORI )
EndIf


Return lOk

/*/


Ŀ
Funo    At040SkCli Autor  Eduardo Riera          Data  17/09/98 
Ĵ
Descrio  Busca Nome do cliente                                      
Ĵ
Sintaxe    Void At040SkCli()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkCli()

Local lRet    := .T.
Local cCampo  := ReadVar()

Do Case
	Case ( cCampo == "M->AA3_LOJA" )
		dbSelectArea("SA1")
		dbSetOrder(1)
		If dbSeek(xFilial("SA1")+M->AA3_CODCLI+&(ReadVar()))
			lRet := RegistroOk("SA1")
			M->AA3_NOMCLI := SA1->A1_NOME
		Else
			HELP(" ", 1,"REGNOIS")
			lRet := .F.
		EndIf
	Case cCampo == "M->AA3_CODCLI"
		dbSelectArea("SA1")
		dbSetOrder(1)
		If ( xFilial("SA1")+&(ReadVar())<>SA1->A1_FILIAL+SA1->A1_COD )
			dbSeek(xFilial("SA1")+&(ReadVar()))
		EndIf
		If ( xFilial("SA1")+&(ReadVar())==SA1->A1_FILIAL+SA1->A1_COD )
			//lRet := RegistroOk("SA1")
			M->AA3_NOMCLI := SA1->A1_NOME
		Else
			HELP(" ", 1,"REGNOIS")
			lRet := .F.
		EndIf
EndCase
Return(lRet)

/*/


Ŀ
Funo    At040SkPrd Autor  Eduardo Riera          Data  17/09/98 
Ĵ
Descrio  Busca Descricao do Produto                                 
Ĵ
Sintaxe    Void At040SkPrd()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkPrd()

Local aArea 	:= GetArea()
Local lRet    	:= .T.
Local cCampo  	:= ReadVar()
Local uConteudo := &(ReadVar())
Local nPosDProd := 0
Local nPosModelo:= 0

If ( lRet )
	dbSelectArea("SB1")
	dbSetOrder(1)
	If ( dbSeek(xFilial("SB1")+uConteudo) )
		lRet := RegistroOk("SB1")
		//Ŀ
 		// Checa se a chamada vem da Enchoice ou da GetDados   
 		//
		If ( cCampo == "M->AA3_CODPRO" )
			M->AA3_DESPRO := SB1->B1_DESC
			M->AA3_MODELO := SB1->B1_MODELO

			dbSelectArea("AA3")
			AA3->(dbSetOrder(10)) //AA3_FILORI+AA3_CODPRO

			dbSelectArea("SB5")
			SB5->(dbSetOrder(1)) //B5_FILIAL+B5_COD

			If SB5->(dbSeek(xFilial("SB5")+uConteudo)) .AND. AA3->(dbSeek(xFilial("AA3")+M->AA3_FILORI+uConteudo))
				If SB5->B5_ISIDUNI == "2"
					lRet := .F.
					MsgInfo( STR0041 ) //"O produto ja est relacionado com uma base de atendimento e no  controlado por ID nico, isso poder afetar no controle de disponibilidade dos equipamentos, no  possvel prosseguir"
				Endif
			Endif
		ElseIf ( cCampo == "M->AA4_PRODAC" ) .And. ( Type("aHeader") == "A" )
			nPosDProd  := AsCan( aHeader,{|x| Trim(x[2]) == "AA4_DESCPR" })
			nPosModelo := AsCan( aHeader,{|x| Trim(x[2]) == "AA4_MODELO" })
            If nPosDProd > 0
				aCols[n][nPosDProd]  := SB1->B1_DESC
			EndIf
			If nPosModelo > 0
				aCols[n][nPosModelo] := SB1->B1_MODELO
			EndIf
		EndIf
	Else
	   HELP(" ", 1,"REGNOIS")
	   lRet := .F.
	EndIf
EndIf

RestArea(aArea)

Return(lRet)

/*/


Ŀ
Funo    At040SkTec Autor  Eduardo Riera          Data  17/08/98 
Ĵ
Descrio  Busca Descricao do Tecnico                                 
Ĵ
Sintaxe    Void At040SkTec()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkTec()

Local aArea 	:= { Alias() , IndexOrd() , RecNo() }
Local lRet		:= .T.

dbSelectArea("AA1")
dbSetOrder(1)
If dbSeek(xFilial("AA1")+&(ReadVar()))
   M->AA3_NOMTEC := AA1->AA1_NOMTEC
Else
   HELP(" ", 1,"REGNOIS")
   lRet := .F.
EndIf

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(lRet)

/*/


Ŀ
Funo    At040Virt  Autor  Eduardo Riera          Data  17/09/98 
Ĵ
Descrio  Atualiza campos virtuais na alteracao,exclusao,visualizacao
Ĵ
Sintaxe    Void At040Virt()                                           
Ĵ
 Uso       TECA040                                                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
21/12/2006Conrado Q.     BOPS: 115729: A descrio do acessrio agora
                          feito atravs do Ini. Padro              
ٱ


/*/
Static Function At040Virt()
Local cAlias := Alias()

dbSelectarea("SA1")
dbSetOrder(1)
If dbSeek(xFilial("SA1")+M->AA3_CODCLI+M->AA3_LOJA)
   M->AA3_NOMCLI := SA1->A1_NOME
EndIf

dbSelectarea("SB1")
dbSetOrder(1)
If dbSeek(xFilial("SB1")+M->AA3_CODPRO)
	M->AA3_DESPRO := SB1->B1_DESC
EndIf

dbSelectArea("AA1")
dbSetOrder(1)
If dbSeek(xFilial("AA1")+M->AA3_CODTEC)
   M->AA3_NOMTEC := AA1->AA1_NOMTEC
EndIf

dbSelectArea(cAlias)

Return .T.

/*/


Ŀ
Funo    At040SkSer Autor  Eduardo Riera          Data  16/09/98 
Ĵ
Descrio  Busca Numero de Serie                                      
Ĵ
Sintaxe    Void At040SkSer()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkSer()

Local lRet    	:= .T.
Local uConteudo := &(ReadVar())
Local nPosSerAc := 0
Local nPosProAc := 0
Local nCntFor 	:= 0
Local lNoVarN   := Type("n") <> "U"
Local nNAnt

// se funcao chamada pelo telemarketing (call center) acerta variavel "n"
If IsInCallStack("TMKA271")
	n:=Len(aCols)
	nNAnt	  := n
Endif

//Ŀ
//Valida a existncia da varivel privada aAA4.
//
If Type("aAA4") != "A"
	aAA4 := {{,}}
EndIf

If lNoVarN
	nPosSerAc := aScan( aHeader,{|x| AllTrim(x[2]) == "AA4_NSERAC" })
	nPosProAc := aScan( aHeader,{|x| AllTrim(x[2]) == "AA4_PRODAC" })
	If nPosSerAc > 0 .And. nPosProAc > 0
		For nCntFor := 1 to Len(aCols)
			If ( !aCols[nCntFor][Len(aHeader)+1] )
				If ( 	( aCols[nCntFor][nPosSerAc]==uConteudo .AND.;
							aCols[nCntFor][nPosProAc]==aCols[n][nPosProAc] .AND.;
							n<> nCntFor ) .OR.;
						(M->AA3_NUMSER==uConteudo .AND.;
							 M->AA3_CODPRO==aCols[n][nPosProAc]) )
					HELP(" ",1,"AT040SKSER")
					lRet := .F.
					Exit
				EndIf
			EndIf
		Next nCntFor
	EndIf
EndIf

If ( lRet )
	//Ŀ
	//Pesquisa se nao encontra amarracao com o mesmo numero de serie        
	//
	If lNoVarN .And. nPosProAc > 0
		DbSelectArea("AA4")
		DbSetOrder(2)
		If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+aCols[n][nPosProAC]+uConteudo) )
			If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
				lRet := .F.
				Help(" ",1,"AT040SKSER")
			EndIf
		EndIf
		DbSelectArea("AA4")
		DbSetOrder(1)
		If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+aCols[n][nPosProAC]+uConteudo) )
			If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
				lRet := .F.
				Help(" ",1,"AT040SKSER")
			EndIf
		EndIf
		DbSelectArea("AA4")
		DbSetOrder(4)
		If ( DbSeek(xFilial("AA4")+M->AA3_CODFAB+M->AA3_LOJAFA+aCols[n][nPosProAC]+uConteudo)  )
			If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
				lRet := .F.
				Help(" ",1,"AT040SKSER")
			EndIf
		EndIf
	EndIf

	DbSelectArea("AA4")
	DbSetOrder(2)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+M->AA3_CODPRO+M->AA3_NUMSER) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
			lRet := .F.
			Help(" ",1,"AT040SKSER")
		EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODCLI+M->AA3_LOJA+M->AA3_CODPRO+M->AA3_NUMSER) )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
			Help(" ",1,"AT040SKSER")
		EndIf
	EndIf
	DbSelectArea("AA4")
	DbSetOrder(4)
	If ( DbSeek(xFilial("AA4")+M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+M->AA3_NUMSER)  )
		If (INCLUI) .Or. (ALTERA .And. aScan( aAA4, {|x| x[2] == Recno() }) == 0)
		lRet := .F.
			Help(" ",1,"AT040SKSER")
		EndIf
	EndIf
EndIf

// se funcao chamada pelo telemarketing (call center) recupera variavel "n"
If IsInCallStack("TMK271")
	n:=nNAnt
Endif

Return(lRet)

/*/


Ŀ
Funo    At040Datas Autor  Eduardo Riera          Data  17/09/98 
Ĵ
Descrio  Verifica e consiste datas                                  
Ĵ
Sintaxe    Void At040Datas()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Datas()

Local aArea    := { Alias() , IndexOrd() , RecNo() }
Local lRet     := .T.
Local cCampo   := ReadVar()
Local uConteudo:= &(ReadVar())
Local nPosDtI  := 0

If Type("aHeader") == "A"
	nPosDtI := AsCan( aHeader,{|x| Trim(x[2]) == "AA4_DTINST" })
EndIf

Do Case
	Case ( "AA3_DTINST" $ cCampo )
		If ( uConteudo < M->AA3_DTVEND )
			HELP(" ", 1,"AT040DAT01")
			lRet := .F.
		EndIf
	Case ( "AA3_DTGAR" $ cCampo )
		If ( uConteudo < M->AA3_DTVEND )
			HELP(" ", 1,"AT040DAT02")
			lRet := .F.
		EndIf
	Case ( "AA4_DTGAR" $ cCampo ) .And. ( nPosDtI >0 )
		If ( uConteudo < aCols[n][nPosDtI] )
			HELP(" ", 1,"A040DAT02")
			lRet := .F.
		EndIf
	Case ( "AA4_DTINST" $ cCampo )
		If ( uConteudo < M->AA3_DTVEND )
			HELP(" ", 1,"A040DAT01")
			lRet := .F.
		EndIf
EndCase
dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return lRet

/*/


Ŀ
Funo    At040SkFor Autor  Eduardo Riera          Data  17/08/98 
Ĵ
Descrio  Busca Descricao do Fornecedor                              
Ĵ
Sintaxe    Void At040SkFor()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkFor()

Local aArea 	:= { Alias() , IndexOrd() , RecNo() }
Local lRet		:= .T.
Local nPFornec := aScan(aHeader,{|x| Trim(x[2])=="AA4_FORNEC"})

dbSelectArea("SA2")
dbSetOrder(1)
If !dbSeek(xFilial("SA2")+aCols[n][nPFornec]+&(ReadVar()))
   HELP(" ", 1,"REGNOIS")
   lRet := .F.
Else
	lRet := RegistroOk("SA2")
EndIf

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(lRet)

/*/


Ŀ
Funo    At040SkFab Autor  Eduardo Riera          Data  17/08/98 
Ĵ
Descrio  Busca Descricao do Fabricante                              
Ĵ
Sintaxe    Void At040SkFab()                                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040SkFab()

Local aArea 	:= { Alias() , IndexOrd() , RecNo() }
Local lRet		:= .T.
Local nPosFab  := aScan(aHeader,{|x| Trim(x[2])=="AA4_CODFAB"})

dbSelectArea("SA1")
dbSetOrder(1)
If !dbSeek(xFilial("SA1")+aCols[n][nPosFab]+&(ReadVar()))
   HELP(" ", 1,"REGNOIS")
   lRet := .F.
EndIf
lRet := RegistroOk("SA1")

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(lRet)

/*/


Ŀ
Funcao    At040IncOk Autor  Eduardo Riera          Data  22.10.98 
Ĵ
Descrio  Efetua a Validacao da Inclusao ( TudOk )                   
Ĵ
Retorno    ExpL1 : Logico                                             
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At040IncOk()

Local lRetorno 	:= .T.
Local cChapa	:= ""
Local lAt040TOkI	:= Existblock("At040TOkI")
Local lAt040TOk	:= Existblock("At040TOk")
Local cAliasAA3	:= ""

//Ŀ
// Verifica a Primary Key Cliente/Loja/Produto/Serie  
//
If Inclui

	// Valida se foi realizado o preenchimento do cliente/loja
	If M->AA3_EQALOC == '2'
		If Empty( M->AA3_CODCLI ) .Or. Empty( M->AA3_LOJA )
			lRetorno := .F.
			Help(,,'AT040CLIENTE',, STR0023,1,0) //" obrigatrio preenchimento de cliente e loja"
		EndIf
	Else

		//Verificao se combinao de ID + Produto j foi utilizada

		cAliasAA3	:= GetNextAlias()

		BeginSQL ALIAS cAliasAA3

			SELECT	COUNT(*) AS QTD
			FROM 	%table:AA3% AA3
			WHERE 	AA3.AA3_FILIAL = %xFilial:AA3% AND
					AA3.AA3_CODPRO = %Exp:M->AA3_CODPRO% AND
					AA3.AA3_NUMSER = %Exp:M->AA3_NUMSER% AND
					AA3.AA3_FILORI= %Exp:M->AA3_FILORI% AND
					AA3.%NotDel%

		EndSQL

		If (cAliasAA3)->QTD >0
			Help(" ",1,"JAGRAVADO")
			lRetorno := .F.
	EndIf

		(cAliasAA3)->(dbCloseArea())

	EndIf


	// somente executa as demais validaes quando cliente/loja estiverem preenchidos
	If lRetorno
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AA3")+M->AA3_CODCLI+M->AA3_LOJA+M->AA3_CODPRO+M->AA3_NUMSER+M->AA3_FILORI) )
			Help(" ",1,"AT040INC01")
			lRetorno := .F.
		EndIf

		//Ŀ
		// Verifica a Primary Key Fabric./Loja/Produto/Serie  
		//
 		If (M->AA3_EQALOC == '2') .or. (M->AA3_EQALOC == '1' .and. !(Empty(M->AA3_CODFAB)) )
			dbSelectArea("AA3")
			dbSetOrder(4)
			If ( dbSeek(xFilial("AA3")+M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+M->AA3_NUMSER) .and. M->AA3_FILORI == AA3->AA3_FILORI )
				Help(" ",1,"AT040INC02")
				lRetorno := .F.
			EndIf
		EndIf

		//Ŀ
		//Verifica a Primary Key Chapa                                            
		//
		If (M->AA3_EQALOC == '2') .or. (M->AA3_EQALOC == '1' .and. !(Empty(M->AA3_CHAPA)) )
			dbSelectArea("AA3")
			dbSetOrder(7)// Filial + Chapa + FilOri
			If ( dbSeek(xFilial("AA3")+M->AA3_CHAPA + M->AA3_FILORI) ) .AND. !Empty(M->AA3_CHAPA)
				Help(" ",1,"AT040INC03")
				lRetorno := .F.
			EndIf
		EndIf
		If lAt040TOk
		lRetorno:= ExecBlock("At040TOk", .F., .F.)
			If ValType(lRetorno) <> "L"
				lRetorno := .T.
			EndIf
		EndIf
	EndIf

Elseif Altera
	dbSelectArea("AA3")
	dbSetOrder(4) //AA3_FILIAL+AA3_CODFAB+AA3_LOJAFA+AA3_CODPRO+AA3_NUMSER - Chave unica
	If ( dbSeek(xFilial("AA3")+ M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+M->AA3_NUMSER))
		cChapa := AA3_CHAPA
		AA3->(dbSetOrder(7))// FILIAL + CHAPA
		If ( dbSeek(xFilial("AA3")+M->AA3_CHAPA + M->AA3_FILORI) .AND. !Empty(M->AA3_CHAPA)  .And. cChapa <> M->AA3_CHAPA)
			Help(" ",1,"AT040INC03")
			lRetorno := .F.
		EndIf
	EndIf
	If lAt040TOk
		lRetorno:= ExecBlock("At040TOk", .F., .F.)
		If ValType(lRetorno) <> "L"
			lRetorno := .T.
		EndIf
	EndIf
EndIf

Return(lRetorno)

/*/


Ŀ
Funcao    At040DtGar Autor Eduardo Riera           Data 06.01.99  
Ĵ
Descrio Calcula a data de Garantia                                  
Ĵ
Retorno   Data de Garantia                                            
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At040DtGar()
Return(&(SuperGetMv("MV_FORMGAR")))

/*/


Ŀ
Funcao	 DataRefHis Autor Marco Aurelio           Data 14.09.99  
Ĵ
Descrio Verifica se a Data Referencia de Historico foi informada	  
Ĵ
Retorno	 .T. ou .F.												  
Ĵ
ParametrosNenhum 													  
			  															  
Ĵ
	DATA	  Programador   Manutencao Efetuada						  
Ĵ
			  				 											  
ٱ


/*/

Function DataRefHis()

Local cReadVar := ReadVar()
Local lRetorno := .T.

If cReadVar $ "AA3_HORRPH|AA3_HOROPH|AA3_QTDFALH|AA3_QTREPH"
	If Empty(M->AA3_DTREFH)
		Help(" ",1,"DATAREFHIS")
		lRetorno := .f.
	EndIf
EndIf

Return(lRetorno)

/*/


Ŀ
Funo    At040Gera  Autor  Kleber Dias Gomes      Data  25/07/03 
Ĵ
Descrio  Programa de Gerao da Base Instalada                      
Ĵ
Sintaxe    Void At040Visua(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada no menu                          
Ĵ
 Uso       TECA040                                                    
ٱ


/*/
Function At040Gera(cAlias,nReg,nOpc)

Local aParam   := {}
Local cPerg	   := "AT040A"
Local cTipo		:= ""
Local nOpcA	   := 0
Local nCliProc := 0
Local nRegProc := 0
Local nAceProc := 0
Local nLoop		:= 0
Local oDlg
Local oBmp
Local oBold
Local oBut

If aRotina == Nil
	aRotina := MenuDef()
EndIf

If nReg <> 0
	If Aviso(STR0013,STR0014,{STR0015,STR0016}) == 1	//"Ateno"###"O processo de gerao automtica da base instalada utilizar como registro mestre o registro posicionado."###"Confirma"###"Abandona"
		nOpcA := At040Visua(cAlias,nReg,nOpc)
		If ( nOpcA == 1 )
			Pergunte(cPerg,.F.)
			//Ŀ
			// Parametros                                                       
			// MV_PAR01: Cliente de ?                                           
			// MV_PAR02: Loja de ?                                              
			// MV_PAR03: Cliente ate ?                                          
			// MV_PAR04: Loja ate ?                                             
			// MV_PAR05: Gerar ?                                                
			//
			FormBatch(STR0010,{STR0011,STR0012},;//"Gerador automtico da base instalada"###
									{{5,.T.,{|| Pergunte(cPerg,.T.) }},;
									{1,.T.,{|o| nOpcA := 1, o:oWnd:End()}},;
									{2,.T.,{|o| nOpcA := 0, o:oWnd:End()}}} )
			If ( nOpcA == 1 )
				//Ŀ
				// Carrega os parametros para a chamada da funcao de processamento 
				//
				aParam := {}
				aAdd( aParam, { "CUSTID_FROM"	, MV_PAR01 } )
				aAdd( aParam, { "UNITID_FROM"	, MV_PAR02 } )
				aAdd( aParam, { "CUSTID_TO"		, MV_PAR03 } )
				aAdd( aParam, { "UNITID_TO"  	, MV_PAR04 } )
				aAdd( aParam, { "CREATE"		, MV_PAR05 } )
				aAdd( aParam, { "RECNO"  		, nReg     } )
				aAdd( aParam, { "CLIPROC"	  	, nCliProc } )
				aAdd( aParam, { "REGPROC" 	 	, nRegProc } )
				aAdd( aParam, { "ACEPROC" 	 	, nAceProc } )

				Processa({|| At040Auto(@aParam)})

				For nLoop := 1 To Len( aParam )
					cTipo		:= aParam[ nLoop, 1 ]
					Do Case
						Case cTipo == "CLIPROC"
							nCliProc		:= aParam[ nLoop, 2 ]
						Case cTipo == "REGPROC"
							nRegProc		:= aParam[ nLoop, 2 ]
						Case cTipo == "ACEPROC"
							nAceProc		:= aParam[ nLoop, 2 ]
					EndCase
				Next nLoop

				//Ŀ
				// Exibe o resumo                                               
				//
				DEFINE MSDIALOG oDlg TITLE STR0017  FROM 9,0 TO 25,50 OF oMainWnd //"Resumo"
				@ 00,00 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 30, 120 NOBORDER WHEN .F. PIXEL
				DEFINE FONT oBold NAME "Arial" SIZE 0, -12 BOLD
				@ 08,38 SAY STR0018 FONT oBold PIXEL //"Resumo da gerao automtica da base instalada "
				@ 24,30 TO 26 ,400 LABEL '' OF oDlg   PIXEL
				@ 45,38 SAY STR0019 SIZE 100,10 PIXEL OF oDlg //"Clientes que geraram base instalada: "
				@ 55,38 SAY STR0020 SIZE 100,10 PIXEL OF oDlg //"Registros de base instalada gerados: "
				@ 65,38 SAY STR0021 SIZE 100,10 PIXEL OF oDlg //"Registros de acessrios gerados: "
				@ 45,140 SAY nCliProc SIZE 20, 10 PIXEL   RIGHT
				@ 55,140 SAY nRegProc SIZE 20, 10 PIXEL   RIGHT
				@ 65,140 SAY nAceProc SIZE 20, 10 PIXEL   RIGHT
				@ 90,030 TO 92 ,400 LABEL '' OF oDlg PIXEL
				DEFINE SBUTTON oBut FROM 98,162  TYPE 1 ACTION ( oDlg:End() ) ENABLE of oDlg
				ACTIVATE MSDIALOG oDlg CENTERED
			EndIf
		EndIf
	EndIf
Else
	HELP(" ", 1,"REGNOIS")
EndIf

Return Nil

/*


Ŀ
Funo    At040Auto  Autor  Kleber Dias Gomes      Data  30/07/03 
Ĵ
Descrio  Geracao automatica da base instalada por clientes          
Ĵ
Sintaxe    At040Auto()                                                
Ĵ
Parametros                                                            
Ĵ
Retorno                                                               
Ĵ
Uso        SIGATEC                                                    
ٱ


*/
Function At040Auto(aParam)

Local aArea     := GetArea()
Local aCpo	    := {}
Local aCpoAA3   := {}
Local aCpoAA4	:= {}

Local bUsFiltro := { || .T. }

Local cAliasSA1 := ""
Local cArqInd   := ""
Local cQuery    := ""
Local cTipo     := ""
Local cATBSPRF  := Left(SuperGetMv("MV_ATBSPRF"),10)
Local cATBSEQ   := Right(SuperGetMv("MV_ATBSSEQ"),10)

Local cCodCliINI:= Space( Len( SA1->A1_COD ) )
Local cCodLojINI:= Space( Len( SA1->A1_LOJA ) )
Local cCodCliFIM:= Replicate( "z", Len( SA1->A1_COD ) )
Local cCodLojFIM:= Replicate( "z", Len( SA1->A1_LOJA ) )

Local nCliProc	:= 0
Local nRegProc	:= 0
Local nAceProc	:= 0

Local nGera		:= 1 // Todos
Local nLoop     := 0
Local nRecno    := 0
Local nCpoAA3   := 0
Local nCntFor   := 0
Local nRegAA4   := 0
Local nCpoAA4   := 0
Local nPCodFab  := 0
Local nPLojaFa  := 0
Local nPCodPro  := 0
Local nPNSerAc  := 0
Local nPos      := 0

Local nIndex    := 0
Local nCpo      := 0
Local nUsado    := 0

Local lGera		:= .T.
Local lGerou    := .F.

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Default aParam  := {}

Private aCols	:= {}
Private aHeader := {}

//Ŀ
// Cria Variaveis de Memoria da Enchoice                
//
aStruField := FWSX3Util():GetAllFields("AA3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField)
	Next nC
EndIf

//Ŀ
// Monta Aheader                                        
//
aStruField := FWSX3Util():GetAllFields("AA4")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			nUsado++	
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"AA4",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

//Ŀ
// Monta ACols                                          
//
If ( Len(aCols) == 0 )
	aadd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		aCols[1][nCntFor] := CriaVar(aHeader[nCntFor][2])
	Next nCntFor
	aCOLS[1][nUsado+1] := .F.
EndIf

//Ŀ
// Definicao do filtro de usuario   
//
If ExistBlock( "AT040FIL" )
	bUsFiltro := ExecBlock( "AT040FIL", .F., .F. )
EndIf

For nLoop := 1 To Len( aParam )
	cTipo		:= aParam[ nLoop, 1 ]
	Do Case
		Case cTipo == "CUSTID_FROM"
			cCodCliINI	:= aParam[ nLoop, 2 ]
		Case cTipo == "UNITID_FROM"
			cCodLojINI := aParam[ nLoop, 2 ]
		Case cTipo == "CUSTID_TO"
			cCodCliFIM	:= aParam[ nLoop, 2 ]
		Case cTipo == "UNITID_TO"
			cCodLojFIM	:= aParam[ nLoop, 2 ]
		Case cTipo == "CREATE"
			nGera			:= aParam[ nLoop, 2 ]
		Case cTipo == "RECNO"
			nRecno		:= aParam[ nLoop, 2 ]
		Case cTipo == "CLIPROC"
			nCliProc		:= aParam[ nLoop, 2 ]
		Case cTipo == "REGPROC"
			nRegProc		:= aParam[ nLoop, 2 ]
		Case cTipo == "ACEPROC"
			nAceProc		:= aParam[ nLoop, 2 ]
	EndCase
Next nLoop

dbSelectArea("SA1")
dbSetOrder(1)

cAliasSA1:=	GetNextAlias()

cQuery := ""
cQuery += "SELECT A1_COD, A1_LOJA FROM " + RetSqlName("SA1") + " SA1 "
cQuery += "WHERE "
cQuery += "A1_FILIAL='"  + xFilial("SA1") + "' AND "
cQuery += "A1_COD>='"    + cCodCliINI + "' AND A1_LOJA>='" + cCodLojINI + "' AND "
cQuery += "A1_COD<='"    + cCodCliFIM + "' AND A1_LOJA<='" + cCodLojFIM + "' AND "
cQuery += "SA1.D_E_L_E_T_ =' '"
If nGera == 2 //Gerar somente para cliente que nao possui base instalada
	cQuery += " AND NOT EXISTS ( SELECT AA3_CODCLI, AA3_LOJA FROM " + RetSqlName( "AA3" ) + " AA3 "
	cQuery += "WHERE "
	cQuery += "AA3_FILIAL='" + xFilial("AA3") + "' AND "
	cQuery += "AA3_CODCLI= SA1.A1_COD  AND "
	cQuery += "AA3_LOJA  = SA1.A1_LOJA AND "
	cQuery += "AA3.D_E_L_E_T_ =' ' )"
EndIf
cQuery += " ORDER BY "+SqlOrder(SA1->(IndexKey()))

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1,.T.,.T.)

dbSelectArea("AA3")
dbSetOrder(1)
dbGoTo(nRecno)
For nCpo := 1 to FCount()
	If ( FieldName(nCpo) <> "AA3_CODMEM" )
		aAdd(aCpoAA3,{FieldName(nCpo),FieldGet(nCpo)})
	Else
		aAdd(aCpoAA3,{"AA3_OBS",MSMM(AA3->AA3_CODMEM,,,,3)})
	EndIf
Next nCpo

For nCpoAA3 := 1 to Len(aCpoAA3)
	M->&(aCpoAA3[nCpoAA3,1])	:= aCpoAA3[nCpoAA3,2]
Next nCpoAA3

dbSelectArea("AA4")
dbSetOrder(1)
dbSeek(xFilial("AA4")+AA3->AA3_CODCLI+AA3->AA3_LOJA+AA3->AA3_CODPRO+AA3->AA3_NUMSER)
While ( !Eof() .AND. AA4->AA4_FILIAL==xFilial("AA4") .AND.;
							AA4->AA4_CODCLI==AA3->AA3_CODCLI .AND.;
							AA4->AA4_LOJA	==AA3->AA3_LOJA   .AND.;
							AA4->AA4_CODPRO==AA3->AA3_CODPRO .AND.;
							AA4->AA4_NUMSER==AA3->AA3_NUMSER )
	For nCpo := 1 to FCount()
		aAdd(aCpo,{FieldName(nCpo),FieldGet(nCpo)})
	Next nCpo
	aAdd(aCpoAA4,aCpo)
	aCpo:={}
	dbSelectArea("AA4")
	dbSkip()
End

For nRegAA4 := 1 To Len(aCpoAA4)
	If nRegAA4 <> 1
		aAdd(aCols,Array(nUsado+1))
		For nCntFor := 1 To nUsado
			aCols[nRegAA4][nCntFor] := CriaVar(aHeader[nCntFor][2])
		Next nCntFor
		aCols[nRegAA4][nUsado+1] := .F.
	EndIf
	For nCpoAA4 := 1 To Len(aCpoAA4[nRegAA4])
		nPos   := aScan(aHeader,{|x|AllTrim(x[2])==aCpoAA4[nRegAA4,nCpoAA4,1]})
		If nPos <> 0
			aCols[nRegAA4,nPos] := aCpoAA4[nRegAA4,nCpoAA4,2]
		EndIf
	Next nCpoAA4
Next nRegAA4

nPCodFab := aScan(aHeader,{|x|AllTrim(x[2])=="AA4_CODFAB"})
nPLojaFa := aScan(aHeader,{|x|AllTrim(x[2])=="AA4_LOJAFA"})
nPCodPro := aScan(aHeader,{|x|AllTrim(x[2])=="AA4_PRODAC"})
nPNSerAc := aScan(aHeader,{|x|AllTrim(x[2])=="AA4_NSERAC"})

dbSelectArea(cAliasSA1)
While !Eof()
	If Eval(bUsFiltro)
		dbSelectArea("AA3")
		dbSetOrder(1)
		#IFNDEF TOP
			If nGera == 2 //Gerar somente para cliente que nao possui base instalada
				If dbSeek(xFilial("AA3")+(cAliasSA1)->A1_COD+(cAliasSA1)->A1_LOJA)
					lGera := .F.
				EndIf
			EndIf
		#ENDIF
		If lGera
			dbSelectArea("AA3")
			dbSetOrder(4)
			dbSelectArea("AA4")
			dbSetOrder(4)

			While AA3->(dbSeek(xFilial("AA3")+M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+cATBSPRF+cATBSEQ)) .OR.;
				AA4->(dbSeek(xFilial("AA4")+M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+cATBSPRF+cATBSEQ))
				cATBSEQ			:= Soma1(cATBSEQ)
			End
			IncProc(STR0003+": "+(cAliasSA1)->A1_COD+"/"+(cAliasSA1)->A1_LOJA)
			M->AA3_CODCLI	:= (cAliasSA1)->A1_COD
			M->AA3_LOJA		:= (cAliasSA1)->A1_LOJA
			M->AA3_NUMSER	:= cATBSPRF+cATBSEQ
			cATBSEQ			:= Soma1(cATBSEQ)
			nRegProc++
			For nRegAA4 := 1 To Len(aCpoAA4)
				While AA4->(dbSeek(xFilial("AA4")+aCols[nRegAA4,nPCodFab]+aCols[nRegAA4,nPLojaFa]+aCols[nRegAA4,nPCodPro]+cATBSPRF+cATBSEQ)) .OR.;
					AA3->(dbSeek(xFilial("AA3")+aCols[nRegAA4,nPCodFab]+aCols[nRegAA4,nPLojaFa]+aCols[nRegAA4,nPCodPro]+cATBSPRF+cATBSEQ))
					cATBSEQ			:= Soma1(cATBSEQ)
				End
				If nPNSerAc <> 0
					aCols[nRegAA4,nPNSerAc] := cATBSPRF+cATBSEQ
					cATBSEQ			:= Soma1(cATBSEQ)
					nAceProc++
				EndIf
			Next nRegAA4

			aT040Grava(1)
			lGerou			:= .T.
			nCliProc++
		EndIf
		lGera	:= .T.
	EndIf
	dbSelectArea(cAliasSA1)
	dbSkip()
End

If lGerou
	PutMV("MV_ATBSSEQ",cATBSEQ)
EndIf

For nLoop := 1 To Len( aParam )
	cTipo		:= aParam[ nLoop, 1 ]
	Do Case
		Case cTipo == "CLIPROC"
			aParam[ nLoop, 2 ] := nCliProc
		Case cTipo == "REGPROC"
			aParam[ nLoop, 2 ] := nRegProc
		Case cTipo == "ACEPROC"
			aParam[ nLoop, 2 ] := nAceProc
	EndCase
Next nLoop

dbSelectArea(cAliasSA1)
dbCloseArea()
dbSelectArea("SA1")

RestArea(aArea)
Return Nil

/*


Ŀ
Funo    At040InCon Autor  Kleber Dias Gomes      Data  30/07/03 
Ĵ
Descrio  Chamada da inclusao da base instalada pela consulta padrao 
Ĵ
Sintaxe    At040InCon()                                               
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    Retorno da funcao inclui                                   
Ĵ
Uso        SIGATEC                                                    
ٱ


*/

Function At040InCon()

Local aAreaSA1 := SA1->( GetArea() )
Local xRet
Local nSavN    := NIL

If Type( "N" ) == "N"
	nSavN := N
EndIf

Private aRotina :={ { STR0003, "AT040INCLU", 0, 3 } }

xRet := At040Inclu( "AA3", AA3->( Recno() ), 1 )

SA1->( RestArea( aAreaSA1 ) )

If ValType( nSavN ) == "N"
	N := nSavN
EndIf

Return( xRet )

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At040InitDes()

Inicializa a descrio do acessorio

@sample 	At040InitDes()
@since		05/09/2013
@version	P11.80
@return 	cDescricao, CHARACTER, contedo da descrio
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At040InitDes()

Local cDescricao := ""
Local nPosProd   := aScan( aHeader,{|x| Trim(x[2]) == "AA4_PRODAC" })

If ( FunName()$"TECA040/TECA620/TECA180" )
	If !INCLUI .And. (Type("N") == "U" .Or. !Empty(aCols[n][nPosProd]))
		cDescricao := POSICIONE("SB1",1, XFILIAL("SB1")+AA4->AA4_PRODAC,"B1_DESC")
	EndIf
EndIf

Return(cDescricao)

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At040Loc()
	Realiza a verificao se h movimentos com aquele equipamento, para permitir ou no a edio dos dados de locao

@sample 	At040Loc()
@since		16/09/2013
@version	P11.90
@return 	ExpL, LOGICO, Permite ou no a edio do campo
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At040Loc()

Local aArea := GetArea()
Local aAreaAA3 := AA3->(GetArea())
Local lRet := .T.
Local cAliasTEW	:= ""
Local cIdExp	:= ""
Local cId 		:= M->AA3_NUMSER
Local cFilOri	:= M->AA3_FILORI
Local cWhere 	:= "%%"

If ALTERA .And. !CaracEsp(cId,@cIdExp) //No entra na query quando possui caracter especial

	cWhere := "% TEW.TEW_BAATD = '" + cIdExp + "' "
	cWhere += " AND TEW.TEW_FILBAT = '" + cFilOri + "' %" 

	cAliasTEW	:= GetNextAlias()

	BeginSQL ALIAS cAliasTEW

		SELECT	COUNT(1) AS QTD
		FROM 	%table:TEW% TEW
		WHERE 	TEW.TEW_FILIAL = %xFilial:TEW% AND
				TEW.TEW_PRODUT = %Exp:M->AA3_CODPRO% AND
				%Exp:cWhere% AND
				TEW.%NotDel%

	EndSQL

	If (cAliasTEW)->QTD >0
		lRet := .F.
	EndIf

	RestArea(aAreaAA3)
	RestArea(aArea)
ElseIf IsInCallStack("TECA450")
	lRet := .F.
EndIf

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At040VSer()
	Realiza a validao da Serie(id) da base de conhecimento.

@sample 	At040VSer()
@since		17/08/2016
@version	P12
@return 	lRet, LOGICO, Valida ou no o Campo
/*/
//--------------------------------------------------------------------------------------------------------------------
Function At040VSer()

	Local aArea := GetArea()
	Local aAreaAA3 := AA3->(GetArea())
	Local lRet := .T.

	DbSelectArea('AA3')
	AA3->( DbSetOrder( 1 ) )

	If AA3->( DbSeek( xFilial("AA3") + M->AA3_CODCLI + M->AA3_LOJA + M->AA3_CODPRO + M->AA3_NUMSER + M->AA3_FILORI )) .And. ( AA3->( ColumnPos('AA3_MSBLQL')) == 0 .Or. ( AA3->AA3_MSBLQL == '2' ))
		MsgAlert(STR0056,STR0048)//#"Numero de serie(Id.Unico), J utilizado em outra base de atendimento, para que o produto seja um produto disponivel para locao o numero de serie(Id.Unico) devera ser unico" #"Favor Verificar ID.Unico"
		lRet := .F.
	EndIf

	If lRet
		AA3->( DbSetOrder( 6 ) ) // AA3_FILIAL + AA3_NUMSER + AA3_FILORI

		// Verifica se Existe uma Base de Atendimento com esse Numero de Serie
		If M->AA3_EQALOC  == '1' .And. !Empty( M->AA3_NUMSER ) .And. AA3->( DbSeek( xFilial("AA3") + M->AA3_NUMSER + M->AA3_FILORI ) ) .And. ( AA3->( ColumnPos('AA3_MSBLQL')) == 0 .Or. ( AA3->AA3_MSBLQL == '2' ))
			MsgAlert(STR0047,STR0048)//#"Numero de serie(Id.Unico), J utilizado em outra base de atendimento, para que o produto seja um produto disponivel para locao o numero de serie(Id.Unico) devera ser unico" #"Favor Verificar ID.Unico"
			lRet := .F.
		EndIf

	EndIf

	If lRet 
		If (M->AA3_EQALOC == '2') .or. (M->AA3_EQALOC == '1' .and. !(Empty(M->AA3_CODFAB)) )
			dbSelectArea("AA3")
			dbSetOrder(4)
			If ( dbSeek(xFilial("AA3")+M->AA3_CODFAB+M->AA3_LOJAFA+M->AA3_CODPRO+M->AA3_NUMSER) .and. M->AA3_FILORI == AA3->AA3_FILORI )
				Help(" ",1,"AT040INC02")
				lRet := .F.
			EndIf
		EndIf
	EndIf 

	RestArea(aAreaAA3)
	RestArea(aArea)

Return lRet

//--------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At040GtSer()
	Gatilho: AA3_NUMSER Seq: 001
	Utilizado para no deixar que uma Serie que ja exista ser cadastrada novamente

@sample 	At040GtSer()
@since		17/08/2016
@version	P12
@return 	cRet, Caractere, 1= Sim. 2=No
/*/
//--------------------------------------------------------------------------------------------------------------------

Function At040GtSer()

	Local aArea    := GetArea()
	Local aAreaAA3 := AA3->(GetArea())
	Local cRet 	   := M->AA3_EQALOC // 1=Sim; 2=No

	DbSelectArea('AA3')
	AA3->( DbSetOrder( 6 ) )

	If !Empty( M->AA3_NUMSER ) .And. AA3->( DbSeek( xFilial("AA3") + M->AA3_NUMSER ) )
		cRet := '2'
	EndIf

	RestArea(aAreaAA3)
	RestArea(aArea)

Return cRet

//-----------------------------------------------
/*/{Protheus.doc} At040LocMan()
	Verifica se foi realizada a configurao do parmetro MV_GSLOCOC com cdigo de ocorrncia padro
para a geraa das O.S. de manuteno aps a locao dos equipamentos

@sample 	At040LocMan()
@since		31/10/2013
@version	P11.90
@return 	ExpL, LOGICO, Permite ou no a edio do campo
/*/
//-----------------------------------------------
Function At040LocMan()

Local cConteudo := ''
Local lRet     := .F.

If M->AA3_MANPRE == '1'

	If Empty(M->AA3_CODBEM)
		cConteudo := GetMv( 'MV_GSLOCOC', , '' )
		DbSelectArea('AAG')
		AAG->( DbSetOrder( 1 ) ) // AAG_FILIAL+AAG_CODPRB

		If !Empty( cConteudo ) .And. AAG->( DbSeek( xFilial('AAG')+cConteudo ) )
			lRet := .T.
		Else
			Help(,,'AT040LOCMAN',, STR0026 + ' "MV_GSLOCOC"' + CRLF + ;  // 'No foi inserido contedo vlido no parmetro'
									STR0027 ,1,0) // 'Insira um cdigo de ocorrncia para a abertura das O.S. de manuteno preventiva.'
		EndIf
	Else
		cConteudo := GetMv( 'MV_GSSVMNT', , '' )
		DbSelectArea('ST4')
		ST4->(DbSetOrder( 1 )) //T4_FILIAL+T4_SERVICO

		DbSelectArea('STE')
		STE->(DbSetOrder( 1 )) //TE_FILIAL+TE_TIPOMAN

		If !Empty( cConteudo ) .And. ;
			ST4->(DbSeek(xFilial('ST4')+cConteudo)) .And. ;
			STE->(DbSeek(xFilial('STE')+ST4->T4_TIPOMAN))

			If !(lRet := (STE->TE_CARACTE == 'C'))
			Help(,,'AT040LOCMAN',, STR0026 + ' "MV_GSSVMNT"' + CRLF + ;  // 'No foi inserido contedo vlido no parmetro'
									STR0032 ,1,0) // 'Insira um cdigo de servio para a abertura das O.S. para inspeo ps locao.'
			EndIf
		Else
			Help(,,'AT040LOCMAN',, STR0026 + ' "MV_GSSVMNT"' + CRLF + ;  // 'No foi inserido contedo vlido no parmetro'
									STR0032 ,1,0) // 'Insira um cdigo de servio para a abertura das O.S. para inspeo ps locao.'
		EndIf
	EndIf
Else
	lRet := .T.
EndIf

Return lRet

/*/{Protheus.doc} At040MNTAF()
	Funo para gatilho dos campos de AA3_CBASE, AA3_ITEM e AA3_CHAPA dos bens que possurem integrao com o Ativo Fixo

@sample 	At040MNTAF(xFilial("ST9"), M->AA3_CODBEM, "N1_CBASE")
@since		08/02/2016
@version	P12
@return 	ExpC, Caracter, Contedo do campo na tabela SN1
/*/
Function At040MNTAF( cFilMNT, cCodBem, cCpoSN1 )

Local cConteudo := ""
Local aArea := GetArea()
Local aAreaST9 := {}
Local aAreaSN1 := {}
Local nField := 0
DbSelectArea("ST9")
DbSelectArea("SN1")

aAreaST9 := ST9->(GetArea())
aAreaSN1 := SN1->(GetArea())

ST9->( DbSetOrder( 1 ) )  // T9_FILIAL+T9_CODBEM
SN1->( DbSetOrder( 1 ) )  // N1_FILIAL+N1_CBASE+N1_ITEM

If !Empty(cCpoSN1) .And. (( nField := SN1->( FieldPos(cCpoSN1) ) ) > 0) .And. ;
	ST9->(DbSeek(cFilMNT+cCodBem)) .And. ;
	SN1->(DbSeek(cFilMNT+ST9->T9_CODIMOB))

	cConteudo := SN1->(FieldGet( nField ) )
EndIf

RestArea(aAreaSN1)
RestArea(aAreaST9)
RestArea(aArea)
Return cConteudo

/*/{Protheus.doc} At040LocBens()
@description 	Funo para criar o browse de importao dos bens do mnt para o gs
@author 		josimar.assuncao
@sample 		At040LocBens()
@since			08/02/2016
@version		P12
/*/
Function At040LocBens(cAlias,nReg,nOpc,cChvAut)

Local cTabMark 		:= 'TMPAA3MARK'
Local cNextAre 		:= GetNextAlias()
Local cQuery   		:= ''
Local oBrwList 		:= Nil
Local aQryMark 		:= {}
Local aSize 		:= FWGetDialogSize( oMainWnd )
Local oDialog 		:= Nil
Local lIntTecMnt := ExistFunc('At040ImpST9') .And. ExistFunc('At800OsxTec') .And. (TEW->( ColumnPos('TEW_TPOS')) > 0 ) .And. (AA3->(ColumnPos('AA3_CODBEM')) > 0)
Local lCont 		:= .F.
Local nI    		:= 0
Local nX			:= 0
Local cFldQry 		:= ''
Local oTempST9		:= Nil
Local aStrST9 		:= {}
Local aInsST9		:= {}
Local aChvST9		:= {}

If lIntTecMnt .And. Pergunte("TECA040MNT", .T.)
	If !IsBlind()
		oDialog := MsDialog():New( aSize[1], aSize[2], aSize[3], aSize[4], STR0028, , , , , , , , oMainWnd, .T. ) // "Importar Bens SIGAMNT"
		aQryMark := At040InfBrw()

		oBrwList := FWFormBrowse():New()
		oBrwList:SetOwner(oDialog)
		oBrwList:AddMarkColumns( { || IIf( (cTabMark)->T9_MARK == 1, "LBOK", "LBNO" ) }, { || IIf( (cTabMark)->T9_MARK == 0,(cTabMark)->T9_MARK := 1, (cTabMark)->T9_MARK := 0 ) } )
		oBrwList:SetDataQuery(.T.)
		oBrwList:SetQuery( aQryMark[1] )
		oBrwList:SetAlias( cTabMark )
		oBrwList:SetColumns( aQryMark[2] )
		oBrwList:SetUseFilter( .F. )
		oBrwList:DisableConfig()
		oBrwList:DisableReport()
		oBrwList:DisableDetails()
		oBrwList:AddButton( STR0033, { || At040PrpST9(1,oBrwList:Alias()), AT040AtBrw( oBrwList, aQryMark[1] ) },,,, .F., 2 )  // "Importar Selecionado"
		oBrwList:AddButton( STR0034, { || At040PrpST9(2,oBrwList:Alias()), AT040AtBrw( oBrwList, aQryMark[1] ) },,,, .F., 2 )  // "Importar Marcados"
		oBrwList:AddButton( STR0035, { || At040PrpST9(3,oBrwList:Alias()), AT040AtBrw( oBrwList, aQryMark[1] ) },,,, .F., 2 )  // "Importar Todos"
		oBrwList:SetParam({||Pergunte("TECA040EQ1")})

		// traz para a memria o pergunte que define as informaes para os campos da Base de Atendimento
		Pergunte("TECA040EQ1",.F.)

		oBrwList:Activate()
		oDialog:Activate( ,,,.T.,,, )
		If Select(cTabMark) > 0
			(cTabMark)->(DbCloseArea())
		EndIf
	Else
		aQryMark 	:= At040InfBrw() 				//Executa Monta a query usada no Browse
		cQuery 	:= ChangeQuery(aQryMark[1]) //Inicia a Query temporaria
		dbUseArea(.T., "TOPCONN", TCGenQry(, , cQuery), cNextAre, .F., .T.)

		aStrST9 	:= At040MonStr() 				//Busca estrutura conforme Browse

		(cNextAre)->(dbGotop()) 				//Verificar a chave passado por parametro
		While (cNextAre)->(!EOF())
			If cChvAut == (cNextAre)->T9_FILIAL + (cNextAre)->T9_CODESTO + (cNextAre)->T9_CODBEM + (cNextAre)->T9_SERIE
				For nI := 1 To len(aStrST9)
					If aStrST9[ni,1] == 'ST9REC'
						aAdd(aInsST9,(cNextAre)->ST9REC)
					EndIf
				Next nI
			EndIf
			(cNextAre)->(dbSkip())
		EndDo

		If Len(aInsST9) > 0
			Pergunte("TECA040EQ1",.F.)
			At040ImpST9( aInsST9)
		EndIf


	EndIf
ElseIf !lIntTecMnt
	Help(,, "INTTECMNT",,STR0038,1,0,,,,,,{STR0039}) // ### "Aplique o pacote de atualizao para a release mais recente."
EndIf

Return

/*/{Protheus.doc} At040InfBrw()
	Funo para buscar a query e os dados dos campos a serem associados ao browse para importao dos bens

@sample 	At040InfBrw()
@since		08/02/2016
@version	P12
/*/
Static Function At040InfBrw()

Local aCpos := {"T9_MARK", "T9_FILIAL", "T9_CODBEM", "T9_CODIMOB", "T9_NOME", "T9_SERIE", "T9_CCUSTO", "T9_CODESTO", ;
				"B1_DESC", "T9_MODELO", "T9_CHAPA" }
Local cQryMark := ""
Local nZ       := 1
Local nCpos    := 0
Local cField   := ""
Local aRet     := {}
Local aObjCpos := {}
Local aArea    := GetArea()

cQryMark += "SELECT ST9.T9_FILIAL,ST9.T9_SITBEM,ST9.T9_CODESTO, ST9.T9_CODBEM, ST9.T9_CODIMOB, ST9.T9_CODFAMI,ST9.T9_NOME, "
cQryMark +=	"ST9.T9_CCUSTO, ST9.T9_SERIE, ST9.T9_TEMCONT ,ST9.T9_MODELO ,"
cQryMark +=	"ST9.T9_CHAPA ,SB1.B1_DESC, 0 T9_MARK, ST9.R_E_C_N_O_ ST9REC, '1' AA3_MANPRE, '2' AA3_EXIGNF "
cQryMark += "FROM "+RetSqlName("ST9")+" ST9"
cQryMark += 	" INNER JOIN "+RetSqlName("SB1")+" SB1 ON SB1.D_E_L_E_T_=' ' AND B1_FILIAL='"+xFilial("SB1")+"' AND B1_COD=T9_CODESTO"
cQryMark += " WHERE "
cQryMark += 	"ST9.D_E_L_E_T_ = ' '"
cQryMark += 	" AND T9_FILIAL = '"+xFilial("ST9")+"'" // ler as filiais que o usurio possui acesso
cQryMark += 	" AND T9_SITBEM = 'A'"
cQryMark += 	" AND T9_CODESTO <> ' '"
cQryMark += 	" AND T9_CODBEM BETWEEN '"+mv_par01+"' AND '"+mv_par02+"'"
cQryMark += 	" AND T9_CODIMOB BETWEEN '"+mv_par03+"' AND '"+mv_par04+"'"
cQryMark += 	" AND T9_CODFAMI BETWEEN '"+mv_par05+"' AND '"+mv_par06+"'"
cQryMark += 	" AND T9_CCUSTO BETWEEN '"+mv_par07+"' AND '"+mv_par08+"'"
cQryMark += 	" AND T9_SERIE BETWEEN '"+mv_par09+"' AND '"+mv_par10+"'"
cQryMark += 	" AND T9_CODESTO BETWEEN '"+mv_par11+"' AND '"+mv_par12+"'"
cQryMark += 	" AND NOT EXISTS("
cQryMark += 		"SELECT 1 FROM "+RetSqlName("AA3")+" AA3"
cQryMark += 		" WHERE AA3.D_E_L_E_T_=' '"
cQryMark += 			" AND AA3_FILIAL='"+xFilial("AA3")+"'"
cQryMark += 			" AND AA3_CDBMFL=ST9.T9_FILIAL"
cQryMark += 			" AND AA3_CODBEM=ST9.T9_CODBEM"
cQryMark += 	")"

For nZ := 1 To Len(aCpos)
	If !aCpos[nZ] == "T9_MARK"
		nCpos++
		cField := aCpos[nZ]
		aRet := FwTamSx3(cField)
		AAdd( aObjCpos, FWBrwColumn():New() )
		aObjCpos[nCpos]:SetData( &("{||" + cField + "}") )
		aObjCpos[nCpos]:SetTitle( FWX3Titulo(cField) )
		aObjCpos[nCpos]:SetSize( aRet[1] )
		aObjCpos[nCpos]:SetDecimal( aRet[2] )
		aObjCpos[nCpos]:SetOptions( Separa(RTrim(X3CBox()),";") )
		aObjCpos[nCpos]:SetPicture( PesqPict(RTrim("S"+Left(cField,2)),cField) )
	EndIf
Next nZ

RestArea(aArea)

Return { cQryMark, aClone(aObjCpos) }

/*/{Protheus.doc} AT040AtBrw()
	Atualiza os dados do browse

@sample 	At040InfBrw()
@since		08/02/2016
@version	P12
/*/
Function AT040AtBrw( oBrowse, cQuery )

Local lRet	:= .T.

oBrowse:Data():DeActivate()
oBrowse:SetQuery( cQuery )
oBrowse:Data():Activate()
oBrowse:UpdateBrowse(.T.)
oBrowse:GoBottom()
oBrowse:GoTo(1, .T.)
oBrowse:Refresh(.T.)

Return lRet

/*/{Protheus.doc} At040PrpST9()
	Executa a preparao dos dados para o processamento da importao dos bens

@sample 	At040PrpST9()
@since		11/02/2016
@version	P12
@param 	nTipoImp, Numrico, indica qual o tipo de processamento acionado pelo usurio {1=Registro posicionado;2=Registros Marcados;3=Todos os registros}
@param cTabBrw, Char, cdigo da tabela do browse para acesso aos contedos chave e campos
/*/
Function At040PrpST9( nTipoImp, cTabBrw )

Local aRecnosBens := {}
Local aImpErros := {}
Local cImpErros		:= ""
Local cGsLocOc 		:= ""
Local lRet			:= .T.

Default nTipoImp := 0

// traz para a memria o pergunte que define as informaes para os campos da Base de Atendimento
Pergunte("TECA040EQ1",.F.)

If (MV_PAR01 = 1)
	cGsLocOc := SuperGetMv("MV_GSLOCOC")
	If Empty(cGsLocOc)
		lRet := .F.
		Help(,,'AT040GSLOCOC',, STR0053,1,0)  // "No foi inserido contedo vlido no parmetro 'MV_GSLOCOC'. Insira um cdigo de ocorrncia para a abertura das O.S. de Manuteno preventiva ou altere o contedo do parametro usando o F12!"
	EndIf
EndIf

AA3->(DbSetOrder(9))
If lRet .And. (AA3->(DbSeek(xFilial("AA3")+(cTabBrw)->T9_CODESTO)))
	DbSelectArea("SB5")
	SB5->(DbSetOrder(1))
	If 	SB5->(DbSeek(xFilial("SB5")+AA3->AA3_CODPRO))
		If SB5->B5_ISIDUNI == "2"
	lRet := .F.
	Help(,,'AT040PRODGRA',, STR0054,1,0)  // "O produto ja est relacionado com uma base de atendimento e no  controlado por ID nico, isso poder afetar no controle de disponibilidade dos equipamentos, no  possvel prosseguir"
		EndIf
	EndIf
EndIf

If lRet
	If nTipoImp == 1  // Bem selecionado
		aAdd( aRecnosBens, (cTabBrw)->ST9REC )
	ElseIf nTipoImp == 2  // Bens marcados
		(cTabBrw)->(DbEval( {|| aAdd( aRecnosBens, (cTabBrw)->ST9REC )}, {|| (cTabBrw)->T9_MARK == 1 } ))
	ElseIf nTipoImp == 3  // Todos os Bens do browse
		(cTabBrw)->(DbEval( {|| aAdd( aRecnosBens, (cTabBrw)->ST9REC )} ))
	EndIf

	If Len(aRecnosBens) > 0
		At040ImpST9( aRecnosBens, aImpErros )
		If Len( aImpErros ) > 0
			aEval( aImpErros, {|x| cImpErros += STR0036+Str(x[1])+CRLF+x[2]+CRLF } )  // "Recno do Erro: "
			If !isBlind()
				AtShowLog(cImpErros, STR0037, .T., .T., .T.)  // "Problemas Importao Bens x Base"
			Else
				Conout ( STR0037 + " : " + cImpErros )
			EndIf
		EndIf
	EndIf
EndIf

Return

/*/{Protheus.doc} At040ImpST9()
	Realiza a importao dos dados

@sample 	At040ImpST9()
@since		11/02/2016
@version	P12
@param 	aListBens, Array Numrico, lista com os Recnos dos registros que sero importados para o cadastro de base de atendimento
@param 	aErros, Array { Numrico, Caracter}, lista com os itens que tiveram problemas no processamento e seus respectivos erros
/*/
Function At040ImpST9( aListBens, aErros )

Local aCabec := {}
Local nI := 1
Local aTmpErro := {}
Local cTmpErro := ''
Local lBkpInc := IIF(TYPE("Inclui") == 'L',Inclui,nil)
Local lBkpAlt := IIF(TYPE("Altera") == 'L',Altera,nil)
Default aListBens := {}
Default aErros := {}

DbSelectArea("ST9")
ST9->(DbSetOrDer( 1 ) )  // T9_FILIAL+T9_CODBEM

DbSelectArea("SN1")
SN1->(DbSetOrDer( 1 ) )  // N1_FILIAL+N1_CBASE+N1_ITEM

Inclui := .T.
Altera := .F.

Private lMsErroAuto    := .F.
Private lAutoErrNoFile := .T.

For nI := 1 To Len(aListBens)

	ST9->( DbGoTo(aListBens[nI]))
	// no ir copiar os contedos dos campos
	// Fabricante, Cliente e Loja do Cliente
	Aadd(aCabec,{"AA3_FILIAL"	,xFilial("AA3")	,Nil})
	Aadd(aCabec,{"AA3_CODPRO"	,ST9->T9_CODESTO,Nil})
	Aadd(aCabec,{"AA3_NUMSER"	,ST9->T9_SERIE	,Nil})
	Aadd(aCabec,{"AA3_DTVEND"	,ST9->T9_DTCOMPR,Nil})
	Aadd(aCabec,{"AA3_DTINST"	,ST9->T9_DTCOMPR,Nil})

	If !Empty(ST9->T9_CODIMOB) .And. SN1->( DbSeek( xFilial("SN1")+ST9->T9_CODIMOB))
		Aadd(aCabec,{"AA3_CBASE" , SN1->N1_CBASE, Nil})
		Aadd(aCabec,{"AA3_ITEM"  , SN1->N1_ITEM	, Nil})
		Aadd(aCabec,{"AA3_CHAPA" , SN1->N1_CHAPA, Nil})
	EndIf

	Aadd(aCabec,{"AA3_MODELO"  	,ST9->T9_MODELO,Nil})
	Aadd(aCabec,{"AA3_NFAQUI"  	,ST9->T9_NFCOMPR,Nil})
	Aadd(aCabec,{"AA3_FORNEC"  	,ST9->T9_FORNECE,Nil})
	Aadd(aCabec,{"AA3_LOJAFO"  	,ST9->T9_LOJA,Nil})
	Aadd(aCabec,{"AA3_LOCAL"  	,ST9->T9_LOCAL,Nil})
	Aadd(aCabec,{"AA3_EQALOC"  	,"1",Nil})
	Aadd(aCabec,{"AA3_CODBEM"  	,ST9->T9_CODBEM,Nil})
	//  Configura os campos de manuteno preventiva e nf para remessa para cada base de atendimento
	// conforme a definio pelo F12 de parmetros do browse
	Aadd(aCabec,{"AA3_MANPRE"  	,cValToChar(mv_par01),Nil})
	Aadd(aCabec,{"AA3_EXIGNF"  	,cValToChar(mv_par02),Nil})

	If ST9->T9_TEMCONT <> "N"
		Aadd(aCabec,{"AA3_HMEATV" , "1", Nil})
		Aadd(aCabec,{"AA3_HMELIM" , Len(cValtoChar(ST9->T9_LIMICON)), Nil})
	EndIf

	lMsErroAuto := .F.
	MsExecAuto( {|w,x,y,z| TECA040(w,x,y,z)},Nil,aCabec,{}, 3)

	If lMsErroAuto
		aTmpErro := GetAutoGrLog()
		If Len(aTmpErro) > 0
			aEval( aTmpErro, {|x| cTmpErro += AllToChar( x ) + CRLF } )
			aAdd( aErros, { ST9->(Recno()), cTmpErro } )
		EndIf
	EndIf

	aSize( aCabec, 0)
Next nI

Inclui := lBkpInc
Altera := lBkpAlt

Return

/*/{Protheus.doc} At040BmxBa()
	Realiza a importao dos dados

@sample 	At040ImpST9()
@since		11/02/2016
@version	P12
@param 	aListBens, Array Numrico, lista com os Recnos dos registros que sero importados para o cadastro de base de atendimento
@param 	aErros, Array { Numrico, Caracter}, lista com os itens que tiveram problemas no processamento e seus respectivos erros
/*/
Function At040BmxBa()

Local aCabec := {}
Local lBkpInc := IIF(TYPE("Inclui") == 'L',Inclui,nil)
Local lBkpAlt := IIF(TYPE("Altera") == 'L',Altera,nil)
Private lMsErroAuto    := .F.
Private lAutoErrNoFile := .T.

Inclui := .F.
Altera := .T.

DbSelectArea("AA3")
AA3->(DbSetOrder(8)) //AA3_FILIAL+AA3_CDBMFL+AA3_CODBEM

If AA3->(DbSeek(xFilial("AA3")+ST9->(T9_FILIAL+T9_CODBEM)))

	Reclock("AA3", .F.)

	AA3->AA3_DTVEND := ST9->T9_DTCOMPR
	AA3->AA3_DTINST := ST9->T9_DTCOMPR
	AA3->AA3_MODELO := ST9->T9_MODELO
	AA3->AA3_NFAQUI := ST9->T9_NFCOMPR
	AA3->AA3_FORNEC := ST9->T9_FORNECE
	AA3->AA3_LOJAFO := ST9->T9_LOJA
	AA3->AA3_LOCAL := ST9->T9_LOCAL

	AA3->(MsUnlock())
EndIf

Inclui := lBkpInc
Altera := lBkpAlt

Return

/*/{Protheus.doc} At040Cores()

@description Monta o array com as cores de legenda que sero apresentadas no browse da rotina Base de Atendimento.
@since		 24/06/2016
@version	 P12
@return		 aRet, Array, Array contendo as cores que sero utilizadas pelo Browse.
/*/
Static Function At040Cores()
Local aRet    := {}
Local aLegend := At040TxtLeg()
Local aCores  := aLegend[1]
Local aTexto  := aLegend[2]
Local nX      := 1

For nX := 1 To Len(aTexto)
	aAdd(aRet,{'AA3_STATUS=="'+StrZero(nX,2)+'"' ,aCores[nX]})
Next nX

If ExistBlock("AT040COR")
	aRet := ExecBlock("AT040COR",.F.,.F.,{aRet,aCores,aTexto})
Endif

Return aRet

/*/{Protheus.doc} At040Legend()

@description Monta a tela exibindo os detalhes da legenda da rotina Base de Atendimento.
@since		 24/06/2016
@version	 P12
@return		 aRet, Array, Array contendo as cores que sero utilizadas pelo Browse.
/*/
Function At040Legend()
Local aLegend := At040TxtLeg()
Local aCores  := aLegend[1]
Local aTexto  := aLegend[2]
Local aRet    := {}
Local nX      := 1

For nX := 1 to Len(aTexto)
	aAdd(aRet,{aCores[nX],aTexto[nX]})
Next nX
/*/
	Ponto de Entrada para alterar cores da legenda
/*/
If ExistBlock("AT040LEG")
	aRet := ExecBlock("AT040LEG",.F.,.F.,{aRet,aCores,aTexto})
Endif
BrwLegenda(cCadastro,"Legendas",aRet)

Return(.T.)

/*/{Protheus.doc} At040TxtLeg()

@description Retorna array multidimensional com as descries da tabela genrica 'A5', alm das cores possveis para as legendas.
@since		 24/06/2016
@version	 P12
@return		 aRet[1], Array, Array contendo as cores que sero utilizadas pelo Browse.
@return		 aRet[2], Array, Array contendo as descries da tabela genrica 'A5', para exibio na legenda.
/*/
Static Function At040TxtLeg()
Local aArea  := GetArea()
Local aRet   := {}
Local aCores := {}
Local aTexto := {}
Local aDescr := {}
Local nC     := 0

aAdd(aCores,'BR_VERDE'   )
aAdd(aCores,'BR_VERMELHO')
aAdd(aCores,'BR_AMARELO' )
aAdd(aCores,'BR_AZUL'    )
aAdd(aCores,'BR_LARANJA' )
aAdd(aCores,'BR_PRETO'   )
aAdd(aCores,'BR_BRANCO'  )
aAdd(aCores,'LIGHTBLU' )
aAdd(aCores,'BR_PINK'    )
aAdd(aCores,'BR_CINZA'   )
aAdd(aCores,'BR_CINZA'   )
aAdd(aCores,'BR_MARROM'  )
aAdd(aCores,'LIGHTBLU'   )
aAdd(aCores,'BR_VIOLETA' )
aAdd(aCores,'BR_CANCEL'  )

aDescr := FWGetSX5("A5")
For nC := 1 To Len(aDescr)
	aAdd(aTexto,aDescr[nC,4])
Next nC

aAdd(aRet,aCores)
aAdd(aRet,aTexto)

RestArea( aArea )

Return aRet

/*/{Protheus.doc} At040MonStr()

@description Retorna array multidimensional com as estruturas dos campos do Browse + recno
@since		 19/06/2016
@version	 P12
@return		 aRet
/*/
Static Function At040MonStr()

	Local aFilCpos := {"T9_MARK", "T9_FILIAL", "T9_CODBEM", "T9_CODIMOB", "T9_NOME", "T9_SERIE", "T9_CCUSTO", "T9_CODESTO", ;
				"B1_DESC", "T9_MODELO", "T9_CHAPA","ST9REC" }

	Local aCpos := ST9->(DBStruct())
	Local aStruTb := {{"T9_MARK",'N',1,0},{"ST9REC",'N',4,0}}
	Local ni	:= 0
	local nz    := 0

	For ni:=1 to Len(aFilCpos)

		If aFilCpos[ni] <> 'T9_MARK' .and. aFilCpos[ni] <> 'ST9REC'

			For nz := 1 to len(aCpos)

				If AllTrim(aFilCpos[ni]) == AllTrim(aCpos[nz,1])

					AADD(aStruTb,aCpos[nz])
					exit

				EndIf

			next nz

		EndIf

	Next ni

Return aStruTb

//-------------------------------------------------------------------------------------------------
/*/{Protheus.doc} At040VdHME
@description	Verifica se a base de atendimento pode ter a ativao do seu horimetro
@sample		At040VdHME()
@param			Nenhum
@return		ExpL: .T.=Ativao do horimetro permitida // .F.=Ativao do horimetro no permitida
@since			02/08/2016
@version		P12
/*/
//-------------------------------------------------------------------------------------------------
Function At040VdHME()

Local cCampo		:= ReadVar()
Local uConteudo	:= &(ReadVar())
Local lRet			:= .T.
Local cIsIDUnico	:= Posicione("SB5",1,xFilial("SB5")+M->AA3_CODPRO,"B5_ISIDUNI")

If cIsIDUnico == "2" .AND. uConteudo == "1"
	MsgStop(STR0042, "At040VdHME")	//"A base de atendimento no  controlada com 'ID.nico'. Portanto, no ser possvel ativar o controle de horimetro para a mesma."
	lRet	:= .F.
Endif
Return	lRet

/*/{Protheus.doc} At040FilOk
@description	Avaliar se a filial digitada ou selecionada corresponde a filial associada com o cadastro de produto
@sample			At040FilOk()
@since			17/08/2016
@version		P12
@param			cFilOrig, Caracter, [Default: M->AA3_FILORI], contedo da filial do sistema para validao
@return			Lgico, se permite ou no a incluso do contedo ou confirmao do cadastro
/*/
Function At040FilOk( cFilOrig )

Local lFilSB1Ok := .T.
Local nTamFilSB1 := AtTamFilTab("SB1")
Local nTamFilST9 := AtTamFilTab("ST9")

Default cFilOrig := M->AA3_FILORI

If nTamFilSB1 > 0
	//   verifica se o nvel de compartilhamento do cadastro de produto est dentro
	// do valor inserido para a filial dona ou filial original do equipamento
	// utiliza o cFilAnt como referncia para o PRODUTO pois a consulta padro e validaes
	// do cdigo de produto na tela esto restritas pela filial ativa no sistema (cFilAnt)
	lFilSB1Ok := ( SubStr( cFilOrig, 1, nTamFilSB1) == SubStr( cFilAnt, 1, nTamFilSB1) )

	If !lFilSB1Ok
		Help(,, "AT040FILOK1",,STR0050,1,0,,,,,,;  // "O cdigo de produto precisa estar na mesma estrutura de Empresa, Unidade e Filial que a Filial Original"
				{I18N(STR0051,{ SubStr( cFilAnt, 1, nTamFilSB1) } )} )  // "Selecione uma filial que esteja a partir de #1"
	EndIf

ElseIf nTamFilST9 > 0 .And. Type("M->AA3_CDBMFL") == "C" .And. !Empty(M->AA3_CDBMFL)
	//   confere se a filial original ou dona da base de atendimento est na mesma estrutura ou  igual
	// a filial do Bem do MNT associado
	lFilSB1Ok := ( SubStr( cFilOrig, 1, nTamFilST9) == SubStr( M->AA3_CDBMFL, 1, nTamFilST9) )

	If !lFilSB1Ok
		Help(,, "AT040FILOK2",,STR0052,1,0,,,,,,;  // "A filial do Bem do SIGAMNT precisa estar na mesma estrutura de Empresa, Unidade e Filial que a Filial Original"
				{I18N(STR0051,{ SubStr( M->AA3_CDBMFL, 1, nTamFilST9) } )} )  // "Selecione uma filial que esteja a partir de #1"
	EndIf
EndIf

Return lFilSB1Ok

/*/{Protheus.doc} At040IsMNT
@description	Verifica se a base de atendimento est vinculada a um Bem do MNT (tambm valida a existncia do bem)
@sample			At040IsMNT( , AA3_FILORI, AA3_NUMSER, AA3_CODPRO )
@since			05.01.2017
@version		P12
@param 			nRecST9, Numrico, Recno do registro da ST9
@param			cFilOrig, Caracter, contedo da filial do sistema para validao
@param 			cSerieBase, Caracter, nmero de srie da filial
@param 			cPrdAA3, Caracter, cdigo do produto para verificao de existncia da Base
@return			Lgico, determina se encontrou vnculo com Bem do MNT
/*/
Function At040IsMNT( nRecST9, cFilOrig, cSerieBase, cPrdAA3 )
Local lRet := .F.

DbSelectArea("ST9")
ST9->( DbSetOrder( 1 ) ) // T9_FILIAL+T9_CODBEM

If (lRet := AtPosAA3( cFilOrig+cSerieBase, cPrdAA3 ) .And.;  // posiciona na AA3, verificando se existe registro com a informao
	ST9->( DbSeek( AA3->(AA3_CDBMFL+AA3_CODBEM) ) ))  // busca o BEM no MNT

	nRecST9 := ST9->(Recno())
EndIf

Return lRet

/*/{Protheus.doc} At040HasBase
@description	Verifica se existe base de atendimento associada a Bem do MNT
@sample			At040HasBase( T9_FILIAL, T9_CODBEM )
@since			05.01.2017
@version		P12
@param			cFilBem, Caracter, filial do bem
@param 			cBemMNT, Caracter, cdigo do bem no MNT
@param 			nRecnoAA3, Numrico, Referncia, varivel para retornar o nmero do recno da AA3
@return			Lgico, determina se encontrou vnculo do Bem com alguma Base
/*/
Function At040HasBase( cFilBem, cBemMNT, nRecnoAA3 )
Local lRet := .F.
Local aSave := GetArea()
Local aSaveAA3 := AA3->(GetArea())
Local cQry := GetNextAlias()

Default nRecnoAA3 := 0

BeginSQL Alias cQry

	SELECT R_E_C_N_O_ AA3RECNO
	FROM %Table:AA3%
	WHERE AA3_CDBMFL = %Exp:cFilBem%
		AND AA3_CODBEM = %Exp:cBemMNT%
		AND %NotDel%

EndSQL

If (lRet := (cQry)->(!EOF()))
	nRecnoAA3 := (cQry)->AA3RECNO
EndIf
(cQry)->(DbCloseArea())

RestArea(aSaveAA3)
RestArea(aSave)

Return lRet

/*/{Protheus.doc} At040LeSTP
@description	Realiza a leitura e importao das informaes dos contadores para o gesto de servios
@since			12.01.2017
@version		P12
@return			Lgico, indica se o processamento aconteceu com sucesso ou no
/*/
Static Function At040LeSTP()
Local lRetorno 		:= .T.
Local aArea 		:= GetArea()
Local aAreaSTP 		:= STP->( GetArea() )
Local aAreaTWT 		:= TWT->( GetArea() )
Local cQryAlias 	:= GetNextAlias()
Local lVirada 		:= .F.
Local lQuebra 		:= .F.
Local cMotivo 		:= ""

// executar a iterao pela tabela
BeginSQL Alias cQryAlias
	COLUMN TP_DTORIGI AS DATE
	COLUMN TP_DTLEITU AS DATE

	SELECT STP.TP_FILIAL
			, STP.TP_CODBEM
			, STP.TP_DTORIGI
			, STP.TP_DTLEITU
			, STP.TP_HORA
			, STP.TP_POSCONT
			, STP.TP_VIRACON
			, STP.TP_TIPOLAN
			, STP.TP_ACUMCON
	FROM %Table:STP% STP
	WHERE STP.TP_FILIAL = %Exp:AA3->AA3_CDBMFL%
		AND STP.TP_CODBEM = %Exp:AA3->AA3_CODBEM%
		AND STP.%NotDel%
	ORDER BY STP.TP_FILIAL, STP.TP_CODBEM, STP.TP_DTLEITU, STP.TP_HORA
EndSQL

While (cQryAlias)->(!EOF()) .And. lRetorno

	lVirada := ((cQryAlias)->TP_TIPOLAN == "V")
	lQuebra := ((cQryAlias)->TP_TIPOLAN == "Q")

	If lQuebra
		cMotivo := "3" // quebra
		cDescMotivo := STR0055  // "Lanamento de quebra no MNT - atualizao no vnculo Base x Bem MNT"

		// realiza a alterao do registro anterior para o motivo de quebra
		lRetorno := lRetorno .And. At970TWTAlt( , AA3->AA3_FILORI, AA3->AA3_NUMSER, AA3->AA3_CODPRO,;
												.T./*lUltLinha*/, /*dDtMarc*/, /*cHoraMarc*/, cMotivo,;
												/*nValMarc*/, cDescMotivo, .F./*lVirada*/ )
	EndIf

	lRetorno := lRetorno .And. At970TWTIns( Nil, AA3->AA3_FILORI, AA3->AA3_NUMSER, AA3->AA3_CODPRO, ;
											(cQryAlias)->TP_DTLEITU, (cQryAlias)->TP_HORA, (cQryAlias)->TP_POSCONT, ;
											lVirada )

	(cQryAlias)->(DbSkip())
End
(cQryAlias)->(DbCloseArea())

RestArea(aAreaTWT)
RestArea(aAreaSTP)
RestArea(aArea)

Return lRetorno

/*/{Protheus.doc} At040NoSTP
@description	Avalia se um determinado bem possui contador e caso tenha, se h registros informados
@sample			At040NoSTP( M->AA3_CDBMFL, M->AA3_CODBEM )
@since			12.01.2017
@version		P12
@param			cFilBem, Caracter, filial do bem
@param 			cBemMNT, Caracter, cdigo do bem no MNT
@return			Lgico, determina se encontrou vnculo com o Bem e se h contador associado
/*/
Function At040NoSTP( cBemFil, cBemCod )
Local lRetorno := .T.
Local aArea 	:= GetArea()
Local aAreaST9 	:= ST9->(GetArea())
Local aAreaSTP 	:= STP->(GetArea())

Default cBemFil := M->AA3_CDBMFL
Default cBemCod := M->AA3_CODBEM

DbSelectArea("STP")
STP->(DbSetOrder(2)) // TP_FILIAL+TP_CODBEM+TP_DTORIGI+TP_DTLEITU+TP_HORA

If !Empty(cBemFil) .And. !Empty(cBemCod) .And. ;
	Posicione("ST9", 1, cBemFil+cBemCod, "T9_TEMCONT") <> "N" .And. ;
	STP->(DbSeek(cBemFil+cBemCod))

	lRetorno := .F.
EndIf

RestArea(aAreaSTP)
RestArea(aAreaST9)
RestArea(aArea)

Return lRetorno

/*/{Protheus.doc} At040DelTWT
@description	Exclui registros de medidor associados com o nmero de srie em excluso
@since			13.01.2017
@version		P12
@return			Lgico, determina se finalizou a excluso dos registros
/*/
Static Function At040DelTWT()
Local lRetorno 			:= .T.
Local cFilTWT 			:= xFilial("TWT")

DbSelectArea("TWT")
TWT->(DbSetOrder(1))  // TWT_FILIAL+TWT_CODAA3+TWT_ITEM+TWT_NUMHOR

If TWT->(DbSeek( cFilTWT+AA3->AA3_NUMSER))
	While TWT->(!EOF()) .And. TWT->TWT_FILIAL == cFilTWT .And. TWT->TWT_CODAA3 == AA3->AA3_NUMSER
		Reclock("TWT",.F.)
			TWT->(DbDelete())
		TWT->(MsUnlock())
		TWT->(DbSkip())
	End
EndIf

Return lRetorno


/*/{Protheus.doc} At040HasID
@description	Verifica se existe alguma base com determinado ID e filial origem
@since			30.04.2018
@author 		Matheus Lando Raimundo
@version		P12
@return			Lgico
/*/
Function At040HasID(cNumSer,cFilOri)
Local cTmpAlias := GetNextAlias()
Local lRet		:= .F.
Local cFilRecno      := "%0 = 0%"

//-- Quando estiver alterando a base, desconsidera ela mesmo na query
If ALTERA
	cFilRecno := "% R_E_C_N_O_ <> " + STR( AA3->(RecNo()) ) + "%"
EndIf

If AA3->( ColumnPos('AA3_MSBLQL')) > 0
	BeginSQL ALIAS cTmpAlias
		SELECT 1 FROM %table:AA3% AA3
		WHERE AA3.AA3_FILIAL = %xFilial:AA3% AND
			  AA3.AA3_NUMSER = %Exp:cNumSer% AND
			  AA3.AA3_FILORI = %Exp:cFilOri% AND
			  AA3.AA3_MSBLQL = '2' AND
			  %Exp:cFilRecno%  AND
			  AA3.%NotDel%
	EndSQL
Else
	BeginSQL ALIAS cTmpAlias
		SELECT 1 FROM %table:AA3% AA3
		WHERE AA3.AA3_FILIAL = %xFilial:AA3% AND
			  AA3.AA3_NUMSER = %Exp:cNumSer% AND
			  AA3.AA3_FILORI = %Exp:cFilOri% AND
			  %Exp:cFilRecno%  AND
			  AA3.%NotDel%

	EndSQL
EndIf


lRet := (cTmpAlias)->(!EOF())

Return lRet


/*/{Protheus.doc} At040LoAb
@description	Retorna a Aba de Equipamento de Locao
@since			03.12.2017
@version		P12
@return			cRet - Aba o qual esta os campos de equipamento de Locao
/*/
Static Function At040LoAb()
Local cRet  := "3"
Local aArea := SXA->(GetArea())
Local nC    := 0
Local aSeleFolder := SeleFolder("AA3")

For nC := 1 To Len(aSeleFolder)
	If "EQUIPAMENTO PARA LOCACAO" $ Upper(Rtrim(FwNoAccent(aSeleFolder[nC]))) //No traduzir esta String
		cRet := Left(aSeleFolder[nC],1)
		Exit
	EndIf
Next nC

RestArea(aArea)

Return cRet
//-------------------------------------------------------------------
/*/{Protheus.doc} At040F3MNT()

Construo da consulta especifica

@author boiani
@since 23/06/2019
/*/
//------------------------------------------------------------------
Function At040F3MNT()
Local lRet        := .F.
Local oBrowse     := Nil
Local cAls        := GetNextAlias()
Local nSuperior   := 0
Local nEsquerda   := 0
Local nInferior   := 0
Local nDireita    := 0
Local cQry        := ""
Local aIndex      := {}
Local aSeek       := {}
Local oDlgEscTela := Nil
Local cTitle := STR0057 //"Bens e Prod.Loc."
Local cProd := M->AA3_CODPRO
Local cSpcPrd := SPACE(TamSX3("B1_COD")[1])

Aadd( aSeek, { STR0058, {{"","C",TamSX3("T9_CODBEM")[1],0,STR0058,,}} } ) //"Cdigo do Bem"
Aadd( aSeek, { STR0059, {{"","C",TamSX3("T9_NOME")[1],0,STR0059,,}} } ) //"Nome do Bem"
Aadd( aSeek, { STR0060, {{"","C",TamSX3("T9_CODESTO")[1],0,STR0060,,}} } ) //"Cdigo no Estoque"


Aadd( aIndex, "T9_CODBEM" )
Aadd( aIndex, "T9_NOME" )
Aadd( aIndex, "T9_CODESTO" )
Aadd( aIndex, "T9_FILIAL")  // adicionado para no ter problema de no encontrar o ltimo ndice, em caso de adicionar mais deixe a filial por ltimo

cQry := " SELECT ST9.T9_FILIAL, ST9.T9_CODBEM, ST9.T9_CODIMOB, ST9.T9_CODFAMI, ST9.T9_NOME, ST9.T9_CCUSTO, ST9.T9_SERIE, ST9.T9_CODESTO "
cQry += " FROM " + RetSqlName("ST9") + " ST9 "
cQry += " WHERE (ST9.T9_CODESTO = '" + cProd + "' "
cQry += " OR ST9.T9_CODESTO = '" + cSpcPrd + "') "
cQry += " AND ST9.D_E_L_E_T_ = ' ' "
cQry += " AND ST9.T9_FILIAL = '" + xFilial("ST9") + "' "

cQry := ChangeQuery(cQry)

nSuperior := 0
nEsquerda := 0
nInferior := GetScreenRes()[2] * 0.4
nDireita  := GetScreenRes()[1] * 0.45

DEFINE MSDIALOG oDlgEscTela TITLE cTitle FROM nSuperior,nEsquerda TO nInferior,nDireita PIXEL

oBrowse := FWFormBrowse():New()
oBrowse:SetOwner(oDlgEscTela)
oBrowse:SetDataQuery(.T.)
oBrowse:SetAlias(cAls)
oBrowse:SetQueryIndex(aIndex)
oBrowse:SetQuery(cQry)
oBrowse:SetSeek(,aSeek)
oBrowse:SetDescription(cTitle)
oBrowse:SetMenuDef("")
oBrowse:DisableDetails()

oBrowse:SetDoubleClick({ || cRetF3 := (oBrowse:Alias())->T9_CODBEM, lRet := .T. ,oDlgEscTela:End()})
oBrowse:AddButton( OemTOAnsi(STR0061), {|| cRetF3  := (oBrowse:Alias())->T9_CODBEM, lRet := .T., oDlgEscTela:End() } ,, 2 ) //"Confirmar"
oBrowse:AddButton( OemTOAnsi(STR0062),  {|| cRetF3  := "", oDlgEscTela:End() } ,, 2 ) //"Cancelar"
oBrowse:DisableDetails()
ADD COLUMN oColumn DATA { ||  T9_CODBEM } TITLE GetSX3Cache("T9_CODBEM", "X3_TITULO" ) SIZE TamSX3("T9_CODBEM")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_CODIMOB} TITLE GetSX3Cache("T9_CODIMOB", "X3_TITULO" ) SIZE TamSX3("T9_CODIMOB")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_CODFAMI} TITLE GetSX3Cache("T9_CODFAMI", "X3_TITULO" ) SIZE TamSX3("T9_CODFAMI")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_NOME  	} TITLE GetSX3Cache("T9_NOME", "X3_TITULO" ) SIZE TamSX3("T9_NOME")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_CCUSTO } TITLE GetSX3Cache("T9_CCUSTO", "X3_TITULO" ) SIZE TamSX3("T9_CCUSTO")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_SERIE  } TITLE GetSX3Cache("T9_SERIE", "X3_TITULO" ) SIZE TamSX3("T9_SERIE")[1] OF oBrowse
ADD COLUMN oColumn DATA { ||  T9_CODESTO} TITLE GetSX3Cache("T9_CODESTO", "X3_TITULO" ) SIZE TamSX3("T9_CODESTO")[1] OF oBrowse

oBrowse:Activate()

ACTIVATE MSDIALOG oDlgEscTela CENTERED

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} At040RF3()

Retorno da consulta especifica

@author boiani
@since 23/06/2019
/*/
//------------------------------------------------------------------
Function At040RF3()

Return cRetF3
