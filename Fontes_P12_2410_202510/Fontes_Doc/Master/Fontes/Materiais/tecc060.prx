#INCLUDE "DBTREE.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TECC060.CH"
                                                                       
/*                                                  
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECC060   ³ Autor ³Sergio Silveira        ³ Data ³11/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta as Ordens de Servico em Aberto                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±       
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function TECC060()

Local aArea	     := GetArea() 
Local aPosObj    := {} 
Local aObjects   := {}                        
Local aSize      := MsAdvSize( .F. )   
Local aUsButtons := {}                 
Local aInfo      := {} 
Local aParam     := {} 

Local cCadastro  := STR0001	//"Monitor - Ordem de Servi‡o"

Local lCreated   := .F. 

Local nPosCol    := 0
Local nLinIni    := 0  
Local nLoop      := 0 

Local oDlg
Local oTree
Local oTree2 
Local oTimer 
Local oScroll
Local oScrPanel
Local oPanel 
Local oMenuTree
Local oMenuTree2
Local oMenuScroll

//Variaveis para pegar a quantidade de digitos para utilizar na função HoraToInt
Static nTALOC1 	:= TamSx3("AA7_TALOC1")[1]-1
Static nTALOC2 	:= TamSx3("AA7_TALOC2")[1]-1	
Static nTMAX1		:= TamSx3("AA7_TMAX1")[1]-1
Static nTMAX2		:= TamSx3("AA7_TMAX2")[1]-1
Static nTCHEG1	:= TamSx3("AA7_TCHEG1")[1]-1
Static nTCHEG2	:= TamSx3("AA7_TCHEG2")[1]-1
Static nTSAID1	:= TamSx3("AA7_TSAID1")[1]-1
Static nTSAID2	:= TamSx3("AA7_TSAID2")[1]-1
Static nTATEN1	:= TamSx3("AA7_TATEN1")[1]-1
Static nTATEN2	:= TamSx3("AA7_TATEN2")[1]-1                                                   

PRIVATE nCx := 0
PRIVATE nCy := 0 

If Atc060Perg( @aParam ) 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Resolve a jenela lateralmente                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	aObjects := {} 
	AAdd( aObjects, { 100, 100, .T., .T. } )
	AAdd( aObjects, { 170, 100, .F., .T. } )
	
	aInfo    := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
	aPosObj1 := MsObjSize( aInfo, aObjects, .T., .T. )    
	                     
	nPosCol  := aPosObj1[1,3] * 2 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Resolve os objetos da direita                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	aObjects := {} 
	AAdd( aObjects, { 100, 80, .T., .T., .T. } )
	AAdd( aObjects, { 100, 20, .T., .F., .T. } )
	
	aInfo    := { aPosObj1[ 2,2 ], aPosObj1[ 2,1 ], aPosObj1[ 2,4 ], aPosObj1[ 2,3 ], 3, 3, 0, 0 } 
	aPosObj2 := MsObjSize( aInfo, aObjects )    
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de entrada para a criacao de botoes de usuario                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "ATC060BT" ) 
		aUsButtons := ExecBlock( "ATC060BT", .F., .F. ) 
	EndIf 
	
	DEFINE MSDIALOG oDlg FROM	aSize[7],00 TO aSize[6],aSize[5] TITLE cCadastro OF oMainWnd PIXEL
	oTree := DbTree():New(aPosObj1[1,1],aPosObj1[1,2],aPosObj1[1,3],aPosObj1[1,4], oDlg,,,.T.)
	
	oTree:bChange   := {|| If(oTimer:lActive,Atc060Msg(oTree,oScroll,@lCreated,oTree2,oScrPanel),.F.) }
	oTree:lShowHint := .F.
	
	oTimer:= TTimer():New(SuperGetMv("MV_ATTMPOS")*60*1000,{|| Processa( { || Atc060Reca(oTimer,oTree,oDlg,@aParam) } ) },oDlg)
		
	aCorDescri := { { "FOLDER10", STR0023 }, ; //  "Situacao Normal"
					{ "FOLDER5",  STR0024 }, ;	// "Alerta"	
					{ "FOLDER7",  STR0025 } } 	// "Alerta Maximo"					
	                               
	nLinIni := aPosObj1[2,1]   
	
	@ aPosObj2[1,1], aPosObj2[1,2] MSPANEL oScrPanel PROMPT "" SIZE aPosObj2[1,3],aPosObj2[1,4] OF oDlg CENTERED LOWERED
	                                                                                             
	oTree2  := DbTree():New(aPosObj2[1,1], aPosObj2[1,2], aPosObj2[1,1] + aPosObj2[1,4] ,aPosObj2[1,2]+ aPosObj2[1,3], oDlg,,,.T.)
	oScroll := TScrollBox():New( oDlg, aPosObj2[1,1], aPosObj2[1,2], aPosObj2[1,4],aPosObj2[1,3])
	
	@ aPosObj2[2,1],aPosObj2[2,2] MSPANEL oPanel PROMPT "" SIZE aPosObj2[2,3],aPosObj2[2,4] OF oDlg CENTERED LOWERED //"Botoes"
	
	MENU oMenuTree POPUP 
		MENUITEM STR0007 Action Atc060Os( oTree )  // "Ordem de servico"
		MENUITEM STR0008 Action Atc060Aloc( oTree ) // "Alocacao"
		MENUITEM STR0028 Action Processa( { || Atc060Reca(oTimer,oTree,oDlg,@aParam) } ) //"Atualizar"
		MENUITEM STR0029 Action Atc060Par(oTimer,oTree,oDlg,@aParam) //"Parametros"
		MENUITEM STR0030 Action Atc060Rast(oTree,oTree2,oScroll,oScrPanel) //"Rastrear"
		MENUITEM STR0031 Action Atc060Lin( oTree, oScroll, oPanel, oScrPanel, oTree2, @nPosCol ) //"Mover barra"
	ENDMENU
	       
	MENU oMenuScroll POPUP 
		MENUITEM STR0031  Action Atc060Lin( oTree, oScroll, oPanel, oScrPanel, oTree2, @nPosCol )    //"Mover barra"
	ENDMENU       
	
	MENU oMenuTree2 POPUP 
		MENUITEM STR0032 Action MaPrepView(oTree2) //"Visualizar"
		MENUITEM STR0031 Action Atc060Lin( oTree, oScroll, oPanel, oScrPanel, oTree2, @nPosCol ) //"Mover barra"
	ENDMENU
	                 
	oTree:bRClicked   := { |oObject,nx,ny| nCx := nX,				nCy := nY, oMenuTree:Activate( nX-80, nY-150, oObject ) }
	oTree2:bRClicked  := { |oObject,nx,ny| nCx := nX + nPosCol, 	nCy := nY, oMenuTree2:Activate( nX, nY, oObject ) }
	oScroll:bRClicked := { |oObject,nx,ny| nCx := nX + nPosCol, 	nCy := nY, oMenuScroll:Activate( nX, nY, oObject ) }
	
	DEFINE SBUTTON FROM 003,005 TYPE 1 ENABLE OF oPanel ACTION ( oDlg:End() )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cria o botao a partir da dialog para o correto funcionamento do menu PopUp  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@ aPosObj2[2,1] + 3,aPosObj2[2,2] + 35 BUTTON oBtnD Prompt STR0043 SIZE 35, 13.5 OF oDlg PIXEL ;
			Action (oMenuTree:aItems[6]:Disable(),oMenuTree:Activate((aPosObj2[2,2]+33)*2,(aPosObj2[2,1]+1)*2,oTree),oMenuTree:aItems[6]:Enable()) // Opcoes 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a criacao dos botoes de usuario                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType( aUsButtons ) == "A"
		If !Empty( aUsButtons ) 
			For nLoop := 1 To Len( aUsButtons ) 
				TButton():New( 003, 5 + nLoop * 33,AllTrim(aUsButtons[nLoop,1]),;
							oPanel,aUsButtons[ nLoop, 2 ],30,13,,oDlg:oFont,.F.,.T.,.F.,,.F.,,,.F.)
			Next nLoop 
		EndIf	
	EndIf
		
	ACTIVATE MSDIALOG oDlg ON INIT Processa( { || Atc060Reca(oTimer,oTree,oDlg,@aParam) } ) 

EndIf
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a situacao de entrada                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea( aArea ) 

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Reca³ Autor ³ Sergio Silveira       ³ Data ³11/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recalcula os sinalizadores do Tree                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oTimer : Objeto Timer                                      ³±±
±±³          ³ oTree  : Objeto Tree                                       ³±±
±±³          ³ oDlg	 : Objeto da Janela                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Atc060Reca(oTimer,oTree,oDlg,aParam)

Local aTempos       := Array(10)
LOCAL aCoresBmp     := { "NULL",;
							"PRET" ,; 
							"CINZ" ,;
							"MARR" ,;
							"PINK" ,;
							"VERM" ,;
							"LARA" ,;
							"AMAR" ,;
							"VERD" ,;
							"AZUL" } 									
		
Local aAuxiliar     := {}
Local aFolder       := {}
Local aTmpGeral     := Array( 10 ) 

Local bWhileAB6     := { || .t. } 
Local bWhileAB7     := { || .t. } 
Local bFiltroAB7    := { || .T. } 

Local cFolder1H     := ""
Local cMotivo       := ""
Local cString       := ""   
Local cFolder1      := ""
Local cFolder2      := ""
Local cFolder3      := ""              
Local cChaveAnt     := ""           
Local cNomNumOs     := RetTitle("AB6_NUMOS")
Local cNomCodCli    := RetTitle("AB6_CODCLI")
Local cNomRegiao    := RetTitle("AB6_REGIAO")
Local cFiltro       := ""
Local cFiltroUS     := ""
Local cQuery        := ""  
Local cAliasAB6     := ""
Local cAliasSA1     := ""
Local cGrpAte       := ""
Local cFldBreak 	:= ""

Local lOs           := .F.
Local lQuery        := .F.
Local lFoundAA7     := .F.
Local lAmarracao    := .F.   

Local nFolder       := 0
Local nSaida1       := 0
Local nSaida2       := 0
Local nLoop         := 0          
Local nPriori       := 0 
Local nPrioriAAG    := 0 
Local nColor        := 0 

Atc060RPar( @aParam ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa os parametros genericos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

CursorWait()

aTmpGeral[1]  := SuperGetMV( "MV_ATTALC1" ) 
aTmpGeral[2]  := SuperGetMV( "MV_ATTALC2" ) 
aTmpGeral[3]  := SuperGetMV( "MV_ATTMAX1" )                                       
aTmpGeral[4]  := SuperGetMV( "MV_ATTMAX2" )  
aTmpGeral[5]  := SuperGetMV( "MV_ATTCHG1" ) 
aTmpGeral[6]  := SuperGetMV( "MV_ATTCHG2" ) 
aTmpGeral[7]  := SuperGetMV( "MV_ATTSAI1" )                                       
aTmpGeral[8]  := SuperGetMV( "MV_ATTSAI2" ) 
aTmpGeral[9]  := SuperGetMV( "MV_ATTATE1" )                                       
aTmpGeral[10] := SuperGetMV( "MV_ATTATE2" ) 

cChaveAnt := oTree:GetCargo() 
	
oTimer:DeActivate()
 
oTree:BeginUpdate()
oTree:Reset()                                            
oTree:EndUpdate()

oTree:BeginUpdate()     

cFiltroUS := ""

If ExistBlock( "ATC060FI" ) 
	If ValType( aFiltro := ExistBlock( "ATC060FI" ) ) == "A"
		#IFDEF TOP
			cFiltroUS := aFiltro[ 2 ]
		#ELSE 
			cFiltroUS := aFiltro[ 1 ]
		#ENDIF
	EndIf
EndIf 							

#IFDEF TOP   
             
	lQueryAB6 := .T. 
                                                                 
	cAliasAB6 := GetNextAlias()
	cAliasSA1 := cAliasAB6 

  	cQuery := "SELECT A1_NOME, AB6.* FROM "
	cQuery += RetSqlName( "AB6" ) + " AB6,"
	cQuery += RetSqlName( "SA1" ) + " SA1 "	
  	cQuery += "WHERE "
  	cQuery += "AB6_FILIAL='" + xFilial( "AB6" ) + "' AND "
  	cQuery += "AB6_STATUS='A' AND "  
  	cQuery += "AB6_NUMOS>='" + MV_PAR01 + "' AND "
  	cQuery += "AB6_NUMOS<='" + MV_PAR02 + "' AND "
  	cQuery += "AB6_EMISSA>='"+ DToS( MV_PAR03 ) + "' AND "
  	cQuery += "AB6_EMISSA<='"+ DToS( MV_PAR04 ) + "' AND "  	
  	cQuery += "AB6_CODCLI>='"+ MV_PAR05 + "' AND "
  	cQuery += "AB6_CODCLI<='"+ MV_PAR07 + "' AND "
  	cQuery += "AB6_LOJA>='"  + MV_PAR06 + "' AND "
 	cQuery += "AB6_LOJA<='"  + MV_PAR08 + "' AND "
  	cQuery += "AB6_REGIAO>='"+ MV_PAR09 + "' AND "
  	cQuery += "AB6_REGIAO<='"+ MV_PAR10 + "' AND "

  	cQuery += cFiltroUS
  	cQuery += "AB6.D_E_L_E_T_=' ' AND "
  	
  	cQuery += "EXISTS ( SELECT AB7_ITEM FROM " + RetSqlName( "AB7" ) + " AB7 WHERE "
  	cQuery += "AB7_NUMOS=AB6_NUMOS AND "
  	                                                      
  	cQuery += "AB7_FILIAL='"  + xFilial( "AB7" ) + "' AND "  	
  	cQuery += "AB7_CODPRO>='" + MV_PAR11 + "' AND "
  	cQuery += "AB7_CODPRO<='" + MV_PAR12 + "' AND "  	
  	cQuery += "AB7_NUMSER>='" + MV_PAR13 + "' AND "
  	cQuery += "AB7_NUMSER<='" + MV_PAR14 + "' AND "  	
  	cQuery += "AB7.D_E_L_E_T_=' ' ) AND "
  	      
  	cQuery += "A1_FILIAL='" + xFilial( "SA1" ) + "' AND "  	
  	cQuery += "( A1_COD=AB6_CODCLI AND A1_LOJA=AB6_LOJA ) AND "
  	cQuery += "SA1.D_E_L_E_T_=' '"        	
   	
	cQuery += "ORDER BY "
	If ValType( MV_PAR15 ) == "N"
		If MV_PAR15 == 1 .OR. MV_PAR15 == 3		//1- Regiao ou 3- Contrato
			cQuery += " AB6_CONTRT, AB6_CODCLI, AB6_LOJA "
		ElseIf MV_PAR15 == 2	//2- Cliente
			cQuery += " AB6_CODCLI, AB6_LOJA "
		EndIf
	Else
		cQuery += " AB6_REGIAO, AB6_CODCLI, AB6_LOJA "
	EndIf

	cQuery := ChangeQuery( cQuery )     
   	
	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB6, .F., .T. ) 
   	
	bWhileAB6 := { || !( cAliasAB6 )->( Eof() ) } 
	
 	bFiltro   := { || .T. } 

#ELSE
                    
	lQueryAB6 := .F. 
 
	cAliasAB6 := "AB6"     
	cAliasSA1 := "SA1"
	
	dbSelectArea("AB6")
	dbSetOrder(4)
	dbSeek(xFilial("AB6")+"A"+MV_PAR05+MV_PAR06,.T.)
      
   bWhileAB6 := { || ( !AB6->( Eof() ) .And. xFilial("AB6") == AB6->AB6_FILIAL .And.;
							AB6->AB6_STATUS == "A" .And. AB6->AB6_CODCLI + AB6->AB6_LOJA <= MV_PAR07 + MV_PAR08 ) } 
							                               
	cFiltro := ""
							
  	cFiltro += "AB6->AB6_NUMOS>=MV_PAR01  .AND. "
  	cFiltro += "AB6->AB6_NUMOS<=MV_PAR02  .AND. "
  	cFiltro += "AB6->AB6_EMISSA>=MV_PAR03 .AND. "
  	cFiltro += "AB6->AB6_EMISSA<=MV_PAR04 .AND. "  	
  	cFiltro += "AB6->AB6_REGIAO>=MV_PAR09 .AND. "
  	cFiltro += "AB6->AB6_REGIAO<=MV_PAR10"
  	
	cFiltro += cFiltroUS   	
	
	bFiltro := &( " { || " + cFiltro + " } " ) 
							
#ENDIF                       


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica por novas Ordens de Servico                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While Eval( bWhileAB6 ) 
                        
	If Eval( bFiltro ) 

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Inicializa a Variaveis com base nos parametros genericos ou com zz:zz   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lOs := .F.
			
		AFill(aTempos,"zz:zz")      
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Registros                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa aTempos com base em atmpgeral                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		aEval( aTmpGeral, { |x,y| aTempos[y] := If(Empty(x), "zz:zz", IntToHora(x) ) } ) 
	
		aTempos[7]	:= 0
		aTempos[8]	:= 0
			
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Registros                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		If !lQuery 
			SA1->( dbSetOrder( 1 ) )  
			SA1->( DbSeek(xFilial("SA1")+( cAliasAB6 )->AB6_CODCLI+( cAliasAB6 )->AB6_LOJA) ) 
		EndIf 	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calcula os Tempos definidos na Tabela : AA7                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica os Tempos da Ordem de Servico - Configurados no AA7            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		lQuery := .F. 
		
		#IFDEF TOP   
		
			lQuery := .T. 
		                                                                 
			cAliasAB7 := GetNextAlias()
		
	   	cQuery := "SELECT AB7_CODPRO, AB7_NUMSER, AB7_CODPRB, AB7_TIPO, AB7_FILIAL, AAG_PRIORI, AAG_COR FROM " 
			cQuery += RetSqlName( "AB7" ) + " AB7," + RetSqlName( "AAG" ) + " AAG "   	
	   	cQuery += "WHERE "
	   	cQuery += "AB7_FILIAL='" + xFilial( "AB7" ) + "' AND "
	   	cQuery += "AB7_NUMOS='"  + ( cAliasAB6 )->AB6_NUMOS   + "' AND "
	   	cQuery += "AB7_TIPO IN ( '1', '3' ) AND "
	   	cQuery += "AB7.D_E_L_E_T_=' ' AND "
	   	 
	   	cQuery += "AB7_CODPRB=AAG_CODPRB AND "
	   	
	   	cQuery += "AAG_FILIAL='" + xFilial( "AAG" ) + "' AND "
	   	cQuery += "AAG.D_E_L_E_T_=' '"   	
	   	
	   	cQuery := ChangeQuery( cQuery ) 
	   	
	   	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB7, .F., .T. ) 
	   	
	   	bWhileAB7  := { || !( cAliasAB7 )->( Eof() ) } 
	   	bFiltroAB7 := { || .T. } 
	
	   #ELSE
	                    
	 		lQuery := .F. 
	 
			cAliasAB7 := "AB7" 
	 
		  	AB7->( dbSetOrder( 1 ) ) 
			AB7->( dbSeek(xFilial("AB7")+( cAliasAB6 )->AB6_NUMOS ) ) 
	      
	   	bWhileAB7  := { || ( !AB7->( Eof() ) .And. xFilial("AB7") == AB7->AB7_FILIAL .And.;
												AB6->AB6_NUMOS == AB7->AB7_NUMOS )	} 
												
			bFiltroAB7 := { || AB7->AB7_TIPO $ "13" .And. AB7->AB7_CODPRO >= MV_PAR11 .And. ;
					AB7->AB7_CODPRO <= MV_PAR12 .And. AB7->AB7_NUMSER >= MV_PAR13 .And. ;
					AB7->AB7_NUMSER <= MV_PAR14 } 
												
	   #ENDIF                       
	   
	   nPriori := 3 
		
		While Eval( bWhileAB7 )      
												
			If Eval( bFiltroAB7 ) 
			
				lOs       := .T.
				lFoundAA7 := .F. 
				
				If lQuery                          
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Obtem a ocorrencia da query             ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nPrioriAAG := Val( ( cAliasAB7 )->AAG_PRIORI )  
				
					If nPrioriAAG <= nPriori 
		
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Seleciona a primeira cor disponivel para o item de priridade maior ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						nPriori := nPrioriAAG		
						nColor  := Val( ( cAliasAB7 )->AAG_COR )
						 	
					EndIf 
			
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Pesquisa o codigo de ocorrencia no AAG  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					AAG->( dbSetOrder( 1 ) ) 
					If	AAG->( DbSeek( xFilial( "AAG" ) + AB7->AB7_CODPRB ) ) 	
				
						nPrioriAAG := Val( AAG->AAG_PRIORI )  
					
						If nPrioriAAG <= nPriori 
			
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³ Seleciona a primeira cor disponivel para o item de priridade maior ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							nPriori := nPrioriAAG		
							nColor  := Val( AAG->AAG_COR )
							 	
						EndIf 
					Else 	
						lOs := .F. 
						Exit
					EndIf	
					
				EndIf 	          
				
				cGrpAte := Space( Len( AAP->AAP_GRPATE ) ) 
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se este equipamento possui um grupo de atendimento especifico  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				AA3->( dbSetOrder( 1 ) ) 	
				If AA3->( DbSeek( xFilial( "AA3" ) + ( cAliasAB6 )->AB6_CODCLI + ( cAliasAB6 )->AB6_LOJA + ( cAliasAB7 )->AB7_CODPRO + ( cAliasAB7 )->AB7_NUMSER ) ) 
				                                      
					If !Empty( AA3->AA3_CONTRT ) 
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Obtem o grupo de atendimento associado a este contrato                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cGrpAte := AtRetGrpAt( AA3->AA3_CONTRT, .T. )
					EndIf 
				EndIf
			
				AA7->( dbSetOrder( 1 ) ) 
				If AA7->( DbSeek(xFilial("AA7")+( cAliasAB7 )->AB7_CODPRO+( cAliasAB7 )->AB7_CODPRB + cGrpAte ) )
					lFoundAA7 := .T. 		
				ElseIf AA7->( dbSeek(xFilial("AA7")+( cAliasAB7 )->AB7_CODPRO+Space(Len(AA7->AA7_CODPRB) ) + cGrpAte ) )
					lFoundAA7 := .T. 						   
				ElseIf AA7->( dbSeek(xFilial("AA7") + Space(Len(AA7->AA7_CODPRO) ) + ( cAliasAB7 )->AB7_CODPRB + cGrpAte ) )
					lFoundAA7 := .T. 				
				EndIf
		        
				If lFoundAA7 
					aTempos[1]  := If(aTempos[1]<AA7->AA7_TALOC1,aTempos[1],AA7->AA7_TALOC1)
					aTempos[2]  := If(aTempos[2]<AA7->AA7_TALOC2,aTempos[2],AA7->AA7_TALOC2)
					aTempos[3]  := If(aTempos[3]<AA7->AA7_TMAX1 ,aTempos[3],AA7->AA7_TMAX1)
					aTempos[4]  := If(aTempos[4]<AA7->AA7_TMAX2 ,aTempos[4],AA7->AA7_TMAX2)
					aTempos[5]  := If(aTempos[5]<AA7->AA7_TCHEG1,aTempos[5],AA7->AA7_TCHEG1)
					aTempos[6]  := If(aTempos[6]<AA7->AA7_TCHEG2,aTempos[6],AA7->AA7_TCHEG2)
					aTempos[7]  += HoraToInt(AA7->AA7_TSAID1,nTSAID1)
					aTempos[8]  += HoraToInt(AA7->AA7_TSAID2,nTSAID2)
					aTempos[9]  := If(aTempos[9] <AA7->AA7_TATEN1,aTempos[9] ,AA7->AA7_TATEN1)
					aTempos[10] := If(aTempos[10]<AA7->AA7_TATEN2,aTempos[10],AA7->AA7_TATEN2)
					lAmarracao  := .T. 
			
				EndIf
				
			EndIf 			
				
			( cAliasAB7 )->( dbSkip() )
	
		EndDo        
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Fecha a area de trabalho da query                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP
			dbSelectArea( cAliasAB7 )
			dbCloseArea()
			dbSelectArea( "AB7" ) 
		#ENDIF 	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Reavalia aTempos para retirar os "zz:zz"                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		aEval(aTempos,{|x,y| aTempos[y] := If(x=="zz:zz","",x)}, 1 , 6 )	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calcula o Status e a Mensagem                                           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lOS )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³1. Tempo Decorrido                                                      ³
			//³2. Intervalo entre Chamado e Alocacao                                   ³
			//³3. Chegou ?                                                             ³
			//³4. Numero de Horas Alocadas                                             ³
			//³5. Data/Hora do Chamado                                                 ³
			//³6. Data/Hora da Alocacao                                                ³
			//³7. Data/Hora de Termino                                                 ³ 
			//³8. Confirmou atendimento ?                                              ³		
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aAuxiliar:= AtOsTempo(( cAliasAB6 )->AB6_NUMOS)

			// Tipo de Quebra
			If ValType( MV_PAR15 ) == "N"
				If MV_PAR15 == 1 .OR. MV_PAR15 == 3		//1- Regiao ou 3- Contrato
					cString 	:= AllTrim( cNomNumOs ) +" : "+( cAliasAB6 )->AB6_NUMOS+Space(12)+;
									STR0002+ IntToHora( aAuxiliar[1], 5 )+Space(12)+; //"Tempo(Hs): "
									AllTrim( cNomCodCli ) +": "+( cAliasAB6 )->AB6_CODCLI+"/"+( cAliasAB6 )->AB6_LOJA+" - " + Capital( ( cAliasSA1 )->A1_NOME )
				ElseIf MV_PAR15 == 2	//2- Cliente
					cString 	:= AllTrim( cNomNumOs ) +" : "+( cAliasAB6 )->AB6_NUMOS+Space(12)+;
									STR0002+ IntToHora( aAuxiliar[1], 5 )+Space(12)+; //"Tempo(Hs): "
									AllTrim( cNomRegiao ) +": "+( cAliasAB6 )->AB6_REGIAO
				EndIf
			Else
				cString 	:= AllTrim( cNomNumOs ) +" : "+( cAliasAB6 )->AB6_NUMOS+Space(12)+;
								STR0002+ IntToHora( aAuxiliar[1], 5 )+Space(12)+; //"Tempo(Hs): "
								AllTrim( cNomCodCli ) +": "+( cAliasAB6 )->AB6_CODCLI+"/"+( cAliasAB6 )->AB6_LOJA+" - " + Capital( ( cAliasSA1 )->A1_NOME )
			EndIf

			nFolder := 0
			cMotivo := ""
			If ( !Empty(aAuxiliar[7][1]) )
				nSaida1 := SubtHoras(aAuxiliar[7][1],aAuxiliar[7][2],dDataBase,Time())
				nSaida2 := nSaida1
				nSaida1 -= aTempos[7]
				nSaida2 -= aTempos[8]
			Else
				nSaida1 := 0
				nSaida2 := 0
			Endif
			
			Do Case
			
				Case ( nSaida2 > 0 .And. aTempos[8] <> 0 )
					nFolder := 7
					cMotivo	:= STR0003	//"TERMINO DA OS NAO CONFIRMADA"
				Case ( nSaida1 > 0 .And. aTempos[7] <> 0 )
					nFolder := 5
					cMotivo	:= STR0003	//"TERMINO DA OS NAO CONFIRMADA"
				Case ( aAuxiliar[1] > HoraToInt(aTempos[2],nTALOC2) .And. aAuxiliar[4]==0 .And. !Empty(aTempos[2]) )
					nFolder := 7
					cMotivo	:= STR0005	//"ALOCACAO NAO EFETUADA"
				Case ( aAuxiliar[1] > HoraToInt(aTempos[1],nTALOC1) .And. aAuxiliar[4]==0 .And. !Empty(aTempos[1]) )
					nFolder := 5
					cMotivo	:= STR0005	//"ALOCACAO NAO EFETUADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[6],nTCHEG2)+aAuxiliar[2]) .And. !aAuxiliar[3] .And. !Empty(aTempos[6]) )
					nFolder := 7
					cMotivo	:= STR0004	//"CHEGADA NAO CONFIRMADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[5],nTCHEG1)+aAuxiliar[2]) .And. !aAuxiliar[3] .And. !Empty(aTempos[5]) )
					nFolder := 5
					cMotivo	:= STR0004	//"CHEGADA NAO CONFIRMADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[10],nTATEN2) +aAuxiliar[2]) .And. !aAuxiliar[8] .And. !Empty(aTempos[10]) )
					nFolder := 7
					cMotivo	:= STR0022	//"ATENDIMENTO NAO CONFIRMADO"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[9],nTATEN1)+aAuxiliar[2]) .And. !aAuxiliar[8] .And. !Empty(aTempos[9]) )
					nFolder := 5
					cMotivo	:= STR0022	//"ATENDIMENTO NAO CONFIRMADO"
				Case ( aAuxiliar[2] > HoraToInt(aTempos[4],nTMAX2) .And. !Empty(aTempos[4]) )
					nFolder := 7
					cMotivo	:= STR0006	//"TEMPO EXCESSIVO PARA ALOCAR"
				Case ( aAuxiliar[2] > HoraToInt(aTempos[3],nTMAX1) .And. !Empty(aTempos[3]))
					nFolder := 5
					cMotivo	:= STR0006	//"TEMPO EXCESSIVO PARA ALOCAR"
			EndCase
			If ( nFolder == 0 )
				nFolder := 10
			EndIf  
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Seleciona o Bitmap combinado de Folder + Cor                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			cFolder1  := "F"+AllTrim(Str(nFolder))   + "_" + aCoresBmp[ nColor + 1 ]  
			cFolder1H := "F"+AllTrim(Str(nFolder))   + "_NULL" 			
			cFolder2  := "F"+AllTrim(Str(nFolder+1)) + "_NULL" 
			
			cFolder3  := cFolder1
	
			// Determina o campo da quebra
			cFldBreak := TC060Quebra()
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Troca o BitMap do Item                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se o item possui Regiao                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (! oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak) ) )
				
				oTree:TreeSeek(Repl("z",Len(( cAliasAB6 )->&(cFldBreak))+Len(( cAliasAB6 )->AB6_NUMOS)))
				oTree:addItem(PadR(RetTitle( cFldBreak ) +": "+( cAliasAB6 )->&(cFldBreak),120),( cAliasAB6 )->&(cFldBreak)+Space(6),cFolder1H,cFolder2,,,1)
				oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak))
				
				oTree:ChangeBmp(cFolder1H,cFolder2)				
				
			EndIf                              
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se encontrou esta OS                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)+( cAliasAB6 )->AB6_NUMOS) )    
				oTree:ChangeBmp(cFolder3,cFolder3)
			Else
				oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak))
				oTree:addItem(cString,( cAliasAB6 )->&(cFldBreak)+( cAliasAB6 )->AB6_NUMOS,cFolder3,cFolder3,,,2)
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Troca o BitMap da Regiao                                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)) )
				aFolder := oTree:ChangeBmp()
		
				If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)) )
					oTree:ChangeBmp(cFolder1H,cFolder2)
				EndIf
				
				If ( "F10_" $ Upper(oTree:aBmps[aFolder[1]][1]) .Or. ( "F5_" $ Upper(oTree:aBmps[aFolder[1]][1]) .And. nFolder == 7 ))
				
					If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)) )
						oTree:ChangeBmp(cFolder1H,cFolder2)
					EndIf
				Else
					If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)) )
						oTree:ChangeBmp(oTree:aBmps[aFolder[1]][1],oTree:aBmps[aFolder[2]][1])
					EndIf
				EndIf
	
			EndIf
			
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Deleta o Item                                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( oTree:TreeSeek(( cAliasAB6 )->&(cFldBreak)+( cAliasAB6 )->AB6_NUMOS) )
				oTree:DelItem()
			EndIf
	
		EndIf
		
	EndIf 	
		
	( cAliasAB6 )->( dbSkip() ) 
	
EndDo   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha a area de trabalho da query                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP
	dbSelectArea( cAliasAB6 )
	dbCloseArea()
	dbSelectArea( "AB6" ) 
#ENDIF 	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Forca a atualizacao dos objetos                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

oTree:EndUpdate()

oTree:TreeSeek(cChaveAnt) 

CursorArrow()

oTree:Refresh()
oTimer:Activate()

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ Atc060OS ³ Autor ³ Sergio Silveira       ³ Data ³16/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualizacao da Ordem de Servico - Selecionada             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Os( ExpO1 )                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 : Objeto Tree                                        ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Atc060Os(oTree)

Local aArea		:= GetArea()
Local cSeek 	:= oTree:GetCargo()
Local cFldBreak	:= TC060Quebra()

PRIVATE aRotina := {	{ "","AllwaysTrue",0,2},;
							{ "","AllwaysTrue",0,2}}
PRIVATE INCLUI  := .F.
PRIVATE ALTERA  := .F.

cSeek := SubStr(cSeek,Len(AB6->&(cFldBreak))+1,Len(AB6->AB6_NUMOS))

dbSelectArea("AB6")
dbSetOrder(1)
If ( DbSeek(xFilial("AB6")+cSeek) .And. !Empty(cSeek))
	At450Visua("AB6",AB6->(RecNo()),1)
Else
	Help(" ",1,"ATC040OS")
EndIf

RestArea( aArea ) 

CursorArrow()

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Aloc³ Autor ³Sergio Silveira        ³ Data ³15/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra as alocacoes da Ordem de Servico.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Aloc( ExpO1 )                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1 : Objeto Tree                                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³26/01/2007³ Conrado Q.    ³Bops 115762: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDados.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Atc060Aloc(oTree)

Local aArea		:= GetArea()
Local cSeek		:= oTree:GetCargo()	// Seek para montagem do aCols
Local cWhile	:= ""				// While para montagem do aCols
Local nCntFor	:= 0
Local nUsado	:= 0
Local oDlg		:= Nil
Local oGetD		:= Nil
Local cCadastro	:= STR0009	//"Aloca‡„o da Ordem de Servi‡o"
Local cFldBreak	:= TC060Quebra()

Local aRet       := {}
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Private aCols	:= {}
Private aHeader	:= {}
Private aRotina := {{ "","AllwaysTrue",0,2}}
Private INCLUI  := .F.
Private ALTERA  := .F.
Private dData	:= dDataBase

cSeek := SubStr(cSeek,Len(AB6->&(cFldBreak))+1,Len(AB6->AB6_NUMOS))

dbSelectArea("AB6")
dbSetOrder(1)
If ( dbSeek(xFilial("AB6")+cSeek) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Montagem aHeader e aCols, com adição do campo ABB_CODTEC³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aStruField := FWSX3Util():GetAllFields("ABB")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField)
			cField := aStruField[nC]
			If (AllTrim(cField)$"ABB_CODTEC")
				aRet := FwTamSx3(cField)
				aadd(aHeader,{AllTrim(FWX3Titulo(cField)),;
							cField,;
							AllTrim(X3Picture(cField)),;
							aRet[1],;
							aRet[2],;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							aRet[3],;
							"ABB",;
							GetSx3Cache(cField, "X3_CONTEXT")})
			EndIf
		Next nC
	EndIf

	dbSelectArea("SA1")
	dbSetOrder(1)
	dbSeek(xFilial("SA1")+AB6->AB6_CODCLI+AB6->AB6_LOJA)
	
	cWhile := "ABB->ABB_FILIAL+ABB->ABB_NUMOS"

	FillGetDados(	1				,"ABB"			,3				,xFilial("ABB")+cSeek	,;
	   				{|| &cWhile }	,{|| .T. }		,{"ABB_CODTEC"}	,/*aYesFields*/			,; 
	   				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/				,;
	   				/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/		)
    
	If ( aCols[ 1 ][ aScan( aHeader, {|x| x[2] == "ABB_REC_WT"} ) ] <> 0 )
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM 09,0 TO 28,80 OF oMainWnd
		@ 015,010 SAY   RetTitle("AB6_CODCLI") 	SIZE 060,009 	OF oDlg PIXEL
		@ 015,050 MSGET SA1->A1_NOME					SIZE 136,009 	OF oDlg PIXEL READONLY
		oGetd:=MsGetDados():New(030,005,138,314,1,"","","",.F.)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})
	Else
		Help(" ",1,"ATC040NALO")
	EndIf
Else
	Help(" ",1,"ATC040OS")
EndIf

RestArea( aArea ) 

CursorArrow()

Return(Nil)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Msg ³ Autor ³ Sergio Silveira       ³ Data ³11/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcular e Exibir Janela de Analise da Situacao            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Msg( ExpO1, ExpO2, ExpL1 )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto Tree                                       ³±±
±±³          ³ ExpO2 -> Objeto ScrollBox                                  ³±±
±±³          ³ ExpL1 -> Indica se os controles ja foram criados           ³±± 
±±³          ³ Expo3 -> Objeto tree do Tracker                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function Atc060Msg(oTree, oScroll, lCreated, oTree2, oScrPanel )

Local aTmpGeral  := Array(10) 
Local aTempos    := Array(10) 
Local aAuxiliar  := {}
Local aValor 	  := Array(10)

Local bControl   := { |x| y := Upper(x), AScan( oScroll:Cargo, { |x| Upper( x[1] ) == y } ) } 
Local bWhileAB7     := { || .T. } 

Local cSeek      := oTree:GetCargo()
Local cHoraSai1  := ""
Local cHoraSai2  := ""
Local cHoraBase  := SubStr(Time(),1,5)
Local cQuery     := ""
Local cAliasAB7  := ""
Local cFldBreak	 := TC060Quebra()
	
Local dDataSai1  := Ctod("")
Local dDataSai2  := Ctod("")

Local lAmarracao := .F. 
Local lTmpAloc   := .F.
Local lTmpIni    := .F.
Local lTmpCheg   := .F. 
Local lTmpAten   := .F.
Local lTmpTerm   := .F.
Local lOs        := .F.

Local nLoop                   

Local oSay1
Local oSay2
Local oSay3
Local oSay4
Local oSay5
Local oSay6
Local oSay7
Local oSay8
Local oSay9
Local oSay10
Local oSay11
Local oSay12
Local oSay13
Local oSay14
Local oSay15
Local oSay16
Local oSay17
Local oSay18
Local oSay19
Local oSay20            
Local oSay21
Local oSay22
Local oSay23
Local oSay24
Local oSay25
Local oSay26
Local oSay27
Local oSay30
Local oSay31
Local oSay32
Local oSay33
Local oSay34
Local oDlg
Local oGet

AFill( aTmpGeral, 0 )                      

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa os parametros genericos                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aTmpGeral[1] := SuperGetMV( "MV_ATTALC1" ) 
aTmpGeral[2] := SuperGetMV( "MV_ATTALC2" ) 
aTmpGeral[3] := SuperGetMV( "MV_ATTMAX1" )                                       
aTmpGeral[4] := SuperGetMV( "MV_ATTMAX2" )  
aTmpGeral[5] := SuperGetMV( "MV_ATTCHG1" ) 
aTmpGeral[6] := SuperGetMV( "MV_ATTCHG2" ) 
aTmpGeral[7] := SuperGetMV( "MV_ATTSAI1" )                                       
aTmpGeral[8] := SuperGetMV( "MV_ATTSAI2" ) 
aTmpGeral[9] := SuperGetMV( "MV_ATTATE1" )                                       
aTmpGeral[10]:= SuperGetMV( "MV_ATTATE2" ) 

cSeek := SubStr(cSeek,Len(AB6->&(cFldBreak))+1,Len(AB6->AB6_NUMOS))

AB6->( dbSetOrder( 1 ) )
If AB6->( dbSeek( xFilial( "AB6" ) + cSeek ) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Inicializa as Variaveis                                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Afill(aTempos,"zz:zz")
    
	aFill(aValor,0)
	
	aTempos[7]	:= 0
	aTempos[8]	:= 0
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os Tempos da Ordem de Servico - Configurados no AA7            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP 
	                                                                 
		cAliasAB7 := GetNextAlias()
	
   	cQuery := "SELECT AB7_CODPRO, AB7_NUMSER, AB7_CODPRB, AB7_TIPO, AB7_FILIAL FROM " + RetSqlName( "AB7" ) + " "
   	cQuery += "WHERE "
   	cQuery += "AB7_FILIAL='" + xFilial( "AB7" ) + "' AND "
   	cQuery += "AB7_NUMOS='"  + AB6->AB6_NUMOS   + "' AND "
   	cQuery += "AB7_TIPO IN ( '1', '3' ) AND "
   	cQuery += "D_E_L_E_T_=' ' "
   	
   	cQuery := ChangeQuery( cQuery ) 
   	
   	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB7, .F., .T. ) 
   	
   	bWhileAB7 := { || !( cAliasAB7 )->( Eof() ) } 

   #ELSE
 
		cAliasAB7 := "AB7" 
 
	  	AB7->( dbSetOrder( 1 ) ) 
		AB7->( dbSeek(xFilial("AB7")+AB6->AB6_NUMOS ) ) 
      
   	bWhileAB7 := { || ( !AB7->( Eof() ) .And. xFilial("AB7") == AB7->AB7_FILIAL .And.;
											AB6->AB6_NUMOS == AB7->AB7_NUMOS )	} 
   #ENDIF
	
	While Eval( bWhileAB7 ) 
											
		If ( ( cAliasAB7 )->AB7_TIPO $ "13" )
		
			cGrpAte := Space( Len( AAP->AAP_GRPATE ) ) 
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se este equipamento possui um grupo de atendimento especifico  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AA3->( dbSetOrder( 1 ) ) 	
			If AA3->( DbSeek( xFilial( "AA3" ) + AB6->AB6_CODCLI + AB6->AB6_LOJA + ( cAliasAB7 )->AB7_CODPRO + ( cAliasAB7 )->AB7_NUMSER ) ) 
			                                      
				If !Empty( AA3->AA3_CONTRT ) 
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Obtem o grupo de atendimento associado a este contrato                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cGrpAte := AtRetGrpAt( AA3->AA3_CONTRT, .T. )
				EndIf 
				
			EndIf
		
			lOs       := .T.
			lFoundAA7 := .F. 
		
			AA7->( dbSetOrder( 1 ) ) 
			If AA7->( DbSeek(xFilial("AA7")+( cAliasAB7 )->AB7_CODPRO+( cAliasAB7 )->AB7_CODPRB + cGrpAte ) )
				lFoundAA7 := .T. 		
			ElseIf AA7->( dbSeek(xFilial("AA7")+( cAliasAB7 )->AB7_CODPRO+Space(Len(AA7->AA7_CODPRB) ) + cGrpAte ) )
				lFoundAA7 := .T. 						   
			ElseIf AA7->( dbSeek(xFilial("AA7") + Space(Len(AA7->AA7_CODPRO) ) + ( cAliasAB7 )->AB7_CODPRB + cGrpAte ) )
				lFoundAA7 := .T. 				
			EndIf
	        
			If lFoundAA7 
				aTempos[1]  := If(aTempos[1]<AA7->AA7_TALOC1,aTempos[1],AA7->AA7_TALOC1)
				aTempos[2]  := If(aTempos[2]<AA7->AA7_TALOC2,aTempos[2],AA7->AA7_TALOC2)
				aTempos[3]  := If(aTempos[3]<AA7->AA7_TMAX1 ,aTempos[3],AA7->AA7_TMAX1)
				aTempos[4]  := If(aTempos[4]<AA7->AA7_TMAX2 ,aTempos[4],AA7->AA7_TMAX2)
				aTempos[5]  := If(aTempos[5]<AA7->AA7_TCHEG1,aTempos[5],AA7->AA7_TCHEG1)
				aTempos[6]  := If(aTempos[6]<AA7->AA7_TCHEG2,aTempos[6],AA7->AA7_TCHEG2)
				aTempos[7]  += HoraToInt(AA7->AA7_TSAID1,nTSAID1)
				aTempos[8]  += HoraToInt(AA7->AA7_TSAID2,nTSAID2)
				aTempos[9]  := If(aTempos[9] <AA7->AA7_TATEN1,aTempos[9] ,AA7->AA7_TATEN1)
				aTempos[10] := If(aTempos[10]<AA7->AA7_TATEN2,aTempos[10],AA7->AA7_TATEN2)
				lAmarracao  := .T. 
		
			EndIf
			
		EndIf 			
			
		( cAliasAB7 )->( dbSkip() )

	EndDo        
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Fecha a area de trabalho da query                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		dbSelectArea( cAliasAB7 )
		dbCloseArea()
		dbSelectArea( "AB7" ) 
	#ENDIF 	

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se nao ha amarracao, inicializa os parametros genericos                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lAmarracao 
		For nLoop := 1 to 10 
			aTempos[nLoop] := If(Empty(aTmpGeral[nLoop]), "zz:zz", IntToHora(aTmpGeral[nLoop])) 
		Next nLoop 	
		aTempos[7] := aTmpGeral[7] 
		aTempos[8] := aTmpGeral[8]  
	EndIf 	
	
	If ( lOs )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³1. Tempo Decorrido                                                      ³
		//³2. Intervalo entre Chamado e Alocacao                                   ³
		//³3. Chegou ?                                                             ³
		//³4. Numero de Horas Alocadas                                             ³
		//³5. Data/Hora do Chamado                                                 ³
		//³6. Data/Hora da Alocacao                                                ³
		//³7. Data/Hora de Termino                                                 ³ 
		//³8. Confirmou atendimento ?                                              ³		
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAuxiliar:= AtOsTempo(AB6->AB6_NUMOS)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta a Janela                                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lCreated                                        
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua a inclusao dos controles                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		   oScroll:Cargo := {}

			DEFINE FONT oBold    NAME "Arial" SIZE 0, -13 BOLD
			DEFINE FONT oBoldUnd NAME "Arial" SIZE 0, -13 BOLD	UNDERLINE 		
											                      
			oScroll:Hide()								 
			
			@ 010,010 SAY oSay1  PROMPT "" SIZE 120,008 OF oScroll PIXEL FONT oBold
			@ 031,010 SAY STR0015 				SIZE 060,008 OF oScroll PIXEL	//"Chamado: "
			@ 031,050 SAY oSay2 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			@ 031,090 SAY oSay3 PROMPT "" SIZE 035,008 OF oScroll PIXEL PICTURE "99:99" 
			
			@ 041,010 SAY STR0016 				SIZE 060,008 OF oScroll PIXEL	//"Atual: "
			@ 041,050 SAY oSay4 PROMPT ""                SIZE 035,008 OF oScroll PIXEL 
			@ 041,090 SAY oSay5 PROMPT ""	               SIZE 035,008 OF oScroll PIXEL PICTURE "99:99" 
			@ 031,120 SAY STR0017					SIZE 030,008 OF oScroll PIXEL //"Decorridas "

			@ 041,120 SAY oSay6 PROMPT ""	SIZE 035,008 OF oScroll PIXEL FONT oBold   
			
			@ 051,010 SAY oSay30 PROMPT STR0033 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL //"Tempo para alocar ( nao alocado )"
			
			@ 064,010 SAY RetTitle("AA7_TALOC1")+":"		SIZE 060,008 OF oScroll PIXEL
			
			@ 064,050 SAY oSay7 PROMPT ""	SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TALOC1") 
			@ 064,120 SAY oSay8 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			
			@ 074,010 SAY RetTitle("AA7_TALOC2")+":"		SIZE 060,008 OF oScroll PIXEL
			
			@ 074,050 SAY oSay9 PROMPT  "" SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TALOC2") 			
			@ 074,120 SAY oSay10 PROMPT "" SIZE 035,008 OF oScroll PIXEL 

			@ 084,010 SAY oSay35 PROMPT STR0034 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL			 //"Alocacao - Inicio"
			@ 097,010 SAY oSay36 PROMPT "" SIZE 120,008 OF oScroll PIXEL			
	
			
			@ 107,010 SAY oSay31 PROMPT STR0035 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL			 //"Tempo ate a inicio ( ja alocado )"
			
			@ 120,010 SAY RetTitle("AA7_TMAX1")+":"		SIZE 060,008 OF oScroll PIXEL
			@ 120,050 SAY oSay11 PROMPT "" SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TMAX1") 
			@ 120,120 SAY oSay12 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			
			@ 130,010 SAY RetTitle("AA7_TMAX2")+":"		SIZE 060,008 OF oScroll PIXEL                
			
			@ 130,050 SAY oSay13 PROMPT "" SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TMAX2") 
			@ 130,120 SAY oSay14 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			                                                                                             
			@ 140,010 SAY oSay32 PROMPT STR0036 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL			 //"Tempo para confirmacao de chegada"
			
			@ 153,010 SAY RetTitle("AA7_TCHEG1")+":"		SIZE 060,008 OF oScroll PIXEL
			
			@ 153,050 SAY oSay15 PROMPT "" SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TCHEG1")
			@ 153,120 SAY oSay16 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			
			@ 163,010 SAY RetTitle("AA7_TCHEG2")+":"		SIZE 060,008 OF oScroll PIXEL
			
			@ 163,050 SAY oSay17 PROMPT ""						SIZE 035,008 OF oScroll PIXEL PICTURE PesqPict("AA7","AA7_TCHEG2") 
			@ 163,120 SAY oSay18 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			
			
			@ 173,010 SAY oSay37 PROMPT STR0037 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL			 //"Alocacao - Termino"
			@ 186,010 SAY oSay38 PROMPT "" SIZE 120,008 OF oScroll PIXEL			
			
			@ 196,010 SAY oSay33 PROMPT STR0038 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL	 //"Tempo para o atendimento da OS"
	
			@ 209,010 SAY RetTitle("AA7_TSAID1")+":"            SIZE 060,008 OF oScroll PIXEL
			
			@ 209,050 SAY oSay19 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			@ 209,120 SAY oSay20 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
	
			@ 219,010 SAY RetTitle("AA7_TSAID2")+":"		SIZE 060,008 OF oScroll PIXEL
			
			@ 219,050 SAY oSay21 PROMPT "" SIZE 035,008 OF oScroll PIXEL PICTURE "99:99" 
			@ 219,120 SAY oSay22 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
			
			@ 229,010 SAY oSay34 PROMPT STR0039 SIZE 150,008 of oScroll FONT oBoldUnd PIXEL	 //"Tempo para confirmacao de atendimento"
			
			@ 242,010 SAY RetTitle("AA7_TATEN1") + ":"		SIZE 060,008 OF oScroll PIXEL 
			
			@ 242,050 SAY oSay24 PROMPT "" SIZE 035,008 OF oScroll PICTURE PesqPict("AA7","AA7_TATEN1") PIXEL 
			@ 242,120 SAY oSay25 PROMPT "" SIZE 035,008 OF oScroll PIXEL 
		
			@ 252,010 SAY RetTitle("AA7_TATEN2") + ":"  SIZE 060,008 OF oScroll PIXEL    
			           
			@ 252,050 SAY oSay26 PROMPT "" SIZE 035,008 OF oScroll PICTURE PesqPict("AA7","AA7_TATEN2") PIXEL 
			@ 252,120 SAY oSay27 PROMPT "" SIZE 035,008 OF oScroll PIXEL 

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona os objetos ao array de controle                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AAdd( oScroll:Cargo, { "oSay1", oSay1 } )						
			AAdd( oScroll:Cargo, { "oSay2", oSay2 } )			
			AAdd( oScroll:Cargo, { "oSay3", oSay3 } )			
			AAdd( oScroll:Cargo, { "oSay4", oSay4 } )			
			AAdd( oScroll:Cargo, { "oSay5", oSay5 } )			
			AAdd( oScroll:Cargo, { "oSay6", oSay6 } )	
			AAdd( oScroll:Cargo, { "oSay7", oSay7 } )			
			AAdd( oScroll:Cargo, { "oSay8", oSay8 } )			
			AAdd( oScroll:Cargo, { "oSay9", oSay9 } )			
			AAdd( oScroll:Cargo, { "oSay10", oSay10 } )	
			AAdd( oScroll:Cargo, { "oSay11", oSay11 } )			
			AAdd( oScroll:Cargo, { "oSay12", oSay12 } )			
			AAdd( oScroll:Cargo, { "oSay13", oSay13 } )			
			AAdd( oScroll:Cargo, { "oSay14", oSay14 } )				
			AAdd( oScroll:Cargo, { "oSay15", oSay15 } )			
			AAdd( oScroll:Cargo, { "oSay16", oSay16 } )			
			AAdd( oScroll:Cargo, { "oSay17", oSay17 } )	
			AAdd( oScroll:Cargo, { "oSay18", oSay18 } )			
			AAdd( oScroll:Cargo, { "oSay19", oSay19 } )			
			AAdd( oScroll:Cargo, { "oSay20", oSay20 } )			
			AAdd( oScroll:Cargo, { "oSay21", oSay21 } )				
			AAdd( oScroll:Cargo, { "oSay22", oSay22 } )			
			AAdd( oScroll:Cargo, { "oSay23", oSay23 } )				
			AAdd( oScroll:Cargo, { "oSay24", oSay24 } )			
			AAdd( oScroll:Cargo, { "oSay25", oSay25 } )				
			AAdd( oScroll:Cargo, { "oSay26", oSay26 } )				
			AAdd( oScroll:Cargo, { "oSay27", oSay27 } )							
			AAdd( oScroll:Cargo, { "oSay30", oSay30 } )							
			AAdd( oScroll:Cargo, { "oSay31", oSay31 } )							
			AAdd( oScroll:Cargo, { "oSay32", oSay32 } )										
			AAdd( oScroll:Cargo, { "oSay33", oSay33 } )
			AAdd( oScroll:Cargo, { "oSay34", oSay34 } )										
			AAdd( oScroll:Cargo, { "oSay35", oSay35 } )							
			AAdd( oScroll:Cargo, { "oSay36", oSay36 } )										
			AAdd( oScroll:Cargo, { "oSay37", oSay37 } )
			AAdd( oScroll:Cargo, { "oSay38", oSay38 } )										
			                                        
			oScroll:Show()
			
			lCreated := .T. 
			
		EndIf
			

			cMotivo := ""
			If ( !Empty(aAuxiliar[7][1]) )
				nSaida1 := SubtHoras(aAuxiliar[7][1],aAuxiliar[7][2],dDataBase,Time())
				nSaida2 := nSaida1
				nSaida1 -= aTempos[7]
				nSaida2 -= aTempos[8]
			Else
				nSaida1 := 0
				nSaida2 := 0
			Endif
			
			Do Case
			
				Case ( nSaida2 > 0 .And. aTempos[8] <> 0 )
					cMotivo	:= STR0003	//"TERMINO DA OS NAO CONFIRMADA"
				Case ( nSaida1 > 0 .And. aTempos[7] <> 0 )
					cMotivo	:= STR0003	//"TERMINO DA OS NAO CONFIRMADA"
				Case ( aAuxiliar[1] > HoraToInt(aTempos[2],nTALOC2) .And. aAuxiliar[4]==0 .And. !Empty(aTempos[2]) )
					cMotivo	:= STR0005	//"ALOCACAO NAO EFETUADA"
				Case ( aAuxiliar[1] > HoraToInt(aTempos[1],nTALOC1) .And. aAuxiliar[4]==0 .And. !Empty(aTempos[1]) )
					cMotivo	:= STR0005	//"ALOCACAO NAO EFETUADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[6],nTCHEG2)+aAuxiliar[2]) .And. !aAuxiliar[3] .And. !Empty(aTempos[6]) )
					cMotivo	:= STR0004	//"CHEGADA NAO CONFIRMADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[5],nTCHEG1)+aAuxiliar[2]) .And. !aAuxiliar[3] .And. !Empty(aTempos[5]) )
					cMotivo	:= STR0004	//"CHEGADA NAO CONFIRMADA"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[10],nTATEN2) +aAuxiliar[2]) .And. !aAuxiliar[8] .And. !Empty(aTempos[10]) )
					cMotivo	:= STR0022	//"ATENDIMENTO NAO CONFIRMADO"
				Case ( aAuxiliar[1] > (HoraToInt(aTempos[9],nTATEN1)+aAuxiliar[2]) .And. !aAuxiliar[8] .And. !Empty(aTempos[9]) )
					cMotivo	:= STR0022	//"ATENDIMENTO NAO CONFIRMADO"
				Case ( aAuxiliar[2] > HoraToInt(aTempos[4],nTMAX2) .And. !Empty(aTempos[4]) )
					cMotivo	:= STR0006	//"TEMPO EXCESSIVO PARA ALOCAR"
				Case ( aAuxiliar[2] > HoraToInt(aTempos[3],nTMAX1) .And. !Empty(aTempos[3]))
					cMotivo	:= STR0006	//"TEMPO EXCESSIVO PARA ALOCAR"
			EndCase
         
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza as informacoes genericas                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oScroll:Cargo[ Eval( bControl, "oSay1" ), 2 ]:SetText( cMotivo )
			oScroll:Cargo[ Eval( bControl, "oSay2" ), 2 ]:SetText( aAuxiliar[5][1] )
			oScroll:Cargo[ Eval( bControl, "oSay3" ), 2 ]:SetText( aAuxiliar[5][2] )
			oScroll:Cargo[ Eval( bControl, "oSay4" ), 2 ]:SetText( dDataBase )
			oScroll:Cargo[ Eval( bControl, "oSay5" ), 2 ]:SetText( cHoraBase )
			oScroll:Cargo[ Eval( bControl, "oSay6" ), 2 ]:SetText( IntToHora( aAuxiliar[1], 5 ) )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza os tempos configurados                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			oScroll:Cargo[ Eval( bControl, "oSay7" ), 2 ]:SetText( aTempos[1] )
			oScroll:Cargo[ Eval( bControl, "oSay9" ), 2 ]:SetText( aTempos[2] )				
			oScroll:Cargo[ Eval( bControl, "oSay11" ), 2 ]:SetText( aTempos[3] )				
			oScroll:Cargo[ Eval( bControl, "oSay13" ), 2 ]:SetText( aTempos[4] )			
			oScroll:Cargo[ Eval( bControl, "oSay15" ), 2 ]:SetText( aTempos[5] )						
			oScroll:Cargo[ Eval( bControl, "oSay17" ), 2 ]:SetText( aTempos[6] )
			oScroll:Cargo[ Eval( bControl, "oSay19" ), 2 ]:SetText( IntToHora( aTempos[7] ) )			
			oScroll:Cargo[ Eval( bControl, "oSay21" ), 2 ]:SetText( IntToHora( aTempos[8] ) )			
			oScroll:Cargo[ Eval( bControl, "oSay24" ), 2 ]:SetText( aTempos[9] )						
			oScroll:Cargo[ Eval( bControl, "oSay26" ), 2 ]:SetText( aTempos[10] )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Atualiza a alocacao                                                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Avalia os itens que devem ser habilitados / desabilitados              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			lTmpAloc := Empty( aAuxiliar[2] ) .And. ( !Empty( aTempos[1] ) .Or. !Empty( aTempos[2] ) )
			lTmpIni  := !lTmpAloc .And. ( !Empty( aTempos[3] ) .Or. !Empty( aTempos[4] ) )
         lTmpCheg := !lTmpAloc .And. !aAuxiliar[3] .And. ( !Empty( aTempos[5] ) .Or. !Empty( aTempos[6]  ) )
         lTmpAten := !lTmpAloc .And. !aAuxiliar[8] .And. ( !Empty( aTempos[9] ) .Or. !Empty( aTempos[10] ) )
         lTmpTerm := !lTmpAloc .And. ( !Empty(aAuxiliar[7][1]) ) .And. ( !Empty( aTempos[7] ) .Or. !Empty( aTempos[8] ) )
         lAlocado := !Empty(aAuxiliar[6,1])
         lTermino := !Empty(aAuxiliar[7,1])
         
			oScroll:Cargo[ Eval( bControl, "oSay8"  ), 2 ]:SetText( "" )	  
			oScroll:Cargo[ Eval( bControl, "oSay10" ), 2 ]:SetText( "" )										

			oScroll:Cargo[ Eval( bControl, "oSay12" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay14" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay16" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay18" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay20" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay22" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay25" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay27" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay36" ), 2 ]:SetText( "" )
			oScroll:Cargo[ Eval( bControl, "oSay38" ), 2 ]:SetText( "" )
        
			oScroll:Cargo[ Eval( bControl, "oSay30" ), 2 ]:Disable()
			oScroll:Cargo[ Eval( bControl, "oSay31" ), 2 ]:Disable() 
			oScroll:Cargo[ Eval( bControl, "oSay32" ), 2 ]:Disable() 				
			oScroll:Cargo[ Eval( bControl, "oSay33" ), 2 ]:Disable() 
			oScroll:Cargo[ Eval( bControl, "oSay34" ), 2 ]:Disable() 				
			oScroll:Cargo[ Eval( bControl, "oSay35" ), 2 ]:Disable() 
			oScroll:Cargo[ Eval( bControl, "oSay37" ), 2 ]:Disable() 				
			
			If lTmpAloc 
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se ainda nao foi efetuada a alocacao                                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				
				If !Empty( aTempos[ 1 ] ) 
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Tempo restante para a alocacao - Alerta amarelo                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aValor[1] := aAuxiliar[1]                                                                   
					aValor[1] -= HoraToInt(aTempos[1],nTALOC1)
					
					oScroll:Cargo[ Eval( bControl, "oSay8" ), 2 ]:SetText( IntToHora( aValor[1], 5 ) )		
					oScroll:Cargo[ Eval( bControl, "oSay8" ), 2 ]:SetColor(If(aValor[1]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf 
					                       
				If !Empty( aTempos[ 2 ] ) 					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Tempo restante para a alocacao - Alerta vermelho                       ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					aValor[2] := aAuxiliar[1]
					aValor[2] -= HoraToInt(aTempos[2],nTALOC2)   
					
					oScroll:Cargo[ Eval( bControl, "oSay10" ), 2 ]:SetText( IntToHora( aValor[2], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay10" ), 2 ]:SetColor(If(aValor[2]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf	
				
			EndIf 
			
			If lTmpIni 	                           

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se ja foi efetuada a alocacao                                          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Tempo restante para a alocacao - Alerta amarelo                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty( aTempos[ 3 ] ) 
					aValor[3] := aAuxiliar[2]
					aValor[3] -= HoraToInt(aTempos[3],nTMAX1)
					
					oScroll:Cargo[ Eval( bControl, "oSay12" ), 2 ]:SetText( IntToHora( aValor[3], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay12" ), 2 ]:SetColor(If(aValor[3]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf					
	
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Tempo restante para a alocacao - Alerta vermelho                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !Empty( aTempos[ 4 ] ) 
					aValor[4] := aAuxiliar[2]
					aValor[4] -= HoraToInt(aTempos[4],nTMAX2)
	
					oScroll:Cargo[ Eval( bControl, "oSay14" ), 2 ]:SetText( IntToHora( aValor[4], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay14" ), 2 ]:SetColor(If(aValor[4]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf
					
			EndIf 				
				   
			If lTmpCheg	
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se ja foi efetuada a alocacao e nao foi confirmada a chegada           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				If !Empty( aTempos[ 5 ] )
					aValor[5] := aAuxiliar[1]
					aValor[5] -= aAuxiliar[2]
					aValor[5] -= HoraToInt(aTempos[5],nTCHEG1)
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Tempo restante para a confirmacao - Alerta amarelo                     ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					oScroll:Cargo[ Eval( bControl, "oSay16" ), 2 ]:SetText( IntToHora( aValor[5], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay16" ), 2 ]:SetColor(If(aValor[5]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf         
	
				If !Empty( aTempos[ 6 ] )	
					aValor[6] := aAuxiliar[1]
					aValor[6] -= aAuxiliar[2]
					aValor[6] -= HoraToInt(aTempos[6],nTCHEG2)
	
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Tempo restante para a confirmacao - Alerta vermelho ( maximo )         ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					oScroll:Cargo[ Eval( bControl, "oSay18" ), 2 ]:SetText( IntToHora( aValor[6], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay18" ), 2 ]:SetColor(If(aValor[6]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf	
			EndIf
			
			If lTmpTerm
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se foi informada data para termino do atendimento                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				If !Empty( aTempos[ 7 ] ) 									
					aValor[7] := SubtHoras(aAuxiliar[7][1],aAuxiliar[7][2],dDataBase,cHoraBase)
					aValor[7] -= aTempos[7]
					dDataSai1 := aAuxiliar[7][1]
					cHoraSai1 := aAuxiliar[7][2]
					SomaDiaHor(@dDataSai1,@cHoraSai1,aTempos[7])
					
					oScroll:Cargo[ Eval( bControl, "oSay20" ), 2 ]:SetText( IntToHora( aValor[7], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay20" ), 2 ]:SetColor(If(aValor[7]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
					
				EndIf 	
					                                       
				If !Empty( aTempos[ 8 ] ) 					
					aValor[8] := SubtHoras(aAuxiliar[7][1],aAuxiliar[7][2],dDataBase,cHoraBase)
					dDataSai2 := aAuxiliar[7][1]
					cHoraSai2 := aAuxiliar[7][2]
					aValor[8] -= aTempos[8]
					SomaDiaHor(@dDataSai2,@cHoraSai2,aTempos[8])
					
					oScroll:Cargo[ Eval( bControl, "oSay22" ), 2 ]:SetText( IntToHora( aValor[8], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay22" ), 2 ]:SetColor(If(aValor[8]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf
				
			EndIf
         
			If lTmpAten 
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Se ja foi efetuada a alocacao e nao foi confirmado o atendimento ( via agendamento ) ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				If !Empty( aTempos[9] )
					aValor[9] := aAuxiliar[1]
					aValor[9] -= aAuxiliar[2]
					aValor[9] -= HoraToInt(aTempos[9],nTATEN1)

					oScroll:Cargo[ Eval( bControl, "oSay25" ), 2 ]:SetText( IntToHora( aValor[9], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay25" ), 2 ]:SetColor(If(aValor[9]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf 	
					
                                   
				If !Empty( aTempos[10] )
					aValor[10] := aAuxiliar[1]
					aValor[10] -= aAuxiliar[2]
					aValor[10] -= HoraToInt(aTempos[10],nTATEN2)
					
					oScroll:Cargo[ Eval( bControl, "oSay27" ), 2 ]:SetText( IntToHora( aValor[10], 5 ) )
					oScroll:Cargo[ Eval( bControl, "oSay27" ), 2 ]:SetColor(If(aValor[10]<=0,CLR_HBLUE,CLR_HRED),oScroll:nClrPane)
				EndIf
				
			EndIf         

			oScroll:Cargo[ Eval( bControl, "oSay30" ), 2 ]:SetEnable( lTmpAloc ) 
			oScroll:Cargo[ Eval( bControl, "oSay31" ), 2 ]:SetEnable( lTmpIni  )
			oScroll:Cargo[ Eval( bControl, "oSay32" ), 2 ]:SetEnable( lTmpCheg )  
			oScroll:Cargo[ Eval( bControl, "oSay33" ), 2 ]:SetEnable( lTmpTerm )  			
			oScroll:Cargo[ Eval( bControl, "oSay34" ), 2 ]:SetEnable( lTmpAten )  
			
			If lAlocado 
				oScroll:Cargo[ Eval( bControl, "oSay35" ), 2 ]:Enable()			
				oScroll:Cargo[ Eval( bControl, "oSay36" ), 2 ]:SetText( STR0040 + DToC( aAuxiliar[6,1] ) + STR0041 + aAuxiliar[6,2] ) //"Data : "###" / Hora : "
			EndIf				
				
			If lTermino 				                                             
				oScroll:Cargo[ Eval( bControl, "oSay37" ), 2 ]:Enable()						
				oScroll:Cargo[ Eval( bControl, "oSay38" ), 2 ]:SetText( STR0040 + DToC( aAuxiliar[7,1] ) + STR0041 + aAuxiliar[7,2] ) //"Data : "###" / Hora : "
			EndIf 				
			
			oScroll:Show()			
		
	EndIf
Else    
	oScroll:Hide()   
	oTree2:Hide() 
	oScrPanel:Show()
EndIf      
                            
oScroll:Reset()

CursorArrow() 

Return(Nil)    

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Move³ Autor ³ Sergio Silveira       ³ Data ³11/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Redimensiona os objetos da tela                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Move( ExpO1, ExpO2, ExpO3, ExpO4, ExpN1 )            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto Tree                                       ³±±
±±³          ³ ExpO2 -> Objeto ScrollBox                                  ³±±
±±³          ³ ExpO3 -> Objeto Panel                                      ³±±
±±³          ³ ExpO4 -> Objeto Pabnel da ScrollBox                        ³±±
±±³          ³ ExpN1 -> Numero de pixels para movimentar                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060Move( oTree, oScroll, oPanel, oScrPanel, oTree2, nStep )

oTree:nRight    -= nStep

oScroll:nLeft   -= nStep
oScrPanel:nLeft -= nStep
oTree2:nLeft    -= nStep 

oPanel:nLeft    -= nStep

oScroll:nRight   += nStep
oScrPanel:nRight += nStep
oTree2:nRight    += nStep 

oPanel:nRight  += nStep

Return( ) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Lin ³ Autor ³ Sergio Silveira       ³ Data ³11/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Move a barra para a coluna especificada                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Lin( ExpO1, ExpO2, ExpO3, ExpO4, ExpN1 )             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto Tree                                       ³±±
±±³          ³ ExpO2 -> Objeto ScrollBox                                  ³±±
±±³          ³ ExpO3 -> Objeto Panel                                      ³±±
±±³          ³ ExpO4 -> Objeto Pabnel da ScrollBox                        ³±±
±±³          ³ ExpN1 -> Coluna que sera posicionado a linha entre objetos ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060Lin( oTree, oScroll, oPanel, oScrPanel, oTree2, nLin ) 

Atc060Move( oTree, oScroll, oPanel, oScrPanel, oTree2, nLin - nCx )

nLin := nCx 

Return( .T. )          

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Rast³ Autor ³ Sergio Silveira       ³ Data ³24/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua o rastro da OS                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Rast( ExpO1, ExpO2, ExpO3, ExpO4 )                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 -> Objeto Tree ( monitor )                           ³±±
±±³          ³ ExpO2 -> Objeto Tree ( tracker )                           ³±±
±±³          ³ ExpO3 -> Objeto ScrollBox                                  ³±±
±±³          ³ ExpO4 -> Objeto Panel do ScrollBox                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060Rast( oTree, oTree2, oScroll, oScrPanel ) 

Local aRoot     := {} 
Local aRootTot  := {}               
Local cFldBreak	:= TC060Quebra()

Local bBlock    := { || .T. } 
Local bWhileAB7 := { || .T. } 

Local cSeek     := oTree:GetCargo()
Local cQuery    := ""
Local cAliasAB7 := ""        

Local nLoop     := 0 
Local nLevel    := 0 

oTree2:Show()
oScroll:Hide() 
oScrPanel:Hide()
                           
cSeek := SubStr(cSeek,Len(AB6->&(cFldBreak))+1,Len(AB6->AB6_NUMOS))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Reinicializa o Tree                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oTree2:BeginUpdate()
oTree2:Reset()      
oTree2:EndUpdate()
                                      
oTree2:BeginUpdate()

oTree2:lShowHint := .F. 

#IFDEF TOP   

	lQuery := .T. 
                                                                 
	cAliasAB7 := GetNextAlias()

  	cQuery := "SELECT AB7_NUMOS, AB7_ITEM FROM " 
	cQuery += RetSqlName( "AB7" ) + " AB7 "   	
  	cQuery += "WHERE "
  	cQuery += "AB7_FILIAL='" + xFilial( "AB7" ) + "' AND "
  	cQuery += "AB7_NUMOS='"  + cSeek            + "' AND "

  	cQuery += "AB7.D_E_L_E_T_=' '"
   	
  	cQuery := ChangeQuery( cQuery ) 
   	
  	dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAB7, .F., .T. ) 
   	
  	bWhileAB7 := { || !( cAliasAB7 )->( Eof() ) } 

#ELSE
                    
	lQuery := .F. 
 
	cAliasAB7 := "AB7" 
 
  	AB7->( dbSetOrder( 1 ) ) 
	AB7->( dbSeek(xFilial("AB7")+ cSeek ) )       
  	bWhileAB7 := { || ( !AB7->( Eof() ) .And. xFilial("AB7") == AB7->AB7_FILIAL .And.;
										cSeek == AB7->AB7_NUMOS )	} 
#ENDIF                       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Localiza os eventos de inicio dos itens da OS                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While Eval( bWhileAB7 ) 
	 
	aRoot := {} 
	
	MaRetRoot( "AB7", ( cAliasAB7 )->AB7_NUMOS + ( cAliasAB7 )->AB7_ITEM, @aRoot )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Localiza o ultimo item do array ( evento de inicio )                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	aRootEnd := ATail( aRoot ) 
                      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Elimina os eventos duplicados                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Empty( AScan( aRootTot, { |x| x[1] == aRootEnd[1] .And. x[2] == aRootEnd[2] } ) ) 
		AAdd( aRootTot, AClone( aRootEnd ) ) 	
	EndIf 	                                  
		
	( cAliasAB7 )->( dbSkip() )

EndDo                   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Dispara os trackers dos eventos de inicio, passando o tree como referencia  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aRootTot )
	oTree2:TreeSeek( "" )                                                                                 
	nLevel := 0 
	bBlock := &( "{ || MaTrk" + aRootTot[ nLoop, 1 ] + "( '" + aRootTot[ nLoop,2 ] + "',oTree2, ,@nLevel ) } " )
	Eval( bBlock ) 
Next nLoop

#IFDEF TOP
	dbSelectArea( cAliasAB7 ) 
	dbCloseArea()
	dbSelectArea( "AB7" ) 
#ENDIF	

oTree2:EndUpdate()            

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona a os no tree                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oTree2:TreeSeek( "AB7-" + cSeek )

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Par ³ Autor ³ Sergio Silveira       ³ Data ³29/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a janela de parametros e o recalculo do Tree         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oTimer : Objeto Timer                                      ³±±
±±³          ³ oTree  : Objeto Tree                                       ³±±
±±³          ³ oDlg	 : Objeto da Janela                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060Par(oTimer,oTree,oDlg,aParam)

If Atc060Perg(@aParam) 
	Processa( { || Atc060Reca(oTimer,oTree,oDlg,@aParam) } ) 
EndIf 

Return( Nil )                

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060Perg³ Autor ³ Sergio Silveira       ³ Data ³29/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz a chamada do pergunte e salva os parametros definidos  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060Perg( @ExpA1 )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 : Array de parametros                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060Perg(aParam)

LOCAL lRet  := .F. 
LOCAL nLoop := 0 

If ( lRet := Pergunte( "ATC060", .T., STR0042 + STR0001 ) ) // "Parametros :  " ## "Monitor - Os"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Guarda o conteudo do parametros                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aParam := {}
	For nLoop := 1 To 20 
		AAdd( aParam, &( "MV_PAR" + StrZero( nLoop, 2 ) ) )
	Next nLoop
EndIf 

Return( lRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc060RPar³ Autor ³ Sergio Silveira       ³ Data ³29/10/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Restaura para a memoria os parametros guardados no array   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Atc060RPar( @ExpA1 )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 : Array de parametros                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function Atc060RPar(aParam)

Local nLoop := 0 

For nLoop := 1 To Len( aParam ) 
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Reinicializa as variaveis de parametros                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	&( "MV_PAR" + StrZero( nLoop, 2 ) ) := aParam[ nLoop ]   		
Next nLoop

Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³TC060Quebra³ Autor ³ Totvs                ³ Data ³14/04/2011³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Determina qual campo fara a quebra na tela                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TC060Quebra()
Local cFldBreak := "AB6_REGIAO"

If ValType( MV_PAR15 ) == "N"
	If MV_PAR15 == 2	// 2- Cliente
		cFldBreak	:= "AB6_CODCLI"
	ElseIf MV_PAR15 == 3	// 3- Contrato de Manutencao
		cFldBreak	:= "AB6_CONTRT"
	EndIf
EndIf

Return cFldBreak
