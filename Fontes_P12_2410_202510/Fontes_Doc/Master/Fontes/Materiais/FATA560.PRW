#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA560.CH"
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFata560   บAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณAmarracao entre Componentes e Tipos de Tarefa               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Fata560()

Local aCores	:={	{"ADT->(DbSeek(ADS->(ADS_FILIAL+ADS_CODIGO)))" , 'BR_VERDE'},;
					{"!ADT->(DbSeek(ADS->(ADS_FILIAL+ADS_CODIGO)))", 'BR_PRETO'}}

Private cCadastro := STR0001//"Componentes x Tp. Tarefa"
Private aRotina := MenuDef()	
Private cDelFunc := ".F."

If !SX2->(DbSeek("ADT"))
	MsgStop(STR0002) //"Execute o update U_TKUPDADM antes de acessar esta rotina"
	Return Nil
EndIf

ADT->(DbSetOrder(1))
dbSelectArea("ADS")
dbSetOrder(1)

mBrowse( 6,1,22,75,"ADS",,,,,,aCores)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560Edit บAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina de edicao da amarracao entre componentes e tipos de  บฑฑ
ฑฑบ          ณtarefa.                                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft560Edit(cAlias, nReg, nOpc)

Local aArea		:= GetArea()
Local aObjects	:= {}
Local aSize		:= {}
Local aInfo		:= {}
Local oDlg		:= Nil
Local oEnch		:= Nil
Local aField	:= {}
Local lMemoria	:= .T.  
Local cDelOk    := "AllWaysTrue()" 
Local cSeek		:= ""
Local aColsGd	:= {}
Local aHeadGd	:= {}
Local aYesFields:= {} 
Local bWhile	:= Nil
Local nX		:= 0
Local nItem		:= 0
Local aRecnos	:= {}
Local nPRec		:= 0 
Local nRecAtu	:= 0 
Local cFilADT	:= xFilial("ADT") 
Local nPosCmp	:= 0
Local nOpcA		:= 0
Local nGdOpc	:= IIf(INCLUI .OR. ALTERA,GD_INSERT+GD_UPDATE+GD_DELETE,0)
Local cQueryAE1 := ""
Local cAliasAE1 := ""
                     
aField 		:= Ft560RetCp()
aYesFields	:= {"ADT_CODCMP","ADT_DSCCMP"}

ADT->(DbSetOrder(1))
ADT->(DbSeek(cFilADT+ADS->ADS_CODIGO))

#IFDEF TOP
	If nOpc == 4 
		cQueryAE1 := "SELECT AE1_COMPOS "
		cQueryAE1 += "FROM "+RetSqlName("AE1")+" AE1 "
		cQueryAE1 += "WHERE AE1.AE1_FILIAL='"+xFilial("AE1")+"' AND "
		cQueryAE1 += "AE1.AE1_TPTARE='"+ADS->ADS_CODIGO+"' AND "
		cQueryAE1 += "AE1.D_E_L_E_T_=' ' "
		cQueryAE1 += "ORDER BY AE1_COMPOS" 
		
		cAliasAE1 := GetNextAlias()	
		cQueryAE1 := ChangeQuery(cQueryAE1)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQueryAE1),cAliasAE1,.T.,.T.)
		If !Empty((cAliasAE1)->AE1_COMPOS)
	   		MSGSTOP(STR0013 + " :" +(cAliasAE1)->AE1_COMPOS + " .")
	   		Return .F.
	    EndIf  
	EndIf    
#ENDIF
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณInicializa as variaveis da Enchoice e GetDados                          ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
RegToMemory( "ADT", .F., .F. )
M->ADT_CODTAR	:= ADS->ADS_CODIGO
M->ADT_DSCTAR	:= ADS->ADS_DESCRI

cSeek 		:= cFilADT+ADS->ADS_CODIGO
bWhile 		:= {||ADT->ADT_FILIAL + ADT->ADT_CODTAR}

FillGetDados(aRotina[nOpc][4],"ADT",1,cSeek,bWhile,,,aYesFields,,,,,@aHeadGD,@aColsGD,{|a,b|Ft560AfCol(a,b)})

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Faz o calculo automatico de dimensoes de objetos     ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aSize := MsAdvSize()
AAdd( aObjects, { 100, 100, .T., .T. } )
AAdd( aObjects, { 200, 200, .T., .T. } )
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 5, 5 }
aPosObj := MsObjSize( aInfo, aObjects,.T.)

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 To aSize[6],aSize[5] of oMainWnd PIXEL

oEnch	:= MsMGet():New("ADT",nReg,nOpc,,,,,aPosObj[1],,3,,,,oDlg,,lMemoria,,,,,aField)
oGetD	:= MsNewGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nGdOpc,,,,,,512,,,cDelOk,oDlg,aHeadGd,aColsGd)

oGetD:lDelete	:= INCLUI .OR. ALTERA
oGetD:bLinhaOk	:= {||Ft560LinOk(@oGetD)}
oGetD:bTudoOk	:= {||Ft560TudOk(@oGetD)}
oGetD:cFieldOk	:= "Ft560FOk(@oGetD)"

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA := 1,If(oGetD:TudoOk(),If(.F.,nOpcA := 0,oDlg:End()),nOpcA := 0)},{||oDlg:End()})

If (nOpcA == 1) .AND. (aRotina[nOpc][4] <> 2)

	nPosCmp	:= aScan(oGetD:aHeader,{|x|AllTrim(x[2]) == "ADT_CODCMP" })
	nPRec	:= aScan(oGetD:aHeader,{|x|AllTrim(x[2]) == "ADT_REC_WT" })
	   
	DbSelectArea("ADT")
	DbSetOrder(1)
    
	If aRotina[nOpc][4] <> 5 //Alteracao e inclusao

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณArmazena os recnos ja utilizadosณ
		//ณe apaga os registros deletados. ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nX := 1 to Len(oGetD:aCols)
			If oGetD:aCols[nX][nPRec] > 0
				If !aTail(oGetD:aCols[nX])
					AAdd(aRecnos,oGetD:aCols[nX][nPRec])
				Else
					ADT->(DbGoTo(oGetD:aCols[nX][nPRec]))
					RecLock("ADT",.F.)
					DbDelete()
					MsUnLock()
				EndIf
			EndIf
		Next nX  
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณGravacao das linhas validasณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nX := 1 to Len(oGetD:aCols)
	
			If !aTail(oGetD:aCols[nX])
	
				nItem++
	
				If nItem <= Len(aRecnos)
					ADT->(DbGoTo(aRecnos[nItem]))
					RecLock("ADT",.F.)
				Else
					RecLock("ADT",.T.)
				EndIf
	            
	            Replace	ADT->ADT_FILIAL	With cFilADT
	            Replace ADT->ADT_CODTAR	With M->ADT_CODTAR
	            Replace ADT->ADT_CODCMP	With oGetD:aCols[nX][nPosCmp]
	            
				MsUnLock()
				
			EndIf
	
		Next nX
	Else //Exclusao

		aRecnos := {}
		ADT->(DbSeek(cFilADT+M->ADT_CODTAR))
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณArmazena os recnos a serem deletadosณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		While !ADT->(Eof()) .AND.;
			ADT->ADT_FILIAL == cFilADT .AND.;
			ADT->ADT_CODTAR == M->ADT_CODTAR
			
			AAdd(aRecnos,ADT->(Recno()))
			
			ADT->(DbSkip())			
		End
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณDeleta os registros na baseณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nX := 1 to Len(aRecnos)
			ADT->(DbGoTo(aRecnos[nX]))
			RecLock("ADT",.F.)
			DbDelete()
			MsUnLock()
		Next nX
	
	EndIf
EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560Leg  บAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณLegenda da rotina                                           บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft560Leg()

Local aLegenda := {	{"BR_VERDE"		, STR0004	},;		//"Configurado"
					{"BR_PRETO"  	, STR0005	}}		//"Nao configurado"

BrwLegenda(cCadastro,STR0003,aLegenda)  //"Componente x Tarefa"

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMenuDef   บAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณUtilizacao do menu funcional                                บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function MenuDef()

Local aRet	:= {{STR0006	,"AxPesqui"		,0,1},;		//"Pesquisar"
				{STR0007	,"Ft560Edit"	,0,2},;		//"Visualizar"
				{STR0008	,"Ft560Edit"	,0,4},;		//"Configurar"
				{STR0009	,"Ft560Edit"	,0,5},;		//"Exc.Config."
				{STR0010	,"Ft560Leg"		,0,2}}		//"Legenda"
Return aRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560RetCpบAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCampos a serem exibidos na getdados                         บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft560RetCp()
Local aCampos 	:= {}
Local aSArea	:= GetArea()
Local cCampos	:= "ADT_CODTAR/ADT_DSCTAR"

DbSelectArea("SX3")
DbSetOrder(1)
If DbSeek("ADT") 
	While !SX3->(Eof()) .AND. SX3->X3_ARQUIVO == "ADT"
		If X3USO(SX3->X3_USADO) .AND. (Alltrim(Upper(SX3->X3_CAMPO)) $ cCampos)
		   	Aadd( aCampos, {X3Titulo(),;
							SX3->X3_CAMPO,;
							SX3->X3_TIPO,;
							SX3->X3_TAMANHO,;
							SX3->X3_DECIMAL,;
							SX3->X3_PICTURE,;
							&("{||" + AllTrim(SX3->X3_VALID)+ "}"),;
							X3Obrigat(SX3->X3_CAMPO),;
							SX3->X3_NIVEL,;
							SX3->X3_RELACAO,;
							SX3->X3_F3,;
							&("{||" + AllTrim(SX3->X3_WHEN) + "}"),;
							SX3->X3_VISUAL=="V",;
							.F.,; 
							SX3->X3_CBOX,;
							VAL(SX3->X3_FOLDER),;
							.F.,;
							""} )
		Endif
		SX3->(DbSkip())
	End
Endif

RestArea(aSArea)

Return aCampos

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt540AfColบAutor  ณVendas Clientes     บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณInicializa campos virtuais do aCols                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft560AfCol(aCols,aHeader)

Local aArea		:= GetArea()
Local nPos		:= Len(aCols)
Local nPDesc	:= aScan(aHeader,{|x|AllTrim(x[2])== "ADT_DSCCMP"})
Local nPCod		:= aScan(aHeader,{|x|AllTrim(x[2])== "ADT_CODCMP"})

aCols[nPos][nPDesc]	:= Posicione("ADR",1,xFilial("ADR")+aCols[nPos][nPCod],"ADR_DESCRI")

RestArea(aArea)

Return .T.

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560LinOkบAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao de linha da getdados                              บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft560LinOk(oGetD)

Local nX	:= 0
Local nPCod	:= aScan(oGetD:aHeader,{|x|AllTrim(x[2])=="ADT_CODCMP"})
Local lRet	:= .T.

If !aTail(oGetD:aCols[oGetD:nAt])
	For nX := 1 to Len(oGetD:aCols)
		If	(nX <> oGetD:nAt) .AND. !aTail(oGetD:aCols[nX]) .AND.;
			(oGetD:aCols[nX][nPCod] == oGetD:aCols[oGetD:nAt][nPCod])
			
			lRet := .F.
			MsgStop(STR0011,STR0012)	//"O componente atual jแ foi informado, favor corrigir."###"Atencao"
			
		EndIf
	Next nX
EndIf

Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560TudOkบAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao da getdados                                       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function Ft560TudOk(oGetD)
Local lRet	:= Ft560LinOk(oGetD)
Return lRet

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt560FOk  บAutor  ณVendas CRM          บ Data ณ  22/01/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณValidacao dos campos da GetDados                            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA560                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft560FOk(oGetD)

Local nLin		:= oGetD:nAt
Local nPCod		:= aScan(oGetD:aHeader,{|x|AllTrim(x[2])=="ADT_CODCMP"})
Local nPDesc	:= aScan(oGetD:aHeader,{|x|AllTrim(x[2])=="ADT_DSCCMP"})

If AllTrim(ReadVar()) == "M->ADT_CODCMP"
	oGetD:aCols[nLin][nPDesc] := Posicione("ADR",1,xFilial("ADR")+M->ADT_CODCMP,"ADR_DESCRI")
EndIf

Return .T.
