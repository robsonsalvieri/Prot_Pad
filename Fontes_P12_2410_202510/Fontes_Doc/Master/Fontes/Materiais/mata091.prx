#INCLUDE "MATA091.CH"
#INCLUDE "PROTHEUS.CH"
/*


Ŀ
Funo     MATA091   Autor  Machima         		 Data  05/03/01 
Ĵ
Descrio  Programa de atualizacao das Taxas de correcao monetaria das
           moedas do sistema.                                         
Ĵ
Sintaxe e  Void MATA091(void)                                         
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
Lucas       07/11/02        Revisao...                              
ٱ


*/
Function MATA091(aRotAuto,nOpc)
nOpc := If (nOpc == Nil,3,nOpc)
//Ŀ
// Define Variaveis                                             
//

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
PRIVATE aRotina := { { STR0003  ,"AxPesqui", 0 , 1},;          		//"Pesquisar"
					{ OemtoAnsi(STR0004) ,"AxVisual", 0 , 2},;      //"Visualizar"
					{ OemtoAnsi(STR0005) ,"A091Inclui", 0 , 3},;    //"Incluir"
					{ OemtoAnsi(STR0006) ,"A091Altera", 0 , 4, 2},; //"Alterar"
					{ OemtoAnsi(STR0007) ,"A091Deleta", 0 , 5, 1}}	//"Excluir"

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemtoAnsi(STR0008)         //"Atualizao de Taxas de Correcao"

PRIVATE INCLUI    := .F.
PRIVATE ALTERA    := .F.
//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private l091Auto := ( aRotAuto <> NIL )

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If l091Auto
	DbSelectArea("SBN")
	MsRotAuto(nOpc,aRotAuto,"SBN")
Else
	dbSelectArea("SBN")
	mBrowse( 6, 1,22,75,"SBN")
EndIf
Return .T.

/*

Ŀ
Funo    A091Deleta Autor  Lucas                  Data  07/11/02 
Ĵ
Descrio  Programa de exclusao das taxas de correo                 
                                                                      
Ĵ
Sintaxe e  Void A091Deleta(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function A091Deleta(cAlias,nReg,nOpc)
LOCAL nLinhas:=0,bCampo,nOpcA,cCod
LOCAL oDlg, lErro := .F.

//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }

While .T.
	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	IF !SoftLock(cAlias)
		Return
	Endif
	If !( l091Auto )
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
		nOpcA:=EnChoice( cAlias, nReg, nOpc,,,)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
	Else
		nOpca := 2
	EndIf
	DbSelectArea(cAlias)
	
	IF nOpcA == 2   
		//Ŀ
		// Antes de deletar eu vou verificar se existe movimentacao     
		//
		Begin Transaction

		//Ŀ
		// apos passar por todas as verificacoes , deleta o registro    
		//
		DbSelectArea(cAlias)
		RecLock(cAlias,.F.,.T.)
		DbDelete()
		MsUnLock()

		End Transaction
	Else
		MsUnLock()
   EndIf
   Exit
EndDo

DbSelectArea(cAlias)
Return

/*

Ŀ
Funo    A091Inclui Autor  Lucas			         Data  07/11/02 
Ĵ
Descrio  Programa de Inclusao das Taxas para correo.              
                                                                      
Ĵ
Sintaxe e  Void A091Inclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       MATA091()                                                  
ٱ


*/
Function A091Inclui(cAlias,nReg,nOpc)

//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]

bCampo := {|nCPO| Field(nCPO) }

INCLUI := .T.
ALTERA := .F.

While .T.
	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0

	If ( l091Auto )
		Begin Transaction
			nOpca := AxIncluiAuto(cAlias,"A091TudoOk()")
		End Transaction
	Else
 		nOpcA:=AxInclui( cAlias, nReg, nOpc,,,,"A091TudoOk()")
	EndIf
	
	DbSelectArea(cAlias)
	
	IF nOpcA == 1
		
		//Ŀ
		//Ponto de Entrada Padrao                                               
		//
		If ExistBlock("MT091INC")
			ExecBlock("MT091INC")
		Endif
		
	Endif
		
	Exit
End
DbSelectArea(cAlias)
Return

/*

Ŀ
Funo    A091Altera Autor  Lucas			         Data  07/11/02 
Ĵ
Descrio  Programa de Alteracao das Taxas de correo                
                                                                      
Ĵ
Sintaxe e  Void A091Altera(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       MATA091()                                                  
ٱ


*/
Function A091ALTERA(cAlias,nReg,nOpc)

//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }
l091Auto := If(Type("l091Auto") == "U", .f.,l091Auto)

INCLUI := .F.
ALTERA := .T.

While .T.
	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	Begin Transaction
		If ( l091Auto )
			nOpcA := AxIncluiAuto(cAlias,,,nOpc,SB1->(RecNo()))          
		Else
			nOpcA := AxAltera( cAlias, nReg, nOpc,,,,,"A091TUDOOK()")
		EndIf
		
		dbSelectArea(cAlias)
		
		If nOpcA == 1
						
			//Ŀ
			//Ponto de Entrada Padrao.                                              			
			//
			If ExistBlock("MT091ALT")
				ExecBlock("MT091ALT",.f.,.f.)
			Endif
		Endif
	End Transaction
	Exit
End
dbSelectArea(cAlias)
Return

/*

Ŀ
Funo     Moeda2Nome  Autor  Lucas		         Data  07/11/02 
Ĵ
Descrio  Retorna descrio da moeda com base na moeda enviada e nos  
           parametros MV_MOEDA1...5                                   
Ĵ
Sintaxe e  ExpC1 := Moeda2Nome(ExpN1)                                 
Ĵ
Parametros ExpN1 = Numero da moeda no sistema...                      
Ĵ
Retorno    ExpC1 = Descrio da moeda no sistema...                   
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function Moeda2Nome(nMoeda)
Local cDescri := GetMV("MV_MOEDA"+Alltrim(Str(nMoeda)))
Return( cDescri )
 
/*

Ŀ
Funo    A091TudoOk Autor  Lucas                  Data  08/11/02 
Ĵ
Descrio Verifica se o registro nao foio incluido por outra estacao  
Ĵ
Sintaxe   A091TudoOk()                                                
Ĵ
 Uso      MATA091                                                     
ٱ


*/
Function A091TudoOk()
Local lRet    := .T.
Local l091TOk := Existblock("A091TOK")
If lRet .And. l091TOk
	lRet:= Execblock("A091TOK",.F.,.F.)
	If ValType(lRet) # "L"
		lRet :=.T.
	EndIf
Endif
Return lRet

/*

Ŀ
Funo    A091Moeda  Autor  Lucas                  Data  08/11/02 
Ĵ
Descrio Validar o cdigo da moeda...                                
Ĵ
Sintaxe   A091Moeda()                                                 
Ĵ
 Uso      MATA091                                                     
ٱ


*/
Function A091Moeda()
Local aArea := GetArea()
Local lRet  := .T.

DbSelectArea("SBN")
DbSeek(xFilial("SBN")+M->BN_MOEDA)
If Found()
   Help("",1,"EXISTE")
   lRet := .F.
EndIf
RestArea( aArea )
Return( lRet )
