#INCLUDE "MATA490.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"


Static lAtuSldNat

/*/


Ŀ
Funo     Mata490   Autor  Cristiano Campos       Data           
Ĵ
Descrio  Programa de atualizacao do Arquivo de Comissoes de Vendas  
Ĵ
Sintaxe e  Void Mata490(void)                                         
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function Mata490(xAuto,nOpcAuto,nPosArotina)
//Ŀ
// Define Variaveis                                             
//
Local aCores 		:= {}
Local lPanelFin 	:= If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)

Local lSeek			:= If((nOpcAuto == 4 .OR. nOpcAuto == 5),.F.,.T.)  //Indica se a funo dever efetuar o posicionamento automatico com base nos dados enviados no parametro aAuto.

Private aAC 		:= { OemToAnsi(STR0001),OemToAnsi(STR0002) },;  //"Abandona"###"Confirma"
aCRA				:= { OemToAnsi(STR0003),OemToAnsi(STR0004),OemToAnsi(STR0005)}  //"Confirma"###"Redigita"###"Abandona"
PRIVATE cCadastro 	:= OemToAnsi(STR0006)  //"Atualizao das Comisses"
Private lTab 		:= .F.

PRIVATE aRotina 	:= MenuDef()
PRIVATE aAuto 		:= {}
PRIVATE l490Auto 	:= xAuto <> NIL

Static lIntegDef  	:= .F.

DEFAULT nPosArotina := 0

lIntegDef := FWHasEAI("MATA490",.T.,,.T.)

PswOrder(1)
If PswSeek( __cUserID, .T. )
   cPermis := subs(PswRet()[2,5],115,1) // verifica as permisses do usurio para Mbrowse
EndIf
//Ŀ
// Ponto de entrada                                             
//
If ExistBlock("MT490BRW")
	ExecBlock("MT490BRW",.F.,.F.)
EndIf

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If ( Type("l490Auto") <> "U" .And. l490Auto )
	aAuto := xAuto
	MBrowseAuto( nOpcAuto, aClone( aAuto ), "SE3", lSeek )
Else
	If nPosArotina > 0 // Sera executada uma opcao diretamento de aRotina, sem passar pela mBrowse
		dbSelectArea("SE3")
		bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
		Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina)
	Else 
		mBrowse( 6, 1,22,75,"SE3",,"E3_DATA",,,,A490Legend())
	Endif	
EndIf
Return

/*/

Ŀ
Funo    A490Deleta Autor  Cristiano Campos       Data           
Ĵ
Descrio  Programa de exclusao de Acumulados                         
                                                                      
Ĵ
Sintaxe e  Void A490Deleta(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
//Ŀ
//Note que para este caso somente ha a delecao do registro no arquivo nao
//importanto a outro arquivo se ha ou nao comissoes registradas          
//
Function A490Deleta(cAlias,nReg,nOpc)
LOCAL LVar01:=1,nLinhas:=0,bCampo,nOpcA,nCntDele:=0,cCod
Local oDlg
Local aButtons
Local lContinua := .T.
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
Local cNatCom	:= PADR(&(GetNewPar("MV_NATCOM","")),TamSx3("E2_NATUREZ")[1])
// Abaixo Posicionamento
Local aSizeAut	  := MsAdvSize(,.F.)
Local aObjects	  := {}
Local aInfo       := {}
Local aPosObj	  := {}
 
//Ŀ
// Ponto de entrada para adicao de botoes na enchoicebar        
//
If ExistBlock("MT490BUT")
	aButtons := ExecBlock("MT490BUT",.F.,.F.)
EndIf
//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0],lTab:=.F.
bCampo := {|nCPO| Field(nCPO) }
SoftLock(cAlias)
nOpcA:=0            


//Ŀ
// Ponto de entrada disparado ao clicar no boto excluir        
//

If ( ExistBlock("MA490CDEL") )  
	lContinua:=	ExecBlock("MA490CDEL",.F.,.F.)
EndIf	

//Ŀ
// Nap permite excluso de comissao ja paga                         
//
If ( !Empty(E3_DATA) )
	Help(" ",1,"A490COMPAG")
	lContinua := .F.
EndIf

If lContinua

	If ( !l490Auto )
		If lPanelFin  //Chamado pelo Painel Financeiro					
			oPanelDados := FinWindow:GetVisPanel()
			oPanelDados:FreeChildren()
			aDim := DLGinPANEL(oPanelDados)
			DEFINE MSDIALOG oDlg OF oPanelDados:oWnd FROM 0,0 To 0,0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )											
	    Else 
			AAdd( aObjects, { 100, 100, .T., .T. })	       		
			aInfo  := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 2, 2 }
			aPosObj:= MsObjSize( aInfo, aObjects )    
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL
		Endif
			
		nOpcA:= EnChoice( cAlias, nReg, nOpc, aAC,"AC",OemToAnsi(STR0013),,aPosObj[1])  //"Quanto  excluso?"
	
		If lPanelFin  //Chamado pelo Painel Financeiro					

			// define dimeno da dialog
			oDlg:nWidth := aDim[4]-aDim[2]

			ACTIVATE MSDIALOG oDlg ON INIT ( FaMyBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},aButtons),	oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]))
		Else		
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()},,aButtons)
		Endif	
	Else
		//Ŀ
		// Cria Variaveis de Memoria da Enchoice                
		//     
		RegToMemory( "SE3", .F. ) 
	
		If EnchAuto(cAlias,aAuto,,aRotina[nOpc,4])
			nOpcA := 2
		EndIf
	EndIf
	If nOpcA == 2
		//Ŀ
		// Ponto de entrada disparado antes da exclusao                 
		//
		Begin Transaction
			If ( ExistBlock("MA490DE2") )
				ExecBlock("MA490DE2",.F.,.F.)
			EndIf	
	
			//Controle de Saldo de Naturezas
			If GetlAtuSld() .and. cNatCom != NIL
				//Atualizo o valor atual para o saldo da natureza retirando do saldo previsto
				AtuSldNat(cNatCom, SE3->E3_VENCTO, "01", "2", "P", SE3->E3_COMIS, SE3->E3_COMIS,"-",,FunName(),"SE3",SE3->(Recno()),nOpc)
	      Endif

			dbSelectArea(cAlias)
			RecLock(cAlias,.F.,.T.)
			dbDelete()
		End Transaction  
		
		If ( ExistBlock("MA490DEL") )
			ExecBlock("MA490DEL",.F.,.F.)
		EndIf
	Endif
	
	MsUnLock()
	
	//Integrao Mensagem Unica 
	If lIntegDef .And. nOpca == 2
		FwIntegDef("MATA490")
	Endif
EndIf

dbSelectArea(cAlias)

If lPanelFin  //Chamado pelo Painel Financeiro						
	cAlias := FinWindow:cAliasFile     
	dbSelectArea(cAlias)					
	FinVisual(cAlias,FinWindow,(cAlias)->(Recno()),.T.)	
Endif
Return

/*/

Ŀ
Funo    A490Altera Autor  Cristina Ogura         Data  11.04.96 
Ĵ
Descrio Toda alteracao deve atualizar Branco no E3_AJUSTE           
                                                                      
Ĵ
Sintaxe e  Void A490Altera(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function A490Altera(cAlias,nReg,nOpc)
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
LOCAL nOpcA     := 0
Local aButtons
Local lContinua := .T.
Local nOldValor := SE3->E3_COMIS
Local dOldVenc  := SE3->E3_VENCTO
Local cNatCom	:= PADR(&(GetNewPar("MV_NATCOM","")),TamSx3("E2_NATUREZ")[1])

//Ŀ
// Ponto de entrada para adicao de botoes na enchoicebar        
//
If ExistBlock("MT490BUT")
	aButtons := ExecBlock("MT490BUT",.F.,.F.)
EndIf

//Ŀ
// Ponto de entrada disparado ao clicar no boto alterar        
//
If ExistBlock("MA490CALT")  
	lContinua := ExecBlock("MA490CALT",.F.,.F.)
ElseIf !Empty(E3_DATA)								//No permite excluso de comissao j paga  
	Help(" ",1,"A490COMPAG")
	lContinua := .F.
EndIf

If lContinua

	//Ŀ
	// Monta a entrada de dados do arquivo        
	// Envia para processamento dos Gets          
	//
	Begin Transaction
	If ( !l490Auto )
		If lPanelFin
			dbSelectArea("SE3")
			RegToMemory("SE3",.T.,,,FunName())                                       
			oPanelDados := FinWindow:GetVisPanel()
			oPanelDados:FreeChildren()
			aDim := DLGinPANEL(oPanelDados)
			Altera := .T.		
			nOpcA := AxAltera(cAlias,nReg,nOpc,,,,     ,"Ma490TUDOK()",,,aButtons,,,,,,.T.,oPanelDados,aDim,FinWindow)						  
	 	Else
			nOpcA:=AxAltera(cAlias,nReg,nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,"Ma490TUDOK()",/*cTransact*/,/*cFunc*/,aButtons,/*aParam*/,/*aAuto*/)
		Endif	
	Else
		nOpcA:=AxAltera(cAlias,nReg,nOpc,/*aAcho*/,/*aCpos*/,/*nColMens*/,/*cMensagem*/,"Ma490TUDOK()",/*cTransact*/,/*cFunc*/,/*aButtons*/,/*aParam*/,aAuto)
	EndIf
	dbSelectArea(cAlias)
	If nOpcA == 1
		RecLock("SE3",.F.)
		Replace E3_AJUSTE With " "
		MsUnlock()

		//Controle de Saldo de Naturezas
		If GetlAtuSld() .and. cNatCom != NIL
			//Alterado o valor da comissao ou vencimento da mesma
			If (nOldValor != SE3->E3_COMIS) .or. (dOldVenc != SE3->E3_VENCTO)
				//Atualizo o valor anterior para o saldo da natureza
				AtuSldNat(cNatCom, dOldVenc, Iif(SE3->(FieldPos("E3_MOEDA")) > 0,SE3->E3_MOEDA,"01"), "2", "P", nOldValor, nOldValor,"-",,FunName(),"SE3",SE3->(Recno()),nOpc)

				//Atualizo o valor atual para o saldo da natureza
				AtuSldNat(cNatCom, SE3->E3_VENCTO, Iif(SE3->(FieldPos("E3_MOEDA")) > 0,SE3->E3_MOEDA,"01"), "2", "P", SE3->E3_COMIS, SE3->E3_COMIS,"+",,FunName(),"SE3",SE3->(Recno()),nOpc)
			Endif	

			//Caso seja dada baixa na comissao
			If !Empty(SE3->E3_DATA)
				//Atualizo o valor atual para o saldo da natureza
				AtuSldNat(cNatCom, SE3->E3_VENCTO, Iif(SE3->(FieldPos("E3_MOEDA")) > 0,SE3->E3_MOEDA,"01"), "3", "P", SE3->E3_COMIS, SE3->E3_COMIS,"+",,FunName(),"SE3",SE3->(Recno()),nOpc)
			Endif
      Endif
		If ( ExistBlock("MA490ALT") )
			ExecBlock("MA490ALT",.F.,.F.)
		EndIf
	EndIF
	End Transaction
	
	//Integrao Mensagem Unica 
	If lIntegDef  .And. nOpca == 1 
		FwIntegDef("MATA490")
	Endif

EndIf


dbSelectArea(cAlias)
Return .T.

/*/

Ŀ
Funo    A490Calc   Autor  Cristina Ogura         Data  15.07.97 
Ĵ
Descrio Calcula o valor base da comissao                            
                                                                      
Ĵ
Sintaxe    Void A490Calc()                                            
Ĵ
 Uso       MATA490                                                    
ٱ


/*/
Function A490Calc()
Local nEndereco, x
nEndereco := Ascan(aGets,{ |x| Subs(x,9,10) == "E3_COMIS  " })
If nEndereco > 0
	aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := STR(M->E3_BASE * M->E3_PORC / 100)
	M->E3_COMIS :=M->E3_BASE * M->E3_PORC / 100
EndIf
lRefresh := .T.
Return .T.


/*/

Ŀ
Funo    A490Inclui Autor  Kleber Dias Gomes      Data  10/05/04 
Ĵ
Descrio Inclusao.                                                   
                                                                      
Ĵ
Sintaxe e  Void A490Inclui(ExpC1,ExpN1,ExpN2)                         
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao                                              
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function A490Inclui(cAlias,nReg,nOpc)
Local aButtons
Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)
Local nOpcA	:= 0

//Ŀ
// Ponto de entrada para adicao de botoes na enchoicebar        
//
If ExistBlock("MT490BUT")
	aButtons := ExecBlock("MT490BUT",.F.,.F.)
EndIf

If ( !l490Auto )
	If lPanelFin  //Chamado pelo Painel Financeiro							
		dbSelectArea("SE3")
		RegToMemory("SE3",.T.,,,FunName())                                       
		oPanelDados := FinWindow:GetVisPanel()
		oPanelDados:FreeChildren()
		aDim := DLGinPANEL(oPanelDados)
		Inclui := .T.	
		nOpcA := AxInclui(cAlias,nReg,nOpc,,,,"Ma490TUDOK()",,"A490Atu()",aButtons,/*aParam*/,,/*lVirtual*/,/*lMaximized*/,/*cTela*/,.T.,oPanelDados,aDim,FinWindow)
	Else
		nOpcA := AxInclui(cAlias,nReg,nOpc,/*aAcho*/,/*cFunc*/,/*aCpos*/,"Ma490TUDOK()",/*lF3*/,"A490Atu()",aButtons,/*aParam*/,/*aAuto*/,/*lVirtual*/,/*lMaximized*/)
	Endif	
Else
	nOpcA := AxInclui(cAlias,nReg,nOpc,/*aAcho*/,/*cFunc*/   ,/*aCpos*/,"Ma490TUDOK()",/*lF3*/,"A490Atu()"               ,aButtons,/*aParam*/,aAuto    ,/*lVirtual*/,/*lMaximized*/)      	
EndIf

//Integrao Mensagem Unica 
If lIntegDef .And. nOpcA == 1
	FwIntegDef("MATA490")
Endif

Return .T.

/*/

Ŀ
Funo    A490Atu    Autor  Kleber Dias Gomes      Data  10/05/04 
Ĵ
Descrio Atualizacao.                                                
                                                                      
Ĵ
Sintaxe e  Void A490Atu()                                             
Ĵ
Parametros                                                            
Ĵ
 Uso       Generico                                                   
ٱ


/*/

Function A490Atu()

Local lA490Atu := ExistBlock("A490ATU")
Local cNatCom	:= PADR(&(GetNewPar("MV_NATCOM","")),TamSx3("E2_NATUREZ")[1])

If lA490Atu
        ExecBlock("A490ATU",.F.,.F.)
Endif

//Controle de Saldo de Naturezas
If GetlAtuSld() .and. cNatCom != NIL
	//Atualizo o valor atual para o saldo da natureza incluindo no saldo previsto
	AtuSldNat(cNatCom, SE3->E3_VENCTO, "01", "2", "P", SE3->E3_COMIS, SE3->E3_COMIS,"+",,FunName(),"SE3",SE3->(Recno()),0)
Endif

Return .T.

/*/

Ŀ
Funo    MyMata490  Autor  Kleber Dias Gomes      Data 21/09/2004 
Ĵ
          Rotina de teste da rotina automatica do programa MATA490     
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta rotina tem como objetivo efetuar testes na rotina de    
          atualizacao da comissao.                                     
                                                                       
Ĵ
Uso        Materiais                                                   
ٱ


/*/
Function MyMata490()

Local aAuto := {}

PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "FAT" TABLES "SE3","SA1","SA3"
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de comissao",80))

//Ŀ
//| Verificacao do ambiente para teste                           |
//
dbSelectArea("SA1")
dbSetOrder(1)
If !SA1->(MsSeek(xFilial("SA1")+"00000101"))
	lOk := .F.
	ConOut("Cadastrar cliente: 00000101")
EndIf

dbSelectArea("SA3")
dbSetOrder(1)
If !SA3->(MsSeek(xFilial("SA3")+"000001"))
	lOk := .F.
	ConOut("Cadastrar vendedor: 000001")
EndIf

aAdd(aAuto,{"E3_VEND","000001",Nil})
aAdd(aAuto,{"E3_NUM","321654",Nil})
aAdd(aAuto,{"E3_EMISSAO",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_SERIE","UNI",Nil})
aAdd(aAuto,{"E3_CODCLI","000001",Nil})
aAdd(aAuto,{"E3_LOJA","01",Nil})
aAdd(aAuto,{"E3_BASE",1000,Nil})
aAdd(aAuto,{"E3_PORC",10,Nil})
aAdd(aAuto,{"E3_DATA",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_PREFIXO","001",Nil})
aAdd(aAuto,{"E3_PARCELA","2",Nil})
aAdd(aAuto,{"E3_TIPO","DH",Nil})
aAdd(aAuto,{"E3_PEDIDO","654321",Nil})
aAdd(aAuto,{"E3_VENCTO",CtoD("21/09/04"),Nil})
If SE3->(FieldPos("E3_MOEDA")) > 0
	aAdd(aAuto,{"E3_MOEDA","01",Nil})
EndIf

Mata490(aAuto,3)

aAuto := {}
ConOut(Repl("-",80))
ConOut(PadC("Teste de Alteracao de comissao",80))

aAdd(aAuto,{"E3_VEND","000001",Nil})
aAdd(aAuto,{"E3_NUM","321654",Nil})
aAdd(aAuto,{"E3_EMISSAO",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_SERIE","UNI",Nil})
aAdd(aAuto,{"E3_CODCLI","000001",Nil})
aAdd(aAuto,{"E3_LOJA","01",Nil})
aAdd(aAuto,{"E3_BASE",12000,Nil})
aAdd(aAuto,{"E3_PORC",10,Nil})
aAdd(aAuto,{"E3_DATA",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_PREFIXO","001",Nil})
aAdd(aAuto,{"E3_PARCELA","2",Nil})
aAdd(aAuto,{"E3_TIPO","DH",Nil})
aAdd(aAuto,{"E3_PEDIDO","654321",Nil})
aAdd(aAuto,{"E3_VENCTO",CtoD("21/09/04"),Nil})
If SE3->(FieldPos("E3_MOEDA")) > 0
	aAdd(aAuto,{"E3_MOEDA","01",Nil})
EndIf

Mata490(aAuto,4)

aAuto := {}
ConOut(Repl("-",80))
ConOut(PadC("Teste de Exclusao de comissao",80))

aAdd(aAuto,{"E3_VEND","000001",Nil})
aAdd(aAuto,{"E3_NUM","321654",Nil})
aAdd(aAuto,{"E3_EMISSAO",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_SERIE","UNI",Nil})
aAdd(aAuto,{"E3_CODCLI","000001",Nil})
aAdd(aAuto,{"E3_LOJA","01",Nil})
aAdd(aAuto,{"E3_BASE",12000,Nil})
aAdd(aAuto,{"E3_PORC",10,Nil})
aAdd(aAuto,{"E3_DATA",CtoD("21/09/04"),Nil})
aAdd(aAuto,{"E3_PREFIXO","001",Nil})
aAdd(aAuto,{"E3_PARCELA","2",Nil})
aAdd(aAuto,{"E3_TIPO","DH",Nil})
aAdd(aAuto,{"E3_PEDIDO","654321",Nil})
aAdd(aAuto,{"E3_VENCTO",CtoD("21/09/04"),Nil})
If SE3->(FieldPos("E3_MOEDA")) > 0
	aAdd(aAuto,{"E3_MOEDA","01",Nil})
EndIf

Mata490(aAuto,5)
aAuto := {}

RESET ENVIRONMENT

Return Nil


/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
     
Private aRotina := {	{ STR0007,"AxPesqui"		, 0 , 1 , 0 , .F.},;  // "Pesquisar"
							{ STR0008,"AxVisual"		, 0 , 2 , 0 , NIL},;  // "Visualizar"
							{ STR0009,"A490Inclui"	, 0 , 3 , 0 , NIL},;  // "Incluir"
							{ STR0010,"A490Altera"	, 0 , 4 , 0 , NIL},;  // "Alterar"
							{ STR0011,"A490Deleta"	, 0 , 5 , 0 , NIL},;  // "Excluir"
							{ STR0016,"A490Legend"	, 0 , 6 , 0 , .F.} }  // "Legenda"


If ExistBlock("MT490MNU")
	ExecBlock("MT490MNU",.F.,.F.)
EndIf

Return(aRotina)


/*

Ŀ
Funo    A490LegendAutor   Marco Bianchi          Data 10/10/2006 
Ĵ
          Demonstra a legenda das cores da mbrowse                     
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   Nenhum                                                       
Ĵ
Descrio Esta rotina monta uma dialog com a descricao das cores da    
          Mbrowse.                                                     
Ĵ
Uso        Materiais                                                   
ٱ


*/
Function A490Legend(nReg)

Local uRetorno := .T.
Local aLegenda := {	{ 'ENABLE'    , STR0014 },;	//'Comisso no paga'
							{ 'DISABLE'   , STR0015 }}		//'Comisso paga'

If nReg = Nil	// Chamada direta da funcao onde nao passa, via menu Recno eh passado
	uRetorno := {}
	Aadd(uRetorno, {'Empty(Dtos(E3_DATA))' , aLegenda[1][1]}) 	//'Comisso no paga'
	Aadd(uRetorno, {'!Empty(Dtos(E3_DATA))', aLegenda[2][1]})	//'Comisso paga'
Else
	BrwLegenda(cCadastro,STR0016,aLegenda) 
Endif
	
Return uRetorno


/*

Ŀ
Funo    Ma490TudOkAutor   Eduardo Riera          Data 25/08/2007 
Ĵ
          Validaao tipo TudoOk da enchoice                            
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   Nenhum                                                       
Ĵ
Descrio Esta rotina  chamada apos a confirmao da interface e inibe
          a confirmacao do mesmo.                                      
Ĵ
Uso        Materiais                                                   
ٱ


*/
Function Ma490TudOk()

Local lRetorno := .T.

If lRetorno
	If ExistBlock("A490TDOK")
		lRetorno := ExecBlock("A490TDOK",.F.,.F.)
	EndIf
EndIf

Return(lRetorno)

/*/

Ŀ
Funo    FinA181T    Autor  Marcelo Celi Marques  Data  05.05.08 
Ĵ
Descrio  Chamada semi-automatica utilizado pelo gestor financeiro   
Ĵ
 Uso       MATA490                                                    
ٱ


/*/
Function MATA490T(aParam)	
	cRotinaExec := "MATA490"
	ReCreateBrow("SE3",FinWindow)      		
	MATA490(,,aParam[1])
	ReCreateBrow("SE3",FinWindow)      	
	dbSelectArea("SE3")
	
	INCLUI := .F.
	ALTERA := .F.
Return .T.

/*/

Ŀ
Funo    GetlAtuSld Autor  Vendas / CRM           Data 01/08/2011
Ĵ
Descrio  Retorna lAtuSldNat.No se deve chamar AliasinDic em static 
           Causa erro via sigamdi                                     
Ĵ
Sintaxe e  Void GetlAtuSld()                                          
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Static Function GetlAtuSld()
	lAtuSldNat := FindFunction("AtuSldNat") .AND. AliasInDic("FIV") .AND. AliasInDic("FIW")
Return lAtuSldNat



//------------------------------------------------------------------------------
/*/{Protheus.doc} IntegDef

Mensagem nica	

@sample		IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction, lJSon ) 

@param		xEnt 
@param		nTypeTrans 
@param		cTypeMessage
@param		cVersion
@param		cTransaction
@param		lJSon

@return		aRet 

@author		Totvs Cascavel
@since		26/09/2018
@version	12
/*/
//------------------------------------------------------------------------------
Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction, lJSon ) 

Local aRet 		:= {}
Default lJSon 	:= .F.

If lJSon .And. FindFunction("MATI490O") 
	aRet := MATI490O( xEnt, nTypeTrans, cTypeMessage)
Endif

Return aRet

