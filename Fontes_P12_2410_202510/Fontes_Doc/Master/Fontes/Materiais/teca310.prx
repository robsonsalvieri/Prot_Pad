#INCLUDE "PROTHEUS.CH"                                     
#INCLUDE "TECA310.CH"
#DEFINE  CADASTRO STR0001 // "Help Desk"

Static aCacheTec      := {}
Static lRefreshListen := .F.
Static lGravaCham     := .F. 
Static lLeFila        := .F. 

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    Ё TECA310  Ё Autor Ё Sergio Silveira       Ё Data Ё27/07/2000 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Programa de atualizacao do Help Desk                        Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void TECA310(void)                                          Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё Generico                                                    Ё╠╠
╠╠цддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      Ё╠╠
╠╠цддддддддддддддбддддддддбддддддбддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё PROGRAMADOR  Ё DATA   Ё BOPS Ё  MOTIVO DA ALTERACAO                    Ё╠╠
╠╠цддддддддддддддеддддддддеддддддеддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё              Ё        Ё      Ё                                         Ё╠╠ 
╠╠юддддддддддддддаддддддддаддддддаддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function TECA310( cRotina )

Local   nScan     := 0 

Private cRoda     := ""
Private cCadastro := CADASTRO

Private aRotina := MenuDef()
							
dbSelectArea("ABK")

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Limpa o cache de habilidades do tecnico                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aCacheTec := {} 

If ValType( cRotina ) <> "C"
	dbSetOrder(1)
	DbSeek(xFilial("ABK"))                                              
	mBrowse( 6, 1,22,75,"ABK" )            
Else 	        
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Tratamento para chamada de outra rotina                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	cRotina := Upper( cRotina ) 
	If !Empty( nScan := AScan( aRotina, { |x| Upper( x[2] ) == cRotina } ) ) 
		cRoda := cRotina + "( 'ABK', ABK->( Recno() ), " + Str( nScan, 2 ) + ") " 		
		Eval( { || &( cRoda ) } ) 
	EndIf 	
EndIf

dbSelectArea("ABK")
dbSetOrder(1)
Return(.T.)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFunГЦo    ЁMenuDef   Ё Autor Ё Conrado Q. Gomes      Ё Data Ё 08.12.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё DefiniГЦo do aRotina (Menu funcional)                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё MenuDef()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MenuDef()
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Define Array contendo as Rotinas a executar do programa      Ё
	//Ё ----------- Elementos contidos por dimensao ------------     Ё
	//Ё 1. Nome a aparecer no cabecalho                              Ё
	//Ё 2. Nome da Rotina associada                                  Ё
	//Ё 3. Usado pela rotina                                         Ё
	//Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	//Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	//Ё    2 - Simplesmente Mostra os Campos                         Ё
	//Ё    3 - Inclui registros no Bancos de Dados                   Ё
	//Ё    4 - Altera o registro corrente                            Ё
	//Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Local aRotina := {	{STR0002	,"AxPesqui"  	,0	,1	,0	,.F.	}	,;    //"Pesquisar"
						{STR0003	,"AT310Visua"	,0	,2	,0	,.T. 	}	,;    //"Visualizar"
						{STR0004	,"AT310Inclu"	,0	,3	,0	,.T.	}	,;    //"Incluir"
						{STR0005	,"AT310Alter"	,0	,4	,0	,.T.	}	,;    //"Alterar"
						{STR0006	,"AT310Exclu"	,0	,5	,0	,.T.	}	,;    //"Excluir"
						{STR0038	,"AT310Listen"	,0	,3	,0	,.T.	}	}   //"Listener"
	Local aRotAdic := {} 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Ponto de entrada para inclusao de novas rotinas              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock( "AT310ROT" ) 
		If ValType( aRotAdic := ExecBlock( "AT310ROT", .F., .F. ) ) == "A"
			AEval( aRotAdic, { |x| AAdd( aRotina, x ) } ) 
		EndIf 
	EndIf 		
Return(aRotina)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT310IncluЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Inclusao de Help Desk                          Ё╠╠    
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void AT310Inclu(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠Ё          Ё ExpX1 = Sem funcao                                         Ё╠╠
╠╠Ё          Ё ExpL1 = Indica se e chamada via listener                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT310Inclu(cAlias,nReg,nOpcx,xFiller,lListen,lEnd)

Local aArea			:= GetArea()
Local aPages		:= { "HEADER" } 
Local aTitles		:= {	STR0001 ,;   // "Help Desk"
							STR0009 }  // "Chamado Tecnico"
Local aSize			:= {}						
Local aInfo			:= {}
Local aObjects		:= {}
Local aPosObj		:= {}
Local aPosObj2		:= {}
Local aFolderArea	:= {}
Local aListaCham	:= {} 
Local cCodUser		:= ""
Local cSeekAA1		:= "" 
Local nOpcA			:= 0
Local nI			:= 0
Local oDlg			:= Nil
Local oEnchVisu		:= Nil
Local oEnchAlte		:= Nil
Local oGetD			:= Nil
Local oGetDAlte		:= Nil
Local oFolder		:= Nil
Local nSaveSX8		:= GetSX8Len()
Local cNomCli		:= ""
Local cNomTec		:= ""
Local aStruField := {}
Local cField     := ""
Local nC         := 0
                                                     
lEnd	:= If( Valtype( lEnd ) == "L", lEnd, .F. )
lListen	:= If( Valtype( lListen ) == "L", lListen, .F. )

If !lListen
	If ExistBlock("AT310INC")
		Execblock("AT310INC",.F.,.F.)
	Endif
EndIf 	
	
Private aTela		:= {}
Private aTela1		:= {}
Private aGets		:= {} 
Private aTela2		:= {} 
Private aHeader		:= {} 
Private aCols		:= {} 
Private oEnchoice1	:= Nil
Private oGetDUS 	:= Nil
Private oGetDUS2	:= Nil
 
INCLUI  := .T.                  
lLeFila := .F. 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se existe algum chamado na fila                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды 
aListaCham := AtFilaHelp()

If Empty( aListaCham ) 
	M->ABK_NRCHAM := CriaVar( "ABK_NRCHAM", .F. )
Else
	If Len( aListaCham ) == 1
		M->ABK_NRCHAM := aListaCham[ 1, 1 ] 	
	EndIf 
EndIf 	

lEnd		:= Empty( M->ABK_NRCHAM )
lContinua	:= !Empty( M->ABK_NRCHAM ) .Or. !lListen  
	
If lContinua 

	If lListen     
		lRefreshListen := .T. 
		If ExistBlock("AT310INC")
			Execblock("AT310INC",.F.,.F.)
		Endif
	EndIf 		
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aStruField := FWSX3Util():GetAllFields("ABK")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			If AllTrim(cField) <> "ABK_NRCHAM" 
				M->&(cField) := CriaVar(cField,.T.)
			EndIf
		Next nC
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty( M->ABK_NRCHAM )
		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				M->&(cField) := CriaVar(cField,.F.)
			Next nC
		EndIf
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se existe o chamado, inicializa com seu conteudo             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AB1->( dbSetOrder( 1 ) ) 
		AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )

		Inclui := .F.

		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				If GetSx3Cache(cField,"X3_CONTEXT")=="V"
					M->&(cField) := CriaVar(cField)
				Else
					M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
				EndIf
			Next nC
		EndIf
		
		dbSelectArea("SA1")
		dbSetOrder(1)
		DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
		cNomCli :=SA1->A1_NOME
		
		M->AB1_NOMCLI := SA1->A1_NOME 
		M->ABK_CODCLI := M->AB1_CODCLI
		M->ABK_LOJA   := M->AB1_LOJA 
		M->ABK_NOMCLI := SA1->A1_NOME
		
		//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Identifica o tecnico do usuario                 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
		cCodUser := RetCodUsr()
		cSeekAA1 := xFilial( "AA1" ) + cCodUser

	  	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Alimenta o tecnico que incluiu esta sequencia   Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддды
	   AA1->( dbSetOrder( 4 ) ) 
	   If AA1->( DbSeek( cSeekAA1 ) ) 
			cNomTec := AA1->AA1_NOMTEC
			M->ABK_CODTEC := AA1->AA1_CODTEC 			
			M->ABK_NOMTEC := AA1->AA1_NOMTEC 
		EndIf
		
		Inclui := .T. 
	
	EndIf 
		
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader e aCols                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AT310Monta(If(Len( aListaCham ) == 1,4 , nOpcx)) // Se jА existe um chamado na fila, envia nOpc como 6 para poder pegar corretamente os dados existentes
	
	dbSelectArea( "AB2" ) 
	
	aSize := MsAdvSize() 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula objetos do primeiro folder                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aObjects := {}
	
	AAdd( aObjects, { 100, 40, .T., .T., .T. } )
	
	aInfo   := { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	aFolderArea := { 3, 3, aPosObj[1,4] - 17,aPosObj[1,3] - 5 }
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula objetos do segundo folder                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aObjects := {}

	AAdd( aObjects, { 100, 15, .T., .F. } )		
	AAdd( aObjects, { 100, 40, .T., .T. } )
	AAdd( aObjects, { 100, 40, .T., .T. } )
	
	aInfo := { 0, 0, aPosObj[1,3] - 2, aPosObj[1,4] - 14, 3, 3 }
	
	aPosObj2 := MsObjSize( aInfo, aObjects ) 
	
	If !IsBlind()
		DEFINE MSDIALOG oDlg TITLE CADASTRO FROM aSize[7],0 TO aSize[6],aSize[5] PIXEL
		
		oFolder := TFolder():New( aPosObj[1,1],aPosObj[1,2],aTitles,aPages,oDlg,,,, .T., .F., aPosObj[1,3],aPosObj[1,4],)
		
		oFolder:nOption := 2 
		
		oFolder:aDialogs[1]:oFont := oDlg:oFont
		oFolder:aDialogs[2]:oFont := oDlg:oFont                          
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da enchoice numero 1                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		oEnchoice1 := MsMGet():New( "ABK" ,nReg,nOpcx,,,,,aFolderArea,,3,,,,oFolder:aDialogs[1],,.T.,, "aTela1" )

		If !Empty( M->ABK_NRCHAM )
			M->ABK_NOMCLI := cNomCli
			M->ABK_NOMTEC := cNomTec
		EndIf
		
		aGets1 := AClone( aGets ) 
		aTela1 := AClone( aTela ) 
		
		aTela  := {} 
		aGets  := {} 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da enchoice visual                                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                       
		
		DEFINE SBUTTON oButConf FROM aPosObj2[1,1],aPosObj2[1,4]-60 TYPE 1 ACTION At310ActCham(1,oGetD,oGetDAlte,oEnchVisu,oEnchAlte,nOpcx) ENABLE OF oFolder:aDialogs[2]
		DEFINE SBUTTON oButCanc FROM aPosObj2[1,1],aPosObj2[1,4]-30 TYPE 2 ACTION At310ActCham(2,oGetD,oGetDAlte,oEnchVisu,oEnchAlte,nOpcx) ENABLE OF oFolder:aDialogs[2]
																															
		oButConf:Disable()
		oButCanc:Disable()	
		
		oEnchVisu := MsMGet():New( "AB1" ,nReg, 2,,,,,aPosObj2[2],,3,,,,oFolder:aDialogs[2],,.T.,, "aTela2" ) 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da enchoice para inclusao                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                       
		oEnchAlte := MsMGet():New( "AB1" ,nReg, 3,,,,,aPosObj2[2],,3,,,,oFolder:aDialogs[2],,.T.,, "aTela2", , .T. ) 	
		oEnchAlte:Hide()
		
		If !Empty( M->ABK_NRCHAM )
			M->AB1_NOMCLI := cNomCli
		EndIf	

		aGets2 := AClone( aGets ) 
		aTela2 := AClone( aTela )                  
		
		Inclui := .T.
		
		nOpcx  := 3 
		
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Nao retirar este bloco                                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nI := 1 to Len(oFolder:aDialogs)
			DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[ni]
		Next nI 	

		oFolder:bSetOption := { |nFolder| At310Fold(nFolder,nOpcx,oEnchVisu,oEnchAlte,oGetD,oGetDAlte) } 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da getdados visual                                  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                       
		oGetD := MsGetDados():New(aPosObj2[3,1],aPosObj2[3,2],aPosObj2[3,3],aPosObj2[3,4],2,"AT310LinOk","AT310TudOk",,.F.,,,,,,,,,oFolder:aDialogs[2])
		oGetDUS2 := oGetD                 
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da getdados para inclusao                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды                                       
		oGetDAlte := MsGetDados():New(aPosObj2[3,1],aPosObj2[3,2],aPosObj2[3,3],aPosObj2[3,4],nOpcx,"At300LinOk","At300TudOk","+AB2_ITEM",.T.,,,,,,,,,oFolder:aDialogs[2])
		oGetDAlte:Hide() 
		
		oFolder:nOption := 1 
		oGetDUS := oGetDAlte 
		
		ACTIVATE MSDIALOG oDlg ON INIT AT310Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela) .And. At310Laudo(),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD,oGetDAlte,oEnchVisu,oEnchAlte,oFolder)
	Else
		nOpca := 1
		M->ABK_MEMO := "ABS"
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
			If ( AT310Grava(1) )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				End
			Else	
				While ( GetSX8Len() > nSaveSx8 )
					RollBackSX8()
				End
			EndIf
		End Transaction
	Else 		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Libera o lock do ABL                                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		ABL->( dbSetOrder( 2 ) ) 	
		If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
			ABL->( MsUnlock() )
		EndIf 	
		
		While ( GetSX8Len() > nSaveSx8 )
			RollBackSX8()
		End

	EndIf                                 

EndIf 
	
RestArea( aArea ) 

If IsInCallStack("At310PrcLis") .AND. IsBlind()
	lEnd := .T.
EndIf

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT310VisuaЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Visualizacao de Help Desk                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void AT310Visua(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT310Visua(cAlias,nReg,nOpcx)

Local aArea       := GetArea()
Local aPages      := { "HEADER" } 
Local aTitles     := {	STR0001,;   // "Help Desk"
						STR0009 }  // "Chamado Tecnico"
Local aSize       := {}						
Local aInfo       := {}
Local aObjects    := {}
Local aPosObj     := {}
Local aPosObj2    := {}
Local aFolderArea := {}

Local nUsado      := 0
Local nCntFor     := 0
Local nCntFor2    := 0
Local nOpcA       := 0
Local nPosItem    := 0
Local nI          := 0 

Local oGetD
Local oDlg

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT310VIS")
	Execblock("AT310VIS",.F.,.F.)
Endif

Private aTela   := {}
Private aTela1  := {}
Private aGets   := {} 
Private aTela2  := {} 
Private aHeader:= {} 
Private aCols  := {} 

INCLUI := .F.
ALTERA := .F. 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

dbSelectArea( "ABK" ) 

aStruField := FWSX3Util():GetAllFields("ABK")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := ABK->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Se existe o chamado, inicializa com seu conteudo             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
AB1->( dbSetOrder( 1 ) ) 
AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )

Inclui := .F. 
aStruField := FWSX3Util():GetAllFields("AB1")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

dbSelectArea("SA1")
dbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
M->AB1_NOMCLI := SA1->A1_NOME 

M->ABK_CODCLI := M->AB1_CODCLI
M->ABK_LOJA   := M->AB1_LOJA	              
M->ABK_NOMCLI := SA1->A1_NOME

Inclui := .T. 
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aHeader                                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

AT310Monta( nOpcx )  

dbSelectArea( "AB2" ) 

aSize := MsAdvSize() 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Calcula objetos do primeiro folder                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aObjects := {}
AAdd( aObjects, { 100, 40, .T., .T., .T. } )

aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 } 

aPosObj := MsObjSize( aInfo, aObjects ) 

aFolderArea := { 3, 3, aPosObj[1,4] - 17,aPosObj[1,3] - 5 }

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Calcula objetos do segundo folder                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aObjects := {}

AAdd( aObjects, { 100, 40, .T., .T. } )
AAdd( aObjects, { 100, 40, .T., .T. } )

aInfo := { 0, 0, aPosObj[1,3] - 2, aPosObj[1,4] - 14, 3, 3 }
aPosObj2 := MsObjSize( aInfo, aObjects ) 

DEFINE MSDIALOG oDlg TITLE CADASTRO FROM aSize[7],0 TO aSize[6],aSize[5] PIXEL

oFolder := TFolder():New( aPosObj[1,1],aPosObj[1,2],aTitles,aPages,oDlg,,,, .T., .F., aPosObj[1,3],aPosObj[1,4],)

oFolder:aDialogs[1]:oFont := oDlg:oFont
oFolder:aDialogs[2]:oFont := oDlg:oFont                          

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem da enchoice numero 1                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
EnChoice( "ABK" ,nReg,nOpcx,,,,,aFolderArea,,3,,,,oFolder:aDialogs[1],,.T.,, "aTela1" )

aGets1 := AClone( aGets ) 
aTela1 := AClone( aTela ) 

aTela  := {} 
aGets  := {} 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Montagem da enchoice numero 2                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
EnChoice( "AB1" ,nReg,nOpcx,,,,,aPosObj2[1],,3,,,,oFolder:aDialogs[2],,.T.,, "aTela2" )

M->AB1_NOMCLI := SA1->A1_NOME

aGets2 := AClone( aGets ) 
aTela2 := AClone( aTela ) 


//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Nao retirar este bloco                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nI := 1 to Len(oFolder:aDialogs)
	DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[ni]
Next nI 	
                                          
oFolder:bSetOption := { |nFolder| At310Fold(nFolder,nOpcx) } 
oGetD := MsGetDados():New(aPosObj2[2,1],aPosObj2[2,2],aPosObj2[2,3],aPosObj2[2,4],nOpcx,"AT310LinOk","AT310TudOk",,.F.,,,,,,,,,oFolder:aDialogs[2])

ACTIVATE MSDIALOG oDlg ON INIT AT310Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)

RestArea( aArea ) 

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT310AlterЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Alteracao de Help Desk                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void AT310Alter(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT310Alter(cAlias,nReg,nOpcx)

Local aArea       := GetArea()
Local aPages      := { "HEADER" } 
Local aTitles     := {	STR0001,;   // "Help Desk"
						STR0009 }  // "Chamado Tecnico"
Local aSize       := {}						
Local aInfo       := {}
Local aObjects    := {}
Local aPosObj     := {}
Local aPosObj2    := {}
Local aFolderArea := {}

Local nUsado      := 0
Local nCntFor     := 0
Local nPosItem    := 0
Local nOpcA       := 0 
Local nI          := 0

Local oGetD                
Local oDlg    
Local oFolder  

Local cNomCli    := ""
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Local nSaveSX8	:= GetSX8Len()

INCLUI := .F.
ALTERA := .T. 

If ExistBlock("AT310ALT")
	Execblock("AT310ALT",.F.,.F.)
Endif

PRIVATE aTela   := {}
PRIVATE aTela1  := {}
PRIVATE aGets   := {} 
PRIVATE aTela2  := {} 
PRIVATE aHeader := {} 
PRIVATE aCols   := {} 

INCLUI := .F.
ALTERA := .T. 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se pode alterar                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ABL->( dbSetOrder( 2 ) ) 
If ABL->( DbSeek( xFilial( "ABL" ) + ABK->ABK_NRCHAM ) ) .And. Empty( ABL->ABL_NUMOS ) 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aStruField := FWSX3Util():GetAllFields("ABK")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			If GetSx3Cache(cField,"X3_CONTEXT")=="V"
				M->&(cField) := CriaVar(cField,.T.)
			Else
				M->&(cField) := ABK->(FieldGet(FieldPos(cField)))
			EndIf
		Next nC
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Inicializa as Variaveis da Enchoice                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty( M->ABK_NRCHAM ) 
		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				M->&(cField) := CriaVar(cField,.F.)
			Next nC
		EndIf
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se existe o chamado, inicializa com seu conteudo             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AB1->( dbSetOrder( 1 ) ) 
		AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )

		Inclui := .F.

		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				If GetSx3Cache(cField,"X3_CONTEXT")=="V"
					M->&(cField) := CriaVar(cField)
				Else
					M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
				EndIf
			Next nC
		EndIf

		dbSelectArea("SA1")
		dbSetOrder(1)
		DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
		cNomCli := SA1->A1_NOME
		M->AB1_NOMCLI := SA1->A1_NOME 
		
		M->ABK_CODCLI := M->AB1_CODCLI
		M->ABK_LOJA   := M->AB1_LOJA
		M->ABK_NOMCLI := SA1->A1_NOME
		
		Inclui := .T. 
	EndIf 
		
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Monta aHeader                                                Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	AT310Monta( nOpcx ) 
	
	dbSelectArea( "AB2" ) 
	
	aSize := MsAdvSize() 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula objetos do primeiro folder                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aObjects := {}
	AAdd( aObjects, { 100, 40, .T., .T., .T. } )
	
	aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	aFolderArea := { 3, 3, aPosObj[1,4] - 17,aPosObj[1,3] - 5 }
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula objetos do segundo folder                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aObjects := {}
	
	AAdd( aObjects, { 100, 40, .T., .T. } )
	AAdd( aObjects, { 100, 40, .T., .T. } )
	
	aInfo := { 0, 0, aPosObj[1,3] - 2, aPosObj[1,4] - 14, 3, 3 }
	aPosObj2 := MsObjSize( aInfo, aObjects ) 
	
	If !IsBlind()
		DEFINE MSDIALOG oDlg TITLE CADASTRO FROM aSize[7],0 TO aSize[6],aSize[5] PIXEL
		
		oFolder := TFolder():New( aPosObj[1,1],aPosObj[1,2],aTitles,aPages,oDlg,,,, .T., .F., aPosObj[1,3],aPosObj[1,4],)
		
		oFolder:aDialogs[1]:oFont := oDlg:oFont
		oFolder:aDialogs[2]:oFont := oDlg:oFont                          
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da enchoice numero 1                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		EnChoice( "ABK" ,nReg,nOpcx,,,,,aFolderArea,,3,,,,oFolder:aDialogs[1],,.T.,, "aTela1" )
		
		M->ABK_NOMCLI := cNomCli
		M->ABK_NOMTEC := Posicione("AA1",1,xFilial("AA1")+ABK->ABK_CODTEC,"AA1_NOMTEC")
		
		aGets1 := AClone( aGets ) 
		aTela1 := AClone( aTela ) 
		
		aTela  := {} 
		aGets  := {} 
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Montagem da enchoice numero 2                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		EnChoice( "AB1" ,nReg, 2,,,,,aPosObj2[1],,3,,,,oFolder:aDialogs[2],,.T.,, "aTela2" )
		
		M->AB1_NOMCLI := cNomCli
		
		aGets2 := AClone( aGets ) 
		aTela2 := AClone( aTela ) 
		

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Nao retirar este bloco                                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		For nI := 1 to Len(oFolder:aDialogs)
			DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[ni]
		Next nI 	
												
		oFolder:bSetOption := { |nFolder| At310Fold(nFolder,nOpcx) } 
		oGetD := MsGetDados():New(aPosObj2[2,1],aPosObj2[2,2],aPosObj2[2,3],aPosObj2[2,4],2,"AT310LinOk","AT310TudOk",,.F.,,,,,,,,,oFolder:aDialogs[2])
		
		ACTIVATE MSDIALOG oDlg ON INIT AT310Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela) .And. At310Laudo(),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)
	Else
		nOpcA := 1
	EndIf

	If ( nOpcA == 1 )
		Begin Transaction
			If ( AT310Grava(2) )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				End
			Else			
				While ( GetSX8Len() > nSaveSx8 )
					RollBackSX8()
				End
			EndIf
		End Transaction
	Else
		While ( GetSX8Len() > nSaveSx8 )
			RollBackSX8()
		End
	EndIf 	
	
Else 
	Help( " ", 1, "AT310OS" ) // Nao pode ser alterado Help Desk na situacao OS 
EndIf 	
		
RestArea( aArea ) 

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT310ExcluЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Exclusao  de Help Desk                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe e Ё Void AT310Exclu(ExpC1,ExpN1,ExpN2)                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Opcao selecionada                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT310Exclu(cAlias,nReg,nOpcx)

Local aArea       := GetArea()
Local aPages      := { "HEADER" } 
Local aTitles     := {	STR0001,;   // "Help Desk"
						STR0009 }  // "Chamado Tecnico"
Local aSize       := {}						
Local aInfo       := {}
Local aObjects    := {}
Local aPosObj     := {}
Local aPosObj2    := {}
Local aFolderArea := {}
						
Local lRetorno    := .F.                     

Local nUsado      := 0,nCntFor := nCntFor2 := 0
Local nOpcA       := 0
Local nPosItem    := 0        
Local nI          := 0

Local oGetD
Local oDlg

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Local nSaveSX8	:= GetSX8Len()

If ExistBlock("AT310EXC")
	Execblock("AT310EXC",.F.,.F.)
Endif

PRIVATE aTela   := {}
PRIVATE aTela1  := {}
PRIVATE aGets   := {} 
PRIVATE aTela2  := {} 

PRIVATE aHeader := {} 
PRIVATE aCols   := {} 

INCLUI := .F.
ALTERA := .F. 
             
If !Eof() 
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se pode excluir                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 
	If ABL->( DbSeek( xFilial( "ABL" ) + ABK->ABK_NRCHAM ) ) .And. Empty( ABL->ABL_NUMOS ) 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Inicializa as Variaveis da Enchoice                          Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aStruField := FWSX3Util():GetAllFields("ABK")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				If GetSx3Cache(cField,"X3_CONTEXT")=="V"
					M->&(cField) := CriaVar(cField,.T.)
				Else
					M->&(cField) := ABK->(FieldGet(FieldPos(cField)))
				EndIf
			Next nC
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se existe o chamado, inicializa com seu conteudo             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AB1->( dbSetOrder( 1 ) ) 
		AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )

		Inclui := .F.
		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				If GetSx3Cache(cField,"X3_CONTEXT")=="V"
					M->&(cField) := CriaVar(cField)
				Else
					M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
				EndIf
			Next nC
		EndIf

		dbSelectArea("SA1")
		dbSetOrder(1)
		DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
		M->AB1_NOMCLI := SA1->A1_NOME 
		
		M->ABK_CODCLI := M->AB1_CODCLI
		M->ABK_LOJA   := M->AB1_LOJA
		M->ABK_NOMCLI := SA1->A1_NOME
		
		Inclui := .T. 
			
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta aHeader                                                Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		
		AT310Monta( nOpcx ) 
		
		dbSelectArea( "AB2" ) 
		
		aSize := MsAdvSize() 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Calcula objetos do primeiro folder                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aObjects := {}
		AAdd( aObjects, { 100, 40, .T., .T., .T. } )
		
		aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 0, 0 } 
		
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		aFolderArea := { 3, 3, aPosObj[1,4] - 17,aPosObj[1,3] - 5 }
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Calcula objetos do segundo folder                            Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		aObjects := {}
		
		AAdd( aObjects, { 100, 40, .T., .T. } )
		AAdd( aObjects, { 100, 40, .T., .T. } )
		
		aInfo := { 0, 0, aPosObj[1,3] - 2, aPosObj[1,4] - 14, 3, 3 }
		aPosObj2 := MsObjSize( aInfo, aObjects ) 
		If !IsBlind()
			DEFINE MSDIALOG oDlg TITLE CADASTRO FROM aSize[7],0 TO aSize[6],aSize[5] PIXEL
			
			oFolder := TFolder():New( aPosObj[1,1],aPosObj[1,2],aTitles,aPages,oDlg,,,, .T., .F., aPosObj[1,3],aPosObj[1,4],)
			
			oFolder:aDialogs[1]:oFont := oDlg:oFont
			oFolder:aDialogs[2]:oFont := oDlg:oFont                          
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da enchoice numero 1                                Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			EnChoice( "ABK" ,nReg,nOpcx,,,,,aFolderArea,,3,,,,oFolder:aDialogs[1],,.T.,, "aTela1" )
			
			aGets1 := AClone( aGets ) 
			aTela1 := AClone( aTela ) 
			
			aTela  := {} 
			aGets  := {} 
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Montagem da enchoice numero 2                                Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			EnChoice( "AB1" ,nReg,nOpcx,,,,,aPosObj2[1],,3,,,,oFolder:aDialogs[2],,.T.,, "aTela2" )
			
			M->AB1_NOMCLI := SA1->A1_NOME 
			
			aGets2 := AClone( aGets ) 
			aTela2 := AClone( aTela ) 
			
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Nao retirar este bloco                                       Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			For nI := 1 to Len(oFolder:aDialogs)
				DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[ni]
			Next nI 	
													
			oFolder:bSetOption := { |nFolder| At310Fold(nFolder,nOpcx) } 
			oGetD := MsGetDados():New(aPosObj2[2,1],aPosObj2[2,2],aPosObj2[2,3],aPosObj2[2,4],nOpcx,"AT310LinOk","AT310TudOk",,.F.,,,,,,,,,oFolder:aDialogs[2])
			
			ACTIVATE MSDIALOG oDlg ON INIT AT310Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)
		Else
			nOpcA := 1
		EndIf
		If ( nOpcA == 1 )
			Begin Transaction
				If ( AT310Grava(3)  )
					EvalTrigger()
					While ( GetSX8Len() > nSaveSx8 )
						ConfirmSX8()
					End
					lRetorno := .T.
				Else 
					While ( GetSX8Len() > nSaveSx8 )
						RollBackSX8()
					End
				EndIf
			End Transaction
		Else 
			While ( GetSX8Len() > nSaveSx8 )
				RollBackSX8()
			End
		EndIf			
	Else 
		Help( " ", 1, "AT310OS" ) // Nao pode ser exluido Help Desk na situacao OS 
		lRetorno := .F. 
	EndIf 	
Else 
	lRetorno := .F. 
EndIf 	
		
RestArea( aArea ) 

Return( lRetorno ) 

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT310MONTA  Ё Autor ЁSergio Silveira      Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Monta aHeader                                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Void AT310MONTA(Void)                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nExp1 = OpГЦo da MsGetDados                                Ё╠╠
╠╠Ё          Ё lExp2 = Se И InclusЦo ou nЦo                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT310MONTA(nOpc, lInclui)

Local aArea		:= GetArea()			// Pega a Аrea atual da tabela em aberto
Local aAreaAB2	:= AB2->( GetArea() )	// Pega a Аrea atual da AB2
Local cSeek		:= ""					// Seek para montagem da aCols
Local cWhile	:= ""					// While para montagem da aHeader
Local lIncBack	:= INCLUI				// Backup da variАvel INCLUI
Default lInclui	:= .F.					// Default para o parБmetro lInclui
Default nOpc	:= 2					// OpГЦo da MsGetDados

INCLUI := lInclui
aHeader := {}
aCols	:= {}

If lInclui
	FillGetDados(	nOpc			,"AB2"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
				

	aCols[1][GdFieldPos("AB2_ITEM")] := "01"
	aCols[1][GdFieldPos("AB2_TIPO")] := "6"			
Else
	//зддддддддддддддддддддддд©
	//ЁMontagem aHeader, aColsЁ
	//юддддддддддддддддддддддды
	cSeek	:= xFilial( "AB2" ) + M->ABK_NRCHAM
	cWhile	:= "AB2->AB2_FILIAL+AB2->AB2_NRCHAM+AB2->AB2_ITEM"

	If Len(aHeader) == 0 .AND. Len(aCols) == 0
		FillGetDados(	nOpc			,"AB2"			,1				,cSeek					,;
		  				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/			,; 
						/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/				,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/	)
	Endif

	If Empty(aCols[1][GdFieldPos("AB2_ITEM")])
		aCols[1][GdFieldPos("AB2_ITEM")] := "01"
		aCols[1][GdFieldPos("AB2_TIPO")] := "6"		
	Endif
Endif

If !Empty( nScan := AScan( aHeader, { |x| AllTrim( x[2] ) == "AB2_TIPO" } ) ) 
	aHeader[nScan,6] := 'Pertence("6")'  
EndIf 						

INCLUI := lIncBack 
    
AB2->( RestArea( aAreaAB2 ) ) 
RestArea( aArea ) 

Return(.T.)

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁAT310Bar  Ё Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Mostra a EnchoiceBar na tela                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠лммммммммммьмммммммммммммммммммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Analista  Ё Data/Bops/Ver ЁManutencao Efetuada                      	  ╨╠╠
╠╠лммммммммммьммммммммяммммммьмммммммммммммммммммммммммммммммммммммммммммм╧╠╠
╠╠╨Hanna C.  |30/03/07|9.12  ЁBops 118469 - Alterado o nome dos Bitmaps   Ё╠╠
╠╠╨        	 Ё        |      Ёdefinidos pela Engenharia para o Protheus 10Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static ;
Function AT310Bar(oDlg,bOk,bCancel,nOpc,oGetD,oGetDAlte,oEnchVisu,oEnchAlte,oFolder)
Local aBut   := { {"SUGESTAO" ,{|| At300bFaq()}, STR0010, STR0010 } }  //"F.A.Q."                          
Local aUsButtons := {}
                                       
AAdd(aBut,{"PRODUT2"   ,{|| At300BPro() }, STR0011, STR0058 })  //"Hist╒rico do Equipamento..."
AAdd(aBut,{"POSCLI"    ,{|| At300BCli() }, STR0012, STR0059 })  //"Hist╒rico do Cliente..."
AAdd(aBut,{"RELATORIO" ,{|| cCodMem := M->ABK_CODMEM,cMemo := M->ABK_MEMO,;
		At310ViSeq(),M->ABK_CODMEM := cCodMem, M->ABK_MEMO := cMemo }, STR0013, STR0060 }) //"Demais sequencias..."
                 
If Inclui 
	AAdd(aBut,{"NOTE"   ,{|| At310New(oGetD,oGetDAlte,oEnchVisu,oEnchAlte,nOpc)}, STR0033, STR0061 })  // "Novo atendimento..." 
	AAdd(aBut,{"NOTE"   ,{|| At310ChamTec(oGetD,oGetDAlte,oEnchVisu,oEnchAlte,oFolder,nOpc)}, STR0039, STR0062 })  //"Inclui chamado tecnico..."
EndIf 
              
If nOpc == 2 .And. !AtIsRotina( "AT310TRACK" ) 	
	Aadd(aBut,{"bmpord1",{|| At310Track()}, STR0034, STR0063 }) //"System Tracker"
EndIf 	

AAdd(aBut,{"DISCAGEM",{|| At310Discar() }, STR0037, STR0037 })  //"Discar"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Adiciona botoes do usuario na EnchoiceBar                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "AT310BUT" ) 
	If ValType( aUsButtons := ExecBlock( "AT310BUT", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| AAdd( aBut, x ) } )  
	EndIf 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aBut))

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAT310GravaЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de Gravacao do projeto                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Grava( ExpN1 )                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁExcN1 : Operacao : 1-Inclusao / 2-Alteracao / 3-Exclusao    Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
 
Function At310Grava( nOper ) 

LOCAL aInfo       := {} 		//Array com dados da Fila de Help Desk
LOCAL cCampo      := ""     	//Nome do campo
LOCAL cCodTec     := ""			//Codigo do tecnico
LOCAL lRet        := .T.		//Variavel de retorno 
LOCAL lIncHelp    := .F. 		//Indica se esta incluindo um chamado de Help Desk
LOCAL nLoop       := 0 			//Variavel usada em For...Next

Do Case
Case nOper <> 3  

	//зддддддддддддддддддддддд©
	//Ё Pesquisa este tecnico Ё
	//юддддддддддддддддддддддды
	AA1->( dbSetOrder( 4 ) ) 
	If AA1->( DbSeek( xFilial( "AA1" ) + RetCodUsr() ) ) 
		cCodTec := AA1->AA1_CODTEC 
	EndIf 				

	ABK->( dbSetOrder( 1 ) ) 
	If ABK->( DbSeek( xFilial( "ABK" ) + M->ABK_NRCHAM + M->ABK_SEQ ) ) 
		RecLock( "ABK", .F. ) 
	Else 
	    
		lIncHelp := .T.
	
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Inclui os campos chave                                                 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		RecLock( "ABK", .T. ) 
		
		ABK->ABK_FILIAL := xFilial( "ABK" ) 
		ABK->ABK_NRCHAM := M->ABK_NRCHAM
		ABK->ABK_SEQ    := M->ABK_SEQ
	
	EndIf 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula o total de horas desta secao de atendimento e soma ao total    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды	
	cHoraAtu  := Left( Time(), 5 )                                            
	nHorasSC  := SubtHoras( M->ABK_DINISC, M->ABK_HINISC, MsDate(), cHoraAtu ) 
	
	cTempo := IntToHora( HoraToInt( M->ABK_TEMPO, 4 ) + nHorasSC, 4 ) 
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se o tempo de atendimento foi inferior a 1 minuto, grava como padrao 1 minuto Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Alltrim(cTempo) == "0000:00"
		cTempo := "0000:01"
	EndIf
	M->ABK_TEMPO := cTempo 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava os demais campos, inclusive especificos                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea( "ABK" ) 
	For nLoop := 1 To FCount()
		cCampo := FieldName( nLoop )
		If !( cCampo $ "ABK_FILIAL|ABK_NRCHAM|ABK_SEQ" ) .And. substr(cCampo,1,3) == "ABK"
			FieldPut( nLoop, M->&cCampo )
		EndIf
	Next nLoop
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava os campo memo                                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	MSMM( ABK->ABK_CODMEM, , , M->ABK_MEMO, 1, , , "ABK","ABK_CODMEM" )
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava o status de atendimento Help Desk na fila de atendimentos        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
 	ABL->( dbSetOrder( 2 ) )     
 	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
		RecLock( "ABL", .F. )                 
		
		aInfo := AtInfoABL() 
		ABL->ABL_SITUAC := aInfo[ 1 ] 
		ABL->ABL_TEMPO  := aInfo[ 2 ] 
		
		If nOper == 1 
			//здддддддддддддддддддддддддддддд©	
			//Ё Atualiza a proxima sequencia Ё
			//юдддддддддддддддддддддддддддддды 
			ABL->ABL_PRXSEQ := SomaIt( ABL->ABL_PRXSEQ ) 
			
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©	
			//Ё Limpa os campos de reminder, reavaliacao e redirecionamento Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды 
			ABL->ABL_DRET   := CriaVar( "ABL_DRET", .F. ) 
			ABL->ABL_HRET   := "" 
			ABL->ABL_REAVAL := "2"	// Sujeita a reavaliacao                     
			ABL->ABL_REAMOT := ""	// Motivo Help Desk 			
			ABL->ABL_PRXTEC := ""
			
		EndIf 	
		
		If lIncHelp 
		
			ABL->ABL_DTHELP := M->ABK_DINISC
			ABL->ABL_HRHELP := M->ABK_HINISC
			
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava o tecnico que efetuou o primeiro atendimento de help desk        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			ABL->ABL_CODTEC := cCodTec 

		EndIf 			
		ABL->( MsUnLock() ) 			
	EndIf 

	//зддддддддддддддддддддд©
	//Ё Pergunta por acao   Ё
	//юддддддддддддддддддддды
 	At310Action( ABL->ABL_SITUAC )
			
Otherwise  

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exclui os campos memo                                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	MSMM( ABK->ABK_CODMEM,,,,2 )  

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exclui a sequencia do Help Desk                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABK->( dbSetOrder( 1 ) ) 
	If ABK->( DbSeek( xFilial( "ABK" ) + M->ABK_NRCHAM + M->ABK_SEQ ) ) 
		RecLock( "ABK", .F., .T. ) 
		ABK->( dbDelete() ) 
		ABK->( MsUnLock() ) 
	EndIf  

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o status de atendimento Help Desk na fila de atendimentos     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
 	ABL->( dbSetOrder( 2 ) ) 
 	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
		RecLock( "ABL", .F. ) 
		aInfo := AtInfoABL() 
		ABL->ABL_SITUAC := aInfo[ 1 ] 
		ABL->ABL_TEMPO  := aInfo[ 2 ]         
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se excluiu todos os itens, volta a situacao de inicio  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !aInfo[ 3 ] 
			ABL->ABL_DRET   := CriaVar( "ABL_DRET", .F. ) 
			ABL->ABL_HRET   := "" 
			ABL->ABL_REAVAL := "1"  // Reavalia                      
			ABL->ABL_REAMOT := "1"  // Chamado  			
			ABL->ABL_PRXTEC := ""
			ABL->ABL_PRXSEQ := "01" 
		EndIf 
		
		ABL->( MsUnLock() ) 			
	EndIf 
	
EndCase           

//здддддддддддддддддддддддддддддддд©
//Ё Ponto de Entrada apos gravacao.Ё
//юдддддддддддддддддддддддддддддддды
If ExistBlock( "AT310GRV" )
   ExecBlock( "AT310GRV", .F., .F., { nOper } )
EndIf

Return( lRet ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310VlCliЁ Autor Ё Sergio Silveira       Ё Data Ё27/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao do Cliente                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At310VlCli()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310VlCli()

Local lRetorno    := .F.
Local aArea       := GetArea() 
Local cCampo      := ReadVar()
Local uConteudo   := &(ReadVar())

Do Case
Case ( "ABK_CODCLI"$cCampo )
	If ( SA1->A1_COD==uConteudo )
		uConteudo += SA1->A1_LOJA
	EndIf
	dbSelectArea("SA1")
	dbSetOrder(1)
	If ( DbSeek(xFilial("SA1")+uConteudo) )
		M->ABK_NOMCLI := SA1->A1_NOME
		lRetorno := .T.
	Else
		Help(" ",1,"REGNOIS")
	EndIf
	
Case ( "ABK_LOJA"$cCampo )
	dbSelectArea("SA1")
	dbSetOrder(1)
	If ( DbSeek(xFilial("SA1")+M->ABK_CODCLI+uConteudo) )
		M->ABK_NOMCLI := SA1->A1_NOME
		lRetorno := .T.
	Else
		Help(" ",1,"REGNOIS")
	EndIf
EndCase

If ( aArea[1] <> "SA1" )
	RestArea( aArea ) 
EndIf
Return(lRetorno)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAtFilaHelpЁ Autor Ё Sergio Silveira       Ё Data Ё28/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Devolve a proxima ocorrencia de Help Desk que este tecnico Ё╠╠
╠╠Ё          Ё deve atender                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := AtFilaHelp( [ ExpC2 ] )                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC2 -> Tecnico ( opcional )                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Numero do chamado ( chamado + item )              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function AtFilaHelp( cTecnico )
    
LOCAL aTecHabil   := {}        
LOCAL aReqHabil   := {}
LOCAL aFilaHabil  := {}
LOCAL aOldArea    
                      
LOCAL cQuery      := ""
LOCAL cNrCham     := ""
LOCAL cSeekAA1    := ""
LOCAL cAvalStr    := SuperGetMV( "MV_ATREGHD" )  
LOCAL cDtTimeAtu  := ""
LOCAL cDtTimeFila := ""
LOCAL cSeekABL    := ""
LOCAL cAliasABL   := ""

LOCAL lHabil      := .F.                   
LOCAL lAvalNivel  := .T. 
LOCAL lAvalHabil  := .T.
LOCAL lQuery      := .F. 
LOCAL lTodosHab   := ( GetNewPar( "MV_ATHDMULT", "1" ) == "2" ) 

LOCAL nLoop       := 0   
LOCAL nScan       := 0               

lAvalHabil := ( SubStr( cAvalStr, 1, 1 ) == "1" ) 
lAvalNivel := ( SubStr( cAvalStr, 2, 1 ) == "1" ) 

If ValType( cTecnico ) == "C"
    cSeekAA1 := xFilial( "AA1" ) + cTecnico 
    AA1->( dbSetOrder( 1 ) ) 
Else
	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Identifica o tecnico do usuario                 Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддды
	cCodUser := RetCodUsr()                  
	cSeekAA1 := xFilial( "AA1" ) + cCodUser
	AA1->( dbSetOrder( 4 ) ) 
EndIf           

If AA1->( DbSeek( cSeekAA1 ) ) 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё E preciso que o tecnico seja de Help Desk ou generico  Ё 
	//Ё e que o mesmo esteja disponivel para alocacao          Ё	
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If AA1->AA1_TIPO $ "23" .And. AA1->AA1_ALOCA == "1" 

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se as habilidades que o tecnico possui estao no cache Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If Empty( nScan := AScan( aCacheTec, { |x| x[1] == AA1->AA1_CODTEC } ) ) 

			//зддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Identifica as habilidades e nivel deste tecnico Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддды
			#IFDEF TOP
				If TcSrvType() # "AS/400"
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Processamento para SQL                                  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					
					aOldArea := GetArea()
					cQuery := ""
					cQuery += "SELECT AA2_HABIL, AA2_NIVEL FROM " + RetSqlName( "AA2" ) + " "
					cQuery += "WHERE AA2_FILIAL='" + xFilial( "AA2" ) + "' AND "
					cQuery += "AA2_CODTEC='" + AA1->AA1_CODTEC + "' AND "
					cQuery += "D_E_L_E_T_=' '"
					cQuery := ChangeQuery( cQuery ) 
					dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), "TRBAA2", .F., .T. ) 
					TcSetField( "TRBAA2", "AA2_NIVEL", "N", 3, 0 ) 
					If Alias() == "TRBAA2"                 
						While !TRBAA2->( Eof()) 
							AAdd( aTecHabil, { TRBAA2->AA2_HABIL, TRBAA2->AA2_NIVEL } ) 
							TRBAA2->( dbSkip() ) 		
						End 
						TRBAA2->( dbCloseArea() )
					EndIf  
	
					RestArea( aOldArea ) 
					dbSelectArea( "AA2" ) 
					
				Else 
			#ENDIF 
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Processamento para ISAM                                 Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					AA2->( dbSetOrder( 1 ) )        
					cSeekAA2 := xFilial( "AA2" ) + AA1->AA1_CODTEC 
					If AA2->( DbSeek( cSeekAA2 ) ) 
						AA2->( dbEval( { || AAdd( aTecHabil, { AA2->AA2_HABIL, AA2->AA2_NIVEL } ) },,;	
							{ || cSeekAA2 == AA2->AA2_FILIAL + AA2->AA2_CODTEC }, , , .T. ) )										
					EndIf 
			#IFDEF TOP                                       
				EndIf 
			#ENDIF                                                                        
			AAdd( aCacheTec, { AA1->AA1_CODTEC, AClone( aTecHabil ) } ) 
		Else
 			aTecHabil := AClone( aCacheTec[ nScan, 2 ] ) 
		EndIf 		
					
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Percorre a fila de Help Desk ate encontrar um chamado valido para este tecnico Ё
		//Ё ou todos os chamados validos ( dependendo da configuracao )                    Ё		
		//Ё Considera em primeiro lugar a prioridade da fila                               Ё	
		//Ё Considera apenas os itens a serem avaliados                                    Ё			
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		cSeekABL := xFilial( "ABL" ) + "1"
		ABL->( dbSetOrder( 3 ) ) 		

		#IFDEF TOP      
		
			lQuery := .T. 
			
			cAliasABL := GetNextAlias()
			
			cQuery := ""
			
			cQuery += "SELECT ABL_FILIAL,ABL_REAVAL,ABL_SITUAC,"
			cQuery += "ABL_DRET,ABL_HRET,R_E_C_N_O_ ABLRECNO FROM " + RetSqlName( "ABL" ) + " " 
			cQuery += "WHERE " 
			cQuery += "ABL_FILIAL='" + xFilial( "ABL" ) + "' AND "
			cQuery += "ABL_REAVAL='1' AND "						
			cQuery += "ABL_SITUAC IN ('1','2') AND "
			cQuery += "D_E_L_E_T_=' ' ORDER BY " + SqlOrder( ABL->( IndexKey() ) )  
			
			cQuery := ChangeQuery( cQuery ) 
			              
			dbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasABL, .F., .T. ) 
			
			TcSetField( cAliasABL, "ABL_DRET", "D",8, 0 )  			
			TcSetField( cAliasABL, "ABLRECNO", "N",10, 0 )  			
		
		#ELSE 
			            
			cAliasABL := "ABL"			            	
			lQuery := .F. 			
			ABL->( dbSetOrder( 3 ) ) 
			ABL->( DbSeek( cSeekABL ) )  
			            
		#ENDIF 

		lContBusca := .T.
		aFilaHabil := {} 

		While !( cAliasABL )->( Eof() ) .And. cSeekABL == ( cAliasABL )->ABL_FILIAL + ;
				 ( cAliasABL )->ABL_REAVAL .And. lContBusca
		
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica se a situacao esta "nao iniciada" ou se foi programado reavaliar Ё
			//Ё ( reminder / redirecionar / eleva nivel )                                 Ё			
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			cDtTimeFila := DToS( ( cAliasABL )->ABL_DRET ) + ( cAliasABL )->ABL_HRET
			cDtTimeAtu  := DToS( MsDate() ) + Left( Time(), 5 ) 

			lHabil     := .F.  
						  
			If ( cAliasABL )->ABL_SITUAC=="1".Or.( (cAliasABL)->ABL_SITUAC == "2" .And. ;
				If( Empty( ( cAliasABL )->ABL_DRET ), .T., cDtTimeAtu >= cDtTimeFila ) )			

				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Posiciona para efetuar o travamento                                     Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If lQuery
					ABL->( dbGoto( ( cAliasABL )->ABLRECNO ) ) 
				EndIf 			
			
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Obtem a trava para garantir exclusividade                               Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ABL->( MsRLock() ) 
					If Empty( ABL->ABL_PRXTEC ) 				
						//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Identifica se avalia habilidade                                         Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				 		If ( lAvalHabil ) 
				
							//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Apura as habilidades necessarias para atender o item do chamado tecnico Ё
							//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							aReqHabil := {} 
							At310HdReq( @aReqHabil ) 						
							   
							lHabil := .T. 
								
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Verifica se o tecnico possui a habilidade para atender o item do chamado tecnico Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							For nLoop := 1 To Len( aReqHabil ) 
								If Empty( nScan := AScan( aTecHabil, { |x| x[1] == aReqHabil[ nLoop, 1 ] } ) )  		
									lHabil := .F. 	     	
									Exit 	
								Else 
									//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
									//Ё Identifica se avalia o nivel                                            Ё
									//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
									If ( lAvalNivel )	
										If aReqHabil[ nLoop,  2 ] > aTecHabil[ nScan, 2 ]  			
											lHabil := .F.
											Exit
										EndIf 	
									Else 
										lHabil := .T. 						
									EndIf 	
								EndIf 	
							Next nLoop 	    
							
						Else 	
							lHabil := .T. 						
						EndIf 	
						
					Else 
						//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Se tecnico pre definido, verifica se e este tecnico                     Ё
						//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						lHabil := ( ABL->ABL_PRXTEC == AA1->AA1_CODTEC )
					EndIf 	
							
					cNrCham := ABL->ABL_NRCHAM 
			
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Libera o Lock apenas se este elemento nao for escolhido Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If !lHabil 
						ABL->( MsUnLock() ) 
					Else
						AAdd( aFilaHabil, { ABL->ABL_NRCHAM, ABL->( Recno() ) } )
					EndIf 

					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Se necessita de apenas 1 e encontrou, encerra a busca   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If !lTodosHab
						lContBusca := !lHabil 											
					EndIf 					
						 		
				EndIf 					
			EndIf

			( cAliasABL )->( dbSkip() ) 
		End 
		
		If lQuery
			( cAliasABL )->( dbCloseArea() ) 
			dbSelectArea( "ABL" ) 
		EndIf 	
		
	Else	                          
		// O tecnico deste usuario nao atende help desk
		// Ou nao esta disponivel para alocacao 
		Help( " ",1,"AT310TPTEC" ) 
	EndIf 	
	
Else 
	Help( " ", 1, "AT310USER" ) // Nao existe tecnico associado a este usuario
EndIf  

Return( aFilaHabil ) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310Fold Ё Autor Ё Sergio Silveira       Ё Data Ё01/08/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Evento de mudanca do Folder                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Fold( ExpN1 )                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 -> Numero do novo Folder                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310Fold( nFolder,nOpcx,oEnchVisu,oEnchAlte,oGetD,oGetDAlte ) 

Local aListaCham := {}  

Local cCodUser := ""
Local cSeekAA1 := ""

Local lRet       := .T. 

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If nFolder == 1 

	aTela  := aClone( aTela1 ) 
	aGets  := aClone( aGets1 ) 
	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se a opcao for inclusao                                 Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If nOpcx == 3 
	        
		If lGravaCham 
			Help( " ", 1, "AT310CHAMA" ) // "E necessario efetuar a confirmacao ou cancelamento deste chamado !"
			lRet := .F. 
		Else 
	
			If lLeFila 
				
				lLeFila := .F.     
			
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Efetua novamente a leitura da fila de Help Desk         Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aListaCham := AtFilaHelp()
				
				If Empty( aListaCham ) 
					M->ABK_NRCHAM := CriaVar( "ABK_NRCHAM", .F. )
				Else
					If Len( aListaCham ) == 1
						M->ABK_NRCHAM := aListaCham[ 1, 1 ] 	
					EndIf 
				EndIf 	
								                    
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Inicializa as Variaveis da Enchoice                          Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aStruField := FWSX3Util():GetAllFields("ABK")
				If Len(aStruField) > 0
					For nC := 1 to Len(aStruField )
						cField := aStruField[nC]
						If AllTrim(cField) <> "ABK_NRCHAM" 
							M->&(cField) := CriaVar(cField,.T.)
						EndIf
					Next nC
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Inicializa as Variaveis da Enchoice                          Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			    
				If Empty( M->ABK_NRCHAM )
					aStruField := FWSX3Util():GetAllFields("AB1")
					If Len(aStruField) > 0
						For nC := 1 to Len(aStruField )
							cField := aStruField[nC]
							M->&(cField) := CriaVar(cField,.F.)
						Next nC
					EndIf
				Else
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Se existe o chamado, inicializa com seu conteudo             Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					AB1->( dbSetOrder( 1 ) ) 
					AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )

					Inclui := .F.

					aStruField := FWSX3Util():GetAllFields("AB1")
					If Len(aStruField) > 0
						For nC := 1 to Len(aStruField )
							cField := aStruField[nC]
							If GetSx3Cache(cField,"X3_CONTEXT")=="V"
								M->&(cField) := CriaVar(cField)
							Else
								M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
							EndIf
						Next nC
					EndIf

					dbSelectArea("SA1")
					dbSetOrder(1)
					DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
					M->AB1_NOMCLI := SA1->A1_NOME 
					
					M->ABK_CODCLI := M->AB1_CODCLI
					M->ABK_LOJA   := M->AB1_LOJA 
					M->ABK_NOMCLI := SA1->A1_NOME
					
					//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Identifica o tecnico do usuario                 Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
					cCodUser := RetCodUsr()                  
					cSeekAA1 := xFilial( "AA1" ) + cCodUser
				   
				  	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Alimenta o tecnico que incluiu esta sequencia   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
				   AA1->( dbSetOrder( 4 ) ) 
				   If AA1->( DbSeek( cSeekAA1 ) ) 
						M->ABK_CODTEC := AA1->AA1_CODTEC 			
						M->ABK_NOMTEC := AA1->AA1_NOMTEC 
					EndIf
					
					Inclui := .T. 

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Monta aHeader e aCols                                        Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды		
					AT310Monta( nOpcx )
					
					oGetD:Refresh() 
		
				EndIf 
			
			EndIf 	
		
		EndIf 	
	
	EndIf 
			
Else             

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se a opcao for inclusao                                 Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If nOpcx == 3        
	
		lGravaCham := .F. 
	
		oEnchVisu:Show()
		oGetD:Show() 
	    
		oEnchVisu:SetFocus() 
		
	EndIf	
	
	aTela := aClone( aTela2 ) 
	aGets := aClone( aGets2 )
	 		
EndIf 

Return( lRet )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310VldChЁ Autor Ё Sergio Silveira       Ё Data Ё04/08/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao do chamado / sequencia Help Desk                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At310VldCh( ExpN1 )                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
                                                
Function At310VldCh()

Local cConteudo := &( ReadVar() )	// ConteЗdo do campo que estА sendo alterado
Local lIncBack  := INCLUI  			
Local lRet      := .F. 				
Local cCodUser  := ""				// CСdigo do usuАrio recuperado
Local cSeekAA1	:= ""				// Chave de procura do tИcnico responsАvel

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If "ABK_NRCHAM" $ ReadVar()                
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o item da fila esta em aberto               Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 
	If ABL->( DbSeek( xFilial( "ABL" ) + cConteudo ) ) 
		If ABL->ABL_SITUAC <> "3"
			ABK->( dbSetOrder( 1 ) )   
			
			M->ABK_SEQ := ABL->ABL_PRXSEQ 
			
			If !ABK->( DbSeek( xFilial( "ABK" ) + cConteudo + M->ABK_SEQ ) ) 
			
				AB2->( dbSetOrder( 1 ) ) 
				If AB2->( DbSeek( xFilial( "AB2" ) + cConteudo ) ) 
					If AB2->AB2_TIPO == "6" 
						lRet := .T. 		
					Else 		
						Help( " ", 1, "AT310SITUA" ) 	
						lRet := .F. 			
					EndIf 
				Else 
					Help( " ", 1, "REGNOIS" ) 
				EndIf                               
				
				If lRet
				
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Realimenta as variaveis de memoria da segunda enchoice  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					AB1->( dbSetOrder( 1 ) ) 
					AB1->( DbSeek( xFilial( "AB1" ) + Left( M->ABK_NRCHAM, Len( AB1->AB1_NRCHAM ) ) ) )
					
					Inclui := .F. 

					aStruField := FWSX3Util():GetAllFields("AB1")
					If Len(aStruField) > 0
						For nC := 1 to Len(aStruField )
							cField := aStruField[nC]
							If GetSx3Cache(cField,"X3_CONTEXT")=="V"
								M->&(cField) := CriaVar(cField)
							Else
								M->&(cField) := AB1->(FieldGet(FieldPos(cField)))
							EndIf
						Next nC
					EndIf

					dbSelectArea("SA1")
					dbSetOrder(1)
					DbSeek(xFilial("SA1")+M->AB1_CODCLI+M->AB1_LOJA)
					M->AB1_NOMCLI := SA1->A1_NOME 
					
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Realimenta as variaveis chave da primeira enchoice  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
					M->ABK_CODCLI := M->AB1_CODCLI
					M->ABK_LOJA   := M->AB1_LOJA
					M->ABK_NOMCLI := SA1->A1_NOME					
					M->ABK_ORIGEM := At310Orig()
					
					//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Identifica o tecnico do usuario                 Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
					cCodUser := RetCodUsr()                  
					cSeekAA1 := xFilial( "AA1" ) + cCodUser
				   
				  	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Alimenta o tecnico que incluiu esta sequencia   Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддды
					DbSelectArea("AA1")
					AA1->( dbSetOrder( 4 ) ) 
					If AA1->( DbSeek( cSeekAA1 ) ) 
						M->ABK_CODTEC := AA1->AA1_CODTEC 			
						M->ABK_NOMTEC := AA1->AA1_NOMTEC 
					EndIf			
					                     
					Inclui := lIncBack 
	
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Realimenta as demais variaveis chave da primeira enchoice  Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					aStruField := FWSX3Util():GetAllFields("ABK")
					If Len(aStruField) > 0
						For nC := 1 to Len(aStruField )
							cField := aStruField[nC]
							If !(AllTrim(cField)  $ "ABK_CODCLI|ABK_LOJA|ABK_NOMCLI|ABK_NRCHAM|ABK_SEQ|ABK_ORIGEM|ABK_CODTEC|ABK_NOMTEC")
								M->&(cField) := CriaVar(cField)
							EndIf
						Next nC
					EndIf
			
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Refaz o acols                                           Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				 	At310Monta( 4 )
					
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Refresh nas enchoice e getdados                         Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					oEnchoice1:EnchRefreshAll() 
					
					If Type( "oGetDUS2" ) == "O" 
						oGetDUS2:oBrowse:Refresh() 
					EndIf 	
					
				EndIf 
			Else 
				Help( " ", 1, "EXISTCHAV" ) 	
				lRet := .F. 		
			EndIf 	
		EndIf 	
	Else
		lRet := .F. 

	EndIf 	
		
ElseIf "ABK_SEQ" $ ReadVar()  	
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Validacao da sequencia                                  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABK->( dbSetOrder( 1 ) ) 
	If !ABK->( DbSeek( xFilial( "ABK" ) + M->ABK_NRCHAM + cConteudo ) ) 
		lRet := .T.
	Else 
		Help( " ", 1, "EXISTCHAV" ) 	
		lRet := .F. 		
	EndIf 	
EndIf 	
	
Return( lRet ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At310New Ё Autor Ё Sergio Silveira       Ё Data Ё08/08/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Inicializacao da sequencia do Help Desk                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310New()                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
                                                
Function At310New(oGetD,oGetDAlte,oEnchVisu,oEnchAlte,nOpc)

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Default nOpc := 2

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Traz a proxima sequencia de atendimento                 Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice1                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("ABK")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If AllTrim(cField) $ "ABK_NRCHAM|ABK_SEQ"
			M->&(cField) := CriaVar(cField,.F.)
		Else
			M->&(cField) := CriaVar(cField,.T.)
		EndIf
	Next nC
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Inicializa as Variaveis da Enchoice2                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

aStruField := FWSX3Util():GetAllFields("AB1")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField,.F.)
	Next nC
EndIf
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Monta aHeader e aCols                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

AT310Monta( nOpc )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Efetua refresh nas enchoice e nas getdados                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
oEnchoice1:EnchRefreshAll() 
oEnchVisu:EnchRefreshAll() 
oGetD:oBrowse:Refresh() 

Return( .T. ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ViSeqЁ Autor Ё Sergio Silveira       Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Visualiza demais sequencias de Help Desk                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310ViSeq()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310ViSeq() 

Local aArea	      := GetArea()
Local aSavCols    := aClone(aCols)
Local aSavHead    := aClone(aHeader)

Local cCadastro   := STR0022 // "Sequencias de atendimento" 
Local cTarefa     := ""
Local cDescTrf    := ""
Local cSeekABK    := ""            
Local cCampoNOK   := "ABK_NRCHAM|ABK_DINISC|ABK_HINISC"
Local cSeq        := ""   
Local cNrCham     := M->ABK_NRCHAM 

Local nCntFor     := 0
Local nUsado      := 0
Local nSavN	      := If(Type("N")=="U",1,N)

Local oDlg
Local oGetd2     

Local aStruField := {}
Local cField     := ""
Local nC         := 0

PRIVATE aCols     := {}
PRIVATE aHeader	  := {}
PRIVATE INCLUI    := .F. 

// oGetD:oBrowse:lDisablePaint:= .T.
	
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMontagem do aHeader                                                     Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("ABK")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL")) .And. ;
			!cField $ cCampoNOK
			nUsado++
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"ABK",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMontagem do aCols                                                       Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

cSeq := M->ABK_SEQ

dbSelectArea("ABK")             

cSeekABK := xFilial("ABK") + M->ABK_NRCHAM 

ABK->( dbSetOrder( 1 ) ) 
ABK->( DbSeek( cSeekABK ) ) 

While ( !ABK->( Eof() ) .And. cSeekABK == ABK->ABK_FILIAL + ABK->ABK_NRCHAM ) 	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exibe as sequencias diferentes da posicionada                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ABK->ABK_SEQ <> cSeq 
		AAdd(aCols,Array(nUsado))
		For nCntFor := 1 To nUsado
			If ( aHeader[nCntFor][10] <> "V" )
				aCols[Len(aCols)][nCntFor] := ABK->(FieldGet(FieldPos(aHeader[nCntFor][2])))
			Else
				aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor,2],.T.)
			EndIf
		Next nCntFor 
	EndIf 	
	ABK->( dbSkip() ) 		
End

If ( Len(aCols) <> 0 )
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 09,0 TO 28,80 OF oMainWnd
	
	@ 016,005 SAY RetTitle("ABK_NRCHAM" ) 	SIZE 035,009 	OF oDlg PIXEL
	@ 016,040 MSGET cNrCham      			SIZE 136,009 	OF oDlg PIXEL WHEN .F.
		
	oGetd2:=MsGetDados():New(030,005,138,314,2,"","","",.F.)  
	
	If GDFieldPos( "ABK_SEQ" ) == 1 
		oGetd2:oBrowse:nFreeze := 1 
	EndIf	
		
	ACTIVATE MSDIALOG oDlg ON INIT At310Bar2(oDlg,{||oDlg:End()},{||oDlg:End()},,oGetD2)
Else
	Help(" ",1,"AT310SEQ") //Hedit2
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁRestaura a entrada da rotina                                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
aCols 	:= aClone(aSavCols)
aHeader	:= aClone(aSavHead)
N		:= nSavN
// oGetD:oBrowse:lDisablePaint:= .F.

RestArea( aArea ) 

Return( .T. ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ActionЁ Autor Ё Sergio Silveira      Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Acao a ser tomada apos a gravacao da sequencia de          Ё╠╠
╠╠Ё          Ё atendimento                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Action( ExpC1 )                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 := Situacao do item da fila                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё 17/04/07 Ё Conrado Q.    Ё Bops 122657: Adicionado possibilidade de   Ё╠╠
╠╠Ё          Ё               Ё encerrar as sequЙncias em aberto do HD.    Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310Action( cSituac ) 

Local aDescri	:= {}					// Textos de ajuda utilizados na selaГЦo do RadioBox
                  
Local lAberto	:= ( cSituac == "2" )	// Se o Help-Desk continua em aberto ou estА sendo encerrado
Local lEncSeqAnt:= .F.					// Se encerra as sequencias de atendimento anteriores tambИm
Local cNrCham	:= ABK->ABK_NRCHAM		// NЗmero de chamado atual
Local aArea		:= GetArea()			// Salva a area atual
                  
Local nRadio	:= 1					// OpГЦo do RadioBox selecionado
Local nOpca		:= 0  					// OpГЦo de botЦo na janela
Local nCount	:= 0					// Quantidade de sequencias que o chamado tem
Local nLastRecno:= ABK->( Recno() )	// PosiГЦo do registro atual

Local oDlg		:= Nil
Local oRadio	:= Nil
Local oBut1		:= Nil
Local oBut2		:= Nil
Local oBold		:= Nil
Local oPanel	:= Nil
Local oSayPan	:= Nil
Local oCheckBox	:= Nil

If !IsBlind()
	If lAberto
		aDescri := { STR0032, STR0025, STR0029, STR0035 } //"Nao efetua acao"
	Else	
		aDescri := { STR0036, STR0035 }	 //"Gera uma ordem de servico a partir do item de Help Desk"###"Nao efetua acao"
	EndIf 

	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 00,0 TO 236,410 OF oMainWnd PIXEL 

	DEFINE FONT oBold NAME "Arial" SIZE 0, -11 BOLD
	@   0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 130 NOBORDER WHEN .F. PIXEL

	If lAberto 
		@ 03, 50 SAY STR0019 PIXEL // "Este item de Help Desk permanece em aberto."
	Else	
		@ 03, 50 SAY STR0023 PIXEL // "Este item de Help Desk esta encerrado." 
	EndIf 

	@ 12, 50 SAY STR0020 PIXEL // "Selecione a atitude que deseja tomar."

	@ 21 ,40 TO 23 ,400 LABEL '' OF oDlg PIXEL

	If lAberto 
		@ 38, 50 RADIO oRadio VAR nRadio 3D SIZE 70, 11 PROMPT STR0014,;
			STR0015, STR0016, STR0018 of oDlg PIXEL // "Agregar conhecimento","Redirecionar","Ativar Reminder","Nenhuma"
		nRadio := 4 		
	Else 		
		@ 48, 50 RADIO oRadio VAR nRadio 3D SIZE 70, 11 PROMPT STR0017, STR0018 ;
			of oDlg PIXEL // "Gerar O.S.","Nenhuma"
		nRadio := 2

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPonto de entrada para validar as propriedades do RadioButton  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ExistBlock("AT310Rad")
			Execblock("AT310Rad",.F.,.F.,oRadio)
		Endif  
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica quantas sequЙncias esse chamado tem, se tiver maisЁ
		//Ёde uma sequЙncia, exibe a opГЦo para encerrar tambИm as    Ё
		//Ёoutras ocorrЙncias.                                        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nLastRecno := ABK->( Recno() )	
		DbSelectArea("ABK")
		DbSetOrder(1)
		
		If ABK->( DbSeek( xFilial("ABK") + cNrCham ) )
			Do While	(	!ABK->( EOF() )						.AND.;
							ABK->ABK_FILIAL == xFilial("ABK")	.AND.;
							ABK->ABK_NRCHAM == cNrCham			)
				nCount++
				ABK->( DbSkip() )
			EndDo
		EndIf
		ABK->( DbGoTo( nLastRecno ) )	
		
		If nCount > 1
			@ 80, 50 CHECKBOX oCheckBox VAR lEncSeqAnt PROMPT STR0064 SIZE 100, 10 OF oDlg PIXEL // "Encerrar sequЙncias anteriores"
		EndIf
			
	EndIf 
														
	@ 90, 50 MSPANEL oPanel PROMPT "" SIZE 147, 20 OF oDlg CENTERED LOWERED

	@ 02, 02 SAY oSayPan PROMPT aDescri[ nRadio ] SIZE 144,18 PIXEL Of oPanel 

	oRadio:bChange := { || oSayPan:SetText( aDescri[ nRadio ] ) } 
		
	DEFINE SBUTTON oBut1 FROM 45, 169  TYPE 1 ACTION ( nOpca := 1,;
			oDlg:End() ) ENABLE of oDlg

	DEFINE SBUTTON oBut2 FROM 60, 169  TYPE 2 ACTION ( nOpca := 0,;
			oDlg:End() ) ENABLE of oDlg
	
	ACTIVATE MSDIALOG oDlg CENTERED 
Else
	nOpca := 1
EndIf
If nOpca == 1        
    
	If lAberto 
		Do Case 
			Case nRadio == 1 
				At310Agreg() 
			Case nRadio == 2 
				At310Redir() 
			Case nRadio == 3 	
				At310Remin() 
		EndCase 
	Else
		Do Case
			Case nRadio == 1
				At310OS()			
		EndCase				

		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁEncerra as sequencias de atendimento anterior.Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		If (lEncSeqAnt) 			
			DbSelectArea("ABK")
			DbSetOrder(1)
	                
	  		If ABK->( DbSeek( xFilial("ABK") + cNrCham ) )
				Do While	(	!ABK->( EOF() )						.AND.;
								ABK->ABK_FILIAL == xFilial("ABK")	.AND.;
								ABK->ABK_NRCHAM == cNrCham			)
		  			RecLock( "ABK", .F. )
					ABK->ABK_SITUAC := "1"		// SituaГЦo "Encerrado"
					ABK->( MsUnLock() )
					ABK->( DbSkip() )
				EndDo				
			EndIf	
		EndIf
	EndIf 			
		
EndIf 	
	
Return( .T. ) 

/*
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁAT310Bar2 Ё Autor Ё Sergio Silveira       Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Mostra a EnchoiceBar na tela                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static ;
Function AT310Bar2(oDlg,bOk,bCancel,nOpc,oGetD)
Local aBut       := {} 
Local aUsButtons := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Adiciona botoes do usuario na EnchoiceBar2                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "AT310BU2" ) 
	If ValType( aUsButtons := ExecBlock( "AT310BU2", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| AAdd( aBut, x ) } )  
	EndIf 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aBut))

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310Redir Ё Autor Ё Sergio Silveira      Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Acao a ser tomada apos a gravacao da sequencia de          Ё╠╠
╠╠Ё          Ё atendimento                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Redir()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310Redir() 

LOCAL cTecnico := CriaVar( "AA1_CODTEC", .F. )
LOCAL cNomeTec := CriaVar( "AA1_NOMTEC", .F. )

LOCAL nOpca    := 0 
LOCAL oDlg 
LOCAL oGet1   

DEFINE MSDIALOG oDlg TITLE cCadastro FROM 00,0 TO 192,410 OF oMainWnd PIXEL

DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
@   0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 120 NOBORDER WHEN .F. PIXEL

@ 03, 50 SAY STR0024 FONT oBold PIXEL // "Redirecionar atendimento"
@ 14, 50 SAY STR0025            PIXEL // "Retorna o chamado a fila com o tecnico pre-definfido"

@ 24 ,40 TO 26 ,400 LABEL '' OF oDlg   PIXEL

@ 45, 50 SAY STR0026 SIZE 40, 10 PIXEL // "Novo tecnico " 
@ 43, 95 MSGET cTecnico F3 "AA1" VALID At310VlTec( cTecnico, @oGet1, @cNomeTec ) PIXEL // "Tecnico "

@ 58, 50 SAY STR0027 SIZE 40, 10 PIXEL // "Nome "
@ 56, 95 MSGET oGet1 VAR cNomeTec SIZE 70, 10 WHEN .F. PIXEL 

DEFINE SBUTTON oBut1 FROM 55, 169  TYPE 1 ACTION ( nOpca := 1,;
		oDlg:End() ) ENABLE of oDlg

DEFINE SBUTTON oBut2 FROM 75, 169  TYPE 2 ACTION ( nOpca := 0,;
		oDlg:End() ) ENABLE of oDlg

ACTIVATE MSDIALOG oDlg CENTERED 

If nOpca == 1 
	Begin Transaction
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Coloca o novo tecnico para efetuar o atendimento                       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 		
	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
		RecLock( "ABL", .F. ) 
		ABL->ABL_PRXTEC := cTecnico 	
		ABL->ABL_REAVAL := "1" 
		ABL->ABL_REAMOT := "2" // Redirecionamento  
		ABL->( MsUnlock() ) 	
	EndIf 	

	End Transaction 
EndIf 

Return( Nil ) 
                                                               
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310VlTec Ё Autor Ё Sergio Silveira      Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao tecnico - Redirecionamento                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At310VlTec()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310VlTec( cTecnico, oGet1, cNomeTec )
           
LOCAL cCodUsr  := ""
LOCAL lRet     := .T. 
   
AA1->( dbSetOrder( 1 ) ) 
If AA1->( DbSeek( xFilial( "AA1" ) + cTecnico ) ) 
	If AA1->AA1_TIPO $ "23" .And. AA1->AA1_ALOCA == "1" 
		cCodUsr  := RetCodUsr()
		cNomeTec := AA1->AA1_NOMTEC 
		                       
		AA1->( dbSetOrder( 4 ) ) 
		If AA1->( DbSeek( xFilial( "AA1" ) + cCodUsr ) ) 
			If cTecnico <> AA1->AA1_CODTEC 
				lRet := .T. 
			Else                           
				// Nao e permitido redirecionar ao mesmo tecnico 	
				Help( " ",1,"AT310REDIR" ) 	        
				lRet := .F. 
			EndIf 		                                 
		Else 
			lRet := .F. 
		EndIf 	
	Else
		// O tecnico deste usuario nao atende help desk
		// Ou nao esta disponivel para alocacao 
		Help( " ",1,"AT310TPTEC" ) 	        
		lRet := .F. 
	EndIf 	
Else
	Help( " ", 1, "REGNOIS" ) 
	lRet := .F. 
EndIf 
                
If lRet 
	oGet1:Refresh() 
EndIf 
 
Return( lRet ) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310Remin Ё Autor Ё Sergio Silveira      Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Acao a ser tomada apos a gravacao da sequencia de          Ё╠╠
╠╠Ё          Ё atendimento                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Remin()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310Remin()                    

LOCAL cHRemind := Left( Time(), 5 ) 
LOCAL dRemind  := MsDate() 

LOCAL nOpca    := 0 
LOCAL oDlg 

DEFINE MSDIALOG oDlg TITLE cCadastro FROM 00,00 TO 192,410 OF oMainWnd PIXEL

DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
@   0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 130 NOBORDER WHEN .F. PIXEL

@ 03, 50 SAY STR0028 FONT        oBold PIXEL // "Ativar reminder"

@ 13, 50 SAY STR0029 PIXEL // "Mantem o tecnico retornando o chamado a partir da data / hora" 

@ 24, 40 TO 26 ,400 LABEL '' OF oDlg   PIXEL

@ 43, 50 SAY STR0030 SIZE 40, 09 PIXEL // "Avisar-me dia  "
@ 43, 90 MSGET dRemind  VALID .t. PIXEL // "Tecnico "

@ 55, 50 SAY STR0031 SIZE 40, 09 PIXEL // "Apos o horario "
@ 55, 90 MSGET cHRemind VALID AtVldHora( cHRemind ) PICTURE "99:99" PIXEL 

DEFINE SBUTTON oBut1 FROM 55, 169  TYPE 1 ACTION ( nOpca := 1,;
		oDlg:End() ) ENABLE of oDlg

DEFINE SBUTTON oBut2 FROM 75, 169  TYPE 2 ACTION ( nOpca := 0,;
		oDlg:End() ) ENABLE of oDlg

ACTIVATE MSDIALOG oDlg CENTERED VALID At310VlRem( dRemind, cHRemind ) 

If nOpca == 1 
	Begin Transaction
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Cadastra a data e hora do reminder e o tecnico atual                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 		
	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
		RecLock( "ABL", .F. ) 
		
		AA1->( dbSetOrder( 4 ) ) 
		If AA1->( DbSeek( xFilial( "AA1" ) + RetCodUsr() ) ) 
			ABL->ABL_PRXTEC := AA1->AA1_CODTEC 
		EndIf 				
		ABL->ABL_DRET   := dRemind 
		ABL->ABL_HRET   := cHRemind
		ABL->ABL_REAVAL := "1"                      
		ABL->ABL_REAMOT := "3" // Reminder 
		
		ABL->( MsUnlock() ) 	
	EndIf 	

	End Transaction 
EndIf 

Return( Nil ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310VlRem Ё Autor Ё Sergio Silveira      Ё Data Ё17/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da dialog do reminder                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 : At310VlRem()                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310VlRem( dRemind, cHRemind ) 

LOCAL cDtTimeRem := ""
LOCAL cDtTimeAtu := ""                  

LOCAL lRet := .F. 

cDtTimeRem := DToS( dRemind  ) + StrTran( cHRemind, ":" ) 
cDtTimeAtu := DToS( MsDate() ) + Left( StrTran( Time(), ":" ), 5 )   

If cDtTimeRem > cDtTimeAtu
	lRet := .T. 
Else
	Help( " ", 1, "AT310REMIN" ) // A data/hora do reminder deve ser superior a atual 
	lRet := .F. 
EndIf 

Return( lRet )


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310HdReq Ё Autor Ё Sergio Silveira      Ё Data Ё17/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Apura as habilidades necessarias para atender um item      Ё╠╠ 
╠╠Ё          Ё de Help Desk, inclusive as habilidades adicionais          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310HdReq( @ExpA1 )                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpA1 -> Array de Habilidades - Passado por referencia     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310HdReq( aReqHabil, nTipo )
        
LOCAL aArea      := GetArea() 
LOCAL cGrpAte    := Space( Len( AAP->AAP_GRPATE ) ) 
LOCAL lTipo      := ( ValType( nTipo ) == "N" ) 

If !lTipo .Or. ( lTipo .And. nTipo == 1 )  

	AB2->( dbSetOrder( 1 ) )        
	If AB2->( DbSeek( xFilial( "AB2" ) + ABL->ABL_NRCHAM ) )
		
		AB1->( dbSetOrder( 1 ) ) 
		If AB1->( DbSeek( xFilial( "AB1" ) + AB2->AB2_NRCHAM ) ) 
		
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica se este equipamento possui um grupo de atendimento especifico  Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			AA3->( dbSetOrder( 1 ) ) 	
			If AA3->( DbSeek( xFilial( "AA3" ) + AB1->AB1_CODCLI + AB1->AB1_LOJA + AB2->AB2_CODPRO + AB2->AB2_NUMSER ) ) 
				If !Empty( AA3->AA3_CONTRT ) 
					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Obtem o grupo de atendimento associado a este contrato                  Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					cGrpAte := AtRetGrpAt( AA3->AA3_CONTRT, .T. )
				EndIf 
			EndIf
		EndIf
		
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Pesquisa no AA7 utilizando a ordem de prioridade                        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		AA7->( dbSetOrder( 1 ) ) 
		If AA7->( DbSeek( xFilial( "AA7" ) + AB2->AB2_CODPRO + AB2->AB2_CODPRB + cGrpAte ) ) 
			AtHabilReq( @aReqHabil )
		Else	
			If AA7->( DbSeek( xFilial( "AA7" ) + AB2->AB2_CODPRO+;
		    		Space( Len( AA7->AA7_CODPRB ) ) + cGrpAte ) )
				AtHabilReq( @aReqHabil )
			Else             
				If AA7->( DbSeek( xFilial( "AA7" ) + ;
					Space( Len( AA7->AA7_CODPRO ) ) + AB2->AB2_CODPRB + cGrpAte ) )
					AtHabilReq( @aReqHabil )
				EndIf 
				
			EndIf 	
			
		EndIf 				
		
	EndIf

EndIf 
	                                         
If !lTipo .Or. ( lTipo .And. nTipo == 2 )  	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Apura as habilidades adicionais deste item de Help Desk                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				
	#IFDEF TOP
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Processamento para SQL                                  Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If TcSrvType() <> "AS/400"
			cQuery := "" 
			cQuery += "SELECT * FROM " + RetSqlName( "ABM" ) + " "
			cQuery += "WHERE ABM_FILIAL='" + xFilial( "ABM" ) + "' AND "
			cQuery += "ABM_NRCHAM='" + ABL->ABL_NRCHAM + "' AND "
			cQuery += "D_E_L_E_T_=' '" 					   
	
			cQuery := ChangeQuery( cQuery ) 
			
			dbUseArea( .T., "TOPCONN", TcGenQry(,,cQuery), "TRBABM", .F., .T. ) 
			
			TcSetField( "TRBABM", "ABM_NIVEL", "N", 3, 0 ) 
			
			If Alias() == "TRBABM" 
				While !TRBABM->( Eof() ) 
					If Empty( nScan := AScan( aReqHabil, { |x| x[1] == TRBABM->ABM_HABIL } ) )
						AAdd( aReqHabil, { TRBABM->ABM_HABIL, TRBABM->ABM_NIVEL } )
					Else
						If TRBABM->ABM_NIVEL > aReqHabil[ nScan , 2 ]
							aReqHabil[ nScan, 2 ] := TRBABM->ABM_NIVEL
						EndIf  
				    EndIf
		        	TRBABM->( dbSkip() )        
				End     
				TRBABM->( dbCloseArea() ) 				
				dbSelectArea( "ABM" ) 
			EndIf 									 
		
		Else 
	#ENDIF 	
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Processamento para ISAM                                 Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			ABM->( dbSetOrder( 1 ) ) 
			cSeekABM := xFilial( "ABM" ) + ABL->ABL_NRCHAM 
			If ABM->( DbSeek( cSeekABM ) ) 
				While !ABM->( Eof() ) .And. cSeekABM == ;
						ABM->ABM_FILIAL + ABM->ABM_NRCHAM 
					If Empty( nScan := AScan( aReqHabil, { |x| x[1] == ABM->ABM_HABIL } ) ) 												
						AAdd( aReqHabil, { ABM->ABM_HABIL, ABM->ABM_NIVEL } ) 		
					Else
						If ABM->ABM_NIVEL > aReqHabil[ nScan , 2 ] 						     
							aReqHabil[ nScan, 2 ] := ABM->ABM_NIVEL 																													
						EndIf  
				    EndIf
		        	ABM->( dbSkip() )        
				End 
		    EndIf   
	#IFDEF TOP 
		EndIf 

	#ENDIF 

EndIf                   

RestArea( aArea ) 
	
Return( Nil ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310Agreg Ё Autor Ё Sergio Silveira      Ё Data Ё16/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Agrega novos conhecimentos necessarios ao atendimento      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Agreg()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310Agreg()

LOCAL aColsAnt  := {}                     

LOCAL cHRemind  := Left( Time(), 5 ) 
LOCAL cSeekABM  := ""

LOCAL dRemind   := MsDate()          

LOCAL nPosNivel := 0 
LOCAL nPosHabil := 0 
LOCAL nLoop     := 0 
LOCAL nOpca     := 0
LOCAL nUsado    := 0          
LOCAL nScan     := 0 
Local nCntFor   := 0

LOCAL oDlg 
LOCAL oGetD 

Local aStruField := {}
Local cField     := ""
Local nC         := 0

PRIVATE aHeader := {}
PRIVATE aCols   := {}  

// Monta o aheader 

aHeader := {}

aStruField := FWSX3Util():GetAllFields("ABM")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"ABM",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

dbSelectArea( "ABM" ) 
                       
// Monta o aCols

nUsado := Len( aHeader ) 

aReqHabil := {} 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Apura as Habilidades adicionais do help desk                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
  
ABL->( dbSetOrder( 2 ) ) 
ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
At310HdReq( @aReqHabil, 2 ) 
aReqHabAdic := aClone( aReqHabil )                               

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Apura as Habilidades inerentes a ocorrencia                            Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
At310HdReq( @aReqHabil, 1 )
    
nPosHabil  := GDFieldPos( "ABM_HABIL"  ) 
nPosNivel  := GDFieldPos( "ABM_NIVEL"  )
nPosDescri := GDFieldPos( "ABM_DESCRI" )  

For nLoop := 1 To Len( aReqHabil ) 
	AAdd(aCols,Array(nUsado+1))                               		
	For nCntFor := 1 To nUsado
		If nCntFor == nPosHabil 
			aCols[Len(aCols),nCntFor] := aReqHabil[ nLoop, 1 ] 						
		ElseIf nCntFor == nPosNivel 
			aCols[Len(aCols),nCntFor] := aReqHabil[ nLoop, 2 ] 						
		ElseIf nCntFor == nPosDescri 
			aCols[Len(aCols),nCntFor] := FWGetSX5("A4",aReqHabil[nLoop,1])[1,4]
		Else 
			aCols[Len(aCols),nCntFor] := CriaVar(aHeader[nCntFor][2])							   
		EndIf		
	Next nCntFor                             
	aCols[Len(aCols),Len(aHeader)+1] := .F.
Next nLoop

If Empty( aCols ) 
	AAdd(aCols,Array(nUsado+1))                               
	For nCntFor := 1 To nUsado
		aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])							   
	Next nCntFor                             
	aCols[Len(aCols),Len(aHeader)+1] := .F.
EndIf 
     
aColsAnt := aClone( aCols ) 

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ordena o aCols por Habilidade                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
ASort( aCols, , , { |x,y| y[ nPosHabil ] > x[ nPosHabil ] } ) 

If !IsBlind()
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 00,00 TO 220,400 OF oMainWnd PIXEL 

	DEFINE FONT oBold NAME "Arial" SIZE 0, -13 BOLD
																		
	@ 03, 10 SAY STR0014 FONT        oBold PIXEL // "Agregar conhecimento" 
	@ 13, 10 SAY STR0032 PIXEL // "Permite adicionar habilidades ou elevar niveis e retornar o chamado a fila"

	@ 24, 10 TO 26 ,400 LABEL '' OF oDlg   PIXEL

	oGetD := MsGetDados():New(33,10,102,153,3,"At310G2LOK","At310G2TOK",,.T.)

	oDlg:Cargo := AClone( aColsAnt ) 

	DEFINE SBUTTON oBut1 FROM 65, 162  TYPE 1 ACTION ( nOpca := 1, oDlg:End() ) ENABLE of oDlg
	DEFINE SBUTTON oBut2 FROM 85, 162  TYPE 2 ACTION ( nOpca := 0, oDlg:End() ) ENABLE of oDlg

	ACTIVATE MSDIALOG oDlg VALID oGetD:TudoOk() CENTERED 
Else
	nOpca := 1 
EndIf
If nOpca == 1 
	Begin Transaction
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Agrega os novos itens de conhecimento ao atendimento e sujeita a       Ё
	//Ё reavaliacao                                                            Ё	
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 		
	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
		RecLock( "ABL", .F. ) 
		ABL->ABL_REAVAL := "1" 
		ABL->ABL_REAMOT := "4" // Agrega conhecimento   
		ABL->( MsUnlock() ) 	
	EndIf 	                 
	      
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Exclui os registros anteriores                                         Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABM->( dbSetOrder( 1 ) ) 
	cSeekABM := xFilial( "ABM" ) + M->ABK_NRCHAM 
	If ABM->( DbSeek( cSeekABM ) ) 
		ABM->( dbEval( { || RecLock("ABM",.F.,.T.),dbDelete(),MsUnlock() },;//Processo 
				, { || cSeekABM == ABM_FILIAL + ABM_NRCHAM },,,.T. ) ) // Condicao While 
	EndIf 	
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Adiciona os novos conhecimentos ( compara com o Array inicial )        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nLoop := 1 To Len( aCols ) 
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Apenas itens nao excluidos                                             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If !aCols[ nLoop, Len( aHeader ) + 1 ]	
		    lAdd := .F. 
			cNewHabil := aCols[ nLoop, nPosHabil ]  	
			nScan     := AScan( aColsAnt, { |x| x[ nPosHabil ] == cNewHabil } ) 
			If Empty( nScan ) 
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё A habilidade nao existia                                               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				lAdd := .T. 
			Else 	
				If aCols[ nLoop, nPosNivel ] <> aColsAnt[ nScan, nPosNivel ]     
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё O nivel foi alterado                                                   Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					lAdd := .T.          
				Else 	 
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Habilidade e nivel nao foram alterados desta vez mas devem ser mantidasЁ
					//Ё pois foram adicionadas em outro momento                                Ё				
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If !Empty( AScan( aReqHabAdic, { |x| x[ 1 ] == cNewHabil } ) ) 
						lAdd := .T. 				
					EndIf 								
				EndIf 
			EndIf 		
			If lAdd 
				RecLock( "ABM", .T. ) 
				ABM->ABM_FILIAL := xFilial( "ABM" ) 
				ABM->ABM_NRCHAM := M->ABK_NRCHAM
				ABM->ABM_HABIL  := aCols[ nLoop, nPosHabil ] 				
				ABM->ABM_NIVEL  := aCols[ nLoop, nPosNivel ] 						
				ABM->( MsUnLock() ) 	
			EndIf 	
		EndIf 			
	Next nLoop 
	End Transaction 
EndIf 

Return( Nil ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310G2TOK Ё Autor Ё Sergio Silveira      Ё Data Ё22/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё TudoOk da segunda GetDados                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310G2TOK()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
    
Function At310G2TOK() 

Local aColsAnt   := {} 
Local lRet       := .T.    
Local nScan      := 0 
Local nPosHabil  := GDFieldPos( "ABM_HABIL"  ) 
Local nPosNivel  := GDFieldPos( "ABM_NIVEL"  ) 
Local nLoop      := 0 
Local oDlgAgreg  := GetWndDefault() 

If lRet

	aColsAnt := aClone( oDlgAgreg:Cargo )  

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Faz verificacoes de habilidade / nivel apenas se o aColsAnt estiver preenchido  Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty( AScan( aColsAnt, { |x| Empty( x[ nPosHabil ] ) } ) )  

		For nLoop := 1 To Len( aColsAnt ) 
		
			If !Empty( nScan := AScan( aCols,{ |x| x[nPosHabil] == aColsAnt[ nLoop, nPosHabil ] } ) )  
			
				If !aCols[ nLoop, Len( aHeader ) + 1 ]  
					If aCols[ nScan, nPosNivel ] < aColsAnt[ nLoop, nPosNivel ] 	  	
						Help( " ", 1, "AT310NIVEL" ) // Nao e possivel diminuir o nivel 
						lRet := .F. 
					EndIf 	
				Else
					Help( " ", 1, "AT310EXCL" ) // Nao e possivel excluir habilidades ja incluidas
					lRet := .F. 
				EndIf 			
			Else 
				Help( " ", 1, "AT310EXCL" ) // Nao e possivel excluir habilidades ja incluidas
				lRet := .F. 
			EndIf
			
			If !lRet 
		 		Exit 	
			EndIf 
			
		Next nLoop 
		
	EndIf 	
	
EndIf
	
Return( lRet ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310G2LOK Ё Autor Ё Sergio Silveira      Ё Data Ё22/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё LinOk da segunda GetDados                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310G2LOK()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
    
Function At310G2LOK() 

LOCAL lRet        := .T. 
LOCAL nPosHabil   := GDFieldPos( "ABM_HABIL" )   
LOCAL nLoop       := 0 

If !aCols[ n, Len( aHeader ) + 1 ]  

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica o preenchimento dos campos                                    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If Empty( GDFieldGet( "ABM_HABIL" ) ) .Or. Empty( GDFieldGet( "ABM_NIVEL" ) ) 		  
		Help( " ", 1, "AT310VAZIO" ) 
		lRet := .F.
	EndIf 	
    
	If lRet 
		For nLoop := 1 To Len( aCols ) 
			If nLoop <> n .And. !aCols[ nLoop, Len( aHeader ) + 1 ] 
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Verifica a existencia de itens duplicados                              Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If aCols[ nLoop, nPosHabil ] == aCols[ n, nPosHabil ] 		
					Help( " ", 1, "AT310HABDP" ) // Existem habilidades duplicadas 	
					lRet := .F. 
					Exit							
				EndIf 	
			EndIf 	                
		Next nLoop                 
	EndIf 		
EndIf
	
Return( lRet ) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310G2DOK Ё Autor Ё Sergio Silveira      Ё Data Ё24/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё DelOk da segunda GetDados                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310G2LOK()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
    
Function At310G2DOK() 
                      
LOCAL lRet := .T. 

Return( lRet ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At310OS   Ё Autor Ё Sergio Silveira      Ё Data Ё24/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Efetua a geracao da Ordem de Servico a partir do Help Desk Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310OS()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Gravou OS                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
    
Function At310OS() 

LOCAL bCampo 	   := { |nCpo| FieldName( nCpo ) }                             
LOCAL cItem        := "00"                      
                      
LOCAL lRet         := .F.

Local nSaveSX8	   := GetSX8Len()
Local nLoop        := 0

Private aCols      := {}
Private aHeader    := {}
Private aColsAB7   := {}
Private aColsAB8   := {}
Private aHeaderAB7 := {}
Private aHeaderAB8 := {}

//здддддддддддддддддддддддддддддддд©
//Ё Monta o aHeader AB7 e AB8      Ё
//юдддддддддддддддддддддддддддддддды
At450Monta()        

//зддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa pelo item do chamado tecnico Ё
//юддддддддддддддддддддддддддддддддддддддды
AB2->( dbSetOrder( 1 ) ) 
If AB2->( DbSeek( xFilial( "AB2" ) + M->ABK_NRCHAM ) )              

	//здддддддддддддддддддддддддддд©
	//Ё Pesquisa pelo item da fila Ё
	//юдддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 
	If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) )              
	
		//зддддддддддддддддддддддддддддддддддддддд©
		//Ё Monta o aColsLine e adiciona ao aCols Ё
		//юддддддддддддддддддддддддддддддддддддддды
		aColsLine := Array( Len( aHeaderAB7 ) + 1 )
			
		aEval( aHeaderAB7, { |x,y| If(!IsHeadRec(x[2]) .AND. !IsHeadAlias(x[2]), aColsLine[ y ] := CriaVar( x[ 2 ] ),) } )       
		
		cItem := SomaIt( cItem ) 
			
		aColsLine[ GDFieldPos( "AB7_CODPRO", aHeaderAB7 ) ] := AB2->AB2_CODPRO 
		aColsLine[ GDFieldPos( "AB7_NUMSER", aHeaderAB7 ) ] := AB2->AB2_NUMSER 
		aColsLine[ GDFieldPos( "AB7_ITEM"  , aHeaderAB7 ) ] := cItem
		aColsLine[ GDFieldPos( "AB7_CODPRB", aHeaderAB7 ) ] := AB2->AB2_CODPRB 
		aColsLine[ GDFieldPos( "AB7_TIPO"  , aHeaderAB7 ) ] := "1"
		aColsLine[ Len( aHeaderAB7 ) + 1 ] := .F.
			
		AAdd( aColsAB7, AClone( aColsLine ) )
		
		aCols   := AClone( aColsAB7 ) 
		aHeader := aClone( aHeaderAB7 )
		
		If !Empty( aCols )               
		
			SA1->( dbSetOrder( 1 ) ) 
			
			If SA1->( DbSeek( xFilial( "SA1" ) + M->ABK_CODCLI + M->ABK_LOJA ) ) 		
				
				dbSelectArea("AB6")
				For nLoop := 1 To FCount()
					M->&( Eval( bCampo, nLoop ) ) := CriaVar( FieldName( nLoop ) )
				Next nLoop
					
				M->AB6_CODCLI := ABK->ABK_CODCLI
				M->AB6_LOJA   := ABK->ABK_LOJA
				M->AB6_CONPAG := SA1->A1_COND
				//здддддддддддддддддддддддддддддддд©
				//Ё Chama a funcao de gravacao     Ё
				//юдддддддддддддддддддддддддддддддды
			
				Begin Transaction
					
					lRet := At450Grava( 1, , , { ABL->ABL_NRCHAM } )
					//здддддддддддддддддддддддддддддддд©
					//Ё Confirma o SX8                 Ё
					//юдддддддддддддддддддддддддддддддды
					EvalTrigger()
					While ( GetSX8Len() > nSaveSx8 )
						ConfirmSX8()
					End
					//здддддддддддддддддддддддддддддддддддддддддд©
					//Ё Grava a situacao do item da fila         Ё
					//юдддддддддддддддддддддддддддддддддддддддддды
					RecLock( "ABL", .F. )
					ABL->ABL_SITUAC := "4" // O.S. 
					ABL->ABL_REAVAL := "2" // Nao reavalia 
					
					ABL->( MsUnlock() )	
					
				End Transaction
				
			EndIf 		
		EndIf                         
	EndIf 		
EndIf 	
	
Return( lRet ) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At310Seq  Ё Autor Ё Sergio Silveira      Ё Data Ё25/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Devolve a sequencia de atendimento (inicializador padrao)  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Seq()                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Sequencia de atendimento                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310Seq() 

LOCAL cSeq := "  "
                      
If !Empty( M->ABK_NRCHAM ) 
	//здддддддддддддддддддддддддддддддд©
	//Ё Se localizou o item da fila    Ё
	//юдддддддддддддддддддддддддддддддды
	ABL->( dbSetOrder( 2 ) ) 
	If ABL->( DbSeek( xFilial( "ABK" ) + M->ABK_NRCHAM ) ) 
		cSeq := ABL->ABL_PRXSEQ 
	Else       
		cSeq := "00"
		cSeq := SomaIt( cSeq ) 	
	EndIf 	
EndIf

Return( cSeq ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At310Orig Ё Autor Ё Sergio Silveira      Ё Data Ё25/08/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Devolve o motivo de reavaliacao ( origem ) da sequencia    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Orig()                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Origem                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310Orig() 

Local aArea    := GetArea()
Local aAreaABL := ABL->( GetArea() ) 

Local cOrigem := CriaVar( "ABK_ORIGEM", .F. ) 

ABL->( dbSetOrder( 2 ) ) 
If ABL->( DbSeek( xFilial( "ABL" ) + M->ABK_NRCHAM ) ) 
	cOrigem := ABL->ABL_REAMOT 
EndIf                                      

RestArea( aAreaABL ) 
RestArea( aArea ) 

Return( cOrigem ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At310LaudoЁ Autor Ё Sergio Silveira      Ё Data Ё15/09/2000Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica se o laudo foi digitado                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310Laudo()                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA310                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
                           
Function At310Laudo()

LOCAL cMemo := M->ABK_MEMO 
LOCAL lRet  := .F.   

lRet := !Empty( cMemo ) 
                  
If !lRet 
	Help( " ", 1, "OBRIGAT" ) 
EndIf 

Return( lRet )  



/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310TrackЁ Autor Ё Sergio Silveira       Ё Data Ё01/02/2002Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz o tratamento da chamada do System Tracker              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At310Track() 

Local aEnt     := {}
Local cChamado := M->ABK_NRCHAM                                     
Local cSeq     := M->ABK_SEQ

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega os itens para rastreamento          Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
AAdd( aEnt, { "ABK", cChamado + cSeq } ) 

MaTrkShow( aEnt ) 
           
Return( .T. ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310DiscarЁ Autor Ё Kleber Dias Gomes    Ё Data Ё09/08/2002Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁExibe o telefone virtual e executa a discagem               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Numero do Telefone                                Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At310Discar()

dbSelectArea("AA1")
dbSetOrder(1)
DbSeek(xFilial("AA1")+M->ABK_CODTEC)

AtDiscar(AllTrim(AA1->AA1_ACESSO)+" "+M->AB1_TEL)

Return(.T.)                            

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ListenЁ Autor Ё Sergio Silveira      Ё Data Ё16/01/2002Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁChamada do listener do Help Desk                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Alias                                             Ё╠╠
╠╠Ё          Ё ExpN1 -> Registro                                          Ё╠╠
╠╠Ё          Ё ExpN2 -> Opcao                                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> .T.                                               Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310Listen(cAlias,nReg,nOpcx) 
If !IsBlind()
	Processa({|lEnd| At310PrcLis(cAlias,nReg,nOpcx,@lEnd)},STR0038,STR0043,.T. ) //"Listener"###"Aguardando chamados..."
Else
	At310PrcLis(cAlias,nReg,nOpcx,.F.)
EndIf
Return( .T. ) 


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310PrcLisЁ Autor Ё Sergio Silveira      Ё Data Ё16/01/2002Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁProcessamento do listener do Help Desk                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Alias                                             Ё╠╠
╠╠Ё          Ё ExpN1 -> Registro                                          Ё╠╠
╠╠Ё          Ё ExpN2 -> Opcao                                             Ё╠╠
╠╠Ё          Ё ExpL1 -> Indicador de saida                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> .T.                                               Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310PrcLis(cAlias,nReg,nOpcx,lEnd) 

LOCAL cTempo    := SuperGetMV( "MV_ATLITMP" )  
LOCAL cOcioso   := ""

LOCAL dRefer    := MsDate() 

LOCAL lOcioso   := .F.
LOCAL lAbandona := .F. 
                          
LOCAL nPosBarra := 0                    
LOCAL nAtivo    := 1000  
LOCAL nOcioso   := 5000 
LOCAL nInterval := 0 
LOCAL nTempOci  := Val( SuperGetMV( "MV_ATOCIOS" ) )
LOCAL nTimes    := 0 
LOCAL nIntLoop  := 0 
LOCAL nLoop     := 0 

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Extrai os tempos de intervalo               Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
nPosBarra := At(  "/", cTempo ) 

nAtivo    := Val( Left( cTempo, nPosBarra - 1 ) ) * 1000
cOcioso   := Substr( cTempo, nPosBarra + 1 )

If cOcioso == "E" 
	lAbandona := .T. 
Else	
	nOcioso   := Val( cOcioso ) * 1000
EndIf                             

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica o tempo minimo de intervalo        Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
If nAtivo < 1000 .Or. nOcioso < 1000 
	nAtivo  := 1000
	nOcioso := 5000 
EndIf 	

lRefreshListen  := .F. 

dRefer    := MsDate()
nSeconds  := Seconds() 
nInterval := nAtivo 

While !lEnd

	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se existem chamados na fila        Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	At310Inclu(cAlias,nReg,nOpcx,NIL,.T.,@lEnd)
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se atendeu, reinicia os parametros quanto a ociosidade Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lRefreshListen 
		dRefer         := MsDate()
		nSeconds       := Seconds() 
		nInterval      := nAtivo 
		lRefreshListen := .F.
		lOcioso        := .F. 
	EndIf 					
	
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o listener esta ocioso          Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	If !lOcioso .And. ( ( Seconds() + ( MsDate() - dRefer ) * 86400 ) - nSeconds ) > nTempOci 
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se configurado, abandona o listener         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		If lAbandona 
			Exit
		EndIf          
		nInterval := nOcioso 
		lOcioso   := .T.
	EndIf 	 
                         
	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Calcula o numero de intervalos para refresh Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	nTimes   := Int( nInterval / 2000 ) 
	
	If Empty( nTimes ) 
		nTimes := 1 
	EndIf	
	
	nIntLoop := nInterval / nTimes 
	
	For nLoop := 1 to nTimes
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Da o intervalo                              Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		Sleep( nIntLoop )	
		//зддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Atualiza comunicacao Server x Remote        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддды
		ProcessMessages()                               

		If lEnd 
			Exit
		EndIf	                                 
		
	Next nLoop 		

End 

Return( .T. ) 
   
/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ChamTecЁ Autor Ё Sergio Silveira     Ё Data Ё21/01/2003Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Funcao para inclusao de um novo chamado tecnico            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At310ChamTec()                                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nil                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё28/12/2006Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310ChamTec(oGetD,oGetDAlte,oEnchVisu,oEnchAlte,oFolder,nOpc)
                  
Local cConteudo  := ""
Local aStruField := {}
Local cField     := ""
Local nC         := 0

Default nOpc := 2

If oFolder:nOption == 2 

	If !lGravaCham 

		oButConf:Enable()
		oButCanc:Enable()	
	
		lGravaCham := .T.
		lLeFila    := .T.

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Habilita a enchoice / getdados de gravacao e desabilita as de visualizacao Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		oGetD:Hide()
		oEnchVisu:Hide()
		
		oGetDAlte:Show()
		oEnchAlte:Show()             
		
		oEnchAlte:SetFocus() 
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Cria Variaveis de Memoria da Enchoice de inclusao do chamado tecnico Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		aStruField := FWSX3Util():GetAllFields("AB1")
		If Len(aStruField) > 0
			For nC := 1 to Len(aStruField )
				cField := aStruField[nC]
				cConteudo := CriaVar(cField)

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Faz a declaracao da variavel como private (nao remover)  Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				SetPrvt( Trim(cField) )
				M->&(cField) := cConteudo 

			Next nC
		EndIf

		At310Monta( nOpc, .T. )
	
		INCLUI := .t. 

	Else
		Help( " ", 1, "AT310NOCHA") // "Ja existe um novo chamado em andamento!"
	EndIf 
		
Else
	Help( " ", 1, "AT310VACHA") // "OpГЦo vАlida apenas para a pasta 'Chamado tИcnico'."
EndIf 	 	
	
Return() 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддбдддддддбддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ChFilaЁ Autor Ё Sergio Silveira      Ё Data Ё17/01/2003Ё╠╠
╠╠цддддддддддедддддддддддадддддддаддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁPergunta qual o modo de transferencia do chamado tecnico    Ё╠╠
╠╠Ё          Ёpara o Help Desk                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpN1 := At310ChFila()                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpN1 -> 1 - Envia para a fila / 2 - Atende imediatamente  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310ChFila() 

Local aDescri  := {}
                  
Local nRadio   := 1
Local nOpca    := 0  

Local oDlg 
Local oRadio 
Local oBut1 
Local oBold 
Local oPanel                       
Local oSayPan

aDescri := { STR0047,; //"Envia o chamado para a fila de Help Desk"
 STR0048 }  //"Disponibiliza o chamado para atendimento para este tecnico com maxima prioridade"

DEFINE MSDIALOG oDlg TITLE cCadastro FROM 00,0 TO 236,410 OF oMainWnd PIXEL 

DEFINE FONT oBold NAME "Arial" SIZE 0, -11 BOLD
@   0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlg SIZE 40, 130 NOBORDER WHEN .F. PIXEL

@ 03, 50 SAY STR0049 PIXEL  //"Voce selecionou gravar este chamado tecnico"
@ 12, 50 SAY STR0050 PIXEL //"Selecione o modo de transferencia ao Help Desk"

@ 21 ,40 TO 23 ,400 LABEL '' OF oDlg PIXEL

@ 48, 50 RADIO oRadio VAR nRadio 3D SIZE 80, 11 PROMPT STR0051, STR0052 ; //"Enviar para a fila"###"Priorizar este atendimento"
	of oDlg PIXEL 
nRadio := 2 				
                                                 
@ 90, 50 MSPANEL oPanel PROMPT "" SIZE 147, 20 OF oDlg CENTERED LOWERED

@ 02, 02 SAY oSayPan PROMPT aDescri[ nRadio ] SIZE 144,18 PIXEL Of oPanel 

oRadio:bChange := { || oSayPan:SetText( aDescri[ nRadio ] ) } 
	
DEFINE SBUTTON oBut1 FROM 45, 169  TYPE 1 ACTION ( nOpca := 1,;
		oDlg:End() ) ENABLE of oDlg

ACTIVATE MSDIALOG oDlg CENTERED 

Return( nRadio ) 

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310ActChamЁ Autor Ё Sergio Silveira     Ё Data Ё17/01/2003Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁFuncao de gravacao ou cancelamento do chamado tecnico no    Ё╠╠
╠╠Ё          ЁHelp Desk                                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At310ActCham(ExpN1)                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 -> Opcao : 1 - Grava chamado / 2 - Processa chamado  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpN1 -> 1 - Envia para a fila / 2 - Atende imediatamente  Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё09/08/05  ЁHenry Fila     ЁBops-84131: Implementacao da validacao      Ё╠╠
╠╠Ё          Ё               Ёde campos obrigatorios na tabela AB1        Ё╠╠
╠╠Ё01/09/06  ЁCleber M.      ЁBops-104132:Acerto da variavel nSaveSX8 paraЁ╠╠
╠╠Ё          Ё               Ёexecutar a confirmacao dos nrs. reservados. Ё╠╠
╠╠Ё28/12/06  Ё Conrado Q.    ЁBops 115747: Montagem do aCols e aHeader    Ё╠╠
╠╠Ё          Ё               ЁatravИs da rotina FillGetDados.             Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310ActCham(nOpcao,oGetD,oGetDAlte,oEnchVisu,oEnchAlte,nOpcx)

Local aRet   := {} 
Local nUsado := Len( aHeader )  
Local lLimpa := .F. 

Local nSaveSX8	:= GetSX8Len()
Local nCntFor   := 0

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Default nOpcx := 2

If nOpcao == 1  
	If Aviso( STR0040, STR0053, { STR0054, STR0055 } ) == 1 //"Atencao !"###"Efetua a gravacao deste chamado tecnico ?"###"Sim"###"Nao"
	
		If ( lRet := ( At310GdTok() .And. At300LinOk() .And. At300TudOk() .And. Obrigatorio(aGets,aTela) ) ) 
		
			nFila := At310ChFila() 
						 
			Begin Transaction
				aRet := At300Grava(1,nFila)  
				If ( aRet[1] )
					EvalTrigger()
					//зддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Confirma os numeros sugeridos pelo semaforo Ё
					//юддддддддддддддддддддддддддддддддддддддддддддды
					nSaveSx8 := 0
					While ( GetSX8Len() > nSaveSx8 )
						ConfirmSx8()
					End   
				
					lRetorno := .T.
				Else			
					dbSelectArea("AB1")
					While ( GetSX8Len() > nSaveSx8 )
						RollBackSx8()
					End
				EndIf
			End Transaction

			lGravaCham := .F. 
			
			lLimpa := .t. 
			
		EndIf 		
	Else	                  
		lGravaCham := .F. 		
		dbSelectArea("AB1")
		While ( GetSX8Len() > nSaveSx8 )
			RollBackSx8()
		End
	EndIf
ElseIf nOpcao == 2
	If Aviso( STR0040, STR0056, { STR0054, STR0055 } ) == 1 //"Atencao !"###"Efetua o cancelamento deste chamado tecnico ?"###"Sim"###"Nao"
		lGravaCham := .F.
		dbSelectArea("AB1")
		While (( GetSX8Len() > nSaveSx8 ) .Or. (GetSX8Len() == 1 .And. nSaveSx8 == 1))
			RollBackSx8()
		End		
		lLimpa := .T. 
		
 	EndIf  
EndIf  

If lLimpa 

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Desabilita os botoes de confirmacao / Cancelamento       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды   
	oButConf:Disable()
	oButCanc:Disable() 
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Limpa as variaveis da enchoice                           Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды   
	aStruField := FWSX3Util():GetAllFields("AB1")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			M->&(cField) := CriaVar(cField,.F.)
		Next nC
	EndIf
            
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Limpa o aCols                                                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	At310Monta( nOpcx )
    
	oEnchVisu:Show()
	oGetD:Show() 
	
	oEnchAlte:Hide()   
	oGetDAlte:Hide()

EndIf 

Return( .T. )    

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt310GdTok  Ё Autor Ё Sergio Silveira     Ё Data Ё21/01/2003Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o ЁValidacao da getdados do chamado tecnico                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At310GdTok                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддбдддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё   DATA   Ё Programador   ЁManutencao Efetuada                         Ё╠╠
╠╠цддддддддддедддддддддддддддедддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё          Ё               Ё                                            Ё╠╠
╠╠юддддддддддадддддддддддддддадддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At310GdTok() 

LOCAL lRet     := .T.
LOCAL nPosTipo := GDFieldPos( "AB2_TIPO" ) 
LOCAL nUsado   := Len( aHeader ) 
LOCAL nLoop    := 0 

For nLoop := 1 To Len( aCols ) 

	If !aCols[ nLoop, nUsado + 1 ] 
		If aCols[ nLoop, nPosTipo ] <> "6" 
		    Help( " ", 1, "AT310DIHEL") // NЦo sЦo permitidos itens com situaГЦo diferente de 'Help Desk'!
			lRet := .F. 
			Exit
		EndIf 
	EndIf		
	
Next nLoop 

Return( lRet )          

