#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "protheus.ch"
#include "totvs.ch"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util

//------------------------------------------------------------------------------
/* {Protheus.doc} NcmGroupsData
    Classe responsável pela consulta de dados de grupos de NCM

    @type Class
    @author Squad PIF
    @version 1.0
*/
//------------------------------------------------------------------------------
Class NcmGroupsData From FWAdapterBaseV2

	Private Data cAliasNcmTmp                        As Character
	Private Data oTempTableNcm                       As Object
	Private Data oBulkTempTableNcm                   As Object
	Private Data lCanUseBulkTempTableNcm             As Logical

	Public Method new() Constructor
	Public Method getNcmGroups()                     As Json
	Public Method formatNcmGroupsResponse()          As Json
	Public Method formatProductsByNcmGroupResponse() As Json
	Public Method getProductsByNcmGroup()            As Json

	Protected Method addDataTabTemp()
	Protected Method getDescriptionByNcm()           As Character

	Private Method createNcmTemp()                   As Logical
	Private Method populateNcmTemp()                 As Logical

EndClass

//------------------------------------------------------------------------------
/*{Protheus.doc} New
    Metodo construtor

    @type Method
    @author Squad PIF
    @version 1.0
    @param cMethod, Character, tipo de verbo HTTP
    @param lList, Logical, indica o retorno de lista de valores
    @return Object, retorna o objeto criado
*/
//------------------------------------------------------------------------------
Method new(cMethod as Character, lList as Logical) Class NcmGroupsData

	Default cMethod := "GET"
	Default lList   := .T.

	::cAliasNcmTmp  := ""

	_Super:New(cMethod, lList)

Return Self


//------------------------------------------------------------------------------
/*{Protheus.doc} getNcmGroups
    Metodo responsável por obter a lista de grupos de NCM dos produtos com movimentação nos últimos 12 meses

    @type Method
    @author Squad PIF
    @version 1.0
    @param nPage, numeric, numero da pagina
	@param nPageSize, numeric, Quantidade de registros por pagina
	@param cFilter, Character, Filtro a ser aplicado na consulta
    @return Json, retorna o objeto json contendo os dados dos grupos de NCM
*/
//------------------------------------------------------------------------------
Method getNcmGroups(nPage as Numeric, nPageSize as Numeric, cFilter as Character) As Json class NcmGroupsData
	Local cWhere     As Character
	Local aArea      As Array
	Local aUrlFilter As Array

	Default nPage     := 1
	Default nPageSize := 10

	aArea      := FWGetArea()
	cWhere     := " "
	aUrlFilter := {}

	AddMapFieldsNcmGroups(self)

	::setUseSpaces(.T.)
	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( getNcmGroupsQuery() )

	// Caso não seja informado a filial nos filtros irá utilizar a filial posicionada
	if !("FT_FILIAL" $ cFilter)
		cWhere := "  SFT.FT_FILIAL = '" + FwxFilial("SFT") +"' AND "
	endif

	cWhere += "  SFT.FT_EMISSAO BETWEEN '20200701' AND '20210724' " // mock de data para testes
	cWhere += "  AND SFT.FT_TIPO NOT IN ('S', 'I', 'C', 'P') "
	cWhere += "  AND SFT.FT_ESPECIE IN ('NF', 'NFE', 'SPED', 'SATCE', 'NFCE') "
	cWhere += "  AND SB1.B1_POSIPI <> '' "
	cWhere += "  AND SB1.B1_MSBLQL <> '1' "
	cWhere += "  AND SFT.D_E_L_E_T_ = ' ' "
	cWhere += "  AND SB1.D_E_L_E_T_ = ' ' "

	::SetWhere(cWhere)
	::SetOrder("SB1.B1_POSIPI")

	if !Empty(cFilter)

		aUrlFilter := {;
			{"FILTER", cFilter};
			}
		::SetUrlFilter(aUrlFilter)
	endif


	If ::Execute()
		::FillGetResponse()
	EndIf

	FWRestArea(aArea)
	FwFreeArray(aArea)

Return


//------------------------------------------------------------------------------
/*/{Protheus.doc} AddMapFieldsNcmGroups
    Metodo responsável por adicionar os campos mapeados para a consulta de grupos de NCM

    @type  Static Function
    @author Squad PIF
    @version 1.0
/*/
//------------------------------------------------------------------------------
Static Function AddMapFieldsNcmGroups(oSelf as Object)

	oSelf:addMapFields("B1_POSIPI", "B1_POSIPI", .T., .F., {"B1_POSIPI", "C", 4, 0}, "SB1.B1_POSIPI")
Return


//------------------------------------------------------------------------------
/*/{Protheus.doc} getNcmGroupField
    Função responsável por obter o campo de grupo de NCM

    @type  Static Function
    @author Squad PIF
    @version 1.0
/*/
//------------------------------------------------------------------------------
Static Function getNcmGroupField()
	Local cQuery     := ""                        As Character
	Local cDatabase  := AllTrim(Upper(TcGetDb())) As Character

	// Tratamento para o SGBD ORACLE.
	cQuery := " SUBSTR(SB1.B1_POSIPI, 1, 4) "

	If cDatabase <> "ORACLE"
		cQuery := " LEFT(SB1.B1_POSIPI, 4) "
	EndIf

Return cQuery


//------------------------------------------------------------------------------
/*/{Protheus.doc} getNcmGroupsQuery
    Metodo responsável por obter a query de grupos de NCM

    @type  Static Function
    @author Squad PIF
    @version 1.0
/*/
//------------------------------------------------------------------------------
Static Function getNcmGroupsQuery()
	Local cQuery   As Character

	cQuery := "SELECT DISTINCT #QueryFields# "
	cQuery += "FROM ("
	cQuery += "  SELECT DISTINCT " + getNcmGroupField() + " AS B1_POSIPI "
	cQuery += "  FROM " + RetSqlName("SFT") + " SFT "
	cQuery += "  INNER JOIN " + RetSqlName("SB1") + " SB1 ON " + FWJoinFilial("SB1", "SFT")
	cQuery += "  AND SB1.B1_COD = SFT.FT_PRODUTO "
	cQuery += "  WHERE #QueryWhere# "
	cQuery += ") AS SB1"

Return cQuery

//------------------------------------------------------------------------------
/*/{Protheus.doc} getProductsByNcmGroupQuery
    Metodo responsável por obter a query de produtos de acordo com o grupo de NCM informado

    @type  Static Function
    @author Squad PIF
    @version 1.0
/*/
//------------------------------------------------------------------------------
Static Function getProductsByNcmGroupQuery()
	Local cQuery   As Character

	cQuery := "SELECT DISTINCT #QueryFields# "
	cQuery += "FROM " + RetSqlName("SFT") + " SFT "
	cQuery += "INNER JOIN " + RetSqlName("SB1") + " SB1 "
	cQuery += "  ON " + FWJoinFilial("SB1", "SFT")
	cQuery += "  AND SB1.B1_COD = SFT.FT_PRODUTO "
	cQuery += "LEFT JOIN " + RetSqlName("SB5") + " SB5 "
	cQuery += "  ON " + FWJoinFilial("SB5", "SB1")
	cQuery += "  AND SB5.B5_COD = SB1.B1_COD "
	cQuery += "WHERE #QueryWhere# "
	cQuery += "GROUP BY SB1.B1_POSIPI, SB1.B1_COD "

Return cQuery


//------------------------------------------------------------------------------
/*{Protheus.doc} formatNcmGroupsResponse
    Metodo responsável por retornar o objeto json formatado com os dados dos grupos de NCM

    @type  Private Method
    @author Squad PIF
    @version 1.0
    @return Json, retorna o objeto json contendo os dados dos grupos de NCM
/*/
//------------------------------------------------------------------------------
Method formatNcmGroupsResponse() As Json Class NcmGroupsData
    Local oJResponse   := JsonObject():New() As Json
	Local cNcmCode     := "" As character

    oJResponse:FromJson(::getJSONResponse())

	aEval(oJResponse:GetJsonObject("items"), {|x| cNcmCode := x:GetJsonText("b1_posipi"),;
		x["ncmGroup"]            := cNcmCode + ".XX.XX",;
		x["ncmGroupDescription"] := ::getDescriptionByNcm(cNcmCode),;
		x:DelName("b1_posipi");
	})

	// Exclusão da tabela temporária
	if ValType(self:oTempTableNcm) == "O"
		self:oTempTableNcm:Delete()
		FWFreeObj(self:oTempTableNcm)
	endif

Return oJResponse

//------------------------------------------------------------------------------
/*{Protheus.doc} getProductsByNcmGroup
    Metodo responsável por retornar os dados dos produtos de acordo com o grupo de NCM informado

    @type Method
    @author Squad PIF
    @version 1.0
    @param nPage, numeric, numero da pagina
	@param nPageSize, numeric, Quantidade de registros por pagina
    @param cGroupCode, Character, Código do grupo de NCM
	@param cFilter, Character, Filtro a ser aplicado na consulta
    @return Json, retorna o objeto json contendo os dados dos grupos de NCM
*/
//------------------------------------------------------------------------------
Method getProductsByNcmGroup(nPage as Numeric, nPageSize as Numeric, cGroupCode as Character, cFilter as Character) As Json class NcmGroupsData
	Local cWhere     As Character
	Local aArea      As Array
	Local aUrlFilter As Array

	Default nPage     := 1
	Default nPageSize := 10

	aArea      := FWGetArea()
	cWhere     := " "
	aUrlFilter := {}

	addMapFieldsByNcmGroupProducts(self)

	::setUseSpaces(.T.)
	::setPage(nPage)
	::setPageSize(nPageSize)
	::SetQuery( getProductsByNcmGroupQuery() )

	// Caso não seja informado a filial nos filtros irá utilizar a filial posicionada
	if !("FT_FILIAL" $ cFilter)
		cWhere := "  SFT.FT_FILIAL = '" + FwxFilial("SFT") +"' AND "
	endif

	cWhere += "  SFT.FT_EMISSAO BETWEEN '20200701' AND '20210724' " // mock de data para testes
	cWhere += "  AND SFT.FT_TIPO NOT IN ('S', 'I', 'C', 'P') "
	cWhere += "  AND SFT.FT_ESPECIE IN ('NF', 'NFE', 'SPED', 'SATCE', 'NFCE') "
	cWhere += "  AND SB1.B1_POSIPI LIKE '" + cGroupCode + "%' "
	cWhere += "  AND SB1.B1_MSBLQL <> '1' "
	cWhere += "  AND SFT.D_E_L_E_T_ = ' ' "
	cWhere += "  AND SB1.D_E_L_E_T_ = ' ' "

	::SetWhere(cWhere)
	::SetOrder("SB1.B1_POSIPI")

	if !Empty(cFilter)

		aUrlFilter := {;
			{"FILTER", cFilter};
			}
		::SetUrlFilter(aUrlFilter)
	endif

	If ::Execute()
		::FillGetResponse()
	EndIf

	FWRestArea(aArea)
	FwFreeArray(aArea)

Return


//------------------------------------------------------------------------------
/*/{Protheus.doc} addMapFieldsByNcmGroupProducts
    Metodo responsável por adicionar os campos mapeados para a consulta de produtos de acordo com o grupo de NCM

    @type  Static Function
    @author Squad PIF
    @version 1.0
/*/
//------------------------------------------------------------------------------
Static Function addMapFieldsByNcmGroupProducts(oSelf as Object)

	oSelf:addMapFields("B1_POSIPI", "B1_POSIPI", .T., .F., {"B1_POSIPI", "C", TamSX3('B1_POSIPI')[1], 0}, "SB1.B1_POSIPI")
	oSelf:addMapFields("B1_COD", "B1_COD", .T., .F., {"B1_COD", 'C', TamSX3('B1_COD')[1], 0}, "SB1.B1_COD")
Return


//------------------------------------------------------------------------------
/*{Protheus.doc} formatProductsByNcmGroupResponse
    Metodo responsável por retornar o objeto json formatado com os dados dos produtos por grupo de NCM

    @type  Private Method
    @author Squad PIF
    @version 1.0
    @return Json, retorna o objeto json contendo os dados dos produtos por grupo de NCM
/*/
//------------------------------------------------------------------------------
Method formatProductsByNcmGroupResponse() As Json Class NcmGroupsData
    Local aArea            := FWGetArea()
    Local oJResponse       := JsonObject():New() As Json

    oJResponse:FromJson(::getJSONResponse())

	aEval(oJResponse:GetJsonObject("items"), {|x| cNcm := x:GetJsonText("b1_posipi"),;
	    cCodProd              := x:GetJsonText("b1_cod"),;
        x["productCode"]      := AllTrim(cCodProd),;
        x["ncmCode"]          := Transf(AllTrim(cNcm), "@R 9999.99.99" ),;
        x["ncmDescription"]   := ::getDescriptionByNcm(cNcm),;
        x["suggestedNcmList"] := {},;
		x["suggestedNcmList"] := getMockSuggestion(),;
        x:DelName("b1_posipi"),;
        x:DelName("b1_cod");
	 })

	// Exclusão da tabela temporária
	if ValType(self:oTempTableNcm) == "O"
		self:oTempTableNcm:Delete()
		FWFreeObj(self:oTempTableNcm)
	endif

	FWRestArea(aArea)
    
Return oJResponse


//------------------------------------------------------------------------------
/*{Protheus.doc} getMockSuggestion
    Função responsável por obter uma sugestão de NCM Mockadas

    @type Static Function
    @author Squad PIF
    @version 1.0
	@param JItem, json, item da lista de produtos
    @return character, descrição do grupo de NCM
*/
//------------------------------------------------------------------------------
Static Function getMockSuggestion(JItem As Json)
	Local aRet           := {}                 As Array
	Local oJSuggestedNcm := JsonObject():New() As Json

	// Mock de como seria o retorno de sugestão de NCM
	oJSuggestedNcm                           := JsonObject():New()
	oJSuggestedNcm["proposedNcmCode"]        := Transf( "01011090", "@R 9999.99.99" )
	oJSuggestedNcm["proposedNcmDescription"] := "Descrição Proposta"
	oJSuggestedNcm["proposedClassification"] := "Classificação Proposta"
	oJSuggestedNcm["productCode"]            := AllTrim(cCodProd)

	aAdd(aRet, oJSuggestedNcm)

	// Mock de como seria o retorno de sugestão de NCM
	oJSuggestedNcm                           := JsonObject():New()
	oJSuggestedNcm["proposedNcmCode"]        := Transf( "01029019", "@R 9999.99.99" )
	oJSuggestedNcm["proposedNcmDescription"] := "Descrição Proposta"
	oJSuggestedNcm["proposedClassification"] := "Classificação Proposta"
	oJSuggestedNcm["productCode"]            := AllTrim(cCodProd)

	aAdd(aRet, oJSuggestedNcm)
	// Mock de como seria o retorno de sugestão de NCM
	oJSuggestedNcm                           := JsonObject():New()
	oJSuggestedNcm["proposedNcmCode"]        := Transf( "01051900", "@R 9999.99.99" )
	oJSuggestedNcm["proposedNcmDescription"] := "Descrição Proposta"
	oJSuggestedNcm["proposedClassification"] := "Classificação Proposta"
	oJSuggestedNcm["productCode"]            := AllTrim(cCodProd)

	aAdd(aRet, oJSuggestedNcm)
Return aRet


//------------------------------------------------------------------------------
/*{Protheus.doc} getDescriptionByNcm
    Metodo responsável por obter a descrição de um ncm

    @type Method
    @author Squad PIF
    @version 1.0
	@param cNcmCode, character, código do NCM
    @return character, descrição do grupo de NCM
*/
//------------------------------------------------------------------------------
Method getDescriptionByNcm(cNcmCode As Character) As Character Class NcmGroupsData
	Local aArea      := FWGetArea()
	Local cDesc      := "Sem descrição"
	Local lContinue  := .F.

	// Caso a tabela temporária não exista, cria e popula
	if Empty(::cAliasNcmTmp)
		lContinue := ::createNcmTemp()

		if lContinue
			lContinue := ::populateNcmTemp()
		endif
	else
		lContinue := .T.
	endif

	// Realiza a busca na tabela temporária
	if lContinue
		(::cAliasNcmTmp)->(dbSetOrder(1))

		if (::cAliasNcmTmp)->(MsSeek(cNcmCode)) .Or. (::cAliasNcmTmp)->(MsSeek(cNcmCode))
			cDesc := (self:cAliasNcmTmp)->(DESCRICAO)
		endIf
	endif

	FWRestArea(aArea)
Return cDesc


//------------------------------------------------------------------------------
/*{Protheus.doc} createNcmTemp
    Função responsavel por criar uma tabela temporária para armazenar o retorno da API do SISCOMEX

    @type Method
    @author Squad PIF
    @version 1.0
    @return Nil
*/
//------------------------------------------------------------------------------
Method createNcmTemp() Class NcmGroupsData
	Local aArea        := FWGetArea()      As Array
	Local aFields      := {}               As Array
	Local cTableName   := ""               As Character
	Local cAliasTabTmp := getNextAlias()   As Character

	::oTempTableNcm   := totvs.framework.database.temporary.SharedTable():New(cAliasTabTmp)

	aAdd(aFields, {"CODIGO",     "C",  8, 0})
	aAdd(aFields, {"DESCRICAO",  "M",  8, 0})

	//Configuração da tabela temporária
	::oTempTableNcm:SetFields(aFields)

	::oTempTableNcm:AddIndex("1", {"CODIGO"} )

	//Efetiva a criação da tabela temporária
	::oTempTableNcm:Create()

	//Recupera o nome da tabela temporária para ser usada no Bulk
	cTableName := ::oTempTableNcm:GetTableNameForTCFunctions()

	::oBulkTempTableNcm := FwBulk():New(cTableName, 20000) // Aumenta o limite para 20000 registros
	::lCanUseBulkTempTableNcm       := FwBulk():CanBulk()

	::cAliasNcmTmp := cAliasTabTmp

	if ::lCanUseBulkTempTableNcm
		::oBulkTempTableNcm:SetFields(aFields)
	endif

	FWRestArea(aArea)
Return .T.


//------------------------------------------------------------------------------
/*{Protheus.doc} populateNcmTemp
    Metodo responsável por popular a tabela temporária consumindo a API do SISCOMEX e armazenar os dados na tabela temporária.

    @type Method
    @author Squad PIF
    @version 1.0
    @return logocal, indica se a operação foi bem sucedida
*/
//------------------------------------------------------------------------------
Method populateNcmTemp() Class NcmGroupsData
	Local aArea           := FWGetArea()
	Local cUrl            := "https://val.portalunico.siscomex.gov.br"             As Character
	Local cEndPoint       := "/classif/api/publico/nomenclatura/download/json?perfil=PUBLICO" As Character
	Local oRest           := FWRest():New(cUrl)                                As Object
	Local aHeader         := {}                                                As Array
	Local oJResponse      := JsonObject():New()                                As Json
	Local oJNcm           := JsonObject():New()                                As Json
	Local lOk             := .F.
	Local aNomenclatures  := {}

	aAdd(aHeader, "Accept: application/json")
	aAdd(aHeader, "Content-Type: application/json; charset=cp1252")

	oRest:setPath(cEndPoint)

	if oRest:Get(aHeader)
		cResult := oRest:GetResult()

		uRet := oJResponse:FromJson(cResult)

		if uRet == Nil

			if ::lCanUseBulkTempTableNcm
				lOk := .T.
			endif

			aNomenclatures := oJResponse:GetJsonObject("Nomenclaturas")

			aEval( aNomenclatures , { |x| ::addDataTabTemp(x, @lOk) })

			if ::lCanUseBulkTempTableNcm
				lOk := lOk .and. ::oBulkTempTableNcm:Close()

				::oBulkTempTableNcm:Destroy()
				::oBulkTempTableNcm := nil
			endif
		endif
	endif

	FWRestArea(aArea)
	FwFreeArray(aHeader)
	FWFreeObj(oRest)
	FreeObj(oJNcm)
Return lOk


//------------------------------------------------------------------------------
/*{Protheus.doc} addDataTabTemp
    Método responsável por adicionar dados à tabela temporária.

    @type Method
    @author Squad PIF
    @version 1.0
	@param oJNcm, Json, Objeto Json com a nomeclatura do Ncm
	@param lOk, Logical, Indica se a operação foi bem sucedida
    @return Nil
*/
//------------------------------------------------------------------------------
Method addDataTabTemp(oJNcm As Json, lOk As Logical) class NcmGroupsData
	Local aArea      := FWGetArea()
	Local cCodigo    := ""
	Local cDesc      := ""

	cCodigo := AllTrim(StrTran(oJNcm:GetJsonText("Codigo"), ".", ""))
	cDesc   := DecodeUTF8(oJNcm:GetJsonText("Descricao"), "cp1252")

	if ::lCanUseBulkTempTableNcm
		lOk := lOk .and. ::oBulkTempTableNcm:AddData({cCodigo, cDesc})
	endif

	FWRestArea(aArea)
Return

