#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.pif.fiscal.insight.ncm.confirm.suggestion.data.ch"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util

//------------------------------------------------------------------------------
/* {Protheus.doc} ConfirmSuggestionData
    Classe responsável por obter os dados de confirmação de sugestão de NCM

    @type Class
    @author Squad PIF
    @return Nil
*/
//------------------------------------------------------------------------------
Class ConfirmSuggestionData

	Public Method new() Constructor
	Public Method postConfirmSuggestion()
	Private Method validConfirmSuggestionDefinitions()

EndClass

//------------------------------------------------------------------------------
/*{Protheus.doc} New
    Metodo construtor

    @type Method
    @author Squad PIF
    @return Object, retorna o objeto criado
/*/
//------------------------------------------------------------------------------
Method new() Class ConfirmSuggestionData
Return Self

//------------------------------------------------------------------------------
/*{Protheus.doc} postConfirmSuggestion
    Metodo responsável por confirmar a sugestão de NCM

    @type Method
    @author Squad PIF
    @return Json, retorna o objeto json contendo os dados do dashboard de NCM
/*/
//------------------------------------------------------------------------------
Method postConfirmSuggestion(oJBody As Json) class ConfirmSuggestionData
    Local aArea          := FWGetArea()
    Local oJResponse     := JsonObject():New()        As Json
    Local oError         := ErrorClass():New()        As Object
    Local aValid         := {}                        As Array
    Local aProducts      := {}                        As Array
    Local nX             := 1                         As Numeric
    Local cCodUsr        := ""                        As Character

    aValid := Self:validConfirmSuggestionDefinitions()

    oError:genCode     := 400
    oError:description := aValid[2]
    oJResponse         := getBadRequestResponse({oError})

    if aValid[1]

        dbSelectarea("SB1")
        SB1->(dbSetOrder(1))

        aProducts := oJBody["products"]
        cCodUsr   := RetCodUsr()

        for nX := 1 to len(aProducts)
            
            if SB1->(dbSeek(FwxFilial("SB1")+aProducts[nX]["codProd"]))

                RecLock("HNB", .T.)
                HNB->HNB_FILIAL   := FWxFilial('HNB')
                HNB->HNB_PROD     := SB1->B1_COD
                HNB->HNB_DESC     := SB1->B1_DESC
                HNB->HNB_ANTNCM   := PadR(SB1->B1_POSIPI, TamSx3("HNB_NOVNCM")[1])
                HNB->HNB_NOVNCM   := PadR(aProducts[nX]["newNcm"], TamSx3("HNB_NOVNCM")[1])
                HNB->HNB_USUARI   := cCodUsr
                HNB->HNB_DTCHG    := dDatabase
                HNB->HNB_HORCHG   := Time()
                HNB->(MsUnlock())

                RecLock("SB1", .F.)
                SB1->B1_POSIPI := aProducts[nX]["newNcm"]
                SB1->(MsUnlock())

                oJResponse            := JsonObject():New()
                oJResponse["message"] := STR0001 //"NCM alterado com sucesso!"
            endif
        next

        oJResponse := getSuccessReponse(oJResponse)
    endif

    FWRestArea(aArea)
   
Return oJResponse


//------------------------------------------------------------------------------
/*{Protheus.doc} validConfirmSuggestionDefinitions
    Metodo responsável por validar as definições necessárias para a confirmação de sugestão de NCM

    @type Method
    @author Squad PIF
    @return aValid, Array, retorna um array com o status da validação e a mensagem de erro caso exista
/*/
//------------------------------------------------------------------------------
Method validConfirmSuggestionDefinitions() class ConfirmSuggestionData
    Local aValid := {} as Array

    aAdd(aValid, .F.)
    aAdd(aValid, STR0002) // "A tabela de Histórico de Ajustes de NCM não existe no banco de dados"

    If ChkFile("HNB")
        aValid[1] := .T.
        aValid[2] := ""
    EndIf
    
Return aValid
