#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.backoffice.pif.fiscal.insight.ncm
using namespace totvs.protheus.backoffice.pif.util


//------------------------------------------------------------------------------
/* {Protheus.doc} NcmGroupsController
    API responsável por consultar os grupos de NCM e os produtos relacionados a esses grupos com movimentação nos últimos 12 meses.

    @type Class
    @author Squad PIF
    @version 1.0
*/
//------------------------------------------------------------------------------
Class NcmGroupsController

	Public Method new() Constructor

	@Get("/api/pif/ncm/groups")
	Public Method getNcmGroups()

	@Get("/api/pif/ncm/groups/:cGroupCode/products")
	Public Method getProductsByNcmGroup()

endClass


//------------------------------------------------------------------------------
/* {Protheus.doc} new
    Metodo construtor

    @type Method
    @author Squad PIF
    @version 1.0
    @return Object, retorna o objeto criado
*/
//------------------------------------------------------------------------------
Method New() Class NcmGroupsController
Return Self


//------------------------------------------------------------------------------
/* {Protheus.doc} getNcmGroups
    Método responsável por retornar a lista de grupos de NCM dos produtos com movimentação nos últimos 12 meses.

    @type Method
    @author Squad PIF
    @version 1.0
*/
//------------------------------------------------------------------------------
Method getNcmGroups() class NcmGroupsController
	Local oService         As Object
	Local oJResponse       As Json
	Local nPage            As Numeric
	Local nPageSize        As Numeric
	Local cFilter          As Character

	nPage     := 1
	nPageSize := 10
	cFilter   := ""

	oJResponse := getNoAccessResponse()

	if canAccess("PIFA010")
		getPageAndPageSize(@nPage, @nPageSize)

		// Pegando os filtros
		If (oRest:getQueryRequest():GetJsonText("$filter") != "null")
       		cFilter := AllTrim(oRest:getQueryRequest():GetJsonText("$filter"))
      	EndIf

		oService     := NcmGroupsService():New()
		oJResponse   := oService:getNcmGroups(@nPage, @nPageSize, cFilter)
	endIF

	answerRest(oJResponse)

	FWFreeObj(oService)
	FreeObj(oJResponse)

Return


//------------------------------------------------------------------------------
/* {Protheus.doc} getProductsByNcmGroup
    Método responsável por retornar a lista de produtos de acordo com o grupo de NCM informado.

    @type Method
    @author Squad PIF
    @version 1.0
*/
//------------------------------------------------------------------------------
Method getProductsByNcmGroup() class NcmGroupsController
	Local oService          As Object
	Local oJResponse     As Json
	Local nPage             As Numeric
	Local nPageSize         As Numeric
	Local JPath             As Json
	Local cGroupCode        As Character
	Local cFilter           As Character

	nPage       := 1
	nPageSize   := 10
	jPath       := oRest:getPathParamsRequest()
	cGroupCode  := ""
	cFilter     := ""

	oJResponse := getNoAccessResponse()

	if canAccess("PIFA010")
		getPageAndPageSize(@nPage, @nPageSize)

		if JPath:GetJsonText("cGroupCode") != "null"
			cGroupCode := PadR(JPath:GetJsonText("cGroupCode"), 4)
		endif

		// Pegando os filtros
		If (oRest:getQueryRequest():GetJsonText("$filter") != "null")
       		cFilter := AllTrim(oRest:getQueryRequest():GetJsonText("$filter"))
      	EndIf

		oService        := NcmGroupsService():New()
		oJResponse      := oService:getProductsByNcmGroup(@nPage, @nPageSize, cGroupCode, cFilter)
	endIf

	answerRest(oJResponse)

	FWFreeObj(oService)
	FreeObj(oJResponse)

Return
