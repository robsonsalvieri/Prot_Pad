#include 'totvs.ch'
#include 'parmtype.ch'

CLASS InsightAdapter FROM FWAdapterBaseV2

    Public data aFields         as Array
    Public data aTables         as Array
    Public data cTableName      as Character
    Public data cCondition      as Character
    Public data cOrder          as Character
    Public data cOrderSort      as Character
    Public data cFieldsTab 	    as Character
    Public data cInsightName    as Character
    Public data cFilterByField  as Character
    Public data cStatus         as Character
    Public data lIsMock         as Logical

    Private data oJson      as Object

    Public METHOD New()
    Public METHOD GetListInsigths()
    
EndClass

/*/{Protheus.doc} New
	Método construtor
@author Victor Vieira 
@since 03/04/2025
/*/ 
Method New( cVerb ) CLASS InsightAdapter
    Self:cTableName := ""
    Self:cCondition := ""
    Self:cOrder := ""
    Self:cOrderSort := ""
    Self:cFieldsTab := ""
    Self:cInsightName := ""
    Self:cFilterByField := ""
    Self:cStatus := "" 
    Self:lIsMock := .F.
    Self:aFields := {}
    Self:aTables := {}
    _Super:New( cVerb, .T. )
return

/*/{Protheus.doc} InsightAdapter::GetListInsigths
Metodo que consulda os dados contidos na I19
@type method
@author Victor Vieira
@since 03/04/2025       
@param nPage, numeric, número da pagina.
@param nPageSize, numeric, tamanho da página.
/*/ 
Method GetListInsigths( nPage ,nPageSize ) CLASS InsightAdapter
    Local aArea     AS ARRAY
    Local cWhere    AS Character
    Local cQryBranch    AS Character
    
    Default nPage       := 1
	Default nPageSize   := 10
    
    aArea   := FwGetArea()
    
    //Adiciona o mapa de campos Json/ResultSet
    AddMapFields( self )

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    
    //Informa a Query a ser utilizada pela API
    ::SetQuery( GetQuery(::cTableName) )
    
    //Informa a clausula Where da Query
    IF ::cInsightName <> "sales_recommendation"
        cWhere := PINSMakeWhere( ::aFields, ::cCondition, , ::cFilterByField )
    Else
		//Verifica se o usuário tem limitação de acesso por filial
		cQryBranch := totvs.protheus.backoffice.ba.insights.pinsBranchUser( __cUserID, {"SC5", "SC6"} )    
        cWhere := totvs.protheus.backoffice.ba.insights.sales.pinsSalesMakeWhere( cQryBranch, ::cStatus, ::cfilterByField )
    EndIf
    ::SetWhere( Upper( cWhere ) )

    If !Empty( ::cOrder )
        ::SetOrder( Upper( ::cOrder ) )
	EndIf

    If ::Execute()
        // Gera o arquivo Json com o retorno da Query
        ::FillGetResponse()
    EndIf
    
    FwrestArea(aArea)
Return 

/*/{Protheus.doc} AddMapFields
Cria o Mapa de campos da estrutura da tabela x API.
@type function
@author Victor Vieira 
@since 03/04/2025
@param oSelf, object, Objeto - Objeto com herança da classe FWAdapterBaseV2
/*/
Static Function AddMapFields( oSelf )

    Local nI := 0
    
    For nI := 1 To Len(oSelf:aFields)
        oSelf:AddMapFields(; 
        Iif( !Empty( oSelf:aFields[ nI ][ 1 ] ), oSelf:aFields[ nI ][ 1 ], oSelf:aFields[nI][2][1] ),UPPER(oSelf:aFields[nI][2][1]),.T.,.F.,; 
         { UPPER(oSelf:aFields[nI][2][1]),;
                oSelf:aFields[nI][2][2],; 
                oSelf:aFields[nI][2][3],;
                oSelf:aFields[nI][2][4] };
        )
    Next nI
Return

/*/{Protheus.doc} GetQuery
Monta a expressão SQL para consulta na tabela temporaria
@type function  
@author Victor Vieira
@since 03/04/2025
@param cTable, character, tabela da entidade.
@return cQuery, character, Query dos datdos contidos na tabela temporaria
/*/ 
Static Function GetQuery(cTable)
    Local cQuery AS CHARACTER

    Default cTable := ""
     
    //Obtem a ordem informada na requisição, a query exterior SEMPRE deve ter o id #QueryFields# ao invés dos campos fixos
    //necessáriamente não precisa ser uma subquery, desde que não contenha agregadores no retorno ( SUM, MAX... )
    //o id #QueryWhere# é onde será inserido o clausula Where informado no método SetWhere()
    cQuery := " SELECT #QueryFields#"
    cQuery +=   " FROM " + cTable  + " I21 "
    cQuery += " #QueryWhere#"
Return cQuery
