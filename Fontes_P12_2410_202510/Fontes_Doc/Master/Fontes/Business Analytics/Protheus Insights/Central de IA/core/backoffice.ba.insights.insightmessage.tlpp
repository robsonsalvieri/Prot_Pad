#Include "tlpp-core.th"
#include 'totvs.ch'
#include "InsightDefs.CH"
#include "backoffice.ba.insights.insightmessage.ch"

NAMESPACE totvs.protheus.backoffice.ba.insights

Class InsightMessage
    
    Public Data cMsgUuid               As Character
    Public Data cMsgTransId            As Character
    Public Data cMsgTenantId           As Character
    Public Data cMsgInsight            As Character
    Public Data cMsgPayload            As Character
    Public Data cMsgReceivedDate       As Character
    Public Data cMsgReceivedStatus     As Character
    Public Data cMsgScheduleDate       As Character
    Public Data cMsgProcessDate        As Character
    Public Data cMsgProcessStatus      As Character
    Public Data cMsgLastProcVers       As Character
    Public Data cMsgLastProcMsg        As Character
    Public Data cError                 As Character
    Public Data cMsgMessId             As Character

    Public Data oInsightConfig         As Object

    Public Data lHasError              As Logical

    Public Data nI19Recno             As Numeric

    Public Method New() Constructor
    Public Method InsertMessage() As Logical
    Public Method LoadMessage(cMessageId As Character) As Logical
    Public Method UpdateProc(cProcMessage As Character, cProcStatus As Logical, cProcVers As Character) As Logical
    Public Method UpdateSchedule() As Logical
    Public Method LoadInsightConfig(cInsightName As Character) As Logical

    Private Method RetMessageRec(cMessageId As Character) As Numeric

EndClass

Method New() Class InsightMessage
Return Self

Method InsertMessage() As Logical Class InsightMessage
    
    Local aArea := GetArea()            As Array
    Local aAreaI19 := {}                As Array
    Local lRet                          As Logical

    ::cMsgUuid := FWUUIDV1( .T. )
    dbSelectArea( "I19" )
    aAreaI19 := I19->( GetArea() )
    
    I19->( dbSetOrder(3) )  //I19_FILIAL+I19_INSIGT+I19_TIDMSG

    If I19->( MsSeek( xFilial( "I19" ) + PadR( ::cMsgInsight, TamSx3("I19_INSIGT")[1]) + ::cMsgTransId ) )
        // Ja existe mensagem para o insight com o mesmo Transaction ID
        I19->( RecLock( "I19", .F. ) )
            I19->I19_STRECV := "DUP"
    Else
        I19->( RecLock( "I19", .T.) )
            I19->I19_UIDMSG := ::cMsgUuid
            I19->I19_STRECV := "RCV"
            I19->I19_DTSCHD := ::cMsgScheduleDate
            I19->I19_DTPROC := ::cMsgProcessDate
            I19->I19_STPROC := ::cMsgProcessStatus
            I19->I19_LSTPRV := ::cMsgLastProcVers
            I19->I19_LSTPRM := ::cMsgLastProcMsg
    EndIf
    
    I19->I19_FILIAL := xFilial( "I19" )
    I19->I19_TIDMSG := ::cMsgTransId
    I19->I19_TENANT := ::cMsgTenantId
    I19->I19_INSIGT := ::cMsgInsight
    I19->I19_PAYLOD := ::cMsgPayload
    I19->I19_DTRECV := ::cMsgReceivedDate
    if I19->(FieldPos("I19_MESSID")) > 0
        I19->I19_MESSID := ::cMsgMessId
    Endif        
    I19->( MsUnlock() )

    ::nI19Recno := I19->( Recno() )

    I19->( RestArea( aAreaI19 ) )
    RestArea( aArea )

    FWFreeArray( aArea )
    FWFreeArray( aAreaI19 )    
Return lRet

Method LoadMessage(cMessageId As Character) As Logical Class InsightMessage
    Local aArea := GetArea()    As Array
    Local aAreaI19 := {}        As Array
    Local nStart

    nStart := Seconds()    

    Try
        ::nI19Recno := ::RetMessageRec( cMessageId )

        If ::nI19Recno == 0
            pinsError( INSIGHTSERROR_REGISTER_NOT_FOUND, 'InsightMessage', { STR0001, { cMessageId } } )  //"Mensagem de Insight com o ID [#1] não encontrada."   
        EndIf

        dbSelectArea( "I19" )
        aAreaI19 := I19->( GetArea() )

        I19->( dbGoTo( ::nI19Recno ) )
        
        ::cMsgUuid              := I19->I19_UIDMSG
        ::cMsgTransId           := I19->I19_TIDMSG
        ::cMsgTenantId          := I19->I19_TENANT
        ::cMsgInsight           := I19->I19_INSIGT
        ::cMsgPayload           := I19->I19_PAYLOD
        ::cMsgReceivedDate      := I19->I19_DTRECV
        ::cMsgReceivedStatus    := I19->I19_STRECV
        ::cMsgScheduleDate      := I19->I19_DTSCHD
        ::cMsgProcessDate       := I19->I19_DTPROC
        ::cMsgProcessStatus     := I19->I19_STPROC
        ::cMsgLastProcVers      := I19->I19_LSTPRV
        ::cMsgLastProcMsg       := I19->I19_LSTPRM

        I19->( RestArea( aAreaI19 ) )
        
        ::LoadInsightConfig( ::cMsgInsight )
    Catch oException
        FwLogMsg( "ERROR", Nil, "ProtheusInsights", "InsightMessage",, "", STR0003,, Seconds() - nStart, ;    //"Exceção ocorrida ao pesquisar a mensagem."
            {;
                { "Message", STR0003 },;   //"Exceção ocorrida ao pesquisar a mensagem."
                { "ErrorCode", oException:genCode },;
                { "ErrorMessage", oException:description };
            })

    EndTry
    RestArea( aArea )

    FWFreeArray( aArea )
    FWFreeArray( aAreaI19 )
Return .T.

Method UpdateSchedule() As Logical Class InsightMessage

    Local lScheduled            As Logical
    Local aArea := GetArea()    As Array
    Local aAreaI19 := {}        As Array
    Local nStart

    nStart := Seconds()    

    Try
        If(::nI19Recno == 0)
            pinsError( INSIGHTSERROR_REGISTER_NOT_FOUND, 'InsightMessage', { STR0002 } )  //"Mensagem de Insight não encontrada."
        EndIf
        
        dbSelectArea( "I19" )
        aAreaI19 := I19->( GetArea() )
        
        I19->( dbGoTo( ::nI19Recno ) )

        I19->( RecLock( "I19", .F. ) )
            I19->I19_DTSCHD := FWTIMESTAMP(5)
            I19->I19_STPROC := "SCH"
        I19->( MsUnLock() )

        lScheduled := .T.  
    Catch oException
        FwLogMsg( "ERROR", Nil, "ProtheusInsights", "InsightMessage",, "", STR0003,, Seconds() - nStart, ;    //"Exceção ocorrida ao pesquisar a mensagem."
            {;
                { "Message", STR0003 },;   //"Exceção ocorrida ao pesquisar a mensagem."
                { "ErrorCode", oException:genCode },;
                { "ErrorMessage", oException:description };
            })

        lScheduled := .F.
    EndTry    

    I19->( RestArea( aAreaI19 ) )
    RestArea( aArea )

    FWFreeArray( aArea )
    FWFreeArray( aAreaI19 )
Return lScheduled

Method UpdateProc(cProcMessage As Character, lSuccess As Logical, cProcVers As Character) As Logical Class InsightMessage

    Local aArea := GetArea()    As Array
    Local aAreaI19 := {}        As Array
    Local nStart

    nStart := Seconds()    

    Try
        If(::nI19Recno == 0)
            pinsError( INSIGHTSERROR_REGISTER_NOT_FOUND, 'InsightMessage', { STR0002 } )  //"Mensagem de Insight não encontrada."
        EndIf

        dbSelectArea( "I19" )
        aAreaI19 := I19->( GetArea() )
        
        I19->( dbGoTo( ::nI19Recno) )

        ::cMsgLastProcMsg := cProcMessage
        ::cMsgProcessStatus := IIF( lSuccess, "SUC", "ERR" )
        ::cMsgProcessDate := FWTIMESTAMP(5)
        ::cMsgLastProcVers := cProcVers
        
        I19->( RecLock( "I19", .F. ) )
            I19->I19_DTPROC := ::cMsgProcessDate
            I19->I19_STPROC := ::cMsgProcessStatus
            I19->I19_LSTPRV := ::cMsgLastProcVers
            I19->I19_LSTPRM := AllTrim( I19->I19_LSTPRM ) +;
                "[" + FWTIMESTAMP(2) + "]: " +;
                AllTrim( ::cMsgLastProcMsg ) + CRLF
        I19->( MsUnlock() )

        lRet := .T.
    Catch oException
        FwLogMsg( "ERROR", Nil, "ProtheusInsights", "InsightMessage",, "", STR0003,, Seconds() - nStart, ;    //"Exceção ocorrida ao pesquisar a mensagem."
            {;
                { "Message", STR0003 },;   //"Exceção ocorrida ao pesquisar a mensagem."
                { "ErrorCode", oException:genCode },;
                { "ErrorMessage", oException:description };
            })

        lRet := .F.
    EndTry    

    I19->( RestArea( aAreaI19 ) )
    RestArea( aArea )
    
    FWFreeArray( aArea )
    FWFreeArray( aAreaI19 )
Return lRet

Method LoadInsightConfig( cInsightName As Character ) As Logical Class InsightMessage
    ::oInsightConfig := InsightConfig():New()
Return ::oInsightConfig:LoadConfig( cInsightName )

Method RetMessageRec( cMessageId As Character ) As Numeric Class InsightMessage
    Local cQuery        As Character
    Local cAlias        As Character
    Local oStatement    As Object
    Local nRecno := 0   As Numeric

    cQuery := "SELECT I19.R_E_C_N_O_"
    cQuery += " FROM ? I19"
    cQuery += " WHERE I19.I19_FILIAL = ? "
    cQuery += " AND I19.I19_UIDMSG = ? "
    cQuery += " AND I19.D_E_L_E_T_ = ' ' "

    cQuery := ChangeQuery( cQuery )
    oStatement := FwExecStatement():New( cQuery )

    oStatement:setUnsafe( 1, RetSqlName( "I19" ) )
    oStatement:setString( 2, xFilial( "I19" ) )
    oStatement:setString( 3, cMessageId )
    
    cAlias := MPSysOpenQuery( oStatement:getFixQuery() )

    ( cAlias )->( dbGoTop() )
    If (cAlias)->( !Eof() )
        nRecno := ( cAlias )->R_E_C_N_O_
    EndIf

    ( cAlias )->( dbCloseArea() )

    FreeObj( oStatement )
Return nRecno
