#INCLUDE "totvs.ch"
#INCLUDE "tlpp-core.th"
#include 'BACKOFFICE.BA.INSIGHTS.INSIGHTSCHEDPROCESS.CH'

NAMESPACE totvs.protheus.backoffice.ba.insights

Function InsightSchedProcess(aParams As Array)

    Local cCompanyGroup         As Character
    Local oException            As Object
    Local oInsightProcessor     As Object
    Local oStatement            As Object
    Local nStart := Seconds()   As Numeric

    oException := ErrorClass():New()
    
    Try
        
        cCompanyGroup := aParams[1]
        
        FwLogMsg("INFO", Nil, "ProtheusInsights", "InsightSchedProcess", Nil, "", "InsightSchedProcess: " + STR0001, Nil, Seconds() - nStart,; //"Execução de insights agendados iniciado"
            {;
                {"Message", "InsightSchedProcess: " + STR0001 },;   //"Execução de insights agendados iniciado"
                {"CompanyGroup", cCompanyGroup};
            })

        RpcSetType(3)
        RpcSetEnv(cCompanyGroup,,,, "FIN",, , /*lShowFinal*/, /*lAbend*/, /*lOpenSX*/, /*lConnect*/)

        cQuery := " SELECT I19.I19_UIDMSG " + ;
            " FROM ? I19 " + ;
            " INNER JOIN ? I20 " + ;
                " ON I20.I20_FILIAL = I19.I19_FILIAL " + ;
                " AND I20.I20_INSIGT = I19.I19_INSIGT " + ;
                " AND I20.I20_STATUS = '1'" + ;
                " AND I20.I20_PRIORI = 'F'" + ;
            " WHERE ( I19_STPROC = 'SCH' OR I19_DTSCHD = ' ' ) " + ;
                " AND I19.D_E_L_E_T_ = ' ' " + ;
                " AND I20.D_E_L_E_T_ = ' ' " + ;
            " ORDER BY I19.I19_DTRECV "

        cQuery := ChangeQuery( cQuery )
        oStatement := FwExecStatement():New( cQuery )
        oStatement:SetUnSafe( 1, RetSqlName( "I19" ))
        oStatement:SetUnSafe( 2, RetSqlName( "I20" ))
        
        cAliasQueue := MPSysOpenQuery( oStatement:getFixQuery() )

        While  ( cAliasQueue )->( !EOF() ) 
            
            FwLogMsg("INFO", Nil, "ProtheusInsights", "InsightSchedProcess", Nil, "", "InsightSchedProcess: " + STR0002, Nil, Seconds() - nStart,;  //"Processamento do Insight iniciado"
                {;
                    {"Message", "InsightSchedProcess: " + STR0002 },; //"Processamento de Insight iniciado"
                    {"MessageId", (cAliasQueue)->I19_UIDMSG};
                })

            oInsightProcessor := InsightsProcessor():New()
            
            oInsightProcessor:ProcessMessage((cAliasQueue)->I19_UIDMSG)
            
            FwLogMsg("INFO", Nil, "ProtheusInsights", "InsightSchedProcess", Nil, "", "InsightSchedProcess: " + STR0003 , Nil, Seconds() - nStart,;     //"Processamento do Insight finalizado"
                {;
                    {"Message", "InsightSchedProcess: " + STR0003 },;   //"Processamento de Insight finalizado"
                    {"MessageId", (cAliasQueue)->I19_UIDMSG};
                })

            (cAliasQueue)->(DbSkip())
       
       EndDo

    Catch oException
        FwLogMsg("ERROR", Nil, "ProtheusInsights", "InsightSchedProcess", Nil, "", "InsightSchedProcess: " + STR0004 , Nil, Seconds() - nStart,;     //"Processamento do insight abortado com exceção"
            {;
                {"Message", "InsightSchedProcess: " + STR0004 },;   //"Processamento do insight abortado com exceção"
                {"CompanyGroup", cCompanyGroup},;
                {"ErrorCode", oException:genCode},;
                {"ErrorMessage", oException:description};
            })

    Finally

        (cAliasQueue)->(DbCloseArea())

        FwLogMsg("INFO", Nil, "ProtheusInsights", "InsightSchedProcess", Nil, "", "InsightSchedProcess: " + STR0005, Nil, Seconds() - nStart,;   //"Execução dos insights agendados finalizado"
            {;
                {"Message", "InsightSchedProcess: " + STR0005 },;   //"Execução dos insights agendados finalizado"
                {"CompanyGroup", cCompanyGroup};
            })

        FreeObj(oException)
        FreeObj(oInsightProcessor)

    EndTry
    
Return
