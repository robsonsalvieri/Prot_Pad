#INCLUDE "tlpp-core.th"
#include "InsightDefs.CH"
#include "backoffice.ba.insights.insightconfig.ch"

NAMESPACE totvs.protheus.backoffice.ba.insights

Class InsightConfig
    
    Public Data cInsight       As Character
    Public Data cVersao        As Character
    Public Data cClasseProc    As Character
    Public Data lPrioritario   As Logical
    Public Data lCalcKey       As Logical
    Public Data lCalcFilter    As Logical
    Public Data lHasError      As Logical
    Public Data cError         As Character
    Public Data cTables        As Character

    Public Method New() Constructor
    Public Method LoadConfig(cInsightName As Character) As Logical
   
EndClass

Method New() Class InsightConfig
Return Self

Method LoadConfig(cInsightName As Character) As Logical Class InsightConfig
    Local oInsightDefinition 
    Local nStart

    nStart := Seconds()

    Try
        oInsightDefinition := pinsObject( Alltrim( cInsightName ) )
        If oInsightDefinition == NIL
            pinsError( INSIGHTSERROR_REGISTER_NOT_FOUND, 'InsightConfig', { STR0001, { cInsightName } } )  //"Configuração de insight [ #1] não localizada."          
        EndIf
            
        oInsightDefinition:Config()
        If !Empty( oInsightDefinition:GetInsightName() )
            ::cInsight      := oInsightDefinition:GetInsightName()
            ::cVersao       := oInsightDefinition:GetVersion()
            ::cClasseProc   := oInsightDefinition:GetProcessClass()
            ::lPrioritario  := oInsightDefinition:GetPriority()
            ::lCalcKey      := oInsightDefinition:GetKey()
            ::lCalcFilter   := oInsightDefinition:GetFilter()
            ::cTables       := oInsightDefinition:GetTables()
            ::lHasError := .F.
        Endif
    Catch oException
        FwLogMsg( "ERROR", Nil, "ProtheusInsights", "InsightConfig",, "", STR0002,, Seconds() - nStart, ;    //"Exceção ocorrida ao pesquisar a configuração do insight."
            {;
                { "Message", STR0002 },;   //"Exceção ocorrida ao pesquisar a configuração do insight."
                { "ErrorCode", oException:genCode },;
                { "ErrorMessage", oException:description };
            })
        ::lHasError := .T.
    EndTry

    FreeObj( oInsightDefinition )
Return !::lHasError
