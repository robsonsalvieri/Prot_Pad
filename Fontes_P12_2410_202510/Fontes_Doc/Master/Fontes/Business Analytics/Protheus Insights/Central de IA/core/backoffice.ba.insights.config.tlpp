#include 'tlpp-core.th'
#include "InsightDefs.CH"

//-------------------------------------------------------------------
/*/{Protheus.doc} PinsConfig
Classe base para o funcionamento das ações de configuração ( Tablea I20 )

@author  Marcia Junko
@since   26/08/2024
/*/
//-------------------------------------------------------------------
Class PinsConfig //from InsightsProcessor
	Public Data cInsightName
	Public Data cProcessClass
	Public Data cModule
	Public Data cVersion
	Public Data cTagKey
	Public Data cTagFilter
	Public Data cTagDateFrom
	Public Data cTagDateTo

	Public Data lHasKey
	Public Data lHasFilter
	Public Data lPriority

	Public Data dLastDate

	Public Data aTables
	Public Data aStruct
	Public Data aFrontFilter

	Public Method New( cName, cModule ) CONSTRUCTOR
	
	Public Method GetInsightName( ) 
	// Public Method SetNewVersion( ) 
	Public Method SetVersion( nVersion ) 
	Public Method GetVersion() 
	Public Method SetHasKey( lHasKey ) 
	Public Method GetHasKey() 
	Public Method SetKey( cTagKey ) 
	Public Method GetKey() 	
	Public Method SetFilter( cTagFilter ) 
	Public Method GetFilter() 	
	Public Method SetTagDateFrom( cTagFrom ) 
	Public Method GetTagDateFrom() 	
	Public Method SetTagDateTo( cTagTo ) 
	Public Method GetTagDateTo() 	
	Public Method SetHasFilter( lHasFilter ) 
	Public Method GetHasFilter() 
	Public Method SetPriority( lPriority ) 
	Public Method GetPriority() 
	Public Method SetTables( aTables ) 
	Public Method GetTables() 
	Public Method SetModule( cModCode ) 
	Public Method GetModule() 
	Public Method SetProcessClass( cProcessClass ) 
	Public Method GetProcessClass() 
	Public Method SetStruct( aStruct ) 
	Public Method GetStruct() 
	Public Method SetFrontFilter( aFrontFilter ) 
	Public Method GetFrontFilter() 
	
	// Public Method ProcessInsight( oInsightMessage )  

	Public Method SaveConfig() 
	Public method Exists(cName)
	Public Method Destroy() 


EndClass

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor.

@param cName, caracter, Nome do insight ou nome a atribuir
@param cModule, caracter, Sigla do módulo.

@Return Self, Instância da Classe. 
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method New( cName, cModule ) Class PinsConfig
	Default cName := ''
	Default cModule := ''

	::cInsightName := cName
	::cModule := cModule
	::cProcessClass := SetProcessClass( cName )
	::cVersion := SetVersion()
	::cTagFilter := ''
	::cTagKey := ''
	::cTagDateFrom := ''
	::cTagDateTo := ''

	::lHasKey := .F.
	::lHasFilter := .F.
	::lPriority := .F.

	::dLastDate := NIL

	::aTables := {}
	::aStruct := {}
	::aFrontFilter := {}

Return Self

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetInsightName
Método para obter o nome do insight

@Return caracter, Nome do insight
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetInsightName( ) Class PinsConfig
Return ::cInsightName 

// Method SetNewVersion( ) Class PinsConfig
// 	Local cLastVersion := ''
// 	Local cNewVersion := ''

// 	cLastVersion := ::GetVersion()
// 	cNewVersion := Soma1( cLastVersion )
// 	::cVersion := cNewVersion
// Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetVersion
Método para setar a versão do insight.

@param nVersion, numeric, Número da versão

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetVersion( nVersion ) Class PinsConfig
	Default nVersion := 1

	::cVersion := SetVersion( nVersion )
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetVersion
Método para obter a versão do insight..

@Return caracter, Código da versão gravada na I20.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetVersion( ) Class PinsConfig
	Local cVersion := ''
	Local aBind := {}
	Local cFieldResult := 'VERSION'
	Local oQuery
	
	aAdd( aBind, {'U', 'MAX( I20_VERSAO ) VERSION' })
	aAdd( aBind, {'S', ::getInsightName() })

	oQuery := usedQuerys( )
	cVersion := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

	FWFreeArray( aBind )
	FreeObj( oQuery )
Return cVersion

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetHasKey
Método para setar a chave do insight

@param lHasKey, boolean, Indica se tem um campo chave.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetHasKey( lHasKey ) Class PinsConfig
	Default lHasKey := .F.

	::lHasKey := lHasKey
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetHasKey
Método para obter a chave do insight.

@Return boolean, Indica se o insight tem um campo chave.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetHasKey( ) Class PinsConfig
	Local aBind := {}
	Local lHasKey := .F.
	Local cFieldResult := 'HASKEY'
	Local oQuery
	
	aAdd( aBind, {'U', 'I20_HASKEY HASKEY' })
	aAdd( aBind, {'S', ::getInsightName() })

	oQuery := usedQuerys( )
	lHasKey := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

	FWFreeArray( aBind )
	FreeObj( oQuery )
Return lHasKey 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetKey
Método para setar a chave do insight

@param cTagKey, caracter, Nome da tag de chave
@param cModule, caracter, Sigla do módulo.

@Return Self, Instância da Classe. 
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetKey( cTagKey ) Class PinsConfig
	Default cTagKey := ''

	If ::lHasKey
		::cTagKey := cTagKey
	EndIf
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetKey
Método para obter a tag de chave

@Return caracter, Tag usada como chave
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetKey( ) Class PinsConfig
Return ::cTagKey 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetFilter
Método para setar a tag de filtro do insight

@param cTagFilter, caracter, tag de filtro a atribuir

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetFilter( cTagFilter ) Class PinsConfig
	Default cTagFilter := ''

	If ::lHasFilter
		::cTagFilter := cTagFilter
	EndIf
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilter
Método para obter a tag de filtro

@Return caracter, tag de filtro do insight
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetFilter() Class PinsConfig
Return ::cTagFilter 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetTagDateFrom
Método para setar a tag de data de

@param cTagFrom, caracter, Nome da tag de data de a atribuir

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetTagDateFrom( cTagFrom ) Class PinsConfig
	Default cTagFrom := ''

	::cTagDateFrom := cTagFrom
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetTagDateFrom
Método para obter a tag de data de

@Return caracter, Nome da tag data de
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetTagDateFrom() Class PinsConfig
Return ::cTagDateFrom 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetTagDateTo
Método para setar a tag de data para

@param cTagFrom, caracter, Nome da tag de data para a atribuir

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetTagDateTo( cTagTo ) Class PinsConfig
	Default cTagTo := ''

	::cTagDateTo := cTagTo
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetTagDateTo
Método para obter a tag de data para

@Return caracter, Nome da tag data para
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetTagDateTo() Class PinsConfig
Return ::cTagDateTo 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetHasFilter
Método para setar se o insight tem filtro

@param lHasFilter, boolean, Indica se o insight tem filtro.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetHasFilter( lHasFilter ) Class PinsConfig
	Default lHasFilter := .F.

	::lHasFilter := lHasFilter
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetHasFilter
Método para obter se o insight tem filtro

@Return boolean, Indica se o insight tem filtro.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetHasFilter( ) Class PinsConfig
	Local aBind := {}
	Local lHasFilter := .F.
	Local cFieldResult := 'HASFIL'
	Local oQuery
	
	aAdd( aBind, {'U', 'I20_HASFIL HASFIL' })
	aAdd( aBind, {'S', ::getInsightName() })

	oQuery := usedQuerys( )
	lHasFilter := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

	FWFreeArray( aBind )
	FreeObj( oQuery )
Return lHasFilter 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetPriority
Método para setar se o insight é prioritário.

@param lPriority, boolean, Indica se o insight deve ser processado como prioritário ou não.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetPriority( lPriority ) Class PinsConfig
	Default lPriority := .F.

	::lPriority := lPriority
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetPriority
Método para obter a prioridade do insight.

@Return boolean, Indica a prioridade do insight.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetPriority( ) Class PinsConfig
	Local aBind := {}
	Local lPriority := .F.
	Local cFieldResult := 'PRIORI'
	Local oQuery
	
	aAdd( aBind, {'U', 'I20_PRIORI PRIORI' })
	aAdd( aBind, {'S', ::getInsightName() })

	oQuery := usedQuerys( )
	lPriority := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

	FWFreeArray( aBind )
	FreeObj( oQuery )
Return lPriority 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetTables
Método para setar as tabelas usadas pelo insight

@param aTables, array, vetor com as tabelas que podem ser utilizadas.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetTables( aTables ) Class PinsConfig
	Default aTables := {}

	::aTables := aTables
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetTables
Método para obter as tabelas do insight..

@Return array, lista de tabelas do insight.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetTables( ) Class PinsConfig
	Local aBind := {}
	Local aTables := {}
	Local cFieldResult := 'TABLES'
	Local oQuery
	
	aAdd( aBind, {'U', 'I20_TABLES TABLES' })
	aAdd( aBind, {'S', ::getInsightName() })

	oQuery := usedQuerys()
	aTables := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

	FWFreeArray( aBind )
	FreeObj( oQuery )
Return aTables 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetModule
Método para setar o módulo do insight.

@param cModule, caracter, Código ou nome do módulo.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetModule( cModule ) Class PinsConfig
	Default cModule := ''

	::cModule := cModule
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetModule
Método para obter a versão do insight..

@Return caracter, Código ou nome do módulo.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetModule( ) Class PinsConfig
Return ::cModule 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetProcessClass
Método para setar o nome da classe de processamento.

@param cProcessClass, caracter, Nome da classe de processamento.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetProcessClass( cProcessClass ) Class PinsConfig
	Default cProcessClass := ::cInsightName + 'Process'

	::cProcessClass := SetProcessClass( cProcessClass )
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetProcessClass
Método para obter o nome da classe de processamento.

@Return caracter, nome da classe de processamento.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetProcessClass( ) Class PinsConfig
Return ::cProcessClass

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetStruct
Método para setar a estrutura do insight para a tabela temporária.

@param aStruct, array, vetor com a estrutura 

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetStruct( aStruct ) Class PinsConfig
	Default aStruct := {}

	::aStruct := aStruct
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetStruct
Método para obter a estrutura para tabela temporária.

@Return array, estrutura para tabela temporária.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetStruct( ) Class PinsConfig
Return ::aStruct 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SetFrontFilter
Método para setar os campos usados como filtro pelo front.

@param aFrontFilter, array, vetor com os campos para filtro.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SetFrontFilter( aFrontFilter ) Class PinsConfig
	Default aFrontFilter := {}

	::aFrontFilter := aFrontFilter
Return 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} GetFrontFilter
Método para obter os campos para filtro no front.

@Return array, vetor com os campos para filtro no front.
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method GetFrontFilter( ) Class PinsConfig
Return ::aFrontFilter 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} SaveConfig
Método para salvar as configurações do insight na tabela I20.

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Method SaveConfig() Class PinsConfig
	Local aSvArea := GetArea()
	Local cVersion := ''
	Local cInsight := ''
	Local nTamName := 0
	Local lDisable := .F.
	Local lInsert := .F.
	
	DBSelectArea( "I20" )

	nTamName := 30	// FWSX3Util():GetFieldStruct( 'I20_INSIGT' )[3]
	cInsight := Padr( ::cInsightName, nTamName )

	cVersion := ::GetVersion()
	
	lInsert := Empty( cVersion ) .Or. ( cVersion != ::cVersion )  
	lDisable := !Empty( cVersion ) .And. ( cVersion != ::cVersion )

	If lDisable
		disableConfig( cInsight )
	EndIf

	If lInsert
		RecLock( "I20", .T. )
			I20->I20_INSIGT := ::cInsightName
			I20->I20_CLSPRC := ::cProcessClass
			I20->I20_VERSAO := ::cVersion
			I20->I20_HASKEY := ::lHasKey
			I20->I20_HASFIL := ::lHasFilter
			I20->I20_STATUS := CONFIG_ENABLE	//1-Ativo
			I20->I20_DTCREA := DTOS( Date() )
			I20->I20_PRIORI := ::lPriority
			I20->I20_TABLES := ArrTokStr( ::aTables, ',' )
		I20->( MSUnlock() )
	EndIf
	RestArea( aSvArea )

	FWFreeArray( aSVArea )
Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} Exists
Método que avalia se a configuração do insight existe.
@param cName, caracter, nome do insight para busca

@Return logical, Indica se a configuração existe ou não na tabela.
@author  Marcia Junko
@since   16/01/2025
/*/
//-------------------------------------------------------------------------------------
Method Exists( cName ) Class PinsConfig
    Local cClassProc := ''
    Local aBind := {}
    Local cFieldResult := 'CLASSPROC'
    Local oQuery
    
    aAdd( aBind, { 'U', 'I20_CLSPRC CLASSPROC' } )
    aAdd( aBind, { 'S', cName } )

    oQuery := usedQuerys( )
    cClassProc := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind, cFieldResult )

    FWFreeArray( aBind )
    FreeObj( oQuery )
Return ( !Empty( cClassProc ) )

//-------------------------------------------------------------------
/*/{Protheus.doc} Destroy
Destroi o objeto e libera a memória alocada. 

@author  Marcia Junko
@since   26/08/2024
/*/
//-------------------------------------------------------------------
Method Destroy( ) Class PinsConfig
	ASize( ::aTables, 0 )
	ASize( ::aStruct, 0 )
	ASize( ::aFrontFilter, 0 )

	::cInsightName := nil
	::cModule := nil
	::cProcessClass := nil
	::cVersion := nil
	::cTagFilter := nil
	::cTagKey := nil
	::cTagDateFrom := nil
	::cTagDateTo := nil

	::lHasKey := .F.
	::lHasFilter := .F.
	::lPriority := .F.

	::dLastDate := NIL
Return NIL

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} ProcessInsight
Executa os processamentos padrões.

@param oInsightMessage, object, Objeto da mensagem

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
// Method ProcessInsight( oInsightMessage ) Class PinsConfig
// Return _Super:ProcessInsight( oInsightMessage )

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} usedQuerys
Função responsável por retornar uma consulta pré-formatada da tabela de configuração.

@param cName, caracter, Nome do insight ou nome a atribuir

@return object, Nome da classe de processamento
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Static Function usedQuerys( cName )
    Local cQuery := ""
    Local oExec

	Default cName := ''

    cQuery := "SELECT ? " + ;
    " FROM " + RetSqlName( "I20" )  + ;
    " WHERE I20_INSIGT = ? " + ;
		" AND I20_STATUS = '1' " + ;
        " AND D_E_L_E_T_ = ' ' "

    cQuery := ChangeQuery( cQuery )
	oExec := FwExecStatement():New( cQuery )
Return oExec

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} disableConfig
Função responsável por tornar inativas as configurações de um determinado insight.

@param cName, caracter, Nome do insight ou nome a atribuir

@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Static Function disableConfig( cInsight )
	Local aSvArea := GetArea()
	Local aBind := {}
	Local oQuery
	
	aAdd( aBind, {'U', 'R_E_C_N_O_ ID' })
	aAdd( aBind, {'S', cInsight })

	oQuery := usedQuerys()

	cAlias := totvs.protheus.backoffice.ba.insights.util.execQuery( oQuery, aBind )

	While ( cAlias )->( !Eof() )
		I20->( DBGoto( ( cAlias )->ID ) )

		RecLock( 'I20', .F. )
			I20->I20_STATUS := CONFIG_DISABLE	//0-Inativo
			I20->I20_DTALT := DTOS( Date() )
		I20->( MSUnlock() )

		( cAlias )->( DBSkip() )
	End
	( cAlias )->( DBCloseArea() )

	RestArea( aSvArea )

	FWFreeArray( aSvArea )
	FWFreeArray( aBind )
	FreeObj( oQuery )
Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} setProcessClass
Função responsável por setar o nome da classe de processamento.

@param cName, caracter, Nome do insight ou nome a atribuir

@return caracter, Nome da classe de processamento
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Static Function setProcessClass( cName )
Return cName + 'Process'

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} setVersion
Função responsável por setar a versão do insight.

@param nVersion, numeric, Número da versão do insight.

@return caracter, Versão do insight
@author  Marcia Junko
@since   02/09/2024
/*/
//-------------------------------------------------------------------------------------
Static Function setVersion( nVersion ) 
	Local nTamVersion := 6	//FWSX3Util():GetFieldStruct( 'I20_VERSAO' )[3]

	Default nVersion := 1

Return StrZero( nVersion, nTamVersion )

