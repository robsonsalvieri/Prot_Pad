#include "tlpp-core.th"
#include "tlpp-rest.th"

NAMESPACE totvs.protheus.backoffice.ba.insights.sales
USING NAMESPACE totvs.protheus.backoffice.ba.insights

//-------------------------------------------------------------------
/*/{Protheus.doc} getGraphs
EndPoint Get retorna os insights gráficos do faturamento

@author Marcia Junko
@since 06/10/2024
/*/
//-------------------------------------------------------------------
@Get(endpoint="api/ba/v1/insights/sales/opportunities/graphs")
Function getGraphs()
	Local aArea := GetArea()	  		As Array
	Local oJParm      					As Json
	Local cTableName     				As Character
	Local oJsRet := JsonObject():new()  As Object
	Local cBranch						As Character
	Local cUserID						As Character

	oJParm := oRest:getQueryRequest()

	cTableName := Iif( oJParm:HasProperty( "tabTmpName" ), oJParm[ "tabTmpName" ], "" )
	cUserID := Iif( oJParm:HasProperty( "codUser" ), oJParm[ "codUser" ], "" )
	nRecords  := Iif( oJParm:HasProperty( "records" ) .And. Isnumeric( oJParm[ "records" ] ), Val( oJParm[ "records" ] ), 5 )
	
  	oJsRet[ "success" ] := .F.
	oJsRet[ "items" ]   := JsonObject():New()
	oRest:setStatusCode( 400 )

	If !Empty( cTableName ) .And. !Empty( cUserID )
		cBranch := totvs.protheus.backoffice.ba.insights.pinsBranchUser( cUserId, {"SC5", "SC6"} )

		oJsRet[ "items" ][ "customersByPotential" ] := customersByPotential( cTableName, cBranch, nRecords )  
		oJsRet[ "items" ][ "productsByLargestStock" ] := productsByLargestStock( cTableName, cBranch, nRecords )  

	  	oRest:setStatusCode( 200 )
	EndIf

	oRest:setResponse( oJsRet )

	RestArea( aArea )

	FWFreeArray( aArea )
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} customersByPotential
Retorna os valores distintos de um campo da tabela temporaria e monta o json de resposta.

@param cTableName, caracter, nome da tabela para pesquisa das informações
@param cBranch, caracter, lista de filiais do usuário.
@param nRecords, number, quantidade de registros a retornar.

@return array, Vetor com os itens a apresentar no response.
@author Marcia Junko
@since 06/10/2024
/*/
//-------------------------------------------------------------------
Static Function customersByPotential( cTableName, cBranch, nRecords )  
	Local aResult := {}
	Local aProperties := {}
	Local aQuery := {}
	Local cFields := ''
	Local cWhere := ''
	Local cGroup := ''
	Local cOrder := ''

	Default nRecords := 5

	cFields := 'CUSTOMER, CUST_NAM, SUM(POT_VL) AS POT_VL'
	If !Empty( cBranch )
		cWhere := 'WHERE BRANCH IN ( '+ cBranch + ') '
	EndIf
	cGroup := 'GROUP BY CUSTOMER, CUST_NAM'
	cOrder := 'ORDER BY POT_VL DESC'

	aQuery := { cFields, cTableName, cWhere, cGroup, cOrder  }
	aAdd( aProperties, { "customer", "C" } )
	aAdd( aProperties, { "cust_nam", "C" } )
	aAdd( aProperties, { "pot_vl", "N" } )

	aResult := makeResult( aQuery, aProperties, nRecords ) 

	FWFreeArray( aProperties )
	FWFreeArray( aQuery )
Return aResult

//-------------------------------------------------------------------
/*/{Protheus.doc} productsByLargestStock
Retorna os produtos com maior estoque

@param cTableName, caracter, nome da tabela para pesquisa das informações
@param cBranch, caracter, lista de filiais do usuário.
@param nRecords, number, quantidade de registros a retornar.

@return array, Vetor com os itens a apresentar no response.
@author Marcia Junko
@since 06/10/2024
/*/
//-------------------------------------------------------------------
Static Function productsByLargestStock( cTableName, cBranch, nRecords )  
	Local aResult := {}
	Local aProperties := {}
	Local aQuery := {}
	Local cFields := ''
	Local cWhere := ''
	Local cGroup := ''
	Local cOrder := ''

	Default nRecords := 5

	cFields := 'DISTINCT PRODUCT, DESCRIP, QUANTITY, STCK_QTT'
	If !Empty( cBranch )
		cWhere := 'WHERE BRANCH IN ( '+ cBranch + ') '
	EndIf
	cOrder := 'ORDER BY STCK_QTT DESC'
	
	aQuery := { cFields, cTableName, cWhere, cGroup, cOrder  }
	aAdd( aProperties, { "product", "C" } )
	aAdd( aProperties, { "descrip", "C" } )
	aAdd( aProperties, { "quantity", "N" } )
	aAdd( aProperties, { "stck_qtt", "N" } )

	aResult := makeResult( aQuery, aProperties, nRecords ) 

	FWFreeArray( aProperties )
	FWFreeArray( aQuery )
Return aResult

//-------------------------------------------------------------------
/*/{Protheus.doc} MakeResult
Retorna os clientes com os maiores valores de venda

@param cQuery, caracter, consulta a ser realizada
@param cTableName, caracter, nome da tabela para pesquisa das informações
@param aProperties, array, vetor com as propriedades de retorno
@param nRecords, number, quantidade de registros a retornar.

@return array, Vetor com os itens a apresentar no response.
@author Marcia Junko
@since 06/10/2024
/*/
//-------------------------------------------------------------------
Static Function makeResult( aQuery, aProperties, nRecords ) 
	Local aArea := GetArea()
	Local aResult := {}
	Local cAlias := ''
	Local cQuery := ''
	Local nCount := 0
	Local nI := 0
	Local xValue
	Local oQuery
	Local oJItem

	Default nRecords := 5

	cQuery := ' SELECT ? '
	cQuery += " FROM ? "
	cQuery += " ? "	// where
	cQuery += " ? " // Group by
	cQuery += " ? " // order by
	
	oQuery := FWExecStatement():New( cQuery )
	oQuery:setUnsafe( 1, aQuery[ 1 ] )
	oQuery:setUnsafe( 2, aQuery[ 2 ] )
	oQuery:setUnsafe( 3, aQuery[ 3 ] )
	oQuery:setUnsafe( 4, aQuery[ 4 ] )
	oQuery:setUnsafe( 5, aQuery[ 5 ] )
	cAlias := MPSysOpenQuery( oQuery:getFixQuery() )

	While ( cAlias )->( !Eof() ) .And.  nRecords > nCount
		nCount++

		oJItem := JsonObject():new()
		For nI := 1 to len( aProperties )
			xValue := ( cAlias )->&( aProperties[ nI] [ 1 ] )
			If aProperties[ nI] [ 2 ] == "C"
				xValue := Alltrim( xValue )
			EndIf
			oJItem[ aProperties[ nI] [ 1 ] ] := xValue
		Next
		
		Aadd( aResult, oJItem )

		( cAlias )->( DbSkip() )
	EndDo
	( cAlias) ->( DbCloseArea() )

	RestArea( aArea )

	FWFreeArray( aArea )
	FreeObj( oQuery )
	FreeObj( oJItem )
Return aResult
