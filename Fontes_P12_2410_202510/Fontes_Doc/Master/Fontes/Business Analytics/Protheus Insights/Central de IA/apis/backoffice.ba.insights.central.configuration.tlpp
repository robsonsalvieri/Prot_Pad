#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'protheus.ch'
#include "backoffice.ba.insights.central.configuration.ch"

//-------------------------- backoffice.ba.insights.central.configuration --------------------------------
// Funções para gerenciamento de configurações do Protheus Insight
//--------------------------------------------------------------------------------------------------------

#DEFINE NAMESIZE TamSX3( "I1A_NAME" )[ 1 ]
#DEFINE STORAGE             PadR( "storageIA", NAMESIZE )               //Configuração de armazéns 
#DEFINE FIN_PURPOSE         PadR( "financialPurpose", NAMESIZE )        //Configuração de Motivo de Baixa
#DEFINE FIN_DOCUMENT_TYPE   PadR( "financialDocumentType", NAMESIZE )   //Configuração de Tipo de Documento
#DEFINE FIN_NATURES         PadR( "financialNatures", NAMESIZE )        //Configuração de Tipo de Natureza

//-------------------------------------------------------------------
/*{Protheus.doc} getCentralConfig

Serviço de retorno das informações usadas nas configurações usadas 
pela Central de IA e registradas na tabela I1A conforme o tipo 
informado

@author  Luiz Junior
@since   04/09/2025

*/
//-------------------------------------------------------------------

@Get(endpoint="api/ba/v1/central/configuration")
Function getCentralConfig() 
	Local nPage As Integer
	Local nPageSize As Integer
	Local cType As Character
    Local cSearch As Character
    Local cResPonse As Character
    Local oAdapter As Object
    Local oResponse As Object
	Local oJsParams As Json	

	oJsParams := oRest:GetQueryRequest()
	
	nPage     		:= Iif( oJsParams:HasProperty( "page" ), oJsParams[ "page" ], 1 )
	nPageSize 		:= Iif( oJsParams:HasProperty( "pageSize" ), oJsParams[ "pageSize" ], 20 ) 
	cType			:= oJsParams[ "type" ]
    cSearch			:= Iif( oJsParams:HasProperty( "search") ,oJsParams[ "search" ],"")

    oAdapter := FWAdapterBaseV2():New( "GET", .T. )
    oAdapter:setpage( nPage ) 
    oAdapter:setPageSize( nPageSize )
        
    addMapFields( @oAdapter, cType )

    oAdapter:SetQuery( getQuery( cType ) )
    oAdapter:SetWhere( getWhere( cType, cSearch ) )
    setOrder( @oAdapter, cType )
    oAdapter:setIsCaseSensitive( .T. )
        
    If oAdapter:Execute()
        oAdapter:FillGetResponse()
        If oAdapter:lOk
            cResPonse := oAdapter:GetJSONResponse()

            oResponse := JsonObject():New()
            oRest:setResponse( makeResponse( oAdapter ) )
        Else
            UserException( oAdapter:cMessage )
        EndIf
    Else
        UserException( oAdapter:cMessage )
    EndIf

    FwFreeObj( oJsParams )
    FwFreeObj( oAdapter )
    FwFreeObj( oResponse )
Return 

//-------------------------------------------------------------------
/*{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@param oAdapter, object, Objeto da classe instanciado.
@param cType, character, Determina qual o tipo de configuração da api deve processar

@author  Luiz Junior
@since   04/09/2025

*/
//-------------------------------------------------------------------
Static Function addMapFields( oAdapter, cType )
    Local cBranch := "" As Character
    Local cCode := "" As character
    Local cDescription := "" As Character
    Local jDefinition As Json

    Default cType := "warehouse"

    jDefinition := getDefinitions( cType )

    cBranch := searchDefinition( jDefinition, 'branchField' )
    cCode := searchDefinition( jDefinition, 'codeField' )
    cDescription := searchDefinition( jDefinition, 'descField' )

    oAdapter:addMapFields( "isSelected"  , "isSelected"  , .T., .F., { "isSelected" , "L", 1, 0, "isSelected" }, "isSelected" )      
    oAdapter:AddMapFields( "branch" , cBranch , .T., .T. , { cBranch, "C", TamSx3( cBranch )[ 1 ], 0, STR0002 }, cBranch )  //"Filial"
    oAdapter:AddMapFields( "code" , cCode , .T., .T., {  cCode, "C", TamSx3( cCode )[ 1 ], 0, STR0003 }, cCode        )     //"Código"
    oAdapter:AddMapFields( "description" , cDescription , .T., .T., {  cDescription, "C", TamSx3( cDescription )[ 1 ], 0, STR0004 }, cDescription )     //"Descrição"

    FreeObj( jDefinition )
Return

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Realiza a query para busca de informações

@param cType, determina qual o tipo de configuração da api deve processar

@author  Luiz Junior
@since   04/09/2025
*/
//-------------------------------------------------------------------
Static Function getQuery( cType ) 
    Local cQuery As Character
    Local cTable As Character
    Local cQueryFields As Character
    Local cBranch As Character
    Local cCode As Character
    Local jDefinition As Json

    Default cType := "warehouse"

    jDefinition  := getDefinitions( cType )
    cTable       := searchDefinition( jDefinition, 'table' )
    cQueryFields := searchDefinition( jDefinition, 'queryFields' )
    cBranch      := searchDefinition( jDefinition, 'branchField' )
    cCode        := searchDefinition( jDefinition, 'codeField' )

    cQuery := "SELECT #QueryFields# FROM ( " + ;
                " SELECT " + ;
                    " CASE WHEN I1A.I1A_BRANCH IS NOT NULL THEN 'T' ELSE 'F' END AS isSelected, " + ;
                    " " + cQueryFields + ", " + ;
                    " I1A.I1A_NAME " + ;
                " FROM " + RetSqlName( cTable ) + " " + cTable + " " + ;
                " LEFT JOIN " + RetSqlName( "I1A ") + " I1A " + ;
                    " ON I1A.I1A_BRANCH = " + cTable + "." + cBranch + " " + ;
                    " AND TRIM( I1A.I1A_VALUE ) = TRIM( " + cTable + "." + cCode + " ) " + ;
                    " #QueryWhere# "                

    FreeObj( jDefinition )
    
Return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} getWhere

Monta o Where para a query da API

@param cType, determina qual o tipo de configuração da api deve processar
@param cSearch, contem dado para pesquisa na query, pode ser codigo ou filial.

@author  Luiz Junior
@since   04/09/2025
*/
//-------------------------------------------------------------------
Static Function getWhere( cType, cSearch )
    Local cWhere As Character
    Local cBranch As Character
    Local cCode As Character
    Local cDescription As Character
    Local cName As Character
    Local cTable As Character
    Local cCondition As Character
    Local jDefinition As Json

    Default cType := "warehouse"
    Default cSearch := ""

    jDefinition := getDefinitions( cType )
    cName        := searchDefinition( jDefinition, 'name' )
    cTable       := searchDefinition( jDefinition, 'table' )
    cBranch      := searchDefinition( jDefinition, 'branchField' )
    cCode        := searchDefinition( jDefinition, 'codeField' )
    cDescription := searchDefinition( jDefinition, 'descField' )
    cCondition   := searchDefinition( jDefinition, 'condition' )

    cWhere := " AND I1A.I1A_NAME = '" + cName + "' " + ;
        " AND I1A.D_E_L_E_T_ = ' ' " + ;
        " WHERE " + cCondition + " " 
    
    If !Empty( cSearch )
        cSearch := UPPER( AllTrim( cSearch ) )
        cWhere += " ( UPPER( " + cTable + "." + cBranch + " ) LIKE '%" + cSearch + "%' OR " + ;
            " UPPER( " + cTable + "." + cCode + " ) LIKE '%" + cSearch + "%' OR " + ;
            " UPPER( " + cTable + "." + cDescription + " ) LIKE '%" + cSearch + "%' ) AND "
    EndIf
    
    cWhere += " " + cTable + ".D_E_L_E_T_ = ' ' " + ;
        " ) AS CONFIG "        

    FreeObj( jDefinition )
Return cWhere

//-------------------------------------------------------------------
/*{Protheus.doc} postCentralConfig

Função responsável por gravar as configurações na tabela I1A.

@author  Luiz Junior
@since   04/09/2025
*/
//-------------------------------------------------------------------
@Post(endpoint="api/ba/v1/central/configuration")
Function postCentralConfig()
     Local oJsParams As Json
     Local jBody     As Character
     Local cType     As Character
    
     oJsParams := oRest:GetQueryRequest()
     cType     := oJsParams[ "type" ]
     
     jBody := JsonObject():New()

     jBody:fromJson( oRest:GetBodyRequest() )

    If postReceipDetail( cType, jBody )            
        oRest:SetResponse('{"message": "' + STR0001 + '"}' , "application/json" , 200 )	//"Configurações salvas"
    EndIf
 return
//-------------------------------------------------------------------
/*{Protheus.doc} postReceipDetail
Funcao responsavel por checar itens selecionados e gravar dados.

@param cType, tipo da configuração
@param jBody, objeto json

@author  Luiz Junior
@since   04/09/2025
*/
//-------------------------------------------------------------------
Static Function postReceipDetail( cType, jBody )
    Local aItems    := {}              As Array
    Local aData     := {}              As Array
    Local cCode     := ""              As Character
    Local cBranch   := ""              As Character
    Local cName     := ""              As Character
    Local cField    := ""              As Character
    Local nLine     := 0               As Integer
    Local lSelected := .F.             As Logical 
    Local nBranchSize As Integer 
    Local nCodeSize   As Integer
    Local jDefinition As Json

    jDefinition := getDefinitions( cType )
    cName      := searchDefinition( jDefinition, 'name' )
    cField      := searchDefinition( jDefinition, 'branchField' )
    nBranchSize := TamSX3( cField )[ 1 ]

    cField      := searchDefinition( jDefinition, 'codeField' )
    nCodeSize   := TamSX3( cField )[ 1 ]

    aData := jBody[ "items" ]
    For nLine := 1 To Len( aData )
        lSelected := aData[ nLine ][ "isSelected" ] 
        cBranch   := PADR( aData[ nLine ][ "branch" ], nBranchSize )
        cCode     := PADR( aData[ nLine ][ "code" ], nCodeSize )
          
        AAdd( aItems, { lSelected, cBranch, cCode, "" } )

    Next nLine

    If Len( aItems ) > 0
        pinsSaveI1A( cName, aItems )
    EndIf

    FwFreeArray( aItems )	
    FwFreeArray( aData )
    FreeObj( jDefinition )	
Return .T.

//-------------------------------------------------------------------
/*/{Protheus.doc} makeResponse
Função que prepara as informações necessárias para serem utilizadas no retorno do Get

@param oAdapter, object, Instacia do Adapter

@return cJson, Retorna String Json para que sera utilizada no response
@author  Luiz Junior
@since   09/09/2025
/*/
//-------------------------------------------------------------------
Static Function makeResponse( oAdapter )
    Local cJson  AS CHARACTER
    Local ojResp AS JSON

    ojResp := JsonObject():New()

    ojResp:FromJSON( oAdapter:getJSONResponse() )
    
    ojResp[ "header" ] := aClone( getHeader( @oAdapter ) )

    cJson := ojResp:ToJson()

    FreeObj( ojResp )
Return cJson

//-------------------------------------------------------------------
/*/{Protheus.doc} getHeader
Função que prepara as informações necessárias para serem utilizadas no objeto Header 

@param oInsAdapter, object, objeto do adapter

@return aHeader, array, Retorna o header pronto para ser atribuido ao response
@author Luiz Junior
@since 09/09/2025
//-------------------------------------------------------------------
/*/
Static Function getHeader(oAdapter) 
	Local aHeader := {} As Array
	Local aHeaderConfig := {}
	Local oHeaderItem As Json
    Local nI := 0 As Integer

    aHeaderConfig := oAdapter:OjSonObj:aMapfields

    For nI := 1 To Len( aHeaderConfig )
        makeHeaderItem( @aHeader, aHeaderConfig[ nI ] )
    Next nI

	FwFreeArray( aHeaderConfig ) 
	FreeObj( oHeaderItem )
Return aHeader

//-------------------------------------------------------------------
/*/{Protheus.doc} makeHeaderItem
Alimenta o array de Header do endpoint

@param @aHeader, array, vetor com a estrutura do header do endpoint
@param aField, array, vetor com as informações do campo

@author  Luiz Junior
@since   09/09/2025
/*/
//-------------------------------------------------------------------
Static Function makeHeaderItem( aHeader, aField )
	Local oHeaderItem As Json
   
    oHeaderItem := JsonObject():New()

    oHeaderItem[ 'property' ]   := aField[ 1 ]
    oHeaderItem[ 'label' ]      := cvaltochar( aField[ 5 ][ 5 ] )
    oHeaderItem[ 'field_name' ] := aField[ 6 ]    
    oHeaderItem[ 'showFilter' ] := .T.

    AAdd( aHeader, oHeaderItem )

	FreeObj( oHeaderItem )
Return

//-------------------------------------------------------------------
/*{Protheus.doc} setOrder
Faz o set da condição padrão utilizada para ordenação do ResultSet

@param oAdapter, objeto da classe instanciado.
@param cType, tipo da configuração

@author  Luiz Junior
@since   15/09/2025

*/
//-------------------------------------------------------------------
Static Function setOrder( oAdapter, cType )
    Local cOrder As Character
    Local jDefinition As Json

    jDefinition := getDefinitions( cType )
    cOrder := searchDefinition( jDefinition, 'order' )

    oAdapter:SetOrder( cOrder )
    
    FreeObj( jDefinition )
Return


//-------------------------------------------------------------------
/*{Protheus.doc} getDefinitions
Cria o objeto Json com todas as definições necessárias para uso deste fonte
a partir do tipo informado.

@param cType, determina qual o tipo de configuração da api deve processar

@return json, Objeto com as propriedades por tipo de configuração
@author  Marcia Junko
@since   07/10/2025
*/
//-------------------------------------------------------------------
Static Function getDefinitions( cType )
    Local cName        As Character
    Local cTable       As Character
    Local cBranch      As Character
    Local cCode        As Character
    Local cDescription As Character
    Local cQueryFields As Character
    Local cCondition   As Character
    Local cOrder       As Character
    Local jDefinition As Json

    jDefinition := JsonObject():New()

    Do Case
        Case cType == "warehouse"   //Configuração de armazéns 
            cName        := STORAGE
            cTable       := "NNR"
            cBranch      := "NNR_FILIAL"
            cCode        := "NNR_CODIGO"
            cDescription := "NNR_DESCRI"
            cQueryFields := "NNR.NNR_FILIAL, NNR.NNR_CODIGO, NNR.NNR_DESCRI"
            cOrder       := "NNR_FILIAL, NNR_CODIGO"
        Case cType == "finPurpose"  //Configuração de Motivo de Baixa
            cName        := FIN_PURPOSE
            cTable       := "F7G"
            cBranch      := "F7G_FILIAL"
            cCode        := "F7G_SIGLA"
            cDescription := "F7G_DESCRI"
            cQueryFields := "F7G.F7G_FILIAL, F7G.F7G_SIGLA, F7G.F7G_DESCRI"
            cOrder       := "F7G_FILIAL, F7G_SIGLA" 
        Case cType == "finDocType"  //Configuração de Tipo de Documento
            cName        := FIN_DOCUMENT_TYPE
            cTable       := "SX5"
            cBranch      := "X5_FILIAL"
            cCode        := "X5_CHAVE"
            cDescription := "X5_DESCRI"
            cQueryFields := "SX5.X5_FILIAL, SX5.X5_CHAVE, SX5.X5_DESCRI"
            cCondition   := "SX5.X5_TABELA = '05' AND "
            cOrder       := "X5_FILIAL, X5_CHAVE" 
        Case cType == "finNatures"  //Configuração de Tipo de Natureza
            cName        := FIN_NATURES
            cTable       := "SED"
            cBranch      := "ED_FILIAL"
            cCode        := "ED_CODIGO"
            cDescription := "ED_DESCRIC"
            cQueryFields := "SED.ED_FILIAL, SED.ED_CODIGO, SED.ED_DESCRIC"
            cOrder       := "ED_FILIAL, ED_CODIGO" 
    EndCase 

    jDefinition[ 'name' ]           := Alltrim( cName )
    jDefinition[ 'table' ]          := cTable
    jDefinition[ 'branchField' ]    := cBranch
    jDefinition[ 'codeField' ]      := cCode   
    jDefinition[ 'descField' ]      := cDescription  
    jDefinition[ 'queryFields' ]    := cQueryFields  
    jDefinition[ 'condition' ]      := cCondition  
    jDefinition[ 'order' ]          := cOrder  
Return jDefinition

//-------------------------------------------------------------------
/*{Protheus.doc} searchDefinition
Pesquisa no objeto e retorna uma determinada definição de acordo com o tipo.

@param jObject, json, Objeto json com as definições 
@param cName, character, indica o nome da propriedade para pesquisa.

@return variant, Retorna o valor da propriedade específica de acordo com o tipo.
@author  Marcia Junko
@since   07/10/2025
*/
//-------------------------------------------------------------------
Static Function searchDefinition( jObject, cName )
    Local xValue As Variant

    jObject:GetJsonValue( cName, @xValue )

Return xValue
