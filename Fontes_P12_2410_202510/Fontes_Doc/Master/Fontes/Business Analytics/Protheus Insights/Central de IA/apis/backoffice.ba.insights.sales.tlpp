#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'protheus.ch'
#include 'BACKOFFICE.BA.INSIGHTS.SALES.CH'

NAMESPACE totvs.protheus.backoffice.ba.insights.sales

/*/{Protheus.doc} GetSalesRecommendation
  EndPoint Get retorna os insights de Alert

  @return json, objeto JSON de retorno do endpoint
  @author Raphael.Santana
  @since 02/08/2024
/*/
@Get(endpoint="api/ba/v1/insights/sales/salesRecommendation")
Function getSalesRecommendation()

	Local oJsGet     as Object
	Local oJsRet     as Object
	Local oJson      as Object
	Local oPrepare   as Object
	Local cTmpAlias  as Character
	Local cCondition as Character
	Local cNextAlias as Character
	Local cOrder     as Character
	Local cOrderSort as Character
	Local cCodUsr    as Character
	Local cBranch    as Character
	Local cQryBranch as Character
	Local cWhere	 as Character
	Local cPropJs	 as Character
	Local cStatus 	 as Character
	Local nPage      as Numeric
	Local nPageSize  as Numeric
	Local nLinhaIni  as Numeric
	Local nLinhaFim  as Numeric
	Local nX		 as Numeric
	Local aCpoTab    as Array
	Local aStruCPO	 as Array
	Local aHasNext   as Array
	Local aArea		 as Array

	oJsGet         := oRest:GetQueryRequest()
	oJsRet         := JsonObject():New()
	oJson          := JsonObject():New()
	oPrepare       := Nil
	cNextAlias     := ""
	cWhere         := ""
	cPropJs        := "salesRecommendation"
	cQryBranch     := ""
	cStatus        := ""
	cfilterByField := ""
	nLinhaIni      := 0
	nLinhaFim      := 400
	nX             := 0
	nPage   	   := 0
	nPageSize      := 10
	aCpoTab        := {}
	aStruCPO       := {}
	aHasNext	   := {}
	aArea  		   := GetArea()
	aCpoTab        := PINC010Fld()

	cTmpAlias  := oJsGet[ 'tabTmpName' ]
	cCodUsr    := oJsGet[ 'codUser' ]
	cBranch    := oJsGet[ 'branch' ]
	cCondition := oJsGet[ 'filter' ]
	cOrder     := oJsGet[ 'order' ]
	cOrderSort := oJsGet[ 'orderSort' ]

	If oJsGet:hasProperty( 'page' )
		nPage      := Val(oJsGet[ 'page' ])
	EndIf

	If oJsGet:hasProperty( 'pageSize' )
		nPageSize  := Val(oJsGet[ 'pageSize' ])
	EndIf

	If oJsGet:hasProperty( 'status' )
		cStatus    := oJsGet[ 'status' ]
	EndIf

	If oJsGet:hasProperty( 'filterByField' )
		cfilterByField := oJsGet[ 'filterByField' ]
	EndIf

	For nX:=1 To Len(aCpoTab)
		Aadd(aStruCPO, {aCpoTab[nX][2][1], aCpoTab[nX][2][2], aCpoTab[nX][2][3], aCpoTab[nX][2][4]})
	Next nI

	//Validação de propriedades obrigatórias que são enviadas pelo front
	If !Empty(cTmpAlias) .AND. !Empty(cCodUsr) .AND. !Empty(cBranch)

		//Verifica se a execução é Mock
		If cTmpAlias != "mockTab"

			//Verifica se o usuário tem limitação de acesso por filial
			cQryBranch := totvs.protheus.backoffice.ba.insights.pinsBranchUser( cCodUsr, {"SC5", "SC6"} )

			//Calcula a lilnha inicial e final que será usada na query para paginação
			If !Empty(nPage) .AND. !Empty(nPageSize)
				nLinhaIni := (( nPage - 1 ) * nPageSize ) + 1
				nLinhaFim := nPage * nPageSize
			EndIf

			//Monta a string de condições usada na query
			cWhere := SalesMakeWhere(cQryBranch, cStatus, cfilterByField)

			//Monta a query base do select na tabela temporária
			oPrepare := PINSMakePage(cTmpAlias, cWhere, cOrder, cOrderSort, cCodUsr, acpotab)

			//Seta a linha inicial e final referente a paginação
			oPrepare:setNumeric( 1, nLinhaIni )
			oPrepare:setNumeric( 2, nLinhaFim )

			Try
				//Executa a query e abre área da tabela com resultado da query
				cNextAlias := MPSysOpenQuery( oPrepare:GetFixQuery(), , aStruCPO )

				//Verifica se área está aberta e fecha
				If Select(cTmpAlias) == 0
					//Abre a área com a tabela temporária
					dbUseArea( .T.,"TOPCONN",cTmpAlias,cTmpAlias,.T.,.T.)
				EndIf

				//Inicia o json para receber um array
				oJsRet[cPropJs] := {}

				// Itera sobre todos os registros com o mesmo I14_MESSID
				While (cNextAlias)->(!Eof())

					//Posiciona no recno da tabela temporária
					(cTmpAlias)->(DbGoTo((cNextAlias)->R_E_C_N_O_))

					//Chama função que monta a estrutura do json com base no registro da tab temporaria posicionado e na estrutura de campos
					oJson := salesJsonLine( cTmpAlias, acpotab )

					//Incrementa o json de retorno
					aadd( oJsRet[ cPropJs ], oJson )

					(cNextAlias)->(DbSkip())
				EndDo

				//Fecha as áreas temporárias abertas
				(cTmpAlias)->(DbCloseArea())
				(cNextAlias)->(DbCloseArea())

				aHasNext := HasNext(cTmpAlias, nLinhaFim, cWhere)

				//Alimenta a estrutura do json de retorno
				oJsRet["success"]  := .T.
				oJsRet["hasNext"]  := aHasNext[1]
				oJsRet["page"]     := nPage
				oJsRet["pageSize"] := nPageSize
				oJsRet["total"]    := aHasNext[2]
				oJsRet["isMock"]   := .F.

			Catch oException

				//Tratamento de erros na executação
				oJsRet := JsonObject():New()
				oJsRet["success"] := .F.
				oJsRet["return"] := STR0001 //Falha ao carregar dados
				oRest:setStatusCode(500)

			EndTry
		EndIf
	Else
		//Retorno referente a validação de parâmetros obrigatórios
		oJsRet["success"] := .F.
		oJsRet["return"] := STR0002 //Parâmetros obrigatórios não informados: tabTmpName, codUser, branch
		oRest:setStatusCode(400)
	EndIf

	Restarea(aArea)

	FreeObj(oPrepare)
	FreeObj(oJson)
	FreeObj(oJsGet)
	FWFreeArray(aCpoTab)
	FWFreeArray(aStruCPO)
	FWFreeArray(aHasNext)
	FWFreeArray(aArea)

Return oRest:setResponse(oJsRet)

/*/{Protheus.doc} PutDiscardSalesRecommendation
  EndPoint PUT para descartar recomendações de venda

  @return json, objeto JSON de retorno do endpoint
  @author Raphael.Santana
  @since 02/08/2024
/*/
	@Put(endpoint="api/ba/v1/insights/sales/salesRecommendation/discard")
Function putDiscardSalesRecommendation()

	Local oJsGet     as Object
	Local oJsRet     as Object
	Local cTmpAlias  as Character
	Local cMessage   as Character
	Local cControler as Character
	Local aControler as Array
	Local nOpc		 as Numeric

	oJsGet     := oRest:GetQueryRequest()
	oJsRet     := JsonObject():New()
	cTmpAlias  := oJsGet[ 'tabTmpName' ]
	cMessage   := oJsGet[ 'discardReason' ]
	cControler := ""
	aControler := {}
	nOpc       := 1 //Opção descartar insight

	If oJsGet:hasProperty('ctrl_numb')
		cControler := oJsGet[ 'ctrl_numb' ]
		aControler := StrTokArr( cControler, ";" )
	EndIf

	oJsRet["success"] := .F.

	//Verifica os query params obrigatórios
	If !Empty(cTmpAlias) .AND. !Empty(cControler)

		//Chama a função que faz update table
		If PINS040UpdTable( cTmpAlias, aControler, cMessage, nOpc ) != Nil
			oJsRet["success"] := .T.
		EndIf
	Else
		//Retorno referente a validação de parâmetros obrigatórios
		oJsRet["success"] := .F.
		oJsRet["return"] := STR0003 //Campos obrigatorios nao informados: tabTmpName, ctrl_numb
		oRest:setStatusCode(400)
	EndIf

	FWFreeArray(aControler)

Return oRest:setResponse(oJsRet)

/*/{Protheus.doc} PutDiscardSalesRecommend
  EndPoint PUT para descartar recomendações de venda

  @return json, objeto JSON de retorno do endpoint
  @author Raphael.Santana
  @since 02/08/2024
/*/
	@Put(endpoint="api/ba/v1/insights/sales/salesRecommendation/restore")
Function putRestoreSalesRecommendation()

	Local oJsGet     as Object
	Local oJsRet     as Object
	Local cTmpAlias  as Character
	Local cControler as Character
	Local aControler as Array
	Local nOpc       as Numeric

	oJsGet     := oRest:GetQueryRequest()
	oJsRet     := JsonObject():New()
	cTmpAlias  := oJsGet[ 'tabTmpName' ]
	cControler := ""
	aControler := {}
	nOpc       := 0 //Opção restaurar insight

	If oJsGet:hasProperty('ctrl_numb')
		cControler := oJsGet[ 'ctrl_numb' ]
		aControler := StrTokArr( cControler, ";" )
	EndIf

	oJsRet["success"] := .F.

	//Verifica os query params obrigatórios
	If !Empty(cTmpAlias) .AND. !Empty(cControler)

		//Chama a função que faz update table
		If PINS040UpdTable( cTmpAlias, aControler, "", nOpc ) != Nil
			oJsRet["success"] := .T.
		EndIf
	Else
		//Retorno referente a validação de parâmetros obrigatórios
		oJsRet["success"] := .F.
		oJsRet["return"] := STR0003 //Campos obrigatorios nao informados: tabTmpName, ctrl_numb
		oRest:setStatusCode(400)
	EndIf

	FWFreeArray(aControler)

Return oRest:setResponse(oJsRet)

/*/{Protheus.doc} HasNext
    @type  Function 
    Função que verifica se existem mais páginas 
	alert enviado pelo smartlink

	@param cTmpAlias, caracter, Nome da tabela temporária
	@param nRegFim, numeric, Quantidade de registros total
	@param cWhere, caracter, String com as condições da query

    @author Raphael Santana Ferreira
    @since 25/07/2024
    @version version
    @param 
    @return array, Array com a primeira posiçõa lógica informando se tem mais registros/ Segunda posição com a quantidade total de registros referente a query
/*/
Static Function HasNext(cTmpAlias, nRegFim, cWhere)

	Local cQuery   as Character
	Local cAlias   as Character
	Local oQuery   as Object
	Local aRet	   as Array
	Local lNext	   as Logical

	cQuery   := ""
	oQuery   := Nil
	aRet     := {}

	cQuery += " SELECT MAX( LINE_NUMBER ) MAX_LINE FROM ( "
	cQuery += "   SELECT ROW_NUMBER() OVER( ORDER BY R_E_C_N_O_ ) LINE_NUMBER,  "
	cQuery += "       R_E_C_N_O_ RECNO "
	cQuery += "   FROM ? "

	cQuery += cWhere

	cQuery += " ) TAB "

	cQuery := ChangeQuery( cQuery )
	oQuery := FwExecStatement():New( cQuery )
	oQuery:setUnsafe( 1, cTmpAlias )

	cAlias := MPSysOpenQuery( oQuery:getFixQuery() )

	( cAlias )->( DBGoTop() )

	If ( cAlias )->( !Eof() )
		lNext := ( cAlias )->MAX_LINE > nRegFim
		aadd(aRet, lNext )
		aadd(aRet, ( cAlias )->MAX_LINE)
	EndIf

	(cAlias)->( DBCloseArea() )
	FreeObj(oQuery)

Return aRet

/*/{Protheus.doc} SalesMakeWhere
    @type  Function 
    Função que gera a query de select na tabela temporária
	Retorna um objeto FWPreparedStatement com a query ordenada, paginada e com os filtros setada no objeto
    @author Raphael Santana Ferreira
    
	@param cStatus, Caracter, Contém o status 
	@param cQryBranch, Caracter, contám as filiais que o usuário pode acessar
	@param cfilterByField, Caracter, string contendo json com campos de filtro
	
	@since 28/09/2024
    @version version   
    @return cWhere, Caracter, string where montada que será usada no select
/*/
Static Function SalesMakeWhere(cQryBranch, cStatus, cfilterByField)

	Local cWhere         as Character
	Local aFilterByField as Array
	Local nI			 as Numeric
	Local nJ			 as Numeric

	aFilterByField := {}
	cWhere         := ""
	nI			   := 0
	nJ			   := 0

	cWhere	  += " MSG_STATUS =  '" +  cStatus + "'"

	If !Empty(cQryBranch)
		If !Empty(cWhere)
			cWhere += " AND "
		EndIf
		cWhere += " branch IN (" + cQryBranch + ")"
	EndIf

	// constroi a condição de Where provenientes dos combos.
	If !Empty( cfilterByField )

		FwJSONDeserialize( cFilterByField, @aFilterByField )

		For nI := 1 to len( aFilterByField )

			cAux := " ( "

			For nJ := 1 to len( aFilterByField[ nI ][ "items" ] )

				If nJ == 1
					cAux += aFilterByField[ nI ][ "field" ] + " LIKE '%" + aFilterByField[ nI ][ "items" ][ nJ ] + "%'"
				Else
					cAux += " OR " + aFilterByField[ nI ][ "field" ] + " LIKE '%" + aFilterByField[ nI ][ "items" ][ nJ ] + "%'"
				EndIf

			Next nJ

			cAux += " ) "

			cWhere += " AND " + cAux

		Next nI
	EndIf

	If !Empty( cWhere )
		cWhere := " WHERE " + cWhere
	EndIf

	FWFreeArray( aFilterByField )

Return cWhere

//-------------------------------------------------------------------
/*/{Protheus.doc} salesJsonLine
Função que transforma os dados da linha na tabela temporária em Json.

@param cTmpAlias, caracter, Nome da tabela temporária
@param aTmpFields, array, Vetor com a estrutura da tabela temporária

@return object, Json com os dados da linha da tabela temporária
@author  Marcia Junko
@since   11/10/2024
/*/
//-------------------------------------------------------------------
Static Function salesJsonLine( cTmpAlias, aTmpFields )
	Local cType 	:= ''
	Local cValue 	:= ''
	Local cName 	:= ''
	Local nI      	:= 0
	Local oJson   
	Local oJsMemo 

	oJson   := JsonObject():New()

	For nI:=1 To Len( aTmpFields )
		cType := aTmpFields[nI][2][2]
		cValue := ( cTmpAlias )->&( aTmpFields[ nI ][ 2 ][ 1 ] )
		cName := IIf( !Empty( aTmpFields[nI][1]), aTmpFields[nI][1], aTmpFields[ nI ][ 2 ][ 1 ] )

		If cType == "C"
			oJson[ cName ] := AllTrim( StrTran( cValue, '"', '' ) )
		ElseIf cType == "N"
			oJson[ cName ] := cValue
		ElseIf cType == "M"
			cJsMemo := '{"' + cName + '" :  [' + cValue + ']}'
			oJsMemo := JsonObject():New()
			oJsMemo:FromJson( cJsMemo )
			oJson[ cName ] := oJsMemo[ cName ]
		// ElseIf cType == "L"
			// oJson[ cName ] := cValue
		EndIf
	Next

	FreeObj( oJsMemo )
Return oJson

//-------------------------------------------------------------------
/*/{Protheus.doc} pinsSalesMakeWhere
Função que gera a condição WHERE da query para o insight Sales_Recommendation
    
@param cQryBranch, Caracter, contám as filiais que o usuário pode acessar
@param cStatus, Caracter, Contém o status 
@param cfilterByField, Caracter, string contendo json com campos de filtro
	
@return cWhere, Caracter, string where montada que será usada no select
@author Marcia Junko
@since 02/06/2025
/*/
//-------------------------------------------------------------------
Function pinsSalesMakeWhere( cQryBranch, cStatus, cfilterByField )
Return SalesMakeWhere( cQryBranch, cStatus, cfilterByField )
