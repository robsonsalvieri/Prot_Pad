#include 'tlpp-core.th'
#include 'fwlibversion.ch'
#include 'TBICONN.CH'
#include "InsightDefs.ch"
#include 'BACKOFFICE.BA.INSIGHTS.FUNCTIONS.CH'

NAMESPACE totvs.protheus.backoffice.ba.insights.util

Static __oTFConfig := NIL

//-------------------------------------------------------------------------------------
// Biblioteca de funções genéricas utilizadas pelo produto Protheus Insights
//-------------------------------------------------------------------------------------

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} getTFConfig
Função responsável por retornar em cache o conteúdo da FWTFConfig()

@param lForceReload, boolean, Indica se deve fazer uma nova consulta ao TFConfig
@return Object, Objeto da FWTFConfig()
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function getTFConfig( lForceReload )
    Default lForceReload := .F.

    If __oTFConfig == NIL .Or. lForceReload
        __oTFConfig := FWTFConfig()
    EndIf
Return __oTFConfig 

//-------------------------------------------------------------------
/*/{Protheus.doc} getSysValue
Função que retorna o conteúdo gravado na SYS_APP_PARAM de acordo com a 
identificação passada.

@param cName, caracter, Identificador 

@return caracter, Conteúdo da configuração.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------
Function getSysValue( cName )
    Local oConfig 
    Local cContent := ''

    If ( __oTFConfig == NIL .Or. ( cContent := __oTFConfig[ cName ] ) == NIL )
        cContent := ''
        oConfig := getTFConfig()
        If oConfig[ cName ] <> NIL
        	cContent := oConfig[ cName ]
        EndIf
        __oTFConfig[ cName ] := cContent
        FreeObj( oConfig )
    EndIf
Return cContent


//-------------------------------------------------------------------------------------
/*/{Protheus.doc} loadParamsByProduct
Função que busca as configurações específicas de um produto\módulo na tabela SYS_APP_PARAM

@param cIdentifier, caracter, item a ser pesquisado. A informação é case sensitive.

@return array, vetor com as informações relacionadas ao cIdentifier que estão no SYS_APP_PARAM
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function loadParamsByProduct( cIdentifier )
    Local aParams := {}
    Local aParamNames := {}
    Local oConfig

    default cIdentifier := lower( 'PINS' ) + '_'

    oConfig := getTFConfig()

    aParamNames := oConfig:GetNames()

    aParams := filterArray( aParamNames, {|x| cIdentifier $ x } )

    FWFreeArray( aParamNames )
    FreeObj( oConfig )
Return aParams 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} loadCarolCompanies
Função que busca as empresas selecionadas pelo usuário para integração com a Carol.

@return array, vetor com as empresas em que a comunicação com a Carol foi habilitada.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function loadCarolCompanies( )
    Local aAllCompanies := {}
    Local aCarolCompanies := {}
    Local nI := 0
    Local lEnable := .F. 

    aAllCompanies := FWAllGrpCompany()

    For nI := 1 to len( aAllCompanies )
        lEnable := TFConfiguration():isTechFinEnabled( aAllCompanies[ nI ] )
        If lEnable
            aAdd( aCarolCompanies, aAllCompanies[ nI ] ) 
        EndIf
    Next

    FWFreeArray( aAllCompanies )
Return aCarolCompanies 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} filterArray
Filtra um array por determinada condição.

@param aData, array, vetor com as informações originais
@param bCondition, codeblock, condição para execução do filtro

@return array, vetor somente com os itens que satisfazem a condição.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function filterArray( aData, bCondition )
    Local aResult:= {}

    aEval( aData, {|uValue, nIndex| ;
        If( Eval( bCondition, uValue, nIndex, aData ), ;
            aAdd( aResult, uValue ), ;
        Nil) ;
    })
Return ( aResult )

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} saveAppParam
Função que grava as configurações na SYS_APP_PARAM

@param aData, array, vetor com as informações da conexão 

@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function saveAppParam( aData )
    Local oParam := JsonObject():New()
    Local nI := 0
    Local cName := 'totvs.protheus.backoffice.ba.insights.util.saveAppParam'

    If LockByName( cName, .F.,.F. )
        For nI := 1 to len( aData )
            oParam[ aData[ nI ][1] ] := aData[ nI ][2]
        Next

        FwTFSetConfig( oParam )

        UnLockByName( cName, .F., .F. )
    EndIf 
    FreeObj( oParam )
Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} getDBDataBase
Função que devolve o database do ambiente em uso.

@return caracter, Nome do database do ambiente
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Static Function getDBDataBase()
    Local cIni := GetAdv97()
    Local cByEnv := ""
    Local cByDBAccess := ""
    Local cDataBase	:= ""

    cByEnv := GetPvProfString( GetEnvServer() , "DBDataBase" , "ERROR" , cIni )
    cByDBAccess := GetPvProfString( "DBAccess" , "DataBase" , "ERROR" , cIni )

    cDataBase := Iif( cByEnv != "ERROR", cByEnv, cByDBAccess )
Return cDataBase 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} getDBAlias
Função que devolve o alias da base de dados do ambiente em uso.

@return caracter, Nome do alias
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Static Function getDBAlias()
    Local cIni := GetAdv97()
    Local cByEnv := ""
    Local cByDBAccess := ""    
    Local cAlias := ""

    cByEnv := GetPvProfString( GetEnvServer() , "DBAlias" , "ERROR" , cIni )
    cByDBAccess := GetPvProfString( "DBAccess" , "Alias" , "ERROR" , cIni )

    cAlias := Iif( cByEnv != "ERROR", cByEnv, cByDBAccess )
Return cAlias 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} getDBInfo
Função que devolve as informações do database do ambiente em uso.

@param nType, number, Indica a informação a ser retornada, sendo:
    1=database
    2=alias

@return caracter, Informação do database de acordo com o type passado.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function getDBInfo( nType )
    Local cInfo := ""

    Default nType := 1

    If nType == 1
        cInfo := getDBDataBase()
    Else
        cInfo := getDBAlias()
    EndIf
Return cInfo 

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} saveInstallInfo
Função que grava as informações de execução do Wizard.

@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function saveInstallInfo()
	Local aParams := {}
    Local cIdentifier := "pins_wizard_exec_"
	Local cDBDataBase := ""
	Local cDBAlias := ""
    Local cTypeEnv := ""
	
    cDBDataBase := getDBInfo(  )
	cDBAlias := getDBInfo( 2 )
    cTypeEnv := totvs.framework.environment.type.envAppGet( .T. )   //1=Produção;2=Homologação;3=Desenvolvimento

    IF cTypeEnv <> "1"
        cTypeEnv := "X" //X=Definido pelo usuário
    EndIf

    aAdd( aParams, { cIdentifier + "info", Alltrim( GetEnvServer() ) + "|" + AllTrim( cDBDataBase ) + "|" + AllTrim( cDBAlias )} )	// armazena o ambiente e o banco e alias de onde o Wizard foi executado
    aAdd( aParams, { cIdentifier + "release", GetRPORelease() } )	// armazena o release que o Wizard foi executado
    aAdd( aParams, { cIdentifier + "type_env", cTypeEnv } )	        // armazena o tipo de ambiente 
    saveAppParam( aParams )
Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} validLibVersion
Função que avalia se o ambiente está compatível a versão da lib passada.

@param cMinimumLibLabel, caracter, Versão mínima da lib

@return boolean, Indica se é compatível com a versão mínima da lib.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function validLibVersion( cMinimumLibLabel )
    Local cLibVersion := ""
    Local lLibUpdated := .F.

    Default cMinimumLibLabel := INSIGHT_LIB
    
    cLibVersion := FwLibVersion()
    lLibUpdated := ( cLibVersion >= cMinimumLibLabel )
Return lLibUpdated

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} isADVPRExec
Função que avalia se está sendo executado um script ADVPR

@return boolean, Indica se está sendo executado por um script ADVPR.
@author  Marcia Junko
@since   07/08/2024
/*/
//-------------------------------------------------------------------------------------
Function isADVPRExec()
Return ( FWIsInCallStack( "FWMYTESTRUNNER" )  .Or. FWIsInCallStack( "CASIGAADV" ) .Or. FWIsInCallStack( "CACOVERTEST" ) .Or. FWIsInCallStack( "CAUNITTEST" ) .Or. FWIsInCallStack( "AUTJOBRUNCT" ) )


//-------------------------------------------------------------------------------------
/*/{Protheus.doc} validateUseOfInsights
Função generica Protheus Insights 

@param nOpc, number, opção que poder ser utilizado para executar as funcionalidades, sendo:
    0 - valida versões dos pacotes
    1 - execução da activeJobs
@param aADVPRInfo, array, vetor contendo informações necessárias para automação

@return any, Retorno da ação solicitada ou NIL.
@author  Danilo Santos
@since   14/08/2024
/*/
//-------------------------------------------------------------------------------------
Function validateUseOfInsights( nOpc, aADVPRInfo )    
    Local xResult := Nil
    
    Default nOpc := 0
    Default aADVPRInfo := NIL
    
    If nOpc == 0 // Validação dos requisitos mínimos necessários 
        xResult := validMinimumRequirements( aADVPRInfo ) //.And. validDictionary(.F.)  //Comentado para ser tratado posteriormente pois a validação da tabela impede o acesso ao modo demo
    ElseIf nOpc == 1 // 1=Execução da activeJobs
        If ( AliasIndic("I14") .Or. AliasInDic("I19") ) .And. FindFunction("totvs.protheus.backoffice.ba.insights.scheduler.activeJobs")
            totvs.protheus.backoffice.ba.insights.scheduler.activeJobs( , , , aADVPRInfo )
        EndIf     
    Endif 

Return xResult


//-------------------------------------------------------------------------------------
/*/{Protheus.doc} validMinimumRequirements
Função para validação dos requisitos mínimos para uso do Protheus Insights.

@return boolean, Indica se o ambiente possui os requisitos mínimos para execução.
@author  Danilo Santos
@since   14/08/2024
/*/
//-------------------------------------------------------------------------------------
Static Function validMinimumRequirements( aADVPRInfo )
    Local lRet := .F.
    Local cSmartVersion := ""

    IF aADVPRInfo == NIL .And. validLibVersion()
        If FindFunction( "FWTECHFINVERSION" )
            If FwtechfinVersion() >= INSIGHT_SMARTLINK
                lRet := .T.
            EndIf
        EndIF
    EndIf
    If !lRet
        If FindFunction( "FWTECHFINVERSION" )
            cSmartVersion := FwtechfinVersion()
        EndIF

        MsgAlert( I18N(STR0002 ,;	//"A execução desta rotina não é possível devido à incompatibilidade das versões atuais da Lib [ #1 ] e do Smartlink [ #2 ] deste ambiente. As versões mínimas exigidas são: Lib igual ou superior a [ #3 ] e Smartlink igual ou superior a [ #4 ].
            { FwLibVersion(), cSmartVersion, INSIGHT_LIB, INSIGHT_SMARTLINK } ) + CRLF + CRLF + STR0003 ) //"Por favor, entre em contato com o administrador para atualização."

    Endif
Return lRet


//-------------------------------------------------------------------------------------
/*/{Protheus.doc} settingEnvironment
Função para Setar a empresa para gravação dos dados pelo Consumer.

@param cCompGroup, caracter, Grupo de Empresa utilizada para abertura de ambiente 
@param cFilMsg   , caracter, Filial utilizada para abertura de ambiente
@param cModule   , caracter, Modulo utilizado para abertura de ambiente
@param aTable    , array   , Array contendo as tabelas a serem abertas.

@return
@author  Danilo Santos
@since   29/10/2024
/*/
//-------------------------------------------------------------------------------------

Function settingEnvironment(cCompGroup, cFilMsg ,cModule ,aTable)

Default cCompGroup := ""
Default cFilMsg := ""
Default cModule := "FAT"
Default aTable := {}

If (!Empty(cCompGroup) .And. !Empty(cEmpAnt) ) .And. ( cEmpAnt != cCompGroup )
    RpcClearEnv( )
    RpcSetType( 3 )       
    OpenSM0( )        
    RpcSetEnv( cCompGroup, , , , cModule ,"RPC" ,aTable, , , ,  )
EndIf

//- Ajusta a filial de processamento  If cFilAnt != cNewFil
If !Empty(cFilMsg) 
    cFilAnt := cFilMsg
EndIf

return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} createTempTable
Função genérica que realiza a criação da tabela temporária.

@param cInsTyp, caracter, nome do insight
@param cModule, caracter, sigla do módulo
@param aFields, array, vetor com a estrutura da tabela

@return object, Indica o objeto da tabela temporária.
@author  Marcia Junko
@since   06/05/2025
/*/
//-------------------------------------------------------------------------------------
Function createTempTable( cInsTyp, cModule, aFields, cQryBranch )
    Local cInsAlias  as Character
    Local oTmpTab    as Object

    Default cInsTyp := 'stock_out'
    Default cModule := 'EST'
    Default aFields := {}
    Default cQryBranch := ""

    //Instancia o objeto da tabela temporária
    cInsAlias	:= GetNextAlias()
    oTmpTab     := Nil

    If !Empty( aFields )
        oTmpTab := PINSMakeTemp( cInsAlias,,,cInsTyp, cModule, aFields, cQryBranch, NEW_ARCHITECTURE )
    EndIf
Return oTmpTab



//-------------------------------------------------------------------------------------
/*/{Protheus.doc} validUseMock
Verifica a necessidade de apresentação dos dados mockados para o usuario utilizando as 
mensagens da nova arquitetura

@param oTempTable, object, Tabela temporaria 
@param aInsight, array, vetor com as informações do insight
    [1] - nome do insight
    [2] - identificação no mock
@param cModule, Character, contém a string referente ao módulo 
@param aFields, array, contém um array com os dados de campos referente a tabela temporária

@return  array, .T. ou .F. , Dados Mockados
[1] - .T. ou .F. referente a geração do Mock
[2] - Mensagem Json que será utilizada no Mock

@author  Victor Vieira 
@since   17/06/2025
/*/
//-------------------------------------------------------------------------------------
Function validUseMock( oTempTable, aInsight, cModule, aFields )
    Local aArea         := GetArea()	
    Local cNextAlias    := ""
    Local cMessage      := ""
    Local cInsightName  := ""
    Local cMockName     := ""
    Local cPropJs       := "alerts"
    Local nTotLinha     := 0
    Local lReturn       := .F.

    //Verifica se a tabela temporária tem registros
    nTotLinha := PINSRegCnt( oTempTable:GetRealName() )

    If nTotLinha == 0		
        cInsightName := aInsight[ 1 ]
        cMockName := Iif( !Empty( aInsight[ 2 ] ), aInsight[ 2 ], cInsightName )
        		
        cNextAlias := PINSGetAlerts( '', cInsightName, cModule, 2 )	
            
        aFields	 := pinsFieldsStruct( cInsightName )  
        PinsMakeMock(oTempTable:GetRealName(), cMockName, aFields, cPropJs, 2, cInsightName )
        lReturn := .T. 

        IF ( cNextAlias )->( !Eof() )
           cMessage := STR0005 // "Não foram encontrados insights para as filiais acessíveis para este usuário."
        EndIf

        ( cNextAlias )->( DBCloseArea() )
    EndIf

    RestArea(aArea)
    FWFreeArray(aArea)
    
Return { lReturn, cMessage }

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} validDictionary
Função para verificar se o dicionario esta atualizado criado no banco de Dados.

@param lWizard, Logico, Indica se a função foi chamada pelo wizard ou 
pela rotina de requerimentos chamadas pelas rotinas de Demand/Rupture
@param lAuto, Logico, Variavel para controle de automação de testes 

@return boolean, Indica se o ambiente possui as tabelas criadas para execução.
@author  Danilo Santos
@since   27/01/2025

/*/
//-------------------------------------------------------------------------------------

//Função comentada para ser tratada posteriormente , pois a validação da tabela impede o acesso ao modo demo

// Function validDictionary(lWizard, lAuto)

//     Local lUpdate := .F.

//     Default lWizard := .F.
//     Default lAuto := .F.
    

//     //Esses comentarios ficaram no fonte pois serão implementados depois na rotina do wizard, nao subiram pois em testes realizados ocorreram erros na classe do framework
//     // If lWizard
//     //     RpcClearEnv( )
//     //     RpcSetType( 3 )       
//     //     OpenSM0( )        
//     //     RpcSetEnv( FWGrpCompany() )
//     // Endif

//     lUpdate := AliasInDic("I19") .And. AliasInDic("I20") .And. AliasInDic("I21")  

//     If lAuto .Or.( !lUpdate .And. !lWizard ) 
//         MsgAlert( I18N(STR0004 ) + CRLF + CRLF + STR0003 ) //"Notamos que o dicionário de dados do Protheus Insights não está atualizado. Por favor, baixe o pacote da Expedição Contínua e execute o UPDDISTR.. #"Por favor, entre em contato com o administrador para atualização."

//     Endif

//     // If lWizard
//     //     RESET ENVIRONMENT
//     // Endif

// return lUpdate

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} pinsCheckEnv
Validação do ambiente: Produção ou Engenharia Protheus Insights

@return lChkEnv, Logical, TRUE=Validou; FALSE=Não Validou

@author plinio.cfilho
@since 18/09/2025
/*/
//-------------------------------------------------------------------------------------
Function pinsCheckEnv()

	Local lChkEnv   As Logical

	lChkEnv := ( ( totvs.framework.environment.type.envAppGet( .T. ) == "1" ) .Or. ;    // 1=Produção;2=Homologação;3=Desenvolvimento
        ( FindClass( "FWEngineeringUtils" ) .And. FWEngineeringUtils():isAlphaEnvironment() ) )

Return lChkEnv
