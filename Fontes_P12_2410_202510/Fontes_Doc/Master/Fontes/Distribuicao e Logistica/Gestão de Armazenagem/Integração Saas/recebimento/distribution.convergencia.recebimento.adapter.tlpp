#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.recebimento.adapter.ch"

/*/{Protheus.doc} WMSSaasRecebimentoAdapter
    Classe para tratamento dos retornos de recebimento da convergencia
    @author Filipe Mendes
    @since 15/04/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergência WMS Saas x Protheus
    /*/
Class WMSSaasRecebimentoAdapter from WMSSaasConvergenciaAdapter implements WMSSaasConvergenciaAdapterInterface
    Public Method new() as object
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Public Method finaliza(cId as character, oJsonBody as json) as logical
    Public Method reinicia(cId as character) as logical
    Public Method montaPayloadRecebimento() as json
    Public Method montaPayloadCompletoRecebimento() as json
    Static Method getCaracteristicas() as json

EndClass

/*/{Protheus.doc} new
   (long_description)
   @author user
   @since 02/05/2024
   @version version
   @param param_name, param_type, param_descr
   @return return_var, return_type, return_description
   /*/
Method new() Class WMSSaasRecebimentoAdapter
Return self

/*/{Protheus.doc} getAll
   Busca cabecalho dos recebimentos
   @author Filipe Mendes
   @since 24/04/2024
   @version version
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oRecebimento, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasRecebimentoAdapter as json
Local oRecebimentos := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoRecebimento(), nPage, nPageSize)
   self:montaPayloadRecebimento()
Return oRecebimentos

/*/{Protheus.doc} getById
   Busca recebimento completo pelo Id
   @author Filipe Mendes
   @since 24/04/2024
   @version version
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oRecebimento, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasRecebimentoAdapter as json
Local oRecebimento := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadCompletoRecebimento()
Return oRecebimento

/*/{Protheus.doc} montaPayloadCompletoRecebimento
   Monta o payload conforme contrato WMS Saas
   @author Filipe Mendes
   @since 24/04/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadRecebimento() Class WMSSaasRecebimentoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "chaveExterna": {;
            "id": item['id'],;
            "documento": item['documento'],;
            "serie": item['serie'],;
            "fornecedor": item['pessoa'],;
            "loja": item['loja'];
         }; 
      };
      );
   })

// -- Refatorar fornecedor para receber tipo do documento usando cliente (B)
Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} montaPayloadCompletoRecebimento
   Monta o payload conforme contrato WMS Saas
   @author Filipe Mendes
   @since 24/04/2024
   @version version
   @param param_name, param_type, param_descr
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadCompletoRecebimento() Class WMSSaasRecebimentoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}
Local oRecebimentoClasse := WMSSaasRecebimento():New()
   oRecebimentoClasse:loadById(currentJsonObject['id'])

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "quantidade": item['quantidade'],;
      "valorUnitario": item['valorUnitario'],;
      "valorTotal": item['quantidade'] * item['valorUnitario'],;
      "sequencia": item['item'],;
      "tipoEstoque": item['armazem'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })

// -- Refatorar fornecedor para receber tipo do documento usando cliente (B)
Return self:oJsonObj:oJsonObj := {;
   "chaveExterna":{;
      "id":currentJsonObject['id'];
   },;
   "fornecedor": WMSSaasFornecedorAdapter():montaPayload(currentJsonObject['pessoa'], currentJsonObject['loja'], oRecebimentoClasse:usaCliente()),;
   "transportadora": IIF(!Empty(currentJsonObject['transportador']), WMSSaasTransportadorAdapter():montaPayload(currentJsonObject['transportador']), ''),;
   "dataEmissao": currentJsonObject['emissao'],;
   "documentoOrigem":{;
      "tipo":"NOTA_FISCAL",;
      "identificador":{;
         "chave_nfe":currentJsonObject['chaveNfe'],;
         "numero":currentJsonObject['documento'],;
         "serie":currentJsonObject['serie'];
      };
   },;
   "itens": aItens }

/*/{Protheus.doc} getCaracteristicas
   Monta as caracteristicas do item no payload
   @author Filipe Mendes
   @since 24/04/2024
   @version version
   @param item, object, item do recebimento
   @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasRecebimentoAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, { "lote": item['lote'] })
      aAdd(caracteristicas, { "dataValidade": item['dataValidade'] })
   EndIf
Return caracteristicas

/*/{Protheus.doc} WMSSaasRecebimentoAdapter::finaliza
Efetua leitura do payload e aciona a finalização do recebimento

@type method
@version  
@author fagner.ferraz
@since 5/15/2024
@param cId, character, Id da convergencia
@param oJsonBody, object, Payload de finalização do recebimento
@return logical, finalização do recebimento
/*/   
Method finaliza(cId, oJsonBody) Class WMSSaasRecebimentoAdapter as logical
Local oRecebimento   := WMSSaasRecebimento():new()
Local item           := 0
Local aItens         := {}
Local jItens         := oJsonBody['content']['itens']
Local lDivergencia   := oJsonBody['content']['divergencia']['possuiDivergencia']

   For item := 1 To Len(jItens)
      Aadd( aItens, { jItens[item]['documentoRecebimentoItem'],;
                     WMSRetCaracteristicas( jItens[item]['caracteristicasConferidas'] ),;
                     jItens[item]['quantidadeConferida']['total'] } )

   Next item
Return !Empty(oRecebimento:loadById(cId)) .And. oRecebimento:finaliza(aItens, lDivergencia)

/*/{Protheus.doc} WMSSaasRecebimentoAdapter::reinicia
Recebe conteudo da api e trata para envio para a classe 
de recebimento
@type method
@version  
@author Filipe Mendes
@since 03/06/2024
@param cId, cId do recebimento
@return logical, finalização do recebimento
/*/   
Method reinicia(cId, oSuccess) Class WMSSaasRecebimentoAdapter as logical
Local oRecebimento := WMSSaasRecebimento():new()
Local lRet         := .T.
   
   oRecebimento:loadById(cId)
    
   if Empty(oRecebimento:id)
       oSuccess:setStatus(400)
       oSuccess:setJsonResponse(STR0001+cId+STR0002) // Registro de id +cId+ não encontrado
       lRet := .F.
   EndIf

   If lRet .And. !oRecebimento:reinicia()
       oSuccess:setStatus(400)
       oSuccess:setJsonResponse(oRecebimento:getMensagemIntegracao())
       lRet := .F.
   EndIf
Return lRet
