#include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.estoque.controle.requisicao.armazem.classe.ch"

#define GERADA      "1"
#define CONCLUIDA   "2"
#define EXCLUIDA    "3"

Static nTamProd   := TamSx3('D3_COD')[1]
Static nTamLocal  := TamSx3('D3_LOCAL')[1]
Static nTamLote   := TamSx3('D3_LOTECTL')[1]
Static nTamSeqReq := TamSX3("DC0_SEQREQ")[1]

/*/{Protheus.doc} WMSSaasEstoqueControleRequisicaoArmazem
    Classe de requisicao ao armazem
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    /*/
Class WMSSaasEstoqueControleRequisicaoArmazem
    Public Method new(cArmazem as character, cNumSA as character, cItemSA as character, cProduto as character, cOrdemProducao as character, nQuantidade as numeric, dDataEmissao as date, cLote as character, cSubLote as character, dDataValidade as date) as object
    Public Method loadConvergencia() as object
    Static Method getStatusGerada() as character
    Public Method save()
    Public Method getSequence() as character
    Public Method loadByChave(cLocal as character, cNumSA as character, cItemSA as character, cProduto as character) as object
    Public Method load() as object
    Public method isGerada() as logical
    Public Method delete()
    Public Method loadByIdDBZ(cId as character) as object   
    Public Method loadByRecno(nRecno as numeric) as object
    Public Method validaRequisicaoProcessada(jRequisicaoArmazem as json) as logical
    Public Method atualizaRequisicaoArmazem(jRequisicaoArmazem as json) as logical
    Public Method geraTransferenciaEntreArmazens(jRequisicaoArmazem as json, cTipEstoqueOrigem as character) as logical
    Public Method geraMovimentoTransferencia(aTransf as array, aWmsCMov as array) as logical
    Public Method geraBaixaRequisicaoArmazem(jRequisicaoArmazem as json, nRecnoSCP as numeric, nRecnoDC0 as numeric) as logical

    Public Data convergencia as object
    Public Data armazem as character
    Public Data numSA   as character
    Public Data itemSA  as character
    Public Data produto as character
    Public Data ordemProducao as character
    Public Data origem as character
    Public Data dataEmissao as date
    Public Data lote as character
    Public Data subLote as character    
    Public Data dataValidade as date
    Public Data quantidade as numeric
    Public Data status as character
    Public Data idConvergencia as character
    Public Data recno as numeric
EndClass

/*/{Protheus.doc} New
    Construtor da classe de requisicao ao armazem
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    @param cArmazem, character, Armazem da SA
    @param cNumSA, character, Numero da SA
    @param cItemSA, character, Item da SA
    @param cProduto, character, Produto da SA
    @param cOrdemProducao, character, Ordem de producao da SA
    @param nQuantidade, character, Quantidade da SA
    @param dDataEmissao, character, Data de emissao da SA
    @param cLote, character, Lote da SA
    @param cSubLote, character, Sublote da SA
    @param dDataValidade, character, Data de validade do lote
    @return self, object, Objeto do controle de requisicao
    /*/
Method new(cArmazem, cNumSA, cItemSA, cProduto, cOrdemProducao, nQuantidade, dDataEmissao, cLote, cSubLote, dDataValidade) Class WMSSaasEstoqueControleRequisicaoArmazem
    self:armazem            := cArmazem    
    self:numSA              := cNumSA
    self:itemSA             := cItemSA
    self:produto            := cProduto
    self:ordemProducao      := cOrdemProducao
    self:lote               := cLote
    self:subLote            := cSubLote
    self:dataValidade       := dDataValidade
    self:dataEmissao        := dDataEmissao
    self:quantidade         := nQuantidade
    self:status             := self:getStatusGerada()
    self:convergencia       := self:loadConvergencia()
    self:origem             := 'S'
Return self

/*/{Protheus.doc} loadConvergencia
    Instancia a classe de convergencia
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    @return oConvergencia, object, Objeto da convergencia
    /*/
Method loadConvergencia() Class WMSSaasEstoqueControleRequisicaoArmazem
Local oConvergencia := WMSSaasEstoqueRequisicaoArmazem():new(self:numSA, self:dataEmissao, self:ordemProducao )

    If !Empty(self:idConvergencia)
        oConvergencia := oConvergencia:loadById(self:idConvergencia)
    EndIf

Return oConvergencia

/*/{Protheus.doc} Load
    @author Fagner Ferraz
    @since 14/04/2025
    @version 1.0
    @return self, object, Objeto do controle de requisicao
    /*/
Method load() Class WMSSaasEstoqueControleRequisicaoArmazem
    self:armazem            := DC0->DC0_LOCAL    
    self:numSA              := DC0->DC0_NUMSA
    self:itemSA             := DC0->DC0_ITEMSA
    self:produto            := DC0->DC0_PRODUT
    self:lote               := DC0->DC0_LOTECT
    self:subLote            := DC0->DC0_NUMLOT
    self:dataValidade       := DC0->DC0_DTVALD
    self:quantidade         := DC0->DC0_QUANT
    self:status             := DC0->DC0_STATUS
    self:idConvergencia     := DC0->DC0_IDDBZ
    self:recno              := DC0->( Recno() )
    self:convergencia       := self:loadConvergencia()
Return self

/*/{Protheus.doc} Save
    Efetua a gravacao das tabelas DBZ, DBY e DC0
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    /*/
Method Save() Class WMSSaasEstoqueControleRequisicaoArmazem    
    
    // Salva as informações da convergencia
    self:convergencia:save()
    self:convergencia:saveItens()

    If RecLock("DC0", .T.)
        DC0->DC0_FILIAL := xFilial("DC0")
        DC0->DC0_LOCAL  := self:armazem
        DC0->DC0_NUMSA  := self:numSA
        DC0->DC0_ITEMSA := self:itemSA 
        DC0->DC0_PRODUT := self:produto 
        DC0->DC0_QUANT  := self:quantidade
        DC0->DC0_STATUS := self:status 
        DC0->DC0_SEQREQ := self:getSequence()
        DC0->DC0_ORIGEM := self:origem 
        DC0->DC0_IDDBZ  := self:convergencia:id
        DC0->(MsUnlock())
    EndIf
Return 

/*/{Protheus.doc} getStatusGerada
    Retorna status GERADA
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    @return Status, character, Status correspondente a requisicao gerada
    /*/
Method getStatusGerada() Class WMSSaasEstoqueControleRequisicaoArmazem
Return GERADA

/*/{Protheus.doc} getSequence
    Retorna a sequencia de requisicao da SA
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    @return cNewSeq, character, nova sequencia da requisicao
    /*/
Method getSequence() Class WMSSaasEstoqueControleRequisicaoArmazem
    Local cAliasDC0     := GetNextAlias()
    Local cNewSeq       := ''
    Local nTamSeqReq    := TamSX3("DC0_SEQREQ")[1]              
    
    BeginSql Alias cAliasDC0
		SELECT MAX(DC0.DC0_SEQREQ) MAXSEQREQ
		  FROM %Table:DC0% DC0
		 WHERE DC0.DC0_FILIAL   = %Exp:xFilial("DC0")%
		   AND DC0.DC0_NUMSA    = %Exp:self:numSA%
		   AND DC0.%NotDel%
	EndSql    
	cNewSeq := Iif(!Empty((cAliasDC0)->MAXSEQREQ), Soma1(PadL((cAliasDC0)->MAXSEQREQ, nTamSeqReq, "0" )), Soma1(PadL("0",nTamSeqReq,"0")))

    (cAliasDC0)->(DbCloseArea())
Return cNewSeq

/*/{Protheus.doc} loadByChave
    Faz o load da classe controle requisicao armazem
    @author Fagner Ferraz
    @since 04/04/2025
    @version 1.0
    @param cLocal, character, Armazem
    @param cNumSA, character, Numero da SA
    @param cItemSA, character, Item da SA
    @param cProduto, character, Produto
    @return self, object, Objeto faturamento antecipado
/*/
Method loadByChave(cLocal, cNumSA, cItemSA, cProduto) Class WMSSaasEstoqueControleRequisicaoArmazem
Local cAliasDC0 := GetNextAlias()
    BeginSql Alias cAliasDC0
		SELECT DC0.R_E_C_N_O_ RECNODC0
		  FROM %Table:DC0% DC0
		 WHERE DC0.DC0_FILIAL = %Exp:xFilial("DC0")%
		   AND DC0.DC0_LOCAL = %Exp:cLocal%
		   AND DC0.DC0_NUMSA = %Exp:cNumSA%
           AND DC0.DC0_ITEMSA  = %Exp:cItemSA%
           AND DC0.DC0_PRODUT = %Exp:cProduto%
		   AND DC0.%NotDel%
	EndSql

    If (cAliasDC0)->(!Eof())
        DbSelectArea("DC0")
        DC0->( DbGoTo((cAliasDC0)->RECNODC0) )
        self:load()
    EndIf
    (cAliasDC0)->(DbCloseArea())
Return self

/*/{Protheus.doc} isGerada
    @author Fagner Ferraz
    @since 14/04/2025
    @version 1.0
    @return lRet, logical, Indica se o status da requisicao esta como gerada
    /*/
Method isGerada() Class WMSSaasEstoqueControleRequisicaoArmazem
Return (self:status == GERADA)

/*/{Protheus.doc} Delete
    Apaga registro de requisicao
    @author Fagner Ferraz
    @since 14/04/2025
    @version 1.0
    /*/
Method delete() Class WMSSaasEstoqueControleRequisicaoArmazem       
    DBSelectArea("DC0")
    DC0->( DbGoTo(self:recno) )

    RecLock("DC0",.F.)
    DC0->(dbDelete())
    DC0->(MsUnlock())
Return

/*/{Protheus.doc} loadByIdDBZ
    Instancia um novo objeto a partir do id
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method loadByIdDBZ(cId) Class WMSSaasEstoqueControleRequisicaoArmazem
Local aArea    := GetArea()
Local aAreaDC0 := DC0->(GetArea())  
    If WMSSaasSeekDC0ByIdDBZ(cId)
        self:load()
    EndIf
    RestArea(aAreaDC0)
    RestArea(aArea)
Return self

/*/{Protheus.doc} validaRequisicaoProcessada
    Verifica se a SA ja foi processada
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method validaRequisicaoProcessada(jRequisicaoArmazem) Class WMSSaasEstoqueControleRequisicaoArmazem
Local cAliasDC0        := GetNextAlias()
Local cOrigem          := 'N'
Local cIdReserva       := cValToChar(jRequisicaoArmazem['idReserva'])
Local lRet             := .F. 

    BeginSql Alias cAliasDC0
        SELECT 1
          FROM %Table:DC0% DC0
         WHERE DC0.DC0_FILIAL = %Exp:xFilial("DC0")%
           AND DC0.DC0_IDDBZ  = %Exp:self:idConvergencia%
           AND DC0.DC0_ORIGEM = %Exp:cOrigem%
           AND DC0.DC0_IDRES  = %Exp:cIdReserva%
           AND DC0.%NotDel%
    EndSql
    If (cAliasDC0)->(!Eof())
        lRet := .T.
    EndIf
    (cAliasDC0)->(DbCloseArea())
Return lRet

/*/{Protheus.doc} atualizaRequisicaoArmazem
    Metodo que aciona a transferencia e a baixa da requisicao ao armazem
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method atualizaRequisicaoArmazem(jRequisicaoArmazem) Class WMSSaasEstoqueControleRequisicaoArmazem
Local cAliasSCP             := GetNextAlias()
Local cTipEstoqueDestino    := Padr(jRequisicaoArmazem['tipoEstoque'],nTamLocal)
Local lRet                  :=  .T.

    //--Busca pelo ID da convergencia porque a geracao da convergencia é feita por item da SA
    BeginSql Alias cAliasSCP
        SELECT DISTINCT SCP.CP_LOCAL, SCP.R_E_C_N_O_ RECNOSCP, DC0.R_E_C_N_O_ RECNODC0
        FROM %Table:DC0% DC0
        INNER JOIN %Table:SCP% SCP
            ON SCP.CP_FILIAL    = %Exp:xFilial("SD4")%
            AND SCP.CP_NUM       = DC0.DC0_NUMSA
            AND SCP.CP_ITEM      = DC0.DC0_ITEMSA
            AND SCP.CP_LOCAL     = DC0.DC0_LOCAL 
            AND SCP.%NotDel%
        WHERE DC0.DC0_FILIAL   = %Exp:xFilial("DC0")%
            AND DC0.DC0_IDDBZ    = %Exp:self:idConvergencia%
            AND DC0.%NotDel%
    EndSql
    If (cAliasSCP)->(!Eof())

        //--Se o armazem onde a separacao foi realizada é diferente do armazem de origem, realiza a transferencia do saldo entre os armazens
        If (cAliasSCP)->CP_LOCAL != cTipEstoqueDestino
            lRet := self:geraTransferenciaEntreArmazens(jRequisicaoArmazem,(cAliasSCP)->CP_LOCAL)
        EndIf

        If lRet
            lRet := self:geraBaixaRequisicaoArmazem(jRequisicaoArmazem, (cAliasSCP)->RECNOSCP, (cAliasSCP)->RECNODC0)
        EndIf

    EndIf
    (cAliasSCP)->(DbCloseArea())
Return lRet

/*/{Protheus.doc} geraTransferenciaEntreArmazens
    Metodo de finalizacao do movimento transferencia
    @author Fagner Ferraz
    @since 03/09/2025
/*/
Method geraTransferenciaEntreArmazens(jRequisicaoArmazem, cTipEstoqueOrigem) Class WMSSaasEstoqueControleRequisicaoArmazem
Local cProduto           := Padr(jRequisicaoArmazem['produto'],nTamProd)
Local lRastro            := Rastro(cProduto,"L")
Local cLote              := If(lRastro .And. !(jRequisicaoArmazem['lote']==NIL),jRequisicaoArmazem['lote'],Space(nTamLote))
Local dDataValidade      := If(lRastro,jRequisicaoArmazem['dataValidade'],CtoD('  /  /  '))
Local nQuantidade        := jRequisicaoArmazem['quantidade']
Local cNumSeq            := ""
Local cUM                := ""
Local cDescricao         := ""
Local aTransf            := {}
Local aWmsCMov           := {}
Local cTipEstoqueDestino  := Padr(jRequisicaoArmazem['tipoEstoque'],nTamLocal)

    DbSelectArea("SD3")
    SD3->(DbSetOrder(1))
    
    DBSelectArea("SB1")
    SB1->(DbSetOrder(1))

    DBSelectArea("SB2")
    SB2->(DbSetOrder(1))

    DBSelectArea("NNR")
    NNR->(DbSetOrder(1))

    If !SB1->(MsSeek(xFilial("SB1") + cProduto))
        self:convergencia:setMensagemIntegracao(STR0001+ AllTrim(cProduto) + STR0003) // "Produto "" nao encontrado na filial."
        Return .F.
    EndIf

    If !NNR->(MsSeek(xFilial("NNR") + cTipEstoqueOrigem))
        self:convergencia:setMensagemIntegracao(STR0002 + AllTrim(cTipEstoqueOrigem) + STR0003) // "Armazem "" nao encontrado na filial."
        Return .F.
    EndIf

    If !NNR->(MsSeek(xFilial("NNR") + cTipEstoqueDestino))
        self:convergencia:setMensagemIntegracao(STR0002 + AllTrim(cTipEstoqueDestino) + STR0003) // "Armazem "" nao encontrado na filial."
        Return .F.
    EndIf
    
    cDescricao := SB1->B1_DESC
    cUM        := SB1->B1_UM

    // Valida se produto esta cadastrado no armazem origem
    If !SB2->(DbSeek(xFilial("SB2") + cProduto + cTipEstoqueOrigem))
        CriaSB2(cProduto, cTipEstoqueOrigem, xFilial("SB2"))
    EndIf

    // Valida se produto esta cadastrado no armazem destino
    If !SB2->(DbSeek(xFilial("SB2") + cProduto + cTipEstoqueDestino))
        CriaSB2(cProduto, cTipEstoqueDestino, xFilial("SB2"))
    EndIf

    cNumSeq := SD3->(ProxNum())

    aAdd(aTransf, {cNumSeq, dDataBase})
    aAdd(aTransf,{})
    aAdd(aTransf[02],{"D3_COD"    , cProduto                           , Nil}) // [01] Produto origem
    aAdd(aTransf[02],{"D3_DESCRI" , cDescricao                         , Nil}) // [02] Descricao origem 
    aAdd(aTransf[02],{"D3_UM"     , cUM                                , Nil}) // [03] Unidade de medica origem
    aAdd(aTransf[02],{"D3_LOCAL"  , cTipEstoqueOrigem                  , Nil}) // [04] Armazem origem
    aAdd(aTransf[02],{"D3_LOCALIZ", CriaVar("D3_LOCALIZ")              , Nil}) // [05] Endereco origem
    aAdd(aTransf[02],{"D3_COD"    , cProduto                           , Nil}) // [06] Produto destino
    aAdd(aTransf[02],{"D3_DESCRI" , cDescricao                         , Nil}) // [07] Descricao origem
    aAdd(aTransf[02],{"D3_UM"     , cUM                                , Nil}) // [08] Unidade de medida destino
    aAdd(aTransf[02],{"D3_LOCAL"  , cTipEstoqueDestino                 , Nil}) // [09] Armazem destino
    aAdd(aTransf[02],{"D3_LOCALIZ", CriaVar("D3_LOCALIZ")              , Nil}) // [10] Endereco destino
    aAdd(aTransf[02],{"D3_NUMSERI", CriaVar("D3_NUMSERI")              , Nil}) // [11] Numero de serie
    aAdd(aTransf[02],{"D3_LOTECTL", cLote                              , Nil}) // [12] Lote
    aAdd(aTransf[02],{"D3_NUMLOTE", CriaVar("D3_NUMLOTE")              , Nil}) // [13] Sub-Lote
    aAdd(aTransf[02],{"D3_DTVALID", dDataValidade                      , Nil}) // [14] Data de validade
    aAdd(aTransf[02],{"D3_POTENCI", CriaVar("D3_POTENCI")              , Nil}) // [15] Potencia
    aAdd(aTransf[02],{"D3_QUANT"  , nQuantidade                        , Nil}) // [16] Quantidade
    aAdd(aTransf[02],{"D3_QTSEGUM", ConvUm(cProduto, nQuantidade, 0, 2), Nil}) // [17] Quantidade segunda unidade
    aAdd(aTransf[02],{"D3_ESTORNO", .F.                                , Nil}) // [18] Estorno
    aAdd(aTransf[02],{"D3_NUMSEQ" , CriaVar("D3_NUMSEQ")               , Nil}) // [19] Numero sequencial
    aAdd(aTransf[02],{"D3_LOTECTL", cLote                              , Nil}) // [20] Lote destino
    aAdd(aTransf[02],{"D3_DTVALID", dDataValidade                      , Nil}) // [21] Data de validade destino
    aAdd(aTransf[02],{"D3_SERVIC" , CriaVar("D3_SERVIC")               , Nil}) // [22] Servico
    aAdd(aTransf[02],{"D3_ITEMGRD", CriaVar("D3_ITEMGRD")              , Nil}) // [23]

    aAdd(aWmsCMov, {"CPROGRAMA", "WMSSAAS"})

    If !self:geraMovimentoTransferencia(aTransf, aWmsCMov)
        Return .F.
    EndIf
Return .T.

/*/{Protheus.doc} geraMovimenotransferencia
    Efetua a chamada do execauto de transferencia
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method geraMovimentoTransferencia(aTransf, aWmsCMov) Class WMSSaasEstoqueControleRequisicaoArmazem
Local cMsgErro  := ""
Local aLogAuto  := {}
Local nErros    := 0

Private lMSHelpAuto     := .T.
Private lAutoErrNoFile  := .T.
Private lMsErroAuto     := .F.

    MSExecAuto({|x,y,z| MATA261(x,y,z)}, aTransf, 3, aWmsCMov)
    If lMsErroAuto

        aLogAuto := GetAutoGRLog()
        For nErros := 1 To Len(aLogAuto)
            cMsgErro += aLogAuto[nErros]
        Next nErros

        self:convergencia:setErroIntegracao(cMsgErro)
        Return .F.        
    EndIf
Return .T.

/*/{Protheus.doc} geraBaixaRequisicaoArmazem
    Efetua a baixa da solicitacao ao armazem
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method geraBaixaRequisicaoArmazem(jRequisicaoArmazem, nRecnoSCP, nRecnoDC0) Class WMSSaasEstoqueControleRequisicaoArmazem
Local lFinalizado       := jRequisicaoArmazem['atendimentoFinalizado']
Local lAtendido         := jRequisicaoArmazem['atendido']
Local cProduto          := jRequisicaoArmazem['produto']
Local lRastro           := Rastro(cProduto,"L")
Local cLote             := If(lRastro .And. !(jRequisicaoArmazem['lote']==NIL),jRequisicaoArmazem['lote'],Space(nTamLote))
Local dDataValidade     := If(lRastro,jRequisicaoArmazem['dataValidade'],CtoD('  /  /  '))
Local nQuantSeparada    := jRequisicaoArmazem['quantidade']
Local cLocal            := Padr(jRequisicaoArmazem['tipoEstoque'],nTamLocal)
Local cIdReserva        := cValToChar(jRequisicaoArmazem['idReserva'])
Local cAliasDC0         := Nil
Local bCampo            := {|nCPO|	Field(nCPO)}
Local cStatus           := '2' /* 1=Gerado | 2=Concluido*/
Local nX                := 0
Local cOrigem           := 'S'
Local cNewReq           := ''     
Local oControleReq      := Nil
Local lRet              := .T.
Local cMsgErro          := ""
Local aLogAuto          := {}
Local nErros            := 0
Local cTmBaixa          := SuperGetMv("MV_WMSTMSA",.F.,"") // Tipo de movimento de sai­da para integracao SAAS

    DbSelectArea("SCP")
    SCP->( DbGoTo(nRecnoSCP) )
    nQujeAnt := SCP->CP_QUJE
    If nQuantSeparada > 0
        aBxSCP	:= {}
        aadd(aBxSCP,{"CP_NUM"  ,SCP->CP_NUM			,nil})
        aadd(aBxSCP,{"CP_ITEM" ,SCP->CP_ITEM		,nil})
        aadd(aBxSCP,{"CP_QUANT",nQuantSeparada      ,nil})

        aMata  := {}
        aadd(aMata,{"D3_TM"     ,cTmBaixa           ,Nil})
        aadd(aMata,{"D3_COD"    ,cProduto   		,nil})
        aadd(aMata,{"D3_LOCAL"  ,cLocal		        ,nil})
        aadd(aMata,{"D3_LOTECTL",cLote          	,nil})
        aadd(aMata,{"D3_DTVALID",dDataValidade      ,nil})
        aadd(aMata,{"D3_EMISSAO",dDataBase			,nil})

        Private lMSHelpAuto     := .T.
        Private lAutoErrNoFile  := .T.
        Private lMsErroAuto     := .F.
        MSExecAuto({|v,x,y,z,w| mata185(v,x,y,z,w)},aBxSCP,aMata,1,Nil,Nil)   // 1 = BAIXA (ROT.AUT)

        //--Verifica se realmente a baixa da solicitacao foi realizada
        SCP->(dbGoTo(nRecnoSCP))
        If SCP->CP_QUJE == nQujeAnt .And. !lMSErroAuto
            cMsgErro := STR0004
            self:convergencia:setMensagemIntegracao(cMsgErro)
            lRet := .F.
        EndIf

        If lRet .And. lMSErroAuto
            aLogAuto := GetAutoGRLog()

            For nErros := 1 To Len(aLogAuto)
                cMsgErro += aLogAuto[nErros]
            Next nErros

            self:convergencia:setMensagemIntegracao(cMsgErro)
            lRet := .F.
        EndIf
    
        If lRet
            /*  Busca o empenho WMS SaaS que quando a origem for 'S' desconta a quantidade separa e 
                quando encotrar origem 'N' cria novo empenho WMS SaaS e grava identificador da reserva*/
            oControleReq := WMSSaasEstoqueControleRequisicaoArmazem():loadByRecno(nRecnoDC0)
            cNewReq := oControleReq:getSequence()
            cOrigem := 'N'                

            DBSelectArea('DC0')
            DC0->(DbGoTo(nRecnoDC0))
            /* Salva a integridade dos campos de Bancos de Dados */
            For nX := 1 To FCount()
                M->&(EVAL(bCampo,nX)) := FieldGet(nX)
            Next nX

            /* Cria o registro de empenho com mesmos dados do original */
            DBSelectArea('DC0')
            RecLock("DC0",.T.)
            For nX	:=	1 To FCount()
                FieldPut(nX,M->&(EVAL(bCampo,nX)))
            Next nX

            /* Atualiza com a quantidade e lote separada */
            DC0->DC0_LOTECT := cLote
            DC0->DC0_DTVALD := dDataValidade
            DC0->DC0_QUANT  := nQuantSeparada
            DC0->DC0_ORIGEM := cOrigem
            DC0->DC0_SEQREQ := cNewReq
            DC0->DC0_IDRES  := cIdReserva
            DC0->(MsUnlock())
        
            /* Posiciona no empenho WMS SaaS com origem igual 'S' para ajustar as quantidade, eliminando quando zerado */
            DBSelectArea('DC0')
            DC0->(DbGoTo(nRecnoDC0))
            RecLock('DC0',.F.)
            If !lFinalizado .And. DC0->DC0_QUANT - nQuantSeparada > 0
                DC0->DC0_QUANT -= nQuantSeparada
            Else
                If lAtendido
                    DC0->(DBDelete())
                Else
                    DC0->DC0_STATUS := EXCLUIDA                
                EndIf
            EndIf    
            DC0->(MsUnlock())

            /* Quando finalizado busca todos os empenhos WMS SaaS de origem igual 'N' e atualiza o status para '2' = Concluido*/
            If lFinalizado
                cOrigem   := 'N'
                cAliasDC0 := GetNextAlias()
                BeginSql Alias cAliasDC0
                    SELECT DC0.R_E_C_N_O_ RECNODC0
                    FROM %Table:DC0% DC0
                    WHERE DC0.DC0_FILIAL   = %Exp:xFilial("DC0")%
                    AND DC0.DC0_NUMSA    = %Exp:SCP->CP_NUM%
                    AND DC0.DC0_ITEMSA   = %Exp:SCP->CP_ITEM%
                    AND DC0.DC0_PRODUT   = %Exp:SCP->CP_PRODUTO%
                    AND DC0.DC0_ORIGEM   = %Exp:cOrigem%
                    AND DC0.%NotDel%
                EndSql
                While (cAliasDC0)->(!Eof())
                    DC0->(DbGoTo((cAliasDC0)->RECNODC0))        
                    
                    RecLock("DC0",.F.)
                    DC0->DC0_STATUS := cStatus
                    DC0->(MsUnlock())
                    
                    (cAliasDC0)->(DbSkip())
                EndDo
                (cAliasDC0)->(DbCloseArea())
            EndIf
        EndIf

    EndIf

Return lRet

/*/{Protheus.doc} loadByRecno
    Instancia um novo objeto a partir do id
    @author Fagner Ferraz
    @since 03/09/2025
    /*/
Method loadByRecno(nRecno) Class WMSSaasEstoqueControleRequisicaoArmazem
Local aArea    := GetArea()
Local aAreaDC0 := DC0->(GetArea())  
    
    DbSelectArea("DC0")
    DC0->( DbGoTo(nRecno) )
    self:load()
    
    RestArea(aAreaDC0)
    RestArea(aArea)
Return self
