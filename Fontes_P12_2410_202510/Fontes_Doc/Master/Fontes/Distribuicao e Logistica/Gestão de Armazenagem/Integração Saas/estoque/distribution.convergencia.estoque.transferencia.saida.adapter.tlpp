#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.estoque.transferencia.saida.adapter.ch"

/*/{Protheus.doc} WMSSaasEstoqueTransferenciaSaidaAdapter
   Classe para tratamento dos retornos de manufatura
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @obs Classe para gerenciamento das transacoes na tabela
   de convergencia WMS Saas x Protheus
   /*/
Class WMSSaasEstoqueTransferenciaSaidaAdapter from WMSSaasConvergenciaAdapter
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Static Method finaliza(oJsonBody as json) as logical    
    Public Method montaPayloadGetAllEstoqueTransferenciaSaida() as json
    Public Method montaPayloadGetFullEstoqueTransferenciaSaida() as json
    Static Method getCaracteristicas() as json
EndClass

/*/{Protheus.doc} getAll
   Busca cabecalho das requisicoes
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oEstoqueTransferenciaSaida, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasEstoqueTransferenciaSaidaAdapter as json
Local oEstoqueTransferenciaSaida := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaSaida(), nPage, nPageSize) 
   self:montaPayloadGetAllEstoqueTransferenciaSaida()
Return oEstoqueTransferenciaSaida

/*/{Protheus.doc} montaPayloadGetAllEstoqueTransferenciaSaida
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetAllEstoqueTransferenciaSaida() Class WMSSaasEstoqueTransferenciaSaidaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "documento": Alltrim(item['documento']),;
         "dadosExternos": {;
            "id": item['id'];
         };
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} getById
   Busca completa de requisicoes pelo Id
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oEstoqueTransferenciaSaida, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasEstoqueTransferenciaSaidaAdapter as json
Local oEstoqueTransferenciaSaida := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullEstoqueTransferenciaSaida()
Return oEstoqueTransferenciaSaida

/*/{Protheus.doc} montaPayloadGetFullEstoqueTransferenciaSaida
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullEstoqueTransferenciaSaida() Class WMSSaasEstoqueTransferenciaSaidaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens            := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": item['armazem'],;
      "quantidade": item['quantidade'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })

Return self:oJsonObj:oJsonObj := {;
   "codigoOrdemProducao": currentJsonObject['documento'],;
   "referenciaProducao": "Transferência Interna de Saída",;
   "produtos": aItens,;
   "dadosExternos":{;
      "id":currentJsonObject['id'];
   } }

/*/{Protheus.doc} getCaracteristicas
   Monta as caracteristicas do item no payload
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param item, object, item do recebimento
   @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasEstoqueTransferenciaSaidaAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
               "valor": item['lote'];
            })
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
               "valor": item['dataValidade'];
            })
   EndIf
Return caracteristicas

/*/{Protheus.doc} finaliza
   Metodo para tratar a finalizacao da Transferencia Saida (requisicao)
   @author Roselaine
   @since 24/02/2025
   @version version
   @param oJsonBody, object
   {
   } 
   @return logico
   /*/
Method finaliza(oJsonBody) Class WMSSaasEstoqueTransferenciaSaidaAdapter as logical
Local oEstoqueTransferenciaSaida  := WMSSaasEstoqueTransferenciaSaida():new()
Local oLogError              := Nil
Local bError                 := Nil
Local lRet                   := .F.
Local oErrorReq              := WMSSaasRestError():new()
   bError := ErrorBlock( { |oLogError| oErrorReq := WMSSaasErrorBlockAdapter(oLogError,@oErrorReq) } )

   oEstoqueTransferenciaSaida:loadById(oJsonBody['dadosExternos']['id'])
   If Empty(oEstoqueTransferenciaSaida:id)
      Return .F.
   EndIf

   lRet := oEstoqueTransferenciaSaida:canBeFinished()
   If !lRet
      oErrorReq:setStatus(400)
      oErrorReq:setDescription(STR0001) //"A separação da transferência de saída não pode ser realizada. Verifique se o registro já está finalizado no ERP."
   Else
      oEstoqueTransferenciaSaida:finaliza() 
   EndIf

   If !lRet
      Break(oErrorReq)
   EndIf

   ErrorBlock( bError )
Return lRet


