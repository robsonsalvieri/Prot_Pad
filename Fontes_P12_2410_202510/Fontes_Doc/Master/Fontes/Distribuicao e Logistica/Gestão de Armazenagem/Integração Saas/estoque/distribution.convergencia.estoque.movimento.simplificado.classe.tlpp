#Include "totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.estoque.movimento.simplificado.classe.ch"

Static nTamProd  := TamSx3('D3_COD')[1]
Static nTamLocal := TamSx3('D3_LOCAL')[1]
Static nTamLote  := TamSx3('D3_LOTECTL')[1]

/*/{Protheus.doc} WMSSaasMovimentoSimplificado
Classe de Movimento Simplificado Wms Saas
@type class
@author Alexsander Burigo Corrêa
@since 16/09/2024
/*/
Class WMSSaasMovimentoSimplificado From WMSSaasConvergencia implements WMSSaasConvergenciaInterface
    Public Method new(cIdSaas as character, dDataMovSimplificado as date)
    Public Method loadOrNew(cIdSaas as character, dDataMovSimplificado as date)
    Public Method loadByChave(cIdSaas as character) as object
    Public Method canBeUpdated() as logical
    Public Method setLiberado() as object
    Public Method finaliza(jMovSimplificado as json ) as logical
    Public Method geraMovimentoSimplificado(aCab as array, aItens as array) as logical
    Public Method getNomePessoa() as character
EndClass

/*/{Protheus.doc} loadOrNew
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 16/09/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method loadOrNew(cIdSaas as character, dDataMovSimplificado as date) Class WMSSaasMovimentoSimplificado
local oLoad := self:loadByChave(cIdSaas)
    If !Empty(oLoad:id)
        Return oLoad
    EndIf

Return self:new(cIdSaas, dDataMovSimplificado)

/*/{Protheus.doc} WMSSaasInventario::New
Carga das propriedades do cabeçalho da classe de convergência
@type method
@author Alexsander Burigo Corrêa
@since 16/09/2024
@param cIdSaas, character, Identificador do documento SAAS
@param dEmissao, date, Data do Movimento
/*/
Method new(cIdSaas, dDataMovSimplificado ) Class WMSSaasMovimentoSimplificado
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoEstoqueMovimentoSimplificado()
    self:IdSaas              := cIdSaas
    self:numeroDocumento     := ""
    self:clienteOuFornecedor := ""
    self:loja                := ""
    self:dataCriacao         := FwTimeStamp(3,dDataBase, Time())
    self:dataEmissao         := dDataMovSimplificado
    self:transportador       := ""
    self:sequenciaIntegracao := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self


/*/{Protheus.doc} loadByChave
    Faz o load da classe de estoque e convergencia pela chave
    Esse metodo deve retornar apenas um registro da DBZ sempre, pois cada identificador de movimento simplificado só pode ter uma DBZ. Os registros de movimento simplificado não possuem DBY.
    @author Alexsander Burigo Corrêa
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method loadByChave(cIdSaas) Class WMSSaasMovimentoSimplificado
Local cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoEstoqueMovimentoSimplificado()%
		   AND DBZ.DBZ_IDSAAS = %Exp:cIdSaas%
		   AND DBZ.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)        
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais os registros de estoque podem ser alterados. No movimento simplificado, só permite o reprocessamento quando status de erro```
    @author Alexsander Burigo Corrêa
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeUpdated() as logical Class WMSSaasMovimentoSimplificado
Return self:isErroIntegracao()

/*/{Protheus.doc} setLiberado
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method setLiberado() Class WMSSaasMovimentoSimplificado
Return self

/*/{Protheus.doc} finaliza
    Metodo de finalizacao do movimento simplificado
    @author Alexsander Burigo Corrêa
    @since 26/04/2024
    @version version
    @param jMovSimplificado, array, array de json com item movimentado
    {;
        "idSaas"              : "b94e524b-04fa-48ad-abf8-9e2713fef10f",;
        "produto"             : "MAGICMOUSE",;
        "tipoEstoque"         : "01",;
        "origem"              : "SAIDA_SIMPLIFICADA",;
        "quantidade"          : 1,;                        
        "lote"                : "LOTE1",;
        "dataValidade"        : "2025-07-16",;
        "dataMovSimplificado" : "2024-07-19";
    }
    @return return_var, return_type, return_description
/*/
Method finaliza(jMovSimplificado) Class WMSSaasMovimentoSimplificado as logical
Local cItem
Local cNumSeq
Local cUM
Local cLote
Local nQuantidade    := 0
Local lRastro        := .F.
Local aCab           := {}
Local aItens         := {}
Local cIdSaas        := ""
Local cTipEstoque    := ""
Local cOrigem        := ""
local cTm            := ""
Local cObservacao
Local dDataMovSimplificado
Local dDataValidade
Local cCCMovimento   := SuperGetMv("MV_WMSCCMO",.F.,"") // MV_WMSCCMO - Centro de custo  para integração SAAS
Local cTmEntrada     := SuperGetMv("MV_WMSTMEN",.F.,"") // MV_WMSTMEN - Tipo de movimento de entrada para integração SAAS
Local cTmSaida       := SuperGetMv("MV_WMSTMSA",.F.,"") // MV_WMSTMSA - Tipo de movimento de saída para integração SAAS

Private lMsErroAuto := .F. 

    DbSelectArea("SD3")
    SD3->(DbSetOrder(1))
    
    DBSelectArea("SB1")
    SB1->(DbSetOrder(1))

    DBSelectArea("SB2")
    SB2->(DbSetOrder(1))

    DBSelectArea("NNR")
    NNR->(DbSetOrder(1))

    If Empty(cTmEntrada) .Or. Empty(cTmSaida)
        self:setMensagemIntegracao(STR0001) // Parâmetro de TM da movimentação interna (MV_WMSTMEN/MV_WMSTMSA) não preenchido.
        Return .F.
    EndIf    
        
    cIdSaas              := jMovSimplificado['idSaas']
    cItem                := Padr(jMovSimplificado['produto'],nTamProd)
    cTipEstoque          := Padr(jMovSimplificado['tipoEstoque'],nTamLocal)
    cOrigem              := AllTrim(jMovSimplificado['origem'])
    nQuantidade          := jMovSimplificado['quantidade']
    lRastro              := Rastro(cItem,"L")
    dDataMovSimplificado := jMovSimplificado['dataMovSimplificado']
    cObservacao          := jMovSimplificado['idSaas']
    cLote                := If(lRastro .And. !(jMovSimplificado['lote']==NIL),Padr(jMovSimplificado['lote'],nTamLote),Space(nTamLote))
    dDataValidade        := If(lRastro,jMovSimplificado['dataValidade'],CtoD('  /  /  '))

    If !SB1->(MsSeek(xFilial("SB1") + cItem))
        self:setMensagemIntegracao(STR0002 + " "+ AllTrim(cItem) + " "+ STR0003) // Produto XXX não encontrado na filial.
        Return .F.
    EndIf

    If !NNR->(MsSeek(xFilial("NNR") + cTipEstoque))
        self:setMensagemIntegracao(STR0004 + " " + AllTrim(cTipEstoque) + " " + STR0003) // Armazem XXX não encontrado na filial.
        Return .F.
    EndIf
    cUM := SB1->B1_UM

    If !SB2->(DbSeek(xFilial("SB2") + cItem + cTipEstoque))
        CriaSB2(cItem, cTipEstoque, xFilial("SB2"))
    EndIf

    // Cabeçalho do documento
    cNumSeq := SD3->(ProxNum())
    If cOrigem == "SAIDA_SIMPLIFICADA"
       cTm := cTmSaida
    Else
       cTm := cTmEntrada
    EndIf

    // Cabeçalho
    aCab := {}
    aAdd(aCab,{"D3_DOC"     , cNumSeq              , NIL})
    aAdd(aCab,{"D3_CC"      , cCCMovimento         , NIL})
    aAdd(aCab,{"D3_TM"      , cTm                  , NIL})
    aAdd(aCab,{"D3_EMISSAO" , dDataMovSimplificado , NIL})

    // item do documento
    aAdd(aItens,{})
    aAdd(aTail(aItens),{"D3_COD"    , cItem              , NIL})
	aAdd(aTail(aItens),{"D3_UM"     , cUM                , NIL})
	aAdd(aTail(aItens),{"D3_QUANT"  , nQuantidade        , NIL})
	aAdd(aTail(aItens),{"D3_LOCAL"  , cTipEstoque        , NIL})
	aAdd(aTail(aItens),{"D3_OBSERVA", cObservacao        , NIL})
    aAdd(aTail(aItens),{"D3_LOTECTL", cLote              , Nil})
    aAdd(aTail(aItens),{"D3_DTVALID", dDataValidade      , Nil})

    If !self:geraMovimentoSimplificado(aCab,aItens)
        Return .F.
    EndIf
    // Atribui no convergência o número do documento gerado no SD3 para ligação o identificador SAAS
    self:numeroDocumento := cNumSeq
    self:save()
    self:setFinalizado()
    self:setMensagemIntegracao(STR0005) // Movimento simplificado gerado com sucesso
Return .T.

/*/{Protheus.doc} getNomePessoa
    (long_description)
    @author user
    @since 24/05/2024
    @version version
    @return cRet, caracter, Retorna vazio por não se tratar de um método obrigatório
    /*/
Method getNomePessoa() Class WMSSaasMovimentoSimplificado as character
Return ""

/*/{Protheus.doc} geraMovimenoSimplificado
(aCab, aItens)
    @author user
    @since 23/09/2024
    @version version
    @param aCab, array, Cabeçalho da movimentação
    @param aItens, array, Itens para movimentação
    @return lRet, logical, Indica se movimentação foi realizada com sucesso
    /*/
Method geraMovimentoSimplificado(aCab, aItens) Class WMSSaasMovimentoSimplificado as logical
Local nAux     := 0
Local aLogAuto
Local cLogTxt  := ""
Local lRet     := .T.

Private _acod          := {"1","MP1"}
Private lMsHelpAuto    := .F.
Private lAutoErrNoFile := .T.

    lMsErroAuto := .F.

    MSExecAuto({|x,y,z| MATA241(x,y,z)},aCab,aItens,3)
    If lMsErroAuto
        aLogAuto := GetAutoGRLog()

        For nAux := 1 To Len(aLogAuto)
            cLogTxt += aLogAuto[nAux] + CRLF
        Next
        self:setErroIntegracao(cLogTxt)
        lRet := .F.
    EndIf
Return lRet
