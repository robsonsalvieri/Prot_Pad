#include "tlpp-core.th"
#include "distribution.convergencia.ch"

/*/{Protheus.doc} WMSSaasEstoqueTransferenciaEntradaAdapter
   Classe para tratamento dos retornos de manufatura
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @obs Classe para gerenciamento das transacoes na tabela
   de convergencia WMS Saas x Protheus
   /*/
Class WMSSaasEstoqueTransferenciaEntradaAdapter from WMSSaasConvergenciaAdapter
    Public Method new() as object
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Public Method finaliza(cId as character, oJsonBody as json) as logical
    Public Method montaPayloadGetAllEstoqueTransferenciaEntrada() as json
    Public Method montaPayloadGetFullEstoqueTransferenciaEntrada() as json
    Static Method getCaracteristicas() as json
EndClass

Method new() Class WMSSaasEstoqueTransferenciaEntradaAdapter
Return self

/*/{Protheus.doc} getAll
   Busca cabecalho das requisicoes
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oEstoqueTransferenciaEntrada, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasEstoqueTransferenciaEntradaAdapter as json
Local oEstoqueTransferenciaEntrada := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaEntrada(), nPage, nPageSize) 
   self:montaPayloadGetAllEstoqueTransferenciaEntrada()
Return oEstoqueTransferenciaEntrada

/*/{Protheus.doc} montaPayloadGetAllEstoqueTransferenciaEntrada
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetAllEstoqueTransferenciaEntrada() Class WMSSaasEstoqueTransferenciaEntradaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "documento": Alltrim(item['documento']),;
         "dadosExternos": {;
            "id": item['id'];
         };
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} getById
   Busca completa de requisicoes pelo Id
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oEstoqueTransferenciaEntrada, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasEstoqueTransferenciaEntradaAdapter as json
Local oEstoqueTransferenciaEntrada := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullEstoqueTransferenciaEntrada()
Return oEstoqueTransferenciaEntrada

/*/{Protheus.doc} montaPayloadGetFullEstoqueTransferenciaEntrada
   Monta o payload conforme contrato WMS Saas
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullEstoqueTransferenciaEntrada() Class WMSSaasEstoqueTransferenciaEntradaAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens            := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": item['armazem'],;
      "quantidade": item['quantidade'],;
      "caracteristicas": self:getCaracteristicas(item); 
      }) ;
   })

Return self:oJsonObj:oJsonObj := {;
   "codigo": currentJsonObject['documento'],;
   "referenciaProducao": "TransferÃªncia Interna de Entrada",;
   "itens": aItens,;
   "dadosExternos":{;
      "id":currentJsonObject['id'];
   } }

/*/{Protheus.doc} getCaracteristicas
   Monta as caracteristicas do item no payload
   @author Alexsander Burigo Correa
   @since 19/02/2025
   @version 1.0
   @param item, object, item do recebimento
   @return caracteristicas, json, caracteristica formatada.
   /*/
Method getCaracteristicas(item) Class WMSSaasEstoqueTransferenciaEntradaAdapter as json
Local caracteristicas := {}
   If !Empty(item['lote'])
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
               "valor": item['lote'];
            })
      aAdd(caracteristicas, {;
               "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
               "valor": item['dataValidade'];
            })
   EndIf
Return caracteristicas

/*/{Protheus.doc} finaliza
   Metodo para tratar a finalizacao da Transferencia Entrada (devolucao)
   @author Roselaine
   @since 24/02/2025
   @version version
   @param cId, character
   @param oJsonBody, object
   {
   } 
   @return logico
   /*/
Method finaliza(cId, oJsonBody) Class WMSSaasEstoqueTransferenciaEntradaAdapter as logical
Local oWMSSaasEstoqueTransferenciaEntrada := WMSSaasEstoqueTransferenciaEntrada():new()

   oWMSSaasEstoqueTransferenciaEntrada:loadById(cId)
   oWMSSaasEstoqueTransferenciaEntrada:setFinalizado() 

Return .T.
