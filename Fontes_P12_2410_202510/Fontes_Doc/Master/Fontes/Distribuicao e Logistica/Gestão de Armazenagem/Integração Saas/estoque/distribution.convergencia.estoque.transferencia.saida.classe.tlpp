#Include "Totvs.ch"
#include "tlpp-core.th"


/*/{Protheus.doc} WMSSaasEstoqueTransferenciaSaida
Classe de Estoque Transferencia Saida (MATA261)
@type class
@author Equipe WMS Protheus
@since 18/02/2025
/*/
Class WMSSaasEstoqueTransferenciaSaida From WMSSaasConvergencia
    Public Method new(cNumeroDocumento as character, dDataEmissao as date) as object
    Public Method parseItemConvergencia(cProduto as character, nQuantidade as numeric, cArmazem as character, cLote as character, dDataValidade as date, cNumSeq as character) as logical
    Public Method canBeUpdated() as logical
    Public Method canBeFinished() as logical
    Public Method finaliza() as logical
    Public Method loadByItem(cNumeroDocumento as character, cNumSeq as character, cLocal as character) as object
    Public Method loadOrNew(cNumeroDocumento as character, dDataEmissao as date) as object
    Public Method loadByChave(cNumeroDocumento as character) as object  
EndClass

/*/{Protheus.doc} loadOrNew
Faz o load ou instancia a convergencia
@author Fagner Ferraz
@since 25/02/2025
/*/
Method loadOrNew(cNumeroDocumento, dDataEmissao) as object Class WMSSaasEstoqueTransferenciaSaida
local oLoad := self:loadByChave(cNumeroDocumento)
    If !Empty(oLoad:id)
        Return oLoad
    EndIf

Return self:new(cNumeroDocumento, dDataEmissao)

/*/{Protheus.doc} new
Instancia as propriedades da convergencia
@author Fagner Ferraz
@since 19/02/2025
/*/
Method new(cNumeroDocumento, dDataEmissao) Class WMSSaasEstoqueTransferenciaSaida
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoEstoqueTransferenciaSaida()
    self:numeroDocumento     := cNumeroDocumento
    self:dataCriacao         := FWTimeStamp(3, dDataBase, Time())
    self:dataEmissao         := dDataEmissao
    self:sequenciaIntegracao := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} loadByChave
Faz o load da convergencia
@author Fagner Ferraz
@since 25/02/2025
/*/
Method loadByChave(cNumeroDocumento) as object Class WMSSaasEstoqueTransferenciaSaida
Local cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
	    FROM %Table:DBZ% DBZ
		WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		    AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaSaida()%
		    AND DBZ.DBZ_NUMDOC = %Exp:cNumeroDocumento%
		    AND DBZ.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} parseItemConvergencia
Alimenta a propriedade itensConvergencia
@author Fagner Ferraz
@since 25/02/2025
/*/
Method parseItemConvergencia(cProduto, nQuantidade, cArmazem, cLote, dDataValidade, cNumSeq) Class WMSSaasEstoqueTransferenciaSaida
Local aArea  := GetArea() 
    
        AAdd(self:itensConvergencia, WMSSaasEstoqueItemTransferenciaSaida():set(;
                self:numeroDocumento,;
                cProduto,;
                nQuantidade,;
                cArmazem,;
                cLote,;
                dDataValidade,;
                cNumSeq;
                ))
   
    RestArea(aArea)
Return .T.

/*/{Protheus.doc} canBeUpdated
    Retorna se o estoque transferencia Saida pode ser alterado
    @author Equipe WMS Protheus
    @since 17/02/2025
    @return logico
/*/
Method canBeUpdated() as logical Class WMSSaasEstoqueTransferenciaSaida
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} canBeFinished
    Retorna se estoque trasnferencia saida pode ser finalizado
    @author user
    @since 17/10/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeFinished() as logical Class WMSSaasEstoqueTransferenciaSaida
Return self:isIntegrado()

/*/{Protheus.doc} loadByItem
    Busca pelo item da transferencia/DBY
    @author Equipe WMS Protheus
    @since 17/02/2025
    @param cNumeroDocumento, character, D3_NUMDOC/DBZ_NUMDOC
    @param cNumSeq, character, D3_NUMSEQ/DBY_NUMSEQ
    @return self, objeto, convergencia
/*/
Method loadByItem(cNumeroDocumento as character, cNumSeq as character, cLocal as character) as object Class WMSSaasEstoqueTransferenciaSaida
Local cId := WMSSaasEstoqueItemTransferenciaSaida():getIdByItem(cNumeroDocumento, cNumSeq, cLocal)

    If !Empty(cId)
        self:loadById(cId)
    Else
        return self:new()
    EndIf

Return self

/*/{Protheus.doc} finaliza
    Metodo de finalizacao de transferencia de saida 
    @author roselaine
    @since 07/03/2025
    @version 1.0
    /*/
Method finaliza() Class WMSSaasEstoqueTransferenciaSaida
    self:setFinalizado()
Return .T.
