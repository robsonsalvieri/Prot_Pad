#include 'totvs.ch'
#include 'parmtype.ch'
#include 'TopConn.ch'
#include "tlpp-core.th"

#define A2_NOME 1
#define A2_TIPO 2
#define A2_CGC 3
#define A2_INSCR 4
#define A2_EST 5
#define JURIDICA "J"
#define FISICA "F"
#define ESTRANGEIRO "E"

//-------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasGetTipoPessoa
Busca tipo da pessoa pelo tipo exposto no A1/A2_TIPO
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Static Function WMSSaasGetTipoPessoa(cKey, cDocumento) as character
Local cVal
   oHash := tHashMap():new()
   oHash:Set(JURIDICA,"JURIDICA")
   oHash:Set(FISICA,"FISICA")
   oHash:Set(ESTRANGEIRO,"ESTRANGEIRO")
   lRet := oHash:Get(cKey, cVal)

   // Destruindo objeto para evitar MemoryLeak
   FwFreeObj(oHash)
   If (!lRet)
      Return WMSSaasGetTipoPessoaByDocument(cDocumento)
   Endif
Return cVal
//-------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasFornecedorAdapter
Classe Adapter para montar fornecedores
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Class WMSSaasFornecedorAdapter FROM FWAdapterBaseV2
	Static Method montaPayload() as json
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} MontaPayload
Metodo est√°tico para retornar payload de fornecedor para cadastro no Saas.
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Method montaPayload(cCodigo, cLoja, lUsaCliente) Class WMSSaasFornecedorAdapter as json
Local nTamCodFor  := TamSx3("A2_COD")[1]
Local nTamLojFor  := TamSx3("A2_LOJA")[1]
Local nTamCodCli  := TamSx3("A1_COD")[1]
Local nTamLojCli  := TamSx3("A1_LOJA")[1]
Local aFornecedor := if(lUsaCliente, ;
   GetAdvFVal("SA1", { "A1_NOME", "A1_TIPO", "A1_CGC", "A1_INSCR", "A1_EST" }, xFilial("SA1")+Padr(cCodigo,nTamCodCli)+Padr(cLoja,nTamLojCli), 1, { "", "", "", "", "" }),;
   GetAdvFVal("SA2", { "A2_NOME", "A2_TIPO", "A2_CGC", "A2_INSCR", "A2_EST" }, xFilial("SA2")+Padr(cCodigo,nTamCodFor)+Padr(cLoja,nTamLojFor), 1, { "", "", "", "", "" }))
Local tipoPessoa := WMSSaasGetTipoPessoa(Alltrim(aFornecedor[A2_TIPO]), Alltrim(aFornecedor[A2_CGC]))

Return {;
      "nome": Alltrim(aFornecedor[A2_NOME]),;
      "tipoPessoa": tipoPessoa,;
      "documentoIdentificacao": Alltrim(aFornecedor[A2_CGC]),;
      "inscricaoEstadual": Alltrim(aFornecedor[A2_INSCR]),;
      "endereco":{;
         "unidadeFederativa": Alltrim(UPPER(aFornecedor[A2_EST]));
      },;
      "chaveExterna":{;
         "codigo": Alltrim(cCodigo),;
         "loja": Alltrim(cLoja);
      };
   }
