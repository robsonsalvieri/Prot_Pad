#include 'totvs.ch'
#include 'parmtype.ch'
#include 'TopConn.ch'
#include "tlpp-core.th"

#define B1_CODBAR 1
#define B1_UM 2
#define B1_SEGUM 3
#define B1_CONV 4
#define B1_TIPCONV 5
#define B1_PESO 6
#define B1_CODGTIN 7
#define B1_COD 8
#define B1_DESC 9
#define B1_GRUPO 10
#define B5_ECPROFU 1
#define B5_ECCOMP 2
#define B5_ECLARGU 3

//-------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasProdutoAdapter
Classe Adapter para montar produtos
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Class WMSSaasProdutoAdapter FROM FWAdapterBaseV2
	Static Method montaPayload(cProduto) as json
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} MontaPayload
Metodo estatico para retornar payload de produto para cadastro no Saas.
@author  Filipe Mendes
@since 23/04/2024  
/*/
//-------------------------------------------------------------------
Method montaPayload(cProduto) Class WMSSaasProdutoAdapter as json
Local caracteristicas   := {}
Local nTamProd          := TamSx3("B1_COD")[1]
Local nTamCompl         := TamSx3("B5_COD")[1]
Local nTamUniMed        := TamSx3("AH_UNIMED")[1]
Local nTamGrupo         := TamSx3("BM_GRUPO")[1]
Local aProduto          := GetAdvFVal("SB1", { "B1_CODBAR", "B1_UM", "B1_SEGUM", "B1_CONV", "B1_TIPCONV","B1_PESO","B1_CODGTIN", "B1_COD", "B1_DESC", "B1_GRUPO" }, xFilial("SB1")+Padr(cProduto,nTamProd), 1, { "", "", "", 0, "", 0, "", "", "" })
Local aComplemento      := GetAdvFVal("SB5", { "B5_ECPROFU", "B5_ECCOMP", "B5_ECLARGU" }, xFilial("SB5")+Padr(cProduto,nTamCompl), 1, { 0, 0, 0 })
Local aSkus             := {}
Local aRetPe            := {}
Local oJson             := Nil

   If Rastro(aProduto[B1_COD])
      aAdd(caracteristicas, { "propriedade": "lote" } )
      aAdd(caracteristicas, { "propriedade": "dataValidade" } )
   EndIf

   aSkus:={;
      {;
         "descricao": Alltrim(GetAdvFVal("SAH","AH_DESCPO",xFilial("SAH")+Padr(aProduto[B1_UM],nTamUniMed),1)),;
         "quantidadeUnidadesProduto": 1,;
         "codigosBarras":{;
            Alltrim(aProduto[B1_CODBAR]);
         },;
         "peso": aProduto[B1_PESO],;
         "dimensao":{;
            "altura": aComplemento[B5_ECPROFU],;
            "largura": aComplemento[B5_ECLARGU],;
            "comprimento": aComplemento[B5_ECCOMP];
         };
      };
   }     

   If ExistBlock("WMSSPSKU")
      aRetPe := ExecBlock('WMSSPSKU',.F.,.F.,{aProduto[B1_COD], aSkus})
      If (ValType(aRetPe)) == "A" .AND. !Empty(aRetPe)
            aSKUS := aRetPe
      EndIf
   EndIf

   //Remove as propriedades de peso e dimensao quando estao com valor zero
   //O WMS SAAS impede a integracao nesta condicao
   oJson := aSkus[1]

   If oJson['peso'] == 0
      oJson:DelName('peso')
   EndIf

   If ( oJson['dimensao']['altura'] + oJson['dimensao']['largura'] + oJson['dimensao']['comprimento'] ) == 0
      oJson:DelName('dimensao')
   EndIf

   aSkus := {oJson}

Return {;
        "chaveExterna": Alltrim(aProduto[B1_COD]),;
        "codigo": Alltrim(aProduto[B1_COD]),;
        "unidadeMedida": Alltrim(aProduto[B1_UM]),;
        "descricaoComercial": Alltrim(aProduto[B1_DESC]),;
        "descricaoInterna": Alltrim(aProduto[B1_DESC]),;
        "caracteristicas": caracteristicas ,;
        "skus": aSkus ,;
        "categoria":{ ;
           "descricao": Alltrim(GetAdvFVal("SBM","BM_DESC",xFilial("SBM")+Padr(aProduto[B1_GRUPO],nTamGrupo),1)) ;
        };
}
