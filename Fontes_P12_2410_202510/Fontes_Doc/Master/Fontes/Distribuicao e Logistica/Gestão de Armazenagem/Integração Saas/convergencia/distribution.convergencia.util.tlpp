#Include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.util.ch"

#define JURIDICA "JURIDICA"
#define FISICA "FISICA"
#define ESTRANGEIRO "ESTRANGEIRO"
#define TAMANHO_CPF 11
//---------------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasUtil
Funções auxiliares para o processo de convergência.

@author Filipe Mendes
@since 10/04/2024
@version 1.0
@obs  Este fonte deve conter apenas funções auxiliares para o processo de convergência.
A tabela de convergência e os processos que implementarão essa integração devem ter suas própria classes.
/*/
//---------------------------------------------------------------------------

//---------------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasInt
Valida se produto + armazém controla WMS SaaS para tratativas de integracao

@author Filipe Mendes
@since 10/04/2024
@version 1.0
@params cProduto - Produto a ser checkado, cArmazem - Armazem a ser checkado
@return boolean, true se o produto + armazém integra com o Saas
@obs
/*/
//---------------------------------------------------------------------------
Function WMSSaasInt(cProduto, cArmazem)

Local nTamProduto := TamSx3("D1A_COD")[1]
Local nTamLocal   := TamSx3("NNR_CODIGO")[1]
Local lIntegra    := .F.

Default cProduto  := ''
Default cArmazem  := ''

    If D1A->( FieldPos("D1A_WSAAS") ) > 0 .And. NNR->( FieldPos("NNR_WSAAS") )
        lIntegra := GetAdvFVal("D1A","D1A_WSAAS",xFilial("D1A") + Padr(cProduto,nTamProduto),1,.F.) 
        If lIntegra .And. !Empty(cArmazem)
            lIntegra := GetAdvFVal("NNR","NNR_WSAAS",xFilial("NNR") + Padr(cArmazem,nTamLocal),1,.F.) 
        EndIf
    EndIf
    If lIntegra .AND. Rastro(cProduto,"S")
        lIntegra := .F. 
    EndIf

Return lIntegra

/*/{Protheus.doc} WMSSaasHas
Valida se ambiente possui o parametro WMS Saas habilitado e as tabelas de convergencia
@type function
@author fagner.ferraz
@since 4/22/2024
@return boolean, true se o ambiente estiver preparado para integração com o Saas
/*/
Function WMSSaasHas()
Return SuperGetMV("MV_WMSSAAS",.F.,.F.) .And. FWAliasInDic("DBZ") .And. FWAliasInDic("DBY")

//---------------------------------------------------------------------------
/*/{Protheus.doc} WMSSaasTrataDados
Valida entrada e transforma conforme necessidade do tipo
C - Faz o Alltrim
D - Transforma em timestamp no formato AAAA-MM-DDTHH:MM:SS-03:00(TIMEZONE)

@author Filipe Mendes
@since 10/04/2024
@version 1.0
@params cProduto - Produto a ser checkado, cArmazem - Armazem a ser checkado
@return boolean, true se o produto + armazem integra com o Saas
@obs
/*/
//---------------------------------------------------------------------------
Function WMSSaasTrataDados(xVal as variant) as variant
Local xResult := xVal
    
    Do Case
        Case ValType(xVal) == "C"
            xResult := Alltrim(xVal)
        Case ValType(xVal) == "D"
            xResult := FWTimeStamp(5, xVal, "00:00")
    EndCase
Return xResult

/*/{Protheus.doc} WMSSaasGetTipoPessoaByDocument
    Busca o tipo de pessoa de acordo com o documento
    @type  Function
    @author Filipe Mendes
    @since 24/04/2024
    @version version
    @param cDocumento, C, Documento para analise
    @return tipoPessoa, C, Tipo de Pessoa no Saas.
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSaasGetTipoPessoaByDocument(cDocumento) as character
Local cTipoDocto := ESTRANGEIRO

   If (Len(cDocumento) > TAMANHO_CPF)
      cTipoDocto := JURIDICA
   ElseIf (!Empty(cDocumento))
      cTipoDocto := FISICA
   EndIf
Return cTipoDocto

/*/{Protheus.doc} WMSSDesTra
    (long_description)
    @type  Function
    @author user
    @since 23/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSDesTra(cTipoTransacao as character) as character
Local cTipoTransac := STR0001 // Não implementado
    Do Case
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoRecebimento()
            cTipoTransac := STR0002 // RECEBIMENTO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoPedidoVenda()
            cTipoTransac := STR0003 // PEDIDO VENDA
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoFaturamentoPedidoVenda()
            cTipoTransac := STR0004 // FATURAMENTO PEDIDO VENDA
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueInventario()
            cTipoTransac := STR0006 // ESTOQUE INVENTARIO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueMovimentoSimplificado()
            cTipoTransac := STR0007 // MOVIMENTO SIMPLIFICADO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueMovimentoTransferencia()
            cTipoTransac := STR0008 // MOVIMENTO TRANSFERENCIA
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoFaturamentoAntecipado()
            cTipoTransac := STR0005 // FATURAMENTO ANTECIPADO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao()
            cTipoTransac := STR0009 // MANUFATURA REQUISICAO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoManufaturaApontamentoProducao()
            cTipoTransac := STR0010 // MANUFATURA APONTAMENTO PRODUCAO
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaEntrada()
            cTipoTransac := STR0011 // ESTOQUE TRANSFERENCIA ENTRADA
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueTransferenciaSaida()
            cTipoTransac := STR0012 // ESTOQUE TRANSFERENCIA SAIDA 
        Case cTipoTransacao == WMSSaasConvergencia():getTipoTransacaoEstoqueRequisicaoArmazem()
            cTipoTransac := STR0013 // ESTOQUE REQUISICAO ARMAZEM 
    EndCase
Return cTipoTransac

/*/{Protheus.doc} WMSSaasGetNomePessoa
    (long_description)
    @type  Function
    @author user
    @since 23/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSaasGetNomePessoa(cTipoTransacao as character, cId as character) as character
Local oClasse := WMSSaasConvergenciaFactory():get(cTipoTransacao)
    oClasse:loadById(cId)
Return oClasse:getNomePessoa()
/*/{Protheus.doc} WMSSaasLiberaRegistro
    Libera registro pelo ID
    @type  Function
    @author user
    @since 27/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
    /*/
Function WMSSLibReg(cTipoTransacao as character, cId as character) as logical  
Local oClasse := WMSSaasConvergenciaFactory():get(cTipoTransacao)
    oClasse:loadById(cId)
return oClasse:canBeUpdated() .And. !Empty(oClasse:setLiberado())


//============================================================================\
/*/{Protheus.doc}WMSSFilter
  ==============================================================================
    @description
    Implementação da Função Filter para ADVPL
    O método filter() cria um novo array com todos os elementos que passaram 
    no teste implementado pela função fornecida.

    Inspirado no ArrayUtils.filter do Javascript moderno
    https://developer.mozilla.org/pt-BR/docs/Web/JavaScript/Reference/Global_Objects/Array/filtro

    @author Filipe Mendes

    @version 1.0
    @since 12/07/2024

    @param aOrigin, Array, Array Original a ser avaliado
    @param bCallback, Bloco de Código, Função que ao ser executada por Eval() testa se o elemento
        deverá compor o novo Array.
    
    @return Array, Novo Array com os elementos que passaram no teste.

    @obs
        O Callback Recebe três argumentos:
        1 - uValue, Qualquer, O elemento que está sendo processado no array.
        2 - nIndex, Número, O índice do elemento atual que está sendo processado no array.
        3 - aOrigin, Array, O array para qual filter foi chamada.


/*/
//============================================================================\
Function WMSSFilter( aOrigin, bCallback )
    Local aDestiny := {}

    aEval(aOrigin, {|uValue, nIndex| If(Eval(bCallback, uValue, nIndex, aOrigin),aAdd(aDestiny, uValue),Nil) })

Return ( aDestiny )

//============================================================================\
/*/{Protheus.doc}WMSSaasMap
  ==============================================================================
    @description
    Implementação da Função Map para ADVPL
    O método map() invoca a função callback passada por argumento para cada
    elemento do Array e devolve um novo Array como resultado.

    Inspirado no ArrayUtils.map do Javascript moderno
    https://developer.mozilla.org/pt-BR/docs/Web/JavaScript/Reference/Global_Objects/Array/map

    @author Filipe Mendes
    @version 1.0
    @since 15/07/2024

    @param aOrigin, Array, Array Original a ser avaliado
    @param bCallback, Bloco de Código, Função que ao ser executada por Eval() produz o elemento do novo Array.

    @return Array, Novo Array com as modificações

    @obs
        O Callback Recebe três argumentos:
        1 - uValue, Qualquer, O elemento que está sendo processado no array.
        2 - nIndex, Número, O índice do elemento atual que está sendo processado no array.
        3 - aOrigin, Array, O array para qual map foi chamada.
        4 - aDestiny, Array   , O novo array que será retornado.

/*/
//============================================================================\

Function WMSSaasMap( aOrigin, bCallback )
    Local aDestiny:= {}

    aEval(aOrigin, {|uValue,nIndex| aAdd(aDestiny, Eval(bCallback, uValue, nIndex, aOrigin, aDestiny)) })

Return ( aDestiny )

//============================================================================\
/*/{Protheus.doc}WMSSaasFind
  ==============================================================================
    @description
    Retorna um elemento do array encontrado com aScan

    @author Filipe Mendes
    
    @version 1.0
    @since 25/07/2024

    @param aOrigin, Array, Array Original a ser avaliado
    @param bCallback, Bloco de Codigo, Funcao que ao ser executada por aScan retorna a posicao do elemento no array
	@para  [uDefault], Numerico, Opcional. define um valor padrao, caso nao encontrar o elemento
	@para  [nIni], Numerico, Opcional. define em que posicao do Array inicia a busca (padrao: 1)
	@para  [nEnd], Numerico, Opcional. define quantos elementos ira avaliar a partir de nIni (padrao: todos)
    
    @return Indefinido, Elemento ou valor default se informado.

    @obs
        O Callback Recebe apenas o proprio elemento como parametro, semelhante ao aScan

	@example
		// Pode ser utilizado para facilitar a leitura de trechos assim:
		aItens:= {"XPTO"}
		
		xItem:= aItens[aScan(aItens, {|x| x[1] == "XPTO" })][1] // XPTO

		xItem:= WMSSaasFind(aItens, {|x| x[1] == "XPTO" })[1] // XPTO

		E usando o Default, evitar erros
		xItem:= WMSSaasFind(aItens, {|x| x[1] == "XPTY" }, {"ARRAY PADRAO"})[1] // ARRAY PADRAO


/*/
//============================================================================\
Function WMSSaasFind(aOrigin, bCallback, uDefault, nIni, nEnd)
Local nPos:= aScan( aOrigin, bCallback, nIni, nEnd )
Local uRet:= uDefault
	If nPos > 0
		uRet:= aOrigin[nPos]
	EndIf

Return ( uRet )

/*/{Protheus.doc} WMSSaasSeekDBZById
    (long_description)
    @type  Static Function
    @author user
    @since 15/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Function WMSSaasSeekDBZById(cId)
    // Sanity check para nao posicionar erroneamente
    If Empty(cId)
        Return .F.
    EndIf
    DbSelectArea("DBZ")
    DBZ->(DbSetOrder(1))
Return DBZ->(DbSeek(xFilial("DBZ") + cId))

/*/{Protheus.doc} WMSSaasSeekDBYById
    (long_description)
    @type  Static Function
    @author user
    @since 16/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Function WMSSaasSeekDBYById(cId)
    DbSelectArea("DBY")
    DBY->(DbSetOrder(1))
Return DBY->(DbSeek(xFilial("DBY") + cId))


/*/{Protheus.doc} WMSSaasSeekDBYByIdAndSeqSaaS
    (long_description)
    @type  Static Function
    @author user
    @since 16/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Function WMSSaasSeekDBYByIdAndSeqSaaS(cId,nSeqSaaS)
    DbSelectArea("DBY")
    DBY->(DbSetOrder(2))
Return DBY->(DbSeek(xFilial("DBY") + cId + cValToChar(nSeqSaaS)))

/*/{Protheus.doc} WMSSaasPadRToField
    Faz o PADR para o tamanho do campo selecionado
    @type  Function
    @author Filipe
    @since 25/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
/*/
Function WMSSaasPadRToField(xValue as character, fieldName as character)
Return Padr(xValue, TamSx3(fieldName)[1])
