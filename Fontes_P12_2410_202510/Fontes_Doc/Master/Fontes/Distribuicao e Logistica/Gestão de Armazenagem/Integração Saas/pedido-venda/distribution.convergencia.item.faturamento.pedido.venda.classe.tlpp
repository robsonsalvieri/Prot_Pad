#Include "Totvs.ch"
#include "tlpp-core.th"

/*/{Protheus.doc} WMSSaasItemPedidoVenda
    Classe de pedidoVenda Wms Saas
    @type class
    @author Filipe Mendes
    @since 09/05/2024
    /*/
Class WMSSaasItemFaturamentoPedidoVenda From WMSSaasItemPedidoVenda
    Public Method set(cProduto as character, cItem as character, cSequencia as character, nQuantidade as numeric, nValorUnitario as numeric, cArmazem as character, cLote as character, dDataValidade as date, cNotaFiscal as character, cSerieNotaFiscal as character) as object
    Static Method getIdByItem(pedido as character, produto as character, item as character, sequencia as character, cNotaFiscal as character, cSerieNotaFiscal as character) as character
EndClass

/*/{Protheus.doc} new
    (long_description) 
    @author Filipe Mendes
    @since 09/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method set(cProduto, cItem, cSequencia, nQuantidade, nValorUnitario, cArmazem, cLote, dDataValidade, cNotaFiscal, cSerieNotaFiscal) Class WMSSaasItemFaturamentoPedidoVenda as object
    self:produto         := cProduto
    self:item            := cItem
    self:sequencia       := cSequencia
    self:quantidade      := nQuantidade
    self:valorUnitario   := nValorUnitario
    self:armazem         := cArmazem
    self:lote            := cLote
    self:dataValidade    := dDataValidade
    self:notaFiscal      := cNotaFiscal
    self:serieNotaFiscal := cSerieNotaFiscal
Return self

/*/{Protheus.doc} getIdByItem
    Busca o item da convergÃªncia
    @author user
    @since 22/07/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
/*/
Method getIdByItem(cPedido, cProduto, cItem, cSequencia, cNotaFiscal, cSerieNotaFiscal) Class WMSSaasItemFaturamentoPedidoVenda
Local cAliasDBZ := GetNextAlias()
Local cId       := ""

    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
	  	  FROM %Table:DBZ% DBZ
         INNER JOIN %Table:DBY% DBY
            ON DBZ_FILIAL = DBY_FILIAL
           AND DBZ_ID     = DBY_ID
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoFaturamentoPedidoVenda()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cPedido%
           AND DBY.DBY_PRODUT = %Exp:cProduto%
           AND DBY.DBY_ITEM   = %Exp:cItem%
           AND DBY.DBY_SEQUEN = %Exp:cSequencia%
           AND DBY.DBY_NFISCA = %Exp:cNotaFiscal%
           AND DBY.DBY_SERIEN = %Exp:cSerieNotaFiscal%
		   AND DBZ.%NotDel%
           AND DBY.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        cId := (cAliasDBZ)->IDCONVERGENCIA
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return cId

