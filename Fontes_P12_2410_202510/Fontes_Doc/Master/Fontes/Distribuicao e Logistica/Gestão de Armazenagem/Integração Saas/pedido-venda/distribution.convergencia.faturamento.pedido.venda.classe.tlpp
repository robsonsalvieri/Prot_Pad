#Include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.ch"

#define CONTROLA_ESTOQUE "S" 
/*/{Protheus.doc} WMSSaasFaturamentoPedidoVenda
    Classe de pedidoVenda Wms Saas
    @type class
    @author fagner.ferraz
    @since 4/22/2024
/*/
Class WMSSaasFaturamentoPedidoVenda From WMSSaasPedidoVenda
    Public Method new(cDoc as character, cFornece as character, cLoja as character, dEmissao as date , cSequencia as character)
    Public Method loadByChave(cDoc as character, cFornece as character, cLoja as character, dEmissao as date , cSequencia as character)
    Public Method loadOrNew(cDoc as character, cFornece as character, cLoja as character, dEmissao as date , cSequencia as character)
    Public Method loadByItem(pedido as character, produto as character, item as character, sequencia as character, cNotaFiscal as character, cSerieNotaFiscal as character)
    Public Method setLiberado() as object
    Public Method canBeUpdated() as logical
    Public Method isItemEligibleToIntegration() as logical
    Public Method parseItemConvergencia(nRecnoSC9 as numeric) as logical
EndClass

/*/{Protheus.doc} WMSSaasFaturamentoPedidoVenda::New
    Carga das propriedades do cabeçalho da classe de convergência
    @type method
    @author fagner.ferraz
    @since 4/22/2024
    @param cDoc, character, Numero da nota 
    @param cSerie, character, Serie da nota
    @param cFornece, character, Fornecedor
    @param cLoja, character, Loja
    @param dCriacao, date, Data de digitação
    @param dEmissao, date, Data de emissão
    @param cTransp, character, Transportador
    @param cChvNfe, character, Chave da nota
/*/
Method New(cDoc, cFornece, cLoja, dEmissao, cSequencia) Class WMSSaasFaturamentoPedidoVenda
    self:id                  := ""
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoFaturamentoPedidoVenda()
    self:numeroDocumento     := cDoc
    self:clienteOuFornecedor := cFornece
    self:loja                := cLoja
    self:dataCriacao         := FwTimeStamp(3,dDataBase, Time())
    self:dataEmissao         := dEmissao
    self:sequenciaIntegracao := cSequencia
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} loadByChave
    Faz o load da classe de pedidoVenda e convergencia pela chave
    Esse metodo deve retornar apenas um registro da DBZ sempre, pois cada pedido de venda só pode ter uma sequencia em branco não integrada.
    @author Alexsander Burigo Corrêa
    @since 22/10/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method loadByChave(cDoc, cFornece, cLoja, dEmissao, cSequencia) Class WMSSaasFaturamentoPedidoVenda
Local cAliasDBZ := GetNextAlias()

    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoFaturamentoPedidoVenda()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDoc%
           AND DBZ.DBZ_SEQINT = %Exp:cSequencia%
           AND DBZ.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} loadOrNew
    Verifica se há uma convergência para documento, caso contrário cria uma nova
    @author Alexsander Burigo Corrêa
    @since 21/10/2024
    @version 1.0
    @param cDoc, character, Documento
    @param cCliForn, character, Cliente ou forncedor
    @param cLoja, character, Loja
    @param dEmissao, date, Data Emissão
    @param cSequencia, character, Sequencia do documento
    @return Objeto, object, Convergência
    /*/
Method loadOrNew(cDoc, cCliForn, cLoja, dEmissao, cSequencia) Class WMSSaasFaturamentoPedidoVenda
local oLoad := self:loadByChave(cDoc, cCliForn, cLoja, dEmissao, cSequencia)
    If !Empty(oLoad:id) .And. oLoad:canBeUpdated() 
        Return oLoad
    EndIf

Return self:new(cDoc, cCliForn, cLoja, dEmissao, cSequencia)


/*/{Protheus.doc} parseItemConvergencia
    (Converte SC9 liberada em objeto ItemConvergencia )
    @author user
    @since 16/05/2024
    @version version
    @param nRecnoSC9, numeric, recno do registro liberado
    @return lRet, logical, sucesso na conversao
/*/
Method parseItemConvergencia(nRecnoSC9) Class WMSSaasFaturamentoPedidoVenda as logical
Local aArea  := GetArea() 
Local lRet   := .F.

    If self:isItemEligibleToIntegration(nRecnoSC9)
        AAdd(self:itensConvergencia, WMSSaasItemFaturamentoPedidoVenda():set(;
            SC9->C9_PRODUTO,;
            SC9->C9_ITEM,;
            SC9->C9_SEQUEN,;
            SC9->C9_QTDLIB,;
            SC9->C9_PRCVEN,;
            SC9->C9_LOCAL,;
            SC9->C9_LOTECTL,;
            IIf(!Empty(SC9->C9_LOTECTL),SC9->C9_DTVALID,),;
            SC9->C9_NFISCAL,;
            SC9->C9_SERIENF;
            ))
        lRet := .T.
    EndIf

    RestArea(aArea)
Return lRet

/*/{Protheus.doc} loadByItem
    Faz o load da classe por um item.
    @author user
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
/*/
Method loadByItem(cPedido, cProduto, cItem, cSequencia, cNotaFiscal, cSerieNotaFiscal) Class WMSSaasFaturamentoPedidoVenda
Local cId := WMSSaasItemFaturamentoPedidoVenda():getIdByItem(cPedido, cProduto, cItem, cSequencia, cNotaFiscal, cSerieNotaFiscal)

    If !Empty(cId)
        self:loadById(cId)
    Else
        return self:new()
    EndIf

Return self

/*/{Protheus.doc} setLiberado
    (long_description)
    @author user
    @since 15/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method setLiberado() Class WMSSaasFaturamentoPedidoVenda
    If WMSSaasSeekDBZById(self:id) .And. RecLock("DBZ", .F.)
        DBZ->DBZ_STATUS := Self:getStatusLiberado()
        DBZ->(MsUnlock())
        self:load()
    EndIf
Return self

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais os documentos de entrada podem ser alterados
    @author user
    @since 26/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeUpdated() as logical Class WMSSaasFaturamentoPedidoVenda
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} isItemEligibleToIntegration
    Verifica se item do pedidoVenda é elegivel para ser integrado ao Saas.
    @author user
    @since 07/05/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method isItemEligibleToIntegration(nRecno) Class WMSSaasFaturamentoPedidoVenda as logical
Local aAreaSC9  := SC9->(GetArea())
Local aAreaSC6  := SC6->(GetArea())

    DbSelectArea("SC9")
    SC9->(DbGoTo(nRecno))

    // -- Se item não controla WMS SaaS, não deve ser enviado
    If !WMSSaasInt(SC9->C9_PRODUTO, SC9->C9_LOCAL)
        Return RestArea(aAreaSC9) .And. .F.
    EndIf
    
    If Empty(SC9->C9_NFISCAL)
        Return RestArea(aAreaSC9) .And. .F.
    EndIf

    SC6->(DbSetOrder(1))
    SC6->(DbSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO))
    
    // -- Se TES está preenchida e não controla estoque, não deve ser enviado
    If Alltrim(GetAdvFVal("SF4","F4_ESTOQUE",xFilial("SF4")+SC6->C6_TES,1)) != CONTROLA_ESTOQUE
        Return RestArea(aAreaSC9) .And. .F.
    EndIf

    RestArea(aAreaSC9)
    RestArea(aAreaSC6)
Return .T.


