#include "tlpp-core.th"
#include "TOTVS.CH"
#include "RESTFUL.CH"
#include "distribution.convergencia.api.convergencia.ch"

#define INTEGRADO WMSSaasConvergencia():getStatusIntegrado()
#define CANCELADO WMSSaasConvergencia():getStatusCancelado()
#define ERRO_INTEGRACAO WMSSaasConvergencia():getStatusErroIntegracao()
#define FINALIZADO WMSSaasConvergencia():getStatusFinalizado()

/*/{Protheus.doc} WMSSaasUpdateStatus(oRest)
	@type  Function
	@author Filipe Mendes
	@since 18/04/2024
	@version version
	@param param, param_type, param_descr
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
	/*/
Function WMSSaasUpdateStatus(oRest, cTipoTransacao, cStatus) as logical
Local lRet     := .T.
Local oConvergencia  As Object
Local oJsonBody
Local oError
Local oSuccess := WMSSaasRestFinish():new(@oRest)
Local bError   := ErrorBlock( { |oError| WMSSaasErrorBlock( oError, oRest ) } )
Local oClasse  := WMSSaasConvergenciaFactory():get(cTipoTransacao)
    Begin Sequence
        oJsonBody     := WMSSaasParseBodyFromRest(oRest)
        oPathParams   := WMSSaasGetPathParamsFromRest(oRest)
        oConvergencia := oClasse:loadById(oJsonBody['dadosExternos']['id'])
        
        if !Empty(oConvergencia:id) .And. setStatusConvergencia(oConvergencia,oJsonBody, cStatus)
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse(STR0001+cStatus) //"Registro atualizado para status "
        else
            oSuccess:setStatus(404)
            oSuccess:setJsonResponse(STR0002+oJsonBody['dadosExternos']['id']+STR0003) //"Registro de id "" n√£o encontrado."
            lRet := .F.
        EndIf
        FwFreeObj(oConvergencia)
	Recover using oKnownError
		SetRestFault(oKnownError:getStatus(),oKnownError:getDescription(),.T.,oKnownError:getStatus(),oKnownError:getDetailedMessage())
		Return .F.
	End Sequence

    ErrorBlock( bError )
Return lRet

/*/{Protheus.doc} WMSSaasFinaliza(oRest)
	@type  Function
	@author Filipe Mendes
	@since 02/05/2024
	@version version
	@param oRest, object, objeto do rest
    @param oAdapter, object, classe instanciada do adapter
    Recebendo adapter para nao obrigar a camada da convergencia
    decidir quem a chamou ou pra onde deve ir. Basicamente, a url
    chamada devera promover quem fara seu tratamento.
	@return return, return_type, return_description
	@example
	(examples)
	@see (links_or_references)
	/*/
Function WMSSaasFinish(oRest as object, oAdapter as object) as logical
Local lRet      := .T.
Local oProcess  As Object
Local oJsonBody
Local oError
Local oSuccess  := WMSSaasRestFinish():new(@oRest)
Local bError    := ErrorBlock( { |oError| WMSSaasErrorBlock( oError, oRest ) } )
    
    Begin Sequence
        oJsonBody := WMSSaasParseBodyFromRest(oRest)
        oPathParams := WMSSaasGetPathParamsFromRest(oRest)
        oConvergencia := WMSSaasConvergencia():loadById(oJsonBody['content']['dadosExternos']['id'])
               
        if !Empty(oConvergencia:id) .And. oAdapter:finaliza(oJsonBody['content']['dadosExternos']['id'], oJsonBody)
            oSuccess:setStatus(200)
            oSuccess:setJsonResponse(STR0004) //"Registro finalizado com sucesso."
        else
            oSuccess:setStatus(400)
            oSuccess:setJsonResponse(STR0002+oJsonBody['content']['dadosExternos']['id']+STR0003)//"Registro de id "" nao encontrado"
            lRet := .F.
        EndIf
        FwFreeObj(oProcess)
    Recover using oError
        SetRestFault(oError:getStatus(),oError:getDescription(),.T.,oError:getStatus(),oError:getDetailedMessage())
        Return .F.
    End Sequence

    ErrorBlock( bError )
Return lRet

/*/{Protheus.doc} setStatusConvergencia
    Set status do registro da convergencia
    de acordo com status recebido no corpo da requisicao
    @type  Static Function
    @author Filipe Mendes
    @since 19/04/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Static Function setStatusConvergencia(oConvergencia, oBody, cStatus) as logical
Local oError    := WMSSaasRestError():new()
// Status deve ser INTEGRADO, CANCELADO ou ERRO
// Demais status serao tratados na api de finalizacao
Local aDetalhes := IF(oBody['detalhes'] != NIL, oBody['detalhes'], {})
Local cMensagem := ""

    // Concatena as mensagens recebidas no array
    aEval(aDetalhes, {|mensagem| cMensagem += mensagem +CRLF})

    Do Case
        Case cStatus == INTEGRADO
            oConvergencia:setIntegrado()
        Case cStatus == FINALIZADO
            oConvergencia:setFinalizado()    
        Case cStatus == CANCELADO
            oConvergencia:setCancelado()
            If !oConvergencia:isCancelado()
                oError:setDescription(oConvergencia:getMensagemIntegracao()) 
                oError:setStatus(400) 
                Break(oError)
            EndIf
        Case cStatus == ERRO_INTEGRACAO
            oConvergencia:setErroIntegracao(cMensagem)
        Otherwise
            oError:setDescription(STR0006+cStatus+STR0007)  //"Status '""' nao e valido."
            oError:setStatus(400)
            Break(oError)
    EndCase
Return .T.
