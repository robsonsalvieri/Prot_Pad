#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.manufatura.apontamento.producao.adapter.ch"


/*/{Protheus.doc} WMSSaasManufaturaApontamentoProducaoAdapter
    Classe para tratamento dos retornos de apontamento de producao
    @author Equipe WMS Protheus
    @since 04/02/2025
    @version 1.0
    /*/
Class WMSSaasManufaturaApontamentoProducaoAdapter from WMSSaasConvergenciaAdapter
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method montaPayloadCompletoManufaturaApontamentoProducao() as json
    Static Method getCaracteristicas(jItem as json) as json
    Static Method finaliza(oJsonBody as json) as logical    
EndClass


/*/{Protheus.doc} getAll
   Busca dos apontamentos de producao
   @author Equipe WMS Protheus
   @since 04/02/2025
   @version 1.0
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oManufaturaApontamentoProducao, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasManufaturaApontamentoProducaoAdapter as json
Local oManufaturaApontamentoProducao := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoManufaturaApontamentoProducao(), nPage, nPageSize) 
   self:montaPayloadCompletoManufaturaApontamentoProducao()
Return oManufaturaApontamentoProducao


/*/{Protheus.doc} montaPayloadCompletoManufaturaApontamentoProducao
   Monta todo o paylod de retorno
   @author Equipe WMS Protheus
   @since 10/02/2025
   @version 1.0
   @return oManufaturaApontamentoProducao, object, objeto FWAdapterBaseV2 com dados
   /*/
Method montaPayloadCompletoManufaturaApontamentoProducao() Class WMSSaasManufaturaApontamentoProducaoAdapter as json
Local currentJsonObject     := self:oJsonObj:oJsonObj
Local aItens                := {}
Local currentItemJsonObject := ""
Local nX                    := 1
Local nY                    := 1
Local cId                   := ""
Local jCabecalhoItem
Local jItem

   //Cabecalhos - DBZ
   For nX := 1 To Len(currentJsonObject['items'])
      jCabecalhoItem := currentJsonObject['items'][nX]
      cId := jCabecalhoItem['id']
      self:getCompletoById(cId, 1, 2000)
      currentItemJsonObject := self:oJsonObj:oJsonObj

      //Itens do cabecalho - DBY
      For nY := 1 To Len(currentItemJsonObject['items'])
         jItem := currentItemJsonObject['items'][nY]
         
         aAdd(aItens,;
            {;
            "produto":  WMSSaasProdutoAdapter():montaPayload(jItem['produto']),;
            "codigoOrdemProducao": Alltrim(jCabecalhoItem['ordemProducao']),;
            "referenciaProducao": " ",;
            "quantidade": jItem['quantidade'],;
            "tipoEstoque": jItem['armazem'],;
            "apontamento": Alltrim(jCabecalhoItem['documento']),;
            "caracteristicas":  self:getCaracteristicas(jItem),; 
            "dadosExternos": {;
               "id": jCabecalhoItem['id'],;
               "numero": Alltrim(jCabecalhoItem['documento'])+Alltrim(jCabecalhoItem['sequenciaIntegracao']);
            };
            };
         )
      Next nY

   Next nX

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}


/*/{Protheus.doc} getCaracteristicas
   Insercao de caracteristicas do item
   @author Equipe WMS Protheus
   @since 10/02/2025
   @version 1.0
   @return caracteristicas
   /*/
Method getCaracteristicas(jItem) Class WMSSaasManufaturaApontamentoProducaoAdapter as json
Local aCaracteristicas := {}
   If !Empty(jItem['lote'])
      aAdd(aCaracteristicas,{;
         "atributoExterno": WMSSaasConvergencia():getPropriedadeLote(),;
         "valor": jItem['lote'];
      })
      aAdd(aCaracteristicas,{;
         "atributoExterno": WMSSaasConvergencia():getPropriedadeDataValidade(),;
         "valor":  Left(FWTimeStamp(3,StoD(jItem['dataValidade']),""),10) ;
      })
   EndIf
Return aCaracteristicas

/*/{Protheus.doc} finaliza
   Metodo para tratar a finalizacao da conferencia do produto acabado do apontamento de producao
   @author Roselaine
   @since 24/02/2025
   @version version
   @param oJsonBody, object
   {
   } 
   @return logico
   /*/
Method finaliza(oJsonBody) Class WMSSaasManufaturaApontamentoProducaoAdapter as logical
Local oManufaturaApontamentoProducao  := WMSSaasManufaturaApontamentoProducao():new()
Local oLogError                       := Nil
Local bError                          := Nil
Local lRet                            := .F.
Local oErrorApt                       := WMSSaasRestError():new()
   bError := ErrorBlock( { |oLogError| oErrorReq := WMSSaasErrorBlockAdapter(oLogError,@oErrorApt) } )

   oManufaturaApontamentoProducao:loadById(oJsonBody['dadosExternos']['id'])
   If Empty(oManufaturaApontamentoProducao:id)
      Return .F.
   EndIf

   lRet := oManufaturaApontamentoProducao:canBeFinished()
   If !lRet
      oErrorApt:setStatus(400)
      oErrorApt:setDescription(STR0001) //"Finalização da conferência do apontamento de produção não pode ser realizada. Verifique se o registro já está finalizado no ERP."
   Else
      oManufaturaApontamentoProducao:finaliza() 
   EndIf

   If !lRet
      Break(oErrorApt)
   EndIf

   ErrorBlock( bError )
Return lRet
