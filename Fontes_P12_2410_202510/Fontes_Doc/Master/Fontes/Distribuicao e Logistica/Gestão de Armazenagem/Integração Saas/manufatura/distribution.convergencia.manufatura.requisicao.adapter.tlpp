#include "tlpp-core.th"
#include "distribution.convergencia.ch"
#include "distribution.convergencia.manufatura.requisicao.adapter.ch"

Static nTamProd  := TamSx3('D3_COD')[1]
Static nTamLocal := TamSx3('D3_LOCAL')[1]
Static nTamLote  := TamSx3('D3_LOTECTL')[1]

/*/{Protheus.doc} WMSSaasManufaturaRequisicaoAdapter
    Classe para tratamento dos retornos de manufatura
    @author Fagner Ferraz
    @since 19/11/2024
    @version 1.0
    @obs Classe para gerenciamento das transacoes na tabela
    de convergencia WMS Saas x Protheus
    /*/
Class WMSSaasManufaturaRequisicaoAdapter from WMSSaasConvergenciaAdapter implements WMSSaasConvergenciaAdapterInterface
    Public Method new() as object
    Public Method getAll(nPage as integer, nPageSize as integer) as json
    Public Method getById(cId as character,nPage as integer, nPageSize as integer) as json
    Static Method finaliza(cId as character, oJsonBody as json) as logical
    Public Method montaPayloadGetAllManufaturaRequisicao() as json
    Public Method montaPayloadGetFullManufaturaRequisicao() as json
EndClass

Method new() Class WMSSaasManufaturaRequisicaoAdapter
Return self

/*/{Protheus.doc} getAll
   Busca cabecalho das requisicoes
   @author Fagner Ferraz
   @since 19/11/2024
   @version 1.0
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oRequisicaoManufatura, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getAll(nPage, nPageSize) Class WMSSaasManufaturaRequisicaoAdapter as json
Local oRequisicaoManufatura := self:getLiberados(WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao(), nPage, nPageSize) 
   self:montaPayloadGetAllManufaturaRequisicao()
Return oRequisicaoManufatura

/*/{Protheus.doc} montaPayloadGetAllManufaturaRequisicao
   Monta o payload conforme contrato WMS Saas
   @author Fagner Ferraz
   @since 19/11/2024
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetAllManufaturaRequisicao() Class WMSSaasManufaturaRequisicaoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, ;
      {;
         "codigoOrdemProducao": Alltrim(item['documento'])+Alltrim(item['sequenciaIntegracao']),;
         "dadosExternos": {;
            "id": item['id'];
         };
      };
      );
   })

Return self:oJsonObj:oJsonObj := {;
   "items": aItens,;
   "hasNext": currentJsonObject['hasNext'];
}

/*/{Protheus.doc} getById
   Busca completa de requisicoes pelo Id
   @author Fagner Ferraz
   @since 19/11/2024
   @version 1.0
   @param cId, C, Id da convergencia
   @param nPage, N, numero da pagina
   @param nPageSize, N, tamanho da pagina
   @return oRequisicaoManufatura, object, objeto FWAdapterBaseV2 com dados
   /*/
Method getById(cId, nPage, nPageSize) Class WMSSaasManufaturaRequisicaoAdapter as json
Local oManufaturaRequisicao := self:getCompletoById(cId, nPage, nPageSize)
   self:montaPayloadGetFullManufaturaRequisicao()
Return oManufaturaRequisicao

/*/{Protheus.doc} montaPayloadGetFullManufaturaRequisicao
   Monta o payload conforme contrato WMS Saas
   @author Fagner Ferraz
   @since 19/11/2024
   @version 1.0
   @return self:oJsonObj:oJsonObj, json, popula jsonObject dentro do objeto
   /*/
Method montaPayloadGetFullManufaturaRequisicao() Class WMSSaasManufaturaRequisicaoAdapter as json
Local currentJsonObject := self:oJsonObj:oJsonObj
Local aItens            := {}

   aEval(currentJsonObject['items'], {|item| aAdd(aItens, {; 
      "produto": WMSSaasProdutoAdapter():montaPayload(item['produto']),;
      "sequencia": item['seqSaaS'],;
      "tipoEstoque": Padr('',nTamLocal),;
      "quantidade": item['quantidade'];
      }) ;
   })

Return self:oJsonObj:oJsonObj := {;
   "codigoOrdemProducao": currentJsonObject['documento']+currentJsonObject['sequenciaIntegracao'],;
   "referenciaProducao": "Ordem Producao",;
   "produtos": aItens,;
   "dadosExternos":{;
      "id":currentJsonObject['id'];
   } }

/*/{Protheus.doc} WMSSaasManufaturaRequisicaoAdapter
   Metodo para tratar a finalizacao da requisicao empenhada WMS SaaS
   @author Fagner Ferraz
   @since 05/12/2024
   @version 1.0
   @param oJsonBody, object
   {
      "grupo": "99",
      "filial": "01",
      "ordemproducao": "Y0329501001000002",
      "requisicaoMateriaPrimaId": "cd2094a9-f8ac-438d-a965-b2e8bed8b42b",
      "ordemProducaoId": "396dc085-fc21-4a2e-9862-84cb5e47de8a",
      "ordemProducaoCodigo": "Y0329501001000002",
      "hashCodeOrdemProducao": "eb5c3f7eb720881b1c00d0f5ab3b9de6",
      "unidadeId": "9b545498-029c-479d-a6af-16affcb88d4c",
      "quantidadeSolicitada": 10,
      "atendimentoFinalizado": true,
      "produto": {
         "id": "ea297826-4eca-4e4f-8cd4-b8b4beacd905",
         "codigo": "041_MERCEDES_PARAFUSOS",
         "descricaoComercial": "041_MERCEDES_PARAFUSOS",
         "descricaoMobile": "041_MERCEDES_PARAFUSOS"
      },
      "reserva": {
         "id": "1865497f-2408-4744-afe6-41908ada863d",
         "enderecoOrigemId": "4432bd42-1f46-4fa4-a8b7-7471696c01af",
         "quantidadeReservada": 2,
         "caracteristicas": [
            {
            "id": "d0dd0231-9422-466f-a35d-887616d27fb8",
            "descricao": "Lote",
            "formato": "TEXTO",
            "valor": "LOTEA"
            },
            {
            "id": "9535c29c-4a26-4213-9c0c-737404244b14",
            "descricao": "Data de Validade",
            "formato": "DATA",
            "valor": "2026-03-03"
            }
         ],
         "tipoEstoque": {
            "id": "71f19725-bbec-4d17-b51a-351b481ff85c",
            "descricao": "Vendas"
         }
      },
      "deposito": null,
      "reservas": [
         {
            "id": "1865497f-2408-4744-afe6-41908ada863d",
            "enderecoOrigemId": "4432bd42-1f46-4fa4-a8b7-7471696c01af",
            "quantidadeReservada": 2,
            "caracteristicas": [
            {
               "id": "d0dd0231-9422-466f-a35d-887616d27fb8",
               "descricao": "Lote",
               "formato": "TEXTO",
               "valor": "LOTEA",
               "atributoExterno": "lote"
            },
            {
               "id": "9535c29c-4a26-4213-9c0c-737404244b14",
               "descricao": "Data de Validade",
               "formato": "DATA",
               "valor": "2026-03-03",
               "atributoExterno": "dataValidade"
            }
            ],
            "tipoEstoque": "01",
            "quantidade": 2
         }
      ],
      "idOrganizacao": 1906,
      "dadosExternos": {
         "id": "A61A3CA8-16D4-4F11-B955-005056ACC547"
      }
   }
   @return return_var, return_type, return_description
   /*/
Method finaliza(oJsonBody) Class WMSSaasManufaturaRequisicaoAdapter as logical
Local oManufaturaRequisicao  := WMSSaasManufaturaRequisicao():new()
Local jReservas              := Nil
Local jManufaturaRequisicao  := Nil
Local jLote                  := Nil
Local jDataValidade          := Nil
Local cId                    := oJsonBody['dadosExternos']['id']
Local cIdReserva             := ""
Local cProduto               := oJsonBody['produto']['codigo']
Local cLote                  := ""
Local dDataValidade          := ""
Local cTipoEstoque           := ""
Local nQuantidade            := 0
Local lAtendimentoFinalizado := oJsonBody['atendimentoFinalizado']
Local oLogError              := Nil
Local bError                 := Nil
Local lRet                   := .T.
Local lAtendido              := If(!Empty(oJsonBody['tipoAtendimentoSelecaoEstoque']) .And. (oJsonBody['tipoAtendimentoSelecaoEstoque'] == 'SEM_ATENDIMENTO'),.F.,.T.)
Local oErrorReq              := WMSSaasRestError():new()
   bError := ErrorBlock( { |oLogError| oErrorReq := WMSSaasErrorBlockAdapter(oLogError,@oErrorReq) } )

   Conout("INICIO | finaliza | WMSSaasManufaturaRequisicaoAdapter")
   If Len(oJsonBody['reservas']) > 0
      Conout("Encontrou reservas | finaliza | WMSSaasManufaturaRequisicaoAdapter")
      jReservas := oJsonBody['reservas'][1] /* payload é um array, mas retorna uma posicao */

      If Empty(jReservas['id'])
         lRet := .F.
         oErrorReq:setStatus(400)
         oErrorReq:setDescription(STR0002) //"O id da reserva não foi informado. Não será possível realizar a separação."
      Else
         cIdReserva    := jReservas['id']
         jLote         := WMSSaasFind(jReservas['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeLote() }, { "valor": "" })
         jDataValidade := WMSSaasFind(jReservas['caracteristicas'], {|x| x['atributoExterno'] == WMSSaasConvergencia():getPropriedadeDataValidade() }, { "valor": "" })
         cLote         := jLote['valor']
         dDataValidade := jDataValidade['valor']
         cTipoEstoque  := jReservas['tipoEstoque']
         nQuantidade   := jReservas['quantidade']
      EndIf
   Else
      lRet := .F.
      oErrorReq:setStatus(400)
      oErrorReq:setDescription(STR0002) //"O id da reserva não foi informado. Não será possível realizar a separação."
   EndIf

   If lRet
      jManufaturaRequisicao := {;
                                 "id"                            : cId,;
                                 "produto"                       : Padr(cProduto,nTamProd),;
                                 "lote"                          : Padr(cLote,nTamLote),;
                                 "dataValidade"                  : StoD(Replace(dDataValidade,"-","")),;
                                 "tipoEstoque"                   : Padr(cTipoEstoque,nTamLocal),;
                                 "quantidade"                    : nQuantidade,;
                                 "idReserva"                     : cIdReserva,;
                                 "atendimentoFinalizado"         : lAtendimentoFinalizado,;
                                 "atendido"                      : lAtendido;
                              }
      oManufaturaRequisicao:loadById(jManufaturaRequisicao['id'])
      lRet := oManufaturaRequisicao:canBeFinished()
      If !lRet
         oErrorReq:setStatus(400)
         oErrorReq:setDescription(STR0001) //"A separacao nao pode ser realizada. Verifique se o registro ja esta finalizado no ERP."
      Else
         Begin Transaction
            Conout("Acionou a finalizacao da requisicao | finaliza | WMSSaasManufaturaRequisicaoAdapter")         
            If !oManufaturaRequisicao:finaliza(jManufaturaRequisicao) 
               lRet := .F.

               oErrorReq:setStatus(400)
               oErrorReq:setDescription(oManufaturaRequisicao:getMensagemIntegracao())
               DisarmTransaction()            
            EndIf
         End Transaction
      EndIf
   EndIf

   If !lRet
      Conout("Erro encontrado | finaliza | WMSSaasManufaturaRequisicaoAdapter")
      Break(oErrorReq)
   EndIf

   ErrorBlock( bError )
   Conout("FIM | finaliza | WMSSaasManufaturaRequisicaoAdapter")
Return lRet


