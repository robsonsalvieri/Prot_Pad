#Include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.manufatura.util.ch"

/*/{Protheus.doc} WMSSGerReq
	Geracao da requisicao empenhada WMS SaaS
	@type  Function
	@author Alexsander Burigo Correa
	@since 13/12/2024
	@version 1.0
	@param nRecnoSC2, numeric, Recno da tabela (SC2)
	@param nRecnoSD4, numeric, Recno da tabela (SD4)
	@param nQuantidade, numeric, Quantidade a requisitar
	@return logico, logical, Indica sucesso
	/*/
Function WMSSGerReq(nRecnoSC2, nRecnoSD4, nQuantidade)
Local oManufaturaControleRequisicao
Local lRet  := .F.

	DbSelectArea("SC2")
	SC2->(DbGoTo(nRecnoSC2))

	DbSelectArea("SD4")
	SD4->(DbGoTo(nRecnoSD4))
	/* 	Valida somente se o produto controla WMS SaaS, o armazem sera selecionado pelo WMS SaaS,
		dessa forma não podera ser validado */
	If WMSSaasInt(SD4->D4_COD) .And. Empty(SD4->D4_OPORIG) .AND. !WMSSaasInt(SD4->D4_COD,SD4->D4_LOCAL)
		oManufaturaControleRequisicao := WMSSaasManufaturaControleRequisicao():loadOrNew(SD4->D4_OP, SD4->D4_COD, SD4->D4_TRT, SD4->D4_LOTECTL, SD4->D4_NUMLOTE,SD4->D4_DTVALID, SD4->D4_LOCAL, SD4->D4_ORDEM, SD4->D4_OPORIG, SD4->D4_SEQ, SD4->D4_DATA, nQuantidade)
		Begin Transaction
			If oManufaturaControleRequisicao:convergencia:parseItemConvergencia(SD4->D4_COD, nQuantidade, SD4->D4_LOCAL, SD4->D4_LOTECTL, SD4->D4_DTVALID)
				oManufaturaControleRequisicao:save()	
				lRet := .T. 			
			EndIf
		End Transaction
	EndIf	
Return lRet

/*/{Protheus.doc} WMSSExItRq
	Exclui informacoes da requisicao empenhada WMS SaaS
	@type  Function
	@author Alexsander Burigo Correa
	@since 04/12/2024
	@version 1.0
	@param nRecnoDBX, numeric, recno da DBX
	@return logico, logical, Indica sucesso
/*/
Function WMSSExItRq(nRecnoDBX)
Local aAreaDBX  := DBX->(GetArea())
Local oManufaturaControleRequisicao

	DbSelectArea("DBX")
	DBX->(DbGoTo(nRecnoDBX))

	oManufaturaControleRequisicao := WMSSaasManufaturaControleRequisicao():loadByRecno(DBX->(Recno()))
	If oManufaturaControleRequisicao:isGerada()
		If !Empty(oManufaturaControleRequisicao:convergencia:id)
			// Quando convergencia finalizada muda situacao da requisicao para cancelada
			If oManufaturaControleRequisicao:convergencia:canBeUpdated()
				oManufaturaControleRequisicao:convergencia:getItem(oManufaturaControleRequisicao:produto,oManufaturaControleRequisicao:lote)
				// Deleta o item da convergencia
				oManufaturaControleRequisicao:convergencia:getItem(oManufaturaControleRequisicao:produto,oManufaturaControleRequisicao:lote):delete()
				// Se não existem mais itens, exclui o cabecalho
				If Empty(oManufaturaControleRequisicao:convergencia:getItem(oManufaturaControleRequisicao:produto,oManufaturaControleRequisicao:lote):loadAllByParentId(oManufaturaControleRequisicao:convergencia:id))
					oManufaturaControleRequisicao:convergencia:delete()
				EndIf
				oManufaturaControleRequisicao:delete()
			EndIf
		EndIf
	EndIf

    RestArea(aAreaDBX)
Return .T.

/*/{Protheus.doc} WMSSaasSeekDBXByRecno
	@type  Function
	@author Alexsander Burigo Correa
	@since 13/12/2024
	@version 1.0
	@param param_name, param_type, param_descr
	@return logico, logical, Indica sucesso
	/*/
Function WMSSaasSeekDBXByRecno(nRecno)	
	DBSelectArea('DBX')
	DBX->(DbGoTo(nRecno))
Return .T.

/*/{Protheus.doc} WMSSaasSeekDBXById
	@type  Function
	@author Alexsander Burigo Correa
	@since 13/12/2024
	@version 1.0
	@param param_name, param_type, param_descr
	@return logico, logical, Indica sucesso
	/*/
Function WMSSaasSeekDBXByIdDBZ(cIdDBZ)	
	DBSelectArea('DBX')
    DBX->(DbSetOrder(2))    	
Return DBX->(DbSeek(xFilial("DBX") + cIdDBZ))

/*/{Protheus.doc} WMSSGerApt
	Grava dados de apontamento de produção na convergência.
	@type  Function
	@author Equipe WMS
	@since 29/01/2025
	@version 1.0
	@param nRecnoSD3, numeric, recno da SD3
	@return boolean
	@example
	(examples)
	@see (links_or_references)
	/*/
Function WMSSGerApt(nRecnoSD3 as numeric)
Local aAreaSD3 := SD3->(GetArea())
Local oManufaturaApontamentoProducao

	DbSelectArea("SD3")
	SD3->(DbGoTo(nRecnoSD3))

	If WMSSaasInt(SD3->D3_COD, SD3->D3_LOCAL) .AND. !WMSSVlOpOr( SD3->D3_OP, SD3->D3_COD)
		Begin Transaction
		    oManufaturaApontamentoProducao := WMSSaasManufaturaApontamentoProducao():new(SD3->D3_DOC, SD3->D3_OP, SD3->D3_EMISSAO)
			If oManufaturaApontamentoProducao:parseItemConvergencia(SD3->D3_COD, SD3->D3_QUANT, SD3->D3_LOCAL, SD3->D3_LOTECTL, SD3->D3_DTVALID, SD3->D3_NUMSEQ)
				oManufaturaApontamentoProducao:save()	
				oManufaturaApontamentoProducao:saveItens()			
			EndIf
		End Transaction
	EndIf	
	RestArea(aAreaSD3)
Return


/*/{Protheus.doc} WMSSVlOpOr
	Valida se a ordem de produção é uma OP intermediária, ou seja possui OP origem.
	@type  Function
	@author Equipe WMS
	@since 29/01/2025
	@version 1.0
	@param cOp, Character, ordem producao
	@param cCod, character, codigo do produto
	@return boolean
	@example
	(examples)
	@see (links_or_references)
	/*/
Function WMSSVlOpOr(cOP,cCod)
Local lTemOpOrig := .F.
Local cAliasSD4  := Nil

	cAliasSD4 := GetNextAlias()
    BeginSql Alias cAliasSD4
	    SELECT 1
		  FROM %Table:SD4% SD4
		 WHERE SD4.D4_FILIAL = %Exp:xFilial("SD4")%
		   AND SD4.D4_OPORIG = %Exp:cOP%
		   AND SD4.D4_COD    = %Exp:cCod%
		   AND SD4.%NotDel%
	EndSql
	If (cAliasSD4)->(!Eof())
		lTemOpOrig := .T.
	EndIf
	(cAliasSD4)->(DbCloseArea())

Return lTemOpOrig


/*/{Protheus.doc} WMSSVExcOP
	@type  Function Valida se uma Ordem de Producao pode ser excluida
	@author Equipe WMS Protheus
	@since 28/01/2025
	@version 1.0
	@param nRecnoSC2, numerico, recno da OP
	@return logico, logical, informa se permite excluir
	/*/
Function WMSSVExcOP(nRecnoSC2)
Local lRet 		            := .T.
Local aArea 	            := GetArea()
Local cAliasDBZ             := ""
Local cTipoTrMR             := WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao()
Local cStatusIntegrado      := WMSSaasConvergencia():getStatusIntegrado()
Local cStatusLiberado       := WMSSaasConvergencia():getStatusLiberado()

	SC2->(DbGoTo(nRecnoSC2))
   
	/* Busca rapida para validar se tem registro que impede a exclusao */
    cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:cTipoTrMR%
		   AND DBZ.DBZ_NUMDOC = %Exp:SC2->C2_OP%
		   AND DBZ.DBZ_STATUS IN (%Exp:cStatusIntegrado%, %Exp:cStatusLiberado%)
		   AND DBZ.%NotDel%
	EndSql
	If (cAliasDBZ)->(!Eof()) //Basta apenas um empenho para nao permitir excluir
		lRet := .F.
	EndIf
	(cAliasDBZ)->(DbCloseArea())

	If !lRet
		//"Existe uma integração ativa com o WMS SaaS com a OP: "" produto: ""Verifique a integração na rotina Convergência Integração WMS Saas (WMSSAAS001)."
		Help(" ",1,"A650WMSSAAS", ,STR0001 + AllTrim(SC2->C2_OP) + STR0002 + AllTrim(SC2->C2_PRODUTO)+".",1,0, ,,,,,{STR0003})
	EndIf

	RestArea(aArea)
Return lRet


/*/{Protheus.doc} WMSSExcOP
	@type  Function Elimina registros WMS SaaS relacionados a ordem de producao
	@author Equipe WMS Protheus
	@since 29/01/2025
	@version 1.0
	@param nRecnoSC2, numerico, recno da OP
	@return logico, logical, informa se permite excluir
	/*/
Function WMSSExcOP(nRecnoSC2)
Local aArea 	            := GetArea()
Local cAliasDBZ             := ""
Local cTipoTrMR             := WMSSaasConvergencia():getTipoTransacaoManufaturaRequisicao()
Local cStatusCriado         := WMSSaasConvergencia():getStatusCriado()
Local cStatusCancelado      := WMSSaasConvergencia():getStatusCancelado()
Local cStatusErroIntegracao := WMSSaasConvergencia():getStatusErroIntegracao()
Local cStatusFinalizado     := WMSSaasConvergencia():getStatusFinalizado()
Local oWMSSaasManufCtrlReq  := Nil
Local oWMSSaasConverg       := Nil
Local cStatusOpDeletada     := "3"
Local cRequisicaoGerada     := "1"
Local cRequisicaoConcluida  := "2"

	SC2->(DbGoTo(nRecnoSC2))

	cAliasDBZ := GetNextAlias()
	BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA, DBZ.DBZ_STATUS, DBX.R_E_C_N_O_ RECNODBX 
		FROM %Table:DBZ% DBZ
		INNER JOIN %Table:DBX% DBX ON(DBX.DBX_FILIAL = %Exp:xFilial("DBX")%
			AND DBX.DBX_IDDBZ = DBZ.DBZ_ID
			AND DBX.DBX_OP = DBZ.DBZ_NUMDOC
			AND (DBX.DBX_STATUS = %Exp:cRequisicaoGerada% OR DBX.DBX_STATUS = %Exp:cRequisicaoConcluida%))
		WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
			AND DBZ.DBZ_TIPOTR = %Exp:cTipoTrMR%
			AND DBZ.DBZ_NUMDOC = %Exp:SC2->C2_OP%
			AND DBZ.DBZ_STATUS IN (%Exp:cStatusCriado%, %Exp:cStatusCancelado%, %Exp:cStatusErroIntegracao%, %Exp:cStatusFinalizado%)
			AND DBZ.%NotDel%
			AND DBX.%NotDel%
	EndSql
	While (cAliasDBZ)->(!Eof())

		oWMSSaasManufCtrlReq := WMSSaasManufaturaControleRequisicao():loadByRecno((cAliasDBZ)->RECNODBX)
		If (cAliasDBZ)->DBZ_STATUS != cStatusFinalizado
			oWMSSaasManufCtrlReq:delete()
		Else
			oWMSSaasManufCtrlReq:updateStatus(cStatusOpDeletada)
		EndIf

		If (cAliasDBZ)->DBZ_STATUS != cStatusFinalizado
			oWMSSaasConverg := WMSSaasConvergencia():loadById((cAliasDBZ)->IDCONVERGENCIA)
			oWMSSaasConverg:delete()
		EndIf
		
		(cAliasDBZ)->(DbSkip())		
	EndDo

	(cAliasDBZ)->(DbCloseArea())
	RestArea(aArea)
Return

/*/{Protheus.doc} WMSSVEsApt
	@type  Function Valida se um Apontamento de Producao pode ser excluido
	@author Equipe WMS Protheus
	@since 14/02/2025
	@version 1.0
	@param nRecnoSD3, numerico, recno do Movimento de Apontamento de Producao
	@param lShowMsg, logical, indica se apresenta mensagem de erro
	@return logico, logical, informa se permite estorna
	/*/
Function WMSSVEsApt(nRecnoSD3, lShowMsg)
Local aAreaSD3                 := SD3->(GetArea())
Local oManufaturaApontProducao := Nil

Default lShowMsg := .F.

	DbSelectArea("SD3")
	SD3->(DbGoTo(nRecnoSD3))

	If WMSSaasInt(SD3->D3_COD, SD3->D3_LOCAL)
		oManufaturaApontProducao := WMSSaasManufaturaApontamentoProducao():new(SD3->D3_DOC, SD3->D3_OP, SD3->D3_EMISSAO)//Deve ser instanciado para executar metodos da convergencia
		oManufaturaApontProducao:loadByChave(SD3->D3_DOC, SD3->D3_OP, SD3->D3_EMISSAO, SD3->D3_NUMSEQ)

		If !Empty(oManufaturaApontProducao:id) .And. !oManufaturaApontProducao:canBeUpdated()
			If lShowMsg
				WmsMessage(STR0004 + AllTrim(oManufaturaApontProducao:getDetalheStatus()),"WMSSVEsApt",5,,,STR0005) //-- "Item ja integrado ao WMS SaaS. Status - ""Realize o estorno da integracao do documento de apontamento de producao no WMS SaaS"
			EndIF
			RestArea(aAreaSD3)
			FwFreeObj(oManufaturaApontProducao)
			Return .F.
		EndIf
	EndIf

	RestArea(aAreaSD3)
	FwFreeObj(oManufaturaApontProducao)
Return .T.


/*/{Protheus.doc} WMSSExcOP
	@type  Function Elimina registros WMS SaaS relacionados a ordem de producao
	@author Equipe WMS Protheus
	@since 29/01/2025
	@version 1.0
	@param nRecnoSD3, numerico, recno do apontamento de producao
	@return logico, logical, informa se permite excluir
	/*/
Function WMSSEstApt(nRecnoSD3)
Local aAreaSD3 := SD3->(GetArea())
Local oManufaturaApontProducao

	DbSelectArea("SD3")
	SD3->(DbGoTo(nRecnoSD3))

	If WMSSaasInt(SD3->D3_COD, SD3->D3_LOCAL)
		oManufaturaApontProducao := WMSSaasManufaturaApontamentoProducao():new(SD3->D3_DOC, SD3->D3_OP, SD3->D3_EMISSAO)//Deve ser instanciado para executar metodos da convergencia
		oManufaturaApontProducao:loadByChave(SD3->D3_DOC, SD3->D3_OP, SD3->D3_EMISSAO, SD3->D3_NUMSEQ)
		If !Empty(oManufaturaApontProducao:id) .And.  oManufaturaApontProducao:canBeUpdated()
			oManufaturaApontProducao:delete()
		EndIf
    EndIf
    RestArea(aAreaSD3)
	FwFreeObj(oManufaturaApontProducao)
Return
