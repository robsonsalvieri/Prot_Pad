#include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.manufatura.requisicao.classe.ch"

#define PRODUTO 5
#define LOTE 1

Static nTamSeqInt := TamSx3('DBZ_SEQINT')[1]

/*/{Protheus.doc} WMSSaasManufaturaRequisicao
    Classe de requisicao manufatura
    @author Fagner Ferraz
    @since 05/12/2024
    @version 1.0
    /*/
Class WMSSaasManufaturaRequisicao From WMSSaasConvergencia
    Public Method new(cOrdemProducao as character, dDataEmpenho as date) as object
    Public Method getItem(cItem as character, cProduto as character, cSequencia as character) as object
    Public Method canBeUpdated() as logical
    Public Method canBeFinished() as logical
    Public Method setLiberado() as object
    Public Method finaliza(jManufaturaRequisicao as json) as logical
    Public Method getSequence() as character
    Public Method parseItemConvergencia(cProduto as character, nQuantidade as numeric, cArmazem as character, cLote as character, dDataValidade as date) as logical
EndClass

/*/{Protheus.doc} New
    @author Alexsander Burigo Correa
    @since 16/12/2024
    @version 1.0
    @param cOrdemProducao, character, Ordem de producao
    @param dDataEmpenho, date, Data de geracao do empenho
    @return self, object, Objeto do controle de requisicao
    /*/
Method new(cOrdemProducao, dDataEmpenho) Class WMSSaasManufaturaRequisicao
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoManufaturaRequisicao()
    self:numeroDocumento     := cOrdemProducao
    self:dataCriacao         := FWTimeStamp(3, dDataBase, Time())
    self:dataEmissao         := dDataEmpenho
    self:sequenciaIntegracao := ""
    self:ordemProducao       := cOrdemProducao
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self


/*/{Protheus.doc} getItem
    @author Alexsander Burigo Correa
    @since 17/12/2024
    @version 1.0
    @param cProduto, character, Produto da convergencia
    @param cLote, character, Sequencia da convergencia
    @return objeto, object, Item da convergencia
/*/
Method getItem(cProduto, cLote) Class WMSSaasManufaturaRequisicao
Return WMSSaasFind(self:itensConvergencia, {|x| x:isItem(cProduto, cLote) })

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais os documentos de entrada podem ser alterados
    @author Alexsander Burigo Correa
    @since 16/12/2024
    @version 1.0
    @return logico, logical, Indica se convergencia permite alteracao
/*/
Method canBeUpdated() Class WMSSaasManufaturaRequisicao
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} canBeFinished
    Retorna se requisicao podera ser finalizada
    @author user
    @since 17/10/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method canBeFinished() as logical Class WMSSaasManufaturaRequisicao
Return self:isIntegrado()

/*/{Protheus.doc} WMSSaasManufaturaRequisicao
    Metodo de finalizacao de requisicao por OP
    @author Fagner Ferraz
    @since 05/12/2024
    @version 1.0
    /*/
Method finaliza(jManufaturaRequisicao) Class WMSSaasManufaturaRequisicao
Local oManufaturaControleRequisicao := WMSSaasManufaturaControleRequisicao():loadByIdDBZ(Self:id)
    Conout("INICIO | finaliza | WMSSaasManufaturaRequisicao")
    If Empty(oManufaturaControleRequisicao:idConvergencia)
        self:setMensagemIntegracao(STR0001) // "Origem integrada da Requisição Empenhada WMS SaaS (DBX) não encontrada"
        Conout("Nao encontrou DBX | finaliza | WMSSaasManufaturaRequisicao")
        Return .F.
    EndIf

    If oManufaturaControleRequisicao:validaRequisicaoProcessada(jManufaturaRequisicao)
        Conout("Requisicao ja foi processada | finaliza | WMSSaasManufaturaRequisicao")
        Return .T.
    EndIf
    
    If !oManufaturaControleRequisicao:atualizaManufaturaRequisicao(jManufaturaRequisicao)
        self:load()
        Conout("Falha na atualizacao da requisicao | finaliza | WMSSaasManufaturaRequisicao")
        Return .F.
    EndIf
    
    If jManufaturaRequisicao['atendimentoFinalizado']
        self:setFinalizado()
        Conout("Convergencia finalizada | finaliza | WMSSaasManufaturaRequisicao")
    EndIf
    Conout("FIM | finaliza | WMSSaasManufaturaRequisicao")
Return .T.

/*/{Protheus.doc} setLiberado
    @author Alexsander Burigo Correa
    @since 16/12/2024
    @version 1.0
    @return self, object, Objeto da convergencia
    /*/
Method setLiberado() Class WMSSaasManufaturaRequisicao
    Begin Transaction
        If WMSSaasSeekDBZById(self:id) .And. RecLock("DBZ", .F.)
            DBZ->DBZ_STATUS := self:getStatusLiberado()
            DBZ->DBZ_SEQINT := self:getSequence()
            DBZ->(MsUnlock())

            self:load()
        EndIf
    End Transaction
Return self

/*/{Protheus.doc} getSequence
    @author Alexsander Burigo Correa
    @since 17/12/2024
    @version 1.0
    @return cNewSeq, character, Nova sequencia da convergencia
    /*/
Method getSequence() Class WMSSaasManufaturaRequisicao
Local cAliasDBZ := ""
Local cNewSeq   := ""
Local cSeqInt   := Space(nTamSeqInt)
    cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT MAX(DBZ.DBZ_SEQINT) MAXSEQINT
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoManufaturaRequisicao()%
		   AND DBZ.DBZ_NUMDOC = %Exp:self:numeroDocumento%
           AND DBZ.DBZ_SEQINT <> %Exp:cSeqInt%
		   AND DBZ.%NotDel%
	EndSql
	cNewSeq := Iif(!Empty((cAliasDBZ)->MAXSEQINT), Soma1(PadL((cAliasDBZ)->MAXSEQINT, self:getTamSeqInt(), "0" )), Soma1(PadL("0",self:getTamSeqInt(),"0")))

    (cAliasDBZ)->(DbCloseArea())
Return cNewSeq

/*/{Protheus.doc} parseItemConvergencia    
    @author Alexsander Burigo Correa
    @since 16/12/2024
    @version 1.0
    @param cProduto, character, materia prima
    @param nQuantidade, numeric, Quantidade para requisitar
    @param cArmazem, character, Armazem
    @param cLote, character, Lote da materia prima
    @param dDataValidade, date, Data de validade do lote
    @return logico, logical, Indica sucesso
    /*/
Method parseItemConvergencia(cProduto, nQuantidade, cArmazem, cLote, dDataValidade) Class WMSSaasManufaturaRequisicao
Local aArea  := GetArea() 
Local oItem  := self:getItem(cProduto, cLote)

    If oItem <> Nil .And. !Empty(oItem:id)
        oItem:quantidade += nQuantidade
    Else
        AAdd(self:itensConvergencia, WMSSaasManufaturaItemRequisicao():set(;
                cProduto,;
                nQuantidade,;
                cArmazem,;
                cLote,;
                dDataValidade;
                ))
    EndIf
    RestArea(aArea)
Return .T.
