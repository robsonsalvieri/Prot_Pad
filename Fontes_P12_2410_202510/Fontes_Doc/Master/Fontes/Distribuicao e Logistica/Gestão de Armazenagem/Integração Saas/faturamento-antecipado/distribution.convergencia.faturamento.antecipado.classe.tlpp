#include "Totvs.ch"
#include "tlpp-core.th"
#include "distribution.convergencia.ch"

#define DEVOLUCAO "D"
#define BENEFICIAMENTO "B"
#define CONTROLA_ESTOQUE "S" 

Static FINALIZADO_WMSSAAS := "05"
Static nTamCli            := TamSx3("F2_CLIENTE")[1]
Static nTamLoja           := TamSx3("F2_LOJA")[1]
Static nTamDoc            := TamSx3("F2_DOC")[1]
Static nTamSerie          := TamSx3("F2_SERIE")[1]

/*/{Protheus.doc} WMSSaasFaturamentoAntecipado
Classe de pedidoVenda Wms Saas
@type class
@author Alexsander Burigo Corrêa
@since 10/10/2024
/*/
Class WMSSaasFaturamentoAntecipado From WMSSaasConvergencia implements WMSSaasConvergenciaInterface
    Public Method new(cDoc as character, cSerie as character, cFornece as character, cLoja as character, dCriacao as date, dEmissao as date , cTransp as character, cChvNfe as character)
    Public Method loadOrNew(cDoc as character, cSerie as character, cCliFor as character, cLoja as character)
    Public Method loadByChave(cDoc as character, cSerie as character, cCliFor as character, cLoja as character) as object
    Public Method loadByItem(cDoc as character, cProduto as character, cItem as character, cSequencia as character)
    Public Method canBeUpdated() as logical
    Public Method setLiberado() as object
    Public Method finaliza(finaliza as array) as logical
    Public Method isItemEligibleToIntegration() as logical
    Public Method parseItemConvergencia(nRecnoSC9 as numeric) as logical
    Public Method getNomePessoa() as character
    Public Method usaFornecedor() as logical
    Public Method getAllItems() as array
EndClass

/*/{Protheus.doc} WMSSaasFaturamentoAntecipado::New
    Carga das propriedades do cabeçalho da classe de convergência
    @type method
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @param cDoc, character, Numero da nota 
    @param cSerie, character, Serie da nota
    @param cFornece, character, Fornecedor
    @param cLoja, character, Loja
    @param dCriacao, date, Data de digitação
    @param dEmissao, date, Data de emissão
    @param cTransp, character, Transportador
    @return self, object, Objeto Faturamento Antecipado
/*/
Method New(cDoc, cSerie, cFornece, cLoja, dCriacao, dEmissao, cTransp) Class WMSSaasFaturamentoAntecipado
    self:status              := self:getStatusCriado()
    self:tipoTransacao       := self:getTipoTransacaoFaturamentoAntecipado()
    self:numeroDocumento     := cDoc
    self:serieDocumento      := cSerie
    self:clienteOuFornecedor := cFornece
    self:loja                := cLoja
    self:dataCriacao         := FwTimeStamp(3,dCriacao, Time())
    self:dataEmissao         := dEmissao
    self:transportador       := cTransp
    self:sequenciaIntegracao := ""
    self:mensagemIntegracao  := ""
    self:itensConvergencia   := {}
Return self

/*/{Protheus.doc} loadOrNew
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 16/09/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method loadOrNew(cDoc, cSerie, cCliFor, cLoja, dCriacao, dEmissao, cTransp) Class WMSSaasFaturamentoAntecipado
local oLoad := self:loadByChave(cDoc, cSerie, cCliFor, cLoja)
    If !Empty(oLoad:id) .And. self:canBeUpdated()
        // Load carrega todos os itens cadastrados, para que não duplique é necessário limpar o array
        oLoad:itensConvergencia := {}
        Return oLoad
    EndIf

Return self:new(cDoc, cSerie, cCliFor, cLoja, dCriacao, dEmissao, cTransp)

/*/{Protheus.doc} parseItemConvergencia
    (Converte SC9 liberada em objeto ItemConvergencia )
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param nRecnoSC9, numeric, recno do registro liberado
    @return lRet, logical, sucesso na conversao
    /*/
Method parseItemConvergencia(nRecnoSC9) Class WMSSaasFaturamentoAntecipado as logical
Local aArea  := GetArea() 
Local lRet   := .F.

    If self:isItemEligibleToIntegration(nRecnoSC9)
        AAdd(self:itensConvergencia, WMSSaasItemFaturamentoAntecipado():set(;
            SC9->C9_PRODUTO,;
            SC9->C9_ITEM,;
            SC9->C9_SEQUEN,;
            SC9->C9_QTDLIB,;
            SC9->C9_PRCVEN,;
            SC9->C9_LOCAL,;
            SC9->C9_LOTECTL,;
            IIf(!Empty(SC9->C9_LOTECTL),SC9->C9_DTVALID,);
            ))
        lRet := .T.
    EndIf

    RestArea(aArea)
Return lRet

/*/{Protheus.doc} loadByChave
    Faz o load da classe de faturamento antecipado e convergencia pela chave
    Esse metodo deve retornar apenas um registro da DBZ sempre, pois cada pedido de venda só pode ter uma sequencia em branco não integrada.
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param cDoc, character, Numero da nota 
    @param cSerie, character, Serie da nota
    @param cFornece, character, Fornecedor
    @param cLoja, character, Loja
    @return self, object, Objeto faturamento antecipado
/*/
Method loadByChave(cDoc, cSerie, cCliFor, cLoja) Class WMSSaasFaturamentoAntecipado
Local cAliasDBZ := GetNextAlias()
    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
		   AND DBZ.DBZ_TIPOTR = %Exp:self:getTipoTransacaoFaturamentoAntecipado()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDoc%
           AND DBZ.DBZ_SERIE  = %Exp:cSerie%
           AND DBZ.DBZ_CLIFOR = %Exp:cCliFor%
           AND DBZ.DBZ_LOJA   = %Exp:cLoja%
		   AND DBZ.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        self:loadById((cAliasDBZ)->IDCONVERGENCIA)
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return self

/*/{Protheus.doc} loadByItem
    Faz o load da chave de faturamento antecipado
    Esse metodo deve retornar apenas um registro da DBZ sempre, pois cada pedido de venda só pode ter uma sequencia em branco não integrada.
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @param cDoc, character, Documento
    @param cProduto, character, Produto
    @param cItem, character, Item
    @param cSequencia, character, Sequencia
    @return lRet, logico, item na tabela de convergencia
/*/
Method loadByItem(cDoc, cProduto, cItem, cSequencia) Class WMSSaasFaturamentoAntecipado
Local cId := WMSSaasItemFaturamentoAntecipado():getIdByItem(cDoc, cProduto, cItem, cSequencia)

    If !Empty(cId)
        self:loadById(cId)
    Else
        Return .F.
    EndIf

Return self

/*/{Protheus.doc} canBeUpdated
    Retorna status nos quais os faturamentos anteciados podem ser alterados
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return lRet, logical, Retorna se permite alteração
    /*/
Method canBeUpdated() as logical Class WMSSaasFaturamentoAntecipado
Return self:isCriado() .Or. self:isCancelado() .Or. self:isErroIntegracao()

/*/{Protheus.doc} setLiberado
    Atualiza status para liberado na convergência
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return self, object, objeto faturamento antecipado
    /*/
Method setLiberado() Class WMSSaasFaturamentoAntecipado
    If WMSSaasSeekDBZById(self:id) .And. RecLock("DBZ", .F.)
        DBZ->DBZ_STATUS := self:getStatusLiberado()
        DBZ->(MsUnlock())

        self:load()
    EndIf
Return self

/*/{Protheus.doc} finaliza
    Metodo de finalizacao do faturamento Antecipado
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param aFinalizados, array, array de json com finalizados
    {
         "produto": aProdutos[item]['produto']['codigo'],
         "item": aReservas[reserva]['sequencia'],
         "sequencia": aReservas[reserva]['sequencia'],
         "tipoEstoque": aReservas[reserva]['tipoEstoque'],
         "quantidade": aReservas[reserva]['quantidade'],
         "lote": aReservas[reserva]['caracteristicas']['lote'],
         "dataValidade": aReservas[reserva]['caracteristicas']['dataValidade']
      }
    @return lRet, logical, Retorna com sucesso
/*/
Method finaliza(aFinalizados) Class WMSSaasFaturamentoAntecipado as logical
    self:setFinalizado()
Return .T.

/*/{Protheus.doc} isItemEligibleToIntegration
    Verifica se item do faturamento antecipado é elegivel para ser integrado ao Saas.
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param nRecno, numeric, Recno 
    @return lRet, logical, Retorno sucesso
    /*/
Method isItemEligibleToIntegration(nRecno) Class WMSSaasFaturamentoAntecipado as logical
Local aAreaSC9  := SC9->(GetArea())
Local aAreaSC6  := SC6->(GetArea())
Local lRet      := .F.
    
    DbSelectArea("SC9")
    SC9->(DbGoTo(nRecno))

    SC6->(DbSetOrder(1))
    SC6->(DbSeek(xFilial("SC6")+SC9->C9_PEDIDO+SC9->C9_ITEM+SC9->C9_PRODUTO))

    // -- Se item não controla WMS SaaS, não deve ser enviado
    If WMSSaasInt(SC9->C9_PRODUTO, SC9->C9_LOCAL)
        lRet := .T.
        // -- Se TES está preenchida e não controla estoque, não deve ser enviado
        If lRet .And. Alltrim(GetAdvFVal("SF4","F4_ESTOQUE",xFilial("SF4")+SC6->C6_TES,1)) != CONTROLA_ESTOQUE
            lRet := .F.
        EndIf
    EndIf
    RestArea(aAreaSC9)
    RestArea(aAreaSC6)
Return lRet

/*/{Protheus.doc} getNomePessoa
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return cNome, character, Nome do cliente
    /*/
Method getNomePessoa() Class WMSSaasFaturamentoAntecipado as character
Local jCliente := WMSSaasClienteAdapter():montaPayload(Padr(self:clienteOuFornecedor,nTamCli), Padr(self:loja,nTamLoja), self:usaFornecedor())
Return jCliente['nome']

/*/{Protheus.doc} usaFornecedor
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return cVal, character, Retorno tipo
    /*/
Method usaFornecedor() Class WMSSaasFaturamentoAntecipado as logical
Return GetAdvFVal(  "SF2",;
                    "F2_TIPO", xFilial("SF2")+;
                    Padr(self:numeroDocumento,nTamDoc)+;
                    Padr(self:serieDocumento,nTamSerie)+;
                    Padr(self:clienteOuFornecedor,nTamCli)+;
                    Padr(self:loja,nTamLoja),;
                    1,; 
                    "") $ BENEFICIAMENTO+"/"+DEVOLUCAO

/*/{Protheus.doc} getAllItems
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return Object, objeto, Objeto do item do faturamento
    /*/
Method getAllItems() Class WMSSaasFaturamentoAntecipado as array
Return WMSSaasItemFaturamentoAntecipado():loadAllByParentId(self:id)
