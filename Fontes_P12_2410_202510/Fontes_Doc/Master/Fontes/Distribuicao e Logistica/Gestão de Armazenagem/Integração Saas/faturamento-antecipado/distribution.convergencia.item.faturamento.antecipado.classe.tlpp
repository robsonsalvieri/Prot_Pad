#include "Totvs.ch"
#include "tlpp-core.th"

/*/{Protheus.doc} WMSSaasItemFaturamentoAntecipado
    Classe de Item Faturamento Antecipado
    @type class
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    /*/
Class WMSSaasItemFaturamentoAntecipado From WMSSaasItemConvergencia
    Public Method set(cProduto as character, cItem as character, cSequencia as character, nQuantidade as numeric, nValorUnitario as numeric, cArmazem as character, cLote as character, dDataValidade as date, cNotaFiscal as character, cSerieNotaFiscal as character) as object
    Static Method getIdByItem(cDoc as character, cProduto as character, cItem as character, cSequencia as character) as character
    Static Method loadAllByParentId(cParentId as character) as array
    Public Method load()
EndClass

/*/{Protheus.doc} new
    (long_description)
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param cProduto, character, Produto
    @param cItem, character, Item
    @param cSequencia, character, Sequencia
    @param nQuantidade, decimal, Quantidade
    @param nValorUnitario, decimal, Valor Unitário
    @param cArmazem, character, Armazém
    @param cLote, character, Lote
    @param dDataValidade, date, Data Validade
    @param cNotaFiscal, character, Numero da nota fiscal
    @param cSerieNotaFiscal, character, Série da nota fiscal
    @return Self, object, Objeto tipo ItemConvergencia    
    /*/
Method set(cProduto, cItem, cSequencia, nQuantidade, nValorUnitario, cArmazem, cLote, dDataValidade, cNotaFiscal, cSerieNotaFiscal) Class WMSSaasItemFaturamentoAntecipado as object
    self:produto         := cProduto
    self:item            := cItem
    self:sequencia       := cSequencia
    self:quantidade      := nQuantidade
    self:valorUnitario   := nValorUnitario
    self:armazem         := cArmazem
    self:lote            := cLote
    self:dataValidade    := dDataValidade
    self:notaFiscal      := cNotaFiscal
    self:serieNotaFiscal := cSerieNotaFiscal
Return self

/*/{Protheus.doc} load
    Carga de dados da convergência do item do faturamento antecipado
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @return Self, object, _Super
    /*/
Method load() Class WMSSaasItemFaturamentoAntecipado
Return _Super:load()

/*/{Protheus.doc} getIdByItem
    Carga pelo chave do item da convergência do item do faturamento antecipado
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param cDoc, character, Documento
    @param cProduto, character, Produto
    @param cItem, character, Item
    @param cSequenciac, character, Sequencia
    @return cId, character, Identificador da convergência
    /*/
Method getIdByItem(cDoc, cProduto, cItem, cSequencia) as character Class WMSSaasItemFaturamentoAntecipado
Local cAliasDBZ := GetNextAlias()
Local cId       := ""

    BeginSql Alias cAliasDBZ
		SELECT DBZ.DBZ_ID IDCONVERGENCIA
		  FROM %Table:DBZ% DBZ
         INNER JOIN %Table:DBY% DBY
            ON DBZ_FILIAL     = DBY_FILIAL
           AND DBZ_ID         = DBY_ID
		 WHERE DBZ.DBZ_FILIAL = %Exp:xFilial("DBZ")%
           AND DBY.DBY_FILIAL = %xFilial:DBY%
		   AND DBZ.DBZ_TIPOTR = %Exp:WMSSaasConvergencia():getTipoTransacaoFaturamentoAntecipado()%
		   AND DBZ.DBZ_NUMDOC = %Exp:cDoc%
           AND DBY.DBY_PRODUT = %Exp:cProduto%
           AND DBY.DBY_ITEM   = %Exp:cItem%
           AND DBY.DBY_SEQUEN = %Exp:cSequencia%
		   AND DBZ.%NotDel%
           AND DBY.%NotDel%
	EndSql

    If (cAliasDBZ)->(!Eof())
        cId := (cAliasDBZ)->IDCONVERGENCIA
    EndIf
    (cAliasDBZ)->(DbCloseArea())
Return cId

/*/{Protheus.doc} loadAllByParentId
    Carrega todos os itens da integração pelo ID do Pai (DBZ)
    @author Alexsander Burigo Corrêa
    @since 10/10/2024
    @version 1.0
    @param cParentId, character, id da tabela DBZ
    @return aItems, array, array de objetos dos itens
/*/
Method loadAllByParentId(cParentId) as array Class WMSSaasItemFaturamentoAntecipado
Local oItemConvergencia
Local aItems := {}
    
    If WMSSaasSeekDBYById(cParentId)
        While DBY->DBY_ID == cParentId
            oItemConvergencia := WMSSaasItemFaturamentoAntecipado():load()
            aAdd(aItems, oItemConvergencia)
            DBY->(DbSkip())
        End
    EndIf
Return aItems
