#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.aproveitamento_veiculo_carga.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DA3, DAK", name="Aproveitamento Veículos por Carga", country="ALL", initialRelease="12.1.2310")
class OMSR081TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    data lExistPergunte as logical

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 17/05/2024 
*/
//-------------------------------------------------------------------
method new() as object class OMSR081TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Aproveitamento Veículos por Carga") 

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMRT02") 
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 17/05/2024 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR081TReportsBusinessObject
return "Objeto contendo informações do Aproveitamento Veículos por Carga"//"Objeto contendo informações do Aproveitamento Veículos por Carga"
 
method getAreas() as array class OMSR081TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 17/05/2024  
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR081TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cDataDe as character 
    local cDataAte as character 
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    local jParams as json
    local nPercPeso as numeric
    local nPercVol as numeric 
    local cNomeMot as character
    local oQryOMSR081 as object

    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    cDataDe     := ArrTokStr(jParams['MV_PAR05'])
    cDataAte    := ArrTokStr(jParams['MV_PAR06'])
       
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 
   
    cQuery := "SELECT DA3_FILIAL, DA3_COD, DA3_DESC, DA3_PLACA, DA3_CAPACN, DA3_CAPACM, DA3_VOLMAX, DAK_FILIAL, DAK_COD, DAK_SEQCAR, DAK_MOTORI, DAK_ROTEIR, DAK_PESO, DAK_CAPVOL, DAK_VALOR, DAK_DATA, DAK_HORA "
    cQuery += " FROM "
    cQuery += RetSqlName("DA3") + " DA3 "
    cQuery += " INNER JOIN " + RetSqlName('DAK') + " DAK "
	cQuery += " ON DAK.DAK_CAMINH = DA3_COD"
	cQuery += " AND DAK.DAK_FILIAL = ? "
    cQuery += " WHERE "
    cQuery += " DA3_FILIAL = ? " 
    cQuery += " AND DA3_COD BETWEEN ? AND ? "
    cQuery += " AND DA3.D_E_L_E_T_ = ? "
    cQuery += " AND DAK_FILIAL = ? "   
    cQuery += " AND DAK_COD BETWEEN ? AND ? "        
    cQuery += " AND DAK_DATA BETWEEN ? AND ?  "             
    cQuery += " AND DAK.D_E_L_E_T_ = ? "        
    cQuery += " ORDER BY DA3_FILIAL, DA3_COD "

    oQryOMSR081 := FwExecStatement():New()

    oQryOMSR081:SetQuery(cQuery)
    oQryOMSR081:SetString(1, FwxFilial('DAK'))
    oQryOMSR081:SetString(2, xFilial("DA3"))
    oQryOMSR081:SetString(3, ArrTokStr(jParams['MV_PAR03']))
    oQryOMSR081:SetString(4, ArrTokStr(jParams['MV_PAR04']))
    oQryOMSR081:SetString(5, ' ')
    oQryOMSR081:SetString(6, xFilial("DAK"))
    oQryOMSR081:SetString(7, ArrTokStr(jParams['MV_PAR01']))
    oQryOMSR081:SetString(8, ArrTokStr(jParams['MV_PAR02']))
    oQryOMSR081:SetString(9, cDataDe)
    oQryOMSR081:SetString(10, cDataAte)
    oQryOMSR081:SetString(11, ' ')

    cAlias := oQryOMSR081:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NREDUZ")
            
        //-- Percentual de ocupação em peso = (DAK_PESO / DA3_CAPACN ) * 100 
        nPercPeso := ((cAlias)->DAK_PESO / (cAlias)->DA3_CAPACN ) * 100
        //-- Percentual de ocupação em Volume = (DAK_CAPVOL / DA3_VOLMAX ) * 100
        nPercVol := ((cAlias)->DAK_CAPVOL / (cAlias)->DA3_VOLMAX ) * 100
        
        self:oData:appendData({"DA3_FILIAL": (cAlias)->DA3_FILIAL,;
                        "DA3_COD":      (cAlias)->DA3_COD,;
                        "DA3_DESC":     (cAlias)->DA3_DESC,;
                        "DA3_PLACA":    (cAlias)->DA3_PLACA,;
                        "DA3_CAPACN":   (cAlias)->DA3_CAPACN,;
                        "DA3_CAPACM":   (cAlias)->DA3_CAPACM,;
                        "DA3_VOLMAX":   (cAlias)->DA3_VOLMAX,;
                        "DAK_FILIAL":   (cAlias)->DAK_FILIAL,;
                        "DAK_COD":      (cAlias)->DAK_COD,;
                        "DAK_SEQCAR":   (cAlias)->DAK_SEQCAR,;
                        "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                        "DESCMOT":      cNomeMot,;
                        "DAK_ROTEIR":   (cAlias)->DAK_ROTEIR,;
                        "DAK_PESO":     (cAlias)->DAK_PESO,;
                        "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                        "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                        "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                        "DAK_HORA":     (cAlias)->DAK_HORA,; 
                        "PERCPESO":     nPercPeso,;         
                        "PERCVOL":      nPercVol})
            
        (cAlias)->( dbSkip() )
        nCount++
    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR081 <> Nil
		oQryOMSR081:Destroy()
		oQryOMSR081 := NIL
	    FwFreeObj(oQryOMSR081)
	EndIf

    RestArea( aAreaDAK )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 20/05/2024 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR081TReportsBusinessObject
    local aFieldsDA3 as array
    local aFieldsDAK as array
 
    aFieldsDA3 := { "DA3_COD", "DA3_DESC", "DA3_PLACA", "DA3_CAPACN", "DA3_CAPACM", "DA3_VOLMAX" }
    aFieldsDAK := { "DAK_FILIAL", "DAK_COD", "DAK_SEQCAR", "DAK_MOTORI", "DAK_ROTEIR", "DAK_PESO", "DAK_CAPVOL", "DAK_VALOR", "DAK_DATA", "DAK_HORA" }
       
    self:oSchema:aliasToSchema("DA3", aFieldsDA3)
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)    
    self:oSchema:addProperty("PERCPESO", "% Ocup. Peso", "number", "% Ocup. Peso", "PERCPESO")    
    self:oSchema:addProperty("PERCVOL", "% Ocup. Volume", "number", "% Ocup. Volume", "PERCVOL")
    self:oSchema:addProperty("DESCMOT", "Nome Motorista", "string", "Nome Motorista", "DESCMOT")
      
return self:oSchema
