#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.roteirizacoes.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DA5, DA6, DA7, DA8, DA9, SA1", name="Roteirizações", country="ALL", initialRelease="12.1.2210")
class OMSR061TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/11/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR061TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Roteirizações")//"Produtos por Carga"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMR060") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/11/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR061TReportsBusinessObject
return "Objeto contendo informações de Roteirizações"//"Objeto contendo informações de Roteirizações"
 
method getAreas() as array class OMSR061TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 06/11/2023  
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR061TReportsBusinessObject
    local aAreaDA8 as array
    local cAlias as character
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    local jParams as json
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDA8    := DA8->( GetArea() )
    lHasNext    := .F.
    
    self:setPageSize(100)

    //--Query Roteirizações 
    cQuery := "SELECT "
    cQuery += "DA8_FILIAL, DA8_COD,DA8_DESC,DA9_FILIAL,DA9_ROTEIR,DA9_SEQUEN,DA9_PERCUR,"
    cQuery += "DA9_ROTA,DA5_COD,DA5_DESC,DA6_ROTA,DA6_REF,DA7_FILIAL,DA7_SEQUEN,"
    cQuery += "DA7_PERCUR,DA7_ROTA,DA7_CLIENT,DA7_LOJA,DA7_CEPDE,DA7_CEPATE,DA7_REF,A1_NOME,A1_END,A1_MUN,A1_EST,A1_CEP FROM "
    cQuery += RetSqlName("DA5") + " DA5 ,"
    cQuery += RetSqlName("DA6") + " DA6 ,"
    cQuery += RetSqlName("DA7") + " DA7 ,"
    cQuery += RetSqlName("DA8") + " DA8 ,"
    cQuery += RetSqlName("DA9") + " DA9 ,"
    cQuery += RetSqlName("SA1") + " SA1 "
    cQuery += "WHERE "
    cQuery += "DA8_FILIAL = ? AND "
    cQuery += "DA8_COD >= ? AND "
    cQuery += "DA8_COD <= ? AND "
    cQuery += "DA9_FILIAL = ? AND "
    cQuery += "DA9_PERCUR >= ? AND "
    cQuery += "DA9_PERCUR <= ? AND "
    cQuery += "DA9_ROTA >= ? AND "
    cQuery += "DA9_ROTA <= ? AND "
    cQuery += "DA8_COD = DA9_ROTEIR AND "
    cQuery += "DA8.D_E_L_E_T_ = ? AND "
    cQuery += "DA9.D_E_L_E_T_ = ? AND "

    cQuery += "DA7_FILIAL = ? AND "
    cQuery += "DA7_PERCUR = DA9_PERCUR AND "
    cQuery += "DA7_ROTA = DA9_ROTA AND "
    cQuery += "DA7_CLIENT >= ? AND "
    cQuery += "DA7_CLIENT <= ? AND "
    cQuery += "DA7_LOJA >= ? AND "
    cQuery += "DA7_LOJA <= ? AND "
    cQuery += "DA7.D_E_L_E_T_ = ? AND "

    cQuery += "A1_FILIAL = ? AND "
    cQuery += "A1_COD = DA7_CLIENT AND "
    cQuery += "A1_LOJA = DA7_LOJA AND "
    cQuery += "A1_CEP >= ? AND "
    cQuery += "A1_CEP <= ? AND "
    cQuery += "SA1.D_E_L_E_T_ = ? AND "

    cQuery += "DA6_FILIAL = ? AND "
    cQuery += "DA6_PERCUR = DA9_PERCUR AND "
    cQuery += "DA6_ROTA = DA9_ROTA AND "
    cQuery += "DA6.D_E_L_E_T_ = ? AND "

    cQuery += "DA5_FILIAL = ? AND "
    cQuery += "DA5_COD = DA9_PERCUR AND "
    cQuery += "DA5_VENDED >= ? AND "
    cQuery += "DA5_VENDED <= ? AND "
    cQuery += "DA5.D_E_L_E_T_ = ? "

    cQuery += "UNION "
    cQuery += "SELECT "

    cQuery += "DA8_FILIAL,DA8_COD,DA8_DESC,DA9_FILIAL,DA9_ROTEIR,DA9_SEQUEN,DA9_PERCUR,"
    cQuery += "DA9_ROTA,DA5_COD,DA5_DESC,DA6_ROTA,DA6_REF,DA7_FILIAL,DA7_SEQUEN,"
    cQuery += "DA7_PERCUR,DA7_ROTA,DA7_CLIENT,DA7_LOJA,DA7_CEPDE,DA7_CEPATE,DA7_REF,' ' A1_NOME,' ' A1_END,' ' A1_MUN,' ' A1_EST,' ' A1_CEP FROM "
    cQuery += RetSqlName("DA5") + " DA5 ,"
    cQuery += RetSqlName("DA6") + " DA6 ,"
    cQuery += RetSqlName("DA7") + " DA7 ,"
    cQuery += RetSqlName("DA8") + " DA8 ,"
    cQuery += RetSqlName("DA9") + " DA9 ,"
    cQuery += RetSqlName("SA1") + " SA1 "
    cQuery += "WHERE "
    cQuery += "DA8_FILIAL = ? AND "
    cQuery += "DA8_COD >= ? AND "
    cQuery += "DA8_COD <= ? AND "
    cQuery += "DA9_FILIAL = ? AND "
    cQuery += "DA9_PERCUR >= ? AND "
    cQuery += "DA9_PERCUR <= ? AND "
    cQuery += "DA9_ROTA >= ? AND "
    cQuery += "DA9_ROTA <= ? AND "
    cQuery += "DA8_COD = DA9_ROTEIR AND "
    cQuery += "DA8.D_E_L_E_T_ = ? AND "
    cQuery += "DA9.D_E_L_E_T_ = ? AND "

    cQuery += "DA7_FILIAL = ? AND "
    cQuery += "DA7_PERCUR = DA9_PERCUR AND "
    cQuery += "DA7_ROTA = DA9_ROTA AND "
    cQuery += "DA7_CLIENT >= ? AND "
    cQuery += "DA7_CLIENT <= ? AND "
    cQuery += "DA7_LOJA >= ? AND "
    cQuery += "DA7_LOJA <= ? AND "
    cQuery += "DA7.D_E_L_E_T_ = ? AND "

    cQuery += "NOT EXISTS ( SELECT A1_COD, A1_LOJA FROM  "+RetSqlName( "SA1" )+" SA1 "
    cQuery += "WHERE A1_FILIAL = ? AND "
    cQuery += "A1_COD = DA7_CLIENT AND "
    cQuery += "A1_LOJA = DA7_LOJA AND "
    cQuery += "A1_CEP >= ? AND "
    cQuery += "A1_CEP <= ? AND "
    cQuery += "SA1.D_E_L_E_T_ = ?) AND "

    cQuery += "DA6_FILIAL = ? AND "
    cQuery += "DA6_PERCUR = DA9_PERCUR AND "
    cQuery += "DA6_ROTA = DA9_ROTA AND "
    cQuery += "DA6.D_E_L_E_T_ = ? AND "

    cQuery += "DA5_FILIAL = ? AND "
    cQuery += "DA5_COD = DA9_PERCUR AND "
    cQuery += "DA5_VENDED >= ? AND "
    cQuery += "DA5_VENDED <= ? AND "
    cQuery += "DA5.D_E_L_E_T_ = ? "

    cQuery += "ORDER BY DA8_COD,DA9_SEQUEN,DA9_PERCUR,DA9_ROTA,DA7_SEQUEN,DA7_CLIENT,DA7_LOJA"

    oQryOMSR061 := FwExecStatement():New()
    
    oQryOMSR061:SetQuery(cQuery)
    oQryOMSR061:SetString(1, xFilial("DA8"))
    oQryOMSR061:SetString(2, ArrTokStr(jParams['MV_PAR01']))
    oQryOMSR061:SetString(3, ArrTokStr(jParams['MV_PAR02']))
    oQryOMSR061:SetString(4, xFilial("DA9"))
    oQryOMSR061:SetString(5, ArrTokStr(jParams['MV_PAR03']))
    oQryOMSR061:SetString(6, ArrTokStr(jParams['MV_PAR04']))
    oQryOMSR061:SetString(7, ArrTokStr(jParams['MV_PAR05']))
    oQryOMSR061:SetString(8, ArrTokStr(jParams['MV_PAR06']))
    oQryOMSR061:SetString(9, ' ')
    oQryOMSR061:SetString(10, ' ')

    oQryOMSR061:SetString(11, xFilial("DA7"))
    oQryOMSR061:SetString(12, ArrTokStr(jParams['MV_PAR07']))
    oQryOMSR061:SetString(13, ArrTokStr(jParams['MV_PAR08']))
    oQryOMSR061:SetString(14, ArrTokStr(jParams['MV_PAR09']))
    oQryOMSR061:SetString(15, ArrTokStr(jParams['MV_PAR10']))
    oQryOMSR061:SetString(16, ' ')

    oQryOMSR061:SetString(17, xFilial("SA1"))
    oQryOMSR061:SetString(18, ArrTokStr(jParams['MV_PAR11']))
    oQryOMSR061:SetString(19, ArrTokStr(jParams['MV_PAR12']))
    oQryOMSR061:SetString(20, ' ')

    oQryOMSR061:SetString(21, xFilial("DA6"))
    oQryOMSR061:SetString(22, ' ')

    oQryOMSR061:SetString(23, xFilial("DA5"))
    oQryOMSR061:SetString(24, ArrTokStr(jParams['MV_PAR13']))
    oQryOMSR061:SetString(25, ArrTokStr(jParams['MV_PAR14']))
    oQryOMSR061:SetString(26, ' ')

    //QUERY UNION
    oQryOMSR061:SetString(27, xFilial("DA8"))
    oQryOMSR061:SetString(28, ArrTokStr(jParams['MV_PAR01']))
    oQryOMSR061:SetString(29, ArrTokStr(jParams['MV_PAR02']))
    oQryOMSR061:SetString(30, xFilial("DA9"))
    oQryOMSR061:SetString(31, ArrTokStr(jParams['MV_PAR03']))
    oQryOMSR061:SetString(32, ArrTokStr(jParams['MV_PAR04']))
    oQryOMSR061:SetString(33, ArrTokStr(jParams['MV_PAR05']))
    oQryOMSR061:SetString(34, ArrTokStr(jParams['MV_PAR06']))
    oQryOMSR061:SetString(35, ' ')
    oQryOMSR061:SetString(36, ' ')

    oQryOMSR061:SetString(37, xFilial("DA7"))
    oQryOMSR061:SetString(38, ArrTokStr(jParams['MV_PAR07']))
    oQryOMSR061:SetString(39, ArrTokStr(jParams['MV_PAR08']))
    oQryOMSR061:SetString(40, ArrTokStr(jParams['MV_PAR09']))
    oQryOMSR061:SetString(41, ArrTokStr(jParams['MV_PAR10']))
    oQryOMSR061:SetString(42, ' ')

    oQryOMSR061:SetString(43, xFilial("SA1"))
    oQryOMSR061:SetString(44, ArrTokStr(jParams['MV_PAR11']))
    oQryOMSR061:SetString(45, ArrTokStr(jParams['MV_PAR12']))
    oQryOMSR061:SetString(46, ' ')

    oQryOMSR061:SetString(47, xFilial("DA6"))
    oQryOMSR061:SetString(48, ' ')

    oQryOMSR061:SetString(49, xFilial("DA5"))
    oQryOMSR061:SetString(50, ArrTokStr(jParams['MV_PAR13']))
    oQryOMSR061:SetString(51, ArrTokStr(jParams['MV_PAR14']))
    oQryOMSR061:SetString(52, ' ')
    
    cAlias := oQryOMSR061:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())
		
        self:oData:appendData({"DA8_COD": (cAlias)->DA8_COD,;
                "DA8_DESC":      (cAlias)->DA8_DESC,;
                "DA9_SEQUEN":   (cAlias)->DA9_SEQUEN,;
                "DA9_PERCUR":   (cAlias)->DA9_PERCUR,;
                "DA5_DESC":     (cAlias)->DA5_DESC,;
                "DA9_ROTA":     (cAlias)->DA9_ROTA,;
                "DA6_REF":      (cAlias)->DA6_REF,;
                "DA7_SEQUEN":   (cAlias)->DA7_SEQUEN,;  
                "DA7_CEPDE":    (cAlias)->DA7_CEPDE,;  
                "DA7_CEPATE":   (cAlias)->DA7_CEPATE,;
                "DA7_CLIENT":   (cAlias)->DA7_CLIENT,;
                "DA7_LOJA":     (cAlias)->DA7_LOJA,;
                "A1_NOME":      (cAlias)->A1_NOME,;
                "A1_END":       (cAlias)->A1_END,; 
                "A1_MUN":       (cAlias)->A1_MUN,;
                "A1_EST":       (cAlias)->A1_EST,;
                "A1_CEP":       (cAlias)->A1_CEP,;
                "DA7_REF":      (cAlias)->DA7_REF})
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR061 <> Nil
		oQryOMSR061:Destroy()
		oQryOMSR061 := NIL
		FwFreeObj(oQryOMSR061)
	EndIf

    RestArea( aAreaDA8 )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 07/11/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR061TReportsBusinessObject
    local aFieldsDA8 as array
    local aFieldsDA9 as array
    local aFieldsDA7 as array
    local aFieldsSA1 as array 
 
    aFieldsDA8 := { "DA8_COD", "DA8_DESC" }
    aFieldsDA9 := { "DA9_SEQUEN", "DA9_PERCUR", "DA9_ROTA"}
    aFieldsDA7 := { "DA7_SEQUEN", "DA7_CEPDE", "DA7_CEPATE", "DA7_CLIENT", "DA7_LOJA", "DA7_REF" }
    aFieldsSA1 := { "A1_NOME", "A1_END", "A1_MUN", "A1_EST", "A1_CEP" }
        
    self:oSchema:aliasToSchema("DA8", aFieldsDA8)  
    self:oSchema:aliasToSchema("DA9", aFieldsDA9)
    self:oSchema:aliasToSchema("DA7", aFieldsDA7)
    self:oSchema:aliasToSchema("SA1", aFieldsSA1)    
    self:oSchema:addProperty("DA5_DESC", "Descricao da Rota", "string", "Descrição", "DA5_DESC")
    self:oSchema:addProperty("DA6_REF", "Ponto de Referencia", "string", "Pto.Referen. Setor", "DA6_REF")
  
return self:oSchema

