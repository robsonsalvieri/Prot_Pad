#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"

namespace totvs.protheus.sigaoms.produtos_por_carga.treportsintegratedprovider
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAOMS", tables="DAK, DAI, SC9", name="Produtos por Carga", country="ALL", initialRelease="12.1.2210")
class OMSR021TReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getAreas() as array
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} New
Metodo de instancia da classe.
@type Metodo
@author Rafael Souza  
@version 12
@since 18/08/2023 
*/
//-------------------------------------------------------------------
method new() as object class OMSR021TReportsBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName("Produtos por Carga")//"Produtos por Carga"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("OMRT01") 
    
    //Indica que o LookUp será do tipo padrão LookUp
    self:setIsLookUp(.T.)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Metodo que retorna a descrição do objeto de negócio.
@type Metodo
@author Rafael Souza  
@version 12
@since 18/08/2023 
*/
//------------------------------------------------------------------- 
method getDescription() as character class OMSR021TReportsBusinessObject
return "Objeto contendo informações de Produtos por Carga"//"Objeto contendo informações de Produtos por Carga"
 
method getAreas() as array class OMSR021TReportsBusinessObject
return {"SIGAOMS"}

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna o Objeto de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 18/08/2023  
*/
//-------------------------------------------------------------------  
method getData(nPage as numeric, oFilter as object) as object class OMSR021TReportsBusinessObject
    local aAreaDAK as array
    local cAlias as character
    local cNomeMot as character
    local cNomeAjud1 as character 
    local cDescVei as character 
    local cDescProd as character
    local cDataDe as character 
    local cDataAte as character
    local aValores as array 
    local nValor as numeric
    local nVolume as numeric
    local nQtdLib as numeric
    local nQtdLib2 as numeric   
    local nPeso as numeric
    local cCodEmb as character
    local cCod2Emb as character     
    local cQuery as character
    local lHasNext as logical
    local nCount as numeric
    local jParams as json
    Local cSubTit as character  
    Local oQryOMSR021 as object
    
    jParams     := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    aAreaDAK    := DAK->( GetArea() )
    lHasNext    := .F.
    cDataDe     := ArrTokStr(jParams['MV_PAR05'])
    cDataAte    := ArrTokStr(jParams['MV_PAR06'])  
    cSubTit     := ArrTokStr(jParams['MV_PAR11'])  
    
    self:setPageSize(100)

    If ValType(cDataDe) == "C"
        cDataDe     := StrTran( cDataDe , "-" , "" )
        cDataAte    := StrTran( cDataAte , "-" , "" )
    EndIf 

    cQuery  := "SELECT DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR, DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_AJUDA1, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, DAK.DAK_DATA, DAK.DAK_HORA "
    cQuery  += ", SC9.C9_FILIAL, SC9.C9_PRODUTO, SC9.C9_LOTECTL, SC9.C9_NUMLOTE "
    cQuery  += "FROM " + RetSqlName("DAK") + " DAK "
    cQuery  += "INNER JOIN " + RetSqlName("DAI") + " DAI ON " + FWJoinFilial( "DAI", "DAK" ) + " AND DAI.DAI_COD = DAK.DAK_COD AND DAI.D_E_L_E_T_ = ' ' "
    cQuery  += "INNER JOIN " + RetSqlName("SC9") + " SC9 ON SC9.C9_FILIAL = DAI.DAI_FILPV AND SC9.C9_PEDIDO = DAI.DAI_PEDIDO AND SC9.C9_CARGA = DAI.DAI_COD AND SC9.D_E_L_E_T_ = ' ' "
    cQuery  += "WHERE DAK.D_E_L_E_T_ = ' '"

    If Len(jParams['MV_PAR01']) > 0
        cQuery += " AND DAK.DAK_FILIAL BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_COD    BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_CAMINH BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_MOTORI BETWEEN ? AND ? "
        cQuery += " AND DAK.DAK_DATA >= ? "
        cQuery += " AND DAK.DAK_DATA <= ? "
    EndIf 

    //Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        cQuery +=   " AND " + oFilter:getSQLExpression()
    EndIf

    cQuery  += "GROUP BY DAK.DAK_FILIAL, DAK.DAK_COD, DAK.DAK_SEQCAR, DAK.DAK_CAMINH, DAK.DAK_MOTORI, DAK.DAK_AJUDA1, DAK.DAK_PESO, DAK.DAK_CAPVOL, DAK.DAK_PTOENT, DAK.DAK_VALOR, DAK.DAK_DATA, DAK.DAK_HORA "
    cQuery  += ",SC9.C9_FILIAL, SC9.C9_PRODUTO, SC9.C9_LOTECTL, SC9.C9_NUMLOTE "
    
    oQryOMSR021 := FwExecStatement():New()

    oQryOMSR021:SetQuery(cQuery)

    If Len(jParams['MV_PAR01']) > 0   
        oQryOMSR021:SetString(1, ArrTokStr(jParams['MV_PAR01']))
        oQryOMSR021:SetString(2, ArrTokStr(jParams['MV_PAR02']))
        oQryOMSR021:SetString(3, ArrTokStr(jParams['MV_PAR03']))
        oQryOMSR021:SetString(4, ArrTokStr(jParams['MV_PAR04']))
        oQryOMSR021:SetString(5, ArrTokStr(jParams['MV_PAR07']))
        oQryOMSR021:SetString(6, ArrTokStr(jParams['MV_PAR08']))
        oQryOMSR021:SetString(7, ArrTokStr(jParams['MV_PAR09']))
        oQryOMSR021:SetString(8, ArrTokStr(jParams['MV_PAR10']))
        oQryOMSR021:SetString(9, cDataDe)
        oQryOMSR021:SetString(10, cDataAte)
    EndIf 

    cAlias := oQryOMSR021:OpenAlias()

    //Posiciona no registro inicial
    If nPage == 1
        (cAlias)->( DBGoTop() )
    EndIf

    while !(cAlias)->(Eof())

        cNomeMot    := Posicione("DA4",1,xFilial("DA4")+(cAlias)->DAK_MOTORI,"DA4_NREDUZ")
        cNomeAjud1  := Posicione("DAU",1,xFilial("DAU")+(cAlias)->DAK_AJUDA1,"DAU_NREDUZ")
        cDescVei    := Posicione("DA3",1,xFilial("DA3")+(cAlias)->DAK_CAMINH,"DA3_DESC")
        cDescProd   := Posicione("SB1",1,xFilial("SB1")+(cAlias)->C9_PRODUTO,"B1_DESC")
        cCodEmb     := Posicione("SB1",1,xFilial("SB1")+(cAlias)->C9_PRODUTO,"B1_UM")
        cCod2Emb    := Posicione("SB1",1,xFilial("SB1")+(cAlias)->C9_PRODUTO,"B1_SEGUM")

        aValores := QtdSumSC9(cAlias)
		nQtdLib  := aValores[1]
		nQtdLib2 := aValores[2]
		nValor   := aValores[3]
		nVolume  := aValores[4]
		nPeso    := aValores[5]
		
        self:oData:appendData({"DAK_FILIAL": (cAlias)->DAK_FILIAL,;
                "DAK_COD":      (cAlias)->DAK_COD,;
                "DAK_SEQCAR":   (cAlias)->DAK_SEQCAR,;
                "DAK_CAMINH":   (cAlias)->DAK_CAMINH,;
                "DESC_VEI":     cDescVei,;
                "DAK_MOTORI":   (cAlias)->DAK_MOTORI,;
                "NOMEMOT":      cNomeMot,;
                "DAK_AJUDA1":   (cAlias)->DAK_AJUDA1,;  
                "NOMEAJUDA1":   cNomeAjud1,;  
                "DAK_PESO":     (cAlias)->DAK_PESO,;
                "DAK_CAPVOL":   (cAlias)->DAK_CAPVOL,;
                "DAK_PTOENT":   (cAlias)->DAK_PTOENT,;
                "DAK_VALOR":    (cAlias)->DAK_VALOR,;
                "DAK_DATA":     totvs.framework.treports.date.dateToTimeStamp(sToD((cAlias)->DAK_DATA)),; 
                "DAK_HORA":     (cAlias)->DAK_HORA,;
                "C9_PRODUTO":   (cAlias)->C9_PRODUTO,;
                "DESCPROD":     cDescProd,;
                "B1_UM":        cCodEmb,;
                "B1_SEGUM":     cCod2Emb,;
                "C9_LOTECTL":   (cAlias)->C9_LOTECTL,;
                "C9_NUMLOTE":   (cAlias)->C9_NUMLOTE,;
                "C9_QTDLIB":    nQtdLib,;
                "C9_QTDLIB2":   nQtdLib2,;
                "C9_VOLUME":    nVolume,;
                "C9_PESO":      nPeso,;
                "C9_VALOR":     nValor,;
                "SUBTITULO":    cSubtit})
               
        (cAlias)->( dbSkip() )
        nCount++

    EndDo

    //Se não for o último registro indica que terá próxima página
    self:setHasNext( !(cAlias)->(EoF()) )
    
    (cAlias)->( DBCloseArea() )

    If oQryOMSR021 <> Nil
		oQryOMSR021:Destroy()
		oQryOMSR021 := NIL
		FwFreeObj(oQryOMSR021)
	EndIf

    RestArea( aAreaDAK )

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Metodo que retorna a Estrutura de dados.
@type Metodo
@author Rafael Souza  
@version 12
@since 10/05/2023 
*/
//-------------------------------------------------------------------   
method getSchema() as object class OMSR021TReportsBusinessObject
    local aFieldsDAK as array
    local aFieldsSC9 as array
 
    aFieldsDAK := { "DAK_FILIAL", "DAK_COD", "DAK_SEQCAR", "DAK_CAMINH","DAK_MOTORI", "DAK_AJUDA1", "DAK_PESO", "DAK_CAPVOL", "DAK_PTOENT", "DAK_VALOR", "DAK_DATA", "DAK_HORA"  }
    aFieldsSC9 := { "C9_PRODUTO", "C9_LOTECTL", "C9_NUMLOTE", "C9_QTDLIB", "C9_QTDLIB2"}
        
    self:oSchema:aliasToSchema("DAK", aFieldsDAK)  
    self:oSchema:aliasToSchema("SC9", aFieldsSC9)    
    self:oSchema:addProperty("DESC_VEI", "Descrição Veículo", "string", "Desc. Vei.", "DESC_VEI")
    self:oSchema:addProperty("NOMEMOT", "Nome Motorista", "string", "Nome Mot.", "NOMEMOT")
    self:oSchema:addProperty("NOMEAJUDA1", "Nome Ajudante", "string", "Nome Ajud.", "NOMEAJUDA1")
    self:oSchema:addProperty("DESCPROD", "Descrição", "string", "Descrição", "DESCPROD")
    self:oSchema:addProperty("B1_UM", "Unidade", "string", "Unidade", "B1_UM")
    self:oSchema:addProperty("B1_SEGUM", "Seg. Un. Med.", "string", "Seg. Un. Med.", "B1_SEGUM")
    self:oSchema:addProperty("C9_VOLUME", "Volume", "number", "Volume", "C9_VOLUME")
    self:oSchema:addProperty("C9_PESO", "Peso Prod.", "number", "Peso Prod.", "C9_PESO")
    self:oSchema:addProperty("C9_VALOR", "Valor", "number", "Valor", "C9_VALOR")
    self:oSchema:addProperty("SUBTITULO", "Subtítulo" , "string", "Subtítulo", "SUBTITULO") //-- Subtitulo do relatório a ser informado pelo usuário
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} QtdSumSC9
Retorna a soma de peso, volume e valor dos itens da SC9 por produto.
@type Function
@author Rafael Souza  
@version 12
@since 21/08/2023 
*/
//------------------------------------------------------------------- 
Static Function QtdSumSC9(cAliasSC9)
Local aValores := {}
Local lPesoLiq := Getmv("MV_PESOCAR") == "L"
Local cAliasQry as character
Local cQrySumC9:="" 
Local oQryC9 as object

Static cQrySumC9 := ""

Default cAliasSC9 := ""

    cQrySumC9 := " SELECT SUM(SC9.C9_QTDLIB) C9_QTDLIB, "
    cQrySumC9 +=        " SUM(SC9.C9_QTDLIB2) C9_QTDLIB2,"
    cQrySumC9 +=        " SUM(SC9.C9_PRCVEN * SC9.C9_QTDLIB) C9_VALOR,"
    cQrySumC9 +=        " SUM((SB5.B5_ALTURLC * SB5.B5_LARGLC * SB5.B5_COMPRLC)* SC9.C9_QTDLIB) C9_VOLUME,"
    If lPesoLiq
        cQrySumC9 +=    " SUM(SB1.B1_PESO * SC9.C9_QTDLIB) C9_PESO"
    Else
        cQrySumC9 +=    " SUM(SB1.B1_PESBRU * SC9.C9_QTDLIB) C9_PESO"
    EndIf
    cQrySumC9 +=   " FROM "+RetSqlName('SC9')+" SC9"
    cQrySumC9 +=   " LEFT JOIN "+RetSqlName('SB5')+" SB5"
    cQrySumC9 +=     " ON SB5.B5_FILIAL  = '"+xFilial("SB5")+"'"
    cQrySumC9 +=    " AND SB5.B5_COD     = SC9.C9_PRODUTO"
    cQrySumC9 +=    " AND SB5.D_E_L_E_T_ = ' '"
    cQrySumC9 +=  " INNER JOIN "+RetSqlName('SB1')+" SB1"
    cQrySumC9 +=     " ON SB1.B1_FILIAL = '"+xFilial("SB1")+"'"
    cQrySumC9 +=    " AND SB1.B1_COD     = SC9.C9_PRODUTO"
    cQrySumC9 +=    " AND SB1.D_E_L_E_T_ = ' '"
    cQrySumC9 +=  " WHERE SC9.C9_FILIAL  = ? "
    cQrySumC9 +=    " AND SC9.C9_CARGA   = ? "
    cQrySumC9 +=    " AND SC9.C9_SEQCAR  = ? "
    cQrySumC9 +=    " AND SC9.C9_PRODUTO = ? "		
    cQrySumC9 += " AND SC9.C9_LOTECTL = ? "
    cQrySumC9 += " AND SC9.C9_NUMLOTE = ? "
    cQrySumC9 +=    " AND SC9.D_E_L_E_T_ = ' ' "
    cQrySumC9 := ChangeQuery(cQrySumC9)

    oQryC9 := FwExecStatement():New()
    oQryC9:SetQuery(cQrySumC9)

    oQryC9:SetString(1, (cAliasSC9)->C9_FILIAL)
    oQryC9:SetString(2, (cAliasSC9)->DAK_COD)
    oQryC9:SetString(3, (cAliasSC9)->DAK_SEQCAR)
    oQryC9:SetString(4, (cAliasSC9)->C9_PRODUTO)
    oQryC9:SetString(5, (cAliasSC9)->C9_LOTECTL)
    oQryC9:SetString(6, (cAliasSC9)->C9_NUMLOTE)

    cAliasQry := oQryC9:OpenAlias()

    If (cAliasQry)->(!EoF())
        aValores := {(cAliasQry)->C9_QTDLIB,(cAliasQry)->C9_QTDLIB2,(cAliasQry)->C9_VALOR,(cAliasQry)->C9_VOLUME,(cAliasQry)->C9_PESO}
    EndIf
    (cAliasQry)->(DbCloseArea())

    If oQryC9 <> Nil
		oQryC9:Destroy()
		oQryC9 := NIL
		FwFreeObj(oQryC9)
	EndIf

Return aValores
