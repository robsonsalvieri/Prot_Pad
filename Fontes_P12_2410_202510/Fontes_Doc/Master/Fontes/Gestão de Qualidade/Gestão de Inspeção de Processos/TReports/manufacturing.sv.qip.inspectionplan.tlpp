#include "manufacturing.sv.qip.inspectionplan.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qip

/*/{Protheus.doc} SmartViewQIPInspectionPlan
Classe responsável pelos métodos de criação e manipulação do Smart View
@author brunno.costa
@since 06/12/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                     ;
	active         = .T.                                      ,;
	team           = "SIGAQIP"                                ,;
	tables         = "QPK,QQK,SC2,QP6,QP1,QQ1,QP7,QP8,QQ7,SA1",; //QQH
	customTables   = "QPK,QQK,SC2,QP6,QP1,QQ1,QP7,QP8"        ,;
	name           = "Plano Inspeção de Processos (Qualidade)",;
	country        = "ALL"                                    ,;
	initialRelease = "12.1.2310"         )

Class SmartViewQIPInspectionPlan From totvs.framework.treports.integratedprovider.IntegratedProvider

	Public Data cAlias                 as Character
	Public Data cUsedFields            as Character
    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aCoalesceTestFields as Array
    Protected Data aFields             as Array
	Protected Data aMirrorFields       as Array
	Protected Data aMirrorTables       as Array
    Protected Data aStruct             as Array
	Protected Data aTestFields         as Array
	Protected Data aTestStruct         as Array
	Protected Data cQuery              as Character
	Protected Data cTestQuery          as Character
	Protected DATA oQueryManager       as object
	Protected DATA oQLTTReports        as object

    Public Method getData()        as object
    Public Method getDescription() as Character
    Public Method getDisplayName() as Character
    Public Method getSchema()      as object
    Public Method new()            as object

	Protected Method aninhaDadosPlanoDeAmostragem(jData, cIDProperty, cAliasEnsaios, cTamanhoAmostra)
	Protected Method mountDefaultFields()
	Protected Method mountDefaultTestFields()
	Protected Method setUpQuery()
	Protected Method setUpTestQuery()

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author brunno.costa
@since 06/12/2023
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQIPInspectionPlan
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) // "Qualidade - Inspeção de Processos"

    self:setIsLookUp(.T.)

	self:cPergunte := "QPR040"
    self:setPergunte(self:cPergunte) // Indica o pergunte que será utilizado no relatório

	self:aCoalesceTestFields := {}
	self:aMirrorFields       := {}
	self:lForceAllTrim       := .T.

	self:oQueryManager       := QLTQueryManager():New()
	self:oQLTTReports        := QLTTReportsManager():New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author brunno.costa
@since 06/12/2023
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQIPInspectionPlan
Return STR0002 // "Plano Inspeção de Processos (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author brunno.costa
@since 06/12/2023
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQIPInspectionPlan
Return STR0003 // "Objeto com os registros das tabelas QPK, QQK, SC2, QP6. Propriedade aninhada 'Ensaios' com registros das tabelas QPK, QP6, QQK, QP7, QP8, QQ7, SA1, QP1 e QQ1. Propriedade aninhada 'PlanoDeAmostragem' com dados do plano de amostragem."

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author brunno.costa
@since 06/12/2023
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQIPInspectionPlan
  
    Local aArea          := GetArea() as Array
	Local aBindParam     := {}        as Array
	Local aBindTestParam := {}        as Array
	Local cAliasEnsaios  := ""        as Character
	Local cFatCon        := ""        as Character
	Local cTipCon        := ""        as Character
    Local lChangeQuery   := .T.       as Logical

    self:setPageSize(10)

	DBSelectArea("QP1")
    DBSelectArea("QP6")
	DBSelectArea("QP7")
	DBSelectArea("QP8")
    DBSelectArea("QPK")
	DBSelectArea("QQ1")
	DBSelectArea("QQ7")
	DBSelectArea("QQH")
	DBSelectArea("QQK")
	DBSelectArea("SA1")
    DBSelectArea("SC2")

	QQH->(dbSetOrder(1))

	self:setUpQuery(@aBindParam, oFilter)
	self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := self:oQueryManager:executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	While !(self:cAlias)->(Eof())

		self:jData  := Jsonobject():new()

		//Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId]       ) } )

		FwFreeArray(aBindTestParam)
		aBindTestParam := {}

		self:setUpTestQuery(oFilter                  ,;
		                   (self:cAlias)->QPK_OP     ,;
						   (self:cAlias)->QPK_LOTE   ,;
						   (self:cAlias)->QPK_NUMSER ,;
						   (self:cAlias)->QPK_PRODUT ,;
						   (self:cAlias)->QPK_REVI   ,;
						   (self:cAlias)->C2_ROTEIRO ,;
						   (self:cAlias)->C2_EMISSAO ,;
						   @aBindTestParam           )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Define o Fator Conversao, se nao estiver definido             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (self:cAlias)->QP6_UNMED1 == (self:cAlias)->C2_UM
			cFatCon := (self:cAlias)->QP6_FATCO1
			cTipCon := (self:cAlias)->QP6_TIPCO1
		ElseIf (self:cAlias)->QP6_UNMED2 == (self:cAlias)->C2_UM
			cFatCon := (self:cAlias)->QP6_FATCO2
			cTipCon := (self:cAlias)->QP6_TIPCO2
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula o Tamanho da Amostra                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		If Superval(cFatCon) <> 0
			cFatCon := StrTran(cFatcon,",",".")
			If cTipCon == "D"
				self:jData["TAMANHO_AMOSTRA"] := Str((self:cAlias)->QPK_TAMLOT / Superval(cFatCon))
			Else
				self:jData["TAMANHO_AMOSTRA"] := Str((self:cAlias)->QPK_TAMLOT * Superval(cFatCon))
			EndIf                 
		Else
			self:jData["TAMANHO_AMOSTRA"]     := Str((self:cAlias)->QPK_TAMLOT)
		EndIf

		self:cTestQuery := self:oQueryManager:changeQuery(self:cTestQuery)
		cAliasEnsaios   := self:oQueryManager:executeQueryWithBind(self:cTestQuery, aBindTestParam, lChangeQuery)

		self:oQLTTReports:aninharAliasEmPropriedade(self, "Ensaios", self:aTestStruct, cAliasEnsaios, @self:jData, self:aCoalesceTestFields)
		self:aninhaDadosPlanoDeAmostragem(@self:jData, "PlanoDeAmostragem", cAliasEnsaios, self:jData["TAMANHO_AMOSTRA"])
		(cAliasEnsaios)->(DbCloseArea())

		//Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self)

		self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 


    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório de Roteiros
@author brunno.costa
@since 06/12/2023
@version 1.0
@param 01 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
@param 02 - oFilter   , objeto, contém o filtro do TReports
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIPInspectionPlan

	Local cExternalUse := ""                         as Character //Campos com uso externo obrigatório a criar na Query
	Local jParams      := Nil                        as Json
    
	//Campos usados em chamadas Externas - INICIO
	cExternalUse += "QPK_PRODUT"
	cExternalUse += ",QPK_REVI"
	cExternalUse += ",QPK_OP"
	cExternalUse += ",QPK_LOTE"
	cExternalUse += ",QPK_NUMSER"
	cExternalUse += ",QP6_UNMED1"
	cExternalUse += ",QP6_UNMED2"
	cExternalUse += ",QP6_TIPCO1"
	cExternalUse += ",QP6_TIPCO2"
	cExternalUse += ",QP6_FATCO1"
	cExternalUse += ",QP6_FATCO2"
	cExternalUse += ",C2_ROTEIRO"
	cExternalUse += ",C2_EMISSAO"
	cExternalUse += ",C2_UM"
	//Campos usados em chamadas Externas - FIM

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	self:cQuery +=     " ( SELECT DISTINCT ' ' A "

	self:cQuery +=                  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPK", "SC2", "QP6", "QQK"},;
	                                "QPK_PRODUT,QPK_REVI,QQK_CODIGO,QP6_UNMED1,QP6_UNMED2,QP6_TIPCO1,QP6_TIPCO2,QP6_FATCO1,QP6_FATCO2,C2_ROTEIRO,C2_EMISSAO,C2_UM", "")
	
	//Identificação do nome do cliente
	If !Empty(jParams['MV_PAR10'][1]) .AND. !Empty(jParams['MV_PAR11'][1])
		self:cQuery +=            ", ( SELECT A1_NOME "
		self:cQuery +=               " FROM " + RetSqlName("SA1") + " SA1 "
		self:cQuery +=               " WHERE  "
		aAdd(aBindParam, {xFilial( "SA1" )      , "S"})
    	aAdd(aBindParam, {jParams['MV_PAR10'][1], "S"})
		aAdd(aBindParam, {jParams['MV_PAR11'][1], "S"})
		aAdd(aBindParam, {" "                   , "S"})
		self:cQuery +=                     " SA1.A1_FILIAL  = ? "
		self:cQuery +=                 " AND SA1.A1_COD     = ? "
		self:cQuery +=                 " AND SA1.A1_LOJA    = ? "
		self:cQuery +=                 " AND SA1.D_E_L_E_T_ = ? ) A1_NOME "
	Else
		self:cQuery +=                 ", ' ' A1_NOME "
	EndIf

	//Cabeçalho da Inspeção de Processos
	self:cQuery += " FROM " + RetSQLName("QPK") + " QPK "

	//Cabeçalho da Especificação de Produtos
	self:cQuery += " INNER JOIN " + RetSQLName("QP6") + " QP6 "
	self:cQuery += " ON "
	aAdd(aBindParam, {xFilial( "QP6" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=        " QP6.QP6_FILIAL = ? " 
	self:cQuery +=    " AND QP6.QP6_PRODUT = QPK.QPK_PRODUT "
	self:cQuery +=    " AND QP6.QP6_REVI   = QPK.QPK_REVI "
	self:cQuery +=    " AND QP6.D_E_L_E_T_ = ? "  

	//Ordem de Produção
	self:cQuery += " INNER JOIN " + RetSQLName("SC2") + " SC2 "
	self:cQuery += " ON "
	aAdd(aBindParam, {xFilial( "SC2" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=        " SC2.C2_FILIAL  = ? "
	self:cQuery +=    " AND " + self:oQueryManager:montaRelationC2OP("QPK.QPK_OP")
	self:cQuery +=    " AND SC2.D_E_L_E_T_ = ? "  

	//Roteiros de Operações
	self:cQuery += " INNER JOIN "
	self:cQuery +=    "(SELECT ' ' A " + self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QQK"}, "QQK_REVIPR,QQK_PRODUT,QQK_CODIGO", "QQK1.")
	self:cQuery +=    " FROM " + RetSQLName("QQK") + " QQK1 "
	self:cQuery +=    " WHERE "
	aAdd(aBindParam, {xFilial( "QQK" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=          " (QQK_FILIAL = ? ) "
	self:cQuery +=      " AND (D_E_L_E_T_ = ? ) "
	self:cQuery +=   " ) QQK "
	self:cQuery += " ON "

	aAdd(aBindParam, {xFilial( "QPK" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cQuery +=       " QPK.QPK_FILIAL = ? "  
	self:cQuery +=   " AND QPK.QPK_REVI   = QQK.QQK_REVIPR "
	self:cQuery +=   " AND QPK.QPK_PRODUT = QQK.QQK_PRODUT "
	self:cQuery +=   " AND QPK.D_E_L_E_T_ = ? "

	self:cQuery += " WHERE 1 = 1 "

	//Ordem de Produção
	If Len(jParams['MV_PAR01'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR01'][1]), "S"}) 	    
        self:cQuery += " AND ? <= LOWER(QPK.QPK_OP) " 
    ENDIF 

	//Ordem de Produção
	If Len(jParams['MV_PAR04'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR04'][1]), "S"}) 	    
        self:cQuery += " AND LOWER(QPK.QPK_OP) <= ? " 
    ENDIF 

	//Lote
	If Len(jParams['MV_PAR02'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR02'][1]), "S"}) 	    
        self:cQuery += " AND ? <= LOWER(QPK.QPK_LOTE) " 
    ENDIF 

	//Lote
	If Len(jParams['MV_PAR05'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR05'][1]), "S"}) 	    
        self:cQuery += " AND LOWER(QPK.QPK_LOTE) <= ? " 
    ENDIF 

	//Número de Série
	If Len(jParams['MV_PAR03'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR03'][1]), "S"}) 	    
        self:cQuery += " AND ? <= LOWER(QPK.QPK_NUMSER) " 
    ENDIF 

	//Número de Série
	If Len(jParams['MV_PAR06'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR06'][1]), "S"}) 	    
        self:cQuery += " AND LOWER(QPK.QPK_NUMSER) <= ? " 
    ENDIF 

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    EndIf

	self:cQuery  += " ) DIST "

Return

/*/{Protheus.doc} setUpTestQuery
Monta query do relatório de Ensaios
@author brunno.costa
@since 06/12/2023
@version 1.0
@param 01 - oFilter  , objeto, contém o filtro do TReports
@param 02 - cOP      , caracter, código da OP relacionada para extração dos ensaios
@param 03 - cLote    , caracter, código do LOTE relacionada para extração dos ensaios
@param 04 - cNumSerie, caracter, código do NÚMERO DE SÉRIE relacionada para extração dos ensaios
@param 05 - cProduto , caracter, código do PRODUTO relacionada para extração dos ensaios
@param 06 - cRevisao , caracter, código da REVISÃO relacionada para extração dos ensaios
@param 07 - cRoteiro , caracter, código da ROTEIRO relacionada para extração dos ensaios
@param 08 - cEmissao , caracter, string com data de emissão relacionada para extração dos ensaios
@param 09 - aBindParam, array , array com os dados dos parâmetros para BIND:
                      {{oDados, cTipo}, {}, ...}
                      Sendo cTipo:
                      N -> Número, para uso com FwExecStatement():setNumeric()
                      S -> String, para uso com FwExecStatement():setString()
                      A -> Array , para uso com FwExecStatement():setInArray()
                      U -> Número, para uso com FwExecStatement():setUnsafe()
/*/
Method setUpTestQuery(	oFilter   as object    ,;
						cOP       as Character ,;
						cLote     as Character ,;
						cNumSerie as Character ,;
						cProduto  as Character ,;
						cRevisao  as Character ,;
						cRoteiro  as Character ,;
						cEmissao  as Character ,;
						aBindParam as Array    ) class SmartViewQIPInspectionPlan

	Local jParams      := Nil                        as Json

	self:oQLTTReports:lRetornaAninhados := .T.

	cRoteiro := Iif(Empty(cRoteiro), QIPRotGene("QQK_CODIGO"), cRoteiro)

	//metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    self:cTestQuery := " SELECT ' ' A "
	self:cTestQuery +=  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QPK","QP6","QQK","QP1","QQ1"}, "QP6_PRODUT,QP6_REVI,QQK_OPERAC,QP1_DESCES,QP1_DESCIN,QP1_DESCPO,QQK_CHAVE")
	self:cTestQuery +=  self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7", "QP8"},;
	                                "QP7_CODREC,QP7_OPERAC,QP7_LABOR,QP7_SEQLAB,QP8_CODREC,QP8_OPERAC,QP8_LABOR,QP8_SEQLAB,QP7_PLAMO,QP7_DESPLA,QP8_PLAMO,QP8_DESPLA", "LAB.")

	If !Empty(jParams['MV_PAR10'][1]) .And. !Empty(jParams['MV_PAR11'][1])
		self:cTestQuery +=      self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QQ7","SA1"}, "", )
	Else
		self:cTestQuery +=      self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QQ7","SA1"}, "", "' ' " )
	EndIf

	self:cTestQuery += ", (CASE "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPY.QPH_OPERAC,'-1') = '-1' " +                                                                       " THEN 'N/A' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPY.QPY_QTDE, 0) > 0 " +                                                                              " THEN 'INS' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPI.QPI_QTDE, 0) > 0 AND COALESCE(SKIPTESTE_QPZ.QPZ_QTDE, 0)   > 0 " +                                " THEN 'INS' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPY.QPY_QTDE, 0) = 0 AND COALESCE(SKIPTESTE_QPN.QPN_CONTAD, 0) >  COALESCE(SKIPTESTE_QPN.QPN_SKPTES, 0) THEN 'INS' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPY.QPY_QTDE, 0) = 0 AND COALESCE(SKIPTESTE_QPN.QPN_CONTAD, 2) <= COALESCE(SKIPTESTE_QPN.QPN_SKPTES, 1) THEN 'CER' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPY.QPY_QTDE, 0) = 0 " +                                                                              " THEN 'INS' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPI.QPI_QTDE, 0) = 0 AND COALESCE(SKIPTESTE_QPO.QPO_CONTAD, 0) >  COALESCE(SKIPTESTE_QPO.QPO_SKPTES, 0) THEN 'INS' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPI.QPI_QTDE, 0) = 0 AND COALESCE(SKIPTESTE_QPO.QPO_CONTAD, 2) <= COALESCE(SKIPTESTE_QPO.QPO_SKPTES, 1) THEN 'CER' "
	self:cTestQuery +=   " WHEN COALESCE(SKIPTESTE_QPI.QPI_QTDE, 0) = 0 " +                                                                              " THEN 'INS' "
	self:cTestQuery +=   " ELSE 'N/A' "
	self:cTestQuery +=   " END "
	self:cTestQuery +=  " ) SKIPTESTE "
	
	//QPK - Insp.Processos - Avaliacoes
	self:cTestQuery += " FROM " + RetSQLName("QPK") + " QPK " 

	//QP6 - Histórico dos Produtos (Cabeçalho Especificação)
 	self:cTestQuery += " INNER JOIN " + RetSQLName("QP6") + " QP6 "
	self:cTestQuery += " ON "
	aAdd(aBindParam, {xFilial( "QP6" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {cRevisao        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=        " QP6.QP6_FILIAL = ? "
	self:cTestQuery +=    " AND QP6.QP6_PRODUT = ? "
	self:cTestQuery +=    " AND QP6.QP6_REVI   = ? "
	self:cTestQuery +=    " AND QP6.D_E_L_E_T_ = ? "

	//QQK - Especificações das Operações  
	self:cTestQuery += " INNER JOIN " + RetSQLName("QQK") + " QQK "
	self:cTestQuery += " ON "
	aAdd(aBindParam, {xFilial( "QQK" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {cRoteiro        , "S"})
	self:cTestQuery +=        " QQK.QQK_FILIAL = ? "
	self:cTestQuery +=    " AND QQK.QQK_PRODUT = ? "
	self:cTestQuery +=    " AND QQK.QQK_CODIGO = ? "
	self:cTestQuery +=    " AND QQK.QQK_PRODUT = QPK.QPK_PRODUT "
	self:cTestQuery +=    " AND QQK.QQK_REVIPR = QPK.QPK_REVI "

	//Operação
	If Len(jParams['MV_PAR07'][1])
		aAdd(aBindParam, {Lower(jParams['MV_PAR07'][1]), "S"})
        self:cTestQuery += " AND ? <= LOWER(QQK.QQK_OPERAC) " 
    ENDIF

	//Operação
	If Len(jParams['MV_PAR08'][1])
		aAdd(aBindParam, {Lower(jParams['MV_PAR08'][1]), "S"})
        self:cTestQuery += " AND LOWER(QQK.QQK_OPERAC) <= ? " 
    ENDIF

	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=    " AND QQK.D_E_L_E_T_ = ? "

	//Considera apenas ultima operacao
	If jParams['MV_PAR09'][1] == 1
		//QQK - Especificações das Operações  
		self:cTestQuery += " INNER JOIN (" 
		self:cTestQuery +=             " SELECT QQK_REVIPR, QQK_PRODUT, MAX(QQK_OPERAC) QQK_OPERAC "
		self:cTestQuery +=             " FROM" + RetSQLName("QQK") + " QQK2 "
		self:cTestQuery +=             " WHERE "
		aAdd(aBindParam, {xFilial( "QQK" ), "S"})
		aAdd(aBindParam, {cProduto        , "S"})
		aAdd(aBindParam, {cRoteiro        , "S"})
		self:cTestQuery +=                      " QQK2.QQK_FILIAL = ? "
		self:cTestQuery +=                  " AND QQK2.QQK_PRODUT = ? "
		self:cTestQuery +=                  " AND QQK2.QQK_CODIGO = ? "

		//Operação
		If Len(jParams['MV_PAR07'][1])
			aAdd(aBindParam, {Lower(jParams['MV_PAR07'][1]), "S"})
			self:cTestQuery += " AND ? <= LOWER(QQK2.QQK_OPERAC) " 
		ENDIF

		//Operação
		If Len(jParams['MV_PAR08'][1])
			aAdd(aBindParam, {Lower(jParams['MV_PAR08'][1]), "S"})
			self:cTestQuery += " AND LOWER(QQK2.QQK_OPERAC) <= ? " 
		ENDIF

		aAdd(aBindParam, {" "             , "S"})
		self:cTestQuery +=                  " AND QQK2.D_E_L_E_T_ = ? "

		self:cTestQuery +=             " GROUP BY QQK_REVIPR, QQK_PRODUT"
		self:cTestQuery += " ) QQK_ULTIMA_OPER " 
		self:cTestQuery += " ON " 
		self:cTestQuery +=        " QQK_ULTIMA_OPER.QQK_REVIPR = QQK.QQK_REVIPR "
		self:cTestQuery +=    " AND QQK_ULTIMA_OPER.QQK_PRODUT = QQK.QQK_PRODUT "
		self:cTestQuery +=    " AND QQK_ULTIMA_OPER.QQK_OPERAC = QQK.QQK_OPERAC "
	EndIf

	// QP7 - Ensaios Mensuráveis Produtos  
	// QP8 - Ensaios Texto dos Produto
	self:cTestQuery +=     " INNER JOIN "
	self:cTestQuery +=       " (SELECT R_E_C_N_O_"
	self:cTestQuery +=                 self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7"}, "QP7_PRODUT,QP7_REVI,QP7_ENSAIO,QP7_LABOR,QP7_PLAMO,QP7_DESPLA")
	self:cTestQuery +=                 self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP8"}, "QP8_PRODUT,QP8_REVI,QP8_ENSAIO,QP8_LABOR,QP8_PLAMO,QP8_DESPLA", "NULL ")
	self:cTestQuery +=        " FROM " + RetSqlName("QP7") + " QP7 "
	self:cTestQuery +=        " WHERE  "

	aAdd(aBindParam, {xFilial( "QP7" ), "S"})
	self:cTestQuery +=              " QP7.QP7_FILIAL = ? "

	//Laboratório
	If Len(jParams['MV_PAR12'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR12'][1]), "S"}) 
		self:cTestQuery +=      " AND ? <= LOWER(QP7.QP7_LABOR) " 
	ENDIF

	//Laboratório
	If Len(jParams['MV_PAR13'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR13'][1]), "S"}) 
		self:cTestQuery +=      " AND ? >= LOWER(QP7.QP7_LABOR) " 
	ENDIF

	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=          " AND QP7.D_E_L_E_T_ = ? "

	self:cTestQuery +=        " UNION ALL "
	self:cTestQuery +=        " SELECT R_E_C_N_O_"
	self:cTestQuery +=                 self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP7"}, "QP7_PRODUT,QP7_REVI,QP7_ENSAIO,QP7_LABOR,QP7_PLAMO,QP7_DESPLA", "NULL ")
    self:cTestQuery +=                 self:oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QP8"}, "QP8_PRODUT,QP8_REVI,QP8_ENSAIO,QP8_LABOR,QP8_PLAMO,QP8_DESPLA")
	self:cTestQuery +=        " FROM " + RetSqlName("QP8") + " QP8 "
	self:cTestQuery +=        " WHERE  "

	aAdd(aBindParam, {xFilial( "QP8" ), "S"})
	self:cTestQuery +=              " QP8.QP8_FILIAL = ? "

	//Laboratório
	If Len(jParams['MV_PAR12'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR12'][1]), "S"}) 
		self:cTestQuery +=      " AND ? <= LOWER(QP8.QP8_LABOR) " 
	ENDIF

	//Laboratório
	If Len(jParams['MV_PAR13'][1]) > 0
		aAdd(aBindParam, {Lower(jParams['MV_PAR13'][1]), "S"}) 
		self:cTestQuery +=      " AND ? >= LOWER(QP8.QP8_LABOR) " 
	ENDIF

	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=          " AND QP8.D_E_L_E_T_ = ? "

	self:cTestQuery +=       " ) LAB "
	self:cTestQuery +=     " ON "
	self:cTestQuery +=           " COALESCE(LAB.QP7_PRODUT, LAB.QP8_PRODUT) = QP6.QP6_PRODUT "
	self:cTestQuery +=       " AND COALESCE(LAB.QP7_REVI  , LAB.QP8_REVI  ) = QPK.QPK_REVI "
	self:cTestQuery +=       " AND COALESCE(LAB.QP7_CODREC, LAB.QP8_CODREC) = QQK.QQK_CODIGO "
	self:cTestQuery +=       " AND COALESCE(LAB.QP7_OPERAC, LAB.QP8_OPERAC) = QQK.QQK_OPERAC "

	If !Empty(jParams['MV_PAR10'][1]) .And. !Empty(jParams['MV_PAR11'][1])
		//QQ7 - Amarr Prod x Cliente - Ensaios
		self:cTestQuery += " INNER JOIN " + RetSQLName("QQ7") + " QQ7 "
		self:cTestQuery += " ON "
		
		aAdd(aBindParam, {xFilial( "QQ7" )             , "S"})
		self:cTestQuery +=        " QQ7.QQ7_FILIAL = ? "
		self:cTestQuery +=    " AND QQ7.QQ7_PRODUT = COALESCE(LAB.QP7_PRODUT, LAB.QP8_PRODUT) "
		self:cTestQuery +=    " AND QQ7.QQ7_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "
		self:cTestQuery +=    " AND QQ7.QQ7_LABOR  = COALESCE(LAB.QP7_LABOR , LAB.QP8_LABOR ) "
		self:cTestQuery +=    " AND QQ7.QQ7_CODREC = COALESCE(LAB.QP7_CODREC, LAB.QP8_CODREC) "
		self:cTestQuery +=    " AND QQ7.QQ7_OPERAC = COALESCE(LAB.QP7_OPERAC, LAB.QP8_OPERAC) "
		aAdd(aBindParam, {LOWER(jParams['MV_PAR10'][1]), "S"})
		aAdd(aBindParam, {LOWER(jParams['MV_PAR11'][1]), "S"})
		aAdd(aBindParam, {" "                          , "S"})
		self:cTestQuery +=    " AND LOWER(QQ7.QQ7_CLIENT) = ? "
		self:cTestQuery +=    " AND LOWER(QQ7.QQ7_LOJA)   = ? "
		self:cTestQuery +=    " AND QQ7.D_E_L_E_T_        = ? "  

		//SA1 - Clientes
		self:cTestQuery += " INNER JOIN " + RetSQLName("SA1") + " SA1 "
		self:cTestQuery += " ON "
		aAdd(aBindParam, {xFilial( "SA1" )             , "S"})
		aAdd(aBindParam, {LOWER(jParams['MV_PAR10'][1]), "S"})
		aAdd(aBindParam, {LOWER(jParams['MV_PAR11'][1]), "S"})
		aAdd(aBindParam, {" "                          , "S"})
		self:cTestQuery +=        " SA1.A1_FILIAL      = ? "
		self:cTestQuery +=    " AND LOWER(SA1.A1_COD)  = ? "
		self:cTestQuery +=    " AND LOWER(SA1.A1_LOJA) = ? "
		self:cTestQuery +=    " AND SA1.D_E_L_E_T_     = ? "

	EndIf

	// QP1 - Ensaios
	self:cTestQuery +=     " LEFT JOIN " + RetSqlName("QP1") + " QP1 "
	self:cTestQuery +=     " ON "
	aAdd(aBindParam, {xFilial( "QP1" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=           " QP1.QP1_FILIAL = ? "
    self:cTestQuery +=       " AND QP1_ENSAIO     = COALESCE(QP7_ENSAIO, QP8_ENSAIO) "
    self:cTestQuery +=       " AND QP1.D_E_L_E_T_ = ? "

	// QQ1 - Instrumentos do Produto       
	self:cTestQuery +=     " LEFT JOIN " + RetSqlName("QQ1") + " QQ1 "
	self:cTestQuery +=     " ON "
	aAdd(aBindParam, {xFilial( "QQ1" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=           " QQ1.QQ1_FILIAL = ? "
	self:cTestQuery +=       " AND QQ1.QQ1_PRODUT = QP7_PRODUT "
	self:cTestQuery +=       " AND QQ1.QQ1_REVI   = QP7_REVI "
	self:cTestQuery +=       " AND QQ1.QQ1_OPERAC = QP7_OPERAC "
	self:cTestQuery +=       " AND QQ1.QQ1_ENSAIO = QP7_ENSAIO "
    self:cTestQuery +=       " AND QP1_ENSAIO     = COALESCE(QP7_ENSAIO, QP8_ENSAIO) "
    self:cTestQuery +=       " AND QQ1.D_E_L_E_T_ = ? "

	//QPH - Skip-Teste Individual         
	//QPY - Hist Skip-Teste Individual
	self:cTestQuery +=     " LEFT JOIN " 
	self:cTestQuery +=     " ( SELECT QPH_OPERAC, QPH_ENSAIO, SUM(CASE WHEN QPY.QPY_CODREC IS NULL THEN 0 ELSE 1 END) QPY_QTDE "  	
	self:cTestQuery +=       " FROM "       + RetSqlName("QPH") + " QPH "
	self:cTestQuery +=       " LEFT JOIN "  + RetSqlName("QPY") + " QPY "
	self:cTestQuery +=       " ON "                                                                                                                                        
	aAdd(aBindParam, {xFilial( "QPY" ), "S"})
	aAdd(aBindParam, {cEmissao        , "S"})
	aAdd(aBindParam, {cLote           , "S"})
	aAdd(aBindParam, {cOP             , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPY.QPY_FILIAL      = ? "
	self:cTestQuery +=         " AND QPY.QPY_CODREC      = QPH.QPH_CODREC "
	self:cTestQuery +=         " AND QPY.QPY_OPERAC      = QPH.QPH_OPERAC "
	self:cTestQuery +=         " AND QPY.QPY_PRODUT      = QPH.QPH_PRODUT "
	self:cTestQuery +=         " AND QPY.QPY_DTENTR     >= ? "
	self:cTestQuery +=         " AND LOWER(QPY.QPY_LOTE) = LOWER(?) "
	self:cTestQuery +=         " AND QPY.QPY_ENSAIO      = QPH.QPH_ENSAIO "
	self:cTestQuery +=         " AND QPY.QPY_ORDPRD      = ? "
	self:cTestQuery +=         " AND QPY.D_E_L_E_T_      = ? "
	self:cTestQuery +=       " WHERE "
	aAdd(aBindParam, {xFilial( "QPH" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {cRoteiro        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPH.QPH_FILIAL = ? "
	self:cTestQuery +=         " AND QPH.QPH_PRODUT = ? "
	self:cTestQuery +=         " AND QPH.QPH_CODREC = ? "
	self:cTestQuery +=         " AND QPH.D_E_L_E_T_ = ? "
	self:cTestQuery +=       " GROUP BY QPH_OPERAC, QPH_ENSAIO "
	self:cTestQuery +=     " ) SKIPTESTE_QPY "  
	self:cTestQuery +=     " ON "
	self:cTestQuery +=             " SKIPTESTE_QPY.QPH_OPERAC = QQK.QQK_OPERAC "
	self:cTestQuery +=         " AND SKIPTESTE_QPY.QPH_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "

	//QPH - Skip-Teste Individual         
	//QPN - Controle Skip-Teste Individual
	self:cTestQuery +=     " LEFT JOIN " 
	self:cTestQuery +=     " ( SELECT QPH_OPERAC, QPH_ENSAIO, QPN_CONTAD, QPH_SKPTES QPN_SKPTES "  	
	self:cTestQuery +=       " FROM "       + RetSqlName("QPH") + " QPH "
	self:cTestQuery +=       " INNER JOIN " + RetSqlName("QPN") + " QPN "
	self:cTestQuery +=       " ON "                                                                                                                                        
	aAdd(aBindParam, {xFilial( "QPN" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPN.QPN_FILIAL      = ? "
	self:cTestQuery +=         " AND QPN.QPN_CODREC      = QPH.QPH_CODREC "
	self:cTestQuery +=         " AND QPN.QPN_OPERAC      = QPH.QPH_OPERAC "
	self:cTestQuery +=         " AND QPN.QPN_PRODUT      = QPH.QPH_PRODUT "
	self:cTestQuery +=         " AND QPN.QPN_ENSAIO      = QPH.QPH_ENSAIO "
	self:cTestQuery +=         " AND QPN.D_E_L_E_T_      = ? "
	self:cTestQuery +=       " WHERE "
	aAdd(aBindParam, {xFilial( "QPH" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {cRoteiro        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPH.QPH_FILIAL = ? "
	self:cTestQuery +=         " AND QPH.QPH_PRODUT = ? "
	self:cTestQuery +=         " AND QPH.QPH_CODREC = ? "
	self:cTestQuery +=         " AND QPH.D_E_L_E_T_ = ? "
	self:cTestQuery +=     " ) SKIPTESTE_QPN "  
	self:cTestQuery +=     " ON "
	self:cTestQuery +=             " SKIPTESTE_QPN.QPH_OPERAC = QQK.QQK_OPERAC "
	self:cTestQuery +=         " AND SKIPTESTE_QPN.QPH_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "

	//QPA - Amarr Grupo Produto x Produto 
	//QPZ - Hist Skip-Teste Grupo
	self:cTestQuery +=     " LEFT JOIN " 
	self:cTestQuery +=     " ( SELECT QPZ.QPZ_GRUPO, QPZ_OPERAC, QPZ_ENSAIO, SUM(CASE WHEN QPZ.QPZ_CODREC IS NULL THEN 0 ELSE 1 END) QPZ_QTDE "  	
	self:cTestQuery +=       " FROM "       + RetSqlName("QPA") + " QPA "
	self:cTestQuery +=       " LEFT JOIN "  + RetSqlName("QPZ") + " QPZ "
	self:cTestQuery +=       " ON "
	aAdd(aBindParam, {xFilial( "QPZ" ), "S"})
	aAdd(aBindParam, {cEmissao        , "S"})
	aAdd(aBindParam, {cLote           , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPZ.QPZ_FILIAL      = ? "
	self:cTestQuery +=         " AND QPZ.QPZ_GRUPO       = QPA.QPA_GRUPO "
	self:cTestQuery +=         " AND QPZ.QPZ_DTENTR     >= ? "
	self:cTestQuery +=         " AND LOWER(QPZ.QPZ_LOTE) = ? "
	self:cTestQuery +=         " AND QPZ.D_E_L_E_T_      = ? "
	self:cTestQuery +=       " WHERE "
	aAdd(aBindParam, {xFilial( "QPA" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPA.QPA_FILIAL = ? "
	self:cTestQuery +=         " AND QPA.QPA_PRODUT = ? "
	self:cTestQuery +=         " AND QPA.D_E_L_E_T_ = ? "
	self:cTestQuery +=         " GROUP BY QPZ.QPZ_GRUPO, QPZ_OPERAC, QPZ_ENSAIO "
	self:cTestQuery +=       " ) SKIPTESTE_QPZ "  
	self:cTestQuery +=     " ON "
	self:cTestQuery +=             " SKIPTESTE_QPZ.QPZ_OPERAC = QQK.QQK_OPERAC "
	self:cTestQuery +=         " AND SKIPTESTE_QPZ.QPZ_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "

	//QPI - Skip-Teste e Grupo
	//QPO - Controle Skip-Teste Grupo
	self:cTestQuery +=     " LEFT JOIN " 
	self:cTestQuery +=     " ( SELECT QPI_GRUPO, QPI_OPERAC, QPI_ENSAIO, QPO_CONTAD, QPI_SKPTES QPO_SKPTES "  	
	self:cTestQuery +=       " FROM "       + RetSqlName("QPI") + " QPI "
	self:cTestQuery +=       " INNER JOIN " + RetSqlName("QPO") + " QPO "
	self:cTestQuery +=       " ON "                                                                                                                                        
	aAdd(aBindParam, {xFilial( "QPO" ), "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPO.QPO_FILIAL      = ? "
	self:cTestQuery +=         " AND QPO.QPO_CODREC      = QPI.QPI_CODREC "
	self:cTestQuery +=         " AND QPO.QPO_OPERAC      = QPI.QPI_OPERAC "
	self:cTestQuery +=         " AND QPO.QPO_GRUPO       = QPI.QPI_GRUPO "
	self:cTestQuery +=         " AND QPO.QPO_ENSAIO      = QPI.QPI_ENSAIO "
	self:cTestQuery +=         " AND QPO.D_E_L_E_T_      = ? "
	self:cTestQuery +=       " WHERE "
	aAdd(aBindParam, {xFilial( "QPI" ), "S"})
	aAdd(aBindParam, {cRoteiro        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPI.QPI_FILIAL = ? "
	self:cTestQuery +=         " AND QPI.QPI_CODREC = ? "
	self:cTestQuery +=         " AND QPI.D_E_L_E_T_ = ? "
	self:cTestQuery +=     " ) SKIPTESTE_QPO "  
	self:cTestQuery +=     " ON "
	self:cTestQuery +=             " SKIPTESTE_QPO.QPI_OPERAC = QQK.QQK_OPERAC "
	self:cTestQuery +=         " AND SKIPTESTE_QPO.QPI_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "
	self:cTestQuery +=         " AND SKIPTESTE_QPO.QPI_GRUPO  = SKIPTESTE_QPZ.QPZ_GRUPO "

	//QPA - Amarr Grupo Produto x Produto 
	//QPI - Skip-Teste Grupo
	self:cTestQuery +=     " LEFT JOIN " 
	self:cTestQuery +=     " ( SELECT QPI_OPERAC, QPI_ENSAIO, COUNT(*) QPI_QTDE "
	self:cTestQuery +=       " FROM "       + RetSqlName("QPA") + " QPA "
	self:cTestQuery +=       " INNER JOIN " + RetSqlName("QPI") + " QPI "
	self:cTestQuery +=       " ON "
	aAdd(aBindParam, {xFilial( "QPI" ), "S"})
	aAdd(aBindParam, {cRoteiro        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPI.QPI_FILIAL      = ? "
	self:cTestQuery +=         " AND QPI.QPI_GRUPO       = QPA.QPA_GRUPO "
	self:cTestQuery +=         " AND QPI.QPI_CODREC      = ? "
	self:cTestQuery +=         " AND QPI.D_E_L_E_T_      = ? "
	self:cTestQuery +=       " WHERE "
	aAdd(aBindParam, {xFilial( "QPA" ), "S"})
	aAdd(aBindParam, {cProduto        , "S"})
	aAdd(aBindParam, {" "             , "S"})
	self:cTestQuery +=             " QPA.QPA_FILIAL = ? "
	self:cTestQuery +=         " AND QPA.QPA_PRODUT = ? "
	self:cTestQuery +=         " AND QPA.D_E_L_E_T_ = ? "
	self:cTestQuery +=         " GROUP BY QPI_OPERAC, QPI_ENSAIO "
	self:cTestQuery +=     " ) SKIPTESTE_QPI "  
	self:cTestQuery +=     " ON "
	self:cTestQuery +=             " SKIPTESTE_QPI.QPI_OPERAC = QQK.QQK_OPERAC "
	self:cTestQuery +=         " AND SKIPTESTE_QPI.QPI_ENSAIO = COALESCE(LAB.QP7_ENSAIO, LAB.QP8_ENSAIO) "

	aAdd(aBindParam, {xFilial( "QPK" ), "S"})
	aAdd(aBindParam, {cOP             , "S"})
	aAdd(aBindParam, {cLote           , "S"})
	aAdd(aBindParam, {cNumSerie       , "S"})
	aAdd(aBindParam, {" "             , "S"})
	
	self:cTestQuery += " WHERE "  
    self:cTestQuery +=        " QPK.QPK_FILIAL        = ? "	
	self:cTestQuery +=    " AND LOWER(QPK.QPK_OP)     = LOWER(?) "
	self:cTestQuery +=    " AND LOWER(QPK.QPK_LOTE)   = LOWER(?) "
	self:cTestQuery +=    " AND LOWER(QPK.QPK_NUMSER) = LOWER(?) "
	self:cTestQuery +=    " AND QPK.D_E_L_E_T_        = ? "  

	//Os filtros serão setados na interface do novo TReports
    If oFilter:hasFilter()
        self:cTestQuery += " AND " + oFilter:getSQLExpression()
    EndIf

	self:cTestQuery += "ORDER BY "
	self:cTestQuery +=   " QP6_PRODUT "
	self:cTestQuery +=   ",QP6_REVI "
	self:cTestQuery +=   ",COALESCE(QP7_CODREC, QP8_CODREC) "
	self:cTestQuery +=   ",COALESCE(QP7_OPERAC, QP8_OPERAC) "
	self:cTestQuery +=   ",COALESCE(QP7_LABOR , QP8_LABOR) "
	self:cTestQuery +=   ",COALESCE(QP7_SEQLAB, QP8_SEQLAB) "

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author brunno.costa
@since 06/12/2023
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQIPInspectionPlan

	Local aPlanoStruct := {}

	self:mountDefaultFields()
	self:mountDefaultTestFields()

	self:aStruct     := self:oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)

	aadd(self:aStruct, {"TAMANHO_AMOSTRA", STR0004 + " (TAMANHO_AMOSTRA)", "string", STR0004 + " (TAMANHO_AMOSTRA)", ""}) // Tamanho Amostra

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

	//Tratamento de COALESCE     ID             , tipo    , Nome QUERY
    aadd(self:aCoalesceTestFields, {"QP7_AM_INS", "string", "QP8_AM_INS"})
    aadd(self:aCoalesceTestFields, {"QP7_ENSAIO", "string", "QP8_ENSAIO"})
    aadd(self:aCoalesceTestFields, {"QP7_LABOR" , "string", "QP8_LABOR" })
    aadd(self:aCoalesceTestFields, {"QP7_METODO", "string", "QP8_METODO"})
    aadd(self:aCoalesceTestFields, {"QP7_NIVEL" , "string", "QP8_NIVEL" })
    aadd(self:aCoalesceTestFields, {"QP7_PLAMO" , "string", "QP8_PLAMO" })
    aadd(self:aCoalesceTestFields, {"QP7_PRODUT", "string", "QP8_PRODUT"})
    aadd(self:aCoalesceTestFields, {"QP7_REVI"  , "string", "QP8_REVI"  })
	
	//Propriedade aninhada com campos do SX3
	self:aTestStruct := self:oQLTTReports:GetNestedStruct(self:aTestFields)
	aadd(self:aTestStruct, {"SKIPTESTE", STR0005 + " (SKIPTESTE)", "string", STR0005 + " (SKIPTESTE)"}) //Skip-Teste
	aadd(self:aTestStruct, {"QQK_OBS"  , STR0012 + " (QQK_OBS)"  , "string", STR0012 + " (QQK_OBS)"  }) //Observação da Operação
	self:addNestedProperty("Ensaios", "Ensaios", "Ensaios",, self:aTestStruct)

	//Propriedade Manual - Plano de Amostragem
	aAdd(aPlanoStruct, {"ENSAIO"     , STR0006 + " (ENSAIO)"  , "string", STR0006 + " (ENSAIO)"} )
	aAdd(aPlanoStruct, {"QP1_DESCES" , AllTrim(FWX3Titulo( "QP1_DESCES" )) + "(QP1_DESCES)"  , "string", AllTrim(FWX3Titulo( "QP1_DESCES" )) + "(QP1_DESCES)"} )
	aAdd(aPlanoStruct, {"QP1_DESCIN" , AllTrim(FWX3Titulo( "QP1_DESCIN" )) + "(QP1_DESCIN)"  , "string", AllTrim(FWX3Titulo( "QP1_DESCIN" )) + "(QP1_DESCIN)"} )
	aAdd(aPlanoStruct, {"QP1_DESCPO" , AllTrim(FWX3Titulo( "QP1_DESCPO" )) + "(QP1_DESCPO)"  , "string", AllTrim(FWX3Titulo( "QP1_DESCPO" )) + "(QP1_DESCPO)"} )
	aAdd(aPlanoStruct, {"ACEITE"     , STR0007 + " (ACEITE)"                                 , "string", STR0007 + " (ACEITE)"                               } ) // Aceite
	aAdd(aPlanoStruct, {"REJEITE"    , STR0008 + " (REJEITE)"                                , "string", STR0008 + " (REJEITE)"                              } ) // Rejeite
	aAdd(aPlanoStruct, {"AMOSTRA"    , STR0009 + " (AMOSTRA)"                                , "string", STR0009 + " (AMOSTRA)"                              } ) // Amostra
	aAdd(aPlanoStruct, {"PLANO"      , STR0010 + " (PLANO)"                                  , "string", STR0010 + " (PLANO)"                                } ) // Plano
	self:addNestedProperty("PlanoDeAmostragem", "PlanoDeAmostragem", "PlanoDeAmostragem",, aPlanoStruct)

Return self:oSchema

/*/{Protheus.doc} mountDefaultFields
Monta Array aFields Default
@author brunno.costa
@since 06/12/2023
@version 1.0
/*/
Method mountDefaultFields() class SmartViewQIPInspectionPlan
	self:aFields := {}
	aadd(self:aFields, "A1_NOME")
	aadd(self:aFields, "C2_ROTEIRO")
	aadd(self:aFields, "C2_EMISSAO")
	aadd(self:aFields, "C2_UM")
	aadd(self:aFields, "QP6_DESCES")
	aadd(self:aFields, "QP6_DESCIN")
	aadd(self:aFields, "QP6_DESCPO")
	aadd(self:aFields, "QP6_DOCOBR")
	aadd(self:aFields, "QP6_DTCAD")
	aadd(self:aFields, "QP6_DTDES")
	aadd(self:aFields, "QP6_DTINI")
	aadd(self:aFields, "QP6_HISTOR")
	aadd(self:aFields, "QP6_RVDES")
	aadd(self:aFields, "QPK_EMISSA")
	aadd(self:aFields, "QPK_FILIAL")
	aadd(self:aFields, "QPK_LOTE")
	aadd(self:aFields, "QPK_NUMSER")
	aadd(self:aFields, "QPK_OP")
	aadd(self:aFields, "QPK_PRODUT")
	aadd(self:aFields, "QPK_REVI")
	aadd(self:aFields, "QPK_TAMLOT")
	aadd(self:aFields, "QPK_UM")
Return 


/*/{Protheus.doc} mountDefaultTestFields
Monta Array aTestFields Default
@author brunno.costa
@since 06/12/2023
@version 1.0
/*/
Method mountDefaultTestFields() class SmartViewQIPInspectionPlan
	self:aTestFields := {}
	aadd(self:aTestFields, "A1_NOME")
	aadd(self:aTestFields, "QP1_QTDE")
	aadd(self:aTestFields, "QP1_DESCES")
	aadd(self:aTestFields, "QP1_DESCIN")
	aadd(self:aTestFields, "QP1_DESCPO")
	aadd(self:aTestFields, "QP7_CODREC")
	aadd(self:aTestFields, "QP7_DESPLA")
	aadd(self:aTestFields, "QP7_ENSAIO")
	aadd(self:aTestFields, "QP7_ENSOBR")
	aadd(self:aTestFields, "QP7_LABOR")
	aadd(self:aTestFields, "QP7_LIE")
	aadd(self:aTestFields, "QP7_LSE")
	aadd(self:aTestFields, "QP7_METODO")
	aadd(self:aTestFields, "QP7_MINMAX")
	aadd(self:aTestFields, "QP7_NIVEL")
	aadd(self:aTestFields, "QP7_NOMINA")
	aadd(self:aTestFields, "QP7_OPERAC")
	aadd(self:aTestFields, "QP7_PLAMO")
	aadd(self:aTestFields, "QP7_SEQLAB")
	aadd(self:aTestFields, "QP7_UNIMED")

	aadd(self:aTestFields, "QP8_CODREC")
	aadd(self:aTestFields, "QP8_DESPLA")
	aadd(self:aTestFields, "QP8_ENSAIO")
	aadd(self:aTestFields, "QP8_ENSOBR")
	aadd(self:aTestFields, "QP8_LABOR")
	aadd(self:aTestFields, "QP8_TEXTO")
	aadd(self:aTestFields, "QP8_METODO")
	aadd(self:aTestFields, "QP8_NIVEL")
	aadd(self:aTestFields, "QP8_OPERAC")
	aadd(self:aTestFields, "QP8_PLAMO")
	aadd(self:aTestFields, "QP8_SEQLAB")

	aadd(self:aTestFields, "QQ1_INSTR")
	aadd(self:aTestFields, "QQ7_CLIENT")
	aadd(self:aTestFields, "QQ7_LOJA")
	aadd(self:aTestFields, "QQK_CHAVE")
	aadd(self:aTestFields, "QQK_DESCRI")
	aadd(self:aTestFields, "QQK_OPERAC")
	aadd(self:aTestFields, "QQK_OPERGR")
Return 

/*/{Protheus.doc} aninhaDadosPlanoDeAmostragem
Aninha dados do Plano de Amostragem
@author brunno.costa
@since 06/12/2023
@version 1.0
@param 01 - jData          , json    , objeto json para adição da propriedade aninhada com o array de dados
@param 02 - cIDProperty    , caracter, nome da propriedade para aninhar os dados
@param 03 - cAliasEnsaios  , caracter, alias dos ensaios para consideração ao aninhar os dados
@param 04 - cTamanhoAmostra, caracter, tamanho da amostra calculado conforme regra de conversão
/*/
Method aninhaDadosPlanoDeAmostragem(jData as Json, cIDProperty as Character, cAliasEnsaios as Character, cTamanhoAmostra as Character) class SmartViewQIPInspectionPlan

	Local aImpressaoPlano := {} as Array
	Local cDescPlano      := "" as Character
	Local cEnsaio         := "" as Character
	Local cOperacao       := "" as Character
	Local cPlano          := "" as Character
	Local cProduto        := "" as Character
	Local cRevisao        := "" as Character
	Local cRoteiro        := "" as Character
	Local jItem                 as Json
	Local nLinha          := 0  as Integer

	jData[cIDProperty] := Iif(jData[cIDProperty] == Nil, {}, jData[cIDProperty])

	(cAliasEnsaios)->(DbGoTop())
	While !(cAliasEnsaios)->(Eof())

		cPlano     := Iif(Empty((cAliasEnsaios)->QP7_PLAMO) ,(cAliasEnsaios)->QP8_PLAMO ,(cAliasEnsaios)->QP7_PLAMO )
		cDescPlano := Iif(Empty((cAliasEnsaios)->QP7_DESPLA),(cAliasEnsaios)->QP8_DESPLA,(cAliasEnsaios)->QP7_DESPLA)

		cProduto  := (self:cAlias)->QPK_PRODUT
		cRevisao  := (self:cAlias)->QPK_REVI
		cRoteiro  := Iif(Empty((self:cAlias)->C2_ROTEIRO), QIPRotGene("QQH_CODREC"), (self:cAlias)->C2_ROTEIRO)
		cOperacao := (cAliasEnsaios)->QQK_OPERAC
		cEnsaio   := Iif(Empty((cAliasEnsaios)->QP7_ENSAIO),(cAliasEnsaios)->QP8_ENSAIO,(cAliasEnsaios)->QP7_ENSAIO)

		If QQH->(dbSeek(xFilial('QQH')+cProduto+cRevisao+cRoteiro+cOperacao+cEnsaio))
			aImpressaoPlano  := QEP_RetAmostra(cPlano,QQH->QQH_AMOST,QQH->QQH_NIVAMO,QQH->QQH_NQA,+Str(Int(Val(cTamanhoAmostra)),8),"QPK_TAMLOT",.F.)

			If cPlano == "I"
				For nLinha := 1 To Len(aImpressaoPlano)
					jItem := JsonObject():New()
					jItem["ENSAIO"]     := cEnsaio
					jItem["QP1_DESCES"] := AllTrim((cAliasEnsaios)->QP1_DESCES)
					jItem["QP1_DESCIN"] := AllTrim((cAliasEnsaios)->QP1_DESCIN)
					jItem["QP1_DESCPO"] := AllTrim((cAliasEnsaios)->QP1_DESCPO)
					jItem["ACEITE"]     := aImpressaoPlano[nLinha,1]
					jItem["REJEITE"]    := aImpressaoPlano[nLinha,2]
					jItem["AMOSTRA"]    := aImpressaoPlano[nLinha,3]
					jItem["PLANO"]      := cPlano + " " + Alltrim(cDescPlano) + " " + STR0011 + ": " + AllTrim(aImpressaoPlano[nLinha,7]) // Lote
					aAdd(jData[cIDProperty], jItem)
				Next nLinha 
			Else
				jItem := JsonObject():New()                                 
				jItem["ENSAIO"]     := cEnsaio
				jItem["QP1_DESCES"] := AllTrim((cAliasEnsaios)->QP1_DESCES)
				jItem["QP1_DESCIN"] := AllTrim((cAliasEnsaios)->QP1_DESCIN)
				jItem["QP1_DESCPO"] := AllTrim((cAliasEnsaios)->QP1_DESCPO)
				jItem["ACEITE"]     := aImpressaoPlano[2] 
				jItem["REJEITE"]    := aImpressaoPlano[3]
				jItem["AMOSTRA"]    := aImpressaoPlano[1]
				jItem["PLANO"]      := cPlano +" "+AllTrim(Substr(cDescPlano,1,90))
				aAdd(jData[cIDProperty], jItem)
			EndIF	
		EndIF

		//Popula Observacao da Operação
		jData["QQK_OBS"] := Iif(!Empty((cAliasEnsaios)->QQK_CHAVE), QA_Rectxt((cAliasEnsaios)->QQK_CHAVE, "QIPA010"), "")

		(cAliasEnsaios)->(DbSkip())
	EndDo

Return 
