#include "manufacturing.sv.qie.latestinflows.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIELatestInflows
Classe responsável pelos métodos de criação e manipulação do Smart View
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(           ;
    active         = .T.                            ,;
    team           = "SIGAQIE"                      ,;
    tables         = "QE6,QEK,QEL,QED"              ,;
    customTables   = "QE6,QEK,QEL,QED"              ,;
    name           = "Últimas Entradas (Qualidade)" ,;
    country        = "ALL"                          ,;
    initialRelease = "12.1.2310"                  )

CLASS SmartViewQIELatestInflows from totvs.framework.treports.integratedprovider.IntegratedProvider
    
    Public Data cAlias        as Character
	Public Data cUsedFields   as Character
    Public Data jData         as Json
	Public Data lForceAllTrim as Logical

    Protected Data aFields       as Array
	Protected Data aMirrorFields as Array
	Protected Data aMirrorTables as Array
    Protected Data aStruct       as Array
	Protected Data cQuery        as Character
    Protected DATA oQueryManager as object

    Public METHOD getData()        as object
    Public METHOD getDescription() as Character
    Public METHOD getDisplayName() as Character
    Public METHOD getSchema()      as object
    Public METHOD new()            as object

    Protected Method setUpQuery() 

ENDCLASS

/*/{Protheus.doc} new
Método de instância da Classe
@return object: self
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/ 
METHOD new() AS object CLASS SmartViewQIELatestInflows
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) // "Qualidade - Inspeção de Entrada"

    self:setIsLookUp(.T.)

    self:setPergunte("QER321") // Indica o pergunte que será utilizado no relatório

    self:lForceAllTrim := .T.

    self:oQueryManager := QLTQueryManager():New()
    
RETURN self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@return string contendo o nome que aparecerá no TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
METHOD getDisplayName() AS Character CLASS SmartViewQIELatestInflows
RETURN STR0002 // "Últimas Entradas (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@return string contendo o descrição que aparecerá no TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
METHOD getDescription() AS Character CLASS SmartViewQIELatestInflows
RETURN STR0003 // "Objeto com os registros das tabelas QE6/QEK/QEL/QED"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
METHOD getData(nPage AS Numeric, oFilter AS object) AS object CLASS SmartViewQIELatestInflows
  
    LOCAL aArea          := GetArea() as Array
    LOCAL jParams        := NIL       as JSON
    LOCAL nContador      := 1         as Numeric
    Local aBindParam   := {}        as Array
    Local lChangeQuery := .F.       as Logical

    jParams := oFilter:getParameters() //metodo para retorno do JSON dos parâmetros

    self:setPageSize(10)

    self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

	WHILE !(self:cAlias)->(Eof())
        
        IF nContador > 1
            self:jData := JSONObject():new()

           //Adicionar Campos da Estrutura no Json
            aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

            //Adicionar Campos Customizados no Json
            aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )

            //Adiciona dados do Cabeçalho do Relatório
            QLTTReportsManager():addReportHeader(self)

            self:jData["SKIP_LOTE"] := Iif((self:cAlias)->QEK_VERIFI == 2, "SIM", "NÃO")

            self:oData:appendData(self:jData)
        ENDIF

        nContador++    
        (self:cAlias)->(DBSkip())        
    ENDDO 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

RETURN self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author brunno.costa
@since 21/11/2023
@version 1.0
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIELatestInflows

	Local cExternalUse := "QEK_VERIFI"               as Character //Campos com uso externo obrigatório a criar na Query
	Local jParams      := Nil                        as Json
	Local oQLTTReports := QLTTReportsManager():New() as object

    //metodo para retorno do Json dos parâmetros
	jParams := oFilter:getParameters()

    self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()

    self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse)
	self:cQuery += " FROM "
	self:cQuery +=     "	(SELECT ' ' ABC "
    
    self:cQuery +=             oQLTTReports:retornaColunasComNovoAlias(self, {"QEK", "QED", "QEL",, "QE6"}, "")

	// QE6 - Insp.Entradas - Especificacoes  
	self:cQuery +=         " FROM " + RetSqlName("QE6") + " QE6 "

    // QEK - Entradas
	self:cQuery +=         " INNER JOIN " + RetSqlName("QEK") + " QEK "
    self:cQuery +=         " ON "
    aAdd(aBindParam, {xFilial("QEK"), "S"})
	self:cQuery +=               " QEK.QEK_FILIAL = ? " 
	self:cQuery +=           " AND QEK.QEK_PRODUT = QE6.QE6_PRODUT "
    self:cQuery +=           " AND QEK.QEK_REVI   = QE6.QE6_REVI "  
    
    IF !Empty(jParams['MV_PAR01'][1])

        aAdd(aBindParam, {jParams['MV_PAR01'][1], "S"})
	    self:cQuery +=       " AND QEK.QEK_FORNEC = ? " 

	EndIf

	If !Empty(jParams['MV_PAR02'][1])

        aAdd(aBindParam, {jParams['MV_PAR02'][1], "S"})
        self:cQuery +=       " AND QEK.QEK_LOJFOR = ? "

    Endif

	aAdd(aBindParam, {" ", "S"})
    self:cQuery +=           " AND QEK.D_E_L_E_T_ = ? "


    // QEL - Laudos das Entradas
	self:cQuery +=         " LEFT JOIN " + RetSqlName("QEL") + " QEL "
	self:cQuery +=         " ON " 
    aAdd(aBindParam, {xFilial("QEL"), "S"})
    self:cQuery +=               " QEL.QEL_FILIAL = ? " 
	self:cQuery +=           " AND QEL.QEL_FORNEC = QEK.QEK_FORNEC "
	self:cQuery +=           " AND QEL.QEL_LOJFOR = QEK.QEK_LOJFOR "
	self:cQuery +=           " AND QEL.QEL_PRODUT = QEK.QEK_PRODUT "
	self:cQuery +=           " AND QEL.QEL_DTENTR = QEK.QEK_DTENTR "
	self:cQuery +=           " AND QEL.QEL_LOTE   = QEK.QEK_LOTE "
    aAdd(aBindParam, {Space(TAMSX3("QEL_LABOR")[1]), "S"})
    aAdd(aBindParam, {" "                          , "S"})
	self:cQuery +=           " AND QEL.QEL_LABOR  = ? "
	self:cQuery +=           " AND QEL.D_E_L_E_T_ = ? "

	// QED - Índice Qualidade de Produtos
	self:cQuery +=     " LEFT JOIN " + RetSqlName("QED") + " QED "
	self:cQuery +=     " ON " 
    aAdd(aBindParam, {xFilial("QED"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " QED.QED_FILIAL = ? "  
	self:cQuery +=       " AND QED.QED_CODFAT = QEL.QEL_LAUDO "
	self:cQuery +=       " AND QED.D_E_L_E_T_ = ? "

	self:cQuery += " WHERE " 
    
    aAdd(aBindParam, {xFilial("QE6"), "S"})
    self:cQuery +=          " QE6.QE6_FILIAL = ? " 

    If !Empty(jParams['MV_PAR03'][1])

	    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
        self:cQuery +=  " AND QE6.QE6_PRODUT = ? "

	EndIf

	aAdd(aBindParam, {" ", "S"})
    self:cQuery +=      " AND QE6.D_E_L_E_T_ = ? " 


	//Os filtros serão setados na interface do novo TReports
    IF oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    ENDIF


    self:cQuery  += " ) TAB "

Return 

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 21/07/2023
@version 1.0
/*/
METHOD getSchema() AS object CLASS SmartViewQIELatestInflows

    LOCAL oQLTTReports   := QLTTReportsManager():New() as object

    self:aFields := { "QEK_DTENTR", "QEK_LOTE", "QEK_DOCENT", "QEK_VERIFI", "QED_DESCPO", "QED_DESCES", "QED_DESCIN" }

	self:aStruct := oQLTTReports:GetStruct(self:aFields)

    // Adicionando estrutura dos campos auxiliares
    AADD(self:aStruct , {"SKIP_LOTE"   , "Skip-Lote", "string", "SKIP_LOTE", ""}) // Skip-Lote

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

RETURN self:oSchema
