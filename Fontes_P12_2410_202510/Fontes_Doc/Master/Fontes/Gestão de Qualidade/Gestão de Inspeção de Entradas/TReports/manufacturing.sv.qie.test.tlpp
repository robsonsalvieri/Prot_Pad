#include "manufacturing.sv.qie.test.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5
#DEFINE POS_cComboValues 6
#DEFINE POS_lIsRequired  7
#DEFINE POS_cRenameField 8

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIETest
Classe responsável pelo métodos de criação e manipulação do Smart View
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                     ;
    active        = .T.                                      , ;
    team          = "SIGAQIE"                                , ;
    tables        = "QE6,QE7,QE8,QE1,SAH,SX5"                , ;
    customTables  = "QE6,QE7,QE8,QE1,SAH"                    , ;
    name          = "Ensaios Inspeção de Entrada (Qualidade)", ;
    country       = "ALL"                                    , ;
    initialRelease= "12.1.2310"           )

CLASS SmartViewQIETest from totvs.framework.treports.integratedprovider.IntegratedProvider

    Public Data cAlias             as Character
	Public Data cUsedFields        as Character
    Public Data cUsedSQLFields     as Character
    Public Data jData              as Json
	Public Data lForceAllTrim      as Logical

    Protected Data aCoalesceFields as Array
    Protected Data aFields         as Array
    Protected Data aMirrorFields   as Array
	Protected Data aMirrorTables   as Array
    Protected Data aStruct         as Array
	Protected Data cQuery          as Character
    Protected DATA oQueryManager   as object

    Public METHOD getData()        as object
    Public METHOD getDescription() as Character
    Public METHOD getDisplayName() as Character
    Public METHOD getSchema()      as object
    Public METHOD new()            as object

	Protected Method setUpQuery() 

ENDCLASS

/*/{Protheus.doc} new
Método de instância da Classe
@return object: self
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/ 
METHOD new() AS object CLASS SmartViewQIETest
    _Super:new()//Define a àrea
    
    self:appendArea(STR0001) // Qualidade - Inspeção de Entrada

    self:setIsLookUp(.T.)

    self:setPergunte("QER320") // Indica o pergunte que será utilizado no relatório

    self:lForceAllTrim   := .T.
    self:aCoalesceFields := {}
	self:aMirrorFields   := {}
    self:aMirrorTables   := {}

    self:oQueryManager   := QLTQueryManager():New()

RETURN self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@return string contendo o nome que aparecerá no TReports
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/
METHOD getDisplayName() AS Character CLASS SmartViewQIETest
RETURN STR0002 // "Ensaios Inspeção de Entrada (Qualidade)"

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@return string contendo o descrição que aparecerá no TReports
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/
METHOD getDescription() AS Character CLASS SmartViewQIETest
RETURN STR0003 // "Objeto com os registros das tabelas QE6/QE7/QE8/QE1/SAH/SX5"

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numeral, indica a página atual do relatório
@param oFilter, objeto, contÃ©m o filtro do TReports
@return object: self:oData, objeto Json contendo a query que será enviada ao TReports
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/
METHOD getData(nPage AS Numeric, oFilter AS object) AS object CLASS SmartViewQIETest
  
    LOCAL aArea        := GetArea()               as Array
    Local aBindParam   := {}                      as Array
    Local jFicticia    := JsonObject():New()      as Json
    Local jParams      := oFilter:getParameters() as Json //metodo para retorno do JSON dos parâmetro
    Local lChangeQuery := .T.                     as Logical
	Local nIndAux      := 0                       as Integer

    self:setPageSize(10)
 
    DBSelectArea("QE6")
    DBSelectArea("QE7")
    DBSelectArea("QE8")
    DBSelectArea("SX5")
    DBSelectArea("QE1")    
    DBSelectArea("SAH")

    self:setUpQuery(@aBindParam, oFilter)
    self:cQuery := self:oQueryManager:changeQuery(self:cQuery)
    self:cAlias := QLTQueryManager():executeQueryWithBind(self:cQuery, aBindParam, lChangeQuery)

    While !(self:cAlias)->(Eof())

        self:jData := JsonObject():new()
        
        //Adicionar Campos da Estrutura no Json
		aEval(self:aStruct          , {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cRealName] ) } )

        //Adiciona campos do aMirrorFields que não sejam NIL referente segundo ALIAS
		aEval(self:aCoalesceFields  , {|aField| QLTTReportsManager():addToData(self, aField[1], aField[2], aField[3] ) } )

		//Adicionar Campos Customizados no Json
		aEval(self:getCustomFields(), {|aField| QLTTReportsManager():addToData(self, aField[POS_cId], aField[POS_cType], aField[POS_cId] ) } )

        //Adiciona dados do Cabeçalho do Relatório
        QLTTReportsManager():addReportHeader(self)

		jFicticia["FICTICIA"] := ""
		self:jData["LinhasAdicionais"] := {}
		For nIndAux := 1 to (jParams['MV_PAR07'][1] - 1)
			aAdd(self:jData["LinhasAdicionais"], jFicticia)
		Next

        //Amostra Inicial/Inspecao -> 1=Amostra Inicial;2=Inspecao;3=Amostra e Inspecao
        If self:cUsedFields == "*" .OR. "AM_INS" $ self:cUsedFields .OR. "AMINS" $ self:cUsedFields
            Do Case
            Case (self:cAlias)->QE7_AM_INS == "1" .OR. (self:cAlias)->QE8_AM_INS == "1"
                self:jData["AMINSS"]       := "AMO"
            Case (self:cAlias)->QE7_AM_INS == "2" .OR. (self:cAlias)->QE8_AM_INS == "2"
                self:jData["AMINSS"]       := "INS"
            Case (self:cAlias)->QE7_AM_INS == "3" .OR. (self:cAlias)->QE8_AM_INS == "3"
                self:jData["AMINSS"]       := "A/I"
            EndCase
        EndIF

        //Skip-Teste - Conforme a rotina QIER320 o Skip-Teste se mantem com o conteúdo N/A
        If self:cUsedFields == "*" .OR. "SKIPTESTE" $ self:cUsedFields
		    self:jData["SKIPTESTE"]        := "N/A"
        EndIf

        self:oData:appendData(self:jData)

        (self:cAlias)->(DBSkip())
    EndDo 

    (self:cAlias)->(DBCloseArea())
    RestArea(aArea)

RETURN self:oData

/*/{Protheus.doc} setUpQuery
Monta query do relatório
@author brunno.costa
@since 21/11/2023
@version 1.0
/*/
Method setUpQuery(aBindParam as Array, oFilter as object) class SmartViewQIETest

    Local cExternalUse := "QE7_AM_INS,QE8_AM_INS"    as Character //Campos com uso externo obrigatório a criar na Query
    LOCAL jParams      := NIL                        as Json
    LOCAL oQLTTReports := QLTTReportsManager():New() as object

    jParams := oFilter:getParameters() //metodo para retorno do JSON dos parâmetro

	self:cUsedFields := Iif(oFilter:hasFields(),;                 //Verifica se o Smart View retornou os campos utilizados    
	                        ArrTokStr(oFilter:getFields(), ","),; //Sim -> Manda Somente os campos que o Smart View retornou como utilizados
	                        "*")                                  //Não -> Manda todos os campos do schema - self:getSQLFields()
    
    self:cQuery := " SELECT " + QLTTReportsManager():getUsedSQLFields(self, self:cUsedFields, cExternalUse, self:aCoalesceFields)
	self:cQuery += " FROM "
	self:cQuery +=     " (SELECT '' ABC "
   	self:cQuery +=               oQLTTReports:retornaColunasComNovoAlias(self, {"QE1", "SAH"}, ""          )
	self:cQuery +=               oQLTTReports:retornaColunasComNovoAlias(self, {"QE6"}       , "QE6_GERAL.")
	self:cQuery +=               oQLTTReports:retornaColunasComNovoAlias(self, {"QE7", "QE8"}, "LAB."      )

	// QE6 - Insp.Entradas - Especificacoes 
	self:cQuery +=     " FROM " + RetSqlName("QE6") + " QE6_GERAL "

    // QE7 - Ensaios Mensuráveis Produtos  
	// QE8 - Ensaios Texto dos Produto
	self:cQuery +=     " INNER JOIN "
	self:cQuery +=        " (SELECT R_E_C_N_O_"
	self:cQuery +=                  oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_PRODUT,QE7_REVI,QE7_ENSAIO,QE7_LABOR,QE7_UNIMED")
	self:cQuery +=                  oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_PRODUT,QE8_REVI,QE8_ENSAIO,QE8_LABOR", "NULL ")
	self:cQuery +=         " FROM " + RetSqlName("QE7") + " QE7 "
    self:cQuery +=         " WHERE  "
    
    aAdd(aBindParam, {xFilial("QE7")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
    self:cQuery +=             " QE7.QE7_FILIAL = ? "
    self:cQuery +=         " AND QE7.QE7_PRODUT = ? "

    If !Empty(jParams['MV_PAR04'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})

        self:cQuery +=      " AND ? <= QE7.QE7_LABOR " 
    EndIf

	If !Empty(jParams['MV_PAR05'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})

        self:cQuery +=      " AND QE7.QE7_LABOR <= ? " 
    EndIf

	aAdd(aBindParam, {" " , "S"})
    self:cQuery +=         " AND QE7.D_E_L_E_T_ = ? "

    self:cQuery +=         " UNION ALL "

    self:cQuery +=         " SELECT R_E_C_N_O_"
	self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE7"}, "QE7_PRODUT,QE7_REVI,QE7_ENSAIO,QE7_LABOR,QE7_UNIMED", "NULL ")
    self:cQuery +=                 oQLTTReports:retornaColunasDistintasComNovoAlias(self, {"QE8"}, "QE8_PRODUT,QE8_REVI,QE8_ENSAIO,QE8_LABOR")
    self:cQuery +=         " FROM " + RetSqlName("QE8") + " QE8 "
    Self:cQuery +=         " WHERE  "

    aAdd(aBindParam, {xFilial("QE8")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
    self:cQuery +=             " QE8.QE8_FILIAL = ? "
    self:cQuery +=         " AND QE8.QE8_PRODUT = ? "

    If !Empty(jParams['MV_PAR04'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR04'][1], "S"})

        self:cQuery +=      " AND ? <= QE8.QE8_LABOR " 
    EndIf

	If !Empty(jParams['MV_PAR05'][1])
		
		aAdd(aBindParam, {jParams['MV_PAR05'][1], "S"})

        self:cQuery +=      " AND QE8.QE8_LABOR <= ? " 
    EndIf
    
    aAdd(aBindParam, {" " , "S"})
    self:cQuery +=         " AND QE8.D_E_L_E_T_ = ? "
    
    self:cQuery +=        " ) LAB "
    self:cQuery +=        " ON "
    self:cQuery +=              " COALESCE(LAB.QE7_PRODUT, LAB.QE8_PRODUT) = QE6_GERAL.QE6_PRODUT "
    self:cQuery +=          " AND COALESCE(LAB.QE7_REVI  , LAB.QE8_REVI)   = QE6_GERAL.QE6_REVI "

    // QE1 - Ensaios
	self:cQuery +=     " LEFT JOIN " + RetSqlName("QE1") + " QE1 "
	self:cQuery +=     " ON "
    aAdd(aBindParam, {xFilial("QE1"), "S"})
    aAdd(aBindParam, {" "           , "S"})
	self:cQuery +=           " QE1.QE1_FILIAL = ? " 
    self:cQuery +=       " AND QE1.QE1_ENSAIO = COALESCE(LAB.QE7_ENSAIO, LAB.QE8_ENSAIO) "
    self:cQuery +=       " AND QE1.D_E_L_E_T_ = ? "

    // SAH - Unidades de Medida
	self:cQuery +=     " LEFT JOIN " + RetSqlName("SAH") + " SAH "
	self:cQuery +=     " ON "
    aAdd(aBindParam, {xFilial("SAH"), "S"})
    aAdd(aBindParam, {" "           , "S"})
    self:cQuery +=           " SAH.AH_FILIAL  = ? " 
    self:cQuery +=       " AND SAH.AH_UNIMED  = LAB.QE7_UNIMED "	
	self:cQuery +=       " AND SAH.D_E_L_E_T_ = ? "		
	

    self:cQuery += " WHERE  " 
    aAdd(aBindParam, {xFilial("QE6")        , "S"})
    aAdd(aBindParam, {jParams['MV_PAR03'][1], "S"})
    aAdd(aBindParam, {" "                   , "S"})
    self:cQuery +=     " QE6_GERAL.QE6_FILIAL = ? " 
    self:cQuery += " AND QE6_GERAL.QE6_PRODUT = ? "
    self:cQuery += " AND QE6_GERAL.D_E_L_E_T_ = ? "		

	//Os filtros serão setados na interface do novo TReports
    IF oFilter:hasFilter()
        self:cQuery += " AND " + oFilter:getSQLExpression()
    ENDIF

    self:cQuery += " ) TAB "

Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
@author thiago.rover
@since 07/08/2023
@version 1.0
/*/
METHOD getSchema() AS object CLASS SmartViewQIETest

    Local aMedicaoStruct := {}                         as Array
    Local oQLTTReports   := QLTTReportsManager():New() as object

    self:aFields := { "QE1_DESCPO", "QE1_DESCES", "QE1_DESCIN", "QE8_PRODUT", "QE8_REVI", "QE8_ENSAIO", "QE8_LABOR", "QE8_AM_INS", "QE8_METODO", "QE8_PLAMO", "QE8_NIVEL"}

	                          //NOME NA QUERY  , NOME NA SX3     , SUFIXO LABEL, ID JSON
	aAdd(self:aMirrorFields, {"QE7_PRODUT"     , "QE7_PRODUT"    , ""          , "PRODUT"             })
    aAdd(self:aMirrorFields, {"QE7_REVI"       , "QE7_REVI"      , ""          , "REVI"               })
    aAdd(self:aMirrorFields, {"QE7_ENSAIO"     , "QE7_ENSAIO"    , ""          , "ENSAIO"             })
    aAdd(self:aMirrorFields, {"QE7_LABOR"      , "QE7_LABOR"     , ""          , "LABORATORIO"        })
    aAdd(self:aMirrorFields, {"QE7_AM_INS"     , "QE7_AM_INS"    , ""          , "AMINS"              })
    aAdd(self:aMirrorFields, {"QE7_METODO"     , "QE7_METODO"    , ""          , "METODO"             })
    aAdd(self:aMirrorFields, {"QE7_UNIMED"     , "QE7_UNIMED"    , ""          , "UNIMED"             })
    aAdd(self:aMirrorFields, {"QE7_NOMINA"     , "QE7_NOMINA"    , ""          , "NOMINAL"            })
    aAdd(self:aMirrorFields, {"QE7_LIE"        , "QE7_LIE"       , ""          , "LIMITEINFERIOR"     })
    aAdd(self:aMirrorFields, {"QE7_LSE"        , "QE7_LSE"       , ""          , "LIMITESUPERIOR"     })
    aAdd(self:aMirrorFields, {"QE7_PLAMO"      , "QE7_PLAMO"     , ""          , "PLANOAMOSTRAGEM"    })
    aAdd(self:aMirrorFields, {"QE7_NIVEL"      , "QE7_NIVEL"     , ""          , "CARACTERISTICA"     })
    
    aAdd(self:aMirrorFields, {"QE8_TEXTO"      , "QE8_TEXTO"     , ""          , "TEXTO"              })

    aAdd(self:aMirrorFields, {"QE1_QTDE"       , "QE1_QTDE"      , ""          , "QUANTIDADEAMOSTRAS" })

    aAdd(self:aMirrorFields, {"AH_UMRES"       , "AH_UMRES"      , ""          , "DESC_UNIMED"        })


    self:aStruct := oQLTTReports:GetStruct(self:aFields, self:aMirrorFields)

    // Adicionando estrutura dos campos auxiliares
    aAdd(self:aStruct, {"SKIPTESTE"   , STR0007                                        , "string", STR0007 + "(SKIP_TESTE)"                       , ""}) //Skip-teste
    aAdd(self:aStruct, {"AMINSS"      , AlLTrim(FWX3Titulo( "QE7_AM_INS" )) + "(Sigla)", "string", AlLTrim(FWX3Titulo( "QE7_AM_INS" )) + " - Sigla(AMINSS)", ""}) //Sigla

    //Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]          ,;
	                                               aField[POS_cDescription] ,;
	                                               aField[POS_cType]        ,;
	                                               aField[POS_cDisplayName] ,;
	                                               aField[POS_cRealName]    ) } )

    //Tratamento de COALESCE     ID                  , tipo    , Nome QUERY
    aAdd(self:aCoalesceFields, {"PRODUT"             , "string", "QE8_PRODUT"     })
    aAdd(self:aCoalesceFields, {"REVI"               , "string", "QE8_REVI"       })
    aAdd(self:aCoalesceFields, {"ENSAIO"             , "string", "QE8_ENSAIO"     })
    aAdd(self:aCoalesceFields, {"LABORATORIO"        , "string", "QE8_LABOR"      })
    aAdd(self:aCoalesceFields, {"AMINS"              , "string", "QE8_AM_INS"     })
    aAdd(self:aCoalesceFields, {"METODO"             , "string", "QE8_METODO"     })
    aAdd(self:aCoalesceFields, {"PLANOAMOSTRAGEM"    , "string", "QE8_PLAMO"      })
    aAdd(self:aCoalesceFields, {"CARACTERISTICA"     , "string", "QE8_NIVEL"      })

    aadd(aMedicaoStruct, {"FICTICIA", "Coluna Fake (FICTICIA)", "string", "Coluna Fake (FICTICIA)"}) //Coluna Fake
	self:addNestedProperty("LinhasAdicionais", "LinhasAdicionais", "LinhasAdicionais",, aMedicaoStruct)

RETURN self:oSchema
