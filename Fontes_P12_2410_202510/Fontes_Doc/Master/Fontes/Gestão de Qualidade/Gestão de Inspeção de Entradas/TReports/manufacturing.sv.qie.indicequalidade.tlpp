#Include "manufacturing.sv.qie.indicequalidade.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"

#DEFINE POS_cId          1
#DEFINE POS_cDescription 2
#DEFINE POS_cType        3
#DEFINE POS_cDisplayName 4
#DEFINE POS_cRealName    5

namespace totvs.protheus.manufacturing.sv.qie

/*/{Protheus.doc} SmartViewQIEIndicequalidade
Classe responsável pelos métodos de criação e manipulação do Smart View
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
@totvsFrameworkTReportsIntegratedProvider(                                     ;
	active         = .T.                                                      ,;
	team           = "SIGAQIE"                                                ,;
	tables         = "QEV, QEW, SA2, SA5, QE0, QEG, QED "                     ,;
	name           = "Índice de qualidade Fornecedor x Produto (Qualidade)"   ,;
	country        = "ALL"                                                    ,;
	initialRelease = "12.1.2410"         )

Class SmartViewQIEIndicequalidade From totvs.framework.treports.integratedprovider.IntegratedProvider

    Public Data jData                  as Json
	Public Data lForceAllTrim          as Logical
	
	Protected Data aFields             as Array
    Protected Data aStruct             as Array
	Protected Data nIndice             as Integer
	Protected Data lAdicionaTotal      as Logical
	Protected DATA oQLTTReports        as object
	Protected DATA oQueryManager       as object

	Private Method calculaMedias()
	Private Method percorreIQF()
	Private Method percorrePPM()
	Private Method percorreQE0()
	Private Method percorreQEV()
	Private Method percorreQEW()
	Private Method preparaArraysPorFornecedor()
	
    Public Method getData(nPage, oFilter) as object
    Public Method getDescription()        as Character
    Public Method getDisplayName()        as Character
    Public Method getSchema()             as object
    Public Method new()                   as object

EndClass

/*/{Protheus.doc} new
Método de instância da Classe
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
@Return object: self
/*/ 
Method new() as object Class SmartViewQIEIndicequalidade
   
    _Super:new()//Define a Área
    
    self:appendArea(STR0001) //Qualidade - Inspeção de Entrada
    self:setIsLookUp(.T.)
	self:setPergunte("QER120SV") // Indica o pergunte que será utilizado no relatório

	Self:lAdicionaTotal := .F.
	self:lForceAllTrim  := .T.
	Self:nIndice        := 2 // 1 - Mensal, 2 - Acumulado
	self:oQLTTReports   := QLTTReportsManager():New()
	self:oQueryManager  := QLTQueryManager()   :New()

Return self

/*/{Protheus.doc} getDisplayName
Classe responsável pelo display no TReports 
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
@Return string contendo o nome que aparecerá no TReports
/*/
Method getDisplayName() as Character Class SmartViewQIEIndicequalidade
Return STR0002 // Índice de qualidade Fornecedor x Produto (Qualidade)

/*/{Protheus.doc} getDescription
Classe responsável pela descrição no TReports
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
@Return string contendo o descrição que aparecerá no TReports
/*/
Method getDescription() as Character Class SmartViewQIEIndicequalidade
Return STR0003 //"Objeto com os registros das tabelas "QEV, QEW, SA2, SA5, QE0, QEG, QED "

/*/{Protheus.doc} getData
Retorna os dados do objeto de negócio
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
@param 01 - nPage, numérico, indica a página atual do relatório
@param 02 - oFilter, objeto, contém o filtro do TReports
@Return object: self:oData, objeto Json contendo a query que será enviada ao TReports
/*/
Method getData(nPage as Numeric, oFilter as object) as object Class SmartViewQIEIndicequalidade
  
    Local aArea    := GetArea() as Array
	Local jParams  := NIL       as Json

	jParams := oFilter:getParameters() //metodo para retorno do Json dos parâmetros

	MV_PAR01 := jParams['MV_PAR01'][1] //Do Fornecedor?
	MV_PAR02 := jParams['MV_PAR02'][1] //Ate Fornecedor?
	MV_PAR03 := jParams['MV_PAR03'][1] //Do Produto?
	MV_PAR04 := jParams['MV_PAR04'][1] //Ate Produto?
	MV_PAR05 := jParams['MV_PAR05'][1] //De Ano?
	MV_PAR06 := jParams['MV_PAR06'][1] //Ate Ano?
	MV_PAR07 := jParams['MV_PAR07'][1] //De Mes?
	MV_PAR08 := jParams['MV_PAR08'][1] //Ate Mes?

	//Monta o Array com a Descricao das Avaliacoes dos Fornecedores
	Private aAvlFor  := {}
	Aadd(aAvlFor, STR0010 ) // "Entradas no periodo"
	Aadd(aAvlFor, STR0011 ) // "Entradas demeritadas no periodo"
	Aadd(aAvlFor, STR0012 ) // "Resultdo das Entradas (IQP)"
	Aadd(aAvlFor, STR0013 ) // "Fatorde criticidade"
	Aadd(aAvlFor, STR0014 ) // "Sistma da Qualidade (IQS)"
	Aadd(aAvlFor, STR0015 ) // "Indice Quaidade do Produto (IQI)"
	Aadd(aAvlFor, STR0016 ) // "Pontualidade (IPO" 
	Aadd(aAvlFor, STR0017 ) // "Indice mensal (IF)"
    self:setPageSize(10)

	R120ImpR3(NIL, .T.)

	TMP->(dbGoTop())

	While TMP->(!Eof())

		Private aForIQF := {}
		Private aForPPM := {}
		Private aForQE0 := {}
		Private aForQEV := {}
		Private aForQEW := {}
		Private aLegPrd := {}
		Private cForPrd := TMP->FORNEC
		Private nForIQS := 0
		Private nQtdIns := 0
		Private nQtdSkp := 0
		Private nVlrRej := 0

		Self:preparaArraysPorFornecedor()

		Self:calculaMedias()		

		SA2->(dbSetOrder(1))
		SA2->(MsSeek(xFilial("SA2")+cForPrd))

		//Adiciona Dados em self:oData
		self:jData := JsonObject():new()
		Self:percorreQEW()		
		Self:percorreQEV()
		Self:percorreQE0()
		Self:percorreIQF()
		Self:percorrePPM()

	EndDo

    RestArea(aArea)

Return self:oData

/*/{Protheus.doc} preparaArraysPorFornecedor
Monta os arrays aForQEV, aForQEW, aForQE0, aForIQF e aForPPM com os dados do relatório para o Fornecedor posicionado
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method preparaArraysPorFornecedor() class SmartViewQIEIndicequalidade

	Local cIndQE0 := ""
	Local cLaudo  := ""
	Local nIndFor := 0
	Local nPos    := 0
	Local nVlrInd := 0

	For nIndFor := 1 to (Len(aAvlFor)-1) //nao considera o IQF
		Aadd(aForQEV,{})
	Next

	While TMP->(!Eof()) .And. cForPrd == TMP->FORNEC 

		If	!Empty(TMP->LAUDO) .And. Empty(TMP->INDICE)
			//Preenche o Vetor aForQEW com a primeira parte do relatorio

			If cLaudo # TMP->LAUDO
				cLaudo := TMP->LAUDO
				Aadd(aForQEW,{})
			EndIf
			
			Aadd(aForQEW[Len(aForQEW)],{TMP->DESCFIQP,TMP->PRODUT,TMP->QTDLAUD,TMP->DESCPRO})
			
			//Preenche o Vetor com as legendas
			nPos := Ascan(aLegPrd,{|x|x[2]==TMP->PRODUT})
				
			If nPos == 0
				Aadd(aLegPrd,{"("+StrZero(Len(aLegPrd)+1,3)+")",TMP->PRODUT,"  "+TMP->DESCPRO}) 
			EndIf
				
		ElseIf Empty(TMP->LAUDO) .And. Empty(TMP->INDICE)
			// Preenche o Vetor aForQEV com a segunda parte do relatorio

			For nIndFor := 1 to (Len(aAvlFor)-1)

				If 	 nIndFor == 1 //Entradas no periodo
					nVlrInd := TMP->LOTENT
						
				ElseIf nIndFor == 2 //Entradas demeritadas no periodo
					nVlrInd :=  TMP->LOTDEM
						
				ElseIf nIndFor == 3 //Resultados das Entradas (IQP)
					nVlrInd := If(Self:nIndice==1,TMP->IQP,TMP->IQPA)
					
				ElseIf nIndFor == 4 //Fator de criticidade
					nVlrInd :=  If(Self:nIndice==1,TMP->IQD,TMP->IQDA)
						
				ElseIf nIndFor == 5 //Sistema de Qualidade (IQS)
					nVlrInd :=  TMP->IQS
					
				ElseIf nIndFor == 6 //Indice de Qualidade do Produto (IQI)
					nVlrInd :=  If(Self:nIndice==1,TMP->IQI,TMP->IQIA)
						
				ElseIf nIndFor == 7 //Pontualidade (IPO)
					nVlrInd :=  If(Self:nIndice==1,TMP->IPO,TMP->IPOA)
												
				EndIf

				Aadd(aForQEV[nIndFor],{aAvlFor[nIndFor],TMP->PRODUT,nVlrInd})

			Next

			//Preenche o IQF no aForIQF
			Aadd(aForIQF,{aAvlFor[8],TMP->PRODUT,TMP->IQF,TMP->IQFA})

			//preenche o PPM no aForPPM
			Aadd(aForPPM,{"PPM",TMP->PRODUT,TMP->PPM}) //PPM
			nVlrRej+=TMP->QTDREJ
			nQtdIns+=TMP->QTDINS
			nQtdSkp+=TMP->QTDSKP
			
		ElseIf Empty(TMP->LAUDO) .And. !Empty(TMP->INDICE)
			//Preenche o Vetor aForQE0 com a terceira parte do relatorio

			If cIndQE0 # TMP->INDICE
				cIndQE0 := TMP->INDICE
				Aadd(aForQE0,{})
			EndIf
			
			Aadd(aForQE0[Len(aForQE0)],{TMP->DESCGER,TMP->PRODUT,TMP->VALOR})
						
		EndIf
			
		TMP->(dbSkip())
	EndDo

Return

/*/{Protheus.doc} calculaMedias
Realiza o cálculo das médias dos arrays
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method calculaMedias() class SmartViewQIEIndicequalidade

	Local nIndQE01 := 0
	Local nIndQE02 := 0
	Local nIndQEV1 := 0
	Local nIndQEV2 := 0
	Local nIndQEW1 := 0
	Local nIndQEW2 := 0
	Local nMedPrd  := 0
	Local nVlrIQF  := 0
	Local nVlrIQFa := 0
	Local nVlrPPM  := 0

	For nIndQEW1 := 1 To Len(aForQEW)
		nMedPrd := 0
		Aadd(aForQEW[nIndQEW1],{"","",0, ""})
		For nIndQEW2 := 1 To Len(aForQEW[nIndQEW1])
			nMedPrd += aForQEW[nIndQEW1,nIndQEW2,3]
		Next nIndQEW2	
		aForQEW[nIndQEW1,Len(aForQEW[nIndQEW1]),3] := nMedPrd
		aForQEW[nIndQEW1,Len(aForQEW[nIndQEW1]),1] := aForQEW[nIndQEW1,1,1] //pega a descricao do primeiro item
	Next
	
	For nIndQEV1 := 1 To Len(aForQEV)
		nMedPrd := 0
		nIteMed := Len(aForQEV[nIndQEV1])
		Aadd(aForQEV[nIndQEV1],{"","",0, ""})
		For nIndQEV2 := 1 To Len(aForQEV[nIndQEV1])
			nMedPrd += If(aForQEV[nIndQEV1,nIndQEV2,3]#999.99,aForQEV[nIndQEV1,nIndQEV2,3],0)
		Next nIndQEV2	
		
		//Nao realiza o calculo da Media se igual Entradas no Periodo/Entradas Demeritadas
		If nIndQEV1 <= 2 
			aForQEV[nIndQEV1,Len(aForQEV[nIndQEV1]),3] := nMedPrd
		Else
			aForQEV[nIndQEV1,Len(aForQEV[nIndQEV1]),3] := nMedPrd/nIteMed
			If nIndQEV1 == 5 //guarda o IQS
				nForIQS := nMedPrd/nIteMed
			EndIf
		EndIf
		aForQEV[nIndQEV1,Len(aForQEV[nIndQEV1]),1] := aForQEV[nIndQEV1,1,1] //pega a descricao do primeiro item
	Next

	For nIndQE01 := 1 To Len(aForQE0)
		nMedPrd := 0
		Aadd(aForQE0[nIndQE01],{"","",0, ""})
		For nIndQE02 := 1 To Len(aForQE0[nIndQE01])
			nMedPrd += aForQE0[nIndQE01,nIndQE02,3]
		Next nIndQE02	
		aForQE0[nIndQE01,Len(aForQE0[nIndQE01]),3] := nMedPrd/(Len(aForQE0[nIndQE01])-1)
		aForQE0[nIndQE01,Len(aForQE0[nIndQE01]),1] := aForQE0[nIndQE01,1,1] //pega a descricao do primeiro item
	Next


	//Calcula a media do IQF
	nVlrIQF := 0
	Aeval(aForIQF,{|x,y|nVlrIQF+=If(aForIQF[y,3]#999.99,aForIQF[y,3],0)})
	Aadd(aForIQF,{"","",0,0})
	aForIQF[Len(aForIQF),3] := (nVlrIQF/(Len(aForIQF)-1))
	aForIQF[Len(aForIQF),1] := aForIQF[1,1] //pega a descricao do primeiro item


	//Calcula a media do IQF Acumulado
	nVlrIQFa := 0
	Aeval(aForIQF,{|x,y|nVlrIQFa+=If(aForIQF[y,4]#999.99,aForIQF[y,4],0)})
	aForIQF[Len(aForIQF),4] := (nVlrIQFa/(Len(aForIQF)-1))


	// Calcula a media do PPM
	nVlrPPM := (nVlrRej/(nQtdIns+nQtdSkp))*1000000
	Aadd(aForPPM,{ "","",0})
	aForPPM[Len(aForPPM),3] := nVlrPPM
	aForPPM[Len(aForPPM),1] := aForPPM[1,1] //pega a descricao do primeiro item

Return

/*/{Protheus.doc} percorreQEW
Percorre array da QEW e monta o Json com os dados para o relatório
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method percorreQEW() class SmartViewQIEIndicequalidade

	Local cProduto := ""
	Local nIndQEW1 := 0
	Local nIndQEW2 := 0
	Local nIQF     := 0
			
	//Realiza o Calculo da Media do IQF Acumulado
	nIQF := NoRound(q120MediaIQF(aForIQF))

	//Impressao do QEW
	For nIndQEW1 := 1 To Len(aForQEW)

		self:jData["entidade"] := "QEW"

		For nIndQEW2 := 1 To Len(aForQEW[nIndQEW1])

			cProduto := aForQEW[nIndQEW1,nIndQEW2,2]
			self:jData["produto"] := cProduto + " - " + AllTrim(Posicione("SB1", 1, xFilial("SB1") + cProduto, "B1_DESC"))

			SA5->(dbSetOrder(2))                    
			SA5->(MsSeek(xFilial("SA2")+cProduto+cForPrd))

			//Posiciona QEG (Sit. Produto X Fornecedor) no valor mais próximo do IQF encontrado e retorna a classificao atual.
			If FINDCLASS( "QIEXFunAuxClass" )
				oQIEXFunAx := QIEXFunAuxClass():New()
				cCategQEG := oQIEXFunAx:PosicionaNaQEGeRetornaCategoriaMaisProximaDoIQFEncontrado(nIQF, nForIQS, 1) //MV_PAR06 = 1 - Idioma Portugues
			EndIf
			
			self:jData["fornecedor"] := AllTrim(cForPrd) +" - "+ AllTrim(SA2->A2_NOME) + " - " + cCategQEG
			self:jData["coluna"]     := aForQEW[nIndQEW1,nIndQEW2,1]
			self:jData["valor"]      := If(aForQEW[nIndQEW1,nIndQEW2,3]==0, Nil, NoRound(aForQEW[nIndQEW1,nIndQEW2,3],4))
			
			If nIndQEW2 == Len(aForQEW[nIndQEW1])
				If !Self:lAdicionaTotal
					Loop
				EndIf
			EndIf

			If Len(aForQEW[nIndQEW1]) == 1 .Or. nIndQEW2 < Len(aForQEW[nIndQEW1])
				self:jData["detalhamentoDeLinha"] := self:jData["produto"]
				self:oData:appendData(self:jData) //Adição referente QEW
			EndIf


		Next nIndQEW2

	
	Next nIndQEW1

	self:jData["entidade"]            := "IQF"

	//If Self:lAdicionaTotal
	//	self:jData["detalhamentoDeLinha"] := "Total/Media"
	//Else
		self:jData["detalhamentoDeLinha"] := "Media IQF - " + cCategQEG
	//EndIf
	self:jData["coluna"]                  := "Média IQF"
	self:jData["valor"]                   := nIQF

	self:oData:appendData(self:jData) //Adição referente Média Geral de IQF

Return

/*/{Protheus.doc} percorreQEV
Percorre array da QEV e monta o Json com os dados para o relatório
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method percorreQEV() class SmartViewQIEIndicequalidade

	Local cProduto := ""
	Local nIndQEV1 := 0
	Local nIndQEV2 := 0

	//Impressao do QEV
	For nIndQEV1 := 1 To Len(aForQEV)

		self:jData["entidade"] := "QEV"
		
		For nIndQEV2 := 1 To Len(aForQEV[nIndQEV1])

			cProduto := aForQEV[nIndQEV1,nIndQEV2,2]
			self:jData["produto"]                 := cProduto + " - " + AllTrim(Posicione("SB1", 1, xFilial("SB1") + cProduto, "B1_DESC"))
			If nIndQEV2 == Len(aForQEV[nIndQEV1])
				If !Self:lAdicionaTotal
					Loop
				EndIf
			Else
				self:jData["detalhamentoDeLinha"] := self:jData["produto"]
			EndIf

			self:jData["coluna"] := aForQEV[nIndQEV1,nIndQEV2,1]
			
			If  nIndQEV1 == 1 .Or. nIndQEV1 == 2 //Entradas no Periodo e Demeritadas
				self:jData["valor"] := If(aForQEV[nIndQEV1,nIndQEV2,3] == 0, Nil,;
												If(aForQEV[nIndQEV1,nIndQEV2,3] == 999.99, Nil, NoRound(aForQEV[nIndQEV1,nIndQEV2,3],4)))
											
			ElseIf nIndQEV1 == 5 //Caso seja o IQS, adiciona somente o segundo elemento
				self:jData["valor"] := NoRound(aForQEV[nIndQEV1,nIndQEV2,3],4)

			Else
				self:jData["valor"] := If(aForQEV[nIndQEV1,nIndQEV2,3] == 999.99, Nil, NoRound(aForQEV[nIndQEV1,nIndQEV2,3],4))
			EndIf

			self:oData:appendData(self:jData)

		Next nIndQEV2
	
	Next nIndQEV1
	//Final da Impressao do QEV
Return

/*/{Protheus.doc} percorreQE0
Percorre array da QE0 e monta o Json com os dados para o relatório
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method percorreQE0() class SmartViewQIEIndicequalidade

	Local cProduto := ""
	Local nIndQE01 := 0
	Local nIndQE02 := 0

	//Impressao do QE0
	For nIndQE01 := 1 To Len(aForQE0)
		
		self:jData["entidade"] := "QE0"

		For nIndQE02 := 1 To Len(aForQE0[nIndQE01])

			cProduto := aForQE0[nIndQE01,nIndQE02,2]
			self:jData["produto"]                 := cProduto + " - " + AllTrim(Posicione("SB1", 1, xFilial("SB1") + cProduto, "B1_DESC"))
			If nIndQE02 == Len(aForQE0[nIndQE01])
				If !Self:lAdicionaTotal
					Loop
				EndIf
			Else
				self:jData["detalhamentoDeLinha"] := self:jData["produto"]
			EndIf
			
			self:jData["coluna"] := aForQE0[nIndQE01,nIndQE02,1]
			self:jData["valor" ] := If(aForQE0[nIndQE01,nIndQE02,3]==0, Nil, NoRound(aForQE0[nIndQE01,nIndQE02,3],4))

			self:oData:appendData(self:jData)

		Next nIndQE02
		
	Next nIndQE01
	//Final da Impressao do QE0
Return

/*/{Protheus.doc} percorreIQF
Percorre array de IQF e monta o Json com os dados para o relatório
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method percorreIQF() class SmartViewQIEIndicequalidade

	Local cProduto := ""
	Local nIndIQF  := 0

	//Impressao do IQF
	For nIndIQF := 1 To Len(aForIQF)

		self:jData["entidade"] := "IQF"

		cProduto := aForIQF[nIndIQF,2]
		self:jData["produto"]                 := cProduto + " - " + AllTrim(Posicione("SB1", 1, xFilial("SB1") + cProduto, "B1_DESC"))
		If nIndIQF == Len(aForIQF)
			If !Self:lAdicionaTotal
				Loop
			EndIf
		Else
			self:jData["detalhamentoDeLinha"] := self:jData["produto"]
		EndIf
			
		self:jData["coluna"]    :=	aForIQF[nIndIQF,1]
		if Self:nIndice ==1	//.Or. mv_par09 = 2  // Mensal ou nao faz media acumulada
			//self:jData["valor"] := If(aForIQF[nIndIQF,3] == 999.99, Nil, NoRound(aForIQF[nIndIQF,3],4))

		else // Acumulado
			self:jData["valor"] := If(aForIQF[nIndIQF,4] == 999.99, Nil, NoRound(aForIQF[nIndIQF,4],4))

		Endif	 

		self:oData:appendData(self:jData)

	Next nIndIQF			
	//Final da Impressao do IQF
Return

/*/{Protheus.doc} percorrePPM
Percorre array de PPM e monta o Json com os dados para o relatório
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
/*/
Method percorrePPM() class SmartViewQIEIndicequalidade

	Local cProduto := ""
	Local nIndPPM  := 0

	//Impressao do PPM
	For nIndPPM := 1 To Len(aForPPM)

		self:jData["entidade"] := "PPM"

		cProduto := aForPPM[nIndPPM,2]
		self:jData["produto"]                 := cProduto + " - " + AllTrim(Posicione("SB1", 1, xFilial("SB1") + cProduto, "B1_DESC"))
		If nIndPPM == Len(aForPPM)
			If !Self:lAdicionaTotal
				Loop
			EndIf
		Else
			self:jData["detalhamentoDeLinha"] := self:jData["produto"]
		EndIf
			
		self:jData["coluna"] := aForPPM[nIndPPM,1]
		self:jData["valor"]  := If(aForPPM[nIndPPM,3]==0, Nil, NoRound(aForPPM[nIndPPM,3],4))

		self:oData:appendData(self:jData)

	Next nIndPPM
	//Final da Impressao do PPM
Return

/*/{Protheus.doc} getSchema
Retorna a estrutura dos campos
@author jefferson.possidonio / brunno.costa
@since 25/06/2025
@version 1.0
@Return object: self:oSchema, objeto oSchema contendo o array da estrutura atraves do self
/*/
Method getSchema() as object Class SmartViewQIEIndicequalidade

    Local oQLTTReports := QLTTReportsManager():New() as object

	self:aFields := {}
	self:aStruct := oQLTTReports:GetStruct(self:aFields,,,.T.)

	aadd(self:aStruct, {"fornecedor"          , STR0004 , "string" , STR0004 , ""}) //"Fornecedor"
	aadd(self:aStruct, {"produto"             , STR0005 , "string" , STR0005 , ""}) //"Produto"
	aadd(self:aStruct, {"entidade"            , STR0006 , "string" , STR0006 , ""}) //"Entidade"
	aadd(self:aStruct, {"detalhamentoDeLinha" , STR0007 , "string" , STR0007 , ""}) //"Detalhamento em Linha"
	aadd(self:aStruct, {"coluna"              , STR0008 , "string" , STR0008 , ""}) //"Descrição da Coluna"
	aadd(self:aStruct, {"valor"               , STR0009 , "number" , STR0009 , ""}) //"Linha Detalhe 2"

	//Adiciona Propriedades ao SCHEMA
	aEval(self:aStruct, {|aField| self:addProperty(aField[POS_cId]         ,;
	                                               aField[POS_cDescription],;
	                                               aField[POS_cType]       ,;
	                                               aField[POS_cDisplayName],;
	                                               aField[POS_cRealName]   ) } )

Return self:oSchema
