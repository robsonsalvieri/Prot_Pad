#INCLUDE "QNCA070.CH"
#INCLUDE "TOTVS.CH"


/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ QNCA070  ≥ Autor ≥ Aldo Marini Junior    ≥ Data ≥ 10/07/00 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Programa de atualizacao de Responsaveis                    ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                   ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥         ATUALIZACOES SOFRIDAS DESDE A CONSTRUÄAO INICIAL.             ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Programador ≥ Data   ≥ BOPS ≥  Motivo da Alteracao                     ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Aldo        ≥30/10/01≥ ---- ≥ Inclusao da Opcao "Transferir" p/ transf.≥±±
±±≥            ≥        ≥      ≥ de Lactos pendentes.                     ≥±±
±±≥Eduardo S.  ≥05/09/02≥ ---- ≥ Alterado as funcoes AxCadastros para En- ≥±±
±±≥            ≥        ≥      ≥ choices prevendo integracao com SIGAGPE. ≥±±
±±≥Eduardo S.  ≥18/09/02≥ ---- ≥ Acerto na transferencia de pendencias pa ≥±±
±±≥            ≥        ≥      ≥ ra gravar usuario com 10 caracteres.     ≥±±
±±≥Eduardo S.  ≥11/10/02≥ ---- ≥ Acerto para transferir corretamente as   ≥±±
±±≥            ≥        ≥      ≥ pendencias para o usuario destino.       ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function MenuDef()

Local aRotina  := {}
Local cNivTran := GetMV("MV_QNCNTRF",.F.,9)
Local cTranFun := GetMV("MV_QNCTFUN",.F.,"N")

aAdd(aRotina,{OemToAnsi(STR0001),"AxPesqui"   , 0 , 1,,.F.})   // "Pesquisar"
aAdd(aRotina,{OemToAnsi(STR0002),"QNC070Telas", 0 , 2})   // "Visualizar"
aAdd(aRotina,{OemToAnsi(STR0003),"QNC070Telas", 0 , 3})   // "Incluir"
aAdd(aRotina,{OemToAnsi(STR0004),"QNC070Telas", 0 , 4})   // "Alterar"
aAdd(aRotina,{OemToAnsi(STR0046),"QNC070Telas", 0 , 5})   // "Excluir"

If cTranFun == "S" .And. cNivel >= cNivTran
	aAdd(aRotina,{OemToAnsi(STR0014),"QNCA070TRF"  , 0 , 4})   // "Transferir"
	aAdd(aRotina,{OemToAnsi(STR0052),"QNC070VIS"  , 0 , 2,,.F.})   //"Visual.Log"
Endif

Aadd(aRotina,{OemToAnsi(STR0048),"QNCA070Vrf", 0, 3,,.F.}) // "Mostrar Inativos"
aAdd(aRotina,{OemToAnsi(STR0009),"QNCA070Leg", 0, 6,,.F.}) // "Legenda"

Return aRotina

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNCA070   ∫Autor  ≥Microsiga           ∫ Data ≥  10/06/09   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Usu·rios                                                   ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNAC070                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QNCA070()

Private aCRA     := {OemToAnsi(STR0006), OemToAnsi(STR0007), OemToAnsi(STR0008)} //"Confirma" ### "Redigita" ### "Abandona"
Private cUsrInat := GetMv("MV_QUSRINA")
Private lQNCHFIL := GetMv("MV_QNCHFIL",.T.,.F.)
Private lUsrInat := If(cUsrInat == "N",.T.,.F.)

// Define o cabecalho da tela de atualizacoes
Private cCadastro := OemtoAnsi(STR0005)  //"Respons·veis/Usu·rios"

// Endereca a funcao de BROWSE 
QAA->(dbSetOrder(1))
QI2->(dbSetOrder(4))
QI3->(dbSetOrder(3))
QI5->(dbSetOrder(2))

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Define Array contendo as Rotinas a executar do programa      ≥
//≥ ----------- Elementos contidos por dimensao ------------     ≥
//≥ 1. Nome a aparecer no cabecalho                              ≥
//≥ 2. Nome da Rotina associada                                  ≥
//≥ 3. Usado pela rotina                                         ≥
//≥ 4. Tipo de TransaáÑo a ser efetuada                          ≥
//≥    1 - Pesquisa e Posiciona em um Banco de Dados             ≥
//≥    2 - Simplesmente Mostra os Campos                         ≥
//≥    3 - Inclui registros no Bancos de Dados                   ≥
//≥    4 - Altera o registro corrente                            ≥
//≥    5 - Remove o registro corrente do Banco de Dados          ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
Private aRotina := MenuDef()

oBrowse := FWmBrowse():New()
oBrowse:SetAlias( 'QAA' )
oBrowse:SetDescription( cCadastro )  
oBrowse:AddLegend( "QAA_SITQNC == 2", 'BR_AMARELO',OemToAnsi(STR0012))// Amarelo - Normal,com lactos pendentes
oBrowse:AddLegend( "QAA_SITQNC == 4", 'BR_PRETO'  ,OemToAnsi(STR0013))// Preta   - Inativo, com lactos pendentes
oBrowse:AddLegend( "QAA_SITQNC == 1", 'ENABLE'    ,OemToAnsi(STR0010))// Verde    - Normal,sem lactos pendentes
oBrowse:AddLegend( "QAA_SITQNC == 3", 'DISABLE'   ,OemToAnsi(STR0011))// Vermelho - Inativo, sem lactos pendentes
oBrowse:SetFilterDefault( "QAA->QAA_STATUS == '1'" )

dbSelectArea('QAA')
QI2->(dbSetOrder(1))
QI3->(dbSetOrder(1))
QI5->(dbSetOrder(1))
oBrowse:Activate()

Return .T.

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥QNCA070Leg ≥ Autor ≥ Aldo Marini Junior   ≥ Data ≥ 24.10.01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Cria uma janela contendo a legenda da mBrowse              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNCA070Leg

Local aLegenda := { {'ENABLE'    , OemtoAnsi(STR0010) },;	// "Usuario ATIVO sem Lactos Pendentes"
                     {'DISABLE'   , OemtoAnsi(STR0011)},;	// "Usuario INATIVO sem Lactos Pendentes"
                     {'BR_AMARELO', OemtoAnsi(STR0012)},;  // "Usuario ATIVO com Lactos Pendentes"
                     {'BR_PRETO'  , OemtoAnsi(STR0013)} }  // "Usuario INATIVO com Lactos Pendentes"

BrwLegenda(cCadastro,STR0009 ,aLegenda) 	// "Legenda"

Return .T.

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥QNCA070VLD ≥ Autor ≥Aldo Marini Junior      ≥ Data ≥ 24/10/01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥Retorna o numero da opcao correspondente a cor da situacao    ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥QNCA070VLD(nOpc) - Cor correspondente                         ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametro ≥ExpN1 = Numerico definindo a cor correspondente a situacao    ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Uso		 ≥QNCA070                                                       ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNCA070VLD(nOpc)
Local nRet   := nOpc
Local lSair  := .F.
Local lAtivo := QA_SitFolh()
Local cFilUsr:= QAA->QAA_FILIAL
Local cMatUsr:= QAA->QAA_MAT


If (nOpc == 1 .Or. nOpc == 2 ) .And. !lAtivo
	nRet := 0
ElseIf (nOpc == 3 .Or. nOpc == 4 ) .And. lAtivo
	nRet := 0
Endif

If nRet > 0


	nRet := 0
	lSair := .F.
	BeginSql alias 'QI3TRB'
		SELECT QAA.QAA_FILIAL  FROM %table:QAA% QAA
		WHERE QAA.QAA_FILIAL= %exp:cFilUsr% AND QAA.QAA_MAT= %exp:cMatUsr% AND

		(EXISTS( SELECT R_E_C_N_O_ FROM %table:QI3% QI3 WHERE
			QI3.QI3_FILMAT=QAA.QAA_FILIAL AND
			QI3.QI3_MAT=QAA.QAA_MAT AND
			QI3.QI3_ENCREA = ' ' AND
			QI3.%notDel% )
		OR
		EXISTS( SELECT R_E_C_N_O_ FROM %table:QI2% QI2  WHERE
			QI2.QI2_FILMAT=QAA.QAA_FILIAL AND
			QI2.QI2_MATRES=QAA.QAA_MAT AND
			QI2.QI2_CONREA = ' ' AND
			QI2.%notDel%  )
		OR
		EXISTS( SELECT R_E_C_N_O_ FROM %table:QI5% QI5  WHERE
			QI5.QI5_FILMAT=QAA.QAA_FILIAL AND
			QI5.QI5_MAT=QAA.QAA_MAT AND
			QI5.QI5_STATUS <> '4' AND
			QI5.%notDel%  ))
	EndSql

	QI3TRB->(DBGotop())
	IF QI3TRB->(!EOF())
		If (nOpc == 2 .And. lAtivo) .Or. (nOpc == 4 .And. !lAtivo)
			lSair := .T.
			nRet := nOpc
		ElseIf (nOpc == 1 .And. lAtivo) .Or. (nOpc == 3 .And. !lAtivo)
			lSair := .T.
			nRet := 9
		Endif
	EndIF
	QI3TRB->(DbCloseArea())

    		DbSelectArea("QAA")

	If nRet == 9
		nRet := 0
	Endif
	If nOpc == 1 .Or. nOpc == 3 .And. !lSair
		nRet := nOpc
	Endif

Endif

Return nRet

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥QNCA070TRF ≥ Autor ≥Aldo Marini Junior      ≥ Data ≥ 25/10/01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥Transfere os lancamentos do Usuario                           ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥QNCA070TRF()                                                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametro ≥Nenhum                                                        ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Uso		 ≥QNCA070                                                       ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNCA070TRF()

Local aCombOrd   := {OemToAnsi(STR0015),; // "Matricula"
					 OemToAnsi(STR0016),; // "Nome"
					 OemToAnsi(STR0017),; // "Nome Reduzido"
					 OemToAnsi(STR0018) } // "Depto"
Local aCposFiltr := {}
Local aQI2       := {}
Local aQI3       := {}
Local aQI5       := {}
Local aStatus    := {" 0%", " 25%", " 50%", " 75%", "100%", "Reprovado"}
Local aStruQI2   := FWFormStruct(3, "QI2",,.F.)[3]
Local cCombOrd   := 1
Local cFilQNC    := QAA->QAA_FILIAL
Local cFiltUsr   := ""
Local cQI2       := ""
Local cQI3       := ""
Local cQI5       := ""
Local cUsuQNC    := QAA->QAA_MAT
Local lChk01     := .T.
Local lChk02     := .F.
Local lChk03     := .F.
Local lFolQI2    := .T.
Local lFolQI3    := .T.
Local lFolQI5    := .T.
Local lTMKPMS    := If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.) //Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local nOldOrdU   := 1
Local nOpcao     := 2
Local nOrdQAA    := QAA->(indexord())
Local nRegQAA    := QAA->(Recno())
Local nX         := 0
Local oBtnFilt   := Nil
Local oChk01     := Nil
Local oChk02     := Nil
Local oCombOrd   := Nil
Local oDlg       := Nil
Local oFWLayer   := Nil
Local oMarcar    := Nil
Local oNo        := LoadBitmap( GetResources(), "LBNO" )
Local oOk        := LoadBitmap( GetResources(), "LBTIK" )
Local oPanel1    := Nil
Local oPanel2    := Nil
Local oPanel3    := Nil
Local oPanel4    := Nil
Local oPesq      := Nil
Local oQI2       := Nil
Local oQI3       := Nil
Local oQI5       := Nil
Local oSize      := Nil
Local oUsuarios  := Nil

Private aMsg       := {}
Private aPagPend   := {}
Private aQI5Aux    := {}
Private aTitPend   := {}
Private cFilMat    := xFilial("QAA")
Private cGrupo     := ""
Private cMotTransf := SPACE(30)
Private cTexto1    := AllTrim(QAA->QAA_FILIAL) + " - " + AllTrim(QAA->QAA_MAT) + " - " + AllTrim(QAA->QAA_NOME)
Private lMarca     := .F.
Private lTrava     := .F.
Private oFolder1   := Nil

CursorWait()

For nX := 1 To Len(aStruQI2)
	If AllTrim(aStruQI2[nX,1]) $ "QI2_FILIAL;QI2_FNC;QI2_REV;QI2_TPFIC;QI2_CODCAT;QI2_CODPRO;QI2_CODACA;QI2_REVACA"
		aAdd(aCposFiltr, AllTrim(aStruQI2[nX,1]) )
	EndIf
Next nX

// Carrega os Lactos Pendentes e de Transferencia QAB
Processa({|| lTrava:=QNC070Pen(@aQI5,@aQI3,@aQI2,cFiltUsr,lChk03,cFilQNC,cUsuQNC)},OemToAnsi(STR0053) ) // "Carregando os lactos pendentes..."

IF !lTrava
	IF MsgYesNo(OemToAnsi(STR0054),OemToAnsi(STR0050)) // ###"AtenÁ„o" //"Existem pendÍncias de TransferÍncia deste usu·rio, Confirme! Para a realizaÁ„o destas TransferÍncias."
		cMotTransf:=OemToAnsi(STR0019) // "TransferÍncia de Lancamentos pendentes"
		// Grava Tranferenciaa das pendencias
		QNC070Grv(@aQI5,@aQI3,@aQI2)
		// Limpa os Arrays apos a realizacao de Transferencia QAB
		aEval(aQI5,{|x| x[1]:=.F.,x[Len(x)]:=SPACE(12)})
		aEval(aQI3,{|x| x[1]:=.F.,x[Len(x)]:=SPACE(12)})
		aEval(aQI2,{|x| x[1]:=.F.,x[Len(x)]:=SPACE(12)})
	Endif
Endif

If Len(aQI5) == 0
	lFolQI5:=.F.
Else
	aAdd(aTitPend,OemToAnsi(STR0033)) // "Etapas/Passos"
	aAdd(aPagPend,"ETA")
Endif

If Len(aQI3) == 0
	lFolQI3:=.F.
Else
	aadd(aTitPend,OemToAnsi(STR0034)) // "Plano de Acao"
	aAdd(aPagPend,"PLN")
Endif

If Len(aQI2) == 0
	lFolQI2:=.F.
Else
	aAdd(aTitPend,OemToAnsi(STR0035)) // "Ficha de Ocorrencias/Nao-Conformidades"
	aAdd(aPagPend,"FNC")
Endif

If  !lFolQI5 .AND. !lFolQI3  .AND. !lFolQI2
	Help( " ", 1, "QNCNPENDU",,OemToAnsi(STR0038),1) // "Nao existem pendÍncias para este Usu·rio"
	QI2->(dbSetOrder(4))
	QI3->(dbSetOrder(3))
	QI5->(dbSetOrder(2))
	Return Nil
EndIf

CursorArrow()
//------------------------------------------
oSize := FwDefSize():New(.T.)

oSize:aMargins := { 3, 3, 0, 3 }

oSize:Process() // Dispara os calculos

DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0006) ; // "Plano de Amostragem por Ensaio"
						FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3]+40,oSize:aWindSize[4] STYLE nOR( WS_VISIBLE, WS_POPUP ) OF oMainWnd PIXEL

DEFINE FONT oFnt NAME "Arial" SIZE 6,15 BOLD

//Criando a camada
oFwLayer := FwLayer():New()
oFwLayer:init(oDlg,.F.)

//Adicionando linhas 
oFWLayer:addLine("CABECALHO" , 014, .F.)
oFWLayer:addLine("GRID1"     , 035, .F.)
oFWLayer:addLine("BOTOES"    , 040, .F.)

//Adicionando as colunas das linhas
oFWLayer:addCollumn("COLNOME"   , 049, .F., "CABECALHO")
oFWLayer:addCollumn("COLOPCOES" , 049, .F., "CABECALHO")
oFWLayer:addCollumn("COLPENDEN" , 098, .F., "GRID1"    )
oFWLayer:addCollumn("COLBOTOES" , 098, .F., "BOTOES"   )

//Adicionando as janelas
oFWLayer:AddWindow("COLNOME"   ,"oPanel1", STR0020 ,100,.F.,.F.,,"CABECALHO" ) // "Usu·rio"
oFWLayer:AddWindow("COLOPCOES" ,"oPanel2", STR0065 ,100,.F.,.F.,,"CABECALHO" ) // "OpÁ„o"
oFWLayer:AddWindow("COLPENDEN" ,"oPanel3", STR0021 ,100,.F.,.F.,,"GRID1"     ) // "Tipos de PendÍncias"
oFWLayer:AddWindow("COLBOTOES" ,"oPanel4", STR0023 ,100,.F.,.F.,,"BOTOES"    ) // "Usu·rios Destinos"

//Criando os paineis
oPanel1 := oFWLayer:GetWinPanel("COLNOME"    ,"oPanel1" ,"CABECALHO")
oPanel2 := oFWLayer:GetWinPanel("COLOPCOES"  ,"oPanel2" ,"CABECALHO")
oPanel3 := oFWLayer:GetWinPanel("COLPENDEN"  ,"oPanel3" ,"GRID1")
oPanel4 := oFWLayer:GetWinPanel("COLBOTOES"  ,"oPanel4" ,"BOTOES")

@ 001, 001 SAY OemToAnsi(cTexto1) SIZE 195,010 OF oPanel1 PIXEL

@ 001 , 001 CHECKBOX oChk01 VAR lChk01 SIZE 62,06 OF oPanel2 PIXEL FONT oFnt PROMPT OemToAnsi(STR0031) // "Todas PendÍncias"
@ 001 , 070 CHECKBOX oChk02 VAR lChk02 SIZE 62,06 OF oPanel2 PIXEL FONT oFnt PROMPT OemToAnsi(STR0064) // "Altera Gr. Etapas"

oBtnFilt := TButton():New(,, STR0066, oPanel2,{||(cFiltUsr := BuildExpr('QI2',,cFiltUsr,.T.,,, aCposFiltr),;
												lChk03 := .T.,;
												QNC070Pen(aQI5,,,cFiltUsr,.T.,cFilQNC,cUsuQNC,@oQI5) );
											   }, 35,4,,,.F.,.T.,.F.,,.F.,,,.F.)
oBtnFilt:Align := CONTROL_ALIGN_RIGHT

oFolder1 := TFolder():New(0,0,aTitPend,aPagPend,oPanel3,,,,.T.,.F.,oPanel3:nWidth,oPanel3:nHeight)
oFolder1:Align := CONTROL_ALIGN_ALLCLIENT

IF lFolQI5
	If lTMKPMS
		// Folder 1 - Etapas/Passos do Plano de AÁ„o
		@ 001,005;
				LISTBOX oQI5 VAR cQI5 FIELDS HEADER " ",;
				Alltrim(TitSx3("QI5_FILIAL") [1]),;
				Alltrim(TitSx3("QI5_CODIGO") [1]),;
				Alltrim(TitSx3("QI2_FNC" )   [1]),;
				Alltrim(TitSx3("QI5_STATUS") [1]),;
				Alltrim(TitSx3("QI5_TPACAO") [1]),;
				Alltrim(TitSx3("QI5_DESCTP") [1]),;
				Alltrim(TitSx3("QI5_PRAZO" ) [1]),;
				Alltrim(TitSx3("QI2_CODCAT" )[1]),;
				Alltrim(TitSx3("QI2_CODPRO" )[1]),;
				Alltrim(TitSx3("QI5_PROJET" )[1]),;
				Alltrim(TitSx3("QI3_MODELO" )[1]),;
				Alltrim(TitSx3("QI2_NCHAMA" )[1]);
				SIZE oPanel3:nWidth,oPanel3:nHeight OF oFolder1:aDialogs[Ascan(aPagPend,"ETA")] PIXEL ;
				ON DBLCLICK (QNCA070LCT(oQI5,@aQI5,lChk01,.F.) ,oQI5:Refresh(.T.))

		oQI5:SetArray(aQI5)
		oQI5:cToolTip:= OemToAnsi(STR0036) //"Duplo click para << MARCAR/DESMARCAR >> PendÍncia"
		oQI5:bLine   := {||{ If(aQI5[oQI5:nAt,1],oOk,oNo),;
			aQI5[oQI5:nAt,2],;	                    //Filial
			Transform(aQI5[oQI5:nAt,3],PesqPict("QI3","QI3_CODIGO"))+"  "+aQI5[oQI5:nAt,4],; // Codigo-Revisao Plano de Acao
			Transform(aQI5[oQI5:nAt,5],PesqPict("QI2","QI2_FNC"))+"  "+aQI5[oQI5:nAt,6],;	 // Codigo-Revisao da FNC
			aStatus[Val(aQI5[oQI5:nAt,7])+1],;   	// Status da Acao
			aQI5[oQI5:nAt,8],;						// Tipo da Acao
			aQI5[oQI5:nAt,9],;						// Desc Tipo da Acao
			DTOC(aQI5[oQI5:nAt,10]),;				// Prazo/Vecto da Acao
			aQI5[oQI5:nAt,11],;  					// Categoria
			aQI5[oQI5:nAt,12],;                     // Produto
			aQI5[oQI5:nAt,13],;						// Projeto
 			aQI5[oQI5:nAt,14],;						// Modelo
			aQI5[oQI5:nAt,15]}}						// Chamado
		oQI5:bChange := { || IF(!Empty(aQI5[oQI5:nAt,Len(aQI5[oQI5:nAt])]),QNCA070Fun(@oUsuarios,aQI5[oQI5:nAt,Len(aQI5[oQI5:nAt])]),) }
		oQI5:Align := CONTROL_ALIGN_ALLCLIENT
	Else
		// Folder 1 - Etapas/Passos do Plano de AÁ„o
		@ 001,005;
				LISTBOX oQI5 VAR cQI5 FIELDS HEADER " ",;
				Alltrim(TitSx3("QI5_FILIAL")[1]),;
				Alltrim(TitSx3("QI5_CODIGO")[1]),;
				Alltrim(TitSx3("QI5_STATUS")[1]),;
				Alltrim(TitSx3("QI5_TPACAO")[1]),;
				Alltrim(TitSx3("QI5_DESCTP")[1]),;
				Alltrim(TitSx3("QI5_DESCRE")[1]),;
				Alltrim(TitSx3("QI5_PRAZO" )[1]);
		        SIZE oPanel3:nWidth,oPanel3:nHeight OF oFolder1:aDialogs[Ascan(aPagPend,"ETA")] PIXEL ;
		        ON DBLCLICK (QNCA070LCT(oQI5,@aQI5,lChk01,.F.) ,oQI5:Refresh(.T.))

		oQI5:SetArray(aQI5)
		oQI5:cToolTip:= OemToAnsi(STR0036) //"Duplo click para << MARCAR/DESMARCAR >> PendÍncia"
		oQI5:bLine   := {||{ If(aQI5[oQI5:nAt,1]==.F.,oNO,oOK),;
			aQI5[oQI5:nAt,2],;	                    //Filial
			Transform(aQI5[oQI5:nAt,3],PesqPict("QI3","QI3_CODIGO"))+"  "+aQI5[oQI5:nAt,4],; // Codigo-Revisao Plano de Acao
			aStatus[Val(aQI5[oQI5:nAt,5])+1],; 	    // Status da Acao
			aQI5[oQI5:nAt,6],;						// Tipo da Acao
			aQI5[oQI5:nAt,7],;						// Desc Tipo da Acao
			aQI5[oQI5:nAt,10],;						// Descricao Resumida
			DTOC(aQI5[oQI5:nAt,8])}}				// Prazo/Vecto da Acao
		oQI5:bChange := { || IF(!Empty(aQI5[oQI5:nAt,Len(aQI5[oQI5:nAt])]),QNCA070Fun(@oUsuarios,aQI5[oQI5:nAt,Len(aQI5[oQI5:nAt])]),) }
		oQI5:Align := CONTROL_ALIGN_ALLCLIENT
	EndIf
Endif

IF lFolQI3
	// Folder 2 - Plano de AÁ„o
	@ 001,005;
			LISTBOX oQI3 VAR cQI3 FIELDS HEADER " ",;
			Alltrim(TitSx3("QI3_FILIAL")[1]),;
			Alltrim(TitSx3("QI3_CODIGO")[1]),;
			Alltrim(TitSx3("QI3_STATUS")[1]),;
			Alltrim(TitSx3("QI3_ABERTU")[1]),;
			Alltrim(TitSx3("QI3_ENCPRE")[1]),;
			Alltrim(TitSx3("QI3_TIPO")[1]);
	        SIZE oPanel3:nWidth,oPanel3:nHeight OF oFolder1:aDialogs[Ascan(aPagPend,"PLN")] PIXEL ;
	        ON DBLCLICK (QNCA070LCT(oQI3,@aQI3,lChk01,.F.) ,oQI3:Refresh(.T.))

	oQI3:SetArray(aQI3)
	oQI3:cToolTip:= OemToAnsi(STR0036) //"Duplo click para << MARCAR/DESMARCAR >> PendÍncia"
	oQI3:bLine   := {||{ If(aQI3[oQI3:nAt,1]==.F.,oNO,oOK),;
		aQI3[oQI3:nAt,2],;	       														//Filial
		Transform(aQI3[oQI3:nAt,3],PesqPict("QI3","QI3_CODIGO"))+"  "+aQI3[oQI3:nAt,4],;//Codigo
		aQI3[oQI3:nAt,5],;                                                              //Status
		DTOC(aQI3[oQI3:nAt,6]),;                                                        //Abertura
		DTOC(aQI3[oQI3:nAt,7]),;                                                        //Encerramento previsto
		aQI3[oQI3:nAt,8]}}                                                              //Tipo
	oQI3:bChange := { || IF(!Empty(aQI3[oQI3:nAt,Len(aQI3[oQI3:nAt])]),QNCA070Fun(@oUsuarios,aQI3[oQI3:nAt,Len(aQI3[oQI3:nAt])]),) }
	oQI3:Align := CONTROL_ALIGN_ALLCLIENT
Endif

IF lFolQI2
	// Folder 3 - Ficha de OcorrÍncias/N„o-conformidades
	@ 001,005;
			LISTBOX oQI2 VAR cQI2 FIELDS HEADER " ", ;
			Alltrim(TitSx3("QI2_FILIAL")[1]),;
			Alltrim(TitSx3("QI2_STATUS")[1]),;
			Alltrim(TitSx3("QI2_PRIORI")[1]),;
			Alltrim(TitSx3("QI2_FNC")[1]),;
			Alltrim(TitSx3("QI2_DESCR")[1]),;
			Alltrim(TitSx3("QI2_OCORRE")[1]),;
			Alltrim(TitSx3("QI2_CONPRE")[1]);
			SIZE oPanel3:nWidth,oPanel3:nHeight OF oFolder1:aDialogs[Ascan(aPagPend,"FNC")] PIXEL ;
			ON DBLCLICK (QNCA070LCT(oQI2,@aQI2,lChk01,.F.) ,oQI2:Refresh(.T.))

	oQI2:SetArray(aQI2)
	oQI2:cToolTip:= OemToAnsi(STR0036) // "Duplo click para << MARCAR/DESMARCAR >> PendÍncia"
	oQI2:bLine   := {||{If(aQI2[oQI2:nAt,1]==.F.,oNO,oOK),;
		aQI2[oQI2:nAt,2],;
		aQI2[oQI2:nAt,3],;
		aQI2[oQI2:nAt,8],;
		Transform(aQI2[oQI2:nAt,4],PesqPict("QI2","QI2_FNC"))+"  "+aQI2[oQI2:nAt,5],;
		aQI2[oQI2:nAt,6],;
		DtoC(aQI2[oQI2:nAt,9]),;
		DtoC(aQI2[oQI2:nAt,10]) }}
	oQI2:bChange := { || IF(!Empty(aQI2[oQI2:nAt,Len(aQI2[oQI2:nAt])]),QNCA070Fun(@oUsuarios,aQI2[oQI2:nAt,Len(aQI2[oQI2:nAt])]),) }
	oQI2:Align := CONTROL_ALIGN_ALLCLIENT
Endif

@ 001, 002 BUTTON oPesq PROMPT OemToAnsi(STR0029) ; // "Pesquisar Usu·rio"
			  ACTION ( If(QNC070PUS()==.F.,QAA->(dbGoTop()),"") ,;
						If( oCombOrd:nAt == 1 .And. QAA->(IndexOrd()) <> 1, QAA->(dbSetOrder(1)),;
						If( oCombOrd:nAt == 2 .And. QAA->(IndexOrd()) <> 3, QAA->(dbSetOrder(3)),;
						If( oCombOrd:nAt == 3 .And. QAA->(IndexOrd()) <> 6, QAA->(dbSetOrder(6)),;
						If( oCombOrd:nAt == 4 .And. QAA->(IndexOrd()) <> 5, QAA->(dbSetOrder(5)),"")))), ;
			  			nOldOrdU := oCombOrd:nAt,oUsuarios:UpsTable(),oUsuarios:Refresh() ) ;
			  SIZE 53,12 PIXEL OF oPanel4

@ 001, 065 BUTTON oMarcar PROMPT OemToAnsi(STR0022) SIZE 135,12 PIXEL OF oPanel4 ; // "<< MARCAR/DESMARCAR USU¡RIOS >>"
			ACTION QNC070BUT(@oQI5,@oQI3,@oQI2,@aQI5,@aQI3,@aQI2,oFolder1:nOption,lChk01)

@ 003, 215 SAY OemToAnsi(STR0030) SIZE 55,010 OF oPanel4 PIXEL // "OrdenaÁ„o Usu·rios:"
@ 001, 281 COMBOBOX oCombOrd VAR cCombOrd ITEMS aCombOrd SIZE 58,70 PIXEL OF oPanel4 ;
			  ON CHANGE If( nOldOrdU <> oCombOrd:nAt,;
			  			  (nOldOrdU := oCombOrd:nAt,;
						If( oCombOrd:nAt == 1, QAA->(dbSetOrder(1)) ,;
						If( oCombOrd:nAt == 2, QAA->(dbSetOrder(3)) ,;
						If( oCombOrd:nAt == 3, QAA->(dbSetOrder(6)) ,;
							                   QAA->(dbSetOrder(5)) ))) ,;
						oUsuarios:UpsTable(),oUsuarios:Refresh() ) ,"")

@ 025 , 003 LISTBOX oUsuarios VAR cUsr; //143
			  FIELDS QAA->QAA_FILIAL,QAA->QAA_MAT,QAA->QAA_NOME,QAA->QAA_APELID,QAA->QAA_CC+"-"+QA_NDEPT(QAA->QAA_CC,.F.,QAA->QAA_FILIAL) ;
			  HEADER OemToAnsi(STR0024),OemToAnsi(STR0025),OemToAnsi(STR0026),OemToAnsi(STR0017),OemToAnsi(STR0028) ; // "Filial" ### "CÛdigo" ### "Nome" ### "Nome Reduzido" ### "Depto"
			  ALIAS "QAA" ;
			  SIZE oPanel4:nWidth, oPanel4:nHeight * 0.4 PIXEL ;
			  OF oPanel4			  

oUsuarios:Align := CONTROL_ALIGN_BOTTOM
oUsuarios:GoTop()
oUsuarios:UpStable()
oUsuarios:Refresh()

ACTIVATE MSDIALOG oDlg CENTERED ON INIT EnchoiceBar(oDlg,;
								{||If(QNC070FIM( aClone(aQI5),aClone(aQI3),aClone(aQI2),oDlg,lChk02,cFilQNC,cUsuQNC),(nOpcao:=1,oDlg:End()),)},;
								{||(nOpcao:=2,oDlg:End())})

If nOpcao == 1	// OK
	// Grava TranferÍncia das pendÍncias
	QNC070Grv(@aQI5,@aQI3,@aQI2,lChk02,cFilQNC,cUsuQNC,cFiltUsr)
Endif

dbSelectArea("QAA")
dbSetOrder(nOrdQAA)
dbGoTo(nRegQAA)

QI2->(dbSetOrder(4))
QI3->(dbSetOrder(3))
QI5->(dbSetOrder(2))

Return Nil

/*/
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ QNCA070Fun ≥ Autor ≥ Aldo Marini Junior  ≥ Data ≥ 26.10.01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Atualiza ponteiro de usuarios                              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥QNCA070Fun(oUsuarios,cChave)                                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥ oUsuarios= Objeto contendo os Usuarios                     ≥±±
±±≥          ≥ cChave   = Caracter contendo a chave de pesquisa de usuario≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                   ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070BUT(oQI5,oQI3,oQI2,aQI5,aQI3,aQI2,nFolderItem,lChk01)
Local cPaginas:= aPagPend[nFolderItem]

If QAA->QAA_STATUS <> "2"
	DO CASE
		CASE cPaginas == "ETA"	// Etapas
			QNCA070LCT(oQI5,@aQI5,lChk01,.T.)
			oQI5:Refresh()

		CASE cPaginas == "PLN"	// Plano de Acao
			QNCA070LCT(oQI3,@aQI3,lChk01,.T.)
			oQI3:Refresh()

		CASE cPaginas == "FNC"	// FNC
			QNCA070LCT(oQI2,@aQI2,lChk01,.T.)
			oQI2:Refresh()
	EndCASE
Else
	Help( " ", 1,"USRINATIVO",,OemToAnsi(STR0037),1 )	// "Usuario selecionado esta INATIVO"
Endif

Return Nil

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ QNCA070Fun ≥ Autor ≥ Aldo Marini Junior  ≥ Data ≥ 26.10.01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Atualiza ponteiro de usuarios                              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥QNCA070Fun(oUsuarios,cChave)                                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥ oUsuarios= Objeto contendo os Usuarios                     ≥±±
±±≥          ≥ cChave   = Caracter contendo a chave de pesquisa de usuario≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                   ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNCA070LCT(oArray,aArray,lChk01,lButton)

Local nArray := 0

IF QAA->QAA_STATUS =="2"
	Help( " ", 1,"USRINATIVO" )
	Return Nil
Endif

If cTexto1 == QAA->QAA_FILIAL + " - " + QAA->QAA_MAT + " - " + QAA->QAA_NOME
	Help(" ",1,"QADUSRTRF",, OemToAnsi(STR0055),1)        //"Usu·rio n„o pode Transferir ele mesmo."
	Return Nil
Endif

If Len(aArray) > 0
	If lChk01 .And. lButton
		lMarca := !lMarca
		For nArray := 1 to Len(aArray)
			aArray[nArray,1] := lMarca
			If lMarca
				aArray[nArray,Len(aArray[nArray])] := QAA->QAA_FILIAL+QAA->QAA_MAT
			Else
				aArray[nArray,Len(aArray[nArray])] := Space(12)
			Endif
		Next
	Else
		aArray[oArray:nAt,1] := !(aArray[oArray:nAt,1])
		If aArray[oArray:nAt,1] == .T.
			aArray[oArray:nAt,Len(aArray[oArray:nAt])] := QAA->QAA_FILIAL+QAA->QAA_MAT
		Else
			aArray[oArray:nAt,Len(aArray[oArray:nAt])] := Space(12)
		Endif
	Endif
Endif

Return Nil

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥ QNCA070Fun ≥ Autor ≥ Aldo Marini Junior  ≥ Data ≥ 26.10.01 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Atualiza ponteiro de usuarios                              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥QNCA070Fun(oUsuarios,cChave)                                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥ oUsuarios= Objeto contendo os Usuarios                     ≥±±
±±≥          ≥ cChave   = Caracter contendo a chave de pesquisa de usuario≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ Generico                                                   ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function QNCA070Fun(oUsuarios,cChave)

Local nOrdem:= QAA->(Indexord())

QAA->(dbSetOrder(1))
If !QAA->(dbSeek(cChave))
	oUsuarios:GoTop()
Endif

QAA->(dbSetOrder(nOrdem))

oUsuarios:UpStable()
oUsuarios:Refresh()

Return Nil


/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥QNC070Telas≥ Autor ≥ Eduardo de Souza     ≥ Data ≥ 05/09/02 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Tela Cadastro de Usuarios                                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ QNC070Telas(ExpC1,ExpN1,ExpN2)                             ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥ ExpC1 - Alias do arquivo                                   ≥±±
±±≥          ≥ ExpN1 - Numero do registro                                 ≥±±
±±≥          ≥ ExpN2 - Numero da opcao selecionada                        ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070Telas(cAlias,nReg,nOpc)

Local oDlg
Local nI     := 0
Local nOpcao := 0
Local nSaveSx8 := GetSX8Len()
Local lRet   := .T.
Local lTMKPMS:= If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local cParticip  := ""
Local cRecurso	 := ""
Local cOperador	 := ""
Local oEnchoice
Local aMsSize	:= MsAdvSize()

Private lAltUsr:= .F.
Private lIntGPE:= If(GetMv("MV_QGINT",.F.,"N") == "S",.T.,.F.)
Private bCampo := {|nCPO| Field( nCPO ) }
Private aTELA[0][0]
Private aGETS[0]

DbSelectArea("QAA")
DbSetOrder(1)
Set Filter To

If nOpc == 3
   For nI := 1 To FCount()
       cCampo := Eval( bCampo, nI )
       lInit  := .F.
       If ExistIni( cCampo )
          lInit := .T.
          M->&( cCampo ) := InitPad( GetSX3Cache(cCampo,"X3_RELACAO") )
          If ValType( M->&( cCampo ) ) = "C"
             M->&( cCampo ) := PADR( M->&( cCampo ), GetSX3Cache(cCampo,"X3_TAMANHO") )
          EndIf
          If M->&( cCampo ) == Nil
             lInit := .F.
          EndIf
       EndIf
       If !lInit
          M->&( cCampo ) := FieldGet( nI )
          If ValType( M->&( cCampo ) ) = "C"
             M->&( cCampo ) := Space( Len( M->&( cCampo ) ) )
          ElseIf ValType( M->&( cCampo ) ) = "N"
             M->&( cCampo ) := 0
          ElseIf ValType( M->&( cCampo ) ) = "D"
             M->&( cCampo ) := CtoD( "  /  /  " )
          ElseIf ValType( M->&( cCampo ) ) = "L"
             M->&( cCampo ) := .f.
          EndIf
       EndIf
	Next nI
	M->QAA_FILIAL:= xFilial("QAA")
Else
   For nI := 1 To FCount()
       M->&( Eval( bCampo, nI ) ) := FieldGet( nI )
   Next nI
EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥Variavel utilizada para bloquear os campos que nao podem ser alterados. ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lIntGPE .And. (INCLUI .Or. M->QAA_TPUSR == "1")
	lAltUsr:= .T.
EndIf

If lRet
//	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 000,000 TO 385,625 PIXEL // "Responsaveis/Usuarios"
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 000,000 To aMsSize[6]-40,aMsSize[5]-350  OF oMainWnd PIXEL

	oDlg:lMaximized := .T.

	oEnchoice := Msmget():New("QAA",nReg,nOpc,,,,,{014,002,190,312})

	oEnchoice:oBox:Align := CONTROL_ALIGN_ALLCLIENT

	If nOpc == 2 .Or. nOpc == 5
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{ || If(nOpc == 5,If(QNC070Dele(),oDlg:End(),.F.),oDlg:End() )},{|| oDlg:End()}) CENTERED
	ElseIf nOpc == 3 .Or. nOpc == 4
	    If !lTMKPMS
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(QNC070TOK(lTMKPMS),(QNC070GrUs(nOpc),nOpcao := 1,oDlg:End()),)},{|| oDlg:End()}) CENTERED
		Else
			If nOpc == 4
				cRecurso  := QAA->QAA_RECUR
				cParticip := QAA->QAA_PARTIC
				cOperador := QAA->QAA_OPERAD
			Endif
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| If(QNC070TOK(lTMKPMS),(QNC070GrUs(nOpc,cParticip,cRecurso,cOperador),nOpcao := 1,oDlg:End()),)},{|| oDlg:End()}) CENTERED
		Endif
	EndIf

	If !lUsrInat
		Set Filter To
	Else
		MsgRun(OemToAnsi(STR0047),OemToAnsi(STR0044),{||Q070Filtro()}) //"Selecionando Usu†rios" ### "Aguarde..."
	Endif

	While (GetSX8Len() > nSaveSx8)
		If nOpcao == 1
		   	ConfirmSX8()
		Else
			RollBackSX8()
		Endif
	Enddo

Endif

Return

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥FunáÖo    ≥QNC070GrUs≥ Autor ≥ Eduardo de Souza      ≥ Data ≥ 05/09/02 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥DescriáÖo ≥ Grava Usuarios                                             ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ QNC070GrUs(ExpN1)                                          ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametros≥ ExpN1 - Opcao do Browse                                    ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070GrUs(nOpc,cParticip,cRecurso,cOperador)

Local lRecLock:= .F.
Local nI      := 0
Local lTMKPMS:= If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local lRetAE8 := .T.
Local lRetSU7 := .T.
Local lRetALL := .F.

Default cParticip  := ""
Default cRecurso   := ""
Default cOperador  := ""

If nOpc == 3
	lRecLock:= .T.
EndIf

Begin Transaction

	If lTMKPMS
        cParticip := M->QAA_PARTIC
       	cRecurso  := M->QAA_RECUR
	   	cOperador := M->QAA_OPERAD

        lRetALL := QN070Chk(cParticip,cRecurso,cOperador)

        If lRetALL
	        If !lIntGPE
				QNC070RDZ(cParticip,"QAA",xFilial("QAA")+M->QAA_MAT, 1,cEmpAnt,cFilAnt,M->QAA_PARTIC)
			Else
				//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒxø
				//≥Qdo o codigo do usuario È gerado via integraÁ„o, o seu cÛdigo    ≥
				//≥ja consta no formato Empresa+Filial+codigo.		            	≥
				//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒxŸ
				QNC070RDZ(cParticip,"QAA",xFilial("QAA")+M->QAA_MAT,1,cEmpAnt,cFilAnt,M->QAA_PARTIC)
			Endif

			lRetAE8 := QN070ChkAE8(cRecurso)
			If lRetAE8
				QNC070RDZ(cParticip,"AE8",xFilial("AE8")+cRecurso, 1,cEmpAnt,cFilAnt,M->QAA_RECUR)
			Else
				MsgAlert("Este recurso encontra-se relacionado a outro usu·rio","Aviso")
				lRet := .F.
			EndIf

			lRetSU7 := QN070ChkSU7(cOperador)
			If lRetSU7
				QNC070RDZ(cParticip,"SU7",xFilial("SU7")+cOperador ,1 ,cEmpAnt    ,cFilAnt,M->QAA_OPERAD)
			Else
				MsgAlert("Este operador encontra-se relacionado a outro usu·rio","Aviso")
				lRet := .F.
			EndIf

			DbSelectArea("AE8")
			DbSetOrder(1)

			If AE8->(DbSeek(xFilial("AE8")+cRecurso))
				If Empty(AE8->AE8_CODRD0)
					RecLock("AE8",.F.)
					AE8->AE8_CODRD0 := cRecurso
					MsUnLock()
				EndIf
			EndIf
	  	EndIf
	Endif

If valAltFunc()==.T.
EndIf

	DbSelectArea("QAA")
	M->QAA_LOGIN:= UPPER(M->QAA_LOGIN)
	RecLock("QAA",lRecLock)
		For nI := 1 TO FCount()
			FieldPut(nI,M->&(Eval(bCampo,nI)))
		Next nI
	MsUnLock()

	QNCATULEG(QAA->QAA_FILIAL,QAA->QAA_MAT)//Atualiza legenda do usu·rios na alteraÁ„o.

End Transaction

Return

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥ QNC070Dele ≥ Autor ≥ Eduardo de Souza    ≥ Data ≥ 05/09/02 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Exclusao de registros do Cadastro de Usuarios              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ QNC070Dele()                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070Dele()

Local cChaveMat := AllTrim(SubStr(QAA->QAA_MAT, Len(cEmpAnt)+1, TAMSX3("QAA_MAT")[1])) // Da posiÁ„o do tamanho da empresa + 1 atÈ o tamanho do cÛdigo do usu·rio
Local cEmpReg   := SubStr(QAA->QAA_MAT, 1, Len(cEmpAnt)) // Da primeira posiÁ„o atÈ o tamanho da empresa
Local cFilReg   := SubStr(QAA->QAA_MAT, Len(cEmpAnt)+1, FWSizeFilial()) // Da posiÁ„o do tamanho da empresa + 1 atÈ o tamanho da filial
Local cTMKPMS   := Str(GetNewPar("MV_QTMKPMS",1),1)
Local lRet      := .F.
Local lRetPMS   := .T.
Local lRetTMK   := .T.
Local lTMKPMS   := If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.) //Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local nOrdQI2   := 0
Local nOrdQI3   := 0
Local nOrdQI4   := 0
Local nOrdQI5   := 0
Local nOrdQIE   := 0
Local nOrdQIF   := 0
Local nRetorno  := 0

If lIntGpe .And. M->QAA_TPUSR == "1" .And. !lTMKPMS

	// Verifica se o usu·rio(QAA) existe na SRA(Gest„o de Pessoal)
	If cEmpReg+cFilReg <> cEmpAnt+cFilAnt
		nRetorno := StartJob("LOCALIZSRA", GetEnvServer(), .T.,cEmpReg, cFilReg, cChaveMat, cModulo)
		
		If nRetorno == 0 // Se a tabela SRA N√O foi localizada ou ocorreu falha na abertura do ambiente 
			// STR0051 - "N„o foi possÌvel encontrar o arquivo: " + "SRA"
			Help(NIL, NIL, cEmpReg+cFilReg, NIL, STR0051+"SRA", 1, 0, NIL, NIL, NIL, NIL, NIL, {""})
			Return .F.

		ElseIf nRetorno == 1 // Se o usu·rio existir na SRA, n„o exclui o usu·rio do QAA
			lRet := .T.
		Endif
	Else
		dbSelectArea("SRA")
		dbSetOrder(1) // Filial + MatrÌcula
		If SRA->(dbSeek(cChaveMat))
			lRet := .T.
		Endif
	Endif

Endif

If !lRet .And. (!lIntGpe .Or. M->QAA_TPUSR <> "1")
	nOrdQI2:= QI2->(IndexOrd())
	nOrdQI3:= QI3->(IndexOrd())
	nOrdQI4:= QI4->(IndexOrd())
	nOrdQI5:= QI5->(IndexOrd())
	nOrdQIE:= QIE->(IndexOrd())
	nOrdQIF:= QI5->(IndexOrd())

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ VALIDACAO DO QNC QUANDO EXISTIR INTEGRACAO TMK X PMS X QNC ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	If cTMKPMS $ "2|3|4"
		lRetTMK := If(cTMKPMS $ "2|4" ,Tk090IntDel(QAA->QAA_PARTIC),.T.)
		lRetPMS := If(cTMKPMS $ "3|4" ,PMSDelRec(QAA->QAA_PARTIC),.T.)
	EndIf

	MsgRun(OemToAnsi(STR0045),OemToAnsi(STR0046),{|| lRet:= Iif(FindFunction("QAAValExc"), QAAValExc(), .F.) }) // "Validando Exclusao de Usuarios..." ### "Aguarde..."
	If lRet
		Begin Transaction
			If RecLock("QAA",.F.)
				QAA->(DbDelete())
				MsUnlock()
				QAA->(DbSkip())
			Endif
		End Transaction
	EndIf
	QI2->(DbSetOrder(nOrdQI2))
	QI3->(DbSetOrder(nOrdQI3))
	QI4->(DbSetOrder(nOrdQI4))
	QI5->(DbSetOrder(nOrdQI5))
	QIE->(DbSetOrder(nOrdQIE))
	QIF->(DbSetOrder(nOrdQIF))
Else
  	Help(" ",1,"QX10EXGPE") // "O Usuario somente podera ser excluido pelo modulo Gestao de Pessoal."
EndIf

Return lRet

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥ QNC070VExc ≥ Autor ≥ Eduardo de Souza    ≥ Data ≥ 05/09/02 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Valida Exclusao de Usuarios                                ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe   ≥ QNC070VExc()                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070                                                    ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070VExc()

Local nOrd01  := 0
Local cIndex  := ""
Local cKey    := ""
Local cFiltro := ""
Local lApaga  := .T.
Local QTD	  := 0

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ CADASTRO DAS NAO CONFORMIDADES                  ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga

	cAlias := GetNextAlias()
	cQuery := "Select Count (*)  QTD"
	cQuery += " From " + RetSqlName("QI2") + " QI2 "
	cQuery += " Where QI2.QI2_FILMAT = '" + QAA->QAA_FILIAL + "'
	cQuery += " AND QI2.QI2_MAT = '" + QAA->QAA_MAT + "'
	cQuery += " OR (QI2.QI2_FILRES = '" + QAA->QAA_FILIAL + "'
	cQuery += " AND QI2.QI2_MATRES = '" + QAA->QAA_MAT + "' )
	cQuery += " AND QI2.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAlias, .F., .T.)

	If (cAlias)->QTD > 0
		lApaga := .F.
	Endif

	dbSelectArea(cAlias)
	dbCloseArea()

EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ CADASTRO DAS ACOES                              ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga
	cQuery := "Select Count (*)  QTD"
	cQuery += " From " + RetSqlName("QI3") + " QI3 "
	cQuery += " Where QI3.QI3_FILMAT = '" + QAA->QAA_FILIAL + "' and "
	cQuery += "       QI3.QI3_MAT = '" + QAA->QAA_MAT + "' and "
	cQuery += "       QI3.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)

	If Qtd > 0
		lApaga := .F.
	Endif
EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ ACAO CORRETIVA X EQUIPE                         ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga
	nOrd01:= QI4->(IndexOrd())
	DbSelectarea("QI4")
	cIndex  := CriaTrab(Nil,.F.)
	cKey    := "QI4_FILMAT+QI4_MAT"
	cFiltro := "QI4->QI4_FILMAT+QI4->QI4_MAT == '"+QAA->QAA_FILIAL+QAA->QAA_MAT+"'"
	IndRegua("QI4",cIndex,cKey,,cFiltro,OemToAnsi(STR0045)) //"Validando Exclusao..."
	If QI4->(!Eof())
		lApaga:= .F.
	EndIf
	RetIndex("QI4")
	Set Filter to
	cIndex += OrDbagExt()
	Delete File &(cIndex)
	QI4->(DbSetOrder(nOrd01))
EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ ACAO CORRETIVA X ACOES                          ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga
	cQuery := "Select Count (*)  QTD"
	cQuery += " From " + RetSqlName("QI5") + " QI5 "
	cQuery += " Where QI5.QI5_FILMAT = '" + QAA->QAA_FILIAL + "' and "
	cQuery += "       QI5.QI5_MAT = '" + QAA->QAA_MAT + "' and "
	cQuery += "       QI5.D_E_L_E_T_ = ' '"

	cQuery := ChangeQuery(cQuery)

	If Qtd > 0
		lApaga := .F.
	Endif
EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ DOCUMENTOS ANEXO PLANO/ETAPAS                   ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga
	nOrd01:= QIE->(IndexOrd())
	DbSelectarea("QIE")
	cIndex  := CriaTrab(Nil,.F.)
	cKey    := "QIE_FILMAT+QIE_MAT"
	cFiltro := "QIE->QIE_FILMAT+QIE->QIE_MAT == '"+QAA->QAA_FILIAL+QAA->QAA_MAT+"'"
	IndRegua("QIE",cIndex,cKey,,cFiltro,OemToAnsi(STR0045)) //"Validando Exclusao..."
	If QIE->(!Eof())
		lApaga:= .F.
	EndIf
	RetIndex("QIE")
	Set Filter to
	cIndex += OrDbagExt()
	Delete File &(cIndex)
	QIE->(DbSetOrder(nOrd01))
EndIf

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ DOCUMENTOS ANEXO FICHAS                         ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If lApaga
	nOrd01:= QIF->(IndexOrd())
	DbSelectarea("QIF")
	cIndex  := CriaTrab(Nil,.F.)
	cKey    := "QIF_FILMAT+QIF_MAT"
	cFiltro := "QIF->QIF_FILMAT+QIF->QIF_MAT == '"+QAA->QAA_FILIAL+QAA->QAA_MAT+"'"
	IndRegua("QIF",cIndex,cKey,,cFiltro,OemToAnsi(STR0045)) //"Validando Exclusao..."
	If QIF->(!Eof())
		lApaga:= .F.
	EndIf
	RetIndex("QIF")
	Set Filter to
	cIndex += OrDbagExt()
	Delete File &(cIndex)
	QIF->(DbSetOrder(nOrd01))
EndIf

Return lApaga

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥Q070Filtro ≥ Autor ≥Aldo Marini Junior    ≥ Data ≥31/08/2002≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Filtra os Usuarios/Responsaveis Inativos                   ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥ Q070Filtro()                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070													  ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function Q070Filtro()
Local cFiltro := Qa_FilSitF() //"ativo"

oBrowse:SetFilterDefault(cFiltro)

Return(NIL)

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥QNCA070Vrf ≥ Autor ≥ Aldo Marini Junior   ≥ Data ≥31/08/2002≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥ Filtra alternadamente Usuarios Inativos e/ou Ativos        ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥ QNCA070Vrf()                                               ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥ Uso      ≥ QNCA070Vrf                                                 ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function QNCA070Vrf()
lUsrInat:= !lUsrInat

If !lUsrInat
	oBrowse:SetFilterDefault("QAA->QAA_STATUS <> '*'")
Else
	MsgRun(OemToAnsi(STR0047),OemToAnsi(STR0044),{||Q070Filtro()}) //"Selecionando Usu†rios" ### "Aguarde..."
Endif

Return(NIL)

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070FIM ∫Autor  ≥Telso Carneiro      ∫ Data ≥  16/09/2004 ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Verifica e Libera a TransferÍncia                          ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNCA070TRF()                                               ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Static Function QNC070FIM(alQI5,alQI3,alQI2,oDlg,lChk02,cFilQNC,cUsuQNC)
Local lRet		:=.F.
Local nI 		:= 0
Local oDlgC
Local oSize
Local aCof		:= {}
Local oCof
Local cCof
Local cFilDe	:= Space(FWSizeFilial())
Local cMatDe	:= ""
Local cFilPara	:= Space(FWSizeFilial())
Local cMatPara	:= ""
Local lTemQI2	:= .F.
Local lTemQI3	:= .F.
Local lTemQI5	:= .F.
Local cTMKPMS	:= Str(GetNewPar("MV_QTMKPMS",1),1)

Default lChk02  := .F.

For nI := 1 to Len(alQI5)
	IF alQI5[nI,1]
		lTemQI5:=.T.
		Exit
	Endif
Next
For nI := 1 to Len(alQI3)
	IF alQI3[nI,1]
		lTemQI3:=.T.
		Exit
	Endif
Next
For nI := 1 to Len(alQI2)
	IF alQI2[nI,1]
		lTemQI2:=.T.
		Exit
	Endif
Next

IF lTemQI2 .OR. lTemQI3 .OR. lTemQI5
	IF lTemQI5
		If lChk02
			cGrupo 	 := QNC070Grp()
		EndIf
		For nI :=1 TO Len(alQI5 )
			If (alQI5[nI,1])
				If cTMKPMS > "1"
					QI5->(dbGoTo(alQI5[nI,17])) //Recno
				Else
					QI5->(dbGoTo(alQI5[nI,9])) //Recno
				EndIf
				If ((cTMKPMS > "1") .AND.(lChk02 = .T.))

					cGrpDe   := alQI5[nI][14]

					AADD(aCof,{aTitPend[Ascan(aPagPend,"ETA")] ,;
					alQI5[nI,2],;	                    //Filial
					Transform(alQI5[nI,3],PesqPict("QI5","QI5_CODIGO"))+"  "+alQI5[nI,4],; // Codigo-Revisao Plano de Acao
					cGrpDe,; 		//Grupo De
					cGrupo}) 			//Grupo Para
				Else
					cFilDe	:= cFilQNC
					cMatDe	:= cUsuQNC
					cFilPara:= SubStr(alQI5[nI,len(alQI5[nI])],1,FWSizeFilial())
					cMatPara:= SubStr(alQI5[nI,len(alQI5[nI])],FWSizeFilial()+1,TamSX3("QI5_MAT")[1])

					QnTrfPMS (cMatDe,alQI5[nI])
				   	AADD(aCof,{aTitPend[Ascan(aPagPend,"ETA")] ,;
					alQI5[nI,2],;	                    //Filial
					Transform(alQI5[nI,3],PesqPict("QI5","QI5_CODIGO"))+"  "+alQI5[nI,4],; // Codigo-Revisao Plano de Acao
					cFilDe+"-"+cMatDe+" "+QA_NUSR(cFilDe,cMatDe),; 			//DE
					cFilPara+"-"+cMatPara+" "+QA_NUSR(cFilPara,cMatPara)}) //PARA
				EndIf
			Endif
		Next
	Endif
	IF lTemQI3
		For nI :=1 TO Len(alQI3 )
			If (alQI3[nI,1])
				QI3->(dbGoTo(alQI3[nI,9])) //Recno
				cFilDe	:= QI3->QI3_FILMAT
				cMatDe	:= QI3->QI3_MAT
				cFilPara:= SubStr(alQI3[nI,len(alQI3[nI])],1,FWSizeFilial())
				cMatPara:= SubStr(alQI3[nI,len(alQI3[nI])],FWSizeFilial()+1,TamSX3("QI5_MAT")[1])

				AADD(aCof,{aTitPend[Ascan(aPagPend,"PLN")],;
				alQI3[nI,2],;	                    //Filial
				Transform(alQI3[nI,3],PesqPict("QI3","QI3_CODIGO"))+"  "+alQI3[nI,4],; // Codigo-Revisao Plano de Acao
				cFilDe+"-"+cMatDe+" "+QA_NUSR(cFilDe,cMatDe),; 			//DE
				cFilPara+"-"+cMatPara+" "+QA_NUSR(cFilPara,cMatPara)}) //PARA
			Endif
		Next
	Endif
	IF lTemQI2
		For nI :=1 TO Len(alQI2 )
			If (alQI2[nI,1])
				QI2->(dbGoTo(alQI2[nI,7]))
				cFilDe	:= QI2->QI2_FILRES
				cMatDe	:= QI2->QI2_MATRES
				cFilPara:= SubStr(alQI2[nI,len(alQI2[nI])],1,FWSizeFilial())
				cMatPara:= SubStr(alQI2[nI,len(alQI2[nI])],FWSizeFilial()+1,TamSX3("QI5_MAT")[1])

				AADD(aCof,{aTitPend[Ascan(aPagPend,"FNC")],;
				alQI2[nI,2],;	                    //Filial
				Transform(alQI2[nI,4],PesqPict("QI2","QI2_FNC"))+"  "+alQI2[nI,5],; // Codigo-Revisao FNC
				cFilDe+"-"+cMatDe+" "+QA_NUSR(cFilDe,cMatDe),; 			//DE
				cFilPara+"-"+cMatPara+" "+QA_NUSR(cFilPara,cMatPara)}) //PARA
			Endif
		Next
	Endif

oSize := FwDefSize():New(.T.)

oSize:AddObject( "CABECALHO"	,  100, 100, .T.,.T.)

oSize:aMargins := { 3, 3, 0, 3 }

oSize:Process()

DEFINE MSDIALOG oDlgC TITLE OemToAnsi(STR0056) ; //"ConfirmaÁ„o de TransferÍncias"
						FROM oSize:aWindSize[1],oSize:aWindSize[2] TO oSize:aWindSize[3],oSize:aWindSize[4] OF oMainWnd PIXEL

	If cTMKPMS > "1" .AND. lChk02
			@oSize:GetDimension('CABECALHO'  , 'LININI'), oSize:GetDimension('CABECALHO'  , 'COLINI');
			LISTBOX oCof VAR cCof FIELDS HEADER ;
			OemToAnsi(STR0021),; //"Tipos de PendÍncias"
			OemToAnsi(STR0024),; //"Filial"
			OemToAnsi(STR0025),; //"CÛdigo"
			OemToAnsi("Do Grupo de Etapas"),;   //"TransferÍncias realizadas (DE): "
			OemToAnsi("Para o Grupo de Etapas");    //"TransferÍncias realizadas (PARA): "
		    COLSIZES NIL,NIL,NIL,120,120 ;
			SIZE oSize:GetDimension('CABECALHO'  , 'XSIZE'), oSize:GetDimension('CABECALHO'  , 'YSIZE') OF oDlgC PIXEL

		oCof:SetArray(aCof)
		oCof:bLine := {|| aCof[oCof:nAt] }

	Else
		@oSize:GetDimension('CABECALHO'  , 'LININI'), oSize:GetDimension('CABECALHO'  , 'COLINI');
			LISTBOX oCof VAR cCof FIELDS HEADER ;
			OemToAnsi(STR0021),; //"Tipos de Pendencias"
			OemToAnsi(STR0024),; //"Filial"
			OemToAnsi(STR0025),; //"CÛdigo"
			OemToAnsi(STR0057),;   //"TransferÍncias realizadas (DE): "
			OemToAnsi(STR0058);    //"TransferÍncias realizadas (PARA): "
		    COLSIZES NIL,NIL,NIL,120,120 ;
			SIZE oSize:GetDimension('CABECALHO'  , 'XSIZE'), oSize:GetDimension('CABECALHO'  , 'YSIZE') OF oDlgC PIXEL

		oCof:SetArray(aCof)
		oCof:bLine := {|| aCof[oCof:nAt] }
	EndIf

	ACTIVATE MSDIALOG oDlgC CENTERED ON INIT EnchoiceBar(oDlgC,{|| lRet:=.T.,oDlgC:End()},{|| lRet:= .F.,oDlgC:End()} )

	IF lRet
		lRet:= MsgYesNo(OemToAnsi(STR0049),OemToAnsi(STR0050)) .And. QNC070Jus(oDlg)  //"Confirma Transferencia de Lancamentos Pendentes ?"###"Atencao"
	Endif
Endif

Return(lRet)

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥QNC070Jus  ≥ Autor ≥Telso Carneiro        ≥ Data ≥16/09/2004≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥Tela de Justificativa (Motivo)                              ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥QNC070Jus(oDlg) 											  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Uso		 ≥QNCA070TRF()                                                ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function QNC070Jus(oDlg)

Local oMotTransf
Local lGrava	:= .F.
Local oDlgTransf

DEFINE MSDIALOG oDlgTransf TITLE OemToAnsi(STR0059) FROM 150,000 TO 230,280 OF oDlg PIXEL    //"Motivo da TransferÍncia"

@ 005,005 MSGET oMotTransf VAR cMotTransf SIZE 130,010 OF oDlgTransf PIXEL

DEFINE SBUTTON FROM 021,075 TYPE 1 ENABLE OF oDlgTransf;
ACTION  (If(NaoVazio(cMotTransf),(lGrava := .T.,oDlgTransf:End()),));

DEFINE SBUTTON FROM 021,105 TYPE 2 ENABLE OF oDlgTransf;
ACTION  (lGrava := .F.,oDlgTransf:end());

ACTIVATE MSDIALOG oDlgTransf CENTERED

Return lGrava

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070PUS ∫Autor  ≥Telso Carneiro      ∫ Data ≥ 27/09/2004  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Tela de Pesquisa do Usuario para definir a Filial/Codigo    ∫±±
±±∫          ≥												              ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥QNCA070TRF                                                  ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Static Function QNC070PUS()

Local cCodUsr := Space(TamSx3("QAA_MAT" )[1])
Local cDesUsr := Space(TamSx3("QAA_NOME")[1])
Local cFilUsr := xFilial("QAA")
Local lRet    := .T.
Local oCodUsr := Nil
Local oDesUsr := Nil
Local oDlgU   := Nil
Local oFilUsr := Nil

DEFINE MSDIALOG oDlgU FROM 000,000 TO 150,470 TITLE OemToAnsi(STR0001) PIXEL // "Pesquisar"

@ 035,003 TO 070,233 LABEL OemToAnsi(STR0020) OF oDlgU PIXEL // "Usu·rio"

@ 045,006 SAY OemToAnsi(STR0024) SIZE 012,010 OF oDlgU PIXEL // "Filial"
@ 055,006 MSGET oFilUsr VAR cFilUsr PICTURE Repl("@!",FWSizeFilial()) F3 "SM0" SIZE 055,008 OF oDlgU PIXEL ;
VALID QA_CHKFIL(cFilUsr,@cFilMat)

@ 045,070 SAY OemToAnsi(STR0025) SIZE 044,008 OF oDlgU PIXEL // "CÛdigo"
@ 055,070 MSGET oCodUsr VAR cCodUsr PICTURE '@!' F3 "QDE" SIZE 044,008 OF oDlgU PIXEL ;
VALID (cDesUsr:= QA_NUSR(cFilUsr,cCodUsr,.T.),	oDesUsr:Refresh(),QA_CHKMAT(cFilUsr,cCodUsr))

@ 045,125 SAY OemToAnsi(STR0026) SIZE 085,008 OF oDlgU PIXEL // "Nome"
@ 055,125 MSGET oDesUsr VAR cDesUsr SIZE 085,008 OF oDlgU PIXEL WHEN .f.

ACTIVATE MSDIALOG oDlgU CENTERED ON INIT EnchoiceBar(oDlgU,{|| lRet:=.T., oDlgU:End()},{|| lRet:=.F., oDlgU:End()} )

Return(lRet)

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070Pen ∫Autor  ≥Telso Carneiro      ∫ Data ≥ 27/09/2004  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥Carrega nos Arrays as Pendencias de Transferencias QAB      ∫±±
±±∫          ≥e pendencias Atuais de FNC e/ou Planos de Acao e/ou Etapas  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥QNCA070TRF()                                                ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

STATIC Function QNC070Pen(aQI5,aQI3,aQI2,cFiltUsr,lChk03,cFilQNC, cUsuQNC,oQI5)

Local nIs		:= 0
Local lRet		:= .T.
Local cFiltro   := ""
Local aBuscaPed := {}
Local aRecTrf   := {}
Local cFilAtu   := Space(FWSizeFilial())
Local cMatAtu   := ""
Local aQI2Sit   := {}
Local aQI3Sit   := {}
Local aQI2Pri   := {}
Local aQTipo    := {}
Local cQuery	:= ""
Local cQryQI2	:= ""
Local cQryQI3	:= ""
Local cAlias	:= "QI5"
Local cAlias1	:= ""
Local cAliasQI2 := "QI2"
Local cAliasQI3 := "QI3"
Local cQueryInt	:= ""
Local lFiltro	:= .F.
Local lTMKPMS	:= If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local bLine     := If(lChk03 .AND. oQI5 != Nil, oQI5:bLine , {|| .T.} )
Local lDBF		:= .F.

Default lChk03   := .F.
Default cFilQNC	 := Space(FWSizeFilial())
Default cUsuQNC	 := ""
Default cFiltUsr := ""

Default aQI2 := {}
Default aQI3 := {}
Default aQI5 := {}

QNCCBOX("QI2_STATUS",@aQI2Sit)
QNCCBOX("QI3_STATUS",@aQI3Sit)
QNCCBOX("QI2_PRIORI",@aQI2Pri)
QNCCBOX("QI3_TIPO"  ,@aQTipo)

ProcRegua(2)

IncProc()

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Carrega os Lactos de Transferencia e QAB                     ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
cFilAtu	 := cFilQNC //QAA->QAA_FILIAL
cMatAtu  :=	cUsuQNC //QAA->QAA_MAT
aAdd(aBuscaPed,{cFilAtu,cMatAtu})

DbSelectArea("QAB")
DbSetOrder(2)

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥ Carrega os Lactos de Transferencia e Matricula atual         ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
If QAB->(DbSeek(cFilAtu+cMatAtu))
	While QAB->(!Eof()) .And. QAB->QAB_FILP + QAB->QAB_MATP == cFilAtu + cMatAtu
		If Ascan(aRecTrf,QAB->(Recno())) > 0
			QAB->(DbSkip())
			Loop
		Else
			Aadd(aRecTrf,QAB->(Recno()))
		Endif
		If QAB->QAB_FILP+QAB->QAB_MATP == cFilAtu+cMatAtu
			If QAB->QAB_FILP+QAB->QAB_MATP <> QAB->QAB_FILD+QAB->QAB_MATD
				If Ascan(aBuscaPed,{|X| X[1]+X[2] == QAB->QAB_FILD+QAB->QAB_MATD}) == 0
					Aadd(aBuscaPed,{QAB->QAB_FILD,QAB->QAB_MATD})
				Endif
			EndIf
			cFilAtu := QAB->QAB_FILD
			cMatAtu := QAB->QAB_MATD
			QAB->(DbSeek(cFilAtu+cMatAtu))
		Else
			QAB->(DbSkip())
		EndIf
	EndDo
EndIf

IncProc()

cFilAtu	 := cFilQNC //QAA->QAA_FILIAL
cMatAtu  :=	cUsuQNC //QAA->QAA_MAT

For nIs:=1 TO Len(aBuscaPed)

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Carrega as pendencias no Array (campos) - Ficha Ocorrencia/Nao-conformidade ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ

  	cAliasQI2 := GetNextAlias()
	cQryQI2 := "SELECT QI2_FILIAL,QI2_STATUS,QI2_ANO,QI2_FNC,QI2_REV,QI2_DESCR,QI2_PRIORI,QI2_OCORRE,QI2_CONPRE,R_E_C_N_O_ QI2RECNO "
	cQryQI2 += "FROM " + RetSqlName("QI2") + " QI2 "
	cQryQI2 += "WHERE "
	cQryQI2 += " QI2_FILRES = '"+aBuscaPed[nIs,1]+"' AND "
	cQryQI2 += " QI2_MATRES = '"+aBuscaPed[nIs,2]+"' AND "
	cQryQI2 += " QI2_CONREA = ' ' AND "
	cQryQI2 += " QI2.D_E_L_E_T_ = ' '"

	cQryQI2 := ChangeQuery(cQryQI2)
	dbUseArea( .T., "TOPCONN", TcGenQry(, , cQryQI2 ), cAliasQI2, .T., .T. )

	TcSetField(cAliasQI2,"QI2_OCORRE","D")
	TcSetField(cAliasQI2,"QI2_CONPRE","D")
	TcSetField(cAliasQI2,"QI2_CONREA","D")


	While (cAliasQI2)->(!Eof())
		If aBuscaPed[nIs,1]+aBuscaPed[nIs,2]<>cFilAtu+cMatAtu 	// Usuario Transf. QAB
			aAdd(aQI2,{.T.,;
			(cAliasQI2)->QI2_FILIAL,;							// Filial FNC
			aQI2Sit[Val((cAliasQI2)->QI2_STATUS)],;
			(cAliasQI2)->QI2_FNC,;
			(cAliasQI2)->QI2_REV,;
			(cAliasQI2)->QI2_DESCR,;
			IIF(!lDBF,(cAliasQI2)->QI2RECNO,(cAliasQI2)->(Recno())),;
			aQI2Pri[Val((cAliasQI2)->QI2_PRIORI)],;
			(cAliasQI2)->QI2_OCORRE,;
			(cAliasQI2)->QI2_CONPRE,;
			cFilAtu+cMatAtu})
			lRet:=.F.
		Else
			aAdd(aQI2,{.F.,;
			(cAliasQI2)->QI2_FILIAL,;							// Filial FNC
			aQI2Sit[Val((cAliasQI2)->QI2_STATUS)],;
			(cAliasQI2)->QI2_FNC,;
			(cAliasQI2)->QI2_REV,;
			(cAliasQI2)->QI2_DESCR,;
			IIF(!lDBF,(cAliasQI2)->QI2RECNO,(cAliasQI2)->(Recno())),;
			aQI2Pri[Val((cAliasQI2)->QI2_PRIORI)],;
			(cAliasQI2)->QI2_OCORRE,;
			(cAliasQI2)->QI2_CONPRE,;
			Space(12)})
		Endif
		(cAliasQI2)->(dbSkip())
	Enddo
	(cAliasQI2)->(DbCloseArea())
	DbSelectArea("QI2")
	dbSetOrder(1)

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Carrega as pendencias no Array (campos) - Plano de Acao      ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	cAliasQI3 := GetNextAlias()
	cQryQI3 := "SELECT QI3_FILIAL,QI3_MAT,QI3_CODIGO,QI3_REV,QI3_STATUS,QI3_ABERTU,QI3_ENCPRE,QI3_TIPO,R_E_C_N_O_ QI3RECNO "
	cQryQI3 += "FROM " + RetSqlName("QI3") + " QI3 "
	cQryQI3 += "WHERE QI3_FILMAT = '"+aBuscaPed[nIs,1]+"' AND "
	cQryQI3 += " QI3_MAT = '"+aBuscaPed[nIs,2]+"' AND "
	cQryQI3 += " QI3_ENCREA = ' ' AND "
	cQryQI3 += " QI3.D_E_L_E_T_ = ' '"

	cQryQI3 := ChangeQuery(cQryQI3)
	dbUseArea( .T., "TOPCONN", TcGenQry(, , cQryQI3 ), cAliasQI3, .T., .F. )

	TcSetField(cAliasQI3,"QI3_ABERTU","D")
	TcSetField(cAliasQI3,"QI3_ENCPRE","D")

	While (cAliasQI3)->(!Eof())
		If aBuscaPed[nIs,1]+aBuscaPed[nIs,2]<>cFilAtu+cMatAtu 	// Usuario Transf. QAB
			AADD( aQI3,{.T.,;
			(cAliasQI3)->QI3_FILIAL,;							// Filial Plano de Acao
			(cAliasQI3)->QI3_CODIGO,;							// Codigo Plano de Acao
			(cAliasQI3)->QI3_REV,;								// Revisao da Acao
			aQI3Sit[Val((cAliasQI3)->QI3_STATUS)],;			// Status da Acao
			(cAliasQI3)->QI3_ABERTU,;							// Data Abertura
			(cAliasQI3)->QI3_ENCPRE,;							// Encerramento Previsto
			aQTipo[Val((cAliasQI3)->QI3_TIPO)],;				// Tipo Acao
			IIF(!lDBF,(cAliasQI3)->QI3RECNO,(cAliasQI3)->(Recno())),;// Registro para controle
			cFilAtu+cMatAtu})                          			// Usuario transf (PARA)
			lRet:=.F.
		Else
			AADD( aQI3,{.F.,;
			(cAliasQI3)->QI3_FILIAL,;							// Filial Plano de Acao
			(cAliasQI3)->QI3_CODIGO,;							// Codigo Plano de Acao
			(cAliasQI3)->QI3_REV,;								// Revisao da Acao
			aQI3Sit[Val((cAliasQI3)->QI3_STATUS)],;			// Status da Acao
			(cAliasQI3)->QI3_ABERTU,;							// Data Abertura
			(cAliasQI3)->QI3_ENCPRE,;							// Encerramento Previsto
			aQTipo[Val((cAliasQI3)->QI3_TIPO)],;				// Tipo Acao
			IIF(!lDBF,(cAliasQI3)->QI3RECNO,(cAliasQI3)->(Recno())),;								// Registro para controle
			Space(12)})
		Endif
		DbSkip()
	Enddo
	(cAliasQI3)->(DbCloseArea())
	dbSelectArea("QI3")
	dbSetOrder(1)

	//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
	//≥ Carrega as pendencias no Array (campos)                      ≥
	//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
	dbSelectArea("QI5")
	dbSetOrder(2)
	dbSeek(aBuscaPed[nIs,1]+aBuscaPed[nIs,2])

	If lTMKPMS
		cQueryInt := ""
		cQueryInt += ",QI5_PROJET"
		cQueryInt += ",QI5_TAREFA"
	EndIf

	If Empty(cFiltUsr)
		cAlias := GetNextAlias()
		cFiltro := "SELECT QI5_FILMAT,QI5_MAT,QI5_PEND,QI5_FILIAL,QI5_CODIGO,QI5_REV,QI5_STATUS,QI5_TPACAO,QI5_PRAZO,QI5_DESCRE,R_E_C_N_O_ QI5RECNO"+cQueryInt+" "
		cFiltro += "FROM " + RetSqlName("QI5") + " QI5 "
		cFiltro += "WHERE "
		cFiltro += " QI5.QI5_FILMAT = '"+aBuscaPed[nIs,1]+"' AND "
		cFiltro += " QI5.QI5_MAT = '"   +aBuscaPed[nIs,2]+"' AND "
		cFiltro += " QI5.D_E_L_E_T_ = ' '"
		cFiltro := ChangeQuery(cFiltro)
		dbUseArea( .T., "TOPCONN", TcGenQry(, , cFiltro ), cAlias, .T., .F. )
		TcSetField(cAlias,"QI5_PRAZO" ,"D")
		TcSetField(cAlias,"QI5_REALIZ","D")
	Else
		cAlias := GetNextAlias()
		cFiltro := "SELECT QI5_FILIAL,QI5_CODIGO,QI5_REV,QI5_STATUS,QI5_TPACAO,QI5_PRAZO,QI5_FILMAT,QI5_MAT,QI5_DESCRE,QI5.R_E_C_N_O_ QI5RECNO"+cQueryInt+" "
		cFiltro += "FROM " + RetSqlName("QI2") + " QI2, " + RetSqlName("QI5") + " QI5 "
		cFiltro += "WHERE (" + cFiltUsr + ") AND "
		cFiltro += " QI2_CODACA = QI5_CODIGO AND "
		cFiltro += " QI5.QI5_FILIAL = '" +xFilial("QI5")+ "' AND "
		cFiltro += " QI2.QI2_FILIAL = '" +xFilial("QI2")+ "' AND "
		cFiltro += " QI5.QI5_MAT = '"+AllTrim(cUsuQNC)+"' AND "
		cFiltro += " QI5.D_E_L_E_T_ = ' ' AND "
		cFiltro += " QI2.D_E_L_E_T_ = ' '"
		cFiltro := ChangeQuery(cFiltro)
		dbUseArea( .T., "TOPCONN", TcGenQry(, , cFiltro ), cAlias, .T., .F. )
		TcSetField(cAlias,"QI5_PRAZO" ,"D")
		TcSetField(cAlias,"QI5_REALIZ","D")
		lFiltro := .T.
	EndIf

	aQI5Aux := aClone(aQI5)
	aQI5 := {}

	While !(cAlias)->(Eof()) .And. (((cAlias)->QI5_FILMAT+(cAlias)->QI5_MAT == aBuscaPed[nIs,1]+aBuscaPed[nIs,2]).OR.!Empty(cFiltUsr) )
		If !((cAlias)->QI5_STATUS $ "4|5") .AND. If(lTMKPMS,!Empty((cAlias)->QI5_TAREFA),.T.)
			DbSelectArea("QI3")
			DbSetOrder(2)
			QI3->(DbSeek((cAlias)->QI5_FILIAL+(cAlias)->QI5_CODIGO+(cAlias)->QI5_REV))
			cGrpDe := QI3->QI3_MODELO
			If Empty(QI3->QI3_ENCREA)
				If lTMKPMS
						cQueryInt := ", QI2_NCHAMA "
						cAlias1 := GetNextAlias()
						cQuery := "SELECT DISTINCT QI2_FNC, QI2_REV, QI2_CODCAT, QI2_CODPRO "+cQueryInt
						cQuery += "FROM " + RetSqlName("QI2") + " QI2 "
						cQuery += "WHERE QI2.QI2_FILIAL  = '" + xFilial("QI2") + "' AND "
						cQuery += " QI2.D_E_L_E_T_ = ' ' AND "
						cQuery += " QI2.QI2_CODACA = '" + (cAlias)->QI5_CODIGO + "' "

						cQuery := ChangeQuery(cQuery)
						dbUseArea( .T., "TOPCONN", TcGenQry(, , cQuery ), cAlias1, .T., .F. )

						TcSetField(cAlias1,"QI5_PRAZO" ,"D")
						TcSetField(cAlias1,"QI5_REALIZ","D")

	                If ((aBuscaPed[nIs,1]+aBuscaPed[nIs,2]<>cFilAtu+cMatAtu) .AND. !lFiltro  ) // Usuario Transf. QAB
						AADD( aQI5,{.T.,;
						(cAlias)->QI5_FILIAL,;							// Filial Plano de Acao
						(cAlias)->QI5_CODIGO,;							// Codigo Plano de Acao
						(cAlias)->QI5_REV,;								// Revisao da Acao
						(cAlias1)->QI2_FNC,;							// Ficha de N„o Conformidade
						(cAlias1)->QI2_REV,;							// Revis„o da FNC
						(cAlias)->QI5_STATUS,;							// Status da Acao
						(cAlias)->QI5_TPACAO,;							// Tipo da Acao
						FQNCDTPACAO((cAlias)->QI5_TPACAO),;				// Desc Tipo da Acao
						(cAlias)->QI5_PRAZO,;							// Prazo/Vecto da Acao
						(cAlias1)->QI2_CODCAT,;							// Categoria da FNC
						(cAlias1)->QI2_CODPRO,;							// Produto da FNC
						(cAlias)->QI5_PROJET,;							// Projeto no qual est· alocado.
						cGrpDe,;										// Grupo ao qual o plano pertence
						(cAlias1)->QI2_NCHAMA,;							// Chamado
						(cAlias)->QI5_TAREFA,;
						Iif(lDBF,(cAlias)->(Recno()),(cAlias)->QI5RECNO),;// Registro para controle
						cFilAtu+cMatAtu})
						lRet:=.F.
					Else
						AADD( aQI5,{.F.,;
						(cAlias)->QI5_FILIAL,;							// Filial Plano de Acao
						(cAlias)->QI5_CODIGO,;							// Codigo Plano de Acao
						(cAlias)->QI5_REV,;								// Revisao da Acao
						(cAlias1)->QI2_FNC,;							// Ficha de N„o Conformidade
						(cAlias1)->QI2_REV,;							// Revis„o da FNC
						(cAlias)->QI5_STATUS,;							// Status da Acao
						(cAlias)->QI5_TPACAO,;							// Tipo da Acao
						FQNCDTPACAO((cAlias)->QI5_TPACAO),;				// Desc Tipo da Acao
						(cAlias)->QI5_PRAZO,;							// Prazo/Vecto da Acao
						(cAlias1)->QI2_CODCAT,;							// Categoria da FNC
						(cAlias1)->QI2_CODPRO,;							// Produto da FNC
						(cAlias)->QI5_PROJET,;							// Projeto no qual est· alocado.
						cGrpDe,;										// Grupo ao qual o plano pertence
						(cAlias1)->QI2_NCHAMA,;							// Chamado
						(cAlias)->QI5_TAREFA,;
						Iif(lDBF,(cAlias)->(Recno()),(cAlias)->QI5RECNO),;// Registro para controle
						Space(Len(cFilAtu+cMatAtu))})
					Endif
				Else
					If aBuscaPed[nIs,1]+aBuscaPed[nIs,2]<>cFilAtu+cMatAtu // Usuario Transf. QAB
						AADD( aQI5,{.T.,;
						(cAlias)->QI5_FILIAL,;							// Filial Plano de Acao
						(cAlias)->QI5_CODIGO,;							// Codigo Plano de Acao
						(cAlias)->QI5_REV,;								// Revisao da Acao
						(cAlias)->QI5_STATUS,;							// Status da Acao
						(cAlias)->QI5_TPACAO,;							// Tipo da Acao
						FQNCDTPACAO((cAlias)->QI5_TPACAO),;				// Desc Tipo da Acao
						(cAlias)->QI5_PRAZO,;							// Prazo/Vecto da Acao
						Iif(lDBF,(cAlias)->(Recno()),(cAlias)->QI5RECNO),;// Registro para controle
						(cAlias)->QI5_DESCRE,;							// Descricao Resumida
						cFilAtu+cMatAtu})
						lRet:=.F.
					Else
						AADD( aQI5,{.F.,;
						(cAlias)->QI5_FILIAL,;							// Filial Plano de Acao
						(cAlias)->QI5_CODIGO,;							// Codigo Plano de Acao
						(cAlias)->QI5_REV,;								// Revisao da Acao
						(cAlias)->QI5_STATUS,;							// Status da Acao
						(cAlias)->QI5_TPACAO,;							// Tipo da Acao
						FQNCDTPACAO((cAlias)->QI5_TPACAO),;				// Desc Tipo da Acao
						(cAlias)->QI5_PRAZO,;							// Prazo/Vecto da Acao
						Iif(lDBF,(cAlias)->(Recno()),(cAlias)->QI5RECNO),;// Registro para controle
						(cAlias)->QI5_DESCRE,;							// Descricao Resumida
						Space(12)})
					Endif
				EndIf
				If ((aBuscaPed[nIs,1]+aBuscaPed[nIs,2]<>cFilAtu+cMatAtu) .AND. !lFiltro  ) // Usuario Transf. QAB
					(cAlias1)->(DbCloseArea())
				EndIf
			EndIf
		Endif
		(cAlias)->(dbSkip())
	Enddo

	If !Empty(cFiltUsr)
		(cAlias)->(DbCloseArea())
	EndIf

	If lFiltro .AND. Len(aQI5)==0
		aQI5 := aClone(aQI5Aux)
	 EndIf

 	If lTMKPMS
	   	aQI5 := aSort(aQI5,,,{|x,y| DtoS(x[10]) < DtoS(y[10]) })
	Else
		aQI5 := aSort(aQI5,,,{|x,y| DtoS(x[8]) < DtoS(y[8]) })
	EndIf

	If ValType(oQI5) == "O"
	 	If Len(aQI5) > 0
	 		oQI5:SetArray(aQI5)
			oQI5:bLine := bLine
			oQI5:Refresh()
			oFolder1:Refresh()
	 	EndIf
 	EndIf

	DbSelectArea("QI5")
	DbSetOrder(1)
Next

Return(lRet)

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070Grv ∫Autor  ≥Telso Carneiro      ∫ Data ≥ 22/09/2004  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥GRAVA os Arrays das Pendencias de Transferencias QAB        ∫±±
±±∫          ≥e pendencias Atuais de Auditorias/Agendas                   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥QAD015Atu()                                                 ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Static Function QNC070Grv(aQI5,aQI3,aQI2,lAltEtp,cFilQNC,cUsuQNC,cFiltUsr)

Local nI		:= 0
Local cSeqTrf	:= ""
Local cFilDe	:= Space(FWSizeFilial())
Local cMatDe	:= ""
Local cFilPara	:= Space(FWSizeFilial())
Local cMatPara	:= ""
Local lExtQIG	:= ChkFile("QIG")
Local aUsuarios := {}
Local aUsrMat	:= QA_USUARIO()
Local aUsr		:= {}
Local nArray        := 0
Local cDepto	:= ""
Local cEtapa	:= ""
Local cDesc 	:= ""
Local lTMKPMS  := If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local lTransf      := .F.
Local lQNCFICHA := ExistBlock( "QNCFICHA" )
Local lQNCRACAO := ExistBlock( "QNCRACAO" )
Local lQNCEACAO := ExistBlock( "QNCEACAO" )

Default lAltEtp := .F.
Default cFiltUsr := ""
CursorWait()
If lTMKPMS
	lTransf :=.T.
Endif

Begin Transaction

dbSelectArea("QAA")
dbSetOrder(1)

If lExtQIG
	cSeqTrf:= GetSxeNum("QIG","QIG_SEQTRF")
Endif

If lAltEtp      //Caso tenha alteraÁ„o de etapa
	DbSelectArea("QUO")
	DbSetOrder(1)
	If !Empty(cGrupo)
		If QUO->(DbSeek(xFilial("QUO")+AllTrim(cGrupo)))
			cDepto := QUO->QUO_DEPTO
			DbSelectArea("QUP")
			DbSetOrder(2)
			If QUP->(DbSeek(xFilial("QUO")+cGrupo))
				cEtapa := QUP->QUP_TPACAO
			Endif
			For nI := 1 To Len(aQI5)
				If aQI5[nI][1]
					DbSelectArea("QI2")
					DbSetOrder(2)
					QI2->(DbSeek(xFilial("QI2")+aQI5[nI][5]+aQI5[nI][6]))
				 	cDesc := MSMM(QI2->QI2_DDETA)
					cGrpDe   := aQI5[nI][14]
				   	cTpAcao := aQI5[nI][8]

					QNCGeraPlano(aQI5[nI][5],aQI5[nI][6],cDepto,cGrupo,aQI5[nI][3],aQI5[nI][4],cEtapa,aQI5[nI][8],.T.,,,,cDesc,lTransf,aQI5[nI])
				EndIf
			Next
		EndIf
	EndIf
Else
	dbSelectArea("QI2")
	For nArray := 1 to Len(aQI2)
		If (aQI2[nArray,1])
			dbGoTo(aQI2[nArray,7])
			cFilDe:= QI2->QI2_FILRES
			cMatDe:= QI2->QI2_MATRES

			RecLock("QI2",.F.)
			QI2->QI2_FILRES := SubStr(aQI2[nArray,Len(aQI2[nArray])],1,FWSizeFilial())
			QI2->QI2_MATRES := SubStr(aQI2[nArray,Len(aQI2[nArray])],FWSizeFilial()+1,TamSX3("QI2_MATRES")[1])
			MsUnLock()

			cFilPara:= QI2->QI2_FILRES
			cMatPara:= QI2->QI2_MATRES

			If Ascan(aUsr,{ |x| x[1]+x[2] == cFilPara+cMatPara })== 0
				Aadd(aUsr,{cFilPara,cMatPara})
			EndIf

			If lExtQIG
				//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
				//≥Log de Transferencia≥
				//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
				QNC070Log(QI2->QI2_FNC,QI2->QI2_REV,,,,"1",cSeqTrf,cMotTransf,cFilDe,cMatDe,cFilPara,cMatPara)
			Endif

			If QAA->(dbSeek(QI2->QI2_FILRES + QI2->QI2_MATRES )) .And. QAA->QAA_RECMAI == "1"
				cMail := AllTrim(QAA->QAA_EMAIL)

				If !Empty(cMail)

					cMsg := QNCSENDMAIL(1,OemToAnsi(STR0039))	// "Ficha de Ocorrencia/Nao-Conformidade (Transferencia de Lancamentos)."
					cAttach := ""
					aMsg:={{OemToAnsi(STR0040)+" "+TransForm(QI2->QI2_FNC,PesqPict("QI2","QI2_FNC"))+"-"+QI2->QI2_REV+Space(10)+DTOC(Date())+"-"+SubStr(TIME(),1,5) , cMsg, cAttach } }	// "Ocorrencia/Nao-conformidade No. "

					// Geracao de Mensagem
					IF lQNCFICHA 
						aMsg := ExecBlock( "QNCFICHA", .f., .f. )
					Endif

					aAdd(aUsuarios,{QAA->QAA_LOGIN, cMail, aMsg})

				Endif
			Endif
		Endif
	Next

	dbSelectArea("QI3")
	For nArray := 1 to Len(aQI3)
		If (aQI3[nArray,1])
			dbGoTo(aQI3[nArray,9])
			cFilDe:= QI3->QI3_FILMAT
			cMatDe:= QI3->QI3_MAT

			RecLock("QI3",.F.)
			QI3->QI3_FILMAT := SubStr(aQI3[nArray,Len(aQI3[nArray])],1,fwsizefilial())
			QI3->QI3_MAT    := SubStr(aQI3[nArray,Len(aQI3[nArray])],fwsizefilial()+1,TamSX3("QI3_MAT")[1])
			MsUnLock()

			cFilPara:= QI3->QI3_FILMAT
			cMatPara:= QI3->QI3_MAT

			If Ascan(aUsr,{ |x| x[1]+x[2] == cFilPara+cMatPara })== 0
				Aadd(aUsr,{cFilPara,cMatPara})
			EndIf

			If lExtQIG
				//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
				//≥Log de Transferencia≥
				//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
				QNC070Log(,,QI3->QI3_CODIGO,QI3->QI3_REV,,"2",cSeqTrf,cMotTransf,cFilDe,cMatDe,cFilPara,cMatPara)
			Endif

			If QAA->(dbSeek(QI3->QI3_FILMAT + QI3->QI3_MAT )) .And. QAA->QAA_RECMAI == "1"
				cMail := AllTrim(QAA->QAA_EMAIL)

				If !Empty(cMail)

					cMsg := QNCSENDMAIL(2,OemToAnsi(STR0041),.T.)	// "Plano de Acao (Transferencia de Lancamentos)"
					cAttach := ""
					aMsg:={{OemToAnsi(STR0042)+" "+TransForm(QI3->QI3_CODIGO,PesqPict("QI3","QI3_CODIGO"))+"-"+QI3->QI3_REV+Space(10)+DTOC(Date())+"-"+SubStr(TIME(),1,5), cMsg, cAttach} }	// "Plano de Acao No. "

					// Geracao de Mensagem para o Responsavel do Plano de Acao
					IF lQNCRACAO
						aMsg := ExecBlock( "QNCRACAO", .f., .f., { OemToAnsi(STR0041),.T. } ) // "Plano de Acao (Transferencia de Lancamentos)"
					Endif

					aAdd(aUsuarios,{QAA->QAA_LOGIN, cMail, aMsg} )
				Endif
			Endif
		Endif
	Next

	dbSelectArea("QI3")
	dbSetOrder(2)

	dbSelectArea("QI5")
	For nArray := 1 to Len(aQI5)
		If (aQI5[nArray,1])
			IF !Empty(cFiltUsr)
				QI5->(DbSetorder(4))  //QI5_FILIAL+QI5_CODIGO+QI5_REV+QI5_TPACAO
				If Dbseek(xFilial("QI5")+aQI5[nArray,3]+aQI5[nArray,4]+aQI5[nArray,8])
					cFilDe	     :=cFilQNC
					cMatDe   :=cUsuQNC

					RecLock("QI5",.F.)
						QI5->QI5_FILMAT := SubStr(aQI5[nArray,len(aQI5[nArray])],1,fwsizefilial())
						QI5->QI5_MAT    := SubStr(aQI5[nArray,len(aQI5[nArray])],fwsizefilial()+1,TamSX3("QI5_MAT")[1])
					MsUnLock()

					cFilPara:=QI5->QI5_FILMAT
					cMatPara:=QI5->QI5_MAT

					If Ascan(aUsr,{ |x| x[1]+x[2] == cFilPara+cMatPara })== 0
						Aadd(aUsr,{cFilPara,cMatPara})
					EndIf

				Endif
			Else
				If lTMKPMS
					dbGoTo(aQI5[nArray,17]) //Recno
				Else
					dbGoTo(aQI5[nArray,9]) //Recno
				EndIf
				cFilDe	     :=QI5->QI5_FILMAT
				cMatDe   :=QI5->QI5_MAT

				RecLock("QI5",.F.)
					QI5->QI5_FILMAT := SubStr(aQI5[nArray,len(aQI5[nArray])],1,fwsizefilial())
					QI5->QI5_MAT    := SubStr(aQI5[nArray,len(aQI5[nArray])],fwsizefilial()+1,TamSX3("QI5_MAT")[1])
				MsUnLock()

				cFilPara:=QI5->QI5_FILMAT
				cMatPara:=QI5->QI5_MAT

				If Ascan(aUsr,{ |x| x[1]+x[2] == cFilPara+cMatPara })== 0
					Aadd(aUsr,{cFilPara,cMatPara})
				EndIf

			Endif

	 		If lExtQIG
				//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
				//≥Log de Transferencia≥
				//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
				QNC070Log(,,QI5->QI5_CODIGO,QI5->QI5_REV,QI5->QI5_TPACAO,"3",cSeqTrf,cMotTransf,cFilDe,cMatDe,cFilPara,cMatPara)
			Endif

			If QI3->(dbSeek(QI5->QI5_FILIAL+QI5->QI5_CODIGO+QI5->QI5_REV))
				If QAA->(dbSeek(QI5->QI5_FILMAT + QI5->QI5_MAT )) .And. QAA->QAA_RECMAI == "1"
					cMail := AllTrim(QAA->QAA_EMAIL)

					If !Empty(cMail)

						cMsg := QNCSENDMAIL(3,OemToAnsi(STR0043),.T.)	// "Existe(m) Etapa(s) para voce neste Plano de Acao para ser executado (Transferencia de Lancamentos)."
						cAttach := ""
						aMsg:={{OemToAnsi(STR0042)+" "+TransForm(QI5->QI5_CODIGO,PesqPict("QI5","QI5_CODIGO"))+"-"+QI5->QI5_REV+Space(10)+DTOC(Date())+"-"+SubStr(TIME(),1,5), cMsg, cAttach} }	// "Plano de Acao No. "

						// Geracao de Mensagem para o Responsavel do Plano de Acao
						IF lQNCEACAO
							aMsg := ExecBlock( "QNCEACAO", .f., .f., { OemToAnsi(STR0043) } ) 	// "Existe(m) Etapa(s) para voce neste Plano de Acao para ser executado (Transferencia de Lancamentos)."
						Endif

						aAdd(aUsuarios,{QAA->QAA_LOGIN, cMail, aMsg} )
					Endif
				Endif
			Endif
		Endif
	Next
EndIf

Aadd(aUsr,{cFilQNC,cUsuQNC}) //Coloca o usuario origem no array para atualizaÁ„o de pendencias
End Transaction

For nI := 1 to Len(aUsr)
	QNCATULEG(aUsr[nI,1],aUsr[nI,2])//Atualiza legenda do usu·rios vinculados durante a transferencias
Next nI

If Len(aUsuarios) > 0
	QaEnvMail(aUsuarios,,,,aUsrMat[5],"2")
Endif
CursorArrow()

Return(Nil)


/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 ≥QNC070Log  ≥ Autor ≥Telso Carneiro        ≥ Data ≥16/09/2004≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥Grava Log da TransferÍncia                                  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥QNC070Log(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6,ExpC7,ExpC8,  ≥±±
±±≥          ≥            ExpC9,ExpC11,ExpC12)		                      ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Parametro ≥ExpC1  - Numero da FNC(Cogido)                              ≥±±
±±≥          ≥ExpC2  - Revisao da FNC                                     ≥±±
±±≥          ≥ExpC3  - Numero do Plano de Acao(Codigo)                    ≥±±
±±≥          ≥ExpC4  - Revisao do Plano de Acao                           ≥±±
±±≥          ≥ExpC5  - Tipo de Etapa do Plano de Acao(Codigo)  			  ≥±±
±±≥          ≥ExpC6  - Tipo de Pendencia 1-FNC 2-PLA 3-ETA     			  ≥±±
±±≥          ≥ExpC7  - Sequencia de gravacao da Transferencia(SXE/SXF)    ≥±±
±±≥          ≥ExpC8  - Motivo da Transferencia	                          ≥±±
±±≥          ≥ExpC09 - Filial De										  ≥±±
±±≥          ≥ExpC10 - Matricula De										  ≥±±
±±≥          ≥ExpC11 - Filial Para										  ≥±±
±±≥          ≥ExpC12 - Matricula Para									  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Uso		 ≥QNCA070TRF()                                                ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function QNC070Log(cNumFNC,cRevFNC,cNumPLA,cRevPLA,cTpAcao,cTpPend,;
							cSeqTrf,cMotTransf,cFilDe,cMatDe,cFilPara,cMatPara)

Local aArea 	:= GetArea()
Local aUsrMat	:= QA_USUARIO()
Local cMatFil	:= aUsrMat[2]
Local cMatCod	:= aUsrMat[3]

Default cNumFNC	:= ""
Default cRevFNC	:= ""
Default cNumPLA	:= ""
Default cRevPLA	:= ""
Default cTpAcao	:= ""

QAA->(DbSetOrder(1))

DbSelectArea("QIG")
DbSetOrder(1)

RecLock("QIG",.T.)
QIG->QIG_FILIAL:= xFilial("QIG")
QIG->QIG_FNC   := cNumFNC    	// Numero da FNC(Cogido)
QIG->QIG_REVFNC:= cRevFNC    	// Revisao da FNC
QIG->QIG_CODIGO:= cNumPLA    	// Numero do Plano de Acao(Codigo)
QIG->QIG_REVCOD:= cRevPLA    	// Revisao do Plano de Acao
QIG->QIG_TPACAO:= cTpAcao    	// Tipo de Etapa do Plano de Acao(Codigo)
QIG->QIG_DTTRAN:= dDataBase  	// Tipo de Pendencia 1-FNC 2-PLA 3-ETA
QIG->QIG_SEQTRF:= cSeqTrf    	// Sequencia de gravacao da Transferencia(SXE/SXF)
QIG->QIG_TPPEND:= cTpPend   	// 1-FNC  2-PLA 3-ETA
QIG->QIG_MOTIVO:= cMotTransf    // Motivo da Transferencia
QIG->QIG_FILRES:= cMatFil		// Filial do Responsavel
QIG->QIG_MATRES:= cMatCod		// Matricula do Responsavel
QIG->QIG_FILDE := cFilDe       	// Filial De
QIG->QIG_MATDE := cMatDe		// Matricula De
QIG->QIG_FILPAR:= cFilPara      // Filial Para
QIG->QIG_MATPAR:= cMatPara		// Matricula Para
MsUnlock()

RestaRea(aArea)
Return


/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070Vis ∫Autor  ≥Telso Carneiro      ∫ Data ≥  14/09/2004 ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Descriáao ≥ Visualiza Log de Transferencia (Historico)                 ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNCA070                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/

Function QNC070Vis()

Local nOpca
Local oDlgR
Local nX
Local aColsDE:={}
Local aColsPA:={}
Local aHeaLog :={}
Local cSeek
Local cWhile
Local lDe   := .F.//Indica se existe registros para DE
Local lPara := .F.//Indica se existe registros para PARA

Private oGetDE
Private oGetPA

IF !ChkFile("QIG")
	Return(NIL)
Endif
//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥Montagem o GetDados das Pendencias DE  ≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
dbSelectArea("QIG")
dbSetOrder(1)
If !dbSeek(xFilial("QIG")+QAA->QAA_FILIAL+QAA->QAA_MAT)
	lDe := .T.
EndIF

cSeek  := QIG->QIG_FILIAL+QIG->QIG_FILDE+QIG->QIG_MATDE
cWhile := "QIG->QIG_FILIAL+QIG->QIG_FILDE+QIG->QIG_MATDE"

  FillGetDados(nOpca,"QIG",1     ,cSeek ,{|| &cWhile},         ,         ,          ,        ,      ,        ,       ,          ,        ,          ,           ,            ,)
//FillGetDados(nOpca,Alias ,nOrdem,cSeek  ,bSeekWhile  ,uSeekFor ,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty ,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry

aHeaLog := aClone(aHeader)
aColsDE := aClone(aCols)

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥Zera aHeader e Acols para monta a GetDados PARA≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
aHeader := {}
aCols   := {}

//⁄ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
//≥Montagem o GetDados das Pendencias PARA≥
//¿ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ
dbSelectArea("QIG")
dbSetOrder(2)
If !dbSeek(xFilial("QIG")+QAA->QAA_FILIAL+QAA->QAA_MAT)
	lPara := .T.
EndIf

cSeek  := QIG->QIG_FILIAL+QIG->QIG_FILPAR+QIG->QIG_MATPAR
cWhile := "QIG->QIG_FILIAL+QIG->QIG_FILPAR+QIG->QIG_MATPAR"

  FillGetDados(nOpca,"QIG",2     ,cSeek ,{|| &cWhile},         ,         ,          ,        ,      ,        ,       ,          ,        ,          ,           ,            ,)
//FillGetDados(nOpca,Alias,nOrdem,cSeek  ,bSeekWhile  ,uSeekFor ,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty ,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry

aHeaLog := aClone(aHeader)
aColsPA := aClone(aCols)

IF lDe .And. lPara
	Help( " ", 1, "QNCNPENDU",,OemToAnsi(STR0038),1)	// "Nao existem pendencias para este Usuario"
	Return(Nil)
Endif

dbSelectArea("QIG")
dbSetOrder(3)  //FILIAL+DATA+SEQ

DEFINE MSDIALOG oDlgR TITLE OemToAnsi(STR0060) FROM 9,0 TO 40,100     //"Historico de TransferÍncias das Pendencias de Fichas,Planos e Etapas "

RegToMemory("QIG")

@015,002 SAY OemToAnsi(STR0057)+QAA->QAA_FILIAL+"-"+QAA->QAA_MAT+" "+QA_NUSR(QAA->QAA_FILIAL,QAA->QAA_MAT) PIXEL    //"TransferÍncias realizadas (DE): "
oGetDE :=MsNewGetDados():New(025,002,120,395,0,"AllwaysTrue()","AllwaysTrue()",,,,,,,,oDlgR,aHeaLog,aColsDE)

@125,002 SAY OemToAnsi(STR0058)+QAA->QAA_FILIAL+"-"+QAA->QAA_MAT+" "+QA_NUSR(QAA->QAA_FILIAL,QAA->QAA_MAT) PIXEL    //"TransferÍncias realizadas (PARA): "
oGetPA :=MsNewGetDados():New(135,002,230,395,0,"AllwaysTrue()","AllwaysTrue()",,,,,,,,oDlgR,aHeaLog,aColsPA)

ACTIVATE MSDIALOG oDlgR CENTERED ON INIT EnchoiceBar(oDlgR,{|| oDlgR:End()},{|| oDlgR:End()} )

Return(NIL)

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070TMK ∫Autor  ≥Leandro		     ∫ Data ≥  01/02/2008 ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Descriáao ≥ Valida se o usuario foi incluido via TMK		              ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNCA070                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/

Function QNC070TMK()
Local lTMKPMS   := If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local nFilFun 	:= 0
Local nFilAnt   := 0
Local cPriFil 	:= ""
Local cCodigo   := ""
Local aArea     := GetArea()
Local lRet      := .T.
Local nEmpAnt   := 0

If lTMKPMS .And. (GetMv("MV_QTMKPMS",.F.,1) == 2)

	nEmpAnt := len(cEmpAnt)
	nFilFun := len(cFilAnt)
	nFilAnt := len(cFilAnt)

	aArea := GetArea()
	DbSeek("QAA")
	cModoQAA:= FWModeAccess("QAA",3)
	DbSetOrder(1)

	cPriFil := QXPOSFIL(cEmpAnt)

	If cModoQAA == "C"
		cCodigo := SubStr(M->QAA_MAT,(nEmpAnt+cPriFil)+1,10)
	Else
		cCodigo := SubStr(M->QAA_MAT,(nEmpAnt+nFilAnt)+1,10)
	Endif

	DbSelectArea("SU7")
	dbSetOrder(1)
	If DbSeek(xFilial("SU7") + cCodigo)
		If !Empty(M->QAA_LOGIN)
			MsgAlert(STR0061,STR0062)//"Devido a integracao com o ambiente de Call Center o usuario nao podera ser excluido ou alterado."##"Atencao, usuario bloqueado para manutencao!!"
    		lRet := .F.
    	Endif
     Endif
    RestArea(aArea)

Endif

Return lRet

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNCDELRDZ ∫Autor  ≥Leandro		     ∫ Data ≥  27/11/2008 ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Descriáao ≥Na alteracao da RDZ, excluir o relacionamento anterior      ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNCA070                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Function QNC070RDZ(cPessoa  ,cEntidade,cChaveEntidade,nIndice,cEmp   ,cFil,cChave)

Local cRDZFil := ""
Local aArea   := GetArea()
Local lAddNew := .T.

DEFAULT cFil := cFilAnt

cRDZFil := xFilial( "RDZ" , cFil )

dbSelectArea('RDZ')
RDZ->(dbSetOrder(3))
lAddNew := !( RDZ->( MsSeek( cRDZFil + cEmp + cFil + cEntidade + cPessoa) ) )
RecLock("RDZ",lAddNew)

RDZ->RDZ_FILIAL := cRDZFil
RDZ->RDZ_EMPENT := cEmp
RDZ->RDZ_FILENT := cFil
RDZ->RDZ_ENTIDA := cEntidade
If lAddNew
	RDZ->RDZ_CODENT := cChaveEntidade
	RDZ->RDZ_CODRD0 := cPessoa
	RDZ->RDZ_ENTIND := StrZero( nIndice , GetSx3Cache( "RDZ_ENTIND" , "X3_TAMANHO" ) )
Else
	RDZ->RDZ_CODENT := cChaveEntidade
Endif

MsUnlock()

RestArea(aArea)

Return

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥ QValPart   ∫Autor  ≥Sandra Ribeiro    ∫ Data ≥  19/05/09   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Busca o departamento de correÁ„o cadastrado para o produto.∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥                                                            ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QValPartRec(cPart)

Local cRecur := ""
Local aArea	:= GetArea()

DbSelectArea("RDZ")
DbSetOrder(2)

//RDZ_FILIAL+RDZ_CODRD0+RDZ_EMPENT+RDZ_FILENT+RDZ_ENTIDA - Indice 2 da RDZ

If RDZ->(DbSeek(xFilial("RDZ")+AllTrim(cPart)+cEmpAnt+cFilAnt+"AE8"))
	RecLock("QAA",.F.)
	cRecur := RDZ->RDZ_CODENT
	MsUnLock()
	cRecur := AllTrim(STUFF(cRecur,1,2,""))
	DbSelectArea("AE8")
	AE8->(DBSetOrder(1))
	If AE8->(DbSeek(xFilial("AE8")+cRecur))
		M->QAA_DESCRE := AllTrim(AE8->AE8_DESCRI)
	EndIf
EndIf

RestArea( aArea )

Return cRecur

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥ QValPart   ∫Autor  ≥Sandra Ribeiro    ∫ Data ≥  19/05/09   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Busca o departamento de correÁ„o cadastrado para o produto.∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥                                                            ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QValPartOpe(cPart)

Local cOperad := ""
Local aArea	  := GetArea()

DbSelectArea("RDZ")
DbSetOrder(2)

//RDZ_FILIAL+RDZ_CODRD0+RDZ_EMPENT+RDZ_FILENT+RDZ_ENTIDA - Indice 2 da RDZ
If RDZ->(DbSeek(xFilial("RDZ")+AllTrim(cPart)+cEmpAnt+cFilAnt+"SU7"))
	RecLock("QAA",.F.)
	cOperad := RDZ->RDZ_CODENT
	MsUnLock()
	cOperad := AllTrim(STUFF(cOperad,1,2,""))
	DbSelectArea("SU7")
	SU7->(DBSetOrder(1))
	If SU7->(DbSeek(xFilial("SU7")+cOperad))
		M->QAA_DESCOP := AllTrim(SU7->U7_NOME)
	EndIf
EndIf

RestArea( aArea )

Return cOperad


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funcao    ≥QN070Chk  ≥ Autor ≥Leandro                ≥ Data ≥ 05/12/08 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descricao ≥Func. verifica se j· existe participante, recurso e operador≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QN070Chk(cParticip,cRecurso,cOperador)

Local lRet    := .F.

cAlias := GetNextAlias()
cQuery := "SELECT COUNT (*)  QTD"
cQuery += " FROM " + RetSqlName("QAA") + " QAA "
cQuery += " WHERE QAA.QAA_FILIAL = '" + xFilial("QAA") + "' AND "
cQuery += "       QAA.QAA_PARTIC = '" + cParticip + "' AND "
cQuery += "       QAA.QAA_RECUR  = '" + cRecurso + "' AND "
cQuery += "       QAA.QAA_OPERAD = '" + cOperador + "' AND "
cQuery += "       QAA.D_E_L_E_T_ = ' '"

cQuery := ChangeQuery(cQuery)
dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAlias, .F., .T.)

If (cAlias)->QTD== 0
	lRet := .T.
Endif
dbSelectArea(cAlias)
dbCloseArea()

Return lRet

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funcao    ≥QN070ChkAE8 Autor ≥Leandro                ≥ Data ≥ 05/12/08 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descricao ≥Func. verifica se j· existe participante, recurso e operador≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QN070ChkAE8(cRecurso)

Local lRet := If(!Empty(cRecurso),.F.,.T.)

cAlias := GetNextAlias()
cQuery := "SELECT COUNT (*)  QTD"
cQuery += " FROM " + RetSqlName("QAA") + " QAA "
cQuery += " WHERE QAA.QAA_FILIAL = '" + xFilial("QAA") + "' AND "
cQuery += "       QAA.QAA_RECUR  = '" + cRecurso + "' AND "
cQuery += "       QAA.D_E_L_E_T_ = ' '"

cQuery := ChangeQuery(cQuery)
dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAlias, .F., .T.)

If (cAlias)->QTD == 0
	lRet := .T.
Endif
dbSelectArea(cAlias)
dbCloseArea()

Return lRet

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funcao    ≥QN070ChkSU7 Autor ≥Leandro                ≥ Data ≥ 05/12/08 ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descricao ≥Func. verifica se j· existe participante, recurso e operador≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QN070ChkSU7(cOperador)

Local lRet := If(!Empty(cOperador),.F.,.T.)

cAlias := GetNextAlias()
cQuery := "SELECT COUNT (*)  QTD"
cQuery += " FROM " + RetSqlName("QAA") + " QAA "
cQuery += " WHERE QAA.QAA_FILIAL = '" + xFilial("QAA") + "' AND "
cQuery += "       QAA.QAA_OPERAD = '" + cOperador + "' AND "
cQuery += "       QAA.D_E_L_E_T_ = ' '"

cQuery := ChangeQuery(cQuery)
dbUseArea(.T., "TOPCONN", TCGenQry(,,cQuery), cAlias, .F., .T.)

If (cAlias)->QTD == 0
	lRet := .T.
Endif
dbSelectArea(cAlias)
dbCloseArea()

Return lRet

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕª±±
±±∫Funáao	 ≥QN070VldDel ∫ Autor ≥ Paulo Fco. Cruz Nt. ∫ Data ≥16/09/2009∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Descriáao ≥ValidaÁ„o da exclusao de usuarios para integraÁ„o			  ∫±±
±±∫          ≥TMK x PMS x QNC											  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Sintaxe	 ≥QNC070Log(ValA1)                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Parametro ≥ValA1 - CÛdigo de participante do usuario a excluir		  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso		 ≥QNCA070()                                                   ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function QN070VldDel(cPart)
Local cMat   := ""
Local lApaga := .T.

dbSelectArea("RDZ")
dbSetOrder(2)

//RDZ_FILIAL+RDZ_CODRD0+RDZ_EMPENT+RDZ_FILENT+RDZ_ENTIDA - Indice 2 da RDZ
If RDZ->(DbSeek(xFilial("RDZ")+cPart+cEmpAnt+xFilial("QAA")+"QAA"))
	cMat := RDZ->RDZ_CODENT
	cMat := AllTrim(STUFF(cMat,1,2,"")) // Retira filial

	If !Empty(cMat)
		dbSelectArea("QAA")
		dbSetOrder(1)
		If dbSeek(xFilial("QAA")+cMat)
			lApaga := Iif(FindFunction("QAAValExc"), QAAValExc(), .F.)
		EndIf
	EndIf
EndIf

Return lApaga


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕª±±
±±∫Funáao	 ≥QnTrfPMS    ∫ Autor ≥ Sandra Ribeiro      ∫ Data ≥10/08/2009∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Descriáao ≥Busca e altera o responsavel pela tarefa no PMS quando inte ∫±±
±±∫          ≥grado via MV_QTMKPMS  									  ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Sintaxe	 ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Parametro ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso		 ≥QNCA070()                                                   ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function QnTrfPMS(cMatDe,aEtapa)

Local cRecPara	:= ""
Local cRevisao	:= ""
Local cRecDe	:= ""
Local lRet		:= .T.
Local lTMKPMS	:= If(GetMv("MV_QTMKPMS",.F.,1) == 1,.F.,.T.)//Integracao do QNC com TMK e PMS: 1-Sem integracao,2-TMKxQNC,3-QNCxPMS,4-TMKxQNCxPMS ≥
Local nX
Local bCampo 	:= {|n| FieldName(n) }
Local aAreaAFA
Local cItem

Default cMatDe := ""

If lTMKPMS
	If !Empty(aEtapa[13])
		cRevisao := PmsAF8Ver(aEtapa[13])
	EndIf

	DbSelectArea("QAA")
	DbSetOrder(1)
	If QAA->(DbSeek(aEtapa[18]))
		If !Empty(QAA->QAA_RECUR)
			cRecPara := QAA->QAA_RECUR
		Else
		   	Alert("AtenÁ„o","N„o h· recurso para este usu·rio! A tarefa n„o ser· transferida!")
			lRet := .F.
		EndIf
	EndIf

	If QAA->(DbSeek((aEtapa[2])+cMatDe))
		cRecDe := QAA->QAA_RECUR
	EndIf

	If lRet
		DbSelectArea("AFA")
		DbSetOrder(5) //AFA_FILIAL+AFA_PROJET +AFA_REVISA+ AFA_TAREFA+AFA_RECURS
		If AFA->(DbSeek((aEtapa[2])+(aEtapa[13])+cRevisao+(aEtapa[16])+cRecDe))
			aAreaAFA := AFA->(GetArea())
			cItem:=strzero(0,AFA->(TamSX3("AFA_ITEM")[1]))
			AFA->(DbSetOrder(1))
			AFA->(DbSeek( (aEtapa[2])+(aEtapa[13])+cRevisao+(aEtapa[16]) ))
			Do While AFA->(!Eof()) .And. (aEtapa[2])+(aEtapa[13])+cRevisao+(aEtapa[16])==AFA->(AFA_FILIAL+AFA_PROJET+AFA_REVISA+AFA_TAREFA)
				cItem := AFA->AFA_ITEM
				AFA->(DbSkip())
			EndDo
			cItem := Soma1(cItem)
			RestArea(aAreaAFA)

			RegToMemory( "AFA", .F.)
			RecLock("AFA",.F.)
				AFA->AFA_RESP := "2"
			MsUnLock()

			dbSelectArea("AFA")
			RecLock("AFA",.T.)
			For nx := 1 TO FCount()
				FieldPut(nx,M->&(EVAL(bCampo,nx)))
			Next nx
			AFA->AFA_FILIAL	:= xFilial("AFA")
			AFA->AFA_RECURS	:= cRecPara
			AFA->AFA_RESP	:= "1"
			AFA->AFA_ITEM	:= cItem
			MsUnlock()
		EndIf
	EndIf
EndIf

Return

/*‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±⁄ƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¬ƒƒƒƒƒƒ¬ƒƒƒƒƒƒƒƒƒƒø±±
±±≥Funáao	 |QNC070Grp  ≥ Autor ≥Telso Carneiro        ≥ Data ≥16/09/2004≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Descriáao ≥Tela de SeleÁ„o do Grupo de Etapas                          ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Sintaxe	 ≥QNC070Grp ()    											  ≥±±
±±√ƒƒƒƒƒƒƒƒƒƒ≈ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ¥±±
±±≥Uso		 ≥QNCA070TRF()                                                ≥±±
±±¿ƒƒƒƒƒƒƒƒƒƒ¡ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒŸ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ*/
Static Function QNC070Grp()

Local oDlgGrp
Local lGrava	:= .F.
Local cGrpEtp   := Space(Len(QUO->QUO_GRUPO))

DEFINE MSDIALOG oDlgGrp TITLE "Selecione o Novo Grupo de Etapas" FROM 150,000 TO 230,280 PIXEL    //Selecione Novo grupo de etapas

@ 005,005 MSGET oGrpEtp VAR cGrpEtp F3 "QUO1" OF oDlgGrp SIZE 130,010  PIXEL

DEFINE SBUTTON FROM 021,075 TYPE 1 ENABLE OF oDlgGrp ACTION  (If(VldGrpEtp(cGrpEtp ).And. NaoVazio(cGrpEtp),(lGrava := .T.,oDlgGrp:End()),));

DEFINE SBUTTON FROM 021,105 TYPE 2 ENABLE OF oDlgGrp ACTION  (oDlgGrp:End());

ACTIVATE MSDIALOG oDlgGrp CENTERED

Return cGrpEtp


/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNCA070   ∫Autor  ≥Iolanda Vilanova	 ∫ Data ≥  04/07/10   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Validar se o grupo de etapas informado existe na QUO       ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ QNCA070                                                    ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/

Function  VldGrpEtp(cGrpEtp )
Local lRet := .T.

DbSelectArea ("QUO")
QUO->(DbSetOrder(1))//QUO_FILIAL+QUO_GRUPO
If !QUO->(DbSeek(xFilial("QUO")+cGrpEtp ))
	Help(" ",1,'REGNOIS')
	lRet := .F.
Endif

Return lRet

/*
‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹‹
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±…ÕÕÕÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÀÕÕÕÕÕÕ—ÕÕÕÕÕÕÕÕÕÕÕÕÕª±±
±±∫Programa  ≥QNC070FIL ∫Autor  ≥Rafael Duram Santos ∫ Data ≥  03/05/11   ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕ ÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Desc.     ≥ Filtro Alias QI5/QI2 para tratamento em DBF                ∫±±
±±∫          ≥                                                            ∫±±
±±ÃÕÕÕÕÕÕÕÕÕÕÿÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕπ±±
±±∫Uso       ≥ MP11                                                       ∫±±
±±»ÕÕÕÕÕÕÕÕÕÕœÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕÕº±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂﬂ
*/
Function QNC070FIL(cCodQI5,cFilQI5,cMatQI5,cMatQNC)

Local lRet := .T.
Local aArea := QI2->(GetArea())

If cFilQI5 <> xFilial("QI5")
	lRet := .F.
	Return lRet
Endif

If cMatQI5 <> Alltrim(cMatQNC)
	lRet := .F.
	Return lRet
Endif

DbSelectArea("QI2")
QI2->(DbClearFilter())
QI2->(DbSetOrder(5))
If !QI2->(DbSeek(xFilial("QI2")+cCodQI5))
	lRet := .F.
EndIf

RestArea(aArea)

Return lRet

/*/{Protheus.doc} QNC070TOK
@type  Static Function
@author rafael.hesse
@since 13/07/2022
@param lTMKPMS, LÛgico, indica se tem Integracao do QNC com TMK e PMS
@return lRet, LÛgico, indica se o cadastro est· v·lido para inclus„o/alteraÁ„o
/*/
Static Function QNC070TOK(lTMKPMS)
Local lRet := .T.	

	If !(QX10VldEmp(lIntGPE) .AND. QA010VRCFG(.T.) .AND. Obrigatorio(aGETS,aTELA) )
		lRet := .F.
	EndIf

	lRet := IIf( lRet .And. Altera .And. M->QAA_STATUS == '2' .And. FindFunction("QAAValIna"), QAAValIna(), lRet) //Iif na mesma linha para facilitar cobertura

Return lRet
