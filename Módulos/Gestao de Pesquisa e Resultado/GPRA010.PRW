#INCLUDE "GPRDEF.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHELL.CH"
#INCLUDE "GPRA010.CH"

#DEFINE NOVALINHA 	CHR( 13 ) + CHR( 10 )
#DEFINE MAXGETDAD 	4096

/*/


Ŀ
Funo    GPRA010    Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Cadastro de Pesquisas                                      
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Marcelo K.09/08/04710   Revisao do fonte:Sintaxe,Documentacao,Logica
Luiz Couto29/08/05811   - Implementacao continua: padronizacao dos  
                        fontes                                      
Henry     17/07/06811   - BOPS 103.397: Correcao do campo KC_STATUS 
			  		  		 na legenda de cores						  
ٱ


/*/
Function GPRA010( aRotAuto, aAutoItens, nOpc )

//Ŀ
// Definicao das legendas do cadastro de Pesquisas                     
//
/*
  Ŀ
  VERMELHO  - Ativa                                 
  VERDE     - Inativa                               
  PRETO     - Vencida                               
  
*/
Local aCores := {	{	"VAL(KC_STATUS) == 1"								, "BR_VERMELHO" },;
					{	"VAL(KC_STATUS) == 2"								, "BR_VERDE"	},;
					{	"VAL(KC_STATUS) == 2 .AND. KC_DTATE <= dDataBase" , "BR_PRETO"	} }
						
Private cCadastro := STR0001    	// Cadastro de Pesquisas

Private aMemos    := { { "KC_CODLST", "KC_LISTA" }, { "KC_CODOBJ", "KC_OBJETIV" } } // Array resp. pela gravacao dos campos memo

Private aRotina   := MenuDef()
Private aAutocab	 := aRotAuto
Private aAutoIte	 := aAutoItens

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica
//
Private l010Auto := ( aRotAuto <> NIL )

If nOpc == NIL 
	nOpc := 3
Endif	

//Ŀ
//Se for entrada automatica
//
DbSelectArea( "SKC" )
DbSetOrder( 1 )

If l010Auto
	MBrowseAuto( nOpc, aRotAuto, "SKC" )
Else
	MBrowse( 	6		, 1		, 22	, 75	,;
				"SKC"	, NIL	, NIL	, NIL	,;
				NIL		, NIL	, aCores )
Endif

Return( .T. )

/*/

Ŀ
Funo     MenuDef   Autor  Conrado Q. Gomes       Data  12.12.06 
Ĵ
Descrio  Definio do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       SIGAGPR                                                    
ٱ


/*/
Static Function MenuDef()
	Local aRotina := {	{ STR0006	,"AxPesqui"		,0	,1	,0	,.F.	}	,;	//Pesquisar
						{ STR0007	,"Gpr010Man"	,0	,2	,0	,.T.	}	,;	//Visualizar
						{ STR0008	,"Gpr010Man"	,0	,3	,0	,.T.	}	,;	//Incluir
						{ STR0009	,"Gpr010Man"	,0	,4	,0	,.T.	}	,;	//Alterar
						{ STR0010	,"Gpr010Exc"	,0	,5	,0	,.T.	}	,;	//Excluir
						{ STR0032	,"Gpr010Man"	,0	,4	,0	,.T.	}	,;	//Copiar
						{ STR0053	,"Gpr010Atv"	,0	,4	,0	,.T.	}	,;	//Ativar
						{ STR0052	,"MsDocument"	,0	,4	,0	,.T.	}	,;	//Conhecimento
						{ STR0048	,"Gpr010Prv"	,0	,6	,0	,.T.	}	,;	//Preview
						{ STR0023	,"Gpr010Leg"	,0	,7	,0	,.T.	}	}	//Legenda
Return(aRotina)

/*/


Ŀ
Funo     Gpr010Man Autor  Henry Fila             Data  17.02.05 
Ĵ
Descrio  Manutencao Cadastro de Pesquisas                           
Ĵ
 Uso       Gerenciamento de Pesquisa e resuiltados (GPR)              
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR    DATA     BOPS   MOTIVO DA ALTERACAO                 
Ĵ
Luiz Couto    29/08/05  2825  - Implementacao continua: padronizacao
                              dos fontes                            
Luiz Couto    29/08/05  2825  - Alteracao na copia de pesquisa:     
                              adicionado a variavel aRecnoSKD       
                              somente se for copia lCopia := .T.    
Conrado Q.    |26/01/07  115964|- Montagem do aCols e aHeader atravs 
                              |da rotina FillGetDados.               
Hanna C.      |30/03/07  |118469Alterado o nome dos Bitmaps definidos 
        	               |       pela Engenharia para o Protheus 10   
ٱ


/*/
Function Gpr010Man( cAlias, nReg, nOpc )

Local aPosObj	:= {}
Local aObjects	:= {}
Local aSize		:= MsAdvSize()
Local aRecnoSKL	:= {}
Local aRecnoSKO	:= {}
Local aRecnoSKD	:= {}
Local aHeaderSKL:= {}    	// aHeader
Local aColsSKL	:= {}    	// aCols
Local aHeaderSKO:= {}    	// aHeader
Local aColsSKO	:= {}    	// aCols
Local aHeaderSKD:= {}    	// aHeader
Local aColsSKD	:= {}    	// aCols
Local aButtons	:= { 	{ "FILTRO"   	, { || Gpr010Fil( @cfiltro ) }             , STR0002, STR0049 },;  //Construtor de Filtros
					   	{ "POSCLI"    	, { || Gpr010Pesq( aHeaderSKO, aColsSKO ) }, STR0045, STR0050 },; //Pesquisadores
					   	{ "BPMSRELAA"  	, { || Gpr010Ques( aHeaderSKD, aColsSKD ) }, STR0046, STR0051 },; //Questionarios
						{ "GEOEMAIL"  	, { || Gpr010Link( M->KC_CODPQ ) } 		, STR0054, STR0054 } } //Gerar Link

Local cFiltro	:= ""
Local lCopia	:= nOpc == 6
Local nSaveSx8	:= GetSx8Len()
Local nOpca		:= 0
Local nCntFor	:= 0
Local nUsado	:= 0
Local nX		:= 0
Local oGetdSKL	:= Nil    
Local cSeek		:= ""		// Seek para montagem do aCols
Local cWhile	:= ""		// While para montagem do aCols

Private aTELA[0][0]
Private aGETS[0]
                                          
//Ŀ
// Inicializa desta forma para criar uma nova instancia de variaveis private 
//
If INCLUI .OR. lCopia 
	RegToMemory( "SKC", .T., .F. )
Else
	RegToMemory( "SKC", .F. )
	AAdd( aButtons, { "MDIEXCEL", { || Gpr100Exp( cAlias, nReg, nOpc ) } ,  STR0059,  STR0058 } )
Endif 
	
//Ŀ
// Inicializa campos memo do cabecalho
//
If Type( "aMemos" ) == "A"
	For nX := 1 To Len( aMemos )
		cMemo := aMemos[nX][2]

		If ExistIni( cMemo )
			&cMemo := InitPad( SX3->X3_RELACAO ) 
		Else
			&cMemo := ""
		Endif
	Next nX
Endif

//Ŀ
// Monta aHeader e aCols das Regras da Pesquisa
//
aHeader	:= {}
aCols	:= {}

If !INCLUI .OR. lCopia
	cSeek := xFilial( "SKL" ) + SKC->KC_CODPQ
	cWhile:= "SKL->KL_FILIAL+SKL->KL_CODPQ"

	FillGetDados(	nOpc			,"SKL"			,1													,cSeek				,;
	   				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/										,/*aYesFields*/		,; 
	   				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/										,/*lEmpty*/			,;
	   				/*aHeaderAux*/	,/*aColsAux*/	,{|| GPR010AfterCols( "SKL", aRecnoSKL, lCopia ) }	,/*bBeforeCols*/	)
			   				
Else
	FillGetDados(	nOpc			,"SKL"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)
EndIf

If Empty( aCols[ 1 ][ GdFieldPos("KL_ITEM") ] )
	aCols[ 1 ][ GdFieldPos("KL_ITEM") ]	 := StrZero( 1, Len( SKL->KL_ITEM ) )
EndIf

aHeaderSKL	:= aClone( aHeader )
aColsSKL	:= aClone( aCols )

//Ŀ
// Monta aHeader e aCols dos Pesquisadores
//
aHeader := {}
aCols	:= {}

If !INCLUI .OR. lCopia
	cSeek := xFilial( "SKO" ) + SKC->KC_CODPQ
	cWhile:= "SKO->KO_FILIAL+SKO->KO_CODPQ"

	FillGetDados(	nOpc			,"SKO"			,1													,cSeek				,;
	   				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/										,/*aYesFields*/		,; 
	   				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/										,/*lEmpty*/			,;
	   				/*aHeaderAux*/	,/*aColsAux*/	,{|| GPR010AfterCols( "SKO", aRecnoSKO, lCopia ) }	,/*bBeforeCols*/	)		   				
Else
	FillGetDados(	nOpc			,"SKO"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)				
EndIf

aHeaderSKO	:= aClone( aHeader )
aColsSKO	:= aClone( aCols )

//Ŀ
// Monta aHeader e aCols dos Questionarios
//
aHeader := {}
aCols	:= {}

If !INCLUI .OR. lCopia
	cSeek := xFilial( "SKD" ) + SKC->KC_CODPQ
	cWhile:= "SKD->KD_FILIAL+SKD->KD_CODPQ"

	FillGetDados(	nOpc			,"SKD"			,1													,cSeek				,;
	   				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/										,/*aYesFields*/		,; 
	   				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/										,/*lEmpty*/			,;
	   				/*aHeaderAux*/	,/*aColsAux*/	,{|| GPR010AfterCols( "SKD", aRecnoSKD, lCopia ) }	,/*bBeforeCols*/	)		   				
Else
	FillGetDados(	nOpc			,"SKD"		,1				,/*cSeek*/		,;
					/*{||&cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)				
EndIf

aHeaderSKD	:= aClone( aHeader )
aColsSKD	:= aClone( aCols )

If Type( "l010Auto" ) == "U" .OR. !l010Auto
	//Ŀ
	// Faz o calculo automatico de dimensoes de objetos
	//
	aSize := MsAdvSize()

	AAdd( aObjects, { 100,065,.T.,.T. } )
	AAdd( aObjects, { 100,100,.T.,.T. } )

	aInfo 	:= { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects, .T. )

	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL
	
		//Ŀ
		// Definindo Enchoice
		//
		EnChoice( 	cAlias	, nReg	, nOpc	, NIL			,;
					NIL		, NIL	, NIL	, aPosObj[1]	,;
					NIL		, NIL	, NIL	, NIL			,;
					NIL		, NIL	, NIL	, .T. )

		//Ŀ
		// Getdados - Alternativas
		//
		oGetdSKL := MsNewGetDados():New( 	aPosObj[2,1]	,;
											aPosObj[2,2]	,;
											aPosObj[2,3]	,;
											aPosObj[2,4]	,;
											IIF( Altera .OR. Inclui, GD_INSERT + GD_DELETE + GD_UPDATE, 0 ),;
											"AllwaysTrue()"	,;
											"Gpra010TOk()"	,;
											"+KL_ITEM"		,;
											NIL				,;
											NIL				,;
											MAXGETDAD		,;
											NIL				,;
											NIL				,;
											NIL				,;
											NIL				,;
											aHeaderSKL		,;
											aColsSKL )
	//Ŀ
	// Ativando a Tela
	//
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg, { ||nOpcA := 1, aColsSKL := oGetdSKL:aCols, IIF( oGetdSKL:TudoOk(), IIF( !Obrigatorio( aGets, aTela ), nOpcA := 0, oDlg:End() ), nOpcA := 0 ) }, { ||oDlg:End() }, NIL, aButtons )
Else
	aHeader:= aClone(aHeaderSKL)
	aCols  := aClone(aColsSKL)
	If EnchAuto(cAlias,aAutocab,{|| Obrigatorio(aGets,aTela)},aRotina[nOpc][4]) .and. MsGetDAuto(aAutoIte,,{||Gpra010Tok()},aAutocab,nOpc)
		nOpcA := 1
		aHeaderSKL:= aClone(aHeader)
		aColsSKL  := aClone(aCols)
	EndIf
EndIf	
If ( nOpca == 1 )
	//Ŀ
	// Gravacao da pesquisa
	//
	Gpr010Grv( 	nOpc - 2	, aRecnoSKL	, aHeaderSKL	, aColsSKL	,;
				aRecnoSKO	, aHeaderSKO, aColsSKO		, aRecnoSKD	,;
				aHeaderSKD	, aColsSKD )

	While ( GetSx8Len() > nSaveSx8 )
		ConfirmSx8()
	End

	EvalTrigger()
Else
	If nOpc <> 2                   
		While ( GetSx8Len() > nSaveSx8 )		
			RollBackSx8()	
		End		
	Endif				
Endif

DbSelectArea( "SKC" )

Return( .T. )

/*/


Ŀ
Funo    Gpra010TOk Autor  Henry Fila             Data  17/02/2005 
Ĵ
Descrio Validacao da getdados de regras                               
Ĵ
Sintaxe   Gpra010TOk                                                    
Ĵ
ParametrosNenhum                                                        
Ĵ
Uso        Pesquisa e Resultado                                         
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
Function Gpra010Tok()

Local lRet     := .F.  //Variavel de retorno da funcao
Local nPosID   := AScan(aHeader,{|x| AllTrim(x[2])=="KL_CHAVE"}) //Variavel de posicao de colunas do aHeader
Local nUsado   := Len(aHeader)+1  //Variavel de dimensoes do acols
Local nX       := 0               //Variavel For

//Ŀ
// Varre todos os itens da regra                                             
//

For nX := 1 To Len( aCols )

	//Ŀ
	// Verifica se nao esta deletado                                             
	//
	If !aCols[nX][nUsado]

		//Ŀ
		// Verifica se a chave esta com Sim e sai pois ja existe o ID                
		//
		If aCols[nX][nPosID] == "1"
			lRet := .T.
			Exit
		Endif
	
	Endif

Next

If !lRet
	Help(" ",1,"GPR010ID")
Endif	

Return( lRet )               

/*/


Ŀ
Funo    Gpr010Grv  Autor  Henry Fila             Data  17/02/2005 
Ĵ
Descrio Rotina de Gravacao da Pesquisa                                
Ĵ
Sintaxe   Gpr010Grv                                                     
Ĵ
ParametrosExpN1: Opcao da Gravacao sendo:                               
                 [1] Inclusao                                           
                 [2] Alteracao                                          
                 [3] Exclusao                                           
          ExpA2: Array de registros                                     
Ĵ
Uso        Pesquisa e Resultado                                         
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
Static Function Gpr010Grv(nOpcao,aRecnoSKL,aHeaderSKL,aColsSKL,aRecnoSKO,aHeaderSKO,aColsSKO,aRecnoSKD,aHeaderSKD,aColsSKD)

Local aArea     := GetArea()                     

Local bCampo 	:= {|nCPO| Field(nCPO) }

Local cAliasMemo
Local cItem     := Repl("0",Len(SKL->KL_ITEM))

Local nX        := 0
Local nCntFor   := 0
Local nY        := 0
Local nUsado    := Len(aHeaderSKL)
Local nPosPsq   := AScan(aHeaderSKO,{|x| AllTrim(x[2])=="KO_CODPSQ"})
Local nPosQt    := AScan(aHeaderSKD,{|x| AllTrim(x[2])=="KD_CODQT"})

Local lTravou   := .F.

DbSelectArea( "SKS" )
DbSelectArea( "SKT" )
DbSelectArea( "SKU" )

If nOpcao <> 3

	//Ŀ
	//Grava a pesquisa e as regras da pesquisa                           
	//

	For nX := 1 To Len(aColsSKL)

		Begin Transaction
		If nX == 1

			DbSelectArea("SKC")
			DbSetOrder(1)
			If DbSeek(xFilial("SKC")+M->KC_CODPQ)
				RecLock("SKC",.F.)
			Else
				RecLock("SKC",.T.)
			Endif
			For nCntFor := 1 To FCount()
				FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor)))
			Next nCntFor
	
			//Ŀ
			//Grava os campos Memos Virtuais					 				  
			//
			If Type("aMemos") == "A"
				For nCntFor := 1 To Len(aMemos)
					cVar := aMemos[nCntFor][2]
					cAliasMemo := If(len(aMemos[nCntFor]) == 3,aMemos[nCntFor][3],Nil)
					MSMM(,TamSx3(aMemos[nCntFor][2])[1],,&cVar,1,,,"SKC",aMemos[nCntFor][1],cAliasMemo)
				Next nCntFor
			Endif
	                        
			SKC->KC_FILIAL := xFilial("SKC")
			MsUnLock()
		Endif		
	
	
		lTravou := .F.
		If nX <= Len(aRecNoSKL)		
			DbSelectArea("SKL")
			DbGoTo(aRecNoSKL[nX])
			RecLock("SKL")
			lTravou := .T.
		Endif
	
		If ( !aColsSKL[nX][nUsado+1] )
			If !lTravou 
				RecLock("SKL",.T.)			
			Endif
			For nY := 1 To Len(aHeaderSKL)
				If aHeaderSKL[nY][10] <> "V"
					SKL->(FieldPut(FieldPos(aHeaderSKL[nY][2]),aColsSKL[nX][nY]))
				Endif
			Next nY
			cItem := Soma1(cItem,Len(SKL->KL_ITEM))
			SKL->KL_FILIAL := xFilial("SKL")
			SKL->KL_CODPQ  := SKC->KC_CODPQ
			SKL->KL_ITEM   := cItem
			
			MsUnLock()                   
			
			lGravou := .T.
		Else	
			If lTravou        
				RecLock("SKL")
					SKL->(dbDelete())
				MsUnlock()
			Endif				
		Endif
		MsUnLock()
		
		End Transaction
		
	Next nX

	//Ŀ
	//Grava os pesquisadores                                             
	//

	nUsado    := Len(aHeaderSKO)

	For nX := 1 To Len(aColsSKO)

		Begin Transaction
		
		lTravou := .F.
		If nX <= Len(aRecNoSKO)		
			DbSelectArea("SKO")
			DbGoTo(aRecNoSKO[nX])
			RecLock("SKO")
			lTravou := .T.
		Endif
	
		If ( !aColsSKO[nX][nUsado+1] ) .AND. !Empty( aColsSKO[nX][nPosPsq] )
			If !lTravou 
				RecLock("SKO",.T.)			
			Endif
			For nY := 1 To Len(aHeaderSKO)
				If aHeaderSKO[nY][10] <> "V"
					SKO->(FieldPut(FieldPos(aHeaderSKO[nY][2]),aColsSKO[nX][nY]))
				Endif
			Next nY
			SKO->KO_FILIAL := xFilial("SKO")
			SKO->KO_CODPQ  := SKC->KC_CODPQ
			MsUnLock()                   
			
			lGravou := .T.
		Else
			If lTravou        
				RecLock("SKO")
					SKO->(dbDelete())
				MsUnlock()
			Endif				
		Endif
		MsUnLock()
		
		End Transaction
		
	Next nX

	//Ŀ
	//Grava os questionarios                                             
	//

	nUsado    := Len(aHeaderSKD)

	For nX := 1 To Len(aColsSKD)    

		Begin Transaction
		
		lTravou := .F.
		If nX <= Len(aRecNoSKD)		
			DbSelectArea("SKD")
			DbGoTo(aRecNoSKD[nX])
			RecLock("SKD")
			lTravou := .T.
		Endif
	
		If ( !aColsSKD[nX][nUsado+1] ) .AND. !Empty( aColsSKD[nX][nPosQt] )
			If !lTravou 
				RecLock("SKD",.T.)			
			Endif
			For nY := 1 To Len(aHeaderSKD)
				If aHeaderSKD[nY][10] <> "V"
					SKD->(FieldPut(FieldPos(aHeaderSKD[nY][2]),aColsSKD[nX][nY]))
				Endif
			Next nY
			SKD->KD_FILIAL := xFilial("SKD")
			SKD->KD_CODPQ  := SKC->KC_CODPQ
			MsUnLock()                   
			
			lGravou := .T.
		Else

			If lTravou        
				RecLock("SKD")
					SKD->(dbDelete())
				MsUnlock()
			Endif				
		Endif
		
		End Transaction
		
	Next nX
                      
Endif

RestArea(aArea)
Return .T.

/*


Ŀ
Funo    GPR010EXC  Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio Rotina de Exclusao de Pesquisa                              
Ĵ
Sintaxe    Gpr010Exc(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       SIGAGPR                                                    
ٱ


*/
Function Gpr010Exc(cAlias,nReg,nOpc)

Local lApaga := .F.		// Flag para verificar se a pesquisa ja foi respondida

//Ŀ
//Verifica se essa Pesquisa ja nao possui Respostas
//
lApaga := GprVldPesq(nOpc,SKC->KC_CODPQ)

If lApaga
	If MsgYesNo(STR0016) // "Deseja realmente efetuar o processo de exclusao da pesquisa e toda sua hierarquia ?"
		Processa({ || Gpr010Lim(cAlias,nReg,nOpc,SKC->KC_CODPQ) })
	Endif
Endif

Return(.T.)

/*/


Ŀ
Funo    Gpr010Lim  Autora  Henry Fila            Data  17/11/03 
Ĵ
Descrio  Programa para Exclusao da Estrutura da Pesquisa            
Ĵ
Sintaxe    Gpr010Lim(ExpC1,ExpN1,ExpN2)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Opcao selecionada                                  
Ĵ
 Uso       SIGAGPR                                                    
ٱ


/*/
Function Gpr010Lim(cAlias,nReg,nOpc,cCodPq)

//Ŀ
//Se existem Questionarios para essa pesquisa comeca a exclusao dos registros abaixos da Pesquisa
//

DbSelectArea( "SKC" )
DbSetOrder(1)
If DbSeek(xFilial( "SKC" )+cCodPq)
	
	Begin Transaction
		
		//Ŀ
		// Passa por todos os Questionarios relacionados a essa Pesquisa                                 
		//
		DbSelectArea( "SKD" )
		DbSetOrder(1) 			// KD_FILIAL+KD_CODPQ+KD_CODQT
		If DbSeek(xFilial( "SKD" ) + cCodPq)
			While 	(!Eof())						  	.AND.;
					(SKD->KD_FILIAL == xFilial( "SKD" )) 	.AND.;
					(SKD->KD_CODPQ  == cCodPq)
			
				//Ŀ
				// Apaga o proprio Questionario onde esta posicionado                                            
				//
				DbSelectArea( "SKD" )
				If RecLock( "SKD", .F.)
					DbDelete()
					MsUnlock()
				Endif
				
				//Ŀ
				// Movimentando a Barra                                                                          
				//
				IncProc( STR0022 ) // "Aguarde... Excluindo a pesquisa e toda sua hierarquia..."
			
				SKD->( DbSkip() )
			
			End
		Endif

		//Ŀ
		// Apaga as regras da pesquisa                                                                   
		//
		DbSelectArea( "SKL" )
		DbSetOrder(1) // FILIAL - PESQUISA
		If DbSeek(xFilial( "SKL" ) + cCodPq)
			While  	(!Eof())							.AND.;
					( SKL->KL_FILIAL == xFilial( "SKL" ) ) .AND.;
					( SKL->KL_CODPQ == cCodPq ) 
					
				If RecLock( "SKL", .F.)
					DbDelete()
					MsUnlock() 
				Endif	
				SKL->( DbSkip() )
			End
		Endif

		//Ŀ
		// Apaga pesquisadores                                                                           
		//
		DbSelectArea( "SKO" )
		DbSetOrder( 1 ) // FILIAL - PESQUISA
		If DbSeek( xFilial("SKO") + cCodPq ) 
			While  	( !Eof() ).AND. ( SKO->KO_FILIAL == xFilial("SKO") ) .AND.;
					( SKO->KO_CODPQ == cCodPq )
					
				If RecLock("SKO",.F.)
					DbDelete()
					MsUnlock() 
				Endif	
				SKO->( DbSkip() )
			End
		Endif
		
		//Ŀ
		// Apos ter apagado os Questionarios e toda a hierarquia vou apagar a Pesquisa                   
		//
		DbSelectArea( "SKC" )
		If RecLock( "SKC", .F.)
			DbDelete()
			MsUnlock()
		Endif
		
	End Transaction 
Endif

Return(.T.)

/*/


Ŀ
Funo    Gpr010Fil  Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Programa para a criacao de FILTROS                         
Ĵ
Sintaxe    GPR010FIL(cFiltro)                                         
Ĵ
Parametros cFiltro - Filtro utilizado                                 
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                     	  
Ĵ
Marcelo K.09/08/04710   -Revisao do fonte:Sintaxe,Documentacao      
                                                                	  
                                                                	  
ٱ


/*/
Function Gpr010Fil(cFiltro)

Local oDlgCons                                             	// Dialog 
Local oListBox                                             	// Objeto da listbox
Local cCampo      	:= Readvar()                           	// Nome do Campo
Local oWnd		  	:= GetWndDefault()                     	// Variavel a ser executada na chamada na tela do filtro
Local aEntidade   	:= {}                                  	// Array da ListBox
Local aDescAlias  	:= {} 									// Array com os alias das Tabelas
Local oOk         	:= LoadBitmap( GetResources(), "LBOK")   // Desenho do Checked
Local oNo         	:= LoadBitmap( GetResources(), "LBNO")   // Desenho do Not Checked
Local lOk         	:= .F.                                  // Confirmacao da tela
Local nI	      	:= 0									// Contador
Local aTabela 	  	:= {{"T5",.T.},{"T8",.F.}} 				// Contem as tabelas com ou sem contatos - Formato:[Nome da tabela, .T. (Com contato)]
Local lRet		  	:= .F.									// Retorno da funcao
Local nLenAux		:= 0                                    // Contador com o Total do array aTabela

If cCampo <> "M->KC_LISTA"
	Help( "", 1, "GPR010FUNN" ) // Funo disponvel somente para o campo Lista // Utilize a funo no campo Lista
	Return( lRet )
Endif

//
//Carrega as tabelas que o Operador podera escolher para executar o filtro generico - Apenas 1 podera ser selecionada  
//
nLenAux := Len( aTabela )
DbSelectArea( "SX5" )
DbSetOrder( 1 )
For nI := 1 To nLenAux
	If DbSeek( xFilial("SX5") + aTabela[nI,1] ) //"T5,T8"
	
		cEntidade := TRIM(SX5->X5_CHAVE)

		While 	(!Eof()) 							.AND.;
				(xFilial("SX5") == SX5->X5_FILIAL) 	.AND.;
				(SX5->X5_TABELA == aTabela[nI,1])

			AAdd(aEntidade,{.F.,;						// Flag
							TRIM(X5DESCRI()),;			// Descricao da Tabela
							TRIM(SX5->X5_CHAVE),;		// Alias da Tabela
							aTabela[nI,2]})				// .T. = T5 com Contato Relacionado a Entidade ou .F.= T8 sem contato Relacionado
			SX5->(DbSkip())
		End
	Endif
Next nI

If Len(aEntidade) == 0
	Help( "", 1, "GPR010PARFIL" ) // No existem entidades parmetrizadas nesta filial para a utilizao do Construtor de Filtros. // Parmetrize alguma filial. 
	Return(lRet)
Endif

//Ŀ
// Monta a Tela    
//
DEFINE MSDIALOG oDlgCons TITLE STR0004 FROM 10,0 To 33,60 OF oMainWnd // Entidade

	//Ŀ
	//Listbox                       
	//
	@ .5,.7 LISTBOX oListBox VAR cListBox Fields HEADER "",;
		STR0004+Space(30),;			// Entidade 
		Substr(STR0005,1,30);		// Tabela
	 SIZE 200,160 
	
	//Ŀ
	//Faz a configuracao da ListBox 
	//
	oListBox:SetArray(aEntidade)
	oListBox:bLine      := { || { 	IIF(aEntidade[oListBox:nAt][1],oOk,oNo),;
									aEntidade[oListBox:nAt,2],;
									aEntidade[oListBox:nAt,3]} }
	// Inverte a selecao no duplo-click
	oListBox:bLDblClick := { || aEval(aEntidade,{|ExpA1| ExpA1[1] := .F.}),;
								aEntidade[oListBox:nAt][1] := !aEntidade[oListBox:nAt][1],oListBox:Refresh() }
	
	//Ŀ
	//Botao de saida                
	//
	DEFINE SBUTTON FROM 006,208 TYPE 1 ACTION (lOk := .T., oDlgCons:End()) ENABLE OF oDlgCons
	DEFINE SBUTTON FROM 022,208 TYPE 2 ACTION (lOk := .F., oDlgCons:End()) ENABLE OF oDlgCons

ACTIVATE MSDIALOG oDlgCons CENTERED

// Se houve a confirmacao do usuario
If lOk               
	nLenAux := Len(aEntidade)
	For nI:=1 To nLenAux
		If aEntidade[nI][1] == .T. .AND. !Empty(aEntidade[nI][3]) // Busca o ALIAS que devo usar
			cFiltro := BuildExpr(aEntidade[nI][3],oWnd)
			
			If !Empty(M->KC_LISTA)
				M->KC_LISTA := M->KC_LISTA + cFiltro
			Else
				M->KC_LISTA := cFiltro
			Endif	
				
		Endif
	Next nI
Endif

Return(.T.)

/*


Ŀ
Funo    GPRA010VLD Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Programa criado para validacao dos campos do SX3           
Ĵ
Sintaxe    GprA010Vld(ExpC1)                                          
Ĵ
Parametros ExpC1 - "H"ora ou "D"ata                                   
Ĵ
 Uso       SIGAGPR / SX3                                              
ٱ


*/
Function GPRA010VLD(cTipo)

Local lRet    := .T.			// Retorno da funcao
Local cValor  := &( ReadVar() )	// Conteudo digitado pelo usuario	
Local cCampo  := ReadVar()	

If !Empty(cValor)
	If Upper(cTipo) == "H"
		If AtVldHora(cValor) // Verifica se a a Hora e Valida
			//
			//Verifica se a hora inicial  maior que a final                 
			//
			If ( cCampo == "M->KC_INICIO" .AND. !Empty(M->KC_FINAL) ) .OR. (cCampo == "M->KC_FINAL")
				If Empty(AtTotHora(M->KC_DTDE,M->KC_INICIO,M->KC_DTATE,M->KC_FINAL,"99:99"))
					Help( "", 1, "GPR010INTHR" ) // Intervalo de horas invlido! // Verifique a hora inicial e final.
					lRet := .F.
				Endif
			Endif
		Else	                                                                                            
			Help( "", 1, "GPR010INTHR" ) // Intervalo de horas invlido! // Verifique a hora inicial e final.		
			lRet := .F.
		Endif
		
	ElseIf UPPER(cTipo) == "D" 
		If ( cCampo == "M->KC_DTDE" .AND. !Empty(M->KC_DTATE) ) .OR. (cCampo == "M->KC_DTATE")
			If !Empty(M->KC_DTATE)
				If M->KC_DTDE > M->KC_DTATE
					Help( "", 1, "GPR010INTDT" ) // Perodo de datas invlido! // Verifique a data inicial e final.
					lRet := .F.
				Endif
			Endif
			
		Endif
	Endif
Endif

Return(lRet)

/*


Ŀ
Funo    GPRA010STA Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Programa criado para validacao dos campos do SX3           
Ĵ
Sintaxe    GPRA010STA()                                               
Ĵ
 Uso       SIGAGPR / SX3                                              
ٱ


*/
Function GPRA010STA()
Local lRet := .F.	// Retorno da funcao

If VAL(M->KC_STATUS) == INATIVA 
	Processa({ || lRet := Gpr010His(M->KC_CODPQ) })
Else
	lRet := .T.
Endif

Return( lRet )

/*


Ŀ
Funo    Gpr010His  Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Verifica o historico da Pesquisa                           
Ĵ
Sintaxe    GPR010HIS()                                                
Ĵ
 Uso       SIGAGPR                                                    
ٱ


*/
Function Gpr010His(cCodPq)

Return( .T. )

/*/


Ŀ
Funcao   Gpr010Cop Autora Henry Fila              Data 05.12.2003
Ĺ
Desc.    Faz a chamada ao procedimento de duplicao de pesquisas... 
͹
Retorno  Logico.                                                     
͹
ParametroEXPC1: Alias proveniente da MBrowse                         
         EXPN2: Registro posicionado na MBrowse                      
         EXPN3: Opcao de Menu Selecionada.                           
͹
Uso       SIGAGPR                                                    
ͼ


*/
Function Gpr010Cop(cAlias, nReg, nOpc)

Local lProcessa := .T. //Variavel de permissao de processamento

//Ŀ
//Verifica se a tabela de pesquisas esta vazia
//
DbSelectArea("SKC")
If Eof()
	Help("",1,"ARQVAZIO")
	lProcessa := .F.
Endif

If lProcessa
	If MsgYesNo(STR0031) // "Deseja realmente efetuar a copia da pesquisa e toda sua hierarquia ?"
		Processa({ || GPR010Dup(SKC->KC_CODPQ) })
	Endif
Endif	

Return(.T.)

/*/


Ŀ
Funcao   Gpr010Dup Autora Henry Fila              Data 05.12.2003
Ĺ
Desc.    Rotina para copia da Pesquisa                               
͹
Sintaxe  Gpr010Cop()                               					 
͹
Parametro                                                            
                                                                     
                                                                     
͹
Uso       SIGAGPR                                                    
ͼ


*/
Function Gpr010Dup(cCodPq)

Local aArea			:= GetArea()			// Salva a area atual
Local cCodPqNew  	:= ""					// Novo codigo da pesquisa
Local nCampo     	:= 0					// Campo para FieldPut
Local aCampos    	:= Array(1,FCount())	// Array com campos para copia
Local lRet       	:= .F.					// Retorno da funcao
Local nRecKD     	:= 0                  	// Posicao na tabela dos questionarios

Begin Transaction

	//Ŀ
	//Copia o SKC - Pesquisa  
	//
	DbSelectArea( "SKC" )
	
	aCampos := Array( 1, FCount() )
	For nCampo := 1 To FCount()
		aCampos[ 1, nCampo ]:= FieldGet(nCampo )
	Next nCampo
	
	RecLock( "SKC", .T. )
	
	For nCampo := 1 To FCount()
	
		If ( Field( nCampo ) == "KC_CODPQ" )
			cCodPqNew := GetSxeNum( "SKC", "KC_CODPQ" )
			FieldPut( nCampo, cCodPqNew )
			ConfirmSx8()
			
		ElseIf ( Field( nCampo ) == "KC_STATUS" )
		
			//Ŀ
			//Efetuar a copia como "Inativa                                                     
			//
			FieldPut( nCampo, "2" )  
			
		ElseIf ( Field( nCampo ) == "KC_DATINAT")

			//Ŀ
			//Data de Inativacao                                                                
			//
			FieldPut(nCampo,space(01)) 
			
		Else
			FieldPut(nCampo,aCampos[1,nCampo])
		Endif

	Next nCampo                                 
		
	MsUnLock()
	DbCommit()
		
	//Ŀ
	//Copia o SKD - Questionarios |
	//

	DbSelectArea("SKD")
	DbSetOrder(1) // KD_FILIAL+KD_CODPQ+KD_CODQT
	DbSeek(xFilial("SKD") + cCodPq) 
	While	(!Eof()) 							.AND.;
			(SKD->KD_FILIAL == xFilial("SKD"))  .AND.;
			(SKD->KD_CODPQ  == cCodPq)

		//Ŀ
		//Salva o Registro atual para reposicionar novamente                                
		//
		nRecKD := Recno()
		
		aCampos := Array( 1, FCount() )
		For nCampo := 1 To FCount()
			aCampos[1,nCampo] := FieldGet(nCampo)
		Next nCampo
		
		RecLock("SKD",.T.)
		
		For nCampo:= 1 To FCount()
			If ( Field( nCampo ) == "KD_CODPQ" )
				FieldPut( nCampo, cCodPqNew )
			Else
				FieldPut( nCampo, aCampos[ 1, nCampo] )
			Endif                                       
		Next nCampo
		
		MsUnLock()
		DbCommit()
		
		//Ŀ
		//Volta ao registro copiado                                                         
		//
		DbSelectArea("SKD")
		DbGoTo(nRecKD)
		SKD->( DbSkip() )
		
	End

End Transaction

RestArea( aArea )

Return( lRet )

/*/


Ŀ
Funo    Gpr010Pesq   Autor Henry Fila            Data  22.02.05 
Ĵ
Descrio  Amarracao Pesquisa x Pesquisadores                         
Ĵ
Sintaxe    Void Qpr010Pesq()                                          
Ĵ
Parametros ExpA1: aHeader da Pesquisa                                 
           ExpA2: aCols da Pesquisa                                   
Ĵ
 Uso       GPRA010                                                    
ٱ


/*/
Function Gpr010Pesq(aHeaderSKO, aColsSKO)

Local aArea    := GetArea()                          
Local aSavCols := aClone(aColsSKO)

Local cCadastro:= STR0045 //Pesquisadores

Local lRetorno := .T.

Local nOpca    := 0

Local oDlg                                          
Local oGetF4
Local oGetdSKO

DEFINE MSDIALOG oDlg TITLE cCadastro From 10,0 To 28,80
	oGetdSKO := MsNewGetDados():New(035,001,125,316,IIF(Altera .OR. Inclui, GD_INSERT+GD_DELETE+GD_UPDATE,0),"Gpr010LOk2()","AllwaysTrue()",,,,MAXGETDAD,,,,,aHeaderSKO,aSavCols)	
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA := 1,If(oGetdSKO:TudoOk(),oDlg:End(),nOpcA := 0),If(nOpca ==1,aColsSKO := oGetDSKO:aCols,)},{||oDlg:End()})	

RestArea(aArea)

Return( lRetorno )

/*


Ŀ
Funo    Gpr010LOk2 Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Validacao da linha dos pesquisadores                       
Ĵ
Sintaxe    Gpr010LOk2()                                               
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
          09/08/04710                                               
                                                                    
                                                                    
ٱ


*/
Function Gpr010LOk2()

Local lRet := .T.

Local nPosCod  := AScan(aHeader,{|x| AllTrim(x[2])=="KO_CODPSQ"})
Local nUsado   := Len(aHeader)+1
Local nX       := 0             

//Ŀ
//Verifica se ja existe o pesquisador informado                     
//
For nX := 1 To Len(aCols)
	If !aCols[nX][nUsado] .AND. n <> nX
		If aCols[nX][nPosCod] == aCols[n][nPosCod]
			Help(" ",1,"JAGRAVADO")
			lRet := .F.
		Endif
	Endif		
Next nX	

Return( lRet )


/*/


Ŀ
Funo    Gpr010Ques   Autor Henry Fila            Data  22.02.05 
Ĵ
Descrio  Amarracao Pesquisa x questionarios                         
Ĵ
Sintaxe    Void Qpr010Ques()                                          
Ĵ
Parametros ExpA1: aHeader da Pesquisa                                 
           ExpA2: aCols da Pesquisa                                   
Ĵ
 Uso       GPRA010                                                    
ٱ


/*/
Function Gpr010Ques(aHeaderSKD, aColsSKD)

Local aArea    := GetArea()                          
Local aSavCols := aClone(aColsSKD)

Local cCadastro:= STR0046 //"Questionarios"

Local lRetorno := .T.

Local nOpca    := 0

Local oDlg                                          
Local oGetF4
Local oGetdSKD

DEFINE MSDIALOG oDlg TITLE cCadastro From 10,0 To 28,80
	oGetdSKD := MsNewGetDados():New(035,001,125,316,IIF(Altera .OR. Inclui, GD_INSERT+GD_DELETE+GD_UPDATE,0),"Gpr010LOk3()","AllwaysTrue()",,,,MAXGETDAD,,,,,aHeaderSKD,aSavCols)	
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA := 1,If(oGetdSKD:TudoOk(),oDlg:End(),nOpcA := 0),If(nOpca ==1,aColsSKD := oGetDSKD:aCols,)},{||oDlg:End()})	

RestArea(aArea)

Return( lRetorno )

/*


Ŀ
Funo    Gpr010LOk2 Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Validacao da linha dos pesquisadores                       
Ĵ
Sintaxe    Gpr010LOk2()                                               
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
          09/08/04710                                               
                                                                    
                                                                    
ٱ


*/
Function Gpr010LOk3()

Local lRet 		:= .T.

Local nPosCod  	:= AScan( aHeader, { |x| AllTrim( x[2] ) == "KD_CODQT" } )
Local nUsado   	:= Len( aHeader ) + 1
Local nX       	:= 0             

//Ŀ
//Verifica se ja existe o questionario informado
//
For nX := 1 To Len( aCols )
	If !aCols[nX][nUsado] .AND. n <> nX
		If aCols[nX][nPosCod] == aCols[n][nPosCod]
			Help(" ",1,"JAGRAVADO")
			lRet := .F.
		Endif
	Endif		
Next nX	

Return( lRet )

/*


Ŀ
Funo    Gpr010Prv  Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Preview da pesquisa                                        
Ĵ
Sintaxe    Gpr010LOk2()                                               
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
          09/08/04710                                               
                                                                    
                                                                    
ٱ


*/
Function Gpr010Prv()                                  

Local cParam := ""

cParam += fPesq2URL( SKC->KC_CODPQ + "000000000000000000000000111" )

ShellExecute("open",GetSrvProfString("UrlGpr","")+"w_pgpr000.apw?cID="+cParam, "","",SW_SHOW)

Return

/*


Ŀ
Funo    Gpr010Atv  Autor   Henry Fila            Data  20/10/03 
Ĵ
Descrio  Ativa ou desativa uma determinada pesquisa                 
Ĵ
Sintaxe    Gpr010Atv()                                                
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Marcelo K.09/08/04710   -Revisao do fonte:Sintaxe, documentacao     
                                                                    
                                                                    
ٱ


*/
Function Gpr010Atv()

If VAL(SKC->KC_STATUS) == INATIVA

	If MsgYesNo(  STR0055 ) //"Esta pesquisa se encontra desativada, deseja ativa-la para que seja iniciada a pesquisa ?"
		GprAvalSKC( "SKC" , 1 )
	Endif               
	
Else

	If MsgYesNo( STR0056 ) 
		GprAvalSKC( "SKC" , 2 )	
	Endif
	
Endif	

Return( .T. ) 


/*


Ŀ
Funo    Gpr010Link Autor   Henry Fila            Data  20/10/03 
Ĵ
Descrio  Mostra o link oficial da pesquisa                          
Ĵ
Sintaxe    Gpr010Link()                                                
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Marcelo K.09/08/04710   -Revisao do fonte:Sintaxe, documentacao     
                                                                    
                                                                    
ٱ


*/
Function Gpr010Link( cCodPesq )

Local cLink := GprxLink( cCodPesq ) //Gera o link oficial e mostra
Local cBitMap   := "LOGIN"          //Bitmap da tela

Local oDlg                          //Variaval da dialog

DEFINE MSDIALOG oDlg Title STR0057 From 200,001 To 400,780 Pixel //"Link da pesquisa"

	@ 10 , 0 BITMAP oBmp RESNAME cBitMap oF oDlg SIZE 48,488 NOBORDER WHEN .F. PIXEL

	@ 055,050 Say STR0057 Size 90,7 Of oDlg Pixel //"Para: "
	@ 055,0100 MsGet cLink  When .T. Size 280,10 Of oDlg Pixel

ACTIVATE DIALOG oDlg CENTERED ON INIT EnchoiceBar( oDlg, { || nOpca := 1,oDlg:End() }, {||nopca := 0,oDlg:End()})		

Return( .T. ) 

/*


Ŀ
Funo    Gpr010Leg  Autora  Henry Fila            Data  20/10/03 
Ĵ
Descrio  Monta a Legenda das Pesquisas                              
Ĵ
Sintaxe    Gpr010Leg()                                                
Ĵ
 Uso       SIGAGPR                                                    
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                         
Ĵ
Marcelo K.09/08/04710   -Revisao do fonte:Sintaxe, documentacao     
                                                                    
                                                                    
ٱ


*/
Function Gpr010Leg()  

/*
  Ŀ
  VERMELHO  - Ativa                                 
  VERDE     - Inativa                               
  PRETO     - Vencida                               
  
*/

Local aLegenda := {	{"BR_VERMELHO",	STR0024},; 	// "Ativa"
					{"BR_VERDE",	STR0028}}

BrwLegenda(STR0030,STR0023,aLegenda) 			//"Pesquisas","Legenda"

Return( .T. )

/*/


Ŀ
Funo    GPR010AfterCols  Autor  Conrado Q.       Data  26/01/2007 
Ĵ
Descrio Rotina executada durante a montagem do aCols                  
Ĵ
Sintaxe   GPR010AfterCols( ExpC1, ExpA2, ExpL3 )                        
Ĵ
ParametrosExpC1: Alias da tabela utilizada no FillGetDados              
          ExpA2: Array de registros utilizados                          
          ExpL3: Se  copia ou no                                      
Ĵ
Uso        Pesquisa e Resultado                                         
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
Static Function GPR010AfterCols( cAlias, aRecnoArray, lCopia )
	If ( Alias() == cAlias )
		If !lCopia
			aAdd( aRecnoArray, (cAlias)->( Recno() ) )
		EndIf
	EndIf
Return( .T. )
