#INCLUDE "Matr965.ch"
#include "inkey.ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR965  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 25.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Declaracao de Movimento Economico Fiscal - DMEF            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR965(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Andreia      ³01/10/99³24535A³ Interpretacao de sintaxe clipper para  ³±±
±±³              ³        ³      ³ Nao-Contribuintes.                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATR965

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva Ambiente                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local	aSavEnv	:=	MSSavEnv()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aTotCFO	:=	{}
Local aResumo	:=	{}
Local aTotEst	:=	{}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao do Programa                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private	titulo		:=	STR0001 //"Declaracao do Movimento Economico Fiscal - DMEF"
Private	Tamanho		:=	"M"

Private	cDesc1		:=	STR0002 //"Ir  imprimir mapa para Declara‡„o do Movimento Econ“mico Fiscal"
Private	cDesc2		:=	STR0003 //"conforme per¡odo informado."
Private cDesc3		:=	"" 
Private	cString		:=	"SF3"
Private	cabec1		:=	""
Private	cabec2		:=	""

Private	lEnd		:=	.F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis de Controle de Impressao                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private	wnrel		:=	"MATR965"
Private	m_pag		:=	1

Private	aReturn 	:=	{ STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
Private	aDataIni 	:=	DataInicio()
Private	aDataFim 	:=	DataFinal()

Private	cPerg		:=	"MTR965"
Private	cbcont		:= 0
Private	cbtxt		:=	SPACE(10)

Private	nLastKey	:=	0
Private	nLin		:=	80

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros utilizados:                                       ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³ mv_par01 = De Data                                           ³
//³ mv_par02 = Ate Data                                          ³
//³ mv_par03 = Numero do livro a ser processado                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SetPrint                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel	:=	SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho)
If nLastKey==27
	MSSavEnv(aSavEnv)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica impressora                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetDefault(aReturn,cString)
If nLastKey==27
	MSSavEnv(aSavEnv)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa relatorio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RptStatus({|lEnd| R965Imp(@lEnd,wnRel,cString,Tamanho)},titulo)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura Ambiente                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MSSavEnv(aSavEnv)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Spool de Impressao                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[5] = 1
	Set Printer TO
	dbcommitAll()
	ourspool(wnrel)
Endif

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Imp  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 18.12.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime Relatorio                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Imp(lEnd,wnRel,cString,Tamanho)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe arquivo de configuracao                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A965LeIni("",@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Totaliza CFO's e resume por estado                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif

SetRegua(7)
IncRegua()
aResumo	:=	R965TotCFO(mv_par01,mv_par02,@lEnd,mv_par03)
aTotCFO	:=	aResumo[1] // Detalhamento por CFO
aTotEst	:=	aResumo[2] // Detalhamento por Estado

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte A - Entradas        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
R965Cabec(@nLin,.T.)
R965Entradas(aTotCFO,@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte B - Saidas          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
R965Saidas(aTotCFO,@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte C - Estoques        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
nlin := 80
R965Cabec(@nLin)
R965Estoque(mv_par01,mv_par02,@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte D - Industrializacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
R965Indust(aTotCFO,@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte E - Inf.Contabeis   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
R965InfContab(mv_par01,mv_par02,@lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parte F - Detal das Operac³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Interrupcao(@lEnd)
	Return
Endif
IncRegua()
R965DetOperac(aTotEst,@lEnd)
If !lEnd
	Roda(cbcont,cbtxt,Tamanho)
Endif

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Cabec³ Autor ³ Juan Jose Pereira     ³ Data ³ 25.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime cabecalho                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Cabec(nLin,lFirst)

lFirst	:=	IIf(Empty(lFirst),.F.,lFirst)

If nLin>=55 .and. !lEnd
	If !lFirst
		@ ++nLin,117 pSay STR0006 //"( Continua... )"
		lFirst:=.F.
	Endif
	Cabec(titulo,cabec1,cabec2,wnrel,Tamanho)
	nLin	:=	PRow()
	@ ++nLin,00 pSay Replic("*",132)
	nLin++
Endif

Return (nLin)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Entradas ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes referente a movimentacoes de entradas  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Entradas(aTotCFO,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao das Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aApuracao	:=	{}
Local aImpostos	:=	{}
Local aTotal	:=	{}
Local aCampos	:=	{}

Local cCFO		:=	""

Local i			:=	0
Local nx		:=	0
Local nEl		:=	0
Local nCol		:=	0
Local nValICM	:=	0
Local nValIPI	:=	0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Bloqueia funcao                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd .or. Empty(aTotCFO)
	Return
Endif                

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a estrutura dos array acumuladores                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Transf Recebidas ¿
// Importacoes ÄÄÄ¿ ³
// Merc Interno ¿ ³ ³
AADD(aApuracao,{0,0,0,STR0007,"111","112","113"})	//"Mat.Prima,Mat.Secund.,Embalag.,Serv."
AADD(aApuracao,{0,0,0,STR0008,"","","123"}) 		//"Produtos (Producao Propria)"
AADD(aApuracao,{0,0,0,STR0009,"131","132","133"})	//"Mercadoria para Revenda"
AADD(aApuracao,{0,0,0,STR0010,"141","142","143"})	//"Energia Eletrica"
AADD(aApuracao,{0,0,0,STR0011,"151","152",""})		//"Servicos de Comunicacao"
AADD(aApuracao,{0,0,0,STR0012,"161","162",""})		//"Servicos de Transporte"
AADD(aImpostos,{0,0,0,STR0013,"171","172","173"})	//"ICMS Creditado"
AADD(aImpostos,{0,0,0,STR0014,"181","182","183"})	//"IPI Creditado"
aTotal:=	   {0,0,0,STR0015,"191","192","193"}	//"Total"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Direciona totais dos CFOS nas colunas do DMEF                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCampos:={	"111","112","113",;
	"123",;
	"131","132","133",;
	"141","142","143",;
	"151","152",;
	"161","162"}

For nx:=1 to Len(aCampos)
	
	cCFO	:=	A965LeIni(aCampos[nx],@lEnd)
	
	If lEnd
		Exit
	Endif            
	
	If Empty(cCFO)
		Loop
	Endif
	
	nEl					:=	Val(Substr(aCampos[nx],2,1))
	nCol				:=	Val(Substr(aCampos[nx],3,1))
	aApuracao[nEl,nCol]	:=	R965ScanCFO(aTotCFO,cCFO,"VCNT")
	nValICM				:=	R965ScanCFO(aTotCFO,cCFO,"ICMS")
	nValIPI				:=	R965ScanCFO(aTotCFO,cCFO,"IPI")
	aImpostos[1,nCol]	+=	nValICM
	aImpostos[2,nCol]	+=	nValIPI
	aTotal[nCol]		+=	(aApuracao[nEl,nCol]-(nValICM+nValIPI))
	
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do Mapa                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0051 //"|Entradas/Aquisicoes                              | Compras no Mercado Interno|       Importacoes        | Transferencias Recebidas |"
@ ++nLin,00 pSay "|-------------------------------------------------|---------------------------|--------------------------|--------------------------|"
//               |XXXXXXXXXXXXXXXXXXXXX 49 XXXXXXXXXXXXXXXXXXXXXXXX|XXX|  999,999,999.999      |XXX|  999,999,999.99      |XXX|  999,999,999.99      |
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x12
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to Len(aApuracao)

	If Interrupcao(@lEnd)
		Return
	Endif
	
	@ ++nLin,00	pSay "|"
	@ nLin,001 	pSay aApuracao[i,4]
	@ nLin,050	pSay "|"
	@ nLin,051	pSay aApuracao[i,5]
	@ nLin,054	pSay "|"
	@ nLin,060	pSay aApuracao[i,1] Picture IIf(Empty(aApuracao[i,5]),"@Z","@E 999,999,999.99")
	@ nLin,078	pSay "|"
	@ nLin,079	pSay aApuracao[i,6]
	@ nLin,082	pSay "|"
	@ nLin,088	pSay aApuracao[i,2] Picture IIf(Empty(aApuracao[i,6]),"@Z","@E 999,999,999.99")
	@ nLin,105	pSay "|"
	@ nLin,106	pSay aApuracao[i,7]
	@ nLin,109	pSay "|"
	@ nLin,115	pSay aApuracao[i,3] Picture IIf(Empty(aApuracao[i,7]),"@Z","@E 999,999,999.99")
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|-------------------------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
Next            

For i:=1 to 2

	If Interrupcao(@lEnd)
		Return
	Endif
	
	@ ++nLin,00 pSay "|"
	
	If i==1
		@ nLin,01 pSay STR0016 //"Informacoes   |"
	Else
		@ nLin,01 pSay "              |"
	Endif
	
	@ nLin,016 pSay aImpostos[i,4]
	@ nLin,050 pSay "|"
	@ nLin,051 pSay aImpostos[i,5]
	@ nLin,054 pSay "|"
	@ nLin,060 pSay aImpostos[i,1] Picture "@E 999,999,999.99"
	@ nLin,078 pSay "|"
	@ nLin,079 pSay aImpostos[i,6]
	@ nLin,082 pSay "|"
	@ nLin,088 pSay aImpostos[i,2] Picture "@E 999,999,999.99"
	@ nLin,105 pSay "|"
	@ nLin,106 pSay aImpostos[i,7]
	@ nLin,109 pSay "|"
	@ nLin,115 pSay aImpostos[i,3] Picture "@E 999,999,999.99"
	@ nLin,132 pSay "|"
	
	If i==1
		@ ++nLin,00 pSay STR0017 //"|Complementares|----------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
	Else
		@ ++nLin,00 pSay "|-------------------------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
	Endif
Next

If Interrupcao(@lEnd)
	Return
Endif

@ ++nLin,00	pSay "|"
@ nLin,001 	pSay aTotal[4]
@ nLin,050 	pSay "|"
@ nLin,051 	pSay aTotal[5]
@ nLin,054 	pSay "|"
@ nLin,060 	pSay aTotal[1] Picture "@E 999,999,999.99"
@ nLin,078 	pSay "|"
@ nLin,079 	pSay aTotal[6]
@ nLin,082 	pSay "|"
@ nLin,088 	pSay aTotal[2] Picture "@E 999,999,999.99"
@ nLin,105 	pSay "|"
@ nLin,106 	pSay aTotal[7]
@ nLin,109	pSay "|"
@ nLin,115 	pSay aTotal[3] Picture "@E 999,999,999.99"
@ nLin,132 	pSay "|"
@ ++nLin,00	pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"

aApuracao	:= Nil
aImpostos	:= Nil
aTotal		:= Nil
aCampos		:= Nil

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Saidas   ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes referente a movimentacoes de saidas    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Saidas(aTotCFO,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Declaracao das Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aApuracao	:=	{}
Local aImpostos	:=	{}
Local aTotal	:=	{}
Local aCampos	:=	{}

Local cCFO		:=	""

Local i			:=	0
Local nx		:=	0
Local nValICM	:=	0
Local nValIPI	:=	0
Local nEl		:=	0
Local nCol		:=	0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Bloqueia funcao                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd .or. Empty(aTotCFO)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a estrutura dos array acumuladores                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Transf Recebidas ¿
// Importacoes ÄÄÄ¿ ³
// Merc Interno ¿ ³ ³
AADD(aApuracao,{0,0,0,STR0007,"211","212","213"}) //"Mat.Prima,Mat.Secund.,Embalag.,Serv."
AADD(aApuracao,{0,0,0,STR0008,"221","222","223"}) //"Produtos (Producao Propria)"
AADD(aApuracao,{0,0,0,STR0009,"231","232","233"}) //"Mercadoria para Revenda"
AADD(aApuracao,{0,0,0,STR0010,"241","242","243"}) //"Energia Eletrica"
AADD(aApuracao,{0,0,0,STR0011,"251","252",""}) //"Servicos de Comunicacao"
AADD(aApuracao,{0,0,0,STR0012,"261","262",""}) //"Servicos de Transporte"
AADD(aImpostos,{0,0,0,STR0018,"271","272","273"}) //"ICMS Debitado"
AADD(aImpostos,{0,0,0,STR0019,"281","282","283"}) //"IPI Debitado"
aTotal:=	   {0,0,0,STR0015,"291","292","293"} //"Total"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Direciona totais dos CFOS nas colunas do DMEF                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCampos:={	"211","212","213",;
	"221","222","223",;
	"231","232","233",;
	"241","242","243",;
	"251","252",;
	"261","262"}

For nx:=1 to Len(aCampos)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancelamento do Operador ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Interrupcao(@lEnd)
		Return
	Endif

	cCFO:=A965LeIni(aCampos[nx],@lEnd)

	If lEnd
		Exit
	Endif

	If Empty(cCFO)
		Loop
	Endif

	nEl			  		:=	Val(Substr(aCampos[nx],2,1))
	nCol				:=	Val(Substr(aCampos[nx],3,1))
	aApuracao[nEl,nCol]	:=	R965ScanCFO(aTotCFO,cCFO,"VCNT")
	nValICM				:=	R965ScanCFO(aTotCFO,cCFO,"ICMS")
	nValIPI				:=	R965ScanCFO(aTotCFO,cCFO,"IPI")
	aImpostos[1,nCol]	+=	nValICM
	aImpostos[2,nCol]	+=	nValIPI
	aTotal[nCol]		+=	(aApuracao[nEl,nCol]-(nValICM+nValIPI))
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do Mapa                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLin++
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0020 //"|Saidas/Prestacoes                                | Vendas no Mercado Interno |       Exportacoes        | Transferencias Remetidas |"
@ ++nLin,00 pSay "|-------------------------------------------------|---------------------------|--------------------------|--------------------------|"
//               |XXXXXXXXXXXXXXXXXXXXX 49 XXXXXXXXXXXXXXXXXXXXXXXX|      999,999,999.999      |      999,999,999.99      |      999,999,999.99      |
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x12
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to Len(aApuracao)

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00	pSay "|"
	@ nLin,01 	pSay aApuracao[i,4]
	@ nLin,50 	pSay "|"
	@ nLin,51 	pSay aApuracao[i,5]
	@ nLin,54 	pSay "|"
	@ nLin,60 	pSay aApuracao[i,1] Picture IIf(Empty(aApuracao[i,5]),"@Z","@E 999,999,999.99")
	@ nLin,78 	pSay "|"
	@ nLin,79 	pSay aApuracao[i,6]
	@ nLin,82 	pSay "|"
	@ nLin,88 	pSay aApuracao[i,2] Picture IIf(Empty(aApuracao[i,6]),"@Z","@E 999,999,999.99")
	@ nLin,105 	pSay "|"
	@ nLin,106 	pSay aApuracao[i,7]
	@ nLin,109 	pSay "|"
	@ nLin,115 	pSay aApuracao[i,3] Picture IIf(Empty(aApuracao[i,7]),"@Z","@E 999,999,999.99")
	@ nLin,132 	pSay "|"
	@ ++nLin,00	pSay "|-------------------------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
Next            

For i:=1 to 2

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00 pSay "|"

	If i==1
		@ nLin,01 pSay STR0021 //"Informacoes   |"
	Else
		@ nLin,01 pSay "              |"
	Endif

	@ nLin,016 pSay aImpostos[i,4]
	@ nLin,050 pSay "|"
	@ nLin,051 pSay aImpostos[i,5]
	@ nLin,054 pSay "|"
	@ nLin,060 pSay aImpostos[i,1] Picture "@E 999,999,999.99"
	@ nLin,078 pSay "|"
	@ nLin,079 pSay aImpostos[i,6]
	@ nLin,082 pSay "|"
	@ nLin,088 pSay aImpostos[i,2] Picture "@E 999,999,999.99"
	@ nLin,105 pSay "|"
	@ nLin,106 pSay aImpostos[i,7]
	@ nLin,109 pSay "|"
	@ nLin,115 pSay aImpostos[i,3] Picture "@E 999,999,999.99"
	@ nLin,132 pSay "|"

	If i==1
		@ ++nLin,00 pSay STR0017 //"|Complementares|----------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
	Else
		@ ++nLin,00 pSay "|-------------------------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
	Endif
Next

If Interrupcao(@lend)
	Return
Endif

@ ++nLin,00	pSay "|"
@ nLin,001	pSay aTotal[4]
@ nLin,050 	pSay "|"
@ nLin,051 	pSay aTotal[5]
@ nLin,054 	pSay "|"
@ nLin,060 	pSay aTotal[1] Picture "@E 999,999,999.99"
@ nLin,078 	pSay "|"
@ nLin,079 	pSay aTotal[6]
@ nLin,082 	pSay "|"
@ nLin,088 	pSay aTotal[2] Picture "@E 999,999,999.99"
@ nLin,105 	pSay "|"
@ nLin,106 	pSay aTotal[7]
@ nLin,109 	pSay "|"
@ nLin,115 	pSay aTotal[3] Picture "@E 999,999,999.99"
@ nLin,132 	pSay "|"
@ ++nLin,00	pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"

aApuracao	:= Nil
aImpostos	:= Nil
aTotal		:= Nil
aCampos		:= Nil

Return         

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Estoque  ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes referente a estoque                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Estoque(dDataIni,dDataFim,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aApuracao		:=	{}
Local aTotal		:=	{}
Local aCampos		:=	{}
Local aProduto		:=	{} 
Local aTeste		:=	{}

Local cSeek			:=	""
Local cAliasSB1		:= 	"SB1"
Local cAliasSB2		:= 	"SB2"

Local i				:=	0
Local nx			:=	0	
Local nCustoUnit	:=	0

#IFDEF TOP
	Local aStruSB1	:=	{}
	Local cQuery	:=	""
#ELSE
	Local cIndex    :=	""
	Local cCondicao :=	""
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Bloqueia funcao                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd
	Return
Endif                   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a estrutura dos array acumuladores                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Em Terceiros     ¿
// Saldo Inicial Ä¿ ³
// Saldo Final  ¿ ³ ³
AADD(aApuracao,{0,0,0,STR0022,"311","312","313"}) 	//"Mat.Prima,Mat.Secund.,Embalagem"
AADD(aApuracao,{0,0,0,STR0008,"321","322","323"}) 	//"Produtos (Producao Propria)"
AADD(aApuracao,{0,0,0,STR0009,"331","332","333"}) 	//"Mercadoria para Revenda"
AADD(aApuracao,{0,0,0,STR0023,"341","342","343"}) 	//"Produtos em Elaboracao"
aTotal:=	   {0,0,0,STR0015,"351","352","353"} 	//"Total"

dbSelectArea("SB1")
dbSetOrder(1)
ProcRegua(LastRec())
#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery 		:= .T.
		cAliasSB1	:= "B1_MTR965"
		cAliasSB2	:= "B1_MTR965"
		aStruSB1	:= SB1->(dbStruct())
		cQuery		:= "SELECT SB1.*,SB2.B2_FILIAL,SB2.B2_COD,SB2.B2_LOCAL,SB2.B2_QTER "
		cQuery 		+= "FROM " + RetSqlName("SB1") + " SB1 , "
		cQuery 		+= RetSqlName("SB2") + " SB2 "
		cQuery 		+= "WHERE SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
		cQuery 		+= "SB2.B2_FILIAL = '" + xFilial("SB2") + "' AND "		
		cQuery 		+= "SB1.B1_COD = SB2.B2_COD "
		cQuery 		+= "ORDER BY " + SqlOrder(SB1->(IndexKey()))
		cQuery 		:= ChangeQuery(cQuery)                       
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSB1)
		For nX := 1 To len(aStruSB1)
			If aStruSB1[nX][2] <> "C" .And. FieldPos(aStruSB1[nX][1])<>0
				TcSetField(cAliasSB1,aStruSB1[nX][1],aStruSB1[nX][2],aStruSB1[nX][3],aStruSB1[nX][4])
			EndIf
		Next nX
		dbSelectArea(cAliasSB1)	
	Else
#ENDIF
		dbSelectArea("SB1")
		dbSetOrder(1)
		dbSeek(xFilial(),.T.) // Posiciona no primeiro produto da Filial
	    ProcRegua(LastRec())
	    dbGoTop()
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Busca saldo dos Produtos no Estoque e posiciona nas colunas do DMEF ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aCampos	:=	{"311","321","331","341"}
For nx	:=	1 to Len(aCampos)
	cExpressao := A965LeIni(aCampos[nx],@lEnd)
	If lQuery
		// Troca o arquivo SB1 pelo alias (cAliasSB1)
		Do While .T.
			nPosi := At("SB1",cExpressao)
			If nPosi == 1                       
				cExpressao := Alltrim(cAliasSB1) + SubStr(cExpressao,4,Len(cExpressao))
			Elseif nPosi <> 0
				cExpressao := SubStr(cExpressao,1,nPosi-1) + Alltrim(cAliasSB1) + SubStr(cExpressao,nPosi+3,Len(cExpressao))
			Else
				Exit
			Endif
		Enddo                    
	Endif
	Aadd(aProduto,cExpressao)
Next    

If Len(aProduto) > 0

	While (cAliasSB1)->B1_FILIAL == xFilial("SB1") .And. !lEnd  .And. !(cAliasSB1)->(Eof())
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cancelamento do Operador ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IncProc(STR0052) // Selecionando Registros
		If Interrupcao(@lEnd)
			Return
		Endif

		If !lQuery
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica os saldos fisico/financeiro³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SB2->(dbSetOrder(1))
			If !SB2->(dbSeek(xFilial("SB2")+SB1->B1_COD))
				SB1->(dbSkip())
				Loop
			Endif
			cSeek := xFilial("SB2")+SB2->B2_COD
		Endif
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Filtra Produtos ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nX := 1 to Len(aProduto)
		
			cProduto := aProduto[nX]
			
			If !Empty(cProduto)
			
				If !(&cProduto)
					Loop
				Endif

				If !lQuery
				
					Do While (!SB2->(Eof())) .And. xFilial("SB2")+SB2->B2_COD == cSeek .And. !lEnd
		
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Cancelamento do Operador ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If Interrupcao(@lEnd)
							Return
						Endif 
						
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Acumula os saldos³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aSaldoIni		:=	CalcEst(SB2->B2_COD,SB2->B2_LOCAL,dDataIni)
						aSaldoFim		:=	CalcEst(SB2->B2_COD,SB2->B2_LOCAL,dDataFim)
						aApuracao[nx,1]	+=	aSaldoIni[2]
						aApuracao[nx,2]	+=	aSaldoFim[2]
						If aSaldoFim[1] > 0
							nCustoUnit	:=	aSaldoFim[2] / aSaldoFim[1]
						Else
							nCustoUnit	:=	0
						Endif
						aApuracao[nx,3]	+=	SB2->B2_QTER * nCustoUnit
						SB2->(dbSkip())
					Enddo
				Else
					aSaldoIni		:=	CalcEst((cAliasSB2)->B2_COD,(cAliasSB2)->B2_LOCAL,dDataIni)
					aSaldoFim		:=	CalcEst((cAliasSB2)->B2_COD,(cAliasSB2)->B2_LOCAL,dDataFim)
					aApuracao[nx,1]	+=	aSaldoIni[2]
					aApuracao[nx,2]	+=	aSaldoFim[2]
					If aSaldoFim[1] > 0
						nCustoUnit	:=	aSaldoFim[2] / aSaldoFim[1]
					Else
						nCustoUnit	:=	0
					Endif
					aApuracao[nx,3]	+=	(cAliasSB2)->B2_QTER * nCustoUnit
				Endif
			Endif
		Next                    
		(cAliasSB1)->(dbSkip())
	Enddo   
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Acumula os totais para apresentacao³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 to Len(aApuracao)
	aTotal[1]	+=	aApuracao[nx,1]
	aTotal[2]	+=	aApuracao[nx,2]
	aTotal[3]	+=	aApuracao[nx,3]
Next

dbSelectArea("SF3")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do Mapa                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLin++
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0024 //"|                                                 |               No Estabelecimento                     |  Em Poder de Terceiros   |"
@ ++nLin,00 pSay STR0025 //"|Estoques Proprios                                |          Inicial          |          Final           |          Final           |"
@ ++nLin,00 pSay "|-------------------------------------------------|---------------------------|--------------------------|--------------------------|"
//               |XXXXXXXXXXXXXXXXXXXXX 49 XXXXXXXXXXXXXXXXXXXXXXXX|      999,999,999.999      |      999,999,999.99      |      999,999,999.99      |
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x12
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to Len(aApuracao)

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00	pSay "|"
	@ nLin,001	pSay aApuracao[i,4]
	@ nLin,050	pSay "|"
	@ nLin,051	pSay aApuracao[i,5]
	@ nLin,054	pSay "|"
	@ nLin,060	pSay aApuracao[i,1] Picture "@E 999,999,999.99"
	@ nLin,078	pSay "|"
	@ nLin,079	pSay aApuracao[i,5]
	@ nLin,082	pSay "|"
	@ nLin,088	pSay aApuracao[i,2] Picture "@E 999,999,999.99"
	@ nLin,105	pSay "|"
	@ nLin,106	pSay aApuracao[i,5]
	@ nLin,109	pSay "|"
	@ nLin,115	pSay aApuracao[i,3] Picture "@E 999,999,999.99"
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|-------------------------------------------------+---+-----------------------+---+----------------------+---+----------------------|"
Next

@ ++nLin,00	pSay "|"
@ nLin,001	pSay aTotal[4]
@ nLin,050	pSay "|"
@ nLin,051	pSay aTotal[5]
@ nLin,054	pSay "|"
@ nLin,060	pSay aTotal[1] Picture "@E 999,999,999.99"
@ nLin,078	pSay "|"
@ nLin,079	pSay aTotal[5]
@ nLin,082	pSay "|"
@ nLin,088	pSay aTotal[2] Picture "@E 999,999,999.99"
@ nLin,105	pSay "|"
@ nLin,106	pSay aTotal[5]
@ nLin,109	pSay "|"
@ nLin,115	pSay aTotal[3] Picture "@E 999,999,999.99"
@ nLin,132	pSay "|"
@ ++nLin,00	pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"

nLin++                    

aApuracao	:=	Nil
aTotal		:=	Nil
aCampos		:=	Nil

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Elimina as areas de trabalho utilizadas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	dbSelectArea(cAliasSB1)
	dbCloseArea()
Else
   	dbSelectArea("SB1")
	RetIndex("SB1")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
Endif	

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965Indust   ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes referente a industrializacao           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965Indust(aTotCFO,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aApuracao		:=	{}
Local aTotal		:=	{}
Local aCampos		:=	{}

Local i				:=	0
Local nx			:=	0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Bloqueia funcao                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd
	Return
Endif                   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a estrutura dos array acumuladores                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
// Valor Agregado ÄÄÄÄ¿
// Mercadoria ÄÄÄÄÄÄ¿ ³
// Valor Agregado ¿ ³ ³
// Mercadoria ÄÄ¿ ³ ³ ³
AADD(aApuracao,{0,0,0,0,STR0026,"411","","413",""}) 		//"Saidas"
AADD(aApuracao,{0,0,0,0,STR0027,"421","422","423","424"}) 	//"Entradas"
AADD(aApuracao,{0,0,0,0,STR0027,"441","","443",""}) 		//"Entradas"
AADD(aApuracao,{0,0,0,0,STR0026,"451","452","453","454"}) //"Saidas"
AADD(aTotal   ,{0,0,0,0,STR0015,"431","","433",""}) 		//"Total"
AADD(aTotal   ,{0,0,0,0,STR0015,"461","","463",""}) 		//"Total"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                               BENEFICIAMENTO                             ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³            POR TERCEIROS            ³            EM TERCEIROS            ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³             FATURAMENTO             ³              COMPRAS               ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³ 593/693 - Produto                  >=> 193/293 - Produto                 ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³              COMPRAS                ³            FATURAMENTO             ³
//ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´
//³ 194/294 - Produto                  <=< 594/694 - Produto                 ³
//³ 113/213 - MO/Material Empregado    <=< 513/613 - MO/Material Empregado   ³
//³ 199/299 - Material nao aproveitado <=< 599/699 - Material nao Empregado  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

aCampos:={"411","413","421","422","423","424","441","443","451","452","453","454"}

For nx	:=	1 to Len(aCampos)

	cCFO	:=	A965LeIni(aCampos[nx],@lEnd)

	If lEnd
		Exit
	Endif

	If Empty(cCFO)
		Loop
	Endif

	nEl					:=	Val(Substr(aCampos[nx],2,1))
	nEl					:=	IIf(nEl>2,nEl-1,nEl)
	nCol				:=	Val(Substr(aCampos[nx],3,1))
	aApuracao[nEl,nCol]	:=R965ScanCFO(aTotCFO,cCFO,"VCNT")
	
	If nCol==1 .or. nCol==3
		If nEl<3
			aTotal[1,nCol]+=aApuracao[nEl,nCol]
		Else
			aTotal[2,nCol]+=aApuracao[nEl,nCol]
		EndIf
	EndIf
Next                    

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do Mapa                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0028 //"|Industrializacao                                                                                                                   |"
@ ++nLin,00 pSay "|-----------------------------------------------------------------------------------------------------------------------------------|"
@ ++nLin,00 pSay STR0029 //"|Por Terceiros         |                     Deste Estado                     |                   Outros Estados                    |"
@ ++nLin,00 pSay STR0030 //"|                      |   Mercadorias/Produtos   |      Valor Agregado       |   Mercadorias/Produtos   |      Valor Agregado      |"
@ ++nLin,00 pSay "|                      |--------------------------+---------------------------|--------------------------+--------------------------|"
//               |XXXXXXXXXXXXXXXXXXXXXX|      999,999,999.99      |      999,999,999.999      |      999,999,999.99      |      999,999,999.99      |
//               |                      |--------------------------+---------------------------|--------------------------+--------------------------|
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x12
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to 2

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00	pSay "|"
	@ nLin,001	pSay aApuracao[i,5]
	@ nLin,023	pSay "|"
	@ nLin,024	pSay aApuracao[i,6]
	@ nLin,027	pSay "|"
	@ nLin,033	pSay aApuracao[i,1] Picture "@E 999,999,999.99"
	@ nLin,050	pSay "|"
	@ nLin,051	pSay aApuracao[i,7]
	@ nLin,054	pSay "|"
	@ nLin,060	pSay aApuracao[i,2] Picture IIf(Empty(aApuracao[i,7]),"@Z","@E 999,999,999.99")
	@ nLin,078	pSay "|"
	@ nLin,079	pSay aApuracao[i,8]
	@ nLin,082	pSay "|"
	@ nLin,088	pSay aApuracao[i,3] Picture "@E 999,999,999.99"
	@ nLin,105	pSay "|"
	@ nLin,106	pSay aApuracao[i,9]
	@ nLin,109	pSay "|"
	@ nLin,115	pSay aApuracao[i,4] Picture IIf(Empty(aApuracao[i,9]),"@Z","@E 999,999,999.99")
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|                      |---+----------------------+---+-----------------------|---+----------------------+---+----------------------|"
Next            

@ ++nLin,00	pSay "|"
@ nLin,001 	pSay aTotal[1,5]
@ nLin,023	pSay "|"
@ nLin,024	pSay aTotal[1,6]
@ nLin,027	pSay "|"
@ nLin,033	pSay aTotal[1,1] Picture "@E 999,999,999.99"
@ nLin,050	pSay "|"
@ nLin,054	pSay "|"
@ nLin,078	pSay "|"
@ nLin,079	pSay aTotal[1,8]
@ nLin,082	pSay "|"
@ nLin,088	pSay aTotal[1,3] Picture "@E 999,999,999.99"
@ nLin,105	pSay "|"
@ nLin,109	pSay "|"
@ nLin,132	pSay "|" ; nLin++
@ nLin,000	pSay "|-----------------------------------------------------------------------------------------------------------------------------------|" ; nLin++
@ nLin,000	pSay STR0031 ; nLin++ //"|Para Terceiros        |                    Para o Estado                     |                 Para Outros Estados                 |"
@ nLin,000	pSay STR0030 ; nLin++ //"|                      |   Mercadorias/Produtos   |      Valor Agregado       |   Mercadorias/Produtos   |      Valor Agregado      |"
@ nLin,000	pSay "|                      |--------------------------+---------------------------|--------------------------+--------------------------|"

For i:=3 to 4

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00	pSay "|"
	@ nLin,001	pSay aApuracao[i,5]
	@ nLin,023	pSay "|"
	@ nLin,024	pSay aApuracao[i,6]
	@ nLin,027	pSay "|"
	@ nLin,033	pSay aApuracao[i,1] Picture "@E 999,999,999.99"
	@ nLin,050	pSay "|"
	@ nLin,051	pSay aApuracao[i,7]
	@ nLin,054	pSay "|"
	@ nLin,060	pSay aApuracao[i,2] Picture IIf(Empty(aApuracao[i,7]),"@Z","@E 999,999,999.99")
	@ nLin,078	pSay "|"
	@ nLin,079	pSay aApuracao[i,8]
	@ nLin,082	pSay "|"
	@ nLin,088	pSay aApuracao[i,3] Picture "@E 999,999,999.99"
	@ nLin,105	pSay "|"
	@ nLin,106	pSay aApuracao[i,9]
	@ nLin,109	pSay "|"
	@ nLin,115	pSay aApuracao[i,4] Picture IIf(Empty(aApuracao[i,9]),"@Z","@E 999,999,999.99")
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|                      |---+----------------------+---+-----------------------|---+----------------------+---+----------------------|"
Next            

@ ++nLin,00	pSay "|"
@ nLin,001	pSay aTotal[2,5]
@ nLin,023	pSay "|"
@ nLin,024	pSay aTotal[2,6]
@ nLin,027	pSay "|"
@ nLin,033	pSay aTotal[2,1] Picture "@E 999,999,999.99"
@ nLin,050	pSay "|"
@ nLin,054	pSay "|"
@ nLin,078	pSay "|"
@ nLin,079	pSay aTotal[2,8]
@ nLin,082	pSay "|"
@ nLin,088	pSay aTotal[2,3] Picture "@E 999,999,999.99"
@ nLin,105	pSay "|"
@ nLin,109	pSay "|"
@ nLin,132	pSay "|"
@ ++nLin,00	pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"

aTotCFO		:=	Nil
aApuracao	:=	Nil
aTotal		:=	Nil
aCampos		:=	Nil

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965InfContab³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime informacoes referentes a Contabilidade             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965InfContab(dDataIni,dDataFim,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aApuracao	:=	{}
Local aTotal	:=	{}
Local aCtContab	:=	{}

Local cLetra	:=	""
Local cTexto	:=	""
Local cCtContab	:=	""

Local i			:=	0
Local nx		:=	0
Local nSaldo	:=	0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Bloqueia funcao                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta a estrutura dos array acumuladores                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aApuracao,{0,STR0032,"511"}) 			//"Fornecedores Terceiros"
AADD(aApuracao,{0,STR0033,"521"}) 			//"Fornecedores Grupo"
AADD(aApuracao,{0,STR0034,"531"}) 	  		//"Emprestimos/Titulos/Financ"
AADD(aApuracao,{0,STR0035,"541"}) 			//"Credito Titular/Socios/Diretores"
AADD(aApuracao,{0,STR0036,"512"}) 			//"Compras a Prazo"
AADD(aApuracao,{0,STR0037,"522"}) 			//"Compras Ativo Permanente"
AADD(aApuracao,{0,STR0038,"532"}) 			//"Vendas Ativo Permanente"
AADD(aApuracao,{0,STR0039,"542"}) 			//"Despesas de Fabricacao"
aTotal:=	   {0,0,STR0040,"551","552"} 	//"Total para Controle"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Busca Contas Contabeis no SIGADMEF.CFP                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCampos	:=	{"511","521","531","541","512","522","532","542"}

dbSelectArea("SI1")
dbSetOrder(1)
For nx:=1 to Len(aCampos)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancelamento do Operador ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Interrupcao(@lEnd)
		Return
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Apura saldo nas contas       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCtContab:=A965LeIni(aCampos[nx],@lEnd)

	If lEnd
		Exit
	Endif

	If Empty(cCtContab)
		Loop
	Endif

	dbSeek(xFilial(),.T.)
	While !eof() .and. xFilial()==I1_FILIAL

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cancelamento do Operador ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Interrupcao(@lEnd)
			Return
		Endif

		If !(&cCtContab)
			dbSkip()
			Loop
		Endif
		nSaldo	:=	Saldo(I1_CODIGO,Month(dDataFim),1)
		aApuracao[nx][1]	+=	nSaldo
		aTotal[IIf(nx<5,1,2)]	+=	nSaldo
		dbSkip()
	End
Next

dbSelectArea("SF3")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao do Mapa                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLin++
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0041 //"|Informacoes contabeis                                                                                                              |"
@ ++nLin,00 pSay "|-----------------------------------------------------------------------------------------------------------------------------------|"
@ ++nLin,00 pSay STR0042+DtoC(dDataIni)+If(Len(dtoc(dDataIni))>8,"","  ")+STR0043 //"|Saldo em "###"                                              |                      Movimento no Periodo                       |"
@ ++nLin,00 pSay "|-----------------------------------------------------------------|-----------------------------------------------------------------|"
//               |XXXXXXXXXXXXXXXXXX 38 XXXXXXXXXXXXXXXX|999|    999,999,999.99    |XXXXXXXXXXXXXXXXXX 38 XXXXXXXXXXXXXXXX|999|    999,999,999.99    |
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x12
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to Len(aApuracao)/2

	If Interrupcao(@lEnd)
		Return
	Endif

	@ ++nLin,00	pSay "|"
	@ nLin,001 	pSay aApuracao[i,2]
	@ nLin,039	pSay "|"
	@ nLin,040	pSay aApuracao[i,3]
	@ nLin,043	pSay "|"
	@ nLin,048	pSay aApuracao[i,1] Picture "@E 999,999,999.99"
	@ nLin,066	pSay "|"
	@ nLin,067	pSay aApuracao[i+4,2]
	@ nLin,105	pSay "|"
	@ nLin,106	pSay aApuracao[i+4,3]
	@ nLin,109	pSay "|"
	@ nLin,114	pSay aApuracao[i+4,1] Picture "@E 999,999,999.99"
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|--------------------------------------+---+----------------------|--------------------------------------+---+----------------------|"
Next            

@ ++nLin,00	pSay "|"
@ nLin,001	pSay aTotal[3]
@ nLin,039	pSay "|"
@ nLin,040	pSay aTotal[4]
@ nLin,043	pSay "|"
@ nLin,048	pSay aTotal[1] Picture "@E 999,999,999.99"
@ nLin,066	pSay "|"
@ nLin,067	pSay aTotal[3]
@ nLin,105	pSay "|"
@ nLin,106	pSay aTotal[5]
@ nLin,109	pSay "|"
@ nLin,114	pSay aTotal[2] Picture "@E 999,999,999.99"
@ nLin,132	pSay "|"
@ ++nLin,00	pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"

aCampos		:=	Nil
aApuracao	:=	Nil
aTotal		:=	Nil
aCtContab	:=	Nil

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965DetOperac³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime detalhamento das operacoes por unidade da Federacao³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R965DetOperac(aTotEst,lEnd)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aTotal	:=	{0,0,0,0,0,0,0}

Local i			:=	1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica Interrupcao                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lEnd .or. Empty(aTotEst)
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime cabecalho                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aTotEst)>2
	nLin	:=	80
	R965Cabec(@nLin)
Endif

nLin++
@ ++nLin,00 pSay "+-----------------------------------------------------------------------------------------------------------------------------------+"
@ ++nLin,00 pSay STR0044 //"|    Detalhamento das Operacoes por Unidade da Federacao                                                                            |"
@ ++nLin,00 pSay "|-----------------------------------------------------------------------------------------------------------------------------------|"
@ ++nLin,00 pSay STR0045 //"|  |          Entradas / Aquisicoes          |                            Saidas / Prestacoes                                       |"
@ ++nLin,00 pSay "|  +-----------------------------------------+-----------------------------------------------------------------------+--------------|"
@ ++nLin,00 pSay STR0046 //"|  |                    |   Valor Base de    |                                   |        Valor Base de Calculo      |              |"
@ ++nLin,00 pSay STR0047 //"|UF|     Valor das      | Calculo das Opera- |         Valor das Operacoes       |      das Operacoes Tributadas     |     ICMS     |"
@ ++nLin,00 pSay STR0048 //"|  |     Operacoes      |  coes Tributadas   |-----------------------------------+-----------------------------------+ Subs.Tribut. |"
@ ++nLin,00 pSay STR0049 //"|  |                    |                    |  Nao Contribuinte| Contribuinte   |  Nao Contribuinte| Contribuinte   |              |"
@ ++nLin,00 pSay "|--+---+----------------+---+----------------+---+--------------+----------------+---+--------------+----------------+--------------|"
//               |XX|999|9,999,999,999.99|999|9,999,999,999.99|999|999,999,999.99|9,999,999,999.99|999|999,999,999.99|9,999,999,999.99|999,999,999.99|"
//               |--+---+----------------+---+----------------+---+--------------+----------------+---+--------------+----------------+--------------|"
//               x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x123456789x0
//               0         10        20        30        40        50        60        70        80        90        100       110       120       130

For i:=1 to Len(aTotEst)

	If nLin >= 55
		R965Cabec(@nLin)
	Endif

	If Interrupcao(@lEnd)
		Return
	Endif

	cCampo:=A965LeIni(aTotEst[i,1]+"_",@lEnd)

	If lEnd
		Exit
	Endif

    @ ++nLin,00	pSay "|"
    @ nLin,001	pSay aTotEst[i,1]
    @ nLin,003	pSay "|"
    @ nLin,004	pSay Substr(cCampo,1,3)
    @ nLin,007	pSay "|"
    @ nLin,008	pSay aTotEst[i,2] Picture "@E 9,999,999,999.99"
    @ nLin,024	pSay "|"
    @ nLin,025	pSay Substr(cCampo,5,3)
    @ nLin,028	pSay "|"
    @ nLin,029	pSay aTotEst[i,3] Picture "@E 9,999,999,999.99"
    @ nLin,045	pSay "|"
    @ nLin,046	pSay Substr(cCampo,9,3)
    @ nLin,049	pSay "|"
    @ nLin,050	pSay aTotEst[i,6] Picture "@E 999,999,999.99"
    @ nLin,064	pSay "|"
    @ nLin,065	pSay aTotEst[i,4] Picture "@E 9,999,999,999.99"
    @ nLin,081	pSay "|"
    @ nLin,082	pSay Substr(cCampo,13,3)
    @ nLin,085	pSay "|"
    @ nLin,086	pSay aTotEst[i,7] Picture "@E 999,999,999.99"
    @ nLin,100	pSay "|"
    @ nLin,101	pSay aTotEst[i,5] Picture "@E 9,999,999,999.99"
    @ nLin,117	pSay "|"
	@ nLin,118	pSay aTotEst[i,8] Picture "@E 999,999,999.99"
    @ nLin,132	pSay "|"
    @ ++nLin,00	pSay "|--+---+----------------+---+----------------+---+--------------+----------------+---+--------------+----------------+--------------|"
    
    aTotal[1]	+=	aTotEst[i,2]
    aTotal[2]	+=	aTotEst[i,3]
    aTotal[3]	+=	aTotEst[i,4]
    aTotal[4]	+=	aTotEst[i,5]
    aTotal[5]	+=	aTotEst[i,6]
    aTotal[6]	+=	aTotEst[i,7]
	aTotal[7]	+=	aTotEst[i,8]
Next   

Campo	:=	A965LeIni("TOT",@lEnd)

If !lEnd
	@ ++nLin,00	pSay STR0050 //"| TOTAL"
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|--+---+----------------+---+----------------+---+--------------+----------------+---+--------------+----------------+--------------|"
	@ ++nLin,00	pSay "|"
	@ nLin,001	pSay Substr(cCampo,1,3)
	@ nLin,003	pSay "|"
	@ nLin,004	pSay Substr(cCampo,1,3)
	@ nLin,007	pSay "|"
	@ nLin,008	pSay aTotal[1] Picture "@E 9,999,999,999.99"
	@ nLin,024	pSay "|"
	@ nLin,025	pSay Substr(cCampo,5,3)
	@ nLin,028	pSay "|"
	@ nLin,029	pSay aTotal[2] Picture "@E 9,999,999,999.99"
	@ nLin,045	pSay "|"
	@ nLin,046	pSay Substr(cCampo,9,3)
	@ nLin,049	pSay "|"
	@ nLin,050	pSay aTotal[5] Picture "@E 999,999,999.99"
	@ nLin,064	pSay "|"
	@ nLin,065	pSay aTotal[3] Picture "@E 9,999,999,999.99"
	@ nLin,081	pSay "|"
	@ nLin,082	pSay Substr(cCampo,13,3)
	@ nLin,085	pSay "|"
	@ nLin,086	pSay aTotal[6] Picture "@E 999,999,999.99"
	@ nLin,100	pSay "|"
	@ nLin,101	pSay aTotal[4] Picture "@E 9,999,999,999.99"
	@ nLin,117	pSay "|"
	@ nLin,118	pSay aTotal[7] Picture "@E 999,999,999.99"
	@ nLin,132	pSay "|"
	@ ++nLin,00	pSay "|--+---+----------------+---+----------------+---+--------------+----------------+---+--------------+----------------+--------------|"
Endif

aTotEst	:=	Nil
aTotal	:=	Nil

Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965TotCFO   ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria Array com totalizacao por CFO e Periodo               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R965TotCFO(dDataIni,dDataFim,lEnd,cNrLivro)

Local aCFO		:=	{}
Local aEstado	:=	{}
Local aDevolucao:=	{}

Local cTipo		:=	""
Local Chave		:=	""
Local cEstado	:=	""
Local cCFOINI	:=	""
Local cNContr 	:=	A965LeIni("999")
Local cAllCFO	:=	A965AllCFO("SIGADMEF.CFP")
Local cAliasSF3	:=	"SF3"
Local cChave 	:= 	""
Local cChaveDev := 	""
Local cCliFor	:= 	""
Local cLoja		:= 	""
Local cNFiscal	:= 	""
Local cSerie	:= 	""
Local cTipoMov	:= 	""

Local lContr 	:=	.F.
Local lQuery	:=	.F.

Local i			:=	0
Local nPos		:=	0
Local nIcmRet 	:=	0

#IFDEF TOP
	Local aStruSF3	:=	{}
	Local cQuery	:=	""
	Local nX		:=	0
#ELSE
	Local cIndex    :=	""
	Local cCondicao :=	""
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processando as movimentacoes.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
dbSetOrder(2)
ProcRegua(LastRec())
#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery 		:= .T.
		cAliasSF3	:= "SF3_MTR965"
		aStruSF3	:= SF3->(dbStruct())
		cQuery		:= "SELECT SF3.F3_FILIAL,SF3.F3_CFO,SF3.F3_ESTADO,SF3.F3_TIPO,"
		cQuery		+= "SF3.F3_CLIEFOR,SF3.F3_LOJA,SF3.F3_NFISCAL,SF3.F3_SERIE,SUM(SF3.F3_VALCONT) F3_VALCONT,"
		cQuery		+= "SUM(SF3.F3_VALICM) F3_VALICM,SUM(SF3.F3_VALIPI) F3_VALIPI,"
		cQuery		+= "SUM(SF3.F3_BASEICM) F3_BASEICM,SUM(SF3.F3_ICMSRET) F3_ICMSRET "
		cQuery 		+= "FROM " + RetSqlName("SF3") + " SF3 "
		cQuery 		+= "WHERE SF3.F3_FILIAL = '" + xFilial("SF3") + "' AND "
		cQuery 		+= "SF3.F3_ENTRADA >= '" + Dtos(dDataIni) + "' AND "
		cQuery 		+= "SF3.F3_ENTRADA <= '" + Dtos(dDataFim) + "' AND "	
		cQuery 		+= "SF3.F3_TIPO <> 'S' AND "
		cQuery 	   	+= "SF3.F3_DTCANC = '" + Space(Len(Dtos(SF3->F3_DTCANC))) + "' AND "
		cQuery    	+= "SF3.F3_OBSERV NOT LIKE '%CANCELAD%' AND "
		cQuery    	+= "SF3.F3_CFO IN(" + cAllCFO + ") AND "
		If cNrLivro <> "*"
			cQuery 	+= "SF3.F3_NRLIVRO = '" + cNrLivro + "' AND "
		EndIf	
		cQuery 		+= "SF3.D_E_L_E_T_ = ' ' "
		cQuery		+= "GROUP BY SF3.F3_FILIAL,SF3.F3_CFO,SF3.F3_ESTADO,SF3.F3_TIPO,"
		cQuery		+= "SF3.F3_CLIEFOR,SF3.F3_LOJA,SF3.F3_NFISCAL,SF3.F3_SERIE "
		cQuery 		+= "ORDER BY " + SqlOrder(SF3->(IndexKey()))
		cQuery 		:= ChangeQuery(cQuery)                       
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF3)
		For nX := 1 To len(aStruSF3)
			If aStruSF3[nX][2] <> "C" .And. FieldPos(aStruSF3[nX][1])<>0
				TcSetField(cAliasSF3,aStruSF3[nX][1],aStruSF3[nX][2],aStruSF3[nX][3],aStruSF3[nX][4])
			EndIf
		Next nX
		
		dbSelectArea(cAliasSF3)	
	Else
#ENDIF
	    cIndex    := CriaTrab(NIL,.F.)
	    cCondicao := 'F3_FILIAL == "' + xFilial("SF3") + '" .And. '
	   	cCondicao += 'DTOS(F3_ENTRADA) >= "' + DTOS(dDataIni) + '" .And. DTOS(F3_ENTRADA) <= "' + DTOS(dDataFim) + '" '
		cCondicao += '.And. F3_TIPO <> "S" '
		cCondicao += '.And. Empty(F3_DTCANC) '
		cCondicao += '.And. !("CANCELAD" $ F3_OBSERV) '
		If cNrLivro <> "*"
			cCondicao += '.And. F3_NRLIVRO == "' + cNrLivro + '" '
		EndIf	                                                        
	    IndRegua(cAliasSF3,cIndex,SF3->(IndexKey()),,cCondicao)
	    dbSelectArea(cAliasSF3)
	    ProcRegua(LastRec())
	    dbGoTop()
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Levantamento de Notas Normais e de Complemento ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !((cAliasSF3)->(Eof())) .And. !lEnd

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancelamento do Operador ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Interrupcao(@lEnd)
		Loop
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtros      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cCFO		:=	alltrim((cAliasSF3)->F3_CFO)
	cTipo		:=	(cAliasSF3)->F3_TIPO
	cEstado		:=	(cAliasSF3)->F3_ESTADO
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Acumula valores por CFO  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nValCont 	:=	(cAliasSF3)->F3_VALCONT
	nValICM		:=	(cAliasSF3)->F3_VALICM
	nValIPI		:=	(cAliasSF3)->F3_VALIPI
	nBaseICM 	:=	(cAliasSF3)->F3_BASEICM
	nIcmRet  	:=	(cAliasSF3)->F3_ICMSRET
                                                                    
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os CFOPs que devem ser processados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lQuery
		If !(cCFO$cAllCFO)
			(cAliasSF3)->(dbSkip())
			Loop
		Endif
	Endif

	nPos := Ascan(aCFO,{|x|x[1]==cCFO})
	If nPos > 0
		If !(cTipo$"IP") // Complemento de IPI ou ICMS
			aCFO[nPos,2] += nValCont
		Endif
		aCFO[nPos,3] += nValICM
		aCFO[nPos,4] += nValIPI
	Else
		If !(cTipo$"IP")
			AADD(aCFO,{cCFO,nValCont,nValICM,nValIPI})
		Else
			AADD(aCFO,{cCFO,0,nValICM,nValIPI})
		Endif
	Endif               
	
	If cTipo$"IP"
		(cAliasSF3)->(dbSkip())
		Loop
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Acumula valores por estado  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPos	:=	Ascan(aEstado,{|x|x[1]==cEstado})
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se e contribuinte.  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Val(substr(cCFO,1,1)) < 5
		If (cTipo $ "DB")
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
		Else
			dbSelectArea("SA2")
			dbSetOrder(1)
			dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
		EndIf
		dbSelectArea("SF3")
	Else
		If (!cTipo $ "DB")
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek(F3Filial("SA1")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
		Else
			dbSelectArea("SA2")
			dbSetOrder(1)
			dbSeek(F3Filial("SA2")+(cAliasSF3)->F3_CLIEFOR+(cAliasSF3)->F3_LOJA)
		EndIf
		dbSelectArea("SF3")
	EndIf

	bError := ErrorBlock({|| lContr := ( At(cCFO,cNContr) > 0 )})
	Begin Sequence
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³controla se o registro de 999 (nao contribuintes) foi digitado o c¢digo³
		//³dos CFOs ou uma expressao logica como SA1->A1_TIPO="F"				  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lContr := &cNContr
		lContr := If(ValType(lContr)=="L",lContr,lContr := ( At(cCFO,cNContr) > 0 ))
	End Sequence
	ErrorBlock(bError)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Estrutura Array aEstado                             ³
	//³[1] Sigla do Estado                                  ³
	//³[2] Vlr. Contabil	- Entrada                       ³
	//³[3] Base Icm			- Entrada 		                ³
	//³[4] Vlr. Contabil	- Saida                         ³
	//³[5] Base Icm			- Saida        	                ³
	//³[6] Vlr. Contabil	- Saida Nao Contribuinte        ³ 
	//³[7] Base Icm			- Saida Nao Contribuinte        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If nPos>0
		If Val(substr(cCFO,1,1)) < 5
			aEstado[nPos,2]	+=	nValCont
			aEstado[nPos,3]	+=	nBaseICM
		Else
			If lContr
				aEstado[nPos,6]	+=	nValCont
				aEstado[nPos,7]	+=	nBaseICM
				aEstado[nPos,8]	+=	nIcmRet
			Else
				aEstado[nPos,4]	+=	nValCont
				aEstado[nPos,5]	+=	nBaseICM
				aEstado[nPos,8]	+=	nIcmRet
			Endif
		Endif
	Else
		If Val(substr(cCFO,1,1)) < 5
			AADD(aEstado,{cEstado,nValCont,nBaseICM,0,0,0,0,0})
		Else
			If lContr
				AADD(aEstado,{cEstado,0,0,0,0,nValCont,nBaseIcm,nIcmRet})
			Else
				AADD(aEstado,{cEstado,0,0,nValCont,nBaseICM,0,0,nIcmRet})
			Endif
		EndIf
	EndIf       
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica as devoluções³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (cAliasSF3)->F3_TIPO == "D"
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Busca notas fiscais originais ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lQuery
			nSvRec	:=	SF3->(Recno())
		Endif       
		
		cCliFor		:=	(cAliasSF3)->F3_CLIEFOR
		cLoja		:=	(cAliasSF3)->F3_LOJA
		cNFiscal	:=	(cAliasSF3)->F3_NFISCAL
		cSerie		:=	(cAliasSF3)->F3_SERIE
		cEstado 	:=	(cAliasSF3)->F3_ESTADO
		cTipoMov	:=	Iif(Val(substr((cAliasSF3)->F3_CFO,1,1)) >= 5,"S","E")
		aDevolucao	:=	{}
		
		If cTipoMov == "S"
			SD2->(dbSetOrder(3))
			cChave := F3Filial("SD2")+cNFiscal+cSerie+cCliFor+cLoja
			SD2->(dbSeek(cChave,.T.))
			While !SD2->(Eof()) .And. SD2->D2_FILIAL+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA == cChave
				If !Empty(SD2->D2_NFORI)
					AADD(aDevolucao,{"E",SD2->D2_NFORI,SD2->D2_SERIORI,SD2->D2_TOTAL+SD2->D2_VALIPI,SD2->D2_VALICM,SD2->D2_VALIPI,SD2->D2_BASEICM})
				Endif
				SD2->(dbSkip())
			End
		Else
			SD1->(dbSetOrder(1))
			cChave	:=	F3Filial("SD1")+cNFiscal+cSerie+cCliFor+cLoja
			SD1->(dbSeek(cChave,.T.))
			While !SD1->(Eof()) .And. SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA == cChave
				If !Empty(SD1->D1_NFORI)
					AADD(aDevolucao,{"S",SD1->D1_NFORI,SD1->D1_SERIORI,SD1->D1_TOTAL+SD1->D1_VALIPI,SD1->D1_VALICM,SD1->D1_VALIPI,SD1->D1_BASEICM})
				Endif
				SD1->(dbSkip())
			End
		Endif                  
		
		dbSelectArea("SF3")
		dbSetOrder(4)
		
		For i := 1 to Len(aDevolucao)
			cChave	:=	xFilial("SF3")+cCliFor+cLoja+aDevolucao[i,2]+aDevolucao[i,3]
			SF3->(dbSeek(cChave,.t.))
			While !SF3->(Eof()) .And. SF3->F3_FILIAL+SF3->F3_CLIEFOR+SF3->F3_LOJA+SF3->F3_NFISCAL+SF3->F3_SERIE == cChave
				cCFO :=	SF3->F3_CFO
				If Iif(Val(Substr(cCFO,1,1)) >= 5,"S","E") != aDevolucao[i,1]
					SF3->(dbSkip())
					Loop
				Endif
				nPos :=	Ascan(aCFO,{|x|x[1]==cCFO})
				If nPos > 0
					aCFO[nPos,2]	-=	aDevolucao[i,4]
					aCFO[nPos,3]	-=	aDevolucao[i,5]
					aCFO[nPos,4]	-=	aDevolucao[i,6]
					nPos :=	Ascan(aEstado,{|x|x[1]==cEstado})
					If nPos > 0
						lContr	:= ( At(cCFO,cNContr) > 0 )
						If Val(substr(cCFO,1,1)) < 5
							aEstado[nPos,2]	-=	aDevolucao[i,4]
							aEstado[nPos,3]	-=	aDevolucao[i,7]
						Else
							If lContr
								aEstado[nPos,6]	-=	aDevolucao[i,4]
								aEstado[nPos,7]	-=	aDevolucao[i,7]
							Else
								aEstado[nPos,4]	-=	aDevolucao[i,4]
								aEstado[nPos,5]	-=	aDevolucao[i,7]
							Endif
						Endif
					Endif
				Endif
				SF3->(dbSkip())
			End
		Next i
		If !lQuery
			SF3->(dbSetOrder(1))
			SF3->(dbGoto(nSvRec))
		Endif
	Endif	

	// *********************************** fim devolucoes 	
	(cAliasSF3)->(dbSkip())
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Elimina as areas de trabalho utilizadas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	dbSelectArea(cAliasSF3)
	dbCloseArea()
Else
   	dbSelectArea("SF3")
	RetIndex("SF3")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
Endif	

If !lEnd
	ASort(aCFO,,,{|x,y|x[1]<y[1]})
	ASort(aEstado,,,{|x,y|x[1]<y[1]})
Endif

Return ({aCFO,aEstado})
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R965ScanCFO  ³ Autor ³ Juan Jose Pereira ³ Data ³ 27.07.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Busca valores no Array que acumulou valores de CFO         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function R965ScanCFO(aTotCFO,cCFO,cColuna)

Local i			:=	0
Local nValor	:=	0
Local nColuna	:=	2

Do Case
	Case cColuna == "VCNT" ; nColuna:=2
	Case cColuna == "ICMS" ; nColuna:=3
	Case cColuna == "IPI"  ; nColuna:=4
EndCase

For i	:=	1 to Len(aTotCFO)
	If aTotCFO[i,1] $ cCFO
		nValor	+=	aTotCFO[i,nColuna]
	Endif
Next

Return (nValor)     

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A965AllCFO  ³Autor³     Marcos Simidu     ³ Data ³ 15.07.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Carrega conteudo de todos CFO's do SIGADMEF.CFP             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA965                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 
Function A965AllCFO(cArqIni)

Local cConteudo	:=	""
Local cRet		:=	""

Local i			:=	0
Local nLinhas	:=	0   

#IFDEF TOP
	Local cNovo	:=	""
	Local nX	:=	0    
#ENDIF


If !File(cArqIni)
	Help(" ",1,cArqIni)
	Return(cRet)
Else
	cConteudo	:=	MemoRead(cArqIni)
	nLinhas		:=	MlCount(cConteudo,254)
	For i := 4 to nLinhas
		cLinha	:=	AllTrim(MemoLine(cConteudo,254,i))
		If !Empty(Substr(cLinha,7))
			cRet	+=	Substr(cLinha,7)+','
		Endif
	Next
Endif  

#IFDEF TOP 
	For nX := 1 to Len(cRet)
		If SubStr(cRet,nX,1)$"0123456789"
			cNovo += SubStr(cRet,nX,1)
		Else 
			If SubStr(cRet,nX+1,1)$"0123456789"
				cNovo := Alltrim(cNovo) + "','"
			Endif
		Endif                                                          
	Next
	cNovo	:= "'" + Alltrim(cNovo) + "'"
	cRet 	:= cNovo
#ENDIF

Return(cRet)
