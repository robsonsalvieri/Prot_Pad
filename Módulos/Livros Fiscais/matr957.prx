#INCLUDE "PROTHEUS.CH"
#INCLUDE "Matr957.ch"
#DEFINE SAY PSAY

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR957  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 03.05.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tabela de Codigo de Mercadorias - Modelo P11               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MATR957(void)                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Alteracoes³ BOPS ³ Data   ³ Descricao                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Andreia   ³24340A³05/11/99³ Troca de CGC por CNPJ.                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Matr957

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Local wnrel		:=	"MATR957"
Local tamanho	:=	"M"
Local titulo	:=	STR0001 //"Tabela de C¢digo de Mercadorias" 

Local cDesc1	:=	STR0002 //"Emiss„o da Tabela de C¢digo de Mercadorias - Livro Modelo P11"
Local cDesc2	:=	STR0003 //"Ir  imprimir a tabela com c¢digo, descri‡„o e classifica‡„o fiscal"
Local cDesc3	:=	STR0004 //"das mercadorias referente as movimenta‡”es no per¡odo informado."
Local cSvAlias	:=	Alias()
Local cFilSel	:= "" 
Local nZ			:= 0
Local nX			:= 0

Private aReturn :=	{ STR0005, 1,STR0006, 2, 2, 1, "",1 } //"Zebrado"###"Administra‡„o"
Private nomeprog:=	"MATR957"
Private cPerg   :=	"MTR957"
Private cString	:=	"SB1"

Private lModoComp:= Empty(xFilial('SB1'))	 // Referente ao X2_MODO
Private aFilsCalc:={}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // da Data                              ³
//³ mv_par02             // ate a Data                           ³
//³ mv_par03             // Pagina Inicial                       ³
//³ mv_par04             // Pagina Final                         ³
//³ mv_par05             // Codigo do Produto Inicial            ³
//³ mv_par06             // Codigo do Produto Final              ³
//³ mv_par07             // Seleciona Filiais                    ³
//³ mv_par08             // Aglutina por CNPJ+IE                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLastKey:= 0
wnrel 	:= SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho)

If nLastKey == 27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey == 27
	dbClearFilter()
	Return
Endif   
// Ponto de entrada criado para que seja possivel aglutinar por CNPJ+IE mesmo que o SB1 seja EXCLUSIVO
if ExistBlock("MTR957C")
	lModoComp := ExecBlock("MTR957C",.F.,.F.)
endif

IF MV_PAR07==1
	If lModoComp
		aFilsCalc	:= MatFilCalc(mv_par07 == 1,,,mv_par07 == 1 .and. mv_par08 == 1, , IIf(mv_par08 == 1,2,0) ) //Parametro Exp6 Valida apenas CNPJ+IE iguais
		If Len(aFilsCalc)==0 
			Return		//aFilsCalc:={{.F.,cFilAnt}}
		Endif
	Else 
 		//Help(nil,1,"MATR957MOD",nil,,6,0)
		aFilsCalc:={{.T.,cFilAnt}}
	Endif
Else
	aFilsCalc:={{.T.,cFilAnt}}

Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa relatorio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aFilsCalc)>0 
   For nX:= 1 to Len(aFilsCalc)
   	If aFilsCalc[nX][1]
   	   nZ++
   	   If nZ>1
   	   	cFilSel:=cFilSel+",'"+aFilsCalc[nX][2]+"'"
   	   Else 
	   	   cFilSel:="'"+aFilsCalc[nX][2]+"'"
	   	Endif
   	Endif   
   Next
   If nZ>0
		RptStatus({|lEnd| R957Imp(@lEnd,wnRel,cString,Tamanho,cFilSel)},titulo)
	Endif
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura Ambiente                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cSvAlias)
dbClearFilter()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Spool de Impressao                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aReturn[5] = 1
	Set Printer To
	ourspool(wnrel)
Endif

MS_FLUSH()

Return
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R957Imp  ³ Autor ³ Juan Jose Pereira     ³ Data ³ 17.02.96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao chamadora do Matr957                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
*/
Static Function R957Imp(lEnd,WnRel,cString,Tamanho,cFilSel)

Local aDados		:=	Array(3)
Local aL			:=	R957LayOut()
Local aEstrut		:= {}

Local cPictPosIpi 	:= "@R 9999.99.99"
Local cCodIni		:= mv_par05		//Codigo do Produto Inicial
Local cCodFim		:= mv_par06		//Codigo do Produto Final
Local cAliasSD1		:= "SD1"
Local cAliasSD2		:= "SD2"
Local cProduto		:= ""
Local cDescr		:= ""
Local cNCM			:= ""
Local cArqTemp 		:= ""
Local cArqIndx 		:= ""
Local cChave 		:= ""

Local dDtIni		:= mv_par01
Local dDtFim		:= mv_par02

Local lQuery		:= .F.

Local nLin			:=	80
Local nPagina		:=	mv_par03
Local nPagFinal		:=	mv_par04

#IFDEF TOP
	Local cQuery	:=	""
	Local aStruSD1	:=	{}
	Local aStruSD2	:=	{}
	Local nX		:=	0   
#ELSE
	Local cIndex    :=	""
	Local cCondicao :=	""
#ENDIF

Local cFilOri	:= cFilAnt		// Grava filial logada para posterior restaração

Default cFilSel := cFilAnt		// Se não vier filial selecionada, carrega logada

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Procura pela picture do dicionario de dados                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(2)
If dbSeek("B1_POSIPI")
	cPictPosIpi := Alltrim(X3_PICTURE)
Endif
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Arquivo Temporario para listar Produtos  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AADD(aEstrut,{'TMP_COD'		,'C',TamSX3("B1_COD")[1],0})
AADD(aEstrut,{'TMP_DESCR'	,'C',TamSX3("B1_DESC")[1],0})
AADD(aEstrut,{'TMP_CODFIS'	,'C',12,0})
cArqTemp := CriaTrab(aEstrut)
dbUseArea(.T.,,cArqTemp,'TMP',.T.,.F.)
cArqIndx := CriaTrab(NIL,.F.)
cChave := "TMP_COD"
IndRegua('TMP',cArqIndx,cChave,,,STR0013) //'Criando Indice...'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processa os produtos com movimentacoes de entrada                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD1")
dbSetOrder(1)
ProcRegua(LastRec())

#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery 		:= .T.
		cAliasSD1	:= "SD1_MTR957"
		aStruSD1	:= SD1->(dbStruct())
		cQuery		:= "SELECT SD1.D1_FILIAL,SD1.D1_COD,SB1.B1_DESC,SB1.B1_POSIPI "
		cQuery 		+= "FROM " + RetSqlName("SD1") + " SD1, " + RetSqlName("SB1") + " SB1 " 
		cQuery 		+= "WHERE "
		If MV_PAR07==1
			cQuery 		+= "SD1.D1_FILIAL IN ("+ cFilSel + ") AND "
		Else
			cQuery 		+= "SD1.D1_FILIAL = '"+ xFilial("SD1") + "' AND "
		Endif
		cQuery 		+= "SD1.D1_EMISSAO BETWEEN '" + Dtos(dDtIni) + "' AND '" + Dtos(dDtFim) + "' AND "	
		cQuery 		+= "SD1.D1_COD BETWEEN '" + cCodIni + "' AND '" + cCodFim + "' AND "
		cQuery 		+= "SD1.D_E_L_E_T_=' ' AND "
		// Alterado filtro de xfilial para cFilSel para que seja possivel aglutinar por CNPJ+IE mesmo que o SB1 seja EXCLUSIVO
		If MV_PAR07==1
			cQuery 		+= "SB1.B1_FILIAL IN (" + cFilSel + ") AND "
		else
			cQuery 		+= "SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
		endif
		cQuery 		+= "SB1.B1_COD = SD1.D1_COD AND "
		cQuery 		+= "SB1.D_E_L_E_T_=' ' "
		cQuery		+= "GROUP BY SD1.D1_FILIAL, SD1.D1_COD, SB1.B1_DESC, SB1.B1_POSIPI "          
		cQuery		+= "ORDER BY 1, 2, 3, 4"
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD1)
		For nX := 1 To len(aStruSD1)
			If aStruSD1[nX][2] <> "C" .And. FieldPos(aStruSD1[nX][1])<>0
				TcSetField(cAliasSD1,aStruSD1[nX][1],aStruSD1[nX][2],aStruSD1[nX][3],aStruSD1[nX][4])
			EndIf
		Next nX
		
		dbSelectArea(cAliasSD1)	
	Else
#ENDIF
	    cIndex    := CriaTrab(NIL,.F.)
	    cCondicao := 'D1_FILIAL == "' + xFilial("SD1") + '" .And. '
		cCondicao += 'Dtos(D1_EMISSAO) >= "' + Dtos(dDtIni) + '" .And. '
		cCondicao += 'Dtos(D1_EMISSAO) <= "' + Dtos(dDtFim) + '" .And. '
	   	cCondicao += 'D1_COD >= "' + cCodIni + '" .And. '
	   	cCondicao += 'D1_COD <= "' + cCodFim + '"'

	    IndRegua(cAliasSD1,cIndex,SD1->(IndexKey()),,cCondicao)
	    dbSelectArea(cAliasSD1)
	    ProcRegua(LastRec())
	    dbGoTop()
	    SB1->(dbSetOrder(1))
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processando as Entradas³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !((cAliasSD1)->(Eof()))

	IncProc(STR0018)
	If Interrupcao(@lEnd)
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apresenta apenas um codigo de produto no relatorio³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TMP->(dbSeek((cAliasSD1)->D1_COD))
		(cAliasSD1)->(dbSkip())
		Loop
	Endif

	If !lQuery              
		If (!SB1->(dbSeek(xFilial("SB1")+(cAliasSD1)->D1_COD))) .Or. Empty(SB1->B1_POSIPI)
			(cAliasSD1)->(dbSkip())
			Loop
		Endif  
		cProduto 	:= SB1->B1_COD
		cDescr		:= SB1->B1_DESC
		cNCM		:= Transform(SB1->B1_POSIPI,cPictPosIpi)
	Else
		cProduto 	:= (cAliasSD1)->D1_COD
		cDescr		:= (cAliasSD1)->B1_DESC
		cNCM		:= Transform((cAliasSD1)->B1_POSIPI,cPictPosIpi)
	Endif
	
	
	RecLock("TMP",.T.)
	Replace TMP->TMP_COD	With cProduto
	Replace TMP->TMP_DESCR	With cDescr
	Replace TMP->TMP_CODFIS	With cNCM
	MsUnlock()
                         	
	(cAliasSD1)->(dbSkip())
                            
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processa os produtos com movimentacoes de saida                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SD2")
dbSetOrder(1)
ProcRegua(LastRec())

#IFDEF TOP
    If TcSrvType()<>"AS/400"
	    lQuery 		:= .T.
		cAliasSD2	:= "SD2_MTR957"
		aStruSD2	:= SD2->(dbStruct())
		cQuery		:= "SELECT SD2.D2_FILIAL,SD2.D2_COD,SB1.B1_DESC,SB1.B1_POSIPI "
		cQuery 		+= "FROM " + RetSqlName("SD2") + " SD2, " + RetSqlName("SB1") + " SB1 " 
		cQuery 		+= "WHERE "  
		If MV_PAR07==1
			cQuery 		+= "SD2.D2_FILIAL IN ("+ cFilSel + ") AND "
		Else
			cQuery 		+= "SD2.D2_FILIAL = '" + xFilial("SD2") + "' AND "		
		Endif
		cQuery 		+= "SD2.D2_EMISSAO BETWEEN '" + Dtos(dDtIni) + "' AND '" + Dtos(dDtFim) + "' AND "	
		cQuery 		+= "SD2.D2_COD BETWEEN '" + cCodIni + "' AND '" + cCodFim + "' AND "
		cQuery 		+= "SD2.D_E_L_E_T_=' ' AND "
		// Alterado filtro de xfilial para cFilSel para que seja possivel aglutinar por CNPJ+IE mesmo que o SB1 seja EXCLUSIVO
		If MV_PAR07==1
			cQuery 		+= "SB1.B1_FILIAL IN (" + cFilSel + ") AND "
		else
			cQuery 		+= "SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
		endif
		cQuery 		+= "SB1.B1_COD = SD2.D2_COD AND "
		cQuery 		+= "SB1.D_E_L_E_T_=' ' "
		cQuery		+= "GROUP BY SD2.D2_FILIAL, SD2.D2_COD, SB1.B1_DESC, SB1.B1_POSIPI "
		cQuery		+= "ORDER BY 1, 2, 3, 4"
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2)
		For nX := 1 To len(aStruSD2)
			If aStruSD2[nX][2] <> "C" .And. FieldPos(aStruSD2[nX][1])<>0
				TcSetField(cAliasSD2,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
			EndIf
		Next nX
		
		dbSelectArea(cAliasSD2)	
	Else
#ENDIF
	    cIndex    := CriaTrab(NIL,.F.)
	    cCondicao := 'D2_FILIAL == "' + xFilial("SD2") + '" .And. '
		cCondicao += 'Dtos(D2_EMISSAO) >= "' + Dtos(dDtIni) + '" .And. '
		cCondicao += 'Dtos(D2_EMISSAO) <= "' + Dtos(dDtFim) + '" .And. '
	   	cCondicao += 'D2_COD >= "' + cCodIni + '" .And. '
	   	cCondicao += 'D2_COD <= "' + cCodFim + '"'
			   	
	    IndRegua(cAliasSD2,cIndex,SD2->(IndexKey()),,cCondicao)
	    dbSelectArea(cAliasSD2)
	    ProcRegua(LastRec())
	    dbGoTop()
	    SB1->(dbSetOrder(1))
#IFDEF TOP
	Endif    
#ENDIF

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processando as Saidas  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
While !((cAliasSD2)->(Eof()))

	IncProc(STR0018)
	If Interrupcao(@lEnd)
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Apresenta apenas um codigo de produto no relatorio³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If TMP->(dbSeek((cAliasSD2)->D2_COD))
		(cAliasSD2)->(dbSkip())
		Loop
	Endif

	If !lQuery              
		If (!SB1->(dbSeek(xFilial("SB1")+(cAliasSD2)->D2_COD))) .Or. Empty(SB1->B1_POSIPI)
			(cAliasSD2)->(dbSkip())
			Loop
		Endif                                                  
		cProduto 	:= SB1->B1_COD
		cDescr		:= SB1->B1_DESC
		cNCM		:= Transform(SB1->B1_POSIPI,cPictPosIpi)
	Else
		cProduto 	:= (cAliasSD2)->D2_COD
		cDescr		:= (cAliasSD2)->B1_DESC
		cNCM		:= Transform((cAliasSD2)->B1_POSIPI,cPictPosIpi)
	Endif
	
	
	RecLock("TMP",.T.)
	Replace TMP->TMP_COD	With cProduto
	Replace TMP->TMP_DESCR	With cDescr
	Replace TMP->TMP_CODFIS	With cNCM
	MsUnlock()
                         	
	(cAliasSD2)->(dbSkip())
                            
Enddo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime Arquivo Temporario                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
TMP->(dbGotop())
SetRegua(LastRec())  

While !(TMP->(Eof())) .and. !lEnd

	IncRegua()
	If Interrupcao(@lEnd)
		Loop
	Endif

	If nLin>60
		If nLin<80
			FmtLin(@aDados,aL[01],,,@nLin)
		Endif
		R957Cabec(aL,@nLin,@nPagina,nPagFinal)
	Endif
	
	aDados[1]:=TMP->TMP_COD
	aDados[2]:=TMP->TMP_DESCR
	aDados[3]:=TMP->TMP_CODFIS
	FmtLin(@aDados,aL[13],,,@nLin)
	
	TMP->(dbSkip())

	If TMP->(Eof())
		FmtLin(@aDados,aL[01],,,@nLin)
	Endif          
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Excluindo as areas de trabalho utilizadas nas entradas e saidas ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lQuery
	dbSelectArea(cAliasSD1)
	dbCloseArea()
	dbSelectArea(cAliasSD2)
	dbCloseArea()
Else
   	dbSelectArea("SD1")
	RetIndex("SD1")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
   	dbSelectArea("SD2")
	RetIndex("SD2")
	dbClearFilter()
	Ferase(cIndex+OrdBagExt())
Endif	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Deleta arquivo e indice temporario                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP")
dbCloseArea()
Ferase(cArqTemp+GetDBExtension())
Ferase(cArqIndx+OrdBagExt())

cFilAnt	:= cFilOri
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R957LayOut() ³Autor ³ Juan Jose Pereira    ³Data³ 17/02/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Armazena lay-out para o modelo P11                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR957                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R957LayOut()

Local aL := {}

Aadd(aL,           "+-------------------------------------------------------------------------------------------------------------------------+")
Aadd(aL,STR0007) //"|                                                 TABELA DE CODIGO DE MERCADORIA                                          |"
Aadd(aL,           "|                                                                                                                         |")
Aadd(aL,STR0008) //"| FIRMA: ########################################################                                                         |"
Aadd(aL,STR0009) //"| INSCR.EST.: ########################## C.N.P.J.: #######################                                                |"
Aadd(aL,STR0010) //"| FOLHA: ####         DATA: ##########                                                                                    |"
Aadd(aL,           "|-------------------------------------------------------------------------------------------------------------------------|")
Aadd(aL,           "|                                  |                                                         |                            |")
Aadd(aL,STR0011) //"|          CODIGO DO               |                   DISCRIMINACAO                         |    CLASSIFICACAO FISCAL    |"
Aadd(aL,STR0012) //"|           PRODUTO                |                                                         |                            |"
Aadd(aL,           "|                                  |                                                         |                            |")
Aadd(aL,           "|----------------------------------+---------------------------------------------------------+----------------------------|")
Aadd(aL,           "|  ##############################  | ####################################################### |      ############          |")

Return(aL) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R957Cabec()  ³Autor ³ Juan Jose Pereira    ³Data³ 03/05/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Imprime cabecalho do relatorio                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR957                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function R957Cabec(aL,nLin,nPagina,nPagFinal)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime caracter de controle de largura de impressao         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nLin := 0
@ nLin++,0 SAY AvalImp(132)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime cabecalho                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
FmtLin({},aL[1],,,@nLin)
FmtLin({},aL[2],,,@nLin)
FmtLin({},aL[3],,,@nLin)
FmtLin({Subs(SM0->M0_NOMECOM,1,35)},aL[4],,,@nLin)
FmtLin({},aL[3],,,@nLin)
FmtLin({InscrEst(),Transform(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99")},aL[5],,,@nLin)
FmtLin({},aL[3],,,@nLin)
FmtLin({StrZero(nPagina,4),DTOC(dDataBase)},aL[6],,,@nLin)
FmtLin({},aL[3],,,@nLin)
FmtLin({},aL[7],,,@nLin)
FmtLin({},aL[8],,,@nLin)
FmtLin({},aL[9],,,@nLin)
FmtLin({},aL[10],,,@nLin)
FmtLin({},aL[12],,,@nLin)

If (nPagina == nPagFinal) .And. (nPagFinal <> 0)
	nPagina := MV_PAR03
Else
	nPagina++
EndIf      

Return(Nil)
