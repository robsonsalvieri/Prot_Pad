#include 'totvs.ch'
#include 'fileio.ch'
#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'backoffice.fiscal.arquivos.estadual.convenio201.ch'

NameSpace backoffice.fiscal.arquivos.estadual.convenio201

/*/{Protheus.doc} Convenio201
    (Classe Para Geracao do Convenio 201/2017)
    @author Ricardo Henrique de Mello Lima
    @since 21/02/2024
    @version 12.1.2310
    /*/  

Class Convenio201

	private Data oProcess as object
	private Data lAutomato as logical
	private Data aAutoFil as array
	private Data aWizard as array
	private Data lValidWizard as logical
	private Data dDataIni as date
	private Data dDataFim as date
	private Data cAliasSFT as character
	private Data nBuffer as numeric
	private Data nLayoutFC as numeric
	private Data cNomeArq as character
	private Data cLinhaArq as character
	private Data aFilialArq as array
	private Data jFilialArq as json
	private Data cCampoCustom as character
	private Data cAliasCustom as character
	private Data lCampoCustom as logical
	private Data cTipoDoc as character
	private Data aTipoDoc as array
	private Data nLimiteLn as numeric
	private Data cFileDest as character
	private Data cFilialQuery as character
	private Data cNumSerie as character
	private Data lAglutinaCPNJ as logical
	private Data nPosFilAglutina as numeric

	// Variaveis do Layout Convenio 201/2017 TIPO FC
	private Data cCPFCNPJ as character
	private Data aCPFCNPJ as array
	private Data cUF as character
	private Data aUF as array
	private Data cEmissao as character
	private Data aEmissao as array
	private Data cRazaoSocial as character
	private Data aRazaoSocial as array
	private Data cCodFatura as character
	private Data aCodFatura as array
	private Data cOrdemItem as character
	private Data aOrdemItem as array
	private Data cCodItem as character
	private Data aCodItem as array
	private Data cDescItem as character
	private Data aDescItem as array
	private Data cVlrItem as character
	private Data aVlrItem as array
	private Data cOrigemItem as character
	private Data aOrigemItem as array
	private Data cCNPJPart as character
	private Data aCNPJPart as array
	private Data cRazaoPart as character
	private Data aRazaoPart as array
	private Data cVlrTotalFatura as character
	private Data aVlrTotalFatura as array
	private Data cEmissaoFatura as character
	private Data aEmissaoFatura as array
	private Data cModelo as character
	private Data aModelo as array
	private Data cSerieNF as character
	private Data aSerieNF as array
	private Data cNumeroNF as character
	private Data aNumeroNF as array
	private Data cVlrTotalNF as character
	private Data aVlrTotalNF as array
	private Data aQtdArq as array
	private Data cTipoLayout as character

	public Method New() CONSTRUCTOR
	public Method Init() as logical
	private Method Destroy()
	
	private Method setDataProcess()
	private Method MontaWizard() as logical
	private Method QueryGroup()
	private Method MontaQuery()
	private Method SetNomeArquivo(aArquivo as array, cNroVolume as character, cSerieArq as character)
	protected Method ProcessaArquivo()
	private Method LayoutFC()

	private Method SetFilial()
	private Method SetTipoDoc()
	private Method setCampo(aCampo as array, xValue as variant) as variant
	private Method setCampoCustom()

EndClass

/*/{Protheus.doc} New
    (Method New construtor da Classe Inicializa o Processo de Geração do Arquivo)
    @author Ricardo Henrique de Mello Lima
    @since 21/02/2024
    @version 12.1.2310
    /*/    
Method New() class Convenio201

	::lAutomato := IsBlind()
	::nBuffer := 4096
	::cAliasSFT := ""
	::aAutoFil := {}
	::aWizard := {}
	::lValidWizard := .F.
	::dDataIni := date()
	::dDataFim := date()
	::cLinhaArq := ""
	::aFilialArq := {}
	::jFilialArq := JsonObject():New()
	::cCampoCustom := alltrim(GetNewPar("MV_CPCV201","F2_DOC"))
	::cAliasCustom := "SF2"
	::lCampoCustom := .F.
	::cTipoDoc := ""
	::aTipoDoc := {}
	::nLimiteLn := 700000 // limite do arquivo pelo Layout 1000000 sendo que a ultima nota precisa estar completa antes de fechar o arquivo.
	::cFileDest := ""
	::aQtdArq := {}
	::cFilialQuery := ""
	::cTipoLayout := "FC"
	::cNumSerie := ""
	::lAglutinaCPNJ := .F.
	::nPosFilAglutina := 0

	::nLayoutFC := 238
	::aCPFCNPJ := {14,"N",0}
	::cCPFCNPJ := ""
	::aUF := {2,"C"}
	::cUF := ""
	::aRazaoSocial := {35,"C"}
	::cRazaoSocial := ""
	::aEmissaoFatura := {8,"D"}
	::cEmissaoFatura := ""
	::aCodFatura := {20,"C"}
	::cCodFatura := ""
	::aOrdemItem := {3,"N",0}
	::cOrdemItem := ""
	::aCodItem := {10,"C"}
	::cCodItem := ""
	::aDescItem := {40,"C"}
	::cDescItem := ""
	::aVlrItem := {11,"N",2}
	::cVlrItem := ""
	::aOrigemItem := {1,"N",0}
	::cOrigemItem := ""
	::aCNPJPart := {14,"N",0}
	::cCNPJPart := ""
	::aRazaoPart := {35,"C"}
	::cRazaoPart := ""
	::aVlrTotalFatura := {11,"N",2}
	::cVlrTotalFatura := ""
	::aEmissao := {8,"D"}
	::cEmissao := ""
	::aModelo := {2,"N",0}
	::cModelo := ""
	::aSerieNF := {3,"C"}
	::cSerieNF := ""
	::aNumeroNF := {10,"N",0}
	::cNumeroNF := ""
	::aVlrTotalNF := {11,"N",2}
	::cVlrTotalNF := ""

Return

 /*/{Protheus.doc} InitFile
	Inicia o Processamento do Arquivo
	@author Ricardo Henrique de Mello Lima
	@since 21/02/2024
	@version 12.1.2310
	@return , return_type, return_description
    /*/
Method Init( aWizard as Array, aAutoFil as Array, nLimite as numeric) as logical class Convenio201

	if ::lAutomato
		::aWizard   := aWizard
		::aAutoFil  := aAutoFil
		::nLimiteLn := nLimite
	endif

	::SetCampoCustom() // Adiciona o campo customizado para o código da fatura do Layout
	::MontaWizard() // monta a tela do wizard de perguntas
	if ::SetFilial() // Acesso o metodo SetFilial para pegar as filiais que vao ser consideradas no processamento da query
	   ::SetTipoDoc() // seleciona os tipos de Documentos considerar na Query pra otimizar a busca dos dados
	   ::setDataProcess() 	// Seta a data do Processamento
	   ::QueryGroup() // Adiciona a quantidade de arquivos a serem processados

	   if ::lValidWizard
			if ::lAutomato
				Processa({|lCancel|::ProcessaArquivo()})
			else
				::oProcess:= FISProgress():New({||::ProcessaArquivo()},"Processamento do Convenio 201/2017")
				::oProcess:Activate()
			endif
		endif
	endif

	::Destroy()

Return

 /*/{Protheus.doc} Destroy
	Inicia o Processamento do Arquivo
	@author Jose Ricardo Bernardo
	@since 27/09/2024
	@version 12.1.2310
	@return , return_type, return_description
    /*/
Method Destroy() class Convenio201

	FwFreeArray(::aAutoFil)
	FwFreeArray(::aWizard)
	FwFreeArray(::aFilialArq)
	FwFreeArray(::aTipoDoc)
	FwFreeArray(::aQtdArq)
	FwFreeArray(::aCPFCNPJ)
	FwFreeArray(::aUF)
	FwFreeArray(::aRazaoSocial)
	FwFreeArray(::aEmissaoFatura)
	FwFreeArray(::aCodFatura)
	FwFreeArray(::aOrdemItem)
	FwFreeArray(::aCodItem)
	FwFreeArray(::aDescItem)
	FwFreeArray(::aVlrItem)
	FwFreeArray(::aOrigemItem)
	FwFreeArray(::aCNPJPart)
	FwFreeArray(::aRazaoPart)
	FwFreeArray(::aVlrTotalFatura)
	FwFreeArray(::aEmissao)
	FwFreeArray(::aModelo)
	FwFreeArray(::aSerieNF)
	FwFreeArray(::aNumeroNF)
	FwFreeArray(::aVlrTotalNF)

	FreeObj(::jFilialArq)

Return 

/*/{Protheus.doc} MontaWizard
    Responsavel por montar a tela de Wizard para geração do Arquivo
    @author Ricardo Henrique de Mello Lima
    @since 23/02/2024
    @version 12.1.2310
    @return lRetorno, logical, true ou false
    /*/
Method MontaWizard() class Convenio201
	Local aTxtApre := {} as array
	Local aPaineis := {} as array
	Local cTitObj1 := "" as character
	Local cTitObj2 := "" as character
	Local nPos := 0 as numeric
	Local cNomeWizard := "CONV201" as character

	// Painel 0
	aAdd (aTxtApre, STR0001	)		//"Parâmetros necessários."
	aAdd (aTxtApre, "")
	aAdd (aTxtApre, STR0002 )		//"Preencha corretamente as informações solicitadas."
	aAdd (aTxtApre, STR0003 ) 		//"Informações necessárias para a geração do meio-magnético Convênio 201/2017."

	// Painel 01
	aAdd (aPaineis, {})
	nPos := Len (aPaineis)
	aAdd (aPaineis[nPos], STR0004 )	//"Preencha corretamente as informações solicitadas."
	aAdd (aPaineis[nPos], STR0005 )	//"Parâmetros para Geração"
	aAdd (aPaineis[nPos], {})

	cTitObj1 := STR0006				//"Mes/Ano de referencia (MMAAAA)"
	cTitObj2 := STR0007				//"Diretório do Arquivo Destino"
	aAdd (aPaineis[nPos][3], {1, cTitObj1,,,,,,})
	aAdd (aPaineis[nPos][3], {1, cTitObj2,,,,,,})

	cTitObj1 := Replicate ("X", 6)
	cTitObj2 := Replicate ("X", 50)
	aAdd (aPaineis[nPos][3], {2,,cTitObj1,1,,,,6})
	aAdd (aPaineis[nPos][3], {2,,cTitObj2,1,,,,50})
	aAdd (aPaineis[nPos][3], {0,"",,,,,,})
	aAdd (aPaineis[nPos][3], {0,"",,,,,,})

	cTitObj1 := STR0008				//"Seleciona Filial?"
	cTitObj2 := STR0009				//"Aglutina CNPJ?"
	aAdd (aPaineis[nPos][3], {1, cTitObj1,,,,,,})
	aAdd (aPaineis[nPos][3], {1, cTitObj2,,,,,,})

	aItens1 := {}
	aAdd (aItens1, "1-Sim")
	aAdd (aItens1, "2-Não")
	aAdd (aPaineis[nPos][3], {3,,,,,aItens1,,})
	aAdd (aPaineis[nPos][3], {3,,,,,aItens1,,})

	aAdd (aPaineis[nPos][3], {1,"",,,,,,})
	aAdd (aPaineis[nPos][3], {1,"",,,,,,})

	cTitObj1 := STR0010				//"Situação (N = Normal) (S = Substituto)"
	aAdd (aPaineis[nPos][3], {1, cTitObj1,,,,,,})
	aAdd (aPaineis[nPos][3], {0,"",,,,,,})

	aItens1 := {}
	aAdd (aItens1, STR0011 )		//"N-Normal"
	aAdd (aItens1, STR0012 )		//"S-Substituto"
	aAdd (aPaineis[nPos][3], {3,,,,,aItens1,,})
	aAdd (aPaineis[nPos][3], {1,"",,,,,,})

	::lValidWizard := xMagWizard(aTxtApre, aPaineis, cNomeWizard)

	if ::lValidWizard
		xMagLeWiz(cNomeWizard,::aWizard,.T.)
	endif

	FwFreeArray(aTxtApre)
	FwFreeArray(aPaineis)

Return

/*/{Protheus.doc} SetFilial
	Classe responsavel por definir quais filias serão processadas conforme solicitação no Wizard da Rotina
	@author Ricardo Henrique de Mello Lima
	@since 04/03/2024
	@version 12.1.2310	
	/*/
Method SetFilial() class Convenio201

	Local nAglutina := 2 as numeric
	Local nCount := 0 as numeric
	Local aDadosFil := {} as array
	Local lSelecionaFilial := .F. as logical
	Local aFilsCalc := {} as array
	Local nConsolida := 0 as numeric
	Local lVldFilial := .F. as Logical

	if !Empty(::aWizard)
		lSelecionaFilial := SubStr(::aWizard[1][3],1,1) == "1"
		nAglutina := Val(SubStr(::aWizard[1][4],1,1))
	endif
	// Define se Vai Aglutinar CNPJ quando CNPJ e IE forem iguais nas Filiais
	if nAglutina == 1
		nConsolida := 1
		::lAglutinaCPNJ := .T.
	elseif nAglutina == 2
		nConsolida := 0
		::lAglutinaCPNJ := .F.
	endif

	// Abre a Tela de Filials para seleção
	if lSelecionaFilial
		aFilsCalc := MatFilCalc(!::lAutomato,::aAutoFil,,lSelecionaFilial,,nConsolida)
		for nCount := 1 To Len( aFilsCalc )
			if aFilsCalc[nCount,1]
				if (aFilsCalc[nCount][1])

					if !Empty(::cFilialQuery)
						::cFilialQuery += "/"
					endif
					::cFilialQuery += alltrim(aFilsCalc[nCount][2])

					aDadosFil := FWArrFilAtu(,aFilsCalc[nCount][2])
					aAdd(::aFilialArq ,{alltrim(aDadosFil[1]),alltrim(aDadosFil[2]),alltrim(aDadosFil[18]),alltrim(aDadosFil[17]),right(aDadosFil[22],2)})
					::jFilialArq[alltrim(aDadosFil[2])] := len( ::aFilialArq )

					if (aFilsCalc[nCount][2]) == cFilAnt
						lVldFilial := .T.
						::nPosFilAglutina := nCount
					endif
					
				endif
			endif
		next
		if !empty(aFilsCalc) .and. !lVldFilial
			FWAlertInfo(STR0013,STR0014)	//"Quando Aglutina CNPJ = Sim, necessário estar logado na filial consolidadora"#"Convênio 201/17"
		endif

	else
		lVldFilial := .T.
		::nPosFilAglutina := 1
		::cFilialQuery := alltrim(cFilAnt)
		aDadosFil := FWArrFilAtu(,cFilAnt)
		aAdd(::aFilialArq ,{alltrim(aDadosFil[1]),alltrim(aDadosFil[2]),alltrim(aDadosFil[18]),alltrim(aDadosFil[17]),right(aDadosFil[22],2) })
		::jFilialArq[alltrim(aDadosFil[2])] := len( ::aFilialArq )
	endif

	FwFreeArray(aFilsCalc)
	FwFreeArray(aDadosFil)

Return lVldFilial

/*/{Protheus.doc} SetCampoCustom
	Tratativa para campos cutomizado (Campo 5 , Código da Fatura Comercial), informado no parametro
	MV_CPCV201
	@author Ricardo Henrique de Mello Lima
	@since 14/03/2024
	@version 12.1.2310	
	/*/
Method SetCampoCustom() class Convenio201

	Local aCamposCustom := {}  as array

	aCamposCustom := FWSX3Util():GetAllFields(::cAliasCustom,.F.)
	if !Empty(aCamposCustom)
		if aScan(aCamposCustom,{|x| x == ::cCampoCustom }) != 0
			::lCampoCustom := .T.
		endif
	endif

	FwFreeArray(aCamposCustom)
Return

/*/{Protheus.doc} SetTipoDoc
	Pega os tipos de Documentos da SX5 Tabela 42 e filtra apenas os tipos 21 e 22  
	movimentos de telecomunicacoes
	@author Ricardo Henrique de Mello Lima
	@since 15/03/2024
	@version 12.1.2310
	/*/
Method SetTipoDoc() class Convenio201

	Local aSX5TipoDoc := {} as array
	Local nCount := 0 as numeric
	Local cModelo := "" as character

	aSX5TipoDoc := FWGetSX5("42")

	for nCount := 1 To Len(aSX5TipoDoc)
		cModelo := AModNot(Alltrim(aSX5TipoDoc[nCount,3]))
		if cModelo == "21" .OR. cModelo == "22"
			if Alltrim(::cTipoDoc) != ""
				::cTipoDoc += "/"
			endif
			::cTipoDoc += alltrim(aSX5TipoDoc[nCount,3])
			aAdd(::aTipoDoc,{alltrim(aSX5TipoDoc[nCount,3]),cModelo})
		endif
	next
	FwFreeArray(aSX5TipoDoc)
Return


/*/{Protheus.doc} ProcessaArquivo
	Processa Arquivo conforme os parametros
	@author Ricardo Henrique de Mello Lima
	@since 01/03/2024
	@version 12.1.2310
	/*/
Method ProcessaArquivo() class Convenio201

	Local oFile := nil as object
	Local cDtHoraIni := "" as character
	Local cDtHoraFim := "" as character
	Local nNroVolume := 0 as numeric
	Local nNroReg := 0 as numeric
	Local nArquivo := 0 as numeric
	Local nFiliais := Len(::aQtdArq) as numeric
	Local cNroDoc := "" as character
	Local nTamSerie := FWSX3Util():GetFieldStruct("FT_SERIE")[3]
	Local cSerieArq := "" as character

	// Caso for para aglutinar os CNPJ+IE iguais processa todos os reistros de todas as empresas/filiais em um unico arquivo

	if !Empty(::aQtdArq)

		cDtHoraIni := DToC(Date()) + "-" + Time()
		
		for nArquivo := 1 to nFiliais

			nNroVolume := 1
			cNroDoc := ""
			nNroReg := 0

			// Caso nao aglutina Filial controla o fluxo da Query conforme a Filial Serie
			if !::lAglutinaCPNJ
				::cFilialQuery := Alltrim(::aQtdArq[nArquivo,1])
			endif
			::cNumSerie := Alltrim(::aQtdArq[nArquivo,2])
			cSerieArq := padl( Alltrim(::aQtdArq[nArquivo,2]), nTamSerie,'0')

			::SetNomeArquivo(::aQtdArq[nArquivo],cValToChar(nNroVolume),cSerieArq)
			oFile := FWFileWriter():New(::cFileDest, .T., ::nBuffer)
			oFile:SetCaseSensitive(.T.)
			oFile:Create()

			::MontaQuery()
			(::cAliasSFT)->(DbGoTop())

			do while (::cAliasSFT)->(!EOF())

				if nNroReg == ::nLimiteLn
					cNroDoc := Alltrim((::cAliasSFT)->FT_NFISCAL)+cSerieArq
				endif

				if nNroReg > ::nLimiteLn
					if cNroDoc <> Alltrim((::cAliasSFT)->FT_NFISCAL)+cSerieArq
						cNroDoc := ""
						nNroReg := 0
						oFile:Close()
						FreeObj(oFile)
						nNroVolume++
						::SetNomeArquivo(::aQtdArq[nArquivo],cValToChar(nNroVolume),cSerieArq)
						oFile := FWFileWriter():New(::cFileDest, .T., ::nBuffer)
						oFile:SetCaseSensitive(.T.)
						oFile:Create()
					endif
				endif

				::cCPFCNPJ := ::setCampo(::aCPFCNPJ,::aQtdArq[nArquivo,5])
				::cUF := ::setCampo(::aUF,::aQtdArq[nArquivo,7])
				::cRazaoSocial := ::setCampo(::aRazaoSocial,::aQtdArq[nArquivo,6])
				::cEmissao := ::setCampo(::aEmissao,(::cAliasSFT)->FT_EMISSAO)
				::cCodFatura := ::setCampo(::aCodFatura,(::cAliasSFT)->CAMPOCUSTOM)
				::cOrdemItem := ::setCampo(::aOrdemItem,(::cAliasSFT)->FT_ITEM)
				::cCodItem := ::setCampo(::aCodItem,(::cAliasSFT)->FT_PRODUTO)
				::cDescItem := ::setCampo(::aDescItem,(::cAliasSFT)->B1_DESC)
				::cVlrItem := ::setCampo(::aVlrItem,(::cAliasSFT)->FT_TOTAL)
				if Alltrim((::cAliasSFT)->FX_RECEP) == "" .OR. Alltrim((::cAliasSFT)->XA1_CGC) == Alltrim(::aQtdArq[nArquivo,5])
					::cOrigemItem := ::setCampo(::aOrigemItem,"1")
					::cCNPJPart := StrZero(0, ::aCNPJPart[1])
					::cRazaoPart := StrZero(0, ::aRazaoPart[1])
				else
					::cOrigemItem := ::setCampo(::aOrigemItem,"2")
					::cCNPJPart := ::setCampo(::aCNPJPart,(::cAliasSFT)->XA1_CGC)
					::cRazaoPart := ::setCampo(::aRazaoPart,(::cAliasSFT)->XA1_NOME)
				endif
				::cVlrTotalFatura := ::setCampo(::aVlrTotalFatura,(::cAliasSFT)->FT_TOTAL)
				::cEmissaoFatura := ::setCampo(::aEmissaoFatura,(::cAliasSFT)->FT_EMISSAO)
				::cModelo := ::setCampo(::aModelo,::aQtdArq[nArquivo,4])
				::cSerieNF := ::setCampo(::aSerieNF,cSerieArq )
				::cNumeroNF := ::setCampo(::aNumeroNF,(::cAliasSFT)->FT_NFISCAL)
				::cVlrTotalNF := ::setCampo(::aVlrTotalNF,(::cAliasSFT)->F2_VALBRUT)

				::LayoutFC() // monta a linha do arquivo
				oFile:Write(::cLinhaArq+CRLF)
				(::cAliasSFT)->(DBSKIP())

				nNroReg++
			enddo

			oFile:Close()
			FreeObj(oFile)
		next
		cDtHoraFim :=  DToC(Date()) + "-" + Time()
		MsgInfo(STR0015 + CRLF + CRLF + STR0016 + cDtHoraIni +  CRLF + STR0017 + cDtHoraFim)	//"Arquivo gerado com sucesso!"#"Início: "#"Término: "
	else
		FWAlertInfo(STR0018,STR0014)	//"Não existe dados a serem listados","Convênio 201/17"
	endif
Return

/*/{Protheus.doc} LayoutFC
	Monta o Layout FC do Convenio 201/2017
	@author Ricardo Henrique de Mello Lima
	@since 11/03/2024
	@version 12.1.2310
	/*/
Method LayoutFC() class Convenio201

	::cLinhaArq := (::cCPFCNPJ+;
		::cUF+;
		::cRazaoSocial+;
		::cEmissao+;
		::cCodFatura+;
		::cOrdemItem+;
		::cCodItem+;
		::cDescItem+;
		::cVlrItem+;
		::cOrigemItem+;
		::cCNPJPart+;
		::cRazaoPart+;
		::cVlrTotalFatura+;
		::cEmissaoFatura+;
		::cModelo+;
		::cSerieNF+;
		::cNumeroNF+;
		::cVlrTotalNF)

Return

/*/{Protheus.doc} setDataProcess
	Define data inicial e final do processamento
	@author Ricardo Henrique de Mello Lima
	@since 06/03/2024
	@version version
	/*/
Method setDataProcess() class Convenio201

	Local nMes := 0 as numeric
	Local nAno := 0 as numeric

	if !Empty(::aWizard)
		nMes := Val(Substr(alltrim(::aWizard[1,1]),1,2))
		nAno := Val(Substr(alltrim(::aWizard[1,1]),3,4))
		::dDataIni   := MonthSub(CtoD("01/"+StrZero(nMes,2)+"/"+StrZero(nAno,4)),0)
		::dDataFim   := LastDay(::dDataIni)
	endif

Return

/*/{Protheus.doc} QueryGroup
	Monta a query para popular o arquivo
	@author Ricardo Henrique de Mello Lima
	@since 01/03/2024
	@version 12.1.2310
	/*/
Method QueryGroup() class Convenio201

	Local cQuery := "" as character
	Local oQueryConv201 := nil as object
	Local cAliasSFTGroup := "" as character

	if ::lAglutinaCPNJ
		cQuery := "SELECT SFT.FT_SERIE,SFT.FT_ESPECIE"
	else
		cQuery := "SELECT SFT.FT_FILIAL,SFT.FT_SERIE,SFT.FT_ESPECIE"
	endif
	cQuery += " FROM "+RetSqlName("SFT")+" SFT LEFT JOIN "+RetSqlName("SFX")+" SFX ON "+ FWJoinFilial("SFX","SFT") + ""
	cQuery += "     AND SFX.FX_SERIE = SFT.FT_SERIE AND SFX.FX_DOC = SFT.FT_NFISCAL  AND SFX.FX_CLIFOR = SFT.FT_CLIEFOR AND SFX.FX_LOJA = SFT.FT_LOJA"
	cQuery += "     AND SFX.FX_ITEM = SFT.FT_ITEM AND SFX.FX_COD = SFT.FT_PRODUTO AND SFX.D_E_L_E_T_ = ? "
	cQuery += " WHERE SFT.FT_FILIAL IN ?" 
	cQuery += " AND SFT.FT_TIPOMOV = ?"
	cQuery += " AND SFT.FT_ENTRADA BETWEEN ? AND ?"
	cQuery += " AND SFT.FT_TIPO NOT IN ?"
	cQuery += " AND SFT.FT_ESPECIE IN ?"
	cQuery += " AND SFT.D_E_L_E_T_= ?"

	if ::lAglutinaCPNJ
		cQuery += " GROUP BY SFT.FT_SERIE,SFT.FT_ESPECIE"
		cQuery += " ORDER BY SFT.FT_SERIE"
	else
		cQuery += " GROUP BY SFT.FT_FILIAL,SFT.FT_SERIE,SFT.FT_ESPECIE"
		cQuery += " ORDER BY SFT.FT_FILIAL,SFT.FT_SERIE"
	endif

	oQueryConv201 := FWPreparedStatement():New(ChangeQuery(cQuery))
	oQueryConv201:SetString(1, ' ' )
	oQueryConv201:SetUnSafe(2,FormatIn(::cFilialQuery,"/"))
	oQueryConv201:SetString(3, 'S' )
	oQueryConv201:SetDate(4,::dDataIni)
	oQueryConv201:SetDate(5,::dDataFim)
	oQueryConv201:SetUnSafe(6,FormatIn('B/D',"/"))
	oQueryConv201:SetUnSafe(7,FormatIn(::cTipoDoc,"/"))
	oQueryConv201:SetString(8, ' ' )

	cQuery := oQueryConv201:GetFixQuery()
	cAliasSFTGroup := MPSysOpenQuery(cQuery)

	do while (cAliasSFTGroup)->(!EOF())
		if !::lAglutinaCPNJ
			::nPosFilAglutina := ::jFilialArq[ alltrim( (cAliasSFTGroup)->FT_FILIAL ) ]
		endif

		aAdd(::aQtdArq,;
			{::aFilialArq[::nPosFilAglutina,2],;
			(cAliasSFTGroup)->FT_SERIE,;
			(cAliasSFTGroup)->FT_ESPECIE,;
			AModNot((cAliasSFTGroup)->FT_ESPECIE),;
			::aFilialArq[::nPosFilAglutina,3],;
			::aFilialArq[::nPosFilAglutina,4],;
			::aFilialArq[::nPosFilAglutina,5]})

		if Alltrim(::cNumSerie) != ""
			::cNumSerie += "/"
		endif
		if !(Alltrim((cAliasSFTGroup)->FT_SERIE) $ ::cNumSerie )
			::cNumSerie += Alltrim((cAliasSFTGroup)->FT_SERIE)
		endif
		(cAliasSFTGroup)->(DBSKIP())
	enddo

	oQueryConv201:Destroy()
	FreeObj(oQueryConv201)
Return

/*/{Protheus.doc} MontaQuery
	Monta a query para popular o arquivo
	@author Ricardo Henrique de Mello Lima
	@since 01/03/2024
	@version 12.1.2310
	/*/
Method MontaQuery() class Convenio201

	Local cQuery := "" as character
	Local oQueryConv201 := nil as object
	Local aEstru := {} as array

	Aadd(aEstru, { "FT_EMISSAO","D",FWSX3Util():GetFieldStruct("FT_EMISSAO")[3],FWSX3Util():GetFieldStruct("FT_EMISSAO")[4]} )	// "FT_EMISSAO"
	Aadd(aEstru, { "FT_ENTRADA","D",FWSX3Util():GetFieldStruct("FT_ENTRADA")[3],FWSX3Util():GetFieldStruct("FT_ENTRADA")[4]} )	// "FT_ENTRADA"

	cQuery := "SELECT"
	cQuery += " SFT.FT_FILIAL,SFT.FT_NFISCAL,SFT.FT_SERIE,SFT.FT_PRODUTO,SFT.FT_ESPECIE,SFT.FT_ENTRADA,SFT.FT_EMISSAO,"
	cQuery += " SFT.FT_ITEM,SFT.FT_PRODUTO,SFT.FT_TIPOMOV,SFT.FT_TOTAL,SFT.FT_CLIEFOR,SFT.FT_LOJA,"
	cQuery += " SA1.A1_CGC,SA1.A1_NOME,SA1.A1_EST,SB1.B1_DESC,SB1.B1_ORIGEM,F2_VALBRUT,"
	cQuery += " SFX.FX_DOC,ISNULL(SFX.FX_RECEP,'') AS FX_RECEP,ISNULL(SA1X.A1_CGC,'') AS XA1_CGC,ISNULL(SA1X.A1_NOME,'') AS XA1_NOME,ISNULL(SFX.FX_LOJAREC,'') AS FX_LOJAREC,"
	if ::lCampoCustom
		cQuery += " "+::cCampoCustom+" AS CAMPOCUSTOM"
	else
		cQuery += " "+Replicate("0",::aCodFatura[1])+" AS CAMPOCUSTOM"
	endif
	cQuery += " FROM "+RetSqlName("SFT")+" SFT"
	cQuery += " LEFT JOIN "+RetSqlName("SB1")+" SB1"
	cQuery += " 	ON "+FWJoinFilial("SFT","SB1")+""
	cQuery += " 	AND SFT.FT_PRODUTO = SB1.B1_COD"
	cQuery += " AND SB1.D_E_L_E_T_ = ''"
	cQuery += " LEFT JOIN "+RetSqlName("SA1")+" SA1"
	cQuery += "     ON "+FWJoinFilial("SFT","SA1")+""
	cQuery += "     AND SA1.A1_COD = SFT.FT_CLIEFOR"
	cQuery += "     AND SA1.A1_LOJA = SFT.FT_LOJA"
	cQuery += "     AND SA1.D_E_L_E_T_ = ''"
	cQuery += " LEFT JOIN "+RetSqlName("SF2")+" SF2"
	cQuery += "     ON "+FWJoinFilial("SF2","SFT")+""
	cQuery += "     AND SF2.F2_DOC = SFT.FT_NFISCAL"
	cQuery += "     AND SF2.F2_SERIE = SFT.FT_SERIE"
	cQuery += "     AND SF2.F2_CLIENTE = SFT.FT_CLIEFOR"
	cQuery += "     AND SF2.F2_LOJA = SFT.FT_LOJA"
	cQuery += "     AND SF2.F2_EMISSAO = SFT.FT_EMISSAO"
	cQuery += " RIGHT JOIN "+RetSqlName("SFX")+" SFX"
	cQuery += "     ON "+FWJoinFilial("SFX","SFT")+""
	cQuery += "     AND SFX.FX_DOC = SFT.FT_NFISCAL"
	cQuery += "     AND SFX.FX_SERIE = SFT.FT_SERIE"
	cQuery += "     AND SFX.FX_CLIFOR = SFT.FT_CLIEFOR"
	cQuery += "     AND SFX.FX_LOJA = SFT.FT_LOJA"
	cQuery += "     AND SFX.FX_ITEM = SFT.FT_ITEM"
	cQuery += "     AND SFX.FX_COD = SFT.FT_PRODUTO AND SFX.D_E_L_E_T_ = ''"
	cQuery += " LEFT JOIN "+RetSqlName("SA1")+" SA1X"
	cQuery += " 	ON "+FWJoinFilial("SA1X","SFX")+""
	cQuery += "     AND SA1X.A1_COD = SFX.FX_RECEP"
	cQuery += "     AND SA1X.A1_LOJA = SFX.FX_LOJAREC"
	cQuery += "     AND SA1X.D_E_L_E_T_ = ''"
	cQuery += " WHERE SFT.D_E_L_E_T_= ''"
	cQuery += " AND SFT.FT_FILIAL IN ?"
	cQuery += " AND SFT.FT_ENTRADA BETWEEN ? AND ?"
	cQuery += " AND SFT.FT_TIPOMOV ='S'"
	cQuery += " AND SFT.FT_TIPO NOT IN ('B','D')"
	cQuery += " AND SFT.FT_ESPECIE IN ?"
	cQuery += " AND SFT.FT_SERIE IN ?"
	cQuery += " ORDER BY FT_FILIAL,FT_ESPECIE,FT_SERIE,FT_NFISCAL"

	oQueryConv201 := FWPreparedStatement():New(ChangeQuery(cQuery))
	oQueryConv201:SetUnSafe(1,FormatIn(::cFilialQuery,"/"))
	oQueryConv201:SetDate(2,::dDataIni)
	oQueryConv201:SetDate(3,::dDataFim)
	oQueryConv201:SetUnSafe(4,FormatIn(::cTipoDoc,"/"))
	oQueryConv201:SetUnSafe(5,FormatIn(::cNumSerie,"/"))
	oQueryConv201:setFields(aEstru)

	cQuery := oQueryConv201:GetFixQuery()
	::cAliasSFT := MPSysOpenQuery(cQuery)
	oQueryConv201:doTcSetField(::cAliasSFT)
	oQueryConv201:Destroy()
	FreeObj(oQueryConv201)

Return

/*/{Protheus.doc} setCampo
	Trata o Campo 1 do Layout conforme manual https://www.confaz.fazenda.gov.br/legislacao/convenios/2017/CV201_17
	@author Ricardo Henrique de Mello Lima
	@since 11/03/2024
	@version 12.1.2310
	@param Parametros 1 = Conteudo do Campo, 2 = Tamanho do campo Conforme Layout, 3 Tipo do Campo 
	@return Valor do Campo (Variant)
	/*/
Method setCampo(aCampo as array, xValue as variant) as variant class Convenio201

	Local nLenValue := 0 as numeric
	Local cData := "" as character
	Local xValorAux as variant

	if aCampo[2] == "N"
		if ValType(xValue) == "N"
			xValue := strzero(val(StrTran( Str( xValue,, aCampo[3]), ".", "")),aCampo[1])
		elseif ValType(xValue) == "C"
			xValorAux := SubStr(Alltrim(xValue),1,aCampo[1])
			nLenValue := Len(xValorAux)
			xValue := cValTochar(StrZero(Val(Alltrim(xValue)),aCampo[1]))
		endif
	elseif aCampo[2] == "C"
		xValorAux := SubStr(Alltrim(xValue),1,aCampo[1])
		nLenValue := Len(xValorAux)
		xValue := xValorAux+Space(aCampo[1] - nLenValue)
	elseif aCampo[2] == "D"
		cData := Day2Str(xValue)+Month2Str(xValue)+Year2Str(xValue)
		xValue := cValTochar(StrZero(Val(cData),aCampo[1]))
	endif
Return xValue


/*/{Protheus.doc} SetNomeArquivo
	Monta o nome do Arquivo conforme regras do Layout https://www.confaz.fazenda.gov.br/legislacao/convenios/2017/CV201_17
	@author Ricardo Henrique de Mello Lima
	@since 15/03/2024
	@version 12.1.2310
	/*/
Method SetNomeArquivo(aArquivo as array,cNroVolume as character,cSerieArq as character) class Convenio201

	Local cNomeArq := "" as character
	Local cPath := alltrim(::aWizard[1,2]) as character
	Local cUF   := "" as character
	Local cCNPJ := "" as character

	// Define o Nome do arquivo conforme Layout. Qdo aglutina CNPJ = SIM, grava arquivo na filial consolidadora
	if ::lAglutinaCPNJ
		cUF   := alltrim(::aFilialArq[::nPosFilAglutina,5])
		cCNPJ := alltrim(::aFilialArq[::nPosFilAglutina,3])
	else
		cUF   := alltrim(aArquivo[7])
		cCNPJ := alltrim(aArquivo[5])
	endif
	
	cNomeArq := cUF + cCNPJ +;			// UF + CNPJ
		SubStr(::aWizard[1,1],5,6)+; 	// ano
		SubStr(::aWizard[1,1],1,2)+; 	// mes
		Alltrim(aArquivo[4])+; 			// Modelo do Documento
		cSerieArq+; 					// Serie do Documento
		Alltrim(::cTipoLayout)+; 		// Tipo do Arquivo
		SubStr(::aWizard[1,5],1,1)+; 	// Situação do Arquivo
		AllTrim(cNroVolume)+; 			// volume
		".txt"

	::cFileDest := cPath+alltrim(cNomeArq)
Return
