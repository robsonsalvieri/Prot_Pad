#include 'tlpp-core.th'
#include 'tlpp-object.th'

/*/{Protheus.doc} SpedFiscalUtil
	(backoffice.fiscal.arquivos.SpedFiscalUtil)
	@author Ricardo H de M Lima
	@since 30/11/2023
	@version 1.0
/*/

NameSpace backoffice.fiscal.arquivos.spedfiscalutil

Class SpedFiscalUtil

	// Atributos da Classe

	Private DATA aEstadoIn4123 as array
	Private DATA cFiliaislIn4123 as character
	Private Data nMes as Numeric
	Private Data nAno as Numeric
	Private Data cFilDe as character
	Private Data cFilAte as character
	Private Data dDataIni as date
	Private Data dDataFim  as date
	Private Data lConcFil as logical
	Private Data aSelFil as array
	Private Data nConFil as numeric
	Private Data nSelFil as numeric

	// Atributos exclusivas IN4123
	Private Data jCodLancMvIN4123 as json
	Private Data cMvIn4123 as character
	Private Data cAliasCDA as character
	Private Data cCodLancMvIN4123 as character

	// Metodos da Classe

	Public Method New() CONSTRUCTOR

	Private Method setMes(nValue as numeric)
	Private Method setAno(nValue as numeric)
	Private Method setDataInicialFinal(cFilDe as character)
	Private Method setFilDe(cValue as character)
	Private Method setFilAte(cValue as character)
	Private Method setaSelFil(aValue as array)
	Private Method setnSelFil(nValue as numeric)
	Private Method setnConFil(nValue as numeric)

	// Metodos exclusivos IN4123

	Private Method setCodigosAjusteIN4123() as logical
	Private Method setFiliaisIN4123()
	Private Method CDAQueryApuracao()
	Private Method CDAQueryArquivos()
	public Method LoadCDAApuracao(aLancamentos as array)
	public Method LoadCDAArquivos(aRegE110 as array, aRegE113 as array, aRegE111 as array , cFilSA2 as character) as array
	Public Method InitIN4123(nMes as numeric ,nAno as numeric,nSelFil as numeric, nConFil as numeric, cFilDe as character , cFilAte as character,aSelFIl as array) as logical
	Public Method LoadRelatorioConferencia() as array

EndClass

/*/{Protheus.doc} New
	Metodo contrutor da classe SpedFiscalUtil Inicializa os Atributos da Classe
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
/*/

Method New() class SpedFiscalUtil

	::aEstadoIn4123 := {"RS"} // pode considerar levara para parametro futuramente(MV_EST4123)
	::cFiliaislIn4123 := ""
	::nMes := 0
	::nAno := 0
	::dDataIni := ddatabase
	::dDataFim := ddatabase
	::cFilDe := """
	::cFilAte := ""
	::cMvIn4123 := GetNewPar("MV_IN4123",'{"RS001503":"RS021401"}')
	::cAliasCDA := ""
	::jCodLancMvIN4123 := nil
	::cCodLancMvIN4123 := ""
	::lConcFil := .T.
	::aSelFil := {}
	::nConFil := 0
	::nSelFil := 0

Return

/*/
	{Protheus.doc} setMes
	(long_description)
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
	@param recebe nValue e atribui a variavel mes
/*/

Method setMes(nValue as numeric) class SpedFiscalUtil
	::nMes := nValue
Return

/*/
	{Protheus.doc} setAno
	(long_description)
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
	@param recebe nValue e atribui a variavel Ano

/*/

Method setAno(nValue as Numeric) class SpedFiscalUtil
	::nAno := nValue
Return

/*/
	{Protheus.doc} setDataIniFim
	Metodo para pegar o primeiro e ultimo dia do periodo da apuração
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
/*/

Method setDataInicialFinal() class SpedFiscalUtil

	::dDataIni   := MonthSub(CtoD("01/"+StrZero(::nMes,2)+"/"+StrZero(::nAno,4)),1)
	::dDataFim   := LastDay(::dDataIni)

Return

/*/{Protheus.doc} setFilDe()
	Metodo responsavel por setar a Filial Inicial 
	@author Ricardo Henrique de Mello Lima
	@since 08/12/2023
	@version 1.0
	
	/*/
Method setFilDe(cValue as character) class SpedFiscalUtil
	::cFilDe := cValue
Return


/*/{Protheus.doc} setFilAte(cValue)
	Metodo responsavel por setar a Filial Final
	@author Ricardo Henrique de Mello Lima
	@since 08/12/2023
	@version 1.0
	/*/
Method setFilAte(cValue as character) class SpedFiscalUtil
	::cFilAte := cValue
Return

/*/{Protheus.doc} setaFil
	Seta o Array com as filiais selecionadas pelo usuario
	@author Ricardo Henrique de Mello Lima
	@since 03/01/2024
	@version 1.0
	/*/

Method setaSelFil(aValue as array) class SpedFiscalUtil
	::aSelFil := aValue
Return

/*/{Protheus.doc} setcSelFil
	Recebe o valor do parametro se seleciona filiais
	@author Ricardo Henrique de Mello Lima
	@since 03/01/2024
	@version 1.0	
	/*/
Method setnSelFil(nValue as numeric) class SpedFiscalUtil
	::nSelFil := nValue
Return

/*/{Protheus.doc} setcConfil
	Recebe o Valor do parametro se considera filias  
	@author Ricardo Henrique de Mello Lima
	@since 03/01/2024
	@version 1.0
	/*/
Method setnConfil(nValue as numeric) class SpedFiscalUtil
	::nConfil := nValue
Return

/*/{Protheus.doc} setCodigosAjusteIN4123
	Metodo que Popula o Json com a quantidade de Códigos de Lancamentos que deverao
	ser considerados na apuracao da IN4123 para estado do RS
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
/*/

Method setCodigosAjusteIN4123() class SpedFiscalUtil as logical

	Local nCount := 0 as numeric
	Local nLanc := 0 as numeric
	Local jMvIn4123 as json
	Local aGetLanc := {} as array
	Local nTamCodAju := FWSX3Util():GetFieldStruct("CDO_CODAJU")[3] as numeric
	Local cJsonResult := "" as character
	Local cRetorno as character
	Local lRetorno := .F. as logical


	jMvIn4123 := JsonObject():New()
	cRetorno := jMvIn4123:FromJson(::cMvIn4123)
	if ValType(cRetorno) == "U"
		::jCodLancMvIN4123 := jsonObject():New()

		if ValType(jMvIn4123) == 'J'

			aGetLanc := jMvIn4123:GetNames()
			nLanc := Len(aGetLanc)

			IF !Empty(aGetLanc)
				DBSELECTAREA("CDO")
				CDO->(DBSETORDER(1))

				for nCount := 1 To nLanc
					if(CDO->(MSSEEK(xFilial("CDO")+PADR(Alltrim(jMvIn4123:GetJsonText(aGetLanc[nCount])),nTamCodAju))))

						cJsonResult := '{'
						cJsonResult += '"'+aGetLanc[nCount]+'":[{'
						cJsonResult += '"origem":"'+aGetLanc[nCount]+'",'
						cJsonResult += '"destino":"'+jMvIn4123:GetJsonText(aGetLanc[nCount])+'",'
						cJsonResult += '"descricao":"'+StrTran(Alltrim(CDO->CDO_DESCR),'"',"")+'"}]'
						cJsonResult += '}'
						lRetorno := .T.
					endif
					::cCodLancMvIN4123 += aGetLanc[nCount] + IIf( nCount < nLanc ,"/","")
				Next
				::jCodLancMvIN4123:FromJson(cJsonResult)
			endif
		endif
	endif
Return lRetorno


/*/{Protheus.doc} setFiliais
	Metodo responsavel por recuperar as filias selecionadas e Filtrar apenas Filias do Estado do Rio Grande do Sul
	@author user
	@since 08/12/2023
	@version 1.0
	/*/

Method setFiliaisIn4123() class SpedFiscalUtil

	Local aAllFil := {} as array
	Local cEstIn4123 := "" as character
	Local cEstadoFil as character
	Local nQtdStrIDMID := 0 as numeric
	Local cIDMID as character
	Local nCount as numeric
	Local nCountEst := 0 as numeric
	Local nQtdAllFill := 0 as numeric
	Local nQtdEstado := 0 as numeric
	local cEstFil as character
	local cEstPar as character

	::cFiliaislIn4123 := ""
	cEstIn4123 := ArrTokStr(::aEstadoIn4123,"|")
	nQtdEstado := len(::aEstadoIn4123)

	if ::nSelfil == 1 .and. !Empty(::aSelFil) // caso o seleciona filias for SIM considero as filias selecionadas na rotina que estiverem com .T. na primeira posicao do array

		For nCount := 1 to Len(::aSelFil)
			if ::aSelFil[nCount,1]
				cIDMID := FWArrFilAtu(,::aSelFil[nCount,2])[22]
				nQtdStrIDMID := Len(cIDMID)
				cEstadoFil := Substring(cIDMID,nQtdStrIDMID-1,nQtdStrIDMID)
				if(cEstadoFil == cEstIn4123)
					if Alltrim(::cFiliaislIn4123) <> ""
						::cFiliaislIn4123 += "/"
					endif
					::cFiliaislIn4123 += alltrim(::aSelFil[nCount,2])
				endif
			endif
		next

	elseif ::nSelFil != 1 .and. ::nConFil == 1 // caso a pergunta seleciona filial for diferente de SIM, e a considera filial = sim , pego o range de Filias De/Até considerando a empresa logada

		aAllFil := FWAllFilial(,,cEmpAnt,.F.)
		nQtdAllFill := len(aAllFil)

		if !Empty(::cFilAte)
			if !Empty(aAllFil)
				for nCountEst := 1 To nQtdEstado
					for nCount := 1 To nQtdAllFill
						cEstFil := Alltrim(FwSM0Util():getSM0Data(,aAllFil[nCount],{"M0_ESTENT"})[1][2])
						cEstPar := Alltrim(::aEstadoIn4123[nCountEst])
						if cEstFil == cEstPar .and. aAllFil[nCount] >= ::cFilDe .and. aAllFil[nCount] <= ::cFilAte
							if ! Empty(::cFiliaislIn4123)
								::cFiliaislIn4123 += "/"
							endif
							::cFiliaislIn4123 += alltrim(aAllFil[nCount])
						endif
					next
				Next
			endif
		else
			::cFiliaislIn4123 := cFilAnt
		endif

	elseif ::nSelFil != 1 .and. ::nConfil != 1 // caso as perguntas na apruracao de seleciona filial e considera filais forem igual a não pego apenas a filial corrente

		::cFiliaislIn4123 := cFilAnt

	endif

	FwFreeArray(aAllFil)
Return


/*/{Protheus.doc} CDAQueryapuracao
	Query Utilizada para Gerar os Registros de Ajuste na Apuração do ICMS , com os dados da CDA
	do Periodo Anterior
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0
/*/

Method CDAQueryApuracao() class SpedFiscalUtil

	Local cQuery := ""  as character
	Local oQueryCDAApuracao := nil as object

	cQuery := "SELECT CDA.CDA_CODLAN,SUM(CDA_VALOR) AS CDA_VALOR"
	cQuery += " FROM "+RetSqlName("CDA")+" CDA"
	cQuery += "	LEFT JOIN "+RetSqlName("SFT")+" SFT ON"
	cQuery += " "+FWJoinFilial("SFT", "CDA")+""
	cQuery += "		AND SFT.FT_TIPOMOV = CDA.CDA_TPMOVI"
	cQuery += "		AND SFT.FT_SERIE = CDA.CDA_SERIE"
	cQuery += "		AND SFT.FT_NFISCAL = CDA.CDA_NUMERO"
	cQuery += "		AND SFT.FT_CLIEFOR = CDA.CDA_CLIFOR"
	cQuery += "		AND SFT.FT_LOJA = CDA.CDA_LOJA"
	cQuery += "		AND SFT.FT_ITEM = CDA.CDA_NUMITE"
	cQuery += "		AND SFT.D_E_L_E_T_ = ''"
	cQuery += "	WHERE CDA.CDA_FILIAL In  ?"
	cQuery += "	AND CDA.CDA_CODLAN In ?"
	cQuery += "	AND SFT.FT_ENTRADA BETWEEN ? AND ?"
	cQuery += "	AND CDA.D_E_L_E_T_ = ''"
	cQuery += "	GROUP BY CDA.CDA_CODLAN"

	cQuery := ChangeQuery(cQuery)

	oQueryCDAApuracao := FWPreparedStatement():New(cQuery)
	oQueryCDAApuracao:SetUnSafe(1,FormatIn(::cFiliaislIn4123,"/"))
	oQueryCDAApuracao:SetUnSafe(2,FormatIn(::cCodLancMvIN4123,"/"))
	oQueryCDAApuracao:SetDate(3,::dDataIni)
	oQueryCDAApuracao:SetDate(4,::dDataFim)

	cQuery := oQueryCDAApuracao:GetFixQuery()
	::cAliasCDA  := MPSysOpenQuery(cQuery)

Return

/*/
	{Protheus.doc} CDAQueryArquivos
	Query Utilizada para Gerar os dados da geração do Arquivo Magnético EFD ICMS/IPI
	com os registros referente as informações da CDA do mes anterior para geração
	dos Blocos E113 0150 0200
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version 1.0

/*/

Method CDAQueryArquivos() class SpedFiscalUtil

	Local cQuery := "" as character
	Local oQueryCDAArquivo := nil as object
	Local aEstru := {} as array

	Aadd(aEstru, { "FT_EMISSAO","D",FWSX3Util():GetFieldStruct("FT_EMISSAO")[3],FWSX3Util():GetFieldStruct("FT_EMISSAO")[4]} )	// "FT_EMISSAO"
	Aadd(aEstru, { "FT_ENTRADA","D",FWSX3Util():GetFieldStruct("FT_ENTRADA")[3],FWSX3Util():GetFieldStruct("FT_ENTRADA")[4]} )	// "FT_ENTRADA"

	cQuery := "SELECT CDA.CDA_CODLAN,CDA.CDA_BASE,CDA.CDA_ALIQ,CDA.CDA_VALOR,CDA.CDA_VLOUTR,CDA.CDA_ESPECI,CDA.CDA_SERIE,CDA.CDA_NUMERO,CDA.CDA_TPMOVI,"
	cQuery += " CDA.CDA_NUMITE,SFT.FT_FILIAL,SFT.FT_EMISSAO,SFT.FT_CHVNFE,SFT.FT_TIPOMOV,SFT.FT_SERIE,SFT.FT_NFISCAL,SFT.FT_CLIEFOR,SFT.FT_LOJA ,SFT.FT_ESTADO,"
	cQuery += "	SFT.FT_CFOP,SFT.FT_PRODUTO,SFT.FT_CLIEFOR,SFT.FT_LOJA,SFT.FT_POSIPI,SFT.FT_CLIENT,SFT.FT_LOJENT,SFT.FT_ENTRADA,SB1.B1_UM,SB1.B1_DESC,SB1.B1_CODGTIN,SB1.B1_CODBAR,SB1.B1_CEST,SB1.B1_CODISS,SB1.B1_EX_NCM"
	cQuery += "	FROM "+RetSqlName("CDA")+" CDA"
	cQuery += "	LEFT JOIN "+RetSqlName("SFT")+" SFT ON"
	cQuery += " "+FWJoinFilial("SFT", "CDA")+""
	cQuery += "		AND SFT.FT_TIPOMOV = CDA.CDA_TPMOVI"
	cQuery += "		AND SFT.FT_SERIE = CDA.CDA_SERIE"
	cQuery += "		AND SFT.FT_NFISCAL = CDA.CDA_NUMERO"
	cQuery += "		AND SFT.FT_CLIEFOR = CDA.CDA_CLIFOR"
	cQuery += "		AND SFT.FT_LOJA = CDA.CDA_LOJA"
	cQuery += "		AND SFT.FT_ITEM = CDA.CDA_NUMITE"
	cQuery += "		AND SFT.D_E_L_E_T_ = ''"
	cQuery += "	LEFT JOIN "+RetSqlName("SB1")+" SB1 ON "+FWJoinFilial("SFT", "SB1")+" AND SB1.B1_COD = SFT.FT_PRODUTO"
	cQuery += "	WHERE CDA.CDA_FILIAL IN  ?"
	cQuery += "	AND CDA.CDA_CODLAN In ?"
	cQuery += "	AND SFT.FT_ENTRADA BETWEEN ? AND ?"
	cQuery += "	AND CDA.D_E_L_E_T_ = ''	"

	cQuery := ChangeQuery(cQuery)

	oQueryCDAArquivo := FWPreparedStatement():New(cQuery)
	oQueryCDAArquivo:SetUnSafe(1,FormatIn(::cFiliaislIn4123,"/"))
	oQueryCDAArquivo:SetUnSafe(2,FormatIn(::cCodLancMvIN4123,"/"))
	oQueryCDAArquivo:SetDate(3,::dDataIni)
	oQueryCDAArquivo:SetDate(4,::dDataFim)
	oQueryCDAArquivo:setFields(aEstru)

	cQuery := oQueryCDAArquivo:GetFixQuery()
	::cAliasCDA := MPSysOpenQuery(cQuery)
	oQueryCDAArquivo:doTcSetField(::cAliasCDA)
	

Return

/*/
	{Protheus.doc} INITIN4123
	Metodo que inicializa os Methodos comuns para geração dos dados
	para compor o Lnaçamento de Ajuste referente a IN4123 do Estado RS
	@author Ricardo H de Mello Lima
	@since 01/12/2023
	@version 1.0

/*/
Method InitIN4123( nMes as numeric, nAno as numeric,nSelFil as numeric,nConFil as numeric, cFilDe as character, cFilAte as character,aLisFil as array,lConcFil as logical) as logical class SpedFiscalUtil  //MV_PAR01,MV_PAR02,MV_PAR10,MV_PAR19,MV_PAR11,MV_PAR12,aLisFil,cMv_Estado

	Local lRetorno := .F. as logical
	::lConcFil := lConcFil
	::setMes(nMes)
	::setAno(nAno)
	::setFilde(cFilDe)
	::setFilAte(cFilAte)
	::setnConfil(nConFil)
	::setnSelFil(nSelFil)
	::setaSelFil(aLisFil)
	::setDataInicialFinal()
	lRetorno := ::setCodigosAjusteIN4123()
	::setFiliaisIn4123()


Return lRetorno

/*/
	{Protheus.doc} CDAIN4123
	Metodo que Recupera a Query e Monta o Array com os Registros da CDA
	@author Ricardo H de M Lima
	@since 01/12/2023
	@version version
/*/

Method LoadCDAArquivos(aRegE110 as array, aRegE113 as array,aRegE111 as array, cFilSA2 as character) as array class SpedFiscalUtil

	Local aQtdCodAjustes := ::jCodLancMvIN4123:GetNames() as array
	Local nQtdAjuste := Len(aQtdCodAjustes) as numeric
	Local nCount := 0 as numeric
	Local jFindCodLanc as json
	Local nSeqReg111 as numeric
	Local aSa2 := {} as array
	Local cChvSeek := "" as character
	Local aConfPart := {} as array
	Local aProd := {} as array
	Local aRegIn4123 as array
	Local lReg := .F. as logical

	::CDAQueryArquivos()
	(::cAliasCDA)->(DbGoTop())

	if (::cAliasCDA)->(!EOF()) .Or. (::cAliasCDA)->(!BOF())
		lReg := .T.
		if nQtdAjuste > 0
			for nCount := 1 to nQtdAjuste

				jFindCodLanc := JsonObject():New()
				jFindCodLanc := ::JCODLANCMVIN4123:GetJsonObject(alltrim(aQtdCodAjustes[nCount]))

				jFindCodLanc[nCount]:GetJsontext('origem') //1 - Codigo Origem
				jFindCodLanc[nCount]:GetJsontext('destino') //2 - Codigo destino
				jFindCodLanc[nCount]:GetJsontext('descricao') //3 - Descrição destino

				nSeqReg111 := aScan(aRegE111,{|x|alltrim(x[3]) == alltrim(jFindCodLanc[nCount]:GetJsontext('destino'))})
				(::cAliasCDA)->(DbGoTop())
				Do While (::cAliasCDA)->(!EOF())
					if alltrim((::cAliasCDA)->CDA_CODLAN) == alltrim(aQtdCodAjustes[nCount])
						// Monta Participantes SA2
						cChvSeek := cFilSA2+(::cAliasCDA)->FT_CLIEFOR+(::cAliasCDA)->FT_LOJA

						If SA2->(MsSeek(cChvSeek))
							aConfPart := {"SA2",iif(::lConcFil,cFilSA2,"")}
							aAdd(aSa2,{InfPartDoc(aConfPart[1])})
						endif
						// Monta Array E113

						AADD(aRegE113,{;
							nSeqReg111,; // Sequencia do Registro Pai E110
						"E113",; // Código do Bloco a Gerar
						aConfPart[1]+iif(::lConcFil,(::cAliasCDA)->FT_FILIAL,"")+alltrim((::cAliasCDA)->FT_CLIEFOR)+alltrim((::cAliasCDA)->FT_LOJA),; //Código do Participante
						AModNot((::cAliasCDA)->CDA_ESPECI),; // Código da especie do Documento
						(::cAliasCDA)->FT_SERIE,; // Serie do Documento
						"",;
							(::cAliasCDA)->CDA_NUMERO,; // Numero do Documento
						StrZero(Day((::cAliasCDA)->FT_EMISSAO),2)+StrZero(Month((::cAliasCDA)->FT_EMISSAO),2)+StrZero(Year((::cAliasCDA)->FT_EMISSAO),4),; // data do movimento
						(::cAliasCDA)->FT_PRODUTO+iif(::lConcFil,alltrim((::cAliasCDA)->FT_FILIAL),""),; // Código do produto
						(::cAliasCDA)->CDA_VALOR,; // Valor do produto
						(::cAliasCDA)->FT_CHVNFE; // chave do documento
						})

						aAdd(aProd,{(::cAliasCDA)->FT_PRODUTO+IIF(::lConcFil,(::cAliasCDA)->FT_FILIAL,"")})
					endif
					(::cAliasCDA)->(DbSkip())
				Enddo
			next
		endif
	endif
	aRegIn4123 := {aRegE113,aSa2,aProd,lReg}
Return aRegIn4123


 /*/{Protheus.doc} LoadCDAApuracao
	Função responável por retornar o array aCDAIC recuperado da Apuração e inserir informações da CDA do mes Anterior.
	@Ricardo Henrique de Mello Lima
	@since 04/12/2023
	@Parametros: Recebe o Array contendo os Lançamentos de Ajuste da Apuração do ICMS
	/*/

Method LoadCDAApuracao(aCDAIC as array) class SpedFiscalUtil

	Local aLancAjuste := {} as array
	Local aQtdCodAjustes := ::jCodLancMvIN4123:GetNames() as array
	Local nQtdAjuste := Len(aQtdCodAjustes) as numeric
	Local nCount := 0 as numeric
	Local jFindCodLanc as json
	Local cCodLanc := "" as character

	::CDAQueryApuracao()
	(::cAliasCDA)->(DbGoTop())
	if (::cAliasCDA)->(!EOF()) .Or. (::cAliasCDA)->(!BOF())
		(::cAliasCDA)->(DbGoTop())
		if nQtdAjuste > 0
			for nCount := 1 to nQtdAjuste
				Do While (::cAliasCDA)->(!EOF())
					cCodLanc := alltrim((::cAliasCDA)->CDA_CODLAN)
					jFindCodLanc := JsonObject():New()
					jFindCodLanc := ::JCODLANCMVIN4123:GetJsonObject(cCodLanc)

					aAdd(aLancAjuste ,{;
						jFindCodLanc[nCount]:GetJsontext('origem'),; 	  	  //1 - Codigo Origem
					jFindCodLanc[nCount]:GetJsontext('destino'),;    	  //2 - Codigo destino
					jFindCodLanc[nCount]:GetJsontext('descricao'),;  	  //3 - Descrição destino
					DtoS(::dDataIni),;  		                          //4 - Dt.Inicio Período Anterior
					DtoS(::dDataFim),; 			                          //5 - Dt.Final Período Anterior
					(::cAliasCDA)->CDA_VALOR;							  //6 - Valor Acumulado
					})
					(::cAliasCDA)->(DbSkip())
				Enddo
			Next
		endif

		if !Empty(aLancAjuste)
			for ncount := 1 to Len(aLancAjuste)
				AADD(aCDAIC,{;
					"006",;
					"",;
					aLancAjuste[nCount,3],;
					aLancAjuste[nCount,6],;
					"",;
					"",;
					alltrim(aLancAjuste[nCount,2]),;
					aLancAjuste[nCount,4]+aLancAjuste[nCount,5]+aLancAjuste[nCount,2],;
					.F.,;
					.F.,;
					"",;
					""})
			Next
		endif
	endif
	FwFreeArray(aLancAjuste)
Return

/*/{Protheus.doc}LoadRelatorioConferencia
	Metodo que Carrega a Query para Utilização da extração de dados para montagem do 
	relatório de conferencia 
	@author Ricardo Henrique de Mello Lima
	@since 26/12/2023
	@version 1.0
	@return alias da tabela
	/*/

Method LoadRelatorioConferencia() as array class SpedFiscalUtil

	Local cAlias as character
	Local aParametros := {} as array

	::CDAQueryArquivos()
	(::cAliasCDA)->(DbGoTop())
	cAlias := ::cAliasCDA

Return aParametros := {::jCodLancMvIN4123,cAlias}
