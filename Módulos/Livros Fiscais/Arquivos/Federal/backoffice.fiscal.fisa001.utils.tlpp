#include 'tlpp-core.th'
#include 'tlpp-rest.th'


Namespace totvs.protheus.backoffice.fiscal.fisa001.utils

//-------------------------------------------------------------------
/*/{Protheus.doc} CodigoReceita
Classe para tratamento dos códigos de receita

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Class CodigoReceita

    Data cMV_CDRCEC as character

    Public Method new() constructor
    Public Method toArray() as array
    
End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Metodo construtor da classe
Faz a carga inicial no atributo cMV_CDRCEC com um GetNewPar do parâmetro
MV_CDRCEC

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method new() Class CodigoReceita

Local cConteudo as character
    
    cConteudo:= alltrim(GetNewPar("MV_CDRCEC","{'','','',''}"))

    if Left(cConteudo,1)=="{" .and. Right(cConteudo,1)=="}"
        ::cMV_CDRCEC:= Substr(cConteudo,2,len(cConteudo)-2)
        ::cMV_CDRCEC:= StrTran(::cMV_CDRCEC,'"')
        ::cMV_CDRCEC:= StrTran(::cMV_CDRCEC,"'")
    else
        ::cMV_CDRCEC:= "'','','',''"
    endif

Return Self
//-------------------------------------------------------------------
/*/{Protheus.doc} toArray
Metodo que tranforma a sprint em um array e devolve um array

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method toArray() as array Class CodigoReceita
Local aArray:= {} as array

    aArray:= StrTokArr(::cMV_CDRCEC,",")

Return(aArray)

//-------------------------------------------------------------------
/*/{Protheus.doc} RegexValids
Classe para tratamento de validações usando o Regex (tRegex)

@author Matheus Bispo
@since 29/04/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Class RegexValids
    Data oRegexNumLetra as Object
    
    Public Method new() constructor
    Public Method VldRegex() as logical
EndClass

Method new() Class RegexValids
    ::oRegexNumLetra := tRegex():new('^[a-zA-Z0-9]+$',.T.) //Regex que só aceita letras e números
Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} VldRegex
Validação do regex

@param cConteud - Conteúdo que será verificado no Regex

@author Matheus Massarotto
@since 18/04/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method VldRegex(cConteud) as logical Class RegexValids
Return ::oRegexNumLetra:Search(cConteud)

//-------------------------------------------------------------------
/*/{Protheus.doc} FieldForFolder
Classe para tratamento de campos e pastas de determinada tabela

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Class FieldForFolder

    Data cAlias as character
    Data aStrFolder as array
    Data aStrFields as array
    Data oFoldFiels as object
    Data nPosFolder as numeric
    Data nPosCampo  as numeric

    Public Method new() constructor
    Public Method getFieldsToJson() as object
    Public Method getFieldsToArray() as array
    Public Method getFolders() as array
    Public Method Destroy()

End Class


//-------------------------------------------------------------------
/*/{Protheus.doc} new
Metodo construtor da classe
Faz a carga inicial nos atributos como os arrays contendo a estrutura 
de pastas e campos de uma tabela

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method new(cAlias) Class FieldForFolder
    ::cAlias := cAlias
    ::oFoldFiels := JsonObject():New()
    ::nPosFolder := 11
    ::nPosCampo  := 1

    ::aStrFolder := FWFormStruct(2, ::cAlias)['AFOLDERS']
    ::aStrFields := FWFormStruct(2, ::cAlias)['AFIELDS']

Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} getFieldsToJson
Metodo para retornar um objeto json com suas pastas e campos
Exemplo: {"1":"CJT_FILIAL,CJT_NUM", "2":CJT_PROD, "3":"CJT_FILD"}

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method getFieldsToJson() as object Class FieldForFolder
    Local nI        as numeric
    Local nX        as numeric
    Local nLenFol   as numeric
    Local nLenFil   as numeric

    nLenFol:= len(::aStrFolder)
    nLenFil:= len(::aStrFields)
    
    ::oFoldFiels["O"]:=""
    for nI:=1 to nLenFol
        ::oFoldFiels[::aStrFolder[nI]:cId]:=""
    next

    for nX:=1 to nLenFil
        ::oFoldFiels[PosObj(::aStrFields[nx][::nPosFolder])]+=::aStrFields[nx][::nPosCampo]+","
    next
    

Return ::oFoldFiels

//-------------------------------------------------------------------
/*/{Protheus.doc} PosObj
Metodo para tratar pastas não definidas(SXA)

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Static Function PosObj(cAtrib)
Local cRet:=cAtrib  as character

    if empty(cAtrib)
        cRet:="O"
    endif

Return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getFieldsToArray
Metodo para retornar um array multidimensional com os campos de cada
pasta
Exemplo: {{CJT_FILIAL,CJT_NUM},
          {CJT_PROD},
          {CJT_FILD}}

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method getFieldsToArray() as array Class FieldForFolder
Local aArray    := {}   as array
Local nLenFol   as numeric
Local nI        as numeric

nLenFol:= len(::aStrFolder)

for nI:=1 to nLenFol
    aadd(aArray,StrTokArr(::oFoldFiels[cvaltochar(nI)],",")) 
next

if ::oFoldFiels:HasProperty("O")
    aadd(aArray,StrTokArr(::oFoldFiels["O"],",")) 
endif

Return aArray

//-------------------------------------------------------------------
/*/{Protheus.doc} getFolders
Metodo para retornar um array com a estrutura de pastas SXA

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method getFolders() as array Class FieldForFolder
Return ::aStrFolder

//-------------------------------------------------------------------
/*/{Protheus.doc} getFolders
Metodo para limpar os atributos da classe

@author Matheus Massarotto
@since 27/03/2024
@version 12.1.2310
/*/
//-------------------------------------------------------------------
Method Destroy() Class FieldForFolder

    ::cAlias := ""
    FreeObj(::oFoldFiels)

    FwFreeArray(::aStrFolder)
    FwFreeArray(::aStrFields)

Return 
