#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.fisa317.efdagenda

//-------------------------------------------------------------------
/*/{Protheus.doc} EfdAgendamento
    Classe que irá tratar das regras de Agendamento 
    vindas da rotina FISA316
    @author Alexandre Esteves
    @since 19/04/2024
    @version 12.1.2410
/*/
//-------------------------------------------------------------------
Class EfdAgendamento

    Data aFields    as array
    Data oQryCJT    as object
    Data oQryCJU    as object    

    Public Method new() constructor
    Public Method queryCJT() as character
    Public Method queryCJU() as character
    Public Method setPeriodoIni() as Date
    Public Method Destroy() 
    
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} New
    Metodo construtor da Classe
    @author Alexandre Esteves
    @since 19/04/2024
    @version 12.1.2410
    @param cAlias, character, Alias da CJT
    @return Self
/*/
//-------------------------------------------------------------------
Method new(cAlias as character) Class EfdAgendamento

    ::aFields    := FWFormStruct(2, cAlias)['AFIELDS']
    ::oQryCJT    := nil
    ::oQryCJU    := nil

Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} queryCJT
    Método para executar a consulta no banco de dados e retornar o
    Alias da CJT
    @author Alexandre Esteves
    @since 23/04/2024
    @version 12.1.2410
    @param cParam, character, Codigo do Agendamento que vem do Scheddef
    @return cAlias, character, Alias da tabela CJT
/*/
//-------------------------------------------------------------------
Method queryCJT(cCodReg as character) as character Class EfdAgendamento

Local cAlias        as character
Local cSelect := "" as character
Local cQuery  := "" as character
Local nLenFil       as numeric
Local nI            as numeric
Local aCampos := {} as array

Default cCodReg := ""

aCampos := ::aFields

nLenFil := len(aCampos)

For nI := 1 to nLenFil
    if nI <> nLenfil
        cSelect += " CJT."+aCampos[nI][1]+ " , "
    else
        cSelect += " CJT."+aCampos[nI][1]+ "  "
    Endif
Next
    
cQuery := " SELECT ? "
cQuery += " FROM "+RetSqlName("CJT")+" CJT "
cQuery += " WHERE CJT_CODREG = ? "
cQuery += " AND CJT_FILIAL = ? "
cQuery += " AND CJT.D_E_L_E_T_ = '' "

::oQryCJT:= FwExecStatement():New(cQuery)
::oQryCJT:SetUnsafe(01, cSelect )
::oQryCJT:SetString(02, cCodReg )
::oQryCJT:SetString(03, FwxFilial("CJT"))

cAlias := ::oQryCJT:OpenAlias(GetNextAlias())

FwFreeArray(::aFields)

Return cAlias

//-------------------------------------------------------------------
/*/{Protheus.doc} queryCJU
    Método para executar a consulta no banco de dados e retornar o
    Alias da CJU
    @author Alexandre Esteves
    @since 23/04/2024
    @version 12.1.2410
    @param cId, character, Id da tabela CJT como referência
    @return cAlias, character, Alias da tabela CJU
/*/
//-------------------------------------------------------------------
Method queryCJU(cId as character) as character Class EfdAgendamento

Local cAlias  := "" as character
Local cQuery  := "" as character

Default cId := ""

cQuery := " SELECT CJU.CJU_EMPSEL , CJU.CJU_FILSEL "
cQuery += " FROM "+RetSqlName("CJU")+" CJU "
cQuery += " WHERE CJU_ID = ? "
cQuery += " AND CJU.D_E_L_E_T_ = '' "

::oQryCJU:= FwExecStatement():New(cQuery)
::oQryCJU:SetString(01, cId )

cAlias := ::oQryCJU:OpenAlias(GetNextAlias())

Return cAlias

//--------------------------------------------------------------------------
/*/{Protheus.doc} setPeriodoIni
    Método para tratar as datas de acordo com o tipo de periodo selecionado
    @author Alexandre Esteves
    @since 29/04/2024
    @version 12.1.2410
    @param cTpPer, character, Tipo de Periodo 
    @return dDataIni, date , Data de Inicio do processamento de acordo com
    o tipo de periodo.
/*/
//--------------------------------------------------------------------------
Method setPeriodoIni(cTpPer as character) as Date Class EfdAgendamento

Local dDataIni := CtoD("01/"+StrZero(Month(dDataBase),2)+"/"+Str(Year(dDataBase))) as Date // Caso seja opção 1 - Mês Corrente

Default cTpPer := "1" 

Do Case   		
	Case cTpPer == "2" //Mês Anterior
		dDataIni := MonthSub(dDataIni,1)
    Case cTpPer == "3" //2 Meses Anterior
		dDataIni := MonthSub(dDataIni,2)
EndCase
    
Return dDataIni

//--------------------------------------------------------------------------
/*/{Protheus.doc} Destroy
    Limpa e Elimina os objetos
    @author Alexandre Esteves
    @since 23/04/2024
    @version 12.1.2410
/*/
//--------------------------------------------------------------------------
Method Destroy() Class EfdAgendamento

    FreeObj(::oQryCJT)
    FreeObj(::oQryCJU)

Return
