#include "SIGAWIN.CH"
#include "PROTHEUS.CH"
#DEFINE _NOMEIMPOS 01
#DEFINE _ALIQUOTA  02
#DEFINE _BASECALC  03
#DEFINE _IMPUESTO  04
#DEFINE _RATEOFRET 11
#DEFINE _IVAFLETE  12
#DEFINE _RATEODESP 13
#DEFINE _IVAGASTOS 14
#DEFINE _VLRTOTAL  3
#DEFINE _FLETE     4
#DEFINE _GASTOS    5

//Posicoes  do terceiro array recebido nos impostos a traves da matxfis...
#DEFINE X_IMPOSTO    01 //Nome do imposto
#DEFINE X_NUMIMP     02 //Sufixo do imposto

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Programa  º   M460IVA  º Autor º     Lucas       º Data º   02/12/99  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º                 Programa que calcula o IVA                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Sintaxe   º M460IVA                                                   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Parametrosº                                         			      º±±
±±º         1 º cCalculo                                                  º±±
±±º         2 º nItem                                                     º±±
±±º         3 º aInfo                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Retorno   º aImposto                                                  º±±
±±ºÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso       º MATA10x, LOJA010 e LOJA220, chamado pelo ponto de entrada º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º         Atualizacoes efetuadas desde a codificacao inicial            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramadorº Data   º BOPS º  Motivo da Alteracao                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºFernando M.º02/11/00ºxxxxxxº Tratamento para FEPP, que afeta base do   º±±
±±º           º        º      º IVA se for negativo (Loc. Chile)          º±±
±±º           º        º      º                                           º±±
±±ºFernando M.º26/03/01ºxxxxxxº Arredondamento de acordo com a moeda, va- º±±
±±º           º        º      º lidar o tipo de cliente para o SigaLoja   º±±
±±º           º        º      º                                           º±±
±±º Nava      º18/07/01º      º Reescrito pela funcao GetCountryList.     º±±
±±º           º        º      º                                           º±±
±±ºL Samaniegoº15/12/17ºDMICNSºValida si existe un campo en aHeaders, si  º±±
±±º           º        º -390 ºno existe, no consulta la posicion en el   º±±
±±º           º        º      ºaCols.                                     º±±
±±ºOscar G.   º15/10/18ºDMINA-ºConsiderar otro impuesto en la base.       º±±
±±º           º        º3756  º(Impuesto incidente) ECU                   º±±
±±ºLEnriquez  º28/11/19ºDMINA-ºSe realiza corrección de error.log por la  º±±
±±º           º        º7405  ºvariable aImp (ECU)                        º±±
±±ºARodriguez º03/12/19ºDMINA-ºRevisión por performance en SigaLoja (MEX) º±±
±±º           º        º7369  º                                           º±±
±±ºEduardo Przº21/09/20ºDMINA-ºSe realiza correccion en funcion M460IVAME º±±
±±º           º        º10070 ºpara calcular correctamente  Imp. IVA (MEX)º±±
±±ºOscar G.   º09/05/21ºDMINA-ºSe añade tratamiento para MV_DESCSAI = 1   º±±
±±º           º        º12335 ºal utilizar descuentos. EQU                º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M460IVA(cCalculo,nItem,aInfo)

LOCAL cFunct
LOCAL aRet
LOCAL lXFis
LOCAL aCountry // Array de Paises nesta forma : // { { "BRA" , "Brasil", "BR" } , { "ARG" , "Argentina","AR"} ,
LOCAL aArea 	:= GetArea()

lXFis:=(MafisFound() .And. ProcName(1)!="EXECBLOCK")
// .T. - metodo de calculo utilizando a matxfis
// .F. - metodo antigo de calculo

Do Case
	Case cPaisLoc == "MEX"
		aRet := M460IVAME(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "PAR"
		aRet := M460IVAPA(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "CHI"
		aRet := M460IVACH(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "ARG"
		aRet := M460IVAAR(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "COL"
		aRet := M460IVACO(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "URU"
		aRet := M460IVAUR(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "COS"
		aRet := M460IVACR(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "VEN"
		aRet := M460IVAVE(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "NIC"
		aRet := M460IVANI(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "BOL"
		aRet := M460IVABO(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "PTG"
		aRet := M460IVAPT(cCalculo,nItem,aInfo,lXFis)
	Case cPaisLoc == "EQU"
		aRet := M460IVAEQ(cCalculo,nItem,aInfo,lXFis)
EndCase

RestArea( aArea )

RETURN aRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³M460IVAME ³ Autor ³ MARCELLO GABRIEL     ³ Data ³ 12.11.1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ CALCULO IVA GENERICO    (MEXICO)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 												   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Percy Horna  ³19/10/00³xxxxxx³Fue alterada a base de datos de Excep-   ³±±
±±³              ³        ³      ³ciones de SF7->SFF, inicialmente utili-  ³±±
±±³              ³        ³      ³zando los Impuestos de IESPS (mejico).   ³±±
±±³ M. Camargo   ³16/08/17³DMINA-³Se abre punto de entrada MT410DECS el    ³±±
±±³              ³        ³68    ³cual permitirá al usuario indicar el nú- ³±±
±±³              ³        ³      ³ mero de decimales a manejar durante  el ³±±
±±³              ³        ³      ³cálculo de impuestos cuando MV_CENT = 2  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION M460IVAME(cCalculo,nItem,aInfo,lXFis)
local aImp,aItem,cImp,xRet,nOrdSFC,nRegSFC
local nBase:=0,nAliq:=0,lALIQ:=.f.,lIsento:=.f.,cFil,cAux,cGrp
local cImpIncid,nE,nI
Local nDecs 	:= 2
Local lDecs	:= GetNewPar('MV_CENT',2)== 2
Local lCalcItem := .F.
Local aAreaSFC := {}
Local cTES := ""
Local lRatVImpMI := FindFunction("RatVImpMI")
Local nMoeda  := 1


If FunName() <> 'LOJA701' //optimiza
	If Type("lPedidos") <> "U" .And. lPedidos
		If "MATA460B" $ AllTrim(FunName())
			If mv_par06 == 1	// Moeda do Pedido
				nDecs := MsDecimais(SC5->C5_MOEDA)
			Else
				nDecs := MsDecimais(mv_par07)
			EndIf
		ElseIf (AllTrim(FunName()) $ "TMKA271|MATA410")
			nDecs := MsDecimais(SC5->C5_MOEDA)
		Else
			//Quando acionado pelo assistente de geração de doc. de saida via mata410 funname será mata461 e os parametros de moeda 12 e 13.
			If (FunName() == "MATA461" .AND. mv_par12 == 1) .OR. mv_par13 == 1// mv_par22 == 1	// Moeda do Pedido
				nDecs := MsDecimais(SC5->C5_MOEDA)
			Else
				If FunName() == "MATA461"
					nDecs := MsDecimais(mv_par13)
				Else
					nDecs := MsDecimais(mv_par23)
				EndIf
			EndIf
		EndIf
	Else
		If lDecs .and. ExistBlock("MT410DECS",,.T.) .AND. FunName() == "MATA410"
			nDecs := ExecBlock("MT410DECS",.F.,.F.)
		Else
			nDecs := MsDecimais(SC5->C5_MOEDA)
		EndIf
	EndIf
Else
		nDecs := MsDecimais(SC5->C5_MOEDA)
EndIf

dbSelectArea("SFF")     // verificando as excecoes fiscais
dbSetOrder(3)
cFil:=xfilial()

If !lXfis
   aItem:=ParamIxb[1]
   aImp:=ParamIxb[2]
   cImp:=aImp[1]
	cImpIncid:=aImp[10]
Else
	cImp:=aInfo[X_IMPOSTO]
    If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
       SBI->(DbSeek(xFilial("SBI")+MaFisRet(nItem,"IT_PRODUTO")))
    Else
        SB1->(DbSeek(xFilial("SB1")+MaFisRet(nItem,"IT_PRODUTO")))
    Endif
	cImpIncid:=""
Endif

If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
   cGrp:=Alltrim(SBI->BI_GRUPO)
Else
    cGrp:=Alltrim(SB1->B1_GRUPO)
Endif

if dbseek(cFil+cImp)
   while FF_IMPOSTO == cImp .and. FF_FILIAL == cFil .and. !lAliq
         cAux:=Alltrim(FF_GRUPO)
         if cAux!=""
            lAliq:=(cAux==cGrp)
         endif
         cAux:=alltrim(FF_ATIVIDA)
         if cAux!=""
            lAliq:=(cAux==alltrim(SA1->A1_ATIVIDA))
         endif
         if lAliq
            if !(lIsento:=(FF_TIPO=="S"))
               nAliq:=FF_ALIQ
            endif
         endif
         dbskip()
   enddo
endif
if !lIsento
   if !lAliq .And. If(!lXFis,.T.,cCalculo=="A")
      dbselectarea("SFB")    // busca a aliquota padrao
      if dbseek(xfilial()+cImp)
         nAliq:=SFB->FB_ALIQ
      endif
   endif
   If !lXFis
      nBase:=aItem[3]+aItem[4]+aItem[5]  //valor total + frete + outros impostos
      //Tira os descontos se for pelo liquido .Bruno
      If Subs(aImp[5],4,1) == "S"
	     nBase-=aImp[18]
      Endif
		//+---------------------------------------------------------------+
		//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
		//+---------------------------------------------------------Lucas-+
		nI:=At(cImpIncid,";" )
		nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
		While nI>1
			nE:=AScan(aItem[6],{|x| x[1]==Left(cImpIncid,nI-1)})
			If nE>0
				nBase+=aItem[6,nE,4]
			End
			cImpIncid:=Stuff(cImpIncid,1,nI,"")
			nI:=At(cImpIncid,";")
			nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
		Enddo
   Else
       If cCalculo=="B"
	       	nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
	       	If cPaisLoc == "MEX" .AND. SD2->(FieldPos("D2_VALADI")) > 0
	       		nBase-=MaFisRet(nItem,"IT_ADIANT")
	       	EndIf

			If GetNewPar('MV_DESCSAI','1')=='1'
				nBase	+= MaFisRet(nItem,"IT_DESCONTO")
			Endif
          //Tira os descontos se for pelo liquido
          nOrdSFC:=(SFC->(IndexOrd()))
          nRegSFC:=(SFC->(Recno()))
          SFC->(DbSetOrder(2))
          If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
				cImpIncid:=Alltrim(SFC->FC_INCIMP)
             If SFC->FC_LIQUIDO=="S"
                nBase-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
             Endif
          Endif
          SFC->(DbSetOrder(nOrdSFC))
          SFC->(DbGoto(nRegSFC))
			//+---------------------------------------------------------------+
			//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
			//+---------------------------------------------------------------+
			If !Empty(cImpIncid)
				aImpRef:=MaFisRet(nItem,"IT_DESCIV")
				aImpVal:=MaFisRet(nItem,"IT_VALIMP")
				For nI:=1 to Len(aImpRef)
					If !Empty(aImpRef[nI])
						If Trim(aImpRef[nI][1])$cImpIncid
							nBase+=aImpVal[nI]
						Endif
					Endif
				Next
			Endif
       Endif
   Endif
endif
If !lXFis
   aImp[02]:=nAliq
   aImp[03]:=nBase

   	If lDecs .and. ExistBlock("MT410DECS",,.T.) .AND. FunName() == "MATA468N"

		nDecs := ExecBlock("MT410DECS",.F.,.F.)
		aImp[04]:=Round( ((nAliq * nBase)/100),nDecs)

	Else
		if FunName() $ "MATA468N|MATA461"
			aImp[04]:=(nAliq * nBase)/100
		Else
			aImp[04]:=Round( ((nAliq * nBase)/100),2)
		Endif
	EndIf

   xRet:=aImp
Else
    Do Case
       Case cCalculo=="B"
            xRet:=nBase
       Case cCalculo=="A"
            xRet:=nALiq
       Case cCalculo=="V"
            nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
			cTES := MaFisRet(nItem,"IT_TES")

			aAreaSFC := SFC->(GetArea())
			dbSelectArea("SFC")
			DbSetOrder(2)
			If (SFC->(MsSeek(xFilial("SFC")+cTES+aInfo[X_IMPOSTO])))
				If SFC->FC_CALCULO == "I"
					nBase := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
					lCalcItem := .T.
				Else
					nBase := MaRetBasT(aInfo[X_NUMIMP], nItem, nAliq)
				EndIf
			EndIf
			RestArea(aAreaSFC)
			FwFreeArray(aAreaSFC)
			nMoeda := MaFisRet(, "NF_MOEDA")
			If lCalcItem .and.lRatVImpMI
				xRet := RatVImpMI(aInfo, nItem, nAliq, nBase, nMoeda, 100)
			Else
           		xRet:=Round( (nAliq *nBase)/100,nDecs)
		   EndIf
    EndCase
Endif
RETURN(xRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funçào    ³M460IVAPA ¦ Autor ¦ Marcio Nunes           ¦ Data ¦ 09.04.13  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descriçào ³Programa que Calcula IVA   (Paraguai)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA460, chamado pelo ponto de entrada                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function M460ivaPA(cCalculo,nItem,aInfo,lXFis)

Local aAreaSFF := SFF->(GetArea())
Local nBase:=0,xRet
Local nOrdSFC,nRegSFC
Local aImpRef,aImpVal
Local nI := 0
Local nMoeda	:= 1
Local cConcProd	:= ""
Local cImp		:= ""
Local cProd		:= ""
Local nAliqAg	:= 0
Local nAliq		:= 0
Local cTes	 	:= ""
Local cCFO      := ""
Local lCalc1 	:= .F.
Local nValMerc	:= 0
Local lTotal 	:= .F.
Local lLiqui 	:= .F.


SetPrvt("CALIASROT,CORDEMROT,AITEMINFO,AIMPOSTO,CIMPINCID,_ALIQIVA")
SetPrvt("NI,NEE,CBUSCADEC,NDECI,")

If !lXFis
   aItemINFO := ParamIxb[1]
   aImposto  := aClone(ParamIxb[2])
   cImpIncid := aImposto[10]
   xRet		 :=aImposto
   cTes	 	 := SF4->F4_CODIGO
   cProd	 := SB1->B1_COD
   cCFO      := Alltrim(SF4->F4_CF)
   cImp		 :=aImposto[1]
Else
	xRet:=0
	cImpIncid	:=""
	cImp		:=aInfo[X_IMPOSTO]
	cTes		:= MaFisRet(nItem,"IT_TES")
    cProd		:= MaFisRet(nItem,"IT_PRODUTO")
	cCFO        := AllTrim(MaFisRet(nItem,"IT_CF"))
    nValMerc	:= MaFisRet(nItem,"IT_VALMERC")
Endif

nMoedaCor	:=	IIf(Type("nMoedaCor")	=="U",1,nMoedaCor)
lCalc1 := .F.

If cModulo $ "FAT|LOJA|FRT|TMK"
	lCalc1 := .T.
Else
	dbSelectArea( "SA2" )
	If A2_TIPO <> "N"
	   lCalc1 := .T.
	Endif
Endif
If !lCalc1
	_aliqIVA	:=	0.00
Else
	If Type("M->F1_MOEDA")<>"U"
		nMoeda:= M->F1_MOEDA
	ElseIf Type("M->C7_MOEDA")<>"U"
		nMoeda:= M->C7_MOEDA
	ElseIf Type("M->F2_MOEDA")<>"U"
		nMoeda:= M->F2_MOEDA
	ElseIf Type("M->C5_MOEDA")<>"U"
		nMoeda:= M->C5_MOEDA
	ElseIf Type("nMoedaPed")<>"U"
		nMoeda:= nMoedaPed
	ElseIf Type("nMoedaNf")<> "U"
		nMoeda:= nMoedaNf
	ElseIf Type("nMoedaCor")<> "U"
		nMoeda:= nMoedaCor
   	ElseIf lXFis
		nMoeda 		:= MAFISRET(,'NF_MOEDA')
	EndIf

	If Type("nTipoGer")<> "U" .And.	Type("nMoedSel")<> "U"
		nMoeda:= If(nTipoGer==2,nMoedSel,SC5->C5_MOEDA)
	EndIf

	nDecs := MsDecimais(nMoeda)

	DbSelectArea("SFB")
   	SFB->(DbSetOrder(1))
   	If SFB->(Dbseek(xFilial("SFB")+cImp))
		nAliq := SFB->FB_ALIQ
	Endif

	If SB1->(FieldPos("B1_CONISC"))>0 .And. SB1->(dbseek(xfilial("SB1")+Alltrim(cProd)))
		cConcProd := SB1->B1_CONISC
	EndIf

    If !lXFis

		If Empty(cConcProd)

			DbSelectArea("SFB")
			If DbSeek(xFilial() + aImposto[1] )
				_AliqIVA := FB_ALIQ
			Endif
			aImposto[2] 	:= _aliqIva									// Alíquota ( 10%)
			aImposto[11]	:= aItemINFO[4]							// Rateio do Frete
			aImposto[13]	:= aIteMINFO[5]     						// Rateio de Despesas
			aImposto[3] 	:= Round(aItemINFO[3]+aItemINFO[4]+aItemInfo[5],nDecs)	//Base de calculo

			//Tira os descontos se for pelo liquido .Bruno
			If Subs(aImposto[5],4,1) == "S"
				aImposto[3]	-=	aImposto[18]
				nBase := aImposto[3]
			Endif
			nBase:=Round(nBase,nDecs)
			//+---------------------------------------------------------------+
			//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
			//+----------------------------------------------------------Lucas+
			nI := At( cImpIncid,";" )
			nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
			While nI > 1
					nEE:= AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
					If nEE> 0
						aImposto[3] := aImposto[3]+aItemINFO[6,nEE,4]
					EndIf
					cImpIncid := Stuff( cImpIncid,1,nI,"" )
					nI := At( cImpIncid,";" )
					nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
			Enddo
			//+---------------------------------------------------------------+
			//¦ Efetua o Cálculo do Imposto                                   ¦
			//+----------------------------------------------------------Lucas+

			DbSelectArea("SFF")
			SFF->(DbSetOrder(6))
			SFF->(DbGotop())
			If SFF->(ColumnPos('FF_RBASCAL'))>0
				If SFF->(MsSeek(xFilial("SFF")+cImp+cCFO))
					If SFF->FF_RBASCAL > 0 
						aImposto[3] := aImposto[3] * (1 - (SFF->FF_RBASCAL / 100))
						aImposto[3]:=Round(aImposto[3],nDecs)
					Endif
				Endif
			Endif

			SFF->(RestArea(aAreaSFF))

			aImposto[4] := Round(aImposto[2] * (aImposto[3]/100),nDecs)
			xRet := aImposto
		Else
			nBase    := aItemINFO[3]+aItemINFO[4]+aItemINFO[5]

			nOrdSFC:=(SFC->(IndexOrd()))
			nRegSFC:=(SFC->(Recno()))

			SFC->(DbSetOrder(2))
			If (SFC->(DbSeek(xFilial("SFC")+cTes+cImp)))
				cImpIncid:=Alltrim(SFC->FC_INCIMP)
			EndIf

			//Tira os descontos se for pelo liquido .Bruno
			If Subs(aImposto[5],4,1) == "S" .And. Len(aImposto) >= 18 .And. ValType(aImposto[18]) == "N"
				nBase -= aImposto[18]
			Endif

			If !Empty(cImpIncid)
				DbSelectArea("SFB")
				If DbSeek(xFilial() + cImpIncid )
					nAliqAg := FB_ALIQ
				Endif
			EndIf

			DbSelectArea("SFF")
			SFF->(DbSetOrder(15))
			If SFF->(DbSeek(xFilial("SFF")+cImp+cConcProd)) .And. SFF->FF_ALIQ>0
				nAliq:=SFF->FF_ALIQ
			EndIf
			If SFF->(DbSeek(xFilial("SFF")+cImpIncid+cConcProd)) .And. SFF->FF_ALIQ>0
				nAliqAg:=SFF->FF_ALIQ
			EndIf

			//Por fora - Destacado
			xRet:= NoRound(nBase * (nAliqAg/100),nDecs)
			aImposto[03]:= Round(nBase + xRet,nDecs)

			//Aplica o valor do ISC(Imposto Seletivo ao Consumo) ao IVA
			aImposto[04]:= NoRound((aImposto[03] * nAliq)/100,nDecs)

			xRet:=aImposto

			SFC->(DbSetOrder(nOrdSFC))
			SFC->(DbGoto(nRegSFC))
		EndIf
  	Else
		nOrdSFC:=(SFC->(IndexOrd()))
		nRegSFC:=(SFC->(Recno()))
		SFC->(DbSetOrder(2))
		If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
			lTotal := (SFC->FC_CALCULO=="T")
			lLiqui := (SFC->FC_LIQUIDO=="S")
		EndIf

	   	//Tira os descontos se for pelo liquido
       	If lLiqui
			nBase-=MaFisRet(nItem,"IT_DESCONTO")
	   	Endif
 	   	Do Case
	      	Case cCalculo=="B"
		    	nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
		    	
		    	If GetNewPar('MV_DESCSAI','1')=='1'
					nBase	+= MaFisRet(nItem,"IT_DESCONTO")
				Endif
		    	
				//Tira os descontos se for pelo liquido
				cImpIncid:=Alltrim(SFC->FC_INCIMP)
				If SFC->FC_LIQUIDO=="S"
					nBase-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
				Endif
	   			//+---------------------------------------------------------------+
				//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
				//+---------------------------------------------------------------+
				If Empty(cConcProd)
					If !Empty(cImpIncid)
						aImpRef:=MaFisRet(nItem,"IT_DESCIV")
						aImpVal:=MaFisRet(nItem,"IT_VALIMP")
						For nI:=1 to Len(aImpRef)
							If !Empty(aImpRef[nI])
								IF Trim(aImpRef[nI][1])$cImpIncid
									nBase+=aImpVal[nI]
								Endif
							Endif
						Next
					Endif

					DbSelectArea("SFF")
					SFF->(DbSetOrder(6))
					SFF->(DbGotop())
					If SFF->(ColumnPos('FF_RBASCAL'))>0
						If SFF->(MsSeek(xFilial("SFF")+cImp+cCFO))
							If SFF->FF_RBASCAL > 0 
								nBase := nBase * (1 - (SFF->FF_RBASCAL / 100))
							Endif
						Endif
					Endif
					xRet := Round(nBase,nDecs)

					SFF->(RestArea(aAreaSFF))
				Else
					If !Empty(cImpIncid)
						DbSelectArea("SFB")
						If DbSeek(xFilial() + cImpIncid )
							nAliqAg := FB_ALIQ
						Endif
						dbSelectArea("SFF")
						SFF->(DbSetOrder(15))
						If SFF->(DbSeek(xFilial("SFF")+cImpIncid+cConcProd)) .And. SFF->FF_ALIQ>0
							nAliqAg	:=SFF->FF_ALIQ
						EndIf
						If SFF->(DbSeek(xFilial("SFF")+cImp+cConcProd)) .And. SFF->FF_ALIQ>0
							nAliq	:=SFF->FF_ALIQ
						EndIf
					EndIf

					xRet:= NoRound(nBase * (nAliqAg/100),nDecs)
					xRet:= Round(nBase + xRet,nDecs)
				EndIf

	      	Case cCalculo=="A"
	      		dbSelectArea("SFF")
				SFF->(DbSetOrder(15))
		      	If SFF->(DbSeek(xFilial("SFF")+cImp+cConcProd)) .And. SFF->FF_ALIQ>0
					nAliq	:=SFF->FF_ALIQ
					xRet:=nALiq
				Else
		      		DbSelectArea("SFB")
		            If DbSeek(xFilial() + aInfo[X_IMPOSTO] )
			           xRet := FB_ALIQ
		            Endif
		        EndIf

	      	Case cCalculo=="V"
				If Empty(cConcProd)
					If SFC->FC_CALCULO=="T"
						nBase:=MaRetBasT(aInfo[X_NUMIMP],nItem,MaFisRet(nItem,'IT_ALIQIV'+aInfo[X_NUMIMP]))
					Else
						nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
					EndIf
					_aliqIVA:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
					xRet := Round((_aliqIVA * nBase)/100,nDecs)
				Else
					If lTotal
						nBase:=MaRetBasT(aInfo[X_NUMIMP],nItem,MaFisRet(nItem,'IT_ALIQIV'+aInfo[X_NUMIMP]))
						If lLiqui
							nBase-=MaFisRet(nItem,"NF_DESCONTO")
						EndIf
					Else
						nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
						If lLiqui
							nBase-=MaFisRet(nItem,"IT_DESCONTO")
						EndIf
					EndIf
					nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])

					//Aplica o valor do ISC(Imposto Seletivo ao Consumo) ao IVA
					xRet:= NoRound((nBase * nAliq)/100,nDecs)
				EndIf
	   	EndCase
		SFC->(DbSetOrder(nOrdSFC))
		SFC->(DbGoto(nRegSFC))
    Endif
Endif
dbSelectArea( cAliasRot )
dbSetOrder( cOrdemRot )
Return( xRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funçào    ³M460IVACH ¦ Autor ¦ Jose Lucas             ¦ Data ¦ 29.04.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descriçào ³Programa que Calcula IVA   (CHILE)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³MATA460, chamado pelo ponto de entrada                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function M460ivaCH(cCalculo,nItem,aInfo,lXFis)

LOCAL nCols    := 0
LOCAL nPosCod, nPosSbLote, nPosLote, nPosLoc
Local nBase    := 0
Local nOrdSFC,nRegSFC
Local _nAliqIVA:= 0
Local nI       := 0
Local nEE      := 0
LOCAL cProd    := ""
LOCAL cSbLote  := ""
LOCAL cLote    := ""
LOCAL cLocal   := ""
Local cSeekB8
Local cImpIncid := ""
Local aImpRef,aImpVal
Local aItemInfo  := {}
Local aImposto   := {}
Local lPrcDec    := SuperGetMV("MV_PRCDEC",,.F.)
Local nDecimais  := MsDecimais(Max(SF2->F2_MOEDA,1))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Release 11.5 - Chile³
//³SIGALOJA/SIGAFRT    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lLocR5	:=	GetRpoRelease ("R5") .AND. ;
					SuperGetMv("MV_CTRLFOL",,.F.) .AND. ;
					cPaisLoc$"CHI" .AND. ;
					!lFiscal

Local cSigEspFo := 	""	                            							//Sigla da especie de documebnto fiscal escolhida no inicio da venda
Local nRecnoFo	:= Iif (cModulo$"LOJA",LjGRecFo(),;
						Iif (cModulo$"FRT",FaGetRecFo(),;
						Iif (cModulo$"FAT",LjxRecnoFo(SL1->L1_SERIE),0))) 		//RECNO do lote/controle de formulario escolhido no inicio da venda

If !lXFis
   aItemINFO := ParamIxb[1]
   aImposto  := aClone(ParamIxb[2])
   cImpIncid := aImposto[10]
   nCols     := ParamIxb[3]
   xRet      := aImposto
Else
    xRet:=0
    nCols:=nItem
    cImpIncid:=""
Endif

nMoedaCor := IIf(Type("nMoedaCor")	=="U",1,nMoedaCor)
//+-------------------------------------------------------------------+
//¦ Considerar o Tipo do Cliente.                                     ¦
//¦                                                                   ¦
//¦ A => Afecto                                                       ¦
//¦ N => Resp. Nao Afecto  , não gera IVA nas notas                   ¦
//+-------------------------------------------------------------------+

lCalc1 := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Release 11.5 - SIGALOJA/SIGAFRT  - F1CHI ³
//³Isencao de impostos quando a escpecie do ³
//³formulario de venda for do tipo:         ³
//³FCX - FACTURA EXENTA                     ³
//³BLX - BOLETA EXENTA                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLocR5 .AND. nRecnoFo > 0
	LjxDadosFo (nRecnoFo,NIL,@cSigEspFo)
EndIf

If cSigEspFo == "FCX" .OR. cSigEspFo == "BLX"
	lCalc1 := .F.
Else
	If cModulo $ "FAT|LOJA|FRT|TMK|OMS"
		dbSelectArea( "SA1" )
		If A1_TIPO <> "N"
		   lCalc1 := .T.
		Endif
	Else
		dbSelectArea( "SA2" )
		If A2_TIPO <> "N"
		   lCalc1 := .T.
		Endif
	Endif
Endif

If !lCalc1
	_nAliqIVA	:=	0.00
Else
    If !lXFis
	   DbSelectArea("SFB")
	   If DbSeek(xFilial() + aImposto[1])
		  _nAliqIVA := FB_ALIQ
	   Endif
       aImposto[2] := _nAliqIVA                     			// Alíquota ( 18 %)
       aImposto[11]:= aItemINFO[4]								// Rateio do Frete
       aImposto[13]:= aIteMINFO[5]     							// Rateio de Despesas
       aImposto[3] := aItemINFO[3]+aItemINFO[4]+aItemINFO[5]	// Base de Cálculo

	   //Tira os descontos se for pelo liquido .Bruno
       If Subs(aImposto[5],4,1) == "S"
	      aImposto[3]	-=	aImposto[18]
	      nBase := aImposto[3]
	   Endif
	Else
	    If cCalculo=="B"
	    	nBase:=	MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
			If GetNewPar('MV_DESCSAI','1')=='1'
				nBase	+= MaFisRet(nItem,"IT_DESCONTO")
			Endif
           nOrdSFC:=(SFC->(IndexOrd()))
           nRegSFC:=(SFC->(Recno()))
           SFC->(DbSetOrder(2))
           If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO])))
              cImpIncid:=Alltrim(SFC->FC_INCIMP)
              If SFC->FC_LIQUIDO=="S"
                 nBase-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
              Endif
           Endif
           SFC->(DbSetOrder(nOrdSFC))
           SFC->(DbGoto(nRegSFC))
        Endif
    Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se Nao e uma chamada dos programas MATA415 e MATA416 - Presupuestos de Venta ³
	//³ Sergio Camurca                                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    If !cModulo $ "LOJA|FRT|TMK" .And. !(Upper(FunName()) $ "MATA415|MATA416|MATR700|MATA410") .and. !__lPyME .And. Type("aHeader")#"U"
	       If Upper(Substr(FunName(),1,7))!= "MATA468" .And. FunName() != "MATA460B" //Fatura manual
	       	If Left(aHeader[1,2],2) == "C6"
		       nPosCod    := Ascan(aHeader,{|x|Trim(x[2])=="C6_PRODUTO"})
	   	       cProd      := aCols[nCols][nPosCod]
	      	   nPosLoc    := Ascan(aHeader,{|x|Trim(x[2])=="C6_LOCAL"})
	           cLocal     := aCols[nCols][nPosLoc]
		       	If Rastro(cProd)
	   	      	 	nPosLote   := Ascan(aHeader,{|x|Trim(x[2])=="C6_LOTECTL"})
	      	     	cLote      := aCols[nCols][nPosLote]
	             	nPosSbLote := Ascan(aHeader,{|x|Trim(x[2])=="C6_NUMLOTE"})
	             	cSbLote    := aCols[nCols][nPosSbLote]
		       	EndIf
			Else
		 		nPosCod    := Ascan(aHeader,{|x|Trim(x[2])=="D2_COD"})
	   	        If nPosCod > 0
	   	        	cProd      := aCols[nCols][nPosCod]
	   	        EndIf
		        nPosLoc    := Ascan(aHeader,{|x|Trim(x[2])=="D2_LOCAL"})
	   	        If nPosLoc > 0
	   	        	cLocal     := aCols[nCols][nPosLoc]
	      	    EndIf
	      	     If !Empty(cProd) .And. Rastro(cProd)
		           	nPosLote   := Ascan(aHeader,{|x|Trim(x[2])=="D2_LOTECTL"})
	   	     	   	cLote      := aCols[nCols][nPosLote]
	      	   	   	nPosSbLote := Ascan(aHeader,{|x|Trim(x[2])=="D2_NUMLOTE"})
	         	   	cSbLote    := aCols[nCols][nPosSbLote]
		    	EndIf
		   EndIf
       Else  //Fatura automatica
          If lPedidos
             cProd   := SC6->C6_PRODUTO
             cLocal  := SC6->C6_LOCAL
          Else
             // Modificado para pegar direto do Arquivo temporario - Sergio Camurca
             cProd   := SD2->D2_COD
	         cLocal  := SD2->D2_LOCAL
          EndIf
          If Rastro(cProd)
             cLote   := SC9->C9_LOTECTL
             cSbLote := SC9->C9_NUMLOTE
          EndIf
       EndIf
       If Rastro(cProd) .And. If(!lXFis,.T.,cCalculo=="B")
	      //Se FEPP eh negativo,tira da base do IVA
	      If !Empty(cSbLote)
	         cSeekB8 := xfilial("SB8")+cProd+cLocal+cLote+cSbLote
	      Else
	         cSeekB8 := xfilial("SB8")+cProd+cLocal+cLote
	      EndIf
          SB8->(dbSetorder(3))
          If SB8->(DbSeek(cSeekB8))
             If SB8->B8_FEPP < 0
                If !lXFis
   	               aImposto[3] -= SB8->B8_IE * aItemInfo[1]
   	            Else
   	                nBase -= SB8->B8_IE * MaFisRet(nItem,"IT_QUANT")
   	            Endif
	         Else
	            If !lXFis
                   aImposto[3] -= ((SB8->B8_IE * aItemInfo[1])+ (SB8->B8_FEPP * aItemInfo[1]))
                Else
                   nQuant:=MaFisRet(nItem,"IT_QUANT")
                   nBase -= ((SB8->B8_IE + SB8->B8_FEPP) * MaFisRet(nItem,"IT_QUANT"))
                Endif
	         EndIf
	      EndIf
	   EndIf
    EndIf

    If !lXFis
       //+---------------------------------------------------------------+
       //¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
       //+----------------------------------------------------------Lucas+
       nI := At( cImpIncid,";" )
       nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
       While nI > 1
             nEE:= AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
             If nEE> 0
                aImposto[3] := aImposto[3]+aItemINFO[6,nEE,4]
             Endif
             cImpIncid := Stuff( cImpIncid,1,nI,"" )
             nI := At( cImpIncid,";" )
             nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
       Enddo
       //+---------------------------------------------------------------+
       //¦ Efetua o Cálculo do Imposto                                   ¦
       //+----------------------------------------------------------Lucas+
       If cPaisLoc == "CHI" .And. lPrcdec
	       aImposto[4] := Round(aImposto[2] * (aImposto[3]/100),nDecimais)
       Else
	       aImposto[4] := aImposto[2] * (aImposto[3]/100)
	   EndIf
       xRet := aImposto
    Else
        Do Case
           Case cCalculo == "B"
                //+---------------------------------------------------------------+
	            //¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
	            //+----------------------------------------------------------Lucas+
	            If !Empty(cImpIncid)
				   aImpRef:=MaFisRet(nItem,"IT_DESCIV")
				   aImpVal:=MaFisRet(nItem,"IT_VALIMP")
				   For nI:=1 to Len(aImpRef)
				       If !Empty(aImpRef[nI])
					      IF Trim(aImpRef[nI][1])$cImpIncid
						     nBase+=aImpVal[nI]
					      Endif
					   Endif
				   Next
	            Endif
                xRet := nBase
           Case cCalculo == "A"
  	            DbSelectArea("SFB")
	            If DbSeek(xFilial() + aInfo[X_IMPOSTO])
		           xRet := FB_ALIQ
	            Endif
           Case cCalculo == "V"
                nBase    := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
                _nAliqIVA:= MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
                If cPaisLoc == "CHI" .And. lPrcdec
                	xRet     := Round((_nAliqIVA * nBase)/100,nDecimais)
	            Else
	                xRet     := (_nAliqIVA * nBase)/100
	            EndIf
        EndCase
    Endif
EndIf

Return( xRet )

/*/
_____________________________________________________________________________
¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦
¦¦+-----------------------------------------------------------------------+¦¦
¦¦¦Funçào    ¦M460IVAAR ¦ Autor ¦ Jose Lucas             ¦ Data ¦ 29.04.98¦¦¦
¦¦+----------+------------------------------------------------------------¦¦¦
¦¦¦Descriçào ¦ Programa que Calcula IVA (ARGENTINA)                       ¦¦¦
¦¦+----------+------------------------------------------------------------¦¦¦
¦¦¦Uso       ¦ MATA460, chamado pelo ponto de entrada                     ¦¦¦
¦¦+-----------------------------------------------------------------------¦¦¦
¦¦¦         ACTUALIZACIONES EFECTUADAS DESDE LA CODIFICACION INICIAL      ¦¦¦
¦¦+-----------------------------------------------------------------------¦¦¦
¦¦¦Programador ¦ Data   ¦ BOPS ¦  Motivo da Alteracao                     ¦¦¦
¦¦+------------+--------+------+------------------------------------------¦¦¦
¦¦¦Jose Otermin¦21/04/99¦xxxxxx¦Ajuste a Tablas SFF/SFH                   ¦¦¦
¦¦¦            ¦19/05/99¦xxxxxx¦Testeado.                                 ¦¦¦
¦¦+-----------------------------------------------------------------------+¦¦
¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦¦
¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
/*/
Function M460ivaAR(cCalculo,nItem,aInfo,lXFis)

Local nBase := 0
Local xRet
Local nDecs := 0
Local aImpRef,aImpVal
Local nI := 0

nMoedaCor := IIf(Type("nMoedaCor")	== "U",1,nMoedaCor)
nDecs := MsDecimais(nMoedaCor)

SetPrvt("CALIASROT,CORDEMROT,AITEMINFO,AIMPOSTO,CIMPINCID,NALICUOTA")
SetPrvt("NI,NE,")

If !lXFis
   aItemINFO := ParamIxb[1]
   aImposto  := ParamIxb[2]
   cImpIncid := aImposto[10]
   xRet:=aImposto
Else
    xRet:=0
    cImpIncid:=""
Endif

//+-------------------------------------------------------------------+
//¦ Considerar o Tipo do Cliente.                                     ¦
//¦                                                                   ¦
//¦ A => Agente de Retençäo, E'sempre Resp. Inscrito.                 ¦
//¦ I => Resp. Inscrito, tem tributacao de IVA, Aliquota em de B1_IPI.¦
//¦ N => Resp. Nao Inscrito, alem da tributacao acima, tem + 50% de   ¦
//¦      tributacao. > M460ID2b.PRX                                   ¦
//¦ F => Consumidor Final, imposto normal como no Inscrito            ¦
//¦ D => Devedores del Exterior. Näo tem tributacao.                  ¦
//¦ X => Exento, Näo tem tributacao                                   ¦
//¦ S => No sujeito, Näo tem tributacao                               ¦
//+-------------------------------------------------------------------+
//+---------------------------------------------------------------+
//¦ No estan siendo consideradas las devoluciones...		         ¦
//+----------------------------------------------------------Bruno+

nAlicuota := If(cModulo=="FRT",SBI->BI_IPI,SB1->B1_IPI)

dbSelectArea( "SA1" )
If SA1->A1_TIPO=="D"   // Personas del Exterior.
	nAlicuota	 := 0.00
Endif

If !(SA1->A1_TIPO $ "OX") // Si el cliente NO es Exento Objetivo/Normal.
	If !lXFis
		aImposto[2]  := nAlicuota             // Alícuota ( 21 %)
		aImposto[3]  := aItemINFO[3]          // Base de Cálculo
		//Tira os descontos se for pelo liquido .Bruno
		If Subs(aImposto[5],4,1) == "S"
			aImposto[3]	-=	aImposto[18]
		Endif
		//+---------------------------------------------------------------+
		//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
		//+----------------------------------------------------------Lucas+
		nI:= At( cImpIncid,";" )
		nI:= IIf( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		While nI > 1
			nE := AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
			If nE > 0
				aImposto[_BASECALC] := aImposto[_BASECALC] + aItemINFO[_GASTOS,nE,4]
			EndIf
            cImpIncid := Stuff( cImpIncid,1,nI,"" )
			nI := At( cImpIncid,";" )
			nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		EndDo
		//+---------------------------------------------------------------+
		//¦ Efetua o Cálculo do Imposto                                   ¦
		//+----------------------------------------------------------Lucas+
		aImposto[_IMPUESTO] := ROUND(aImposto[_ALIQUOTA ] * aImposto[_BASECALC]/100,2) // Valor Calculado
		xRet := aImposto
	Else
		Do Case
			Case cCalculo == "B"
				nBase := MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
				If GetNewPar('MV_DESCSAI','1')=='1'
					nBase	+= MaFisRet(nItem,"IT_DESCONTO")
				Endif
				//Tira os descontos se for pelo liquido
				nOrdSFC:=(SFC->(IndexOrd()))
				nRegSFC:=(SFC->(Recno()))
				SFC->(DbSetOrder(2))
				If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO])))
					cImpIncid:=Alltrim(SFC->FC_INCIMP)
					If SFC->FC_LIQUIDO=="S"
						nBase-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
					Endif
				Endif
				SFC->(DbSetOrder(nOrdSFC))
				SFC->(DbGoto(nRegSFC))
   				//+---------------------------------------------------------------+
				//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
				//+---------------------------------------------------------------+
	            If !Empty(cImpIncid)
				   aImpRef:=MaFisRet(nItem,"IT_DESCIV")
				   aImpVal:=MaFisRet(nItem,"IT_VALIMP")
				   For nI:=1 to Len(aImpRef)
				       If !Empty(aImpRef[nI])
					      IF Trim(aImpRef[nI][1])$cImpIncid
						     nBase+=aImpVal[nI]
					      Endif
					   Endif
				   Next
				Endif
				xRet := nBase
			Case cCalculo == "A"
				xRet := nAlicuota
			Case cCalculo == "V"
				nBase := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
				xRet := ((nAlicuota * nBase)/100)
		EndCase
	Endif
EndIf

Return( xRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M460IVACO ºAutor  ³Denis Martins       º Data ³  01/20/2000 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³    Calculo de Imposto IVA - Localizacao Colombia           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA460                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M460IVACO(cCalculo,nItem,aInfo,lXFis)

Local nAliq := 0
Local nBase,cImp,xRet,nOrdSFC,nRegSFC
Local nI:=1
Local nAuxVlr := 0
Local nPerAUI := 0
Local lPerAUI := .F.
Local nMoeda := 1
Local cFunName   := FunName()
Local nSeguro := 0
Local lCalcItem := .F.
Local aAreaSFC := {}
Local cTES := ""

SetPrvt("CALIASROT,CORDEMROT,AITEMINFO,AIMPOSTO,_CPROCNAME,_CZONCLSIGA")
SetPrvt("_LAGENTE,_LEXENTO,AFISCAL,_LCALCULAR,_LESLEGAL,_NALICUOTA,NALIQ")
SetPrvt("_NVALORMIN,_NREDUCIR,CTIPOEMPR,CTIPOCLI,CTIPOFORN,CZONFIS,CCFO,LRET")
SetPrvt("CCODPROD,LRET,NBASE,CQUALI,CPROD")

Static lCpoAlqAIU := SA1->(ColumnPos("A1_ALQAIU"))>0 .And. SA2->(ColumnPos("A2_ALQAIU"))>0
Static lRatVICol := FindFunction("RatVICol")

If Type("lPedidos") == "U"
	lPedidos := .F.
EndIf

lRet := .T.
cAliasRot  := Alias()
cOrdemRot  := IndexOrd()

If !lXFis
	aItemINFO:=ParamIxb[1]
	aImposto:=ParamIxb[2]
	xRet:=aImposto
	cProd:=aImposto[16]
	cImp:=aImposto[1]
Else
    xRet:=0
    cImp:=aInfo[X_IMPOSTO]
    cProd:=MafisRet(nItem,"IT_PRODUTO")
Endif

If cModulo == 'FAT|LOJA|TMK|FRT'
	cTipoCli   := SA1->A1_TIPO
	cZonfis    := SA1->A1_EST
Else
	cTipoForn  := SA2->A2_TIPO
	cZonfis    := SA2->A2_EST
Endif

nBase := 0

// Busca o CFO do Tes Correspondente - SF4
dbSelectArea("SF4")
If lXFis
   SF4->(DbSeek(xFilial("SF4")+MaFisRet(nItem,"IT_TES")))
Endif
cCFO    := Alltrim(SF4->F4_CF)
cVerIVA := SF4->F4_CALCIVA
//TES nao calcula IVA
If cVerIva == "2" .or. cVerIva == "3"
   lRet := .F.
   Return ( xRet )
Endif

If lRet
	dbSelectArea("SFB")
	dbSetOrder(1)
	If dbSeek(xFilial("SFB")+cImp)

   		//+---------------------------------------------------------------+
   		//¦ Verifica a Alíquota                                           ¦
   		//+---------------------------------------------------------------+
		If IIf(lXFis,cCalculo$"BA",.T.)
			nAliq := SF4->F4_PERCIVA
			nBase := 1
			If SB1->B1_ALQIVA <> 0
				nAliq := SB1->B1_ALQIVA
			EndIf
			If nAliq == 0
				nAliq := SFB->FB_ALIQ
			EndIf
		EndIf

 		//+---------------------------------------------------------------+
   		//¦ Efectua el Cálculo del Impuesto                               ¦
   		//+---------------------------------------------------------------+
		If !lXFis

			IF lCpoAlqAIU
					aImposto[_ALIQUOTA]  := nAliq // Alicuota de Zona Fiscal
					aImposto[_BASECALC]  :=  aItemINFO[_VLRTOTAL] + aItemINFO[_FLETE] + aItemINFO[_GASTOS] // Base de Cálculo
				   	nTotBase:= 0
				  	cImpIncid:=aImposto[10]
					If Len(Alltrim(cImpIncid)) >0
						nI:=At(cImpIncid,";" )
						nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
						nI:=At(cImpIncid,";" )
						nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)

						While nI>1
							nE:=AScan(aItemINFO[6],{|x| x[1]==Left(cImpIncid,nI-1)})
							If nE>0
								nTotBase+=aItemINFO[6,nE,4]
							End
							cImpIncid:=Stuff(cImpIncid,1,nI,"")
							nI:=At(cImpIncid,";")
							nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
						End
				    EndIf

				  	aImposto[_BASECALC]:=aImposto[_BASECALC] + nTotBase
				   If Subs(aImposto[5],4,1) == "S"  .And. Len(aImposto) >= 18 .And. ValType(aImposto[18])=="N"
					  aImposto[_BASECALC]	-=	aImposto[18]
				   Endif
			   	   //+---------------------------------------------------------------+
				   //¦ Efectua el Cálculo del Impuesto                               ¦
				   //+---------------------------------------------------------------+
				   aImposto[_IMPUESTO]  := aImposto[_BASECALC] * ( aImposto[_ALIQUOTA]/100)
				   xRet:=aImposto

			Else
				dbSelectArea("SFC")
				dbSetOrder(2)
				SFC->(dbGoTop())
				If SFC->(dbSeek(xFilial("SFC")+SF4->F4_CODIGO+"AUI"))
					dbSelectArea("SFB")
					dbSetOrder(1)
					SFB->(dbGoTop())
					If dbSeek(xFilial("SFB")+"AUI")
						aImposto[_BASECALC] += aItemINFO[3] * (SFB->FB_ALIQ/100)
					EndIf
					aImposto[_ALIQUOTA] := nAliq
						aImposto[_BASECALC] := aImposto[_BASECALC] * (GetNewPar("MV_PERAUI",0)/100)
					aImposto[_IMPUESTO] := round((aImposto[_BASECALC] * ( nAliq /100)) ,2)
					xRet := aImposto
				Else
				//Tira os descontos se for pelo liquido .Bruno
					If lPedidos
						nSeguro := SC5->C5_SEGURO
					Else
						nSeguro := SF2->F2_SEGURO
					EndIf
					If Subs(aImposto[5],4,1) == "S"
						aImposto[_BASECALC]	:=	aImposto[18] * -1
					Endif
					aImposto[_ALIQUOTA] := nAliq
					aImposto[_BASECALC] += ( aItemINFO[3] + aItemINFO[4] + aItemINFO[5] - nSeguro ) * nBase
	        		aImposto[_IMPUESTO] := aImposto[_BASECALC] * ( nAliq /100)
					xRet := aImposto
				EndIf
			EndIf
		Else
			If cCalculo=="B"

			////////////////////////
				IF lCpoAlqAIU

			       	dbSelectArea("SFC")
					dbSetOrder(2)

					If FunName() $ "MATA410"
						If M->C5_TPFRETE == "F"
							xRet:=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")) * nBase
						Else
							xRet:=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_DESPESA")) * nBase
						EndIf
					Else
						xRet:=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")) * nBase
					EndIf

					nOrdSFC:=(SFC->(IndexOrd()))
					nRegSFC:=(SFC->(Recno()))

					SFC->(DbSetOrder(2))
					If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO])))
					   //Tira os descontos se for pelo liquido
					   If SFC->FC_LIQUIDO=="S"
						  xRet-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
					   Endif
			  		Endif

					cImpIncid:=Alltrim(SFC->FC_INCIMP)
					nVal:= 0
					If !Empty(cImpIncid)
						aImpRef:=MaFisRet(nItem,"IT_DESCIV")
						aImpVal:=MaFisRet(nItem,"IT_VALIMP")
						For nI:=1 to Len(aImpRef)
		       				If !Empty(aImpRef[nI])
			      				IF Trim(aImpRef[nI][1])$cImpIncid
				    				 nVal+=aImpVal[nI]
			      				Endif
			   				Endif
						Next
					EndIf
					xRet := xRet + nVal

					SFC->(DbSetOrder(nOrdSFC))
					SFC->(DbGoto(nRegSFC))
				Else

					dbSelectArea("SFC")
					dbSetOrder(2)
					SFC->(dbGoTop())
					If SFC->(dbSeek(xFilial("SFC")+SF4->F4_CODIGO+"AUI"))
						dbSelectArea("SFB")
						dbSetOrder(1)
						SFB->(dbGoTop())
						If dbSeek(xFilial("SFB")+"AUI")
							xRet += MaFisRet(nItem,"IT_VALMERC") * (SFB->FB_ALIQ/100)
						EndIf
					Else
						If GetNewPar('MV_DESCSAI','1')=='1' .And. MaFisRet(,'NF_OPERNF')	== 'S'
							xRet += MaFisRet(nItem,"IT_DESCONTO")
						Endif
						//Tira os descontos se for pelo liquido
						nOrdSFC := (SFC->(IndexOrd()))
						nRegSFC := (SFC->(Recno()))
						SFC->(DbSetOrder(2))
						SFC->(dbGoTop())
						If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
							cImpIncid:=Alltrim(SFC->FC_INCIMP)
							If SFC->FC_LIQUIDO=="S"
								xRet -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
							Endif
						Endif
						SFC->(DbSetOrder(nOrdSFC))
						SFC->(DbGoto(nRegSFC))

						If FunName() $ "MATA410"
							If M->C5_TPFRETE == "F"
								xRet+=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")) * nBase
							Else
								xRet+=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_DESPESA")) * nBase
							EndIf
						Else
							xRet+=(MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")) * nBase
						EndIf
					EndIf
				EndIf

			ElseIf cCalculo=="V"
				cTES := MaFisRet(nItem,"IT_TES")

				aAreaSFC := SFC->(GetArea())
				dbSelectArea("SFC")
				DbSetOrder(2)
				If (SFC->(MsSeek(xFilial("SFC")+cTES+aInfo[X_IMPOSTO])))
					If SFC->FC_CALCULO == "I"
						lCalcItem := .T. //Calcula por item
					EndIf
				EndIf
				IF !(lCpoAlqAIU)
					If SFC->(MsSeek(xFilial("SFC")+cTES+"AUI"))
						nPerAUI := SuperGetMV("MV_PERAUI", .F., 0)
						lPerAUI := .T.
					EndIf
				EndIf
				RestArea(aAreaSFC)

				nAliq := MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
				nBase := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
				
				If !lRatVICol .Or. lPerAUI .Or. !lCalcItem
					If lPerAUI
						nBase := nBase * (nPerAUI/100)
					EndIf
					nAuxVlr  := nBase * (nAliq /100)
				Else
					If cFunName $ "MATA410" .And. Type("M->C5_MOEDA") <> "U" 
						nMoeda := M->C5_MOEDA
						nTaxaMoed := M->C5_TXMOEDA	
                	Else
						nMoeda := MaFisRet(, "NF_MOEDA")
					EndIf

					nAuxVlr := RatVICol(aInfo, nItem, nAliq, nBase, nMoeda, 100)
				EndIf

				xRet := nAuxVlr

			ElseIf cCalculo=="A"
				xRet := nALiq
			EndIf
		EndIf
	EndIf
EndIf
dbSelectArea( cAliasRot )
dbSetOrder( cOrdemRot )
Return( xRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ M460IVAUR³ Autor ³ Lucas                  ³ Data ³ 06.06.01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calculo do IVA para Uruguay...							   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ AP5			                                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function M460IVAUR(cCalculo,nItem,aInfo,lXFis)
Local aItemINFO
Local aImposto
Local cImpIncid
Local nE,nI
Local xRet,nOrdSFC,nRegSFC,cImp,nAliq,nBase,cVerIVA,cCFO
Local aImpRef,aImpVal
Local nMoeda  	:=	IIf(Type("nMoedaCor")=="U",1,nMoedaCor)
// Busca o CFO do Tes Correspondente - SF4
dbSelectArea("SF4")
If lXFis
   SF4->(DbSeek(xFilial("SF4")+MaFisRet(nItem,"IT_TES")))
Endif
cCFO    := Alltrim(SF4->F4_CF)
cVerIVA := SF4->F4_CALCIVA
//TES nao calcula IVA
If cVerIva == "2" .or. cVerIva == "3"
   lRet := .F.
   Return ( xRet )
Endif

If !lXFis
   aItemINFO := ParamIxb[1]
   aImposto  := AClone( ParamIxb[2] )
   cImpIncid := aImposto[10]
   xRet:=aImposto
   cImp:=aImposto[1]
Else
    xRet:=0
    cImp:=aInfo[X_IMPOSTO]
    cImpIncid:=""
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obter Base de C lculo e Al¡quota dos Impostos.				 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obs: No Uruguay temos uma alicuota de IVA de 14% ou 23% esta ³
//³ realiconada con base na Classifica‡Æo de Produtos e a aliquo-³
//³ ta será pega do FB_ALIQ pois sera definido um imposto para   ³
//³ cada alicuota.                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (cModulo$"COM|EST|EIC" .And. !(SA2->A2_TIPO $ "E|6") ).Or. ( !(cModulo$"COM|EST|EIC") .And. !(SA1->A1_TIPO $ "E|6"))
	If !lXFis
		aImposto[11] := aItemINFO[4]      // Rateio do Frete
		aImposto[4]  := aItemINFO[5]      // Rateio de Despesas
		aImposto[3]  := aItemINFO[3]+aItemINFO[4]+aItemINFO[5] // Base de Cálculo
		If Subs(aImposto[5],4,1) == "S"
			aImposto[_BASECALC] -= aImposto[18]
		EndIf
		SFB->(DbSeek(xFilial("SFB")+aImposto[1]))
		aImposto[_ALIQUOTA] := SFB->FB_ALIQ

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Soma base dos impostos incidentes...							     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nI := At( ";",cImpIncid )
		nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		While nI > 1
			nE := AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
			If nE > 0
				aImposto[3] := aImposto[3]+aItemINFO[6,nE,4]
			EndIf
			cImpIncid := Stuff( cImpIncid,1,nI,"" )
			nI := At( ";",cImpIncid )
			nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		End
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua o Calculo do imposto...							   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aImposto[4]  := ROUND(aImposto[3] * aImposto[2]/100,Msdecimais(nMoeda))
		xRet:=aImposto
	Else
		Do Case
			Case cCalculo=="A"
				SFB->(DbSeek(xFilial("SFB")+aInfo[1]))
				xRet:=SFB->FB_ALIQ
			Case cCalculo=="B"
				xRet:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
				If GetNewPar('MV_DESCSAI','1')=='1'
					xRet	+= MaFisRet(nItem,"IT_DESCONTO")
				Endif
				//Tira os descontos se for pelo liquido
				nOrdSFC:=(SFC->(IndexOrd()))
				nRegSFC:=(SFC->(Recno()))
				SFC->(DbSetOrder(2))
				If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
					cImpIncid:=Alltrim(SFC->FC_INCIMP)
					If SFC->FC_LIQUIDO=="S"
						xRet-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
					Endif
				Endif
				SFC->(DbSetOrder(nOrdSFC))
				SFC->(DbGoto(nRegSFC))
   				//+---------------------------------------------------------------+
				//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
				//+---------------------------------------------------------------+
	            If !Empty(cImpIncid)
				   aImpRef:=MaFisRet(nItem,"IT_DESCIV")
				   aImpVal:=MaFisRet(nItem,"IT_VALIMP")
				   For nI:=1 to Len(aImpRef)
				       If !Empty(aImpRef[nI])
					      IF Trim(aImpRef[nI][1])$cImpIncid
						     xRet+=aImpVal[nI]
					      Endif
					   Endif
				   Next
				Endif
			Case cCalculo=="V"
				nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
				nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
				xRet:=Round((nBase * nAliq/100),MsDecimais(nMoeda))
				if nAliq == 0
					xRet:= 0
				Endif
		EndCase
	Endif
Endif
Return( xRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Programa  º M460IVACR  º Autor º      Nava       º Data º   17/07/01  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º        Programa que calcula o IVA Incluido ( Costa Rica [CR] )        º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Sintaxe   º M460IVACR                                                 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Parametrosº Nenhum                                  			      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Retorno   º aImposto                                                  º±±
±±ºÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso       º Chamado neste programa por M460IVA 			   			  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º         Atualizacoes efetuadas desde a codificacao inicial            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÍËÍÍÍÍÍÍËÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºProgramador    º Data       º Motivo da Alteracao                      º±±
±±ºCamila Januárioº 07/10/11   º Localização Costa Rica 2011              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÎÍÍÍÍÍÍÎÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º           ºxx/xx/xxºxxxxxxº                                           º±±
±±º           º        º      º                                           º±±
±±º           º        º      º                                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÊÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M460ivaCR(cCalculo,nItem,aInfo,lXFis)
Local nDecs
LOCAL cImpIncid := ""
LOCAL nI
LOCAL nEE
LOCAL lCalcIVA := .F.
LOCAL nOrdSFC,nRegSFC
Local aImpRef,aImpVal
Local cConcept := ""
Local cProduto := ""
Local cImp     := ""
Local aImposto := {}
Local aItem := {}
Local xRet := 0

If !lXFis
	aItem    := ParamIxb[1]
	aItemINFO:=ParamIxb[1]
	aImposto:=ParamIxb[2]
	xRet:=aImposto
	cProduto:=aImposto[16]
	cImp:=aImposto[1]
	cImpIncid := aImposto[10]
Else
	xRet     := 0
	cProduto := MaFisRet(nItem,"IT_PRODUTO")
	cImp     := aInfo[X_IMPOSTO]
EndIf

nMoedaCor := IIf(Type("nMoedaCor")	=="U",1,nMoedaCor)

dbSelectArea("SB1")
SB1->(dbSetOrder(1))
If SB1->(dbSeek(xFilial("SB1")+cProduto))
	cConcept := SB1->B1_CONIVA
	lCalcIVA := IIF(SB1->B1_CALCIVA=="1",.T.,.F.)
EndIf

dbSelectArea("SFB")
SFB->(dbSetOrder(1))
If SFB->(dbSeek(xFilial("SFB")+cImp))
	nAliq := SFB->FB_ALIQ
Endif

dbSelectArea("CCR")
CCR->(dbSetOrder(1))//CCR_FILIAL+CCR_CONCEP+CCR_PAIS
If CCR->(dbSeek(xFilial("CCR")+cConcept))
	nAliq := CCR->CCR_ALIQ
EndIf

nDecs := MsDecimais(nMoedaCor)

If lCalcIVA
	If !lXFis
		aImposto[2]  := nAliq                     			     	// Alíquota
		aImposto[11] := aItemINFO[4]								// Rateio do Frete
		aImposto[13] := aIteMINFO[5]     							// Rateio de Despesas
		aImposto[3]  := aItemINFO[3]//+aItemINFO[4]+aItemINFO[5]   	// Base de Cálculo
		//Tira os descontos se for pelo liquido .Bruno
		If Subs(aImposto[5],4,1) == "S"
			aImposto[3]	-=	aImposto[18]
		Endif
		//+---------------------------------------------------------------+
		//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
		//+----------------------------------------------------------Lucas+
		nI := At( cImpIncid,";" )
		nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		While nI > 1
			nEE:= AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
			If nEE> 0
				aImposto[3] := aImposto[3]+aItemINFO[6,nEE,4]
			Endif
			cImpIncid := Stuff( cImpIncid,1,nI,"" )
			nI := At( cImpIncid,";" )
			nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
		Enddo
		//+---------------------------------------------------------------+
		//¦ Efetua o Cálculo do Imposto                                   ¦
		//+----------------------------------------------------------Lucas+
		aImposto[4] := Round(aImposto[2] * (aImposto[3]/100),nDecs)
		xRet := aImposto
	Else
		If cCalculo=="B"
			xRet := MaFisRet(nItem,"IT_VALMERC") //+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
			If GetNewPar('MV_DESCSAI','1')=='1'
				xRet	+= MaFisRet(nItem,"IT_DESCONTO")
			Endif
			//Tira os descontos se for pelo liquido
			nOrdSFC := (SFC->(IndexOrd()))
			nRegSFC := (SFC->(Recno()))
			SFC->(DbSetOrder(2))
			If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[1])))
				cImpIncid := Alltrim(SFC->FC_INCIMP)
				If SFC->FC_LIQUIDO=="S"
					xRet -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
				Endif
			Endif
			SFC->(DbSetOrder(nOrdSFC))
			SFC->(DbGoto(nRegSFC))
			//+---------------------------------------------------------------+
			//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
			//+---------------------------------------------------------------+
			If !Empty(cImpIncid)
				aImpRef := MaFisRet(nItem,"IT_DESCIV")
				aImpVal := MaFisRet(nItem,"IT_VALIMP")
				For nI:=1 to Len(aImpRef)
			        If !Empty(aImpRef[nI])
				       IF Trim(aImpRef[nI][1])$cImpIncid
					      xRet += aImpVal[nI]
				       Endif
				    Endif
				Next
			Endif
		ElseIf cCalculo == "V"
			xRet     := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
			_aliqIVA := MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
			xRet     := ((nAliq * xRet)/100)
			xRet	 := Round(xRet,nDecs)
		ElseIf cCalculo == "A"
			xRet := nAliq
		EndIf
	Endif
EndIf

Return( xRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³M460IVAVE ³ Autor ³ Fernando Machima     ³ Data ³ 27.07.2001 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ CALCULO IVA GENERICO (VENEZUELA)                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 										           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Fernando M.  ³27/07/01³xxxxxx³Desenvolvimento inicial                  ³±±
±±³ Tiago Bizan  ³13/07/10³xxxxxx³Mudanças no calculo do imposto IVA	   ³±±
±±³				 ³		  ³		 ³(Alíquota, Base de Calculo e Valor)      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION M460IVAVE(cCalculo,nItem,aInfo,lXFis)

	Local aImp,aItem,cImp,xRet
	Local nBase:=0,nAliq:=0,clProd:=""

	If !lXfis
		aItem:=ParamIxb[1]
		aImp:=ParamIxb[2]
		cImp:=aImp[1]
		xRet:=aImp
	Else
		cImp:=SFB->FB_CODIGO
		xRet:=0
		cImp:=aInfo[X_IMPOSTO]
	Endif

	If !lXFis
		//BUSCA A ALÍQUOTA
		DbSelectarea("SFB")
		If SFB->(DbSeek(xFilial("SFB")+cImp))
			If SB1->B1_ALQIVA <> 0
				nAliq:=SB1->B1_ALQIVA
			Else
				If SF4->F4_CALCIVA == "4" .OR. SF4->F4_CALCIVA == "3"
					nAliq:=0
				ElseIF SF4->F4_TPALIQ == "G" .OR. SF4->F4_TPALIQ == " "
					nAliq:=SFB->FB_ALIQ
				ElseIF SF4->F4_TPALIQ == "R"
					nAliq:=SFB->FB_ALIQRD
				ElseIF SF4->F4_TPALIQ == "A"
					nAliq:=SFB->FB_ALIQAD
				EndIF
			Endif
		Endif

		//CALCULA A BASE DE CALCULO DO IVA
		If SF4->F4_CALCIVA == "3"
			nBase:=0
		Else
			nBase:=aItem[3]
		EndIF

		//CALCULA O VALOR DO IVA
		aImp[02]:=nAliq
		If SF4->F4_CALCIVA == "3"
			aImp[03]:=0
		Else
			aImp[03]:=nBase
		EndIF
		aImp[04]:=Round((nBase*nAliq)/100,2)
		xRet:=aImp
	Else
		If cCalculo=="B"//CALCULA A BASE DE CALCULO DO IVA
			If SF4->F4_CALCIVA == "3"
				nBase:=0
			Else
				nBase:=MaFisRet(nItem,"IT_VALMERC")
			EndIF
			xRet:=nBase
		ElseIf cCalculo=="V"//CALCULA O VALOR DO IVA
			nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
			If SF4->F4_CALCIVA == "3"
				nBase:=0
			Else
				nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
			EndIF
			xRet:= Round((nBase*nAliq)/100,2)
		ElseIf cCalculo=="A" //CALCULA A ALÍTUOTA DO IVA
			DbSelectarea("SFB")
			If SFB->(DbSeek(xFilial("SFB")+cImp))
				clProd:=SB1->B1_COD
				DbSelectarea("SB1")
				SB1->(DBGotop())
				SB1->(DBSetORder(1))
				If SB1->(DbSeek(xFilial("SB1")+clProd)) .And. SB1->B1_ALQIVA <> 0
					xRet:=SB1->B1_ALQIVA
				Else
					If SF4->F4_CALCIVA == "4" .OR. SF4->F4_CALCIVA == "3"
						xRet:=0
					ElseIF SF4->F4_TPALIQ == "G" .OR. SF4->F4_TPALIQ == " "
						xRet:=SFB->FB_ALIQ
					ElseIF SF4->F4_TPALIQ == "R"
						xRet:=SFB->FB_ALIQRD
					ElseIF SF4->F4_TPALIQ == "A"
						xRet:=SFB->FB_ALIQAD
					EndIF
				EndIF
			EndIF
		Endif
	Endif

RETURN(xRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o	 ³M460IVANI ³ Autor ³ MARCELLO GABRIEL     ³ Data ³ 12.11.1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ CALCULO IVA GENERICO    (NICARAGUA)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³ Generico 												   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
FUNCTION M460IVANI(cCalculo,nItem,aInfo,lXFis)
local aImp,aItem,cImp,xRet,nOrdSFC,nRegSFC
local nBase:=0,nAliq:=0,lALIQ:=.f.,lIsento:=.f.,cFil,cAux,cGrp
local cImpIncid,nE,nI

Local lCalc1 := .F.
If cModulo $ "FAT|LOJA|FRT|TMK"
	dbSelectArea( "SA1" )
	If A1_TIPO $ "1"
		lCalc1 := .T.
	Endif
Else
	dbSelectArea( "SA2" )
	If A2_TIPO $ "1"
		lCalc1 := .T.
	Endif
Endif

If !lCalc1
	_aliqIVA	:=	0.00
Else
	dbSelectArea("SFF")     // verificando as excecoes fiscais
	dbSetOrder(3)
	cFil:=xfilial()

	If !lXfis
		aItem:=ParamIxb[1]
		aImp:=ParamIxb[2]
		cImp:=aImp[1]
		cImpIncid:=aImp[10]
	Else
		cImp:=aInfo[X_IMPOSTO]
		If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
			SBI->(DbSeek(xFilial("SBI")+MaFisRet(nItem,"IT_PRODUTO")))
		Else
			SB1->(DbSeek(xFilial("SB1")+MaFisRet(nItem,"IT_PRODUTO")))
		Endif
		cImpIncid:=""
	Endif

	If cModulo=="FRT" //Frontloja usa o arquivo SBI para cadastro de produtos
		cGrp:=Alltrim(SBI->BI_GRUPO)
	Else
		cGrp:=Alltrim(SB1->B1_GRUPO)
	Endif

	if dbseek(cFil+cImp)
		while FF_IMPOSTO == cImp .and. FF_FILIAL == cFil .and. !lAliq .and. EOF()
			cAux:=Alltrim(FF_GRUPO)
			if cAux!=""
				lAliq:=(cAux==cGrp)
			endif

			if lAliq
				nAliq:=FF_ALIQ
			endif
			dbskip()
		enddo
	endif
	if !lIsento
		if !lAliq .And. If(!lXFis,.T.,cCalculo=="A")
			dbselectarea("SFB")    // busca a aliquota padrao
			if dbseek(xfilial()+cImp)
				nAliq:=SFB->FB_ALIQ
			endif
		endif
		If !lXFis
			nBase:=aItem[3]+aItem[4]+aItem[5]  //valor total + frete + outros impostos
			//Tira os descontos se for pelo liquido .Bruno
			If Subs(aImp[5],4,1) == "S"
				nBase-=aImp[18]
			Endif
			//+---------------------------------------------------------------+
			//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
			//+---------------------------------------------------------Lucas-+
			nI:=At(cImpIncid,";" )
			nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
			While nI>1
				nE:=AScan(aItem[6],{|x| x[1]==Left(cImpIncid,nI-1)})
				If nE>0
					nBase+=aItem[6,nE,4]
				End
				cImpIncid:=Stuff(cImpIncid,1,nI,"")
				nI:=At(cImpIncid,";")
				nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
			Enddo
		Else
			If cCalculo=="B"
				nBase:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")

				If GetNewPar('MV_DESCSAI','1')=='1'
					nBase	+= MaFisRet(nItem,"IT_DESCONTO")
				Endif

				//Tira os descontos se for pelo liquido
				nOrdSFC:=(SFC->(IndexOrd()))
				nRegSFC:=(SFC->(Recno()))
				SFC->(DbSetOrder(2))
				If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
					cImpIncid:=Alltrim(SFC->FC_INCIMP)
					If SFC->FC_LIQUIDO=="S"
						nBase-=If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
					Endif
				Endif
				SFC->(DbSetOrder(nOrdSFC))
				SFC->(DbGoto(nRegSFC))
				//+---------------------------------------------------------------+
				//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
				//+---------------------------------------------------------------+
				If !Empty(cImpIncid)
					aImpRef:=MaFisRet(nItem,"IT_DESCIV")
					aImpVal:=MaFisRet(nItem,"IT_VALIMP")
					For nI:=1 to Len(aImpRef)
						If !Empty(aImpRef[nI])
							If Trim(aImpRef[nI][1])$cImpIncid
								nBase+=aImpVal[nI]
							Endif
						Endif
					Next
				Endif
			Endif
		Endif
	endif

	If !lXFis
		aImp[02]:=nAliq
		aImp[03]:=nBase
		aImp[04]:=(nAliq * nBase)/100
		xRet:=aImp
	Else
		Do Case
			Case cCalculo=="B"
				xRet:=nBase
			Case cCalculo=="A"
				xRet:=nALiq
			Case cCalculo=="V"
				nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
				xRet:=(nAliq * MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP]))/100
		EndCase
	Endif
EndIf


RETURN(xRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³M460IVABO ³ Autor ³ Sergio S. Fuzinaka   ³ Data ³ 14.06.2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³CALCULO IVA GENERICO                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³BOLIVIA                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³            ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                         ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function M460IVABO(cCalculo,nItem,aInfo,lXFis)

Local aItemINFO
Local aImposto
Local cImpIncid
Local nE,nI
Local xRet,nOrdSFC,nRegSFC,cImp,nAliq,nBase
Local aImpRef,aImpVal
Local nMoeda
Local nDecs

If !lXFis
	aItemINFO	:= ParamIxb[1]
	aImposto	:= AClone( ParamIxb[2] )
	cImpIncid	:= aImposto[10]
	xRet		:= aImposto
	cImp		:= aImposto[1]
    cTes	 	:= SF4->F4_CODIGO
Else
    xRet		:= 0
    cImp		:= aInfo[X_IMPOSTO]
    cImpIncid	:= ""
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os decimais da moeda para arredondamento do valor  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 	If Type("M->F1_MOEDA")<>"U"
	   nMoeda:= M->F1_MOEDA
	ElseIf Type("M->C7_MOEDA")<>"U"
			nMoeda:= M->C7_MOEDA
	ElseIf Type("M->F2_MOEDA")<>"U"
			nMoeda:= M->F2_MOEDA
	ElseIf Type("M->C5_MOEDA")<>"U"
			nMoeda:= M->C5_MOEDA
	ElseIf Type("nMoedaPed")<>"U"
			nMoeda:= nMoedaPed
	ElseIf Type("nMoedaNf")<> "U"
			nMoeda:= nMoedaNf
	ElseIf Type("nMoedaCor")<> "U"
			nMoeda:= nMoedaCor
	ElseIf lXFis
			nMoeda 		:= MAFISRET(,'NF_MOEDA')
	EndIf
	If Type("nTipoGer")<> "U" .And.	Type("nMoedSel")<> "U"
		nMoeda:= If(nTipoGer==2,nMoedSel,SC5->C5_MOEDA)
	EndIf
	nDecs := MsDecimais(nMoeda)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Obter Base de Calculo e Al¡quota dos Impostos.				 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nOrdSFC:=(SFC->(IndexOrd()))
nRegSFC:=(SFC->(Recno()))
SFC->(DbSetOrder(2))

If !lXFis
	aImposto[11] := aItemINFO[4]      // Rateio do Frete
	aImposto[4]  := aItemINFO[5]      // Rateio de Despesas
	aImposto[3]  := aItemINFO[3]+aItemINFO[4]+aItemINFO[5] // Base de Calculo
	If Subs(aImposto[5],4,1) == "S"
		aImposto[_BASECALC] -= aImposto[18]
	EndIf
	SFB->(DbSeek(xFilial("SFB")+aImposto[1]))
	aImposto[_ALIQUOTA] := SFB->FB_ALIQ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Soma base dos impostos incidentes...					     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nI := At( ";",cImpIncid )
	nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
	While nI > 1
		nE := AScan( aItemINFO[6],{|x| x[1] == Left(cImpIncid,nI-1) } )
		If nE > 0
			If "IE" $ AllTrim(aItemINFO[6,nE,1]) 
				aImposto[3] := aImposto[3]-aItemINFO[6,nE,4]
			Else
				aImposto[3] := aImposto[3]+aItemINFO[6,nE,4]
			EndIf
		EndIf
		cImpIncid := Stuff( cImpIncid,1,nI,"" )
		nI := At( ";",cImpIncid )
		nI := If( nI==0,Len( AllTrim( cImpIncid ) )+1,nI )
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua o Calculo do imposto...							     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (SFC->(MsSeek(xFilial("SFC")+cTes+cImp)))
		If SFC->FC_BASE > 0
			aImposto[3] :=  Round(aImposto[3] * (1 - SFC->FC_BASE / 100), MsDecimais(nMoeda))
		Endif
	EndIf

	aImposto[4]  := ROUND(aImposto[3] * aImposto[2]/100,2)

	xRet:=aImposto
Else
	Do Case
		Case cCalculo=="A"
			SFB->(DbSeek(xFilial("SFB")+aInfo[1]))
			xRet:=SFB->FB_ALIQ
		Case cCalculo=="B"
			xRet:=MaFisRet(nItem,"IT_VALMERC")+MaFisRet(nItem,"IT_FRETE")+MaFisRet(nItem,"IT_DESPESA")+MaFisRet(nItem,"IT_SEGURO")
			If GetNewPar('MV_DESCSAI','1')=='1'
				xRet	+= MaFisRet(nItem,"IT_DESCONTO")
			Endif
			//Tira os descontos se for pelo liquido

			If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
				cImpIncid:=Alltrim(SFC->FC_INCIMP)
				If SFC->FC_LIQUIDO=="S"
					xRet-=MaFisRet(nItem,"IT_DESCONTO")
				Endif
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Soma a Base de Calculo os Impostos Incidentes                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
            If !Empty(cImpIncid)
			   aImpRef:=MaFisRet(nItem,"IT_DESCIV")
			   aImpVal:=MaFisRet(nItem,"IT_VALIMP")
			   For nI:=1 to Len(aImpRef)
			       If !Empty(aImpRef[nI])
				      IF Trim(aImpRef[nI][1])$cImpIncid
					    If "IE" $ AllTrim(aImpRef[nI][1])
							xRet-=aImpVal[nI]
						Else
							xRet+=aImpVal[nI]
						EndIf
				      Endif
				   Endif
			   Next
			Endif
			If (SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES") +cImp)))
				If SFC->FC_BASE > 0
					xRet := Round(xRet * (1 - SFC->FC_BASE / 100), MsDecimais(nMoeda))
				Endif
			EndIf
		Case cCalculo=="V"

		    SFC->(DbSetOrder(2))
			SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[1]))
			If SFC->FC_CALCULO=="T"
				nBase := MaRetBasT(aInfo[2],nItem,MaFisRet(nItem,'IT_ALIQIV'+aInfo[X_NUMIMP]    ))
			Else
				nBase:=MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
			Endif


			nAliq:=MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])
			xRet:= ROUND(nBase *(nAliq/100),nDecs)
	EndCase
Endif

SFC->(DbSetOrder(nOrdSFC))
SFC->(DbGoto(nRegSFC))

Return( xRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M460IVAPT ºAutor  ³Mary C. Hergert     º Data ³ 06/08/2008  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Calcula o IVA destacado para Portugal                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³SigaFis                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M460IVAPT(cCalculo,nItem,aInfo,lXFis)

Local aImp 		:= {}
Local aItem 	:= {}
Local aArea		:= GetArea()
Local aImpRef 	:= {}
Local aImpVal 	:= {}

Local cImp		:= ""
Local cTes   	:= ""
Local cRegiao	:= ""
Local cGrpAces	:= GetNewPar("MV_GRPACES","FR=004;SE=005;DT=006;DN=007;TA=008")
Local cGrupo	:= ""
Local cProd		:= ""
Local cImpIncid	:= ""
Local cImpDesp	:= "IFD|IDD|ISD|ITD"

Local nOrdSFC   := 0
Local nRegSFC   := 0
Local nBase		:= 0
Local nAliq 	:= 0
Local nDecs 	:= 0
Local nPos	 	:= 0
Local nI		:= 0
Local nE		:= 0

Local lIsento	:= .F.

Local cChave    := ""
Local dEmissao  := dDataBase

Local xRet
Local nMoeda	:=1

/*
ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
³ Observacao :                                                                  ³
³                                                                               ³
³ A variavel ParamIxb tem como conteudo um Array[2], contendo :                 ³
³ [1,1] > Quantidade Vendida                                                    ³
³ [1,2] > Preco Unitario                                                        ³
³ [1,3] > Valor Total do Item, com Descontos etc...                             ³
³ [1,4] > Valor do Frete rateado para este Item                                 ³
³         Para Portugal, o imposto do frete e calculado em separado do item     ³
³ [1,5] > Valor das Despesas rateado para este Item                             ³
³         Para Portugal, o imposto das despesas e calculado em separado do item ³
³ [1,6] > Array Contendo os Impostos já calculados, no caso de incidência de    ³
³         outros impostos.                                                      ³
³ [2,1] > Array aImposto, contendo as Informações do Imposto que será calculado.³
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
*/
If !lXfis
   aItem	:= ParamIxb[1]
   aImp		:= ParamIxb[2]
   cImp		:= aImp[1]
   cTes		:= SF4->F4_CODIGO
   cProd 	:= SB1->B1_COD
Else
   cImp		:= aInfo[1]
   cTes		:= MaFisRet(nItem,"IT_TES")
   cProd	:= MaFisRet(nItem,"IT_PRODUTO")
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verificando o cadastro do produto movimentado³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//Frontloja usa o arquivo SBI para cadastro de produtos
If cModulo == "FRT"
	SBI->(DbSeek(xFilial("SBI")+cProd))
Else
	SB1->(DbSeek(xFilial("SB1")+cProd))
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o cliente ou fornecedor sao isentos pelo cadastro:         ³
//³1 = SP IVA - Pessoa Singular (sujeito passivo de IVA, pessoa singular) ³
//³2 = SP IVA - Pessoa Colectiva (sujeito passivo de IVA, pessoa coletiva)³
//³3 = Isento IVA Pessoa Singular                                         ³
//³4 = Isento IVA Pessoa Colectiva                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cModulo $ "FAT|LOJA|FRT|TMK"
	If SA1->A1_TIPO $ "34"
		lIsento := .T.
	Endif
	If SA1->(ColumnPos("A1_PAISEMP")) == 0 .or. SA1->A1_PAISEMP == "S"
		cRegiao := GetNewPar("MV_GRPTRIB","")
	Else
		cRegiao := SA1->A1_GRPTRIB
	Endif
Else
	If SA2->A2_TIPO $ "34"
		lIsento := .T.
	Endif
	If SA2->(ColumnPos("A2_PAISEMP")) == 0 .or. SA2->A2_PAISEMP == "S"
		cRegiao := GetNewPar("MV_GRPTRIB","")
	Else
		cRegiao := SA2->A2_GRPTRIB
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o grupo de tributacao das despesas acessorias:³
//³IVC - IVA destacado das mercadorias                    ³
//³IFD - IVA destacado frete                              ³
//³IDD - IVA destacado despesas                           ³
//³ISD - IVA destacado seguro                             ³
//³ITD - IVA destacado tara                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do Case
Case cImp == "IFD"
	nPos := At("FR",cGrpAces)
Case cImp == "IDD"
	nPos := At("DT",cGrpAces)
Case cImp == "ISD"
	nPos := At("SE",cGrpAces)
Case cImp == "ITD"
	nPos := At("TA",cGrpAces)
EndCase

If cImp $ cImpDesp
	If nPos > 0
		cGrupo := Padr(SubStr(cGrpAces,nPos+3,3),TamSx3("B1_GRTRIB")[1])
	Endif
Else
	//Frontloja usa o arquivo SBI para cadastro de produtos
	If cModulo=="FRT"
		cGrupo := SBI->BI_GRTRIB
	Else
		cGrupo := SB1->B1_GRTRIB
	Endif
Endif

If Empty(cRegiao)
	cRegiao := GetNewPar("MV_GRPTRIB","")
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a existencia do Plano IVA  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SFF")
SFF->(dbSetOrder(14))
If SFF->(dbseek(xFilial("SFF")+cImp+cRegiao+cGrupo))

	If !lIsento
		//Busca a aliquota na tabela de plano IVA
		If SFF->FF_TIPO == "S" .or. SFF->FF_TIPO == "I"
			lIsento := .T.
		Endif
		nAliq := SFF->FF_ALIQ
	EndIf
	cChave := xFilial("CE8")+SFF->FF_IMPOSTO+SFF->FF_REGIAO+SFF->FF_TIPO

Else

	//Busca a aliquota padrao para o imposto quando nao ha o plano IVA
   	DbSelectArea("SFB")
   	SFB->(DbSetOrder(1))
   	If SFB->(Dbseek(xFilial("SFB")+cImp))
		nAliq := SFB->FB_ALIQ
	EndIf
	If FieldPos("FB_TIPALIQ") > 0
		cChave := xFilial("CE8")+SFB->FB_CODIGO+Space(TamSX3("CE8_EST")[1])+SFB->FB_TIPALIQ
	EndIf

Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a existencia de aliquota na tabela de validade  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If AliasInDic("CE8")
	//Busca data de emissao
	If Type("dDEmissao") == "D"
		dEmissao := dDEmissao
	Else
		dEmissao := dDataBase
	EndIf

	//Busca aliquota na tabela de aliquotas por periodo
	dbSelectArea("CE8")
	CE8->(dbSetOrder(2))
	If CE8->(dbSeek(cChave))
		Do While cChave == CE8->CE8_FILIAL+CE8->CE8_CODIMP+CE8->CE8_EST+CE8->CE8_TIPO .and. CE8->(!EOF())

			If CE8->CE8_DATINI <= dEmissao  .and. CE8->CE8_DATFIN >= dEmissao
				If CE8->CE8_ISEN == "1"
					lIsento := .T.
				Else
					nAliq := CE8->CE8_ALIQ
				EndIf
				Exit
			EndIf

			CE8->(dbSkip())
		EndDo
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a base de calculo do IVA do item. Somente sera somado ao total do        ³
//³item os valores referentes a outros impostos que incidem na base do IVA. As    ³
//³despesas acessorias (frete, seguro, despesas e tara) tem tributacao especifica ³
//³e nao devem ser somadas para compor a base de calculo do item.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica qual imposto vai calcular:³
//³IVC - IVA destacado das mercadorias³
//³IFD - IVA destacado frete          ³
//³IDD - IVA destacado despesas       ³
//³ISD - IVA destacado seguro         ³
//³ITD - IVA destacado tara           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lIsento
	If !lXFis
		Do Case
		Case cImp == "IFD"
			nBase := aItem[4]
		Case cImp == "IDD"
			nBase := aItem[8]
		Case cImp == "ISD"
			nBase := aItem[9]
		Case cImp == "ITD"
			nBase := aItem[11]
		OtherWise
			nBase := aItem[3]
		EndCase
	Else
  		Do Case
		Case cImp == "IFD"
			nBase := MaFisRet(nItem,"IT_FRETE")
		Case cImp == "IDD"
			nBase := MaFisRet(nItem,"IT_DESPESA")
		Case cImp == "ISD"
			nBase := MaFisRet(nItem,"IT_SEGURO")
		Case cImp == "ITD"
			nBase := MaFisRet(nItem,"IT_TARA")
		OtherWise
			nBase := MaFisRet(nItem,"IT_VALMERC")
			If GetNewPar('MV_DESCSAI','1')=='1'
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Somente quando for IVA de mercadorias aplica o desconto³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nBase += MaFisRet(nItem,"IT_DESCONTO")
			Endif
		EndCase
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os decimais da moeda para arredondamento do valor  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 	If Type("M->F1_MOEDA")<>"U"
	   nMoeda:= M->F1_MOEDA
	ElseIf Type("M->C7_MOEDA")<>"U"
			nMoeda:= M->C7_MOEDA
	ElseIf Type("M->F2_MOEDA")<>"U"
			nMoeda:= M->F2_MOEDA
	ElseIf Type("M->C5_MOEDA")<>"U"
			nMoeda:= M->C5_MOEDA
	ElseIf Type("nMoedaPed")<>"U"
			nMoeda:= nMoedaPed
	ElseIf Type("nMoedaNf")<> "U"
			nMoeda:= nMoedaNf
	ElseIf Type("nMoedaCor")<> "U"
			nMoeda:= nMoedaCor
	ElseIf lXFis
			nMoeda 		:= MAFISRET(,'NF_MOEDA')
	EndIf
	If Type("nTipoGer")<> "U" .And.	Type("nMoedSel")<> "U"
		nMoeda:= If(nTipoGer==2,nMoedSel,SC5->C5_MOEDA)
	EndIf
	nDecs := MsDecimais(nMoeda)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os descontos e os impostos incidentes³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lXFis

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Soma a Base de Calculo os Impostos Incidentes ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nI := At(cImpIncid,";" )
	nI := Iif(nI==0,Len(AllTrim(cImpIncid))+1,nI)
	While nI > 1
		nE := AScan(aItem[6],{|x| x[1]==Left(cImpIncid,nI-1)})
		If nE > 0
			nBase += Round(aItem[6,nE,4],nDecs)
		Endif
		cImpIncid := Stuff(cImpIncid,1,nI,"")
		nI := At(cImpIncid,";")
		nI := Iif(nI==0,Len(AllTrim(cImpIncid))+1,nI)
	Enddo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Reduz os descontos, quando a configuração indica calculo pelo liquido.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Subs(aImp[5],4,1) == "S" .And. Len(aImp) >= 18 .And. ValType(aImp[18])=="N"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Somente quando for IVA de mercadorias aplica o desconto³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(cImp $ cImpDesp)
			If !lIsento
				nBase -= Round(aImp[18],nDecs)
			Endif
		Endif
	Endif

Else

	If cCalculo == "B"
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Somente quando for IVA de mercadorias aplica o desconto³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !(cImp $ cImpDesp)
			nOrdSFC := (SFC->(IndexOrd()))
			nRegSFC := (SFC->(Recno()))
			SFC->(DbSetOrder(2))
			If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
				cImpIncid := Alltrim(SFC->FC_INCIMP)
				If SFC->FC_LIQUIDO == "S"
					If !lIsento
						nBase -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
						nBase := Round(nBase,nDecs)
					Endif
				Endif
			Endif
			SFC->(DbSetOrder(nOrdSFC))
			SFC->(DbGoto(nRegSFC))
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Soma a Base de Calculo os Impostos Incidentes ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(cImpIncid)
			aImpRef := MaFisRet(nItem,"IT_DESCIV")
			aImpVal := MaFisRet(nItem,"IT_VALIMP")
			For nI := 1 to Len(aImpRef)
				If !Empty(aImpRef[nI])
					If Trim(aImpRef[nI][1]) $ cImpIncid
						nBase += Round(aImpVal[nI],nDecs)
					Endif
				Endif
			Next
		Endif
	Endif
Endif

If !lXFis
   aImp[02] := nAliq
   aImp[03] := nBase
   aImp[04] := Round(((nAliq * nBase)/100),nDecs)
   xRet := aImp
Else
    Do Case
       Case cCalculo=="B"
            xRet := nBase
       Case cCalculo=="A"
            xRet := nALiq
       Case cCalculo=="V"
			SFC->(DbSetOrder(2))
			SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[1]))
			If SFC->FC_CALCULO=="T"
				nBase := MaRetBasT(aInfo[2],nItem,MaFisRet(nItem,'IT_ALIQIV'+aInfo[2]))
			Else
				nBase :=MaFisRet(nItem,"IT_BASEIV"+aInfo[2])
			Endif
            xRet := Round(((nAliq * nBase)/100),nDecs)
    EndCase
Endif

Return(xRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M460IVAEQ ºAutor  ³ Nilton (Onsten)    º Data ³  15/06/2010 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³    Calculo de Imposto IVA - Localizacao Equadora           º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA460                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M460IVAEQ(cCalculo,nItem,aInfo,lXFis)
Local nAliq := 0
Local nBase,cImp,xRet,nOrdSFC,nRegSFC
Local cImpIncid,nE,nI
Local aImpRef := {}
Local aImpVal := {}
Local nMoeda  := 1 
Local lCalcItem := .F.
Local nDecimais := 2
Local lRatVImpMI := FindFunction("RatVImpMI")

If(Type("nMoedaCor")=="U")
	nMoeda := MaFisRet(, "NF_MOEDA")
Else
	nMoeda := nMoedaCor
Endif

nDecimais:= Msdecimais(nMoeda)

SetPrvt("CALIASROT,CORDEMROT,AITEMINFO,AIMPOSTO,_CPROCNAME,_CZONCLSIGA")
SetPrvt("_LAGENTE,_LEXENTO,AFISCAL,_LCALCULAR,_LESLEGAL,_NALICUOTA,NALIQ")
SetPrvt("_NVALORMIN,_NREDUCIR,CTIPOEMPR,CTIPOCLI,CTIPOFORN,CZONFIS,CCFO,LRET")
SetPrvt("CCODPROD,LRET,NBASE,CQUALI,CPROD")

lRet := .T.
cAliasRot  := Alias()
cOrdemRot  := IndexOrd()

If !lXFis
	aItemINFO:=ParamIxb[1]
	aImposto:=ParamIxb[2]
	xRet:=aImposto
	cImp:=aImposto[1]
	cImpIncid:=aImposto[10]
	cProd:=aImposto[16]
Else
    xRet:=0
    cImp:=aInfo[X_IMPOSTO]
    cProd:=MafisRet(nItem,"IT_PRODUTO")
    cImpIncid:=""
Endif

nBase := 0

// Busca o CFO do Tes Correspondente - SF4
dbSelectArea("SF4")
If lXFis
   SF4->(DbSeek(xFilial("SF4")+MaFisRet(nItem,"IT_TES")))
Endif
cCFO    := Alltrim(SF4->F4_CF)
cVerIVA := SF4->F4_CALCIVA

//TES nao calcula IVA
If cVerIva == "2" .or. cVerIva == "3"
   lRet := .F.
   Return ( xRet )
Endif

If lRet
	dbSelectArea("SFB")
	dbSetOrder(1)
	If dbSeek(xFilial("SFB")+cImp)

   		//+---------------------------------------------------------------+
   		//¦ Verifica a Alíquota                                           ¦
   		//+---------------------------------------------------------------+
		If IIf(lXFis,cCalculo$"BA",.T.)
			nAliq := 0
			nBase := 1
			If SB1->B1_ALQIVA <> 0
				nAliq := SB1->B1_ALQIVA
			EndIf
			If nAliq == 0
				nAliq := SFB->FB_ALIQ
			EndIf
			If cVerIva == "4"
				nAliq := 0
			EndIf
		EndIf

   		//+---------------------------------------------------------------+
   		//¦ Efectua el Cálculo del Impuesto                               ¦
   		//+---------------------------------------------------------------+
		If !lXFis
			//Tira os descontos se for pelo liquido
			If Subs(aImposto[5],4,1) == "S"
				aImposto[_BASECALC]	:=	aImposto[18] * -1
			Endif
			aImposto[_ALIQUOTA] := nAliq
			aImposto[_BASECALC] +=  aItemINFO[3] * nBase
			aImposto[_IMPUESTO] := (aImposto[_BASECALC] * (nAliq /100))
			xRet := aImposto
			//+---------------------------------------------------------------+
			//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
			//+---------------------------------------------------------Lucas-+
			nI:=At(cImpIncid,";" )
			nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
			While nI>1
				nE:=AScan(aItemINFO[6],{|x| x[1]==Left(cImpIncid,nI-1)})
				If nE>0
					aImposto[_BASECALC] += aItemINFO[6,nE,4]
				End
				cImpIncid:=Stuff(cImpIncid,1,nI,"")
				nI:=At(cImpIncid,";")
				nI:=If(nI==0,Len(AllTrim(cImpIncid))+1,nI)
			EndDo
		Else
			If cCalculo=="B"
				xRet += MaFisRet(nItem,"IT_VALMERC") * nBase
				//Tira os descontos se for pelo liquido*/
				nOrdSFC := (SFC->(IndexOrd()))
				nRegSFC := (SFC->(Recno()))
				SFC->(DbSetOrder(2))
				SFC->(dbGoTop())
				If (SFC->(DbSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+cImp)))
					cImpIncid:=Alltrim(SFC->FC_INCIMP)
					If SFC->FC_LIQUIDO=="S" .And. GetNewPar('MV_DESCSAI','1')<>'1'
						xRet -= If(SFC->FC_CALCULO=="T",MaFisRet(nItem,"NF_DESCONTO"),MaFisRet(nItem,"IT_DESCONTO"))
					Endif
				Endif
				SFC->(DbSetOrder(nOrdSFC))
				SFC->(DbGoto(nRegSFC))

				//+---------------------------------------------------------------+
				//¦ Soma a Base de Cálculo os Impostos Incidentes                 ¦
				//+---------------------------------------------------------------+
				If !Empty(cImpIncid)
					aImpRef:=MaFisRet(nItem,"IT_DESCIV")
					aImpVal:=MaFisRet(nItem,"IT_VALIMP")
					For nI:=1 to Len(aImpRef)
						If !Empty(aImpRef[nI])
							If Trim(aImpRef[nI][1])$cImpIncid
								xRet+=aImpVal[nI]
							EndIf
						EndIf
					Next
				EndIf
			ElseIf cCalculo=="V"
				nAliq := MaFisRet(nItem,"IT_ALIQIV"+aInfo[X_NUMIMP])

				SFC->(DbSetOrder(2))
				SFC->(MsSeek(xFilial("SFC")+MaFisRet(nItem,"IT_TES")+aInfo[X_IMPOSTO]))	
				If SFC->FC_CALCULO=="T"
					nBase := MaRetBasT(aInfo[X_NUMIMP], nItem, nAliq)
				Else
					nBase := MaFisRet(nItem,"IT_BASEIV"+aInfo[X_NUMIMP])
					lCalcItem := .T.
				Endif
				
				If lCalcItem .and. lRatVImpMI
					xRet := RatVImpMI(aInfo, nItem, nAliq, nBase, nMoeda, 100)
				Else
					xRet := Round(nBase * (nAliq/100),nDecimais)
				Endif

			ElseIf cCalculo=="A"
				xRet := nALiq
			EndIf
		EndIf
	EndIf
EndIf
dbSelectArea( cAliasRot )
dbSetOrder( cOrdemRot )
Return( xRet )
