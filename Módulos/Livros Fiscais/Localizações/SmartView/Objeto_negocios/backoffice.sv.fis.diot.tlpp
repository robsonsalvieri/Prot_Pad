#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fis.diot.ch'

namespace custom.fiscal.diot.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fis.diot
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 12/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIS', tables='SA2,CE8', name='Clientes por cobrador', country='MEX', initialRelease='12.1.2410', customTables='SA2,CE8' )
class diotTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class diotTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'DIOT'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'DIOT'

	// Define a Área
	self:appendArea( STR0002 ) // 'Libros Fiscales'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FISSV188' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// adiciona capmos
	aAdd( aCpos, { 'DESCRI'        , STR0007, 'string', STR0007 } ) // 'descripcion'
	aAdd( aCpos, { 'VALOR'         , STR0008, 'number', STR0008 } ) // 'Importe'
	aAdd( aCpos, { 'REPORT'        , STR0009, 'string', STR0009 } ) // 'Report'
	aAdd( aCpos, { 'CE8_BASE8'     , STR0071, 'number', STR0071 } )	// "Devols RFN"
	aAdd( aCpos, { 'CE8_BASE10'    , STR0072, 'number', STR0072 } )	// "Pagos RFS"
	aAdd( aCpos, { 'CE8_BI1011'    , STR0073, 'number', STR0073 } )	// "Devols RFS"
	aAdd( aCpos, { 'CE8_BA1516'    , STR0074, 'number', STR0074 } )	// "Pagos Interior"
	aAdd( aCpos, { 'CE8_BI1516'    , STR0075, 'number', STR0075 } )	// "Devols Interior"
	aAdd( aCpos, { 'CE8_BASE15'    , STR0076, 'number', STR0076 } )	// "Pag Imp Tang"
	aAdd( aCpos, { 'CE8_BASDVA'    , STR0077, 'number', STR0077 } )	// "Dev Imp Tang"
	aAdd( aCpos, { 'CE8_BASDVB'    , STR0078, 'number', STR0078 } )	// "Val Imp Intang"
	aAdd( aCpos, { 'CE8_BASDVC'    , STR0079, 'number', STR0079 } )	// "Dev Imp Intang"
	aAdd( aCpos, { 'CE8_IVAACA'    , STR0080, 'number', STR0080 } )	// "No Prop RFN"
	aAdd( aCpos, { 'CE8_IVAACB'    , STR0081, 'number', STR0081 } )	// "Propor RFN"
	aAdd( aCpos, { 'CE8_IVAACC'    , STR0082, 'number', STR0082 } )	// "No Prop RFS"
	aAdd( aCpos, { 'CE8_IVAACD'    , STR0083, 'number', STR0083 } )	// "Propor RFS"
	aAdd( aCpos, { 'CE8_IVAACE'    , STR0084, 'number', STR0084 } )	// "No Prop Inter"
	aAdd( aCpos, { 'CE8_IVAACF'    , STR0085, 'number', STR0085 } )	// "Propor Interior"
	aAdd( aCpos, { 'CE8_IVAACG'    , STR0086, 'number', STR0086 } )	// "No Prop Tangib"
	aAdd( aCpos, { 'CE8_IVAACH'    , STR0087, 'number', STR0087 } )	// "Propor Tangib"
	aAdd( aCpos, { 'CE8_IVAACI'    , STR0088, 'number', STR0088 } )	// "No Prop Intang"
	aAdd( aCpos, { 'CE8_IVAACJ'    , STR0089, 'number', STR0089 } )	// "Propor Intang"
	aAdd( aCpos, { 'CE8_IVANAA'    , STR0090, 'number', STR0090 } )	// "Propor RFN"
	aAdd( aCpos, { 'CE8_IVANAB'    , STR0091, 'number', STR0091 } )	// "No Prop RFN"
	aAdd( aCpos, { 'CE8_IVANAC'    , STR0092, 'number', STR0092 } )	// "Exento RFN"
	aAdd( aCpos, { 'CE8_IVANAD'    , STR0093, 'number', STR0093 } )	// "No Objeto RFN"
	aAdd( aCpos, { 'CE8_IVANAE'    , STR0094, 'number', STR0094 } )	// "Propor RFS"
	aAdd( aCpos, { 'CE8_IVANAF'    , STR0095, 'number', STR0095 } )	// "No Prop RFS"
	aAdd( aCpos, { 'CE8_IVANAG'    , STR0096, 'number', STR0096 } )	// "Exento RFS"
	aAdd( aCpos, { 'CE8_IVANAH'    , STR0097, 'number', STR0097 } )	// "No Objeto RFS"
	aAdd( aCpos, { 'CE8_IVANAI'    , STR0098, 'number', STR0098 } )	// "Propor Interior"
	aAdd( aCpos, { 'CE8_IVANAJ'    , STR0099, 'number', STR0099 } )	// "No Prop Interior"
	aAdd( aCpos, { 'CE8_IVANAK'    , STR0100, 'number', STR0100 } )	// "Exento Interior"
	aAdd( aCpos, { 'CE8_IVANAL'    , STR0101, 'number', STR0101 } )	// "No Objeto Int"
	aAdd( aCpos, { 'CE8_IVANAM'    , STR0102, 'number', STR0102 } )	// "Propor Tangib"
	aAdd( aCpos, { 'CE8_IVANAN'    , STR0103, 'number', STR0103 } )	// "No Prop Tangib"
	aAdd( aCpos, { 'CE8_IVANAO'    , STR0104, 'number', STR0104 } )	// "Exento Tangib"
	aAdd( aCpos, { 'CE8_IVANAP'    , STR0105, 'number', STR0105 } )	// "No Objeto Tang"
	aAdd( aCpos, { 'CE8_IVANAQ'    , STR0106, 'number', STR0106 } )	// "Propor Intangib"
	aAdd( aCpos, { 'CE8_IVANAR'    , STR0107, 'number', STR0107 } )	// "No Prop Intang"
	aAdd( aCpos, { 'CE8_IVANAS'    , STR0108, 'number', STR0108 } )	// "Exento Intangib"
	aAdd( aCpos, { 'CE8_IVANAT'    , STR0109, 'number', STR0109 } )	// "No Objeto Intan"
	aAdd( aCpos, { 'CE8_IVARET'    , STR0110, 'number', STR0110 } )	// "IVA Retenido"
	aAdd( aCpos, { 'CE8_BASIEX'    , STR0111, 'number', STR0111 } )	// "Pagos Imp Exen"
	aAdd( aCpos, { 'CE8_BASEEX'    , STR0112, 'number', STR0112 } )	// "Pagos Exentos"
	aAdd( aCpos, { 'CE8_BASE0'     , STR0113, 'number', STR0113 } )	// "Pagos Tasa 0%"
	aAdd( aCpos, { 'CE8_VALTOT'    , STR0114, 'number', STR0114 } )	// "No Obj IVA Nal"
	aAdd( aCpos, { 'CE8_IVADEV'    , STR0115, 'number', STR0115 } )	// "No Obj IVA Ext"

	// demais campos
	self:aFields := {'CE8_FILIAL','CE8_PROV','CE8_TIENDA','A2_NOME','CE8_MESANO','CE8_RFC','CE8_IDFISC','CE8_TIPTER','CE8_TIPOPE','CE8_PAIS','CE8_NUMDOC','CE8_FECDOC','CE8_TIPO','DESCRI','VALOR','REPORT','CE8_BASE8','CE8_BASE10','CE8_BI1011','CE8_BA1516','CE8_BI1516','CE8_BASE15','CE8_BASDVA','CE8_BASDVB','CE8_BASDVC','CE8_IVAACA','CE8_IVAACB','CE8_IVAACC','CE8_IVAACD','CE8_IVAACE','CE8_IVAACF','CE8_IVAACG','CE8_IVAACH','CE8_IVAACI','CE8_IVAACJ','CE8_IVANAA','CE8_IVANAB','CE8_IVANAC','CE8_IVANAD','CE8_IVANAE','CE8_IVANAF','CE8_IVANAG','CE8_IVANAH','CE8_IVANAI','CE8_IVANAJ','CE8_IVANAK','CE8_IVANAL','CE8_IVANAM','CE8_IVANAN','CE8_IVANAO','CE8_IVANAP','CE8_IVANAQ','CE8_IVANAR','CE8_IVANAS','CE8_IVANAT','CE8_IVARET','CE8_BASIEX','CE8_BASEEX','CE8_BASE0','CE8_VALTOT','CE8_IVADEV'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class diotTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array
	Local aResumo       as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	Local nPosDesc      as numeric
	Local nPosValor     as numeric
	Local nSeq          as numeric
	Local cDiotVer      as Character

	cDiotVer := SuperGetMV("MV_DIOTVER", .F., "1")

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }
	aResumo         := { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA2 := getCpoUser( aCustomFields, 'SA2', 'A2', ',' )
	cCpoCE8 := getCpoUser( aCustomFields, 'CE8', 'CE8', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )

		// Define os dados para conversão de moedas
		nPosExp := aScan( aFiltro, { | x | AllTrim( x[1] ) == 'REPORT' } )
		If ( nPosExp > 0 )
			cExp := aFiltro[nPosExp, 3]
			aDel( aFiltro, nPosExp )
			aSize( aFiltro, ( Len( aFiltro ) - 1 ) )
		EndIf
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	If cExp == "'R'"
		If cDiotVer == "2"

			cQuery := "SELECT CE8.CE8_FILIAL,CE8.CE8_MESANO,COUNT(DISTINCT CE8.CE8_PROV+CE8.CE8_TIENDA) AS VALOR,"
			cQuery += " SUM(CE8_BA1516) CE8_BA1516, SUM(CE8_BASE15) CE8_BASE15, SUM(CE8_BA1011) CE8_BA1011, SUM(CE8.CE8_BASE8) CE8_BASE8,"
			cQuery += " SUM(CE8_BASE10) CE8_BASE10, SUM(CE8_BI1516) CE8_BI1516, SUM(CE8_BI1011) CE8_BI1011, "
			cQuery += " SUM(CE8_BASIEX) CE8_BASIEX, SUM(CE8_BASE0) CE8_BASE0, SUM(CE8_BASEEX) CE8_BASEEX,"
			cQuery += " SUM(CE8_IVARET) CE8_IVARET, SUM(CE8_IVADEV) CE8_IVADEV, SUM(CE8_IVAPAG) CE8_IVAPAG, SUM(CE8_IVPAGI) CE8_IVPAGI,"

			cQuery += " SUM(CE8_BASDVA) CE8_BASDVA, SUM(CE8_BASDVB) CE8_BASDVB, SUM(CE8_BASDVC) CE8_BASDVC,"
			cQuery += " SUM(CE8_IVAACG) CE8_IVAACG, SUM(CE8_IVAACH) CE8_IVAACH, SUM(CE8_IVAACI) CE8_IVAACI, SUM(CE8_IVAACJ) CE8_IVAACJ, "
			cQuery += " SUM(CE8_IVAACA) CE8_IVAACA, SUM(CE8_IVAACB) CE8_IVAACB, SUM(CE8_IVAACC) CE8_IVAACC, SUM(CE8_IVAACD) CE8_IVAACD, SUM(CE8_IVAACE) CE8_IVAACE, SUM(CE8_IVAACF) CE8_IVAACF, "
			cQuery += " SUM(CE8_IVANAA) CE8_IVANAA, SUM(CE8_IVANAB) CE8_IVANAB, SUM(CE8_IVANAC) CE8_IVANAC, SUM(CE8_IVANAD) CE8_IVANAD, SUM(CE8_IVANAE) CE8_IVANAE, SUM(CE8_IVANAF) CE8_IVANAF, "
			cQuery += " 0 CE8_IVANAG, SUM(CE8_IVANAH) CE8_IVANAH, SUM(CE8_IVANAI) CE8_IVANAI, SUM(CE8_IVANAJ) CE8_IVANAJ, 0 CE8_IVANAK, SUM(CE8_IVANAL) CE8_IVANAL, "
			cQuery += " SUM(CE8_IVANAM) CE8_IVANAM, SUM(CE8_IVANAN) CE8_IVANAN, 0 CE8_IVANAO, SUM(CE8_IVANAP) CE8_IVANAP, SUM(CE8_IVANAQ) CE8_IVANAQ, SUM(CE8_IVANAR) CE8_IVANAR, "

			cQuery += " 0 CE8_IVANAS, SUM(CE8_IVANAT) CE8_IVANAT, SUM(CE8_VALTOT) CE8_VALTOT,"
			cQuery += " '' AS DESCRI, 'R' AS REPORT, 0 AS REG"
			cQuery += " FROM " + RetSqlName("CE8") + " CE8"
			cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = CE8.CE8_PROV AND SA2.A2_LOJA = CE8.CE8_TIENDA AND SA2.D_E_L_E_T_ = ?"
			cQuery += " WHERE CE8.CE8_FILIAL = ?"
			cQuery += "	  AND CE8.CE8_MESANO = ?"
			cQuery += "   AND CE8.D_E_L_E_T_ = ?"
		Else
			cQuery := "SELECT CE8.CE8_FILIAL,CE8.CE8_MESANO,COUNT(DISTINCT CE8.CE8_PROV+CE8.CE8_TIENDA) AS VALOR,"
			cQuery += " SUM(CE8_BA1516) CE8_BA1516, SUM(CE8_BASE15) CE8_BASE15, SUM(CE8_BA1011) CE8_BA1011, SUM(CE8.CE8_BASE8) CE8_BASE8,"
			cQuery += " SUM(CE8_BASE10) CE8_BASE10, SUM(CE8_BI1516) CE8_BI1516, SUM(CE8_BI1011) CE8_BI1011,"
			cQuery += " SUM(CE8_BASIEX) CE8_BASIEX, SUM(CE8_BASE0) CE8_BASE0, SUM(CE8_BASEEX) CE8_BASEEX, SUM(CE8_VALTOT) CE8_VALTOT,"
			cQuery += " SUM(CE8_IVARET) CE8_IVARET, SUM(CE8_IVADEV) CE8_IVADEV, SUM(CE8_IVAPAG) CE8_IVAPAG, SUM(CE8_IVPAGI) CE8_IVPAGI,"
			cQuery += " 0 CE8_BASDVA, 0 CE8_BASDVB, 0 CE8_BASDVC,"
			cQuery += " 0 CE8_IVAACG, 0 CE8_IVAACH, 0 CE8_IVAACI, 0 CE8_IVAACJ, 0 CE8_IVANAT, 0 CE8_IVANAS, "
			cQuery += " 0 CE8_IVAACA, 0 CE8_IVAACB, 0 CE8_IVAACC, 0 CE8_IVAACD, 0 CE8_IVAACE, 0 CE8_IVAACF, "
			cQuery += " 0 CE8_IVANAA, 0 CE8_IVANAB, 0 CE8_IVANAC, 0 CE8_IVANAD, 0 CE8_IVANAE, 0 CE8_IVANAF, "
			cQuery += " 0 CE8_IVANAG, 0 CE8_IVANAH, 0 CE8_IVANAI, 0 CE8_IVANAJ, 0 CE8_IVANAK, 0 CE8_IVANAL, "
			cQuery += " 0 CE8_IVANAM, 0 CE8_IVANAN, 0 CE8_IVANAO, 0 CE8_IVANAP, 0 CE8_IVANAQ, 0 CE8_IVANAR, "
			cQuery += " '' AS DESCRI, 'R' AS REPORT, 0 AS REG"
			cQuery += " FROM " + RetSqlName("CE8") + " CE8"
			cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = CE8.CE8_PROV AND SA2.A2_LOJA = CE8.CE8_TIENDA AND SA2.D_E_L_E_T_ = ?"
			cQuery += " WHERE CE8.CE8_FILIAL = ?"
			cQuery += "	  AND CE8.CE8_MESANO = ?"
			cQuery += "   AND CE8.D_E_L_E_T_ = ?"
		Endif
	Else

		If cDiotVer == "2"
			cQuery := "SELECT CE8.CE8_FILIAL,CE8.CE8_PROV,CE8.CE8_TIENDA,SA2.A2_NOME,CE8.CE8_MESANO,CE8.CE8_RFC,CE8.CE8_IDFISC,CE8.CE8_TIPTER,CE8.CE8_TIPOPE,CE8.CE8_PAIS,"
			cQuery += " CE8.CE8_NUMDOC,CE8.CE8_FECDOC,CE8.CE8_TIPO,CE8.CE8_BA1516,CE8.CE8_BASE15,CE8.CE8_BA1011,CE8.CE8_BASE10,CE8.CE8_BASE8,CE8.CE8_BI1516,CE8.CE8_BI1011,"
			cQuery += " CE8.CE8_BASDVA, CE8.CE8_BASDVB, CE8.CE8_BASDVC, CE8.CE8_IVANAT, 0 CE8_IVANAS,"
			cQuery += " CE8.CE8_IVAACG, CE8.CE8_IVAACH, CE8.CE8_IVAACI, CE8.CE8_IVAACJ, CE8.CE8_VALTOT,"
			cQuery += " CE8.CE8_IVAACA, CE8.CE8_IVAACB, CE8.CE8_IVAACC, CE8.CE8_IVAACD, CE8.CE8_IVAACE, CE8.CE8_IVAACF, "
			cQuery += " CE8.CE8_IVANAA, CE8.CE8_IVANAB, CE8.CE8_IVANAC, CE8.CE8_IVANAD, CE8.CE8_IVANAE, CE8.CE8_IVANAF, "
			cQuery += " 0 CE8_IVANAG, CE8.CE8_IVANAH, CE8.CE8_IVANAI, CE8.CE8_IVANAJ, 0 CE8_IVANAK, CE8.CE8_IVANAL, "
			cQuery += " CE8.CE8_IVANAM, CE8_IVANAN, 0 CE8_IVANAO, CE8.CE8_IVANAP, CE8.CE8_IVANAQ, CE8.CE8_IVANAR, "
			cQuery += " CE8.CE8_BASIEX,CE8.CE8_BASE0,CE8.CE8_BASEEX,CE8.CE8_IVARET,CE8.CE8_IVADEV, '' AS DESCRI, 0 AS VALOR, 'A' AS REPORT,"
			cQuery += " ? "
			cQuery += " ? "
			cQuery += " CE8.R_E_C_N_O_ AS REG"
			cQuery += " FROM " + RetSqlName("CE8") + " CE8"
			cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = CE8.CE8_PROV AND SA2.A2_LOJA = CE8.CE8_TIENDA AND SA2.D_E_L_E_T_ = ?"
			cQuery += " WHERE CE8.CE8_FILIAL = ?"
			cQuery += "	  AND CE8.CE8_MESANO = ?"
			cQuery += "   AND CE8.D_E_L_E_T_ = ?"
		Else
			cQuery := "SELECT CE8.CE8_FILIAL,CE8.CE8_PROV,CE8.CE8_TIENDA,SA2.A2_NOME,CE8.CE8_MESANO,CE8.CE8_RFC,CE8.CE8_IDFISC,CE8.CE8_TIPTER,CE8.CE8_TIPOPE,CE8.CE8_PAIS, CE8.CE8_VALTOT, "
			cQuery += " CE8.CE8_NUMDOC,CE8.CE8_FECDOC,CE8.CE8_TIPO,CE8.CE8_BA1516,CE8.CE8_BASE15,CE8.CE8_BA1011,CE8.CE8_BASE10,CE8.CE8_BASE8,CE8.CE8_BI1516,CE8.CE8_BI1011,"
			cQuery += " 0 CE8_BASDVA, 0 CE8_BASDVB, 0 CE8_BASDVC, 0 CE8_IVANAT, "
			cQuery += " 0 CE8_IVAACG, 0 CE8_IVAACH, 0 CE8_IVAACI, 0 CE8_IVAACJ, 0 CE8_IVANAS, "
			cQuery += " 0 CE8_IVAACA, 0 CE8_IVAACB, 0 CE8_IVAACC, 0 CE8_IVAACD, 0 CE8_IVAACE, 0 CE8_IVAACF, "
			cQuery += " 0 CE8_IVANAA, 0 CE8_IVANAB, 0 CE8_IVANAC, 0 CE8_IVANAD, 0 CE8_IVANAE, 0 CE8_IVANAF, "
			cQuery += " 0 CE8_IVANAG, 0 CE8_IVANAH, 0 CE8_IVANAI, 0 CE8_IVANAJ, 0 CE8_IVANAK, 0 CE8_IVANAL, "
			cQuery += " 0 CE8_IVANAM, 0 CE8_IVANAN, 0 CE8_IVANAO, 0 CE8_IVANAP, 0 CE8_IVANAQ, 0 CE8_IVANAR, "
			cQuery += " CE8.CE8_BASIEX,CE8.CE8_BASE0,CE8.CE8_BASEEX,CE8.CE8_IVARET,CE8.CE8_IVADEV, '' AS DESCRI, 0 AS VALOR, 'A' AS REPORT,"
			cQuery += " ? "
			cQuery += " ? "
			cQuery += " CE8.R_E_C_N_O_ AS REG"
			cQuery += " FROM " + RetSqlName("CE8") + " CE8"
			cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = CE8.CE8_PROV AND SA2.A2_LOJA = CE8.CE8_TIENDA AND SA2.D_E_L_E_T_ = ?"
			cQuery += " WHERE CE8.CE8_FILIAL = ?"
			cQuery += "	  AND CE8.CE8_MESANO = ?"
			cQuery += "   AND CE8.D_E_L_E_T_ = ?"
		Endif
	Endif

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	If cExp == "'R'"
		cQuery += " GROUP BY CE8.CE8_FILIAL, CE8.CE8_MESANO"
	Endif


	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	If !cExp == "'R'"
		oExecA:SetUnSafe( nSeq+=1, cCpoSA2 )
		oExecA:SetUnSafe( nSeq+=1, cCpoCE8 )
	Endif
	oExecA:SetString( nSeq+=1 , xFilial("SA2") )			// A2_FILIAL
	oExecA:SetString( nSeq+=1, ' ' )						// SA2.D_E_L_E_T_
	oExecA:SetString( nSeq+=1, xFilial("CE8") )				// CE8_FILIAL
	oExecA:SetString( nSeq+=1, jParams['MV_PAR01'][1] )		// CE8_MESANO
	oExecA:SetString( nSeq+=1, ' ' )						// CE8.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		If cExp == "'R'"

			For nX := 1 To Len( self:aStruct )
				If ( self:aStruct[nx][5] == 'VALOR' )
					nPosValor := nX
				Endif
				If ( self:aStruct[nx][5] == 'DESCRI' )
					nPosDesc := nX
				Endif
				If ( self:aStruct[nx][5] == 'CE8_FILIAL' )
					nPosFilial := nX
				Endif
				If ( self:aStruct[nx][5] == 'CE8_MESANO' )
					nPosMesAno := nX
				Endif
			Next

			// adiciona array com totais
			If cDiotVer == "2"
				// Valor de los actos o actividades
				AADD(aResumo,{STR0025,(cAliasA)->CE8_BA1011})       // "Valor total de actos o actividades pagadas / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0026,(cAliasA)->CE8_BASE8})		// "Devoluciones, descuentos y bonificaciones / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0027,(cAliasA)->CE8_BASE10})		// "Valor total de actos o actividades pagadas / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0028,(cAliasA)->CE8_BI1011})		// "Devoluciones, descuentos y bonificaciones / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0029,(cAliasA)->CE8_BA1516})		// "Valor total de actos o actividades pagadas / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0030,(cAliasA)->CE8_BI1516})		// "Devoluciones, descuentos y bonificaciones / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0031,(cAliasA)->CE8_BASE15})		// "Valor total de actos o actividades pagadas / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0032,(cAliasA)->CE8_BASDVA})		// "Devoluciones, descuents y bonificaciones / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA "
				AADD(aResumo,{STR0033,(cAliasA)->CE8_BASDVB})		// "Valor total de actos o actividades pagadas / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% de IVA"
				AADD(aResumo,{STR0034,(cAliasA)->CE8_BASDVC})		// "Devoluciones, descuentos y bonificaciones / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% de IVA"
				AADD(aResumo,{STR0035,(cAliasA)->CE8_IVAACA})		// "Exclusivamente de actividades gravadas / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0036,(cAliasA)->CE8_IVAACB})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0037,(cAliasA)->CE8_IVAACC})		// "Exclusivamente de actividades gravadas / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0038,(cAliasA)->CE8_IVAACD})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0039,(cAliasA)->CE8_IVAACE})		// "Exclusivamente de actividades gravadas / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0040,(cAliasA)->CE8_IVAACF})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0041,(cAliasA)->CE8_IVAACG})		// "Exclusivamente de actividades gravadas / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0042,(cAliasA)->CE8_IVAACH})		// "Actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0043,(cAliasA)->CE8_IVAACI})		// "Exclusivamente de actividades gravadas / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% de IVA"
				AADD(aResumo,{STR0044,(cAliasA)->CE8_IVAACJ})		// "Actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% de IVA"
				AADD(aResumo,{STR0045,(cAliasA)->CE8_IVANAA})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0046,(cAliasA)->CE8_IVANAB})		// "Asociado a actividades que no cumple con requisitos / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0047,(cAliasA)->CE8_IVANAC})		// "Asociado a actividades exentas / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0048,(cAliasA)->CE8_IVANAD})		// "Asociado a actividades no objeto / Actos o actividades pagados en la región fronteriza norte"
				AADD(aResumo,{STR0049,(cAliasA)->CE8_IVANAE})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0050,(cAliasA)->CE8_IVANAF})		// "Asociado a actividades que no cumple con requisitos / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0051,(cAliasA)->CE8_IVANAG})		// "Asociado a actividades exentas / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0052,(cAliasA)->CE8_IVANAH})		// "Asociado a actividades no objeto / Actos o actividades pagados en la región fronteriza sur"
				AADD(aResumo,{STR0053,(cAliasA)->CE8_IVANAI})		// "Asociado a actividades por las cuales se aplicó una proporción / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0054,(cAliasA)->CE8_IVANAJ})		// "Asociado a actividades que no cumple con requisitos / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0055,(cAliasA)->CE8_IVANAK})		// "Asociado a actividades exentas / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0056,(cAliasA)->CE8_IVANAL})		// "Asociado a actividades no objeto / Actos o actividades totales pagados a la tasa del 16% de IVA"
				AADD(aResumo,{STR0057,(cAliasA)->CE8_IVANAM})		// "Actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0058,(cAliasA)->CE8_IVANAN})		// "Asociado a actividades que no cumple con requisitos / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0059,(cAliasA)->CE8_IVANAO})		// "Asociado a actividades exentas / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0060,(cAliasA)->CE8_IVANAP})		// "Asociado a actividades no objeto / Actos o actividades pagados en la importación por aduana de bienes tangibles a la tasa del 16% de IVA"
				AADD(aResumo,{STR0061,(cAliasA)->CE8_IVANAQ})		// "Actividades por las cuales se aplicó una proporción / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% del IVA"
				AADD(aResumo,{STR0062,(cAliasA)->CE8_IVANAR})		// "Asociado a actividades que no cumple con requisitos / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% del IVA"
				AADD(aResumo,{STR0063,(cAliasA)->CE8_IVANAS})		// "Asociado a actividades exentas / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% del IVA"
				AADD(aResumo,{STR0064,(cAliasA)->CE8_IVANAT})		// "Asociado a actividades no objeto / Actos o actividades pagados en la importación de bienes intangibles y servicios a la tasa del 16% del IVA"
				AADD(aResumo,{STR0065,(cAliasA)->CE8_IVARET})		// "IVA retenido por el contribuyente"
				AADD(aResumo,{STR0066,(cAliasA)->CE8_BASIEX})		// "Actos o actividades pagados en la importación de bienes y servicios por los que no se pagara el IVA (Exentos)"
				AADD(aResumo,{STR0067,(cAliasA)->CE8_BASEEX})		// "Actos o actividades pagados por los que no se pagará el IVA (Exentos)"
				AADD(aResumo,{STR0068,(cAliasA)->CE8_BASE0})		// "Demás actos o actividades pagados a la tasa del 0% de IVA"
				AADD(aResumo,{STR0069,(cAliasA)->CE8_VALTOT})		// "Actos o actividades no objeto del IVA realizados en territorio nacional"
				AADD(aResumo,{STR0070,(cAliasA)->CE8_IVADEV})		// "Actos o actividades no objeto del IVA por no contar con establecimiento en territorio nacional"
			Else
				AADD(aResumo,{STR0010,(cAliasA)->CE8_BA1516}) // "Valor Total pagado a la tasa del 15% o 16% de IVA"
				AADD(aResumo,{STR0011,(cAliasA)->CE8_BASE15}) // "Valor Total pagado a la tasa del 15% de IVA"
				AADD(aResumo,{STR0012,(cAliasA)->CE8_BA1011}) // "Valor Total pagado a la tasa del 10% u 11% de IVA"
				AADD(aResumo,{STR0013,(cAliasA)->CE8_BASE10}) // "Valor Total pagado a la tasa del 10%  de IVA"
				AADD(aResumo,{STR0014,(cAliasA)->CE8_BASE8 }) // "Valor Total pagado de los actos o actividades sujeto al estímulo de la región fronteriza norte"
				AADD(aResumo,{STR0015,(cAliasA)->CE8_BI1516}) // "Valor Total pagado de importación de bienes y servicios a la tasa del 15% o 16% de IVA"
				AADD(aResumo,{STR0016,(cAliasA)->CE8_BI1011}) // "Valor Total pagado de importación de bienes y servicios a la tasa del 10% o 11% de IVA"
				AADD(aResumo,{STR0017,(cAliasA)->CE8_BASIEX}) // "Valor Total pagado de importación de bienes y servicios que no pagara IVA (Exentos)"
				AADD(aResumo,{STR0018,(cAliasA)->CE8_BASE0 }) // "Valor Total pagado a la tasa del 0% de IVA"
				AADD(aResumo,{STR0019,(cAliasA)->CE8_BASEEX}) //
				AADD(aResumo,{STR0020,(cAliasA)->CE8_IVARET}) // "IVA Total Retenido por el contribuyente"
				AADD(aResumo,{STR0021,(cAliasA)->CE8_IVADEV}) // "IVA Total Correspondiente a las devoluciones, descuentos y bonificaciones sobre compras"
				AADD(aResumo,{STR0022,(cAliasA)->CE8_IVAPAG}) // "IVA Total transferido (pagado) de bienes y servicios excepto importaciones"
				AADD(aResumo,{STR0023,(cAliasA)->CE8_IVPAGI}) // "IVA Total pagado en las importaciones de bienes y servicios"
			Endif

			// adiciona linha de total
			If cDiotVer == "1"
				self:jItems := JsonObject():new( )
				self:jItems[self:aStruct[nPosFilial][1]] := ( cAliasA )->&( self:aStruct[nPosFilial][5] )
				self:jItems[self:aStruct[nPosMesAno][1]] := ( cAliasA )->&( self:aStruct[nPosMesAno][5] )
				self:jItems[self:aStruct[nPosValor][1]]  := ( cAliasA )->VALOR
				self:jItems[self:aStruct[nPosDesc][1]]   := STR0024 // 'Total de operaciones que relaciona'
				self:oData:appendData( self:jItems )
			Endif

			// adiciona linhas de totais
			For nX := 1 To Len(aResumo)
				self:jItems := JsonObject():new( )
				self:jItems[self:aStruct[nPosFilial][1]] := ( cAliasA )->&( self:aStruct[nPosFilial][5] )
				self:jItems[self:aStruct[nPosMesAno][1]] := ( cAliasA )->&( self:aStruct[nPosMesAno][5] )
				self:jItems[self:aStruct[nPosValor][1]]  := aResumo[nX,2]
				self:jItems[self:aStruct[nPosDesc][1]]   :=aResumo[nX,1]
				self:oData:appendData( self:jItems )
			Next

		Else

			self:jItems := JsonObject():new( )
			self:nRecno := ( cAliasA )->REG

			For nX := 1 To Len( self:aStruct )
				If ( self:aStruct[nX][3] == 'date' )
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
				Else
					self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
				Endif
			Next

			// Inclui os dados no objeto paea retorno ao SmartView
			//self:processData()
			self:oData:appendData( self:jItems )

		Endif



		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class diotTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
