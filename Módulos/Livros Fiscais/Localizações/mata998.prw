#Include "MATA998.CH"
#Include "FIVEWIN.CH"
/*


Ŀ
Funcao     MATA998   Autor  Sergio S. Fuzinaka     Data  16.11.01 
Ĵ
Descricao  Programa de Atualizacao de Agentes Fiscais x Impostos (SFZ)
Ĵ
Sintaxe e  MATA998(ExpA1,ExpN1)                                       
Ĵ
Parametros ExpA1 - Array da Rotina Automatica                         
           ExpN1 - Numero da Opcao                                    
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL              
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Function MATA998(aRotAuto,nOpc)

If cPaisLoc <> "BRA"
	CriaSFZ(.T.)
EndIf

nOpc := If (nOpc == Nil,3,nOpc)

//Ŀ
// Define Variaveis                                             
//
Private aAC  := { STR0001, STR0002 }			//"Abandona"###"Confirma"

Private aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
Private cCadastro := OemtoAnsi(STR0008)         //"Atualizao de Agente Fiscal x Impostos"

//Ŀ
// Definicao de variaveis utilizadas na rotina automatica       
//
Private Inclui := .F.
Private Altera := .F.

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private l998Auto := ( aRotAuto <> NIL )

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If l998Auto
	dbSelectArea("SFZ")
	MsRotAuto(nOpc,aRotAuto,"SFZ")
Else
	dbSelectArea("SFZ")
	mBrowse( 6, 1,22,75,"SFZ")
EndIf

Return .T.

/*

Ŀ
Funcao    A998Inclui Autor  Sergio S. Fuzinaka     Data  16.11.01 
Ĵ
Descricao  Programa de Inclusao                                       
Ĵ
Sintaxe e  A998Inclui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA998()                                                  
ٱ


*/
Function A998Inclui(cAlias,nReg,nOpc)

//Ŀ
// Variaveis utilizadas na rotina automatica                    
//
Inclui := .T.
Altera := .F.

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	If ( l998Auto )
		nOpcA := AxIncluiAuto(cAlias,"A998TudoOk()")
	Else
		nOpcA:=AxInclui(cAlias,nReg,nOpc,,,,"A998TudoOk()")
	EndIf
	
	IF nOpcA == 1
		//Ŀ
		//Ponto de entrada depois da confirmacao                                
		//
	Endif
	
	dbSelectArea(cAlias)

Return

/*

Ŀ
Funcao    A998Altera Autor  Sergio S. Fuzinaka     Data  30.11.01 
Ĵ
Descricao  Programa de Alteracao                                      
Ĵ
Sintaxe e  A998Altera(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA998()                                                  
ٱ


*/
Function A998Altera(cAlias,nReg,nOpc)

Local nOpcA

l998Auto := If(Type("l998Auto") == "U", .F.,l998Auto)

//Ŀ
// Variaveis utilizadas na rotina automatica                    
//
Inclui := .F.
Altera := .T.

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	If ( l998Auto )
		nOpcA := AxIncluiAuto(cAlias,,,nOpc,SFZ->(RecNo()))
	Else
		nOpcA:=AxAltera(cAlias,nReg,nOpc,,,,,"A998TudoOk()")
	EndIf
	
	IF nOpcA == 1
		//Ŀ
		//Ponto de entrada depois da confirmacao                                
		//
	Endif

dbSelectArea(cAlias)

Return

/*

Ŀ
Funcao    A998TudoOk Autor Sergio S. Fuzinaka      Data  16.11.01 
Ĵ
Descricao Consistencia do registro                                    
Ĵ
Sintaxe   A998TudoOk()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso      MATA998()                                                   
ٱ


*/
Function A998TudoOk()

Local lRet := ExistChav("SFZ",M->FZ_TIPMOV+M->FZ_ESPECIE+M->FZ_TIPO+M->FZ_SERIE+M->FZ_IMPOSTO)
	
	If lRet 
		If Empty( M->FZ_IMPOSTO ) .Or. Empty( M->FZ_TIPO )
			HELP(" ",1,"OBRIGAT")		
			lRet := .F.
		Endif
	Endif
	
Return( lRet )

/*

Ŀ
Funcao    A998Deleta Autor  Sergio S. Fuzinaka     Data 16.11.01  
Ĵ
Descricao  Programa de exclusao                                       
Ĵ
Sintaxe e  A998Deleta(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA998()                                                  
ٱ


*/
Function A998Deleta(cAlias,nReg,nOpc)

Local nOpcA

l998Auto  := If (Type("l998Auto") =="U",.F.,l998Auto)

//Ŀ
// Monta tela de exclusao de dados do arquivo                   
//
Private aTELA[0][0], aGETS[0]

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	IF !SoftLock(cAlias)
		Return
	Endif
	
	If !( l998Auto )
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
		nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",OemToAnsi(STR0009) )//"Quanto a exclusao?"
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
	Else
		nOpcA := 2
	EndIf

	IF nOpcA == 2  

		//Ŀ
		// Exclusao do registro                                         
		//
		dbSelectArea(cAlias)
		RecLock(cAlias,.F.,.T.)
		dbDelete()

		//Ŀ
		// Ponto de entrada apos a confirmacao                          
		//

   ENDIF

   MsUnlock()
   
   dbSelectArea(cAlias)

Return

/*

Ŀ
Funcao    A998DesImp Autor  Sergio S. Fuzinaka     Data 16.11.01  
Ĵ
Descricao  Consiste o campo Descricao do Imposto ( FZ_DESIMPO )       
Ĵ
Sintaxe e  A998DesImp()                                               
Ĵ
Parametros                                                            
Ĵ
 Uso       MATA998()                                                  
ٱ


*/
Function A998DesImp()

Local aArea := GetArea()                   
Local lRet  := .T.

	If M->FZ_IMPOSTO == "***"
		M->FZ_DESIMPO := OemToAnsi(STR0010)
	ElseIf Empty(M->FZ_IMPOSTO)
		M->FZ_DESIMPO := ""	
	Else
		dbSelectArea("SFB")
		dbSetOrder(1)
		IF dbSeek(xFilial()+M->FZ_IMPOSTO)
			M->FZ_DESIMPO := SFB->FB_DESCR
		Else
			Help("",1,"REGNOIS")
			lRet := .F.	
		Endif
    Endif

RestArea( aArea )

Return( lRet )

/*/


Ŀ
Programa  MenuDef    Autor  Marco Bianchi          Data 01/09/2006
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados           
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
     
Private aRotina := {	{ STR0003, "AxPesqui"  , 0 , 1,0,.F.}   ,;	//"Pesquisar"
							{ STR0004, "AxVisual"  , 0 , 2,0,NIL}   ,;	//"Visualizar"
							{ STR0005, "A998Inclui", 0 , 3,0,NIL}   ,;	//"Incluir"
							{ STR0006, "A998Altera", 0 , 4,0,NIL}   ,;	//"Alterar"
							{ STR0007, "A998Deleta", 0 , 5,0,NIL}    }	//"Excluir"


If ExistBlock("MA998MNU")
	ExecBlock("MA998MNU",.F.,.F.)
EndIf

Return(aRotina)

