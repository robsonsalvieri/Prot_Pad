#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "FISA844.CH"

#DEFINE _SEPARADOR	CHR(09)
#DEFINE _BUFFER     16384

Static cMes      := StrZero(Month(dDataBase),2)
Static cMesIni	 := StrZero(Month(dDataBase),2)
Static cAnoIni	 := StrZero(Year(dDataBase),4)
Static lPer	     := .T.
Static cFile     := ""
Static dDataIni  := CTOD("\\")
Static dDataFim  := CTOD("\\")
Static lFor      := .F.
Static lCli      := .F.
Static lImp      := .F.
Static cAliasPdr := "PADRSCOEF"
Static cSA1      := ""
Static cSA2      := ""

/*/{Protheus.doc} FISA844
    Función utilizada para realizar la carga del Padrón de Coeficientes Unificados Salta, RG06-2005
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param cFileSel , character, Archivo a ser utilizado para carga del padrón (Uso para automatizados)
    @param cTipoSel, character, Selección del clientes o proveedores (Uso para automatizados) - Opción de aCombo
    @param cMesSel , character, Mes de carga del padrón (Uso para automatizados)
    @param cAnoSel , character, Año de carga del padrón (Uso para automatizados)
    @param lPerSel , logical  , Indica si el padrón realiza la actualización para percepciones (Uso para automatizados)
    
    /*/  

Function FISA844(cFileSel, cTipoSel, cMesSel, cAnoSel, lPerSel)

	Local cCombo	:= ""
	Local aCombo	:= {}
	Local oDlg		:= Nil
	Local oFld		:= Nil
	Local oCombo	:= Nil
	Local oChk1		:= Nil
	Local lAutomato := IsBlind()

	DEFAULT cFileSel	:= ""
	DEFAULT cTipoSel:= ""
	DEFAULT cMesSel	:= ""
	DEFAULT cAnoSel	:= ""
	DEFAULT lPerSel	:= .F.

	aAdd( aCombo, STR0002 ) //"1- Fornecedor"
	aAdd( aCombo, STR0003 ) //"2- Cliente"
	aAdd( aCombo, STR0004 ) //"3- Ambos  "

If !lAutomato
	DEFINE MSDIALOG oDlg TITLE STR0005 FROM 0,0 TO 260,455 OF oDlg PIXEL //"SALTA - RG 06-2005 – COEFICIENTES UNIFICADOS"

	@ 006,006 TO 040,175 LABEL STR0006 OF oDlg PIXEL //"Info. Preliminar"

	@ 011,010 SAY STR0007 SIZE 065,008 PIXEL OF oFld //"Registro"
	@ 025,010 COMBOBOX oCombo VAR cCombo ITEMS aCombo SIZE 65,8 PIXEL OF oFld

//+----------------------
//| Campos Check-Up
//+----------------------

	@ 10,115 SAY STR0008 SIZE 065,012 PIXEL OF oFld //"Importación de archivo TXT"
	@ 026,115 CHECKBOX oChk1 VAR lPer PROMPT STR0009 SIZE 40,8 PIXEL OF oFld WHEN .F. //"Percepción"


	@ 041,006 FOLDER oFld OF oDlg PROMPT STR0011 PIXEL SIZE 170,080 //"Importación de archivo TXT"

//+----------------
//| Campos Folder 2
//+----------------
	@ 005,005 SAY STR0010 SIZE 150,008 PIXEL OF oFld:aDialogs[1] //"Esta opción tiene como objetivo actualizar el registro"
	@ 015,005 SAY STR0012 SIZE 150,018 PIXEL OF oFld:aDialogs[1] //"Proveedor / Cliente vs. Coeficiente de reducción de la base de cálculo de la percepción del IB Fiscal , de acuerdo con el archivo TXT"
	@ 035,005 SAY STR0013 SIZE 150,008 PIXEL OF oFld:aDialogs[1] //"Puesto a disposición por el gobierno"

	@ 049,005 SAY STR0014  SIZE 150,008 PIXEL OF oFld:aDialogs[1] //"Informe o periodo:"
	@ 047,055 MSGET cMesIni PICTURE "@E 99" VALID !Empty(cMesIni) SIZE  015,008 PIXEL OF oFld:aDialogs[1]
	@ 049,070 SAY " /" SIZE  150, 8 PIXEL OF oFld:aDialogs[1]
	@ 047,075 MSGET cAnoIni PICTURE "@E 9999" VALID !Empty(cAnoIni) SIZE 020,008 PIXEL OF oFld:aDialogs[1]

//+-------------------
//| Boton de MSDialog
//+-------------------
	@ 070,182 BUTTON STR0015 SIZE 036,016 PIXEL ACTION ImpArq(aCombo,cCombo) //"&Importar"
	@ 090,182 BUTTON STR0017 SIZE 036,016 PIXEL ACTION oDlg:End() //"&Sair"

	ACTIVATE MSDIALOG oDlg CENTER
Else
	cMesIni := cMesSel
	cAnoIni := cAnoSel
	lPer	:= lPerSel
	cFile   := cFileSel
	ImpArq(aCombo, cTipoSel)
EndIF
	FwFreeObj(oDlg)
	FwFreeObj(oFld)
	FwFreeObj(oCombo)
	FwFreeObj(oChk1)
	aSize(aCombo,0)

Return Nil

/*/{Protheus.doc} ImpArq
    Inicializa a importacao do arquivo.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  aCombo   , array , opcoes do combo cliente/fornec.
    @param  cCombo   , character, opcao escolhida do combo
    
    /*/  

Static Function ImpArq(aCombo, cCombo)

	Local   nPos        := 0
	Local 	lAutomato   := IsBlind()

	Default aCombo		:= {}
	Default cCombo		:= ""

	cCombo := Substr(AllTrim(cCombo),1,1)
	nPos := aScan(aCombo,{|x| Substr(AllTrim(x),1,1) == cCombo})
	
	If nPos == 1 // Fornecedor
		lFor := .T.
	ElseIf nPos == 2 // Cliente
		lCli := .T.
	ElseIf nPos == 3 // Ambos
		lFor := .T.
		lCli := .T.
	EndIf

	
	If !lAutomato
		cFile := FGetFile()
	EndIf
	If Empty(cFile)
		MsgStop(STR0030) //"Seleccione un archivo e intente nuevamente."
		Return Nil
	EndIf

	If !File(cFile)
		MsgStop(STR0030) //"Seleccione un archivo e intente nuevamente."
		Return Nil
	EndIf
	If VldArch()
		If !lAutomato
			MsAguarde({|| Import(cFile)} ,STR0018,STR0019 ,.T.) //"Lendo Arquivo, Aguarde..."###"Atualizacao de coeficiente"
		Else
			Import(cFile)
		EndIf
	EndIf

	If (lImp ) .and. !lAutomato
		MsgAlert(STR0024,"") //"Registro importado!"
	EndIf
Return Nil

/*/{Protheus.doc} VldArch
    Validacion si el archivo TXT de importación tienes los espacios en blanco
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  aCombo   , array , opcoes do combo cliente/fornec.
    @param  cCombo   , character, opcao escolhida do combo
    @param  cFileAuto, character, archivo a ser importado
    
    @return logical, .F. si genera error
    /*/  

Static Function VldArch()

	Local cErro		:= STR0028 //"Ocurrió un error al procesar un registro seleccionado. Verifique si el contenido está correcto e intente nuevamente."
	Local cSolucao 	:= STR0030 //"Seleccione un registro e intente nuevamente."
	Local cTitulo	:= STR0001 //"Atención"
	Local oFile		:= Nil

	oFile := FWFileReader():New(cFile)
	if (oFile:Open())
		If oFile:hasLine()
			If !(_SEPARADOR $ (oFile:GetLine()))
				xMagHelpFis(cTitulo,cErro,cSolucao)
				Return .F.
			EndIf
		EndIf
	EndIf
	dDataIni := CTOD("01/"+cMesIni+"/"+cAnoIni)
	dDataFim := LastDay(dDataIni)
	oFile:Close()
	FwFreeObj(oFile)

Return .T.

/*/{Protheus.doc} FGetFile
    Tela de seleção do arquivo txt a ser importado.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    
    @return character, Diretori e arquivo selecionado.
    /*/  

Static Function FGetFile()
	Local oDlg01 := Nil
	Local oGet01 := Nil
	Local oBtn01 := Nil
	Local oBtn02 := Nil
	Local oBtn03 := Nil
	Local cRet := Space(50)

	oDlg01 := MSDialog():New(000,000,100,500,STR0026,,,,,,,,,.T.)//"Selecionar arquivo"

	oGet01 := TGet():New(010,010,{|u| If(PCount()>0,cRet:=u,cRet)},oDlg01,215,10,,,,,,,,.T.,,,,,,,,,,"cRet")
	oBtn01 := TBtnBmp2():New(017,458,025,028,"folder6","folder6",,,{|| FGetDir(oGet01)},oDlg01,STR0026,,.T.)//"Selecionar arquivo"

	oBtn02 := SButton():New(035,185,1,{|| oDlg01:End() }         ,oDlg01,.T.,,)
	oBtn03 := SButton():New(035,215,2,{|| cRet:="",oDlg01:End() },oDlg01,.T.,,)

	oDlg01:Activate(,,,.T.,,,)

	FwFreeObj(oDlg01)
	FwFreeObj(oGet01)
	FwFreeObj(oBtn01)
	FwFreeObj(oBtn02)
	FwFreeObj(oBtn03)

Return cRet

/*/{Protheus.doc} FGetDir
    Tela para procurar e selecionar o arquivo nos diretorios
    locais/servidor/unidades mapeadas.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  oTGet, object, TGet que ira receber o local e o arquivo
    
    /*/  
Static Function FGetDir(oTGet)

	Local cDir := ""

	DEFAULT oTGet := Nil

	cDir := cGetFile(,STR0026,,,.T.,GETF_LOCALFLOPPY+GETF_LOCALHARD+GETF_NETWORKDRIVE)//"Selecionar arquivo"
	If !Empty(cDir)
		oTGet:cText := cDir
		oTGet:Refresh()
	Endif
	oTGet:SetFocus()

Return Nil

/*/{Protheus.doc} Import
    Función para realizar la importación del archivo.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cFile, character, archivo a ser importado
    
    @return logical, Indica si completo el proceso.
    /*/  

Static Function Import(cFile)

	Local lReturn   	:= .T.
	Local lAutomato 	:= IsBlind()

	DEFAULT cFile		:= ""

	If !lAutomato
		Processa({|| lReturn := GeraTemp(cFile)})// cualquier base de datos
	Else
		lReturn := GeraTemp(cFile)
	EndIf

Return lReturn

/*/{Protheus.doc} ProvPer
    Función para realizar la busca, verificaciones y actualizacion de los registros en la tabla SFH para proveedores.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  lPadron , logical  , indica si el CUIT de la empresa protheus existe en el archivo TXT de importación.
    @param  nCoefMul, numeric, valor del coeficiente unificado
    
    /*/  

Static Function ProvPer(lPadron,nCoefMul)
	Local lSfh	:= .F.
	Local lGenera := .F.
	Local cChave := ""
	Local nRecFim := 0

	DEFAULT lPadron := .F.
	DEFAULT nCoefMul := 0

	If lFor .and. lPer
		(cSA2)->(dbGoTop())
		If lPadron
			Do While (cSA2)->(!EOF())
				SFH->(dbGoTop())
				cChave := xFilial("SFH")+(cSA2)->A2_COD+(cSA2)->A2_LOJA+"IBI"+"SA"
				If SFH->(MsSeek(cChave))
					nRecFim := MayorFech((cSA2)->A2_COD,(cSA2)->A2_LOJA,"IBI",.F.)
					SFH->(DbGoTo(nRecFim))
					If nRecfim >0
						lSfh := .T.
						If nCoefMul <> SFH->FH_COEFMUL
							lGenera  := .T.
						EndIf
					EndIf
				EndIf
				If lSfh
					If !lGenera
						RecLock("SFH", .F.)
						SFH->FH_FIMVIGE := dDataFim
						SFH->(MsUnlock())
					Else
						RecLock("SFH", .F.)
						SFH->FH_FIMVIGE := DaySub(dDataIni,1)
						SFH->(MsUnlock())
						ActSFH(SFH->FH_AGENTE,SFH->FH_FORNECE,SFH->FH_TIPO,SFH->FH_LOJA,SFH->FH_PERCIBI,SFH->FH_APERIB,SFH->FH_ZONFIS,SFH->FH_IMPOSTO,SFH->FH_ALIQ,SFH->FH_PERCENT,nCoefMul,"SA2",(cSA2)->A2_NOME)
					EndIf
				EndIf
				lGenera     := .F.
				lSfh 		:= .F.
				(cSA2)->(dbSkip())
			EndDo
		Else // no padron
			Do While (cSA2)->(!EOF())
				SFH->(dbGoTop())
				cChave := xFilial("SFH")+(cSA2)->A2_COD+(cSA2)->A2_LOJA+"IBI"+"SA"
				If SFH->(MsSeek(cChave))
					nRecFim := MayorFech((cSA2)->A2_COD,(cSA2)->A2_LOJA,"IBI",.F.)
					SFH->(DbGoTo(nRecFim))
					If nRecfim >0
						If  SFH->FH_FIMVIGE > dDataIni .Or. Empty(SFH->FH_FIMVIGE)
							RecLock("SFH", .F.)
							SFH->FH_FIMVIGE := dDataIni
							SFH->(MsUnlock())
						EndIf
					EndIf
				EndIf
				(cSA2)->(dbSkip())
			EndDo
		EndIf // fin cuit empresa
		lGenera     := .F.
		lSfh 		:= .F.
		lImp := .T.
	EndIf // fin prove

Return Nil

/*/{Protheus.doc} CliPer
    Función para realizar la busca, verificaciones y actualizacion de los registros en la tabla SFH para clientes.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  lPadron , logical  , indica si el CUIT de lo Cliente existe en el archivo TXT de importación.
    @param  nCoefMul, numeric  , valor del coeficiente unificado
    
    /*/  

Static Function CliPer(lPadron,nCoefMul)
	Local lSfh := .F.
	Local lGenera := .F.
	Local cChave := ""
	Local nRecFim := 0

	DEFAULT lPadron := .F.
	DEFAULT nCoefMul := 0

	If lCli .and. lPer
		If lPadron
			SFH->(dbGoTop())
			cChave := xFilial("SFH")+(cSA1)->A1_COD+(cSA1)->A1_LOJA+"IBI"+"SA"
			If SFH->(MsSeek(cChave))
				nRecFim :=  MayorFech((cSA1)->A1_COD,(cSA1)->A1_LOJA,"IBI",.T.)
				SFH->(DbGoTo(nRecFim))
				If nRecFim >0
					lSfh := .T.
					If nCoefMul <> SFH->FH_COEFMUL
						lGenera  := .T.
					EndIf
				EndIf
			EndIf
			If lSfh
				If !lGenera
					RecLock("SFH", .F.)
					SFH->FH_FIMVIGE := dDataFim
					SFH->(MsUnlock())
				Else
					RecLock("SFH", .F.)
					SFH->FH_FIMVIGE := DaySub(dDataIni,1)
					SFH->(MsUnlock())
					ActSFH(SFH->FH_AGENTE,SFH->FH_CLIENTE,SFH->FH_TIPO,SFH->FH_LOJA,SFH->FH_PERCIBI,SFH->FH_APERIB,SFH->FH_ZONFIS,SFH->FH_IMPOSTO,SFH->FH_ALIQ,SFH->FH_PERCENT,nCoefMul,"SA1",(cSA1)->A1_NOME)
				EndIf
			Else// no esta en SFH
				ActSFH("N",(cSA1)->A1_COD,"V",(cSA1)->A1_LOJA,"S","S","SA","IBI",0,0,nCoefMul,"SA1",(cSA1)->A1_NOME)
			EndIf
		Else //no Padron
			SFH->(dbGoTop())
			cChave := xFilial("SFH")+(cSA1)->A1_COD+(cSA1)->A1_LOJA+"IBI"+"SA"
			If SFH->(MsSeek(cChave))
				nRecFim :=  MayorFech((cSA1)->A1_COD,(cSA1)->A1_LOJA,"IBI",.T.)
				SFH->(DbGoTo(nRecFim))
				If nRecFim >0
					If SFH->FH_FIMVIGE > dDataIni .Or. Empty(SFH->FH_FIMVIGE)
						RecLock("SFH", .F.)
						SFH->FH_FIMVIGE := dDataIni
						SFH->(MsUnlock())
					EndIf
				EndIf
			EndIf
		EndIf // fin Padron
		lGenera := .F.
		lImp := .T.
	EndIf // fin cli & per

Return Nil

/*/{Protheus.doc} ActSFH
    Función para realizar la creacion de nuevos registros en la tabla SFH.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cAgente , character, indica si es agente.
    @param  cCOD    , character, codigo del proveedor o cliente.
	@param  cTipo   , character, indica el tipo.
    @param  cLoja   , character, indica la surcursal.
	@param  cPercIBI, character, indica si paga impuesto en esta zona.
    @param  cAPERIB , character, indica si paga IB.
	@param  cZonaFis, character, indica la zona.
    @param  cImpost , character, indica el impuesto.
	@param  nAliq   , numeric, indica el valor de la alicuota.
    @param  nPercent, numeric, incida la porcentaje de exencion
    @param  nCoefMul, numeric, indica el valor del coeficiente de reduc.
    @param  cTable  , character, indica la tabla si es A1 o A2, cliente o proveedor.
    @param  cNome   , character, indica el nombre del proveedor o cliente.
    
    /*/  

Static Function ActSFH(cAgente,cCOD,cTipo,cLoja,cPercIBI,cAPERIB,cZonaFis,cImpost,nAliq,nPercent,nCoefMul,cTable,cNome)
	DEFAULT cAgente := ""
	DEFAULT cCOD    := ""
	DEFAULT cTipo   := ""
	DEFAULT cLoja   := ""
	DEFAULT cPercIBI := ""
	DEFAULT cAPERIB  := ""
	DEFAULT cZonaFis := ""
	DEFAULT cImpost  := ""
	DEFAULT nAliq    := 0
	DEFAULT nPercent := 0
	DEFAULT nCoefMul := 0
	DEFAULT cTable   := ""
	DEFAULT cNome    := ""

	If RecLock("SFH", .T.)
		SFH->FH_FILIAL		:=  xFilial("SFH")
		SFH->FH_AGENTE		:= cAgente
		SFH->FH_ZONFIS		:= cZonaFis
		If cTable == "SA2"
			SFH->FH_FORNECE := cCOD
		ElseIf cTable == "SA1"
			SFH->FH_CLIENTE := cCOD
		EndIf
		SFH->FH_LOJA		:= cLoja
		SFH->FH_NOME		:= cNome
		SFH->FH_IMPOSTO		:= cImpost
		SFH->FH_PERCIBI		:= cPercIBI
		SFH->FH_ISENTO		:= "N"
		SFH->FH_APERIB		:= cAPERIB
		SFH->FH_ALIQ		:= nAliq
		SFH->FH_PERCENT		:= nPercent
		SFH->FH_TIPO    	:= cTipo
		SFH->FH_INIVIGE 	:= dDataIni
		SFH->FH_FIMVIGE 	:= dDataFim
		SFH->FH_COEFMUL 	:= nCoefMul
		SFH->(MsUnlock())
	EndIf

Return Nil

/*/{Protheus.doc} MayorFech
    Función para realizar la busca del registro en la tabla SFH de mayor fecha fin vigencia.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cCod     , character, codigo del proveedor o cliente.
    @param  cLoja    , character, indica la surcursal.
	@param  cImpuesto, character, indica el impuesto.
	@param  lTabla   , logical  , indica si la busca es para proveedor o cliente.
    
    @return            numeric  , retorna el recno del registro con mayor fecha fin vigencia.
    /*/  

Static Function MayorFech(cCod,cLoja,cImpuesto,lTabla)

	Local cQuery      := ""
	Local oQryExec    := Nil
	Local nParam      := 1
	Local cTMayor     := ""
	Local cFecAnt     := ""
	Local nAux        :=0
	Local nAuxIni     :=0
	Local lFecVacia   := .F.
	Local nAuxVacia   :=0

	DEFAULT cCod      := ""
	DEFAULT cLoja     := ""
	DEFAULT cImpuesto := ""
	DEFAULT lTabla    := .F.

	cQuery := ""
	cQuery := "SELECT  FH_FIMVIGE AS FECHA,R_E_C_N_O_ AS NUM,FH_INIVIGE AS INI"
	cQuery += " FROM                     ? "
	cQuery += "  WHERE FH_FILIAL       = ? "
	If lTabla
		cQuery += "    AND FH_CLIENTE  = ? "
	Else
		cQuery += "    AND FH_FORNECE  = ? "
	EndIf
	cQuery += "    AND FH_LOJA         = ? "
	cQuery += "    AND FH_IMPOSTO      = ? "
	cQuery += "    AND FH_ZONFIS       = ? "
	cQuery += "    AND D_E_L_E_T_      = ? "
	cQuery := ChangeQuery(cQuery)

	oQryExec := FwExecStatement():New(cQuery)
	oQryExec:SetUnsafe(nParam++, RetSqlName("SFH"))
	oQryExec:SetString(nParam++, xFilial("SFH"))
	oQryExec:SetString(nParam++, cCod)
	oQryExec:SetString(nParam++, cLoja)
	oQryExec:SetString(nParam++, cImpuesto)
	oQryExec:SetString(nParam++, "SA")
	oQryExec:SetString(nParam++, Space(1))
	cTMayor := oQryExec:OpenAlias()
	(cTMayor)->(dbGoTop())
	Do While (cTMayor)->(!EOF())
		If Empty((cTMayor)->FECHA)
			lFecVacia := .T.
			nAuxVacia := (cTMayor)->NUM
		EndIf
		If (cTMayor)->FECHA > cFecAnt
			nAux := (cTMayor)->NUM
			cFecAnt := (cTMayor)->FECHA
		EndIf

		If(DTOS(dDataIni) == (cTMayor)->INI)
			nAuxIni :=(cTMayor)->NUM
		EndIf
		(cTMayor)->(dbSkip())
	EndDo
	If(nAuxIni<>0)
		nAux :=nAuxIni
	ElseIf lFecVacia
		nAux := nAuxVacia
	EndIf
	(cTMayor)->(dbCloseArea())
	oQryExec:Destroy()
	oQryExec:=Nil
Return nAux

/*/{Protheus.doc} GeraTemp
    Función para realizar la busca de todos los proveedores y clientes para verificar si existe en el archivo TXT de importación.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cArqProc , character, archivo a ser importado
    
    @return            logical  , retorna .T. si la importación no tuve errores.
    /*/  

Static Function GeraTemp(cArqProc)
	
	Local cErro	     := ""		// Texto de mensagem de erro ocorrido na validacao do arquivo a ser importado
	Local cSolucao   := ""		// Texto de solucao proposta em relacao a algum erro ocorrido na validacao do arquivo a ser importado
	Local lArqValido := .T.		// Determina se o arquivo XLS esta ok para importacao
	Local cTitulo	 := STR0001  //"Problemas en la importación del archivo"
	Local cQuery	 := ""
	Local cCGCEmp	 := FWArrFilAtu()[18] // SM0->M0_CGC
	Local lReturn    := .T.
	Local nParamA1   := 1
	Local nParamA2   := 1
	Local oQryExecA1 := Nil
	Local oQryExecA2 := Nil

	Default cArqProc := ""

	If lCli .and. lPer
		cQuery := ""
		cQuery := "SELECT A1.A1_COD, A1.A1_LOJA, A1.A1_CGC, A1.A1_NOME"
		cQuery += "FROM                         ? A1 "
		cQuery += " WHERE A1.A1_FILIAL        = ?    "
		cQuery += "   AND A1.A1_CGC          <> ?    "
		cQuery += "   AND A1.D_E_L_E_T_       = ?    "
		cQuery += " ORDER BY A1.A1_CGC  ASC          "
		cQuery := ChangeQuery(cQuery)
		oQryExecA1 := FwExecStatement():New(cQuery)
		oQryExecA1:SetUnsafe(nParamA1++, RetSqlName("SA1"))
		oQryExecA1:SetString(nParamA1++, xFilial("SA1"))
		oQryExecA1:SetString(nParamA1++, Space(1))
		oQryExecA1:SetString(nParamA1++, Space(1))
		cSA1 := oQryExecA1:OpenAlias()
		(cSA1)->(dbGoTop())
	EndIf

	If lFor .and. lPer
		cQuery := ""
		cQuery := "SELECT A2.A2_COD, A2.A2_LOJA, A2.A2_CGC, A2.A2_NOME"
		cQuery += "FROM                           ? A2 "
		cQuery += " WHERE A2.A2_FILIAL          = ?    "
		cQuery += "   AND A2.A2_CGC            <> ?    "
		cQuery += "   AND A2.D_E_L_E_T_         = ?    "
		cQuery := ChangeQuery(cQuery)
		oQryExecA2 := FwExecStatement():New(cQuery)
		oQryExecA2:SetUnsafe(nParamA2++, RetSqlName("SA2"))
		oQryExecA2:SetString(nParamA2++, xFilial("SA2"))
		oQryExecA2:SetString(nParamA2++, Space(1))
		oQryExecA2:SetString(nParamA2++, Space(1))
		cSA2 := oQryExecA2:OpenAlias()
		(cSA2)->(dbGoTop())
	EndIf
	If File(cArqProc)
		lArqValido := CreaTabla(cArqProc)
		If lArqValido
			dbSelectArea("SFH")
			If lFor .and. lPer
				SFH->(dbSetOrder(1)) //FH_FILIAL+FH_FORNECE+FH_LOJA+FH_IMPOSTO+FH_ZONFIS
				If (cAliasPdr)->(MsSeek(cCGCEmp))
					ProvPer(.T.,(cAliasPdr)->COEFMUL)
				Else
					ProvPer(.F.,0)
				EndIf
			EndIf
			If lCli .and. lPer
				SFH->(dbSetOrder(3)) //FH_FILIAL+FH_CLIENTE+FH_LOJA+FH_IMPOSTO+FH_ZONFIS
				While (cSA1)->(!EOF())
					If (cAliasPdr)->(MsSeek((cSA1)->A1_CGC))
						CliPer(.T.,(cAliasPdr)->COEFMUL)
					Else
						CliPer(.F.,0)
					EndIf
					(cSA1)->(dbSkip())
				Enddo
			EndIf
			SFH->(dbCloseArea())
			(cAliasPdr)->(dbCloseArea())
		Else
			cErro	   := STR0022 + cArqProc + STR0023	//"El registro" +cArqProc+ "no puede abrirse."
			cSolucao   := STR0030 						//"Seleccione un registro e intente nuevamente."
		EndIf
		If lCli .and. lPer
			(cSA1)->(dbCloseArea())
			oQryExecA1:Destroy()
			oQryExecA1:=Nil
		EndIf
		If lFor .and. lPer
			(cSA2)->(dbCloseArea())
			oQryExecA2:Destroy()
			oQryExecA2:=Nil
		EndIf
		If !Empty(cErro)
			xMagHelpFis(cTitulo,cErro,cSolucao)
			lReturn := .F.
		Endif
	EndIf
Return(lReturn)

/*/{Protheus.doc} CreaTabla
    Función para realizar la geracion de la tabla temporaria PADRSCOEF con todos los datos del archivo TXT de importación.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cArqProc, character, archivo a ser importado
    
    @return           logical  , retorna .T. si la importación no tuve errores.
    /*/  

Static Function CreaTabla(cArqProc)
	Local aInforma    := {} // Array auxiliar com as informacoes da linha lida no arquivo XLS
	Local cMsg        := STR0019 //"Atualização de coeficientes"
	Local aStruct     := {}
	Local lRetu       := .T.
	Local nTotArq     := 0
	Local nProcArq    := 0
	Local nPerProc    := 0
	Local oFile       := Nil
	Local nTamCoef    := TAMSX3('FH_COEFMUL')[1]
	Local nDecCoef    := TAMSX3('FH_COEFMUL')[2]
	Local oBulk       := Nil
	Local lCanUseBulk := .F.
	Local cIndex      := ""

	DEFAULT cArqProc := ""

	aAdd( aStruct, { 'CUIT'     , 'C', 20	,0 } )
	aAdd( aStruct, { 'DENOM'    , 'C', 100	,0 } )
	aAdd( aStruct, { 'COEFMUL'  , 'N', nTamCoef	,nDecCoef } )
	aAdd( aStruct, { 'PERIODO'  , 'C', 8	,0 } )
	
		If Select(cAliasPdr) <> 0
			(cAliasPdr)->(dbCloseArea())
		EndIf
		If TCCanOpen(cAliasPdr)
			If !TCDelFile(cAliasPdr)
				MsgAlert( "DROP table error PADRSCOEF " + TCSqlError() )
				lRetu := .F.
			EndIf
		EndIf

		FWDBCreate(cAliasPdr, aStruct , 'TOPCONN' , .T.)
		If (ArchEstand(cArqProc))
			Return .F.
		EndIf
		oFile := FWFileReader():New(cArqProc)
		oFile:Open()
		nTotArq := oFile:getFileSize()
		oBulk := FwBulk():New(cAliasPdr,600)
		lCanUseBulk := FwBulk():CanBulk() // Este método não depende da classe FWBulk ser inicializada por NEW
		If lCanUseBulk
			oBulk:SetFields(aStruct)
			ProcRegua(100)
			while (oFile:hasLine())
				aInforma := StrTokArr2(oFile:GetLine(),_SEPARADOR)
				oBulk:AddData({aInforma[1],Substr(aInforma[2],1,100),Round(Val(aInforma[3]),nDecCoef),aInforma[4]})
				aSize(aInforma,0)
				nProcArq := oFile:getBytesRead()
				nProcArq := NoRound((nProcArq*100)/nTotArq,0)
				If nProcArq > nPerProc
					nPerProc := nProcArq
					IncProc(cMsg + str(nProcArq) + "%")
				EndIf
			EndDo
			oBulk:Flush()
			oBulk:Close()
			oBulk:Destroy()
			oBulk := nil
		EndIf
		
		If Select(cAliasPdr) == 0
			DbUseArea(.T.,"TOPCONN",cAliasPdr,cAliasPdr,.T.)
			cIndex := cAliasPdr+"1"
			If ( !MsFile(cAliasPdr,cIndex, "TOPCONN") )
				DbCreateIndex(cIndex,"CUIT",{|| "CUIT" })
			EndIf
			Set Index to (cIndex)
		EndIf
		oFile:Close()	 // Fecha o Arquivo
		FwFreeObj(oFile)
		aSize(aInforma,0)
		aSize(aStruct,0)

Return lRetu

/*/{Protheus.doc} ArchEstand
    Función para realizar la validacion del archivo TXT de importacion para garantizar 
    que es el archivo TXT de importacion correcto del padrón de coeficientes RG 06-2005.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cArqProc, character, archivo a ser importado
    
    @return            logical , retorna .T. si ocurrio error ao abrir el archivo o el achivo TXT no es el padrón correcto.
    /*/  

Static Function ArchEstand(cArqProc)
	Local oFile := Nil
	Local lRet := .F.

	DEFAULT cArqProc := ""

	oFile := FWFileReader():New(cArqProc)
	oFile:Open()
	If Len(StrTokArr2(oFile:GetLine(),_SEPARADOR)) <> 4 .Or. !(QtdDecimais(StrTokArr2(oFile:GetLine(),_SEPARADOR)[3]) == 4)
		MsgStop(STR0031) //"Archivo fuera del estándar, ¡verifique si es el archivo correcto!"
		lRet := .T.
	EndIf

	oFile:Close()	 // Fecha o Arquivo
	FwFreeObj(oFile)

Return lRet

/*/{Protheus.doc} QtdDecimais
    Función para realizar la validacion se el campo de coeficiente en el archivo TXT de importacion tienes 4 decimales
    para garantizar que es el archivo TXT de importacion correcto del padrón de coeficientes RG 06-2005.
    @type  Function
    @author pedro.candido
    @since 20/07/2025
    @param  cValor      , character, coeficiente del archivo TXT de importación.
    
    @return numeric     , retorna a cuantidad de decimales.
    /*/  

Static Function QtdDecimais(cValor)
	Local nPosVirgula := 0
	Local nQtdDecimais := 0

	Default cValor := ""

	nPosVirgula := At(".", cValor)

	If nPosVirgula > 0
		nQtdDecimais := Len(cValor) - nPosVirgula
	EndIf
Return nQtdDecimais
