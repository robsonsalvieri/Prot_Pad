#INCLUDE "MATA055.CH"
#INCLUDE "PROTHEUS.CH"
/*


Ŀ
Funo     MATA055   Autor  Bruno Sobieski         Data 25/10/2002
Ĵ
Descrio  Programa de atualizacao de Solictacao de NCP               
Ĵ
Sintaxe    Void MATA055(void)                                         
ٱ


*/
Function MATA055(aRotAuto,nOpc)
Local aCores    := {	{ 'Empty(CU_NCRED)'	, 'ENABLE'    },;// Pendente
						{'!Empty(CU_NCRED)'	, 'DISABLE'   }} // Fechada  

If !GetMv('MV_SOLNCP') 
	Help(" ",1,"MA055005")
	Return
Endif	

nOpc := If (nOpc == Nil,3,nOpc)
//Ŀ
// Define Variaveis                                             
//

//Ŀ
// Define Array contendo as Rotinas a executar do programa      
// ----------- Elementos contidos por dimensao ------------     
// 1. Nome a aparecer no cabecalho                              
// 2. Nome da Rotina associada                                  
// 3. Usado pela rotina                                         
// 4. Tipo de Transao a ser efetuada                          
//    1 - Pesquisa e Posiciona em um Banco de Dados             
//    2 - Simplesmente Mostra os Campos                         
//    3 - Inclui registros no Bancos de Dados                   
//    4 - Altera o registro corrente                            
//    5 - Remove o registro corrente do Banco de Dados          
//
PRIVATE aRotina := { 	{ OemToAnsi(STR0003) ,"AxPesqui", 0 , 1},;         //"Pesquisar"
						{ OemtoAnsi(STR0004) ,"AxVisual", 0 , 2},;			//"Visualizar"
						{ OemtoAnsi(STR0005) ,"A055Inclui", 0 , 3},;		//"Incluir"
						{ OemtoAnsi(STR0006) ,"A055Amarra", 0 , 4, 2},;	//"Amarrar"
						{ OemtoAnsi(STR0007) ,"A055Deleta", 0 , 5, 1},;	//"Excluir"
						{ OemToAnsi(STR0011) ,"A055Legenda", 0, 2} } 		//"Legenda"

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemtoAnsi(STR0008)         //"Solicitacao de Notas de credito"
PRIVATE INCLUI    := .F.
PRIVATE ALTERA    := .F.
//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
PRIVATE l055Auto := ( aRotAuto <> NIL )

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If l055Auto
	dbSelectArea("SCU")
	MsRotAuto(nOpc,aRotAuto,"SCU")
Else
	dbSelectArea("SCU")
	mBrowse( 6, 1,22,75,"SCU",,,,,,aCores)
EndIf

//Ŀ
// Desativa a tecla F4 neste momento por seguranca              
//
If !l055Auto
	SET KEY VK_F4 TO
EndIF
Return .T.

/*

Ŀ
Funo    A055Deleta Autor  Bruno Sobieski         Data           
Ĵ
Descrio  Programa de exclusao de Solicitacao de notas de credito    
                                                                      
Ĵ
Sintaxe e  Void A055Deleta(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       Generico                                                   
ٱ


*/
Function A055Deleta(cAlias,nReg,nOpc)
l055Auto  := If (Type("l055Auto") =="U",.F.,l055Auto)
//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]
bCampo := {|nCPO| Field(nCPO) }

//Ŀ
// Envia para processamento dos Gets          
//
nOpcA:=0
If !Empty(CU_DTBAIXA)
	Help(" ",1,"MA055001")
	Return
Endif			

IF !SoftLock(cAlias)
	Return
Endif
If !( l055Auto )
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 9,0 TO 28,80 OF oMainWnd
	nOpcA:=EnChoice( cAlias, nReg, nOpc,,,)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
Else
	nOpca := 2
EndIf
DbSelectArea(cAlias)

IF nOpcA == 2   
	//Ŀ
	// Antes de deletar eu vou verificar se existe movimentacao     
	//
	
	Begin Transaction
	//Ŀ
	// apos passar por todas as verificacoes , deleta o registro    
	//
	dbSelectArea(cAlias)
	RecLock(cAlias,.F.,.T.)
	dbDelete()
	End Transaction
ELSE
	MsUnLock()
ENDIF
dbSelectArea(cAlias)

Return

/*

Ŀ
Funo    A055Inclui Autor  Bruno Sobieski         Data  25/10/02 
Ĵ
Descrio  Programa de Inclusao    de solicitacoes de nota de credito 
                                                                      
Ĵ
Sintaxe e  Void A055Inclui(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       MATA055()                                                  
ٱ


*/
Function A055Inclui(cAlias,nReg,nOpc)
Local aButtons	:=	{}
PRIVATE cSDocIDF1:= ""

//Ŀ
// Ativa tecla F4 para comunicacao com Notas fiscais            
//
If !l055Auto
	Set Key VK_F4 TO A055Tipo1()
EndIf

Aadd(aButtons,{"PEDIDO",{|| SF12SCU(1)},OemToAnsi(STR0009),OemToAnsi(STR0016)}) //"Notas fiscais de entrada <F4>"
if AxInclui(cAlias,nReg,nOpc,,,,'A055Tudook(1)',.T.,,aButtons) == 1
	Reclock(cAlias,.F.)
		SerieNfId ("SCU",1,"CU_SERNF",,,,cSDocIDF1)		
	MsUnLock()
Endif


Set Key VK_F4 TO

dbSelectArea(cAlias)

Return
/*

Ŀ
Funo    A055AMarra Autor  Bruno Sobieski         Data  25/10/02 
Ĵ
Descrio  Programa de Atualizacao de solicitacoes de nota de credito 
                                                                      
Ĵ
Sintaxe e  Void A055Amarra(ExpC1,ExpN1)                               
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
Ĵ
 Uso       MATA055()                                                  
ٱ


*/
Function A055AMARRA(cAlias,nReg,nOpc)
Local aCpos		:=	{'CU_NCRED','CU_SERNCP'}
Local aButtons	:=	{}
PRIVATE cSDocIDF2:= ""

Aadd(aButtons,{"RECALC",{|| SF12SCU(2)},OemToAnsi(STR0010),OemToAnsi(STR0015)}) //"Notas de credito <F4>"
If Empty(CU_DTAMARR)
	Set Key VK_F4 TO A055Tipo2()
	If AxAltera(cAlias,nReg,nOpc,,aCpos,,,'A055Tudook(2)',,,aButtons) == 1
		Reclock(cAlias,.F.)
			Replace CU_DTAMARR	With	dDataBase
			SerieNfId ("SCU",1,"CU_SERNCP",,,,cSDocIDF2)		
		MsUnLock()
	Endif
Else
	Help(" ",1,"MA055004")
Endif

Set Key VK_F4 TO 
dbSelectArea(cAlias)


Return
/*

Ŀ
Funo    A055TudoOk Autor  Bruno Sobieski         Data  25/10/02 
Ĵ
Descrio  Valida a inclusao e amarracao das solicitacoes             
                                                                      
Ĵ
Sintaxe e  Void A055TudoOk(ExpN1)                                     
Ĵ
Parametros ExpN1 = 1->Inclusao                                        
                   2->Amarracao                                       
Ĵ
 Uso       MATA055()                                                  
ٱ


*/
Function A055TudoOk(nTipo)
Local lRet	:=	.F.
Local lExisteNCP := .F.
Local aArea	:=	GetArea()
Local cHelp	:=	""

If nTipo == 1
	cSDocIDF1:=SerieNFID("SCU",4, "CU_SERNF",M->CU_DTNF,"NF   ",M->CU_SERNF,)
	cHelp	:=	"MA055002"
	DbSelectArea('SF1')
	DbSetOrder(1)	
	If MsSeek(xFilial()+M->CU_NFISCAL+cSDocIDF1+M->CU_FORNECE+M->CU_LOJA+'N')//Tiago Silva , campo M->CU_SERNF substituido pela id da serie cSDocIDF1, projeto chave unica.
		lRet	:=	.T.
	Endif	
	M->CU_NCRED		:=	""
	M->CU_SERNCP	:=	""
Else
	cSDocIDF2:=SerieNFID("SCU",4, "CU_SERNCP",M->CU_DTNCP,"NCP  ",M->CU_SERNCP,)
	cHelp	:=	"MA055003"
	DbSelectArea('SF2')
	DbSetOrder(1)	
	MsSeek(xFilial()+M->CU_NCRED+cSDocIDF2+M->CU_FORNECE+M->CU_LOJA)//Tiago Silva campo M->CU_SERNCP substituido pela id da serie cSDocIDF2, projeto chave unica.
	While !EOF().And. !lRet .And. xFilial()+M->CU_NCRED+cSDocIDF2+M->CU_FORNECE+M->CU_LOJA == ;
			F2_FILIAL+F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA 
		If F2_TIPO == "D"	.And. xMoeda(F2_VALBRUT,F2_MOEDA,M->CU_MOEDA) >=  M->CU_VALMIN
			lRet	:=	.T.
		Endif	
		SF2->(DBskip())
	Enddo	
	//Verifica se a NCP ja foi utilizada em alguma outra solicitacao...
	If lRet
		lRet       := A055ValidNCP(xFilial(),M->CU_FORNECE,M->CU_LOJA,M->CU_NCRED,cSDocIDF2)//Tiago Silva, campo M->CU_SERNCP substituido pela id da serie cSDocIDF2, projeto chave unica.
		lExisteNCP := .T.	
	EndIf
Endif
If !lRet .And. !lExisteNCP
	Help(" ",1,cHelp)
Endif
RestArea(aArea)
Return lRet

/*

Ŀ
Funo    SF12SCU    Autor  Bruno Sobieski         Data  25/10/02 
Ĵ
Descrio  Traz as notas fiscais e notas de credito para a amarracao  
                                                                      
Ĵ
Sintaxe e  Void SF12SCU(nTipo)                                        
Ĵ
Parametros                                                            
Ĵ
 Uso       MATA055()                                                  
ٱ


*/
Static Function SF12SCU(nTipo)
Local cAOrig	:=	"",cWhile,	cSeek,aCpos,cTitulo,cCondicao
Local aAreaSCU  := GetArea()
If nTipo == 1
	If !Empty(M->CU_FORNECE)
		cAOrig	:=	'SF1'	
		cWhile 	:=	"SF1->(F1_FILIAL+F1_FORNECE+F1_LOJA)=='"+xFilial('SF1')+M->CU_FORNECE+M->CU_LOJA+"'"
		cSeek		:=	"'"+xFilial('SF1')+M->CU_FORNECE+M->CU_LOJA+"'"
		aCpos		:=	{'F1_DOC','F1_SERIE','F1_EMISSAO','F1_DTDIGIT','F1_VALBRUT'}
		cTitulo	:=	STR0001
		cCondicao:=	"F1_TIPODOC == '10'"	
	Endif
ElseIf nTipo == 2
	cAOrig	:=	'SF2'	
	cWhile 	:=	"SF2->(F2_FILIAL+F2_CLIENTE+F2_LOJA)=='"+xFilial('SF2')+M->CU_FORNECE+M->CU_LOJA+"'"
	cSeek		:=	"'"+xFilial('SF2')+M->CU_FORNECE+M->CU_LOJA+"'"
	aCpos		:=	{'F2_DOC','F2_SERIE','F2_EMISSAO','F2_VALBRUT'}
	cTitulo	:=	STR0002
	cCondicao:=	"F2_TIPO == 'D' .And. xMoeda(F2_VALBRUT,F2_MOEDA,"+Alltrim(STR(M->CU_MOEDA))+") >=  "+Alltrim(STR(M->CU_VALMIN))
	cCondicao:= cCondicao + " .And. !SCU->(MsSeek(SF2->(F2_FILIAL+F2_CLIENTE+F2_LOJA+F2_DOC+F2_SERIE)))" 
Endif

If !Empty(cAorig)    
	dbSetOrder(3)
	aRet	:=	LocxF4(cAOrig,2,cWhile,cSeek,aCpos,,cTitulo,cCondicao,.T.,,,,,,.F.)
	RestArea(aAreaSCU)
	If Type('aRet') == "A" .And. Len(aRet) == 3 .And. Len(aRet[3]) > 0 .And. Valtype(aRet[3][1]) == 'N'
		(cAorig)->(MsGoTo(aRet[3][1]))
		If cAOrig == "SF1"
			M->CU_NFISCAL	:=	SF1->F1_DOC
			M->CU_SERNF  	:=	SF1->F1_SERIE 
			M->CU_DTNF  	:=	SF1->F1_EMISSAO
		ElseIf cAOrig == "SF2"
			M->CU_NCRED  	:=	SF2->F2_DOC
			M->CU_SERNCP 	:=	SF2->F2_SERIE
			M->CU_DTNCP 	:=	SF2->F2_EMISSAO
			M->CU_DTAMARR	:=	dDataBase 
		Endif	
	Endif
Endif

Return
/*/

Ŀ
Funo    A055Legenda Autor  Bruno Sobieski        Data  25.10.02 
Ĵ
Descrio  Cria uma janela contendo a legenda da mBrowse              
Ĵ
 Uso       MATA055                                                    
ٱ


/*/
Function A055Legenda()
Local aLegenda := {	{"DISABLE" ,OemToAnsi(STR0013)},; //"Fechada"
				    {"ENABLE"  ,OemToAnsi(STR0012)}}  //"Pendente"

BrwLegenda(cCadastro,STR0011 ,aLegenda) //"Legenda"

Return .T.

/*/

Ŀ
Funo    A055ValidNCP Autor  Julio Cesar           Data  28.10.02 
Ĵ
Descrio  Verifica se a NCP informada pelo usuario ja foi utilizada   
           em outra solicitacao.                                       
Ĵ
Sintaxe    A055ValidNCP(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5)                 
Ĵ
Parametros ExpC1 = Filial                                              
           ExpC2 = Codigo do Fornecedor                                
           ExpC3 = Codigo da Loja do Fornecedor                        
           ExpC4 = Codigo da NCP                                       
           ExpC5 = Serie da NCP                                        
Ĵ
 Uso       MATA055                                                     
ٱ


/*/
Function A055ValidNCP(cFil,cFornec,cLoja,cNumNCP,cSerNCP) //cSerNCP j vem com ID caso necessrio

Local lRet     := .T.
Local aAreaSCU := {}


If !Empty(cSerNCP) .And. Empty(cNumNCP) 
	MsgAlert(OemToAnsi(STR0014))//"Para informar la serie de la NCP es obligatorio informar primeramente su respectivo numero."
	lRet := .F.
EndIf

//So eh verificado o numero da NCP pq pode existir NCP sem serie...
If !Empty(cNumNCP) .And. lRet
	dbSelectArea("SCU")
	aAreaSCU := GetArea()
	dbSetOrder(3)
	If MsSeek(cFil+cFornec+cLoja+cNumNCP+cSerNCP)
		Help(" ",1,"MA055006")
		lRet := .F.
	EndIf
	RestArea(aAreaSCU)
EndIf

Return(lRet)

/*/

Ŀ
Funo    A055AtuDtBx  Autor  Julio Cesar           Data  28.10.02 
Ĵ
Descrio  Atualiza o campo CU_DTBAIXA na baixa do titulo e no         
           cancelamento.                                               
Ĵ
Sintaxe    A055AtuDtBx(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpD1)            
Ĵ
Parametros ExpC1 = Tipo de Operacao ("1" => Baixa, "2" => Cancelamento)
           ExpC2 = Codigo do Fornecedor                                
           ExpC3 = Codigo da Loja do Fornecedor                        
           ExpC4 = Codigo da Factura de Venda (Titulo)                 
           ExpC5 = Serie da Factura de Venda (Prefixo )                 
           ExpD1 = Data da Baixa                                       
Ĵ
 Uso       MATA055                                                     
ٱ


/*/
Function A055AtuDtBx(cTpOper,cFornec,cLoja,cNum,cSer,DtaBaixa)

Local aAreaAtu  := GetArea()
Local aAreaSCU  := {}
Local aAreaSE2  := {}
Local lDtaBaixa := .T.
Local nTamVal   := 0
Local nDecsVal  := 0
Local cQuery	:= ""
Local cAliasSCU := ""

If cTpOper == "1"
	//Verifica se a factura esta amarrada a alguma solicitaca, caso esteja o
	//campo CU_DTBAIXA tem que ser atualizado...
	If SuperGetMv('MV_SOLNCP') .And. SE2->E2_TIPO == MVNOTAFIS
		
		/*
		Query adicionada para substituir o SEEK que era feito na tabela SCU utilizando E2_PREFIXO como serie. 
		Projeto Chave Unica - Tiago Silva - 05/05/2015
		*/
		cQuery:=" select 
		cQuery+=" 	SCU.R_E_C_N_O_ as SCUREC"
		cQuery+=" from "+retSqlName("SE2")+" SE2 inner join "+retSqlName("SF1")+" SF1 "
		cQuery+="   ON SF1.F1_FILIAL	='"+xFilial("SF1")+"' "		
		cQuery+="  and SF1.F1_PREFIXO 	= SE2.E2_PREFIXO		
		cQuery+="  and SF1.F1_DOC		= SE2.E2_NUM			
		cQuery+="  and SF1.F1_FORNECE 	= SE2.E2_FORNECE 	
		cQuery+="  and SF1.F1_LOJA    	= SE2.E2_LOJA	
		cQuery+="  and SF1.D_E_L_E_T_	=   '' "
		cQuery+=" inner join "+retSqlName("SCU")+" SCU "
		cQuery+="   on SCU.CU_FILIAL	=	'"+xFilial("SCU")+"' "
		cQuery+="  and SCU.CU_FORNECE	=	SF1.F1_FORNECE "
		cQuery+="  and SCU.CU_LOJA		=	SF1.F1_LOJA "
		cQuery+="  and SCU.CU_NFISCAL	=	SF1.F1_DOC "
		cQuery+="  and SCU.CU_SERNF		=	SF1.F1_SERIE "
		cQuery+="  and SCU.D_E_L_E_T_	=   '' "
		cQuery+="  where SE2.E2_FILIAL	=	'"+xFilial("SE2")+"' "
		cQuery+="    and SE2.E2_PREFIXO	=	'"+cSer+"' "
		cQuery+="    and SE2.E2_NUM		=	'"+cNum+"' "
		cQuery+="    and SE2.E2_TIPO	=	'NF' " 
		cQuery+="    and SE2.E2_FORNECE =	'"+cFornec+"' "
		cQuery+="    and SE2.E2_LOJA	=	'"+cLoja+"' "
		cQuery+="  	 and SE2.D_E_L_E_T_	=   '' "
		cQuery+="  group by SCU.R_E_C_N_O_ "
		
		cQuery	  := ChangeQuery(cQuery)
		cAliasSCU := CriaTrab(Nil,.F.)
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasSCU, .T., .T. )
		dbSelectArea(cAliasSCU)
		dbSelectArea("SCU")
		aAreaSCU := GetArea()
		
		While (cAliasSCU)->(!EOF())
			SCU->(dbGoTo((cAliasSCU)->SCUREC))
			If Empty(SCU->CU_DTBAIXA)
				RecLock("SCU",.F.)
					Replace CU_DTBAIXA With DtaBaixa
				MsUnLock()
			EndIf
			(cAliasSCU)->(dbSkip())
		End
	
		/* Removido por utilizar o campo E2_PREFIXO para posicionar a tabela SCU campo CU_SERNF, substitudo por query. Tiago Silva
		dbSelectArea("SCU")
		aAreaSCU := GetArea()
		dbSetOrder(2)
		If MsSeek(xFilial("SCU")+cFornec+cLoja+cNum+cSer)
			While !Eof() .And.(cFornec+cLoja+cNum+cSer == CU_FORNECE+CU_LOJA+CU_NFISCAL+CU_SERNF)
				If Empty(CU_DTBAIXA)
					RecLock("SCU",.F.)
					Replace CU_DTBAIXA With DtaBaixa
					MsUnLock()
				EndIf
				dbSkip()
			End
		EndIf 
		*/
		(cAliasSCU)->(dbCloseArea())
		RestArea(aAreaSCU)	
	EndIf 
Else
	If SuperGetMv('MV_SOLNCP')
		//Verifica se existe alguma baixa para a factura em questao...
		dbSelectArea("SE2")
		aAreaSE2 := GetArea()
		dbSetOrder(6)
		If MsSeek(xFilial("SE2")+cFornec+cLoja+cSer+cNum) 
			nTamVal   := TAMSX3("E2_SALDO")[1]
			nDecsVal  := TAMSX3("E2_SALDO")[2]
			While !Eof() .And. (E2_FORNECE+E2_LOJA+E2_PREFIXO+E2_NUM == cFornec+cLoja+cSer+cNum)
				If E2_TIPO != MVNOTAFIS
					dbSkip()
					Loop					
				EndIf
				If Str(E2_SALDO,nTamVal,nDecsVal) <> Str(E2_VALOR,nTamVal,nDecsVal)
					lDtaBaixa := .F.
					Exit
				EndIf
				dbSkip()
			End
		EndIf
		RestArea(aAreaSE2)
		
		//So atualiza o campo CU_DTBAIXA com branco caso nao exista nenhuma baixa para
		//a factura em questao...
		If lDtaBaixa
			/*
			Query adicionada para substituir o SEEK que era feito na tabela SCU utilizando E2_PREFIXO como serie. 
			Projeto Chave Unica - Tiago Silva - 05/05/2015
			*/
			cQuery:=" select 
			cQuery+=" 	SCU.R_E_C_N_O_ as SCUREC"
			cQuery+=" from "+retSqlName("SE2")+" SE2 inner join "+retSqlName("SF1")+" SF1 "
			cQuery+="   ON SF1.F1_FILIAL	='"+xFilial("SF1")+"' "		
			cQuery+="  and SF1.F1_PREFIXO 	= SE2.E2_PREFIXO		
			cQuery+="  and SF1.F1_DOC		= SE2.E2_NUM			
			cQuery+="  and SF1.F1_FORNECE 	= SE2.E2_FORNECE 	
			cQuery+="  and SF1.F1_LOJA    	= SE2.E2_LOJA	
			cQuery+="  and SF1.D_E_L_E_T_	=   '' "
			cQuery+=" inner join "+retSqlName("SCU")+" SCU "
			cQuery+="   on SCU.CU_FILIAL	=	'"+xFilial("SCU")+"' "
			cQuery+="  and SCU.CU_FORNECE	=	SF1.F1_FORNECE "
			cQuery+="  and SCU.CU_LOJA		=	SF1.F1_LOJA "
			cQuery+="  and SCU.CU_NFISCAL	=	SF1.F1_DOC "
			cQuery+="  and SCU.CU_SERNF		=	SF1.F1_SERIE "
			cQuery+="  and SCU.D_E_L_E_T_	=   '' "
			cQuery+="  where SE2.E2_FILIAL	=	'"+xFilial("SE2")+"' "
			cQuery+="    and SE2.E2_PREFIXO	=	'"+cSer+"' "
			cQuery+="    and SE2.E2_NUM		=	'"+cNum+"' "
			cQuery+="    and SE2.E2_TIPO	=	'NF' " 
			cQuery+="    and SE2.E2_FORNECE =	'"+cFornec+"' "
			cQuery+="    and SE2.E2_LOJA	=	'"+cLoja+"' "
			cQuery+="  	 and SE2.D_E_L_E_T_	=   '' "
			cQuery+="  group by SCU.R_E_C_N_O_ "
			
			cQuery	  := ChangeQuery(cQuery)
			cAliasSCU := CriaTrab(Nil,.F.)
			dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasSCU, .T., .T. )
			dbSelectArea(cAliasSCU)
			dbSelectArea("SCU")
			aAreaSCU := GetArea()
			
			While (cAliasSCU)->(!EOF())
				SCU->(dbGoTo((cAliasSCU)->SCUREC))
					RecLock("SCU",.F.)
					Replace CU_DTBAIXA With CtoD("")
					MsUnLock()
				(cAliasSCU)->(dbSkip())
			End
			
			/*
			dbSelectArea("SCU")
			aAreaSCU := GetArea()
			dbSetOrder(2)
			If MsSeek(xFilial("SCU")+cFornec+cLoja+cNum+cSer)
				While !Eof() .And.(cFornec+cLoja+cNum+cSer == CU_FORNECE+CU_LOJA+CU_NFISCAL+CU_SERNF)
					RecLock("SCU",.F.)
					Replace CU_DTBAIXA With CtoD("")
					MsUnLock()
					dbSkip()
				End
			EndIf*/
			(cAliasSCU)->(dbCloseArea())
			RestArea(aAreaSCU)	
		EndIf		
	EndIf
EndIf
                      
RestArea(aAreaAtu)

Return(Nil)

/*/

Ŀ
Funo    A055Tipo1    Autor  Microsiga             Data  22.03.06 
Ĵ
Descrio  Traz as notas fiscais e notas de credito para a amarracao   
           				                                               
Ĵ
 Uso       MATA055                                                     
ٱ


/*/
Function A055Tipo1()
	SF12SCU(1)
Return
/*/

Ŀ
Funo    A055Tipo2    Autor  Microsiga             Data  22.03.06 
Ĵ
Descrio  Traz as notas fiscais e notas de credito para a amarracao   
           				                                               
Ĵ
 Uso       MATA055                                                     
ٱ


/*/
Function A055Tipo2()
	SF12SCU(2)
Return