#INCLUDE "Matr908.ch"
 
/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MATR908   ³ Autor ³ Edstron Everson Correia  ³ Data ³ 04.11.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Emiss„o do Registro de ISS - Anexo IX do Decreto 16.128-6/12/94³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³MATR908                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Eduardo Ju    ³17.03.03³XXXXXX³Incluso da rotina de Impressao do Valor do ³±±
±±³              ³        ³      ³Servico Prestado a Orgao Publico           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function Matr908

Local i	:=0
Local cArqAbert		:=	GetNewPar("MV_MTR908A","")
Local cArqEncer		:=	GetNewPar("MV_MTR908E","")
Local aDriver 		:= 	ReadDriver()
Local aDados  := {}
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
titulo		:=	STR0001 //"Registro de Servicos Prestados - Imposto sobre Servicos de Qualquer Natureza."
cDesc1		:=	STR0002 //"Ira imprimir os Lancamentos Fiscais referentes a Imposto Sobre "
cDesc2		:=	STR0003 //"Servicos de Qualquer Natureza, conforme o periodo informado."
cDesc3		:=	""
aReturn  	:=	{ STR0004, 1,STR0005, 2, 2, 1, "",1 } //"Zebrado"###"Administracao"
nomeprog 	:=	"MATR908"
cPerg    	:=	"MTR908"
nLastKey 	:=	0
nPagina		:=	1
Limite   	:= 220
Tamanho  	:=	"G"      

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para Impressao do Cabecalho e Rodape    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cString  :=	"SF3"
Titulo   :=	STR0006 //"REGISTRO DE SERVICOS PRESTADOS - IMPOSTO SOBRE SERVICOS DE QUALQUER NATUREZA"
cabec1   :=	""
cabec2   :=	""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // Ano                                  ³
//³ mv_par02             // Mes                                  ³
//³ mv_par03             // Livro Selecionado                    ³
//³ mv_par04             // Apuracao                             ³
//³ mv_par05             // Periodo                              ³
//³ mv_par06             // Numero da Folha                      ³
//| mv_par07             // Layout                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel	:=	"MATR908"   // nome default do relatorio em disco
wnrel	:=	SetPrint(cString,wnrel,cPerg,Titulo,cDesc1,cDesc2,cDesc3,.F.,"",,Tamanho)

nPagina	:=	1

If nLastKey == 27
	dbClearFilter()
	Return
Endif
SetDefault(aReturn,cString)
If nLastKey == 27
	dbClearFilter()
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recebe filtro definido pelo usuario                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilterUser	:=	aReturn[7]
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Impressao de Termo / Livro                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")		// COCACOLA
_F3_CAMPOS:=FCount()

For i:=1 to _F3_CAMPOS
	cCampo:=FieldName(i)
	&("_"+cCampo):=i
Next

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa relatorio                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If MV_PAR10==2 .Or. MV_PAR10==3	
	aDados  := {}
	AADD(aDados,{"D_I_A",Day(dDatabase)})
	AADD(aDados,{"M_E_S",MesExtenso(Month(dDatabase))})
	AADD(aDados,{"A_N_O",Year(dDatabase)})
	TERMGO(cArqAbert,cArqEncer,cPerg,aDriver[4],aDados)
EndIf

If MV_PAR10==1 .Or. MV_PAR10==3	
	RptStatus({|lEnd| R908Livro(@lEnd,wnRel,cString,Tamanho)},titulo)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura Ambiente                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
dbSetOrder(1)
If aReturn[5] == 1
	Set Printer TO
	dbCommitAll()
	Ourspool(wnrel)
Endif

Return

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R908Livro() ³ Autor ³ Edstron E. Correia ³ Data ³05/11/02  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Impressao do Livro de Registro de ISS                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR908()                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Static Function R908Livro(lEnd,wnRel,cString,Tamanho)       

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao de Variaveis                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lQuery    	:= .F.
Local lImprime		:= .F.
Local lF3_Reciss 	:= SF3->(FieldPos ("F3_RECISS"))>0
Local lDocCanc		:= .T.
Local lDocCupom		:= .T.
Local lPula			:= .F.
Local cModNot		:= GetNewPar("MV_M908MOD","")
Local cModelo		:= ""
Local aLay   	 	:= Array(24)
Local aStru			:= {}
Local aObservacao   := {}
Local aObserva2  	:= {}
Local aDespesas		:= {}

Local nLin   	 	:= 0
Local nTotPrest 	:= 0.00
Local nTotMatAp 	:= 0.00
Local nTotSubEm 	:= 0.00
Local nTotIsent 	:= 0.00
Local nTotBase  	:= 0.00                                                  
Local nTotGer	 	:= 0.00
Local nValEnv	 	:= 0.00
Local nValRet   	:= 0.00
Local nMes		 	:= mv_par01
Local nAno		 	:= mv_par02
Local nApuracao 	:= mv_par04
Local nPeriodo	 	:= mv_par05
Local nFolha    	:= Val(mv_par06)
Local nLayout		:= mv_par07
Local nPublico  	:= 0.00 
Local nX				:= 0
Local nAluguel		:= 0.00
Local nAgua			:= 0.00
Local nProLabore	:= 0.00
Local nPagamento	:= 0.00
Local nPreviden	:= 0
Local nDespesas	:= 0
Local nISS			:= 0
Local nISSRet		:= 0.00
Local nValor		:= 0
Local nTotBsRet	:= 0
Local nTotImpRet	:= 0
Local nTotImpDev	:= 0
Local nImpRet		:= 0
Local nImpDev		:= 0  
Local nDocCanc		:= 0
Local nDocCupom		:= 0
Local cAliasSF3 	:= "SF3"
Local cMes      	:= ""                                                    
Local cImp      	:= "IS"                                                  
Local cPictVal  	:= "@E 999,999,999.99"
Local cNrLivro	 	:= mv_par03
Local cArqApur	 	:= NmArqApur(cImp,nAno,nMes,nApuracao,nPeriodo,Alltrim(cNrLivro))
Local cBanco1		:= SPACE(06)
Local cData1		:= SPACE(08)
Local cBanco2		:= SPACE(06)
Local cData2		:= SPACE(08)
Local cArqInd   	:= ""
Local cFiltro   	:= ""
Local cChave		:= ""
Local cQuery		:= ""
Local cRecIss		:= ""
Local cDocCanc		:= ""
Local cDocCupom		:= ""

Local lInfoCab 	:= GetNewPar("MV_M908CAB",.F.)
Local lSubEmp		:= SF3->(FieldPos("F3_ISSSUB")) > 0
Local lDedMat		:= SF3->(FieldPos("F3_ISSMAT")) > 0
Local nModelo		:= 0

Local lMapResumo	:= IIF((SuperGetMV("MV_LJLVFIS",,1) == 2) .AND. mv_par12 == 1,.T.,.F.)
Local aMapaResumo	:= 	{}
Local aGravaMapRes	:= 	{}
Local cArqBkpQry	:= 	""
Local cArqTmpMP		:= 	""

Private aDatas	 	:= DetDatas(nMes,nAno,nApuracao,nPeriodo)
Private dDtIni	 	:= aDatas[1]
Private dDtFim	 	:= aDatas[2]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Cria Indice Condicional                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SF3")
#IFDEF TOP
    lQuery := .T.   
	cAliasSF3 := GetNextAlias() 
	aStru  := SF3->(dbStruct())
	cQuery := "SELECT * "
	cQuery += "FROM "+RetSqlName("SF3")+" "
	cQuery += "WHERE F3_FILIAL='"+xFilial("SF3")+"' AND "    
	cQuery += "F3_TIPO IN ('S','L')  AND "
	cQuery += "F3_CFO >'5"+SPACE(LEN(F3_CFO)-1)+"'AND "
	cQuery += "F3_ENTRADA>='"+Dtos(dDtIni)+"' AND "
	cQuery += "F3_ENTRADA<='"+Dtos(dDtFim)+"' AND "
	cQuery += "F3_CODISS<>'    ' AND "
	If !(AllTrim(cNrLivro)=="*")
	   cQuery	+=	"F3_NRLIVRO='"+cNrLivro+"' AND "
	Endif
    cQuery += "D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY F3_ENTRADA,F3_SERIE,F3_NFISCAL"
    cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF3,.T.,.T.)
	For nX := 1 To len(aStru)
		If aStru[nX][2] <> "C" .And. FieldPos(aStru[nX][1])<>0
		   TcSetField(cAliasSF3,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
		EndIf
	Next nX
	dbSelectArea(cAliasSF3)	
#Else
	nIndex	:=	RetIndex("SF3")
	cArqInd	:=	CriaTrab(NIL,.F.)
	cChave	:=	"DTOS(F3_ENTRADA)+F3_SERIE+F3_NFISCAL"
	cFiltro := "F3_FILIAL=='"+xFilial()+"'.AND.F3_TIPO$'SL'.AND. F3_CFO >'5"+SPACE(LEN(F3_CFO)-1)+"'"
	cFiltro	+=	" .AND. dtos(F3_ENTRADA) >='"+dtos(dDtIni)+"' .and. dtos(F3_ENTRADA)<='"+dtos(dDtFim)+"'"
	If !(AllTrim(cNrLivro)=="*")
	   cFiltro	+=	".AND.F3_NRLIVRO=='"+cNrLivro+"'"
	Endif
	IndRegua("SF3",cArqInd,cChave,,cFiltro,STR0007) //"Selecionando Registros..."
	dbGotop()
	SetRegua(LastRec())
#ENDIF

cMes :=	MesExtenso(Month(dDtIni))

R908LayOut(@aLay,nLayout)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Executa funções para mostrar os dados do Mapa Resumo³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMapResumo

	cChave	:=	"DTOS(F3_ENTRADA)+F3_SERIE+F3_NFISCAL"

	cArqBkpQry 		:= cAliasSf3

	aMapaResumo		:= 	MaxRMapRes(dDtIni,dDtFim)
	aGravaMapRes	:= 	MaXRAgrupF3(/*cFilAnt*/,aMapaResumo,"MATR908")
	cArqTmpMP		:= 	MaXRExecArq(1)
	cAliasSf3		:=	MaXRAddArq(		1		,	cArqTmpMP	,	cAliasSf3	,/*aCposTemp*/	,;
									aGravaMapRes,	cChave		)
	
EndIf

While !(cAliasSF3)->(Eof())

	If !lQuery
		IncRegua()
	Endif
	  
	If Interrupcao(@lEnd)
		Exit
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Considera filtro do usuario                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(cFilterUser).and.!(&cFilterUser)
		dbSkip()
		Loop
	Endif
	
	If ( nLin > 60 .Or. nLin == 0 )
		If ( nLin == 60 )
			FmtLin(,aLay[11],,,@nLin)
		EndIf
		nLin := 0
		@ nLin,000 Psay aValImp(Limite)
		nLin++
        If nLayout == 1
			FmtLin({SM0->M0_NOMECOM,Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM,StrZero(nFolha,6)},aLay[01],,,@nLin)
			FmtLin({SM0->M0_ENDENT,SM0->M0_INSC,cMes,Alltrim(STRZERO(nAno,4))},aLay[02],,,@nLin)
		Else
			If !lInfoCab
				FmtLin({cMes,Alltrim(STRZERO(nAno,4))},aLay[01],,,@nLin) 
				FmtLin({SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
			Else
				FmtLin({cMes,Alltrim(STRZERO(nAno,4)),Left(SM0->M0_NOMECOM,40),Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM},aLay[01],,,@nLin) 
				FmtLin({Left(SM0->M0_ENDENT,40),SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
			Endif
		Endif
		FmtLin(,aLay[03],,,@nLin)
		FmtLin(,aLay[04],,,@nLin)
		FmtLin(,aLay[05],,,@nLin)
		FmtLin(,aLay[06],,,@nLin)
		FmtLin(,aLay[07],,,@nLin)
		FmtLin(,aLay[08],,,@nLin)
		FmtLin(,aLay[09],,,@nLin)
		nFolha++
	EndIf
	
	dDia 		:=Alltrim(STRZERO(DAY((cAliasSF3)->F3_EMISSAO),2))
	dDiaNota	:=Alltrim(STRZERO(DAY((cAliasSF3)->F3_EMISSAO),2))
	nMesNota	:=Alltrim(STRZERO(MONTH((cAliasSF3)->F3_EMISSAO),2))
	nAnoNota	:=Alltrim(STRZERO(YEAR((cAliasSF3)->F3_EMISSAO),4))
		  
	nModelo		:= aT((cAliasSF3)->F3_ESPECIE,cModNot)
	If nModelo > 0
		cModelo	:= Alltrim(SubStr(cModNot,nModelo+6,3))
	Else
		cModelo	:= aModNot((cAliasSF3)->F3_ESPECIE)
	Endif
	If nLayout == 1
		If Empty((cAliasSF3)->F3_DTCANC)
			FmtLin({dDia,(cAliasSF3)->F3_ESPECIE,cModelo,(cAliasSF3)->F3_NFISCAL, Iif(Empty((cAliasSF3)->F3_DOCOR),(cAliasSF3)->F3_NFISCAL,(cAliasSF3)->F3_DOCOR),dDiaNota,nMesNota,nAnoNota,(cAliasSF3)->F3_VALCONT,Iif(lDedMat,(cAliasSF3)->F3_ISSMAT,0.00),Iif(lSubEmp,(cAliasSF3)->F3_ISSSUB,0.00),((cAliasSF3)->F3_ISENICM+(cAliasSF3)->F3_OUTRICM),(cAliasSF3)->F3_BASEICM,SUBS((cAliasSF3)->F3_OBSERV,1,30)},aLay[10],cPictVal,,@nLin)
		Else
			FmtLin({dDia,(cAliasSF3)->F3_ESPECIE,cModelo,(cAliasSF3)->F3_NFISCAL, Iif(Empty((cAliasSF3)->F3_DOCOR),(cAliasSF3)->F3_NFISCAL,(cAliasSF3)->F3_DOCOR),dDiaNota,nMesNota,nAnoNota,0.00,0.00,0.00,0.00,0.00,SUBS((cAliasSF3)->F3_OBSERV,1,30)},aLay[10],cPictVal,,@nLin)
		Endif
	Else		  
		//Rotina de calculo de imposto devido/retido igual do matr990.
		If (lF3_Reciss) .And. !Empty  ((cAliasSF3)->F3_RECISS)
			cRecIss := Iif ((cAliasSF3)->F3_RECISS$"12", Iif ((cAliasSF3)->F3_RECISS=="1", "S", "N"), (cAliasSF3)->F3_RECISS)
		Else
			cRecIss	:= Mtr908ISS ((cAliasSF3)->F3_CLIEFOR, (cAliasSF3)->F3_LOJA, (cAliasSF3)->F3_TIPO, (cAliasSF3)->F3_CFO)
		EndIf
			
		If cRecIss == "S"
			nImpRet := (cAliasSF3)->F3_VALICM
			nImpDev := 0.00
		Else
			nImpRet := 0.00
			nImpDev	:= IIF(subs((cAliasSF3)->F3_CFO,1,1)>="5",(cAliasSF3)->F3_VALICM,0.00)
		Endif
		//Fim do calculo do imposto devido/retido
		If Empty((cAliasSF3)->F3_DTCANC)
			FmtLin({dDia,(cAliasSF3)->F3_ESPECIE,cModelo,(cAliasSF3)->F3_NFISCAL, Iif(Empty((cAliasSF3)->F3_DOCOR),(cAliasSF3)->F3_NFISCAL,(cAliasSF3)->F3_DOCOR),dDiaNota,nMesNota,nAnoNota,(cAliasSF3)->F3_VALCONT,Iif(lDedMat,(cAliasSF3)->F3_ISSMAT,0.00),((cAliasSF3)->F3_ISENICM+(cAliasSF3)->F3_OUTRICM),(cAliasSF3)->F3_BASEICM, 0,Transform ((cAliasSF3)->F3_ALIQICM, "@E 999.99"), nImpRet, nImpDev},aLay[10],cPictVal,,@nLin)
		Else
			FmtLin({dDia,(cAliasSF3)->F3_ESPECIE,cModelo,(cAliasSF3)->F3_NFISCAL, Iif(Empty((cAliasSF3)->F3_DOCOR),(cAliasSF3)->F3_NFISCAL,(cAliasSF3)->F3_DOCOR),dDiaNota,nMesNota,nAnoNota,0.00,0.00,0.00,0.00,0.00,Transform(0,"@E 999.99"),0.00,0.00},aLay[10],cPictVal,,@nLin)
		Endif
	Endif

	If Empty((cAliasSF3)->F3_DTCANC)
		nTotPrest +=(cAliasSF3)->F3_VALCONT
		nTotMatAp +=Iif(lDedMat,(cAliasSF3)->F3_ISSMAT,0)
		nTotSubEm +=Iif(lSubEmp,(cAliasSF3)->F3_ISSSUB,0)
		nTotIsent +=((cAliasSF3)->F3_ISENICM+(cAliasSF3)->F3_OUTRICM)
		nTotBase  +=(cAliasSF3)->F3_BASEICM
		nTotBsRet +=0
		nTotImpRet+=nImpRet
	 	nTotImpDev+=nImpDev
	Endif
		     
	If ( nLin > 55 )
		FmtLin(,aLay[09],,,@nLin)
		nLin := 0
		@ nLin,000 Psay aValImp(Limite)
	Else
		FmtLin(,aLay[09],,,@nLin)
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta observacao dos documentos sobre Cupom Fiscal³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nLayout == 2 .And. !Empty((cAliasSF3)->F3_OBSERV) .And. ("NF/SERIE:" $ (cAliasSF3)->F3_OBSERV .OR. "CF/SERIE:" $ (cAliasSF3)->F3_OBSERV)
		
		If lDocCupom
			aAdd(aObserva2,OemToAnsi(STR0057)) //"Notas Fiscais Relacionadas a Cupons:"
			lDocCupom := .F.
		EndIf
		
		If nDocCupom == 2
 			aAdd(aObserva2,cDocCupom)
			nDocCupom := 0
			cDocCupom := ""
		Endif
		
		nPos := AT(":",(cAliasSF3)->F3_OBSERV) + 1
		cDocCupom += STR0058 + (cAliasSF3)->F3_NFISCAL + "/" + (cAliasSF3)->&(SerieNfId("SF3",3,"F3_SERIE")) + STR0059 + Alltrim(Substr((cAliasSF3)->F3_OBSERV,nPos,30)) + " ; " //"Documento "###" substitui o Cupom Fiscal "
		nDocCupom++
		
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta observacao dos documentos cancelados³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nLayout == 2 .And. !Empty((cAliasSF3)->F3_DTCANC)
	
		If lDocCanc
			lDocCanc := .F.
 			aAdd(aObservacao,OemToAnsi(STR0056)) //"DOCUMENTOS CANCELADOS(ESPECIE/MODELO/NUMERACAO DE/NUMERACAO ATE): "
		Endif

		If nDocCanc == 7
 			aAdd(aObservacao,cDocCanc)
			nDocCanc := 0
			cDocCanc := ""
		Endif

		cDocCanc += (cAliasSF3)->F3_ESPECIE + "/" + cModelo + "/" + (cAliasSF3)->F3_NFISCAL + "/" + (cAliasSF3)->F3_NFISCAL + " ; "
		nDocCanc++

	Endif
	
	(cAliasSF3)->(DbSkip())
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Exclui o arquivo temporario³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lMapResumo
	MaXRExecArq(2,cArqTmpMP)
	cAliasSf3 := cArqBkpQry
	DbSelectArea(cAliasSF3)
EndIf		

If nLayout == 2 .And. !Empty(cDocCupom)
	aAdd(aObserva2,cDocCupom)
EndIf
If nLayout == 2 .And. !Empty(cDocCanc)
	aAdd(aObservacao,cDocCanc)
Endif

If ( nLin > 60 .Or. nLin == 0 )
	If ( nLin == 60 )
  		FmtLin(,aLay[11],,,@nLin)
   EndIf
   nLin := 0
   @ nLin,000 Psay aValImp(Limite)
   nLin++
   If nLayout == 1
	   FmtLin({SM0->M0_NOMECOM,Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM,StrZero(nFolha,6)},aLay[01],,,@nLin)
	   FmtLin({SM0->M0_ENDENT,SM0->M0_INSC,cMes,Alltrim(STRZERO(nAno,4))},aLay[02],,,@nLin)
   Else 
       If !lInfoCab
			FmtLin({cMes,Alltrim(STRZERO(nAno,4))},aLay[01],,,@nLin) 
			FmtLin({SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
       Else
			FmtLin({cMes,Alltrim(STRZERO(nAno,4)),Left(SM0->M0_NOMECOM,40),Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM},aLay[01],,,@nLin) 
			FmtLin({Left(SM0->M0_ENDENT,40),SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
       Endif
   Endif
   FmtLin(,aLay[03],,,@nLin)
   FmtLin(,aLay[04],,,@nLin)
   FmtLin(,aLay[05],,,@nLin)
   FmtLin(,aLay[06],,,@nLin)
   FmtLin(,aLay[07],,,@nLin)
   FmtLin(,aLay[08],,,@nLin)
   FmtLin(,aLay[09],,,@nLin)
   nFolha++
EndIf
	
If nLayout == 1
	FmtLin({nTotPrest,nTotMatAp,nTotSubEm,nTotIsent,nTotBase},aLay[12],,,@nLin)
	FmtLin(,aLay[15],,,@nLin)                                                  
Else
	FmtLin({nTotPrest,nTotMatAp,nTotIsent,nTotBase, nTotBsRet, nTotImpRet, nTotImpDev},aLay[12],,,@nLin)
	FmtLin(,aLay[13],,,@nLin)
Endif

If (File(cArqApur))
	If nLayOut == 1
		If nLin < 57
			FmtLin(,aLay[13],,,@nLin)
			FmtLin(,aLay[14],,,@nLin)
			FmtLin(,aLay[15],,,@nLin)
		Else
			nLin := 61
			CabecRel("N",nLayout,aLay,@nLin,@nFolha,cMes,nAno)
		Endif
   Endif
   FT_FUse(cArqApur)
   FT_FGotop()

   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Armazena os Valores referente ao Demonstrativo da Apuracao ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   While (!FT_FEof()) 
		cLinha	:= AllTrim(FT_FReadLN())
		If Left (cLinha, 3)=="OBS"
 			aAdd (aObservacao, SubStr (cLinha, 5))
 		EndIf
      If IsDigit(Substr(cLinha,1,1)).and.IsDigit(Substr(cLinha,3,1))
			cValor	:=	Substr(cLinha,52,18)
		   cValor	:=	StrTran(cValor,'.')
		   cValor	:=	StrTran(cValor,',','.')
		   nValEnv  :={Val(cValor),.F.}
		   R908Cv(cPictVal,nValEnv,@nValRet)
		   nValor	:=	nValRet 
		Endif

      If nLayout == 1
	      If "ORGAO PUBLICO"$Alltrim(cLinha)
			   nPublico	:=	nValor
			ElseIf "ALUGUEL"$Alltrim(cLinha)
			   nAluguel	:=	nValor
			ElseIf "AGUA"$Alltrim(cLinha)
				nAgua		:=	nValor		 
			ElseIf "PRO-LABORE"$Alltrim(cLinha)
			   nProLabore	:=	nValor		 
			ElseIf "PAGAMENTO"$Alltrim(cLinha)
			   nPagamento	:=	nValor		 
			ElseIf "PREVIDENCIA"$Alltrim(cLinha)
			   nPreviden	:=	nValor
			ElseIf "DEMAIS DESPESAS"$Alltrim(cLinha)
			    nDespesas	:=	nValor		 
			ElseIf "ISS PROPRIO"$Alltrim(cLinha)
				 nISS		:=	nValor		 
			ElseIf "BANCO1"$Alltrim(cLinha)
			    cBanco1	:=	nValor		 		                             
			ElseIf "DATA1"$Alltrim(cLinha)
			    cData1		:=	nValor		 
			ElseIf "ISS RETIDO"$Alltrim(cLinha)
			    nISSRet	:=	nValor		 
			ElseIf "BANCO2"$Alltrim(cLinha)
			    cBanco2	:=	nValor		 
			ElseIf "DATA2"$Alltrim(cLinha)
			    cData2		:=	nValor		 
	      Endif
		Else 
	       If "ORGAO PUBLICO"$Alltrim(cLinha)
	          aAdd (aDespesas, {"ORGAO PUBLICO", nValor})
			 ElseIf "ALUGUEL"$Alltrim(cLinha)
				 aAdd (aDespesas, {"ALUGUEL", nValor})
			 ElseIf "AGUA"$Alltrim(cLinha)
				 aAdd (aDespesas, {"AGUA", nValor})
			 ElseIf "PRO-LABORE"$Alltrim(cLinha)
			 	 aAdd (aDespesas, {"PRO-LABORE", nValor})
			 ElseIf "PAGAMENTO"$Alltrim(cLinha)
				 aAdd (aDespesas, {"PAGAMENTO", nValor})
			 ElseIf "PREVIDENCIA"$Alltrim(cLinha)
				 aAdd (aDespesas, {"PREVIDENCIA", nValor})
			 ElseIf "DEMAIS DESPESAS"$Alltrim(cLinha)
				 aAdd (aDespesas, {"DEMAIS DESPESAS", nValor})
	       Endif
       Endif
		 FT_FSkip()
   EndDo 
   
   //ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
   //³Rotina p/ Impressao (1-Orgao Publico; 2- Demonstrativos            ³
   //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
   If nLayOut == 1
	   FT_FGotop()
	   While (!FT_FEof()) 
			cLinha		:= AllTrim(FT_FReadLN())
			nTamLinha 	:= Len(cLinha)+1
			cCampo		:=	Substr(cLinha,1,3)
			If cCampo == "000"
			   If cLinha <> Chr(10) .And. cLinha <> Chr(0)                                     
			      lImprime	:=.T.
			      nTotGer :=(nAluguel+nAgua+nProLabore+nPagamento+nPreviden+nDespesas)-nPublico
		         FmtLin({TransForm(nAluguel,"@E 999,999,999.99"),TransForm(nISS,"@E 999,999,999.99"),TransForm(nPublico,"@E 999,999,999.99")},aLay[16],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)
		         FmtLin({TransForm(nAgua,"@E 999,999,999.99")},aLay[17],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)
		         FmtLin({TransForm(nProLabore,"@E 999,999,999.99"),Transform(cBanco1,"999999"),Transform(cData1,"@R 99999999")},aLay[18],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)
		         FmtLin({TransForm(nPagamento,"@E 999,999,999.99")},aLay[19],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)		         
		         FmtLin({TransForm(nPreviden,"@E 999,999,999.99"),TransForm(nISSRet,"@E 999,999,999.99")},aLay[20],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)
		         FmtLin({TransForm(nDespesas,"@E 999,999,999.99")},aLay[21],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)		         
		         FmtLin({TransForm(cBanco2,"999999"),Transform(cData2,"@R 99999999")},aLay[22],,,@nLin)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno)		         
		         FmtLin({TransForm(nTotGer,"@E 999,999,999.99")},aLay[23],,,@nLin)
			      FmtLin(,aLay[15],,,@nLin)
		      EndIf
			EndIf               
		   If lImprime
		      Exit
		   Endif
			FT_FSkip()
	   EndDo      
	   FT_FUse()   
   Else 
		//
		//Monto quadro de despesas do periodo de acordo com os lancaomentos da apuracao de ISS
		If (Len (aDespesas)>0)
			nLin++
	   	FmtLin(,aLay[14],,,@nLin)
			FmtLin(,aLay[15],,,@nLin)
			//
			For nX := 1 To Len (aDespesas)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno,"D")
				FmtLin({aDespesas[nX][1], Transform (aDespesas[nX][2], "@E 999,999,999,999.99")},aLay[16],,,@nLin)
			Next (nX)
			//
			FmtLin(,aLay[17],,,@nLin)
		EndIf
		//
		//Monto o quadro de observacoes de acordo com a observacao da apuracao de ISS
		If Len(aObservacao)>0 .or. Len(aObserva2)>0
			nLin++
			lPula := .F.
			FmtLin(,aLay[18],,,@nLin)
			FmtLin(,aLay[19],,,@nLin)
			//Nfs sobre cupom fiscal
			For nX := 1 To Len (aObserva2)
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno,"O")
				FmtLin({aObserva2[nX]},aLay[20],,,@nLin)
				lPula := .t.
			Next (nX)
			//Nfs canceladas
			For nX := 1 To Len (aObservacao)
				If lPula
					FmtLin({" "},aLay[20],,,@nLin)
					lPula := .f.
				EndIf
				CabecRel("S",nLayout,aLay,@nLin,@nFolha,cMes,nAno,"O")
				FmtLin({aObservacao[nX]},aLay[20],,,@nLin)
			Next (nX)
			//
			FmtLin(,aLay[21],,,@nLin)
		EndIf
   Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Exclui as areas de trabalho                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
#IFDEF TOP 
	( cAliasSF3 )->( dbCloseArea() ) 
	dbSelectArea( "SF3" ) 
#ELSE 	
	FErase( cArqInd + OrdBagExt() ) 		
	RetIndex( "SF3" ) 
#ENDIF 	

Return( .T. ) 

Function R908LayOut(aLay,nLayout)

Local lInfoCab := GetNewPar("MV_M908CAB",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica o layout escolhido:³
//³Tipo 1, ate 2004            ³
//³Tipo 2, apos 2004           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nLayout == 1         
	aLay[01]	:= STR0010 //" REGISTRO DE SERVICOS PRESTADOS                  RAZAO SOCIAL : ###############################################  CNPJ : ##################           INSCR. MUNICIPAL : #########################             Folha : ###### "
	aLay[02]	:= STR0011 //" IMPOSTO SOBRE QUALQUER NATUREZA                 ENDERECO     : ###############################################  INSCR.ESTADUAL : ##################                                           Mes : ########## e   Ano #### "
	aLay[03]	:= STR0012 //"+-----+------------------------------------------------------------+----------------+--------------------------------------------------+--------------+---------------------------------------------------------------------+"
	aLay[04]	:= STR0013 //"| Dia |                    DOCUMENTOS EMITIDOS                     |                |                   DEDUCOES LEGAIS                |   Base de    |                                                                     |"
	aLay[05]	:= STR0014 //"+-----+---------+--------+-----------------------+-----------------|  Valor Total   |----------------+----------------+----------------|              |                                                                     |"
	aLay[06]	:= STR0015 //"|     | Especie | Modelo |       Numeracao       |      Data       |                |    Material    | Subempreitada/ |   Isentos/     |   Calculo    |                             Observacoes                             |"
	aLay[07]	:= STR0016 //"+-----+---------+--------+-----------+-----------+-----+-----+-----+  da Prestacao  |                |                |                |              |                                                                     |"
	aLay[08]	:= STR0017 //"|     |         |        | De        | a         | Dia | Mes | Ano |                |    Aplicado    | Subcontratacao | Nao Tributados |   Propria    |                                                                     |"
	aLay[09]	:= STR0018 //"+-----+---------+--------+-----------+-----------+-----+-----+-----+----------------+----------------+----------------+----------------+--------------+---------------------------------------------------------------------+"
	aLay[10]	:= STR0019 //"| ##  | ####### |   ##   | ######### | ######### |  ## |  ## |#### | ############## | ############## | ############## | ############## |##############| ##############################                                      |"
	aLay[11]	:= STR0020 //"+-----+---------+--------+-----------+-----------+-----+-----+-----+----------------+----------------+----------------+----------------+--------------+---------------------------------------------------------------------+"
	aLay[12]	:= STR0021 //"| TOTAIS R$                                                        | ############## | ############## | ############## | ############## |##############|                                                                     |"
	aLay[13]	:= STR0022 //"|                                                  D E M O N S T R A T I V O S                                                                        |                      VALOR DO SERVICO PRESTADO                      |"
	aLay[14]	:= STR0023 //"|                                                                                                                                                     |                           A ORGAO PUBLICO                           |"
	aLay[15]	:= STR0024 //"|-----------------------------------------------------------------------------------------------------------------------------------------------------+---------------------------------------------------------------------|"
	aLay[16]	:= STR0025 //"|     ALUGUEL MENSAL....................R$  ############## | ISS PROPRIO...................R$ ##############                                          |                                                     ##############  |"
	aLay[17]	:= STR0026 //"|     AGUA, ENERGIA, TELEFONO...........R$  ############## |                                                                                          |                                                                     |"
	aLay[18]	:= STR0027 //"|     RETIRADA PRO-LABORE...............R$  ############## | BANCO : ######  DATA : ##########                                                        |                                                                     |"
	aLay[19]	:= STR0028 //"|     PAGAMENTO DE EMPREGADO............R$  ############## |                                                                                          |                                                                     |"
	aLay[20]	:= STR0029 //"|     PREVIDENCIA SOCIAL................R$  ############## | ISS RETIDO/SUBCONTRATACAO.....R$ ##############                                          |                                                                     |"
	aLay[21]	:= STR0030 //"|     DEMAIS DESPESAS...................R$  ############## |                                                                                          |                                                                     |"
	aLay[22]	:= STR0031 //"|                                                          | BANCO : ######  DATA : ##########                                                        |                                                                     |"
	aLay[23]	:= STR0032 //"|     TOTAL.............................R$  ############## |                                                                                          |                                                                     |"
	aLay[24]	:= STR0033 //"|                                                          |                                                  TITULAR OU RESPONSAVEL LEGAL            |                                                                     |"
Else
	If !lInfoCab
		aLay[01]	:= STR0035 //" REGISTRO DE SERVICOS PRESTADOS                  ######### e ####                                                                                                                                      "
		aLay[02]	:= STR0036 //" IMPOSTO SOBRE QUALQUER NATUREZA                    mes      ano                            INSCR. ESTADUAL: ##################                                                         Folha : ###### "
	Else
		aLay[01]	:= STR0060 //" REGISTRO DE SERVICOS PRESTADOS       ######### e ####        RAZAO SOCIAL : ########################################   CNPJ : ##################   INSCR. MUNICIPAL : #########################       "
		aLay[02]	:= STR0061 //" IMPOSTO SOBRE QUALQUER NATUREZA         mes      ano         ENDERECO     : ########################################   INSCR.ESTADUAL : ##################                             Folha : ###### "
	Endif
	aLay[03]	:= STR0037 //"+-----+-------------------------------------------------------------+----------------+---------------------------------+----------------+----------------+----------+----------------+----------------+"
	aLay[04]	:= STR0038 //"| Dia |                    DOCUMENTOS EMITIDOS                      |                |         DEDUCOES LEGAIS         |    Base de     |    Base de     | Aliquota |     Imposto    |     Imposto    |"
	aLay[05]	:= STR0039 //"+-----+---------+--------+-----------------------+------------------|  Valor Total   |----------------+----------------|                |    Calculo     |          |                |                |"
	aLay[06]	:= STR0040 //"|     | Especie | Modelo |       Numeracao       |      Data        |                |    Material    |    Isentos/    |    Calculo     |  Substituicao  |          |     Retido     |     Devido     |"
	aLay[07]	:= STR0041 //"+-----+---------+--------+-----------+-----------+-----+-----+------+  da Prestacao  |                |                |                |   Tributaria   |          |                |                |"
	aLay[08]	:= STR0042 //"|     |         |        | De        | a         | Dia | Mes |  Ano |                |    Fornecido   |    Imunes      |    Propria     |                |          |                |                |"
	aLay[09]	:= STR0043 //"+-----+---------+--------+-----------+-----------+-----+-----+------+----------------+----------------+----------------+----------------+----------------+----------+----------------+----------------+"
	aLay[10]	:= STR0044 //"| ##  | ####### |   ##   | ######### | ######### |  ## |  ## | #### | ############## | ############## | ############## | ############## | ############## |  ######  | ############## | ############## |"
	aLay[11]	:= STR0045 //"+-----+---------+--------+-----------+-----------+-----+-----+------+----------------+----------------+----------------+----------------+----------------+----------+----------------+----------------+"
	aLay[12]	:= STR0046 //"| TOTAIS R$                                                         | ############## | ############## | ############## | ############## | ############## |          | ############## | ############## |"
	aLay[13]	:= STR0047 //"+-------------------------------------------------------------------+----------------+----------------+----------------+----------------+----------------+----------+----------------+----------------+"
	aLay[14]	:= STR0048 //"+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+"
	aLay[15]	:= STR0049 //"|DESPESAS DO PERIODO                                                                                                                                                                                  |"
	aLay[16]	:= STR0050 //"|######################################################################################################################################################################### R$ ##################      |"
	aLay[17]	:= STR0051 //"+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+"	
	aLay[18]	:= STR0052 //"+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+"
	aLay[19]	:= STR0053 //"|OBSERVACOES                                                                                                                                                                                          |"
	aLay[20]	:= STR0054 //"|###############################################################################################################################################################################################      |"
	aLay[21]	:= STR0055 //"+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+"
Endif

Return(.T.)

/*ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R908Cv()     ³Autor ³ Juan Jose Pereira    ³Data³ 03/05/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Converte valores                                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function R908Cv(cPictVal,nValEnv,nValRet)

nValor := nValEnv[1]
lString:= nValEnv[2]
cValor := ""
cValor := Transform(nValor,cPictVal)
nValRet:= NIL
nValRet:= IIf(lString,cValor,nValor)
Return
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Mtr908ISS ³ Autor ³Gustavo G. Rueda       ³ Data ³05/04/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorno da opcao de Recolhe ISS                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametro ³ExpC1 : Codigo do Cliente no F3                             ³±±
±±³          ³ExpC2 : Loja do Cliente no F3                               ³±±
±±³          ³ExpC3 : Tipo do F3                                          ³±±
±±³          ³ExpC4 : CFO do F3                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 19/06/06 ³ Cleber M.     ³Bops 101207: declarada a variavel cRecIss.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Mtr908ISS (cCliFor, cLoja, cTipo, cCfo)

Local aArea   := GetArea()			//Guarda a area atual
Local aAreaSA1:= SA1->(GetArea())	//Guarda a area do SA1
Local aAreaSA2:= SA2->(GetArea())	//Guarda a area do SA2
Local cAliasB := ""					//Alias da tabela a pesquisar
Local cCampo2 := ""					//Conteudo a ser utilizado
Local cRecIss := ""					//Indica se recolhe ISS no retorno da funcao

If Left( cCfo, 1) >= "5"
	cAliasB := Iif( cTipo $ "DB", "SA2", "SA1" )
	cCampo2 := Iif( cTipo $ "DB", "A2_RECISS", "A1_RECISS" )	
Else	
	cAliasB := Iif( cTipo $ "DB", "SA1", "SA2" )
	cCampo2 := Iif( cTipo $ "DB", "A1_RECISS", "A2_RECISS" )
EndIf	

(cAliasB)->(dbSetOrder(1))
If (cAliasB)->(MsSeek(xFilial(cAliasB)+cCliFor+cLoja))
	cRecIss :=(cAliasB)->(FieldGet(FieldPos(cCampo2)))
	cRecIss :=If(cRecIss$"12",If(cRecIss=="1","S","N"),cRecIss)
EndIf

RestArea(aAreaSA1)
RestArea(aAreaSA2)
RestArea(aArea)

Return(cRecISS)
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³CabecRel  ºAutor  ³Sergio S. Fuzinaka  º Data ³ 20/05/2005  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³Cabecalho Padrao                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       |MATR908                                                     º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CabecRel(cImpLin,nLayout,aLay,nLin,nFolha,cMes,nAno,cTpImp)

Local lInfoCab 	:= GetNewPar("MV_M908CAB",.F.)

If ( nLin > 60 .Or. nLin == 0 )
	If nLayout == 1
		If cImpLin == "S"
			FmtLin(,aLay[15],,,@nLin)
		Endif
	Else
		FmtLin(,aLay[14],,,@nLin)
	Endif
	nLin := 0
	@ nLin,000 Psay aValImp(Limite)
	nLin++
	If nLayout == 1
		FmtLin({SM0->M0_NOMECOM,Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM,StrZero(nFolha,6)},aLay[01],,,@nLin)
		FmtLin({SM0->M0_ENDENT,SM0->M0_INSC,cMes,Alltrim(STRZERO(nAno,4))},aLay[02],,,@nLin)
	Else
		If !lInfoCab
			FmtLin({cMes,Alltrim(STRZERO(nAno,4))},aLay[01],,,@nLin) 
			FmtLin({SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
		Else
			FmtLin({cMes,Alltrim(STRZERO(nAno,4)),Left(SM0->M0_NOMECOM,40),Transf(SM0->M0_CGC,"@R! NN.NNN.NNN/NNNN-99"),SM0->M0_INSCM},aLay[01],,,@nLin) 
			FmtLin({Left(SM0->M0_ENDENT,40),SM0->M0_INSC,StrZero(nFolha,6)},aLay[02],,,@nLin)
		Endif
	Endif
	If nLayout == 1
		FmtLin(,aLay[15],,,@nLin)
		FmtLin(,aLay[13],,,@nLin)
		FmtLin(,aLay[14],,,@nLin)
		FmtLin(,aLay[15],,,@nLin)
	Else
		FmtLin(,aLay[14],,,@nLin)
		If cTpImp == "D"	//Despesas
			FmtLin(,aLay[15],,,@nLin)
		Else				//Observacoes
			FmtLin(,aLay[19],,,@nLin)
		Endif
	Endif
	nFolha++
EndIf

Return Nil
