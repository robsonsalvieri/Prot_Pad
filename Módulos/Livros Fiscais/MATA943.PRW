
#Include "MATA943.CH"
#Include "FIVEWIN.CH"
/*


Ŀ
Funcao     MATA943   Autor  William P. Alves       Data  07.06.10 
Ĵ
Descricao  Cadastro de Codigo de Servico de ISS (CCQ)		          
Ĵ
Sintaxe e  MATA943(ExpA1,ExpN1)                                       
Ĵ
Parametros ExpA1 - Array da Rotina Automatica                         
           ExpN1 - Numero da Opcao                                    
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL              
Ĵ
Programador  Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                    
ٱ


*/
Function MATA943(aRotAuto,nOpc)

nOpc := If (nOpc == Nil,3,nOpc)

//Ŀ
// Define Variaveis                                             
//
Private aAC  := { STR0001, STR0002 }			//"Abandona"###"Confirma"

Private aRotina := MenuDef()

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
Private cCadastro := OemtoAnsi(STR0008)         // "Atualizacao do Cadastro de Provincias"

//Ŀ
// Definicao de variaveis utilizadas na rotina automatica       
//
Private Inclui := .F.
Private Altera := .F.

//Ŀ
// Definicao de variaveis para rotina de inclusao automatica    
//
Private l943Auto := ( aRotAuto <> NIL )

//Ŀ
// Endereca a funcao de BROWSE                                  
//
If l943Auto
	dbSelectArea("CCQ")
	MsRotAuto(nOpc,aRotAuto,"CCQ")
Else
	dbSelectArea("CCQ")
	mBrowse( 6, 1,22,75,"CCQ")
EndIf

Return .T.
	
/*

Ŀ
Funcao    A943Inclui Autor  William P. Alves       Data  07.06.10 
Ĵ
Descricao  Programa de Inclusao                                       
Ĵ
Sintaxe e  A943Inclui(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA943()                                                  
ٱ


*/
Function A943Inclui(cAlias,nReg,nOpc)

//Ŀ
// Variaveis utilizadas na rotina automatica                    
//
Inclui := .T.
Altera := .F.

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	If ( l943Auto )
		nOpcA := AxIncluiAuto(cAlias,"A943TudoOk()")
	Else
		nOpcA:=AxInclui(cAlias,nReg,nOpc,,,,"A943TudoOk()")
	EndIf
	
	IF nOpcA == 1
		//Ŀ
		//Ponto de entrada depois da confirmacao                                
		//
	Endif
	
	dbSelectArea(cAlias)

Return

/*

Ŀ
Funcao    A943Altera Autor  William P. Alves       Data  07.06.10 
Ĵ
Descricao  Programa de Alteracao                                      
Ĵ
Sintaxe e  A943Altera(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA943()                                                  
ٱ


*/
Function A943Altera(cAlias,nReg,nOpc)

Local nOpcA

l943Auto := If(Type("l943Auto") == "U", .F.,l943Auto)

//Ŀ
// Variaveis utilizadas na rotina automatica                    
//
Inclui := .F.
Altera := .T.

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	If ( l943Auto )
		nOpcA := AxIncluiAuto(cAlias,,,nOpc,CCQ->(RecNo()))
	Else
		nOpcA:=AxAltera(cAlias,nReg,nOpc,,,,,"A943TudoOk()")
	EndIf
	
	IF nOpcA == 1
		//Ŀ
		//Ponto de entrada depois da confirmacao                                
		//
	Endif

dbSelectArea(cAlias)

Return

/*

Ŀ
Funcao    A943TudoOk Autor  William P. Alves       Data  07.06.10 
Ĵ
Descricao Consistencia do registro                                    
Ĵ
Sintaxe   A943TudoOk()                                                
Ĵ
Parametros                                                            
Ĵ
 Uso      MATA943()                                                   
ٱ


*/
Function A943TudoOk()

Local lRet :=.T.

	if !Empty(M->CCQ_CODIGO) 
		lRet := ExistChav("CCQ",M->CCQ_CODIGO)
	Endif

Return( lRet )

/*

Ŀ
Funcao    A943Deleta Autor  William P. Alves       Data  07.06.10 
Ĵ
Descricao  Programa de exclusao                                       
Ĵ
Sintaxe e  A943Deleta(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao                                    
Ĵ
 Uso       MATA943()                                                  
ٱ


*/
Function A943Deleta(cAlias,nReg,nOpc)

Local nOpcA
Local aInfo     := {}
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := MsAdvSize() 
Local nGd1      := 2
Local nGd2 		:= 2
Local nGd3 		:= 0
Local nGd4 		:= 0

l943Auto  := If (Type("l943Auto") =="U",.F.,l943Auto)

//Ŀ
// Monta tela de exclusao de dados do arquivo                   
//
Private aTELA[0][0], aGETS[0]

aObjects := {} 
AAdd( aObjects, {100, 100, .t., .t. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 } 
aPosObj := MsObjSize( aInfo, aObjects )

nGd1 := 2
nGd2 := 2
nGd3 := aPosObj[1,3]-aPosObj[1,1]
nGd4 := aPosObj[1,4]-aPosObj[1,2]

	//Ŀ
	// Envia para processamento dos Gets          
	//
	nOpcA:=0
	
	IF !SoftLock(cAlias)
		Return
	Endif
	
	If !( l943Auto )
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM nGd1,nGd2 TO nGd3,nGd4 OF oMainWnd PIXEL
		nOpcA:=EnChoice( cAlias, nReg, nOpc, aAC,"AC",OemToAnsi(STR0009),,aPosObj[1] , , 3 )//"Quanto a exclusao?"
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
	Else
		nOpcA := 2
	EndIf

	IF nOpcA == 2  

		//Ŀ
		// Exclusao do registro                                         
		//
		dbSelectArea(cAlias)
		RecLock(cAlias,.F.,.T.)
		dbDelete()

		//Ŀ
		// Ponto de entrada apos a confirmacao                          
		//

   ENDIF

   MsUnlock()
   DbSkip()
   
   dbSelectArea(cAlias)

Return()
                     
/*/


Ŀ
Programa  MenuDef    Autor  William P. Alves       Data 07.06.2010
Ĵ
Descrio  Utilizacao de menu Funcional                               
                                                                      
                                                                      
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados         
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
     
Private aRotina := {	{ STR0003, "AxPesqui"  , 0 , 1 , 0 , .F.},;	//"Pesquisar"
							{ STR0004, "AxVisual"  , 0 , 2 , 0 , NIL},;	//"Visualizar"
							{ STR0005, "A943Inclui", 0 , 3 , 0 , NIL},;	//"Incluir"
							{ STR0006, "A943Altera", 0 , 4 , 0 , NIL},;	//"Alterar"
							{ STR0007, "A943Deleta", 0 , 5 , 0 , NIL}}	//"Excluir"

Return(aRotina)
