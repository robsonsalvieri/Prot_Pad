#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "FISA318.ch"

/*-------------------------------------------------------------
{Protheus.doc} GET MTICMSBenUfFilter
API que retorna os valores dos benefícios fiscais do ICMS (cBenef) calculados no período.

@author Gabriela Luche
@parâmetros de entrada do endpoint
    branches: Filiais
    periodStart: Data Inicial
    periodEnd: Data Final

@since 08/05/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/current-tax-benefits-per-uf', description='Obtém valores de benefícios fiscais por localidade, para preenchimento do gráfico de Benefícios Fiscais de ICMS.')

Function MTICMSBenUfFilter()
    Local oJsResponse      := JsonObject():new() as json
    Local oQryStr          := oRest:getQueryRequest() as json
    Local aRequiredProperties as array
    //--------------------------------
    //Monta um array bidimensional com query strings que se deseja verificar se existem na requisição
    //--------------------------------
        aRequiredProperties	:= {;
            {'branches',400,; //PROPERTY //ERROR_CODE
            STR0069,; //"O parâmetro 'branches' é obrigatório e não foi informado."
            STR0070},;//"Informar Código de Filiais separadas por vírgula."
            {'periodStart',400,;
            STR0071,; //"Nenhuma Data foi informada no parâmetro 'periodStart'."
            STR0072},; //"Informar a data desejada para o período"
            {'periodEnd',400,;
            STR0073,; //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
            STR0072}; //"Informar a data desejada para o período"
        }
    //--------------------------------
    // Verifica se tem parâmetros obrigatórios na requisição da API
    //--------------------------------	
    If FISA318F6HasApiRequiredProperties(aRequiredProperties, oQryStr, @oJsResponse) 
        oJsResponse := ICMSBenUfFilter(oQryStr['branches'] , oQryStr['periodStart'] , oQryStr['periodEnd'] , oQryStr['page'], oQryStr['pageSize'] )   
    EndIf  

    oRest:setKeyHeaderResponse('Content-Type', 'application/json')
    oRest:setResponse(oJsResponse)
    //--------------------------------
    // Libera os objetos
    //-------------------------------    
    FWFreeObj(oJsResponse)
    FWFreeObj(oQryStr)

Return 
/*-------------------------------------------------------------------
{Protheus.doc} GET ICMSBenUfFilter
Função responsável por preencher o Json com informações dos benefícios fiscais do ICMS
A funcao retorna um Json com os seguintes campos:

@author Gabriela Luche
@parâmetros de entrada da funcao
    Filiais         as character  // Filiais
    Data Inicial    as character  // Data Inicial
    Data Final      as character  // Data Final
@since 08/05/2025
@return oJsonResp:toJson() as character
-------------------------------------------------------------------*/
Static Function ICMSBenUfFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character, cPage as character, cPageSize as character)
    Local cAlias        := getNextAlias()           as character
    Local nPosItem      := 0                        as numeric    
    Local nPageSize     := 10                       as integer
    Local nPage         := 1                        as integer
    Local lHasNext      := .f.                      as logical    
    Local oJsonResp     := JsonObject():New()       as object
    Local cDbMs         := ""                       as character
    
    Default cPage       := "1"
    Default cPageSize   := "10"

    nPage               := Val(cPage)
    nPageSize           := Val(cPageSize)
    //--------------------------------
    // Verifica o banco de dados do ambiente
    //--------------------------------	
    cDbMs := TcGetDb()
    //--------------------------------
    // Chama a função responsável por retornar a consulta da query
    //--------------------------------	
    GetICMSBenUf(cDbMs , @cAlias, cBranches, cPeriodStart, cPeriodEnd, nPage, nPageSize )  
    //--------------------------------
    // Percorre todo o alias retornado pela query
    //--------------------------------	
    If (cAlias)->(!Eof()) 
        //--------------------------------	
        // Inicia o objeto Json
        //--------------------------------	
        oJsonResp["items"] := {}
      
        While (cAlias)->(!Eof()) 

            nPosItem++

            If nPosItem <= nPageSize
                 //--------------------------------
                // Alimenta o json com os dados retornado pela query
                //--------------------------------	  
                aAdd( oJsonResp["items"], JsonObject():New() )            
                oJsonResp["items"][nPosItem]["uf"]      :=   (cAlias)->UF
                oJsonResp["items"][nPosItem]["value"]   :=   (cAlias)->TOTALBENEF   
            Else
                lHasNext := .t.
                exit            
            Endif

            (cAlias)->(DbSkip())

        EndDo
        oJsonResp["hasNext"]        := lHasNext
    Else
        oJsonResp['code']           := 200
        oJsonResp['items']          := {}
        oJsonResp['hasNext']        := lHasNext
    EndIf  
    //--------------------------------
    // Fecha o alias
    //--------------------------------	     
    (cAlias)->(DBCloseArea())

Return oJsonResp:toJson()
/*--------------------------------------------------------------------
{Protheus.doc} GetICMSBenUf
(Responsável por executar a consulta aos campos da tabela CDV )
A função retorna um alias com os seguintes campos:
UF : Estado(UF) que originou o beneficio (dois digitos iniciais do código cBenef)
TOTALBENEF : Valor total do calculado no período 

@author Gabriela Luche
@parâmetros de entrada da funcao
@since 08/05/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetICMSBenUf(cDbMs as character, cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, nPage as numeric, nPageSize as numeric)
    Local cQuery    := ''  as character
    Local oGetIcms  := Nil as object
    //--------------------------------
    // Montagem da query
    //--------------------------------	
    If cDbMs $ "ORACLE/POSTGRES"
        cQuery := "SELECT "
        cQuery += "     SUBSTR(CDV_CODAJU, 1,2) AS UF, "
        cQuery += "     SUM(CDV_VALOR) AS TOTALBENEF "
        cQuery += " FROM "+RetSQLName("CDV")+" CDV "
        cQuery += " WHERE "
        cQuery += " CDV.CDV_FILIAL IN (?) "
        cQuery += "     AND CDV.CDV_PERIOD BETWEEN ? AND ? " 
        cQuery += "     AND CDV.D_E_L_E_T_ = ? "
        cQuery += " GROUP BY "
        cQuery += "    SUBSTR(CDV_CODAJU, 1,2) "
        cQuery += " ORDER BY TOTALBENEF "
        cQuery += "    OFFSET ( ? * ? ) ROWS "
        cQuery += "    FETCH NEXT ? ROWS ONLY "        
    Else
        cQuery := "SELECT "
        cQuery += "     LEFT(CDV_CODAJU, 2) AS UF, "
        cQuery += "     SUM(CDV_VALOR) AS TOTALBENEF "
        cQuery += " FROM "+RetSQLName("CDV")+" CDV "
        cQuery += " WHERE "
        cQuery += " CDV.CDV_FILIAL IN (?) "
        cQuery += "     AND CDV.CDV_PERIOD BETWEEN ? AND ? " 
        cQuery += "     AND CDV.D_E_L_E_T_ = ? "
        cQuery += " GROUP BY "
        cQuery += "    LEFT(CDV_CODAJU, 2) "
        cQuery += " ORDER BY TOTALBENEF "
        cQuery += "    OFFSET ( ? * ? ) ROWS "
        cQuery += "    FETCH NEXT ? ROWS ONLY "
    EndIf
    //--------------------------------
    // Executa a criação da área de acordo com a query
    //--------------------------------
    oGetIcms := FwExecStatement():New()
    //--------------------------------
    // Montagem dos Binds
    //--------------------------------	
    oGetIcms:SetQuery(ChangeQuery(cQuery))
    oGetIcms:setIn(1, StrTokArr(cBranches, "," ))
    oGetIcms:SetString(2, Left(StrTran(cPeriodStart, "-", ""),4) + StrZero(Month(Stod(StrTran(cPeriodStart, "-", ""))),2 ) )
    oGetIcms:setString(3, Left(StrTran(cPeriodEnd, "-", ""),4)   + StrZero(Month(Stod(StrTran(cPeriodEnd  , "-", ""))),2 ) )
    oGetIcms:setString(4, ' ')
    oGetIcms:SetUnsafe(5, cValToChar(nPage-1))
    oGetIcms:SetUnsafe(6, cValToChar(nPageSize))
    oGetIcms:SetUnsafe(7, cValToChar(nPageSize+1))
    cAlias := oGetIcms:OpenAlias()
    //--------------------------------
    // Libera o objeto
    //--------------------------------
    FWFreeObj(oGetIcms)

Return
