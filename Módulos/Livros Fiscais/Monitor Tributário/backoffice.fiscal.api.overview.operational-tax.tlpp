#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetTotalImp := Nil
Static oGetValorOpe := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTTaxTotal
API de Total de Impostos por parâmetros.
@author william.palma
@parâmetros de entrada do endpoint
@since 10/12/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/overview/tax-total', description='API de total dos impostos')
Function MTTaxTotal()
Local oJsResponse         as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character
Local cmovimentType     := oRest:getQueryRequest()['movimentType']  as character

If valtype(cmovimentType) != 'U' .and. !empty(cmovimentType) .and. valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
    oJsResponse := TaxTotalFilter(cbranches, cperiodStart, cperiodEnd, cmovimentType)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------
{Protheus.doc} GET MTOperationalValue
API de Valor Operacional por parâmetros.
@author william.palma
@parâmetros de entrada do endpoint
@since 10/12/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/overview/operational-value', description='API de Valor Operacional')
Function MTOperationalValue()
Local oJsResponse         as json
Local cbranches        := oRest:getQueryRequest()['branches']        as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']     as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']       as character
Local cmovimentType    := oRest:getQueryRequest()['movimentType']    as character
Local ctaxedMovements  := oRest:getQueryRequest()['taxedMovements']  as character

If valtype(cmovimentType) != 'U' .and. !empty(cmovimentType) .and. valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd) .and. valtype(ctaxedMovements) != 'U' .and. !empty(ctaxedMovements)
    oJsResponse := OperationalValueFilter(cbranches, cperiodStart, cperiodEnd, cmovimentType, ctaxedMovements)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET TaxTotalFilter
Função responsável por preencher o Json com com informações referentes aos totais de impostos
@author william.palma
@parâmetros de entrada do endpoint
@since 10/12/2024
-------------------------------------------------------------------*/
Static Function TaxTotalFilter(cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character)
Local nPos         := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
  
GetTaxtotal(@cAlias, cbranches, cperiodStart, cperiodEnd, cmovimentType)

// Iniciando o While
If (cAlias)->(!Eof()) .And. ((cAlias)->ICMS > 0 .Or. (cAlias)->ISS > 0 .Or. (cAlias)->IPI > 0 .Or. (cAlias)->PIS > 0 .Or. (cAlias)->COFINS > 0)
    While (cAlias)->(!Eof())
        oJsonResp["items"] := {}
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "ICMS"
        oJsonResp["items"][nPos]["value"]        :=   (cAlias)->ICMS
        
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "ISS"
        oJsonResp["items"][nPos]["value"]        :=   (cAlias)->ISS

        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "IPI"
        oJsonResp["items"][nPos]["value"]        :=   (cAlias)->IPI

        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "PIS"
        oJsonResp["items"][nPos]["value"]        :=   (cAlias)->PIS  
         
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "COFINS"
        oJsonResp["items"][nPos]["value"]        :=   (cAlias)->COFINS 
    (cAlias)->(DbSkip())
    EndDo
    oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code'] := 400
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
    oJsonResp['detailMessage'] := 'value not found'
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*-------------------------------------------------------------------
{Protheus.doc} GET OperationalValueFilter
Função responsável por preencher o Json com com informações referentes ao valor operacional
@author william.palma
@parâmetros de entrada do endpoint
@since 10/12/2024
-------------------------------------------------------------------*/
Static Function OperationalValueFilter(cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character, ctaxedMovements as character)
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character

GetperationalValue(@cAlias,cbranches, cperiodStart, cperiodEnd, cmovimentType, ctaxedMovements)

// Iniciando o While
If (cAlias)->(!Eof()) .And. (cAlias)->VALOPER > 0
    While (cAlias)->(!Eof())
        oJsonResp["value"] := (cAlias)->VALOPER
    (cAlias)->(DbSkip())
    EndDo
Else
    oJsonResp['code'] := 400
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
    oJsonResp['detailMessage'] := 'value not found'
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*--------------------------------------------------------------------
{Protheus.doc} GetTaxtotal
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@since 10/12/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetTaxtotal(cAlias as character, cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character)
Local cQuery    := ''  as character

If oGetTotalImp == Nil
    cQuery := "SELECT SUM(CASE WHEN FT_TIPO = ? THEN ? ELSE FT_VALICM END) AS ICMS, SUM(CASE WHEN FT_TIPO = ? THEN FT_VALICM ELSE ? END) AS ISS,"
    cQuery += "SUM(FT_VALIPI) AS IPI, SUM(FT_VALPIS) AS PIS, SUM(FT_VALCOF) AS COFINS"
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL IN (?) AND FT_TIPOMOV = ? AND FT_ENTRADA BETWEEN ? AND ? AND D_E_L_E_T_ = ?   "
    cQuery += " AND (FT_VALICM > ? OR FT_VALIPI > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND FT_VALCONT > ? "
	cQuery +="  AND FT_DTCANC = ? "
    oGetTotalImp := FwExecStatement():New()
    oGetTotalImp:SetQuery(ChangeQuery(cQuery))
Endif

If oGetTotalImp != Nil
    oGetTotalImp:setString(1, "S")
    oGetTotalImp:SetNumeric(2, 0)
    oGetTotalImp:setString(3, "S")
    oGetTotalImp:SetNumeric(4, 0)
    oGetTotalImp:SetIn(5, StrTokArr(cbranches, "," ))
    oGetTotalImp:setString(6, cmovimentType)
    oGetTotalImp:setString(7, StrTran(cperiodStart, "-", ""))
    oGetTotalImp:setString(8, StrTran(cperiodEnd, "-", ""))
    oGetTotalImp:setString(9, ' ')
    oGetTotalImp:SetNumeric(10, 0)
    oGetTotalImp:SetNumeric(11, 0)
    oGetTotalImp:SetNumeric(12, 0)
    oGetTotalImp:SetNumeric(13, 0)
    oGetTotalImp:SetNumeric(14, 0)
    oGetTotalImp:setString(15, "")
    oGetTotalImp:CBASEQUERY := oGetTotalImp:GETFIXQUERY()
    cAlias := oGetTotalImp:OpenAlias()
Endif
FWFreeObj(oGetTotalImp)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetperationalValue
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@since 10/12/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetperationalValue(cAlias as character, cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character, ctaxedMovements as character)
Local cQuery    := ''  as character

If oGetValorOpe == Nil
    cQuery := "SELECT SUM(FT_VALCONT) AS VALOPER"
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL IN (?) AND FT_TIPOMOV = ? AND FT_ENTRADA BETWEEN ? AND ? AND D_E_L_E_T_ = ?   "
    If ctaxedMovements == "1"
        cQuery += " AND (FT_VALICM  > ? OR FT_VALIPI  > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND  FT_VALCONT > ? "
    ElseIF ctaxedMovements == "2"
        cQuery += " AND (FT_VALICM  = ? AND FT_VALIPI  = ? AND FT_VALPIS = ? AND FT_VALCOF = ?) AND  FT_VALCONT > ? "
    Else 
        cQuery += " AND FT_VALCONT > ? "  
    EndIf  
	cQuery +="  AND FT_DTCANC = ? "
    oGetValorOpe := FwExecStatement():New()
    oGetValorOpe:SetQuery(ChangeQuery(cQuery))
Endif

If oGetValorOpe != Nil
    oGetValorOpe:SetIn(1, StrTokArr(cbranches, "," ))
    oGetValorOpe:setString(2, cmovimentType)
    oGetValorOpe:setString(3, StrTran(cperiodStart, "-", ""))
    oGetValorOpe:setString(4, StrTran(cperiodEnd, "-", ""))
    oGetValorOpe:setString(5, ' ')
    If ctaxedMovements <> "3"
        oGetValorOpe:SetNumeric(6, 0)
        oGetValorOpe:SetNumeric(7, 0)
        oGetValorOpe:SetNumeric(8, 0)
        oGetValorOpe:SetNumeric(9, 0)
        oGetValorOpe:SetNumeric(10, 0)
        oGetValorOpe:setString(11, "")
    Else
        oGetValorOpe:SetNumeric(6, 0)
        oGetValorOpe:setString(7, "")
    EndIf
    oGetValorOpe:CBASEQUERY := oGetValorOpe:GETFIXQUERY()
    cAlias := oGetValorOpe:OpenAlias()
Endif
FWFreeObj(oGetValorOpe)
Return 

