#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetCreditDebitICMS := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTCreditICMSFilter
API de Créditos de ICMS por parâmetros.
@author william.palma
@parâmetros de entrada do endpoint
@since 06/01/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/credits', description='API de crédito de ICMS')
Function MTCreditICMSFilter()
Local oJsResponse         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character
Local cTipoMovICM      := "E"

If valtype(cBranches) != 'U' .and. !empty(cBranches) .and. valtype(cPeriodStart) != 'U' .and. !empty(cPeriodStart) .and. valtype(cPeriodEnd) != 'U' .and. !empty(cPeriodEnd)
    oJsResponse := CreditDebitICMSFilter(cBranches, cPeriodStart, cPeriodEnd, cTipoMovICM)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------
{Protheus.doc} GET MTDebitICMSFilter
API de Débitos de ICMS por parâmetros.
@author william.palma
@parâmetros de entrada do endpoint
@since 06/01/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/icms/debts', description='API de débito de ICMS')
Function MTDebitICMSFilter()
Local oJsResponse         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character
Local cTipoMovICM      := "S"

If valtype(cBranches) != 'U' .and. !empty(cBranches) .and. valtype(cPeriodStart) != 'U' .and. !empty(cPeriodStart) .and. valtype(cPeriodEnd) != 'U' .and. !empty(cPeriodEnd)
    oJsResponse := CreditDebitICMSFilter(cBranches, cPeriodStart, cPeriodEnd, cTipoMovICM)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET CreditDebitICMSFilter
Função responsável por preencher o Json com com informações referentes aos débitos e créditos de ICMS
@author william.palma
@parâmetros de entrada do endpoint
@since 06/01/2025
-------------------------------------------------------------------*/
Static Function CreditDebitICMSFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTipoMovICM)
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local nPos2        := 0                         as numeric
Local cPeriod      := ""                        as character
Local cFilialICM   := ""                        as character
Local cEstado      := ""                        as character
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local cAlias2      := getNextAlias()            as character
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    
Local cDbMs        := ""                        as character

If valtype(oJParams) == 'J'
    If valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    EndIf
    If valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    EndIf
EndIf

cDbMs := TcGetDb()

If "MSSQL" $ cDbMs
    GetCreditDebitICMSMSSQL(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd, cTipoMovICM)
ElseIf "ORACLE" $ cDbMs
    GetCreditDebitICMSORACLE(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd, cTipoMovICM)
ElseIf "POSTGRES" $ cDbMs
    GetCreditDebitICMSPOSTGRES(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd, cTipoMovICM)
EndIf

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        nPageAux++
        If nPageAux == 1
            oJsonResp["items"] := {}
        Endif
        If nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            oJsonResp["items"][nPos]["period"]        :=   AllTrim((cAlias)->MESNOME)
            oJsonResp["items"][nPos]["branch"]        :=   (cAlias)->FILIAL    
            oJsonResp["items"][nPos]["uf"]            :=   (cAlias)->ESTADO
            oJsonResp["items"][nPos]["icms"]          :=   (cAlias)->ICMS
            oJsonResp["items"][nPos]["icmsSt"]        :=   (cAlias)->ICMSRET
            
            cFilialICM  := (cAlias)->FILIAL  
            cPeriod     := (cAlias)->PERIODO
            cEstado     := (cAlias)->ESTADO

            If "MSSQL" $ cDbMs
                GetCreditDebitICMSTotalMSSQL(@cAlias2, cBranches, cPeriodStart, cPeriodEnd, cPeriod, cFilialICM, cTipoMovICM, cEstado)
            ElseIf "ORACLE" $ cDbMs
                GetCreditDebitICMSTotalORACLE(@cAlias2, cBranches, cPeriodStart, cPeriodEnd, cPeriod, cFilialICM, cTipoMovICM, cEstado)
            ElseIf "POSTGRES" $ cDbMs
                GetCreditDebitICMSTotalPOSTGRES(@cAlias2, cBranches, cPeriodStart, cPeriodEnd, cPeriod, cFilialICM, cTipoMovICM, cEstado)
            EndIf

            oJsonResp["items"][nPos]["detail"] := {}
            nPos2 := 0 
            While (cAlias2)->(!Eof())

                nPos2++
                Aadd(oJsonResp["items"][nPos]["detail"], JsonObject():New() )
                oJsonResp["items"][nPos]["detail"][nPos2]["cfop"]        :=   (cAlias2)->CFOP
                oJsonResp["items"][nPos]["detail"][nPos2]["cst"]         :=   (cAlias2)->CST    
                oJsonResp["items"][nPos]["detail"][nPos2]["icms"]        :=   (cAlias2)->ICMS
                oJsonResp["items"][nPos]["detail"][nPos2]["icmsSt"]      :=   (cAlias2)->ICMSRET
                (cAlias2)->(DbSkip())
            EndDo
        Else
            lHasNext := .t.
            Exit
        EndIf
        (cAlias)->(DbSkip())
    EndDo
oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := 'value not found'
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
EndIf       
(cAlias)->(DBCloseArea())
If Select(cAlias2) > 0
    (cAlias2)->(DBCloseArea())
EndIf
Return oJsonResp:toJson()


/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSMSSQL()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSMSSQL(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTipoMovICM as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT DISTINCT CJZ.MESNOME AS MESNOME, CJZ.FILIAL AS FILIAL, CJZ.ESTADO AS ESTADO, SUM(CJZ.ICMS) AS ICMS, SUM(CJZ.ICMSRET) AS ICMSRET, "
    cQuery += "CJZ.MESNUM AS MESNUMERO, CJZ.ANONUM AS ANONUM, CJZ.PERIODO AS PERIODO "
    cQuery += "FROM (SELECT CJZ_FILIAL AS FILIAL, MONTH(CJZ_ENTRAD) AS MESNUM, YEAR(CJZ_ENTRAD) AS ANONUM, "
    cQuery += "SUBSTRING(CJZ_ENTRAD,5,2) + LEFT(CJZ_ENTRAD,4) AS PERIODO, "
	cQuery += "CASE "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jan/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Fev/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Mar/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Abr/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Mai/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jun/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jul/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Ago/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Set/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Out/' + LEFT(CJZ_ENTRAD,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Nov/' + LEFT(CJZ_ENTRAD,4) "
    cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Dez/' + LEFT(CJZ_ENTRAD,4) "
    cQuery += "ELSE ? END as MESNOME,"
    cQuery += "CJZ_ESTADO AS ESTADO,"
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE CJZ_ENTRAD BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND  CJZ_FILIAL IN (?) AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? "
    cQuery += "GROUP BY CJZ_FILIAL, CJZ_ESTADO, SUBSTRING(CJZ_ENTRAD,5,2), LEFT(CJZ_ENTRAD,4), MONTH(CJZ_ENTRAD), YEAR(CJZ_ENTRAD)) AS CJZ "
    cQuery += "GROUP BY CJZ.MESNOME, CJZ.MESNUM, CJZ.ANONUM, CJZ.FILIAL, CJZ.ESTADO, CJZ.PERIODO "
    cQuery += "ORDER BY CJZ.ANONUM, CJZ.MESNUM, CJZ.FILIAL, CJZ.ESTADO "
    cQuery += " OFFSET ( ? * ? ) ROWS "
    cQuery += " FETCH NEXT ? ROWS ONLY "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:setString(1, "01")
    oGetCreditDebitICMS:setString(2, "02")
    oGetCreditDebitICMS:setString(3, "03")
    oGetCreditDebitICMS:setString(4, "04")
    oGetCreditDebitICMS:setString(5, "05")
    oGetCreditDebitICMS:setString(6, "06")
    oGetCreditDebitICMS:setString(7, "07")
    oGetCreditDebitICMS:setString(8, "08")
    oGetCreditDebitICMS:setString(9, "09")
    oGetCreditDebitICMS:setString(10,"10")
    oGetCreditDebitICMS:setString(11,"11")
    oGetCreditDebitICMS:setString(12,"12")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:SetUnsafe(14, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(17, ' ')
    oGetCreditDebitICMS:SetIn(18, StrTokArr(cBranches, "," ))
    oGetCreditDebitICMS:SetNumeric(19, 0)
    oGetCreditDebitICMS:SetNumeric(20, 0)
    oGetCreditDebitICMS:setString(21, GetSubtrib())
    oGetCreditDebitICMS:setString(22, "S")
    oGetCreditDebitICMS:setString(23, cTipoMovICM)
    oGetCreditDebitICMS:setString(24, "")
    oGetCreditDebitICMS:setString(25, "")
    oGetCreditDebitICMS:SetUnsafe(26, cValToChar(nPage-1))
    oGetCreditDebitICMS:SetUnsafe(27, cValToChar(nPageSize))
    oGetCreditDebitICMS:SetUnsafe(28, cValToChar(nPageSize+1))
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSORACLE()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSORACLE(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTipoMovICM as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT DISTINCT CJZ.MESNOME AS MESNOME, CJZ.FILIAL AS FILIAL, CJZ.ESTADO AS ESTADO, SUM(CJZ.ICMS) AS ICMS, SUM(CJZ.ICMSRET) AS ICMSRET, "
    cQuery += "CJZ.MESNUM AS MESNUMERO, CJZ.ANONUM AS ANONUM, CJZ.PERIODO AS PERIODO "
    cQuery += "FROM (SELECT CJZ_FILIAL AS FILIAL, TO_CHAR(CAST(CJZ_ENTRAD AS DATE), 'MM') AS MESNUM, TO_CHAR(CAST(CJZ_ENTRAD AS DATE), 'YYYY') AS ANONUM, "
    cQuery += "SUBSTR(CJZ_ENTRAD,5,2) || SUBSTR(CJZ_ENTRAD,1,4) AS PERIODO, "
	cQuery += "CASE "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Jan/' || SUBSTR(CJZ_ENTRAD,1,4)  "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Fev/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Mar/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Abr/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Mai/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Jun/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Jul/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Ago/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Set/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Out/' || SUBSTR(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Nov/' || SUBSTR(CJZ_ENTRAD,1,4) "
    cQuery += "WHEN SUBSTR(CJZ_ENTRAD,5,2) = ? THEN 'Dez/' || SUBSTR(CJZ_ENTRAD,1,4) "
    cQuery += "ELSE ? END as MESNOME, "
    cQuery += "CJZ_ESTADO AS ESTADO, "
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE TO_CHAR(CAST(CJZ_ENTRAD AS DATE), 'YYYYMMDD') BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND  CJZ_FILIAL IN (?) AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? "
    cQuery += "GROUP BY CJZ_FILIAL, CJZ_ESTADO, SUBSTR(CJZ_ENTRAD,5,2), SUBSTR(CJZ_ENTRAD,1,4), TO_CHAR(CAST(CJZ_ENTRAD AS DATE), 'MM'), TO_CHAR(CAST(CJZ_ENTRAD AS DATE), 'YYYY')) CJZ "
    cQuery += "GROUP BY CJZ.MESNOME, CJZ.MESNUM, CJZ.ANONUM, CJZ.FILIAL, CJZ.ESTADO, CJZ.PERIODO "
    cQuery += "ORDER BY CJZ.ANONUM, CJZ.MESNUM, CJZ.FILIAL, CJZ.ESTADO "
    cQuery += " OFFSET ( ? * ? ) ROWS "
    cQuery += " FETCH NEXT ? ROWS ONLY "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:setString(1, "01")
    oGetCreditDebitICMS:setString(2, "02")
    oGetCreditDebitICMS:setString(3, "03")
    oGetCreditDebitICMS:setString(4, "04")
    oGetCreditDebitICMS:setString(5, "05")
    oGetCreditDebitICMS:setString(6, "06")
    oGetCreditDebitICMS:setString(7, "07")
    oGetCreditDebitICMS:setString(8, "08")
    oGetCreditDebitICMS:setString(9, "09")
    oGetCreditDebitICMS:setString(10,"10")
    oGetCreditDebitICMS:setString(11,"11")
    oGetCreditDebitICMS:setString(12,"12")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:SetUnsafe(14, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(17, ' ')
    oGetCreditDebitICMS:SetIn(18, StrTokArr(cBranches, "," ))
    oGetCreditDebitICMS:SetNumeric(19, 0)
    oGetCreditDebitICMS:SetNumeric(20, 0)
    oGetCreditDebitICMS:setString(21, GetSubtrib())
    oGetCreditDebitICMS:setString(22, "S")
    oGetCreditDebitICMS:setString(23, cTipoMovICM)
    oGetCreditDebitICMS:setString(24, "")
    oGetCreditDebitICMS:setString(25, "")
    oGetCreditDebitICMS:SetUnsafe(26, cValToChar(nPage-1))
    oGetCreditDebitICMS:SetUnsafe(27, cValToChar(nPageSize))
    oGetCreditDebitICMS:SetUnsafe(28, cValToChar(nPageSize+1))
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSPOSTGRES()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSPOSTGRES(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTipoMovICM as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT DISTINCT CJZ.MESNOME AS MESNOME, CJZ.FILIAL AS FILIAL, CJZ.ESTADO AS ESTADO, SUM(CJZ.ICMS) AS ICMS, SUM(CJZ.ICMSRET) AS ICMSRET, "
    cQuery += "TO_NUMBER(CJZ.MESNUM,'99') AS MESNUM, TO_NUMBER(CJZ.ANONUM,'9999') AS ANONUM, CJZ.PERIODO AS PERIODO "
    cQuery += "FROM (SELECT CJZ_FILIAL AS FILIAL, TO_CHAR(CJZ_ENTRAD:: DATE, 'MM') AS MESNUM, TO_CHAR(CJZ_ENTRAD:: DATE, 'YYYY') AS ANONUM, "
    cQuery += "SUBSTRING(CJZ_ENTRAD,5,2) || SUBSTRING(CJZ_ENTRAD,1,4) AS PERIODO, "
	cQuery += "CASE "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jan/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Fev/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Mar/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Abr/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Mai/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jun/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Jul/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Ago/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Set/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Out/' || SUBSTRING(CJZ_ENTRAD,1,4) "
	cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Nov/' || SUBSTRING(CJZ_ENTRAD,1,4) "
    cQuery += "WHEN SUBSTRING(CJZ_ENTRAD,5,2) = ? THEN 'Dez/' || SUBSTRING(CJZ_ENTRAD,1,4) "
    cQuery += "ELSE ? END as MESNOME,"
    cQuery += "CJZ_ESTADO AS ESTADO,"
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE CJZ_ENTRAD BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND  CJZ_FILIAL IN (?) AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? "
    cQuery += "GROUP BY CJZ_FILIAL, CJZ_ESTADO, SUBSTRING(CJZ_ENTRAD,5,2), SUBSTRING(CJZ_ENTRAD,1,4), TO_CHAR(CJZ_ENTRAD:: DATE, 'MM'), TO_CHAR(CJZ_ENTRAD:: DATE, 'YYYY')) AS CJZ "
    cQuery += "GROUP BY CJZ.MESNOME, CJZ.MESNUM, CJZ.ANONUM, CJZ.FILIAL, CJZ.ESTADO, CJZ.PERIODO "
    cQuery += "ORDER BY TO_NUMBER(CJZ.ANONUM,'9999'), TO_NUMBER(CJZ.MESNUM,'99'), CJZ.FILIAL, CJZ.ESTADO "
    cQuery += " OFFSET ( ? * ? ) ROWS "
    cQuery += " FETCH NEXT ? ROWS ONLY "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:setString(1, "01")
    oGetCreditDebitICMS:setString(2, "02")
    oGetCreditDebitICMS:setString(3, "03")
    oGetCreditDebitICMS:setString(4, "04")
    oGetCreditDebitICMS:setString(5, "05")
    oGetCreditDebitICMS:setString(6, "06")
    oGetCreditDebitICMS:setString(7, "07")
    oGetCreditDebitICMS:setString(8, "08")
    oGetCreditDebitICMS:setString(9, "09")
    oGetCreditDebitICMS:setString(10,"10")
    oGetCreditDebitICMS:setString(11,"11")
    oGetCreditDebitICMS:setString(12,"12")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:SetUnsafe(14, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(15, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(16, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(17, ' ')
    oGetCreditDebitICMS:SetIn(18, StrTokArr(cBranches, "," ))
    oGetCreditDebitICMS:SetNumeric(19, 0)
    oGetCreditDebitICMS:SetNumeric(20, 0)
    oGetCreditDebitICMS:setString(21, GetSubtrib())
    oGetCreditDebitICMS:setString(22, "S")
    oGetCreditDebitICMS:setString(23, cTipoMovICM)
    oGetCreditDebitICMS:setString(24, "")
    oGetCreditDebitICMS:setString(25, "")
    oGetCreditDebitICMS:SetUnsafe(26, cValToChar(nPage-1))
    oGetCreditDebitICMS:SetUnsafe(27, cValToChar(nPageSize))
    oGetCreditDebitICMS:SetUnsafe(28, cValToChar(nPageSize+1))
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSTotalMSSQL()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSTotalMSSQL(cAlias2 as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cPeriod as character, cFilialICM as character, cTipoMovICM as character, cEstado as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT CJZ_CFOP AS CFOP, "
    cQuery += "SUBSTRING(CJZ_CLASFI,2,2) AS CST, "
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE SUBSTRING(CJZ_ENTRAD,5,2) + LEFT(CJZ_ENTRAD,4) = ? AND CJZ_ENTRAD BETWEEN ? AND ? "
    cQuery += "AND D_E_L_E_T_ = ? AND  CJZ_FILIAL = ? AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? AND CJZ_ESTADO = ? "
    cQuery += "GROUP BY CJZ_CFOP, SUBSTRING(CJZ_CLASFI,2,2) "
    cQuery += "ORDER BY CJZ_CFOP, SUBSTRING(CJZ_CLASFI,2,2) "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
EndIf

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:SetUnsafe(1, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(2, cPeriod)
    oGetCreditDebitICMS:setString(3, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(4, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(5, ' ')
    oGetCreditDebitICMS:setString(6, cFilialICM)
    oGetCreditDebitICMS:SetNumeric(7, 0)
    oGetCreditDebitICMS:SetNumeric(8, 0)
    oGetCreditDebitICMS:setString(9, GetSubtrib())
    oGetCreditDebitICMS:setString(10, "S")
    oGetCreditDebitICMS:setString(11, cTipoMovICM)
    oGetCreditDebitICMS:setString(12, "")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:setString(14, cEstado)
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias2 := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSTotalORACLE()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSTotalORACLE(cAlias2 as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cPeriod as character, cFilialICM as character, cTipoMovICM as character, cEstado as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT CJZ_CFOP AS CFOP, "
    cQuery += "SUBSTR(CJZ_CLASFI,2,2) AS CST, "
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE SUBSTR(CJZ_ENTRAD,5,2) || SUBSTR(CJZ_ENTRAD,1,4) = ? AND CJZ_ENTRAD BETWEEN ? AND ? "  
    cQuery += "AND D_E_L_E_T_ = ? AND  CJZ_FILIAL = ? AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? AND CJZ_ESTADO = ? "
    cQuery += "GROUP BY CJZ_CFOP, SUBSTR(CJZ_CLASFI,2,2) "
    cQuery += "ORDER BY CJZ_CFOP, SUBSTR(CJZ_CLASFI,2,2) "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:SetUnsafe(1, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(2, cPeriod)
    oGetCreditDebitICMS:setString(3, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(4, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(5, ' ')
    oGetCreditDebitICMS:setString(6, cFilialICM)
    oGetCreditDebitICMS:SetNumeric(7, 0)
    oGetCreditDebitICMS:SetNumeric(8, 0)
    oGetCreditDebitICMS:setString(9, GetSubtrib())
    oGetCreditDebitICMS:setString(10, "S")
    oGetCreditDebitICMS:setString(11, cTipoMovICM)
    oGetCreditDebitICMS:setString(12, "")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:setString(14, cEstado)
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias2 := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 

/*--------------------------------------------------------------------
{Protheus.doc} GetCreditDebitICMSTotalPOSTGRES()
(Responsável por executar a consulta aos campos da tabela CJZ)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetCreditDebitICMSTotalPOSTGRES(cAlias2 as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cPeriod as character, cFilialICM as character, cTipoMovICM as character, cEstado as character)
Local cQuery    := ''  as character

If oGetCreditDebitICMS == Nil
    cQuery := "SELECT CJZ_CFOP AS CFOP, "
    cQuery += "SUBSTRING(CJZ_CLASFI,2,2) AS CST, "
    cQuery += "SUM(CJZ_VALICM) ICMS, "
    cQuery += "SUM(CJZ_ICMSRE) ICMSRET "
    cQuery += "FROM ? CJZ "
    cQuery += "WHERE SUBSTRING(CJZ_ENTRAD,5,2) || SUBSTRING(CJZ_ENTRAD,1,4) = ? AND CJZ_ENTRAD BETWEEN ? AND ? "
    cQuery += "AND D_E_L_E_T_ = ? AND  CJZ_FILIAL = ? AND ((CJZ_VALICM > ?) OR (CJZ_ICMSRE > ? AND CJZ_ESTADO LIKE (?))) "
    cQuery += "AND CJZ_TIPO != ? AND CJZ_TIPOMO = ? AND CJZ_CFOP != ? AND CJZ_CLASFI != ? AND CJZ_ESTADO = ? "
    cQuery += "GROUP BY CJZ_CFOP, SUBSTRING(CJZ_CLASFI,2,2) "
    cQuery += "ORDER BY CJZ_CFOP, SUBSTRING(CJZ_CLASFI,2,2) "
    oGetCreditDebitICMS := FwExecStatement():New()
    oGetCreditDebitICMS:SetQuery(ChangeQuery(cQuery))
Endif

If oGetCreditDebitICMS != Nil
    oGetCreditDebitICMS:SetUnsafe(1, FIS319SqlName("CJZ"))
    oGetCreditDebitICMS:setString(2, cPeriod)
    oGetCreditDebitICMS:setString(3, StrTran(cPeriodStart, "-", ""))
    oGetCreditDebitICMS:setString(4, StrTran(cPeriodEnd, "-", ""))
    oGetCreditDebitICMS:setString(5, ' ')
    oGetCreditDebitICMS:setString(6, cFilialICM)
    oGetCreditDebitICMS:SetNumeric(7, 0)
    oGetCreditDebitICMS:SetNumeric(8, 0)
    oGetCreditDebitICMS:setString(9, GetSubtrib())
    oGetCreditDebitICMS:setString(10, "S")
    oGetCreditDebitICMS:setString(11, cTipoMovICM)
    oGetCreditDebitICMS:setString(12, "")
    oGetCreditDebitICMS:setString(13, "")
    oGetCreditDebitICMS:setString(14, cEstado)
    oGetCreditDebitICMS:CBASEQUERY := oGetCreditDebitICMS:GETFIXQUERY()
    cAlias2 := oGetCreditDebitICMS:OpenAlias()
Endif
FWFreeObj(oGetCreditDebitICMS)
Return 
