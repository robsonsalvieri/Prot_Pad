#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} pisCofinsDebitsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Débitos de PIS COFINS do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 12/05/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="pisCofinsDebitsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class PisCofinsDebitsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 12/05/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class PisCofinsDebitsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0075 + FISA318F4GetDisplayNameComplement()) //"CARD_PIS-COFINS_DEBITOS_"
    ::setDescription(STR0076) //"Abertura dos dados do Card de Débitos de ICMS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 12/05/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class PisCofinsDebitsBusinessObject
    Local nISynthetic   as numeric
    Local nIAnalytical  as numeric
    Local aDebitItems   as array
    Local oSynthetic    as object
    Local oAnalytical   as object
    Local oDebits       as object    
    Local oFilterParams as object

    oFilterParams   := oFilter:getParameters()
    aDebitItems     := FISA318F1SVGetApiData(oFilterParams)
    oDebits         := JsonObject():new()

    For nISynthetic := 1 To Len(aDebitItems)
        oSynthetic        := aDebitItems[nISynthetic]
        oDebits["period"]  := oSynthetic["period"]
        oDebits["branch"]  := DecodeUTF8(oSynthetic["branch"])

        For nIAnalytical := 1 To Len(oSynthetic["detail"])
            oAnalytical             := oSynthetic["detail"][nIAnalytical]
            oDebits["cst"]          := oAnalytical["cst"]
            oDebits["pisDebits"]    := oAnalytical["pisDebits"]
            oDebits["cofinsDebits"] := oAnalytical["cofinsDebits"]

            ::appendData(oDebits)
        Next nIAnalytical
    Next nISynthetic

    FwFreeArray(aDebitItems)
    FwFreeObj(oSynthetic)
    FwFreeObj(oAnalytical)
    FwFreeObj(oDebits)
    FwFreeObj(oFilterParams)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 12/05/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class PisCofinsDebitsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("pis-cofins")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"      , STR0009   , "string", STR0009 , "period"      ) //"Período"
    ::addProperty("branch"      , STR0010   , "string", STR0010 , "branch"      ) //"Filial"
    ::addProperty("cst"         , STR0057   , "string", STR0057 , "cst"         ) //"CST"
    ::addProperty("pisDebits"   , STR0030   , "number", STR0030 , "pisDebits"   ) //"PIS"
    ::addProperty("cofinsDebits", STR0031   , "number", STR0031 , "cofinsDebits") //"COFINS"

    FwFreeArray(aSVParams)
Return ::oSchema
