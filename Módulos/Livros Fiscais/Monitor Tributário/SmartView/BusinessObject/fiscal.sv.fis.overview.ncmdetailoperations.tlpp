#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} overviewNcmDetailOperationsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Detalhamento das Operações de NCM do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 11/03/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="overviewNcmDetailOperationsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class OverviewNcmDetailOperationsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 11/03/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class OverviewNcmDetailOperationsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0032 + strTran(dToC(date()), "/", "-") + "_" + strTran(time(), ":", "-")) //"CARD_VISAO-GERAL_DETALHAMENTO-OPERACOES-DE-NCM_"
    ::setDescription(STR0033) //"Abertura dos dados do Card de Detalhamento das Operações de NCM da Visão Geral do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 11/03/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class OverviewNcmDetailOperationsBusinessObject
    Local nINcm                 as numeric
    Local nIProduct             as numeric
    Local aNcmDetailOperItems   as array
    Local oNcm                  as object
    Local oProduct              as object
    Local oNcmDetailOper        as object
    Local oFilterParams         as object

    oFilterParams       := oFilter:getParameters()
    aNcmDetailOperItems := FISA318F1SVGetApiData(oFilterParams)
    oNcmDetailOper      := JsonObject():new()

    For nINcm := 1 To Len(aNcmDetailOperItems)
        oNcm                        := aNcmDetailOperItems[nINcm]
        oNcmDetailOper["branch"]    := oNcm["branch"]
        oNcmDetailOper["ncmNbs"]    := oNcm["ncmNbs"]
        oNcmDetailOper["code"]      := oNcm["code"]

        For nIProduct := 1 To Len(oNcm["detail"])
            oProduct                            := oNcm["detail"][nIProduct]
            oNcmDetailOper["productService"]    := oProduct["productService"]
            oNcmDetailOper["operationalValue"]  := oProduct["operationalValue"]
            oNcmDetailOper["icms"]              := oProduct["icms"]
            oNcmDetailOper["iss"]               := oProduct["iss"]
            oNcmDetailOper["ipi"]               := oProduct["ipi"]
            oNcmDetailOper["pis"]               := oProduct["pis"]
            oNcmDetailOper["cofins"]            := oProduct["cofins"]

            ::appendData(oNcmDetailOper)
        Next nIProduct
    Next nINcm

    FwFreeObj(oNcm)
    FwFreeObj(oProduct)
    FwFreeObj(oNcmDetailOper)
    FwFreeObj(oFilterParams)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 11/03/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class OverviewNcmDetailOperationsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("overview")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("branch"          , STR0010   , "string", STR0010 , "branch"          )   //"Filial"
    ::addProperty("ncmNbs"          , STR0034   , "string", STR0034 , "ncmNbs"          )   //"NCM/NBS"  
    ::addProperty("code"            , STR0035   , "string", STR0035 , "code"            )   //"Código NCM/NBS"
    ::addProperty("productService"  , STR0036   , "string", STR0036 , "productService"  )   //"Código Produto/Serviço"
    ::addProperty("operationalValue", STR0027   , "number", STR0027 , "operationalValue")   //"Valor Operacional"
    ::addProperty("icms"            , STR0028   , "number", STR0028 , "icms"            )   //"ICMS"
    ::addProperty("iss"             , STR0029   , "number", STR0029 , "iss"             )   //"ISS"
    ::addProperty("ipi"             , STR0017   , "number", STR0017 , "ipi"             )   //"IPI"
    ::addProperty("pis"             , STR0030   , "number", STR0030 , "pis"             )   //"PIS"
    ::addProperty("cofins"          , STR0031   , "number", STR0031 , "cofins"          )   //"COFINS"
Return ::oSchema
