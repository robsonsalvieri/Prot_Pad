#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} icmsCreditsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Créditos de ICMS do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 05/04/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="icmsCreditsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class IcmsCreditsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 05/04/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class IcmsCreditsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0059 + FISA318F4GetDisplayNameComplement()) //"CARD_ICMS_CREDITOS_"
    ::setDescription(STR0060) //"Abertura dos dados do Card de Créditos de ICMS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 05/04/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class IcmsCreditsBusinessObject
    Local nISynthetic   as numeric
    Local nIAnalytical  as numeric
    Local aCreditItems  as array
    Local oSynthetic    as object
    Local oAnalytical   as object
    Local oCredits      as object    
    Local oFilterParams as object

    oFilterParams   := oFilter:getParameters()
    aCreditItems    := FISA318F1SVGetApiData(oFilterParams)
    oCredits        := JsonObject():new()

    For nISynthetic := 1 To Len(aCreditItems)
        oSynthetic          := aCreditItems[nISynthetic]
        oCredits["period"]  := oSynthetic["period"]
        oCredits["branch"]  := oSynthetic["branch"]
        oCredits["uf"]      := oSynthetic["uf"]

        For nIAnalytical := 1 To Len(oSynthetic["detail"])
            oAnalytical         := oSynthetic["detail"][nIAnalytical]
            oCredits["cfop"]    := oAnalytical["cfop"]
            oCredits["cst"]     := oAnalytical["cst"]
            oCredits["icms"]    := oAnalytical["icms"]
            oCredits["icmsSt"]  := oAnalytical["icmsSt"]

            ::appendData(oCredits)
        Next nIAnalytical
    Next nISynthetic

    FwFreeObj(oSynthetic)
    FwFreeObj(oAnalytical)
    FwFreeObj(oCredits)
    FwFreeObj(oFilterParams)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 05/04/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class IcmsCreditsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("icms")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"  , STR0009   , "string", STR0009 , "period"  ) //"Período"
    ::addProperty("branch"  , STR0010   , "string", STR0010 , "branch"  ) //"Filial"
    ::addProperty("uf"      , STR0056   , "string", STR0056 , "uf"      ) //"Estado"
    ::addProperty("cfop"    , STR0015   , "string", STR0015 , "cfop"    ) //"CFOP"
    ::addProperty("cst"     , STR0057   , "string", STR0057 , "cst"     ) //"CST"
    ::addProperty("icms"    , STR0028   , "number", STR0028 , "icms"    ) //"ICMS"
    ::addProperty("icmsSt"  , STR0058   , "number", STR0058 , "icmsSt"  ) //"ICMS ST"

    FwFreeArray(aSVParams)
Return ::oSchema
