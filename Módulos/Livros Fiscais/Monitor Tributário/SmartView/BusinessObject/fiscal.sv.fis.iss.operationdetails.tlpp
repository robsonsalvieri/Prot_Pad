#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} issOperationDetailsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Detalhamento das Operações de ISS do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 26/03/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="issOperationDetailsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class IssOperationDetailsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 26/03/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class IssOperationDetailsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0048 + FISA318F4GetDisplayNameComplement()) //"CARD_ISS_DETALHAMENTO_DAS_OPERACOES_"
    ::setDescription(STR0049) //"Abertura dos dados do Card de Detalhamento das Operações da Visão de ISS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 26/03/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class IssOperationDetailsBusinessObject
    Local aOperationDetailsItems    as array
    Local oFilterParams             as object

    oFilterParams           := oFilter:getParameters()
    aOperationDetailsItems  := FISA318F1SVGetApiData(oFilterParams)

    aEval(aOperationDetailsItems, {|oOperationDetails| ::appendData(oOperationDetails)})

    FwFreeObj(oFilterParams)
    FwFreeArray(aOperationDetailsItems)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 26/03/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class IssOperationDetailsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("iss")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"          , STR0009   , "string", STR0009 , "period"          )   //"Período"
    ::addProperty("branch"          , STR0010   , "string", STR0010 , "branch"          )   //"Filial"
    ::addProperty("municipality"    , STR0050   , "string", STR0050 , "municipality"    )   //"Município"
    ::addProperty("payable"         , STR0051   , "number", STR0051 , "payable"         )   //"ISS Devido"
    ::addProperty("withheldAtEntry" , STR0052   , "number", STR0052 , "withheldAtEntry" )   //"ISS Retido Entrada"
    ::addProperty("exempt"          , STR0053   , "number", STR0053 , "exempt"          )   //"Isento"

    FwFreeArray(aSVParams)
Return ::oSchema
