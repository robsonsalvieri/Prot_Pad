#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} icmsCalculationAdjustmentsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Ajustes de Apuração de ICMS do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 20/05/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="icmsCalculationAdjustmentsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class IcmsCalculationAdjustmentsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 20/05/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class IcmsCalculationAdjustmentsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0082 + strTran(dToC(date()), "/", "-") + "_" + strTran(time(), ":", "-")) //"CARD_ICMS_AJUSTES-DE-APURACAO_"
    ::setDescription(STR0083) //"Abertura dos dados do Card de Ajustes de Apuração de ICMS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 20/05/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class IcmsCalculationAdjustmentsBusinessObject
    Local nIPeriod          as numeric
    Local nIBranch          as numeric
    Local aCalcAdjustItems  as array
    Local oPeriod           as object
    Local oBranch           as object
    Local oFilterParams     as object
    Local oCalcAdjustItems  as object

    oFilterParams       := oFilter:getParameters()
    aCalcAdjustItems    := FISA318F1SVGetApiData(oFilterParams)
    oCalcAdjustItems    := JsonObject():new()

    For nIPeriod := 1 To Len(aCalcAdjustItems)
        oPeriod                     := aCalcAdjustItems[nIPeriod]
        oCalcAdjustItems["period"]  := DecodeUTF8(oPeriod["period"])

        For nIBranch := 1 To Len(oPeriod["detail"])
            oBranch                         := oPeriod["detail"][nIBranch]
            oCalcAdjustItems["branch"]      := DecodeUTF8(oBranch["branch"])
            oCalcAdjustItems["credit"]      := oBranch["credit"]
            oCalcAdjustItems["debit"]       := oBranch["debit"]
            oCalcAdjustItems["deduction"]   := oBranch["deduction"]

            ::appendData(oCalcAdjustItems)

        Next nIBranch
    Next nIPeriod

    FwFreeArray(aCalcAdjustItems)
    FwFreeObj(oPeriod)
    FwFreeObj(oBranch)
    FwFreeObj(oFilterParams)
    FwFreeObj(oCalcAdjustItems)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 20/05/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class IcmsCalculationAdjustmentsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("icms")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"          , STR0009, "string", STR0009, "period"          ) //"Período"
    ::addProperty("branch"          , STR0010, "string", STR0010, "branch"          ) //"Filial"
	::addProperty("credit"          , STR0011, "number", STR0011, "credit"          ) //"Ajustes de Crédito"
    ::addProperty("debit"           , STR0012, "number", STR0012, "debit"           ) //"Ajustes de Débito"
    ::addProperty("deduction"       , STR0084, "number", STR0084, "deduction"       ) //"Dedução"
Return ::oSchema
