#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.framework.treports.integratedprovider.th"
#include "FISA318.CH"
    
namespace totvs.framework.treports.integratedprovider

/*/{Protheus.doc} pisCofinsCreditsBusinessObject
    Objeto de Negócio que abre no formato Visão de Dados da Tecnologia SmartView o Card de Créditos de PIS COFINS do TOTVS Inteligência Tributária
    
    @author Fábio Mendonça
    @since 14/05/2025
    @version 1.0

    @see https://tdn.totvs.com/pages/releaseview.action?pageId=625448935
/*/
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", name="pisCofinsCreditsBusinessObject", country="BRA", initialRelease="12.1.2310")
Class PisCofinsCreditsBusinessObject From totvs.framework.treports.integratedprovider.IntegratedProvider
    Public Method new()         as object
    Public Method getData()     as object
    Public Method getSchema()   as object
EndClass

/*{Protheus.doc} new
    Construtor da classe
    
    @author Fábio Mendonça
    @since 14/05/2025
    @version 1.0

    @return object: self
*/  
Method new() as object Class PisCofinsCreditsBusinessObject
    _Super:new()

    ::appendArea(STR0006) //"Monitor Tributário"
    ::setDisplayName(STR0080 + FISA318F4GetDisplayNameComplement()) //"CARD_PIS-COFINS_CREDITOS_"
    ::setDescription(STR0081) //"Abertura dos dados do Card de Créditos de ICMS do TOTVS Inteligência Tributária no formato Visão de Dados da Tecnologia SmartView"
Return self
 
/*/{Protheus.doc} getData
    Retorna dados do objeto de negócio
    
    @author Fábio Mendonça
    @since 14/05/2025
    @version 1.0
    @param nPage, numérico, Indica a página atual do relatório
    @param oFilter, objeto, Objeto com filtro do SmartView    
    @return object: self:oData
/*/  
Method getData(nPage as numeric, oFilter as object) as object Class PisCofinsCreditsBusinessObject
    Local nISynthetic   as numeric
    Local nIAnalytical  as numeric
    Local aCreditItems  as array
    Local oSynthetic    as json
    Local oAnalytical   as json
    Local oCredits      as json    
    Local oFilterParams as json

    oFilterParams   := oFilter:getParameters()
    aCreditItems    := FISA318F1SVGetApiData(oFilterParams)
    oCredits        := JsonObject():new()

    For nISynthetic := 1 To Len(aCreditItems)
        oSynthetic          := aCreditItems[nISynthetic]
        oCredits["period"]  := oSynthetic["period"]
        oCredits["branch"]  := DecodeUTF8(oSynthetic["branch"])

        For nIAnalytical := 1 To Len(oSynthetic["detail"])
            oAnalytical                 := oSynthetic["detail"][nIAnalytical]
            oCredits["cst"]             := DecodeUTF8(oAnalytical["cst"])
            oCredits["creditType"]      := oAnalytical["creditType"]
            oCredits["pisCredits"]      := oAnalytical["pisCredits"]
            oCredits["cofinsCredits"]   := oAnalytical["cofinsCredits"]

            ::appendData(oCredits)
        Next nIAnalytical
    Next nISynthetic

    FwFreeArray(aCreditItems)
    FwFreeObj(oSynthetic)
    FwFreeObj(oAnalytical)
    FwFreeObj(oCredits)
    FwFreeObj(oFilterParams)
Return ::oData
 
/*/{Protheus.doc} getSchema
    Retorna estrutura dos campos

    @author Fábio Mendonça
    @since 14/05/2025
    @version 1.0
    @return object: self:oSchema
/*/  
Method getSchema() as object Class PisCofinsCreditsBusinessObject
    Local aSVParams   as array

    // Coleta parâmetros do ON
    aSVParams := FISA318F2SVGetParametersByDashboard("pis-cofins")
    aEval(aSVParams, {|aParam| ::addParameter(aParam[1], aParam[2], aParam[3], aParam[4])})

    // Colunas do ON
    ::addProperty("period"          , STR0009   , "string", STR0009 , "period"          ) //"Período"
    ::addProperty("branch"          , STR0010   , "string", STR0010 , "branch"          ) //"Filial"    
    ::addProperty("cst"             , STR0057   , "string", STR0057 , "cst"             ) //"CST"
    ::addProperty("creditType"      , STR0077   , "string", STR0077 , "creditType"      ) //"Tipo de Crédito"
    ::addProperty("pisCredits"      , STR0078   , "number", STR0078 , "pisCredits"      ) //"Créditos PIS"
    ::addProperty("cofinsCredits"   , STR0079   , "number", STR0079 , "cofinsCredits"   ) //"Créditos COFINS"

    FwFreeArray(aSVParams)
Return ::oSchema
