#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetNcmDetailOper := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTDetailOperationsNcm
API de ddetalhe de operação por NCM/NBS.
@author william.palma
@parâmetros de entrada do endpoint
@since 06/01/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/overview/ncm-detail-operations', description='API de detalhe de operações')
Function MTDetailOperationsNcm()
Local oJsResponse         as json
Local cbranches        := oRest:getQueryRequest()['branches']       as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']    as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']      as character
Local cmovimentType    := oRest:getQueryRequest()['movimentType']   as character
Local ctaxedMovements  := oRest:getQueryRequest()['taxedMovements'] as character

If valtype(cmovimentType) != 'U' .and. !empty(cmovimentType) .and. valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd) .and. valtype(ctaxedMovements) != 'U' .and. !empty(ctaxedMovements)
    oJsResponse := DetailOperationsNcmFilter(cbranches, cperiodStart, cperiodEnd, cmovimentType, ctaxedMovements)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET DetailOperationsNcmFilter
Função responsável por preencher o Json com com informações referentes aos detalhes de oprações por NCM/NBS
@author william.palma
@parâmetros de entrada do endpoint
@since 06/01/2025
-------------------------------------------------------------------*/
Static Function DetailOperationsNcmFilter(cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character, ctaxedMovements as character)
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local nPos2        := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local cAlias2      := getNextAlias()            as character
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    
Local cFilialQ2    := ""                        as character
Local cNcmNbsQ2    := ""                        as character

If valtype(oJParams) == 'J'
    If valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    EndIf
    If valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    EndIf
EndIf

GetNcmDetailOper(@cAlias, nPage, nPageSize, cbranches, cperiodStart, cperiodEnd, cmovimentType, ctaxedMovements) 

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        nPageAux++
        If nPageAux == 1
            oJsonResp["items"] := {}
        Endif
        If nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            oJsonResp["items"][nPos]["branch"]              :=   (cAlias)->FILIAL
            oJsonResp["items"][nPos]["ncmNbs"]              :=   (cAlias)->NCMNBS     
            oJsonResp["items"][nPos]["code"]                :=   (cAlias)->CODE
            oJsonResp["items"][nPos]["operationalValue"]    :=   (cAlias)->VALOROPER
            oJsonResp["items"][nPos]["icms"]                :=   (cAlias)->ICMS
            oJsonResp["items"][nPos]["iss"]                 :=   (cAlias)->ISS
            oJsonResp["items"][nPos]["ipi"]                 :=   (cAlias)->IPI
            oJsonResp["items"][nPos]["pis"]                 :=   (cAlias)->PIS
            oJsonResp["items"][nPos]["cofins"]              :=   (cAlias)->COFINS
            
            cFilialQ2 := (cAlias)->FILIAL
            cNcmNbsQ2 := (cAlias)->CODE

            GetNcmDetailOperQ2(@cAlias2, nPage, nPageSize, cperiodStart, cperiodEnd, cmovimentType, cFilialQ2, cNcmNbsQ2, ctaxedMovements) 

            oJsonResp["items"][nPos]["detail"] := {}
            nPos2 := 0 
            While (cAlias2)->(!Eof())

                nPos2++
                Aadd(oJsonResp["items"][nPos]["detail"], JsonObject():New() )
                oJsonResp["items"][nPos]["detail"][nPos2]["productService"]        :=  AllTrim((cAlias2)->PRODUTO)
                oJsonResp["items"][nPos]["detail"][nPos2]["operationalValue"]      :=   (cAlias2)->VALOROPER    
                oJsonResp["items"][nPos]["detail"][nPos2]["icms"]                  :=   (cAlias2)->ICMS
                oJsonResp["items"][nPos]["detail"][nPos2]["iss"]                   :=   (cAlias2)->ISS
                oJsonResp["items"][nPos]["detail"][nPos2]["ipi"]                   :=   (cAlias2)->IPI
                oJsonResp["items"][nPos]["detail"][nPos2]["pis"]                   :=   (cAlias2)->PIS
                oJsonResp["items"][nPos]["detail"][nPos2]["cofins"]                :=   (cAlias2)->COFINS

                (cAlias2)->(DbSkip())
            EndDo
        Else
            lHasNext := .t.
            Exit
        EndIf
        (cAlias)->(DbSkip())
    EndDo
oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code'] := 400
    oJsonResp['message'] := "value not found"
    oJsonResp['detailedMessage'] := "Dados não encontrados para o período e/ou filial informados."
EndIf       
(cAlias)->(DBCloseArea())
If Select(cAlias2) > 0
    (cAlias2)->(DBCloseArea())
EndIf
Return oJsonResp:toJson()


/*--------------------------------------------------------------------
{Protheus.doc} GetNcmDetailOper()
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetNcmDetailOper(cAlias as character, nPage as numeric, nPageSize as numeric, cbranches as character, cperiodStart as character, cperiodEnd as character, cmovimentType as character, ctaxedMovements as character)
Local cQuery    := ''  as character

If oGetNcmDetailOper == Nil
    cQuery := " SELECT DISTINCT SFT.FILIAL AS FILIAL, SFT.NCMNBS AS NCMNBS, SFT.CODE AS CODE, SUM(SFT.VALOROPER) AS VALOROPER, SUM(SFT.ICMS) AS ICMS, SUM(SFT.ISS) AS ISS, "
    cQuery += " SUM(SFT.IPI) AS IPI, SUM(SFT.PIS) AS PIS, SUM(SFT.COFINS) AS COFINS"
    cQuery += " FROM (SELECT FT_FILIAL AS FILIAL, FT_TIPO AS TIPO,"
    cQuery += " CASE WHEN FT_CODISS = ? THEN ? ELSE ? END AS NCMNBS,"
    cQuery +=  "CASE WHEN FT_CODISS = ? THEN FT_POSIPI ELSE FT_CODISS END AS CODE, "
    cQuery += " SUM(FT_VALCONT) AS VALOROPER, SUM(CASE WHEN FT_TIPO IN (?) THEN FT_VALICM ELSE ? END) AS ICMS, SUM(CASE WHEN FT_TIPO = ? THEN FT_VALICM ELSE ? END) AS ISS,  "
	cQuery += " SUM(FT_VALIPI) AS IPI, SUM(FT_VALPIS) AS PIS, SUM(FT_VALCOF) AS COFINS "
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL IN (?) "
    cQuery += " AND FT_TIPOMOV = ? AND FT_ENTRADA BETWEEN ? AND ? AND SFT.D_E_L_E_T_ = ?  "
    If ctaxedMovements == "1"
        cQuery += " AND (FT_VALICM  > ? OR FT_VALIPI  > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND  FT_VALCONT > ? "
    ElseIF ctaxedMovements == "2"
        cQuery += " AND (FT_VALICM  = ? AND FT_VALIPI  = ? AND FT_VALPIS = ? AND FT_VALCOF = ?) AND  FT_VALCONT > ? "
    Else 
        cQuery += " AND FT_VALCONT > ? "  
    EndIf  
    cQuery += " AND FT_DTCANC = ? AND ((FT_POSIPI <> ? AND FT_TIPO <> ?) OR (FT_CODISS <> ? AND FT_TIPO = ?))"
    cQuery += " GROUP BY FT_FILIAL, FT_TIPO, FT_CODISS, FT_POSIPI) SFT "
    cQuery += " GROUP BY SFT.FILIAL, SFT.NCMNBS, SFT.CODE "
    cQuery += " ORDER BY SFT.FILIAL, SFT.NCMNBS"
    cQuery += " OFFSET ( ? * ? ) ROWS "
    cQuery += " FETCH NEXT ? ROWS ONLY "
    oGetNcmDetailOper := FwExecStatement():New()
    oGetNcmDetailOper:SetQuery(ChangeQuery(cQuery))
Endif

If oGetNcmDetailOper != Nil
    oGetNcmDetailOper:setString(1, "")
    oGetNcmDetailOper:setString(2, "NCM")
    oGetNcmDetailOper:setString(3, "NBS")
    oGetNcmDetailOper:setString(4, "")
    oGetNcmDetailOper:SetIn(5, {'D','B','N','L','I','C','P',' '})
    oGetNcmDetailOper:SetNumeric(6, 0)
    oGetNcmDetailOper:setString(7, "S")
    oGetNcmDetailOper:SetNumeric(8, 0)
    oGetNcmDetailOper:SetIn(9, StrTokArr(cbranches, "," ))
    oGetNcmDetailOper:setString(10, cmovimentType)
    oGetNcmDetailOper:setString(11, StrTran(cperiodStart, "-", ""))
    oGetNcmDetailOper:setString(12, StrTran(cperiodEnd, "-", ""))
    oGetNcmDetailOper:setString(13, ' ')
    If ctaxedMovements <> "3"
        oGetNcmDetailOper:SetNumeric(14, 0)
        oGetNcmDetailOper:SetNumeric(15, 0)
        oGetNcmDetailOper:SetNumeric(16, 0)
        oGetNcmDetailOper:SetNumeric(17, 0)
        oGetNcmDetailOper:SetNumeric(18, 0)
        oGetNcmDetailOper:setString(19, "")
        oGetNcmDetailOper:setString(20,"")
        oGetNcmDetailOper:setString(21, "S")
        oGetNcmDetailOper:setString(22, "")
        oGetNcmDetailOper:setString(23, "S")
        oGetNcmDetailOper:SetUnsafe(24, cValToChar(nPage-1))
        oGetNcmDetailOper:SetUnsafe(25, cValToChar(nPageSize))
        oGetNcmDetailOper:SetUnsafe(26, cValToChar(nPageSize+1))
    Else
        oGetNcmDetailOper:SetNumeric(14, 0)
        oGetNcmDetailOper:setString(15, "")
        oGetNcmDetailOper:setString(16,"")
        oGetNcmDetailOper:setString(17, "S")
        oGetNcmDetailOper:setString(18, "")
        oGetNcmDetailOper:setString(19, "S")
        oGetNcmDetailOper:SetUnsafe(20, cValToChar(nPage-1))
        oGetNcmDetailOper:SetUnsafe(21, cValToChar(nPageSize))
        oGetNcmDetailOper:SetUnsafe(22, cValToChar(nPageSize+1))
    EndIf
    oGetNcmDetailOper:CBASEQUERY := oGetNcmDetailOper:GETFIXQUERY()
    cAlias := oGetNcmDetailOper:OpenAlias()
Endif
FWFreeObj(oGetNcmDetailOper)
Return 


/*--------------------------------------------------------------------
{Protheus.doc} GetNcmDetailOperQ2()
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@since 06/01/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetNcmDetailOperQ2(cAlias2 as character, nPage as numeric, nPageSize as numeric, cperiodStart as character, cperiodEnd as character, cmovimentType as character, cFilialQ2 as character, cNcmNbsQ2 as character, ctaxedMovements as character)
Local cQuery    := ''  as character

If oGetNcmDetailOper == Nil
    cQuery := "SELECT FT_PRODUTO AS PRODUTO, "
    cQuery += "SUM(FT_VALCONT) AS VALOROPER, SUM(CASE WHEN FT_TIPO IN (?) THEN FT_VALICM ELSE ? END) AS ICMS, SUM(CASE WHEN FT_TIPO = ? THEN FT_VALICM ELSE ? END) AS ISS, "
    cQuery += " SUM(FT_VALIPI) AS IPI, SUM(FT_VALPIS) AS PIS, SUM(FT_VALCOF) AS COFINS "
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL = ? "
    cQuery += " AND FT_TIPOMOV = ? AND FT_ENTRADA BETWEEN ? AND ? AND SFT.D_E_L_E_T_ = ?"
    If ctaxedMovements == "1"
        cQuery += " AND (FT_VALICM  > ? OR FT_VALIPI  > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND  FT_VALCONT > ? "
    ElseIF ctaxedMovements == "2"
        cQuery += " AND (FT_VALICM  = ? AND FT_VALIPI  = ? AND FT_VALPIS = ? AND FT_VALCOF = ?) AND  FT_VALCONT > ? "
    Else 
        cQuery += " AND FT_VALCONT > ? "  
    EndIf  
    cQuery += " AND FT_DTCANC = ? AND ((FT_POSIPI = ? AND FT_TIPO <> ?) OR (FT_CODISS = ? AND FT_TIPO = ?)) "
    cQuery += " GROUP BY FT_PRODUTO  "
    cQuery += " ORDER BY FT_PRODUTO  "
    oGetNcmDetailOper := FwExecStatement():New()
    oGetNcmDetailOper:SetQuery(ChangeQuery(cQuery))
Endif

If oGetNcmDetailOper != Nil
    oGetNcmDetailOper:SetIn(1, {'D','B','N','L','I','C','P',' '})
    oGetNcmDetailOper:SetNumeric(2, 0)
    oGetNcmDetailOper:setString(3, "S")
    oGetNcmDetailOper:SetNumeric(4, 0)
    oGetNcmDetailOper:setString(5, cFilialQ2)
    oGetNcmDetailOper:setString(6, cmovimentType)
    oGetNcmDetailOper:setString(7, StrTran(cperiodStart, "-", ""))
    oGetNcmDetailOper:setString(8, StrTran(cperiodEnd, "-", ""))
    oGetNcmDetailOper:setString(9, ' ')
    If ctaxedMovements <> "3"
        oGetNcmDetailOper:SetNumeric(10, 0)
        oGetNcmDetailOper:SetNumeric(11, 0)
        oGetNcmDetailOper:SetNumeric(12, 0)
        oGetNcmDetailOper:SetNumeric(13, 0)
        oGetNcmDetailOper:SetNumeric(14, 0)
        oGetNcmDetailOper:setString(15, "")
        oGetNcmDetailOper:setString(16, cNcmNbsQ2)
        oGetNcmDetailOper:setString(17, "S")
        oGetNcmDetailOper:setString(18, cNcmNbsQ2)
        oGetNcmDetailOper:setString(19, "S")
    Else
        oGetNcmDetailOper:SetNumeric(10, 0)
        oGetNcmDetailOper:setString(11, "")
        oGetNcmDetailOper:setString(12, cNcmNbsQ2)
        oGetNcmDetailOper:setString(13, "S")
        oGetNcmDetailOper:setString(14, cNcmNbsQ2)
        oGetNcmDetailOper:setString(15, "S")
    EndIF
    oGetNcmDetailOper:CBASEQUERY := oGetNcmDetailOper:GETFIXQUERY()
    cAlias2 := oGetNcmDetailOper:OpenAlias()
Endif
FWFreeObj(oGetNcmDetailOper)
Return 
