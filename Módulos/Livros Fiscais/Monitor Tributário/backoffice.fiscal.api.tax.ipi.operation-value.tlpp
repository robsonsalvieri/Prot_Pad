#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oGetIPI := Nil

/*-------------------------------------------------------------
API de Valor Operacional de IPI.
@author eduardo.cirqueira
@parâmetros de entrada dos endpoints
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado
cperiodStart = data final do período a ser filtrado
@since 20/12/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/ipi/operational-value', description='API de valor operacional do IPI')
Function MTValOperIPI()
Local oJsResponse            as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character

//Todos os parâmetros são obrigatórios
if valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
   oJsResponse := ValOperIPIFilter(1, cbranches, cperiodStart, cperiodEnd)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 



/*-------------------------------------------------------------
API de Valor Total de IPI.
@author eduardo.cirqueira
@parâmetros de entrada do endpoint
cbranches = filiais a serem consideradas (ex.: 'XIFIS26, DM1MG') )
cperiodStart = data inicial do período a ser filtrado.
cperiodStart = data final do período a ser filtrado.
@since 20/12/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/ipi/tax-total', description='API de Total do IPI')
Function MTValTotIPI()
Local oJsResponse            as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character

//Todos os parâmetros são obrigatórios
if valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
   oJsResponse := ValOperIPIFilter(2, cbranches, cperiodStart, cperiodEnd)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 



/*-------------------------------------------------------------------
{Protheus.doc} GET ValOperIPIFilter
Função responsável por preencher o Json com com informações referentes a:
- Valor Operacional que tiveram incidência de IPI
- Total do Imposto (IPI)
@author eduardo.cirqueira
@parâmetros de entrada
nCard = 1-Card Valor Operacional; 2-Card Total do Imposto
@since 20/12/2024
-------------------------------------------------------------------*/
Static Function ValOperIPIFilter(nCard as numeric, cbranches as character, cperiodStart as character, cperiodEnd as character)
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character
Local aRetOrig     := {}                        as array

GetValOperIPI(nCard,@cAlias,cbranches,cperiodStart,cperiodEnd)

If (cAlias)->(!Eof()) .And. ( (cAlias)->TOTENT+(cAlias)->TOTSAI ) > 0
    If nCard == 1   //  Valor Operacional
        oJsonResp["entries"] := (cAlias)->TOTENT     
        oJsonResp["exits"]   := (cAlias)->TOTSAI
    Else            //  Total do IPI
        oJsonResp["credits"] := (cAlias)->TOTENT     
        oJsonResp["debts"]   := (cAlias)->TOTSAI
    EndIf
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := 'value not found'
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
EndIf

(cAlias)->(DBCloseArea())

aSize(aRetOrig,0)
Return oJsonResp:toJson()


/*--------------------------------------------------------------------
{Protheus.doc} GetValOperIPI()
(Responsável por executar a consulta aos campos da tabela CJX)
@author william.palma
@parâmetros de entrada
nCard = 1-Card Valor Operacional; 2-Card Total do Imposto
@since 10/12/2024
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetValOperIPI(nCard as numeric, cAlias as character, cbranches as character, cperiodStart as character, cperiodEnd as character)
Local cQuery    := ''  as character
Local cCampoSUM := 'CJX.CJX_VALCON'  as character

If nCard == 2 /*Total do Imposto*/
    cCampoSUM = 'CJX.CJX_VALIPI'
EndIf

If oGetIPI == Nil
    cQuery := "SELECT ROUND(SUM(CASE WHEN CJX.CJX_TIPMOV = ? THEN ? ELSE 0 END), 2) AS TOTENT "
    cQuery +=      ", ROUND(SUM(CASE WHEN CJX.CJX_TIPMOV = ? THEN ? ELSE 0 END), 2) AS TOTSAI "
    cQuery +=   "FROM " + FIS319SqlName('CJX') + " CJX "
    cQuery +=  "WHERE CJX.CJX_FILIAL IN (?) "
    cQuery +=    "AND CJX.CJX_ENTRAD BETWEEN ? AND ? "
    cQuery +=    "AND CJX.CJX_VALIPI > ? "
	cQuery +=    "AND CJX.D_E_L_E_T_ = ? "
    oGetIPI := FWExecStatement():New()
    oGetIPI:SetQuery(ChangeQuery(cQuery))
Endif

If oGetIPI != Nil
    oGetIPI:SetString(1, 'E')
    oGetIPI:SetUnSafe(2, cCampoSUM)
    oGetIPI:SetString(3, 'S')
    oGetIPI:SetUnSafe(4, cCampoSUM)
    oGetIPI:SetIn(5, StrTokArr(cbranches, "," ))
    oGetIPI:SetString(6, StrTran(cperiodStart, "-", ""))
    oGetIPI:SetString(7, StrTran(cperiodEnd, "-", ""))
    oGetIPI:setNumeric(8, 0)//cValToChar(0))
    oGetIPI:SetString(9, " ")
    cAlias := oGetIPI:OpenAlias()
Endif

FWFreeObj(oGetIPI)
Return 
