#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

Static oDetailOper := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTIssDetailOperations
API de Detalhe de Operações.
@author william.palma
@parâmetros de entrada do endpoint
@since 26/02/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/iss/operation-details', description='API de detalhe de operações')
Function MTIssDetailOperations()
Local oJsResponse         as json
Local cbranches        := oRest:getQueryRequest()['branches'] as character
Local cperiodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cperiodEnd       := oRest:getQueryRequest()['periodEnd']  as character

If valtype(cbranches) != 'U' .and. !empty(cbranches) .and. valtype(cperiodStart) != 'U' .and. !empty(cperiodStart) .and. valtype(cperiodEnd) != 'U' .and. !empty(cperiodEnd)
    oJsResponse := DetailOperationsFilter(cbranches, cperiodStart, cperiodEnd)
Endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET DetailOperationsFilter
Função responsável por preencher o Json com com informações referentes ao Detalhe de Operações
@author william.palma
@parâmetros de entrada do endpoint
@since 26/02/2025
-------------------------------------------------------------------*/
Static Function DetailOperationsFilter(cbranches as character, cperiodStart as character, cperiodEnd as character)
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local nPage        := 1                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local oJParams     := oRest:getQueryRequest()   as json
Local cAlias       := getNextAlias()            as character

If valtype(oJParams) == 'J'
    If valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    EndIf
    If valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    EndIf
EndIf

GetDetailOperations(@cAlias,nPage,nPageSize,cbranches, cperiodStart, cperiodEnd)  

// Iniciando o While
If (cAlias)->(!Eof())
    While (cAlias)->(!Eof())
        nPageAux++
        If nPageAux == 1
            oJsonResp["items"] := {}
        Endif
        If nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            oJsonResp["items"][nPos]["branch"]          :=   (cAlias)->FILIAL
            oJsonResp["items"][nPos]["period"]          :=   (cAlias)->MESNOME
            oJsonResp["items"][nPos]["municipality"]    :=   Alltrim((cAlias)->MUN)
            oJsonResp["items"][nPos]["payable"]         :=   (cAlias)->VALORDEV
            oJsonResp["items"][nPos]["withheldAtEntry"] :=   (cAlias)->VALORRET
            oJsonResp["items"][nPos]["exempt"]          :=   (cAlias)->VALORISEN
        Else
            lHasNext := .t.
            Exit
        EndIf
    (cAlias)->(DbSkip())
    EndDo
    oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := 'value not found'
    oJsonResp['message'] := "Dados não encontrados para o período e/ou filial informados."
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*--------------------------------------------------------------------
{Protheus.doc} GetDetailOperations
(Responsável por executar a consulta aos campos da tabela CJY)
@author william.palma
@since 26/02/2025
@return Nil, nulo, não tem retorno
//-------------------------------------------------------------------*/
Static Function GetDetailOperations(cAlias as character, nPage as numeric, nPageSize as numeric, cbranches as character, cperiodStart as character, cperiodEnd as character)
Local cQuery    := ""  as character
Local cDbMs     := ""  as character

cDbMs := TcGetDb()

If "MSSQL" $ cDbMs
    If oDetailOper == Nil
        cQuery := "SELECT DISTINCT CJY.MESNOME AS MESNOME, CJY.FILIAL AS FILIAL, CJY.MUN AS MUN, SUM(CJY.VALORDEV) AS VALORDEV, SUM(CJY.VALORRET) AS VALORRET, "
        cQuery += "SUM(CJY.VALORISEN) AS VALORISEN, CJY.MESNUM AS MESNUMERO, CJY.ANONUM "
        cQuery += "FROM (SELECT CJY_FILIAL AS FILIAL, MONTH(CJY_ENTRAD) AS MESNUM, YEAR(CJY_ENTRAD) AS ANONUM, "
        cQuery += "CASE "
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jan/' + LEFT(CJY_ENTRAD,4) "
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Fev/' + LEFT(CJY_ENTRAD,4) "    
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Mar/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Abr/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Mai/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jun/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jul/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Ago/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Set/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Out/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Nov/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Dez/' + LEFT(CJY_ENTRAD,4) "  
        cQuery += "ELSE ? END as MESNOME, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORDEV, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORRET, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? THEN A1_MUN ELSE A2_MUN END AS MUN, "  
        cQuery += "SUM(CJY_ISENIC) AS VALORISEN "  
        cQuery += "FROM ? CJY "
        cQuery += "LEFT JOIN "+RetSQLName("SA1")+" SA1 ON "+FWJoinFilial("SA1", "CJY")+" AND CJY_TIPOMO = ? AND A1_COD = CJY_CLIEFO AND A1_LOJA = CJY_LOJA AND SA1.D_E_L_E_T_ = ? "
        cQuery += "LEFT JOIN "+RetSQLName("SA2")+" SA2 ON "+FWJoinFilial("SA2", "CJY")+" AND CJY_TIPOMO = ? AND A2_COD = CJY_CLIEFO AND A2_LOJA = CJY_LOJA AND SA2.D_E_L_E_T_ = ? "
        cQuery += " WHERE CJY_ENTRAD BETWEEN ? AND ? AND CJY.D_E_L_E_T_ = ? AND  CJY_FILIAL IN (?) AND CJY_VALICM > ? "
        cQuery += " AND CJY_TIPO = ? AND ((CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_ISENIC > ?)) "
        cQuery += " GROUP BY CJY_FILIAL, SUBSTRING(CJY_ENTRAD,5,2), LEFT(CJY_ENTRAD,4), MONTH(CJY_ENTRAD), YEAR(CJY_ENTRAD), CJY_TIPOMO, CJY_RECISS, A1_MUN, A2_MUN) AS CJY "
        cQuery +="  GROUP BY CJY.MESNOME, CJY.MESNUM, CJY.ANONUM, CJY.FILIAL, CJY.MUN "
        cQuery += " ORDER BY CJY.ANONUM, CJY.MESNUM, CJY.FILIAL, CJY.MUN "
        cQuery += " OFFSET ( ? * ? ) ROWS "
        cQuery += " FETCH NEXT ? ROWS ONLY "
        oDetailOper := FwExecStatement():New()
        oDetailOper:SetQuery(ChangeQuery(cQuery))
    Endif
EndIF

If "ORACLE" $ cDbMs
    If oDetailOper == Nil
        cQuery := "SELECT DISTINCT CJY.MESNOME AS MESNOME, CJY.FILIAL AS FILIAL, CJY.MUN AS MUN, SUM(CJY.VALORDEV) AS VALORDEV, SUM(CJY.VALORRET) AS VALORRET, "
        cQuery += "SUM(CJY.VALORISEN) AS VALORISEN, CJY.MESNUM AS MESNUMERO, CJY.ANONUM "
        cQuery += "FROM (SELECT CJY_FILIAL AS FILIAL, TO_CHAR(CAST(CJY_ENTRAD AS DATE), 'MM') AS MESNUM, TO_CHAR(CAST(CJY_ENTRAD AS DATE), 'YYYY') AS ANONUM, "
        cQuery += "CASE "
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Jan/' || SUBSTR(CJY_ENTRAD,1,4) "
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Fev/' || SUBSTR(CJY_ENTRAD,1,4) "    
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Mar/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Abr/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Mai/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Jun/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Jul/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Ago/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Set/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Out/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Nov/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "WHEN SUBSTR(CJY_ENTRAD,5,2) = ? THEN 'Dez/' || SUBSTR(CJY_ENTRAD,1,4) "  
        cQuery += "ELSE ? END as MESNOME, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORDEV, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORRET, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? THEN A1_MUN ELSE A2_MUN END AS MUN, "  
        cQuery += "SUM(CJY_ISENIC) AS VALORISEN "  
        cQuery += "FROM ? CJY "
        cQuery += "LEFT JOIN "+RetSQLName("SA1")+" SA1 ON "+FWJoinFilial("SA1", "CJY")+" AND CJY_TIPOMO = ? AND A1_COD = CJY_CLIEFO AND A1_LOJA = CJY_LOJA AND SA1.D_E_L_E_T_ = ? "
        cQuery += "LEFT JOIN "+RetSQLName("SA2")+" SA2 ON "+FWJoinFilial("SA2", "CJY")+" AND CJY_TIPOMO = ? AND A2_COD = CJY_CLIEFO AND A2_LOJA = CJY_LOJA AND SA2.D_E_L_E_T_ = ? "
        cQuery += " WHERE TO_CHAR(CAST(CJY_ENTRAD AS DATE), 'YYYYMMDD') BETWEEN ? AND ? AND CJY.D_E_L_E_T_ = ? AND  CJY_FILIAL IN (?) AND CJY_VALICM > ? "
        cQuery += " AND CJY_TIPO = ? AND ((CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_ISENIC > ?)) "
        cQuery += " GROUP BY CJY_FILIAL, SUBSTR(CJY_ENTRAD,5,2), SUBSTR(CJY_ENTRAD,1,4), TO_CHAR(CAST(CJY_ENTRAD AS DATE), 'MM'), TO_CHAR(CAST(CJY_ENTRAD AS DATE), 'YYYY'), CJY_TIPOMO, CJY_RECISS, A1_MUN, A2_MUN) CJY "
        cQuery += " GROUP BY CJY.MESNOME, CJY.MESNUM, CJY.ANONUM, CJY.FILIAL, CJY.MUN "
        cQuery += " ORDER BY CJY.ANONUM, CJY.MESNUM, CJY.FILIAL, CJY.MUN "
        cQuery += " OFFSET ( ? * ? ) ROWS "
        cQuery += " FETCH NEXT ? ROWS ONLY "
        oDetailOper := FwExecStatement():New()
        oDetailOper:SetQuery(ChangeQuery(cQuery))
    Endif
EndIF

If "POSTGRES" $ cDbMs
    If oDetailOper == Nil
        cQuery := "SELECT DISTINCT CJY.MESNOME AS MESNOME, CJY.FILIAL AS FILIAL, CJY.MUN AS MUN, SUM(CJY.VALORDEV) AS VALORDEV, SUM(CJY.VALORRET) AS VALORRET, "
        cQuery += "SUM(CJY.VALORISEN) AS VALORISEN, TO_NUMBER(CJY.MESNUM,'99') AS MESNUMERO, TO_NUMBER(CJY.ANONUM,'9999') AS ANONUM "
        cQuery += "FROM (SELECT CJY_FILIAL AS FILIAL, TO_CHAR(CJY_ENTRAD:: DATE, 'MM') AS MESNUM, TO_CHAR(CJY_ENTRAD:: DATE, 'YYYY') AS ANONUM, "
        cQuery += "CASE "
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jan/' || LEFT(CJY_ENTRAD,4) "
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Fev/' || LEFT(CJY_ENTRAD,4) "    
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Mar/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Abr/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Mai/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jun/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Jul/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Ago/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Set/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Out/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Nov/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "WHEN SUBSTRING(CJY_ENTRAD,5,2) = ? THEN 'Dez/' || LEFT(CJY_ENTRAD,4) "  
        cQuery += "ELSE ? END as MESNOME, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORDEV, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? AND CJY_RECISS = ? THEN SUM(CJY_VALICM) ELSE ? END  AS VALORRET, "  
        cQuery += "CASE WHEN CJY_TIPOMO = ? THEN A1_MUN ELSE A2_MUN END AS MUN, "  
        cQuery += "SUM(CJY_ISENIC) AS VALORISEN "  
        cQuery += "FROM ? CJY "
        cQuery += "LEFT JOIN "+RetSQLName("SA1")+" SA1 ON "+FWJoinFilial("SA1", "CJY")+" AND CJY_TIPOMO = ? AND A1_COD = CJY_CLIEFO AND A1_LOJA = CJY_LOJA AND SA1.D_E_L_E_T_ = ? "
        cQuery += "LEFT JOIN "+RetSQLName("SA2")+" SA2 ON "+FWJoinFilial("SA2", "CJY")+" AND CJY_TIPOMO = ? AND A2_COD = CJY_CLIEFO AND A2_LOJA = CJY_LOJA AND SA2.D_E_L_E_T_ = ? "
        cQuery += " WHERE CJY_ENTRAD BETWEEN ? AND ? AND CJY.D_E_L_E_T_ = ? AND  CJY_FILIAL IN (?) AND CJY_VALICM > ? "
        cQuery += " AND CJY_TIPO = ? AND ((CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_TIPOMO = ? AND CJY_RECISS = ? AND CJY_VALICM > ?) OR (CJY_ISENIC > ?)) "
        cQuery += " GROUP BY CJY_FILIAL, SUBSTRING(CJY_ENTRAD,5,2), LEFT(CJY_ENTRAD,4), TO_CHAR(CJY_ENTRAD:: DATE, 'MM'), TO_CHAR(CJY_ENTRAD:: DATE, 'YYYY'), CJY_TIPOMO, CJY_RECISS, A1_MUN, A2_MUN) AS CJY "
        cQuery +="  GROUP BY CJY.MESNOME, CJY.MESNUM, CJY.ANONUM, CJY.FILIAL, CJY.MUN "
        cQuery += " ORDER BY TO_NUMBER(CJY.ANONUM,'9999'), TO_NUMBER(CJY.MESNUM,'99'), CJY.FILIAL, CJY.MUN "
        cQuery += " OFFSET ( ? * ? ) ROWS "
        cQuery += " FETCH NEXT ? ROWS ONLY "
        oDetailOper := FwExecStatement():New()
        oDetailOper:SetQuery(ChangeQuery(cQuery))
    Endif
EndIF

If oDetailOper != Nil

    oDetailOper:setString(1, "01")
    oDetailOper:setString(2, "02")
    oDetailOper:setString(3, "03")
    oDetailOper:setString(4, "04")
    oDetailOper:setString(5, "05")
    oDetailOper:setString(6, "06")
    oDetailOper:setString(7, "07")
    oDetailOper:setString(8, "08")
    oDetailOper:setString(9, "09")
    oDetailOper:setString(10, "10")
    oDetailOper:setString(11, "11")
    oDetailOper:setString(12, "12")
    oDetailOper:setString(13, '')
    oDetailOper:setString(14, "S")
    oDetailOper:setString(15, "1")
    oDetailOper:SetNumeric(16, 0)
    oDetailOper:setString(17, "E")
    oDetailOper:setString(18, "2")
    oDetailOper:SetNumeric(19, 0)
    oDetailOper:setString(20, "S")
    oDetailOper:SetUnsafe(21, FIS319SqlName("CJY"))
    oDetailOper:setString(22, "S")
    oDetailOper:setString(23, ' ')
    oDetailOper:setString(24, "E")
    oDetailOper:setString(25, ' ')
    oDetailOper:setString(26, StrTran(cperiodStart, "-", ""))
    oDetailOper:setString(27, StrTran(cperiodEnd, "-", ""))
    oDetailOper:setString(28, ' ')
    oDetailOper:SetIn(29, StrTokArr(cbranches, "," ))
    oDetailOper:SetNumeric(30, 0)
    oDetailOper:setString(31, "S")
    oDetailOper:setString(32, "S")
    oDetailOper:setString(33, "1")
    oDetailOper:SetNumeric(34, 0)
    oDetailOper:setString(35, "E")
    oDetailOper:setString(36, "2")
    oDetailOper:SetNumeric(37, 0)
    oDetailOper:SetNumeric(38, 0)
    oDetailOper:SetUnsafe(39, cValToChar(nPage-1))
    oDetailOper:SetUnsafe(40, cValToChar(nPageSize))
    oDetailOper:SetUnsafe(41, cValToChar(nPageSize+1))
    oDetailOper:CBASEQUERY := oDetailOper:GETFIXQUERY()
    cAlias := oDetailOper:OpenAlias()
Endif

FWFreeObj(oDetailOper)
Return 

