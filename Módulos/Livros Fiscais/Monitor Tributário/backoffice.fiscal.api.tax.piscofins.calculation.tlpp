#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "FISA318.ch"

Static oGetPISCOFINS := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET MTApurCalcPISCOFINSFilter
API com retorno de informações para o gráfico de Apuração de PISCOFINS e Ajustes de Apuração de PISCOFINS

@author William.palma
@parâmetros de entrada do endpoint
    branches: Filiais
    periodStart: Data Inicial
    periodEnd: Data Final

@since 23/04/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/pis-cofins/calculation', description='Retorna os valores mensais de credito debito e apuracao para preenchimento do card de apuracao de PIS COFINS')

Function MTApurCalcPISCOFINSFilter()
Local oJsResponse         as json
Local cBranches        := oRest:getQueryRequest()['branches'] as character
Local cPeriodStart     := oRest:getQueryRequest()['periodStart']  as character
Local cPeriodEnd       := oRest:getQueryRequest()['periodEnd']  as character

//--------------------------------
// Verifica os parâmetro de entrada da API
//--------------------------------	
If !Empty(cBranches) .and. !Empty(cPeriodStart) .and. !Empty(cPeriodEnd)
    oJsResponse := ApurCalcPISCOFINSFilter(cBranches, cPeriodStart, cPeriodEnd)
ElseIf Empty(cBranches)
    oJsResponse := JsonObject():new()
    oJsResponse['code']             := 400
    oJsResponse['message']          := STR0069  //"O parâmetro 'branches' é obrigatório e não foi informado."
    oJsResponse['detailedMessage']  := STR0070 //"Informar Código de Filiais separadas por vírgula."
    oRest:setStatusCode(400)
ElseIf Empty(cPeriodStart)
    oJsResponse := JsonObject():new()
    oJsResponse['code']             := 400
    oJsResponse['message']          := STR0071  //"Nenhuma Data foi informada no parâmetro 'periodStart'."
    oJsResponse['detailedMessage']  := STR0072  //"Informar a data desejada para o período"
    oRest:setStatusCode(400)
Else
    oJsResponse := JsonObject():new()
    oJsResponse['code']             := 400
    oJsResponse['message']          := STR0073  //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
    oJsResponse['detailedMessage']  := STR0072  //"Informar a data desejada para o período"
    oRest:setStatusCode(400)
Endif  
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET ApurCalcPISCOFINSFilter
Função responsável por preencher o Json com informações do resumo de apuração de PISCOFINS.
A funcao retorna um Json com os seguintes campos:

@author William.palma
@parâmetros de entrada da funcao
    Filiais         as character  // Filiais
    Data Inicial    as character  // Data Inicial
    Data Final      as character  // Data Final

@since 23/04/2025
-------------------------------------------------------------------*/
Static Function ApurCalcPISCOFINSFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character)

Local cAlias       := getNextAlias()            as character
Local nPos         := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 12                        as numeric
Local nPage        := 1                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local oJParams     := oRest:getQueryRequest()   as json
Local cDbMs        := ""                        as character
//--------------------------------
// Verifica se o tipo do objeto é Json
//--------------------------------	
If valtype(oJParams) == 'J'
    If valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    EndIf
    If valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    EndIf
EndIf
//--------------------------------
// Verifica o banco de dados do ambiente
//--------------------------------	
cDbMs := TcGetDb()
//--------------------------------
// Chama a função responsável por retornar a consulta da query de acordo com o tipo de banco de dados utilizado
//--------------------------------	
If "MSSQL" $ cDbMs
    GetApurCalcPISCOFINSMSSQL(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd)  
ElseIf "ORACLE" $ cDbMs
    GetApurCalcPISCOFINSOracle(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd)
ElseIf "POSTGRES" $ cDbMs
    GetApurCalcPISCOFINSPostgres(@cAlias, nPage, nPageSize, cBranches, cPeriodStart, cPeriodEnd)
EndIf
//--------------------------------
// Percorre todo o alias retornado pela query
//--------------------------------	
If (cAlias)->(!Eof()) 
    While (cAlias)->(!Eof())

        nPageAux++
        If nPageAux == 1
            oJsonResp["items"] := {}
        Endif

        If nPageAux <= nPageSize
            //--------------------------------	
            // Inicia o objeto Json
            //--------------------------------	        
            aAdd( oJsonResp["items"], JsonObject():New() )
            nPos++
            //--------------------------------
            // Alimenta o json com os dados retornado pela query
            //--------------------------------	               
            oJsonResp["items"][nPos]["month"]           :=   AllTrim((cAlias)->MESANO)
            oJsonResp["items"][nPos]["credits"]         :=   (cAlias)->VALORCRED
            oJsonResp["items"][nPos]["debits"]          :=   (cAlias)->VALORDEBT
            oJsonResp["items"][nPos]["calculation"]     :=   (cAlias)->VALORAPUR
   
        Else
            lHasNext := .t.
            Exit
        EndIf
        (cAlias)->(DbSkip())
    EndDo
    oJsonResp["hasNext"]        := lHasNext
Else
    oJsonResp['code']           := 200
    oJsonResp['items']          := {}
    oJsonResp['hasNext']        := lHasNext
EndIf  
//--------------------------------
// Fecha o alias
//--------------------------------
FWFreeObj(oJParams)	     
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*--------------------------------------------------------------------
{Protheus.doc} GetApurCalcPISCOFINSMSSQL
(Responsável por executar a consulta aos campos da tabela CKR para banco de dados MSSQL)
@author William.palma
@parâmetros de entrada da funcao
@since 26/02/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetApurCalcPISCOFINSMSSQL(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQuery    := ''  as character
//--------------------------------
// Montagem da query
//--------------------------------	
cQuery := " SELECT DISTINCT (CKR.MESNOME) AS MESANO,  SUM(CKR.VALORCRED) AS VALORCRED, "
cQuery += " SUM(CKR.VALORDEBT) AS VALORDEBT, CKR.MESNUM AS MESNUMERO, CKR.ANONUM AS ANONUMERO, SUM(CKR.VALORAPUR) AS VALORAPUR "
cQuery += " FROM (SELECT SUBSTRING(CKR_PER,5,2) AS MES, LEFT(CKR_PER,4) AS ANO, MONTH(CKR_PER) AS MESNUM, YEAR(CKR_PER) AS ANONUM, "
cQuery += " CASE "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jan/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'fev/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'mar/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'abr/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'mai/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jun/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jul/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'ago/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'set/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'out/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'nov/' + LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'dez/' + LEFT(CKR_PER,4) "
cQuery += " ELSE ? END as MESNOME, "
cQuery += " SUM(CKR_CRDANT + CKR_CRDATU + CKR_RTANTN + CKR_RTATUN + CKR_OUTDNC + CKR_RTANTC + CKR_RTATUC + CKR_OUTDC) AS VALORCRED, "
cQuery += " SUM(CKR_CONNC + CKR_CONC) AS VALORDEBT, "
cQuery += " SUM(CKR_NCREC + CKR_CREC) AS VALORAPUR "
cQuery += " FROM "+RetSQLName("CKR")+" CKR "
cQuery += " WHERE CKR_PER BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND CKR_FILIAL IN (?) "
cQuery += " GROUP BY CKR_FILIAL, SUBSTRING(CKR_PER,5,2), LEFT(CKR_PER,4), MONTH(CKR_PER), YEAR(CKR_PER)) AS CKR "
cQuery += " WHERE CKR.VALORCRED > ? OR CKR.VALORDEBT > ? "
cQuery += " GROUP BY CKR.MESNOME, CKR.MESNUM, CKR.ANONUM "
cQuery += " ORDER BY CKR.ANONUM, CKR.MESNUM "
cQuery += "    OFFSET ( ? * ? ) ROWS "
cQuery += "    FETCH NEXT ? ROWS ONLY "
oGetPISCOFINS := FwExecStatement():New()
oGetPISCOFINS:SetQuery(ChangeQuery(cQuery))
//--------------------------------
// Montagem dos Binds
//--------------------------------
If oGetPISCOFINS != Nil
    oGetPISCOFINS:setString(1, "01")
    oGetPISCOFINS:setString(2, "02")
    oGetPISCOFINS:setString(3, "03")
    oGetPISCOFINS:setString(4, "04")
    oGetPISCOFINS:setString(5, "05")
    oGetPISCOFINS:setString(6, "06")
    oGetPISCOFINS:setString(7, "07")
    oGetPISCOFINS:setString(8, "08")
    oGetPISCOFINS:setString(9, "09")
    oGetPISCOFINS:setString(10,"10")
    oGetPISCOFINS:setString(11,"11")
    oGetPISCOFINS:setString(12,"12")
    oGetPISCOFINS:setString(13,"")
    oGetPISCOFINS:SetString(14, StrTran(cPeriodStart, "-", ""))
    oGetPISCOFINS:setString(15, StrTran(cPeriodEnd, "-", ""))
    oGetPISCOFINS:setString(16, ' ')
    oGetPISCOFINS:setIn(17, StrTokArr(cBranches, "," ))
    oGetPISCOFINS:SetNumeric(18, 0)
    oGetPISCOFINS:SetNumeric(19, 0)
    oGetPISCOFINS:SetUnsafe(20, cValToChar(nPage-1))
    oGetPISCOFINS:SetUnsafe(21, cValToChar(nPageSize))
    oGetPISCOFINS:SetUnsafe(22, cValToChar(nPageSize+1))
    cAlias := oGetPISCOFINS:OpenAlias()
EndIf
//--------------------------------
// Libera o objeto
//--------------------------------
cAlias := oGetPISCOFINS:OpenAlias()
FWFreeObj(oGetPISCOFINS)

Return
/*--------------------------------------------------------------------
{Protheus.doc} GetApurCalcPISCOFINSOracle
(Responsável por executar a consulta aos campos da tabela CKR para banco de dados ORACLE)
@author William.palma
@parâmetros de entrada da funcao
@since 26/02/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetApurCalcPISCOFINSOracle(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQuery    := ''  as character
//--------------------------------
// Montagem da query
//--------------------------------	
cQuery := " SELECT DISTINCT (CKR.MESNOME) AS MESANO,  SUM(CKR.VALORCRED) AS VALORCRED, "
cQuery += " SUM(CKR.VALORDEBT) AS VALORDEBT, CKR.MESNUM AS MESNUMERO, CKR.ANONUM AS ANONUMERO, SUM(CKR.VALORAPUR) AS VALORAPUR "
cQuery += " FROM (SELECT SUBSTR(CKR_PER,5,2) AS MES, "
cQuery += " SUBSTR(CKR_PER,1,4) AS ANO, TO_CHAR(CAST(CKR_PER AS DATE), 'MM') AS MESNUM, TO_CHAR(CAST(CKR_PER AS DATE), 'YYYY') AS ANONUM, "
cQuery += " CASE "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'jan/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'fev/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'mar/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'abr/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'mai/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'jun/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'jul/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'ago/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'set/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'out/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'nov/' || SUBSTR(CKR_PER,1,4) "
cQuery += " WHEN SUBSTR(CKR_PER,5,2) = ? THEN 'dez/' || SUBSTR(CKR_PER,1,4) "
cQuery += " ELSE ? END as MESNOME, "
cQuery += " SUM(CKR_CRDANT + CKR_CRDATU + CKR_RTANTN + CKR_RTATUN + CKR_OUTDNC + CKR_RTANTC + CKR_RTATUC + CKR_OUTDC) AS VALORCRED, "
cQuery += " SUM(CKR_CONNC + CKR_CONC) AS VALORDEBT, "
cQuery += " SUM(CKR_NCREC + CKR_CREC) AS VALORAPUR "
cQuery += " FROM "+RetSQLName("CKR")+" CKR "
cQuery += " WHERE TO_CHAR(CAST(CKR_PER AS DATE), 'YYYYMMDD')  BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND CKR_FILIAL IN (?) "
cQuery += " GROUP BY CKR_FILIAL, SUBSTR(CKR_PER,5,2), SUBSTR(CKR_PER,1,4), TO_CHAR(CAST(CKR_PER AS DATE), 'MM'), TO_CHAR(CAST(CKR_PER AS DATE), 'YYYY')) CKR "
cQuery += " WHERE CKR.VALORCRED > ? OR CKR.VALORDEBT > ? "
cQuery += " GROUP BY CKR.MESNOME, CKR.MESNUM, CKR.ANONUM "
cQuery += " ORDER BY CKR.ANONUM, CKR.MESNUM"
cQuery += "    OFFSET ( ? * ? ) ROWS "
cQuery += "    FETCH NEXT ? ROWS ONLY "
oGetPISCOFINS := FwExecStatement():New()
oGetPISCOFINS:SetQuery(ChangeQuery(cQuery))
//--------------------------------
// Montagem dos Binds
//--------------------------------
If oGetPISCOFINS != Nil
    oGetPISCOFINS:setString(1, "01")
    oGetPISCOFINS:setString(2, "02")
    oGetPISCOFINS:setString(3, "03")
    oGetPISCOFINS:setString(4, "04")
    oGetPISCOFINS:setString(5, "05")
    oGetPISCOFINS:setString(6, "06")
    oGetPISCOFINS:setString(7, "07")
    oGetPISCOFINS:setString(8, "08")
    oGetPISCOFINS:setString(9, "09")
    oGetPISCOFINS:setString(10,"10")
    oGetPISCOFINS:setString(11,"11")
    oGetPISCOFINS:setString(12,"12")
    oGetPISCOFINS:setString(13,"")
    oGetPISCOFINS:SetString(14, StrTran(cPeriodStart, "-", ""))
    oGetPISCOFINS:setString(15, StrTran(cPeriodEnd, "-", ""))
    oGetPISCOFINS:setString(16, ' ')
    oGetPISCOFINS:setIn(17, StrTokArr(cBranches, "," ))
    oGetPISCOFINS:SetNumeric(18, 0)
    oGetPISCOFINS:SetNumeric(19, 0)
    oGetPISCOFINS:SetUnsafe(20, cValToChar(nPage-1))
    oGetPISCOFINS:SetUnsafe(21, cValToChar(nPageSize))
    oGetPISCOFINS:SetUnsafe(22, cValToChar(nPageSize+1))
    cAlias := oGetPISCOFINS:OpenAlias()
EndIf
//--------------------------------
// Libera o objeto
//--------------------------------
cAlias := oGetPISCOFINS:OpenAlias()
FWFreeObj(oGetPISCOFINS)

Return
/*--------------------------------------------------------------------
{Protheus.doc} GetApurCalcPISCOFINSPostgres
(Responsável por executar a consulta aos campos da tabela CKR para banco de dados POSTGRES)
@author William.palma
@parâmetros de entrada da funcao
@since 26/02/2025
@return Nil, nulo, não tem retorno.
//-------------------------------------------------------------------*/
Static Function GetApurCalcPISCOFINSPostgres(cAlias as character, nPage as numeric, nPageSize as numeric, cBranches as character, cPeriodStart as character, cPeriodEnd as character)
Local cQuery    := ''  as character

cQuery := " SELECT DISTINCT (CKR.MESNOME) AS MESANO,  SUM(CKR.VALORCRED) AS VALORCRED, "
cQuery += " SUM(CKR.VALORDEBT) AS VALORDEBT, CKR.MESNUM AS MESNUMERO, CKR.ANONUM AS ANONUMERO, SUM(CKR.VALORAPUR) AS VALORAPUR "
cQuery += " FROM (SELECT SUBSTRING(CKR_PER,5,2) AS MES, LEFT(CKR_PER,4) AS ANO, TO_CHAR(CKR_PER:: DATE, 'MM') AS MESNUM, TO_CHAR(CKR_PER:: DATE, 'YYYY') AS ANONUM, "
cQuery += " CASE "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jan/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'fev/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'mar/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'abr/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'mai/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jun/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'jul/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'ago/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'set/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'out/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'nov/' || LEFT(CKR_PER,4) "
cQuery += " WHEN SUBSTRING(CKR_PER,5,2) = ? THEN 'dez/' || LEFT(CKR_PER,4) "
cQuery += " ELSE ? END as MESNOME, "
cQuery += " SUM(CKR_CRDANT + CKR_CRDATU + CKR_RTANTN + CKR_RTATUN + CKR_OUTDNC + CKR_RTANTC + CKR_RTATUC + CKR_OUTDC) AS VALORCRED, "
cQuery += " SUM(CKR_CONNC + CKR_CONC) AS VALORDEBT, "
cQuery += " SUM(CKR_NCREC + CKR_CREC) AS VALORAPUR "
cQuery += " FROM "+RetSQLName("CKR")+" CKR "
cQuery += " WHERE TO_CHAR(CAST(CKR_PER AS DATE), 'YYYYMMDD')  BETWEEN ? AND ? AND D_E_L_E_T_ = ? AND CKR_FILIAL IN (?) "
cQuery += " GROUP BY CKR_FILIAL, SUBSTRING(CKR_PER,5,2), LEFT(CKR_PER,4), TO_CHAR(CKR_PER:: DATE, 'MM'), TO_CHAR(CKR_PER:: DATE, 'YYYY')) CKR "
cQuery += " WHERE CKR.VALORCRED > ? OR CKR.VALORDEBT > ? "
cQuery += " GROUP BY CKR.MESNOME, CKR.MESNUM, CKR.ANONUM "
cQuery += " ORDER BY CKR.ANONUM, CKR.MESNUM "
cQuery += "    OFFSET ( ? * ? ) ROWS "
cQuery += "    FETCH NEXT ? ROWS ONLY "
oGetPISCOFINS := FwExecStatement():New()
oGetPISCOFINS:SetQuery(ChangeQuery(cQuery))
//--------------------------------
// Montagem dos Binds
//--------------------------------
If oGetPISCOFINS != Nil
    oGetPISCOFINS:setString(1, "01")
    oGetPISCOFINS:setString(2, "02")
    oGetPISCOFINS:setString(3, "03")
    oGetPISCOFINS:setString(4, "04")
    oGetPISCOFINS:setString(5, "05")
    oGetPISCOFINS:setString(6, "06")
    oGetPISCOFINS:setString(7, "07")
    oGetPISCOFINS:setString(8, "08")
    oGetPISCOFINS:setString(9, "09")
    oGetPISCOFINS:setString(10,"10")
    oGetPISCOFINS:setString(11,"11")
    oGetPISCOFINS:setString(12,"12")
    oGetPISCOFINS:setString(13,"")
    oGetPISCOFINS:SetString(14, StrTran(cPeriodStart, "-", ""))
    oGetPISCOFINS:setString(15, StrTran(cPeriodEnd, "-", ""))
    oGetPISCOFINS:setString(16, ' ')
    oGetPISCOFINS:setIn(17, StrTokArr(cBranches, "," ))
    oGetPISCOFINS:SetNumeric(18, 0)
    oGetPISCOFINS:SetNumeric(19, 0)
    oGetPISCOFINS:SetUnsafe(20, cValToChar(nPage-1))
    oGetPISCOFINS:SetUnsafe(21, cValToChar(nPageSize))
    oGetPISCOFINS:SetUnsafe(22, cValToChar(nPageSize+1))
    cAlias := oGetPISCOFINS:OpenAlias()
EndIf
//--------------------------------
// Libera o objeto
//--------------------------------
cAlias := oGetPISCOFINS:OpenAlias()
FWFreeObj(oGetPISCOFINS)

Return
