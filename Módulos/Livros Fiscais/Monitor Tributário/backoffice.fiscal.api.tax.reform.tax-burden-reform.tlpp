#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"
#INCLUDE "FISA318.ch"

Static oGetTotalImp     := Nil
Static oGetTransition   := Nil

/*-------------------------------------------------------------
{Protheus.doc} GET TITCalculationBurdenReformFilter
API Valores de Entrada/Saída Reforma Tributária.
@author william.palma
@parâmetros de entrada do endpoint: cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements
@return Nil, nulo, não tem retorno.
@since 28/08/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/tax-reform/tax-burden-reform', description='API Valores de Entrada/Saída Reforma Trib.')
Function TITCalculationBurdenReformFilter()
    Local oJsResponse      := JsonObject():new() as json
    Local oQryStr          := oRest:getQueryRequest() as json
    Local aRequiredProperties as array
    //--------------------------------
    //Monta um array bidimensional com query strings que se deseja verificar se existem na requisição
    //--------------------------------
        aRequiredProperties	:= {;
            {'branches',400,; //PROPERTY //ERROR_CODE
            STR0069,;   //"O parâmetro 'branches' é obrigatório e não foi informado."
            STR0070},;  //"Informar Código de Filiais separadas por vírgula."
            {'periodStart',400,;
            STR0071,;   //"Nenhuma Data foi informada no parâmetro 'periodStart'."
            STR0072},;  //"Informar a data desejada para o período"
            {'periodEnd',400,;
            STR0073,;   //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
            STR0072},;  //"Informar a data desejada para o período"
            {'taxedMovements',400,;
            STR0087,;   //"O parâmetro 'taxedMovements' é obrigatório e não foi informado."
            STR0088};   //"O parâmetro deve conter 1,2 ou 3. Conforme manual de utilização."
        }
    //--------------------------------
    // Verifica se tem parâmetros obrigatórios na requisição da API
    //--------------------------------	
    If FISA318F6HasApiRequiredProperties(aRequiredProperties, oQryStr, @oJsResponse) 
        oJsResponse := CalculationBurdenReformFilter(oQryStr['branches'] , oQryStr['periodStart'] , oQryStr['periodEnd'] , oQryStr['taxedMovements'])   
    EndIf  

    oRest:setKeyHeaderResponse('Content-Type', 'application/json')
    oRest:setResponse(oJsResponse)
    //--------------------------------
    // Libera os objetos
    //-------------------------------  
    FwFreeArray(aRequiredProperties) 
    FWFreeObj(oJsResponse)
    FWFreeObj(oQryStr)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET CalculationBurdenReformFilter
Função responsável por preencher o Json com com informações referentes a valores da Reforma Tributária
@author william.palma
@parâmetros de entrada do endpoint: cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements
@return object, Objeto contendo o JSON com resultado da query executada.
@since 28/08/2025
-------------------------------------------------------------------*/
Static Function CalculationBurdenReformFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTaxedMovements as character)
Local nPos         := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character  

GetCalculationBurdenReformFilter(@cAlias, cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements)

// Iniciando o While
If (cAlias)->(!Eof()) .And. ((cAlias)->TOT_CBS_ENT > 0 .Or. (cAlias)->TOT_CBS_SAI > 0 .Or. (cAlias)->TOT_IBS_ENT > 0 .Or. (cAlias)->TOT_IBS_SAI > 0 .Or. (cAlias)->TOT_IS_ENT > 0 .Or. (cAlias)->TOT_IS_SAI > 0)
    While (cAlias)->(!Eof())

        oJsonResp["items"] := {}
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "CBS Projetado"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->TOT_CBS_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->TOT_CBS_SAI
        
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "IBS Projetado"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->TOT_IBS_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->TOT_IBS_SAI

        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]          :=   "IS Projetado"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->TOT_IS_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->TOT_IS_SAI

    (cAlias)->(DbSkip())
    EndDo
    oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code']       := 200
    oJsonResp["items"]      := {}
    oJsonResp["hasNext"]    := lHasNext
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*--------------------------------------------------------------------
{Protheus.doc} GetCalculationUFICMSFilter
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@parâmetros de entrada do endpoint: cAlias, cBranches, cPeriodStart, cPeriodEnd
@return Nil, nulo, não tem retorno.
@since 28/08/2025
//-------------------------------------------------------------------*/
Static Function GetCalculationBurdenReformFilter(cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTaxedMovements as character)
Local cQuery    := ''  as character

If oGetTotalImp == Nil
    cQuery := "SELECT "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_CBS_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_CBS_SAI, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_IBS_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_IBS_SAI, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_IS_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS TOT_IS_SAI "
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL IN (?) AND FT_ENTRADA BETWEEN ? AND ? AND D_E_L_E_T_ = ? "
    If cTaxedMovements == "1"
        cQuery += " AND (FT_VALICM > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND FT_VALCONT > ? "
    ElseIF cTaxedMovements == "2"
        cQuery += " AND (FT_VALICM = ? AND FT_VALPIS = ? AND FT_VALCOF = ?) AND FT_VALCONT > ? "
    Else
        cQuery += " AND FT_VALCONT > ? "
    EndIf
	cQuery += " AND FT_DTCANC = ? "
    oGetTotalImp := FwExecStatement():New()
    oGetTotalImp:SetQuery(ChangeQuery(cQuery))
Endif

If oGetTotalImp != Nil
    oGetTotalImp:setString(1, "E")
    oGetTotalImp:SetNumeric(2, 0.009)
    oGetTotalImp:SetNumeric(3, 0)
    oGetTotalImp:setString(4, "S")
    oGetTotalImp:SetNumeric(5, 0.009)
    oGetTotalImp:SetNumeric(6, 0)
    oGetTotalImp:setString(7, "E")
    oGetTotalImp:SetNumeric(8, 0.001)
    oGetTotalImp:SetNumeric(9, 0)
    oGetTotalImp:setString(10, "S")
    oGetTotalImp:SetNumeric(11, 0.001)
    oGetTotalImp:SetNumeric(12, 0)
    oGetTotalImp:setString(13, "E")
    oGetTotalImp:SetNumeric(14, 0)
    oGetTotalImp:SetNumeric(15, 0)
    oGetTotalImp:setString(16, "S")
    oGetTotalImp:SetNumeric(17, 0)
    oGetTotalImp:SetNumeric(18, 0)
    oGetTotalImp:SetIn(19, StrTokArr(cbranches, "," ))
    oGetTotalImp:setString(20, StrTran(cperiodStart, "-", ""))
    oGetTotalImp:setString(21, StrTran(cperiodEnd, "-", ""))
    oGetTotalImp:setString(22, ' ')
    If cTaxedMovements <> "3"
        oGetTotalImp:SetNumeric(23, 0)
        oGetTotalImp:SetNumeric(24, 0)
        oGetTotalImp:SetNumeric(25, 0)
        oGetTotalImp:SetNumeric(26, 0)
        oGetTotalImp:setString(27, "")
    Else
        oGetTotalImp:SetNumeric(23, 0)
        oGetTotalImp:setString(24, "")
    EndIf
    oGetTotalImp:CBASEQUERY := oGetTotalImp:GETFIXQUERY()
    cAlias := oGetTotalImp:OpenAlias()
Endif
FWFreeObj(oGetTotalImp)
Return 

/*-------------------------------------------------------------
{Protheus.doc} GET TITCalculationtransitionReformFilter
API Valores de Entrada/Saída Reforma Tributária.
@author william.palma
@parâmetros de entrada do endpoint: cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements, cTransitionYear
@return Nil, nulo, não tem retorno.
@since 05/09/2025
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/tax-monitor/v1/tax-reform/tax-total', description='API Valores de Entrada/Saída Ref. a Transição da Reforma Trib.')
Function TITCalculationtransitionReformFilter()
    Local oJsResponse      := JsonObject():new() as json
    Local oQryStr          := oRest:getQueryRequest() as json
    Local aRequiredProperties as array
    //--------------------------------
    //Monta um array bidimensional com query strings que se deseja verificar se existem na requisição
    //--------------------------------
        aRequiredProperties	:= {;
            {'branches',400,; //PROPERTY //ERROR_CODE
            STR0069,;   //"O parâmetro 'branches' é obrigatório e não foi informado."
            STR0070},;  //"Informar Código de Filiais separadas por vírgula."
            {'periodStart',400,;
            STR0071,;   //"Nenhuma Data foi informada no parâmetro 'periodStart'."
            STR0072},;  //"Informar a data desejada para o período"
            {'periodEnd',400,;
            STR0073,;   //"Nenhuma Data foi informada no parâmetro 'periodEnd'."
            STR0072},;  //"Informar a data desejada para o período"
            {'taxedMovements',400,;
            STR0087,;   //"O parâmetro 'taxedMovements' é obrigatório e não foi informado."
            STR0088},;  //"O parâmetro deve conter 1,2 ou 3. Conforme manual de utilização."
            {'transitionYear',400,;
            STR0089,;   //"O parâmetro 'transitionYear' é obrigatório e não foi informado."
            STR0090};   //"O parâmetro deve conter o ano da reforma. Conforme manual de utilização."
        }
    //--------------------------------
    // Verifica se tem parâmetros obrigatórios na requisição da API
    //--------------------------------	
    If FISA318F6HasApiRequiredProperties(aRequiredProperties, oQryStr, @oJsResponse) 
        oJsResponse := CalculationtransitionReformFilter(oQryStr['branches'] , oQryStr['periodStart'] , oQryStr['periodEnd'], oQryStr['taxedMovements'], oQryStr['transitionYear'])   
    EndIf  

    oRest:setKeyHeaderResponse('Content-Type', 'application/json')
    oRest:setResponse(oJsResponse)
    //--------------------------------
    // Libera os objetos
    //-------------------------------  
    FwFreeArray(aRequiredProperties) 
    FWFreeObj(oJsResponse)
    FWFreeObj(oQryStr)
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET CalculationtransitionReformFilter
Função responsável por preencher o Json com com informações referentes a valores da Reforma Tributária
@author william.palma
@parâmetros de entrada do endpoint: cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements, cTransitionYear
@return object, Objeto contendo o JSON com resultado da query executada.
@since 05/09/2025
-------------------------------------------------------------------*/
Static Function CalculationtransitionReformFilter(cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTaxedMovements as character, cTransitionYear as character)
Local nPos         := 0                         as numeric
Local lHasNext     := .f.                       as logical
Local oJsonResp    := JsonObject():New()        as object
Local cAlias       := getNextAlias()            as character  

GetCalculationtransitionReformFilter(@cAlias, cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements, cTransitionYear)

// Iniciando o While
If (cAlias)->(!Eof()) .And. ((cAlias)->CBS_ENT > 0  .Or. (cAlias)->IBSUF_ENT > 0 .Or. (cAlias)->IBSMUN_ENT > 0 .Or. (cAlias)->IS_ENT > 0 .Or. (cAlias)->IS_SAI > 0)
    While (cAlias)->(!Eof())

        oJsonResp["items"] := {}
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]     :=   "CBS"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->CBS_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->CBS_SAI
        
        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]     :=   "IBS UF"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->IBSUF_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->IBSUF_SAI

        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]     :=   "IBS Mun"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->IBSMUN_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->IBSMUN_SAI

        aAdd( oJsonResp["items"], JsonObject():New() )
        nPos++
        oJsonResp["items"][nPos]["tax"]     :=   "IS"
        oJsonResp["items"][nPos]["valueEntry"]   :=   (cAlias)->IS_ENT
        oJsonResp["items"][nPos]["valueExit"]    :=   (cAlias)->IS_SAI

    (cAlias)->(DbSkip())
    EndDo
    oJsonResp["hasNext"]    := lHasNext
Else
    oJsonResp['code']       := 200
    oJsonResp["items"]      := {}
    oJsonResp["hasNext"]    := lHasNext
EndIf       
(cAlias)->(DBCloseArea())
Return oJsonResp:toJson()

/*--------------------------------------------------------------------
{Protheus.doc} GetCalculationtransitionReformFilter
(Responsável por executar a consulta aos campos da tabela SFT)
@author william.palma
@parâmetros de entrada do endpoint: cAlias, cBranches, cPeriodStart, cPeriodEnd, cTaxedMovements, cTransitionYear
@return Nil, nulo, não tem retorno.
@since 05/09/2025
//-------------------------------------------------------------------*/
Static Function GetCalculationtransitionReformFilter(cAlias as character, cBranches as character, cPeriodStart as character, cPeriodEnd as character, cTaxedMovements as character, cTransitionYear as character)
Local cQuery    := ''  as character

If oGetTransition == Nil
    cQuery := "SELECT "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS CBS_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS CBS_SAI, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IBSUF_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IBSUF_SAI, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IBSMUN_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IBSMUN_SAI, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IS_ENT, "
    cQuery += "COALESCE(SUM(CASE WHEN FT_TIPOMOV = ? THEN FT_VALCONT * ? ELSE ? END),0) AS IS_SAI "
    cQuery += " FROM "+RetSQLName("SFT")+" SFT "
    cQuery += " WHERE FT_FILIAL IN (?) AND FT_ENTRADA BETWEEN ? AND ? AND D_E_L_E_T_ = ? "
    If cTaxedMovements == "1"
        cQuery += " AND (FT_VALICM > ? OR FT_VALPIS > ? OR FT_VALCOF > ?) AND FT_VALCONT > ? "
    ElseIF cTaxedMovements == "2"
        cQuery += " AND (FT_VALICM = ? AND FT_VALPIS = ? AND FT_VALCOF = ?) AND FT_VALCONT > ? "
    Else
        cQuery += " AND FT_VALCONT > ? "
    EndIf
	cQuery += " AND FT_DTCANC = ? "
    oGetTransition := FwExecStatement():New()
    oGetTransition:SetQuery(ChangeQuery(cQuery))
Endif

If oGetTransition != Nil
    oGetTransition:setString(1, "E")
    oGetTransition:SetNumeric(2, 0.009)
    oGetTransition:SetNumeric(3, 0)
    oGetTransition:setString(4, "S")
    oGetTransition:SetNumeric(5, 0.009)
    oGetTransition:SetNumeric(6, 0)
    If cTransitionYear == "2026"
        oGetTransition:setString(7, "E")
        oGetTransition:SetNumeric(8, 0.001)
        oGetTransition:SetNumeric(9, 0)
        oGetTransition:setString(10, "S")
        oGetTransition:SetNumeric(11, 0.001)
        oGetTransition:SetNumeric(12, 0)
        oGetTransition:setString(13, "E")
        oGetTransition:SetNumeric(14, 0)
        oGetTransition:SetNumeric(15, 0)
        oGetTransition:setString(16, "S")
        oGetTransition:SetNumeric(17, 0)
        oGetTransition:SetNumeric(18, 0)
    Else
        oGetTransition:setString(7, "E")
        oGetTransition:SetNumeric(8, 0.0005)
        oGetTransition:SetNumeric(9, 0)
        oGetTransition:setString(10, "S")
        oGetTransition:SetNumeric(11, 0.0005)
        oGetTransition:SetNumeric(12, 0)
        oGetTransition:setString(13, "E")
        oGetTransition:SetNumeric(14, 0.0005)
        oGetTransition:SetNumeric(15, 0)
        oGetTransition:setString(16, "S")
        oGetTransition:SetNumeric(17, 0.0005)
        oGetTransition:SetNumeric(18, 0)
    EndIf
    oGetTransition:setString(19, "E")
    oGetTransition:SetNumeric(20, 0)
    oGetTransition:SetNumeric(21, 0)
    oGetTransition:setString(22, "S")
    oGetTransition:SetNumeric(23, 0)
    oGetTransition:SetNumeric(24, 0)
    oGetTransition:SetIn(25, StrTokArr(cbranches, "," ))
    oGetTransition:setString(26, StrTran(cperiodStart, "-", ""))
    oGetTransition:setString(27, StrTran(cperiodEnd, "-", ""))
    oGetTransition:setString(28, ' ')
    If cTaxedMovements <> "3"
        oGetTransition:SetNumeric(29, 0)
        oGetTransition:SetNumeric(30, 0)
        oGetTransition:SetNumeric(31, 0)
        oGetTransition:SetNumeric(32, 0)
        oGetTransition:setString(33, "")
    Else
        oGetTransition:SetNumeric(29, 0)
        oGetTransition:setString(30, "")
    EndIf
    oGetTransition:CBASEQUERY := oGetTransition:GETFIXQUERY()
    cAlias := oGetTransition:OpenAlias()
Endif
FWFreeObj(oGetTransition)
Return 
