#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.ciapAprop.ch"
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFA,SF9", name="CIAP", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc}  ciapApropSmartviewBusinessObject
Classe para criação do Objeto CIAP - Controle de Crédito do ICMS do Ativo Permanente
@author Raphael Ventura
@since 14/08/2023
*/
//-------------------------------------------------------------------  

class ciapApropSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Raphael Ventura
@since 14/08/2023
*/
//-------------------------------------------------------------------   
method new() class ciapApropSmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"Objeto de Negócio CIAP"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto CIAP - Controle de Icms sobre Ativo Permanente - Apropriações (SFA/SF9)"
    
    self:nPageSize := oFisTools:paginacao(3000)
    oFisTools:validPerg(self:setPergunte("FISTRR9951"))//Indica o pergunte que será utilizado no relatório
    freeobj(oFisTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Raphael Ventura
@since 14/08/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class ciapApropSmartviewBusinessObject

    Local cQuery        as Character
    Local oFisTools     := fisSmartviewTools():new()
    Local oFiltQry      := oFilter:getParameters() as Json
    Local dStartDate    := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
    Local dEndDate      := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
    Local cFilDe        := oFiltQry['MV_PAR03',1] as Character
    Local cFilAte       := oFiltQry['MV_PAR04',1] as Character
    Local cCodDe        := oFiltQry['MV_PAR05',1] as Character
    Local cCodAte       := oFiltQry['MV_PAR06',1] as Character
    Local dDtEntDe      := FwDateTimeToLocal(oFiltQry['MV_PAR07',1])[1] as date
    Local dDtEntAte     := FwDateTimeToLocal(oFiltQry['MV_PAR08',1])[1] as date    
    Local lOk           := .T. as logical
    Local aFiliais      := {} as array
    Local cFils         := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    local aBind         := {}  as array
    local nlA           := 0   as numeric
    Local oPrepare      := nil as object

    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    If lOk

        //definindo a quantidade máxima por página (default 100)
        self:setPageSize(200)
        
        cQuery := "         SELECT  "
        cQuery += "             SFA.FA_FILIAL, "
        cQuery += "             SFA.FA_CODIGO, "
        cQuery += "             SFA.FA_DATA, "
        cQuery += "             SFA.FA_VALOR, "
        cQuery += "             SFA.FA_VALICCO, "
        cQuery += "             SFA.FA_TOTTRIB, "
        cQuery += "             SFA.FA_TOTSAI, "
        cQuery += "             CASE "
        cQuery += "             	WHEN SF9.F9_CODIGO IS NULL THEN '' "
        cQuery += "             	WHEN SF9.F9_CODIGO IS NOT NULL THEN SF9.F9_CODIGO "
        cQuery += "             END AS F9_CODIGO, "
        cQuery += "             SF9.F9_DESCRI, "
        cQuery += "             CASE "
        cQuery += "                 WHEN SF9.F9_SLDPARC = 0 THEN 'BAIXADO' "
        cQuery += "                 ELSE 'EM APROPRIACAO' "
        cQuery += "             END AS STATUS, "
        cQuery += "             SF9.F9_DTENTNE, "
        cQuery += "             SF9.F9_VALICMS, "
        cQuery += "             SF9.F9_PICM, "
        cQuery += "             SF9.F9_ICMIMOB, "
        cQuery += "             SF9.F9_VLESTOR, "
        cQuery += "             SF9.F9_BXICMS, "
        cQuery += "             SF9.F9_VALICCO, "
        cQuery += "             SF9.F9_VALFRET, "
        cQuery += "             SF9.F9_VALICMP, "
        cQuery += "             SF9.F9_VALICST, "
        cQuery += "             SF9.F9_QTDPARC, "
        cQuery += "             SF9.F9_SLDPARC, "
        cQuery += "             SF9.F9_CODBAIX "
        cQuery += "         FROM "
        cQuery +=               RetSqlName("SFA") + " SFA "
        cQuery += "             LEFT JOIN " + RetSqlName("SF9") + " SF9     ON SF9.F9_FILIAL = SFA.FA_FILIAL
        cQuery += "         						                        AND SF9.F9_CODIGO = SFA.FA_CODIGO "
        cQuery += "         						                        AND SF9.F9_MOTIVO = SFA.FA_MOTIVO "
        cQuery += "         						                        AND SF9.D_E_L_E_T_ = ? "
        aadd( aBind , ' ' )
        cQuery += "		    WHERE "

        If !Empty(cFilAte)
            cQuery +=     " SFA.FA_FILIAL IN ( ? ) AND "
            aadd( aBind , aFiliais )
        EndIf
        
        cQuery += "	            SFA.FA_DATA BETWEEN ? AND ? " //SFA.FA_DATA BETWEEN " + ValToSql(dStartDate) + " AND " + ValToSql(dEndDate)
                                aadd( aBind , dStartDate )
                                aadd( aBind , dEndDate )
        cQuery += "	            AND SF9.F9_DTENTNE BETWEEN ? AND ? " //AND SF9.F9_DTENTNE BETWEEN " + ValToSql(dDtEntDe) + " AND " + ValToSql(dDtEntAte)
                                aadd( aBind , dDtEntDe )
                                aadd( aBind , dDtEntAte )        
        cQuery += "	            AND SFA.D_E_L_E_T_ = ? "
        aadd( aBind , ' ' )        
        cQuery += "             AND F9_CODIGO BETWEEN ? AND ? " //AND F9_CODIGO BETWEEN '" + cCodDe + "' AND '" + cCodAte + "' "
        aadd( aBind , cCodDe )
        aadd( aBind , cCodAte )

        If oFilter:hasFilter()
            cQuery += " AND " + oFilter:getSQLExpression()
        EndIf

        oPrepare := FWPreparedStatement():New( ChangeQuery(cQuery) )
        for nlA := 1 to len(aBind)
            if Valtype(aBind[nlA]) == 'A'
                oPrepare:setIn( nlA, aBind[nlA] )
            elseif Valtype(aBind[nlA]) == 'D'
                oPrepare:setDate( nlA, aBind[nlA] )
            else
                oPrepare:setString( nlA, aBind[nlA] )
            endif
        next nlA
        cQuery := oPrepare:getFixQuery()
        self:setQuery(cQuery)         
    ENDIF
    freeobj(oFisTools)
    aSize(aBind,0)
    aBind := Nil
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Raphael Ventura
@since 14/08/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class ciapApropSmartviewBusinessObject

    //              ( Nome,             Descricao ,                                     Tipo ,      Display ,               cRealName ,     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_FILIAL",       "Filial" ,                                      "string",   "Filial" ,              "FA_FILIAL" ,   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_CODIGO",       "Código CIAP" ,                                 "string",   "Cód. CIAP" ,           "FA_CODIGO",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_DATA",         "Data de Apropriacao" ,                         "date",     "Data Aprop." ,         "FA_DATA",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_VALOR",        "Valor de Apropriacao" ,                        "number",   "Valor Aprop." ,        "FA_VALOR",     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_VALICCO",      "Valor Parcela ICMS Complementar",              "number",   "Val. Parc. ICMS Comp.","FA_VALICCO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_TOTTRIB",      "Valor Total Tributado" ,                       "number",   "Val. Tot. Trib." ,     "FA_TOTTRIB",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FA_TOTSAI",       "Total de Saídas" ,                             "number",   "Tot. Saídas" ,         "FA_TOTSAI",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_CODIGO",       "Código do Bem" ,                               "string",   "Cod. do Bem" ,         "F9_CODIGO",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_DESCRI",       "Descrição do Bem" ,                            "string",   "Desc. do Bem" ,        "F9_DESCRI",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("STATUS",          "Status do Bem" ,                               "string",   "Status do Bem" ,       "STATUS",       /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_DTENTNE",      "Data de Entrada do Bem" ,                      "date",     "Data Entr. do Bem." ,  "F9_DTENTNE",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICMS",      "Valor Total Passível de Crédito" ,             "number",   "Val. Tot. Pass. Cred.","F9_VALICMS",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_PICM",         "Alíquota ICMS" ,                               "number",   "Aliq. ICMS" ,          "F9_PICM",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_ICMIMOB",      "ICMS Imobilizado" ,                            "number",   "ICMS Imob." ,          "F9_ICMIMOB",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VLESTOR",      "Valor Parcela Passível de Credito" ,           "number",   "Val. Parc. Pass. Cred.","F9_VLESTOR",  /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_BXICMS",       "Valor Baixado ICMS" ,                          "number",   "Val. Baix. ICMS" ,     "F9_BXICMS",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICCO",      "Valor ICMS Complementar" ,                     "number",   "Val. ICMS Comp." ,     "F9_VALICCO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALFRET",      "Valor ICMS Frete" ,                            "number",   "Val. ICMS Frete" ,     "F9_VALFRET",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICMP",      "Valor ICMS Próprio" ,                          "number",   "Val. ICMS Próp." ,     "F9_VALICMP",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICST",      "Valor ICMS-ST" ,                               "number",   "Val. ICMS-ST" ,        "F9_VALICST",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_QTDPARC",      "Parcela" ,                                     "number",   "Parcela" ,             "F9_QTDPARC",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_SLDPARC",      "Saldo Parcelas" ,                              "number",   "Saldo Parcelas" ,      "F9_SLDPARC",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_CODBAIX",      "Código do Bem Principal" ,                     "string",   "Cód. Bem Princ." ,     "F9_CODBAIX",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )

return self:oSchema

