#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdf3o.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="F3O", name="efdF3OSmartviewBusinessObject", country="BRA", initialRelease="12.1.2210") 
//-------------------------------------------------------------------
/*{Protheus.doc}  efdF3OSmartviewBusinessObject
Classe para criação do Objeto Retenção na fonte PIS e COFINS
@author julia.mota
@since 14/12/2023
*/
//-------------------------------------------------------------------  
class efdF3OSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider       
    public method new() as object
    public method getData() as object 
    public method getSchema() as object 
endclass
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author julia.mota
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method new() class efdF3OSmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001) //"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002) //" EFD Contribuiçoes - F3O"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003) //"Este objeto de negócio fornece o detalhamento do EFD Contribuições (F3O)"
   
    oFisTools:validPerg(self:setPergunte("FISTRR1023"))
    freeobj(oFisTools)
 
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author julia.mota
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdF3OSmartviewBusinessObject
    Local cQuery     as Character
    Local cAlias     as Character
    Local oFisTools  := fisSmartviewTools():new()
    Local oFiltQry   := oFilter:getParameters() as Json
    Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as Date
    Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as Date
    Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
    Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
    Local lOk        := .T. as Logical
    Local aFiliais   := {} as array
    Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    Local oPrepare   := nil as object

    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk
        cQuery := " SELECT " 
        cQuery += " F3O.F3O_FILIAL, "
        cQuery += " F3O.F3O_CST, 	"
        cQuery += " F3O.F3O_RECBA,  "
        cQuery += " F3O.F3O_RECBC, 	"
        cQuery += " F3O.F3O_RECBD, 	"
        cQuery += " F3O.F3O_RECBF, 	"
        cQuery += " F3O.F3O_RECBCD, "
        cQuery += " F3O.F3O_TOTREC, "
        cQuery += " F3O.F3O_PERCD, 	"
        cQuery += " F3O.F3O_REGIME, "
        cQuery += " F3O.F3O_METODO, "
        cQuery += " F3O.F3O_PER 	"
        cQuery += " FROM " + RetSQLName("F3O") + " F3O "
        cQuery += " WHERE " 
        cQuery += "    F3O.F3O_FILIAL IN ( ? ) "        		            
        cQuery += "    AND F3O.F3O_PER BETWEEN ? AND ? "    
        cQuery += "    AND F3O.D_E_L_E_T_ = ? "     
		
        // Instancia a classe
    	oPrepare := FwExecStatement():New(cQuery)

        oPrepare:setIn( 1, aFiliais )
		oPrepare:setDate( 2, dStartDate)
		oPrepare:setDate( 3, dEndDate)	
        oPrepare:setString( 4, ' ' )
 
        cAlias := oPrepare:OpenAlias() 

        While !(cAlias)->(Eof())

            self:oData:appendData({;
                "F3O_FILIAL"      : (cAlias)->F3O_FILIAL,;
                "F3O_CST"         : (cAlias)->F3O_CST,;
                "F3O_RECBA"       : (cAlias)->F3O_RECBA,;
                "F3O_RECBC"       : (cAlias)->F3O_RECBC,;
                "F3O_RECBD"       : (cAlias)->F3O_RECBD,;
                "F3O_RECBF"       : (cAlias)->F3O_RECBF,;
                "F3O_RECBCD"      : (cAlias)->F3O_RECBCD,;
                "F3O_TOTREC"      : (cAlias)->F3O_TOTREC,;
                "F3O_PERCD"       : (cAlias)->F3O_PERCD,;
                "F3O_REGIME"      : (cAlias)->F3O_REGIME,;
                "F3O_METODO"      : (cAlias)->F3O_METODO,;
                "F3O_PER"         : totvs.framework.treports.date.stringToTimeStamp((cAlias)->F3O_PER);
            })

            (cAlias)->(dbSkip())

        Enddo

        (cAlias)->(DBCloseArea())
        oPrepare:Destroy()
        
    endif

    freeobj(oFisTools)
	oPrepare := nil 
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Julia Mota
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdF3OSmartviewBusinessObject
    self:addProperty("F3O_FILIAL"   , "Filial          "             , "string"  , "Filial"                        , "F3O_Filial"    )
    self:addProperty("F3O_CST"      , "CST de Pis e Cofins      "    , "string"  , "CST"                           , "F3O_CST"       )
    self:addProperty("F3O_RECBA"    , "Receita Bloco A          "    , "number"  , "Receita Bloco A"               , "F3O_RECBA"     )
    self:addProperty("F3O_RECBC"    , "Receita Bloco C          "    , "number"  , "Receita Bloco C"               , "F3O_RECBC"     )
    self:addProperty("F3O_RECBD"    , "Receita Blocos C e D     "    , "number"  , "Receita Bloco CD"              , "F3O_RECBD"     )
    self:addProperty("F3O_RECBF"    , "Receita Bloco F          "    , "number"  , "Receita Bloco F"               , "F3O_RECBF"     )
    self:addProperty("F3O_RECBCD"   , "Receita do Bloco D       "    , "number"  , "Receita Bloco D"               , "F3O_RECBCD"    )
    self:addProperty("F3O_TOTREC"   , "Total Receita A, C, D e F"    , "number"  , "Total Receita A,C,D E F "      , "F3O_TOTREC"    )
    self:addProperty("F3O_PERCD"    , "Percentual C e D.        "    , "number"  , "Percentual C e D"              , "F3O_PERCD"     )
    self:addProperty("F3O_REGIME"   , "Regime de Apuração       "    , "string"  , "Regime de Apuração"            , "F3O_REGIME"    )
    self:addProperty("F3O_METODO"   , "Método de Rateio         "    , "string"  , "Método de Rateio"              , "F3O_METODO"    )
    self:addProperty("F3O_PER"      , "Período de Apuração      "    , "date"    , "Período Apuração"              , "F3O_PER"       )

return self:oSchema
