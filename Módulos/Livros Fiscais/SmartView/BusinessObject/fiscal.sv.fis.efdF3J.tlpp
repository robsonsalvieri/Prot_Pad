#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdF3J.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="F3J",;
	name="efdF3JSmartviewBusinessObject", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc}  efdF3JSmartviewBusinessObject
@author Djalma Mathias da Silva
@since 14/12/2023
*/
//-------------------------------------------------------------------  
class efdF3JSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data oPrepare as object

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Djalma Mathias da Silva
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method new() class efdF3JSmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new() as object
	::oPrepare := nil 
	_Super:new()
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contribuiçoes - F3J"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece os valores por regra de código de receita do EFD Contribuições (F3J)"
	//self:setIsCBoxLookup(.T., .F.)

	oFisTools:validPerg(::setPergunte("FISTRR1022"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Djalma Mathias da Silva
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdF3JSmartviewBusinessObject

	Local cQuery     as Character
	Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new() as object
	Local oFiltQry   := oFilter:getParameters() as Json
	Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
	Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
	Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
	Local lOk		 := .T. as Logical
	Local aFiliais   := {} as array
	Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	If lOk

		cQuery += " SELECT "
		cQuery +=  " F3J_FILIAL,"
		cQuery +=  " F3J_PER,"
		cQuery +=  " F3J_CODREC,"
		cQuery +=  " F3J_VLTRIB,"
		cQuery +=  " CASE "
		cQuery +=   	 " WHEN F3J.F3J_TRIBUT = '1' THEN '1=PIS Não Cumulativo' "
		cQuery +=   	 " WHEN F3J.F3J_TRIBUT = '2' THEN '2=PIS Cumulativo' "
		cQuery +=   	 " WHEN F3J.F3J_TRIBUT = '3' THEN '3=COFINS Não Cumulativo' "
		cQuery +=   	 " WHEN F3J.F3J_TRIBUT = '4' THEN '4=COFINS Cumulativo' "
		cQuery +=   	 " ELSE '' "
		cQuery += 		" END AS F3J_TRIBUT, "
		cQuery += 	" F3J_TOTPAG,"
		cQuery += 	" F3J_CODPAG,"
		cQuery += 	" F3J_PERCEN,"
		cQuery += 	" CASE WHEN F3J_CONSOL = '1' THEN 'Sim' WHEN F3J_CONSOL = '2' THEN 'Não' ELSE '' END F3J_CONSOL "
		cQuery += 	" FROM "
		cQuery +=     RetSqlName("F3J")+" F3J "
		cQuery += " WHERE "
		cQuery += 	" F3J_FILIAL IN ( ? ) "
		cQuery += 	" AND F3J_PER BETWEEN ? AND ? "
		cQuery += 	" AND D_E_L_E_T_ = ?"
		cQuery += " ORDER BY F3J_FILIAL,F3J_PER "

		::oPrepare := FWExecStatement():New(ChangeQuery(cQuery))
		::oPrepare:setIn(1,aFiliais)
		::oPrepare:setDate(2,dStartDate )
		::oPrepare:setDate(3,dEndDate)
		::oPrepare:setString(4,' ')
        
        cAlias:= ::oPrepare:OpenAlias()

		while !(cAlias)->(Eof())

			self:oData:appendData({;
            "F3J_FILIAL": AllTrim((cAlias)->F3J_FILIAL),; 
			"F3J_PER"   : totvs.framework.treports.date.stringToTimeStamp((cAlias)->F3J_PER),; 
			"F3J_CODREC": (cAlias)->F3J_CODREC,;
			"F3J_VLTRIB": (cAlias)->F3J_VLTRIB,;
			"F3J_TOTPAG": (cAlias)->F3J_TOTPAG,;
			"F3J_CONSOL": (cAlias)->F3J_CONSOL,;
			"F3J_CODPAG": (cAlias)->F3J_CODPAG,;
			"F3J_TRIBUT": (cAlias)->F3J_TRIBUT,;
			"F3J_PERCEN": (cAlias)->F3J_PERCEN;
			})

			(cAlias)->(dbSkip())

		enddo

		(cAlias)->(DBCloseArea())

	Endif

	freeobj(oFisTools)
	freeObj(::oPrepare)
	freeobj(oFiltQry)
	aSize(aFiliais,0)
	aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Djalma Mathias da Silva
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdF3JSmartviewBusinessObject

	self:addProperty("F3J_FILIAL"   ,"Filial"          ,"string","Filial"      ,"F3J_FILIAL"   )//01"Filial"           "F3J_FILIAL"
	self:addProperty("F3J_PER"      ,"Per.Apuração"    ,"date"  ,"Per.Apuração","F3J_PER"      )//02"Per.Apuração"     "F3J_PER"
	self:addProperty("F3J_CODREC"   ,"Cod. Receita"    ,"string","Cod. Receita","F3J_CODREC"   )//03"Cod. Receita"     "F3J_CODREC"
	self:addProperty("F3J_VLTRIB"   ,"Vl.TRibuto"      ,"number","Vl.TRibuto"  ,"F3J_VLTRIB"   )//04"Vl.TRibuto  "     "F3J_VLTRIB"
	self:addProperty("F3J_TOTPAG"   ,"Vl.Tot.Pagar"    ,"number","Vl.Tot.Paga" ,"F3J_TOTPAG"   )//05"Vl.Tot.Pagar"     "F3J_TOTPAG"
	self:addProperty("F3J_CONSOL"   ,"Consolidado "    ,"string","Consolidado" ,"F3J_CONSOL"   )//06"Consolidado"      "F3J_CONSOL"
	self:addProperty("F3J_CODPAG"   ,"Tot.Pagar"       ,"number","Tot.Pagar"   ,"F3J_CODPAG"   )//07"Tot.Pagar   "     "F3J_CODPAG"
	self:addProperty("F3J_TRIBUT"   ,"Tributo"         ,"string","Tributo"     ,"F3J_TRIBUT"   )//08"Tributo     "     "F3J_TRIBUT"
	self:addProperty("F3J_PERCEN"   ,"Perc.Tot.Deb"    ,"number","Perc.Tot.Deb","F3J_PERCEN"   )//09"Perc.Tot.Deb"     "F3J_PERCEN"


return self:oSchema
