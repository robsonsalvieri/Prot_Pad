#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdF0T.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider

//-------------------------------------------------------------------
/*{Protheus.doc}  efdF0TSmartviewBusinessObject
@author allef.souza
@since 31/07/2023
@see https://tdn.totvs.com/pages/viewpage.action?pageId=781211878
*/
//-------------------------------------------------------------------  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="F0T",;
	name="Objeto de Negócio EFD - F0T", country="BRA", initialRelease="12.1.2310")
class efdF0TSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new() as object
	public method getData() as object
	public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdF0TSmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new()

	_Super:new()
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contrinuiçoes - F0T"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (F0T)"
	self:setIsCBoxLookup(.T., .F.)

	oFisTools:validPerg(::setPergunte("FISTRR1011"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: ::oData 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdF0TSmartviewBusinessObject

	Local cQuery     as Character
	Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new()
	Local oFiltQry   := oFilter:getParameters() as Json
    Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
    Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
	Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
	Local lOk		 := .T. as Logical
    Local aFiliais   := {} as array
    Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    Local oPrepare   := nil as object
	Private lProcRH  as Logical
	
	if cFils == 'Erro'
		oFisTools:validFil(.f.) //zzz a branco cai aqui
		lOk := .f.
	endif

	if lOk

		lProcRH := GetNewPar("MV_SPCBPRH",.F.)

		cQuery += "SELECT "
	
		cQuery += "  F0T.F0T_TIPO, "
		cQuery += "  CASE "
		cQuery += "      WHEN F0T.F0T_TIPO = '1' THEN '1-Nota Fiscal - Regime Caixa' "
		cQuery += "      WHEN F0T.F0T_TIPO = '2' THEN '2-Título Financeiro - Regime Caixa' "
		cQuery += "      WHEN F0T.F0T_TIPO = '3' THEN '3-CPRB Regime Caixa' "
		cQuery += "      WHEN F0T.F0T_TIPO = '4' THEN '4-Diferimento da Contribuição do Período Atual' "
		cQuery += "      WHEN F0T.F0T_TIPO = '5' THEN '5-Diferimento da Contribuição de Período Anterior' "
		cQuery += "      WHEN F0T.F0T_TIPO = '6' THEN '6-CPRB (RH)' "
		cQuery += "      WHEN F0T.F0T_TIPO = '7' THEN '7-CPRB (Faturamento)' "
		cQuery += "      WHEN F0T.F0T_TIPO = '8' THEN '8-Títulos Instituição Financeira' "
		cQuery += "      WHEN F0T.F0T_TIPO = 'A' THEN 'A-[PIS] Créditos a Descontar Vinculados a Contribuição Diferida de Período Anterior' "
		cQuery += "      WHEN F0T.F0T_TIPO = 'B' THEN 'B-[COFINS] Créditos a Descontar Vinculados a Contribuição Diferida de Período Anterior' "
		cQuery += "      ELSE '' "
		cQuery += "  END AS F0T_TIPODSC, "
		cQuery += "  F0T.F0T_FILIAL, "
		cQuery += "  F0T.F0T_NUMTIT, "
		cQuery += "  F0T.F0T_PREFIX, "
		cQuery += "  F0T.F0T_PARC, "
		cQuery += "  F0T.F0T_DTEMI, "
		cQuery += "  F0T.F0T_DTRECB, "
		cQuery += "  F0T.F0T_PERREC, "
		cQuery += "  F0T.F0T_VLCONT, "
		cQuery += "  F0T.F0T_CSTPIS, "
		cQuery += "  F0T.F0T_BASPIS, "
		cQuery += "  F0T.F0T_ALQPIS, "
		cQuery += "  F0T.F0T_VALPIS, "
		cQuery += "  F0T.F0T_CSTCOF, "
		cQuery += "  F0T.F0T_BASCOF, "
		cQuery += "  F0T.F0T_ALQCOF, "
		cQuery += "  F0T.F0T_VALCOF, "
		cQuery += "  F0T.F0T_NUMNF, "
		cQuery += "  F0T.F0T_SER, "
		cQuery += "  F0T.F0T_CFOP, "
		cQuery += "  F0T.F0T_ITEM, "
		cQuery += "  F0T.F0T_MODELO, "
		cQuery += "  F0T.F0T_EXCPRB, "
		cQuery += "  F0T.F0T_CODATV, "
		cQuery += "  F0T.F0T_BCCPRB, "
		cQuery += "  F0T.F0T_AQCPRB, "
		cQuery += "  F0T.F0T_VLCPRB, "
		cQuery += "  F0T.F0T_IDCF8, "
		cQuery += "  F0T.F0T_RECDIF, "
		cQuery += "  F0T.F0T_VALREC, "
		cQuery += "  F0T.F0T_IFEXCL, "
		cQuery += "  F0T.F0T_CDBLCI, "
		cQuery += "  F0T.F0T_CNPJ "
		cQuery += "  FROM "
		cQuery +=    RetSQLName("F0T") + " F0T "
		cQuery += "  WHERE "
		cQuery += "  F0T.F0T_FILIAL IN ( ? ) " 	                   
		cQuery += "  AND F0T.F0T_PER BETWEEN ? AND ? " 		                   
		cQuery += "  AND F0T.D_E_L_E_T_ = ? "

		oPrepare := FWExecStatement():New(ChangeQuery(cQuery))
		
		oPrepare:setIn(1,aFiliais)
		oPrepare:setDate(2,dStartDate )
		oPrepare:setDate(3,dEndDate)
		oPrepare:setString(4,' ')

		cAlias:= oPrepare:OpenAlias() 

		while !(cAlias)->(Eof())			

			self:oData:appendData({;
				"F0T_ALQCOF" : (cAlias)->F0T_ALQCOF,;
				"F0T_ALQPIS" : (cAlias)->F0T_ALQPIS,;
				"F0T_AQCPRB" : (cAlias)->F0T_AQCPRB,;
				"F0T_BASCOF" : (cAlias)->F0T_BASCOF,;
				"F0T_BASPIS" : (cAlias)->F0T_BASPIS,;
				"F0T_BCCPRB" : (cAlias)->F0T_BCCPRB,;
				"F0T_CFOP"   : (cAlias)->F0T_CFOP,;
				"F0T_CODATV" : (cAlias)->F0T_CODATV,;
				"F0T_CSTCOF" : (cAlias)->F0T_CSTCOF,;
				"F0T_CSTPIS" : (cAlias)->F0T_CSTPIS,;
				"F0T_DTEMI"  : totvs.framework.treports.date.stringToTimeStamp((cAlias)->F0T_DTEMI),;
				"F0T_DTRECB" : totvs.framework.treports.date.stringToTimeStamp((cAlias)->F0T_DTRECB),;
				"F0T_EXCPRB" : (cAlias)->F0T_EXCPRB,;
				"F0T_FILIAL" : (cAlias)->F0T_FILIAL,;
				"F0T_IDCF8"  : (cAlias)->F0T_IDCF8,;
				"F0T_IFEXCL" : (cAlias)->F0T_IFEXCL,;
				"F0T_ITEM"   : (cAlias)->F0T_ITEM,;
				"F0T_MODELO" : (cAlias)->F0T_MODELO,;
				"F0T_NUMNF"  : (cAlias)->F0T_NUMNF,;
				"F0T_NUMTIT" : (cAlias)->F0T_NUMTIT,;
				"F0T_PARC"   : (cAlias)->F0T_PARC,;
				"F0T_PERREC" : (cAlias)->F0T_PERREC,;
				"F0T_PREFIX" : (cAlias)->F0T_PREFIX,;
				"F0T_RECDIF" : (cAlias)->F0T_RECDIF,;
				"F0T_SER"    : (cAlias)->F0T_SER,;
				"F0T_TIPO"   : (cAlias)->F0T_TIPO,;
				"TIPODSC"    : (cAlias)->F0T_TIPODSC,;
				"F0T_VALCOF" : (cAlias)->F0T_VALCOF,;
				"F0T_VALPIS" : (cAlias)->F0T_VALPIS,;
				"F0T_VALREC" : (cAlias)->F0T_VALREC,;
				"F0T_VLCONT" : (cAlias)->F0T_VLCONT,;
				"F0T_VLCPRB" : (cAlias)->F0T_VLCPRB,;
				"F0T_CDBLCI" : (cAlias)->F0T_CDBLCI,;
				"F0T_CNPJ"   : (cAlias)->F0T_CNPJ;
				})

			(cAlias)->(dbSkip())

		enddo
	
		(cAlias)->(DBCloseArea())

	endif

    freeobj(oFisTools)
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: ::oSchema 
@author allef.souza
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdF0TSmartviewBusinessObject

	Local   cLegendaF0T_TIPO    as Character

	cLegendaF0T_TIPO := "1=1-Nota Fiscal - Regime Caixa;"
	cLegendaF0T_TIPO += "2=2-Título Financeiro - Regime Caixa;"
	cLegendaF0T_TIPO += "3=3-CPRB Regime Caixa;"
	cLegendaF0T_TIPO += "4=4-Diferimento da Contribuição do Período Atual;"
	cLegendaF0T_TIPO += "5=5-Diferimento da Contribuição de Período Anterior;"
	cLegendaF0T_TIPO += "6=6-CPRB (RH);"
	cLegendaF0T_TIPO += "7=7-CPRB (Faturamento);"
	cLegendaF0T_TIPO += "8=8-Títulos Instituição Financeira;"
	cLegendaF0T_TIPO += "A=A-[PIS] Créditos a Descontar Vinculados a Contribuição Diferida de Período Anterior;"
	cLegendaF0T_TIPO += "B=B-[COFINS] Créditos a Descontar Vinculados a Contribuição Diferida de Período Anterior"

	self:addProperty("F0T_ALQCOF", "Alíquota de COF"          , "number", "Alq.COF"        , "F0T_ALQCOF"                      )
	self:addProperty("F0T_ALQPIS", "Alíquota de PIS"          , "number", "Alq.PIS"        , "F0T_ALQPIS"                      )
	self:addProperty("F0T_AQCPRB", "Alíquota de CPRB"         , "number", "Alq.CPRB"       , "F0T_AQCPRB"                      )
	self:addProperty("F0T_BASCOF", "Base de Cálculo COF"      , "number", "Base COF"       , "F0T_BASCOF"                      )
	self:addProperty("F0T_BASPIS", "Base de Cálculo PIS"      , "number", "Base PIS"       , "F0T_BASPIS"                      )
	self:addProperty("F0T_BCCPRB", "Base de Cálculo CPRB"     , "number", "Base CPRB"      , "F0T_BCCPRB"                      )
	self:addProperty("F0T_CFOP"  , "CFOP"                     , "string", "CFOP"           , "F0T_CFOP"                        )
	self:addProperty("F0T_CODATV", "Código de Atividade"      , "string", "Cod. Ativ."     , "F0T_CODATV"                      )
	self:addProperty("F0T_CSTCOF", "CST COF"                  , "string", "CST COF"        , "F0T_CSTCOF"                      )
	self:addProperty("F0T_CSTPIS", "CST PIS"                  , "string", "CST PIS"        , "F0T_CSTPIS"                      )
	self:addProperty("F0T_DTEMI" , "Data de Emissão"          , "date"  , "Data Emissão"   , "F0T_DTEMI"                       )
	self:addProperty("F0T_DTRECB", "Data de Recebimento"      , "date"  , "Data Recebim"   , "F0T_DTRECB"                      )
	self:addProperty("F0T_EXCPRB", "Exclusão da CPRB"         , "number", "Exclu.CPRB"     , "F0T_EXCPRB"                      )
	self:addProperty("F0T_FILIAL", "Filial do Sistema"        , "string", "Filial"         , "F0T_FILIAL"                      )
	self:addProperty("F0T_IDCF8" , "Demais Documentos"        , "string", "Demais Docs."   , "F0T_IDCF8"                       )
	self:addProperty("F0T_IFEXCL", "Exclusões do Bloco I"     , "number", "Excl BlocoI"    , "F0T_IFEXCL"                      )
	self:addProperty("F0T_ITEM"  , "Item da Nota Fiscal"      , "string", "Item NF"        , "F0T_ITEM"                        )
	self:addProperty("F0T_MODELO", "Modelo Documento Fiscal"  , "string", "Modelo NF"      , "F0T_MODELO"                      )
	self:addProperty("F0T_NUMNF" , "Número da Nota Fiscal"    , "string", "Número Nota"    , "F0T_NUMNF"                       )
	self:addProperty("F0T_NUMTIT", "Número do Título"         , "string", "Num.Tit"        , "F0T_NUMTIT"                      )
	self:addProperty("F0T_PARC"  , "Parcela do Título"        , "string", "Parcela"        , "F0T_PARC"                        )
	self:addProperty("F0T_PERREC", "Percentual de Recebimento", "number", "Perc.Receb."    , "F0T_PERREC"                      )
	self:addProperty("F0T_PREFIX", "Prefixo do Título"        , "string", "Prefixo"        , "F0T_PREFIX"                      )
	self:addProperty("F0T_RECDIF", "Receita Diferida"         , "number", "Rec.Dif"        , "F0T_RECDIF"                      )
	self:addProperty("F0T_SER"   , "Série da Nota Fiscal"     , "string", "Série NF"       , "F0T_SER"                         )
	self:addProperty("F0T_TIPO"  , "Tipo de Detalhamento"     , "string", "Tipo Det."      , "F0T_TIPO"    , cLegendaF0T_TIPO  )
	self:addProperty("TIPODSC"   , "Descrição do Tipo"        , "string", "Desc. Tipo Det.", "TIPODSC"     ,                   )
	self:addProperty("F0T_VALCOF", "Valor de COF"             , "number", "Val. COF"       , "F0T_VALCOF"                      )
	self:addProperty("F0T_VALPIS", "Valor de PIS"             , "number", "Val. PIS"       , "F0T_VALPIS"                      )
	self:addProperty("F0T_VALREC", "Valor Recebido"           , "number", "Val. Rec."      , "F0T_VALREC"                      )
	self:addProperty("F0T_VLCONT", "Valor Contábil"           , "number", "Vl.Contábil"    , "F0T_VLCONT"                      )
	self:addProperty("F0T_VLCPRB", "Valor de CPRB"            , "number", "Val. CPRB"      , "F0T_VLCPRB"                      )
	self:addProperty("F0T_CDBLCI", "Codigo Bloco I"           , "string", "Cod Bloco I"    , "F0T_CDBLCI"                      )
	self:addProperty("F0T_CNPJ"  , "CNPJ"                     , "string", "CNPJ"           , "F0T_CNPJ"                        )

return self:oSchema
