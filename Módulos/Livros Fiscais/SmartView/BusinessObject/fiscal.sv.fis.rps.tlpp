#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.rps.ch" 
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SF3, SFT, SA1, SB1, SD2, SF2", name="RpsSmartViewBusinessObject", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc} IssVerificationTReportsBusinessObject
Classe para criação do Objeto RPS - Recibo de prestação de serviço a partir de notas fiscais de saída de prestação de serviço.
 
@author Juliana Mellão
@since 17/07/2023
*/
//-------------------------------------------------------------------  
class RpsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Juliana Mellão
@since 17/07/2023
*/
//-------------------------------------------------------------------   
method new() class RpsSmartViewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"RPS - Recibo de prestação de serviço"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto de negócios para conferência e impressão de recibos de prestação de serviços (SF3/SFT/SA1/SB1/SD2/SF2)"

    oFisTools:validPerg(self:setPergunte("FISTRPS01"))
    freeobj(oFisTools)
return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Juliana Mellão
@since 17/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class RpsSmartViewBusinessObject
    local cAlias        as character
    local cQuery        as character
    Local oFisTools     := fisSmartviewTools():new()
    Local oFiltQry      := oFilter:getParameters() as Json
    Local dStartDate    := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1]
    Local dEndDate      := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1]
    local cClide        := Alltrim(oFiltQry['MV_PAR03',1]) as character
    local cCliAte       := Alltrim(oFiltQry['MV_PAR04',1]) as character
    local cRPSde        := Alltrim(oFiltQry['MV_PAR05',1]) as character
    local cRPSate       := Alltrim(oFiltQry['MV_PAR06',1]) as character
    local cFilDe        := Alltrim(oFiltQry['MV_PAR07',1]) as character
    local cFilAte       := Alltrim(oFiltQry['MV_PAR08',1]) as character
    Local nFilial       as numeric
    Local lOk           := .t. as logical
    local cRazaoSocial  as character
    local cCNPJ         as character
    local cEnd          as character
    local cCEP          as character
    local cBairro       as character
    local cCidade       as character
    local cEstado       as character
    local cTel          as character
    local cIM           as character
    Local aFiliais      := {} as array
    Local cFils         := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    local aBind         := {}  as array
    local nlA           := 0   as numeric
    Local oPrepare      := nil as object
    Local cTipo         := 'S' as character
    Local cTipoMov      := 'S' as character

    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk
        aSM0 := FWSM0Util():GetSM0Data(cEmpAnt, cFilAnt, { "M0_NOMECOM","M0_CGC","M0_ENDENT","M0_CEPENT","M0_BAIRENT","M0_CIDENT","M0_ESTENT","M0_TEL","M0_INSC" })
        cRazaoSocial := SubStr(Alltrim(aSM0[1][2]),1,30)
        cCNPJ := Alltrim(aSM0[2][2])
        if len(cCNPJ) == 14
            cCNPJ := Transform(cCNPJ,"@R! NN.NNN.NNN/NNNN-99")
        endif
        cEnd := Alltrim(aSM0[3][2])
        cCEP := Alltrim(aSM0[4][2])
        if len(cCEP) == 8
            cCEP := Transform(cCEP,"@R 99999-999")
        endif
        cBairro := Alltrim(aSM0[5][2])
        cCidade := Alltrim(aSM0[6][2])
        cEstado := Alltrim(aSM0[7][2])
        cTel := Alltrim(aSM0[8][2])
        cIM := Alltrim(aSM0[9][2])

        //definindo a quantidade máxima por página (default 100)
		self:setPageSize(self:nPageSize)
        
        cQuery += "     SELECT "
        cQuery += "         SF3.F3_FILIAL, "
        cQuery += "         SF3.F3_EMISSAO, "
        cQuery += "         SF3.F3_BASEICM, "
        cQuery += "         SF3.F3_NFISCAL, "
        cQuery += "         SF3.F3_ISENICM, "
        cQuery += "         SF3.F3_OUTRICM, "
        cQuery += "         SF3.F3_VALCONT, "
        cQuery += "         SF3.F3_ESPECIE, "
        cQuery += "         SF3.F3_ISSMAT, "
        cQuery += "         SF3.F3_ISSSUB, "
        cQuery += "         SF3.F3_CFO, "
        cQuery += "         SF3.F3_NRLIVRO, "
        cQuery += "         SF3.F3_ISSST, "
        cQuery += "         SF3.F3_EMINFE, "
        cQuery += "         SF3.F3_CODNFE, "
        cQuery += "         SF3.F3_CREDNFE, "
        cQuery += "         SF3.F3_NFELETR,"
        cQuery += "         SFT.FT_RECISS, "
        cQuery += "         SFT.FT_VALCONT, "
        cQuery += "         SFT.FT_ITEM, "
        cQuery += "         SFT.FT_SERIE, "
        cQuery += "         SFT.FT_CODISS, "
        cQuery += "         SFT.FT_ALIQICM, "
        cQuery += "         SFT.FT_VALICM, "
        cQuery += "         SFT.FT_TES, "
        cQuery += "         SFT.FT_OBSERV, "
        cQuery += "         SF2.F2_VALINSS, "
        cQuery += "         SF2.F2_VALIRRF, "
        cQuery += "         SF2.F2_VALPIS, "
        cQuery += "         SF2.F2_VALCOFI, "
        cQuery += "         SF2.F2_VALCSLL, "
        cQuery += "         SF2.F2_HORA,"
        cQuery += "         SD2.D2_QUANT, "
        cQuery += "         SD2.D2_PRCVEN, "
        cQuery += "         SD2.D2_TOTAL, "
        cQuery += "         SB1.B1_DESC, "
        cQuery += "         SA1.A1_NOME, "
        cQuery += "         SA1.A1_INSCRM, "
        cQuery += "         SA1.A1_END, "
        cQuery += "         SA1.A1_BAIRRO, "
        cQuery += "         SA1.A1_CEP, "
        cQuery += "         SA1.A1_MUN, "
        cQuery += "         SA1.A1_COD_MUN, "
        cQuery += "         SA1.A1_EST, "
        cQuery += "         SA1.A1_EMAIL, "
        cQuery += "         SA1.A1_CGC, "
        cQuery += "         SA1.A1_PESSOA, "
        cQuery += "         SX5.X5_DESCRI "
        cQuery += "     FROM "
        cQuery += "         ? SF3 "
        cQuery += "         INNER JOIN ? SFT ON SFT.FT_FILIAL = ? "
        cQuery += "                                                     AND SF3.F3_CLIEFOR = SFT.FT_CLIEFOR "
        cQuery += "                                                     AND SF3.F3_EMISSAO = SFT.FT_EMISSAO "
        cQuery += "                                                     AND SF3.F3_NFISCAL = SFT.FT_NFISCAL "
        cQuery += "                                                     AND SF3.F3_SERIE = SFT.FT_SERIE "
        cQuery += "                                                     AND SF3.F3_ESPECIE = SFT.FT_ESPECIE "
        cQuery += "                                                     AND SF3.F3_IDENTFT = SFT.FT_IDENTF3 "
        cQuery += "                                                     AND SFT.D_E_L_E_T_ = ? "
        cQuery += "         LEFT JOIN ? SF2 ON SF2.F2_FILIAL = ? "
        cQuery += "                                                     AND SFT.FT_NFISCAL = SF2.F2_DOC "
        cQuery += "                                                     AND SFT.FT_CLIEFOR = SF2.F2_CLIENTE "
        cQuery += "                                                     AND SFT.FT_LOJA = SF2.F2_LOJA "
        cQuery += "                                                     AND SFT.FT_SERIE = SF2.F2_SERIE "
        cQuery += "                                                     AND SFT.FT_ESPECIE = SF2.F2_ESPECIE "
        cQuery += "                                                     AND SFT.FT_EMISSAO = SF2.F2_EMISSAO "
        cQuery += "                                                     AND SF2.D_E_L_E_T_ = ? "
        cQuery += "         LEFT JOIN ? SD2 ON SD2.D2_FILIAL = ? "
        cQuery += "                                                     AND SFT.FT_NFISCAL = SD2.D2_DOC "
        cQuery += "                                                     AND SFT.FT_CLIEFOR = SD2.D2_CLIENTE "
        cQuery += "                                                     AND SFT.FT_LOJA = SD2.D2_LOJA "
        cQuery += "                                                     AND SFT.FT_SERIE = SD2.D2_SERIE "
        cQuery += "                                                     AND SFT.FT_ITEM = SD2.D2_ITEM "
        cQuery += "                                                     AND SFT.FT_EMISSAO = SD2.D2_EMISSAO "
        cQuery += "                                                     AND SF2.D_E_L_E_T_ = ? "
        cQuery += "         LEFT JOIN ? SA1 ON SA1.A1_FILIAL = ? "
        cQuery += "                                                     AND SD2.D2_CLIENTE = SA1.A1_COD "
        cQuery += "                                                     AND SD2.D2_LOJA = SA1.A1_LOJA "
        cQuery += "                                                     AND SA1.D_E_L_E_T_ = ? "
        cQuery += "         LEFT JOIN ? SB1  ON SB1.B1_FILIAL = ? "
        cQuery += "                                                     AND SD2.D2_COD = SB1.B1_COD "
        cQuery += "                                                     AND SB1.D_E_L_E_T_ = ? "
        cQuery += "         LEFT JOIN ? SX5 ON SX5.X5_TABELA = ? "
        cQuery += "                                                     AND SX5.X5_CHAVE = SFT.FT_CODISS "
        cQuery += "                                                     AND SX5.D_E_L_E_T_ = ? "
        cQuery += "     WHERE "
        cQuery += "         SFT.FT_CODISS <> ? " //SFT.FT_CODISS <> '"+Space(TamSX3("FT_CODISS")[1])+"' "
        cQuery += "         AND SF3.F3_DTCANC = ? " //AND SF3.F3_DTCANC = '"+Space(Len(Dtos(SF3->F3_DTCANC)))+"' "
        cQuery += "         AND SFT.FT_TIPOMOV = ? " //AND SFT.FT_TIPOMOV = 'S'"
        cQuery += "         AND SF3.F3_TIPO = ? " //AND SF3.F3_TIPO = 'S'"
        cQuery += "         AND SF3.F3_EMISSAO BETWEEN ? AND ? " //AND SF3.F3_EMISSAO BETWEEN '" + Strtran(oFiltQry['MV_PAR01',1],"-","") + "' AND '" + Strtran(oFiltQry['MV_PAR02',1],"-","") + "'"
                

        If !Empty(cClide)
            cQuery += " AND SF3.F3_CLIEFOR >= '" + oFiltQry['MV_PAR03',1]+ "'"
        EndIf
        If !Empty(cCliAte)
            cQuery += " AND SF3.F3_CLIEFOR <= '" + oFiltQry['MV_PAR04',1] + "'"
        EndIf
        If !Empty(cRPSde)
            cQuery += " AND SF3.F3_NFISCAL >= '" + oFiltQry['MV_PAR05',1] + "'"
        EndIf
        If !Empty(cRPSAte)
            cQuery += " AND SF3.F3_NFISCAL <= '" + oFiltQry['MV_PAR06',1] + "'"
        EndIf
        cQuery += " AND SF3.D_E_L_E_T_ = ? "

        cQuery += "AND SFT.FT_FILIAL = ? "

        if oFilter:hasFilter()
            cQuery +=   " AND " + oFilter:getSQLExpression()
        endif

        oPrepare := FwExecStatement():New( ChangeQuery(cQuery) )  

                //Percorre as filiais
        For nFilial := 1 to Len(aFiliais)

            Aadd( aBind, {'U', RetSqlName("SF3")})
            Aadd( aBind, {'U', RetSqlName("SFT")})
            Aadd( aBind, {'C', FWxFilial( 'SFT', aFiliais[nFilial] )})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind, {'U', RetSqlName("SF2")})
            Aadd( aBind, {'C', FWxFilial( 'SF2', aFiliais[nFilial] )})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind ,{'U', RetSqlName("SD2")})
            Aadd( aBind, {'C', FWxFilial( 'SD2', aFiliais[nFilial] )})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind, {'U', RetSqlName("SA1")})
            Aadd( aBind, {'C', FWxFilial( 'SA1', aFiliais[nFilial] )})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind, {'U', RetSqlName("SB1")})
            Aadd( aBind, {'C', FWxFilial( 'SB1', aFiliais[nFilial] )})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind, {'U', RetSqlName("SX5")})
            Aadd( aBind, {'C','60'})			
            Aadd( aBind, {'C',' '})					
            aadd( aBind, {'C', Space(FWTamSX3("FT_CODISS")[1])})
            Aadd( aBind, {'C', Space(Len(Dtos(SF3->F3_DTCANC)))})
            Aadd( aBind, {'C', cTipoMov})
            Aadd( aBind, {'C', cTipo})
            Aadd( aBind, {'D', dStartDate})
            Aadd( aBind, {'D', dEndDate})
            Aadd( aBind, {'C',' '})				
            Aadd( aBind, {'C', FWxFilial( 'SFT', aFiliais[nFilial] )})             

            for nlA := 1 to len(aBind)
                If aBind[nlA][1] == 'U'
					oPrepare:SetUnsafe(nlA, aBind[nlA][2])
				Elseif aBind[nlA][1] == 'C'
					oPrepare:setString(nlA, aBind[nlA][2])
				Elseif aBind[nlA][1] == 'D'
					oPrepare:setDate(nlA, aBind[nlA][2])
				Endif
            next nlA

            cQuery := oPrepare:getFixQuery()
        	cAlias:= MPSysOpenQuery(cQuery) //executequery			

            while !(cAlias)->(Eof())

                self:oData:appendData({ "FILIAL"     : (cAlias)->F3_FILIAL,;
                                        "DTEMISSAO"  : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_EMISSAO)),;
                                        "BASEICM"    : (cAlias)->F3_BASEICM,;
                                        "NFISCAL"    : (cAlias)->F3_NFISCAL,;
                                        "ISENICM"    : (cAlias)->F3_ISENICM,;
                                        "OUTRICM"    : (cAlias)->F3_OUTRICM,;
                                        "VALNF"      : (cAlias)->F3_VALCONT,;
                                        "ESPECIE"    : (cAlias)->F3_ESPECIE,;
                                        "ISSMAT"     : (cAlias)->F3_ISSMAT,;
                                        "ISSSUB"     : (cAlias)->F3_ISSSUB,;
                                        "CFOP"       : (cAlias)->F3_CFO,;
                                        "NRLIVRO"    : (cAlias)->F3_NRLIVRO,;
                                        "ISSST"      : (cAlias)->F3_ISSST,;
                                        "EMINFE"     : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_EMINFE)),;
                                        "CODNFE"     : (cAlias)->F3_CODNFE,;
                                        "CREDNFE"    : (cAlias)->F3_CREDNFE,;
                                        "NFELETR"    : (cAlias)->F3_NFELETR,;
                                        "RECISS"     : (cAlias)->FT_RECISS,;
                                        "VALCONT"    : (cAlias)->FT_VALCONT,;
                                        "ITEM"       : (cAlias)->FT_ITEM,;
                                        "SERIE"      : (cAlias)->FT_SERIE,;
                                        "CODISS"     : (cAlias)->FT_CODISS,;
                                        "ALIQICM"    : (cAlias)->FT_ALIQICM,;
                                        "VALICM"     : (cAlias)->FT_VALICM,;
                                        "TES"        : (cAlias)->FT_TES,;
                                        "OBSERV"     : (cAlias)->FT_OBSERV,;
                                        "VALINSS"    : (cAlias)->F2_VALINSS,;
                                        "VALIRRF"    : (cAlias)->F2_VALIRRF,;
                                        "VALPIS"     : (cAlias)->F2_VALPIS,;
                                        "VALCOFI"    : (cAlias)->F2_VALCOFI,;
                                        "VALCSLL"    : (cAlias)->F2_VALCSLL,;
                                        "HORA"       : Transform((cAlias)->F2_HORA,"@R 99:99"),;
                                        "QUANT"      : (cAlias)->D2_QUANT,;
                                        "PRCVEN"     : (cAlias)->D2_PRCVEN,;
                                        "TOTAL"      : (cAlias)->D2_TOTAL,;
                                        "DESCPROD"   : (cAlias)->B1_DESC,;
                                        "NOMECLI"    : (cAlias)->A1_NOME,;
                                        "INSCMCLI"   : (cAlias)->A1_INSCRM,;
                                        "ENDCLI"     : (cAlias)->A1_END,;
                                        "BAIRROCLI"  : (cAlias)->A1_BAIRRO,;
                                        "CEPCLI"     : (cAlias)->A1_CEP,;
                                        "MUNCLI"     : (cAlias)->A1_MUN,;
                                        "COD_MUNCLI" : (cAlias)->A1_COD_MUN,;
                                        "ESTCLI"     : (cAlias)->A1_EST,;
                                        "EMAILCLI"   : (cAlias)->A1_EMAIL,;
                                        "CGCCLI"     : (cAlias)->A1_CGC,;
                                        "PESSOA"     : (cAlias)->A1_PESSOA,;
                                        "DESCISS"    : (cAlias)->X5_DESCRI,;
                                        "RAZAOSOCIAL": cRazaoSocial,;
                                        "CNPJ"       : cCNPJ,;
                                        "ENDERECO"   : cEnd,;
                                        "CEP"        : cCEP,;
                                        "BAIRRO"     : cBairro,;
                                        "CIDADE"     : cCidade,;
                                        "ESTADO"     : cEstado,;
                                        "TELEFONE"   : cTel,;
                                        "IM"         : cIM})

                (cAlias)->( dbSkip() )
            enddo

            (cAlias)->( DBCloseArea() )
            aSize(aBind,0)           
        Next nFilial
    endif

    FWfreeobj(oFisTools)
    aSize(aBind,0)
    aBind := Nil
    FWfreeObj(oPrepare)
    FWfreeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Juliana Mellão
@since 17/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class RpsSmartViewBusinessObject

    //              ( Nome          , Descricao                   , Tipo    , Display                  , cRealName
    self:addProperty("FILIAL"       , "Filial"                    , "string", "Filial"                 , "F3_FILIAL"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DTEMISSAO"    , "Data Emissao"              , "date"  , "Data Emissao"           , "F3_EMISSAO"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("BASEICM"      , "Base de Calculo"           , "number", "Base Calculo"           , "F3_BASEICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NFISCAL"      , "Nota Fiscal"               , "string", "Nota Fiscal"            , "F3_NFISCAL"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISENICM"      , "ISS Isento"                , "number", "ISS Isento"             , "F3_ISENICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("OUTRICM"      , "ISS Outros"                , "number", "ISS Outros"             , "F3_OUTRICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALNF"        , "Valor da NF"               , "number", "Valor da NF"            , "F3_VALCONT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ESPECIE"      , "Especie Doc"               , "string", "Especie Doc."           , "F3_ESPECIE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSMAT"       , "Material Aplicado"         , "number", "ISS Mat"                , "F3_ISSMAT"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSSUB"       , "ISS da Subempreitada"      , "number", "ISS Subempr"            , "F3_ISSSUB"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CFOP"         , "Codigo Fiscal"             , "string", "CFOP"                   , "F3_CFO"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NRLIVRO"      , "Numero do Livro"           , "string", "Nr. Livro"              , "F3_NRLIVRO"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSST"        , "Pagamento Imposto"         , "string", "Pag. Imposto"           , "F3_ISSST"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("EMINFE"       , "Emissão NFe"               , "date"  , "Emissao NFe"            , "F3_EMINFE"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODNFE"       , "Código Verificação NFe"    , "string", "Cod. Verif. NFe"        , "F3_CODNFE"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CREDNFE"      , "Crédito Concedido NFe"     , "number", "Cred. Conc. NFe"        , "F3_CREDNFE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NFELETR"      , "Nota Fiscal Eletronica"    , "string", "NF Eletr."              , "F3_NFELETR"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("RECISS"       , "ISS Retido"                , "string", "ISS Retido"             , "FT_RECISS"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALCONT"      , "Valor do Item"             , "number", "Valor Item"             , "FT_VALCONT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ITEM"         , "Item da NF"                , "string", "Item NF "               , "FT_ITEM"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("SERIE"        , "Série"                     , "string", "Serie "                 , "FT_SERIE"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODISS"       , "Codigo de Serviço"         , "string", "Cod Servico "           , "FT_CODISS"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ALIQICM"      , "Aliquota Iimposto"         , "number", "Aliquota Imposto"       , "FT_ALIQICM"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALICM"       , "Valor Imposto"             , "number", "Valor Imposto"          , "FT_VALICM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TES"          , "Tipo Entrada/Saida  "      , "string", "TES"                    , "FT_TES"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("OBSERV"       , "Observação"                , "string", "Observacao"             , "FT_OBSERV"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALINSS"      , "Valor INSS"                , "number", "Valor INSS"             , "F2_VALINSS"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALIRRF"      , "Valor IRRF"                , "number", "Valor IRRF"             , "F2_VALIRRF"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALPIS"       , "Valor PIS"                 , "number", "Valor PIS"              , "F2_VALPIS"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALCOFI"      , "Valor COFINS"              , "number", "Valor COFINS"           , "F2_VALCOFI"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALCSLL"      , "Valor CSLL"                , "number", "Valor CSLL"             , "F2_VALCSLL"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("HORA"         , "Hora de Emissão"           , "string", "Hora Emissao"           , "F2_HORA"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("QUANT"        , "Quantidade"                , "number", "Quantidade"             , "D2_QUANT"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PRCVEN"       , "Valor Unitário"            , "number", "Valor Unitario"         , "D2_PRCVEN"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TOTAL"        , "Valor Total"               , "number", "Vlr Total"              , "D2_TOTAL"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DESCPROD"     , "Descrição do Produto"      , "string", "Desc. Produto"          , "B1_DESC"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NOMECLI"      , "Nome do Cliente"           , "string", "Nome Cliente"           , "A1_NOME"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("INSCMCLI"     , "Insc. Municipal Cliente"   , "string", "I.M. Cliente"           , "A1_INSCRM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ENDCLI"       , "Endereço Cliente"          , "string", "End. Cliente"           , "A1_END"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("BAIRROCLI"    , "Bairro Cliente"            , "string", "Bairro Cliente"         , "A1_BAIRRO"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CEPCLI"       , "CEP Cliente"               , "string", "CEP Cliente"            , "A1_CEP"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("MUNCLI"       , "Municipio Cliente"         , "string", "Mun. Cliente"           , "A1_MUN"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("COD_MUNCLI"   , "Cod Municipio Cliente"     , "string", "Cod. Mun. Cliente"      , "A1_COD_MUN"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ESTCLI"       , "Estado Cliente"            , "string", "Estado Cliente"         , "A1_EST"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("EMAILCLI"     , "Email Cliente"             , "string", "Email Cliente"          , "A1_EMAIL"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CGCCLI"       , "CNPJ Cliente"              , "string", "CNPJ Cliente"           , "A1_CGC"        , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PESSOA"       , "Fisica/Juridica"           , "string", "Fisica/Jurid."          , "A1_PESSOA"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DESCISS"      , "Descrição Cod ISS"         , "string", "Descricao Cod.ISS"      , "X5_DESCRI"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("RAZAOSOCIAL"  , "Razão Social"              , "string", "Razao Social"           , "cRazaoSocial"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CNPJ"         , "CNPJ"                      , "string", "CNPJ"                   , "cCNPJ"         , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ENDERECO"     , "Endereço"                  , "string", "Endereco"               , "cEnd"          , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CEP"          , "CEP"                       , "string", "CEP"                    , "cCEP"          , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("BAIRRO"       , "Bairro"                    , "string", "Bairro"                 , "cBairro"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CIDADE"       , "Cidade"                    , "string", "Cidade"                 , "cCidade"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ESTADO"       , "Estado"                    , "string", "Estado"                 , "cEstado"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TELEFONE"     , "Telefone"                  , "string", "Telefone"               , "cTel"          , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("IM"           , "Inscrição Municipal"       , "string", "Insc. Municipal"        , "cIM"           , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema
