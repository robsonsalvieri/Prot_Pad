#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.productlistinventory.ch"


namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="F0M,SB1", name="prodlistinventorySmartViewBusinessObject", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc} prodlistinventorySmartViewBusinessObject
Classe para criação do Objeto productlistinventory - Listagem produtos Iventario para compor SPED FISCAL bloco H, registro H020
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------  
class prodlistinventorySmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method new() class prodlistinventorySmartViewBusinessObject
	Local oFisTools := fisSmartviewTools():new()
	_Super:new()
	//Define Area
	self:appendArea(STR0001)//"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002)//"Listagem produtos Inventário"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003)//"Objeto prodlistinventory - Listagem produtos Inventário para compor SPED FISCAL bloco H, registro H020 (F0M/SB1)"

	oFisTools:validPerg(self:setPergunte("FISTRR1161"))
	freeobj(oFisTools)
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class prodlistinventorySmartViewBusinessObject

	Local cQuery                                                        as character
	Local cAlias                                                        as character
	Local oFisTools := fisSmartviewTools():new()                        as object
	Local oFiltQry  := oFilter:getParameters()                          as json
	Local dDtFech   := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1]     as date
	Local cFilDe    := oFiltQry['MV_PAR02',1]                           as character
	Local cFilAte   := oFiltQry['MV_PAR03',1]                           as character
	Local lOk       := .t.                                              as logical
	Local aFiliais  := {}                                               as array
	Local cFils     := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
	Local nFilial                                                       as numeric
	Local oPrepare  := nil                                              as object

	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	if lOk

		cQuery := " SELECT "
		cQuery += "         F0M.F0M_FILIAL, "
		cQuery += "         CASE "
		cQuery += "             WHEN F0M.F0M_REGRA = '1' THEN '1-Media Pond. Base ICMS ST' "
		cQuery += "             WHEN F0M.F0M_REGRA = '2' THEN '2-Custo Medio com MVA' "
		cQuery += "             WHEN F0M.F0M_REGRA = '3' THEN '3-Preco+Seguro+Frete+IPI' "
		cQuery += "             WHEN F0M.F0M_REGRA = '4' THEN '4-Valor Def. Legis. Espec.' "
		cQuery += "             ELSE '5-Media Pond.ICMS ST Rec.Ant.' "
		cQuery += "         END REGRACALC, "
		cQuery += "         CASE "
		cQuery += "             WHEN F0M.F0M_SITUA = '0' THEN '0-Item em seu poder' "
		cQuery += "             WHEN F0M.F0M_SITUA = '1' THEN '1-Item em posse de terceiro.' "
		cQuery += "             ELSE '2-Item de terc. em posse inform.' "
		cQuery += "         END SITUAIT, "
		cQuery += "         F0M.F0M_REGRA, "
		cQuery += "         F0M.F0M_CODIGO, "
		cQuery += "         F0M.F0M_QUANT, "
		cQuery += "         F0M.F0M_CST, "
		cQuery += "         F0M.F0M_SITUA, "
		cQuery += "         F0M.F0M_CLIFOR, "
		cQuery += "         F0M.F0M_LOJA, "
		cQuery += "         F0M.F0M_BASICM, "
		cQuery += "         F0M.F0M_ALIQ, "
		cQuery += "         F0M.F0M_VALICM, "
		cQuery += "         F0M.F0M_TOTICM, "
		cQuery += "         SB1.B1_DESC "
		cQuery += "     FROM "
		cQuery +=           RetSqlName("F0M") + " F0M "
		cQuery += "         INNER JOIN " +RetSqlName("SB1")+" SB1   ON  SB1.B1_FILIAL = ? "
		cQuery += "                                                 AND SB1.B1_COD = F0M.F0M_CODIGO "
		cQuery += "                                                 AND SB1.D_E_L_E_T_= ? "
		cQuery += "     WHERE "
		cQuery += "         F0M.F0M_FILIAL = ? "
		cQuery += "         AND F0M.F0M_DTFECH = ? "
		cQuery += "         AND F0M.D_E_L_E_T_ = ? "


		oPrepare := FwExecStatement():New( ChangeQuery(cQuery) )

		For nFilial := 1 to Len(aFiliais)

			oPrepare:setString(1,aFiliais[nFilial])
			oPrepare:setString(2,' ')
			oPrepare:setString(3,aFiliais[nFilial])
			oPrepare:setDate(4,dDtFech)
			oPrepare:setString(5,' ')

			cAlias:= oPrepare:OpenAlias()

			while !(cAlias)->(Eof())

				self:oData:appendData({ "FILIAL"    : (cAlias)->F0M_FILIAL,;
					"CODIGO"    : (cAlias)->F0M_CODIGO,;
					"DESC"      : Alltrim((cAlias)->B1_DESC),;
					"QUANT"     : (cAlias)->F0M_QUANT,;
					"_REGRACALC": (cAlias)->REGRACALC,;
					"_SITUAIT"  : ((cAlias)->SITUAIT),;
					"CST"       : ((cAlias)->F0M_CST),;
					"CLIFOR"    : ((cAlias)->F0M_CLIFOR),;
					"LOJA"      : ((cAlias)->F0M_LOJA),;
					"BASICM"    : (cAlias)->F0M_BASICM ,;
					"ALIQ"      : (cAlias)->F0M_ALIQ,;
					"VALICM"    : (cAlias)->F0M_VALICM,;
					"TOTICM"    : (cAlias)->F0M_TOTICM } )

				(cAlias)->( dbSkip() )
			enddo

			(cAlias)->( DBCloseArea() )
		Next
	Endif

	freeobj(oFisTools)
	freeObj(oPrepare)
	freeobj(oFiltQry)

	aSize(aFiliais,0)
	aFiliais := Nil


return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class prodlistinventorySmartViewBusinessObject

	//              ( Nome   ,  Descricao                   ,  Tipo   , Display                     , cRealName
	self:addProperty("FILIAL"    , "Filial"                 , "string", "Filial"                    , "F0M_FILIAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("CODIGO"    , "Código do Produto"      , "string", "Código do Produto"         , "F0M_CODIGO"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("DESC"      , "Descrição do Produto"   , "string", "Descrição do Produto"      , "B1_DESC"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("QUANT"     , "Quantidade"             , "number", "Quantidade"                , "F0M_QUANT"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("_REGRACALC", "Regra de Calculo"       , "string", "Regra de Calculo"          , "REGRACALC"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("_SITUAIT"  , "Situação"               , "string", "Situação"                  , "SITUAIT"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("CST"       , "Sit.Trib"               , "string", "Sit.Trib"                  , "F0M_CST"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("CLIFOR"    , "Cli/For"                , "string", "Cli/For"                   , "F0M_CLIFOR"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("LOJA"      , "Loja"                   , "string", "Loja"                      , "F0M_LOJA"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("BASICM"    , "Base ICMS"              , "number", "Base ICMS"                 , "F0M_BASICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("ALIQ"      , "Aliq. ICMS"             , "number", "Aliq. ICMS"                , "F0M_ALIQ"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("VALICM"    , "Valor ICMS"             , "number", "Valor ICMS"                , "F0M_VALICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
	self:addProperty("TOTICM"    , "Total ICMS"             , "number", "Total ICMS"                , "F0M_TOTICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema
