#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdf3p.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider

//-------------------------------------------------------------------
/*{Protheus.doc}  efdF3PSmartviewBusinessObject
@author julia.mota
@since 22/12/2023
*/
//-------------------------------------------------------------------  
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="F3P",;
	name="Objeto de Negócio EFD - F3P", country="BRA", initialRelease="12.1.2310")
class efdF3PSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	private data oPrepare as object

    public method new() as object
	public method getData() as object
	public method getSchema() as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Julia Mota
@since 22/12/2023
*/
//-------------------------------------------------------------------   
method new() class efdF3PSmartviewBusinessObject

	Local oFisTools := fisSmartviewTools():new() as object

	_Super:new()
    ::oPrepare := nil
	//Define Area
	self:appendArea(STR0001) //"Livros Fiscais"
	//Define o nome do objeto de Negócio
	self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contrinuiçoes - F3P"
	//Define a descrição do objeto de Negócio
	self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (F3P)"
	self:setIsCBoxLookup(.T., .F.)

	oFisTools:validPerg(::setPergunte("FISTRR1024"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author julia.mota
@since 22/12/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdF3PSmartviewBusinessObject
    Local cQuery     as Character
    Local cAlias     as Character
    Local oFisTools  := fisSmartviewTools():new() as object
    Local oFiltQry   := oFilter:getParameters() as Json
    Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as Date
    Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as Date
    Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
    Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
    Local lOk        := .t. as Logical
    Local aFiliais   := {} as array
    Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character

    
    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk        
    
        cQuery := " SELECT " 
        cQuery +=   " F3P.F3P_FILIAL,"
        cQuery +=   " F3P.F3P_PER,"
        cQuery +=   " F3P.F3P_CODAJU,"
        cQuery +=   " F3P.F3P_CNPJ,"
        cQuery +=   " F3P.F3P_TOTAJU,"
        cQuery +=   " F3P.F3P_CST01,"
        cQuery +=   " F3P.F3P_CST02,"
        cQuery +=   " F3P.F3P_CST03,"
        cQuery +=   " F3P.F3P_CST04,"
        cQuery +=   " F3P.F3P_CST05,"
        cQuery +=   " F3P.F3P_CST06,"
        cQuery +=   " F3P.F3P_CST07,"
        cQuery +=   " F3P.F3P_CST08,"
        cQuery +=   " F3P.F3P_CST09,"
        cQuery +=   " F3P.F3P_CST49,"
        cQuery +=   " F3P.F3P_CST99,"
        cQuery +=   " F3P.F3P_INDAP,"
        cQuery +=   " F3P.F3P_RECIBO,"
        cQuery +=   " F3P.F3P_INFCOM,"
        cQuery +=   " F3P.F3P_VALICM,"
        cQuery +=   " F3P.F3P_REGIME,"
        cQuery +=   " F3P.F3P_AJMANU"
        cQuery += " FROM "
        cQuery +=   RetSQLName("F3P") + " F3P "
        cQuery += " WHERE " 
        cQuery +=   " F3P.F3P_FILIAL IN ( ? ) " 
        cQuery +=   " AND F3P.F3P_PER BETWEEN ? AND ? "
        cQuery +=   " AND F3P.D_E_L_E_T_ = ?"     
        
      	::oPrepare := FWExecStatement():New(ChangeQuery(cQuery))
		::oPrepare:setIn(1,aFiliais)
		::oPrepare:setDate(2,dStartDate )
		::oPrepare:setDate(3,dEndDate)
		::oPrepare:setString(4,' ')
        
        cAlias:= ::oPrepare:OpenAlias()
      
        while !(cAlias)->(Eof())

            self:oData:appendData({;
                "F3P_FILIAL"    : (cAlias)->F3P_FILIAL,;
                "F3P_PER"       : totvs.framework.treports.date.stringToTimeStamp((cAlias)->F3P_PER),;
                "F3P_CODAJU"    : (cAlias)->F3P_CODAJU,;
                "F3P_CNPJ"      : (cAlias)->F3P_CNPJ,;
                "F3P_TOTAJU"    : (cAlias)->F3P_TOTAJU,;
                "F3P_CST01"     : (cAlias)->F3P_CST01,;
                "F3P_CST02"     : (cAlias)->F3P_CST02,;
                "F3P_CST03"     : (cAlias)->F3P_CST03,;
                "F3P_CST04"     : (cAlias)->F3P_CST04,;
                "F3P_CST05"     : (cAlias)->F3P_CST05,;
                "F3P_CST06"     : (cAlias)->F3P_CST06,;
                "F3P_CST07"     : (cAlias)->F3P_CST07,;
                "F3P_CST08"     : (cAlias)->F3P_CST08,;
                "F3P_CST09"     : (cAlias)->F3P_CST09,;
                "F3P_CST49"     : (cAlias)->F3P_CST49,;
                "F3P_CST99"     : (cAlias)->F3P_CST99,;
                "F3P_INDAP"     : (cAlias)->F3P_INDAP,;
                "F3P_RECIBO"    : (cAlias)->F3P_RECIBO,;
                "F3P_INFCOM"    : (cAlias)->F3P_INFCOM,;
                "F3P_VALICM"    : (cAlias)->F3P_VALICM,;
                "F3P_REGIME"    : (cAlias)->F3P_REGIME,;
                "F3P_AJMANU"    : (cAlias)->F3P_AJMANU;
            })

            (cAlias)->(dbSkip())
        
        EndDo
        (cAlias)->(DBCloseArea())
    Endif
    
    freeobj(oFisTools)
    freeObj(::oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Julia Mota
@since 14/12/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdF3PSmartviewBusinessObject
    self:addProperty("F3P_FILIAL"   , "Filial    "                        , "string"  , "Filial"                        , "F3P_Filial"    )
    self:addProperty("F3P_PER"      , "Período da Apuração "              , "date"    , "Período da Apuração  "         , "F3P_Per"       )
    self:addProperty("F3P_CODAJU"   , "Cod. Ajuste         "              , "string"  , "Código Ajuste "                , "F3P_CODAJU"    )
    self:addProperty("F3P_CNPJ"     , "CNPJ          "                    , "string"  , "CNPJ"                          , "F3P_CNPJ"      )
    self:addProperty("F3P_TOTAJU"   , "Total do Ajuste.     "             , "number"  , "Total do Ajuste."              , "F3P_TOTAJU"    )
    self:addProperty("F3P_CST01"    , "Ajuste CST 01        "             , "number"  , "Ajuste CST 01         "        , "F3P_CST01"     )
    self:addProperty("F3P_CST02"    , "Ajuste CST 02        "             , "number"  , "Ajuste CST 02         "        , "F3P_CST02"     )
    self:addProperty("F3P_CST03"    , "Ajuste CST 03        "             , "number"  , "Ajuste CST 03         "        , "F3P_CST03"     )
    self:addProperty("F3P_CST04"    , "Ajuste CST 04        "             , "number"  , "Ajuste CST 04         "        , "F3P_CST04"     )
    self:addProperty("F3P_CST05"    , "Ajuste CST 05        "             , "number"  , "Ajuste CST 05         "        , "F3P_CST05"     )
    self:addProperty("F3P_CST06"    , "Ajuste CST 06        "             , "number"  , "Ajuste CST 06         "        , "F3P_CST06"     )
    self:addProperty("F3P_CST07"    , "Ajuste CST 07        "             , "number"  , "Ajuste CST 07         "        , "F3P_CST07"     )
    self:addProperty("F3P_CST08"    , "Ajuste CST 08        "             , "number"  , "Ajuste CST 08         "        , "F3P_CST08"     )
    self:addProperty("F3P_CST09"    , "Ajuste CST 09        "             , "number"  , "Ajuste CST 09         "        , "F3P_CST09"     )
    self:addProperty("F3P_CST49"    , "Ajuste CST 49        "             , "number"  , "Ajuste CST 49         "        , "F3P_CST49"     )
    self:addProperty("F3P_CST99"    , "Ajuste CST 99        "             , "number"  , "Ajuste CST 99         "        , "F3P_CST99"     )
    self:addProperty("F3P_INDAP"    , "Indicador de Apropriação"          , "string"  , "Indicador de Apropriação "     , "F3P_INDAP"     )
    self:addProperty("F3P_RECIBO"   , "Número do Recibo    "              , "string"  , "Número do Recibo      "        , "F3P_RECIBO"    )
    self:addProperty("F3P_INFCOM"   , "Inf. Complementar   "              , "string"  , "Informação Complementar"       , "F3P_INFCOM"    )
    self:addProperty("F3P_VALICM"   , "ICMS a Recolher     "              , "number"  , "ICMS a Recolher        "       , "F3P_VALICM"    )
    self:addProperty("F3P_REGIME"   , "Regime de Apuração  "              , "string"  , "Regime de Apuração     "       , "F3P_REGIME"    )
    self:addProperty("F3P_AJMANU"   , "Ajuste Manual?      "              , "string"  , "Ajuste Manual?         "       , "F3P_AJMANU"    )
Return self:oSchema
