#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.fciAnalytical.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SB1,CFD,SD3", name="fciAnalyticalSmartviewBusinessObject", country="BRA", initialRelease="12.1.2210")

//-------------------------------------------------------------------
/*/{Protheus.doc} fciAnalyticalSmartviewBusinessObject
Classe para criação do Objeto de Negócio de FCI
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
class fciAnalyticalSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer
    
    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------  
method new() class fciAnalyticalSmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
    //Define Area
    self:appendArea(STR0001) //"Livros Fiscais"
    //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002) //"FCI - Visão de dados - FCI Analítico"
    //Define a descrição do objeto de Negócio
    self:setDescription(STR0003) //"Objeto FCI - Ficha de Conteúdo de Importação - Visão de dados - FCI Analítico"

    oFisTools:validPerg(self:setPergunte("FISTRFCI01"))
    freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: self:oData
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class fciAnalyticalSmartviewBusinessObject
    local cAlias     as character
    local cQuery     as character
    Local cSepara    as character 
    Local oFiltQry   := oFilter:getParameters() as Json
    Local cCodde     := oFiltQry['MV_PAR01',1] as Character
    Local cCodAte    := oFiltQry['MV_PAR02',1] as Character
    local cParFciMi  := SUPERGETMV("MV_FCIMI", .T., "RE1") as character
    local aParFciMi  := {}  as array
    Local oPrepare   := nil as object
    

    //Retorna o separador da string que recebe o valor do MV_FCIMI
    cSepara     := XFciRetMv_("MV_FCIMI"  , cParFciMi ) 
    aParFciMi   := StrToArray( cSepara, "/" )
    
    If oPrepare == NIL
      	cQuery := " SELECT "
        cQuery += " CFD.CFD_FILIAL, "
        cQuery += " CFD.CFD_COD, "
        cQuery += " CFD.CFD_PERCAL, "
        cQuery += " CFD.CFD_PERVEN, "
        cQuery += " CFD.CFD_VPARIM, "
        cQuery += " CFD.CFD_VSAIIE, "
        cQuery += " CFD.CFD_CONIMP, "
        cQuery += " CFD.CFD_OP, "
        cQuery += " SB1.B1_DESC, "
        cQuery += " SB1.B1_UM, "
        cQuery += " SB1.B1_POSIPI, "
        cQuery += " SB1.B1_CODBAR, "
        cQuery += " SD3.D3_COD, "
        cQuery += " SD3.D3_QUANT, "
        cQuery += " SD3.D3_CUSTO1 "
        cQuery += " FROM " + RetSQLName("CFD") + " CFD "
        cQuery += " INNER JOIN " + RetSQLName("SB1") + " SB1 "
        cQuery += "     ON  SB1.B1_FILIAL = ? " 
        cQuery += " 	AND SB1.B1_COD = CFD.CFD_COD "
        cQuery += " 	AND SB1.D_E_L_E_T_ = ? "
        cQuery += " INNER JOIN " + RetSQLName("SD3") + " SD3 "
        cQuery += " 	ON SD3.D3_FILIAL = ? "
        cQuery += " 	AND SD3.D3_OP = CFD.CFD_OP "
        cQuery += " 	AND SD3.D3_COD = CFD.CFD_COD "
        cQuery += " 	AND SD3.D_E_L_E_T_ = ? "
        cQuery += " 	AND SD3.D3_ESTORNO = ? "
        cQuery += " LEFT JOIN " + RetSQLName("SD3") + " SD31 "
        cQuery += " 	ON SD31.D3_FILIAL = SD3.D3_FILIAL "
        cQuery += "  AND SD31.D3_OP = SD3.D3_OP "
        cQuery += "  AND SD31.D3_CF IN (?) "
        cQuery += "  AND SD31.D3_ESTORNO = ? "
        cQuery += "  AND SD31.D_E_L_E_T_ = ? "
        cQuery += " WHERE "
        cQuery += "  CFD.CFD_FILIAL = ? "
        cQuery += "  AND CFD.CFD_COD BETWEEN ? AND ? "
        cQuery += "  AND CFD.D_E_L_E_T_= ? "
        cQuery += "  ORDER BY CFD.CFD_FILIAL,CFD.CFD_COD,CFD.CFD_PERCAL "

   		// Instancia a classe
    	oPrepare := FwExecStatement():New(cQuery)

    Endif

    oPrepare:setString( 1, xFilial("SB1") ) 
    oPrepare:setString( 2, ' ' )
    oPrepare:setString( 3, xFilial("SD3")  ) 
    oPrepare:setString( 4, ' ' )
    oPrepare:setString( 5, ' ' )
    oPrepare:setIn( 6, aParFciMi )        
    oPrepare:setString( 7, ' ' )
    oPrepare:setString( 8, ' ' )
    oPrepare:setString( 9, xFilial("CFD")  )
    oPrepare:setString( 10, cCodDe )
    oPrepare:setString( 11, cCodAte )
    oPrepare:setString( 12, ' ' )

    cAlias := oPrepare:OpenAlias()  

    While !(cAlias)->(Eof())
 
        self:oData:appendData({"FILIAL" : (cAlias)->CFD_FILIAL,;
                                "CODIGO": Alltrim((cAlias)->CFD_COD),;
                                "PERCAL": transform((cAlias)->CFD_PERCAL, "@R 99-9999"),;
                                "PERVEN": transform((cAlias)->CFD_PERVEN, "@R 99-9999"),;
                                "VPARIM": ((cAlias)->CFD_VPARIM),;
                                "VSAIIE": ((cAlias)->CFD_VSAIIE),;
                                "CONIMP": ((cAlias)->CFD_CONIMP),;
                                "OP"    : ((cAlias)->CFD_OP),;
                                "DESC"  : Alltrim((cAlias)->B1_DESC),;
                                "UM"    : ((cAlias)->B1_UM),;
                                "POSIPI": ((cAlias)->B1_POSIPI),;
                                "CODBAR": Alltrim((cAlias)->B1_CODBAR),;
                                "CODMT" : ((cAlias)->D3_COD),;
                                "QUANT" : ((cAlias)->D3_QUANT),;
                                "VALMT" : (cAlias)->D3_CUSTO1 / (cAlias)->D3_QUANT;
                            })
        (cAlias)->( dbSkip() )

    EndDo

    (cAlias)->( DBCloseArea() )  
    oPrepare:Destroy()
	oPrepare := nil 
    freeobj(oFiltQry)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
@return object: self:oSchema
@author Evandro Italo
@since 15/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class fciAnalyticalSmartviewBusinessObject

    //              ( Nome   ,  Descricao                   ,  Tipo   , Display                     , cRealName
    self:addProperty("FILIAL", "Filial"                     , "string", "Filial"                    , "CFD_FILIAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODIGO", "Código do Produto"          , "string", "Código do Produto"         , "CFD_COD"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PERCAL", "Período do Calculo"         , "string", "Período do Cálculo"        , "CFD_PERCAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("PERVEN", "Periodo de vendas"          , "string", "Período de vendas"         , "CFD_PERVEN"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VPARIM", "Valor da Parcela Importada" , "number", "Valor da Parcela Importada", "CFD_VPARIM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VSAIIE", "Valor Saida Interestadual"  , "number", "Valor Saida Interestadual" , "CFD_VSAIIE"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CONIMP", "Conteudo de Importacao"     , "number", "Conteúdo de Importação"    , "CFD_CONIMP"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("OP"    , "Ordem de Produção FCI"      , "string", "Ordem de Produção FCI"     , "CFD_OP"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DESC"  , "Descrição do Produto"       , "string", "Descrição do Produto"      , "B1_DESC"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("UM"    , "Unidade de Medida"          , "string", "Unidade de Medida"         , "B1_UM"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("POSIPI", "Nomenclatura Ext.Mercosul"  , "string", "Nomenclatura Ext.Mercosul" , "B1_POSIPI"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODBAR", "Codigo de Barras"           , "string", "Código de Barras"          , "B1_CODBAR "  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODMT" , "Código Matéria Prima"       , "string", "Código Matéria Prima"      , "D3_COD"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("QUANT" , "Quantidade Mat. Prima"      , "number", "Quantidade Mat. Prima"     , "D3_QUANT"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALMT" , "Custo Mat. Prima"           , "number", "Custo Mat. Prima"          , "D3_CUSTO1"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema
