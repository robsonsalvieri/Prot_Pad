#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.ciapBem.ch"
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFT,SF9",name="CIAP", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc}  ciapSmartViewBusinessObject
Classe para criação do Objeto CIAP - Controle de Crédito do ICMS do Ativo Permanente
@author Raphael Ventura
@since 02/08/2023
*/
//-------------------------------------------------------------------  
class ciapSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Raphael Ventura
@since 02/08/2023
*/
//-------------------------------------------------------------------   
method new() class ciapSmartViewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"Objeto de Negócio CIAP"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto CIAP - Controle de Crédito do ICMS do Ativo Permanente (SF9/SFT/SF3/SF4/SB1)"

    self:nPageSize := oFisTools:paginacao(200)
    oFisTools:validPerg(self:setPergunte("FISTRR0001"))
    freeobj(oFisTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Raphael Ventura
@since 02/08/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class ciapSmartViewBusinessObject
    Local cQuery      as Character
    Local oFisTools   := fisSmartviewTools():new()
    Local oFiltQry    := oFilter:getParameters() as Json
    Local dStartDate  := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
    Local dEndDate    := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
    Local cFilDe      := oFiltQry['MV_PAR03',1] as Character
    Local cFilAte     := oFiltQry['MV_PAR04',1] as Character
    Local cCodDe      := oFiltQry['MV_PAR05',1] as Character
    Local cCodAte     := iif(!empty(oFiltQry['MV_PAR06',1]),oFiltQry['MV_PAR06',1],'ZZZZZZZZ') as Character
    Local lOk         := .t. as logical
    Local aFiliais    := {} as array
    Local cFils       := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    local aBind       := {}  as array
    local nlA         := 0   as numeric
    Local oPrepare    := nil as object

    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk
        self:setPageSize(self:nPageSize)
        
        cQuery := "SELECT       SFT.FT_FILIAL, "
        cQuery += "		        SFT.FT_NFISCAL, "
        cQuery += "		        SFT.FT_SERIE, "
        cQuery += "		        SFT.FT_PRODUTO, "
        cQuery += "		        SB1.B1_DESC, "
        cQuery += "		        SFT.FT_ITEM, "
        cQuery += "		        SFT.FT_TES, "
        cQuery += "		        SF9.F9_CODIGO, "
        cQuery += "		        SF9.F9_DESCRI, "
        cQuery += "             CASE "
        cQuery += "                 WHEN SF9.F9_SLDPARC = 0 THEN 'BAIXADO' "
        cQuery += "                 ELSE 'EM APROPRIACAO' "
        cQuery += "             END AS STATUS, "
        cQuery += "		        SF9.F9_DTENTNE, "
        cQuery += "		        SF9.F9_VALICMS, "
        cQuery += "		        SF9.F9_PICM, "
        cQuery += "		        SF9.F9_ICMIMOB, "
        cQuery += "		        SF9.F9_VLESTOR, "
        cQuery += "		        SF9.F9_BXICMS, "
        cQuery += "             SF9.F9_VLDBATV, "
        cQuery += "             SF9.F9_VALICCO, "
        cQuery += "             SF9.F9_VALFRET, "
        cQuery += "             SF9.F9_VALICMP, "
        cQuery += "             SF9.F9_VALICST, "
        cQuery += "		        SF9.F9_QTDPARC, "
        cQuery += "		        SF9.F9_SLDPARC, "
        cQuery += "		        SF9.F9_CODBAIX "
        cQuery += "		    FROM
        cQuery +=               RetSqlName("SF9") + " SF9 "
        cQuery += "             LEFT JOIN " + RetSqlName("SFT") + " SFT     ON " + FwJoinFilial("SFT", "SF9") + " "
        cQuery += "		    							                    AND SFT.FT_NFISCAL = SF9.F9_DOCNFE
        cQuery += "		    							                    AND SFT.FT_SERIE = SF9.F9_SERNFE
        cQuery += "		    							                    AND SFT.FT_ITEM = SF9.F9_ITEMNFE
        cQuery += "		    							                    AND SFT.FT_CLIEFOR = SF9.F9_FORNECE
        cQuery += "		    							                    AND SFT.FT_LOJA = SF9.F9_LOJAFOR
        cQuery += "		    							                    AND SFT.FT_TIPOMOV = ?
        aadd( aBind , 'E' )
        cQuery += "		    							                    AND SFT.D_E_L_E_T_ = ?
        aadd( aBind , ' ' )
        cQuery += "             INNER JOIN " + RetSqlName("SF4") + " SF4	ON " + FwJoinFilial("SF4", "SFT") + " "
        cQuery += "		    							                    AND SF4.F4_CODIGO = SFT.FT_TES
        cQuery += "		    							                    AND SF4.F4_CIAP = ?
        aadd( aBind , 'S' )
        cQuery += "		    							                    AND SF4.D_E_L_E_T_ = ?
        aadd( aBind , ' ' )
        cQuery += "             INNER JOIN " + RetSqlName("SB1") + " SB1    ON " + FwJoinFilial("SB1","SFT") + " "
        cQuery += "		    							                    AND SB1.B1_COD = SFT.FT_PRODUTO
        cQuery += "		    							                    AND SB1.D_E_L_E_T_ = ?
        aadd( aBind , ' ' )
        cQuery += "		    WHERE "

        If !Empty(cFilAte)
            cQuery +=     " SF9.F9_FILIAL IN ( ? ) AND "
            aadd( aBind , aFiliais )
        EndIf
        
        cQuery += "	        SF9.F9_DTENTNE BETWEEN ? AND ? " //SF9.F9_DTENTNE BETWEEN " + ValToSql(dStartDate) + " AND " + ValToSql(dEndDate)
                            aadd( aBind , dStartDate )
                            aadd( aBind , dEndDate )        
        cQuery += "         AND SF9.F9_CODIGO BETWEEN ? AND ? " //AND TMP.F9_CODIGO BETWEEN '" + cCodDe + "' AND '" + cCodAte + "' "
                            aadd( aBind , cCodDe )
                            aadd( aBind , cCodAte )
        cQuery += "	        AND SF9.D_E_L_E_T_ = ? "
                            aadd( aBind , ' ' )        
        
        If oFilter:hasFilter()
            cQuery += " AND " + oFilter:getSQLExpression()
        EndIf

        oPrepare := FWPreparedStatement():New( ChangeQuery(cQuery) )
        for nlA := 1 to len(aBind)
            if Valtype(aBind[nlA]) == 'A'
                oPrepare:setIn( nlA, aBind[nlA] )
            elseif Valtype(aBind[nlA]) == 'D'
                oPrepare:setDate( nlA, aBind[nlA] )
            else
                oPrepare:setString( nlA, aBind[nlA] )
            endif
        next nlA
        cQuery := oPrepare:getFixQuery()
        self:setQuery(cQuery)        
        self:setOrder("SFT.FT_FILIAL, SFT.FT_SERIE, SFT.FT_ENTRADA")
    endif
    freeobj(oFisTools)
    aSize(aBind,0)
    aBind := Nil
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Raphael Ventura
@since 02/08/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class ciapSmartViewBusinessObject

    //              ( Nome,             Descricao ,                                     Tipo ,      Display ,               cRealName ,     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_FILIAL",       "Filial" ,                                      "string",   "Filial" ,              "FT_FILIAL" ,   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_NFISCAL",      "Número do documento fiscal" ,                  "string",   "Nota Fiscal" ,         "FT_NFISCAL",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_SERIE",        "Série do documento fiscal" ,                   "string",   "Série" ,               "FT_SERIE",     /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_PRODUTO",      "Código do Produto",                            "string",   "Cód. Prod." ,          "FT_PRODUTO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("B1_DESC",         "Descrição do Produto" ,                        "string",   "Desc. Prod." ,         "B1_DESC",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_ITEM",         "Item" ,                                        "string",   "Item" ,                "FT_ITEM",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("FT_TES",          "TES" ,                                         "string",   "TES" ,                 "FT_TES",       /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_CODIGO",       "Código do Bem" ,                               "string",   "Cod. do Bem" ,         "F9_CODIGO",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_DESCRI",       "Descrição do Bem" ,                            "string",   "Desc. do Bem" ,        "F9_DESCRI",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("STATUS",          "Status do Bem" ,                               "string",   "Status do Bem" ,       "STATUS",       /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICMS",      "Valor Total Passível de Crédito" ,             "number",   "Val. Tot. Pass. Cred." ,"F9_VALICMS",  /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_PICM",         "Alíquota ICMS" ,                               "number",   "Aliq. ICMS" ,          "F9_PICM",      /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_ICMIMOB",      "ICMS Imobilizado" ,                            "number",   "ICMS Imob." ,          "F9_ICMIMOB",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VLESTOR",      "Valor Parcela Passível de Credito" ,           "number",   "Val. Parc. Pass. Cred.","F9_VLESTOR",  /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_BXICMS",       "Valor Baixado ICMS" ,                          "number",   "Val. Baix. ICMS" ,     "F9_BXICMS",    /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VLDBATV",      "Valor de Débito por Diferencial de Alíquota" , "number",   "Val. Déb. Difal" ,     "F9_VLDBATV",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICCO",      "Valor ICMS Complementar" ,                     "number",   "Val. ICMS Comp." ,     "F9_VALICCO",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALFRET",      "Valor ICMS Frete" ,                            "number",   "Val. ICMS Frete" ,     "F9_VALFRET",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICMP",      "Valor ICMS Próprio" ,                          "number",   "Val. ICMS Próp." ,     "F9_VALICMP",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_VALICST",      "Valor ICMS-ST" ,                               "number",   "Val. ICMS-ST" ,        "F9_VALICST",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_QTDPARC",      "Parcela" ,                                     "number",   "Parcela" ,             "F9_QTDPARC",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )
    self:addProperty("F9_SLDPARC",      "Saldo Parcelas" ,                              "number",   "Saldo Parcelas" ,      "F9_SLDPARC",   /*cComboValues*/ ,  /*lIsRequired*/,    /*cRenameField*/ )

return self:oSchema
