#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.cat207.ch"
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFT,CFF,SA1,SYA,SF4", name="cat207SmartviewBusinessObject", country="BRA", initialRelease="12.1.2310") 
//-------------------------------------------------------------------
/*{Protheus.doc}  cat207SmartviewBusinessObject
Classe para criação do Objeto cat207SmartviewBusinessObject - Enquadramento Legal da Operação/Prestação Geradora de Crédito Acumulado do ICMS
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------  
class cat207SmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data cDBMS as character
    
    public method new() as object
    public method getData() as object
    public method getSchema() as object

endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method new() class cat207SmartviewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"Objeto de Negocio CAT207"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto cat207SmartviewBusinessObject - Objeto de negócio para conferencia do arquivo da CAT207 (SFT/CFF/SA1/SYA/SF4)"
    
    oFisTools:validPerg(self:setPergunte("FISTRR2071"))

    self:cDBMS := UPPER(TcGetDb()) //verifica qual banco é utilizado

    freeobj(oFisTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getData( nPage as numeric, oFilter as object) as object class cat207SmartviewBusinessObject

    Local cQuery        as Character
    Local cAlias        as Character
    Local oFisTools     := fisSmartviewTools():new()
    Local oFiltQry      := oFilter:getParameters() as Json
    Local dStartDate    := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1]
    Local dEndDate      := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1]
    Local cFilDe        := oFiltQry['MV_PAR03',1] as Character
    Local cFilAte       := oFiltQry['MV_PAR04',1] as Character
    Local lOk           := .T. as Logical
    Local oPrepare      := nil as object
    Local cCodEnq       := ""  as character
    Local cObsCFF       := ""  as character
    Local nFilial       := 0   as numeric
    // variaveis obrigatórias para execução do MontFilial
	Local aSM0Data	 := {} as array
	Local nPosDe 	 := 0  as numeric
    Local nPosAte 	 := 0  as numeric
	Local oFilAcum	 := nil as object 
	Local lRetFil    := oFisTools:montFilial(@cFilDe, @cFilAte, @nPosDe, @nPosAte, @aSM0Data) as logical
	Local aFilCFF	 := {} as array
	Local aFilSA1	 := {} as array
    Local aFilSYA	 := {} as array
    Local aFilSF4	 := {} as array
    Local aFilSFT	 := {} as array

    if !lRetFil
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk

        cQuery += "SELECT " 
        cQuery += "   TAB.FT_FILIAL,"
        cQuery += "   TAB.FT_ENTRADA,"
        cQuery += "   TAB.FT_NFISCAL,"
        cQuery += "   TAB.FT_SERIE,"
        cQuery += "   TAB.FT_CLIEFOR,"
        cQuery += "   TAB.FT_LOJA,"
        cQuery += "   TAB.FT_ESTADO,"
        cQuery += "   TAB.FT_CFOP,"
        cQuery += "   TAB.FT_ALIQICM,"
        cQuery += "   TAB.FT_VALCONT,"
        cQuery += "   TAB.FT_BASEICM,"
        cQuery += "   TAB.FT_VALICM,"
        cQuery += "   TAB.FT_NFORI,"
        cQuery += "   TAB.FT_SERORI,"
        cQuery += "   TAB.FT_ITEMORI,"
        cQuery += "   TAB.FT_ESPECIE,"
        cQuery += "   TAB.FT_PRODUTO,"
        cQuery += "   TAB.FT_ITEM,"
        cQuery += "   TAB.FT_QUANT,"
        cQuery += "   TAB.CNPJ,"
        cQuery += "   TAB.NOMPAR,"
        cQuery += "   TAB.INCESC,"
        cQuery += "   TAB.TIPOMOV,"
        cQuery += "   TAB.TIPO,"
        cQuery += "   TAB.F4_PERCMED,"
        cQuery += "   TAB.F4_CRDACUM,"
        cQuery += "   TAB.F4_IVAUTIL,"
        cQuery += "   TAB.F4_LFICM,"
        cQuery += "   TAB.A1_EST,"
        cQuery += "   TAB.A1_CEP,"
        cQuery += "   TAB.A1_CODPAIS,"
        cQuery += "   TAB.A1_PAIS,"
        cQuery += "   TAB.YA_CODGI,"
        cQuery += "   TAB.YA_DESCR,"
        cQuery += "   TAB.CFF_FILIAL,"
        cQuery += "   TAB.CFF_CODLEG,"
        cQuery += "   TAB.CFF_CODIGO,"
        cQuery += "   TAB.CFF_ANEXO,"
        cQuery += "   TAB.CFF_ART,"
        cQuery += "   TAB.CFF_INC,"
        cQuery += "   TAB.CFF_ALIN,"
        cQuery += "   TAB.CFF_PRG,"
        cQuery += "   TAB.CFF_ITM,"
        cQuery += "   TAB.CFF_LTR," //rever linha abaixo
        cQuery += "   LTRIM(RTRIM(TAB.CFF_CODIGO)) || LTRIM(RTRIM(TAB.CFF_ANEXO)) || LTRIM(RTRIM(TAB.CFF_ART)) || LTRIM(RTRIM(TAB.CFF_INC)) || LTRIM(RTRIM(TAB.CFF_ALIN)) || LTRIM(RTRIM(TAB.CFF_PRG)) || LTRIM(RTRIM(TAB.CFF_ITM)) || LTRIM(RTRIM(TAB.CFF_LTR)) AS CODENQ,"
        cQuery += "   TAB.CFF_OBS,
   
        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '01' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "           WHERE CFFAUX.CFF_FILIAL = ? AND " // 1
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " // 2
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ?  THEN (TAB.FT_VALCONT * - 1) " //3 # 4
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Op_Inter7,"

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '02' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "           WHERE CFFAUX.CFF_FILIAL = ? AND " //5
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //6
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //7#8
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Op_Inter12, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '03' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //9
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //10
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //11#12
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Op_Interna_7, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '06' AND TAB.FT_ALIQICM = 7 THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND TAB.FT_ALIQICM = 7 AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "           WHERE CFFAUX.CFF_FILIAL = ? AND " //13
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //14
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //15#16
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Reducao_7_4, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '06' AND TAB.FT_ALIQICM = 12 THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND TAB.FT_ALIQICM = 12 AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //17
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //18
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //19#20
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Reducao_12_8, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '06' AND TAB.FT_ALIQICM = 18 THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND TAB.FT_ALIQICM = 18 AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //21
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //22
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //23#24
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Reducao_18_5, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '07' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //25
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //26
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //27#28
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Exportacao, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '08' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //29
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND " 
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND " 
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //30
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //31#32
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Exportacao, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '10' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //33
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND " 
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //34
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ? THEN (TAB.FT_VALCONT * - 1) " //35#36
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Diferimento, "

        cQuery += "   CASE  WHEN TAB.CFF_CODIGO = '11' THEN TAB.FT_VALCONT "
        cQuery += "         WHEN TAB.TIPO = 'Devolucao' AND "
        cQuery += "         (SELECT COUNT(CFF_NUMDOC) "
        cQuery += "            FROM " + RetSqlName("CFF") + " CFFAUX "
        cQuery += "			  WHERE CFFAUX.CFF_FILIAL = ? AND " //37
        cQuery += "                 CFFAUX.CFF_NUMDOC = TAB.FT_NFORI AND "
        cQuery += "                 CFFAUX.CFF_SERIE  = TAB.FT_SERORI AND "
        cQuery += "                 CFFAUX.CFF_CLIFOR = TAB.FT_CLIEFOR AND "
        cQuery += "                 CFFAUX.CFF_LOJA   = TAB.FT_LOJA AND "
        cQuery += "                 CFFAUX.CFF_ITEMNF = TAB.FT_ITEMORI AND "
        cQuery += "                 CFFAUX.CFF_CODIGO = ? AND " //38
        cQuery += "                 CFFAUX.D_E_L_E_T_ = ? ) > ?  THEN (TAB.FT_VALCONT * - 1) " //39#40
        cQuery += "         ELSE 0 "
        cQuery += "   END AS Isencao, TAB.F4_CROUTSP AS Cred_Out "

        cQuery += " FROM "
        cQuery += " ( "
        cQuery += " SELECT ROW_NUMBER() OVER( ORDER BY TMP.FT_FILIAL, TMP.SERNF, TMP.TIPOMOV DESC) LINHA,"
        cQuery += " TMP.F4_CROUTSP,"
        cQuery += " TMP.FT_CFOP,"
        cQuery += " TMP.FT_ITEM,"
        cQuery += " TMP.F4_PERCMED,"
        cQuery += " TMP.F4_CRDACUM,"
        cQuery += " TMP.F4_IVAUTIL,"
        cQuery += " TMP.F4_LFICM,"
        cQuery += " TMP.CNPJ,"
        cQuery += " TMP.NOMPAR,"
        cQuery += " TMP.INCESC,"
        cQuery += " TMP.A1_EST,"
        cQuery += " TMP.A1_CEP,"
        cQuery += " TMP.A1_CODPAIS,"
        cQuery += " TMP.A1_PAIS,"
        cQuery += " TMP.YA_CODGI,"
        cQuery += " TMP.YA_DESCR,"
        cQuery += " TMP.TIPOMOV,"
        cQuery += " TMP.TIPO,"
        cQuery += " TMP.FT_FILIAL,"
        cQuery += " TMP.FT_ENTRADA,"
        cQuery += " TMP.FT_ESPECIE,"
        cQuery += " TMP.FT_NFISCAL,"
        cQuery += " TMP.FT_ESTADO,"
        cQuery += " TMP.FT_SERIE,"
        cQuery += " TMP.FT_CLIEFOR,"
        cQuery += " TMP.FT_LOJA,"
        cQuery += " TMP.FT_NFORI,"
        cQuery += " TMP.FT_SERORI,"
        cQuery += " TMP.FT_ITEMORI,"
        cQuery += " TMP.FT_BASEICM,"
        cQuery += " TMP.FT_ALIQICM,"
        cQuery += " TMP.FT_VALICM,"
        cQuery += " TMP.FT_VALCONT,"
        cQuery += " TMP.FT_QUANT,"
        cQuery += " TMP.FT_PRODUTO,"
        cQuery += " TMP.CFF_FILIAL,"
        cQuery += " TMP.CFF_CODLEG,"
        cQuery += " TMP.CFF_CODIGO,"
        cQuery += " TMP.CFF_ANEXO,"
        cQuery += " TMP.CFF_ART,"
        cQuery += " TMP.CFF_INC,"
        cQuery += " TMP.CFF_ALIN,"
        cQuery += " TMP.CFF_PRG,"
        cQuery += " TMP.CFF_ITM,"
        cQuery += " TMP.CFF_LTR,"
        cQuery += " TMP.CFF_OBS"
        cQuery += " FROM "
        cQuery += " ( "
        cQuery += " SELECT DISTINCT "
        cQuery += " SF4.F4_CROUTSP F4_CROUTSP,"
        cQuery += " SFT.FT_CFOP FT_CFOP,"
        cQuery += " SFT.FT_ITEM FT_ITEM,"
        cQuery += " SF4.F4_PERCMED F4_PERCMED,"
        cQuery += " SF4.F4_CRDACUM F4_CRDACUM,"
        cQuery += " SF4.F4_IVAUTIL F4_IVAUTIL,"
        cQuery += " SF4.F4_LFICM F4_LFICM,"
        cQuery += " SA1.A1_CGC CNPJ,"
        cQuery += " SA1.A1_NOME NOMPAR,"
        cQuery += " SA1.A1_INSCR INCESC,"
        cQuery += " SA1.A1_EST A1_EST,"
        cQuery += " SA1.A1_CEP A1_CEP,"
        cQuery += " SA1.A1_CODPAIS A1_CODPAIS,"
        cQuery += " SA1.A1_PAIS A1_PAIS,"
        cQuery += " SYA.YA_CODGI YA_CODGI,"
        cQuery += " SYA.YA_DESCR YA_DESCR,"
        cQuery += " CASE WHEN SFT.FT_TIPOMOV = 'E' THEN 'Entrada' "
        cQuery += "      ELSE 'Saida' "
        cQuery += " END TIPOMOV, "
        cQuery += " CASE WHEN SFT.FT_TIPO = 'D' THEN 'Devolucao' "
        cQuery += "      WHEN SFT.FT_TIPO = 'S' THEN 'Servico' "
        cQuery += "      WHEN SFT.FT_TIPO = 'L' THEN 'Em Lote' "
        cQuery += "      WHEN SFT.FT_TIPO = 'C' THEN 'Complemento' "
        cQuery += "      WHEN SFT.FT_TIPO = ' ' OR SFT.FT_TIPO = 'N'  THEN 'Normal' "
        cQuery += "      WHEN SFT.FT_TIPO = 'B' THEN 'Beneficiamento' "
        cQuery += "      ELSE SFT.FT_TIPO "
        cQuery += " END "
        cQuery += " TIPO, SFT.FT_FILIAL FT_FILIAL, SFT.FT_ENTRADA FT_ENTRADA, SFT.FT_ESPECIE FT_ESPECIE, SFT.FT_NFISCAL FT_NFISCAL, SFT.FT_ESTADO FT_ESTADO, SFT.FT_SERIE FT_SERIE, SFT.FT_CLIEFOR FT_CLIEFOR, SFT.FT_LOJA FT_LOJA, SFT.FT_NFORI FT_NFORI, SFT.FT_SERORI FT_SERORI, SFT.FT_ITEMORI FT_ITEMORI, "
        cQuery += " CASE "
        cQuery += "      WHEN SFT.FT_TIPO = 'D' THEN SFT.FT_SERORI || SFT.FT_NFORI || SFT.FT_ITEMORI "
        cQuery += "      ELSE SFT.FT_SERIE || SFT.FT_NFISCAL || SFT.FT_ITEM "
        cQuery += " END AS SERNF, "
        cQuery += " SFT.FT_BASEICM FT_BASEICM, SFT.FT_ALIQICM FT_ALIQICM, SFT.FT_VALICM FT_VALICM, SFT.FT_VALCONT FT_VALCONT, SFT.FT_QUANT FT_QUANT, SFT.FT_PRODUTO FT_PRODUTO, CFF.CFF_FILIAL CFF_FILIAL, CFF.CFF_CODLEG CFF_CODLEG, CFF.CFF_CODIGO CFF_CODIGO, CFF.CFF_ANEXO CFF_ANEXO, CFF.CFF_ART CFF_ART, CFF.CFF_INC CFF_INC, CFF.CFF_ALIN CFF_ALIN, CFF.CFF_PRG CFF_PRG, CFF.CFF_ITM CFF_ITM, CFF.CFF_LTR CFF_LTR, CFF.CFF_OBS CFF_OBS "
        cQuery += " FROM " + RetSqlName("SFT") + " SFT "
        cQuery += "    LEFT JOIN " + RetSqlName("CFF") + " CFF ON "
        cQuery += "         CFF.CFF_FILIAL = ? AND " //41
        cQuery += "         CFF.CFF_NUMDOC = SFT.FT_NFISCAL AND "
        cQuery += "         CFF.CFF_SERIE  = SFT.FT_SERIE AND "
        cQuery += "         CFF.CFF_CLIFOR = SFT.FT_CLIEFOR AND "
        cQuery += "         CFF.CFF_LOJA   = SFT.FT_LOJA AND "
        cQuery += "         CFF.CFF_ITEMNF = SFT.FT_ITEM AND "
        cQuery += "         CFF.D_E_L_E_T_ = ? "     //42

        cQuery += "    INNER JOIN " + RetSqlName("SA1") + " SA1 ON "
        cQuery += "         SA1.A1_FILIAL  = ? AND " //43
        cQuery += "         SA1.A1_COD     = SFT.FT_CLIEFOR AND "
        cQuery += "         SA1.A1_LOJA    = SFT.FT_LOJA AND "
        cQuery += "         SA1.D_E_L_E_T_ = ? "     //44
        cQuery += "    LEFT JOIN " + RetSqlName("SYA") + " SYA ON "
        cQuery += "         SYA.YA_FILIAL  = ? AND " //45
        cQuery += "         SYA.YA_CODGI   = SA1.A1_PAIS AND "
        cQuery += "         SYA.D_E_L_E_T_ = ? "     //46
        cQuery += "    INNER JOIN " + RetSqlName("SF4") + " SF4 ON "
        cQuery += "         SF4.F4_FILIAL  = ? AND " //47
        cQuery += "         SF4.F4_CODIGO  = SFT.FT_TES AND "
        cQuery += "         SF4.D_E_L_E_T_ = ? "     //48

        cQuery += " WHERE SFT.FT_FILIAL  = ? AND "   //49
        cQuery += "    ( (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO = ?) OR (SFT.FT_TIPOMOV = ? AND SFT.FT_TIPO <> ?)) AND " //50#51#52#53
        cQuery += "       SFT.FT_ENTRADA BETWEEN ? AND ? AND " //54#55
        cQuery += "       SFT.D_E_L_E_T_ = ? " //56
        cQuery += "	   ) TMP "
        cQuery += " ) "
        cQuery += " TAB ORDER BY LINHA "

        oPrepare := FwExecStatement():New( ChangeQuery(cQuery) )   

        //Percorre as Filiais informadas nos parametros Filial De / Filial Ate
        oFilAcum := JsonObject():New()
		oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilCFF,'CFF',@oFilAcum)
        oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilSA1,'SA1',@oFilAcum)
        oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilSYA,'SYA',@oFilAcum)
        oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilSF4,'SF4',@oFilAcum)
        oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilSFT,'SFT',@oFilAcum)
        /*
        nPosDe  := Filial de, Fil Ate, Fil SM0 ,  
        nPosAte := Filial Atev
        aSM0Data:= Filiais da SM0
        aFil### := Retorna filial Já filtrada pelo xFilial
        'CFF'   := Tabela para filtar o xFilial  
        oFilAcum:= Json com o cache de filiais por nivel de compartilhamento das tabelas
        */
        For nFilial := 1 to Len(aFilCFF)

            oPrepare:SetString(01,  aFilCFF[nFilial] )
            oPrepare:SetString(02, '01')
            oPrepare:SetString(03, ' ' )
            oPrepare:SetNumeric(04, 0  )

            oPrepare:SetString(05, aFilCFF[nFilial] )
            oPrepare:SetString(06, '02')
            oPrepare:SetString(07, ' ' )
            oPrepare:SetNumeric(08, 0  )

            oPrepare:SetString(09, aFilCFF[nFilial] )
            oPrepare:SetString(10, '03')
            oPrepare:SetString(11, ' ' )
            oPrepare:SetNumeric(12, 0  )

            oPrepare:SetString(13, aFilCFF[nFilial] )
            oPrepare:SetString(14, '06')
            oPrepare:SetString(15, ' ' )
            oPrepare:SetNumeric(16, 0  )

            oPrepare:SetString(17, aFilCFF[nFilial] )
            oPrepare:SetString(18, '06')
            oPrepare:SetString(19, ' ' )
            oPrepare:SetNumeric(20, 0  )

            oPrepare:SetString(21, aFilCFF[nFilial] )
            oPrepare:SetString(22, '06')
            oPrepare:SetString(23, ' ' )
            oPrepare:SetNumeric(24, 0  )

            oPrepare:SetString(25, aFilCFF[nFilial] )
            oPrepare:SetString(26, '07')
            oPrepare:SetString(27, ' ' )
            oPrepare:SetNumeric(28, 0  )

            oPrepare:SetString(29, aFilCFF[nFilial] )
            oPrepare:SetString(30, '08')
            oPrepare:SetString(31, ' ' )
            oPrepare:SetNumeric(32, 0  )

            oPrepare:SetString(33, aFilCFF[nFilial] )
            oPrepare:SetString(34, '10')
            oPrepare:SetString(35, ' ' )
            oPrepare:SetNumeric(36, 0  )

            oPrepare:SetString(37, aFilCFF[nFilial] )
            oPrepare:SetString(38, '11')
            oPrepare:SetString(39, ' ' )
            oPrepare:SetNumeric(40, 0  )

            oPrepare:SetString(41, aFilCFF[nFilial] )
            oPrepare:SetString(42, ' ' )
            oPrepare:SetString(43, aFilSA1[nFilial] )
            oPrepare:SetString(44, ' ' )
            oPrepare:SetString(45, aFilSYA[nFilial] )
            oPrepare:SetString(46, ' ' )
            oPrepare:SetString(47, aFilSF4[nFilial] )
            oPrepare:SetString(48, ' ' )
            oPrepare:SetString(49, aFilSFT[nFilial] )
            oPrepare:SetString(50, 'E' )    // SFT.FT_TIPOMOV  = 'E' - Entrada
            oPrepare:SetString(51, 'D' )    // SFT.FT_TIPO     = 'D'
            oPrepare:SetString(52, 'S' )    // SFT.FT_TIPOMOV  = 'S' - Saída
            oPrepare:SetString(53, 'D' )    // SFT.FT_TIPO    <> 'D'
            oPrepare:SetDate(54, dStartDate	)
            oPrepare:SetDate(55, dEndDate	)
            oPrepare:SetString(56, ' ' )

            cAlias := oPrepare:OpenAlias()

            While !(cAlias)->(Eof())

                cCodEnq := StrTran((cAlias)->(CFF_CODIGO+CFF_ANEXO+CFF_ART+CFF_INC+CFF_ALIN+CFF_PRG+CFF_ITM+CFF_LTR)," ","")
                cObsCFF := Alltrim((cAlias)->(CFF_OBS))

                If(Empty(cCodEnq)) 
                    cCodEnq := "Sem enquadramento"
                Endif

                If(Empty(cObsCFF)) 
                    cObsCFF := "«Vazio»" 
                Endif

                self:oData:appendData({ "FT_FILIAL" :       (cAlias)->FT_FILIAL,;
                                        "FT_ESPECIE" :      (cAlias)->FT_ESPECIE,;
                                        "FT_ENTRADA" :      totvs.framework.treports.date.stringToTimeStamp((cAlias)->FT_ENTRADA),; 
                                        "FT_NFISCAL" :      Alltrim((cAlias)->FT_NFISCAL),;
                                        "FT_SERIE" :        (cAlias)->FT_SERIE,; 
                                        "FT_ITEM" :         (cAlias)->FT_ITEM,;
                                        "FT_CLIEFOR" :      (cAlias)->FT_CLIEFOR,;
                                        "FT_LOJA" :         ((cAlias)->FT_LOJA),;
                                        "FT_BASEICM" :      ((cAlias)->FT_BASEICM),;
                                        "FT_ALIQICM" :      ((cAlias)->FT_ALIQICM),;
                                        "FT_VALICM" :       ((cAlias)->FT_VALICM),;
                                        "FT_VALCONT" :      ((cAlias)->FT_VALCONT),;
                                        "NOMPAR" :          ((cAlias)->NOMPAR),;
                                        "CNPJ" :            ((cAlias)->CNPJ),;
                                        "INCESC" :          ((cAlias)->INCESC),;
                                        "FT_ESTADO" :       (cAlias)->FT_ESTADO,; 
                                        "FT_CFOP" :         (cAlias)->FT_CFOP,;
                                        "FT_NFORI" :        (cAlias)->FT_NFORI,;
                                        "FT_SERORI" :       (cAlias)->FT_SERORI,;
                                        "TIPO" :            (cAlias)->TIPO,;
                                        "TIPOMOV" :         (cAlias)->TIPOMOV,;
                                        "CODENQ" :          cCodEnq,;
                                        "CFF_CODLEG" :      (cAlias)->CFF_CODLEG,;
                                        "CFF_CODIGO" :      (cAlias)->CFF_CODIGO,;
                                        "CFF_ANEXO" :       (cAlias)->CFF_ANEXO,;
                                        "CFF_ART" :         (cAlias)->CFF_ART,;
                                        "CFF_INC" :         (cAlias)->CFF_INC,;
                                        "CFF_ALIN" :        (cAlias)->CFF_ALIN,;
                                        "CFF_PRG" :         (cAlias)->CFF_PRG,;
                                        "CFF_ITM" :         (cAlias)->CFF_ITM,;
                                        "CFF_LTR" :         (cAlias)->CFF_LTR,;
                                        "CFF_OBS" :         cObsCFF,;
                                        "OP_INTEREST_7" :   (cAlias)->Op_Inter7,;
                                        "OP_INTEREST_12" :  (cAlias)->Op_Inter12,; 
                                        "OP_INTERNA_7" :    (cAlias)->Op_Interna_7,; 
                                        "REDUCAO_7_4" :     (cAlias)->Reducao_7_4,; 
                                        "REDUCAO_12_8" :    (cAlias)->Reducao_12_8,; 
                                        "REDUCAO_18_5" :    (cAlias)->Reducao_18_5,; 
                                        "EXPORTACAO" :      (cAlias)->Exportacao,; 
                                        "DIFERIMENTO" :     (cAlias)->Diferimento,; 
                                        "ISENCAO" :         (cAlias)->Isencao,; 
                                        "CRED_OUT" :        (cAlias)->Cred_Out; 
                                    }) 

                (cAlias)->(dbSkip())
            Enddo
                
            (cAlias)->(DBCloseArea())
        
        Next         
        oPrepare:Destroy()

    Endif

    freeobj(oFisTools)
    oPrepare := nil 
    freeobj(oFiltQry)
    freeobj(oFilAcum)
    
	aSize(aFilCFF,0)
	aSize(aFilSA1,0)
    aSize(aFilSYA,0)
    aSize(aFilSF4,0)
    aSize(aFilSFT,0)
    aSize(aSM0Data,0)
    aSM0Data := Nil
	aFilCFF  := Nil
	aFilSA1  := Nil
    aFilSYA  := Nil
    aFilSF4  := Nil
    aFilSFT  := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Adilson Roberto
@since 16/05/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class cat207SmartviewBusinessObject

    self:addProperty("FT_FILIAL",       "Filial"                                                                    , "string", "Filial"                , "FT_FILIAL")
    self:addProperty("FT_ENTRADA",      "Data Entrada"                                                              , "date" , "Data Entrada"           , "FT_ENTRADA")
    self:addProperty("FT_ESPECIE",      "Espécie do documento fiscal"                                               , "string", "Espécie"               , "FT_ESPECIE")
    self:addProperty("FT_NFISCAL",      "Número do documento fiscal"                                                , "string", "Nota Fiscal"           , "FT_NFISCAL")
    self:addProperty("FT_SERIE",        "Série do documento fiscal"                                                 , "string", "Série"                 , "FT_SERIE")
    self:addProperty("FT_ITEM",         "Item do documento fiscal"                                                  , "string", "Item NF"               , "FT_ITEM")
    self:addProperty("FT_CLIEFOR",      "Código do participante (cliente/fornecedor)"                               , "string", "Cód. participante"     , "FT_CLIEFOR")
    self:addProperty("FT_LOJA",         "Loja do participante"                                                      , "string", "Lj. participante"      , "FT_LOJA")    
    self:addProperty("FT_BASEICM",      "Base ICMS"                                                                 , "number", "Base ICMS"             , "FT_BASEICM")
    self:addProperty("FT_ALIQICM",      "Alíquota ICMS"                                                             , "number", "Alíq. ICMS"            , "FT_ALIQICM")
    self:addProperty("FT_VALICM",       "Valor ICMS"                                                                , "number", "Valor ICMS"            , "FT_VALICM")  
    self:addProperty("FT_VALCONT",      "Valor Contábil"                                                            , "number", "Valor Contábil"        , "FT_VALCONT")    
    self:addProperty("NOMPAR",          "Nome do Participante"                                                      , "string", "Participante"          , "NOMPAR")
    self:addProperty("CNPJ",            "CNPJ/CPF do participante"                                                  , "string", "CNPJ/CPF"              , "CNPJ")
    self:addProperty("INCESC",          "Inscrição estadual do participante"                                        , "string", "Insc. Est."            , "INCESC")
    self:addProperty("FT_ESTADO",       "UF do participante"                                                        , "string", "UF"                    , "FT_ESTADO")
    self:addProperty("FT_CFOP",         "CFOP"                                                                      , "string", "CFOP"                  , "FT_CFOP")
    self:addProperty("FT_NFORI",        "Documento fiscal de origem"                                                , "string", "N.Fiscal Origem"       , "FT_NFORI")
    self:addProperty("FT_SERORI",       "Série doc. fiscal de origem"                                               , "string", "Série Origem"          , "FT_SERORI")
    self:addProperty("TIPO",            "Tipo de nota fiscal (Ex.: Devolução, Normal etc)"                          , "string", "Tp. Nota"              , "TIPO")
    self:addProperty("TIPOMOV",         "Tipo de movimento (Ex.: Entrada/Saída)"                                    , "string", "Entrada/Saída"         , "TIPOMOV")
    self:addProperty("CODENQ",          "Concatenação dos campos do registro 0300 do arquivo CAT207"                , "string", "Enquadramento"         , "CODENQ")
    self:addProperty("CFF_CODLEG",      "Código enquadramento legal"                                                , "string", "Cód.Enq.Legal"         , "CFF_CODLEG")
    self:addProperty("CFF_CODIGO",      "Código da hipótese de geração conforme o inciso do artigo 71 do RICMS/00"  , "string", "Cód.Hip.Ger"           , "CFF_CODIGO")
    self:addProperty("CFF_ANEXO",       "Anexo do RICMS"                                                            , "string", "Anexo RICMS"           , "CFF_ANEXO")
    self:addProperty("CFF_ART",         "Artigo do RICMS"                                                           , "string", "Art. RICMS"            , "CFF_ART")
    self:addProperty("CFF_INC",         "Inciso do RICMS"                                                           , "string", "Inc. RICMS"            , "CFF_INC")
    self:addProperty("CFF_ALIN",        "Alínea do RICMS"                                                           , "string", "Ali. RICMS"            , "CFF_ALIN")
    self:addProperty("CFF_PRG",         "Parágrafo do RICMS"                                                        , "string", "Parág. RICMS"          , "CFF_PRG")
    self:addProperty("CFF_ITM",         "Item do RICMS"                                                             , "string", "Item RICMS"            , "CFF_ITM")
    self:addProperty("CFF_LTR",         "Letra do RICMS"                                                            , "string", "Letra RICMS"           , "CFF_LTR")
    self:addProperty("CFF_OBS",         "Informação complementar"                                                   , "string", "Inf. complementar"     , "CFF_OBS")
    self:addProperty("OP_INTEREST_7",   "Operação Interestadual 7%"                                                 , "number", "Op. Interestadual 7%"  , "OP_INTEREST_7" )
    self:addProperty("OP_INTEREST_12",  "Operação Interestadual 12%"                                                , "number", "Op. Interestadual 12%" , "OP_INTEREST_12" )
    self:addProperty("OP_INTERNA_7",    "Operação Interna 7%"                                                       , "number", "Op. Interna 7%"        , "OP_INTERNA_7" )
    self:addProperty("REDUCAO_7_4",     "Redução 7% p/ 4,1%"                                                        , "number", "Red. 7% p/ 4,1%"       , "REDUCAO_7_4" )
    self:addProperty("REDUCAO_12_8",    "Redução 12% p/ 8,8%"                                                       , "number", "Red. 12% p/ 8,8%"      , "REDUCAO_12_8" )
    self:addProperty("REDUCAO_18_5",    "Redução 18% p/ 5,6%"                                                       , "number", "Red. 18% p/ 5,6%"      , "REDUCAO_18_5" )
    self:addProperty("EXPORTACAO",      "Exportação"                                                                , "number", "Exportação"            , "EXPORTACAO" )
    self:addProperty("DIFERIMENTO",     "Diferimento"                                                               , "number", "Diferimento"           , "DIFERIMENTO" )
    self:addProperty("ISENCAO",         "Isenção"                                                                   , "number", "Isenção"               , "ISENCAO" )
    self:addProperty("CRED_OUT",        "Crédito Outorgado 7%"                                                      , "number", "Créd. Out. 7%"         , "CRED_OUT" )
    
return self:oSchema
