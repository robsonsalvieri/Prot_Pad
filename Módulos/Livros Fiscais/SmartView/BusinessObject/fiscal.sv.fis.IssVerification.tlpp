#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.IssVerification.ch" 
    
namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFIS", tables="SF3, SFT, SA1, SA2", name="IssVerificationSmartViewBusinessObject", country="BRA", initialRelease="12.1.2310")
//-------------------------------------------------------------------
/*{Protheus.doc} IssVerificationSmartViewBusinessObject
Classe para criação do Objeto IssVerification - Registro de Apuração de ISS para Notas fiscais de entrada e saída de serviço com ISS Isento, 
Tributado e Outros, com e sem retenção do imposto, ou seja, que tenham gerado ou não título de ISS a recolher no contas a pagar ou 
contas a receber
 
@author Juliana Mellão
@since 16/06/2023
*/
//-------------------------------------------------------------------  
class IssVerificationSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Juliana Mellão
@since 16/06/2023
*/
//-------------------------------------------------------------------   
method new() class IssVerificationSmartViewBusinessObject
    Local oFisTools := fisSmartviewTools():new()
    _Super:new()
        //Define Area
    self:appendArea(STR0001)//"Livros Fiscais"
        //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002)//"Apuração de ISS"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003)//"Objeto IssVerification - Objeto de negócio da tabela SF3 para conferência de valores de ISS (SF3/SFT/SA1/SA2)"

    oFisTools:validPerg(self:setPergunte("FISTRISS01"))
    freeobj(oFisTools)

return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Juliana Mellão
@since 16/06/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class IssVerificationSmartViewBusinessObject
    
    Local oFisTools := fisSmartviewTools():New()                        as object
    Local oPrepare                                                      as object
    Local oFiltQry  := oFilter:getParameters()                          as Json    
    local aBind     := {}                                               as array
    Local aFiliais  := {}                                               as array
    local cAlias                                                        as character
    Local cCodISS                                                       as character    
    Local dEntrada  := FwDateTimeToLocal(oFiltQry[ 'MV_PAR01' ,1])[1]   as date
    Local dSaida    := FwDateTimeToLocal(oFiltQry[ 'MV_PAR02' ,1])[1]   as date    
    local cFilDe    := oFiltQry[ 'MV_PAR03' ,1]                         as character
    local cFilAte   := oFiltQry[ 'MV_PAR04' ,1]                         as character
    local cNrLivro  := oFiltQry[ 'MV_PAR05' ,1]                         as character
    local cUFMun    := oFiltQry[ 'MV_PAR06' ,1]                         as character
    local cCodMun   := oFiltQry[ 'MV_PAR07' ,1]                         as character
    Local cFils     := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character    
    local cQuery                                                        as character    
    Local lOk       := .T.                                              as logical
    local nlA                                                           as numeric
    Local nFilial                                                       as numeric
    Local cA1DescMun as character
    Local cA1CodMun  as character
    Local cA1NomeCli as character
    Local cA1CGC     as character
    Local cA2DescMun as character
    Local cA2CodMun  as character
    Local cA2NomeFor as character
    Local cA2CGC     as character


    if cFils == 'Erro'
        oFisTools:validFil(.f.)
        lOk := .f.
    endif

    if lOk

        cQuery := " SELECT  "
        cQuery += "     CASE  "
        cQuery += "         WHEN SFT.FT_TIPOMOV = 'E' THEN 'Entrada'  "
        cQuery += "         ELSE 'Saida'  "
        cQuery += "     END TIPOMOV,  "
        cQuery += "     CASE  "
        cQuery += "         WHEN SFT.FT_RECISS = '1' THEN 'Sim'  "
        cQuery += "         ELSE 'Nao'  "
        cQuery += "     END RECISS,  "
        cQuery += "     CASE  "
        cQuery += "         WHEN SF3.F3_ISSST = '1' THEN 'Dentro Municipio'  "
        cQuery += "         WHEN SF3.F3_ISSST = '2' THEN 'Fora Municipio'  "
        cQuery += "         WHEN SF3.F3_ISSST = '3' THEN 'Isencao'  "
        cQuery += "         WHEN SF3.F3_ISSST = '4' THEN 'Imune'  "
        cQuery += "         WHEN SF3.F3_ISSST = '5' THEN 'Exigibilidade Susp. Judicial'  "
        cQuery += "         WHEN SF3.F3_ISSST = '6' THEN 'Exigibilidade Susp. Proc. Adm.'  "
        cQuery += "         ELSE 'Nao'  "
        cQuery += "     END ISSST,  "
        cQuery += "     SF3.F3_FILIAL,  "
        cQuery += "     SF3.F3_ENTRADA,  "
        cQuery += "     SFT.FT_TIPOMOV,  "
        cQuery += "     SF3.F3_NFISCAL,  "
        cQuery += "     SFT.FT_ITEM,  "
        cQuery += "     SFT.FT_SERIE,  "
        cQuery += "     SFT.FT_CODISS,  "
        cQuery += "     SF3.F3_ESPECIE,  "
        cQuery += "     SFT.FT_RECISS,  "
        cQuery += "     SFT.FT_ESTADO,  "
        cQuery += "     SF3.F3_BASEICM,  "
        cQuery += "     SF3.F3_ISENICM,  "
        cQuery += "     SF3.F3_OUTRICM,  "
        cQuery += "     SFT.FT_VALCONT,  "
        cQuery += "     SF3.F3_VALCONT,  "
        cQuery += "     SFT.FT_ALIQICM,  "
        cQuery += "     SFT.FT_VALICM,  "
        cQuery += "     SF3.F3_ISSMAT,  "
        cQuery += "     SF3.F3_ISSSUB,  "
        cQuery += "     SFT.FT_TES,  "
        cQuery += "     SF3.F3_CFO,  "
        cQuery += "     SF3.F3_NRLIVRO,  "
        cQuery += "     SF3.F3_ISSST,  "
        cQuery += "     SFT.FT_OBSERV,  "
        cQuery += "     SA2.A2_MUN,  "
        cQuery += "     SA2.A2_COD_MUN,  "
        cQuery += "     SA2.A2_NOME,  "
        cQuery += "     SA2.A2_CGC,  "
        cQuery += "     SA1.A1_MUN,  "
        cQuery += "     SA1.A1_COD_MUN,  "
        cQuery += "     SA1.A1_NOME,  "
        cQuery += "     SA1.A1_CGC  "
        cQuery += " FROM "
        cQuery +=     RetSqlName( 'SF3' ) + " SF3 "
        cQuery += " INNER JOIN " + RetSqlName('SFT') + " SFT ON " + "SFT.FT_FILIAL = ? " // 1
        cQuery += "                                                  AND SFT.FT_TIPOMOV = (CASE WHEN SF3.F3_CFO > '5' THEN 'S' ELSE 'E' END) "
        cQuery += "                                                  AND SFT.FT_CLIEFOR = SF3.F3_CLIEFOR "
        cQuery += "                                                  AND SFT.FT_ENTRADA = SF3.F3_ENTRADA "
        cQuery += "                                                  AND SFT.FT_NFISCAL = SF3.F3_NFISCAL "
        cQuery += "                                                  AND SFT.FT_SERIE   = SF3.F3_SERIE "
        cQuery += "                                                  AND SFT.FT_ESPECIE = SF3.F3_ESPECIE "
        cQuery += "                                                  AND SFT.FT_IDENTF3 = SF3.F3_IDENTFT "
        cQuery += "                                                  AND SFT.D_E_L_E_T_ = ? " // 2
        cQuery += "                                                  AND SFT.FT_TIPO = ? " // 3
        cQuery += "                                                  AND SFT.FT_CODISS != ? " // 4
        cQuery += "     LEFT JOIN " +RetSqlName("SA2")+" SA2 ON " + "SA2.A2_FILIAL = ? " // 5
        cQuery += "                                                  AND SA2.A2_COD = SFT.FT_CLIEFOR "
        cQuery += "                                                  AND SA2.A2_LOJA =  SFT.FT_LOJA "
        cQuery += "                                                  AND SA2.D_E_L_E_T_ = ? " // 6
        cQuery += "     LEFT JOIN " +RetSqlName("SA1")+" SA1 ON " + "SA1.A1_FILIAL = ? " // 7
        cQuery += "                                                  AND SA1.A1_COD = SFT.FT_CLIEFOR "
        cQuery += "                                                  AND SA1.A1_LOJA =  SFT.FT_LOJA "
        cQuery += "                                                  AND SA1.D_E_L_E_T_ = ? " // 8
        cQuery += " WHERE "
        cQuery += " SF3.F3_FILIAL = ? " // 9
        cQuery += " AND SF3.F3_ENTRADA >= ? " // 10
        cQuery += " AND SF3.F3_ENTRADA <= ? " // 11
        cQuery += " AND SF3.D_E_L_E_T_ = ? " // 12 
        cQuery += " AND SF3.F3_DTCANC = ? "  // 13      
        
        If cNrLivro <> "*"
            cQuery += " AND SF3.F3_NRLIVRO = ? " //"AND SF3.F3_NRLIVRO='"+cNrLivro+"'"
        EndIf

        If !Empty(cUFMun)
            cQuery += " AND (SA2.A2_MUN = ? OR SA1.A1_MUN = ? )" //AND (SA2.A2_MUN = '"+cUFMun+"' OR SA1.A1_MUN = '"+cUFMun+"')"
        EndIf

        If !Empty(cCodMun)
            cQuery += " AND (SA2.A2_COD_MUN = ? OR SA1.A1_COD_MUN = ? )" //AND (SA2.A2_COD_MUN = '"+cCodMun+"' OR SA1.A1_COD_MUN = '"+cCodMun+"')"
        EndIf
      
         If oFilter:hasFilter()
            cQuery += " AND " + oFilter:getSQLExpression()
        EndIf

        oPrepare := FwExecStatement():New( ChangeQuery(cQuery) ) 

        //Percorre as filiais
        For nFilial := 1 to Len(aFiliais)

            Aadd( aBind, {'C', xFilial( 'SFT', aFiliais[nFilial] )}) // 1
            Aadd( aBind, {'C', ' ' } )                               // 2
            Aadd( aBind, {'C', 'S' } )                               // 3   
            Aadd( aBind, {'C', cCodISS } )                           // 4   
            Aadd( aBind, {'C', xFilial( 'SA2', aFiliais[nFilial] )}) // 5
            Aadd( aBind, {'C', ' ' } )                               // 6 
            Aadd( aBind, {'C', xFilial( 'SA1', aFiliais[nFilial] )}) // 7
            Aadd( aBind, {'C', ' ' } )                               // 8
            Aadd( aBind, {'C', xFilial( 'SF3', aFiliais[nFilial] )}) // 9
            Aadd( aBind, {'D', dEntrada })                           // 10
            Aadd( aBind, {'D', dSaida } )                            // 11
            Aadd( aBind, {'C', ' ' } )                               // 12
            Aadd( aBind ,{'C',Space(Len(Dtos(SF3->F3_DTCANC)))} )    // 13

            If cNrLivro <> "*"
                Aadd( aBind, {'C', cNrLivro })
            EndIf
            If !Empty(cUFMun)    
                Aadd( aBind, {'C', ALLTRIM(cUFMun) } )
                Aadd( aBind, {'C', ALLTRIM(cUFMun) })
            EndIf
            If !Empty(cCodMun)
                Aadd( aBind, {'C', ALLTRIM(cCodMun) })
                Aadd( aBind, {'C', ALLTRIM(cCodMun) })
            EndIf
            
            for nlA := 1 to len(aBind)
                If aBind[nlA][1] == 'C'
                    oPrepare:setString( nlA, aBind[nlA][2] )
                ElseIf aBind[nlA][1] == 'D'
                    oPrepare:setDate( nlA, aBind[nlA][2] )
                EndIf

            next nlA

            cAlias := oPrepare:OpenAlias() 
            
            while !(cAlias)->(Eof())

                    cA1DescMun := " "
                    cA1CodMun  := " "
                    cA1NomeCli := " "
                    cA1CGC  := " "
                    cA2DescMun := " "
                    cA2CodMun  := " "
                    cA2NomeFor := " "
                    cA2CGC  := " "

                    If( (cAlias)->FT_TIPOMOV == 'E' ) 
                        cA2DescMun := (cAlias)->A2_MUN
                        cA2CodMun  := (cAlias)->A2_COD_MUN
                        cA2NomeFor := (cAlias)->A2_NOME
                        cA2CGC  := (cAlias)->A2_CGC                        
                    Else
                        cA1DescMun := (cAlias)->A1_MUN
                        cA1CodMun  := (cAlias)->A1_COD_MUN
                        cA1NomeCli := (cAlias)->A1_NOME
                        cA1CGC  := (cAlias)->A1_CGC
                    EndIf

                    self:oData:appendData({ "FILIAL"    : (cAlias)->F3_FILIAL,;
                                            "DTENTRADA" : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->F3_ENTRADA)),;
                                            "TIPOMOV"   : (cAlias)->TIPOMOV,; 
                                            "TPMOV"     : (cAlias)->FT_TIPOMOV,; 
                                            "NFISCAL"   : (cAlias)->F3_NFISCAL,;
                                            "ITEM"      : (cAlias)->FT_ITEM,;
                                            "SERIE"     : (cAlias)->FT_SERIE,;
                                            "CODISS"    : (cAlias)->FT_CODISS,;
                                            "ESPECIE"   : (cAlias)->F3_ESPECIE,;
                                            "RECISS"    : (cAlias)->RECISS,;
                                            "ESTADO"    : (cAlias)->FT_ESTADO,;
                                            "BASEICM"   : (cAlias)->F3_BASEICM,;
                                            "ISENICM"   : (cAlias)->F3_ISENICM,;
                                            "OUTRICM"   : (cAlias)->F3_OUTRICM,;
                                            "VALITEM"   : (cAlias)->FT_VALCONT,;
                                            "VALNOTA"   : (cAlias)->F3_VALCONT,;
                                            "ALIQICM"   : (cAlias)->FT_ALIQICM,;
                                            "VALICM"    : (cAlias)->FT_VALICM,;
                                            "ISSMAT"    : (cAlias)->F3_ISSMAT,;
                                            "ISSSUB"    : (cAlias)->F3_ISSSUB,;
                                            "TES"       : (cAlias)->FT_TES,;
                                            "CFO"       : (cAlias)->F3_CFO,;
                                            "NRLIVRO"   : (cAlias)->F3_NRLIVRO,;
                                            "ISSST"     : (cAlias)->ISSST,;
                                            "OBSERV"    : (cAlias)->FT_OBSERV,;                                            
                                            "MUNFOR"    : cA2DescMun,;
                                            "CODMUNFOR" : cA2CodMun,;
                                            "NOMEFOR"   : cA2NomeFor,;
                                            "CGCFOR"    : cA2CGC,;
                                            "MUNCLI"    : cA1DescMun,;
                                            "CODMUNCLI" : cA1CodMun,;
                                            "NOMECLI"   : cA1NomeCli,;
                                            "CGCCLI"    : cA1CGC;
                                        })

                    (cAlias)->( dbSkip() )
            enddo
                (cAlias)->( DBCloseArea() )
                aSize(aBind,0)           
        Next nFilial
    endif
    
    freeobj(oFisTools)
    
    aBind := Nil
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author Juliana Mellão
@since 16/06/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class IssVerificationSmartViewBusinessObject

    //              ( Nome       , Descricao                   , Tipo    , Display                  , cRealName
    self:addProperty("FILIAL"    , "Filial"                    , "string", "Filial"                 , "F3_FILIAL"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("DTENTRADA" , "Data Entrada"              , "date"  , "Data Entrada"           , "F3_ENTRADA"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TIPOMOV"   , "Movimento"                 , "string", "Movimento"              , "TIPOMOV"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TPMOV"     , "TPMOV"                     , "string", "TPMOV"                  , "FT_TIPOMOV"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NFISCAL"   , "Nota Fiscal"               , "string", "Nota Fiscal"            , "F3_NFISCAL"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ITEM"      , "Item da nota"              , "string", "Item da NF"             , "FT_ITEM"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("SERIE"     , "Serie"                     , "string", "Serie"                  , "FT_SERIE"    , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODISS"    , "Codigo ISS"                , "string", "Codigo ISS"             , "FT_CODISS"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ESPECIE"   , "Especie Doc"               , "string", "Especie Doc"            , "F3_ESPECIE"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("RECISS"    , "Recolhe ISS"               , "string", "Recolhe ISS"            , "RECISS"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ESTADO"    , "Estado"                    , "string", "Estado"                 , "FT_ESTADO"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("BASEICM"   , "Base de Calculo"           , "number", "Base de calculo"        , "F3_BASEICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISENICM"   , "Isentos    "               , "number", "Isentos"                , "F3_ISENICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("OUTRICM"   , "Outros"                    , "number", "Outros"                 , "F3_OUTRICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALITEM"   , "Valor Item"                , "number", "Valor Item"             , "F3_VALCONT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALNOTA"   , "Valor NF"                  , "number", "Valor NF"               , "F3_VALCONT"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ALIQICM"   , "Aliquota"                  , "number", "Aliquota"               , "FT_ALIQICM"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("VALICM"    , "Imposto Debitado"          , "number", "Imposto debitado"       , "F3_VALICM"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSMAT"    , "Material Aplicado"         , "number", "Material Aplicado"      , "F3_ISSMAT"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSSUB"    , "Iss da Subempreitada"      , "number", "Iss Subempr"            , "F3_ISSSUB"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("TES"       , "TES"                       , "string", "TES "                   , "FT_TES"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CFO"       , "CFOP"                      , "string", "CFOP "                  , "F3_CFO"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NRLIVRO"   , "Num Livro"                 , "string", "Num Livro "             , "F3_NRLIVRO"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("ISSST"     , "Pgto Imposto"              , "string", "Pagamento Imposto"      , "ISSST"       , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("OBSERV"    , "Observacao"                , "string", "Observacao"             , "FT_OBSERV"   , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("MUNFOR"    , "Municipio Fornecedor"      , "string", "Municipio Forn"         , "A2_MUN"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODMUNFOR" , "Cod Municipio Fornecedor"  , "string", "Cod Mun Fornecedor"     , "A2_COD_MUN"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NOMEFOR"   , "Fornecedor"                , "string", "Fornecedor"             , "A2_NOME"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CGCFOR"    , "CNPJ/CPF Fornecedor"       , "string", "CNPJ/CPF Fornecedor"    , "A2_CGC"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("MUNCLI"    , "Municipio Cliente"         , "string", "Municipio"              , "A1_MUN"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CODMUNCLI" , "Cod Municipio Cliente"     , "string", "Cod Mun Cliente"        , "A1_COD_MUN"  , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("NOMECLI"   , "Cliente"                   , "string", "Cliente"                , "A1_NOME"     , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )
    self:addProperty("CGCCLI"    , "CNPJ/CPF Cliente"          , "string", "CNPJ/CPF Cliente"       , "A1_CGC"      , /*cComboValues*/ , /*lIsRequired*/, /*cRenameField*/ )

return self:oSchema
