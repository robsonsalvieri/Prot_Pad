#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdCF4.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="CF4", name="efdCF4SmartviewBusinessObject", country="BRA", initialRelease="12.1.2210")
//-------------------------------------------------------------------
/*{Protheus.doc}  efdCF4SmartviewBusinessObject
Classe para criação do Objeto Retenção na fonte PIS e COFINS
@author Raphael Ventura
@since 31/07/2023
*/
//-------------------------------------------------------------------  
class efdCF4SmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new() as object
	public method getData() as object
	public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author Raphael Ventura
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdCF4SmartviewBusinessObject
	Local oFisTools := fisSmartviewTools():new()

	_Super:new()
	self:appendArea(STR0001) //"Livros Fiscais"
	self:setDisplayName(STR0002) //"Devolucoes do Periodo Atual"
	self:setDescription(STR0003) //"Objeto de Negocio que lista as Devolucoes do Periodo Atual (CF4)"

	oFisTools:validPerg(self:setPergunte("FISTRR1013"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author Raphael Ventura
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdCF4SmartviewBusinessObject
	Local cQuery     := '' as Character
	Local cAlias     := '' as Character
	Local oFisTools  := fisSmartviewTools():new()
	Local jParams    := oFilter:getParameters() as Json
    Local dStartDate := FwDateTimeToLocal(jParams['MV_PAR03',1])[1] as Date
    Local dEndDate   := FwDateTimeToLocal(jParams['MV_PAR04',1])[1] as Date
	Local lOk        := .t. as Logical
	Local oPrepare   := nil as object
	// variaveis obrigatórias para execução do MontFilial
	Local cFilDe     := jParams['MV_PAR01',1] as Character
	Local cFilAte    := jParams['MV_PAR02',1] as Character
	Local aSM0Data	 := {} as array
	Local nPosDe 	 := 0  as numeric
    Local nPosAte 	 := 0  as numeric
	Local oFilAcum	 := nil as object 
	Local lRetFil    := oFisTools:montFilial(@cFilDe, @cFilAte, @nPosDe, @nPosAte, @aSM0Data) as logical
	Local aFilProc	 := {} as array
	Local aFilOri	 := {} as array

	if !lRetFil
		oFisTools:validFil(.f.)
		lOk := .f.
	endif

	if lOk
		oFilAcum := JsonObject():New()
		oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilProc,'CF4',@oFilAcum)
		oFisTools:FilterFil(nPosDe, nPosAte, aSM0Data, aFilOri,'SFT',@oFilAcum) // compatibilizado com o Legado fonte FISR101, função PrintCF4 
		//pois na teoria a informação que chega da filial vem da SFT

		cQuery := " SELECT "
		cQuery += " CF4.CF4_FILIAL, "
		cQuery += " CF4.CF4_NOTA, "
		cQuery += " CF4.CF4_SERIE, "
		cQuery += " CF4.CF4_ITEM, "
		cQuery += " CF4.CF4_CLIFOR, "
		cQuery += " CF4.CF4_LOJA, "
		cQuery += " CF4.CF4_TIPMOV, "
		cQuery += " CF4.CF4_VALPIS, "
		cQuery += " CF4.CF4_VALCOF, "
		cQuery += " CF4.CF4_DATAE, "
		cQuery += " CF4.CF4_CFOP, "
		cQuery += " CF4.CF4_CSTPIS, "
		cQuery += " CF4.CF4_CSTCOF, "
		cQuery += " CF4.CF4_CONPIS, "
		cQuery += " CF4.CF4_CONCOF, "
		cQuery += " CF4.CF4_PATPIS, "
		cQuery += " CF4.CF4_PATCOF, "
		cQuery += " CF4.CF4_ORIPIS, "
		cQuery += " CF4.CF4_ORICOF, "
		cQuery += " CF4.CF4_DTALT, "
		cQuery += " CF4.CF4_QUANT, "
		cQuery += " CF4.CF4_TIPO, "
		cQuery += " CF4.CF4_FLORIG, "
		cQuery += " CF4.CF4_SDOC "
		cQuery += " FROM "
		cQuery +=   RetSQLName("CF4") + " CF4 "
		cQuery += " WHERE CF4.CF4_FILIAL IN ( ? ) "
		cQuery += " AND CF4.CF4_FLORIG IN ( ? ) "
		cQuery += " AND CF4.CF4_DATAE BETWEEN ? AND ? "
		cQuery += " AND CF4.D_E_L_E_T_ = ? "		

		oPrepare := FWExecStatement():New(ChangeQuery(cQuery))

		oPrepare:setIn(1,aFilProc)
		oPrepare:setIn(2,aFilOri)
		oPrepare:setDate(3,dStartDate )	
		oPrepare:setDate(4,dEndDate)	
		oPrepare:setString(5,' ')	

		cAlias:= oPrepare:OpenAlias()

		while !(cAlias)->(Eof())		

			self:oData:appendData({;
				"CF4_FILIAL"    : (cAlias)->CF4_FILIAL,;
				"CF4_NOTA"      : (cAlias)->CF4_NOTA,;
				"CF4_SERIE"     : (cAlias)->CF4_SERIE,;
				"CF4_ITEM"      : (cAlias)->CF4_ITEM,;
				"CF4_CLIFOR"    : (cAlias)->CF4_CLIFOR,;
				"CF4_LOJA"      : (cAlias)->CF4_LOJA,;
				"CF4_TIPMOV"    : (cAlias)->CF4_TIPMOV,;
				"CF4_VALPIS"    : (cAlias)->CF4_VALPIS,;
				"CF4_VALCOF"    : (cAlias)->CF4_VALCOF,;
				"CF4_DATAE"     : totvs.framework.treports.date.stringToTimeStamp((cAlias)->CF4_DATAE),;
				"CF4_CFOP"      : (cAlias)->CF4_CFOP,;
				"CF4_CSTPIS"    : (cAlias)->CF4_CSTPIS,;
				"CF4_CSTCOF"    : (cAlias)->CF4_CSTCOF,;
				"CF4_CONPIS"    : (cAlias)->CF4_CONPIS,;
				"CF4_CONCOF"    : (cAlias)->CF4_CONCOF,;
				"CF4_PATPIS"    : (cAlias)->CF4_PATPIS,;
				"CF4_PATCOF"    : (cAlias)->CF4_PATCOF,;
				"CF4_ORIPIS"    : (cAlias)->CF4_ORIPIS,;
				"CF4_ORICOF"    : (cAlias)->CF4_ORICOF,;
				"CF4_DTALT"     : substr((cAlias)->CF4_DTALT,1,2)+"/"+substr((cAlias)->CF4_DTALT,3,6),;
				"CF4_QUANT"     : (cAlias)->CF4_QUANT,;
				"CF4_TIPO"      : (cAlias)->CF4_TIPO,;
				"CF4_FLORIG"    : (cAlias)->CF4_FLORIG,;
				"CF4_SDOC"      : (cAlias)->CF4_SDOC;
				})

			(cAlias)->(dbSkip())

		enddo

		(cAlias)->(DBCloseArea())
	endif

	freeobj(oFisTools)
	freeObj(oPrepare)
	freeobj(jParams)
	freeobj(oFilAcum)
	aSize(aFilOri,0)
	aSize(aFilProc,0)
	aFilOri  := Nil
	aFilProc := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author Raphael Ventura
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdCF4SmartviewBusinessObject
	self:addProperty("CF4_FILIAL"   , "Filial"              , "string"  , "Filial"      , "CF4_FILIAL"  )
	self:addProperty("CF4_NOTA"     , "Nota Fiscal"         , "string"  , "Nt. Fiscal"  , "CF4_NOTA"    )
	self:addProperty("CF4_SERIE"    , "Serie"               , "string"  , "Serie"       , "CF4_SERIE"   )
	self:addProperty("CF4_ITEM"     , "Item"                , "string"  , "Item"        , "CF4_ITEM"    )
	self:addProperty("CF4_CLIFOR"   , "Participante"        , "string"  , "Particip."   , "CF4_CLIFOR"  )
	self:addProperty("CF4_LOJA"     , "Loja"                , "string"  , "Loja"        , "CF4_LOJA"    )
	self:addProperty("CF4_TIPMOV"   , "Tipo do Movimento"   , "string"  , "Tipo Mov."   , "CF4_TIPMOV"  )
	self:addProperty("CF4_VALPIS"   , "Valor PIS"           , "number"  , "Val. PIS"    , "CF4_VALPIS"  )
	self:addProperty("CF4_VALCOF"   , "Valor COF"           , "number"  , "Val. COF"    , "CF4_VALCOF"  )
	self:addProperty("CF4_DATAE"    , "Data de Entrada"     , "date"    , "Dt. Entrada" , "CF4_DATAE"   )
	self:addProperty("CF4_CFOP"     , "CFOP"                , "string"  , "CFOP"        , "CF4_CFOP"    )
	self:addProperty("CF4_CSTPIS"   , "CST PIS"             , "string"  , "CST PIS"     , "CF4_CSTPIS"  )
	self:addProperty("CF4_CSTCOF"   , "CST COF"             , "string"  , "CST COF"     , "CF4_CSTCOF"  )
	self:addProperty("CF4_CONPIS"   , "Codigo PIS"          , "string"  , "Cod. PIS"    , "CF4_CONPIS"  )
	self:addProperty("CF4_CONCOF"   , "Codigo COF"          , "string"  , "Cod. COF"    , "CF4_CONCOF"  )
	self:addProperty("CF4_PATPIS"   , "PIS Pauta"           , "number"  , "PIS Pauta"   , "CF4_PATPIS"  )
	self:addProperty("CF4_PATCOF"   , "COFINS Pauta"        , "number"  , "COF Pauta "  , "CF4_PATCOF"  )
	self:addProperty("CF4_ORIPIS"   , "Original PIS"        , "number"  , "Orig. Pauta" , "CF4_ORIPIS"  )
	self:addProperty("CF4_ORICOF"   , "Original COFINS"     , "number"  , "Orig. COF "  , "CF4_ORICOF"  )
	self:addProperty("CF4_DTALT"    , "Mês/Ano Alteração"   , "string"  , "Mês/Ano Alt.", "CF4_DTALT"   )
	self:addProperty("CF4_QUANT"    , "Quantidade"          , "number"  , "Quant."      , "CF4_QUANT"   )
	self:addProperty("CF4_TIPO"     , "Tipo Operação"       , "string"  , "Tipo Oper."  , "CF4_TIPO"    )
	self:addProperty("CF4_FLORIG"   , "Filial Origem"       , "string"  , "Filial Orig.", "CF4_FLORIG"  )
	self:addProperty("CF4_SDOC"     , "Serie Origem"        , "string"  , "Serie Orig." , "CF4_SDOC"    )
return self:oSchema
