#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "fiscal.sv.fis.efdSFTAgru.ch"

namespace totvs.protheus.fiscal.fis.treportsintegratedprovider
@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGAFIS", tables="SFT,SA1,SB1,SF4",;
    name="efdSFTAgruSmartviewBusinessObject", country="BRA", initialRelease="12.1.2310") 
//-------------------------------------------------------------------
/*{Protheus.doc} efdSFTAgruSmartviewBusinessObject
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------  

class efdSFTAgruSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    data nPageSize as integer

    public method new() as object
    public method getData() as object
    public method getSchema() as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self 
@author eduardo.melo
@since 31/07/2023
*/
//-------------------------------------------------------------------   
method new() class efdSFTAgruSmartviewBusinessObject

    Local oFisTools := fisSmartviewTools():new()

    _Super:new()
    //Define Area
    self:appendArea(STR0001) //"Livros Fiscais"
    //Define o nome do objeto de Negócio
    self:setDisplayName(STR0002) //"Objeto de Negócio EFD Contribuiçoes - SFT"
        //Define a descrição do objeto de Negócio
    self:setDescription(STR0003) //"Objeto de negócio que fornece o detalhamento do EFD Contribuições (SFT)"
    self:setIsCBoxLookup(.T., .F.)

	self:nPageSize := oFisTools:Paginacao(500)
	oFisTools:validPerg(::setPergunte("FISTRR1021"))
	freeobj(oFisTools)

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports 
@return object: self:oData 
@author eduardo.melo
@since 27/10/2023
*/
//-------------------------------------------------------------------   
method getData(nPage as numeric, oFilter as object) as object class efdSFTAgruSmartviewBusinessObject

	Local cQuery     as Character
    Local cAlias     as Character
	Local oFisTools  := fisSmartviewTools():new()
	Local oFiltQry   := oFilter:getParameters() as Json
    Local dStartDate := FwDateTimeToLocal(oFiltQry['MV_PAR01',1])[1] as date
    Local dEndDate   := FwDateTimeToLocal(oFiltQry['MV_PAR02',1])[1] as date
	Local cFilDe     := oFiltQry['MV_PAR03',1] as Character
	Local cFilAte    := oFiltQry['MV_PAR04',1] as Character
	Local lOk		 := .T. as Logical
    Local aFiliais   := {} as array
    Local cFils      := oFisTools:montaFilial(cFilDe, cFilAte, aFiliais) as character
    local aBind      := {}  as array
    local nlA        := 0   as numeric
    Local oPrepare   := nil as object
    Local cTpMovEnt  := 'E' as Character
    Local cTpMovSai  := 'S' as Character
    Local cAtuatf    := 'S' as Character
    Local nBase      := 0 as numeric
    Local aCSTE      := {}  as array
    Local aCSTS      := {}  as array
    Local aCodRet    := {}  as array
    Local cObserv    := "" as Character
    Local cFilDT6    := "" as Character
    Local cSeekDT6   := "" as Character
    Local cFilSF4    := "" as Character
    Local cFilSB1    := "" as Character
	
    Private lProcRH  as Logical

    aCSTE := {"70", "71", "72", "73", "74", "98", "99"}
    aCSTS := {"01", "05", "07", "08", "09", "49"}
    aCodRet := {'110', '204', '205', '301', '302', '303', '304', '305', '306', '102'}
	
	if cFils == 'Erro'
		oFisTools:validFil(.f.)
		lOk := .f.
	endif
    
    If lOk

		//definindo a quantidade máxima por página (default 100)
		self:setPageSize(self:nPageSize)

		lProcRH := GetNewPar("MV_SPCBPRH",.F.)

        cFilSF4 := xFilial("SF4")
        cFilSB1 := xFilial("SB1")

        cQuery := "		 SELECT SF4.F4_TPREG, "
        cQuery += "             SFT.FT_FILIAL,"
        cQuery += "             SFT.FT_NFISCAL,"
        cQuery += "             SFT.FT_SERIE,"
        cQuery += "             SFT.FT_TIPOMOV,"
        cQuery += "             SFT.FT_ENTRADA,"
        cQuery += "             SFT.FT_CFOP,"
        cQuery += "             SFT.FT_ESPECIE,"
        cQuery += "             SFT.FT_ALIQPIS,"
        cQuery += "             SFT.FT_ALIQCOF,"
        cQuery += "             SFT.FT_ALIQCF3,"
        cQuery += "             SFT.FT_ALIQPS3,"
        cQuery += "             SFT.FT_CSTPIS,"
        cQuery += "             SFT.FT_CSTCOF,"
        cQuery += "             SFT.FT_PAUTPIS,"
        cQuery += "             SFT.FT_PAUTCOF,"
        cQuery += "             SFT.FT_TNATREC,"
        cQuery += "             SFT.FT_CNATREC,"
        cQuery += "             SFT.FT_GRUPONC,"
        cQuery += "             SFT.FT_DTFIMNT,"
        cQuery += "             SFT.FT_CODBCC,"
        cQuery += "             SFT.FT_CONTA,"
        cQuery += "             SFT.FT_MALQCOF,"
        cQuery += "             SFT.FT_MALQPIS,"
        cQuery += "             SFT.FT_TIPO ,"
        cQuery += "             SFT.FT_CREDST,"
        cQuery += "             SFT.FT_ESTADO,"
        cQuery += "             SFT.FT_SERSAT,"
        cQuery += "             SFT.FT_ALIQCPB,"
        cQuery += "             SFT.FT_ATIVCPB,"
        cQuery += "             SFT.FT_TES,"
        cQuery += "             SFT.FT_OBSERV,"
        cQuery += "             SFT.FT_NFORI,"
        cQuery += "             SFT.FT_SERORI,"
        cQuery += "             SUM(SFT.FT_VALPIS) AS FT_VALPIS,"
        cQuery += "             SUM(SFT.FT_VALCOF) AS FT_VALCOF,"
        cQuery += "             SUM(SFT.FT_VALCONT) AS FT_VALCONT,"
        cQuery += "             SUM(SFT.FT_TOTAL) AS FT_TOTAL,"
        cQuery += "             SUM(SFT.FT_BASEPIS) AS FT_BASEPIS,"
        cQuery += "             SUM(SFT.FT_BASECOF) AS FT_BASECOF,"
        cQuery += "             SUM(SFT.FT_BASECF3) AS FT_BASECF3,"
        cQuery += "             SUM(SFT.FT_BASEPS3) AS FT_BASEPS3,"
        cQuery += "             SUM(SFT.FT_VALPS3) AS FT_VALPS3,"
        cQuery += "             SUM(SFT.FT_VALCF3) AS FT_VALCF3,"
        cQuery += "             SUM(SFT.FT_MVALCOF) AS FT_MVALCOF,"
        cQuery += "             SUM(SFT.FT_MVALPIS) AS FT_MVALPIS,"
        cQuery += "             SUM(SFT.FT_ICMSRET) AS FT_ICMSRET,"
        cQuery += "             SUM(SFT.FT_SOLTRIB) AS FT_SOLTRIB,"
        cQuery += "             SUM(SFT.FT_ISENRET) AS FT_ISENRET,"
        cQuery += "             SUM(SFT.FT_OBSSOL) AS FT_OBSSOL,"
        cQuery += "             SUM(SFT.FT_OUTRRET) AS FT_OUTRRET,"
        cQuery += "             SUM(SFT.FT_QUANT) AS FT_QUANT,"
        cQuery += "             SUM(SFT.FT_DESCONT) AS FT_DESCONT,"
        cQuery += "             SUM(SFT.FT_DESCZFR) AS FT_DESCZFR ,"
        cQuery += "             SUM(SFT.FT_BASECPB) AS FT_BASECPB,"
        cQuery += "             SUM(SFT.FT_VALCPB) AS FT_VALCPB,"
        cQuery += "             SUM(SFT.FT_VALIPI) AS FT_VALIPI "
        cQuery += "			FROM "+RetSqlName("SFT")+" SFT "
        cQuery += "			LEFT JOIN "+RetSqlName("SD1")+" SD1 ON ( "        
        cQuery += "					SD1.D1_FILIAL = SFT.FT_FILIAL"
        cQuery += "					AND SD1.D1_DOC = SFT.FT_NFISCAL"
        cQuery += "					AND SD1.D1_SERIE = SFT.FT_SERIE"
        cQuery += "					AND SD1.D1_FORNECE = SFT.FT_CLIEFOR"
        cQuery += "					AND SD1.D1_LOJA = SFT.FT_LOJA"
        cQuery += "					AND SD1.D1_COD = SFT.FT_PRODUTO"
        cQuery += "					AND SD1.D1_ITEM = SFT.FT_ITEM"
        cQuery += "					AND SD1.D_E_L_E_T_ = ? )"
                                    aadd( aBind , ' ' )  
        cQuery += "			LEFT JOIN "+RetSqlName("SF4")+" SF4 ON ( "        
        cQuery += "					SF4.F4_FILIAL = ? "
                                    aadd( aBind , cFilSF4 )
        cQuery += "					AND SF4.F4_CODIGO = SD1.D1_TES"
        cQuery += "					AND SF4.D_E_L_E_T_ = ? )"
                                    aadd( aBind , ' ' )  
        cQuery += "         LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ( "        
        cQuery += "					SB1.B1_FILIAL = ? "
                                    aadd( aBind , cFilSB1 )
        cQuery += "                 AND SB1.B1_COD = SFT.FT_PRODUTO "
        cQuery += "                 AND SB1.D_E_L_E_T_ = ?) "
                                    aadd( aBind , ' ' )
        cQuery += "			WHERE "
        cQuery += "      		SFT.FT_FILIAL IN ( ? ) " //SFT.FT_FILIAL IN (" + cFils + ")
                                aadd( aBind , aFiliais )   
        cQuery += "				AND SFT.FT_TIPOMOV = ? " //AND SFT.FT_TIPOMOV = 'E' 
                                aadd( aBind , cTpMovEnt )   
        cQuery += "				AND SFT.FT_ENTRADA >= ? " //AND SFT.FT_ENTRADA >= "+ valToSql(dStartDate) 
        		                aadd( aBind , dStartDate )
        cQuery += "				AND SFT.FT_ENTRADA <= ? " //AND SFT.FT_ENTRADA <= "+ valToSql(dEndDate) 
                                aadd( aBind , dEndDate )  
        cQuery += "				AND ( (SF4.F4_ATUATF <> ? " //AND ( (SF4.F4_ATUATF <> 'S'"
                                aadd( aBind , cAtuatf )  
        cQuery += "					AND (((SFT.FT_BASEPIS > ? " //AND (((SFT.FT_BASEPIS > 0
                                    aadd( aBind , nBase )  
        cQuery += "						OR SFT.FT_BASEPS3 > ? )" //AND (((SFT.FT_BASEPS3 > 0
                                        aadd( aBind , nBase )          
        cQuery += "					OR SFT.FT_CSTPIS IN ( ? ))" //OR SFT.FT_CSTPIS IN ('70', '71', '72', '73', '74', '98', '99'))
                                    aadd( aBind , aCSTE )
        cQuery += "						OR ((SFT.FT_BASECOF > ? " //OR ((SFT.FT_BASECOF > 0
                                        aadd( aBind , nBase ) 
        cQuery += "							OR SFT.FT_BASECF3 > ? )" //OR SFT.FT_BASECF3 > 0
                                            aadd( aBind , nBase ) 
        cQuery += "						OR SFT.FT_CSTCOF IN ( ? ))))" //OR SFT.FT_CSTCOF IN ('70', '71', '72', '73', '74', '98', '99'))))
                                        aadd( aBind , aCSTE )         
        cQuery += "					OR (SF4.F4_ATUATF = ?" //OR (SF4.F4_ATUATF = 'S'
                                    aadd( aBind , cAtuatf )  
        cQuery += "						AND ((SFT.FT_CSTCOF IN ( ? ))" //AND ((SFT.FT_CSTCOF IN ('70', '71', '72', '73', '74', '98', '99'))
                                        aadd( aBind , aCSTE )        
        cQuery += "							OR (SFT.FT_CSTPIS IN ( ? )))) )" //OR (SFT.FT_CSTPIS IN ('70', '71', '72', '73', '74', '98', '99')))) )
                                            aadd( aBind , aCSTE )        
        cQuery += "				AND SFT.D_E_L_E_T_ = ? "
                                aadd( aBind , ' ' )
        cQuery += "	        GROUP BY "
        cQuery += "             SF4.F4_TPREG, "
        cQuery += "             SFT.FT_FILIAL,"
        cQuery += "             SFT.FT_NFISCAL,"
        cQuery += "             SFT.FT_SERIE,"
        cQuery += "             SFT.FT_TIPOMOV,"
        cQuery += "             SFT.FT_ENTRADA,"
        cQuery += "             SFT.FT_CFOP,"
        cQuery += "             SFT.FT_ESPECIE,"
        cQuery += "             SFT.FT_ALIQPIS,"
        cQuery += "             SFT.FT_ALIQCOF,"
        cQuery += "             SFT.FT_ALIQCF3,"
        cQuery += "             SFT.FT_ALIQPS3,"
        cQuery += "             SFT.FT_CSTPIS,"
        cQuery += "             SFT.FT_CSTCOF,"
        cQuery += "             SFT.FT_PAUTPIS,"
        cQuery += "             SFT.FT_PAUTCOF,"
        cQuery += "             SFT.FT_TNATREC,"
        cQuery += "             SFT.FT_CNATREC,"
        cQuery += "             SFT.FT_GRUPONC,"
        cQuery += "             SFT.FT_DTFIMNT,"
        cQuery += "             SFT.FT_CODBCC,"
        cQuery += "             SFT.FT_CONTA,"
        cQuery += "             SFT.FT_MALQCOF,"
        cQuery += "             SFT.FT_MALQPIS,"
        cQuery += "             SFT.FT_TIPO ,"
        cQuery += "             SFT.FT_CREDST,"
        cQuery += "             SFT.FT_ESTADO,"
        cQuery += "             SFT.FT_SERSAT,"
        cQuery += "             SFT.FT_ALIQCPB,"
        cQuery += "             SFT.FT_ATIVCPB,"
        cQuery += "             SFT.FT_TES,"
        cQuery += "             SFT.FT_OBSERV,"
        cQuery += "             SFT.FT_NFORI,"
        cQuery += "             SFT.FT_SERORI"
        cQuery += "			UNION ALL		"
        cQuery += "			"
        cQuery += "			SELECT SF4.F4_TPREG, "
        cQuery += "             SFT.FT_FILIAL,"
        cQuery += "             SFT.FT_NFISCAL,"
        cQuery += "             SFT.FT_SERIE,"
        cQuery += "             SFT.FT_TIPOMOV,"
        cQuery += "             SFT.FT_ENTRADA,"
        cQuery += "             SFT.FT_CFOP,"
        cQuery += "             SFT.FT_ESPECIE,"
        cQuery += "             SFT.FT_ALIQPIS,"
        cQuery += "             SFT.FT_ALIQCOF,"
        cQuery += "             SFT.FT_ALIQCF3,"
        cQuery += "             SFT.FT_ALIQPS3,"
        cQuery += "             SFT.FT_CSTPIS,"
        cQuery += "             SFT.FT_CSTCOF,"
        cQuery += "             SFT.FT_PAUTPIS,"
        cQuery += "             SFT.FT_PAUTCOF,"
        cQuery += "             SFT.FT_TNATREC,"
        cQuery += "             SFT.FT_CNATREC,"
        cQuery += "             SFT.FT_GRUPONC,"
        cQuery += "             SFT.FT_DTFIMNT,"
        cQuery += "             SFT.FT_CODBCC,"
        cQuery += "             SFT.FT_CONTA,"
        cQuery += "             SFT.FT_MALQCOF,"
        cQuery += "             SFT.FT_MALQPIS,"
        cQuery += "             SFT.FT_TIPO ,"
        cQuery += "             SFT.FT_CREDST,"
        cQuery += "             SFT.FT_ESTADO,"
        cQuery += "             SFT.FT_SERSAT,"
        cQuery += "             SFT.FT_ALIQCPB,"
        cQuery += "             SFT.FT_ATIVCPB,"
        cQuery += "             SFT.FT_TES,"
        cQuery += "             SFT.FT_OBSERV,"
        cQuery += "             SFT.FT_NFORI,"
        cQuery += "             SFT.FT_SERORI,"
        cQuery += "             SUM(SFT.FT_VALPIS) AS FT_VALPIS,"
        cQuery += "             SUM(SFT.FT_VALCOF) AS FT_VALCOF,"
        cQuery += "             SUM(SFT.FT_VALCONT) AS FT_VALCONT,"
        cQuery += "             SUM(SFT.FT_TOTAL) AS FT_TOTAL,"
        cQuery += "             SUM(SFT.FT_BASEPIS) AS FT_BASEPIS,"
        cQuery += "             SUM(SFT.FT_BASECOF) AS FT_BASECOF,"
        cQuery += "             SUM(SFT.FT_BASECF3) AS FT_BASECF3,"
        cQuery += "             SUM(SFT.FT_BASEPS3) AS FT_BASEPS3,"
        cQuery += "             SUM(SFT.FT_VALPS3) AS FT_VALPS3,"
        cQuery += "             SUM(SFT.FT_VALCF3) AS FT_VALCF3,"
        cQuery += "             SUM(SFT.FT_MVALCOF) AS FT_MVALCOF,"
        cQuery += "             SUM(SFT.FT_MVALPIS) AS FT_MVALPIS,"
        cQuery += "             SUM(SFT.FT_ICMSRET) AS FT_ICMSRET,"
        cQuery += "             SUM(SFT.FT_SOLTRIB) AS FT_SOLTRIB,"
        cQuery += "             SUM(SFT.FT_ISENRET) AS FT_ISENRET,"
        cQuery += "             SUM(SFT.FT_OBSSOL) AS FT_OBSSOL,"
        cQuery += "             SUM(SFT.FT_OUTRRET) AS FT_OUTRRET,"
        cQuery += "             SUM(SFT.FT_QUANT) AS FT_QUANT,"
        cQuery += "             SUM(SFT.FT_DESCONT) AS FT_DESCONT,"
        cQuery += "             SUM(SFT.FT_DESCZFR) AS FT_DESCZFR ,"
        cQuery += "             SUM(SFT.FT_BASECPB) AS FT_BASECPB,"
        cQuery += "             SUM(SFT.FT_VALCPB) AS FT_VALCPB,"
        cQuery += "             SUM(SFT.FT_VALIPI) AS FT_VALIPI "
        cQuery += "			FROM "+RetSqlName("SFT")+" SFT"
        cQuery += "			INNER JOIN "+RetSqlName("SF3")+" SF3 ON ( "
        cQuery += "					SF3.F3_FILIAL = SFT.FT_FILIAL"
        cQuery += "					AND SF3.F3_SERIE = SFT.FT_SERIE"
        cQuery += "					AND SF3.F3_NFISCAL = SFT.FT_NFISCAL"
        cQuery += "					AND SF3.F3_CLIEFOR = SFT.FT_CLIEFOR"
        cQuery += "					AND SF3.F3_LOJA = SFT.FT_LOJA"
        cQuery += "					AND SF3.F3_IDENTFT = SFT.FT_IDENTF3"
        cQuery += "					AND SF3.D_E_L_E_T_ = ? "
                                    aadd( aBind , ' ' )
        cQuery += "					AND SF3.F3_CODRSEF NOT IN ( ? )"
                                    aadd( aBind , aCodRet )
        cQuery += "						AND SF3.F3_CFO = SFT.FT_CFOP)"
        cQuery += "			LEFT JOIN "+RetSqlName("SF4")+" SF4 ON ( "        
        cQuery += "					SF4.F4_FILIAL = ? "
                                    aadd( aBind , cFilSF4 )
        cQuery += "					AND SF4.F4_CODIGO = SFT.FT_TES"
        cQuery += "					AND SF4.D_E_L_E_T_ = ? )"
                                    aadd( aBind , ' ' )
        cQuery += "         LEFT JOIN "+RetSqlName("SB1")+" SB1 ON ( "        
        cQuery += "					SB1.B1_FILIAL = ? "
                                    aadd( aBind , cFilSB1 )
        cQuery += "                 AND SB1.B1_COD = SFT.FT_PRODUTO "
        cQuery += "                 AND SB1.D_E_L_E_T_ = ? ) "
                                    aadd( aBind , ' ' )
        cQuery += "			WHERE "
        cQuery += "      		SFT.FT_FILIAL IN ( ? ) "  //SFT.FT_FILIAL IN (" + cFils + ") 
                                aadd( aBind , aFiliais )  
        cQuery += "				AND SFT.FT_TIPOMOV = ? " //AND SFT.FT_TIPOMOV = 'S'
                                aadd( aBind , cTpMovSai ) 
        cQuery += "				AND SFT.FT_ENTRADA >= ? " //AND SFT.FT_ENTRADA >= "+ valToSql(dStartDate) 
        		                aadd( aBind , dStartDate )
        cQuery += "				AND SFT.FT_ENTRADA <= ? " //AND SFT.FT_ENTRADA <= "+ valToSql(dEndDate) 
                                aadd( aBind , dEndDate )  
        cQuery += "				AND (( (SFT.FT_BASEPIS > ? " //AND (( (SFT.FT_BASEPIS > 0
                                aadd( aBind , nBase ) 
        cQuery += "					OR SFT.FT_BASEPS3 > ? ) " //OR SFT.FT_BASEPS3 > 0 ) 
                                    aadd( aBind , nBase )
        cQuery += "				OR SFT.FT_CSTPIS IN ( ? )) " //OR SFT.FT_CSTPIS IN ('01', '05', '07', '08', '09', '49'))
                                Aadd( aBind , aCSTS ) 
        cQuery += "					OR ( (SFT.FT_BASECOF > ? " //OR ( (SFT.FT_BASECOF > 0
                                    aadd( aBind , nBase )       
        cQuery += "						OR SFT.FT_BASECF3 > ? ) " //R SFT.FT_BASECF3 > 0 )
                                        aadd( aBind , nBase ) 
        cQuery += "					OR SFT.FT_CSTCOF IN ( ? ))) " //OR SFT.FT_CSTCOF IN ('01', '05', '07', '08', '09', '49')))
                                    Aadd( aBind , aCSTS ) 
        cQuery += "				AND SFT.D_E_L_E_T_ = ? "
                                aadd( aBind , ' ' )
        cQuery += "	        GROUP BY "
        cQuery += "             SF4.F4_TPREG, "
        cQuery += "             SFT.FT_FILIAL,"
        cQuery += "             SFT.FT_NFISCAL,"
        cQuery += "             SFT.FT_SERIE,"
        cQuery += "             SFT.FT_TIPOMOV,"
        cQuery += "             SFT.FT_ENTRADA,"
        cQuery += "             SFT.FT_CFOP,"
        cQuery += "             SFT.FT_ESPECIE,"
        cQuery += "             SFT.FT_ALIQPIS,"
        cQuery += "             SFT.FT_ALIQCOF,"
        cQuery += "             SFT.FT_ALIQCF3,"
        cQuery += "             SFT.FT_ALIQPS3,"
        cQuery += "             SFT.FT_CSTPIS,"
        cQuery += "             SFT.FT_CSTCOF,"
        cQuery += "             SFT.FT_PAUTPIS,"
        cQuery += "             SFT.FT_PAUTCOF,"
        cQuery += "             SFT.FT_TNATREC,"
        cQuery += "             SFT.FT_CNATREC,"
        cQuery += "             SFT.FT_GRUPONC,"
        cQuery += "             SFT.FT_DTFIMNT,"
        cQuery += "             SFT.FT_CODBCC,"
        cQuery += "             SFT.FT_CONTA,"
        cQuery += "             SFT.FT_MALQCOF,"
        cQuery += "             SFT.FT_MALQPIS,"
        cQuery += "             SFT.FT_TIPO ,"
        cQuery += "             SFT.FT_CREDST,"
        cQuery += "             SFT.FT_ESTADO,"
        cQuery += "             SFT.FT_SERSAT,"
        cQuery += "             SFT.FT_ALIQCPB,"
        cQuery += "             SFT.FT_ATIVCPB,"
        cQuery += "             SFT.FT_TES,"
        cQuery += "             SFT.FT_OBSERV,"
        cQuery += "             SFT.FT_NFORI,"
        cQuery += "             SFT.FT_SERORI"
        cQuery += "             ORDER BY FT_FILIAL "
        
        oPrepare := FWExecStatement():New( ChangeQuery(cQuery) )
        for nlA := 1 to len(aBind)
            if Valtype(aBind[nlA]) == 'A'
                oPrepare:setIn( nlA, aBind[nlA] )
            elseif Valtype(aBind[nlA]) == 'D'
                oPrepare:setDate( nlA, aBind[nlA] )
            elseif Valtype(aBind[nlA]) == 'N'
                oPrepare:setNumeric( nlA, aBind[nlA] )
            else
                oPrepare:setString( nlA, aBind[nlA] )
            endif
        next nlA
       
        cAlias := oPrepare:OpenAlias()

        cFilDT6 := xFilial("DT6")
        DT6->(DbSetOrder(1)) // DT6_FILIAL, DT6_FILDOC, DT6_DOC, DT6_SERIE

        while !(cAlias)->(Eof())
            cSeekDT6 := cFilDT6+(cAlias)->FT_FILIAL+(cAlias)->FT_NFISCAL+(cAlias)->FT_SERIE

			cObserv := vldMsgObserv((cAlias)->FT_OBSERV, (cAlias)->FT_TIPOMOV, Alltrim((cAlias)->FT_ESPECIE), (cAlias)->FT_NFORI, cSeekDT6)

            self:oData:appendData({;
                "F4_TPREG"      : (cAlias)->F4_TPREG,;
                "FT_FILIAL"     : (cAlias)->FT_FILIAL,;
                "FT_NFISCAL"    : (cAlias)->FT_NFISCAL,;
                "FT_ENTRADA"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->FT_ENTRADA)),;
                "FT_ESTADO"     : (cAlias)->FT_ESTADO,;
                "FT_CFOP"       : (cAlias)->FT_CFOP,;
                "FT_VALCONT"    : (cAlias)->FT_VALCONT,;
                "FT_VALIPI"     : (cAlias)->FT_VALIPI,;
                "FT_ICMSRET"    : (cAlias)->FT_ICMSRET,;
                "FT_TIPO"       : (cAlias)->FT_TIPO,;
                "FT_OBSSOL"     : (cAlias)->FT_OBSSOL,;
                "FT_SOLTRIB"    : (cAlias)->FT_SOLTRIB,;
                "FT_ESPECIE"    : (cAlias)->FT_ESPECIE,;
                "FT_CREDST"     : (cAlias)->FT_CREDST,;
                "FT_CONTA"      : (cAlias)->FT_CONTA,;
                "FT_ISENRET"    : (cAlias)->FT_ISENRET,;
                "FT_OUTRRET"    : (cAlias)->FT_OUTRRET,;
                "FT_DESCONT"    : (cAlias)->FT_DESCONT,;
                "FT_TOTAL"      : (cAlias)->FT_TOTAL,;
                "FT_BASEPIS"    : (cAlias)->FT_BASEPIS,;
                "FT_ALIQPIS"    : (cAlias)->FT_ALIQPIS,;
                "FT_VALPIS"     : (cAlias)->FT_VALPIS,;
                "FT_BASECOF"    : (cAlias)->FT_BASECOF,;
                "FT_ALIQCOF"    : (cAlias)->FT_ALIQCOF,;
                "FT_VALCOF"     : (cAlias)->FT_VALCOF,;
                "FT_BASEPS3"    : (cAlias)->FT_BASEPS3,;
                "FT_ALIQPS3"    : (cAlias)->FT_ALIQPS3,;
                "FT_VALPS3"     : (cAlias)->FT_VALPS3,;
                "FT_BASECF3"    : (cAlias)->FT_BASECF3,;
                "FT_ALIQCF3"    : (cAlias)->FT_ALIQCF3,;
                "FT_VALCF3"     : (cAlias)->FT_VALCF3,;
                "FT_CSTPIS"     : (cAlias)->FT_CSTPIS,;
                "FT_CSTCOF"     : (cAlias)->FT_CSTCOF,;
                "FT_CODBCC"     : (cAlias)->FT_CODBCC,;
                "FT_PAUTPIS"    : (cAlias)->FT_PAUTPIS,;
                "FT_PAUTCOF"    : (cAlias)->FT_PAUTCOF,;
                "FT_TNATREC"    : (cAlias)->FT_TNATREC,;
                "FT_CNATREC"    : (cAlias)->FT_CNATREC,;
                "FT_GRUPONC"    : (cAlias)->FT_GRUPONC,;
                "FT_DTFIMNT"    : totvs.framework.treports.date.dateToTimeStamp(StoD((cAlias)->FT_DTFIMNT)),;
                "FT_MALQCOF"    : (cAlias)->FT_MALQCOF,;
                "FT_MVALCOF"    : (cAlias)->FT_MVALCOF,;
                "FT_DESCZFR"    : (cAlias)->FT_DESCZFR,;
                "FT_MALQPIS"    : (cAlias)->FT_MALQPIS,;
                "FT_MVALPIS"    : (cAlias)->FT_MVALPIS,;
                "FT_ATIVCPB"    : (cAlias)->FT_ATIVCPB,;
                "FT_BASECPB"    : (cAlias)->FT_BASECPB,;
                "FT_SERSAT"     : (cAlias)->FT_SERSAT,;
                "FT_ALIQCPB"    : (cAlias)->FT_ALIQCPB,;
                "FT_VALCPB"     : (cAlias)->FT_VALCPB,;
                "FT_TES"        : (cAlias)->FT_TES,;
                "FT_OBSERV"     : cObserv;
                })

            (cAlias)->(dbSkip())

        enddo

        self:setHasNext(.F.)
        (cAlias)->(DBCloseArea())
        
    Endif

    freeobj(oFisTools)
    aSize(aBind,0)
    aBind := Nil
    freeObj(oPrepare)
    freeobj(oFiltQry)
    aSize(aFiliais,0)
    aFiliais := Nil

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos 
@return object: self:oSchema 
@author eduardo.melo
@since 27/10/2023
*/
//-------------------------------------------------------------------   
method getSchema() as object class efdSFTAgruSmartviewBusinessObject
    
    self:addProperty("F4_TPREG","Tipo de Regime","string","Tp Reg","F4_TPREG","1=Não Cumulativo;2=Cumulativo;3=Ambos")
    self:addProperty("FT_FILIAL","Filial","string","Filial","FT_FILIAL")
    self:addProperty("FT_NFISCAL","Nota Fiscal","string","Nota Fiscal","FT_NFISCAL")
    self:addProperty("FT_ENTRADA","Data Entrada NF","date","Data Entrada","FT_ENTRADA")
    self:addProperty("FT_ESTADO","Estado Referencia","string","Estado Ref","FT_ESTADO")
    self:addProperty("FT_CFOP","Codigo Fiscal Oper/Prest","string","Cod. Fiscal","FT_CFOP")
    self:addProperty("FT_VALCONT","Valor Contábil","number","Vlr Contábil","FT_VALCONT")
    self:addProperty("FT_VALIPI","Valor do IPI","number","Valor IPI","FT_VALIPI")
    self:addProperty("FT_ICMSRET","Valor do ICMS Retido ST","number","Vlr ICMS Ret","FT_ICMSRET")
    self:addProperty("FT_TIPO","Tipo Lançamento","string","Tipo Lanc","FT_TIPO","L=Nota em Lote;S=Nota com ISS;B=Beneficiamento;D=Devolucao")
    self:addProperty("FT_OBSSOL","Valor Solidário na Obs","number","Vlr Sol Obs","FT_OBSSOL")
    self:addProperty("FT_SOLTRIB","Vlr ICMS Solidário Tribut","number","ICMS Sol Tri","FT_SOLTRIB")
    self:addProperty("FT_ESPECIE","Espécie da NF","string","Espécie NF","FT_ESPECIE")
    self:addProperty("FT_CREDST","Crédito/Débito ST","string","Crd/Deb ST","FT_CREDST","1=Credita;2=Não se aplica;3=Debita")
    self:addProperty("FT_CONTA","Número da Conta Contábil","string","Conta Contáb","FT_CONTA")
    self:addProperty("FT_ISENRET","ST Retido Isento","number","Retido Isent","FT_ISENRET")
    self:addProperty("FT_OUTRRET","ST Retido Outros","number","Retido Outro","FT_OUTRRET")
    self:addProperty("FT_DESCONT","Valor do Desconto item","number","Vlr Desconto","FT_DESCONT")
    self:addProperty("FT_TOTAL","Vlr Total Item","number","Vlr Total","FT_TOTAL")
    self:addProperty("FT_BASEPIS","Base calculo PIS","number","Base PIS","FT_BASEPIS")
    self:addProperty("FT_ALIQPIS","Alíquota de PIS","number","Aliq. PIS","FT_ALIQPIS")
    self:addProperty("FT_VALPIS","Valor de PIS","number","Valor PIS","FT_VALPIS")
    self:addProperty("FT_BASECOF","Base de COFINS","number","Base COFINS","FT_BASECOF")
    self:addProperty("FT_ALIQCOF","Alíquota de COFINS","number","Aliq. COFINS","FT_ALIQCOF")
    self:addProperty("FT_VALCOF","Valor de COFINS","number","Valor COFINS","FT_VALCOF")
    self:addProperty("FT_BASEPS3","Base Pis ST","number","Bs. Pis ST","FT_BASEPS3")
    self:addProperty("FT_ALIQPS3","Alíquota Pis ST","number","Al. Pis ST","FT_ALIQPS3")
    self:addProperty("FT_VALPS3","Valor Pis ST","number","Vl. Pis ST","FT_VALPS3")
    self:addProperty("FT_BASECF3","Base Cofins ST","number","Bs. Cof ST","FT_BASECF3")
    self:addProperty("FT_ALIQCF3","Alíquota Cofins ST","number","Al. Cof ST","FT_ALIQCF3")
    self:addProperty("FT_VALCF3","Valor Cofins ST","number","Vl. Cof ST","FT_VALCF3")
    self:addProperty("FT_CSTPIS","CST Pis","string","CST Pis","FT_CSTPIS")
    self:addProperty("FT_CSTCOF","CST Cofins","string","CST COF","FT_CSTCOF")
    self:addProperty("FT_CODBCC","Cod.BC Cred.","string","Cod.BC Cred.","FT_CODBCC")
    self:addProperty("FT_PAUTPIS","Valor do PIS de Pauta","number","PIS Pauta","FT_PAUTPIS")
    self:addProperty("FT_PAUTCOF","Valor do Cofins de Pauta","number","Cofins Pauta","FT_PAUTCOF")
    self:addProperty("FT_TNATREC","Tabela Nat. da Receita","string","Tab.Nat.Rec","FT_TNATREC")
    self:addProperty("FT_CNATREC","Cod. Natureza da Receita","string","Cod.Nat.Rec.","FT_CNATREC")
    self:addProperty("FT_GRUPONC","Grupo Natureza da Receita","string","Grp.Nat.Rec.","FT_GRUPONC")
    self:addProperty("FT_DTFIMNT","Data Fim Nat. Receita","date","Dt.Fim N. R","FT_DTFIMNT")
    self:addProperty("FT_MALQCOF","Aliquota COFINS Majorada","number","Aliq. COF M.","FT_MALQCOF")
    self:addProperty("FT_MVALCOF","Valor COFINS Majorada","number","Val. COF M.","FT_MVALCOF")
    self:addProperty("FT_DESCZFR","Desc. Zona Franca Manaus","number","Desc.ZFM","FT_DESCZFR")
    self:addProperty("FT_MALQPIS","Alíquota PIS Majorada","number","Aliq. PIS M.","FT_MALQPIS")
    self:addProperty("FT_MVALPIS","Valor do PIS Majorada","number","Val. PIS M.","FT_MVALPIS")
    self:addProperty("FT_ATIVCPB","Cód. Atividade da CPRB","string","Cód. Ativ.","FT_ATIVCPB")
    self:addProperty("FT_BASECPB","Base da CPRB","number","Base CPRB","FT_BASECPB")
    self:addProperty("FT_SERSAT","Série do SAT","string","Sér.SAT","FT_SERSAT")
    self:addProperty("FT_ALIQCPB","Aliquota da CPRB","number","Alíquota CPR","FT_ALIQCPB")
    self:addProperty("FT_VALCPB","Valor da CPRB","number","Valor CPRB","FT_VALCPB")
    self:addProperty("FT_TES","Tipo de entrada / saída","string","Tipo E/S","FT_TES")
    self:addProperty("FT_OBSERV","Menssagem Livro Fiscal","string","Obs Liv. Fis","FT_OBSERV")

return self:oSchema

/*/{Protheus.doc} vldMsgObserv
	Valida a mensagem que será exibida no FT_OBSERV
	@type  Static Function
	@author Matheus Bispo
	@since 25/04/2025
	@version 12.1.2410
	@param cFTObserv, character, observação vindo da tabela SFT
	@param cTipoMov, character, tipo de movimento (entrada ou saida)
	@param cEspecie, character, especie da nota
	@param cNotaOrigem, character, nota de origem (FT_NFORI)
	@param cSeekDT6, character, chave para fazer seek na DT6
	@return cObserv, character, Observação que será apresentada no relatorio
	@see (links_or_references)
/*/
static function vldMsgObserv(cFTObserv as character, cTipoMov as character, cEspecie as character, cNotaOrigem as character, cSeekDT6 as character)
	Local cObserv := cFTObserv as character

	if !Empty(cNotaOrigem) .And. ((cTipoMov == "S" .And. cEspecie == "BPE") .Or. (cTipoMov == "E" .And. cEspecie == "CTE"))
		cObserv := cEspecie + " SUBST."+Alltrim(cNotaOrigem)
	elseIf cTipoMov == "S" .And. cEspecie == "CTE"        
        If DT6->(MsSeek(cSeekDT6)) .And. !Empty(DT6->(DT6_DOCDCO))
		    cObserv := "CTE SUBST."+Alltrim(DT6->(DT6_DOCDCO))
        EndIf
	endIf

return cObserv
