#Include 'protheus.ch'

namespace protheus.backoffice.fiscal

//---------------------------------------------------------------------------------------------------------------------------------
//-  NewSerieNFd()
//- Função responsável por retornar o formato e estrutura do campo série
//- tendo em vista compatibilizar a SerieNFID
//- @type 		Function
//- @author		Nilton Rodrigues
//- @since 		02/12/2024
//- @Param		
//- cAlias   -> Alias da Tabela do campo Serie
//- 
//- nOpcao   -> 1 - Gravacao
//- 2 - Visualizacao
//- 3 - Retorna o nome do campo serie a ser utilizado em Querys
//- 4 - Retorna a Chave de Pesquisa ID ou Serie Real para utilizar em validacoes dbseeks ANTES da gravacao
//- 5 - Retorna o CriaVar do campo _SDOC em caso onde o campo _SERIE foi alterado tamanho para 14 para gravar o novo formato
//- 6 - Retorna o TamSX3 do campo  _SDOC em caso onde o campo _SERIE foi alterado tamanho para 14 para gravar o novo formato
//- 7 - Retorna o RetTitle do campo _SDOC em caso onde o campo _SERIE foi alterado tamanho para 14 para gravar o novo formato
//- 
//- cCpoOrig -> String contendo o nome do campo Serie Original
//- cSerieGrv-> Variavel Conteudo da Serie a ser gravada (OPCIONAL Usar somente com opcao "1" - Gravacao e "4" - Validacao)
//- 	@return		resultado de acordo com a opção 
//-     nOpcao = 1 -> Nil
//-     nOpcao = 2 -> Conteudo do Campo Serie a ser Utilizado
//-     nOpcao = 3 -> Nome do Campo Serie a ser utilizado
//-     nOpcao = 4 -> Chave de Pesquisa ID ou Serie Real sempre encima do conteudo gravados nos campos _SERIE
//-     nOpcao = 5 -> retorna o CriaVar do campo
//-     nOpcao = 6 -> retorna o TamSX3 do campo
//-     nOpcao = 7 -> retorna o X3Titulos do campo
//---------------------------------------------------------------------------------------------------------------------------------
Function NewSerieNFd(cAlias as character ,nOpcao as numeric ,cCpoOrig as character ,cSerieGrv as character)
	
	Local xRetConteudo

	Do Case
	Case nOpcao == 1
		//--------------------------------------------
		//- efetua a gravação do campo
		//--------------------------------------------
		(cAlias)->(FieldPut(FieldPos(cCpoOrig),cSerieGrv))

	Case nOpcao == 2
		//----------------------------------------------------------------------------
		//- Uso para visualizacao dos campos serie em consulta, relatorios, .INIs
		//----------------------------------------------------------------------------
		xRetConteudo := (cAlias)->(FieldGet(FieldPos(cCpoOrig)))

	Case nOpcao == 3
		//----------------------------------------------------------------------------
		//- Retornar qual campo devera ser utilizado em Querys, Filtros, Arrays
		//----------------------------------------------------------------------------
		xRetConteudo := cCpoOrig

	Case nOpcao == 4
		//----------------------------------------------------------------------------
		// Em caso de campos _SERIE com tamanho = 3 retorna a Serie Real
		//----------------------------------------------------------------------------
		xRetConteudo := cSerieGrv

	Case nOpcao == 5
		//----------------------------------------------------------------------------
		// Criação da variável
		//----------------------------------------------------------------------------
		xRetConteudo := CriaVar( AllTrim(cCpoOrig) )

	Case nOpcao == 6
		//----------------------------------------------------------------------------
		// Tamanho do Campo
		//----------------------------------------------------------------------------
		xRetConteudo := TamSX3( AllTrim(cCpoOrig))[1]

	Case nOpcao == 7
		//----------------------------------------------------------------------------
		// Titulo do campo
		//----------------------------------------------------------------------------
		xRetConteudo := RetTitle(Alltrim(cCpoOrig))

	EndCase


Return xRetConteudo
