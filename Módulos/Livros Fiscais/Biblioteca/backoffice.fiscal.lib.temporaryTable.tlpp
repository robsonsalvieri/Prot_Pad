#include 'tlpp-core.th'

Namespace totvs.protheus.backoffice.fiscal.lib

/*/{Protheus.doc} FisTemporaryTable
    Classe responsável por instanciar e manipular dados em tabelas temporárias.

    @type class
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param jQuery, json, contém a query e o bind a serem executados.
/*/
Class FisTemporaryTable
    Data aFieldsTable   as array
    Data oTempTab       as object
    Data cTableSGBD     as character
    Data oBulkTmp       as object
    Data lCanBulk       as logical
    Data jStatus        as json
    Data lLogs          as logical
    Data nTimer         as numeric
    Data cExecution     as character
    Data cError         as character

    //ProtheusDOC de métodos devem ser efetuados na implementação.
    Public Method new() CONSTRUCTOR
    Public Method destroy()

    Public Method setField()        //indica os campos que serão criados na tabela temporária
    Public Method setIndex()        //indica os índices que serão utilizados na tabela temporária
    Public Method createTable()     //cria a tabela temporária
    Public Method startBulk()       //inicia o bulk dos dados na tabela temporária
    Public Method bulkData()        //insere dados no bulk
    Public Method cleanTable()      //realiza zap na tabela temporária
    Public Method getLogTimer()     //retorna o log
    Public Method getError()        //retorna o erro ao tentar adicionar itens ao bulk

    Private Method startLogTimer()  //inicializa o timer do log
    Private Method finishLogTimer() //finaliza o timer do log
EndClass

/*/{Protheus.doc } Method new()
    Método construtor da classe
/*/
Method new(lLogUser) Class FisTemporaryTable
    Default lLogUser := .F.

    self:lLogs := lLogUser
    If ::lLogs
        ::jStatus := JsonObject():New()
        ::startLogTimer()
    EndIf

    self:oTempTab   := totvs.framework.database.temporary.SharedTable():New()
    
    If ::lLogs
       ::finishLogTimer("new")
    EndIf
Return

/*/{Protheus.doc } Method destroy()
    Método para desconstruir as variáveis inicializadas da classe
/*/
Method destroy() class FisTemporaryTable

    If Valtype(self:oTempTab) == "O"
        FreeObj(self:oTempTab)
        ::oTempTab := nil
    ENDIF

    If ::lLogs .AND. (Valtype(self:jStatus) == "J")
        FwFreeVAR(self:jStatus)
    EndIf
Return

/*/{Protheus.doc } Method setField(aFields)
    Método responsável por determinar os campos que serão criados 
    na tabela temporária. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param aFields, array, contém os dados dos campos que serão criados.
/*/
Method setField(aFields) class FisTemporaryTable
    Default aFields := {}

    If ::lLogs
        ::startLogTimer()
    EndIf
    
    ::aFieldsTable := aFields

    self:oTempTab:SetFields(::aFieldsTable)

    If ::lLogs
       ::finishLogTimer("setField")
    EndIf
Return


/*/{Protheus.doc } Method setIndex(aIndex)
    Método responsável por indicar a query a ser executada. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param aIndex, array, contém os índices que serão criados na tabela temporária.
/*/
Method setIndex(aIndex) class FisTemporaryTable
    Local nX        as numeric

    Default aIndex  := {}

    If ::lLogs
        ::startLogTimer()
    EndIf

    For nX := 1 to Len(aIndex)
        self:oTempTab:AddIndex(aIndex[nX][1],aIndex[nx][2])
    Next nX

    If ::lLogs
       ::finishLogTimer("setIndex")
    EndIf
Return


/*/{Protheus.doc } Method createTable
    Método responsável por indicar a query a ser executada. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
/*/
Method createTable() class FisTemporaryTable
    
    If ::lLogs
        ::startLogTimer()
    EndIf
    
    self:oTempTab:Create()
    self:cTableSGBD := UPPER(self:oTempTab:GetRealName())
    
    If ::lLogs
       ::finishLogTimer("createTable")
    EndIf
Return self:cTableSGBD


/*/{Protheus.doc } Method startBulk
    Método responsável iniciar o bulk dos dados na tabela temporária. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param cQuery, string, contém a query a ser executada.
/*/
Method startBulk(nBlock) class FisTemporaryTable
    Default nBlock := 950
   
    If ::lLogs
        ::startLogTimer()
    EndIf

    self:oBulkTmp := FwBulk():New(self:cTableSGBD,nBlock)
    self:lCanBulk := FwBulk():CanBulk()
    
    if self:lCanBulk             
        self:oBulkTmp:SetFields(::aFieldsTable)
    endif
    
    If ::lLogs
       ::finishLogTimer("startBulk")
    EndIf
Return self:lCanBulk

/*/{Protheus.doc } Method bulkData
    Método responsável pela inserção de dados na tabela temporária. 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
    @param aData, array, contém as linhas a serem incluídas via bulk na tabela temporária.
/*/
Method bulkData(aData) class FisTemporaryTable
    Local nX        as numeric
    Local lInsert   := .T. as logical
    
    If ::lLogs
        ::startLogTimer()
    EndIf

    // BEGIN TRANSACTION
        For nX := 1 to Len(aData)
            if ( lInsert )
                lInsert := self:oBulkTmp:AddData(aData[nX])        

                if ( lInsert )
                    self:cError := self:oBulkTMP:GetError()
                    lInsert := Empty( self:cError )                    
                endif
            endif
        Next nX			
		
        IF !(self:oBulkTmp == nil)
			IF self:oBulkTmp:Close()
			    self:oBulkTmp:Destroy()
			    FreeObj(self:oBulkTmp)
            EndIf
		EndIF
    // END TRANSACTION
    oBulkTmp := nil

    If ::lLogs
       ::finishLogTimer("bulkData")
    EndIf
Return lInsert

/*/{Protheus.doc } Method cleanTable
    Método responsável por limpar os dados da tabela temporária 

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 31/07/2024
/*/
Method cleanTable() class FisTemporaryTable
    //criar verificação de execução de ZAP com sucesso
    If ::lLogs
        ::startLogTimer()
    EndIf

    //executa o zap na tabela temporária
    self:oTempTab:Zap()

    If ::lLogs
       ::finishLogTimer("cleanTable")
        //Soma 1 na execução para tratativas de log
        self:cExecution := cValtoChar(++Val(self:cExecution))
    EndIf


Return

/*/{Protheus.doc } Method startLogTimer
    Método responsável por inicializar o timer do log

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 05/08/2024
/*/
Method startLogTimer() class FisTemporaryTable
    Local jDetail := JsonObject():New() as json

    If Val(self:cExecution) == 0 
        self:cExecution := cValtoChar(++Val(self:cExecution))
    EndIf

    self:nTimer    := seconds()
    If !self:jStatus:HasProperty(self:cExecution)
        jDetail["executionTime"] := 0
        self:jStatus[self:cExecution] := jDetail
    EndIf
Return


/*/{Protheus.doc } Method finishLogTimer
    Método responsável por finalizar o timer do log

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 05/08/2024
/*/
Method finishLogTimer(cMethod) class FisTemporaryTable
    local jDetail   := JsonObject():New() as json
    
    jDetail["methodTime"] := seconds()-::nTimer
    ::jStatus[self:cExecution][cMethod] := jDetail
    ::jStatus[self:cExecution]["executionTime"] := ::jStatus[self:cExecution]["executionTime"] + ::jStatus[self:cExecution][cMethod]["methodTime"]	

Return

/*/{Protheus.doc } Method getLogTimer
    Método responsável por retornar o log

    @type method
    @version P12.1.2410
    @author Thiago Moreira
    @since 05/08/2024
/*/
Method getLogTimer() class FisTemporaryTable

Return ::jStatus

/*/{Protheus.doc } Method getError
    Método responsável por obter o erro gerado ao adicionar dados no bulk

    @type method
    @version P12.1.2410
    @author Juliano Fernandes
    @since 05/08/2024
/*/
Method getError() class FisTemporaryTable

    local aMensagens := {} as array
    local i as numeric

    Aadd(aMensagens, 'Classificacao Tributaria - temporaryTable.tlpp - getError')
    Aadd(aMensagens, 'Error: ' + self:cError)

    ConOut(Replicate("-",120))

    for i := 1 to Len(aMensagens)
        ConOut("- " + PadC(aMensagens[i] ,116) + " -")
    next i

    ConOut(Replicate("-",120))   

    ASize(aMensagens, 0) 

Return self:cError
