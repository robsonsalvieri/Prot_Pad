#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#INCLUDE "msobject.ch"
#include "tlpp-core.th"
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISCriaCenario
Classe que irá criar os cenários para enviar a Systax
@author  Yuri Gimenes da Costa
@since   04.04.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISCriaCenario

    data aPerfil as array
    data cPerfil as character
    data oJsonCena as object
    data cGrpProd as character
    data oJsonNat as object
    data oJsonOper as object
    data oJsonFin as object
    data oJsonPar as object

    PUBLIC METHOD New() CONSTRUCTOR
    PUBLIC METHOD CriaCenario(nEntSai,cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,oJsonPar)

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes
@since   04.04.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISCriaCenario

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} CriaCen()
Método para criar os cenários e enviá-los a Systax
@author  Yuri Gimenes
@since   04.04.2022
/*/
//-------------------------------------------------------------------
METHOD CriaCenario(nEntSai,cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,cEmpresa_dest) CLASS FISCriaCenario

Local cJson     as character
Local oJsonCena := JsonObject():New()
    
oJsonCena := CriaCena(nEntSai,cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,cEmpresa_dest) 

cJson := oJsonCena:toJSON( )

FreeObj(oJsonCena)

Return cJson

//-------------------------------------------------------------------
/*/{Protheus.doc} CriaCena
Static function que cria o cenário para ser enviado a Systax
@author  Yuri Gimenes
@since   30.03.2022
@version 1.0
/*/
//-------------------------------------------------------------------
STATIC FUNCTION CriaCena(nEntSai,cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,cEmpresa_dest) //nEntSai,cEmpresa,cGrpProd,oJsonNat,oJsonFin,oJsonOper,oJsonPar

Local oJson     as object
Local oJsonDt   as object
Local nY  := 0  as integer
Local nYC := 0  as integer
Local cApelido  as character

oJson := JsonObject():New()
oJson["cenarios"] := {}

//Um cenário por operação
For nY := 1 to len(oJsonOper["operacao"]) 

        //Um cenário por Natureza de operação/Finalidade
        For nYC := 1 to Len(oJsonNat["nat_fin"])

            If nEntSai == 1 .or. nEntSai == 4
                cApelido := "" + NoAcento(Alltrim(oJsonFin["nat_fin"][nYC]["descricao"]))
            else
                cApelido := "" + NoAcento(Alltrim(oJsonNat["nat_fin"][nYC]["descricao"]))
            EndIF

            oJsonDt := JsonObject():New()

            oJsonDt["id"]                           := ""
            oJsonDt["id_cliente"]                   := ""
            oJsonDt["grupo_cache"]                  := "A"
            oJsonDt["apelido"]                      := Substr(cApelido,1,50)
            oJsonDt["ent_sai"]                      := nEntSai //1 - Entradas, 2 - Devolução de Entradas, 3 - Saídas e 4 - Devolução de Saídas
            oJsonDt["cod_nat_op"]                   := oJsonNat["nat_fin"][nYC]["codigo"]
            oJsonDt["finalidade"]                   := Iif(!Empty(AllTrim(cEmpresa_dest)).And. Valtype(oJsonFin["nat_fin"]) == 'A' .And. Len(oJsonFin["nat_fin"]) > 0,oJsonFin["nat_fin"][nYC]["codigo"],"")
            oJsonDt["destinacao"]                   := cEmpresa_dest
            oJsonDt["alternativo_ex_origem"]        := cEmpresa
            oJsonDt["uf_origem"]                    := Alltrim(oJsonOper["operacao"][nY]["origem"] )
            oJsonDt["uf_destino"]                   := Alltrim(oJsonOper["operacao"][nY]["destino"])
            oJsonDt["id_mun_origem"]                := Nil
            oJsonDt["id_mun_destino"]               := Nil
            oJsonDt["cnpj_ex_origem"]               := ""
            oJsonDt["cnae_destinatario"]            := ""
            oJsonDt["origem_produto_alternativo"]   := Nil
            oJsonDt["cenario_ind_prod"]             := ""
            oJsonDt["st_cest"]                      := Nil
            oJsonDt["frequencia"]                   := IIf(nEntSai == 1 .or. nEntSai == 4,2,1)//Se for 1 - Entradas ou 4 - Devolução de Saídas -> 2 = Entrada, senão -> 1 = Saída
            oJsonDt["dias_futuros"]                 := 30
            oJsonDt["dt_criacao"]                   := ""
            oJsonDt["grupos"]                       := cGrpProd

            aadd(oJson["cenarios"],oJsonDt)

            FreeObj(oJsonDt)

        Next nYC
Next nY

Return oJson
