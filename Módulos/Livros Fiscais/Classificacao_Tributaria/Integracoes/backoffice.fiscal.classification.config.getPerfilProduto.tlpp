#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FisGetProduto
Classe que contém get do perfil de produtos
da regra da Systax

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Class FisGetProduto

	Public METHOD New() CONSTRUCTOR

	@Get("/GetProduto")
	Public METHOD GetProduto()

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() Class FisGetProduto

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método para obter os dados dos produtos e coloca-los em arquivo Json

@Obs    Passar via QueryParam a filial logada em sistema

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetProduto() Class FisGetProduto

Local cQry              as character
Local cOrigem           as character
Local cProduto          as character
Local cItem             as character
Local filGroup          as character
Local oJPerfil          as object
Local cJson             as character
Local jQuery            as object
Local nPage             as numeric
Local nPageSize         as numeric
Local nX                as numeric
Local nRegIni           as numeric
Local nRegFim           as numeric
Local lOk               as logical
Local aAux              as array
local oJDetalhes        as object
Local cFilter           as character
Local nIndex            as numeric
Local oStatement        as object
Local oJPerfilProduto   as object
Local cADetProd         as character
Local cAliasProd        as character
Local nCount            as integer

cQry        := ""
jQuery      := oRest:getQueryRequest()
lOk         := .F.
nPage       := IIF(jQuery['page'] <> Nil,Val(jQuery['page']), 1 )
nPageSize   := IIF(jQuery['pageSize'] <> Nil,Val(jQuery['pageSize']), 15 )
nRegIni     := ( ( nPage - 1 ) * nPageSize ) + 1
nRegFim     := nPage * nPageSize

filGroup    := IIF(jQuery['filGroup'] <> Nil .and. !('*' $ jQuery['filGroup']) , jQuery['filGroup'], '')
aAux		:= StrTokArr2(filGroup,',')

for nX := 1 To Len(aAux)
    If ('origem:' $ aAux[nX])
        cOrigem += IIF(Empty(cOrigem), '',',') + AllTrim(StrTran(aAux[nX],'origem:',''))
    ElseIf ('produto:' $ aAux[nX])
        cProduto += IIF(Empty(cProduto), '',',') + AllTrim(StrTran(aAux[nX],'produto:',''))
    ElseIf ('item:' $ aAux[nX])
        cItem += IIF(Empty(cItem), '',',') + AllTrim(StrTran(aAux[nX],'item:',''))
    Else
        If ( !Empty(aAux[nX]) )
            cFilter := aAux[nX]
        EndIf
    EndIf
Next

cOrigem := AllTrim(StrTran(cOrigem,'origem:',''))
cProduto := Alltrim(StrTran(cProduto,'produto:',''))
cItem := Alltrim(StrTran(cItem,'item:',''))

If ( !Empty(cFilter) )
    cFilter := '%' + cFilter + '%'
EndIf

cQry += " SELECT DISTINCT F20_CODIGO, F20_DESC FROM ( "
cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY F20_CODIGO, F20_DESC ) "
cQry += "       LINE_NUMBER, F20_CODIGO, F20_DESC "
cQry += "   FROM "+RetSqlName("F20")+" F20 "
cQry += "   INNER JOIN "+RetSqlName("F24")+" F24 ON F24_FILIAL = ?  "
cQry += "                                       AND F24_CODIGO = F20_CODIGO "
cQry += "                                       AND F24.D_E_L_E_T_ = ' ' "
cQry += "   WHERE F20_FILIAL = ? AND F20_TIPO   = ? AND F24_CDPROD NOT IN ('TODOS') "

If (!Empty(cProduto))
    cQry += "       AND F20_CODIGO IN " + FormatIn(cProduto, ",") + " "
EndIf

If (!Empty(cItem))
    cQry += "       AND F24_CDPROD IN " + FormatIn(cItem, ",") + " "
EndIf

If ( !Empty(cFilter) )
    cQry += "       AND ( UPPER(F20_CODIGO) LIKE ? OR UPPER(F20_DESC) LIKE ? ) "
EndIf

cQry += "       AND F20.D_E_L_E_T_ = ' ' "
cQry += "   GROUP BY F20_CODIGO,F20_DESC"
cQry += " ) TAB "
cQry += " WHERE LINE_NUMBER BETWEEN ? AND ? "

oStatement := FwExecStatement():New( ChangeQuery(cQry) )

oStatement:setString( ++nIndex, xFilial('F24') )
oStatement:setString( ++nIndex, xFilial('F20') )
oStatement:setString( ++nIndex, '04' )

If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
EndIf

oStatement:SetNumeric( ++nIndex, nRegIni )
oStatement:SetNumeric( ++nIndex, nRegFim + 1 ) //Soma mais um registro para retorno do HasNext

cQry := oStatement:getFixQuery()

cAliasProd := oStatement:OpenAlias(GetNextAlias())

oJPerfilProduto := JsonObject():New()
oJPerfilProduto["perfil_produto"] := {}

While (cAliasProd)->(!EoF()) .And. nCount < nPageSize
    nCount++

    oJPerfil                    := JsonObject():New()
    OJPerfil["cod_perfil"]      := (cAliasProd)->F20_CODIGO
    OJPerfil["descricao_perfil"]:= AllTrim((cAliasProd)->F20_DESC)
    OJPerfil["detalhes"]        := {}

    cADetProd := DetPerfProd((cAliasProd)->F20_CODIGO)
    
    While (cADetProd)->(!EoF())

        oJDetalhes                  := JsonObject():New()
        oJDetalhes["cod_produto"]   := (cADetProd)->F24_CDPROD
        oJDetalhes["descr_produto"] := AllTrim((cADetProd)->B1_DESC)
        oJDetalhes["ncm_produto"]   := (cADetProd)->B1_POSIPI
        oJDetalhes["origem"]		:= (cADetProd)->B1_ORIGEM

        aadd(OJPerfil["detalhes"], oJDetalhes )
        
        FREEOBJ( oJDetalhes )

        (cADetProd)->(DbSkip())
    EndDo

    (cADetProd)->( DBCloseArea() )

    Aadd(oJPerfilProduto["perfil_produto"], oJPerfil)

    ( cAliasProd )->( DBSkip() )

    FreeObj(oJPerfil)

    lOk := .T.

EndDo

oJPerfilProduto["success"] := lOk
oJPerfilProduto["hasNext"] := !(cAliasProd)->(EoF())

( cAliasProd )->( DBCloseArea() )

cJson := oJPerfilProduto:toJson()

FreeObj(oStatement)
FreeObj(oJPerfilProduto)

FWFreeArray(aAux)

Return oRest:setResponse(cJson)

/*/{Protheus.doc} DetPerfProd
    (long_description)
    @type  Static Function
    @author Erich Buttner
    @since 31/08/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    @example
    (examples)
    @see (links_or_references)
/*/
Static Function DetPerfProd(cCodPerfil)
Local cQry	    as character
Local oStatDet  as object
Local cADetProd	as character

cQry += " SELECT F24_CDPROD,B1_POSIPI,B1_ORIGEM,B1_DESC " 
cQry += " FROM "+RetSqlName("F24")+" F24 " 
cQry += " INNER JOIN "+RetSqlName("SB1")+" SB1 ON B1_FILIAL = ? AND F24_CDPROD = B1_COD AND SB1.D_E_L_E_T_ = ' ' "
cQry += " WHERE  F24_FILIAL = ? AND F24_CODIGO = ? AND F24.D_E_L_E_T_ = ' ' "

oStatDet := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatDet:setString(1, xFilial("SB1"))
oStatDet:setString(2, xFilial("F24"))
oStatDet:setString(3, cCodPerfil)

cQry := oStatDet:getFixQuery()

cADetProd := MPSysOpenQuery(cQry)

FreeObj(oStatDet)

Return cADetProd
