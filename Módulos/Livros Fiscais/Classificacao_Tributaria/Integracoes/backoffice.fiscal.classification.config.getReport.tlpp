#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} GetReports
Classe que contém os Gets para serem utilizados na tela de Relatórios

@author  Yuri Gimenes
@since   24.10.2022
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class GetReports
    
    public method new() constructor

    @GET("/GetCenariosRel")    
    public method GetCenariosRel()

    @GET("/GetRegrasRel")    
    public method GetRegrasRel()

    @GET("/GetAcumRegrasMensal")
    public method GetAcumRegrasMensal()


End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe

@author  Yuri Gimenes
@since   24.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class GetReports


return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetCenarios()
Método para retornar os dados da quantidade de cenários enviados
e não enviados
        
@author  Yuri Gimenes
@since   24.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method GetCenariosRel() class GetReports

Local cAliasCenarios    := ""
Local cQry 			    := ""
Local cRet			    := ""
Local oJsonBase         := JsonObject():New()
Local jQuery            := oRest:GetQueryRequest()
Local cDtInicio         := ""
Local cDtFinal          := ""
Local lFiltraData       as logical
Local oStatement        as object
Local nIndex            as integer

cDtInicio   := IIF( jQuery['dtinicio'] <> Nil , jQuery['dtinicio'] , '' )
cDtFinal    := IIF( jQuery['dtfinal'] <> Nil , jQuery['dtfinal'] , '' )

If !Empty(cDtInicio) .And. !Empty(cDtFinal)
    lFiltraData := .T.
    cDtInicio   := DToS(CToD(cDtInicio))
    cDtFinal    := DToS(CToD(cDtFinal))
EndIf

cQry += " SELECT "
cQry += " 	SUM(CASE WHEN CJG_IDCENA = '' AND CJG_DTENV = '' AND CJG_HRENV = '' AND CJG_JSON IS NOT NULL THEN 1 ELSE 0 END) NAO_ENVIADO, "
cQry += " 	SUM(CASE WHEN CJG_IDCENA <> '' AND " + If(lFiltraData, "CJG_DTENV BETWEEN ? AND ?", "CJG_DTENV <> ''") + " AND CJG_HRENV <> '' AND CJG_JSON IS NOT NULL THEN 1 ELSE 0 END) ENVIADO "
cQry += " FROM " + RetSqlName("CJG")
cQry += " WHERE CJG_FILIAL = ? "
cQry += " 	AND D_E_L_E_T_ = ' ' "

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

If ( lFiltraData )
    oStatement:SetString( ++nIndex, cDtInicio )
    oStatement:SetString( ++nIndex, cDtFinal )
EndIf

oStatement:SetString( ++nIndex, xFilial("CJG") )

cQry := oStatement:GetFixQuery()

cAliasCenarios := MPSysOpenQuery(cQry)

If !(cAliasCenarios)->(EoF())
    oJsonBase['nao_enviado'] := (cAliasCenarios)->NAO_ENVIADO
    oJsonBase['enviado'] := (cAliasCenarios)->ENVIADO
EndIf

(cAliasCenarios)->(DbCloseArea())

cRet := oJsonBase:toJson()

FreeObj(oStatement)
FreeObj(oJsonBase)
FreeObj(jQuery)

Return oRest:setResponse(cRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} GetCenarios()
Método para retornar os dados da quantidade de regras integradas
e não integradas
        
@author  Yuri Gimenes
@since   24.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method GetRegrasRel() class GetReports

Local cAliasRegras  := ""
Local cQry 			:= ""
Local cRet			:= ""
Local oJsonBase     := JsonObject():New()
Local jQuery        := oRest:GetQueryRequest()
Local cDtInicio     := ""
Local cDtFinal      := ""
Local lFiltraData   as logical
Local oStatement    as object
Local nIndex        as integer

cDtInicio   := IIF( jQuery['dtinicio'] <> Nil , jQuery['dtinicio'] , '' )
cDtFinal    := IIF( jQuery['dtfinal'] <> Nil , jQuery['dtfinal'] , '' )

If ( !Empty(cDtInicio) .And. !Empty(cDtFinal) )
    lFiltraData := .T.
    cDtInicio   := DToS(CToD(cDtInicio))
    cDtFinal    := DToS(CToD(cDtFinal))
EndIf

cQry += " SELECT "
cQry += " 	SUM(CASE WHEN CJB_STATUS = 'AP' " + If(lFiltraData, "AND SUBSTRING(CJB_DTAPRO,1,8) BETWEEN ? AND ?", "") + " THEN 1 ELSE 0 END) APROVADAS, "
cQry += " 	SUM(CASE WHEN CJB_STATUS = 'AA' THEN 1 ELSE 0 END) AG_APROVAC, "
cQry += " 	SUM(CASE WHEN CJB_STATUS = 'R'  THEN 1 ELSE 0 END) RECUSADAS, "
cQry += " 	SUM(CASE WHEN CJB_STATUS = 'I'  THEN 1 ELSE 0 END) INATIVAS, "
cQry += " 	SUM(CASE WHEN CJB_STATUS = 'S'  THEN 1 ELSE 0 END) SUSPENSAS, "
cQry += " 	SUM(CASE WHEN CJB_STATUS <> '' " + If(lFiltraData, "AND SUBSTRING(CJB_PTSYS,1,8) BETWEEN ? AND ?", "") + " THEN 1 ELSE 0 END) TOTAL "
cQry += " FROM " + RetSqlName("CJB")
cQry += " WHERE CJB_FILIAL = ? "
cQry += "   AND D_E_L_E_T_ = ' ' "

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

If ( lFiltraData )
    oStatement:SetString( ++nIndex, cDtInicio )
    oStatement:SetString( ++nIndex, cDtFinal )

    oStatement:SetString( ++nIndex, cDtInicio )
    oStatement:SetString( ++nIndex, cDtFinal )
EndIf

oStatement:SetString( ++nIndex, xFilial("CJB") )

cQry := oStatement:GetFixQuery()

cAliasRegras := MPSysOpenQuery(cQry)

If !(cAliasRegras)->(EoF())
    oJsonBase['aprovada'] := (cAliasRegras)->APROVADAS
    oJsonBase['aguardando_aprovacao'] := (cAliasRegras)->AG_APROVAC
    oJsonBase['recusada'] := (cAliasRegras)->RECUSADAS
    oJsonBase['inativa'] := (cAliasRegras)->INATIVAS
    oJsonBase['suspensa'] := (cAliasRegras)->SUSPENSAS
    oJsonBase['total'] := (cAliasRegras)->TOTAL
EndIf

(cAliasRegras)->(DbCloseArea())

cRet := oJsonBase:toJson()

FreeObj(oStatement)
FreeObj(oJsonBase)
FreeObj(jQuery)

Return oRest:setResponse(cRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAcumRegrasMensal()
Método para retornar a quantidade de regras recebidas por mês
        
@author  Erich Buttner 
@since   10.11.2022
@version 1.0
/*/
//-------------------------------------------------------------------


method GetAcumRegrasMensal() Class GetReports

    Local cAliasRegras  := ""
    Local cQry          := ""
    Local cRet          := ""
    Local oJsonBase     := JsonObject():New()    
    Local cDtAnual      := cValtoChar(Val(Substr(Dtos(Date()),1,4))-1)+StrZero(Val(Substr(Dtos(Date()),5,2))+1,2)
    Local oJData        as object
    Local cDB           as character
    Local lAutomato     := IsBlind()
    Local oStatement    as object

    If ( lAutomato )
        cDtAnual := '202308'
    EndIf

    cDB := Upper(TCGetDB())

    cQry += " SELECT CJBA.CJB_FILIAL, "
    
    If "POSTGRES" $ cDB
        cQry += "CJBA.CJB_PTSYS,"
    EndIf

    cQry += " SUBSTRING(CJBA.CJB_PTSYS,1,6) MESANO, COUNT(CJBA.CJB_PTSYS) QUANTREGRAS,"
	cQry += " (SELECT COUNT(CJBB.CJB_PTSYS) FROM " +RetSqlName("CJB")+ " CJBB WHERE CJBB.CJB_FILIAL = CJBA.CJB_FILIAL AND CJBB.D_E_L_E_T_ = ' ' AND SUBSTRING(CJBB.CJB_PTSYS,1,6) <= SUBSTRING(CJBA.CJB_PTSYS,1,6)) ACUMREG "
    cQry += " FROM " +RetSqlName("CJB")+ " CJBA"
	cQry +=	" WHERE CJBA.CJB_FILIAL = ? "
	cQry +=	" AND CJBA.D_E_L_E_T_ = ' ' "
	cQry +=	" AND SUBSTRING(CJBA.CJB_PTSYS,1,6) >= ? "
	cQry +=	" GROUP BY SUBSTRING(CJBA.CJB_PTSYS,1,6), CJBA.CJB_FILIAL "
    
    If "POSTGRES" $ cDB
        cQry += ", CJBA.CJB_PTSYS"
    EndIf
	
    cQry +=	" ORDER BY MESANO " 

    oStatement := FwExecStatement():New( ChangeQuery(cQry) )

    oStatement:SetString(1, xFilial("CJB"))
    oStatement:SetString(2, cDtAnual)

    cAliasRegras := oStatement:OpenAlias(GetNextAlias())
        
    oJsonBase["datas"] := {}

    While !(cAliasRegras)->(Eof())

        oJData := JsonObject():New()
        oJData['mes_ano'] := MesExtenso(Val(Substr((cAliasRegras)->MESANO,5,2)))+'/'+Substr((cAliasRegras)->MESANO,1,4)
        oJData['qtd_regras'] := (cAliasRegras)->QUANTREGRAS
        oJData['acum_reg'] := (cAliasRegras)->ACUMREG
 
        aadd(oJsonBase["datas"], oJData )

        FREEOBJ( oJData )
        
        (cAliasRegras)->(DbSkip())
    
    EndDo

    (cAliasRegras)->(DbCloseArea())

    cRet := oJsonBase:toJson()

    FreeObj(oJsonBase)
    FreeObj(oStatement)

Return oRest:setResponse(cRet)
