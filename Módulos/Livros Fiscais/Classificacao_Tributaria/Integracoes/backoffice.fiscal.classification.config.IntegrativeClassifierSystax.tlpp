#include 'tlpp-core.th'

Static jCache := JsonObject():New() 

Namespace totvs.protheus.backoffice.fiscal.Classifier

/*/{Protheus.doc} className
    
    Classe responsável por integrar o Classificador fiscal by Systax com o Protheus

    @author Rafael Oliveira
    @since 30/11/2023
    @version 12.1.2310
    /*/
Class IntegrativeClassifierSystax
    Private Data aRegisterAliquot              as array //array com os dados de cadastro de alíquota
    Private Data aRegisterBase                 as array //array com os dados de cadastro de base de cálculo
    Private Data aRelationTaxProtheusAndSystax as array //array com a relação de tributos do Protheus e Systax
    PRIVATE Data cCodeCEST                     as character //código de CEST
    PRIVATE Data cCodeNcm                      as character //ncm    
    Private Data cCodeTaxIPI                   as character //código de IPI
    PRIVATE Data cData                         as character //conteúdo recebido em caractere
    PRIVATE Data cError                        as character //erro ocorrido
    PRIVATE Data cFederativeUnitDestination    as character //estado de destino
    PRIVATE Data cFederativeUnitOrigin         as character //estado de origem
    PRIVATE Data cOriginProduct                as character //origem do produto    
    PRIVATE Data JsonData                      as json //dados convertidos em json
    PRIVATE Data jTaxesReturn                  as json //json de tributos
    Private Data lExistIPI                     as logical //indica se existe IPI no conteúdo recebido    
    Private Data lIntegrativeAliquot           as logical //indica se o conteúdo enviado possui dados de cadastro de alíquota
    Private Data lIntegrativeBaseCalc          as logical //indica se o conteúdo enviado possui dados de cadastro de base de cálculo
    PRIVATE Data lIntegrativeNCM               as logical //indica se o conteúdo enviado possui dados de cadastro de ncm    
    Private Data lIntegrativeWriting           as logical //indica se o conteúdo enviado possui dados de cadastro de escrituração fiscal
    PRIVATE Data lReady                        as logical //indica se o conteúdo enviado é json esta pronto para ser processado
    Private Data nContAliquot                  as numeric //contador de alíquotas
    Private Data nContBaseCalc                 as numeric //contador de base de cálculo
    Private Data nContRulesMajoracao           as numeric //contador de regras de tributos
    Private Data nContRulesMva                 as numeric //contador de regras de tributos
    Private Data nContRulesPauta               as numeric //contador de regras de tributos
    Private Data nContTax                      as numeric //contador de tributos
    Private Data cMockServer                   as character //url do mock server

    Public Data aNCMs                          as array
    Public Data aBases                         as array
    Public Data aAliquotas                     as array

    
    //métodos construtores e processamento dos dados
    Public Method new(value) CONSTRUCTOR //método construtor da classe IntegrativeClassifierSystax        
    Public Method recordTaxes() as logical //método para gravar os tributos no Protheus
    Public Method destroy() //método para limpar os dados da classe
    Public Method saveNCM()                      as logical //método para gravar os dados de cadastro de ncm no Protheus
    Public Method saveAliquot()                  as logical //método para gravar os dados de cadastro de alíquota no Protheus
    Public Method saveBaseCalc()                 as logical //método para gravar os dados de cadastro de base de cálculo no Protheus


    Private Method addAliquot(jProtheus)                             as json //método para adicionar dados de cadastro de alíquota no objeto json jTaxesReturn
    Private Method addMajoracao(jProtheus, jTaxSystax, cTax)         as json //método para adicionar majoração no objeto json jTaxesReturn
    Private Method addMva(jProtheus, jTaxSystax, cTax)               as json //método para adicionar MVA no objeto json jTaxesReturn
    Private Method addPauta(jProtheus, jTaxSystax, cTax)             as json //método para adicionar valor de pauta no objeto json jTaxesReturn
    Private Method addProperty(jProtheus, cProperty)                 as json //método para adicionar dados de cadastro de ncm no objeto json jTaxesReturn
    Private Method addTaxes(cTax, jTax)                              as logical //método para adicionar os tributos no objeto json jTaxesReturn
    Private Method addTaxMajoracao(cTaxFecpic, jTaxSystax)           as json //método para adicionar majoração no objeto json jTaxesReturn
    Private Method convertUnitMeasure(cUnitMeasure)                  as character //método para converter a unidade de medida para o formato do Protheus
    Private Method ConvertValue(cValue)                              as numeric //método para converter o valor para o formato do Protheus
    Private Method initializeTaxesReturn()                           as json //método para inicializar o objeto json jTaxesReturn
    Private Method IsTaxFecp(cTax)                                   as logical //método para verificar se o tributo é FECP
    Private Method MountCalcBase(nCodigo)                            as character //método para montar o cálculo da base de cálculo
    Private Method MountVigencia(jTaxSystax)                         as character //método para montar a vigência de    
    Private Method organizeJson()                                    as json //método para organizar os dados recebidos em json para o cadastro dos tributos
    Private Method processAliquot(jProtheus, jTaxSystax, cTax)       as json //método para processar dados de cadastro de alíquota
    Private Method processBaseCalc(jProtheus, jTaxSystax, cTax)      as json //método para processar dados de cadastro de base de cálculo
    Private Method processFiscalWriting(jProtheus, jTaxSystax, cTax) as json //método para processar dados de cadastro de escrituração fiscal
    Private Method processJsonReturns(jJsonRetorno)                  as json //método para processar os retornos do json    
    Private Method processNCM(jProtheus,jTaxSystax, cTax)            as json //método para processar dados de cadastro de ncm
    Private Method processTaxCalc(jProtheus, jTaxSystax, cTax)       as json //método para processar dados de cadastro de cálculo do tributo
    Private Method relationTaxWhitMajoracao(cTax)                    as logical //método para verificar se o tributo possui relação com majoração
    Private Method taxesConvert(cTax)                                as character //método para converter os tributos para o formato do Protheus    
    Private Method validateReceivedData()                            as logical //método para validar os dados recebidos    
    Private Method validTaxes(cTributo)                              as logical //método para validar os tributos aceitos para importação

    //métodos para receber e retornar os dados
    Private Method setCodeCEST(value) //método para receber um dado carácter com o código de CEST
    Private Method setError(value) //método para receber um dado carácter com o erro ocorrido
    Private Method setFederativeUnitDestination(value) //método para receber um dado carácter com o estado de destino
    Private Method setFederativeUnitOrigin(value) //método para receber um dado carácter com o estado de origem
    Private Method setJsonData()                 as logical //método para converter dados recebidos em json
    Private Method setNcm(value) //método para receber um dado carácter com o ncm
    Private Method setOriginProduct(value) //método para receber um dado carácter com a origem do produto
    Private Method setRelationTaxProtheusAndSystax() //método para receber um dado carácter com a origem do produto
    
    Public Method getCodeCEST()                  as character //método para retornar o código de CEST
    Public Method getContNCM()                   as numeric //método para retornar o contador de ncm
    Public Method getContRulesMajoracao()        as numeric //método para retornar o contador de regras de majoração
    Public Method getContRulesMva()              as numeric //método para retornar o contador de regras de mva
    Public Method getContRulesPauta()            as numeric //método para retornar o contador de regras de pauta
    Public Method getContTax()                   as numeric //método para retornar o contador de tributos
    Public Method getData()                      as character //método para dado carácter com conteúdo json recebido
    Public Method getError()                     as character //método para retornar o erro ocorrido
    Public Method getFederativeUnitDestination() as character //método para retornar o estado de destino
    Public Method getFederativeUnitOrigin()      as character //método para retornar o estado de origem
    Public Method getIntegrativeAliquot()        as logical //método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de alíquota
    Public Method getIntegrativeBaseCalc()       as logical //método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de base de cálculo
    Public Method getIntegrativeNCM()            as logical //método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de ncm
    Public Method getIntegrativeWriting()        as logical //método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de escrituração fiscal
    Public Method getJsonData()                  as json //método para retornar o objeto json
    Public Method getjTaxesReturn()              as json //método para retornar o objeto json com os tributos    
    Public Method getNcm()                       as character //método para retornar o ncm    
    Public Method getOriginProduct()             as character //método para retornar a origem do produto    
    Public Method setData(value) //método para receber um dado carácter com conteúdo json
    Public Method setCodeTaxIPI(value) //método para receber um dado carácter com o código de IPI
    Public Method getRegisterAliquot()           as array //método para retornar o registro de alíquota
    Public Method getRegisterBase()              as array //método para retornar o registro de base de cálculo
    Public Method setMockServer(value) //método para receber um dado carácter com a url do mock server


EndClass

/*/{Protheus.doc} new

    método construtor da classe IntegrativeClassifierSystax

    @author Rafael Oliveira
    @since 30/11/2023
    @version 12.1.2310
    @param cValue, character, conteúdo em formato json
    @return objeto da classe IntegrativeClassifierSystax
    /*/
Method new(cValue as character ) CLASS IntegrativeClassifierSystax

    //Inicializa variÃ¡veis
    Self:aRegisterAliquot           := {}
    Self:aRegisterBase              := {}
    self:cCodeCEST                  := ""
    self:cCodeNcm                   := ""
    Self:cCodeTaxIPI                := ""
    self:cData                      := ""
    self:cError                     := ""
    self:cFederativeUnitDestination := ""
    self:cFederativeUnitOrigin      := ""
    Self:cMockServer                := ""
    self:cOriginProduct             := ""
    self:JsonData                   := JsonObject():New()
    self:jTaxesReturn               := JsonObject():New()
    Self:lExistIPI                  := .F.
    Self:lIntegrativeAliquot        := .F.
    Self:lIntegrativeBaseCalc       := .F.
    self:lIntegrativeNCM            := .F.
    Self:lIntegrativeWriting        := .F.
    self:lReady                     := .F.
    self:nContAliquot               := 0
    self:nContBaseCalc              := 0
    self:nContRulesMajoracao        := 0
    self:nContRulesMva              := 0
    self:nContRulesPauta            := 0
    self:nContTax                   := 0
    Self:setRelationTaxProtheusAndSystax()

    // Se for passado um valor para o parâmetro value executa o método organizador de dados
    IF !empty(cValue)
        self:setData(cValue)    
    EndIf

    self:aNCMs := {}
    self:aBases := {}
    self:aAliquotas := {}
    
Return self

// métodos para receber e retornar os dados

/*/{Protheus.doc} setData

    método para receber um dado carácter com conteúdo json
    
    @param cValue, character, conteúdo em formato json
    @return objeto da classe IntegrativeClassifierSystax
/*/

Method setData(cValue as character) CLASS IntegrativeClassifierSystax

    self:cData := cValue
    
    self:setJsonData()

Return self

/*/{Protheus.doc} getData

    método para dado carácter com conteúdo json recebido
    
    @return cData, character, conteúdo em formato json

/*/

Method getData() as character CLASS IntegrativeClassifierSystax
Return self:cData

/*/{Protheus.doc} setJsonData

    método para converter dados recebidos em json
    
    @return objeto da classe IntegrativeClassifierSystax
/*/

Method setJsonData() CLASS IntegrativeClassifierSystax
    Local cError as character

    iF valType(self:JsonData) == 'J'
        FREEOBJ(self:JsonData)         
        self:JsonData := JsonObject():New()
    Endif

    cError := self:JsonData:FromJson(self:cData)

    //Valida se a conversão foi realizada com sucesso
    IF !empty(cError) .or. !Self:getJsonData():hasProperty('cenario')
        
        self:setError("Erro ao converter o conteúdo recebido em json ou o conteúdo recebido não é um json válido. " + AllToChar(cError))

        self:lReady := .F.

    else
        self:lReady := .T.
        self:organizeJson()
    Endif

Return self:lReady

/*/{Protheus.doc} setRelationTaxProtheusAndSystax

    método para receber um dado carácter com a relação de tributos do Protheus e Systax
    
    @return objeto da classe IntegrativeClassifierSystax

/*/


Method setRelationTaxProtheusAndSystax() CLASS IntegrativeClassifierSystax

                                    //   Protheus : Systax
    Self:aRelationTaxProtheusAndSystax :={{"icms", "icms"},;
                                        {"icmsst" , "icms_st"},;
                                        {"difal"  , "icms_uf_dest"},;
                                        {"fecpic" , "fcp"},;
                                        {"fcpst"  , "fcp"},;
                                        {"fcpcmp" , "fcp"},;
                                        {"pismaj" , "pismaj"},;
                                        {"cofmaj" , "cofmaj"},;
                                        {"pisst"  , "pisst"},;
                                        {"cofst"  , "cofst"},;
                                        {"antec"  , "antec"},;
                                        {"cmp"    , "cmp"},;
                                        {"ipi"    , "ipi"},;
                                        {"ii"     , "ii"},;
                                        {"pis"    , "pis"},;
                                        {"cof"    , "cofins"}}


Return

/*/{Protheus.doc} getJsonData

    método para retornar o objeto json
    
    @return JsonData, json, objeto json

/*/

Method getJsonData() as json CLASS IntegrativeClassifierSystax
Return self:JsonData

/*/{Protheus.doc} setNcm

    método para receber um dado carácter com o ncm
    
    @param cValue, character, ncm
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setNcm(cValue as character) CLASS IntegrativeClassifierSystax
    self:cCodeNcm := cValue

Return self

/*/{Protheus.doc} getNcm

    método para retornar o ncm
    
    @return cCodeNcm, character, ncm

/*/

Method getNcm() as character CLASS IntegrativeClassifierSystax
Return self:cCodeNcm

/*/{Protheus.doc} setFederativeUnitOrigin

    método para receber um dado carácter com o estado de origem
    
    @param cValue, character, estado de origem
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setFederativeUnitOrigin(cValue as character) CLASS IntegrativeClassifierSystax
    self:cFederativeUnitOrigin := cValue
Return self

/*/{Protheus.doc} getFederativeUnitOrigin

    método para retornar o estado de origem
    
    @return cFederativeUnitOrigin, character, estado de origem

/*/

Method getFederativeUnitOrigin() as character CLASS IntegrativeClassifierSystax
Return self:cFederativeUnitOrigin

/*/{Protheus.doc} setFederativeUnitDestination

    método para receber um dado carácter com o estado de destino
    
    @param cValue, character, estado de destino
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setFederativeUnitDestination(cValue as character) CLASS IntegrativeClassifierSystax
    self:cFederativeUnitDestination := cValue
Return self

/*/{Protheus.doc} getFederativeUnitDestination

    método para retornar o estado de destino
    
    @return cFederativeUnitDestination, character, estado de destino

/*/

Method getFederativeUnitDestination() as character CLASS IntegrativeClassifierSystax
Return self:cFederativeUnitDestination

/*/{Protheus.doc} setOriginProduct

    método para receber um dado carácter com a origem do produto
    
    @param cValue, character, origem do produto
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setOriginProduct(cValue as character) CLASS IntegrativeClassifierSystax
    self:cOriginProduct := cValue
Return self

/*/{Protheus.doc} getOriginProduct

    método para retornar a origem do produto
    
    @return cOriginProduct, character, origem do produto

/*/ 

Method getOriginProduct() as character CLASS IntegrativeClassifierSystax
Return self:cOriginProduct


/*/{Protheus.doc} setCodeCEST

    método para receber um dado carácter com o código de CEST
    
    @param cValue, character, código de CEST
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setCodeCEST(cValue as character) CLASS IntegrativeClassifierSystax
    self:cCodeCEST := cValue
Return self

/*/{Protheus.doc} getCodeCEST

    método para retornar o código de CEST
    
    @return cCodeCEST, character, código de CEST

/*/

Method getCodeCEST() as character CLASS IntegrativeClassifierSystax
Return self:cCodeCEST


/*/{Protheus.doc} setError

    método para receber um dado carácter com o erro ocorrido
    
    @param cValue, character, erro ocorrido
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setError(cValue as character) CLASS IntegrativeClassifierSystax
    self:cError := cValue
Return self

/*/{Protheus.doc} getError

    método para retornar o erro ocorrido
    
    @return cError, character, erro ocorrido

/*/

Method getError() as character CLASS IntegrativeClassifierSystax
Return self:cError

/*/{Protheus.doc} getjTaxesReturn

    método para retornar o objeto json com os tributos
    
    @return jTaxes, json, objeto json com os tributos

/*/

Method getjTaxesReturn() as json CLASS IntegrativeClassifierSystax
Return self:jTaxesReturn

/*/{Protheus.doc} getIntegrativeNCM

    método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de ncm
    
    @return lIntegrativeNCM, logical, indica se o conteúdo recebido possui dados de cadastro de ncm

/*/

Method getIntegrativeNCM() as logical CLASS IntegrativeClassifierSystax
Return self:lIntegrativeNCM

/*/{Protheus.doc} getIntegrativeAliquot

    método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de alíquota
    
    @return lIntegrativeAliquot, logical, indica se o conteúdo recebido possui dados de cadastro de alíquota

/*/

Method getIntegrativeAliquot() as logical CLASS IntegrativeClassifierSystax
Return self:lIntegrativeAliquot

/*/{Protheus.doc} getIntegrativeBaseCalc

    método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de base de cálculo
    
    @return lIntegrativeBaseCalc, logical, indica se o conteúdo recebido possui dados de cadastro de base de cálculo

/*/

Method getIntegrativeBaseCalc() as logical CLASS IntegrativeClassifierSystax
Return self:lIntegrativeBaseCalc

/*/{Protheus.doc} getIntegrativeWriting

    método para retornar um dado lógico indicando se o conteúdo recebido possui dados de cadastro de escrituração fiscal
    
    @return lIntegrativeWriting, logical, indica se o conteúdo recebido possui dados de cadastro de escrituração fiscal

/*/

Method getIntegrativeWriting() as logical CLASS IntegrativeClassifierSystax
Return self:lIntegrativeWriting

/*/{Protheus.doc} getContRulesMajoracao

    método para retornar o contador de regras de majoração
    
    @return nContRulesMajoracao, numeric, contador de regras de majoração

/*/

Method getContRulesMajoracao() as numeric CLASS IntegrativeClassifierSystax
Return self:nContRulesMajoracao

/*/{Protheus.doc} getContRulesMva

    método para retornar o contador de regras de mva
    
    @return nContRulesMva, numeric, contador de regras de mva

/*/

Method getContRulesMva() as numeric CLASS IntegrativeClassifierSystax
Return self:nContRulesMva

/*/{Protheus.doc} getContRulesPauta

    método para retornar o contador de regras de pauta
    
    @return nContRulesPauta, numeric, contador de regras de pauta

/*/

Method getContRulesPauta() as numeric CLASS IntegrativeClassifierSystax
Return self:nContRulesPauta

/*/{Protheus.doc} getContTax

    método para retornar o contador de tributos
    
    @return nContTax, numeric, contador de tributos

/*/

Method getContTax() as numeric CLASS IntegrativeClassifierSystax
Return self:nContTax

/*/{Protheus.doc} getContNCM

    método para retornar o contador de ncm
    
    @return nContNCM, numeric, contador de ncm

/*/

Method getContNCM() as numeric CLASS IntegrativeClassifierSystax
Return len(self:jTaxesReturn['ncm']:getnames())


/*/{Protheus.doc} cCodeTaxIPI

    método para receber um dado carácter com o código de IPI
    
    @param cValue, character, código de IPI
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setCodeTaxIPI(cValue as character) CLASS IntegrativeClassifierSystax
    self:cCodeTaxIPI := upper(cValue)
Return self

/*/{Protheus.doc} getRegisterAliquot

    método para retornar o registro de alíquota
    
    @return aRegisterAliquot, array, array com os dados de cadastro de alíquota

/*/

Method getRegisterAliquot() as array CLASS IntegrativeClassifierSystax
Return self:aRegisterAliquot

/*/{Protheus.doc} getRegisterBase

    método para retornar o registro de base de cálculo
    
    @return aRegisterBase, array, array com os dados de cadastro de base de cálculo

/*/

Method getRegisterBase() as array CLASS IntegrativeClassifierSystax
Return self:aRegisterBase

/*/{Protheus.doc} setMockServer

    método para receber um dado carácter com a url do mock server
    
    @param cValue, character, url do mock server
    @return objeto da classe IntegrativeClassifierSystax

/*/

Method setMockServer(cValue as character) CLASS IntegrativeClassifierSystax
    self:cMockServer := cValue
Return self

/*/{Protheus.doc} organizeJson

    método para organizar os dados recebidos em json para o cadastro dos tributos
    
    @author Rafael
    @since 30/11/2023
    @version 12.1.2310        
/*/
Method organizeJson() as  json CLASS IntegrativeClassifierSystax

    
    Local jScenario as json //Objeto json com cenario    
    Local nC, nR    as numeric //Contador

    //METHOD CADNCM_001(cCodNCM,cCodTrib,cUFOri,cUFDest,cData,cUnidMed,nVlTribPt,nVlTribMVA,cOrigem,nMajora,nMjAux,aTributos) CLASS FISCadRegraNCMTestCase
    //Valida se o conteúdo recebido é um json e esta pronto para ser processado alíquota
    IF self:lReady
        
        //Percorre o objeto json para cadastrar os tributos
        for nC := 1 to len(self:getJsonData()['cenario'])

            jScenario := self:getJsonData()['cenario'][nC]

            self:setFederativeUnitOrigin(jScenario['uf_origem'])
            self:setFederativeUnitDestination(jScenario['uf_destino'])        

            For nR := 1 to Len(jScenario['retorno'])

                Self:processJsonReturns(jScenario['retorno'][nR])
                
            Next nX
        Next nY
    Endif
    
Return self:jTaxesReturn

/*/{Protheus.doc} processJsonReturns

    método para processar os retornos do json
    
    @author Rafael
    @since 08/12/2023
    @version 12.1.2310
    @param jJsonRetorno, json, objeto json com os dados do tributo
    @return jTaxesReturn, json, objeto json com os tributos
    /*/
Method processJsonReturns(jJsonRetorno) as json CLASS IntegrativeClassifierSystax

    Local aTaxas as array //Array com tributos
    Local nT     as numeric //Contador    

    self:setNcm(cValToChar(jJsonRetorno['ncm']))
    self:setOriginProduct(cValToChar(jJsonRetorno['origem_produto']))        
    self:lExistIPI := .F.

    If jJsonRetorno:hasProperty('cest')
        self:setCodeCEST(jJsonRetorno['cest'])
    Else 
        self:setCodeCEST("")
    Endif

    //Verifica se o conteúdo recebido possui dados de cadastro de IPI para o cenario atual
    If jJsonRetorno:hasProperty('ipi')
        If jJsonRetorno['ipi']:hasProperty('aliquota')
            self:lExistIPI := .T.
        Endif
    Endif

    IF self:validateReceivedData()

        aTaxas := jJsonRetorno:getNames()

        For nT := 1 to len(aTaxas)
            If valType(jJsonRetorno[aTaxas[nT]]) == 'J'
                //Adiciona os tributos no objeto json jTaxesReturn                
                self:addTaxes(self:taxesConvert(aTaxas[nT]),jJsonRetorno[aTaxas[nT]])            
            Endif
        Next nT

        aSize(aTaxas, 0)
    Endif

Return self:jTaxesReturn

/*/{Protheus.doc} addTaxes
    
    método para adicionar os tributos no objeto json jTaxesReturn

    Esta rotina pode trabalhar com recursão, pois o tributo pode ter um tributo dentro dele, como é o caso do ICMS ST.

    @author Rafael Oliveira
    @since 11/12/2023
    @version 12.1.2310
    @param cTax, character, tributo a ser adicionado
    @param jTax, json, objeto json com os dados do tributo    
    @return lReturn, logical, indica se os tributos foram adicionados com sucesso ou não    
/*/
Method addTaxes(cTax as character, jTax as json) as logical CLASS IntegrativeClassifierSystax    

    Local aTaxas      as Array
    Local jsonProcess as json
    Local lReturn     as logical
    Local nT          as numeric

    cCodeCEST                  := Self:getCodeCEST()
    cCodeNcm                   := Self:getNcm()
    cFederativeUnitDestination := Self:getFederativeUnitDestination()
    cFederativeUnitOrigin      := Self:getFederativeUnitOrigin()
    cOriginProduct             := Self:getOriginProduct()
    
    lReturn                    := .T.
    aTaxas                     := {}

    //Valida se o tributo é válido e se os dados recebidos são válidos conforme cadastros no Protheus
    IF self:validTaxes(cTax)

        aTaxas := jTax:getNames()        
        
        //Inicializa o objeto json jTaxesReturn com NCM do cenário atual
        jsonProcess := Self:initializeTaxesReturn()

        //Adiciona os tributos no objeto json jTaxesReturn        
        If !jsonProcess:hasProperty(cTax)
            jsonProcess[cTax] := JsonObject():New()

            //Incrementa o contador de tributos
            self:nContTax := self:nContTax + 1
        Endif        

        //Processa dados de cadastro de ncm NCM
        Self:ProcessNCM(jsonProcess[cTax], jTax, cTax )

        //Processa dados de cadastro de Base de Cálculo
        Self:ProcessBaseCalc(jsonProcess[cTax], jTax, cTax)

        //Processa dados de cadastro de Alíquota
        Self:ProcessAliquot(jsonProcess[cTax], jTax, cTax)

        //Processa dados de cadastro de Escrituração Fiscal
        Self:ProcessFiscalWriting(jsonProcess[cTax], jTax, cTax)

        //Processa dados de cadastro de Calculo do tributo
        Self:ProcessTaxCalc(jsonProcess[cTax], jTax, cTax)

        // Processa os tributos que pertencem ao tributo atual, como é o caso do ICMS ST
        For nT := 1 to len(aTaxas)
            IF valType(jTax[aTaxas[nT]]) == 'J'
                //Adiciona os tributos no objeto json jTax                
                self:addTaxes(self:taxesConvert(aTaxas[nT]),jTax[aTaxas[nT]])
            Endif
        Next nT
    else
        lReturn := .F.
    Endif

    ASIZE( aTaxas, 0 )

Return lReturn

/*/{Protheus.doc} initializeTaxesReturn
    
    método para inicializar o objeto json jTaxesReturn

    @author user
    @since 11/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method initializeTaxesReturn() as json CLASS IntegrativeClassifierSystax
    
    Local cCodeNcm                   as character
    Local cCodeCEST                  as character
    Local cFederativeUnitDestination as character
    Local cFederativeUnitOrigin      as character
    Local cOriginProduct             as character    

    cCodeNcm := Self:getNcm()
    
    // Inicializa o objeto json jTaxesReturn
    IF !valType(Self:jTaxesReturn['ncm']) == 'J'
        Self:jTaxesReturn['ncm'] := JsonObject():New()
    Endif

    IF !Self:jTaxesReturn['ncm']:hasProperty(cCodeNcm)
        Self:jTaxesReturn['ncm'][cCodeNcm] := JsonObject():New()

        cCodeCEST                  := Self:getCodeCEST()        
        cFederativeUnitDestination := Self:getFederativeUnitDestination()
        cFederativeUnitOrigin      := Self:getFederativeUnitOrigin()
        cOriginProduct             := Self:getOriginProduct()        

        jsonProcess := Self:jTaxesReturn['ncm'][cCodeNcm]

        // Adiciona os dados do NCM no objeto json jTaxesReturn
        jsonProcess['ncm']                        := cCodeNcm
        jsonProcess['cFederativeUnitOrigin']      := cFederativeUnitOrigin
        jsonProcess['cFederativeUnitDestination'] := cFederativeUnitDestination
        jsonProcess['cOriginProduct']             := cOriginProduct        
        jsonProcess['cCodeCEST']                  := cCodeCEST
    Else
        jsonProcess := Self:jTaxesReturn['ncm'][cCodeNcm]
    Endif
    
Return jsonProcess


/*/{Protheus.doc} processNCM
    
    método para processar dados de cadastro de ncm NCM
    
    @author Rafael Oliveira
    @since 08/12/2023
    @version 12.1.2310
    @param jTax, json, objeto json com os dados do tributo
    @return jProtheus, json, objeto json com os dados de cadastro de ncm
/*/

Method processNCM(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax

    //Processa dados de Majoração
    Self:AddMajoracao(jProtheus, jTaxSystax, cTax)

    //Processa dados de MVA
    self:AddMva(jProtheus, jTaxSystax, cTax)    

    //Processa dados de Valor de Pauta
    self:AddPauta(jProtheus, jTaxSystax, cTax)


Return jProtheus

/*/{Protheus.doc} AddMajoracao
    (long_description)
    @author user
    @since 13/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method AddMajoracao(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax
    Local cTaxFecpic            as character //Tributo FECPIC
    Local jMajoracao            as json //Objeto json com majoração    
    Local cNumRules             as character //Número de regras de Majoração
    Local cHashTax              as character //Hash do tributo    
    Local cVigenciaAte          as character //Vigencia ate
    Local cVigencia_de          as character //Vigencia de
    Local nPercentualMajoracao  as numeric //Percentual de majoração

    IF jTaxSystax:hasProperty('fcp')
        nPercentualMajoracao := self:convertValue(jTaxSystax['fcp'])

        If ( nPercentualMajoracao > 0 )
            Self:addProperty(jProtheus, 'ncm')

            cVigencia_de := Self:MountVigencia(jTaxSystax, 'vigencia_de')
            cVigenciaAte := Self:MountVigencia(jTaxSystax, 'vigencia_ate')

            //Controle para evitar duplicidade de tributos e processamento desnecessário
            cHashTax := Md5( Self:getNcm() + cTax + self:getFederativeUnitOrigin() + self:getFederativeUnitDestination() + cVigencia_de + cVigenciaAte + self:getCodeCEST())
            
            cNumRules := 'majoracao_'+cHashTax

            //Verifica se o tributo já foi processado
            If !jProtheus['ncm']:hasProperty(cNumRules)
                
                //Incrementa o contador de regras de tributos
                self:nContRulesMajoracao := self:nContRulesMajoracao + 1
                
                //Adiciona estrutura de majoração no objeto json jProtheus
                jProtheus['ncm'][cNumRules] := { 'uf_origem' : '' ,;
                                                'uf_destino' : '' ,;
                                                'vigencia_de' : '' ,;
                                                'vigencia_ate' : Self:MountVigencia(jTaxSystax, 'vigencia_ate' ) ,;
                                                'codigo_cest' : '' ,;
                                                'percentual_majoracao' :0,;
                                                'indice_auxiliar_majoracao' :0,;
                                                'origem_produto' : ''}

                jMajoracao := jProtheus['ncm'][cNumRules]

                //Alterara as propriedades existentes no objeto json da Systax 
                jMajoracao[ 'percentual_majoracao' ] := nPercentualMajoracao

                jMajoracao[ 'uf_origem' ]      := self:getFederativeUnitOrigin()
                jMajoracao[ 'uf_destino' ]     := self:getFederativeUnitDestination()
                jMajoracao[ 'origem_produto' ] := self:getOriginProduct()
                jMajoracao[ 'codigo_cest' ]    := self:getCodeCEST()
                jMajoracao[ 'vigencia_de' ]    := cVigencia_de
                jMajoracao[ 'vigencia_ate' ]   := cVigenciaAte
                
                
                /*
                If jTaxSystax:hasProperty('indice_auxiliar_majoracao')

                    jMajoracao['indice_auxiliar_majoracao'] := Self:convertValue(jTaxSystax[ 'indice_auxiliar_majoracao' ])

                Endif
                */

                //Adiciono o json para calculo de majoração no objeto json jTaxSystem para posterior processamento deste tributo no Protheus
                If (.not. self:IsTaxFecp(cTax))
                    
                    //Adiciona estrutura de majoração no objeto json jTaxSystem
                    If Self:relationTaxWhitMajoracao(cTax, @cTaxFecpic)
                        self:AddtaxMajoracao(cTaxFecpic, jTaxSystax)
                    Endif
                Endif

            Endif
        Endif
    Endif
    
Return jProtheus

/*/{Protheus.doc} relationTaxWhitMajoracao
    
    método validar se o tributo possui majoração e relacionar com o tributo FECP (Fundo de Combate à Pobreza)

    @author Rafael Oliveira
    @since 21/12/2023
    @version 12.1.2310
    @param cTax, character, tributo a ser relacionado
    @param cTaxFecpic, character, tributo FECP
    @return lAddNewTex, logical, indica se deve adicionar um novo tributo no objeto json jTaxSystem
    /*/
Method relationTaxWhitMajoracao(cTax, cTaxFecpic) as logical CLASS IntegrativeClassifierSystax

    Local cTaxFecpic as character //Tributo FECPIC
    Local lAddNewTex as logical //Indica se deve adicionar um novo tributo no objeto json jTaxSystem

    Do CASE
        Case cTax == 'icms'
            cTaxFecpic := 'fecpic'
            lAddNewTex := .T.           
        Case cTax == 'icmsst'
            cTaxFecpic := 'fcpst'  
            lAddNewTex := .T.          
        Case cTax == 'difal'
            cTaxFecpic := 'fcpcmp'
            lAddNewTex := .T.
    EndCase
    
Return lAddNewTex

/*/{Protheus.doc} IsTaxFecp

    método para verificar se o tributo é FECP (Fundo de Combate à Pobreza)

    @author Rafael
    @since 21/12/2023
    @version 12.1.2310
    @param cTax, character, tributo a ser verificado
    @return lIsTaxFecp, logical, indica se o tributo é FECP
/*/
Method IsTaxFecp(cTax) as logical CLASS IntegrativeClassifierSystax
    Local lIsTaxFecp as logical //Indica se o tributo é FECP

    lIsTaxFecp := (cTax == 'fecpic' .or. cTax == 'fcpst' .or. cTax == 'fcpcmp')

Return lIsTaxFecp

/*/{Protheus.doc} AddtaxMajoracao
    (long_description)
    @author user
    @since 13/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method AddtaxMajoracao(cTax as character, jTaxSystax as json) as json CLASS IntegrativeClassifierSystax
    Local jTaxMajoracao as json //Objeto json com majoração

    //Adiciona estrutura de majoração no objeto json jTaxMajoracao
    jTaxMajoracao                        := JsonObject():New()
    jTaxMajoracao[ 'bc_composicao_fcp' ] := jTaxSystax[ 'bc_composicao_fcp' ]    
    jTaxMajoracao[ 'generico' ]          := jTaxSystax[ 'generico' ]
    jTaxMajoracao[ 'vigencia_de' ]       := jTaxSystax[ 'vigencia_de' ]
    jTaxMajoracao[ 'observacoes' ]       := jTaxSystax[ 'observacoes' ]
    jTaxMajoracao[ 'dispositivo_legal' ] := jTaxSystax[ 'dispositivo_legal' ]
    jTaxMajoracao[ 'inf_adicionais' ]    := jTaxSystax[ 'inf_adicionais' ]
    jTaxMajoracao[ 'origem_produto' ]    := self:getOriginProduct()

    jTaxMajoracao[ 'fcp' ] := self:convertValue(jTaxSystax[ 'fcp' ])
    jTaxMajoracao['vigencia_ate'] := Self:MountVigencia(jTaxSystax, 'vigencia_ate')

    /*
    If jTaxSystax:hasProperty('indice_auxiliar_majoracao')

        jTaxMajoracao[ 'indice_auxiliar_majoracao' ] := self:convertValue(jTaxSystax[ 'indice_auxiliar_majoracao' ])

    Endif
    */
    
    self:addTaxes(cTax, jTaxMajoracao)

    FREEOBJ( jTaxMajoracao )
Return jTaxSystax

/*/{Protheus.doc} methodName
    (long_description)
    @author user
    @since 13/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method AddMva(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax
    Local jMVA              as json //Objeto json com MVA
    Local cNumRules         as character //Número de regras de MVA
    Local cHashTax          as character //Hash do tributo    
    Local cVigenciaAte      as character //Vigencia ate
    Local cVigencia_de      as character //Vigencia de

    IF jTaxSystax:hasProperty('mva')

        Self:addProperty(jProtheus, 'ncm')

        //Prepara vigencia de
        cVigencia_de := Self:MountVigencia(jTaxSystax, 'vigencia_de')
        cVigenciaAte := Self:MountVigencia(jTaxSystax, 'vigencia_ate')

        cHashTax := Md5( Self:getNcm() + cTax + self:getFederativeUnitOrigin() + self:getFederativeUnitDestination() + cVigencia_de + cVigenciaAte + self:getCodeCEST() + self:getOriginProduct())

        cNumRules := 'mva_'+cHashTax

        If !jProtheus['ncm']:hasProperty(cNumRules)
            
            //Incrementa o contador de regras de tributos
            self:nContRulesMva := self:nContRulesMva + 1

            //Adiciona estrutura de MVA no objeto json jProtheus
            jProtheus[ 'ncm' ][cNumRules] := { 'uf_origem' : '' ,;
                                                'uf_destino' : '' ,;
                                                'vigencia_de' : '' ,;
                                                'vigencia_ate' : Self:MountVigencia(jTaxSystax, 'vigencia_ate' ) ,;
                                                'origem_produto' : '' ,;
                                                'codigo_cest' : '' ,;
                                                'mva' :0,;
                                                'indice_auxiliar_mva' :0}

            jMVA := jProtheus['ncm'][cNumRules]
            
            //Alterara as propriedades existentes no objeto json da Systax
            jMVA[ 'mva' ]            := Self:convertValue(jTaxSystax[ 'mva' ])
            jMVA[ 'uf_origem' ]      := self:getFederativeUnitOrigin()
            jMVA[ 'uf_destino' ]     := self:getFederativeUnitDestination()
            jMVA[ 'origem_produto' ] := self:getOriginProduct()
            jMVA[ 'codigo_cest' ]    := self:getCodeCEST()
            jMVA[ 'vigencia_de' ]    := cVigencia_de
            jMVA[ 'vigencia_ate' ]   := cVigenciaAte
            

            /*
            If jTaxSystax:hasProperty('indice_auxiliar_mva')

                jMVA[ 'indice_auxiliar_mva' ] := Self:convertValue(jTaxSystax[ 'indice_auxiliar_mva' ])

            Endif
            */
        Endif
    Endif
Return jProtheus

/*/{Protheus.doc} methodName
    (long_description)
    @author user
    @since 13/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method AddPauta(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax
    Local jPauta            as json //Objeto json com valor de pauta
    Local cNumRules         as character //Número de regras de pauta
    Local cHashTax          as character //Hash do tributo    
    Local cVigenciaAte      as character //Vigencia ate
    Local cVigencia_de      as character //Vigencia de


    IF jTaxSystax:hasProperty('valor_pauta')

        Self:addProperty(jProtheus, 'ncm')

        //Prepara vigencia de
        cVigencia_de := Self:MountVigencia(jTaxSystax, 'vigencia_de')
        cVigenciaAte := Self:MountVigencia(jTaxSystax, 'vigencia_ate')

        cHashTax := Md5( Self:getNcm() + cTax + self:getFederativeUnitOrigin() + self:getFederativeUnitDestination()    + cVigencia_de + cVigenciaAte + self:getCodeCEST()  + Self:convertUnitMeasure(jTaxSystax['unidade_pauta']))

        cNumRules := 'pauta_'+cHashTax

        If !jProtheus['ncm']:hasProperty(cNumRules)

            //Incrementa o contador de regras de tributos
            self:nContRulesPauta := self:nContRulesPauta + 1

            //Adiciona estrutura de Valor de Pauta no objeto json jProtheus
            jProtheus['ncm'][cNumRules] := { 'uf_origem' : '' ,;
                                            'uf_destino' : '' ,;
                                            'vigencia_de' : '' ,;
                                            'vigencia_ate' : Self:MountVigencia(jTaxSystax, 'vigencia_ate') ,;
                                            'unidade_medida' : '' ,;
                                            'codigo_cest' : '' ,;
                                            'valor_pauta' :0,;
                                            'origem_produto' : ''}
                                            
            jPauta := jProtheus['ncm'][cNumRules]

            jPauta[ 'valor_pauta' ] := Self:convertValue(jTaxSystax[ 'valor_pauta' ])

            //Alterara as propriedades existentes no objeto json da Systax        
            jPauta[ 'uf_origem' ]      := self:getFederativeUnitOrigin()
            jPauta[ 'uf_destino' ]     := self:getFederativeUnitDestination()
            jPauta[ 'codigo_cest' ]    := self:getCodeCEST()
            jPauta[ 'unidade_medida' ] := Self:convertUnitMeasure(jTaxSystax[ 'unidade_pauta' ])
            jPauta[ 'origem_produto' ] := self:getOriginProduct()
            jPauta[ 'vigencia_de' ]    := cVigencia_de
            jPauta[ 'vigencia_ate' ]   := cVigenciaAte            
            
        Endif
    Endif
Return jProtheus

/*/{Protheus.doc} convertUnitMeasure
    
    método para converter unidade de medida

    @author Rafael Oliveira
    @since 18/12/2023
    @version 12.1.2310
    @param cUnitMeasure, character, unidade de medida
    @return cReturn, character, unidade de medida convertida    
    /*/
Method convertUnitMeasure(cUnitMeasure) as character CLASS IntegrativeClassifierSystax

    If UPPER(cUnitMeasure) == UPPER('sc50kg')
        cUnitMeasure := 'S5'
    ElseIf UPPER(cUnitMeasure) == UPPER('saca')
        cUnitMeasure := 'SA'
    Endif
    
Return cUnitMeasure

/*/{Protheus.doc} addProperty

    método para adicionar os dados de cadastro de ncm no objeto json jProtheus
    
    @author Rafael Oliveira
    @since 18/12/2023
    @version 12.1.2310
    @param jProtheus, json, objeto json com os dados do tributo
    
    /*/
Method addProperty(jProtheus, cProperty) as json CLASS IntegrativeClassifierSystax

    If !jProtheus:hasProperty(cProperty)
        jProtheus[cProperty] := JsonObject():New()
        
        Do Case
            Case cProperty == 'ncm'
                //Indica que o conteúdo recebido possui dados de cadastro de ncm                
                self:lIntegrativeNCM := .T.
            Case cProperty == 'baseCalc'
                //Indica que o conteúdo recebido possui dados de cadastro de base de cálculo
                Self:lIntegrativeBaseCalc := .T.
            Case cProperty == 'aliquot'
                //Indica que o conteúdo recebido possui dados de cadastro de alíquota
                Self:lIntegrativeAliquot := .T.
            //Case cProperty == 'writing'
            //    //Indica que o conteúdo recebido possui dados de cadastro de escrituração fiscal
            //    Self:lIntegrativeWriting := .T.
        endCase
    Endif
    
Return jProtheus


/*/{Protheus.doc} processBaseCalc
    
    método para processar dados de cadastro de Base de Cálculo
    
    @param jTax, json, objeto json com os dados do tributo
    @return jBaseCalc, json, objeto json com os dados de cadastro de Base de Cálculo

/*/

Method processBaseCalc(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax
    Local cBaseCalc as character //Base de Cálculo
    Local jBaseCalc as json //Objeto json com base de cálculo
    Local cProperty as character //Propriedade do tributo
    Local cCodigo   as character //Código do tributo
    Local cFormula  as character //Fórmula
    Local nPcRed   as numeric //Percentual de redução
    Local cDescricao   as character //Descrição
    Local cFormulaOrigin as character //Fórmula original
    Local cCodeTaxIPI as character //Código do tributo IPI
    
    
    Do case 
        Case .not. self:IsTaxFecp(cTax) .and. jTaxSystax:hasProperty('bc_composicao')
            cBaseCalc := cValtochar(jTaxSystax['bc_composicao'])
        Case self:IsTaxFecp(cTax) .and. jTaxSystax:hasProperty('bc_composicao_fcp')
            cBaseCalc := cValtochar(jTaxSystax['bc_composicao_fcp'])        
    Endcase

    If .not. EMPTY( cBaseCalc ) .and. (cTax == 'icmsst' .or. cTax == 'fcpst' .or. cTax == 'fcpcmp' .or. cTax == 'fecpic' .or. cTax == 'difal')

        cProperty := 'baseCalc'

        cCodigo := 'baseCalc_' + md5(cBaseCalc)

        Self:addProperty(jProtheus, cProperty)

        IF .Not. jProtheus[cProperty]:hasProperty(cCodigo)

            //Incrementa o contador de base de cálculo
            self:nContBaseCalc := self:nContBaseCalc + 1
            
            //adiciona estrutura de base de cálculo no objeto json jProtheus
            jProtheus[cProperty][cCodigo] := JsonObject():New()
            jBaseCalc := jProtheus[cProperty][cCodigo]

            //Monta a base de cálculo
            cCodeTaxIPI := ALLTRIM(upper(self:cCodeTaxIPI))
            oRuleBaseCalc := FISGravaBase():New()
            cFormulaOrigin := self:MountCalcBase(val(cBaseCalc), cTax, oRuleBaseCalc)
            cFormula := cFormulaOrigin
            
            //Ajusta a fórmula de base de cálculo
            cDescricao := oRuleBaseCalc:descricaoBase(cBaseCalc)
            cFormula := oRuleBaseCalc:aJustaFormulaIPI(self:lExistIPI, cCodeTaxIPI, @cDescricao, cFormula )
            cFormula := oRuleBaseCalc:aJustaFormulaReducao(@cDescricao, cFormula, @nPcRed, jTaxSystax)

            If .not. EMPTY( cFormula )

                //Adiciona os dados de cadastro de base de cálculo no objeto json jBaseCalc
                jBaseCalc[ 'description' ]    := cDescricao
                jBaseCalc[ 'formula' ]        := cFormula
                jBaseCalc[ 'value_origin' ]   := '11'
                jBaseCalc[ 'reduction_base' ] := nPcRed
                jBaseCalc[ 'exist_ipi' ]      := self:lExistIPI
                jBaseCalc[ 'taxipi' ]         := cCodeTaxIPI

            Endif

            FwFreevar(oRuleBaseCalc)

        Endif
    Endif


Return jProtheus

/*/{Protheus.doc} MountCalcBase
    (long_description)
    @author user
    @since 21/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method MountCalcBase(nCodigo as numeric, cTax as character, oRuleBaseCalc as object) as character CLASS IntegrativeClassifierSystax

    Local jFormula as json //Objeto json com base de cálculo
    Local oRuleBaseCalc as object //Objeto RuleBaseCalc
    Local cTaxSystax as character //Tributo Systax
    Local cFormula   as character //Fórmula
    Local cDescricao as character //Descrição

    //Obtem tributo utilizado pela Systax
    cTaxSystax := Self:taxesConvert('',cTax)

    jFormula := oRuleBaseCalc:FISGetFormulaBase(nCodigo, cTaxSystax, Self:cMockServer )

    If Valtype(jFormula) == 'J'        

        If jFormula:hasProperty('bcComposicao')

            cDescricao := DECODEUTF8(jFormula["bcComposicao"][1]["descricao"], "CP1252")

            //If ("Pauta IPI" $ jFormula["bcComposicao"][1]["descricao"]) .OR. ("IMPORTACAO" $ UPPER(NoAcento(cDescricao)))
            //    cFormula := jFormula["bcComposicao"][1]["formulaTributo"]
            //Else
                cFormula := jFormula["bcComposicao"][1]["formulaBC"]
            //EndIf

        //ElseIf jFormula:hasproperty('indicadorCredito')

        //    cFormula := jFormula["indicadorCredito"][1]["formulaBC"]

        EndIf


        cFormula := oRuleBaseCalc:FisDeParaFormula(cFormula)

    Endif

    FwFreevar(jFormula)

Return cFormula


/*/{Protheus.doc} processAliquot
    
    método para processar dados de cadastro de Alíquota
    
    @param jTax, json, objeto json com os dados do tributo
    @return jAliquot, json, objeto json com os dados de cadastro de Alíquota

/*/

Method processAliquot(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax
    Local cAliquota as character //Alíquota
    Local jAliquot  as json //Objeto json com alíquota
    Local cProperty as character //Propriedade do tributo
    Local cCodigo   as character //Código do tributo 

    Do case 
        Case jTaxSystax:hasProperty('aliquota')
            cAliquota := cValtochar(jTaxSystax['aliquota'])
        Case jTaxSystax:hasProperty('aliquota_st')
            cAliquota := cValtochar(jTaxSystax['aliquota_st'])
        Case jTaxSystax:hasProperty('aliq_interna_dest')
            cAliquota := cValtochar(jTaxSystax['aliq_interna_dest'])            
    Endcase   
    
    If .not. EMPTY( cAliquota )
        
        cProperty := 'aliquot'

        //Quando o tributo possuir alíquota de FCP, será necessário subtrair a alíquota de FCP do tributo principal
        If jTaxSystax:hasproperty('fcp')
            cAliquota := cValtochar((val(cAliquota)) - val(cValtochar(jTaxSystax['fcp'])))
        Endif

        cCodigo := 'aliquot_' + md5(cAliquota)

        Self:addProperty(jProtheus, cProperty)

        IF .Not. jProtheus[cProperty]:hasProperty(cCodigo)

            //Incrementa o contador de alíquotas
            self:nContAliquot := self:nContAliquot + 1
            
            //adiciona estrutura de alíquota no objeto json jProtheus
            jProtheus[cProperty][cCodigo] := JsonObject():New()
            jAliquot := jProtheus[cProperty][cCodigo]

            //Alterara as propriedades existentes no objeto json da Systax
            jAliquot[ 'descricao' ] := 'Aliquota ' + alltrim(cValtochar(val(cAliquota))) + '%'
            jAliquot[ 'val_origem' ] := '04' //formula
            jAliquot[ 'tp_aliquota' ] := '1' //percentual
            jAliquot[ 'aliquota' ] := cAliquota            

        Endif
    Endif

    //Processa dados de cadastro de alíquota que serão utilizados com indice de NCM
    Self:addAliquot(jProtheus, jTaxSystax, cTax)
    

Return jProtheus

/*/{Protheus.doc} AddAliquot
    
    método para adicionar os dados de cadastro de alíquota e que serão utilizados com indice de NCM no objeto json jProtheus
    Cadastros genéricos que serão utilizados com indice de NCM

    @author Rafael Oliveira
    @since 19/12/2023
    @version 12.1.2310
    @param jProtheus, json, objeto json com os dados do tributo
    @return jProtheus, json, objeto json com os dados de cadastro de alíquota
    /*/
Method addAliquot(jProtheus, jTaxSystax , cTax) as json CLASS IntegrativeClassifierSystax

    Local jAliquot as json //Objeto json com alíquota
    Local cProperty as character //Propriedade do tributo
    Local cCodigo   as character //Código do tributo
    

    If self:lIntegrativeNCM

        If jTaxSystax:hasproperty('fcp') .and. self:IsTaxFecp(cTax)

            cProperty := 'aliquot'

            Self:addProperty(jProtheus, cProperty)

            cCodigo := 'aliquot_' + md5('INDMAJ')
            //Majoracao
            jProtheus[cProperty][cCodigo] := JsonObject():New()
            jAliquot := jProtheus[cProperty][cCodigo]

            //Alterara as propriedades existentes no objeto json da Systax
            jAliquot[ 'descricao' ] := 'Alíquota de indice de Majoração'
            jAliquot[ 'val_origem' ] := '06' //formula
            jAliquot[ 'tp_aliquota' ] := '1' //percentual        
            jAliquot[ 'formula' ] := 'I:MAJORACAO'
        Endif

        If jTaxSystax:hasproperty('valor_pauta')

            cProperty := 'aliquot'

            Self:addProperty(jProtheus, cProperty)
            
            //pauta
            cCodigo := 'aliquot_' + md5('INDPTA')        
            jProtheus[cProperty][cCodigo] := JsonObject():New()
            jAliquot := jProtheus[cProperty][cCodigo]      

            //Alterara as propriedades existentes no objeto json da Systax
            jAliquot[ 'descricao' ] := 'Alíquota de indice de pauta'
            jAliquot[ 'val_origem' ] := '06' //formula
            jAliquot[ 'tp_aliquota' ] := '1' //percentual        
            jAliquot[ 'formula' ] := 'I:PAUTA'
        Endif

    Endif


Return jProtheus

/*/{Protheus.doc} processFiscalWriting
    
    método para processar dados de cadastro de Escrituração Fiscal
    
    @param jTax, json, objeto json com os dados do tributo
    @return jFiscalWriting, json, objeto json com os dados de cadastro de Escrituração Fiscal

/*/

Method processFiscalWriting(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax

Return jProtheus

/*/{Protheus.doc} processTaxCalc
    
    método para processar dados de cadastro de Cálculo do tributo
    
    @param jTax, json, objeto json com os dados do tributo
    @return jTaxCalc, json, objeto json com os dados de cadastro de Cálculo do tributo

/*/

Method processTaxCalc(jProtheus as json, jTaxSystax as json, cTax as character) as json CLASS IntegrativeClassifierSystax

Return jProtheus

/*/{Protheus.doc} validateReceivedData
    (long_description)
    @author Rafael Oliveira
    @since 08/12/2023
    @version 12.1.2310    
    @return lReturn, logical, indica se os dados recebidos são válidos ou não
    /*/
Method validateReceivedData() as logical CLASS IntegrativeClassifierSystax
    Local cCodeNcm                   := Self:getNcm()
    Local cFederativeUnitOrigin      := Self:getFederativeUnitOrigin()
    Local cFederativeUnitDestination := Self:getFederativeUnitDestination()
    Local cOriginProduct             := Self:getOriginProduct()    
    Local cCodeCEST                  := Self:getCodeCEST()
    Local lReturn                    := .T.

    //Valida se os dados recebidos são válidos
    IF (empty(cCodeNcm) .or. empty(cFederativeUnitOrigin) .or. empty(cFederativeUnitDestination) .or. empty(cOriginProduct))
        self:setError("Dados recebidos inválidos, verifique o preenchimento do NCM, estado de origem, estado de destino e origem do produto")
        lReturn := .F.
    Endif
    
    // Valida se o NCM já existe
    IF lReturn .and. !empty(cCodeNcm) .and. MemoryCache('NCM_'+cCodeNcm,,{||!ExistCpo("SYD",cCodeNcm)},.T.)                                                                         
        self:setError("NCM não cadastrado: " + cCodeNcm)
        lReturn := .F.        
    Endif

    // Valida se o estado de origem existe na SX5
    IF lReturn .and. !empty(cFederativeUnitOrigin) .and. MemoryCache('UF_'+cFederativeUnitOrigin,,{||!ExistCpo("SX5","12"+cFederativeUnitOrigin)},.T.)
        self:setError("Estado de origem não cadastrado: " + cFederativeUnitOrigin)
        lReturn := .F.        
    Endif

    // Valida se o estado de destino existe na SX5
    IF lReturn .and. !empty(cFederativeUnitDestination) .and. MemoryCache('UF_'+cFederativeUnitDestination,,{||!ExistCpo("SX5","12"+cFederativeUnitDestination)},.T.)
        self:setError("Estado de destino não cadastrado: " + cFederativeUnitDestination)
        lReturn := .F.
    Endif

    // Valida se a origem do produto existe na SX5
    IF lReturn .and. !empty(cOriginProduct) .and. MemoryCache('Origin_'+cOriginProduct,,{||!ExistCpo("SX5","S0"+cOriginProduct)},.T.)   
        self:setError("Origem do produto não cadastrada: " + cOriginProduct)
        lReturn := .F.
        
    Endif

    // Valida se o código de CEST existe na F0G
    IF lReturn .and. !empty(cCodeCEST) .and. MemoryCache('Cest_'+cCodeCEST,,{||!ExistCpo("F0G", cCodeCEST)},.T.) 
        self:setError("Código de CEST não cadastrado: " + cCodeCEST)
        lReturn := .F.
    Endif

    
Return lReturn

/*/{Protheus.doc} validTaxes
    
    método para validar os tributos aceitos para importação

    @author Rafael Oliveira
    @since 08/12/2023
    @version 12.1.2310
    @param cTributo, character, tributo a ser validado
    @return lValid, logical, indica se o tributo é válido ou não
/*/
Method validTaxes(cTributo as character) as logical CLASS IntegrativeClassifierSystax
Local lValid as logical

If !EMPTY( cTributo ) 
    IF MemoryCache(cTributo,,{||ExistCpo("F2E",UPPER(cTributo),2)},.T.)    
        lValid := .T.
    Endif
Endif

Return lValid


/*/{Protheus.doc} taxesConvert

    método para converter os tributos para o formato do Protheus legado (id_totvs)
    
    @author Rafael Oliveira
    @since 08/12/2023
    @version 12.1.2310
    @param cTax, character, tributo a ser convertido
    @return cRet, character, tributo convertido
    /*/
Method taxesConvert(cSystax, cProtheus) as character CLASS IntegrativeClassifierSystax
    Local cRet as character

    DEFAULT cProtheus := ''
    DEFAULT cSystax := ''

    cProtheus := LOWER(cProtheus)
    cSystax := LOWER(cSystax)

    If !EMPTY( cProtheus )
        
        // Pesquisa pelo ID Protheus
        If (nX := aScan(Self:aRelationTaxProtheusAndSystax,{|x| x[1] == cProtheus})) > 0
            cRet := Self:aRelationTaxProtheusAndSystax[nX][2]
        Endif
    
    ElseIf !EMPTY( cSystax )

        //Pesquisa pelo tributo Systax
        If (nX := aScan(Self:aRelationTaxProtheusAndSystax,{|x| x[2] == cSystax})) > 0
            cRet := Self:aRelationTaxProtheusAndSystax[nX][1]
        Endif
    Endif

Return cRet

/*/{Protheus.doc} ConvertValue
    
    método para converter o valor para o formato correto

    @author Rafael Oliveira
    @since 26/12/2023
    @version 12.1.2310
    @param xValue, variant, valor a ser convertido
    @return xRet, variant, valor convertido
    /*/
Method convertValue(xValue as variant) as numeric CLASS IntegrativeClassifierSystax

    Local nRet as numeric

    If ValType(xValue) == 'N'
        nRet := xValue
    Else
        nRet := val(xValue)
    Endif
    
Return nRet

/*/{Protheus.doc} MountVigencia
    
    método para montar a vigência do tributo

    @author Rafael
    @since 26/12/2023
    @version 12.1.2310
    @param jTaxSystax, json, objeto json com os dados do tributo
    @param cChave, character, chave do tributo
    /*/
Method MountVigencia(jTaxSystax, cChave) as character CLASS IntegrativeClassifierSystax

    Local cVigencia as character
    Local cData   as character

    //Verifica se existe chave de vigência
    If jTaxSystax:hasProperty(cChave)
        cData := jTaxSystax[ cChave ]
        cVigencia := SUBSTR( cData, 9, 2 ) + '/' + SUBSTR( cData, 6, 2 ) + '/' + SUBSTR(cData, 1, 4 )
    Endif

Return cVigencia


/*/{Protheus.doc} recordTaxes
    
    método para gravar os dados processados e organizados no Protheus

    @author Rafael Oliveira
    @since 12/12/2023
    @version 12.1.2310    
    @return lReturn, logical, indica se os dados foram gravados com sucesso ou não
    /*/
Method recordTaxes() as logical CLASS IntegrativeClassifierSystax

    Local lReturn := .T.

    //Grava os dados de cadastro de ncm
    lReturn := Self:saveNCM()

    If ( lReturn )
        //Grava os dados de cadastro de base de cálculo
        Self:saveBaseCalc()
    EndIf

    If ( lReturn )
        //Grava os dados de cadastro de alíquota
        Self:saveAliquot()
    EndIf

Return lReturn

/*/{Protheus.doc} saveNCM
    (long_description)
    @author user
    @since 19/12/2023
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method saveNCM() as logical CLASS IntegrativeClassifierSystax

    Local oRecordTaxesNCM as object
    Local lReturn         := .T.

    If Self:getIntegrativeNCM()
        oRecordTaxesNCM := RecordTaxesNCM():New(self:jTaxesReturn:toJSON())
        //Grava os dados processados e organizados no Protheus
        IF !oRecordTaxesNCM:recordTaxes()
            self:setError(oRecordTaxesNCM:getError())        
            lReturn := .F.
        Endif

        self:aNCMs := oRecordTaxesNCM:aNCMs
        
        oRecordTaxesNCM:destroy()        
        FREEOBJ( oRecordTaxesNCM )
        oRecordTaxesNCM := nil
    Endif
    
Return lReturn

/*/{Protheus.doc} saveAliquot
    
    método para gravar os dados de cadastro de alíquota no Protheus

    @author Rafael
    @since 19/12/2023
    @version 12.1.2310
    @return lReturn, logical, indica se os dados foram gravados com sucesso ou não    
    /*/
Method saveAliquot() as logical CLASS IntegrativeClassifierSystax

    Local oRecordTaxesAliquot as object
    Local lReturn            := .T.    

    If Self:getIntegrativeAliquot()
        oRecordTaxesAliquot := FISGravAliquota():New()
        //Grava os dados processados e organizados no Protheus

        Self:aRegisterAliquot := oRecordTaxesAliquot:gravaDadosjson(self:jTaxesReturn:toJSON())

        IF Len(Self:aRegisterAliquot) == 0
            self:setError('Erro ao gravar aliquotas')
            lReturn := .F.
        Endif

        self:aAliquotas := oRecordTaxesAliquot:aAliquotas
                
        FREEOBJ( oRecordTaxesAliquot )
        oRecordTaxesAliquot := nil
    Endif
    
Return lReturn

/*/{Protheus.doc} saveBaseCalc
    
    método para gravar os dados de cadastro de base de cálculo no Protheus

    @author Rafael
    @since 19/12/2023
    @version version    
    @return lReturn, logical, indica se os dados foram gravados com sucesso ou não
    /*/
Method saveBaseCalc() as logical CLASS IntegrativeClassifierSystax

    Local oRecordTaxesbaseCalc as object
    Local lReturn            := .T.

    If Self:getIntegrativeBaseCalc()
        oRecordTaxesbaseCalc := FISGravaBase():New()
        //Grava os dados processados e organizados no Protheus

        Self:aRegisterBase := oRecordTaxesbaseCalc:gravaDadosjson(self:jTaxesReturn:toJSON())
        
        IF Len(Self:aRegisterBase) == 0
            self:setError('Erro ao gravar base de calculo')  
            lReturn := .F.
        Endif

        self:aBases := oRecordTaxesbaseCalc:aBases
                
        FREEOBJ( oRecordTaxesbaseCalc )
        oRecordTaxesbaseCalc := nil
    Endif
    
Return lReturn

/*/{Protheus.doc} clear
    
    método para limpar os dados da classe IntegrativeClassifierSystax

    @author Rafael Oliveira
    @since 15/12/2023
    @version 12.1.2310
    
    https://tdn.totvs.com/display/tec/FreeObj
    /*/
Method destroy() CLASS IntegrativeClassifierSystax

    self:cError := ''
    Self:cData := ''

    aSize(Self:aRegisterAliquot,0)
    Self:aRegisterAliquot := nil

    aSize(Self:aRegisterBase,0)
    Self:aRegisterBase := nil    
    
    aSize(self:aRelationTaxProtheusAndSystax,0)
    Self:aRelationTaxProtheusAndSystax := nil

    FwFreevar( Self:JsonData )
    FwFreevar( Self:jTaxesReturn )

Return 


/*/{Protheus.doc} MemoryCache

	Função responsavel por guardar em cache os valores de quelquer tipo para serem reutilizados no processamento dos calculos fiscais, evitando assim a necessidade de realizar validações desnecessárias.
	
	@type  Function
	@author Rafael Oliveira
	@since 07/12/2023
	@version version
	@param cChave, character, chave para identificar o valor
	@param xValue, variant, valor a ser guardado em cache
	@param lEmpresa, logical, indica se a chave deve ser prefixada com a empresa e filial
	@return xRetorno, variant, retorna o valor guardado em cache
	@example
	
	
	/*/
Function MemoryCache(cChave ,xValue , bBloco, lEmpresa)	
	Local xRetorno := .F.
	
	DEFAULT lEmpresa := .F.	
	DEFAULT bBloco := nil
	DEFAULT xValue := nil

	IF !Empty(xValue) .or. !Empty(bBloco)
		If lEmpresa
			cChave := AllTrim(cEmpAnt) + AllTrim(cFilAnt) + "_" + cChave
		EndIF	

		xRetorno := jCache[cChave]

		IF xRetorno == NIL

            xRetorno := xValue
			
			If !EMPTY(bBloco)				
				xRetorno := Eval(bBloco)
			Endif			

			jCache[cChave] := xRetorno			
		EndIF
	EndIF
	
Return xRetorno
