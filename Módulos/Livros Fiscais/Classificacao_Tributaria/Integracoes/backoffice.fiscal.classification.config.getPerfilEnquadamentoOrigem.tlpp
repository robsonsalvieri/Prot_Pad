#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FisGetParticipante
Classe que contém as operações para obter os dados de enquadramento, seja de Cornecedor ou Cliente
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class FisGetEnquadramento

	Public METHOD New() CONSTRUCTOR

	@Get("/GetEnquadramento")
	Public METHOD GetEnquadramento()
	@Get("/GetDestinatario")
	Public METHOD GetDestinatario()
    @Post("/postenquadramentos")
    Public METHOD PostEnquadramento()
    Private METHOD GetMessageRequiredFields(cField as character, oJReturn as object) 
    Private METHOD ValidateJsonStructure(cJson as character, oJBody as object, oJReturn as object)

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() Class FisGetEnquadramento

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método para obter os dados dos participantes e coloca-los em arquivo Json

@Obs    Passar via QueryParam tipo da busca ques erá realizada
        1 -> Fornecedor
        2 -> Cliente

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetDestinatario() Class FisGetEnquadramento

    ::GetEnquadramento("Destinatario","2")

Return
//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método para obter os dados dos participantes e coloca-los em arquivo Json

@Obs    Passar via QueryParam tipo da busca ques erá realizada
        1 -> Fornecedor
        2 -> Cliente

@author  Yuri Gimenes da Costa
@since   07.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetEnquadramento(cDestEnq,cTipo) Class FisGetEnquadramento

Local cQry          as character
Local filGroup      as character
Local oJBody        as object
Local oJBodyDt      as object
Local cJson         as character
Local lOk           as logical
local jQuery        as object
Local nPage         as numeric
Local nPageSize     as numeric
Local nRegIni       as numeric
Local nRegFim       as numeric
Local cCodigos      as character
Local oStatement    as object
Local cAliasEnquad  as character
Local aCodigos      := {} as array
Local nIndex        as integer
Local nCount        as integer

Default cDestEnq    := "Enquadramento"
Default cTipo       := "1"

oJBody      := JsonObject():New()
jQuery      := oRest:getQueryRequest()
nPage       := IIF(jQuery['page'] <> Nil,Val(jQuery['page']), 1 )
nPageSize   := IIF(jQuery['pageSize'] <> Nil,Val(jQuery['pageSize']), 15 )
filGroup    := IIF(jQuery['filGroup'] <> Nil .and. !('*' $ jQuery['filGroup']) , jQuery['filGroup'], '')

nRegIni := ( ( nPage - 1 ) * nPageSize ) + 1
nRegFim := nPage * nPageSize

cQry += " SELECT CJK_CODIGO, CJK_DESC, CJK_TIPO FROM ( "
cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY CJK.R_E_C_N_O_ ) LINE_NUMBER, "
cQry += "       CJK.R_E_C_N_O_ CJK_RECNO ,CJK_CODIGO, CJK_DESC, CJK_TIPO "
cQry += "   FROM " + RetSqlName("CJK") + " CJK "
cQry += "   WHERE CJK_FILIAL = ? "
cQry += "   AND CJK_TIPO  = ? "
cQry += "   AND CJK.D_E_L_E_T_ = ' ' "

If ( !Empty(filGroup) .And. 'enquadramento:' $ filGroup)

    cCodigos := AllTrim(StrTran(filGroup, "enquadramento: ", ""))
    aCodigos := StrTokArr(cCodigos, ",")

	cQry += " AND CJK_CODIGO IN ( ? ) "

ElseIf (!Empty(filGroup) .And. !('enquadramento:' $ filGroup))

	cQry += " AND ( "
	cQry += "   CJK_CODIGO LIKE ? OR " 
	cQry += "   UPPER(CJK_DESC) LIKE ? "
	cQry += " ) "

EndIf

cQry += " ) TAB "
cQry += " WHERE LINE_NUMBER BETWEEN ? AND ? "

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(++nIndex, xFilial("CJK"))
oStatement:setString(++nIndex, cTipo)

If ( !Empty(filGroup) .And. 'enquadramento:' $ filGroup .And. !Empty(aCodigos) )

    oStatement:setIn(++nIndex, aCodigos)

ElseIf ( !Empty(filGroup) .And. !('enquadramento:' $ filGroup) )

    oStatement:setString(++nIndex, "%" + filGroup + "%")
    oStatement:setString(++nIndex, "%" + Upper(filGroup) + "%")

EndIf

oStatement:SetNumeric(++nIndex, nRegIni)
oStatement:SetNumeric(++nIndex, nRegFim + 1) //Soma mais um registro para retorno do HasNext

cQry := oStatement:getFixQuery()

cAliasEnquad := MPSysOpenQuery(cQry)

oJBody[cDestEnq] := {}

While (cAliasEnquad)->(!EoF()) .And. nCount < nPageSize
    nCount++

    oJBodyDt := JsonObject():New()

    oJBodyDt['codigo']  := AllTrim((cAliasEnquad)->CJK_CODIGO) 
    oJBodyDt['desc']    := AllTrim((cAliasEnquad)->CJK_DESC)
    oJBodyDt['tipo']    := IIF(AllTrim((cAliasEnquad)->CJK_TIPO) == '1', 'Fornecedor', 'Cliente')

    aadd(oJBody[cDestEnq],oJBodyDt)

    FREEOBJ( oJBodyDt )

    (cAliasEnquad)->(DbSkip())

    lOk := .T.

End

FreeObj(oStatement)

oJBody["success"] := lOk
oJBody["hasNext"] := !(cAliasEnquad)->(EoF())

( cAliasEnquad )->( DBCloseArea() )

cJson := oJBody:toJson()
FREEOBJ( oJBody )

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} PostEnquadramento()
Método para inserir/alterar os dados dos participantes

@Obs    Passar via body json para inserção/alteração
        {
            "codigo": "0001",
            "desc": "Descrição do Enquadramento",
            "tipo": "1" // 1 - Fornecedor | 2 - Cliente
        }

@author  Vitor Rosa
@since   16/09/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Method PostEnquadramento() Class FisGetEnquadramento
    Local oJBody := JsonObject():New() as object
    Local cJson := oRest:getBodyRequest() as character
    Local lOk := .T. as logical
    Local oJReturn := JsonObject():New() as object
    Local aRequired := {'codigo','desc','tipo'} as array
    Local nI := 0 as numeric

    If oJBody:fromJson(cJson) == "U"
        Self:ValidateJsonStructure(@oJBody, @oJReturn)
        Return
    Else
        For nI := 1 To Len(aRequired)
            if !oJBody:hasProperty(aRequired[nI])
                self:GetMessageRequiredFields(aRequired[nI],oJReturn)
                Return
            endif
        Next
    Endif

    DBSelectArea("CJK")
    CJK->(DbSetOrder(1))

    If DbSeek(FWxFilial("CJK")+PadR(oJBody['codigo'],TamSx3("CJK_CODIGO")[1])+oJBody['tipo'])
        If CJK->(RecLock('CJK',.F.))
            CJK->CJK_DESC   := AllTrim(DecodeUtf8(oJBody['desc']))
            CJK->(MsUnLock())
            lOk := .T.
        EndIf
    Else
        If CJK->(RecLock('CJK',.T.))
            CJK->CJK_FILIAL := FWxFilial("CJK")
            CJK->CJK_CODIGO := AllTrim(DecodeUtf8(oJBody['codigo']))
            CJK->CJK_DESC   := AllTrim(DecodeUtf8(oJBody['desc']))
            CJK->CJK_TIPO   := AllTrim(DecodeUtf8(oJBody['tipo']))
            CJK->(MsUnLock())
            lOk := .T.
        EndIf
    EndIf

    oJReturn["success"] := lOk

    If lOk
        oJReturn["message"] := EncodeUtf8("Enquadramento salvo com sucesso!")
    Else
        oJReturn["message"] := EncodeUtf8("Erro ao salvar enquadramento.")
    Endif
    
    If lOK
        oRest:setStatusCode(200)
    Else
        oRest:setStatusCode(400)
    Endif
    
    oRest:setResponse(oJReturn:toJson())

    CJK->(DBCLOSEAREA())
    FreeObj(ojBody)
    FreeObj(oJReturn)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidateJsonStructure()
Método para validar a estrutura do Json recebido no metodo PostEnquadramento
@author  Julia Mota
@since   29/09/2025
@version 1.0
/*/
//-------------------------------------------------------------------

Method ValidateJsonStructure(cJson as character, oJBody as object, oJReturn as object) Class FisGetEnquadramento
    
    oJReturn["success"] := .F.
    oJReturn["message"] := EncodeUtf8(cJson)
    oRest:setStatusCode(400)
    oRest:setResponse(oJReturn:toJson())
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetMessageRequiredFields()
Método para validar os campos obrigatórios do Json recebido no metodo PostEnquadramento
@author  Julia Mota
@since   29/09/2025
@version 1.0
/*/
//-------------------------------------------------------------------
Method GetMessageRequiredFields(cField as character , oJReturn as object) Class FisGetEnquadramento
    
    oJReturn["success"] := .F.
    oJReturn["message"] := EncodeUtf8("Campo obrigatório não informado: " + cField)
    oRest:setStatusCode(400)
    oRest:setResponse(oJReturn:toJson())

Return
