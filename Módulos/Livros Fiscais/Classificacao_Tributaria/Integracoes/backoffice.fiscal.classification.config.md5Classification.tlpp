#include 'tlpp-core.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

/*/{Protheus.doc} Md5Classification
Classe para gravação dos campos MD5 para localização de registros nas tabelas da Classificação Tributária By Systax
@author Juliano Fernandes
@since 01/04/2024
@version 12.1.2310
/*/
class Md5Classification

    private data cTable as character
    private data nIndex as numeric
    private data cKey as character
    private data jKey as variant
    private data cMd5Key as character
    private data cTableKey as character
    private data lMd5FieldExists as logical
    private data cField as character
    private data cCode as character
    private data cCodeField as character
    private data cRuleTypes as character
    private data nRecno as numeric
    private data lFound as logical

    public method New() constructor

    private method SetTable()
    private method SetIndex()
    private method SetKey()
    private method SetJsonKey()
    private method SetField()
    private method SetMd5FieldExists()
    private method SetMd5Key()
    private method SetTableKey()
    private method SetCode()
    private method SetCodeField()
    private method SetRuleTypes()
    private method SetRecno()
    private method SetFound()

    public method GetMd5Key() as character
    public method GetCode() as character
    public method GetFound() as logical
    public method GetMd5FieldExists() as logical

    public method Find()
    
    private method FindByCache()
    private method FindByMd5Field()
    private method FindByQuery()

    private method UpdateMd5Field()

    private method GetStatementTaxableBase() as object
    private method GetStatementTaxRate() as object
    private method GetStatementCalculationRule() as object
    private method GetStatementTaxBookkeeping() as object

    public method Destroy()

endclass

/*/{Protheus.doc} New
Método construtor da classe Md5Classification
@author Juliano Fernandes
@since 01/04/2024
@version 12.1.2310
@return self, object, Objeto da classe Md5Classification
/*/
method New() class Md5Classification

    self:cTable := ''
    self:nIndex := 0
    self:cKey := ''
    self:jKey := nil
    self:cMd5Key := ''
    self:cTableKey := ''
    self:lMd5FieldExists := .F.
    self:cField := ''
    self:cCode := ''
    self:cCodeField := ''
    self:cRuleTypes := ''
    self:nRecno := 0
    self:lFound := .F.

return self

/*/{Protheus.doc} SetTable
Define a tabela para consulta do campo MD5
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
@param cTable, character, Código da tabela a ser definido no objeto
/*/
method SetTable(cTable as character) class Md5Classification

    self:cTable := cTable
    self:SetField()

return

/*/{Protheus.doc} SetIndex
Define o índice da tabela para consulta do campo MD5
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
@param nIndex, numeric, Código do índice da tabela a ser definido no objeto
/*/
method SetIndex(nIndex as numeric) class Md5Classification

    self:nIndex := nIndex

return

/*/{Protheus.doc} SetKey
Define a chave de busca da tabela para consulta do campo MD5
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
@param jKey, json, Json contendo os campos e valores da chave de busca da tabela a ser definido no objeto
/*/
method SetKey(jKey as json) class Md5Classification

    local aKeys as array
    local i as numeric

    aKeys := jKey:GetNames()

    for i := 1 to Len(aKeys)
        do case
            case ( ValType( jKey[aKeys[i]] ) == 'C' )
                self:cKey += jKey[aKeys[i]]

            case ( ValType( jKey[aKeys[i]] ) == 'N' )
                self:cKey += CValToChar(jKey[aKeys[i]])

            case ( ValType( jKey[aKeys[i]] ) == 'D' )
                self:cKey += DToS(jKey[aKeys[i]])

        endcase
    next i

    self:SetJsonKey(jKey)

    self:SetMd5Key()

    FWFreeArray(aKeys)

return

/*/{Protheus.doc} SetJsonKey
Define a chave separada, campo e valor de busca da tabela para consulta do campo MD5
@author Juliano Fernandes
@since 23/04/2024
@version 12.1.2310
@param jKey, json, Json contendo os campos da chave de busca da tabela a ser definido no objeto
/*/
method SetJsonKey(jKey as json) class Md5Classification

    self:jKey := jKey

return

/*/{Protheus.doc} SetField
Define o campo MD5 da tabela
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetField() class Md5Classification

    self:cField := self:cTable + '_CHVMD5'
    self:SetMd5FieldExists()

return

/*/{Protheus.doc} SetMd5FieldExists
Define se o campo MD5 da tabela existe
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetMd5FieldExists() class Md5Classification

    DbSelectArea(self:cTable)
    self:lMd5FieldExists := (self:cTable)->((FieldPos(self:cField)) > 0)
    (self:cTable)->(DbCloseArea())

return

/*/{Protheus.doc} SetMd5Key
Define a chave de busca em MD5
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetMd5Key() class Md5Classification

    local cPrefix as character

    cPrefix := 'CONFITRIB-'

    if ( FWIsInCallStack('IntegraRegras') )
        cPrefix := 'CLASSTRIB-'
    endif

    self:cMd5Key := cPrefix + Md5(self:cKey)
    self:SetTableKey()

return

/*/{Protheus.doc} SetTableKey
Define a chave de busca da tabela
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetTableKey() class Md5Classification

    local cRecordChanged as character

    cRecordChanged := '2'

    self:cTableKey := FWxFilial(self:cTable) + self:cMd5Key + cRecordChanged

return

/*/{Protheus.doc} SetCode
Define o código encontrado
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetCode(cCode as character) class Md5Classification

    self:cCode := cCode

return

/*/{Protheus.doc} SetCodeField
Define o campo que contém o código da tabela
@author Juliano Fernandes
@since 15/04/2024
@version 12.1.2310
/*/
method SetCodeField(cCodeField as character) class Md5Classification

    self:cCodeField := cCodeField

return

/*/{Protheus.doc} SetRuleTypes
Define os tipos de regra para consulta do campo MD5
@author Juliano Fernandes
@since 15/04/2024
@version 12.1.2310
@param cRuleTypes, character, Tipo de regra a ser definido no objeto
/*/
method SetRuleTypes(cRuleTypes as character) class Md5Classification

    self:cRuleTypes := cRuleTypes

return

/*/{Protheus.doc} SetRecno
Define o RECNO do registro encontrado
@author Juliano Fernandes
@since 11/04/2024
@version 12.1.2310
/*/
method SetRecno(nRecno as numeric) class Md5Classification

    self:nRecno := nRecno

return

/*/{Protheus.doc} SetFound
Define se o registro foi encontrado
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method SetFound() class Md5Classification

    self:lFound := !Empty(self:cCode)

return

/*/{Protheus.doc} GetMd5Key
Retorna a chave de busca em MD5
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method GetMd5Key() as character class Md5Classification
return self:cMd5Key

/*/{Protheus.doc} GetCode
Retorna o código encontrado após realizar a busca na tabela
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method GetCode() as character class Md5Classification
return self:cCode

/*/{Protheus.doc} GetFound
Retorna se o registro foi encontrado
@type method
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method GetFound() as logical class Md5Classification
return self:lFound

/*/{Protheus.doc} GetMd5FieldExists
Retorna se o campo MD5 existe
@type method
@author Juliano Fernandes
@since 12/04/2024
@version 12.1.2310
/*/
method GetMd5FieldExists() as logical class Md5Classification
return self:lMd5FieldExists

/*/{Protheus.doc} Find
Realiza a busca do registro no cache, campo MD5 e Fórmula
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
@param jFind, json, JSON contendo os dados necessários para buscar o registro
/*/
method Find(jFind as json) class Md5Classification

    self:SetTable(jFind['table'])
    self:SetIndex(jFind['index'])
    self:SetKey(jFind['key'])
    self:SetCodeField(jFind['codeField'])
    self:SetRuleTypes(jFind['ruleTypes'])

    self:FindByCache(jFind['cache'])

    if ( !self:lFound )
        self:FindByMd5Field()
    endif

    if ( !self:lFound )
        self:FindByQuery()

        if ( self:lFound )
            self:UpdateMd5Field()
        endif            
    endif

return

/*/{Protheus.doc} FindByCache
Realiza a busca do registro no cache
@author Juliano Fernandes
@since 12/04/2024
@version 12.1.2310
@param jCache, json, JSON contendo o cache de informações processadas anteriormente
/*/
method FindByCache(jCache as json) class Md5Classification

    if ( jCache:HasProperty(self:cMd5Key) )
        self:SetCode(jCache[self:cMd5Key])

        self:SetFound()
    endif

return

/*/{Protheus.doc} FindByMd5Field
Realiza a busca do registro no campo MD5
@author Juliano Fernandes
@since 12/04/2024
@version 12.1.2310
/*/
method FindByMd5Field() class Md5Classification

    local cQuery as character
    local cRecordChanged as character
    local oStatement as object

    cRecordChanged := '2'

    if ( self:GetMd5FieldExists() )
        if ( self:cTable == 'F2B' ) // Regras
            cQuery += " SELECT F2B_REGRA "
            cQuery += " FROM " + RetSqlName('F2B')
            cQuery += " WHERE F2B_FILIAL = ? "
            cQuery += "     AND F2B_CHVMD5 = ? "
            cQuery += "     AND F2B_ALTERA = ? "
            cQuery += "     AND ? >= F2B_VIGINI "
            cQuery += "     AND ( ? <= F2B_VIGFIM OR F2B_VIGFIM = ? ) "
            cQuery += "     AND D_E_L_E_T_ = ? "

            oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

            oStatement:SetString(1, FWxFilial(self:cTable))
            oStatement:SetString(2, self:cMd5Key)
            oStatement:SetString(3, cRecordChanged)
            oStatement:SetDate(4, dDataBase)
            oStatement:SetDate(5, dDataBase)
            oStatement:SetString(6, ' ')
            oStatement:SetString(7, ' ')

            self:SetCode(oStatement:ExecScalar('F2B_REGRA'))

            FWFreeVar(oStatement)
        else
            self:SetCode(Posicione(self:cTable, self:nIndex, self:cTableKey, self:cCodeField))
        endif

        self:SetFound()
    endif

return

/*/{Protheus.doc} FindByQuery
Realiza a busca do registro através de query
@author Juliano Fernandes
@since 12/04/2024
@version 12.1.2310
/*/
method FindByQuery() class Md5Classification

    local oStatement as object
    local cAlias as character
    local aBind as array
    local i as numeric

    aBind := {}

    do case 
        case ( self:cRuleTypes == '1' ) // Bases
            oStatement := self:GetStatementTaxableBase(@aBind)

        case ( self:cRuleTypes == '2' ) // Alíquotas
            oStatement := self:GetStatementTaxRate(@aBind)

        case ( self:cRuleTypes == '8,11,12' ) // Regras
            oStatement := self:GetStatementCalculationRule(@aBind)

        otherwise // Escrituração
            oStatement := self:GetStatementTaxBookkeeping(@aBind)

    endcase

    for i := 1 to Len(aBind)
        do case
            case ( ValType( aBind[i] ) == 'C' )
                oStatement:SetString(i, aBind[i])

            case ( ValType( aBind[i] ) == 'N' )
                oStatement:SetNumeric(i, aBind[i])

            case ( ValType( aBind[i] ) == 'D' )
                oStatement:SetDate(i, aBind[i])                

        endcase
    next i

    cAlias := oStatement:OpenAlias(GetNextAlias())

    if ( !(cAlias)->(Eof()) )
        self:SetCode( (cAlias)->(&(self:cCodeField)) )

        self:SetRecno( (cAlias)->RECNO )

        self:SetFound()        
    endif

    (cAlias)->(DbCloseArea())

    FWFreeVar(oStatement)
    FWFreeArray(aBind)

return

/*/{Protheus.doc} UpdateMd5Field
Realiza a atualização do campo MD5 caso esteja vazio
@author Juliano Fernandes
@since 08/04/2024
@version 12.1.2310
/*/
method UpdateMd5Field() class Md5Classification

    if ( self:GetMd5FieldExists() )
        DbSelectArea(self:cTable)

        (self:cTable)->(DbGoTo(self:nRecno))

        if ( (self:cTable)->(Recno()) == self:nRecno )

            RecLock(self:cTable, .F.)

            (self:cTable)->(&(self:cField)) := self:cMd5Key

            (self:cTable)->(MsUnlock())

        endif

        (self:cTable)->(DbCloseArea())
    endif

return

/*/{Protheus.doc} GetStatementTaxableBase
Retorna o statement para busca de fórmula de Base de Cálculo
@author Juliano Fernandes
@since 15/04/2024
@version 12.1.2310
@param, aBind, array, Array contendo os binds da query
@return oStatement, object, Statement da query
/*/
method GetStatementTaxableBase(aBind) as object class Md5Classification

    local cQuery as character
    local cRecordChanged as character
    local oStatement as object

    cRecordChanged := '2'

    cQuery += " SELECT F27.F27_CODIGO, F27.R_E_C_N_O_ RECNO "
    cQuery += " FROM " + RetSqlName('CIN') + " CIN "
    cQuery += " 	INNER JOIN " + RetSqlName('F27') + " F27 "
    cQuery += " 		ON  F27.F27_FILIAL = ? "
    cQuery += " 		AND F27.F27_ID = CIN.CIN_IREGRA "
    cQuery += " 		AND F27.F27_ALTERA = ? "
    cQuery += " 		AND F27.D_E_L_E_T_ = ? "
    cQuery += " WHERE CIN.CIN_FILIAL = ? "
    cQuery += " 	AND CIN.CIN_TREGRA = ? "
    cQuery += " 	AND CIN.CIN_FNPI = ? "
    cQuery += " 	AND CIN.D_E_L_E_T_ = ? "

    Aadd(aBind, FWxFilial(self:cTable))
    Aadd(aBind, cRecordChanged)
    Aadd(aBind, ' ')
    Aadd(aBind, FWxFilial('CIN'))
    Aadd(aBind, self:cRuleTypes)
    Aadd(aBind, xFisSYard(self:cKey))
    Aadd(aBind, ' ')

    oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

return oStatement

/*/{Protheus.doc} GetStatementTaxRate
Retorna o statement para busca de fórmula de Aliquotas
@author Juliano Fernandes
@since 15/04/2024
@version 12.1.2310
@param, aBind, array, Array contendo os binds da query
@return oStatement, object, Statement da query
/*/
method GetStatementTaxRate(aBind) as object class Md5Classification

    local cQuery as character
    local cRecordChanged as character
    local oStatement as object

    cRecordChanged := '2'

    cQuery += " SELECT F28.F28_CODIGO, F28.R_E_C_N_O_ RECNO "
    cQuery += " FROM " + RetSqlName('CIN') + " CIN "
    cQuery += " 	INNER JOIN " + RetSqlName('F28') + " F28 "
    cQuery += " 		ON  F28.F28_FILIAL = ? "
    cQuery += " 		AND F28.F28_ID = CIN.CIN_IREGRA "
    cQuery += " 		AND F28.F28_TPALIQ = ? "
    cQuery += " 		AND F28.F28_ALTERA = ? "
    cQuery += " 		AND F28.D_E_L_E_T_ = ? "
    cQuery += " WHERE CIN.CIN_FILIAL = ? "
    cQuery += " 	AND CIN.CIN_TREGRA = ? "
    cQuery += " 	AND CIN.CIN_FNPI = ? "
    cQuery += " 	AND CIN.D_E_L_E_T_ = ? "

    Aadd(aBind, FWxFilial(self:cTable))
    Aadd(aBind, self:jKey['F28_TPALIQ'])
    Aadd(aBind, cRecordChanged)
    Aadd(aBind, ' ')
    Aadd(aBind, FWxFilial('CIN'))
    Aadd(aBind, self:cRuleTypes)
    Aadd(aBind, xFisSYard(self:jKey['CIN_FNPI']))
    Aadd(aBind, ' ')

    oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

return oStatement

/*/{Protheus.doc} GetStatementCalculationRule
Retorna o statement para busca de fórmula de Regra de Cálculo
@author Juliano Fernandes
@since 16/04/2024
@version 12.1.2310
@param, aBind, array, Array contendo os binds da query
@return oStatement, object, Statement da query
/*/
method GetStatementCalculationRule(aBind) as object class Md5Classification

    local cQuery as character
    local cRecordChanged as character
    local oStatement as object

    cRecordChanged := '2'

    cQuery += " SELECT F2B.F2B_REGRA, F2B.R_E_C_N_O_ RECNO "
    cQuery += " FROM " + RetSqlName('F2B') + " F2B "
    cQuery += " WHERE F2B.F2B_FILIAL = ? "
    cQuery += "     AND F2B.F2B_TRIB = ? "
    cQuery += "     AND ? >= F2B.F2B_VIGINI "
    cQuery += "     AND ( ? <= F2B.F2B_VIGFIM OR F2B.F2B_VIGFIM = ? ) "
    cQuery += "     AND F2B.F2B_RBASE = ? "
    cQuery += "     AND F2B.F2B_RALIQ = ? "
    cQuery += "     AND F2B.F2B_PEROD = ? "
    cQuery += "     AND F2B.F2B_PERFPA = ? "
    cQuery += "     AND F2B.F2B_PERFOP = ? "
    cQuery += "     AND F2B.F2B_PERFPR = ? "
    cQuery += "     AND F2B.F2B_RFIN  = ? "
    cQuery += "     AND F2B.F2B_RAPUR = ? "
    cQuery += "     AND F2B.F2B_RND = ? "
    cQuery += "     AND F2B.F2B_RBASES = ? "
    cQuery += "     AND F2B.F2B_TRBMAJ = ? "
    cQuery += "     AND F2B.F2B_DEDDEP = ? "
    cQuery += "     AND F2B.F2B_DEDPRO = ? "
    cQuery += "     AND F2B.F2B_CODESC = ? "
    cQuery += "     AND F2B.F2B_MAXMIN = ? "
    cQuery += "     AND F2B.F2B_RGGUIA = ? "
    cQuery += "     AND F2B.F2B_VLRMAX = ? "
    cQuery += "     AND F2B.F2B_VLRMIN = ? "
    cQuery += "     AND F2B.F2B_OPRMIN = ? "
    cQuery += "     AND F2B.F2B_OPRMAX = ? "
    cQuery += "     AND F2B.F2B_ALTERA = ? "
    cQuery += "     AND F2B.D_E_L_E_T_ = ? "

    Aadd(aBind, FWxFilial(self:cTable))
    Aadd(aBind, self:jKey['F2B_TRIB'])
    Aadd(aBind, dDataBase)
    Aadd(aBind, dDataBase)
    Aadd(aBind, ' ')
    Aadd(aBind, self:jKey['F2B_RBASE'])
    Aadd(aBind, self:jKey['F2B_RALIQ'])
    Aadd(aBind, self:jKey['F2B_PEROD'])
    Aadd(aBind, self:jKey['F2B_PERFPA'])
    Aadd(aBind, self:jKey['F2B_PERFOP'])
    Aadd(aBind, self:jKey['F2B_PERFPR'])
    Aadd(aBind, self:jKey['F2B_RFIN'])
    Aadd(aBind, self:jKey['F2B_RAPUR'])
    Aadd(aBind, self:jKey['F2B_RND'])
    Aadd(aBind, self:jKey['F2B_RBASES'])
    Aadd(aBind, self:jKey['F2B_TRBMAJ'])
    Aadd(aBind, self:jKey['F2B_DEDDEP'])
    Aadd(aBind, self:jKey['F2B_DEDPRO'])
    Aadd(aBind, self:jKey['F2B_CODESC'])
    Aadd(aBind, self:jKey['F2B_MAXMIN'])
    Aadd(aBind, self:jKey['F2B_RGGUIA'])
    Aadd(aBind, self:jKey['F2B_VLRMAX'])
    Aadd(aBind, self:jKey['F2B_VLRMIN'])
    Aadd(aBind, self:jKey['F2B_OPRMIN'])
    Aadd(aBind, self:jKey['F2B_OPRMAX'])
    Aadd(aBind, cRecordChanged)
    Aadd(aBind, ' ')

    oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

return oStatement

/*/{Protheus.doc} GetStatementTaxBookkeeping
Retorna o statement para busca de Regra de Escrituração
@author Juliano Fernandes
@since 16/04/2024
@version 12.1.2310
@param, aBind, array, Array contendo os binds da query
@return oStatement, object, Statement da query
/*/
method GetStatementTaxBookkeeping(aBind) as object class Md5Classification

    local cQuery as character
    local cRecordChanged as character
    local oStatement as object

    cRecordChanged := '2'

    cQuery += " SELECT CJ2.CJ2_CODIGO, CJ2.R_E_C_N_O_ RECNO "
    cQuery += " FROM " + RetSqlName('CJ2') + " CJ2 "
    cQuery += " WHERE CJ2.CJ2_FILIAL = ? "
    cQuery += " 	AND CJ2.CJ2_INCIDE = ? "
    cQuery += " 	AND CJ2.CJ2_STOTNF = ? "
    cQuery += " 	AND CJ2.CJ2_PERDIF = ? "
    cQuery += " 	AND CJ2.CJ2_IREDBS = ? "
    cQuery += " 	AND CJ2.CJ2_CSTCAB = ? "
    cQuery += " 	AND CJ2.CJ2_CST = ? "
    cQuery += " 	AND CJ2.CJ2_CSTDEV = ? "
    cQuery += " 	AND CJ2.CJ2_ALTERA = ? "
    cQuery += " 	AND CJ2.D_E_L_E_T_ = ? "

    Aadd(aBind, FWxFilial(self:cTable))
    Aadd(aBind, self:jKey['CJ2_INCIDE'])
    Aadd(aBind, self:jKey['CJ2_STOTNF'])
    Aadd(aBind, self:jKey['CJ2_PERDIF'])
    Aadd(aBind, self:jKey['CJ2_IREDBS'])
    Aadd(aBind, self:jKey['CJ2_CSTCAB'])
    Aadd(aBind, self:jKey['CJ2_CST'])
    Aadd(aBind, self:jKey['CJ2_CSTDEV'])
    Aadd(aBind, cRecordChanged)
    Aadd(aBind, ' ')

    oStatement := FWExecStatement():New( ChangeQuery(cQuery) )

return oStatement

/*/{Protheus.doc} Destroy
Realiza a destruição do objeto instanciado pela classe Md5Classification
@author Juliano Fernandes
@since 12/04/2024
@version 12.1.2310
/*/
method Destroy() class Md5Classification

    self:cTable := ''
    self:nIndex := 0
    self:cKey := ''
    self:cMd5Key := ''
    self:cTableKey := ''
    self:lMd5FieldExists := .F.
    self:cField := ''
    self:cCode := ''
    self:cCodeField := ''
    self:cRuleTypes := ''
    self:nRecno := 0
    self:lFound := .F.

    FwFreevar( self:jKey )

return
