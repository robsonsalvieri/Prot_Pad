#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISGravaEmpresaFilha
Classe responsavel pelo envio de novas empresas filhas cadastradas
@author  Julia Mota
@since   06.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISGravaEmpresaFilha

    data username as character
    data senha as character
    data url as character

    data rest_client as object
    data error as numeric
    data oJsonRet as object
    data oBody as object
    data oJson as object
 
    Public METHOD New() CONSTRUCTOR
    @Post("/RegisterNewCompany")    
    public method RegisterNewCompany()
    Public METHOD FISPostGravaCliente()
    Public METHOD FISGravaTokenTotvs(oBody)
    METHOD ValidaJson(oJson)

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Julia Mota
@since   18.09.2023
@version 1.0
/*/
//-------------------------------------------------------------------

METHOD New() CLASS FISGravaEmpresaFilha


Return 

//-------------------------------------------------------------------
/*/{Protheus.doc} RegisterNewCompany()
Método responsável por solicitar o token para a systax montar o json
com os dados que vieram do formulário
@author  Julia Mota
@since   18.09.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD RegisterNewCompany() CLASS FISGravaEmpresaFilha

Local jRetorno                  := JsonObject():New()
Local jBody                     := oRest:GetBodyRequest()//solicito os dados do formulario que vem do payload
Local jQuery                    := oRest:GetQueryRequest()
Local cTelefone                 := ""
Local cDDD                      := ""
Local cTelComp                  := ""
Local cRetSys                   := ""
Local cRetErro                  := ""
Local oRetSys                   := JsonObject():New()
Local oJsonRet                  := JsonObject():New()
Local oValidClassifierLicense   := ValidClassifierLicense():New()
Local lAutomato                 := .F.

//Recebo o Body e transformo em um Json
oJsonRet:FromJson(jBody)

lAutomato := oJsonRet:HasProperty('automato')

if ( ( !oValidClassifierLicense:Valid() .and. !lAutomato ) .or. ( lAutomato .and. oJsonRet:HasProperty('unlicensedClassifier') ) )
    oRest:SetStatusCode(403)

    jRetorno['sucesso'] := .F.
    jRetorno['erro'] := oValidClassifierLicense:GetErrorMessage()

    oValidClassifierLicense:Destroy()
    FreeObj(oValidClassifierLicense)

    return oRest:SetResponse(jRetorno)
endif

oValidClassifierLicense:Destroy()
FreeObj(oValidClassifierLicense)

cRetErro := ::validaJson(oJsonRet)

If !Empty(cRetErro)
    oRest:setStatusCode(400)
    jRetorno['sucesso'] := .T.
    jRetorno['erro']    := cRetErro
    return oRest:SetResponse(jRetorno)
EndIf

cTelComp := oJsonRet['telefone']
cDDD := SubStr(cTelComp, 1, 2)
cTelefone := SubStr(cTelComp, 3)
 
if oJsonRet != NIL 
    oJsonRet['quantidade_regras'] := 1000
    oJsonRet['ddd'] := cDDD
    oJsonRet['telefone'] := cTelefone
    oJsonRet['user']['id'] := 480973
    oJsonRet['user']['tipo'] := 20
    oJsonRet['user']['nome'] := oJsonRet['nome']
    oJsonRet['user']['email'] := oJsonRet['email']
    oJsonRet['user']['senha'] := MD5(oJsonRet['user']['senha'])
    oJsonRet['flag_aprovacao_auto_regras'] := "1" // '1' - SIM, '0' - NAO
endif

cRetSys := ::FISPostGravaCliente(oJsonRet,jQuery)

oRetSys:FromJson(cRetSys)

jRetorno['sucesso'] := oRetSys['success']

If !jRetorno['sucesso']
    oRest:setStatusCode(400)
    jRetorno['erro'] := oRetSys['message']
EndIf

FreeObj(oJsonRet)
FreeObj(oRetSys)
FreeObj(jQuery)

Return oRest:SetResponse(jRetorno)

//-------------------------------------------------------------------
/*/{Protheus.doc} FISPostGravaCliente()
Método que faz o post na systax utilizando o token
@author  Julia Mota
@since   18.09.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD FISPostGravaCliente(oBody,jQuery) CLASS FISGravaEmpresaFilha

Local cToken        := ""
Local oTokenAtu     := FISTokenClassificador():new()
Local aHeaderStr    := {}
Local lRet          as logical
Local cResult       as character
Local cBody         := oBody:toJson()
Local oRest         := FwRest():New(oTokenAtu:url)
Local oResult       as object
Local cMockServer   as character

If (ValType(jQuery['mockserver']) == "C")
    cMockServer := jQuery['mockserver']
EndIf

cToken := oTokenAtu:tokenSysFront(oBody['systaxuser'],oBody['systaxsenha'],cMockServer) // para isto funcionar preciso passar o user e senha que vem do json

Aadd(aHeaderStr, "Authorization: Bearer " + cToken)
Aadd(aHeaderStr, "Content-Type: application/json; charset=UTF-8")
Aadd(aHeaderStr, "Accept: */*")


If Empty(cMockServer)
    oRest := FwRest():New(oTokenAtu:url)
    oRest:setPath("/partner/clients")
ElseIf jQuery['endpoint'] == "/EnvDupReg"
    oRest := FwRest():New(cMockServer)
    oRest:setPath("/EnvDupReg")
Else
    oRest := FwRest():New(cMockServer)
    oRest:setPath("/EnvNovoReg")
EndIf


oRest:setPostParams(cBody)

oRest:SetChkStatus(.F.)

lRet := oRest:Post( aHeaderStr )


cResult := oRest:GetResult()

oResult := JsonObject():New()

oResult:FromJson(cResult)

FreeObj(oTokenAtu)
FreeObj(oResult)

FWFreeArray(aHeaderStr)

return cResult

//-------------------------------------------------------------------
/*/{Protheus.doc} ValidaJson()
Método que valida se os dados do JSON estão corretos .
@author  Julia Mota
@since   18.09.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD ValidaJson(oJson) CLASS FISGravaEmpresaFilha

Local cErros :=""

if Empty(oJson['entidade'])
    cErros+= STR0176 //Razao Social nao pode estar vazio"
endif
if Empty(oJson['nome'])
    cErros+= STR0183 //Nome do Contato nao pode estar vazio"
endif
if Empty(oJson['email'])
    cErros+= STR0177 //Email nao pode estar vazio"
elseIf !("@" $ oJson['email'])
    cErros+= STR0178  //Email invalido"
endif
if  EMPTY(oJson['telefone']) .or. !IsNumeric(oJson['telefone']) .or. Len(oJson['telefone']) <10 .or. Len(oJson['telefone']) >11
    cErros+= STR0179 //O telefone deve ser numerico entre 10 e 11 caracteres
endif
if !IsNumeric(oJson['ramal'])
    cErros+= STR0180//Ramal aceita apenas números
endif
if Empty(oJson['user']['username'])
    cErros+= STR0181 //Usuario nao pode estar vazio"
endif
if Empty(oJson['user']['senha']) .or. Len(oJson['user']['senha']) < 6
    cErros+= STR0182 //Senha deve ter no minimo 6 caracteres
endif

Return cErros
