#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//Chamando a classe framework.types.json
Using Namespace framework.types.json

//-------------------------------------------------------------------
/*/{Protheus.doc} FISValidaCenario
Classe que contém as operações para validar o cenário antes de enviá-lo a Systax
@author  Yuri Gimenes da Costa
@since   02.05.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISValidaCenario

    data oJsonCenario as object
    data cGrpProd as character
 
    Public METHOD New() CONSTRUCTOR
    Public METHOD FISValidaCenario(cGrpProd,cCodPart,oJsonCenario)

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   02.05.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISValidaCenario


Return

//-------------------------------------------------------------------
/*/{Protheus.doc} obtNatOper
Método chamador das classes
@author  Yuri Gimenes da Costa
@since   02.05.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD FisValidaCenario(cGrpProd,aCodPart,oJsonCenario) CLASS FISValidaCenario

Local cJson     := ""
Local oJsonCena := JsonObject():New()
    
oJsonCena := ValidCena(cGrpProd,aCodPart,oJsonCenario)

cJson += oJsonCena:toJSON( )

FreeObj(oJsonCena)

Return cJson

//-------------------------------------------------------------------
/*/{Protheus.doc} GetNatur
Função para validar os dados dos cenários compatíveis
@author  Yuri Gimenes da Costa
@since   02.05.2022
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function ValidCena(cGrpProd,aCodPart,oJsonCenario)

Local cQry              := ""
Local oJBody        
Local oJBodyDt          := JsonObject():New()
Local oJsValid          := JsonObject():New()
Local oJsValidDt        := JsonObject():New()
Local aJsRetorno        := {} as array
Local oJsonCena         := JsonObject():New()
Local nY                := 0
Local nYB               := 0
Local nExc              := 0
Local nTotal            := 0
Local aExclui           := {} as array
Local jValidacenario    := JsonObject():New() as json
Local cValidacenario    := ''
Local cCenarioatual     := ''
Local oStatement        as object
Local cAliasCena        as character
Local cDB               as character

cDB := Upper(TCGetDB())

cQry += " SELECT CJG_IDENVI, "
If "ORACLE" $ cDB
	cQry += " UTL_RAW.CAST_TO_VARCHAR2(DBMS_LOB.SUBSTR(CJG_JSON, 4000,1)) JSON  "
ElseIf "POSTGRES" $ cDB
    cQry += " encode(CJG_JSON,'escape') JSON  "
Else
    cQry += " ISNULL(CAST(CAST(CJG_JSON AS VARBINARY(8000)) AS VARCHAR(8000)),'') AS [JSON] "
ENDIF 
cQry += " FROM "+RetSqlName("CJG")+" CJG "
cQry += " WHERE CJG_FILIAL = ? "
cQry += " AND CJG_IDPPRO = ? "
cQry += " AND CJG_IDPPAR = ? "
cQry += " AND CJG.D_E_L_E_T_ = ' ' "


oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, xFilial("CJB"))
oStatement:setString(2, cGrpProd)
oStatement:setString(3, aCodPart[1][1])

cQry := oStatement:getFixQuery()

cAliasCena := MPSysOpenQuery(cQry)

oJBodyDt["cenarios"] := {}

While (cAliasCena)->(!EoF())

    oJBody := JsonObject():New()

    oJBody:FromJSON((cAliasCena)->JSON)

    If oJBody["cenarios"] <> Nil

        For nY := 1 to len(oJBody["cenarios"])

            oJBody["cenarios"][nY]["id_cenario"] := AllTrim((cAliasCena)->CJG_IDENVI)      

            cValidacenario := oJBody["cenarios"][nY]["uf_origem"] // Origem
            cValidacenario += oJBody["cenarios"][nY]["uf_destino"] // Destino
            cValidacenario += cValtochar(oJBody["cenarios"][nY]["ent_sai"]) // Tipo de movimento
            cValidacenario += oJBody["cenarios"][nY]["destinacao"] // Segmento de negócio do destino
            cValidacenario += If(oJBody["cenarios"][nY]["finalidade"] != Nil, oJBody["cenarios"][nY]["finalidade"], "") // Finalidade
            cValidacenario += oJBody["cenarios"][nY]["cod_nat_op"] // Operação
            cValidacenario += oJBody["cenarios"][nY]["grupos"] // Perfis de produtos
            cValidacenario += oJBody["cenarios"][nY]["alternativo_ex_origem"] // Segmento de negócio

            jValidacenario[MD5(cValidacenario)] := oJBody["cenarios"][nY]["id_cenario"]

            aadd(oJBodyDt["cenarios"],oJBody["cenarios"][nY])

        Next nY

    ENDIF

    FreeObj(oJBody)

    (cAliasCena)->(dbSkip())

End

(cAliasCena)->(DbCloseArea())
FreeObj(oStatement)

aJsRetorno := {}

If Len(oJBodyDt["cenarios"]) > 0 

    For nYB := 1 to Len(oJsonCenario["cenarios"])

        cCenarioatual := oJsonCenario["cenarios"][nYB]["uf_origem"] // Origem
        cCenarioatual += oJsonCenario["cenarios"][nYB]["uf_destino"] // Destino
        cCenarioatual += cValToChar(oJsonCenario["cenarios"][nYB]["ent_sai"]) // Tipo de movimento
        cCenarioatual += oJsonCenario["cenarios"][nYB]["destinacao"] // Segmento de negócio do destino
        cCenarioatual += If(oJsonCenario["cenarios"][nYB]["finalidade"] != Nil, oJsonCenario["cenarios"][nYB]["finalidade"], "") // Finalidade
        cCenarioatual += oJsonCenario["cenarios"][nYB]["cod_nat_op"] // Operação
        cCenarioatual += oJsonCenario["cenarios"][nYB]["grupos"] // Perfis de produtos
        cCenarioatual += oJsonCenario["cenarios"][nYB]["alternativo_ex_origem"] // Segmento de negócio

        IF jValidacenario:HasProperty(MD5(cCenarioatual))       
            
            oJsValid      := JsonObject():New()
            
            oJsValid["success"]         := .F.
            oJsValid["message"]         := "Já existe cenário semelhante com regra criada"
            oJsValid["total_itens"]     := 0
            oJsValid["itens"]           := {}
            oJsValidDt["id_cenario"]    := jValidacenario["id_cenario"]
            oJsValidDt["uf_origem"]     := oJsonCenario["cenarios"][nYB]["uf_origem"] 
            oJsValidDt["uf_destino"]    := oJsonCenario["cenarios"][nYB]["uf_destino"]
            oJsValidDt["ent_sai"]       := oJsonCenario["cenarios"][nYB]["ent_sai"]   
            oJsValidDt["destinacao"]    := oJsonCenario["cenarios"][nYB]["destinacao"]
            oJsValidDt["finalidade"]    := oJsonCenario["cenarios"][nYB]["finalidade"]
            oJsValidDt["cod_nat_op"]    := oJsonCenario["cenarios"][nYB]["cod_nat_op"]

            aadd(oJsValid["itens"],oJsValidDt)
            aadd(aJsRetorno,oJsValid)

            oJsValid["total_itens"] := Len(oJsValid["itens"])

            aadd(aExclui,nYB)            

            FREEOBJ( oJsValid )

        else
            oJsValid      := JsonObject():New()
            
            nTotal += 1
            oJsValid["success"]     := .T.
            oJsValid["message"]     := "Ok"
            oJsValid["total_itens"] := nTotal
            oJsValid["itens"]       := {}

            aadd(aJsRetorno,oJsValid)

            FREEOBJ( oJsValid )
        EndIf       
    Next nYB
else
    oJsValid["success"]     := .T.
    oJsValid["message"]     := "Ok"
    oJsValid["total_itens"] := 0
    oJsValid["itens"]       := {}

    aadd(aJsRetorno,oJsValid)
EndIF

//==================================================|
//Exclui do array o cenário que já existe           |
//==================================================|
aSort(aExclui,,,{|x,y| x > y})
For nExc := 1 to Len(aExclui)

    If Len(oJsonCenario["cenarios"]) > 1
        ADel(oJsonCenario["cenarios"], aExclui[nExc] )
        ASize(oJsonCenario["cenarios"], Len(oJsonCenario["cenarios"]) -1)
    Else
        ASize(oJsonCenario["cenarios"], Len(oJsonCenario["cenarios"]) -1)
    EndIf

Next nExc
//==================================================|

oJsonCena["cenarios"] := aJsRetorno

FreeObj(oJBodyDt)
FreeObj(oJsValid)
FreeObj(oJsValidDt)
FreeObj(jValidacenario)

FWFreeArray(aJsRetorno)
FWFreeArray(aExclui)

Return oJsonCena
