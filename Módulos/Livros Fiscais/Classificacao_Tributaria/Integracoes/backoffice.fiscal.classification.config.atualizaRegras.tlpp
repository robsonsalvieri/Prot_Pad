#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FisConfiguraRegra
Classe que contém as operações para configrar a intergração das regras com o Protheus
@author  Yuri Gimenes da Costa
@since   20.07.2022
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class FisConfiguraRegra

    public method new() constructor

    @Get("/GetConfiguraRegra")    
    public method GetConfiguraRegra()

    @Put("/PutConfiguraRegra")    
    public method PutConfiguraRegra()

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   20.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class FisConfiguraRegra

return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetMonitorCenario()
Método para obter o status atual das configurações de Regras
        
@author  Yuri Gimenes da Costa
@since   20.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method GetConfiguraRegra() class FisConfiguraRegra

Local oJBody        := JsonObject():New()
Local oJBodyDt      := JsonObject():New()
Local cJson         as character
Local jQuery

jQuery := oRest:GetQueryRequest()

DbSelectArea("CJE")
CJE->(DbSetOrder(1))

If !Empty(jQuery['param']) .and. !(jQuery['param'] == Nil)

    If CJE->(MsSeek(xFilial("CJE")+jQuery['param']))

        oJBody['success']   := .T.
        oJBody['parametro'] := {}

        oJBodyDt['param'] := ALLTRIM(CJE->CJE_PARAM)
        oJBodyDt['value'] := &(ALLTRIM(CJE->CJE_CONTEU))

        AADD(oJBody['parametro'],oJBodyDt)

        FreeObj(oJBodyDt)

    Else

        oJBody['success'] := .F.
        oJBody['message'] := STR0217 + ": " + jQuery['param'] //"Parâmetro não encontrado"

    EndIf
else
    oJBody['success'] := .F.
    oJBody['message'] := STR0218 //"Parâmetro não informado"
EndIF

CJE->(DbCloseArea())

cJson := oJBody:toJson()

Return oRest:setResponse(oJBody)

//-------------------------------------------------------------------
/*/{Protheus.doc} PostMonitorCenario()
Método para alterar o status atual das configurações de Regras
        
@author  Yuri Gimenes da Costa
@since   20.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method PutConfiguraRegra() class FisConfiguraRegra

Local cJson     := ""
Local oBody     := JsonObject():New()
Local oJBody    := JsonObject():New()

oBody:FromJSON(oRest:getBodyRequest())

DbSelectArea("CJE")
CJE->(DbSetOrder(1))

If (!(oBody['param'] == Nil) .and. !empty(oBody['param'])) .and.;
   (!(oBody['value']  == Nil) )

    If CJE->(MsSeek(xFilial("CJE")+oBody['param']))

        //-------------------------------------------------------------------
        //O campo CJE_CONTEU está recebendo o parâmetro como texto, 
        //pois o mesmo é caracter devido os demais parâmetros e configurações
        //-------------------------------------------------------------------
        Reclock("CJE", .F.)
            CJE->CJE_CONTEU := IIF(oBody['value'], ".T.", ".F.")
        CJE->(MsUnlock())

        oJBody['success']   := .T.

    Else

        oJBody['success'] := .F.
        oJBody['message'] := STR0217 //"Parâmetro não encontrado"

    EndIf
else
    oJBody['success'] := .F.
    oJBody['message'] := STR0219 //"Parâmetro e/ou conteúdo não informado"
EndIf

CJE->(DbCloseArea())

cJson := oJBody:toJson()

FreeObj(oBody)
FreeObj(oJBody)

Return oRest:setResponse(cJson)
