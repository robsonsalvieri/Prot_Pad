#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISNatOperacao
Classe que contém as operações para obter os dados da natureza de operação 
e Finalidade e devolver ao FrontEnd
@author  Yuri Gimenes da Costa
@since   19.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISgetNaturezaOperacao

    data aNatOper as array
    data jQuery as json
    data lRest as logical
 
    Public METHOD New() CONSTRUCTOR

    @get("/GetNatOper")
    Public METHOD FISGetOperFin()

    @get("/GetGrupFinOper")
    Public METHOD FISObtemGrupoNatOper()

    Public METHOD GetNaturFinCena(aNatOper)

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Yuri Gimenes da Costa
@since   19.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISgetNaturezaOperacao


Return

//-------------------------------------------------------------------
/*/{Protheus.doc} obtNatOper
Método para obter os Dados da Natureza de operação e Finalidade
@author  Yuri Gimenes da Costa
@since   19.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD FISGetOperFin() CLASS FISgetNaturezaOperacao

Local jQuery 	    as Object
Local jNatFin 	    := JsonObject():New()
Local jBody 	    as object
Local cQry 		    := ""
Local nPage 	    as numeric
Local nPageSize     as numeric
Local nRegIni 	    as numeric
Local nRegFim 	    as numeric
Local nX		    as numeric
Local cGrupo	    as character
Local cfilGroup	    as character
Local aAux		    as array
Local cFilter       as character
Local oStatement    as object
Local cAliasProd    as character
Local nCount        as integer

jQuery      := oRest:getQueryRequest()

nPage       := IIF(jQuery['page'] <> Nil,Val(jQuery['page']), 1 )
nPageSize   := IIF(jQuery['pageSize'] <> Nil,Val(jQuery['pageSize']), 15 )
lFilter     := .F.
cTipo       := IIF(jQuery['tipo'] <> Nil, jQuery['tipo'], '')

cfilGroup   := IIF(jQuery[ 'filGroup' ] <> Nil .And. !( '*' $ jQuery[ 'filGroup' ]) , StrTran( jQuery[ 'filGroup' ],'grupo:',''), '' )
cfilGroup   := Decode64(cfilGroup)
aAux        := StrTokArr2(cfilGroup, ',' )

for nX := 1 To Len(aAux)
    If ('grupo:' $ aAux[nX])
        If Empty(cGrupo)
            cGrupo += '' + AllTrim(StrTran(aAux[nX],'grupo:',''))
        Else
            cGrupo += ',' + AllTrim(StrTran(aAux[nX],'grupo:',''))
        EndIf
    Else
        If ( !Empty(aAux[nX]) )
            cFilter := '%' + aAux[nX] + '%'
        EndIf
    EndIf
Next

nRegIni := ( ( nPage - 1 ) * nPageSize ) + 1    
nRegFim := nPage * nPageSize

cQry += " SELECT CJH_COD, CJH_DESCR, CJH_GRUPO FROM ( "
cQry += "   SELECT ROW_NUMBER() OVER( ORDER BY CJH.R_E_C_N_O_ ) LINE_NUMBER, "
cQry += " 		CJH.R_E_C_N_O_ CJH_RECNO, CJH_COD, CJH_DESCR, CJH_GRUPO "  
cQry += " 	FROM " + RetSqlName("CJH") + " CJH "
cQry += " 	WHERE CJH_TIPO = ? "
cQry += " 	AND CJH_FILIAL = ? "
cQry += " 	AND CJH.D_E_L_E_T_ = ' ' "

If ( !Empty(cGrupo)) 
    cGrupo := FormatIN( cGrupo , "," )
    cQry += " AND CJH_GRUPO  IN " + cGrupo + " "
EndIf

If (!Empty(cFilter))
    cQry += " AND ( 
    cQry += "   UPPER(CJH_COD) LIKE '" + Upper(cFilter) + "' OR "
    cQry += "   UPPER(CJH_DESCR) LIKE '" + Upper(cFilter) + "' OR "
    cQry += "   UPPER(CJH_GRUPO) LIKE '" + Upper(cFilter) + "' "
    cQry += " ) "
EndIf

cQry += " ) TAB "
cQry += " WHERE LINE_NUMBER BETWEEN " + cValTochar(nRegIni) + " AND " + cValTochar(nRegFim + 1) + " " //Soma mais um registro para retorno do HasNext

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, jQuery['tipo'])
oStatement:setString(2, xFilial("CJH"))

cQry := oStatement:getFixQuery()

cAliasProd := MPSysOpenQuery(cQry)

jNatFin['nat_fin']  := {}

While (cAliasProd)->(!EoF()) .And. nCount < nPageSize
    nCount++

    jBody := JsonObject():New()

    jBody["codigo"]     := AllTrim((cAliasProd)->CJH_COD)
    jBody["descricao"]  := AllTrim((cAliasProd)->CJH_DESCR)
    jBody["grupo"]  	:= AllTrim((cAliasProd)->CJH_GRUPO)

    aadd(jNatFin['nat_fin'], jBody)

    (cAliasProd)->(dbSkip())

    FREEOBJ( jBody )

End

jNatFin["hasNext"] := !(cAliasProd)->(EoF())

(cAliasProd)->(DbCloseArea())

FreeObj(oStatement)
FreeObj(jQuery)
FWFreeArray(aAux)

Return oRest:setResponse(jNatFin)

//-------------------------------------------------------------------
/*/{Protheus.doc} Method GetGrupFinOper
Método para obter os grupos das Naturezas de Operação de Finalidades
@author  Yuri Gimenes da Costa
@since   14.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD FISObtemGrupoNatOper() CLASS FISgetNaturezaOperacao

Local cQry as character
Local oJsonGrp as object
Local jQuery as object
Local oStatement as object
Local oJsonDet as object
Local cAliasProd as character

cQry      := ""
oJsonGrp  := JsonObject():New()
jQuery    := oRest:getQueryRequest()

cQry += "SELECT CJH_GRUPO "  
cQry += "FROM "+RetSqlName("CJH")+" CJH "  
cQry += "WHERE CJH_FILIAL = ? "
cQry += "AND CJH_TIPO = ? "
cQry += "AND CJH.D_E_L_E_T_ = ' ' "
cQry += "GROUP BY CJH_GRUPO "

oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, xFilial("CJH"))
oStatement:setString(2, jQuery['tipo'])

cQry := oStatement:getFixQuery()

cAliasProd := MPSysOpenQuery(cQry)

oJsonGrp['grupo'] := {}

While (cAliasProd)->(!EoF())

    oJsonDet := JsonObject():New()

    oJsonDet['value'] := AllTrim((cAliasProd)->CJH_GRUPO)
    oJsonDet['label'] := AllTrim((cAliasProd)->CJH_GRUPO)

    aadd(oJsonGrp['grupo'],oJsonDet)

    FREEOBJ( oJsonDet )

    (cAliasProd)->(dbSkip())

EndDo

(cAliasProd)->(DbCloseArea())
FreeObj(oStatement)

RETURN oRest:setResponse(oJsonGrp)

//-------------------------------------------------------------------
/*/{Protheus.doc} obtNatOper
Método para obter os Dados da Natureza de operação e Finalidade
@author  Yuri Gimenes da Costa
@since   19.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetNaturFinCena(aNatOper) CLASS FISgetNaturezaOperacao

Local jNatFin 	    := JsonObject():New()
Local jBody 	    as object
Local cQry 		    := ""
Local oStatement    as object
Local cAliasProd    as character
Local cJson         as character

cQry += "   SELECT CJH_COD, CJH_DESCR, CJH_GRUPO "  
cQry += " 	FROM " + RetSqlName("CJH") + " CJH "
cQry += " 	WHERE CJH_TIPO = ? "
cQry += " 	AND CJH_COD = ? "
cQry += " 	AND CJH_FILIAL = ? "
cQry += " 	AND CJH.D_E_L_E_T_ = ' ' "


oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

oStatement:setString(1, aNatOper[1][2]) //tipo
oStatement:setString(2, aNatOper[1][1]) //código
oStatement:setString(3, xFilial("CJH"))

cQry := oStatement:getFixQuery()

cAliasProd := MPSysOpenQuery(cQry)

jNatFin['nat_fin']  := {}

While (cAliasProd)->(!EoF())

    jBody := JsonObject():New()

    jBody["codigo"]     := AllTrim((cAliasProd)->CJH_COD)
    jBody["descricao"]  := AllTrim((cAliasProd)->CJH_DESCR)
    jBody["grupo"]  	:= AllTrim((cAliasProd)->CJH_GRUPO)

    aadd(jNatFin['nat_fin'], jBody)

    (cAliasProd)->(dbSkip())

    FREEOBJ( jBody )

End

(cAliasProd)->(DbCloseArea())

cJson := jNatFin:toJson()

FreeObj(oStatement)
FreeObj(jNatFin)

Return cJson
