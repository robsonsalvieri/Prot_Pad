#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier
using Namespace totvs.protheus.backoffice.fiscal.lib

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} FisConfiguraRegra
Classe que contém as operações para chamada de envio de cenários
@author  Erich Buttner
@since   25.07.2022
@version 1.0
/*/
//-----------------------------------------------------------------------------------------------------
Class EnviaCenarioClassificador

    data cCodProd as character
    data cOrigem as character
    data cPerfil as character
    data jQuery as json
    data oTable as object
    data oQuery as object
    
    public method new() constructor

    @Post("/PostEnviaCenario")    
    public method PostEnviaCenario(cCodProd,cOrigem,cPerfil)

    @Post("/PostEnviaMonitor")    
    public method PostEnviaMonitor(cCodProd,cOrigem,cPerfil)

    @Post("/PostExcluiCenario")    
    public method PostExcluiCenario()

    private method organizaJson() as json
    private method montaJson() as json
    private method verificarCenarioCadastrado() as json
    private method criarTabelaTemporaria() as character
    private method limparEDestruirTabelaTemporaria()
    private method associarPerfilDeProdutoAoCenario() as character
    
    public method obterCampoJsonConvertido() as character

End Class

//-------------------------------------------------------------------
/*/{Protheus.doc} New()
Método construtor da classe
@author  Erich Buttner
@since   25.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method new() class EnviaCenarioClassificador


return

//-------------------------------------------------------------------
/*/{Protheus.doc} PostEnviaCenario()
Método para chamada de função de envio de canários
        
@author  Erich Buttner
@since   25.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method PostEnviaCenario() class EnviaCenarioClassificador

Local aCodPart             as array
Local aFinalid             as array
Local aNatOper             as array
Local aOriDest             as array
Local cEmpresa             as character
Local cEntSai              as character
Local cMockServer          as character
Local cPath                as character
Local cPerfil              as character
Local jBody                as json
Local jQuery               as json
Local lEnvia        := .F. as logical
Local lEnviaCenario := .T. as logical
Local nE, nX        := 0   as numeric
Local oJRetorno            as json
Local oJsonEnv             as json
Local oJsonFront           as json
local jDadosDoFront        as json
Local jSonOrganizado       as json
Local cErro                as character
Local cPerfPart            as character

jDadosDoFront := JsonObject():New()
oJsonFront    := JsonObject():New()
oJRetorno     := JsonObject():New()

jQuery        := oRest:GetQueryRequest()
jBody         := oRest:GetBodyRequest()

oJsonEnv      := FISEnviaCenario():New()

If Valtype(jQuery['mockserver']) == "C"
    cMockServer := jQuery['mockserver']
EndIf

If Valtype(jQuery['endpoint']) == "C"
    cPath := jQuery['endpoint']
EndIf

//Recebo o Body e transformo em um Json
jDadosDoFront:FromJson(jBody)

//Organizo o Json e agrupo as informações vindas do Front
jSonOrganizado := self:organizaJson(jDadosDoFront)
FreeObj( jDadosDoFront )

//Deixo o Json pronto para uso
oJsonFront := self:montaJson(jSonOrganizado,jQuery)
FreeObj( jSonOrganizado )

If oJsonFront:HasProperty('cenarios')

    For nE := 1 To Len(oJsonFront['cenarios'])

        aCodPart := {}
        aOriDest := {}
        aNatOper := {}
        aFinalid := {}

        cPerfil  := oJsonFront['cenarios'][nE]['perfil_produto']

        If Upper(oJsonFront['cenarios'][nE]['status']) $ "SEND"
            oJBodyProd := GetProduto(cPerfil,@cErro)

            if ( Empty(cErro) )
                oEnvProd := FISEnvPerfilProduto():New()            

                if ( !oEnvProd:consulEnvProd(cPerfil,oJBodyProd['products']) )
                    oEnvProd:envPerfilProd(cPerfil,cMockServer,@lEnviaCenario,,oJBodyProd,@cErro)
                endif
                
                lEnvia := .T.
            endif

            FreeObj(oEnvProd)

        EndIf

        If lEnviaCenario    
            cEmpresa := oJsonFront['cenarios'][nE]['empresa'] //Enquadramento Origem
            cEmpresa_dest := oJsonFront['cenarios'][nE]['empresa_dest']//Enquadramento destinatario
            cEntSai  := oJsonFront['cenarios'][nE]['entrada_saida']

            For nX := 1 To Len(oJsonFront['cenarios'][nE]['participante'])
            
                cPerfPart := oJsonFront['cenarios'][nE]['participante'][nX]['perfil_participante']
            
                aAdd(aCodPart, {cPerfPart})
            
            Next nX

            For nX := 1 To Len(oJsonFront['cenarios'][nE]['origem_destino'])
            
                aAdd(aOriDest, oJsonFront['cenarios'][nE]['origem_destino'][nX])
            
            Next nX

            For nX := 1 To Len(oJsonFront['cenarios'][nE]['natureza_operacao'])
            
                aAdd(aNatOper, {oJsonFront['cenarios'][nE]['natureza_operacao'][nX]['codigo_natur'],;
                                oJsonFront['cenarios'][nE]['natureza_operacao'][nX]['tipo']})
            Next nX

            If oJsonFront['cenarios'][nE]:hasproperty('finalidade')

                For nX := 1 To Len(oJsonFront['cenarios'][nE]['finalidade'])
                
                    aAdd(aFinalid, {oJsonFront['cenarios'][nE]['finalidade'][nX]['codigo_final'],;
                                    oJsonFront['cenarios'][nE]['finalidade'][nX]['tipo']})
                Next nX

            EndIf

            cJsonRet := oJsonEnv:enviaCenario(cEntSai,cEmpresa,aCodPart,aOriDest,cPerfil,aNatOper,aFinalid,lEnvia,,,cEmpresa_dest,cMockServer,cPath)
            oJRetorno:FromJSON(cJsonRet)
        EndIf

        FWFreeArray(aCodPart)
        FWFreeArray(aOriDest)
        FWFreeArray(aNatOper)
        FWFreeArray(aFinalid)
    
    Next nE

EndIf

FreeObj(oJsonFront)
FreeObj(oJsonEnv)

Return oRest:setResponse(oJRetorno)

//-------------------------------------------------------------------
/*/{Protheus.doc} PostEnviaMonitor()
Método para chamada de função de envio de canários
        
@author  Erich Buttner
@since   25.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method PostEnviaMonitor() class EnviaCenarioClassificador

Local cPerfil           := ""
Local oJsonFront        := JsonObject():New()
Local oJsonEnv          as object
Local oEnvProd          as object
Local cEmpresa          := ""
Local cEntSai           := ""
Local aCodPart          := {}
Local aOriDest          := {}
Local aNatOper          := {}
Local aFinalid          := {}         
Local oJRetorno         := JsonObject():New()
Local jQuery            := oRest:GetQueryRequest()
Local jBody             := oRest:GetBodyRequest()
Local nE                := 0
Local cMockServer       := ""
Local cPath             := ""
Local cErro             := ""
Local lErro             := .F.
Local jCenario          := Nil

cCodCena    := IIF( jQuery['codigo_cena'] <> Nil , jQuery['codigo_cena'] , '' )

If Valtype(jQuery['mockserver']) == "C"
    cMockServer := jQuery['mockserver']
EndIf

If Valtype(jQuery['endpoint']) == "C"
    cPath := jQuery['endpoint']
EndIf

oJsonEnv := FISEnviaCenario():New()

//Recebo o Body e transformo em um Json
oJsonFront:FromJson(jBody)
For nE := 1 To Len(oJsonFront['cenarios'])

    cPerfil := oJsonFront['cenarios'][nE]['grupos']
    oJBodyProd := GetProduto(cPerfil,@cErro)

    lErro := !Empty(cErro)

    if ( !lErro )
        oEnvProd := FISEnvPerfilProduto():New()

        if ( !oEnvProd:consulEnvProd(cPerfil,oJBodyProd['products']) )

            oEnvProd:envPerfilProd(cPerfil,cMockServer,,,oJBodyProd,@cErro)

            lErro := !Empty(cErro)
        endif 

        FreeObj(oEnvProd)       
    endif 

    FreeObj(oJBodyProd)    

    if ( lErro )
        oJRetorno["sucesso"] := .F.
        oJRetorno["erro"] := cErro

        exit
    endif           
Next nE

if ( !lErro )
    jCenario := self:verificarCenarioCadastrado(cPerfil, oJsonFront)

    if ( jCenario['cenarioCadastrado'] )
        cJsonRet := self:associarPerfilDeProdutoAoCenario(cPerfil, jCenario['codigoCenario'], cCodCena, cMockServer, cPath)
    else
        cJsonRet := oJsonEnv:enviaCenario(cEntSai,cEmpresa,aCodPart,aOriDest,cPerfil,aNatOper,aFinalid,.T.,oJsonFront,cCodCena,,cMockServer,cPath)
    endif

    oJRetorno:FromJSON(cJsonRet)
endif

FreeObj(oJsonFront)
FreeObj(oJsonEnv)
FreeObj(jCenario)
FWFreeArray(aCodPart)
FWFreeArray(aOriDest)
FWFreeArray(aNatOper)
FWFreeArray(aFinalid)

Return oRest:setResponse(oJRetorno)

//-------------------------------------------------------------------
/*/{Protheus.doc} PostEnviaMonitor()
Método para chamada de função de envio de canários
        
@author  Erich Buttner
@since   25.07.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method PostExcluiCenario() class EnviaCenarioClassificador

Local jRetorno  := JsonObject():New()
Local jQuery    := oRest:GetQueryRequest()

cCodCena := IIF( jQuery['codigo_cena'] <> Nil , jQuery['codigo_cena'] , '' )

DbSelectArea("CJG")
CJG->(DbSetOrder(1))

If MsSeek(xFilial("CJG")+cCodCena)
    RecLock("CJG",.F.)
    CJG->(DbDelete())
    CJG->(MsUnlock())
    jRetorno['sucesso'] := .T.
    jRetorno["erro"] := ""
Else
    jRetorno['sucesso'] := .F.
    jRetorno["erro"] := STR0221 //"O cenário não existe na base de dados"    
EndIf 

CJG->(DbCloseArea())

cJson := jRetorno:toJson()

Return oRest:setResponse(jRetorno)

//-------------------------------------------------------------------
/*/{Protheus.doc} organizaJson(oJsonFront)
Método para organizar o Json que vem do Front
        
@author  Yuri Gimenes
@since   03.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method organizaJson(oJsonFront) as json class EnviaCenarioClassificador

    Local nY as numeric
    Local oJsonCena as json
    Local oJsonProduto as json
    Local oJsonParticipante as json
    Local oJsonOperacao as json
    Local oJsonFinalidade as json
    Local aDados as array
    Local jItem as json
    Local cInfo as character
    Local cTipoMovimento as character
    Local cPerfilOrigem as character
    Local cPerfilDestino as character
    Local cPerfilProduto as character
    Local cPerfilParticipante as character
    Local cOperacao as character
    Local cFinalidade as character
    Local cOrigem as character
    Local cDestino as character

    cTipoMovimento      := FwNoAccent(Upper(STR0151)) // "Tipo de Movimento"
    cPerfilOrigem       := FwNoAccent(Upper(STR0011)) // "Segmento de Negócio da Origem (Perfil de Origem)"
    cPerfilDestino      := FwNoAccent(Upper(STR0149)) // "Segmento de Negócio do Destino (Perfil de Destino)"
    cPerfilProduto      := FwNoAccent(Upper(STR0012)) // "Perfil de Produto (Configurador de Tributos)"
    cPerfilParticipante := FwNoAccent(Upper(STR0013)) // "Perfil de Participante (Configurador de Tributos)"
    cOperacao           := FwNoAccent(Upper(STR0014)) // "Natureza de Operação"
    cFinalidade         := FwNoAccent(Upper(STR0030)) // "Finalidade"
    cOrigem             := FwNoAccent(Upper(STR0222)) // "Origem"
    cDestino            := FwNoAccent(Upper(STR0090)) // "Destino"

    oJsonCena := JsonObject():New()
    oJsonCena['produtos']           := {}
    oJsonCena['participante']       := {}
    oJsonCena['natureza_operacao']  := {}
    oJsonCena['origens']            := {}
    oJsonCena['destinos']           := {}

    aDados := oJsonFront['dados'][1]

    For nY := 1 To len(aDados)

        cInfo := FwNoAccent(Upper(DecodeUTF8(aDados[nY]['perfil'],'cp1252')))

        jItem := aDados[nY]['item']

        Do Case

            Case cInfo == cTipoMovimento

                oJsonCena['entrada_saida']  := jItem['entrada_saida']
                oJsonCena['desc_ent_sai']   := jItem['desc']

            Case cInfo == cPerfilOrigem

                oJsonCena['empresa']    := jItem['codigo']
                oJsonCena['desc']       := jItem['desc']
                oJsonCena['tipo']       := jItem['tipo']

            Case cInfo == cPerfilDestino

                oJsonCena['empresa_dest']   := jItem['codigo']
                oJsonCena['desc_dest']      := jItem['desc']
                oJsonCena['tipo_dest']      := jItem['tipo']

            Case cInfo == cPerfilProduto

                oJsonProduto := JsonObject():New()

                oJsonProduto['perfil_produto']  := jItem['cod_perfil']

                aadd(oJsonCena['produtos'], oJsonProduto)

                FreeObj( oJsonProduto )

            Case cInfo == cPerfilParticipante

                oJsonParticipante := JsonObject():New()

                oJsonParticipante['perfil'] := jItem['perfil']

                aadd(oJsonCena['participante'], oJsonParticipante)

                FreeObj( oJsonParticipante )
            
            Case cInfo == cOperacao

                oJsonOperacao := JsonObject():New()

                oJsonOperacao['codigo'] := jItem['codigo']
                oJsonOperacao['tipo']   := '1'
                oJsonOperacao['grupo']  := jItem['grupo']

                aadd(oJsonCena['natureza_operacao'], oJsonOperacao)

                FreeObj( oJsonOperacao )

            Case cInfo == cFinalidade

                oJsonCena['finalidade'] := {}

                oJsonFinalidade := JsonObject():New()

                oJsonFinalidade['codigo']   := jItem['codigo']
                oJsonFinalidade['tipo']     := '2'
                oJsonFinalidade['grupo']    := jItem['grupo']

                aadd(oJsonCena['finalidade'], oJsonFinalidade)

                FreeObj( oJsonFinalidade )

            Case cInfo == cOrigem

                aadd(oJsonCena['origens'], jItem['valueO'])

            Case cInfo == cDestino

                aadd(oJsonCena['destinos'], jItem['valueD'])
                
        EndCase

    Next nY

Return oJsonCena

//-------------------------------------------------------------------
/*/{Protheus.doc} montaJson(oJsonFront)
Método para organizar o Json que vem do Front
        
@author  Yuri Gimenes
@since   03.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
method montaJson(oJsonFront,jQuery) as json class EnviaCenarioClassificador

    Local nY                as integer
    Local nYB               as integer
    Local nYC               as integer
    Local nYD               as integer
    Local nYE               as integer
    Local oJsonCena         as json
    Local oJsonDt           as json
    Local oJsonOperacao     as json
    Local oJsonFinalidade   as json
    Local oJsonOriDest      as json
    Local oJsonParticipante as json

    oJsonCena := JsonObject():New()
    oJsonCena['cenarios'] := {}

    For nY := 1 To len(oJsonFront['produtos'])
        oJsonDt                     := JsonObject():New()

        oJsonDt['status']           := jQuery['event']
        oJsonDt['perfil_produto']   := oJsonFront['produtos'][nY]['perfil_produto']
        oJsonDt['empresa']          := oJsonFront['empresa']
        oJsonDt['empresa_dest']     := oJsonFront['empresa_dest']
        oJsonDt['entrada_saida']    := oJsonFront['entrada_saida']

        //PARTICIPANTE
        oJsonDt['participante'] := {}

        For nYB := 1 to len(oJsonFront['participante'])
    
            oJsonParticipante := JsonObject():New()

            oJsonParticipante['perfil_participante'] := oJsonFront['participante'][nYB]['perfil']

            aadd(oJsonDt['participante'], oJsonParticipante)

            FreeObj( oJsonParticipante )

        Next nYB

        //ORIGEM x DESTINO
        oJsonDt['origem_destino'] := {}

        For nYC := 1 to Len(oJsonFront['origens'])
            For nYE := 1 To Len(oJsonFront['destinos'])
                oJsonOriDest := JsonObject():New()

                oJsonOriDest['origem'] := oJsonFront['origens'][nYC]
                oJsonOriDest['destino'] := oJsonFront['destinos'][nYE]
                
                Aadd(oJsonDt['origem_destino'], oJsonOriDest)

                FreeObj( oJsonOriDest )
            Next nYE
        Next nYC

        //NATUREZA DE OPERAÇÃO
        oJsonDt['natureza_operacao'] := {}

        For nYD := 1 to len(oJsonFront['natureza_operacao'])

            oJsonOperacao := JsonObject():New()

            oJsonOperacao['codigo_natur'] := oJsonFront['natureza_operacao'][nYD]['codigo']
            oJsonOperacao['tipo']         := oJsonFront['natureza_operacao'][nYD]['tipo']

            aadd(oJsonDt['natureza_operacao'], oJsonOperacao)

            FreeObj( oJsonOperacao )

        Next nYD

        //FINALIDADE
        If ( oJsonFront:HasProperty('finalidade') )
            oJsonDt['finalidade'] := {}

            For nYD := 1 to len(oJsonFront['finalidade'])

                oJsonFinalidade := JsonObject():New()

                oJsonFinalidade['codigo_final'] := oJsonFront['finalidade'][nYD]['codigo']
                oJsonFinalidade['tipo']         := oJsonFront['finalidade'][nYD]['tipo']

                aadd(oJsonDt['finalidade'], oJsonFinalidade)

                FreeObj( oJsonFinalidade )

            Next nYD
        ENDIF

        aadd(oJsonCena['cenarios'], oJsonDt)
        FreeObj( oJsonDt )

    Next nY

Return oJsonCena

/*/{Protheus.doc} verificarCenarioCadastrado
Método responsável por verificar se já existe um cenario cadastrado com os mesmos parâmetros do cenário que 
está tentando enviar para a Systax, porém com perfil de produto diferente
@author Juliano Fernandes
@since 07/01/2025
@version 12.1.2410
@param cPerfil, character, Código do perfil a ser associado
@param jCenario, json, Json contendo os dados do cenário para comparação
@return jCenarioCadastrado, json, Json com informações do cenário encontrado
/*/
method verificarCenarioCadastrado(cPerfil as character, jCenario as json) as json class EnviaCenarioClassificador

    local cQry as character
    local oStatement as object
    local cAlias as character
    local cAliasTemp as character
    local jCenarioCadastrado as json

    cAliasTemp := self:criarTabelaTemporaria(cPerfil)

    cQry += ' SELECT IDCENA '
    cQry += ' FROM ? '
    cQry += ' WHERE TIPOMOVIM = ? '
    cQry += '     AND NATUREZA = ? '
    cQry += '     AND FINALIDADE = ? '
    cQry += '     AND PERFORIGEM = ? '
    cQry += '     AND PERFDESTIN = ? '
    cQry += '     AND UFORIGEM = ? '
    cQry += '     AND UFDESTINO = ? '

    oStatement := FwExecStatement():New( ChangeQuery(cQry) )

    oStatement:SetUnsafe(1, cAliasTemp)
    oStatement:SetNumeric(2, jCenario['cenarios'][1]['ent_sai'])
    oStatement:SetString(3, jCenario['cenarios'][1]['cod_nat_op'])
    oStatement:SetString(4, jCenario['cenarios'][1]['finalidade'])
    oStatement:SetString(5, jCenario['cenarios'][1]['alternativo_ex_origem'])
    oStatement:SetString(6, jCenario['cenarios'][1]['destinacao'])
    oStatement:SetString(7, jCenario['cenarios'][1]['uf_origem'])
    oStatement:SetString(8, jCenario['cenarios'][1]['uf_destino'])

    cAlias := oStatement:OpenAlias(GetNextAlias())

    jCenarioCadastrado := JsonObject():New()
    jCenarioCadastrado['cenarioCadastrado'] := .F.
    jCenarioCadastrado['codigoCenario'] := ''

    if ( !(cAlias)->(Eof()) )
        jCenarioCadastrado['cenarioCadastrado'] := .T.
        jCenarioCadastrado['codigoCenario'] := AllTrim((cAlias)->IDCENA)
    endif

    (cAlias)->(DbCloseArea())

    FreeObj(oStatement)

    self:limparEDestruirTabelaTemporaria()

return jCenarioCadastrado

/*/{Protheus.doc} criarTabelaTemporaria
Método responsável por criar a tabela temporaria que irá armazenar os dados para verificar a existencia de um cenário
@author Juliano Fernandes
@since 07/01/2025
@version 12.1.2410
@param cPerfil, character, Código do perfil a ser associado
@return cTempAlias, character, Alias da tabela temporária
/*/
method criarTabelaTemporaria(cPerfil as character) as character class EnviaCenarioClassificador

    local aCmp := {} as array
    local aIndice := {} as array
    local cTempAlias as character
    local cQry as character
    local aBind as array
    local cAlias as character
    local lChangeQuery := .F. as logical
    local aCenarios := {} as array
    local jCenario as json
    local aFieldStruct as array

    //Prepara a classe que inicializa a tabela temporária
    Aadd(aCmp, {'TIPOMOVIM', 'N', 1, 0})

    aFieldStruct := FWSX3Util():GetFieldStruct('CJH_COD')
    Aadd(aCmp, {'NATUREZA',	aFieldStruct[2], aFieldStruct[3], 0})
    Aadd(aCmp, {'FINALIDADE', aFieldStruct[2], aFieldStruct[3], 0})    

    aFieldStruct := FWSX3Util():GetFieldStruct('CJK_CODIGO')
    Aadd(aCmp, {'PERFORIGEM', aFieldStruct[2], aFieldStruct[3], 0})    
    Aadd(aCmp, {'PERFDESTIN', aFieldStruct[2], aFieldStruct[3], 0})    
    Aadd(aCmp, {'UFORIGEM', 'C', 2, 0})    
    Aadd(aCmp, {'UFDESTINO', 'C', 2, 0})

    aFieldStruct := FWSX3Util():GetFieldStruct('CJG_IDCENA')
    Aadd(aCmp, {'IDCENA', aFieldStruct[2], aFieldStruct[3], 0})

    aFieldStruct := FWSX3Util():GetFieldStruct('CJG_IDPPRO')
    Aadd(aCmp, {'PERFPROD', aFieldStruct[2], aFieldStruct[3], 0})

    aFieldStruct := FWSX3Util():GetFieldStruct('CJG_IDPPAR')
    Aadd(aCmp, {'PERFPART', aFieldStruct[2], aFieldStruct[3], 0})

    Aadd(aIndice, {'01', {'TIPOMOVIM', 'NATUREZA', 'FINALIDADE', 'PERFORIGEM', 'PERFDESTIN', 'UFORIGEM', 'UFDESTINO', 'IDCENA', 'PERFPROD', 'PERFPART'}})  

    //Instancia a classe que vai gerar a tabela temporária
    self:oTable := FisTemporaryTable():New()
    self:oTable:SetField(aCmp)
    self:oTable:SetIndex(aIndice)

    //Cria a tabela temporária
    cTempAlias := self:oTable:CreateTable()

    // Busco os cenários que contenham o perfil de produto diferente do cenário que estou enviando para a Systax
    cQry += ' SELECT '
    cQry += self:obterCampoJsonConvertido(Upper(TCGetDB()))
    cQry += ' , CJG_IDCENA, CJG_IDPPRO, CJG_IDPPAR '
    cQry += ' FROM ' + RetSQLName('CJG')
    cQry += ' WHERE CJG_FILIAL = ? '
    cQry += '     AND CJG_IDCENA <> ? '
    cQry += '     AND CJG_IDPPRO <> ? '
    cQry += '     AND CJG_DTENV <> ? '
    cQry += '     AND CJG_HRENV <> ? '
    cQry += '     AND D_E_L_E_T_ = ? '

    //aBind := {valtype, value}
    aBind := {}
    aAdd(aBind, {'C', FWxFilial('CJG')})
    aAdd(aBind, {'C', ' '})     
    aAdd(aBind, {'C', cPerfil})
    aAdd(aBind, {'C', ' '})     
    aAdd(aBind, {'C', ' '})     
    aAdd(aBind, {'C', ' '})   

    //Instanciamento da classe de query
    self:oQuery := FisQuery():New()
    self:oQuery:SetQuery(cQry)
    self:oQuery:SetBind(aBind)

    cAlias := self:oQuery:ExecuteQuery(lChangeQuery)

	while ( !(cAlias)->(EoF()) )
		// Separar os dados do campo JSON e gravar no bulk
        jCenario := JsonObject():New()
        jCenario:FromJson((cAlias)->JSON)

		Aadd(aCenarios, {   jCenario['cenarios'][1]['ent_sai'],;
                            jCenario['cenarios'][1]['cod_nat_op'],;
                            jCenario['cenarios'][1]['finalidade'],;
                            jCenario['cenarios'][1]['alternativo_ex_origem'],;
                            jCenario['cenarios'][1]['destinacao'],;
                            jCenario['cenarios'][1]['uf_origem'],;
                            jCenario['cenarios'][1]['uf_destino'],;
                            (cAlias)->CJG_IDCENA,;
                            (cAlias)->CJG_IDPPRO,;
                            (cAlias)->CJG_IDPPAR})

        (cAlias)->(DbSkip())
	enddo

	//Inicializa o bulk dos dados
    self:oTable:StartBulk()
    self:oTable:BulkData(aCenarios)

    ASize(aCenarios, 0)
    ASize(aFieldStruct, 0)

return cTempAlias

/*/{Protheus.doc} limparEDestruirTabelaTemporaria
Método responsável por limpar e destruir a tabela temporaria gerada para verificar a existencia de um cenário
@author Juliano Fernandes
@since 07/01/2025
@version 12.1.2410
/*/
method limparEDestruirTabelaTemporaria() class EnviaCenarioClassificador

    self:oTable:CleanTable()
    self:oQuery:Destroy()
    self:oTable:Destroy()

    FreeObj(self:oQuery)
    FreeObj(self:oTable)

return

/*/{Protheus.doc} associarPerfilDeProdutoAoCenario
Método responsável por associar o perfil de produto ao cenario já existente
@author Juliano Fernandes
@since 07/01/2025
@version 12.1.2410
@param cPerfil, character, Código do perfil a ser associado
@param cIdCenario, character, Id do cenário a ser associado
@param cIdEnvio, character, Id de envio do cenário que está enviando
@param cMockServer, character, Mockserver
@param cPath, character, Path do mockserver
@return cJsonRet, character, Campo json em formato string com o retorno para o frontend
/*/
method associarPerfilDeProdutoAoCenario(cPerfil as character, cIdCenario as character, cIdEnvio as character, cMockServer as character, cPath as character) as character class EnviaCenarioClassificador

    local jAssociacao as json
    local jItem as json
    local oProductGroup as object
    local jRetAssociacao as json
    local jRet as json
    local cMsgErro as character
    local cMsgSucesso as character
    local dDtEnvio as date
    local cHrEnvio as character
    local jInfo as json

    jAssociacao := JsonObject():New()
    jAssociacao['items'] := {}

    jItem := JsonObject():New()
    jItem['acao'] := 'associar'     
    jItem['id_cenario'] := cIdCenario
    jItem['nome_grupo'] := cPerfil

    Aadd(jAssociacao['items'], jItem)

    oProductGroup := ProductGroup():New()

    if ( !Empty(cMockServer) )
        oProductGroup:SetMockServer(cMockServer)
        oProductGroup:SetPath(cPath)
    endif

    jRetAssociacao := oProductGroup:ProductGroupsAssociationDisassociation(jAssociacao)

    cMsgErro := STR0309 + ' ' + ; // "Um cenário com os mesmos parâmetros já foi enviado anteriormente."
                STR0310 + ' ' + ; // "A tentativa de associar o perfil de produtos ao cenário existente falhou devido a um erro."
                STR0311 + ': ' + AllTrim(cPerfil) + '. ' + ; // "Perfil de produto
                STR0259 + ': ' + AllTrim(cIdCenario) + '. ' + ; // "Cenário"
                STR0287 + ': ' + jRetAssociacao['message'] // "Erro"
    
    cMsgSucesso := STR0309 + ' ' + ; // "Um cenário com os mesmos parâmetros já foi enviado anteriormente."
                   STR0312 + ' ' + ; // "O perfil de produtos foi associado ao cenário já existente."
                   STR0311 + ': ' + AllTrim(cPerfil) + '. ' + ; // "Perfil de produto
                   STR0259 + ': ' + AllTrim(cIdCenario) + '.' // "Cenário"    

    jRet := JsonObject():New()
    jRet["sucesso"] := .F.
    jRet["erro"] := cMsgErro
    jRet["data"] := SToD('')
    jRet["hora"] := ''
    jRet["id_cenario"] := ''

    if ( jRetAssociacao['code'] == 201 )
        DbSelectArea('CJG')
        CJG->(DbSetOrder(1)) // CJG_FILIAL+CJG_IDENVI+CJG_IDPPRO+CJG_IDPPAR
        if ( CJG->(MsSeek(FWxFilial('CJG') + cIdEnvio)) )
            dDtEnvio := Date()
            cHrEnvio := Time()

            if ( !Empty(cMockServer) )
                dDtEnvio := SToD('20240101')
                cHrEnvio := '12:00:00'
            endif

            RecLock('CJG', .F.)

            CJG->CJG_IDCENA := cIdCenario
            CJG->CJG_DTENV  := dDtEnvio
            CJG->CJG_HRENV  := cHrEnvio

            if ( CJG->(FieldPos('CJG_JSRET') > 0) )
                jInfo := JsonObject():New()
                jInfo['mensagem'] := cMsgSucesso

                CJG->CJG_JSRET := jInfo:ToJson()

                FWFreeObj(jInfo)
            endif

            CJG->(MsUnLock())

            jRet["sucesso"] := .T.
            jRet["erro"] := ''
            jRet["mensagem"] := cMsgSucesso
            jRet["data"] := dDtEnvio
            jRet["hora"] := cHrEnvio
            jRet["id_cenario"] := cIdCenario                
        endif

        CJG->(DbCloseArea())            
    endif

    cJsonRet := jRet:ToJson()

    FWFreeObj(jItem)
    FWFreeObj(jAssociacao)
    FWFreeObj(jRetAssociacao)
    FWFreeObj(jRet)
    FWFreeObj(oProductGroup)

return cJsonRet

/*/{Protheus.doc} obterCampoJsonConvertido
Método responsável por obter o campo json convertido para utilização na query
@author Juliano Fernandes
@since 06/01/2025
@version 12.1.2410
@param cDbType, character, Tipo do banco de dados em que o sistema está sendo executado
@return cCampo, character, Campo json convertido para utilização na query
/*/
method obterCampoJsonConvertido(cDbType as character) as character class EnviaCenarioClassificador

    local cCampo as character 

    if ( "ORACLE" $ cDbType )
        cCampo += " UTL_RAW.CAST_TO_VARCHAR2(DBMS_LOB.SUBSTR(CJG_JSON, 4000,1)) JSON "
    elseif ( "POSTGRES" $ cDbType )
        cCampo += " encode(CJG_JSON,'escape') JSON "
    else
        cCampo += " ISNULL(CAST(CAST(CJG_JSON AS VARBINARY(8000)) AS VARCHAR(8000)),'') AS [JSON] "     
    endif 

return cCampo
