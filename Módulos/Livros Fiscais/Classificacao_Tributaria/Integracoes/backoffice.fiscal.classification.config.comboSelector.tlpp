#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

#include 'classificadorfiscal.ch'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISGetComboSelector
Classe que contém as UFs e devolve ao FrontEnd
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISGetComboSelector

    data aUf as array
    data jQuery as json
    data lRest as logical
 
    Public METHOD New() CONSTRUCTOR

    @get("/getSx5Codigo")
    Public METHOD getSx5Codigo()

    @get("/getProdutoCod")
    Public METHOD getProdutoCod()

    @get("/getPerfilCod")
    Public METHOD getPerfilCod()

    @get("/getGrupoOperFinalidade")
    Public METHOD getGrupoOperFinalidade()

    @get("/getCliFor")
    Public METHOD getCliFor()

    @get("/getStatusCenario")
    Public METHOD getStatusCenario()

    @get("/getStatusRegra")
    Public METHOD getStatusRegra()

    @get("/GetTpNf")
    Public METHOD GetTpNf()

    @get("/GetEntSaiFilter")
    Public METHOD GetEntSaiFilter()

    @get("/getFiltroEnquadramento")
    Public METHOD getFiltroEnquadramento()

    @get("/getProdutosDoGrupo")
    Public METHOD getProdutosDoGrupo()

END CLASS

//-------------------------------------------------------------------
/*/{Protheus.doc} New
Método construtor da classe
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD New() CLASS FISGetComboSelector

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getSx5Codigo
Método que retorna os códigos da SX5 de acordo com a tabela passada
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getSx5Codigo() CLASS FISGetComboSelector

  Local cQry        as character  
  Local cFilter     as character
  Local cTabela     as character
  Local oJBody      as object
  Local oJBodyDt    as object
  Local cJson       as character
  Local lOk         as logical
  Local jQuery      as object
  Local lOrigem     as logical
  Local lUf         as logical
  Local lUfOrigem   as logical
  Local lUfDestino  as logical
  Local aSX5        as array
  Local nI          as integer
  Local cChave      as character
  Local cDescricao  as character

  lOrigem		  := .F.
  lUf			    := .F.
  lUfOrigem		:= .F.
  lUfDestino	:= .F.

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter     := AllTrim(IIf(jQuery['filter'] <> Nil, jQuery['filter'], ''))

  cTabela     := IIF( jQuery['tabela'] <> Nil , jQuery['tabela'] , '' )

  If ( cTabela == 'S0')
    lOrigem := .T.
  ElseIf ( cTabela == '12')
    lUf := .T.
  ElseIf ( cTabela == '12-1')
    cTabela := '12'
    lUfOrigem := .T.
  ElseIf ( cTabela == '12-2')
    cTabela := '12'
    lUfDestino := .T.
  EndIf

  oJBody["items"] := {}

  aSX5 := FWGetSX5(cTabela)

  For nI := 1 To Len(aSX5)

    cChave      := AllTrim(aSX5[nI][3])
    cDescricao  := AllTrim(aSX5[nI][4])

    If ( !Empty(cFilter) )
      If !( Upper(cFilter) $ Upper(cChave) .Or. Upper(cFilter) $ Upper(cDescricao) )
        Loop
      EndIf
    EndIf

    oJBodyDt    := JsonObject():New()

    If lOrigem
      oJBodyDt["filter"] := 'origem: ' + cChave
    ElseIf lUf
      oJBodyDt["filter"] := 'UF: ' + cChave
    ElseIf lUfOrigem
      oJBodyDt["filter"] := 'UF origem: ' + cChave
      cTabela := '12'
    ElseIf lUfDestino
      oJBodyDt["filter"] := 'UF destino: ' + cChave
      cTabela := '12'
    EndIf

    oJBodyDt["value"] := cChave
    oJBodyDt["label"] := cChave + ' - ' + cDescricao

    Aadd(oJBody["items"],oJBodyDt)

    FreeObj(oJBodyDt)

    lOk := .T.

  Next nI

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()

  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getProdutoCod
Método que retorna uma lista com o codigo e a descrição dos produtos
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getProdutoCod() CLASS FISGetComboSelector

  Local cQry          as character  
  Local cFilter       as character
  Local oJBody        as object
  Local oJBodyDt      as object
  Local oStatement    as object
  Local cJson         as character
  Local lOk           as logical
  Local jQuery        as object
  Local nIndex        as integer

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter := IIf(jQuery['filter'] <> Nil, jQuery['filter'], '')
  
  If ( !Empty(cFilter) )
    cFilter := '%' + cFilter + '%'
  EndIf

  cQry += " SELECT F20_CODIGO, F20_DESC "
  cQry += " FROM " + RetSqlName("F20") + " F20 "
  cQry += " WHERE F20_FILIAL = ? "

  If ( !Empty(cFilter) )
    cQry += "      AND ( UPPER(F20_CODIGO) LIKE ? OR UPPER(F20_DESC) LIKE ? ) "
  EndIf

  cQry += "      AND F20_TIPO = ? "
  cQry += "      AND F20.D_E_L_E_T_ = ' ' "
  cQry += " GROUP BY F20_CODIGO, F20_DESC "
  cQry += " ORDER BY F20_CODIGO, F20_DESC "
 
  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:setString( ++nIndex, xFilial('F20') )

  If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
  EndIf

  oStatement:setString( ++nIndex, '04' )

  cQry := oStatement:getFixQuery()

  cAlias := MPSysOpenQuery(cQry)

  oJBody["items"] := {}

  While (cAlias)->(!EoF())

    oJBodyDt    := JsonObject():New()

    oJBodyDt["filter"] := 'produto: '+ AllTrim((cAlias)->F20_CODIGO)
    oJBodyDt["value"] := AllTrim((cAlias)->F20_CODIGO)
    oJBodyDt["label"] := AllTrim((cAlias)->F20_CODIGO) + ' - ' + AllTrim((cAlias)->F20_DESC)

    AADD(oJBody["items"],oJBodyDt)

    FreeObj(oJBodyDt)

    lOk := .T.

    (cAlias)->(DbSkip())
  End

  ( cAlias )->( DBCloseArea() )

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()

  FreeObj(oStatement)
  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getPerfilCod
Método que retorna uma lista com o código e a descrição do perfil
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getPerfilCod() CLASS FISGetComboSelector

  Local cQry        as character
  Local cFilter     as character
  Local oJBody      as object
  Local oJBodyDt    as object
  Local oStatement  as object
  Local cJson       as character
  Local lOk         as logical
  Local jQuery      as object
  Local nIndex      as integer

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter := IIf(jQuery['filter'] <> Nil, jQuery['filter'], '')
  
  If ( !Empty(cFilter) )
    cFilter := '%' + cFilter + '%'
  EndIf

  cQry += " SELECT F20_CODIGO, F20_DESC "
  cQry += " FROM " + RetSqlName("F20") + " F20 "
  cQry += " WHERE F20_FILIAL = ? "

  If ( !Empty(cFilter) )
    cQry += "      AND ( UPPER(F20_CODIGO) LIKE ? OR UPPER(F20_DESC) LIKE ? ) "
  EndIf

  cQry += "      AND F20_TIPO = ? "
  cQry += "      AND F20.D_E_L_E_T_ = ' ' "
  cQry += " GROUP BY F20_CODIGO, F20_DESC "
  cQry += " ORDER BY F20_CODIGO, F20_DESC "
  
  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:setString( ++nIndex, xFilial('F20') )

  If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
  EndIf

  oStatement:setString( ++nIndex, '02' )

  cQry := oStatement:getFixQuery()

  cAlias := MPSysOpenQuery(cQry)

  oJBody["items"] := {}

  While (cAlias)->(!EoF())

    oJBodyDt    := JsonObject():New()

    oJBodyDt["filter"] := 'perfil: '+ AllTrim((cAlias)->F20_CODIGO)
    oJBodyDt["value"] := AllTrim((cAlias)->F20_CODIGO)
    oJBodyDt["label"] := AllTrim((cAlias)->F20_CODIGO) + ' - ' + AllTrim((cAlias)->F20_DESC)

    AADD(oJBody["items"],oJBodyDt)

    FreeObj(oJBodyDt)

    lOk := .T.

    (cAlias)->(DbSkip())
  End

  ( cAlias )->( DBCloseArea() )

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()

  FreeObj(oStatement)
  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)


//-------------------------------------------------------------------
/*/{Protheus.doc} getGrupoOper
Método que retorna uma lista com os crupos de operação
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getGrupoOperFinalidade() CLASS FISGetComboSelector

  Local cQry        as character  
  Local cFilter     as character
  Local cTipo       as character
  Local oJBody      as object
  Local oJBodyDt    as object
  Local oStatement  as object
  Local cJson       as character
  Local lOk         as logical
  Local jQuery      as object
  Local nIndex      as integer

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter := IIf(jQuery['filter'] <> Nil, jQuery['filter'], '')

  If ( !Empty(cFilter) )
    cFilter := '%' + cFilter + '%'
  EndIf
  
  cTipo := IIF( jQuery['tipo'] <> Nil , jQuery['tipo'] , '%%' )

  cQry += " SELECT CJH_GRUPO "
  cQry += " FROM " + RetSqlName("CJH") + " CJH "
  cQry += " WHERE CJH_FILIAL = ? "

  If ( !Empty(cFilter) )
    cQry += "      AND UPPER(CJH_GRUPO) LIKE ? "
  EndIf

  cQry += "      AND CJH_TIPO = ? "
  cQry += "      AND CJH.D_E_L_E_T_ = ' ' "
  cQry += " GROUP BY CJH_GRUPO "
  
  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:setString( ++nIndex, xFilial('CJH') )

  If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
  EndIf

  oStatement:setString( ++nIndex, Upper(cTipo) )

  cQry := oStatement:getFixQuery()

  cAlias := MPSysOpenQuery(cQry)

  oJBody["items"] := {}

  While (cAlias)->(!EoF())

    oJBodyDt    := JsonObject():New()

    If !Empty((cAlias)->CJH_GRUPO)
    
      oJBodyDt["filter"] := 'grupo: '+ AllTrim((cAlias)->CJH_GRUPO)
      oJBodyDt["value"] := AllTrim((cAlias)->CJH_GRUPO)
      oJBodyDt["label"] := AllTrim((cAlias)->CJH_GRUPO)

      AADD(oJBody["items"],oJBodyDt)

      FreeObj(oJBodyDt)

      lOk := .T.
    EndIf

    (cAlias)->(DbSkip())
  End

  ( cAlias )->( DBCloseArea() )

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()
  
  FreeObj(oStatement)
  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getCliFor
Método que retorna uma lista com Cliente e Fornecedor
@author  almeida.veronica
@since   21.09.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getCliFor() CLASS FISGetComboSelector

  Local oJBody    as object
  Local oJBodyDt  as object
  Local cJson     as character

  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()

  oJBody["items"] := {}

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'tipo: CLIENTE'
  oJBodyDt["value"] := 'CLIENTE'
  oJBodyDt["label"] := 'Cliente'

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'tipo: FORNECEDOR'
  oJBodyDt["value"] := 'FORNECEDOR'
  oJBodyDt["label"] := 'Fornecedor'

  AADD(oJBody["items"],oJBodyDt)

  oJBody["success"] := .T.
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()
  FREEOBJ( oJBody )
  FREEOBJ( oJBodyDt )

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getStatusCenario
Método que retorna uma lista com Cliente e Fornecedor
@author  Erich Buttner
@since   20.10.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getStatusCenario() CLASS FISGetComboSelector

  Local oJBody    as object
  Local oJBodyDt  as object
  Local cJson     as character

  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()

  oJBody["items"] := {}

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'status: enviado'
  oJBodyDt["value"] := 'enviado'
  oJBodyDt["label"] := 'Enviado'

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'status: a enviar'
  oJBodyDt["value"] := 'a enviar'
  oJBodyDt["label"] := 'A Enviar'

  AADD(oJBody["items"],oJBodyDt)

  oJBody["success"] := .T.
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()
  FREEOBJ( oJBody )
  FREEOBJ( oJBodyDt )

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getStatusRegra
Método que retorna uma lista com Cliente e Fornecedor
@author  Erich Buttner
@since   01.11.2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getStatusRegra() CLASS FISGetComboSelector

  Local oJBody    as object
  Local oJBodyDt  as object
  Local cJson     as character

  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()

  oJBody["items"] := {}

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'status: ' + Lower(STR0110) // "Recusada"
  oJBodyDt["value"] := Lower(STR0110) // "Recusada"
  oJBodyDt["label"] := STR0110 // "Recusada"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'status: ' + Lower(STR0112) // "Inativa"
  oJBodyDt["value"] := Lower(STR0112) // "Inativa"
  oJBodyDt["label"] := STR0112 // "Inativa"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBodyDt["filter"] := 'status: ' + Lower(STR0111) // "Suspensa"
  oJBodyDt["value"] := Lower(STR0111) // "Suspensa"
  oJBodyDt["label"] := STR0111 // "Suspensa"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()
  oJBodyDt["filter"] := 'status: ' + Lower(STR0109) // "Aprovada"
  oJBodyDt["value"] := Lower(STR0109) // "Aprovada"
  oJBodyDt["label"] := STR0109 // "Aprovada"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()
  oJBodyDt["filter"] := 'status: ' + Lower(STR0113) // "Pendente"
  oJBodyDt["value"] := Lower(STR0113) // "Pendente"
  oJBodyDt["label"] := STR0113 // "Pendente"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()
  oJBodyDt["filter"] := 'status: ' + Lower(STR0161) // "Rejeitada"
  oJBodyDt["value"] := Lower(STR0161) // "Rejeitada"
  oJBodyDt["label"] := STR0161 // "Rejeitada"

  AADD(oJBody["items"],oJBodyDt)

  FREEOBJ( oJBodyDt )
  oJBodyDt    := JsonObject():New()

  oJBody["success"] := .T.
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()
  FREEOBJ( oJBody )
  FREEOBJ( oJBodyDt )

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getTpNf
Método que retorna uma lista com os tipos de notas fiscais
@author  Erich Buttner
@since   01.02.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetTpNf() CLASS FISGetComboSelector

  Local jBody as json
  Local jDetail as json
  Local cBody as character
  Local jQueryParams as json
  Local cFilter as character
  Local jHeaderResponse as json

	jHeaderResponse := JsonObject():New()
	jHeaderResponse['Content-Type'] := 'application/json;charset=cp1252'
	oRest:SetHeaderResponse(jHeaderResponse)
  FreeObj(jHeaderResponse)

  jBody := JsonObject():New()
  jBody['TipoNotaFiscal'] := {}
  jBody['success'] := .T.
  jBody['hasNext'] := .F.

  jQueryParams := oRest:GetQueryRequest()

  If ( jQueryParams:HasProperty('filGroup') .And. !('*' $ jQueryParams['filGroup']) )
    cFilter := FwNoAccent(Lower(DecodeUTF8(jQueryParams['filGroup'], 'cp1252')))
  EndIf

  If (Empty(cFilter)) .Or. (!Empty(cFilter) .And. (('codigo: 1' $ cFilter) .Or. (cFilter $ '1 entradas')))
    jDetail := JsonObject():New()
    jDetail['entrada_saida'] := '1'
    jDetail['desc'] := 'Entradas'

    Aadd(jBody['TipoNotaFiscal'], jDetail)

    FreeObj(jDetail)
  EndIf

  If (Empty(cFilter)) .Or. (!Empty(cFilter) .And. (('codigo: 3' $ cFilter) .Or. (cFilter $ '3 saidas')))
    jDetail := JsonObject():New()
    jDetail['entrada_saida'] := '3'
    jDetail['desc'] := 'Saídas'

    Aadd(jBody['TipoNotaFiscal'], jDetail)

    FreeObj(jDetail)
  EndIf

  cBody := jBody:ToJson()
  FreeObj(jBody)

Return oRest:SetResponse(cBody)

//-------------------------------------------------------------------
/*/{Protheus.doc} GetEntSaiFilter
Método que retorna o tipo de nota fiscal de entrada e saída para o filtro de notas fiscais
@author  Erich Buttner
@since   23.06.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GetEntSaiFilter() CLASS FISGetComboSelector

  Local jBody as json
  Local jDetail as json
  Local cBody as character
  Local jHeaderResponse as json

	jHeaderResponse := JsonObject():New()
	jHeaderResponse['Content-Type'] := 'application/json;charset=cp1252'
	oRest:SetHeaderResponse(jHeaderResponse)    
  FreeObj(jHeaderResponse)  

  jBody := JsonObject():New()
  jBody['items'] := {}
  jBody['success'] := .T.
  jBody['hasNext'] := .F.

  jDetail := JsonObject():New()
  jDetail['filter'] := 'codigo: 1'
  jDetail['value'] := 'entradas'
  jDetail['label'] := 'Entradas'

  Aadd(jBody['items'], jDetail)

  FreeObj(jDetail)

  jDetail := JsonObject():New()
  jDetail['filter'] := 'codigo: 3'
  jDetail['value'] := 'saidas'
  jDetail['label'] := 'Saídas'

  Aadd(jBody['items'], jDetail)

  FreeObj(jDetail)

  cBody := jBody:ToJson()
  FreeObj(jBody)

Return oRest:SetResponse(cBody)

//-------------------------------------------------------------------
/*/{Protheus.doc} getFiltroEnquadramento
Método que retorna uma lista com os enquadramentos (CJK) para uso 
no filtro da tela de Perfis - Segmento de Negocio da Origem e Segmento 
de Negocio de Destino na Classificação Tributária

@author  Juliano Fernandes
@since   06.10.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getFiltroEnquadramento() CLASS FISGetComboSelector

  Local cQry      as character  
  Local cFilter   as character
  Local cTipo     as character
  Local oJBody    as object
  Local oJBodyDt  as object
  Local cJson     as character
  Local lOk       as logical
  Local jQuery    as object
  Local nIndex    as integer

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter := IIf(jQuery['filter'] <> Nil, jQuery['filter'], '')

  If ( !Empty(cFilter) )
    	cFilter := '%' + cFilter + '%'
  EndIf
  
  cTipo := IIF( jQuery['tipo'] <> Nil , jQuery['tipo'] , '' )

  cQry += " SELECT CJK_CODIGO, CJK_DESC "
  cQry += " FROM " + RetSqlName("CJK") + " CJK "
  cQry += " WHERE CJK_FILIAL = ? "

  If ( !Empty(cFilter) )
    cQry += "      AND ( UPPER(CJK_CODIGO) LIKE ? OR UPPER(CJK_DESC) LIKE ? ) "
  EndIf

  cQry += "      AND CJK_TIPO = ? "
  cQry += "      AND D_E_L_E_T_ = ' ' "
  cQry += " GROUP BY CJK_CODIGO, CJK_DESC "
  cQry += " ORDER BY CJK_CODIGO, CJK_DESC "
  
  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:setString( ++nIndex, xFilial('CJK') )

  If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
  EndIf

  oStatement:setString( ++nIndex, Upper(cTipo)   )

  cQry := oStatement:getFixQuery()

  cAlias := MPSysOpenQuery(cQry)

  oJBody["items"] := {}

  While (cAlias)->(!EoF())

    oJBodyDt    := JsonObject():New()
    
    oJBodyDt["filter"] := 'enquadramento: '+ AllTrim((cAlias)->CJK_CODIGO)
    oJBodyDt["value"] := AllTrim((cAlias)->CJK_CODIGO)
    oJBodyDt["label"] := AllTrim((cAlias)->CJK_CODIGO) + " - " + AllTrim((cAlias)->CJK_DESC)

    AADD(oJBody["items"],oJBodyDt)

    FreeObj(oJBodyDt)

    lOk := .T.

    (cAlias)->(DbSkip())
  End

  ( cAlias )->( DBCloseArea() )

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()

  FreeObj(oStatement)
  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)

//-------------------------------------------------------------------
/*/{Protheus.doc} getProdutosDoGrupo
Método que retorna uma lista de produtos do grupo informado no filtro
Utilizado na busca avançada da tela de Perfis de Produto na 
Classificação Tributária

@author  Juliano Fernandes
@since   09.10.2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD getProdutosDoGrupo() CLASS FISGetComboSelector

  Local cQry      as character  
  Local cFilter   as character
  Local oJBody    as object
  Local oJBodyDt  as object
  Local cJson     as character
  Local lOk       as logical
  Local jQuery    as object
  Local nIndex    as integer

  cQry        := ""
  oJBody      := JsonObject():New()
  oJBodyDt    := JsonObject():New()
  jQuery      := oRest:GetQueryRequest()

  cFilter := IIf(jQuery['filter'] <> Nil, jQuery['filter'], '')

  If ( !Empty(cFilter) )
    cFilter := '%' + cFilter + '%'
  EndIf

  cQry += " SELECT F24_CDPROD, B1_DESC "
  cQry += " FROM " + RetSqlName("F24") + " F24 "
  cQry += "   INNER JOIN " + RetSqlName("F20") + " F20 ON F20_FILIAL = ? AND F20_CODIGO = F24_CODIGO AND F20.D_E_L_E_T_ = ' ' "
  cQry += "   INNER JOIN " + RetSqlName("SB1") + " SB1 ON B1_FILIAL = ? AND B1_COD = F24_CDPROD AND SB1.D_E_L_E_T_ = ' ' "
  cQry += " WHERE F24_FILIAL = ? "

  If ( !Empty(cFilter) )
    cQry += "   AND ( UPPER(F24_CDPROD) LIKE ? OR UPPER(B1_DESC) LIKE ? ) "
  EndIf

  cQry += "   AND F24.D_E_L_E_T_ = ' ' "
  cQry += " GROUP BY F24_CDPROD, B1_DESC  "
  
  oStatement := FWPreparedStatement():New( ChangeQuery(cQry) )

  oStatement:setString( ++nIndex, xFilial('F20') )
  oStatement:setString( ++nIndex, xFilial('SB1') )
  oStatement:setString( ++nIndex, xFilial('F24') )

  If ( !Empty(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
    oStatement:setString( ++nIndex, Upper(cFilter) )
  EndIf

  cQry := oStatement:getFixQuery()

  cAlias := MPSysOpenQuery(cQry)

  oJBody["items"] := {}

  While (cAlias)->(!EoF())

    oJBodyDt    := JsonObject():New()
    
    oJBodyDt["filter"] := 'item: ' + AllTrim((cAlias)->F24_CDPROD)
    oJBodyDt["value"] := AllTrim((cAlias)->F24_CDPROD)
    oJBodyDt["label"] := AllTrim((cAlias)->F24_CDPROD) + " - " + AllTrim((cAlias)->B1_DESC)

    AADD(oJBody["items"],oJBodyDt)

    FreeObj( oJBodyDt )

    lOk := .T.

    (cAlias)->(DbSkip())
  End

  ( cAlias )->( DBCloseArea() )

  oJBody["success"] := lOk
  oJBody["hasNext"] := .F.

  cJson := oJBody:toJson()

  FreeObj(oStatement)
  FreeObj(oJBody)
  FreeObj(jQuery)

Return oRest:setResponse(cJson)
