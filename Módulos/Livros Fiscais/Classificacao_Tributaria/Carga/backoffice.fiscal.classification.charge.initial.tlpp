#Include "Protheus.ch"
#Include "RwMake.ch"
#Include "FwBrowse.ch"
#Include "FwMvcDef.ch"
#Include "TOPCONN.ch"
#INCLUDE "TBICONN.CH"
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace totvs.protheus.backoffice.fiscal.Classifier

//-------------------------------------------------------------------
/*/{Protheus.doc} FISImportCarga
Classe que contém as operações referente a integração com a API da 
Systax
@author  Erich Buttner
@since   20/04/2022
@version 1.0
/*/
//-------------------------------------------------------------------
CLASS FISImportCarga

    data aNatOperFin    as array
    data aDeParaForm    as array
    data aParam         as array
    data aImpEnquad     as array

    data oStatementCJE  as object
    data oStatementCJH  as object
    data oStatementCJF  as object

    public method ImportNatOper(aNatOperFin, lAutomato)
    public method ImpDeParaFormul(aDeParaForm)
    public method ImpParamClassif(aParam)
    public method ImportEnquad(aImpEnquad)

    method BuscaRegistroCJE(cParam)
    method SetStatementCJE()
    method GravaRegistroCJE(cParam, cConteu, cTipo, nCJERecno)

    method BuscaRegistroCJH(cCod, cTipo)
    method SetStatementCJH()
    method GravaRegistroCJH(cCod, cDescr, cGrupo, cTipo, nCJHRecno)

    method BuscaRegistroCJF(cCod, cTipo, cCFOP)
    method SetStatementCJF()
    method GravaRegistroCJF(cCod, cTipo, cCFOP)

    public method RemoveEnquad()

END CLASS

/*/{Protheus.doc} ImpTabNatOper
    Importacão inicial das tabelas de natureza de Operação e finalidade
    @type  Function
    @author Erich Buttner
    @since 18/04//2022
    @version version
    @param param, param_type, param_descr
    @return 
    @example
    (examples)
    @see (links_or_references)
    /*/
METHOD ImportNatOper(aNatOperFin, lAutomato) CLASS FISImportCarga

    Local nE        := 0
    Local nE2       := 0
    Local cCFOPs    := ""
    Local cAliasCJH as character
    Local cAliasCJF as character
    Local aCFOP     as array
    Local nTamCFOP  as integer
    Local cCod      as character
    Local cDescr    as character
    Local cGrupo    as character
    Local cTpNat    as character
    Local cCFOP     as character

    aCFOP := FWGetSX5("13")
    nTamCFOP := TamSX3("X5_CHAVE")[1]

    For nE := 1 To Len(aNatOperFin)
        cCod   := aNatOperFin[nE][1]
        cDescr := AllTrim(aNatOperFin[nE][2])
        cGrupo := AllTrim(aNatOperFin[nE][3])
        cTpNat := Iif(SubStr(cValToChar(aNatOperFin[nE][4]),1,1) $ '1|2|3', '2', '1')

        cAliasCJH := ::BuscaRegistroCJH(cCod, cTpNat)

        If (cAliasCJH)->(EoF())
            ::GravaRegistroCJH(cCod, cDescr, cGrupo, cTpNat, 0)

            For nE2 := 4 To Len(aNatOperFin[nE])

                If !(aNatOperFin[nE][nE2] $ cCFOPs)
                    cCFOP := PadR(aNatOperFin[nE][nE2], nTamCFOP)

                    If ( AScan(aCFOP, { |x| x[3] == cCFOP }) > 0 )
                        cAliasCJF := ::BuscaRegistroCJF(cCod, cTpNat, cCFOP)

                        If (cAliasCJF)->(EoF())
                            ::GravaRegistroCJF(cCod, cTpNat, cCFOP)
                        EndIf

                        (cAliasCJF)->(DbCloseArea())
                    EndIf

                    cCFOPs += aNatOperFin[nE][nE2]
                EndIf

            Next nE2
        Else
            If ( AllTrim((cAliasCJH)->CJH_DESCR) != cDescr .Or. lAutomato )
                ::GravaRegistroCJH(cCod, cDescr, Nil, Nil, (cAliasCJH)->CJH_RECNO)
            EndIf
        EndIf

        (cAliasCJH)->(DbCloseArea())

    Next nE

    FreeObj(::oStatementCJH)
    FreeObj(::oStatementCJF)

    FWFreeArray(aCFOP)

Return

/*/{Protheus.doc} ImpDeParaFormul
    Importacão inicial do De/Para das formulas do classificador
    @type  Function
    @author Erich Buttner
    @since 06/06//2022
    @version version
    @param param, param_type, param_descr
    @return 
    @example
    (examples)
    @see (links_or_references)
    /*/
METHOD ImpDeParaFormul(aDeParaForm) CLASS FISImportCarga

    Local nE          as integer
    Local cAliasQry   as character
    Local cParam      as character

    For nE := 1 To Len(aDeParaForm)

        cParam := PadR(Upper(aDeParaForm[nE][1]), TamSX3("CJE_PARAM")[1])

        cAliasQry := ::BuscaRegistroCJE(cParam)

        If (cAliasQry)->(EOF())
            ::GravaRegistroCJE(cParam, aDeParaForm[nE][2], "2", 0)
        EndIf

        (cAliasQry)->(DbCloseArea())

    Next nE

    FreeObj(::oStatementCJE)

Return

/*/{Protheus.doc} ImpParamClassif
    Importacão inicial dos parametros do classificador
    @type  Function
    @author Erich Buttner
    @since 06/06//2022
    @version version
    @param param, param_type, param_descr
    @return 
    @example
    (examples)
    @see (links_or_references)
    /*/
METHOD ImpParamClassif(aParam) CLASS FISImportCarga

    Local nE          as integer
    Local cAliasQry   as character
    Local cParam      as character
    Local cConteu     as character

    For nE := 1 To Len(aParam)

        cParam := PadR(Upper(aParam[nE][1]), TamSX3("CJE_PARAM")[1])

        cAliasQry := ::BuscaRegistroCJE(cParam)

        If (cAliasQry)->(EOF())
            cConteu := ""

            If AllTrim(cParam) $ "SENHADEFAULT"
                If !Empty(aParam[nE][2])
                    cConteu := rc4crypt(aParam[nE][2],"123456789",.F.) //Encripta e decripta uma determinada cadeia de caracteres usando o algoritmo RC4.
                EndIf
            Else
                cConteu := aParam[nE][2]
            EndIf

            ::GravaRegistroCJE(cParam, cConteu, "1", 0)

        ElseIf AllTrim(cParam) $ "URLPRINCIPAL"

            If !Empty(aParam[nE][2])

                ::GravaRegistroCJE(Nil, aParam[nE][2], Nil, (cAliasQry)->CJE_RECNO)

            EndIf

        EndIf

        (cAliasQry)->(DbCloseArea())

    Next nE

    FreeObj(::oStatementCJE)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} BuscaRegistroCJE
Método que realiza a busca de registro na tabela CJE - 
Control Config Classificador  

@author  Juliano Fernandes
@since   30/11/2023
@version 1.0
@param   cParam, character, Parametro a ser buscado na tabela CJE
@return  Alias com o registro encontrado
/*/
//-------------------------------------------------------------------
METHOD BuscaRegistroCJE(cParam as character) CLASS FISImportCarga

    If ( self:oStatementCJE == Nil )
        self:SetStatementCJE()
    EndIf

    self:oStatementCJE:SetString( 1, xFilial("CJE") )
    self:oStatementCJE:SetString( 2, cParam )
    self:oStatementCJE:SetString( 3, ' ' )

Return self:oStatementCJE:OpenAlias(GetNextAlias())

//-------------------------------------------------------------------
/*/{Protheus.doc} SetStatementCJE
Método para obter o statement da tabela CJE - 
Control Config Classificador  

@author  Juliano Fernandes
@since   07/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD SetStatementCJE() CLASS FISImportCarga

    Local cQry as character

    cQry := "SELECT R_E_C_N_O_ CJE_RECNO "
    cQry += "FROM " + RetSqlName("CJE") + " CJE "
    cQry += "WHERE CJE_FILIAL = ? "
    cQry += " AND CJE_PARAM = ? "
    cQry += " AND D_E_L_E_T_ = ? "

    self:oStatementCJE := FWExecStatement():New( ChangeQuery(cQry) )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaRegistroCJE
Método que realiza a gravação de registro na tabela CJE - 
Control Config Classificador  

@author  Juliano Fernandes
@since   30/11/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GravaRegistroCJE(cParam, cConteu, cTipo, nCJERecno) CLASS FISImportCarga

    DbSelectArea("CJE")

    If ( nCJERecno == 0 )
        RecLock("CJE", .T.)

        CJE->CJE_FILIAL := xFilial("CJE")
        CJE->CJE_PARAM  := cParam
        CJE->CJE_TIPO   := cTipo
    Else
        CJE->(DbGoTo(nCJERecno))

        RecLock("CJE", .F.)
    EndIf

    CJE->CJE_CONTEU := cConteu

    CJE->(MsUnlock())

    CJE->(DbCloseArea())

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} FISImportEnquad
Método que realiza a importação do arquivo Json para a tabela CJK
Systax
@author  Yuri Gimenes
@since   13/05/2022
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD ImportEnquad(aImpEnquad) CLASS FISImportCarga

    Local nE := 0 as numeric
    Local cCodigo as character
    Local cTipo as character
    Local nTamCodigo := 0 as numeric
    Local nTamTipo := 0 as numeric

    nTamCodigo := FWSX3Util():GetFieldStruct('CJK_CODIGO')[3]
    nTamTipo   := FWSX3Util():GetFieldStruct('CJK_TIPO')[3]

    DbSelectArea('CJK')
    CJK->(DbSetOrder(1))

    For nE := 1 To Len(aImpEnquad)
        If Len(aImpEnquad[nE][1]) <= nTamCodigo
            cCodigo := PadR(aImpEnquad[nE][1] ,nTamCodigo)
            cTipo   := PadR(aImpEnquad[nE][3] ,nTamTipo)

            If (CJK->(MsSeek(xFilial("CJK")+cCodigo+cTipo)))
                CJK->(RecLock('CJK',.F.))
                CJK->CJK_DESC   := AllTrim(aImpEnquad[nE][2])
                CJK->(MsUnLock())

            Else
                CJK->(RecLock('CJK',.T.))
                CJK->CJK_FILIAL := xFilial("CJK")
                CJK->CJK_CODIGO := aImpEnquad[nE][1]
                CJK->CJK_DESC   := AllTrim(aImpEnquad[nE][2])
                CJK->CJK_TIPO   := aImpEnquad[nE][3] //1 - Remetente/Fornecedor | 2 - Destinatario/Cliente
                CJK->(MsUnLock())

            Endif
        EndIf
    Next nE

    CJK->(DbCloseArea())

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} BuscaRegistroCJH
Método que realiza a busca de registro na tabela CJH - 
Cab. Cad. Nat. Oper. Classif. 

@author  Juliano Fernandes
@since   03/12/2023
@version 1.0
@param   cCod, character, Código a ser buscado na tabela CJH
@param   cTipo, character, Tipo a ser buscado na tabela CJH
@return  Alias com o registro encontrado
/*/
//-------------------------------------------------------------------
METHOD BuscaRegistroCJH(cCod as character, cTipo as character) CLASS FISImportCarga

    If ( self:oStatementCJH == Nil )
        self:SetStatementCJH()
    EndIf

    self:oStatementCJH:SetString( 1, xFilial("CJH") )
    self:oStatementCJH:SetString( 2, cCod )
    self:oStatementCJH:SetString( 3, cTipo )
    self:oStatementCJH:SetString( 4, ' ' )

Return self:oStatementCJH:OpenAlias(GetNextAlias())

//-------------------------------------------------------------------
/*/{Protheus.doc} SetStatementCJH
Método para obter o statement da tabela CJH - 
Cab. Cad. Nat. Oper. Classif. 

@author  Juliano Fernandes
@since   07/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD SetStatementCJH() CLASS FISImportCarga

    Local cQry as character

    cQry := "SELECT R_E_C_N_O_ CJH_RECNO, CJH_DESCR "
    cQry += "FROM " + RetSqlName("CJH") + " CJH "
    cQry += "WHERE CJH_FILIAL = ? "
    cQry += " AND CJH_COD = ? "
    cQry += " AND CJH_TIPO = ? "
    cQry += " AND D_E_L_E_T_ = ? "

    self:oStatementCJH := FWExecStatement():New( ChangeQuery(cQry) )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaRegistroCJH
Método que realiza a gravação de registro na tabela CJH - 
Cab. Cad. Nat. Oper. Classif. 

@author  Juliano Fernandes
@since   03/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GravaRegistroCJH(cCod, cDescr, cGrupo, cTipo, nCJHRecno) CLASS FISImportCarga

    DbSelectArea("CJH")

    If ( nCJHRecno == 0 )
        RecLock("CJH", .T.)

        CJH->CJH_FILIAL := xFilial("CJH")
        CJH->CJH_COD    := cCod
        CJH->CJH_GRUPO  := cGrupo
        CJH->CJH_TIPO   := cTipo
    Else
        CJH->(DbGoTo(nCJHRecno))

        RecLock("CJH", .F.)
    EndIf

    CJH->CJH_DESCR := cDescr

    CJH->(MsUnlock())

    CJH->(DbCloseArea())

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} BuscaRegistroCJF
Método que realiza a busca de registro na tabela CJF - 
Cad. Nat. Oper. Classif.

@author  Juliano Fernandes
@since   03/12/2023
@version 1.0
@param   cCod, character, Código a ser buscado na tabela CJF
@param   cTipo, character, Tipo a ser buscado na tabela CJF
@param   cCFOP, character, CFOP a ser buscado na tabela CJF
@return  Alias com o registro encontrado
/*/
//-------------------------------------------------------------------
METHOD BuscaRegistroCJF(cCod as character, cTipo as character, cCFOP as character) CLASS FISImportCarga

    If ( self:oStatementCJF == Nil )
        self:SetStatementCJF()
    EndIf

    self:oStatementCJF:SetString( 1, xFilial("CJF") )
    self:oStatementCJF:SetString( 2, cCod )
    self:oStatementCJF:SetString( 3, cTipo )
    self:oStatementCJF:SetString( 4, cCFOP )
    self:oStatementCJF:SetString( 5, ' ' )

Return self:oStatementCJF:OpenAlias(GetNextAlias())

//-------------------------------------------------------------------
/*/{Protheus.doc} SetStatementCJF
Método que realiza a busca de registro na tabela CJF - 
Cad. Nat. Oper. Classif.

@author  Juliano Fernandes
@since   07/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD SetStatementCJF() CLASS FISImportCarga

    Local cQry as character

    cQry := "SELECT R_E_C_N_O_ CJF_RECNO "
    cQry += "FROM " + RetSqlName("CJF") + " CJF "
    cQry += "WHERE CJF_FILIAL = ? "
    cQry += " AND CJF_COD = ? "
    cQry += " AND CJF_TIPO = ? "
    cQry += " AND CJF_CFOP = ? "
    cQry += " AND D_E_L_E_T_ = ? "

    self:oStatementCJF := FWExecStatement():New( ChangeQuery(cQry) )

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GravaRegistroCJF
Método que realiza a gravação de registro na tabela CJF - 
Cad. Nat. Oper. Classif.

@author  Juliano Fernandes
@since   03/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------
METHOD GravaRegistroCJF(cCod, cTipo, cCFOP) CLASS FISImportCarga

    DbSelectArea("CJF")

    RecLock("CJF", .T.)

    CJF->CJF_FILIAL := xFilial("CJF")
    CJF->CJF_COD    := cCod
    CJF->CJF_TIPO   := cTipo
    CJF->CJF_CFOP   := cCFOP

    CJF->(MsUnlock())

    CJF->(DbCloseArea())

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} RemoveEnquad
Método que realiza a remoção de enquadramentos da tabela CJK

@author  Juliano Fernandes
@since   11/02/2025
@version 1.0
@param   aRemove, array, Array com os enquadramentos a serem removidos
/*/
//-------------------------------------------------------------------
method RemoveEnquad(aRemove as array) class FISImportCarga

    local i as numeric
    local cCodigo as character
    local cTipo as character
    local nTamCodigo := 0 as numeric
    local nTamTipo := 0 as numeric

    nTamCodigo := FWSX3Util():GetFieldStruct('CJK_CODIGO')[3]
    nTamTipo   := FWSX3Util():GetFieldStruct('CJK_TIPO')[3]

    DbSelectArea('CJK')
    CJK->(DbSetOrder(1)) // CJK_FILIAL+CJK_CODIGO+CJK_TIPO

    for i := 1 to Len(aRemove)
        cCodigo := PadR(aRemove[i][1], nTamCodigo)
        cTipo   := PadR(aRemove[i][3], nTamTipo)

        if ( CJK->(MsSeek(FWxFilial('CJK') + cCodigo + cTipo)) )

            CJK->(RecLock('CJK', .F.))
            CJK->(DbDelete())
            CJK->(MsUnLock())

        endif
    next i

    CJK->(DbCloseArea())

return
