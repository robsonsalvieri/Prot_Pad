#include 'protheus.ch'
#include 'fwMBrowse.ch'
#include 'fwMvcDef.ch'
#include 'tlpp-core.th'
#include "taxClassification.ch"

nameSpace totvs.protheus.backoffice.fiscal.configurador.tax.classification.registration

static cProgram := 'totvs.protheus.backoffice.fiscal.configurador.tax.classification.registration.TaxClassification'

/*/{Protheus.doc} taxClassification
Função que cria o cadastro de códigos cClassFis.
@type function
@version 12.1.2410 
@author Vogas
@since 4/06/2025
/*/
function taxClassification(cTesteAutomatizado) as object

	local oBrowse as object
	default cTesteAutomatizado := ''

	if ( aliasIndic('CKB') ) .and. empty(cTesteAutomatizado)
		oBrowse := fwMBrowse():new()
		oBrowse:setAlias('CKB')		
		oBrowse:setUseFilter(.T.)
		oBrowse:setDescription(STR0002) //'Códigos classificação tributária IBS/CBS.' 		
		oBrowse:activate()
	else
		help('',1,'Help','Help',STR0003,1,0) //'Dicionário desatualizado, verifique as atualizações do motor tributário fiscal.' 
	endIf

return oBrowse

/*/{Protheus.doc} MenuDef
Função que cria o menu do cadastro de códigos cClassFis.
@type function
@version 12.1.2410
@author Vogas
@since 27/06/2025
@return array, Menu do cadastro de códigos cClassFis.
/*/
function menuDef() as array	

local aMenu:= {} as array	

	add option aMenu title STR0001 action 'VIEWDEF.' + cProgram operation 2 access 0 // 'Visualizar'	
	add option aMenu title STR0007 action 'VIEWDEF.' + cProgram operation 3 access 0 // 'Incluir'
	add option aMenu title STR0008 action 'VIEWDEF.' + cProgram operation 4 access 0 // 'Alterar'
	add option aMenu title STR0009 action 'VIEWDEF.' + cProgram operation 5 access 0 // 'Excluir'
	add option aMenu title STR0010 action 'VIEWDEF.' + cProgram operation 9 access 0 // 'Copiar'
                                                                
    add option aMenu title STR0011 action 'totvs.protheus.backoffice.fiscal.configurador.tax.classification.registration.import.ImpClassTribIbsCbs()' operation 3 access 0 //'Importa Tabela'

return aMenu

/*/{Protheus.doc} ModelDef
Função que cria o modelo de dados do cadastro de códigos cClassFis.
@type function
@version 12.1.2410
@author Vogas
@since 4/06/2025
@return object, Modelo de dados do cadastro de códigos cClassFis.
/*/
function modelDef() as object

	local oModel as object
	local oTaxClassificationStruct as object

	oTaxClassificationStruct:= fwFormStruct(1,'CKB')
	oTaxClassificationStruct:setProperty('CKB_CSTCCT',  MODEL_FIELD_VALID, {|| validateIdentifierCode(oModel)})
	oTaxClassificationStruct:setProperty('CKB_DTINI' , 	MODEL_FIELD_VALID, {|| validateIdentifierCode(oModel)})
	oTaxClassificationStruct:setProperty('CKB_DTFIM' , 	MODEL_FIELD_VALID, {|| validateIdentifierCode(oModel)})
	oTaxClassificationStruct:setProperty('CKB_CSTCCT', 	MODEL_FIELD_WHEN , {|| (oModel:getOperation() == 3)	 })
	oTaxClassificationStruct:setProperty('CKB_DTINI' , 	MODEL_FIELD_WHEN , {|| (oModel:getOperation() == 3)	 })
	
	oModel := mpFormModel():new('TAXCLASSIFMOD') // Cria o objeto do Modelo de Dados

	oModel:addFields('TAXCLASSIFMOD',,oTaxClassificationStruct) // Adiciona ao modelo uma estrutura de formulário de edição por campo
	
return oModel

/*/{Protheus.doc} ViewDef
Função que cria a View do cadastro de códigos cClassFis.
@type function
@version 12.1.2410
@author Vogas
@since 4/06/2025
@return object, View do cadastro de códigos cClassFis.
/*/
function viewDef() as object

	local oView  := fwFormView():new() as object
	local oModel := fwLoadModel(cProgram) as object	
	local oTaxClassificationStruct as object

	oTaxClassificationStruct := fwFormStruct(2,'CKB')
	
	oView:setModel(oModel) // Define qual o Modelo de dados será utilizado

	oView:addField('VIEW_CAB',oTaxClassificationStruct,'TAXCLASSIFMOD') // Adiciona no nosso View um controle do tipo FormFields(antiga enchoice)

	oView:createHorizontalBox('CABEC',100)

	oView:setOwnerView('VIEW_CAB','CABEC')

return oView

/*/{Protheus.doc} validateIdentifierCode
Função que aplica a regras específicas aos campos e valida a operação.
@type function
@version 12.1.2410
@author Vogas
@since 4/06/2025
@param oModel, object, Modelo de dados do cadastro de códigos cClassFis.
@return logical, Retorna .T. se o cadastro foi salvo com sucesso
/*/
static function validateIdentifierCode(oModel as object) as logical

	local cClassTrib		:= oModel:getValue('TAXCLASSIFMOD','CKB_CSTCCT') as character
	local dStartOfValidity 	:= oModel:getValue('TAXCLASSIFMOD','CKB_DTINI') as character
	local dEndOfValidity 	:= oModel:getValue('TAXCLASSIFMOD','CKB_DTFIM') as character
	local nOperation		:= oModel:getOperation() as numeric
	local lValid 			:= .T. as logical

	if !empty(dStartOfValidity) .and. (nOperation == MODEL_OPERATION_INSERT) .or. (nOperation == MODEL_OPERATION_UPDATE) 				
		CKB->(dbSetorder(1))

		if CKB->(dbSeek(xFilial('CKB')+cClassTrib+dToS(dStartOfValidity)+dToS(dEndOfValidity)))
			help(nil,nil,STR0004,nil,STR0005,1,0,nil,nil,nil,nil,nil,{STR0006}) //'Registro já informado.','Chave única.','Campos Class Trib, Data Inicial e Data Final, já cadastrados. Por favor, verifique.'
			lValid := .F.
		endif			
	endif	

return lValid 
