#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

/*-------------------------------------------------------------
API de consulta a lista de Origens.
@author adilson.roberto
@parâmetros de entrada do endpoint
cfilter = filtro na lista
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/orige', description='API de consulta a lista de Origem')
Function GSimuOrige()
Local oJsResponse        as json
Local cfilter     := oRest:getQueryRequest()['filter']  as character

oJsResponse := SimuOrig(cfilter)
oRest:setResponse( (oJsResponse ))
FWFreeObj(oJsResponse)
oJsResponse := Nil
Return

/*-------------------------------------------------------------
API de consulta Origem pelo id.
@author adilson.roberto
@parâmetros de entrada do endpoint
cId = Origem a ser consultado
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/orige/:id', description='API de consulta a lista de Origem pelo código')
Function GSimuOriId()
Local oJsResponse        as json
Local cId         := oRest:getPathParamsRequest()['id']
//O parâmetro id é obrigatório
if valtype(cId) != 'U' .and. !empty(cId)
    oJsResponse := SimuOrigId(cId)
endif    
oRest:setResponse( (oJsResponse )) 
FWFreeObj(oJsResponse)
oJsResponse := Nil
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuOrig
Função responsável por preencher o Json com com informações referente a lista de Origens
@author adilson.roberto
@parâmetros de entrada
cfilter = filtro na lista
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuOrig(cfilter as character)
Local nPos         := 1                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local aRetOrig     := {}                        as array
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    
Local nTamReg      := 0                         as numeric 
Local nReg         := 0                         as numeric
Local cRet         := ""                        as character

Default cFilter := ""

if valtype(oJParams) == 'J'
    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
endif

aRetOrig := FWGetSX5( "S0", cfilter)
If nPage > 1 .And. Empty(cfilter)
    nPos := nPage * nPageSize
EndIf

nTamReg  := Len(aRetOrig)

If nPos <= nTamReg
    For nReg := nPos To nTamReg          
        nPageAux++
        if nPageAux == 1
            oJsonResp["items"] := {}
        endif
        if nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            oJsonResp["items"][nPageAux]["id"]      :=   AllTrim(aRetOrig[nReg,3])
            oJsonResp["items"][nPageAux]["descorig"] :=  Left(AllTrim(aRetOrig[nReg,3]) + '-' + AllTrim(aRetOrig[nReg,4]),40)
        Else
            lHasNext := .t.
            Exit
        EndIf
    Next    
Endif

oJsonResp["hasNext"]    := lHasNext
If lHasNext
    oJsonResp["nextPageHref"] := "/api/fiscal/v1/simulator/orige?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
Else
    oJsonResp["nextPageHref"] := ""
EndIf

aSize(aRetOrig,0)
aRetOrig := Nil

cRet := oJsonResp:toJson() 
FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuOrigId
Função responsável por preencher o Json com com informações referente a Origem 
buscado pelo ID
@author adilson.roberto
cId = Origem a ser consultado
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuOrigId(cId as character)
Local oJsonResp    := JsonObject():New()        as object
Local aRetOrig     := {}                        as array
Local cRet         := ""                        as character

Default cId     := ""

//1 - Filial
//2 - Tabela
//3 - Chave
//4 - Descrição
aRetOrig := FWGetSX5( "S0", cId )

If Len(aRetOrig) > 0 .And. AllTrim(aRetOrig[1,3]) == AllTrim(cId)
    oJsonResp["id"]        :=   AllTrim(aRetOrig[1,3])
    oJsonResp["descorig"]  :=   Left(AllTrim(aRetOrig[1,3]) + '-' + AllTrim(aRetOrig[1,4]),40)
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "O código origem não foi encontrado."  
EndIf

aSize(aRetOrig,0)
aRetOrig := Nil

cRet := oJsonResp:toJson() 
FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet
