#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

/*-------------------------------------------------------------
API de consulta a lista de CFOP.
@author adilson.roberto
@parâmetros de entrada do endpoint
cfilter = filtro na lista
ctype   = tipo de movimento E/S
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/cfop', description='API de consulta a lista de CFOP')
Function GSimuCfop()
Local oJsResponse        as json
Local cfilter     := oRest:getQueryRequest()['filter']  as character
Local ctype       := oRest:getQueryRequest()['type']  as character

oJsResponse := SimuCfop(cfilter,ctype)
oRest:setResponse(oJsResponse)
FWFreeObj(oJsResponse)
oJsResponse := Nil
Return

/*-------------------------------------------------------------
API de consulta CFOP pelo id.
@author adilson.roberto
@parâmetros de entrada do endpoint
cId = CFOP a ser consultado
@since 13/10/2024
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/cfop/:id', description='API de consulta a lista de CFOP pelo código')
Function GSimuCfopId()
Local oJsResponse        as json
Local cId         := oRest:getPathParamsRequest()['id']
//O parâmetro id é obrigatório
if valtype(cId) != 'U' .and. !empty(cId)
    oJsResponse := SimuCfopId(cId)
endif    
oRest:setResponse(oJsResponse)
FWFreeObj(oJsResponse)
oJsResponse := Nil
Return 

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuCfop
Função responsável por preencher o Json com com informações referente a lista de CFOP
@author adilson.roberto
@parâmetros de entrada
cfilter = filtro na lista
ctype   = tipo de movimento E/S
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuCfop(cfilter as character, ctype as character)
Local nPos         := 1                         as numeric
Local nPass        := 0                         as numeric
Local nPageAux     := 0                         as numeric
Local nPageSize    := 10                        as numeric
Local lHasNext     := .f.                       as logical
Local nPage        := 1                         as numeric
Local oJsonResp    := JsonObject():New()        as object
Local aRetCfop     := {}                        as array
Local aRetCoMov    := {}                        as array
Local oJParams     := oRest:getQueryRequest()   as json //Json com os parâmetros (query param)    
Local nTamReg      := 0                         as numeric 
Local nReg         := 0                         as numeric
Local cRet         := ""                        as character

Default cfilter := ""
Default ctype   := "E"

if valtype(oJParams) == 'J'
    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
endif

//1 - Filial
//2 - Tabela
//3 - Chave
//4 - Descrição
aRetCoMov := FWGetSX5( "13", cfilter)
For nPass := 1 To Len(aRetCoMov)
    If (ctype == "S" .And. Left(AllTrim(aRetCoMov[nPass,3]),1) > "4");
        .Or. (ctype == "E" .And. Left(AllTrim(aRetCoMov[nPass,3]),1) < "5")
        Aadd(aRetCfop,aRetCoMov[nPass])    
    Endif
Next
If nPage > 1 .And. Empty(cfilter)
    nPos := nPage * nPageSize
Endif   

nTamReg  := Len(aRetCfop)
 
If nPos <= nTamReg
    For nReg := nPos To nTamReg  
        nPageAux++
        if nPageAux == 1
            oJsonResp["items"] := {}
        endif
        if nPageAux <= nPageSize
            aAdd( oJsonResp["items"], JsonObject():New() )
            oJsonResp["items"][nPageAux]["id"]      :=   AllTrim(aRetCfop[nReg,3])
            oJsonResp["items"][nPageAux]["descfop"] :=   Left(AllTrim(aRetCfop[nReg,4]),40)
        Else
            lHasNext := .t.
            Exit
        EndIf
    Next    
Else

Endif

oJsonResp["hasNext"]    := lHasNext
If lHasNext
    oJsonResp["nextPageHref"] := "/api/fiscal/v1/simulator/cfop?page=" + cValtoChar(nPage + 1);
                                 + "&pageSize=" + cValtoChar(nPageSize) + "&type=" + ctype
Else
    oJsonResp["nextPageHref"] := ""
EndIf
aSize(aRetCfop,0)
aSize(aRetCoMov,0)

aRetCfop := Nil
aRetCoMov := Nil

cRet := oJsonResp:toJson()
FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet

/*-------------------------------------------------------------------
{Protheus.doc} GET SimuCfopId
Função responsável por preencher o Json com com informações referente a CFOP 
buscado pelo ID
@author adilson.roberto
cId = CFOP a ser consultado
@since 13/10/2024
-------------------------------------------------------------------*/
Static Function SimuCfopId(cId as character)
Local oJsonResp    := JsonObject():New()        as object
Local aRetCfop     := {}                        as array
Local cRet         := ""                        as character

Default cId     := ""
//Default lOrig   := .F.

//1 - Filial
//2 - Tabela
//3 - Chave
//4 - Descrição
aRetCfop := FWGetSX5( "13", cId )

If Len(aRetCfop) > 0 .And. AllTrim(aRetCfop[1,3]) == AllTrim(cId)
    oJsonResp["id"]       :=   AllTrim(aRetCfop[1,3])
    oJsonResp["descfop"]  :=   Left(AllTrim(aRetCfop[1,4]),40)
Else
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "O CFOP não foi encontrado."
EndIf
aSize(aRetCfop,0)
cRet := oJsonResp:toJson()
FWFreeObj(oJsonResp)
oJsonResp := Nil
Return cRet

