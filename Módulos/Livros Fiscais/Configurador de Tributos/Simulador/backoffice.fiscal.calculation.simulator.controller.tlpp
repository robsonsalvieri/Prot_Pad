#INCLUDE "TOTVS.CH"
#INCLUDE "tlpp-rest.th"
#INCLUDE "TLPP-CORE.TH"



using namespace totvs.protheus.backoffice.fiscal.calculation

namespace totvs.protheus.backoffice.fiscal.simulator


/*/{Protheus.doc} SimulatorController
    Classe responsável por processar a requisição de calculo do simulador
    @author anedino.santos
    @since 19/09/2024
    @version 12.1.2510
/*/
Class SimulatorCalculate
    private data jRequestBody as json
    private data jResponseBody as json
    private data dSystemDateBkp as date
    private data cFilAtu as character 

    public method new() Constructor

    @Post(endpoint="/api/fiscal/v1/simulator/calculate")
    public method calculate()

    private method makeRequestJson() as json
    private method runCalcProcess() as json
    private method prepareItemsToCalc()
    private method setSystemDate()
    private method restSystemDate()
    private method setCalcResult(oCalc)
    private method setDataItems(jTribTaxes)
    private method setDataAdjustments(oCalc)
    private method finish()

EndClass


/*/{Protheus.doc} new
    Método que instância objeto da classe.
    @author anedino.santos
    @since 19/09/2024
    @version 12.1.2510
    @return self, object, instância da classe
/*/
Method new() class SimulatorCalculate
    self:jResponseBody := JsonObject():New()
Return


/*/{Protheus.doc} calculate
    Método reponsável por responder a requisição de calculo do simulador.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
    @return Nil, Nil, sem retorno
/*/
Method calculate() class SimulatorCalculate
    
    self:jRequestBody := self:makeRequestJson(oRest:getBodyRequest())
    if self:jRequestBody <> Nil
        try
            cFilAtu := cFilAnt  // neste ponto guarda a filial corrente na variavel cFilAtu
            cFilAnt := self:jRequestBody["cabecalho"]["filial"] // atualizo a variavel cFilAnt com a filial selecionada
            self:runCalcProcess()
            oRest:setResponse(self:jResponseBody:toJson())
        catch oError
            SetRestFault(500, FWHttpEncode(oError:description), .T.)
        finally
            self:finish()
            cFilAnt := cFilAtu  // retorno filial corrente para variavel cFilAnt
            //Telemetria
	        FWLsPutAsyncInfo("LS006",RetCodUsr(),'09',"SimuladorOperacao")
        endtry
    endif

Return


/*/{Protheus.doc} checkRequestBody
    Checa se o corpo da requisição não está vazio e se sua estrutura está em 
    formato json para criar o objeto json da requisição.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
    @param cBody, character, string em formato json com os dados do corpo da requisição
    @return jBody, json, objeto json conforme corpo da requisição.
/*/
Method makeRequestJson(cBody) class SimulatorCalculate
    local jBody as json
    local xErro as variant

    if !Empty(cBody)
        jBody := JsonObject():New()
        xErro := jBody:fromJson(cBody)
        if xErro <> Nil
            FwFreeObj(jBody)
            jBody := Nil
            SetRestFault(400, FWHttpEncode("Estrutura de body da requisição deve ser em formato json. Erro: "+xErro))
        endif
    else
        SetRestFault(400, FWHttpEncode("Erro: Body da requisição vazio."))
    endif
Return jBody


/*/{Protheus.doc} runCalcService
    Executa o serviço de calculo.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
/*/
Method runCalcProcess() class SimulatorCalculate
    local oCalcService as object

    TaxCalculationService():checkDependencies()
    self:prepareItemsToCalc()
    self:setSystemDate()

    oCalcService := TaxCalculationService():New(self:jRequestBody)
    oCalcService:startCalculationEngine(.T.)
    oCalcService:runCalculationEngine()

    self:jResponseBody := self:setCalcResult(oCalcService)

    oCalcService:finishCalculationEngine()
    self:restSystemDate()
    // Libero o objeto
    FwFreeObj(oCalcService)
    oCalcService := Nil
Return


/*/{Protheus.doc} prepareItemsToCalc
    Define as chaves com valores default para que os itens possam ser enviados
    para calculo sem gerar erro no serviço de calculo.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
/*/
Method prepareItemsToCalc() class SimulatorCalculate
    local nI as numeric
    local aItem := self:jRequestBody["itens"] as array
    // as chaves abaixo receberão valores default para não quebrar 
    // o serviço de calculo pois não precisamos delas
    for nI := 1 to len(aItem)
        If !aItem[nI]:hasProperty("freteAutonomo")
            aItem[nI]["freteAutonomo"] := 0
        endif

        If !aItem[nI]:hasProperty("nfOri")
            aItem[nI]["nfOri"] := ""
        endif

        If !aItem[nI]:hasProperty("serieOri")
            aItem[nI]["serieOri"] := ""
        endif

        If !aItem[nI]:hasProperty("recnoOri")
            aItem[nI]["recnoOri"] := ""
        endif

        If !aItem[nI]:hasProperty("origemProduto")
            aItem[nI]["origemProduto"] := ""
        endif
    next nI
Return


/*/{Protheus.doc} setDateToCalc
    Altera a data do Sistema.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
/*/
Method setSystemDate() class SimulatorCalculate
    self:dSystemDateBkp := dDataBase
    dDataBase := CToD(self:jRequestBody["cabecalho"]["data"])
Return


/*/{Protheus.doc} restSystemDate
    Restaura a data do Sistema.
    @author anedino.santos
    @since 26/09/2024
    @version 12.1.2510
/*/
Method restSystemDate() class SimulatorCalculate
    dDataBase := self:dSystemDateBkp
Return


/*/{Protheus.doc} setCalcResult
    Pega os dados dos tributos para retornar na resposta da requisição.
    @author anedino.santos
    @since 27/09/2024
    @version 12.1.2510
    @param oCalc, object, objeto do serviço de calculo
    @return jResponse, json, objeto json com a estrutura da resposta da requisição
/*/
Method setCalcResult(oCalc) class SimulatorCalculate
    local jGenericTaxes as json
    local jGenericTaxesPerItem as json
    local jResponse := JsonObject():New() as json

    jGenericTaxes := oCalc:getGenericTaxes()
    jGenericTaxesPerItem  := oCalc:getGenericTaxesPerItem({"detalhe_livro"})

    jResponse["tributos"] := jGenericTaxes["planilha_financeira"]
    jResponse["tributos_por_item"] := self:setDataItems(jGenericTaxesPerItem)
    jResponse["lancamentos_por_item"] := self:setDataAdjustments(oCalc)
Return jResponse


/*/{Protheus.doc} setDataItems
    Define os dados que serão retornados na resposta referente aos tributos por
    item.
    @author anedino.santos
    @since 27/09/2024
    @version 12.1.2510
    @param jGenericTaxesPerItem, json, objeto json com os dados dos tributos por item retornados pelo serviço de calculo.
    @return jItens, json, objeto json apenas com os dados necessários para a resposta referente aos tributos por item.
/*/
Method setDataItems(jGenericTaxesPerItem) class SimulatorCalculate
    local jItens as json // será o json principal de retorno
    local jTrib as json // será o json que guardara os tributos do item
    local jDetTrib as json // será o json que vai guardar os dados do tributo calculado
    local jAux as json // objeto json auxiliar

    local aNItem as array // guardará os numeros dos itens para podermos percorrer o json
    local aTrib as array // guardará as chaves dos tributos para podermos acessar seus dados

    local nI as numeric // contador primário
    local nX as numeric // contador secundário

    aNItem := jGenericTaxesPerItem["dados_itens"]:getNames()

    jItens := JsonObject():New()
    for nI := 1 to len(aNItem)
        jTrib := JsonObject():New()
        aTrib := jGenericTaxesPerItem["dados_itens"][aNItem[nI]]:getnames()
        for nX := 1 to len(aTrib)
            jDetTrib := JsonObject():New()
            if aTrib[nX] <> "Aviso"
                jAux := jGenericTaxesPerItem["dados_itens"][aNItem[nI]][aTrib[nX]]
                jDetTrib["cod_regra"]          := jAux["cod_regra"]
                jDetTrib["desc_regra"]         := jAux["desc_regra"]
                jDetTrib["base_trib"]          := jAux["base_trib"]
                jDetTrib["aliq_trib"]          := jAux["aliq_trib"]
                jDetTrib["val_trib"]           := jAux["val_trib"]
                jDetTrib["cst"]                := jAux["detalhe_livro"]["cst"]
                jDetTrib["valor_tributado"]    := jAux["detalhe_livro"]["valor_tributado"]
                jDetTrib["valor_isento"]       := jAux["detalhe_livro"]["valor_isento"]
                jDetTrib["valor_outros"]       := jAux["detalhe_livro"]["valor_outros"]
                jDetTrib["valor_nao_tribut"]   := jAux["detalhe_livro"]["valor_nao_tribut"]
                jDetTrib["valor_diferido"]     := jAux["detalhe_livro"]["valor_diferido"]
                jDetTrib["valor_majorado"]     := jAux["detalhe_livro"]["valor_majorado"]
                if jAux:hasProperty("status")
                    if jAux["status"] == "1"
                        jDetTrib["status"]     := "Em teste"
                    else
                        jDetTrib["status"]     := "Aprovada"
                    endif
                else
                    jDetTrib["status"]         := ""
                endif
                jTrib[aTrib[nX]] := jDetTrib
            else
                jTrib[aTrib[nX]] := jGenericTaxesPerItem["dados_itens"][aNItem[nI]][aTrib[nX]]
            endif
        next nX
        jItens[aNItem[nI]] := jTrib
    next nI

    asize(aNItem, 0)
    asize(aTrib, 0)
    FwFreeObj(jAux)
    jAux := Nil

Return jItens


/*/{Protheus.doc} setDataAdjustments
    Define o formato do retorno para os dados dos ajustes.
    @author anedino.santos
    @since 01/10/2024
    @version 12.1.2510
    @param oCalc, objeto, objeto objeto para manipulação do serviço de calculo.
    @return jRet, json, objeto json com a nova estrutura de retorno dos ajustes.
    /*/
Method setDataAdjustments(oCalc) class SimulatorCalculate
    local jRet := JsonObject():New() as json
    local jResponse as json
    local jTributos as json
    local aItems as array
    local nI as numeric

    aItems := self:jRequestBody["itens"]

    for nI := 1 to len(aItems)
        jResponse := oCalc:getAdjustments(nI)
        jTributos := jResponse["ajustes_tributos"]

        if Valtype(jResponse) == "J" .and. jTributos:hasProperty("lancamentos")
            jRet[cValToChar(nI)] := jTributos["lancamentos"]
        else
            jRet[cValToChar(nI)] := jTributos
        endif
    next nI
Return jRet


/*/{Protheus.doc} finish
    Finaliza o processamento do end-point.
    @author anedino.santos
    @since 01/10/2024
    @version 12.1.2510
/*/
Method finish() class SimulatorCalculate
    FwFreeObj(self:jResponseBody)
    FwFreeObj(self:jRequestBody)
    self:jResponseBody := Nil
    self:jRequestBody := Nil
Return
