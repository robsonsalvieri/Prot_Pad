#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "protheus.ch"

/*/{Protheus.doc} FISGetBranches
Função responsável por retornar a consulta de todos as filiais

@type function
@author Alexandre Esteves
@since 08/10/2024
@version 1.0
@return Nil, Não há retorno 
/*/
@GET(endpoint="/api/fiscal/v1/simulator/branches", description="Realiza a consulta de todas as filiais")
Function FISGetBranches()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} FISGetFilialById
Função responsável por Realiza a consulta de um código de Filial

@type function
@author Alexandre Esteves
@since 08/10/2014
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/branches/:id", description='Realiza a consulta de um código de Filial') 
Function FISGetFilialById()

	Local jPathParams   := oRest:GetPathParamsRequest()     as json
	Local jResponse     := GetResponse(Nil, jPathParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jPathParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta da consulta de filiais

@type static function
@author Alexandre Esteves
@since 08/10/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@param jPathParams, json, Objeto JSON contendo os path params
@return json, Retorna um objeto JSON com as filiais
/*/
Static Function GetResponse(jQueryParams as json, jPathParams as json) as json

	Local aItems	:= {}	as array
	Local nPage     := 0	as integer
	Local nPageSize := 0	as integer
	Local lHasNext  := .F.	as logical
	Local jResponse := Nil  as json
	Local cId       := ""  	as character
	Local cFilter   := ""  	as character
	
	Default jQueryParams := Nil
	Default jPathParams  := Nil

	nPage 		:= 1
	nPageSize 	:= 30

	If jQueryParams != Nil
		
		If jQueryParams:HasProperty("filter")
			cFilter := AllTrim(jQueryParams["filter"])
		EndIf

		If jQueryParams:HasProperty("page")
			nPage := Val(jQueryParams["page"])
		EndIf

		If jQueryParams:HasProperty("pageSize")
			nPageSize := Val(jQueryParams["pageSize"])
		EndIf

	EndIf

	If jPathParams != Nil
		If jPathParams:HasProperty("id")
			cId := AllTrim(jPathParams["id"])
		EndIf
	EndIf
	aItems 	  := GetBranches(cId, cFilter,nPage, nPageSize, @lHasNext)
	jResponse := JSONObject():New()

	If jQueryParams != Nil
		jResponse["items"]        := aItems
		jResponse["hasNext"]      := lHasNext
		jResponse["nextPageHref"] := IIf(lHasNext, "/api/fiscal/v1/simulator/branches?" + "page" + "=" + cValToChar(nPage + 1) + "&" + "pageSize" +  "=" + cValToChar(nPageSize), "")
	endif

	If jPathParams != Nil .And. !Empty(aItems)
		jResponse := aItems[1]
	EndIf

Return jResponse

/*/{Protheus.doc} GetBranches
Função responsável por retornar um alias contendo as filiais
@type static function
@author Alexandre Esteves
@since 08/10/2024
@version 1.0
@param nPage, integer, Página da consulta
@param nPageSize, integer, Quantidade de registros por página
@return array, Retorna um array de json com as filiais
/*/
Static Function GetBranches(cId as character, cFilter as character, nPage as integer, nPageSize as integer, lHasNext as logical) as array
    
    Local aFiliais  := {}   as array
	Local aEmpresas := {}   as array
    Local nIx        := 0    as integer    
    Local nEmpresas := 0    as integer
    Local nItems    := 0    as integer
    Local jItems    := Nil  as json

	Default cId    		:= ""
	Default cFilter     := ""
    Default nPage       := 1
    Default nPageSize   := 30
    Default lHasNext    := .F.

    aEmpresas := FWLoadSM0(.T., .T.)
    nEmpresas := Len(aEmpresas)
        
    If !Empty(aEmpresas)

		If !Empty(cId) .or. !Empty(cFilter)
			
			For nIx:=1 to nEmpresas

				If aEmpresas[nIx][SM0_GRPEMP] == cEmpAnt .and. Alltrim(aEmpresas[nIx][2]) == AllTrim(cId)
					
					jItems := JSONObject():New()

					jItems["id"]          := Alltrim(aEmpresas[nIx][SM0_CODFIL])
					jItems["company"]		:= aEmpresas[nIx][SM0_GRPEMP]
					jItems["description"] := AllTrim(aEmpresas[nIx][SM0_NOMRED])

					AAdd(aFiliais, jItems)
					
				EndIf

				If !Empty(cFilter) .and. Alltrim(aEmpresas[nIx][2]) == AllTrim(cFilter) 
					
					jItems := JSONObject():New()

					jItems["id"]          := Alltrim(aEmpresas[nIx][SM0_CODFIL])
					jItems["company"]	  := aEmpresas[nIx][SM0_GRPEMP]
					jItems["description"] := AllTrim(aEmpresas[nIx][SM0_NOMRED])

					AAdd(aFiliais, jItems)
					
				EndIf

			Next

		Else

			For nIx := IIf(nPage == 1, 1, ((nPage - 1) * nPageSize) + 1) To nEmpresas
				If nItems < nPageSize .And. !Empty(aEmpresas[nIx]) .And. aEmpresas[nIx][SM0_GRPEMP] == cEmpAnt .And. aEmpresas[nIx][SM0_USEROK]
					jItems := JSONObject():New()

					jItems["id"]          := Alltrim(aEmpresas[nIx][SM0_CODFIL])
					jItems["company"]	  := aEmpresas[nIx][SM0_GRPEMP]
					jItems["description"] := AllTrim(aEmpresas[nIx][SM0_NOMRED])

					AAdd(aFiliais, jItems)

					If ++nItems == nPageSize
						Exit
					EndIf
				EndIf
			Next
		
			lHasNext := nIx < nEmpresas
		Endif

    EndIf

    FWFreeArray(aEmpresas)

Return aFiliais
