#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"
#INCLUDE "totvs.ch"

using namespace totvs.protheus.backoffice.fiscal.simulator.dao

/*-------------------------------------------------------------
API que Consulta as series de documento de acordo com o periodo do movimento das notas.
@author alexandre.esteves
@since 15/01/2025
@version 12.1.2510
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/invoice-series', description='API de consulta com a lista das series de nota fiscal')
Function FisGetInvoiceSeries()

oRest:setResponse( GetResponse())

Return

/*-------------------------------------------------------------
API de consulta da serie da nota fiscal pelo seu código.
@author alexandre.esteves
@since 15/01/2025
@version 12.1.2510
--------------------------------------------------------------*/
@get(endpoint='/api/fiscal/v1/simulator/invoice-series/:id', description='API de consulta da serie da nota fiscal pelo seu código')
Function FisGetInvoiceSeriesByID()

oRest:setResponse( GetResponse()) 

Return 

/*-------------------------------------------------------------------
{Protheus.doc} GetResponse
Função responsável por preencher o Json com com informações referente a lista de Series
@author alexandre.esteves
@since 15/01/2025
@version 12.1.2510
-------------------------------------------------------------------*/
Static Function GetResponse()
Local nPos          := 0                         as numeric
Local nPageAux      := 0                         as numeric
Local nPageSize     := 10                        as numeric
Local nPage         := 1                         as numeric
Local lHasNext      := .f.                       as logical
Local lFilial       := .f.                       as logical
Local lTpMov        := .f.                       as logical
Local lDtIni        := .f.                       as logical
Local lDtFim        := .f.                       as logical
Local oJsonResp     := JsonObject():New()        as object
Local oResult       := Nil                       as object
Local oJParams      := oRest:getQueryRequest()   as json //Json com os parâmetros (query param) 
Local cAlias        := ""                        as character
Local cRet          := ""                        as character
Local cApiReq       := "INVOICESERIES"           as character
Local cFil          := ""                        as character
Local cTpMov        := ""                        as character
Local cDtIni        := ""                        as character
Local cDtFim        := ""                        as character
Local cParticipante := ""                        as character
Local cFilter       := ""                        as character
Local cId           := ""                        as character


if valtype(oJParams) == 'J'
    if valtype( oJParams['page'] ) != 'U' .and. Val(oJParams['page']) > 0
        nPage := Val(oJParams['page'])
    endif
    if valtype( oJParams['pageSize'] ) != 'U' .and. Val(oJParams['pageSize']) > 0
        nPageSize := Val(oJParams['pageSize'])
    endif
    if valtype( oJParams['filial'] ) != 'U' .and. !Empty(Alltrim(oJParams['filial']))
        cFil := Alltrim(oJParams['filial'])
        lFilial := .T.
    endif
    if valtype( oJParams['tipomovimento'] ) != 'U' .and. !Empty(Alltrim(oJParams['tipomovimento']))
        cTpMov := Alltrim(oJParams['tipomovimento'])
        lTpMov := .T.
    endif
    if valtype( oJParams['periodoinicio'] ) != 'U' .and. !Empty(Alltrim(oJParams['periodoinicio']))
        cDtIni := Alltrim(oJParams['periodoinicio'])
        lDtIni := .T.
    endif
    if valtype( oJParams['periodofim'] ) != 'U' .and. !Empty(Alltrim(oJParams['periodofim']))
        cDtFim := Alltrim(oJParams['periodofim'])
        lDtFim := .T.
    endif
    if valtype( oJParams['participante'] ) != 'U' .and. !Empty(Alltrim(oJParams['participante']))
        cParticipante := oJParams['participante']
    endif
    if valtype( oJParams['filter'] ) != 'U' .and. !Empty(Alltrim(oJParams['filter']))
        cfilter := oJParams['filter']
    endif

endif

FWFreeObj(oJParams)
oJParams := Nil

if !Empty(alltrim(oRest:getPathParamsRequest()['id']))
    cId := alltrim(oRest:getPathParamsRequest()['id'])
endif

If  lFilial .and. lTpMov .and. lDtIni .and. lDtFim

    oResult := SimulatorInvoiceDAO():New()
    cAlias  := oResult:loadInvoices(nPage,nPageSize,cApiReq,cId,cfilter,cFil,cTpMov,cDtIni,cDtFim,cParticipante)
    
    If Empty(cId)

        While (cAlias)->(!Eof())
            nPageAux++
            if nPageAux == 1
                oJsonResp["items"] := {}
            endif
            if nPageAux <= nPageSize
                aAdd( oJsonResp["items"], JsonObject():New() )
                nPos++ 
                oJsonResp["items"][nPos]["idserie"]             :=   AllTrim((cAlias)->SERIE)
            Else
                lHasNext := .t.
                Exit
            EndIf
            (cAlias)->(DbSkip())
        EndDo
        oJsonResp["hasNext"]    := lHasNext
        If lHasNext
            oJsonResp["nextPageHref"] := "/api/fiscal/v1/simulator/invoice-series?page=" + cValtoChar(nPage + 1) + "&pageSize=" + cValtoChar(nPageSize)
        Else
            oJsonResp["nextPageHref"] := ""
        EndIf
    Else
        If !Empty(AllTrim((cAlias)->SERIE)) .AND. (cAlias)->(!Eof())
            oJsonResp["idserie"]             :=   AllTrim((cAlias)->SERIE)   
        Else
            oRest:setStatusCode(404)
            oJsonResp['code'] := 404
            oJsonResp['detailMessage'] := ''
            oJsonResp['message'] := "A série do documento fiscal não foi encontrada."
        Endif
    Endif
    (cAlias)->(DBCloseArea())
    FWFreeObj(oResult)
    oResult := Nil
Else
    oRest:setStatusCode(400)
    oJsonResp['code'] := 400
    oJsonResp['detailMessage'] := ''
    oJsonResp['message'] := "Parâmetros da consulta não atendidos."

Endif

cRet := oJsonResp:toJson()

FWFreeObj(oJsonResp)
oJsonResp := Nil

Return cRet

