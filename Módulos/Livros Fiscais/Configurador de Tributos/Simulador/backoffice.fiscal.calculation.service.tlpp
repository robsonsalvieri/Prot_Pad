#INCLUDE "TOTVS.CH"
#INCLUDE "TLPP-CORE.TH"


using namespace totvs.protheus.backoffice.fiscal.tciclass

namespace totvs.protheus.backoffice.fiscal.calculation


/*/{Protheus.doc} TaxCalculationService

    Manipula o mecanismo de calculo conforme operação submetida retornando os
    tributos calculados e seus respectivos lançamentos de apuração e valores
    declaratórios.

    @author anedino.santos
    @since 10/09/2024
    @version 12.1.2510
/*/
class TaxCalculationService

    private data jData as json // json com os dados do cabeçalho e dados dos itens para o calculo

    // métodos construtor e destrutor da classe
    public method new(jData) Constructor // instancia o objeto inicializando o jData
    public method destroy() // destroi o objeto apagando todos os dados da memória.

    // -------------------------métodos estáticos------------------------------------------------------
    static method checkDependencies(cDependencie) // checa as dependências para uso do componente

    // -------------------------métodos abertos para uso-----------------------------------------------
    public method startCalculationEngine() // inicializa o mecanismo de calculo - MaFisIni
    public method runCalculationEngine() // efetua o calculo dos tributos conforme cabeçalho e itens - MaFisAdd
    public method getGenericTaxes() as json // retorna os tributos genéricos semelhante a planilha financeira - GetSpreadSheetData()
    public method getGenericTaxesPerItem(aDetail) as json // retorna os tributos genéricos por item - GetDataItems()
    public method getAdjustments(nItem) // retorna os códigos de lançamentos e valores declaratórios
    public method finishCalculationEngine() // finaliza o mecanismos de calculo - MaFisEnd

    // -------------------------métodos internos-----------------------------------------------
    private method checkData(jData) // validação simples para saber se o jData está preenchido
endclass


/*/{Protheus.doc} new

    Método construtor da classe.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @param jData, json, objeto json contendo o cabeçalho e os itens para calculo.
    @return self, object, instância da classe.
/*/
Method new(jData) class TaxCalculationService

    self:checkData(jData) // lança erro se o json não estiver minimamente preenchido
    self:jData := jData

return self


/*/{Protheus.doc} checkDependencies

    Responsável por verificar as dependências para uso da classe TaxCalculationService.
    Caso alguma dependência não exista lança erro em run time.
    Conforme a classe for sofrendo modificações que gerem novas dependências 
    este método deve ser alterado para validar cada uma delas.
    Por ser um método estático deve ser usado antes da instância do objeto para
    garantir que não ocorrerá error log.

    @author anedino.santos
    @since 10/09/2024
    @version 12.1.2510
    @param cDependencie, character, dependência a ser verificada.
    @return nil, nil, sem retorno
/*/
Method checkDependencies(cDependencie) class TaxCalculationService
    default cDependencie := "TCIProcessing"

    if !FindClass(cDependencie)
        UserException("A dependência "+cDependencie+ " não foi encontrada.")
    endif
Return


/*/{Protheus.doc} checkData

    Verifica se os dados de jData estão minimamente preenchidos.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @param jData, json, json que será analisado.
    @return lRet, logical, variavel que indica se jData está minimamente preenchido.
/*/
Method checkData(jData) class TaxCalculationService
    local lRet as logical

    if len(jData:getNames()) > 0 .and. jData:hasProperty("cabecalho") .and. jData:hasProperty("itens")
        if len(jData["cabecalho"]:getNames()) > 0 .and. len(jData["itens"]) > 0
            lRet := .T.
        endif
    endif

    if !lRet
        UserException("Objeto json inválido. Verique a estrutura do mesmo.")
    endif
Return


/*/{Protheus.doc} startCalculationEngine

    Método que inicializa a matxfis através da função MaFisIni conforme a chave
    cabeçalho contida em jData.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @param lCalTG, logical, define se considera as regras do configurador de tributos para os calculos
    @return Nil, Nil, sem retorno.
/*/
Method startCalculationEngine(lCalcTG as logical) class TaxCalculationService    
    local jCabec           as json
    local lF2BTeste := .F. as logical

    default lCalcTG := .F.

    jCabec := self:jData["cabecalho"]

    if jCabec["regrateste"] == 'T'
        lF2BTeste := .T.    
    endif

    MaFisIni(;
        jCabec["codParticipante"],;      // 01-Cod. Cli/For
        jCabec["lojaParticipante"],;     // 02-Lj do Cli/For
        jCabec["tipoParticipante"],;     // 03-C:Cliente , F:Fornecedor
        jCabec["tipoOperacao"],;         // 04-Tp NF( "N","D","B","C","P","I" )
        jCabec["categoriaParticipante"],;// 05-Tp do Cli/For apenas para clientes F=Cons.Final;L=Produtor Rural;R=Revendedor;S=Solidario;X=Exportacao
        /*MaFisRelImp("MT100",aNFTables)*/,; // 06-Relacao de Impostos que são suportados no arquivo
        ,.T.,,,,,,,,,,,,,,,,,,,,,,,,,lCalcTG,,,,;
        lF2BTeste )
Return


/*/{Protheus.doc} runCalculationEngine

    Adiciona os itens para calculo e executa o calculo através da função MaFisAdd
    da matxfis verificando também as depesas acessórias.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @return Nil, Nil, sem retorno.
/*/
Method runCalculationEngine() class TaxCalculationService    
    local jItem            as json
    local nI               as numeric   

    for nI := 1 to len(self:jData["itens"])
        jItem := self:jData["itens"][nI]
        jItem["nItem"] := ;              // recebe o número do item
        MaFisAdd(jItem["codigo"],;       //  1-Codigo do Produto ( Obrigatorio )
                jItem["tes"],;           //  2-Codigo do TES ( Opcional )
                jItem["quantidade"],;    //  3-Quantidade ( Obrigatorio )
                jItem["valorUnitario"],; //  4-Preco Unitario ( Obrigatorio )
                jItem["desconto"],;      //  5-Valor do Desconto ( Opcional )
                jItem["nfOri"],;         //  6-Numero da NF Original ( Devolucao/Benef )
                jItem["serieOri"],;      //  7-Serie da NF Original ( Devolucao/Benef )
                jItem["recnoOri"],;      //  8-RecNo da NF Original no arq SD1/SD2
                jItem["frete"],;         //  9-Valor do Frete do Item ( Opcional )
                jItem["despesa"],;       // 10-Valor da Despesa do item ( Opcional )
                jItem["seguro"],;        // 11-Valor do Seguro do item ( Opcional )
                jItem["freteAutonomo"],; // 12-Valor do Frete Autonomo ( Opcional )
                jItem["valorTotal"],;    // 13-Valor da Mercadoria ( Obrigatorio )
                ,,,,,,;                  // parâmetros opcionais que não serão utilizados por enquanto
                jItem["cfop"],;           // 20-CFOP ( Obrigatorio se não passou TES )
                ,,,,,,,jItem["origemProduto"]; // 28-Classificação Fiscal
        )

        //Altera o CFOP do item 
        MaFisAlt("IT_CF",jItem["cfop"],jItem["nItem"])

    next nI

    if self:jData["cabecalho"]["frete"] > 0
        MaFisAlt("NF_FRETE", self:jData["cabecalho"]["frete"])
    endif

    if self:jData["cabecalho"]["desconto"] > 0
        MaFisAlt("NF_DESCONTO", self:jData["cabecalho"]["desconto"])
    endif

    if self:jData["cabecalho"]["despesa"] > 0
        MaFisAlt("NF_DESPESA", self:jData["cabecalho"]["despesa"])
    endif

    if self:jData["cabecalho"]["seguro"] > 0
        MaFisAlt("NF_SEGURO", self:jData["cabecalho"]["seguro"])
    endif

Return


/*/{Protheus.doc} getGenericTaxes

    Retorna os tributos genéricos semelhante a planilha financeira. Para usar
    esse método é necessário que o mecanismo de calculo tenha sido iniciado e 
    executado.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @return jTributes, json, objeto json contendo os dados dos tributos genéricos calculados.
/*/
Method getGenericTaxes() as json class TaxCalculationService
    local oTCIPro as object
    local jTributes as json

    oTCIPro := TCIProcessing():New()
    jTributes := JsonObject():New()
    jTributes:fromJson(oTCIPro:getSpreadSheetData())

    FwFreeObj(oTCIPro)
    oTCIPro := Nil

Return jTributes


/*/{Protheus.doc} getGenericTaxesPerItem

    Retorna os tributos genéricos para cada item calculado. Para usar esse método 
    é necessário que o mecanismo de calculo tenha sido iniciado e executado.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @param aDetail, array, indica quais detalhes dos tributos genéricos devem ser considerados no retorno dos valores exemplo {"regras_base", "regras_aliquota", "regras_escrituracao", "detalhe_livro"}
    @return jTributes, json, objeto json contendo os dados dos tributos genéricos calculados por item.
/*/
Method getGenericTaxesPerItem(aDetail as array) as json class TaxCalculationService
    local oTCIPro as object
    local jTributes as json

    default aDetail := {}

    oTCIPro := TCIProcessing():New()
    jTributes := JsonObject():New()

    if len(aDetail) > 0
        oTCIPro:setDataItems(aDetail)
    endif

    jTributes:fromJson(oTCIPro:getDataItems())

    FwFreeObj(oTCIPro)
    oTCIPro := Nil

    aSize(aDetail, 0)
    aDetail := Nil
Return jTributes


/*/{Protheus.doc} getAdjustments
    Retorna os valores referente a códigos declaratórios e códigos de ajustes.
    @author anedino.santos
    @since 27/09/2024
    @version 12.1.2510
    @param nItem, numeric, item inicial ao qual será feita a busca.    
    @return jRet, json, objeto json com os valores declaratórios e códigos de ajustes.
    /*/
Method getAdjustments(nItem as numeric) class TaxCalculationService
    local cAdjustments as character
    local oTCIPro as object
    local jRet as json

    default nItem := 1

    oTCIPro := TCIProcessing():New()    
    oTCIPro:setItems({nItem}) // seta o item a ser processado

    cAdjustments := oTCIPro:getTaxAdjustments()

    FwFreeObj(oTCIPro)

    jRet := JsonObject():New()
    jRet:fromJson(cAdjustments)
    
Return jRet

/*/{Protheus.doc} finishCalculationEngine

    Finaliza o mecanismo de calculo através da função MaFisEnd da matxfis.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @return Nil, Nil, sem retorno.
/*/
Method finishCalculationEngine() class TaxCalculationService
    MaFisEnd()
Return


/*/{Protheus.doc} destroy

    Libera da memória a instância da classe.

    @author anedino.santos
    @since 06/09/2024
    @version 12.1.2510
    @return Nil, Nil, sem retorno.
/*/
Method destroy() class TaxCalculationService
    FwFreeObj(self:jData)
    self:jData := Nil
Return
