#INCLUDE "tlpp-core.th"
#INCLUDE "tlpp-rest.th"

/*/{Protheus.doc} FISGetCliente
Função responsável por retornar a consulta de Participantes

@type function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/customer", description='Realiza a consulta de todos os clientes')
Function FISGetCliente()

	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jQueryParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} FISGetCliForById
Função responsável por Realiza a consulta de um código de Cliente

@type function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@return Nil, Não há retorno
/*/
@GET(endpoint="/api/fiscal/v1/simulator/customer/:id", description='Realiza a consulta de um código de Cliente') 
Function FISGetClienteById()

	Local jPathParams   := oRest:GetPathParamsRequest()     as json
	Local jQueryParams  := oRest:GetQueryRequest()      as json
	Local jResponse     := GetResponse(jQueryParams, jPathParams)    as json

	oRest:SetKeyHeaderResponse("Content-Type", "application/json")
	oRest:SetStatusCode(200)
	oRest:SetResponse(jResponse)

	FWFreeObj(jPathParams)
	FWFreeObj(jResponse)

Return

/*/{Protheus.doc} GetResponse
Função responsável por retornar o JSON de resposta com os dados de Cliente

@type static function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@param jQueryParams, json, Objeto JSON contendo os query params
@param jPathParams, json, Objeto JSON contendo os path params
@return json, Retorna um objeto JSON com os codigos dos Cliente
/*/
Static Function GetResponse(jQueryParams as json, jPathParams as json) as json

	Local aItems            := {}	as array
	Local cSA1              := ""	as character
	Local cId               := ""  	as character
	Local cFilter           := ""  	as character
	Local cFil				:= ""   as character
	Local iPage				:= 0	as integer
	Local iPageSize			:= 0	as integer
	Local lHasNext			:= .F.	as logical
	Local jItems            := Nil  as json
	Local jResponse         := Nil  as json

	Default jQueryParams    := Nil
	Default jPathParams     := Nil

	iPage 		:= 1
	iPageSize 	:= 30

	If jQueryParams != Nil

		If jQueryParams:HasProperty("filter")
			cFilter := AllTrim(jQueryParams["filter"])
		EndIf

		If jQueryParams:HasProperty("filial")
			cFil:= AllTrim(jQueryParams["filial"])
		EndIf

		If jQueryParams:HasProperty("page")
			iPage := Val(jQueryParams["page"])
		EndIf

		If jQueryParams:HasProperty("pageSize")
			iPageSize := Val(jQueryParams["pageSize"])
		EndIf
	EndIf

	If jPathParams != Nil
		If jPathParams:HasProperty("id")
			cId := AllTrim(jPathParams["id"])
		EndIf
	EndIf

	cSA1		:= GetClienteCodes(cId, cFilter, cFil, iPage, iPageSize)
	jResponse 	:= JSONObject():New()

	If !Empty(cSA1)
		While !(cSA1)->(EOF()) 
			If (cSA1)->ROW_NUMBER > iPageSize
				lHasNext := .T.
				Exit
			EndIf

			jItems 				:= JSONObject():New()
			jItems["id"]          := (cSA1)->A1_COD
			jItems["store"]       := (cSA1)->A1_LOJA
			jItems["description"] := AllTrim((cSA1)->A1_NOME)
			jItems["type"]		:= (cSA1)->A1_TIPO

			AAdd(aItems, jItems)

			(cSA1)->(DbSkip())
		EndDo
	EndIf

	If jQueryParams != Nil
		jResponse["hasNext"]      := lHasNext
		jResponse["items"]        := aItems
		jResponse["nextPageHref"] := IIF(lHasNext, "/api/fiscal/v1/simulator/customer?" + "page" + "=" + cValToChar(iPage + 1) + "&" + "pageSize" +  "=" + cValToChar(iPageSize), "")
	EndIf

	If jPathParams != Nil .And. !Empty(aItems)
		jResponse := aItems[1]
	EndIf

	(cSA1)->(DBCloseArea())

Return jResponse

/*/{Protheus.doc} GetClienteCodes
Função responsável por retornar um alias contendo os registros da consulta de Clientes

@type static function
@author Alexandre Esteves
@since 23/09/2024
@version 1.0
@param cId, character, Codigo do Cliente
@param cFilter, character, Filtro de pesquisa
@param iPage, integer, Página da consulta
@param iPageSize, integer, Quantidade de registros por página
@return character, Retorna um alias contendo os registros da consulta de Clientes
/*/
Static Function GetClienteCodes(cId as character, cFilter as character, cFil as character ,iPage as integer, iPageSize as integer) as character

	Local cQuery		:= ""   as character
	Local cSA1			:= ""   as character
	Local iBind			:= 0    as integer
	Local oQuery		:= Nil  as object

	Default cId    		:= ""
	Default cFilter     := ""
	Default cFil		:= ""
	Default iPage    	:= 1
	Default iPageSize	:= 30


	cQuery := " SELECT ROW_NUMBER() OVER (ORDER BY SA1.A1_COD) ROW_NUMBER, "
	cQuery += " 	SA1.A1_COD, SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_TIPO "
	cQuery += " 	FROM (
	cQuery += " 		SELECT SA1.A1_COD, SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_TIPO "
	cQuery += "     		FROM " + RetSQLName("SA1") + " SA1 "
	cQuery += "     		WHERE SA1.D_E_L_E_T_ = ? "
	

	If !Empty(cFil)
		cQuery += "         		AND SA1.A1_FILIAL = ? "
	EndIf

	If !Empty(cId)
		cQuery += "         		AND SA1.A1_COD = ? "
	EndIf

	If !Empty(cFilter)
		cQuery += "         		AND SA1.A1_COD LIKE(?) "
	EndIf

	If Empty(cId)
		cQuery += " 		ORDER BY SA1.A1_COD "
		cQuery += " 		OFFSET ? ROWS "
		cQuery += " 		FETCH NEXT ? ROWS ONLY "
	EndIf

	cQuery += " 	) SA1 "

	oQuery := FwExecStatement():New(cQuery)

	If oQuery != Nil

		oQuery:SetString(++iBind, " ")

		If !Empty(cFil)
			oQuery:SetString(++iBind, xFilial("SA1",cFil))
		Endif

		If !Empty(cId)
			oQuery:SetString(++iBind, cId)
		EndIf

		If !Empty(cFilter)
			oQuery:SetString(++iBind, cFilter + '%')
		EndIf

		If Empty(cId)
			oQuery:SetUnsafe(++iBind, cValToChar((iPage - 1) * iPageSize))
			oQuery:SetUnsafe(++iBind, cValToChar(iPageSize + 1))
		EndIf

		cSA1 := oQuery:OpenAlias()
	EndIf

	oQuery:Destroy()
	oQuery:= Nil

Return cSA1
