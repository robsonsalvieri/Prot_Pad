#INCLUDE "tlpp-core.th"
#INCLUDE "MATXDEF.CH"

namespace totvs.protheus.backoffice.fiscal.additionalData


/*/{Protheus.doc} TCAdditionalData
    
    Classe responsável por processar e retornar os dados adicionais  gravados ou em processamento da rotina dados adicionais
    do Configurador de tributos

    @author Rhuan Carvalho
    @since 12/05/2025
    @version 12.1.2510
    /*/
Class TCAdditionalData
    Private Data aIdRequest                  as array // Variável privada que armazena os IDs que serão tratados antes da pesquisa
    Private Data abookkeepingRuleId          as array // Variável privada que armazena os IDs das regras de escrituracao
    Private Data abookkeepingRuleMetadata    as array // Variável privada que armazena os dados de metadados da regra de escrituracao
    Private Data aOperationProfileId         as array // Variável privada que armazena os IDs dos perfis de operação
    Private Data aProcessList                as array // Variável privada que armazena os IDs que serão processados
    Private Data lRequestErrorStatus         as logical // Variável privada que armazena o status de erro da requisição
    Private Data lProcessErrorToNotify       as logical // Variável privada que armazena se há erro que precisa ser notificado no json de retorno
    Private Data cDataMode                   as character // Variável privada que armazena o modo de operação
    Private Data jCacheData                  as json // Variável privada que armazena os dados adicionais em cache
    Private Data jID                         as json // Variável privada que armazena os IDs que estão em processamento
    Private Data jIncorrectID                as Json //Variável privada que armazena registros e notificações incorretas 
    Private Data JsonBookkeepingRuleMetadata as json // Variável privada que armazena os dados de metadados da regra de escrituracao
    Private Data JsonOperationProfileMetadata as json // Variável privada que armazena os dados de metadados dos perfis de operação
    
    Public Method new() CONSTRUCTOR // Método construtor da classe TCAdditionalData
    Public Method setId(aId)                 as array //Método público para passar os Ids que serão pesquisados 
    Public Method getAdditionalData()        as character //Método público para retornar os dados adicionais
    Public Method setDataMode(mode)          as character //Método público para definir o modo de operação
    Public Method destroy()                               // Método para destruir os objetos criados na classe    

    Private Method isValidId(aId)            as logical //Método privado para validar os Ids
    Private Method processRequestData()      as array //Método privado para processar os Ids
    Private Method setCache()                as json //Método privado para armazenar os dados adicionais em cache
    Private Method getCache()                as json //Método privado para retornar os dados adicionais em cache
    Private Method getQueryAlias()              //Método privado para executar query nas tabelas do configurador de tributos e obter os ids para consulta nas tabelas dos metadados
    Private Method getBookkeepingRuleMetadataAlias() // Método privado para executar query na tabela CK3 de metadados
    Private Method getOperationProfileMetadataAlias() // Método privado para executar query na tabela CK4 de metadados
    //TODO: Criar métodos para executar a querie na CK5
    Private Method getAdditionalDataIdsFromMatxfis() // Método privado para retornar os IDs que estão em processamento na Matxfis
    Private Method buildMetadataIdsJson() // Método privado para construir o Json que contem os IDs dos metadados que serão usados nas queries das tabelas
    Private Method buildMetadataIdArrays(ID_REGESC, COD_PERFOP, ID_REGRCLC) // Método privado para construir os arrays que serão usados nas queries das tabelas de metadados
    Private Method buildProcessingIdsJson() // Método privado retornar os IDs da Matxfis
    Private Method buildMetadataContentJson() // Método privado para construir o Json que contem os conteúdos do metadados	
    Private Method composeJsonMatadata() // Método privado para compor o Json que será retornado
    Private Method executeMetadataTablesQuery() // Método para centralizar a execução das queries das tabelas de metadados
    Private Method setReturnMessage() // Método para inserir mensagens de notificação no Json de retorno
    Private Method setRequestErrorStatus() // Método para armazenar o status de erro da requisição
    Private Method hasRequestError() // Método para verificar se houve erro na requisição
    Private Method hasProcessErrorToNotify() // Método para verificar se há algum erro de processo que precisa ser notificado no json de retorno
    Private Method setProcessErrorToNotify() // Método para armazenar se será notificado um erro no json de retorno
    Private Method getOperationProfileByRule(cRuleId) as character // Método privado para retornar o perfil de operação de acordo com a regra de cálculo

EndClass

/*/{Protheus.doc} new
    Método construtor da classe TCAdditionalData
    @author Rhuan Carvalho
    @since 12/05/2025
    @version 12.1.2510

    /*/
Method new() Class TCAdditionalData
    self:aIdRequest                   := {}
    self:abookkeepingRuleId           := {}
    self:abookkeepingRuleMetadata     := {}
    self:aOperationProfileId          := {}
    self:aProcessList                 := {}
    self:lRequestErrorStatus          := .F.
    self:lProcessErrorToNotify        := .F.
    self:cDataMode                    := ""
    self:jCacheData                   := JsonObject():New()
    self:jID                          := JsonObject():New()
    self:jIncorrectID                 := JsonObject():New()
    self:JsonBookkeepingRuleMetadata  := JsonObject():New()
    self:JsonOperationProfileMetadata := JsonObject():New()
    
Return
/*/{Protheus.doc} setDataMode
    Metodo para definir o modo de operação
    @author Rhuan Carvalho
    @since 19/05/2025
    @version version
    @param mode, caracter, modo de como serão processados os dados
    /*/
Method setDataMode(mode as character) as character Class TCAdditionalData

    If ValType(mode) == "C" .And. !Empty(mode)
        self:cDataMode := Upper(mode)
    Else
        self:setReturnMessage("notification", "Necessário passar o modo de operação como parâmetro")
        self:setRequestErrorStatus(.T.)
        self:setProcessErrorToNotify(.T.)
    EndIf
    
Return self
/*/{Protheus.doc} setId
    Método público para passar os Ids que serão pesquisados
    @author Rhuan Carvalho
    @since 12/05/2025
    @version version
    @params aID, id contendo os IDs que serão processados, array
    /*/
Method setId(aId as array) as array Class TCAdditionalData
    Local nIndex as numeric
    Local cId    as character
    
    If ValType(aId) == "A" .And. !Empty(aId)
        self:aIdRequest := aId
        
        For nIndex      := 1 to Len(self:aIdRequest)
            cId         := self:aIdRequest[nIndex]
            If !self:getCache():hasProperty(cId)
                If self:isValidId(cId)
                    aadd(self:aProcessList, cId)
                Else
                    self:setReturnMessage(cId, "ID inválido")
                    self:setProcessErrorToNotify(.T.)
                EndIf
            EndIf
            
        Next nIndex
    Else
        self:setReturnMessage("notification", "Necessário passar um array com os IDs que serão processados")
        self:setRequestErrorStatus(.T.)
        self:setProcessErrorToNotify(.T.)
    EndIf

Return self
/*/{Protheus.doc} setCache
    Método privado para armazenar os dados adicionais em cache
    @author Rhuan Carvalho
    @since 12/05/2025
    @version 12.1.2510

    /*/
Method setCache() as json Class TCAdditionalData
return self:jCacheData := self:jID

/*/{Protheus.doc} isValidId
    Métod para validar se os aids passados são válidos
    @author Rhuan Carvalho
    @since 12/05/2025
    @version version
    @param ID, caracter, id do iten que será processado
    @return retorna a validação se o ID é um caracter e possue 36 posições. 
    /*/
Method isValidId(ID as character) as logical Class TCAdditionalData
Return ValType(ID) == "C" .And. Len(ID) == 36

/*/{Protheus.doc} getCache()
    Retorna o cahe dos IDs
    @author Rhuan Carvalho
    @since 14/05/2025
    @version version
    @return self:jCacheData, Json, json que armazena dados processados pelo modo Written
    /*/
Method getCache() as json Class TCAdditionalData
Return self:jCacheData

/*/{Protheus.doc} getTCAdditionalData
    Método para retornar os dados adicionais
    @author carvalho.rhuan
    @since 13/05/2025
    @version version
    @param param_name, param_type, param_descr
    @return return_var, character, dados retornados da rotina de dados adicionais do configurador de tributos
    /*/
Method getAdditionalData() as character Class TCAdditionalData

    IF !self:hasRequestError()
        self:processRequestData()
    EndIF

    If self:hasProcessErrorToNotify()	
        self:jID["warning"] := self:jIncorrectID
    EndIf
Return self:jID:ToJson()


/*/{Protheus.doc} processRequestData
    Método privado para processar os Ids
    @author Rhuan Carvalho
    @since 13/05/2025
    @version 12.1.251010

    /*/
Method processRequestData() Class TCAdditionalData
    
    If self:cDataMode == "WRITTEN"
        self:getQueryAlias(self:aProcessList)
        self:executeMetadataTablesQuery()
        self:composeJsonMatadata()
        self:setCache()
    elseif self:cDataMode == "PROCESSING"
        self:buildProcessingIdsJson()
        self:executeMetadataTablesQuery()
        self:composeJsonMatadata()
    else
        self:setReturnMessage("notification", "Necessário indicar o método de obtencão dos dados através do método setDataMode()")
        self:setRequestErrorStatus(.T.)
        self:setProcessErrorToNotify(.T.)
    EndIf
    
Return self


/*/{Protheus.doc} getQueryAlias
    Método privado para executar query nas tabelas do configurador de 
    tributos e obter os ids para consulta nas tabelas dos metadados
    @author Rhuan Carvalho
    @since 14/05/2025
    @version 12.1.2510

    /*/
Method getQueryAlias(ARRAYID as array) Class TCAdditionalData
    
    Local cQuery     as character
    Local cAlias     as character
    Local cCJ3IDRESC as character
    Local cF2BPERFOP as character
    Local cF2DIDREL  as character
    Local cF2DTRIB   as character
    Local cSpace     := ' '
    Local nInterator := 1
    Local oPrepared  as object

    cQuery := " SELECT F2D.F2D_IDREL F2DIDREL, F2D.F2D_TRIB F2DTRIB "
    cQuery += " ,F2D.F2D_FILIAL F2DFILIAL ,F2B.F2B_TRIB F2BTRIB, F2B.F2B_PERFOP F2BPERFOP, CJ3.CJ3_IDRESC CJ3IDRESC "

    cQuery += " FROM ? F2D "
    cQuery += " JOIN ? F2B ON F2B.F2B_FILIAL = ? AND F2B.F2B_ID = F2D.F2D_IDCAD AND F2B.D_E_L_E_T_ = ?"
    cQuery += " LEFT JOIN ? CJ3 ON CJ3.CJ3_FILIAL = ? AND CJ3.CJ3_IDF2D = F2D.F2D_ID AND CJ3.D_E_L_E_T_ = ? "

    cQuery += " WHERE F2D.F2D_IDREL IN (?)"
    cQuery += " AND F2D_DTEXCL = ?"
    cQuery += " AND F2D.D_E_L_E_T_ = ?"

    oPrepared := FWExecStatement():New()
    oPrepared:SetQuery(ChangeQuery(cQuery))
    
    // From
    oPrepared:setUnSafe(nInterator++, RetSqlName("F2D"))

    // Join F2B
    oPrepared:setUnSafe(nInterator++, RetSqlName("F2B"))
    oPrepared:setString(nInterator++, xFilial("F2B"))
    oPrepared:setString(nInterator++, cSpace)

    // Left Join CJ3
    oPrepared:setUnSafe(nInterator++, RetSqlName("CJ3"))
    oPrepared:setString(nInterator++, xFilial("CJ3"))
    oPrepared:setString(nInterator++, cSpace)

    // Where
    oPrepared:setIn(nInterator++, ARRAYID)
    oPrepared:setString(nInterator++, cSpace)
    oPrepared:setString(nInterator++, cSpace)

    cAlias := oPrepared:OpenAlias()

    While (cAlias)->(!EOF())
        
        cCJ3IDRESC := AllTrim((cAlias)->CJ3IDRESC)
        cF2BPERFOP := AllTrim((cAlias)->F2BPERFOP)
        cF2DIDREL  := AllTrim((cAlias)->F2DIDREL)
        cF2DTRIB   := AllTrim((cAlias)->F2DTRIB)

        self:buildMetadataIdArrays(cCJ3IDRESC, cF2BPERFOP)

        self:buildMetadataIdsJson(cF2DIDREL, cCJ3IDRESC, cF2BPERFOP, cF2DTRIB)
        
        (cAlias)->(DbSkip())
    EndDo

    dbSelectArea(cAlias)
    (cAlias)->(DbCloseArea())

Return 

/*/{Protheus.doc} buildMetadataIdArrays
    Método privado para construir os arrays que serão usados nas queries das tabelas de metadados
    @author Rhuan Carvalho
    @since 19/05/2025
    @version 12.1.2510
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
Method buildMetadataIdArrays(ID_REGESC as character, COD_PERFOP as character) Class TCAdditionalData

    If !Empty(ID_REGESC)
        aAdd(self:abookkeepingRuleId, ID_REGESC) // Adiciona o ID na lista de IDs Regra de escrituração
    EndIf
    
    If !Empty(COD_PERFOP)
        aAdd(self:aOperationProfileId, COD_PERFOP) // Adiciona o ID na lista de IDs Perfil de operação
    EndIf
    
Return self

/*/{Protheus.doc} buildMetadataIdsJson
    Método privado para construir o Json que contem os IDs dos metadados que serão usados nas queries das tabelas
    @author Rhuan Carvalho
    @since 16/05/2025
    @version version
    @param ID_ITEM, ID_REGESC, COD_PERFOP, ID_REGRCLC, ID_TRIBUTO , character,
    /*/
Method buildMetadataIdsJson(ID_ITEM as character, ID_REGESC as character, COD_PERFOP as character, ID_TRIBUTO as character) Class TCAdditionalData
    Local jIDMetaData   := JsonObject():New()
    Local jIDRegrEsc    := JsonObject():New()
    Local jIDRegrPerfOp := JsonObject():New()
    Local jTax          := JsonObject():New()

    jIDRegrEsc["id"]                  := ID_REGESC // ID TRIBUTO
    jIDRegrEsc["dados adicionais"]    := JsonObject():New()

    jIDRegrPerfOp["id"]               := COD_PERFOP // ID PERFIL DE OPERAÇÃO
    jIDRegrPerfOp["dados adicionais"] := JsonObject():New()
    
    jIDMetaData["regra escrituracao"] := jIDRegrEsc
    jIDMetaData["perfil de operacao"] := jIDRegrPerfOp

    jTax[ID_TRIBUTO]             := jIDMetaData // ID TRIBUTO

    self:jID[ID_ITEM]            := jTax // ID ITEM

    FreeObj(jTax)
    FreeObj(jIDMetaData)
    FreeObj(jIDRegrEsc)
    FreeObj(jIDRegrPerfOp)
    
Return self

/*/{Protheus.doc} getBookkeepingRuleMetadataAlias
Método privado para executar query na tabela CK3 de metadados
@author Rhuan Carvalho
@since 19/05/2025
@version 12.1.2510

/*/
Method getBookkeepingRuleMetadataAlias() Class TCAdditionalData
    Local cQuery    as character
    Local oPrepared as object
    Local cSpace     := ' '
    Local nInterator := 1
    Local cAlias    as character
    Local cContent  as character
    Local cIDCJ2    as character
    Local cCodCK3   as character

    cQuery := " SELECT CK3_IDCJ2 CK3IDCJ2, CK3_CODIGO CK3CODIGO ,CK3_CONTEU CK3CONTEU " 
    cQuery += " FROM ? "
    cQuery += " WHERE CK3_FILIAL = ? AND CK3_IDCJ2 IN (?)"
    cQuery += " AND D_E_L_E_T_ = ?"
    cQuery += " ORDER BY CK3_CODIGO"  

    oPrepared := FWExecStatement():New()   
    oPrepared:SetQuery(ChangeQuery(cQuery))

    // From
    oPrepared:setUnSafe(nInterator++, RetSqlName("CK3"))

    // Where
    oPrepared:setString(nInterator++, xFilial("CK3"))
    oPrepared:setIn(nInterator++, self:abookkeepingRuleId)
    oPrepared:setString(nInterator++, cSpace)

    cAlias := oPrepared:OpenAlias()

    While (cAlias)->(!EOF())
        cContent := AllTrim((cAlias)->CK3CONTEU)
        cCodCK3  := AllTrim((cAlias)->CK3CODIGO)
        cIDCJ2   := AllTrim((cAlias)->CK3IDCJ2)

        self:buildMetadataContentJson(cContent, cCodCK3, cIDCJ2 , 'CK3')       

        (cAlias)->(DbSkip())   
    EndDo

    dbSelectArea(cAlias)
    (cAlias)->(DbCloseArea())

return self

/*/{Protheus.doc} getOperationProfileMetadataAlias
Método privado para executar query na tabela CK4 de metadados
@author Douglas Dourado
@since 09/06/2025
@version 12.1.2510

/*/
Method getOperationProfileMetadataAlias() Class TCAdditionalData
    Local cQuery    as character
    Local oPrepared as object
    Local cSpace     := ' '
    Local nInterator := 1
    Local cAlias    as character
    Local cContent  as character
    Local cCodF20    as character
    Local cCodCK4   as character

    cQuery := " SELECT CK4_CODF20 CK4CODF20, CK4_CODIGO CK4CODIGO ,CK4_CONTEU CK4CONTEU " 
    cQuery += " FROM ? "
    cQuery += " WHERE CK4_FILIAL = ? AND CK4_CODF20 IN (?)"
    cQuery += " AND D_E_L_E_T_ = ?"
    cQuery += " ORDER BY CK4_CODIGO"  

    oPrepared := FWExecStatement():New()   
    oPrepared:SetQuery(ChangeQuery(cQuery))

    // From
    oPrepared:setUnSafe(nInterator++, RetSqlName("CK4"))

    // Where
    oPrepared:setString(nInterator++, xFilial("CK4"))
    oPrepared:setIn(nInterator++, self:aOperationProfileId)
    oPrepared:setString(nInterator++, cSpace)

    cAlias := oPrepared:OpenAlias()

    While (cAlias)->(!EOF())
        cContent := AllTrim((cAlias)->CK4CONTEU)
        cCodCK4  := AllTrim((cAlias)->CK4CODIGO)
        cCodF20   := AllTrim((cAlias)->CK4CODF20)

        self:buildMetadataContentJson(cContent, cCodCK4, cCodF20 , 'CK4')       

        (cAlias)->(DbSkip())   
    EndDo

    dbSelectArea(cAlias)
    (cAlias)->(DbCloseArea())

return self

/*/{Protheus.doc} composeJsonMatadata
    Método privado para compor o Json que será retornado
    @author Rhuan Carvalho
    @since 19/05/2025
    @version 12.1.2510
    @param param_name, param_type, param_descr
@return return_var, return_type, return_description
/*/
Method composeJsonMatadata() Class TCAdditionalData
    Local aID                        := self:jID:GetNames()
    Local aTax                       := {}
    Local nIndex                     as numeric
    Local nIndex2                    as numeric
    Local cIDRegra                   as character
    Local JsonCK3                    := self:JsonBookkeepingRuleMetadata
    Local JsonCK4                    := self:JsonOperationProfileMetadata
    
    For nIndex := 1 to Len(aID) // Percorre os IDs SD1/SD2
        aTax := self:jID[aID[nIndex]]:GetNames()
        For nIndex2 := 1 to Len(aTax) // Percorre os tributos devido ao ID estar contido no tributo

            // Pega os Metadados de escrituracao
            cIDRegra := self:jID[aID[nIndex]][aTax[nIndex2]]["regra escrituracao"]["id"]
            If JsonCK3:hasProperty(cIDRegra)
                self:jID[aID[nIndex]][aTax[nIndex2]]["regra escrituracao"]["dados adicionais"] := JsonCK3[cIDRegra]
            EndIf

            // Pega os Metadados de perfil de operação
            cIDRegra := self:jID[aID[nIndex]][aTax[nIndex2]]["perfil de operacao"]["id"]
            If JsonCK4:hasProperty(cIDRegra)
                self:jID[aID[nIndex]][aTax[nIndex2]]["perfil de operacao"]["dados adicionais"] := JsonCK4[cIDRegra]
            EndIf

            // Metadados de regra de cálculo
            // TODO ...

        Next nIndex2
    Next nIndex

    asize(aID, 0)
    asize(aTax, 0)
    FreeObj(JsonCK3)
    FreeObj(JsonCK4)

Return self


/*/{Protheus.doc} buildProcessingIdsJson
    Método privado retornar os IDs da Matxfis
    @author Rhuan Carvalho
    @since 21/05/2025
    @version 12.1.2510

    /*/
Method buildProcessingIdsJson() Class TCAdditionalData
    Local nItems := 1 as numeric

    While MafisFound("IT", nItems)
    	self:getAdditionalDataIdsFromMatxfis(nItems)
        nItems++
    EndDo
    
Return self

/*/{Protheus.doc} getAdditionalDataIdsFromMatxfis
    Método privado para retornar os IDs que estão em processamento na Matxfis
    @author Rhuan Carvalho
    @since 21/05/2025
    @version 12.1.2510
    @Params nItems, número dos itens contidos na Matxfis, numeric
    /*/
Method getAdditionalDataIdsFromMatxfis(nITems as numeric) Class TCAdditionalData
    Local aItTribGen        := {} as array
    Local aRefTribGen       := MafisRet(nItems, 'IT_TRIBGEN' )
    Local nPosRef           := Len(aRefTribGen)
    Local nIndex            as numeric
    Local cIdRegrEscr       as character
    Local cCodPerfOp        as character
    Local cCodCadTrib       as character

    If nPosRef > 0
        For nIndex := 1 to nPosRef

            aItTribGen  := MafisRet(nItems, 'IT_TRIBGEN' , {aRefTribGen[nIndex][1]})
            cCodCadTrib := Alltrim(aItTribGen[TG_IT_SIGLA])
            cIdRegrEscr := alltrim(aItTribGen[TG_IT_REGRA_ESCR][RE_ID])
            cCodPerfOp  := Alltrim(aItTribGen[TG_IT_PERFOP][OP_COD])

            self:buildMetadataIdArrays(cIdRegrEscr, cCodPerfOp)
            self:buildMetadataIdsJson(cValTochar(nItems), cIdRegrEscr, cCodPerfOp, cCodCadTrib)
        
        Next nIndex
    EndIf

    aSize(aItTribGen, 0)
    aSize(aRefTribGen, 0)
    
Return self


/*/{Protheus.doc} buildMetadataContentJson
    Método privado para construir o Json que contem os conteúdos do metadados
    
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510
    @Params content, conteúdo do metadado, character, codigo, codigo do metadado, character, id, id do metadado, caracter, cType, tipo do metadado, character
    /*/
Method buildMetadataContentJson(content as character, codigo as character, id as character , cType as character) class TCAdditionalData
    Local jsonMetadatacontent as json
    
    jsonMetadatacontent             := JsonObject():New()
    jsonMetadatacontent["conteudo"] := content

    If cType == 'CK3'

        If !self:JsonBookkeepingRuleMetadata:hasProperty(id)
            self:JsonBookkeepingRuleMetadata[id] := JsonObject():New()
        EndIf
        self:JsonBookkeepingRuleMetadata[id][codigo] :=  jsonMetadatacontent

    Elseif cType == 'CK4'

        If !self:JsonOperationProfileMetadata:hasProperty(id)
            self:JsonOperationProfileMetadata[id] := JsonObject():New()
        EndIf
        self:JsonOperationProfileMetadata[id][codigo] :=  jsonMetadatacontent

    // TODO: Implementar tratamento para CK5
    //elseif cType == 'CK5'

    EndIf

    FreeObj(jsonMetadatacontent)

Return self

/*/{Protheus.doc} executeMetadataTablesQuery
    Método para centralizar a execução das queries das tabelas de metadados
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510

    /*/
Method executeMetadataTablesQuery() Class TCAdditionalData
    self:getBookkeepingRuleMetadataAlias()
    self:getOperationProfileMetadataAlias()
    //Todo: incluir aqui metodo para executar querie da CK5
Return self

/*/{Protheus.doc} setReturnMessage
    Método para inserir mensagens de notificação no Json de retorno
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510
    @params ID, Id propriedade Json, character, message, mensagem, character
    /*/
Method setReturnMessage(ID as character, message as character) Class TCAdditionalData
    self:setProcessErrorToNotify(.T.)
    self:jIncorrectID[ID] := message
Return

/*/{Protheus.doc} setRequestErrorStatus
    Método para armazenar o status de erro da requisição
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510

    /*/
Method setRequestErrorStatus(status) Class TCAdditionalData
    self:lRequestErrorStatus := status
Return self

/*/{Protheus.doc} hasRequestError
    Método para verificar se houve erro na requisição
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510

    /*/
Method hasRequestError() Class TCAdditionalData
Return self:lRequestErrorStatus

/*/{Protheus.doc} setProcessErrorToNotify
    Método para armazenar se será notificado um erro no json de retorno
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510

    /*/
Method setProcessErrorToNotify(status as logical) Class TCAdditionalData
    self:lProcessErrorToNotify := status
Return

/*/{Protheus.doc} hasProcessErrorToNotify
    Método para verificar se há algum erro de processo que precisa ser notificado no json de retorno
    @author Rhuan Carvalho
    @since 22/05/2025
    @version 12.1.2510

    /*/
Method hasProcessErrorToNotify() Class TCAdditionalData
Return self:lProcessErrorToNotify

/*/{Protheus.doc} destroy
    Método para destruir os objetos criados na classe
    @author Rhuan Carvalho
    @since 22/05/2025
    @version version

    /*/
Method destroy() Class TCAdditionalData
    
    aSize(self:aIdRequest,0)
    aSize(self:abookkeepingRuleId, 0)
    aSize(self:abookkeepingRuleMetadata, 0)
    aSize(self:aOperationProfileId, 0)
    aSize(self:aProcessList, 0)
    FreeObj(self:jCacheData)
    FreeObj(self:jID)
    FreeObj(self:JsonBookkeepingRuleMetadata)
    FreeObj(self:JsonOperationProfileMetadata)
    FreeObj(self:jIncorrectID)

Return 

