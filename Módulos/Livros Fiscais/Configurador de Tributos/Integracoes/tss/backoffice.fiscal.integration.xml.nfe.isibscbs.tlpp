#include "tlpp-core.th"
#include "tlpp-rest.th"

#define IBS_ESTADUAL        '000060'
#define IBS_MUNICIPAL       '000061'
#define CBS_FEDERAL         '000062'
#define IMPOSTO_SELETIVO    '000063'
#define IBS_EST_REGULAR     '000064'
#define IBS_MUN_REGULAR     '000065'
#define CBS_REGULAR         '000066'
#define IBS_CRED_PRESUMIDO  '000067'
#define CBS_CRED_PRESUMIDO  '000068'
#define IBS_MONOFASICO      '000069'
#define CBS_MONOFASICO      '000070'

static lExistCJ3_CCT := len(FWSX3Util():GetFieldStruct('CJ3_CCT')) > 0 as logical

namespace totvs.protheus.backoffice.fiscal.integration
using namespace totvs.protheus.backoffice.fiscal.tciclass

/*/{Protheus.doc} IBSCBSNFE
Classe responsável por gerar no XML para o grupo UB - Informações dos tributos IBS / CBS e Imposto Seletivo da NFe/NFCe.
@type class
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 21/05/2025
/*/
Class TaxInformation

	//Propriedades
	public  data nTotalBaseIbsCbs as numeric
	private data oTaxByTotvsId as object
	public  data oTotalTaxByTotvsId as object

	//Metodos get
	public  method New() as object
	public  method getXmlIS() as character
	public  method getXmlIBSCBS() as character
	private method getXmlgIBSCBS() as character
	private method getXmlgIBSUF(oStateTax as json, nTotalIbs as numeric) as character
	private method getXmlgDif(oTax as json) as character
	private method getXmlgDevTrib(oTax as json) as character
	private method getXmlgRed(oTax as json) as character
	private method getXmlgIBSMun(oMunicipalTax as json, nTotalIbs as numeric) as character
	private method getXmlgCBS(oTax as json) as character
	private method getXmlgTribRegular() as character
	private method getXmlgIBSCredPres(oJsIbsCreditoPresumido) as character
	private method getXmlgCBSCredPres(oJsCbsCreditoPresumido) as character
	//public  method getXmlgTribCompraGov() as character
	//private method getXmlgIBSCBSMono(oJsIbsMonofasico, oJsCbsMonofasico) as character
	//private method getXmlgTransfCred() as character
	//private method getXmlgCredPresIBSZFM() as character
	//Se for criado um id totvs para cada tipo de monofasico, o ideal é criar metodos separados para cada um deles
	//private method getXmlMonoPadrao() as character
	//private method getXmlMonoReten() as character
	//private method getXmlMonoRet() as character
	//private method getXmlMonoDif() as character

	public  method getXmlTotalIbsCbsIs() as character
	public  method getXmlTotalIs() as character
	public  method getXmlTotalIbsCbs() as character

	private method getXmlTotalgIBS() as character
	private method getXmlTotalgIbsUf() as character
	private method getXmlTotalgIbsMun() as character
	private method getXmlTotalgCbs() as character
	private method getXmlTotalgMono() as character

	//metodos set
	private method setDocumentItemId()
	private method setDocumentItemTaxes()
	private method setTotalTaxByTotvsId()
	private method setTotalBaseIbsCbs(nBaseIbsCbs as numeric)

	public method Destroy()

EndClass

/*/{Protheus.doc} New
Metodo construtor da classe TaxInformation, recebe o id do documento que 
será utilizado para gerar as tags de tributos.
@type method
@version  P12 V1.2410
@param cDocumentItemId, Id do documento atual
@author Carlos Eduardo Boy
@since 21/05/2025
/*/
Method New() as object class TaxInformation

	::oTaxByTotvsId      := JsonObject():New()
	::oTotalTaxByTotvsId := JsonObject():New()

Return self

/*/{Protheus.doc} IBSCBSBPE::GeraIS
Gera tags do imposto IS.
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 21/05/2025/*/
Method getXmlIS(cDocumentItemId as character) as character class TaxInformation
	Local oJsImpSel as json
	Local cXmlImpSel as character
	Local cAliasDoc as character
	Local cAlsField as character
	Local cClassTrib as character
	Local aDadosItem  as array

	self:setDocumentItemId(cDocumentItemId)

	if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IMPOSTO_SELETIVO)

		oJsImpSel := ::oTaxByTotvsId[cDocumentItemId][IMPOSTO_SELETIVO]

		//Tratamento temporário, posteriormente a classe TciWritten deve retornar esses valores para seram passados para o XML
		cAliasDoc   := GetAdvFVal('F2D', 'F2D_TABELA', xFilial('F2D')+cDocumentItemId+oJsImpSel['dados_escriturados']['codigo_tributo'], 2 )
		cAlsField   := substr(cAliasDoc,2,2)
		aDadosItem  := GetAdvFVal(cAliasDoc, {cAlsField+'_UM',cAlsField+'_QUANT'}, xFilial(cAliasDoc)+cDocumentItemId, 18 )

		//Proteção para o caso de não existir o campo CJ3_CCT
		if lExistCJ3_CCT
			cClassTrib := oJsImpSel['dados_escriturados']['cclasstrib']
		endif

		cXmlImpSel += '<imposto>'
		cXmlImpSel +=     '<codigo>IS</codigo>'
		cXmlImpSel += 	'<Tributo>'
		cXmlImpSel +=         '<CSTIS>' + oJsImpSel['dados_escriturados']['cst'] + '</CSTIS>'
		cXmlImpSel +=         '<cClassTribIS>' + cClassTrib + '</cClassTribIS>'
		cXmlImpSel +=         '<vBCIS>' + ltrim(str(oJsImpSel['base_tributo'],13,2)) + '</vBCIS>'
		cXmlImpSel +=         '<pIS>' + ltrim(str(oJsImpSel['aliquota_tributo'],8,4)) + '</pIS>'
		cXmlImpSel +=         '<pISEspec>' + lTrim(str(0, 8, 4)) + '</pISEspec>'
		cXmlImpSel +=         '<uTrib>' + aDadosItem[1] + '</uTrib>'
		cXmlImpSel +=         '<qTrib>' + lTrim(str(aDadosItem[2],16,4)) + '</qTrib>'
		cXmlImpSel +=         '<vIS>' + ltrim(str(oJsImpSel['valor_tributo'],13 ,2)) + '</vIS>'
		cXmlImpSel += 	'</Tributo>'
		cXmlImpSel += '</imposto>''
	endif

Return cXmlImpSel

/*/{Protheus.doc} IBSCBSBPE::GeraIBSCBS
Gera tags dos imposto IBS e CBS.
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 21/05/2025
/*/
Method getXmlIBSCBS(cDocumentItemId) as character class TaxInformation
	Local lGeraXml as logical
	Local cXmlCbsIbs as character
	Local cCst as character
	Local cClassTrib as character
	Local nBaseIbsCbs as numeric
	Private oJsCbs as json
	Private oJsIbsEstadual  as json
	Private oJsIbsMunicipal as json
	Private oJsIbsCreditoPresumido as json
	Private oJsCbsCreditoPresumido as json
	Private oJsIbsMonofasico as json
	Private oJsCbsMonofasico as json
	Private oJsIbsEstadualRegular as json
	Private oJsIbsMunicipalRegular as json
	Private oJsCbsRegular as json

	self:setDocumentItemId(cDocumentItemId)

	if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_ESTADUAL) .or. ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_MUNICIPAL)

		oJsIbsEstadual  := ::oTaxByTotvsId[cDocumentItemId][IBS_ESTADUAL]
		oJsIbsMunicipal := ::oTaxByTotvsId[cDocumentItemId][IBS_MUNICIPAL]

		//Pego do estadual pois sempre existIrão os dois tributos(Estadual e municipal)
		nBaseIbsCbs := iif(oJsIbsEstadual != nil, oJsIbsEstadual['base_tributo'], oJsIbsMunicipal['base_tributo'])
		self:setTotalBaseIbsCbs(nBaseIbsCbs)

		cCst := iif(oJsIbsEstadual != nil, oJsIbsEstadual['dados_escriturados']['cst'], oJsIbsMunicipal['dados_escriturados']['cst'])

		//Proteção para o caso de não existir o campo CJ3_CCT
		if lExistCJ3_CCT
			cClassTrib := iif(oJsIbsEstadual != nil, oJsIbsEstadual['dados_escriturados']['cclasstrib'], oJsIbsMunicipal['dados_escriturados']['cclasstrib'])
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_CRED_PRESUMIDO)
			oJsIbsCreditoPresumido := ::oTaxByTotvsId[cDocumentItemId][IBS_CRED_PRESUMIDO]
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_EST_REGULAR)
			oJsIbsEstadualRegular := ::oTaxByTotvsId[cDocumentItemId][IBS_EST_REGULAR]
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_MUN_REGULAR)
			oJsIbsMunicipalRegular := ::oTaxByTotvsId[cDocumentItemId][IBS_MUN_REGULAR]
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(IBS_MONOFASICO)
			oJsIbsMonofasico := ::oTaxByTotvsId[cDocumentItemId][IBS_MONOFASICO]
		endif

		lGeraXml := .t.
	endif

	if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(CBS_FEDERAL)

		oJsCbs := ::oTaxByTotvsId[cDocumentItemId][CBS_FEDERAL]

		if nBaseIbsCbs = 0
			nBaseIbsCbs := oJsCbs['base_tributo']
			self:setTotalBaseIbsCbs(nBaseIbsCbs)
		endif

		if empty(cCst) .and. empty(cClassTrib)
			cCst := oJsCbs['dados_escriturados']['cst']

			//Proteção para o caso de não existir o campo CJ3_CCT
			if lExistCJ3_CCT
				cClassTrib := oJsCbs['dados_escriturados']['cclasstrib']
			endif
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(CBS_CRED_PRESUMIDO)
			oJsCbsCreditoPresumido := ::oTaxByTotvsId[cDocumentItemId][CBS_CRED_PRESUMIDO]
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(CBS_REGULAR)
			oJsCbsRegular := ::oTaxByTotvsId[cDocumentItemId][CBS_REGULAR]
		endif

		if ::oTaxByTotvsId[cDocumentItemId]:HasProperty(CBS_MONOFASICO)
			oJsCbsMonofasico := ::oTaxByTotvsId[cDocumentItemId][CBS_MONOFASICO]
		endif

		lGeraXml := .t.
	endif

	if lGeraXml
		cXmlCbsIbs := '<imposto>'
		cXmlCbsIbs += 	'<codigo>IBSCBS</codigo>'
		cXmlCbsIbs += 	'<Tributo>'
		cXmlCbsIbs +=         '<CST>' + cCst + '</CST>'
		cXmlCbsIbs +=         '<cClassTrib>' + cClassTrib + '</cClassTrib>'
		cXmlCbsIbs +=         self:getXmlgIBSCBS(nBaseIbsCbs)
		//cXmlCbsIbs +=         self:getXmlgIBSCBSMono(oJsIbsMonofasico, oJsCbsMonofasico)
		//cXmlCbsIbs +=         self:getXmlgTransfCred()
		//cXmlCbsIbs +=         self:getXmlgCredPresIBSZFM()
		cXmlCbsIbs += 	'</Tributo>'
		cXmlCbsIbs += '</imposto>'
	endif

	//Verificar se os Jsons criados devem ser liberados da memória

Return cXmlCbsIbs

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSCBS do grupo IBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 21/05/2025
/*/
Method getXmlgIBSCBS(nBaseIbsCbs as numeric) as character class TaxInformation
	Local cXml as character
	Local nTotalIbsItem as numeric

	cXml += '<gIBSCBS>'
	cXml +=     '<vBC>' + ltrim(str(nBaseIbsCbs,13,2)) + '</vBC>'
	cXml +=     self:getXmlgIBSUF(oJsIbsEstadual, @nTotalIbsItem)
	cXml +=     self:getXmlgIBSMun(oJsIbsMunicipal, @nTotalIbsItem)
	cXml +=     '<vIBS>' + lTrim(str(nTotalIbsItem,13,2)) + '</vIBS>'
	cXml +=     self:getXmlgCBS(oJsCbs)
	cXml +=     self:getXmlgTribRegular(oJsIbsEstadualRegular, oJsIbsMunicipalRegular, oJsCbsRegular)
	cXml +=     self:getXmlgIBSCredPres(oJsIbsCreditoPresumido)
	cXml +=     self:getXmlgCBSCredPres(oJsCbsCreditoPresumido)
	//cXml +=     self:getXmlgTribCompraGov()

	cXml += '</gIBSCBS>'
return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSUF do grupo gIbSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 21/05/2025
/*/
Method getXmlgIBSUF(oStateTax as json, nTotalIbs as numeric) as character class TaxInformation
	Local cXml as character
	Local nAliquotaTributo as numeric
	Local nValorTributo    as numeric

	if oStateTax != nil
		nAliquotaTributo := oStateTax['aliquota_tributo']
		nValorTributo    := oStateTax['valor_tributo']
	endif

	cXml += '<gIBSUF>'
	cXml +=     '<pIBSUF>' + ltrim(str(nAliquotaTributo,8,4)) + '</pIBSUF>'
	cXml +=     self:getXmlgDif(oStateTax)
	cXml +=     self:getXmlgDevTrib(oStateTax)
	cXml +=     self:getXmlgRed(oStateTax)
	cXml +=     '<vIBSUF>' + ltrim(str(nValorTributo,16,2)) + '</vIBSUF>'
	cXml += '</gIBSUF>'

	nTotalIbs += nValorTributo

return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gDif do grupo gIBSUF
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgDif(oTax as json) as character class TaxInformation
	Local cXml as character
	if oTax != nil .and. oTax['dados_escriturados']['valor_diferido'] > 0
		cXml += '<gDif>'
		cXml +=     '<pDif>' + ltrim(str(oTax['dados_escriturados']['perc_diferido'],8,4)) + '</pDif>'
		cXml +=     '<vDif>' + ltrim(str(oTax['dados_escriturados']['valor_diferido'],13,2)) + '</vDif>'
		cXml += '</gDif>'
	endif
return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gDevTrib do grupo gIBSUF
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgDevTrib(oTax as json) as character class TaxInformation
	Local cXml as character
	if oTax != nil
		cXml += '<gDevTrib>'
		cXml +=     '<vDevTrib>' + lTrim(str(0, 13, 2)) + '</vDevTrib>'
		cXml += '</gDevTrib>'
	endif

return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gRed(Grupo de informações da redução da alíquota) do grupo gIBSUF
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgRed(oTax as json) as character class TaxInformation
	Local cXml as character
	if oTax != nil .and. oTax['dados_escriturados']['perc_red_aliquota'] > 0
		cXml += '<gRed>'
		cXml +=     '<pRedAliq>' +  lTrim(str(oTax['dados_escriturados']['perc_red_aliquota'],8,4)) + '</pRedAliq>'
		cXml +=     '<pAliqEfet>' + lTrim(str(oTax['dados_escriturados']['perc_aliquota_original'],8,4)) + '</pAliqEfet>'
		cXml += '</gRed>'
	endif
return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gIBSMun() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgIBSMun(oMunicipalTax as json, nTotalIbs as numeric) as character class TaxInformation

	Local cXml as character
	Local nAliquotaTributo as numeric
	Local nValorTributo    as numeric

	if oMunicipalTax != nil
		nAliquotaTributo := oMunicipalTax['aliquota_tributo']
		nValorTributo    := oMunicipalTax['valor_tributo']
	endif

	cXml += '<gIBSMun>'
	cXml +=     '<pIBSMun>' + ltrim(str(nAliquotaTributo,8,4)) + '</pIBSMun>'
	cXml +=     self:getXmlgDif(oMunicipalTax)
	cXml +=     self:getXmlgDevTrib(oMunicipalTax)
	cXml +=     self:getXmlgRed(oMunicipalTax)
	cXml +=     '<vIBSMun>' + ltrim(str(nValorTributo,13,2)) + '</vIBSMun>'
	cXml += '</gIBSMun>'

	nTotalIbs += nValorTributo

return cXml

/*/{Protheus.doc} 
Gera tags do subgrupo gCBS() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgCBS(oTax as json) as character class TaxInformation
	Local cXml as character
	Local nAliquotaTributo as numeric
	Local nValorTributo    as numeric

	if oTax != nil
		nAliquotaTributo := oTax['aliquota_tributo']
		nValorTributo    := oTax['valor_tributo']
	endif

	cXml += '<gCBS>'
	cXml +=     '<pCBS>' + ltrim(str(nAliquotaTributo,8,4)) + '</pCBS>'
	cXml +=     self:getXmlgDif(oTax)
	cXml +=     self:getXmlgDevTrib(oTax)
	cXml +=     self:getXmlgRed(oTax)
	cXml +=     '<vCBS>' + ltrim(str(nValorTributo,13,2)) + '</vCBS>'
	cXml += '</gCBS>

return cXml

/*/{Protheus.doc}
Gera tags do subgrupo gTribRegular() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgTribRegular(oJsIbsEstadualRegular as json, oJsIbsMunicipalRegular as json, oJsCbsRegular as json) as character class TaxInformation
	Local cXml as character
	Local nAliqEfetRegIbsUf as numeric
	Local nAliqEfetRegIbsMun as numeric
	Local nAliqEfetRegCbs as numeric
	Local nValorTribRegIbsUf as numeric
	Local nValorTribRegIbsMun as numeric
	Local nValorTribRegCbs as numeric
	Local lTributoRegular := oJsIbsEstadualRegular != nil .or. oJsIbsMunicipalRegular != nil .or. oJsCbsRegular != nil as logical
	Local cCstTributoRegular as character
	Local cClassTributoRegular as character

	if lTributoRegular

		//Verifica se o imposto estadual regular foi informado
		if oJsIbsEstadualRegular != nil
			nAliqEfetRegIbsUf := oJsIbsEstadualRegular['aliquota_tributo']
			nValorTribRegIbsUf := oJsIbsEstadualRegular['valor_tributo']

			cCstTributoRegular := oJsIbsEstadualRegular['dados_escriturados']['cst']
			cClassTributoRegular := oJsIbsEstadualRegular['dados_escriturados']['cclasstrib']
		endif

		//Verifica se o imposto municipal regular foi informado
		if oJsIbsMunicipalRegular != nil
			nAliqEfetRegIbsMun := oJsIbsMunicipalRegular['aliquota_tributo']
			nValorTribRegIbsMun := oJsIbsMunicipalRegular['valor_tributo']
			if empty(cCstTributoRegular)
				cCstTributoRegular := oJsIbsMunicipalRegular['dados_escriturados']['cst']
				cClassTributoRegular := oJsIbsMunicipalRegular['dados_escriturados']['cclasstrib']
			endif
		endif

		//Verifica se o imposto federal regular foi informado
		if oJsCbsRegular != nil
			nAliqEfetRegCbs := oJsCbsRegular['aliquota_tributo']
			nValorTribRegCbs := oJsCbsRegular['valor_tributo']
			if empty(cCstTributoRegular)
				cCstTributoRegular := oJsCbsRegular['dados_escriturados']['cst']
				cClassTributoRegular := oJsCbsRegular['dados_escriturados']['cclasstrib']
			endif
		endif

		cXml += '<gTribRegular>'
		cXml +=     '<CSTReg>' + cCstTributoRegular + '</CSTReg>'
		cXml +=     '<cClassTribReg>' + cClassTributoRegular + '</cClassTribReg>'
		cXml +=     '<pAliqEfetRegIBSUF>' + lTrim(str(nAliqEfetRegIbsUf ,8,4)) + '</pAliqEfetRegIBSUF>'
		cXml +=     '<vTribRegIBSUF>' + lTrim(str(nValorTribRegIbsUf,13,2)) + '</vTribRegIBSUF>'
		cXml +=     '<pAliqEfetRegIBSMun>' + '0' + '</pAliqEfetRegIBSMun>' //lTrim(str(nAliqEfetRegIbsMun,8,4))
		cXml +=     '<vTribRegIBSMun>' + '0' + '</vTribRegIBSMun>' //lTrim(str(nValorTribRegIbsMun,13,2))
		cXml +=     '<pAliqEfetRegCBS>' + lTrim(str(nAliqEfetRegCbs,8,4)) + '</pAliqEfetRegCBS>'
		cXml +=     '<vTribRegCBS>' + lTrim(str(nValorTribRegCbs,13,2)) + '</vTribRegCBS>'
		cXml += '</gTribRegular>'

	endif

return cXml

/*/{Protheus.doc}
Gera tags do subgrupo gIBSCredPres() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgIBSCredPres(oJsIbsCreditoPresumido as json) as character class TaxInformation
	Local cXml as character

	if oJsIbsCreditoPresumido != nil
		cXml += '<gIBSCredPres>'
		cXml +=     '<cCredPres>' + '5' + '</cCredPres>' //Código de Classificação do Crédito Presumido
		cXml +=     '<pCredPres>' + ltrim(str(oJsIbsCreditoPresumido['aliquota_tributo'],8,4)) + '</pCredPres>'
		cXml +=     '<vCredPres>' + ltrim(str(oJsIbsCreditoPresumido['valor_tributo'],13,2)) + '</vCredPres>'
		cXml +=     '<vCredPresCondSus>' + lTrim(str(0, 13, 2)) + '</vCredPresCondSus>'
		cXml += '</gIBSCredPres>'
	endif
return cXml

/*/{Protheus.doc}
Gera tags do subgrupo gCBSCredPres() do grupo gIBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
Method getXmlgCBSCredPres(oJsCbsCreditoPresumido as json) as character class TaxInformation
	Local cXml as character

	if oJsCbsCreditoPresumido != nil
		cXml += '<gCBSCredPres>'
		cXml +=     '<cCredPres>' + '3' + '</cCredPres>' //Código de Classificação do Crédito Presumido
		cXml +=     '<pCredPres>' + ltrim(str(oJsCbsCreditoPresumido['aliquota_tributo'],8,4)) + '</pCredPres>'
		cXml +=     '<vCredPres>' + ltrim(str(oJsCbsCreditoPresumido['valor_tributo'],13,2)) + '</vCredPres>'
		cXml +=     '<vCredPresCondSus>' + lTrim(str(0, 13, 2)) + '</vCredPresCondSus>'
		cXml += '</gCBSCredPres>'
	endif
return cXml


/*/{Protheus.doc}
Gera tags do subgrupo gTribCompraGov
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 16/06/2025
/*/
/*Method getXmlgTribCompraGov() as character class TaxInformation
    Local cXml as character
    cXml += '<gTribCompraGov>'
    cXml +=     '<pAliqIBSUF>' + lTrim(str(0, 8, 4)) + '</pAliqIBSUF>'
    cXml +=     '<vTribIBSUF>' + lTrim(str(0, 13, 2)) + '</vTribIBSUF>'
    cXml +=     '<pAliqIBSMun>' + lTrim(str(0, 8, 4)) + '</pAliqIBSMun>'
    cXml +=     '<vTribIBSMun>' + lTrim(str(0, 13, 2)) + '</vTribIBSMun>'
    cXml +=     '<pAliqCBS>' + lTrim(str(0, 8, 4)) + '</pAliqCBS>'
    cXml +=     '<vTribCBS>' + lTrim(str(0, 13, 2)) + '</vTribCBS>'
    cXml += '</gTribCompraGov>'
return cXml*/

/*/{Protheus.doc}
Gera tags do subgrupo gIBSCBSMono() do grupo IBSCBS
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 22/05/2025
/*/
/*Method getXmlgIBSCBSMono(oJsIbsMonofasico as json, oJsCbsMonofasico as json) as character class TaxInformation
    Local cXml as character
    Local lIbsMono := valtype(oJsIbsMonofasico) == 'J' as logical
    Local lCbsMono := valtype(oJsCbsMonofasico) == 'J' as logical

    //Se for criado um idTotvs para cada tipo de monofasico, o ideal é criar metodos separados para cada um deles
    //e chamar dentro do grupo gIBSCBSMono
    
    if lIbsMono .or. lCbsMono
        cXml += '<gIBSCBSMono>'
        //cXml +=     self:getXmlMonoPadrao()
        cXml +=     '<gMonoPadrao>
        cXml +=         '<qBCMono>' + '5' + '</qBCMono>'
        cXml +=         '<adRemIBS>' + iif(lIbsMono,lTrim(str(oJsIbsMonofasico['aliquota_tributo'],8,4)),'0') + '</adRemIBS>'
        cXml +=         '<adRemCBS>' + iif(lCbsMono,lTrim(str(oJsCbsMonofasico['aliquota_tributo'],8,4)),'0') + '</adRemCBS>'
        cXml +=         '<vIBSMono>' + iif(lIbsMono,lTrim(str(oJsIbsMonofasico['valor_tributo'],13,2)),'0')   + '</vIBSMono>'
        cXml +=         '<vCBSMono>' + iif(lCbsMono,lTrim(str(oJsCbsMonofasico['valor_tributo'],13,2)),'0')   + '</vCBSMono>'
        cXml +=     '</gMonoPadrao>
        //cXml +=     self:getXmlMonoReten()
        cXml +=     '<gMonoReten>
        cXml +=         '<qBCMonoReten>' + lTrim(str(0, 16, 2)) + '</qBCMonoReten>'
        cXml +=         '<adRemIBSReten>' + lTrim(str(0, 8, 4)) + '</adRemIBSReten>'
        cXml +=         '<vIBSMonoReten>' + lTrim(str(0, 13, 2)) + '</vIBSMonoReten>'
        cXml +=         '<adRemCBSReten>' + lTrim(str(0, 8, 4)) + '</adRemCBSReten>'
        cXml +=         '<vCBSMonoReten>' + lTrim(str(0, 13, 2)) + '</vCBSMonoReten>'
        cXml +=     '</gMonoReten>'
        //cXml +=     self:getXmlMonoRet()
        cXml +=     '<gMonoRet>
        cXml +=         '<qBCMonoRet>' + lTrim(str(0, 16, 2)) + '</qBCMonoRet>'
        cXml +=         '<adRemIBSRet>' + lTrim(str(0, 8, 4)) + '</adRemIBSRet>'
        cXml +=         '<vIBSMonoRet>' + lTrim(str(0, 13, 2)) + '</vIBSMonoRet>'
        cXml +=         '<adRemCBSRet>' + lTrim(str(0, 8, 4)) + '</adRemCBSRet>'
        cXml +=         '<vCBSMonoRet>' + lTrim(str(0, 13, 2)) + '</vCBSMonoRet>'
        cXml +=     '</gMonoRet>'
        //cXml +=     self:getXmlMonoDif()
        cXml +=     '<gMonoDif>
        cXml +=         '<pDifIBS>'     + iif(lIbsMono, lTrim(str(oJsIbsMonofasico['dados_escriturados']['perc_diferido'],8,4)), '0')        + '</pDifIBS>'
        cXml +=         '<vIBSMonoDif>' + iif(lIbsMono, lTrim(str(oJsIbsMonofasico['dados_escriturados']['valor_diferido'],13,2)), '0')      + '</vIBSMonoDif>'
        cXml +=         '<pDifCBS>'     + iif(lCbsMono, lTrim(str(oJsCbsMonofasico['dados_escriturados']['perc_diferido'],8,4)), '0')        + '</pDifCBS>'
        cXml +=         '<vCBSMonoDif>' + iif(lCbsMono, lTrim(str(oJsCbsMonofasico['dados_escriturados']['valor_diferido'],13,2)), '0')      + '</vCBSMonoDif>'
        cXml +=     '</gMonoDif>'
        cXml +=     '<vTotIBSMonoItem>' + iif(lIbsMono, lTrim(str(oJsIbsMonofasico['dados_escriturados']['valor_tributado'],13,2)), '0') + '</vTotIBSMonoItem>'
        cXml +=     '<vTotCBSMonoItem>' + iif(lCbsMono, lTrim(str(oJsCbsMonofasico['dados_escriturados']['valor_tributado'],13,2)), '0') + '</vTotCBSMonoItem>'
        cXml += '</gIBSCBSMono>'
    endif
return cXml*/

/*/{Protheus.doc}
Instacia a classe que retorna as inforamações dos impostos da reforma 
tributária e seta o id do item do documento que esta sendo transmitido
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 30/05/2025
/*/
Method setDocumentItemId(cDocumentItemId) class TaxInformation
    Local oDocumentData as object
    
    //Só carrega as informações se for um novo item, caso contrario usa os dados já carregados anteriormente.
    if !::oTaxByTotvsId:HasProperty(cDocumentItemId)
        
        //Instacia classe e seta o id do item do documento
        oDocumentData := TCIWritten():New()
        oDocumentData:setId({cDocumentItemId})
        
        //Recupera e formata os dados do id informado
        self:setDocumentItemTaxes(cDocumentItemId, oDocumentData:getDataId())
        
        //Libera os recursos utilizados pela instância da classe
        oDocumentData:destroy()
    endif    
return 

/*/{Protheus.doc}
Gera as tags do gTransfCred
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 11/06/2025
/*/
/*Method getXmlgTransfCred() as character class TaxInformation
    Local cXml as character
    cXml += '<gTransfCred>'
    cXml +=     '<vIBS>' + lTrim(str(0, 13, 2)) + '</vIBS>'
    cXml +=     '<vCBS>' + lTrim(str(0, 13, 2)) + '</vCBS>'
    cXml += '</gTransfCred>'
return cXml*/

/*/{Protheus.doc}
Gera as tags do gCredPresIBSZFM
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 11/06/2025
/*/

/*Method getXmlgCredPresIBSZFM() as character class TaxInformation
    Local cXml as character
    cXml += '<gCredPresIBSZFM>'
    cXml +=     '<tpCredPresIBSZFM>' + lTrim(str(0, 1)) + '</tpCredPresIBSZFM>'
    cXml +=     '<vCredPresIBSZFM>' + lTrim(str(0, 13, 2)) + '</vCredPresIBSZFM>'
    cXml += '</gCredPresIBSZFM>'
return cXml*/

/*/{Protheus.doc}
Pega o retorno da classe TCIWritten com os dados dos impostos e formata
em um json para uso na geração do XML da NFe/NFCe.
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 30/06/2025
/*/
Method setDocumentItemTaxes(cDocumentItemId as character, cDocumentItemTaxes as character ) class TaxInformation
    Local oDocumentItemId := JsonObject():New() as json
    Local aIdsTrib := {} as array
    Local cIdTotvsTrib as character
    Local i as integer

    oDocumentItemId:fromJson(cDocumentItemTaxes) 

    aIdsTrib := oDocumentItemId['dados_Id'][cDocumentItemId]:GetNames()
    ::oTaxByTotvsId[cDocumentItemId] := JsonObject():New()
    for i := 1 to Len(aIdsTrib)
        if upper(aIdsTrib[i]) == 'AVISO' //Id não calculado pelo configurador de tributos
            exit
        endif
        cIdTotvsTrib := oDocumentItemId['dados_Id'][cDocumentItemId][aIdsTrib[i]]['codigo_tributo_relacionado']
        ::oTaxByTotvsId[cDocumentItemId][cIdTotvsTrib] := oDocumentItemId['dados_Id'][cDocumentItemId][aIdsTrib[i]]

        self:setTotalTaxByTotvsId(cDocumentItemId, cIdTotvsTrib)
    next    

    FwFreeArray(aIdsTrib)

return

/*/{Protheus.doc}
Totaliza o valor dos tributos para geração das tags de total do XML
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 02/07/2025
/*/
Method setTotalTaxByTotvsId(cDocumentItemId as character, cIdTotvsTrib as character ) class TaxInformation

    if !::oTotalTaxByTotvsId:HasProperty(cIdTotvsTrib)
        ::oTotalTaxByTotvsId[cIdTotvsTrib] := JsonObject():New()
        ::oTotalTaxByTotvsId[cIdTotvsTrib]['total_diferimento'] := 0
        ::oTotalTaxByTotvsId[cIdTotvsTrib]['total_tributo']     := 0
    endif    

    if cIdTotvsTrib $ (IBS_ESTADUAL + '|' +  IBS_MUNICIPAL + '|' + CBS_FEDERAL)
        ::oTotalTaxByTotvsId[cIdTotvsTrib]['total_diferimento'] += ::oTaxByTotvsId[cDocumentItemId][cIdTotvsTrib]['dados_escriturados']['valor_diferido']
    endif

    ::oTotalTaxByTotvsId[cIdTotvsTrib]['total_tributo'] += ::oTaxByTotvsId[cDocumentItemId][cIdTotvsTrib]['valor_tributo']    

return    

/*/{Protheus.doc}
Libera os recursos utilizados pela instância da classe
@type method
@version  P12 V1.2410
@author Carlos Eduardo Boy
@since 03/07/2025
/*/
Method Destroy() class TaxInformation
    FreeObj(::oTaxByTotvsId)
    FreeObj(::oTotalTaxByTotvsId)
    ::oTaxByTotvsId := nil
    ::oTotalTaxByTotvsId := nil
Return

/*/{Protheus.doc} getXmlGroupW03
Gera a tag principal do total dos  tributos CBS/IBS/IS.
@type method
@author Evandro Italo,
@since 21/05/2025
/*/
Method getXmlTotalIbsCbsIs() as character Class TaxInformation
    Local cXml as character

    cXml += self:getXmlTotalIs()
    cXml += self:getXmlTotalIbsCbs()

Return cXml

/*/{Protheus.doc} getXmlTotalIs
Gera a tag do total do imposto seletivo (IS) 
@type method
@author Evandro Italo,
@since 21/05/2025
/*/
Method getXmlTotalIs() as character class TaxInformation
    Local cXml as character

	if ::oTotalTaxByTotvsId:HasProperty(IMPOSTO_SELETIVO) 
        cXml += '<ISTot>' 
        cXml +=     '<vIS>' + alltrim(str(::oTotalTaxByTotvsId[IMPOSTO_SELETIVO]['total_tributo'],13,2)) + '</vIS>' 
        cXml += '</ISTot>'
    endif

return cXml    

/*/{Protheus.doc} getXmlTotalIbsCbs
Gera as tags dos totais dos tributos IBS e CBS
@type method
@author Evandro Italo,
@since 21/05/2025
/*/
Method getXmlTotalIbsCbs() as character class TaxInformation
    Local cXml as character

	if ::oTotalTaxByTotvsId:HasProperty(IBS_ESTADUAL) .or. ::oTotalTaxByTotvsId:HasProperty(IBS_MUNICIPAL) .or. ::oTotalTaxByTotvsId:HasProperty(CBS_FEDERAL)
        cXml += '<IBSCBSTot>'
        cXml +=     '<vBCIBSCBS>'+ lTrim(str(::nTotalBaseIbsCbs,13,2)) +'</vBCIBSCBS>'
        cXml +=     self:getXmlTotalgIBS()
        cXml +=     self:getXmlTotalgCbs()
        cXml +=     self:getXmlTotalgMono()
        cXml += '</IBSCBSTot>'    
    endif    

return cXml

/*/{Protheus.doc} getXmlTotalgIBS
Gera as tags de totalização do do subgrupo gIBS
@type method
@author Evandro Italo
@since 21/05/2025
/*/
Method getXmlTotalgIBS() as character Class TaxInformation
    Local cXml as character
    Private nTotalIbs as numeric
	
    if ::oTotalTaxByTotvsId:HasProperty(IBS_ESTADUAL) .or. ::oTotalTaxByTotvsId:HasProperty(IBS_MUNICIPAL)
        
        cXml += '<gIBS>' // Tag W36
        cXml +=     self:getXmlTotalgIbsUf()
        cXml +=     self:getXmlTotalgIbsMun()
        cXml +=     '<vIBS>' + lTrim(Str(nTotalIbs,13,2)) + '</vIBS>' 
        cXml +=     '<vCredPres>' + lTrim(str(0, 13, 2)) + '</vCredPres>' 
        cXml +=     '<vCredPresCondSus>' + lTrim(str(0, 13, 2)) + '</vCredPresCondSus>'
        cXml += '</gIBS>'
    endif    

Return cXml

/*/{Protheus.doc}getXmlTotalgIbsUf
Gera as tags de totalização do subgrupo gIBSUF
@type method
@author Evandro Italo
@since 21/05/2025
/*/
Method getXmlTotalgIbsUf() as character Class TaxInformation
    Local cXml as character
    Local nTotalDiferimento as numeric
    Local nTotalDevolucaoTributo as numeric
    Local nTotalTributo as numeric

	if ::oTotalTaxByTotvsId:HasProperty(IBS_ESTADUAL)
        nTotalDiferimento := ::oTotalTaxByTotvsId[IBS_ESTADUAL]['total_diferimento']
        nTotalDevolucaoTributo := 0 //::oTotalTaxByTotvsId[IBS_ESTADUAL]['total_devolucao_tributo']
        nTotalTributo := ::oTotalTaxByTotvsId[IBS_ESTADUAL]['total_tributo']
    endif    

    cXml += '<gIBSUF>' 
    cXml +=     '<vDif>' + lTrim(str( nTotalDiferimento,13,2)) + '</vDif>'
    cXml +=     '<vDevTrib>' + lTrim(str(nTotalDevolucaoTributo, 13, 2)) + '</vDevTrib>' 
    cXml +=     '<vIBSUF>' + lTrim(str(nTotalTributo,13,2)) + '</vIBSUF>' 
    cXml += '</gIBSUF>'

    nTotalIbs += nTotalTributo
Return cXml

/*/{Protheus.doc}getXmlTotalgIbsMun
Gera as tags de totalização do subgrupo gIBSMun
@type method
@author Evandro Italo
@since 21/05/2025
/*/
Method getXmlTotalgIbsMun() as character Class TaxInformation
    Local cXml as character
    Local nTotalDiferimento as numeric
    Local nTotalDevolucaoTributo as numeric
    Local nTotalTributo as numeric

	if ::oTotalTaxByTotvsId:HasProperty(IBS_MUNICIPAL)
        nTotalDiferimento := ::oTotalTaxByTotvsId[IBS_MUNICIPAL]['total_diferimento']
        nTotalDevolucaoTributo := 0 //::oTotalTaxByTotvsId[IBS_MUNICIPAL]['total_devolucao_tributo']
        nTotalTributo := ::oTotalTaxByTotvsId[IBS_MUNICIPAL]['total_tributo']
    endif    

    cXml += '<gIBSMun>' 
    cXml +=     '<vDif>' + lTrim(Str(nTotalDiferimento,13,2)) + '</vDif>' 
    cXml +=     '<vDevTrib>' + lTrim(str(nTotalDevolucaoTributo, 13, 2)) + '</vDevTrib>' //lTrim(str(nTotalDevolucaoTributo, 13, 2))
    cXml +=     '<vIBSMun>' + lTrim(Str(nTotalTributo,13,2)) + '</vIBSMun>' 
    cXml += '</gIBSMun>'

    nTotalIbs += nTotalTributo

Return cXml

/*/{Protheus.doc} getXmlTotalgCbs()
Gera as tags de totalização do subgrupo gCBS
@type method
@author Evandro Italo
@since 21/05/2025
/*/
Method getXmlTotalgCbs() as character Class TaxInformation
    Local cXml as character

	if ::oTotalTaxByTotvsId:HasProperty(CBS_FEDERAL)
        cXml += '<gCBS>' 
        cXml +=     '<vDif>' + lTrim(Str(::oTotalTaxByTotvsId[CBS_FEDERAL]['total_diferimento'],13,2)) + '</vDif>' 
        cXml +=     '<vDevTrib>' + lTrim(str(0, 13, 2)) + '</vDevTrib>' 
        cXml +=     '<vCBS>' + lTrim(Str(::oTotalTaxByTotvsId[CBS_FEDERAL]['total_tributo'],13,2)) +  '</vCBS>' 
        cXml +=     '<vCredPres>' + lTrim(str(0, 13, 2)) + '</vCredPres>'
        cXml +=     '<vCredPresCondSus>' + lTrim(str(0, 13, 2)) + '</vCredPresCondSus>'
        cXml += '</gCBS>'
    endif    

Return cXml

/*/	{Protheus.doc} TaxInformation::getXmlTotalgMono()
	Gera as tags de totalização do subgrupo getXmlTotalgMono
	@type method
	@author Evandro Italo
	@since 21/05/2025
/*/
Method getXmlTotalgMono() as character Class TaxInformation
    Local cXml as character
    Local nTotalIbsMonofasico as numeric
    Local nTotalCbsMonofasico as numeric
    Local nTotalIbsSujeitoRetencao as numeric
    Local nTotalCbsSujeitoRetencao as numeric
    Local nTotalIbsRetidoAnteriormente as numeric
    Local nTotalCbsRetidoAnteriormente as numeric

	if ::oTotalTaxByTotvsId:HasProperty(IBS_MONOFASICO)
        nTotalIbsMonofasico := ::oTotalTaxByTotvsId[IBS_MONOFASICO]['total_tributo']
        nTotalIbsSujeitoRetencao := ::oTotalTaxByTotvsId[IBS_MONOFASICO]['total_tributo']
        nTotalIbsRetidoAnteriormente := ::oTotalTaxByTotvsId[IBS_MONOFASICO]['total_tributo']
    endif
    
    if ::oTotalTaxByTotvsId:HasProperty(CBS_MONOFASICO)
        nTotalCbsMonofasico := ::oTotalTaxByTotvsId[CBS_MONOFASICO]['total_tributo']
        nTotalCbsSujeitoRetencao := ::oTotalTaxByTotvsId[CBS_MONOFASICO]['total_tributo']
        nTotalCbsRetidoAnteriormente := ::oTotalTaxByTotvsId[CBS_MONOFASICO]['total_tributo']
    endif
    
    if ::oTotalTaxByTotvsId:HasProperty(IBS_MONOFASICO) .or. ::oTotalTaxByTotvsId:HasProperty(CBS_MONOFASICO)
        cXml += '<gMono>' 
        cXml +=     '<vIBSMono>'      + lTrim(str(nTotalIbsMonofasico,13,2))          + '</vIBSMono>' 
        cXml +=     '<vCBSMono>'      + lTrim(str(nTotalCbsMonofasico,13,2))          + '</vCBSMono>' 
        cXml +=     '<vIBSMonoReten>' + lTrim(str(nTotalIbsSujeitoRetencao,13,2))     + '</vIBSMonoReten>'  //??? Verificar se procede!
        cXml +=     '<vCBSMonoReten>' + lTrim(str(nTotalCbsSujeitoRetencao,13,2))     + '</vCBSMonoReten>'  //??? Verificar se procede!
        cXml +=     '<vIBSMonoRet>'   + lTrim(str(nTotalIbsRetidoAnteriormente,13,2)) + '</vIBSMonoRet>'    //??? Verificar se procede!
        cXml +=     '<vCBSMonoRet>'   + lTrim(str(nTotalCbsRetidoAnteriormente,13,2)) + '</vCBSMonoRet>'    //??? Verificar se procede!
        cXml += '</gMono>'
    endif    

Return cXml

/*/{Protheus.doc} setTotalBaseIbsCbs
	Soma a base de calculo dos impostos IBS e CBS por item
	@type method
	@author Carlos Eduardo Boy
	@since 21/05/2025
/*/
Method setTotalBaseIbsCbs(cValorBaseItem as numeric) class TaxInformation
    ::nTotalBaseIbsCbs += cValorBaseItem

return
