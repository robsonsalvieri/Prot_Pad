#include 'tlpp-core.th'

/*/{Protheus.doc} RetNfCanc
    Função que filtrar as notas emitidas em períodos anteriores e canceladas 
    no período atual, a fim de facilitar o processamento de estorno ou crédito.
    @type  Function
    @author Bruce Mello
    @since 05/04/2024
    @version version
    @param dData data de inicio do período
    @param cNrLivro número do livro fiscal
    @return oJNfCanc retorna um json com as notas 
    @example
    (examples)
    @see (links_or_references)
    /*/
Function RetNfCanc(dDtIni, cNrLivro, aRegApur)
    Local cQuery              := ""
    Local cQueryTribGen       := ""
    Local cCamposTribGen      := ""
    Local cAliasQry           := ""
    Local cTipoMov            := "S"
    Local aNfCanc             := {}
    Local oMontQry            := Nil
    Local cLivro              := ''
    Local cCodAju             := aRegApur[2]
    Local cTributo            := aRegApur[3]
    Local nValorTributo       := 0
    Local nPos                := 0
    Local cValidTributoLegado := ""    

    IF cNrLivro <> '*'
        cLivro := "AND SFT.FT_NRLIVRO = ? "
    Endif

    iF !Empty(cTributo) 
        cCamposTribGen := ", F2D.F2D_VALOR "

        //BEGINCONTENT var cQueryTribGen 

        cQueryTribGen +=    " INNER JOIN " + RetSqlName("F2D") + " F2D ON ( "
        cQueryTribGen +=                "F2D.F2D_FILIAL = ? "
        cQueryTribGen +=                "AND F2D.F2D_IDREL = SFT.FT_IDTRIB "
        cQueryTribGen +=                "AND F2D.F2D_TRIB = ? "
        cQueryTribGen +=                "AND F2D.D_E_L_E_T_ = ? "
        cQueryTribGen +=     ") "
        
        If Substring(aRegApur[06],1,3) <> 'VAL'
            cCamposTribGen := ", CJ3.CJ3_VLTRIB, CJ3.CJ3_VLISEN, CJ3.CJ3_VLOUTR, CJ3.CJ3_VLNTRI, CJ3.CJ3_VLDIFE "

            cQueryTribGen +=    " INNER JOIN " + RetSqlName("CJ3") + " CJ3 ON ( "
            cQueryTribGen +=                "CJ3.CJ3_FILIAL = ? "
            cQueryTribGen +=                "AND CJ3.CJ3_IDTGEN = SFT.FT_IDTRIB "
            cQueryTribGen +=                "AND CJ3.CJ3_TRIB = ? "
            cQueryTribGen +=                "AND CJ3.D_E_L_E_T_ = ? "
            cQueryTribGen +=     ") "
        Endif
        //ENDCONTENT        
    Else
        cValidTributoLegado := ValidaTributo(aRegApur)
    Endif

    //BEGINCONTENT var cQuery 

        cQuery +="    SELECT"        
        cQuery +="        FT_NFISCAL,"
        cQuery +="        FT_SERIE,"
        cQuery +="        FT_ESPECIE,"
        cQuery +="        FT_CLIEFOR,"
        cQuery +="        FT_TIPOMOV,"
        cQuery +="        FT_ITEM,"
        cQuery +="        FT_FORMUL,"
        cQuery +="        FT_LOJA,"
        cQuery +="        FT_DTCANC,"
        cQuery +="        FT_VALFAC,"
        cQuery +="        FT_VALFET,"
        cQuery +="        FT_VALFMD,"
        cQuery +="        FT_VALFASE,"
        cQuery +="        FT_VALFAB,"
        cQuery +="        FT_UFERMS,"
        cQuery +="        FT_VALFUND,"
        cQuery +="        FT_VALIMA,"
        cQuery +="        FT_VALICM,"
        cQuery +="        FT_ICMSRET,"
        cQuery +="        FT_VALFEEF,"
        cQuery +="        FT_VALPRO,"
        cQuery +="        FT_CRPRST,"
        cQuery +="        FT_CRDPRES,"        
        cQuery +="        FT_CRDPCTR,"
        cQuery +="        FT_VALTST,"
        cQuery +="        FT_ICMSDIF,"
        cQuery +="        FT_ICMSCOM,"
        cQuery +="        FT_VALANTI,"
        cQuery +="        FT_VALFECP,"
        cQuery +="        FT_VFECPST,"
        cQuery +="        FT_VFCPDIF,"
        cQuery +="        FT_ESTCRED,"
        cQuery +="        FT_IDTRIB,"
        cQuery +="        FT_NRLIVRO"
        cQuery +=        cCamposTribGen //%Exp:cCamposTribGen%

        cQuery +="    FROM "
        cQuery +=       RetSqlName("SFT") + " SFT"
        cQuery +=        cQueryTribGen //%Exp:cQueryTribGen%
        cQuery +="    WHERE"
        cQuery +="        SFT.FT_FILIAL = ? "
        cQuery +="        AND SFT.FT_TIPOMOV = ? "
        cQuery +="        AND FT_ENTRADA < ? "
        cQuery +="        AND FT_DTCANC BETWEEN ? AND ? "
        cQuery +="        AND SFT.FT_ESPECIE IN ( ? ) "
        cQuery +=        cLivro //%Exp:cLivro%
        cQuery +=        cValidTributoLegado //%Exp:cValidTributoLegado%
        cQuery +="        AND SFT.D_E_L_E_T_ = ? "
        cQuery +="    ORDER BY"
        cQuery +="        SFT.FT_SERIE, "
        cQuery +="        SFT.FT_NFISCAL "

    //ENDCONTENT

    oMontQry := FwExecStatement():New(cQuery)

    If !Empty(cTributo)
        oMontQry:SetString(nPos += 1, xFilial("F2D"))
        oMontQry:SetString(nPos += 1, cTributo)
        oMontQry:SetString(nPos += 1, ' ')
        
        If Substring(aRegApur[06],1,3) <> 'VAL'
            oMontQry:SetString(nPos += 1, xFilial("CJ3"))
            oMontQry:SetString(nPos += 1, cTributo)
            oMontQry:SetString(nPos += 1, ' ')
        Endif
    Endif

    oMontQry:SetString(nPos += 1, xFilial("SFT"))
    oMontQry:SetString(nPos += 1, cTipoMov)
    oMontQry:SetDate(nPos += 1, Firstday(dDtIni))
    oMontQry:SetDate(nPos += 1, Firstday(dDtIni))
    oMontQry:SetDate(nPos += 1, LastDay(dDtIni))
    oMontQry:SetIn(nPos += 1, ConvModxEsp(Firstday(dDtIni), LastDay(dDtIni), "67#63#57"))
    If cNrLivro <> '*'
        oMontQry:SetString(nPos += 1, cNrLivro)
    Endif
    IF Empty(cTributo)
        oMontQry:SetNumeric(nPos += 1, 0)
    Endif
    oMontQry:SetString(nPos += 1, ' ')
    
  

    cAliasQry := oMontQry:OpenAlias()    

    Do While !(cAliasQry)->(EOF())

        nValorTributo := ProcessaTributo(cAliasQry, aRegApur) // Processa o tributo da nota fiscal

        
        If VerificaRegra(cNrLivro,cCodAju,cTributo,cAliasQry)
            (cAliasQry)->(dbSkip()) 
			Loop
        EndIf

        If nValorTributo > 0
            Aadd(aNfCanc, {;
                (cAliasQry)->FT_NFISCAL,;
                (cAliasQry)->FT_SERIE,;
                (cAliasQry)->FT_CLIEFOR,;
                (cAliasQry)->FT_LOJA,;
                (cAliasQry)->FT_DTCANC,;
                nValorTributo,; // Valor do tributo
                (cAliasQry)->FT_ESPECIE,;
                (cAliasQry)->FT_TIPOMOV,;
                (cAliasQry)->FT_ITEM,;
                (cAliasQry)->FT_FORMUL,;
                0,; // Base 
                0,; // Alíquota            
                })
        Endif

        (cAliasQry)->(DbSkip())
    Enddo
        
    (cAliasQry)->(DbCloseArea())
    oMontQry:Destroy()
    oMontQry := Nil

Return aNfCanc

/*/{Protheus.doc} RetRegApu
    Retornar os Codigos de Lançamento x Regra de Tributo e valor a ser estornado
    @type  Function
    @author Bruce Mello
    @since 08/04/2024
    @version P12.1.2410 
    @return aRegApur retorna um array com os códigos de lançamento
    @example
    (examples)
    @see (links_or_references)
    /*/

Function RetRegApu(dDtIni)
Local cAliasQJW := ""
Local cRegApur  := ""
Local aRegApur  := {}
Local cQuery    := "" // Initialize cQuery variable
Local cDescLanc := ""
Local cDescLanc2 := ""
Local oMontCJW  := Nil
Local cMv_Estado  := SuperGetMv("MV_ESTADO",.F.,"")    

cQuery += "SELECT CJW.CJW_CODTAB, CJW.CJW_CODLAN, CJW.CJW_TRIB, CJW.CJW_REGAPU, CJW.CJW_TRILEG, CJW.CJW_VALOR, CJW.CJW_ORIVLR, CDO.CDO_DESCR, CDO.CDO_DESCR2, CDY.CDY_DESCR "
cQuery += "FROM ? CJV "
cQuery += "INNER JOIN ? CJW ON(CJW.CJW_FILIAL = ? AND CJW.CJW_ID_CAB = CJV.CJV_ID AND CJV.D_E_L_E_T_ = ' ') "
cQuery += "LEFT JOIN ? CDO ON(CDO.CDO_FILIAL = ? AND CDO.CDO_CODAJU = CJW.CJW_CODLAN AND CDO.D_E_L_E_T_ = ' ') "
cQuery += "LEFT JOIN ? CDY ON(CDY.CDY_FILIAL = ? AND CDY.CDY_CODAJU = CJW.CJW_CODLAN AND CDY.D_E_L_E_T_ = ' ') "
cQuery += "WHERE CJV.CJV_FILIAL = ? AND "
cQuery += "? >= CJV.CJV_VIGINI AND ( ? <= CJV.CJV_VIGFIM OR CJV.CJV_VIGFIM =' ') AND "
cQuery += "? >= CJW.CJW_VIGINI AND ( ? <= CJW.CJW_VIGFIM OR CJW.CJW_VIGFIM =' ') AND "
cQuery += "( CJW.CJW_VIGINI >= CDY.CDY_DTINI AND ( CJW.CJW_VIGFIM  <= CDY.CDY_DTFIM OR CDY.CDY_DTFIM =' ') OR (CDY.CDY_DTINI = ' ' OR CDY.CDY_DTINI IS NULL)) AND SUBSTRING(CJW.CJW_CODLAN,1,2) = ? AND "
cQuery += "CJW.D_E_L_E_T_= ' ' "

oMontCJW := FwExecStatement():New(ChangeQuery(cQuery))
oMontCJW:SetUnsafe(1, RetSqlName("CJV"))
oMontCJW:SetUnsafe(2, RetSqlName("CJW"))
oMontCJW:SetString(3, xFilial("CJW"))
oMontCJW:SetUnsafe(4, RetSqlName("CDO"))
oMontCJW:SetString(5, xFilial("CDO"))
oMontCJW:SetUnsafe(6, RetSqlName("CDY"))
oMontCJW:SetString(7, xFilial("CDY"))
oMontCJW:SetString(8, xFilial("CJV"))
oMontCJW:SetDate(9, Firstday(dDtIni))
oMontCJW:SetDate(10, LastDay(dDtIni))
oMontCJW:SetDate(11, Firstday(dDtIni))
oMontCJW:SetDate(12, LastDay(dDtIni))
oMontCJW:SetString(13, cMv_Estado)

cAliasQJW := oMontCJW:OpenAlias()

Do While !(cAliasQJW)->(EOF())
    If  cRegApur <> (cAliasQJW)->CJW_CODTAB + (cAliasQJW)->CJW_CODLAN + (cAliasQJW)->CJW_TRILEG + (cAliasQJW)->CJW_VALOR
        
        cRegApur := (cAliasQJW)->CJW_CODTAB + (cAliasQJW)->CJW_CODLAN + (cAliasQJW)->CJW_TRILEG + (cAliasQJW)->CJW_VALOR 

        If (cAliasQJW)->CJW_CODTAB == '01'
            cDescLanc   := Alltrim((cAliasQJW)->CDO_DESCR)
            cDescLanc2 := Alltrim((cAliasQJW)->CDO_DESCR2)    
        //Else
        //    cDescLanc := Alltrim((cAliasQJW)->CDY_DESCR)
        Endif
        
        Aadd(aRegApur,{;
            (cAliasQJW)->CJW_CODTAB,;
            (cAliasQJW)->CJW_CODLAN,;
            (cAliasQJW)->CJW_TRIB,;
            (cAliasQJW)->CJW_REGAPU,;
            (cAliasQJW)->CJW_TRILEG,;
            (cAliasQJW)->CJW_VALOR,;
            UPPER(FwNoAccent(cDescLanc)),;
            (cAliasQJW)->CJW_ORIVLR,;
            cDescLanc2})

    Endif
    (cAliasQJW)->(DbSkip())
Enddo

(cAliasQJW)->(DbCloseArea())
oMontCJW:Destroy()
oMontCJW := Nil 

Return aRegApur



/*/{Protheus.doc} BuscaLanc
    Função que busca os lançamentos de ajuste e  as notas fiscais de saída canceladas 
    no período atual, a fim de facilitar o processamento de estorno ou crédito.
    Gerando um array com os lançamentos de ajuste e as notas fiscais de saída canceladas.
    para lançar na apuração de ICMS e gravar na tabela CDA.    
    @type  Function
    @author Bruce Mello
    @since 09/04/2024
    @version version
    @param dDtIni data de inicio do período
    @return aCDAIC para compor a apuração do ICMS
    @see (links_or_references)
    /*/
Function BuscaLanc(dDtIni,aRegApur,aCDAIC, cNrLivro)

    Local nCount      := 0
    Local nCountNf    := 0
    Local jLancAjuste := JsonObject():New() 
    Local aNfCanc     := {}
    Local nNfCanc     := 0
    Local nLanc       := 0
    Local cCodLan     := ""
    Local cSerie      := ""
    Local cNumNF      := ""
    Local cEspecie    := ""
    Local cCliefor    := ""
    Local cLoja       := ""
    Local cValIcm     := ""
    Local cTipoMov    := ""
    Local cNumItem    := "" 
    Local oBulk 	  := Nil
    Local cFormul     := ""
    Local cBaseIcm    := ""
    Local cAliquota   := ""
    Local cSeq        := ""
    Local cChave      := ""
    Local lRet        := .F.
    Local cFilCda	  := ""
    Local cDescLanc   := ""
    Local cCodTab     := ""
    Local cAj_REFLEX  := ""
    Local cReflex     := ""
    Local aStruct     := aStructFields({"CDA_FILIAL","CDA_TPMOVI","CDA_ESPECI","CDA_NUMERO","CDA_SERIE","CDA_CLIFOR","CDA_LOJA","CDA_CODLAN","CDA_VALOR","CDA_ORIGEM","CDA_NUMITE","CDA_CALPRO","CDA_FORMUL","CDA_BASE","CDA_ALIQ","CDA_TPLANC","CDA_SDOC","CDA_SEQ"})
    Local lCanUseBulk := FwBulk():CanBulk() as logical

    //Limpa registros da tabela CDA apurados para o período atual
    DelCDA(dDtIni, cNrLivro)

    nLanc   := Len(aRegApur)
    cFilCda	:= xFilial("CDA")
    oBulk   := FwBulk():New(RetSqlName("CDA"),850)
    
    oBulk:setFields(aStruct)
    
    For nCount := 1 To nLanc
        // Trata o código do lançamento de ajuste        
        //busca notas canceladas
        aNfCanc := RetNfCanc(dDtIni, cNrLivro, aRegApur[nCount])
        nNfCanc := Len(aNfCanc)

        cCodTab     := aRegApur[nCount][1]
        cCodLan     := Alltrim(aRegApur[nCount][2])
        cAj_REFLEX  := Substring(cCodLan, 4, 1)

        Do Case
            Case cAj_REFLEX == "0";cReflex := "002"
            Case cAj_REFLEX == "1";cReflex := "003"
            Case cAj_REFLEX == "2";cReflex := "006"
            Case cAj_REFLEX == "3";cReflex := "007"
            Case cAj_REFLEX == "4";cReflex := "012"
            Case cAj_REFLEX == "5";cReflex := "900"
        EndCase

        If cCodTab == '01'
            cDescLanc   := Alltrim(aRegApur[nCount][7])
        //Else
        //    cDescLanc   := Alltrim(aRegApur[nCount][8])
        Endif     

        // Processa as notas fiscais de saída canceladas
        For nCountNf := 1 to nNfCanc

            cNumNF      := aNfCanc[nCountNf][1]
            cSerie      := aNfCanc[nCountNf][2]
            cCliefor    := aNfCanc[nCountNf][3]
            cLoja       := aNfCanc[nCountNf][4]
            cValIcm     := aNfCanc[nCountNf][6]
            cEspecie    := aNfCanc[nCountNf][7]
            cTipoMov    := aNfCanc[nCountNf][8]
            cNumItem    := aNfCanc[nCountNf][9]
            cFormul     := aNfCanc[nCountNf][10]
            cBaseIcm    := aNfCanc[nCountNf][11]
            cAliquota   := aNfCanc[nCountNf][12]

            IF !Empty(aRegApur)
                LancAjuste(cCodLan, cDescLanc, cValIcm, cBaseIcm, @jLancAjuste, cReflex)
            Endif

            IF cChave == cNumNF + cSerie + cCliefor + cLoja + cEspecie + cTipoMov + cNumItem
                cSeq := Soma1(cSeq)            
            Else
                cSeq := '001'
                cChave := cNumNF + cSerie + cCliefor + cLoja + cEspecie + cTipoMov + cNumItem
            EndIf

            if lCanUseBulk
                lRet := oBulk:addData({cFilCda,;
                    cTipoMov    ,; // CDA_TPMOVI
                    cEspecie    ,; // CDA_ESPECI
                    cNumNF      ,; // CDA_NUMERO
                    cSerie      ,; // CDA_SERIE 
                    cCliefor    ,; // CDA_CLIFOR
                    cLoja       ,; // CDA_LOJA
                    cCodlan     ,; // CDA_CODLAN
                    cValIcm     ,; // CDA_VALOR
                    "6"         ,; // CDA_ORIGEM
                    cNumItem    ,; // CDA_NUMITE
                    "1"         ,; // CDA_CALPRO
                    cFormul     ,; // CDA_FORMUL
                    cBaseIcm    ,; // CDA_BASE
                    cAliquota   ,; // CDA_ALIQ
                    "1"         ,; // CDA_TPLANC
                    cSerie      ,; // CDA_SDOC
                    cSeq})         // CDA_SEQ
            EndIf
                         
        Next nCountNf

        FwFreeArray(aNfCanc)
    Next nCount

    // Adiciona os lançamentos de ajuste no array de apuração de ICMS
    adiconaAjuste(aCDAIC, jLancAjuste )  

    if lCanUseBulk
        if lRet
            //Se os dados estiverem corretos, faz o Close do FwBulk para inserir possíveis registros não inseridos e finalizar o bulk.
            lRet := oBulk:Close()
            oBulk:Destroy()
            oBulk := nil
            
        EndIf
    EndIf

    FreeObj(jLancAjuste)
    FwFreeArray(aRegApur)
    FwFreeArray(aStruct)
    
Return aCDAIC

/*/{Protheus.doc} aStructFields
  Função que obtém os campos da SX3 e os transforma em uma estrutura necessária para passar no oBulk:setFields.
  @type Static Function
  @author Rafael P. Gonçalves / Luiz Felipe da Silva Oliveira
  @since 27/09/2024
  @version version
  @param aFields - Array com os campos da SX3
  @return aStruct - Estrutura necessária para passar no oBulk:setFields
  @see (links_or_references)
/*/
Static Function aStructFields(aFields as array)

    Local aStruct := {} as array
    Local nI      := 0 as Integer
    Local cField  := "" as Character
    Local aTamSX3 as array
    Local nLen    := Len(aFields) as Integer

    For nI := 1 To nLen
        cField    := aFields[nI]
        aTamSX3   := TamSX3(cField)
        aAdd(aStruct,{cField, aTamSX3[3], aTamSX3[1], aTamSX3[2]})
    Next nI

    FwFreeArray(aTamSX3)
Return (aStruct)

/**
    Função para realizar criar o array aLancAjuste com as informações do lançamento de ajuste de ICMS
    @param cCodLan Código do lançamento de ajuste.
    @param cDescLanc Descrição do lançamento de ajuste.
    @param cValIcm Valor do ICMS a ser ajustado.
    @param cBaseIcm Base do ICMS a ser ajustado.
    @param jLancAjuste Json com os lançamentos de ajuste.
    @return O array atualizado com o lançamento de ajuste.
    @author Bruce Mello
    @since 08/04/2024
    @version 12.1.2410
 */
Function LancAjuste(cCodLan, cDescLanc, cValIcm, cBaseIcm, jLancAjuste, cReflex)
    
    If jLancAjuste:hasproperty(cCodLan)
        jLancAjuste[cCodLan]["valor"] += cValIcm
        jLancAjuste[cCodLan]["base"]  += cBaseIcm
    Else
        jLancAjuste[cCodLan] := {"codigo": cCodLan,;         //0 - Código do Ajuste
                                 "descricao": cDescLanc,;    //1 - Descrição destino
                                 "valor": cValIcm,;          //6 - Valor Acumulado
                                 "base": cBaseIcm,;          //11 - Base ICM
                                 "Reflexo": cReflex}         //Código de reflexo
    Endif

Return jLancAjuste

/**
    {Protheus.doc} Deleta registros de uma tabela com base em uma condição.
    Esta função deleta registros de uma tabela onde o campo 'D_E_L_E_T_' não está marcado para exclusão e o campo 'CDA_ORIGEM' corresponde a um valor especificado.
    @autor Bruce Mello
    @versao 12.1.2410
    @return Nenhum
 */
Function DelCDA(dDtIni, cNrLivro)
    Local cQuery := ""
    Local dDataIni := CToD(" / / ")
    Local dDataFim := CToD(" / / ")
    Local cLivro := ''
        
    dDataIni := Firstday(dDtIni)
    dDataFim := LastDay(dDtIni)

    IF cNrLivro <> '*'
        cLivro := "AND SFT.FT_NRLIVRO = '" + cNrLivro + "'"
    Endif

    //BEGINCONTENT var cQuery
    cQuery +="    DELETE CDA"
    cQuery +="    FROM " +RetSqlName("CDA")+ " CDA "
    cQuery +="        INNER JOIN " +RetSqlName("SFT") +" SFT ON (" 
    cQuery +=             FWJoinFilial("SFT", "CDA")
    cQuery +="            AND SFT.FT_TIPOMOV = CDA.CDA_TPMOVI"
    cQuery +="            AND SFT.FT_SERIE = CDA.CDA_SERIE"
    cQuery +="            AND SFT.FT_NFISCAL = CDA.CDA_NUMERO"
    cQuery +="            AND SFT.FT_CLIEFOR = CDA.CDA_CLIFOR"
    cQuery +="            AND SFT.FT_LOJA = CDA.CDA_LOJA"
    cQuery +="            AND SFT.FT_ITEM = CDA.CDA_NUMITE"
    cQuery +="            AND SFT.FT_ESPECIE = CDA.CDA_ESPECI                "
    cQuery +="            AND SFT.D_E_L_E_T_ = ' '"
    cQuery +="        )"
    cQuery +="    WHERE"
    cQuery +="        CDA.CDA_FILIAL = " + ValToSQL(xFilial("CDA"))
    cQuery +="        AND CDA.CDA_ORIGEM = '6'"
    cQuery +="        AND SFT.FT_ENTRADA < " +ValToSQL(dDataIni)
    cQuery +="        AND SFT.FT_DTCANC BETWEEN " +ValToSQL(dDataIni) + " AND " +ValToSQL(dDataFim)
    cQuery +=         cLivro
    cQuery +="        AND CDA.D_E_L_E_T_ = ' '"
    //ENDCONTENT

    TCSqlExec(cQuery)

Return



/*/{Protheus.doc} ConvModxEsp
	
	Função para converter modelo de nota em espécie

	@type  Static Function
	@author julia.mota
	@since 24/04/2024
	@version 12.1.2310
	@param dPeridoIni, date, data inicial
	@param dPeriodoFim, date, data final
	@param cModelo, character, modelo da nota	
	@remarn cRet, character, variável de retorno	
/*/
Function ConvModxEsp(dPeridoIni, dPeriodoFim, cModelo)

Local aEspecie := {} as array
Local cAlias   := '' as character
Local cModel   := '' as character
Local cQuery   := '' as character
Local oExec          as object

cQuery := "SELECT SF3.F3_ESPECIE FROM " + RetSqlName("SF3") + " SF3 WHERE SF3.F3_FILIAL = ? AND SF3.F3_DTCANC BETWEEN ? AND ? AND SF3.D_E_L_E_T_ = ? GROUP BY SF3.F3_ESPECIE"

oExec := FwExecStatement():New(cQuery)

oExec:SetString(1, xFilial("SF3"))
oExec:setDate(2, dPeridoIni)
oExec:setDate(3, dPeriodoFim)
oExec:SetString(4, ' ')

cAlias := oExec:OpenAlias()

Do While !(cAlias)->(Eof())
	cModel := AModNot((cAlias)->F3_ESPECIE)
	If cModel $ cModelo
		AAdd(aEspecie, Alltrim((cAlias)->F3_ESPECIE))
	EndIf
	
	( cAlias )->( dbSkip() )

EndDo

( cAlias )->( dbCloseArea() )

oExec:destroy()
oExec := Nil

Return aEspecie

/*/{Protheus.doc} ProcessaTributo
    Função retorna o valor do tributo conforme o tipo de tributo se configurador ou legado e chamada pela função RetNfCanc
    @type  Static Function
    @author Bruce e Rafael
    @since 02/05/2024
    @version 12.1.2410
    @param cAliasQry da função RetNfCanc
    @param aRegApur da função RetRegApu
    @return nValorTributo
/*/
Static Function ProcessaTributo(cAliasQry, aRegApur)

    Local nValorTributo := 0    
    Local cValorGen     := ""
    Local cValTributoLegado := ""

 
        IF aRegApur[08] == '02' .And. (cAliasQry)->FT_IDTRIB = ' ' // Valor do tributo é legado
            cValTributoLegado := aRegApur[05]        

          Do Case
                Case cValTributoLegado == '01' // FABOV - Fundo de Apoio a Bovinocultura de Corte
                    nValorTributo := (cAliasQry)->FT_VALFAC
                Case cValTributoLegado == '02' // FACS - Fundo de Apoio à Cultura da Soja
                    nValorTributo := (cAliasQry)->FT_VALFET
                Case cValTributoLegado == '03' // FAMAD - Fundo de Apoio à Madeira
                    nValorTributo := (cAliasQry)->FT_VALFMD
                Case cValTributoLegado == '04' // FASE/MT - Fundo Mato-Grossense de Apoio à Cultura da Semente
                    nValorTributo := (cAliasQry)->FT_VALFASE
                Case cValTributoLegado == '05' // FETHAB - Fundo Estadual de Transporte e Habitação
                    nValorTributo := (cAliasQry)->FT_VALFAB
                Case cValTributoLegado == '06' // FUNDERSUL - Fundo de Desenvolvimento do Sistema Rodoviário do Estado de Mato Grosso do Sul
                    nValorTributo := (cAliasQry)->FT_UFERMS
                Case cValTributoLegado == '07' // FUNDESA - Fundo de Desenvolvimento e Defesa Sanitária Animal
                    nValorTributo := (cAliasQry)->FT_VALFUND
                Case cValTributoLegado == '08' // IMA/MT - Instituto Mato-Grossense do Algodão
                    nValorTributo := (cAliasQry)->FT_VALIMA
                //Case cValTributoLegado == '09' // TPDP - Taxa de Processamento de Despesa Pública do Estado da Paraíba
                  //  nValorTributo := (cAliasQry)->CJ3_VLTRIB
                Case cValTributoLegado == '10' // ICMS - Imposto sobre Circulação de Mercadorias e Serviços
                    nValorTributo := (cAliasQry)->FT_VALICM
                Case cValTributoLegado == '11' // ICMS-ST - Imposto sobre Circulação de Mercadorias e Serviços - Substituição Tributária
                    nValorTributo := (cAliasQry)->FT_ICMSRET
                Case cValTributoLegado == '12' // FEEF - Fundo Estadual de Equilíbrio Fiscal
                    nValorTributo := (cAliasQry)->FT_VALFEEF
                Case cValTributoLegado == '13' // PROTEG - Fundo de Proteção Social do Estado de Goiás
                    nValorTributo := (cAliasQry)->FT_VALPRO
                Case cValTributoLegado == '14' // Crédito Presumido para Icms
                    nValorTributo := (cAliasQry)->FT_CRDPRES
                Case cValTributoLegado == '15' // Crédito Presumido sobre prestação Serviço de Transporte com ICMS/ST
                     nValorTributo := (cAliasQry)->FT_CRPRST
                //Case cValTributoLegado == '16' // FRTAUT - Frete Autônomo
                  //  nValorTributo := (cAliasQry)->CJ3_VLTRIB
                Case cValTributoLegado == '17' // FRTEMB - Frete Embarcado  
                    nValorTributo := (cAliasQry)->FT_VALTST
                Case cValTributoLegado == '18' // PRESCG - Crédito Presumido pela Carga Tributária
                    nValorTributo := (cAliasQry)->FT_CRDPCTR
                Case cValTributoLegado == '19' // DIFAL - Diferencial de Alíquota
                    nValorTributo := (cAliasQry)->FT_ICMSDIF
                Case cValTributoLegado == '20' // Icms Complementar
                    nValorTributo := (cAliasQry)->FT_ICMSCOM
                Case cValTributoLegado == '21' // Antecipação do ICMS
                    nValorTributo := (cAliasQry)->FT_VALANTI
                Case cValTributoLegado == '22' // FECP ICMS - Fundo de Combate à Pobreza
                    nValorTributo := (cAliasQry)->FT_VALFECP
                Case cValTributoLegado == '23' // FECP ICMS ST - Fundo de Combate à Pobreza ICMS-ST
                    nValorTributo := (cAliasQry)->FT_VFECPST
                Case cValTributoLegado == '24' // FECP ICMS COMPLEMENTAR- Fundo de Combate à Probreza
                    nValorTributo := (cAliasQry)->FT_VFCPDIF
                Case cValTributoLegado == '25' // ESTICM - Estorno Crédito do ICMS
                    nValorTributo := (cAliasQry)->FT_ESTCRED
            EndCase

        Elseif aRegApur[08] == '01' // Valor do tributo é configurador        
            cValorGen   := Substring(aRegApur[06],1,3)

            Do Case 
                Case cValorGen == 'VAL' // Valor do tributo                    
                    nValorTributo := (cAliasQry)->F2D_VALOR                    
                Case cValorGen == 'ISE' // Valor do tributo isento
                    nValorTributo := (cAliasQry)->CJ3_VLISEN
                Case cValorGen == 'OUT' // Valor do tributo outro
                    nValorTributo := (cAliasQry)->CJ3_VLOUTR
                //Case cValorGen == 'NTR' // Valor do tributo não tributado
                //    nValorTributo := (cAliasQry)->CJ3_VLNTRI
                Case cValorGen == 'DIF' // Valor do tributo diferido
                    nValorTributo := (cAliasQry)->CJ3_VLDIFE
            EndCase
        Endif




Return nValorTributo


/*/{Protheus.doc} ValidaTributo
    Função que valida se o campo onde grava o valor do Tributo é maior que zero antes de adcionar na query
    @type  Static Function
    @author Rafael
    @since 02/05/2024
    @version 12.1.2410
    @param cQuery - para ser adicionado na query
    @return nValorTributo
/*/
Static Function ValidaTributo(aRegApur)

Local cValTributoLegado := aRegApur[05]  
Local cQuery := ""      

    Do Case
        Case cValTributoLegado == '01' // FABOV - Fundo de Apoio a Bovinocultura de Corte
            cQuery := " AND FT_VALFAC  > ?"
        Case cValTributoLegado == '02' // FACS - Fundo de Apoio à Cultura da Soja
            cQuery := " AND FT_VALFET  > ?"
        Case cValTributoLegado == '03' // FAMAD - Fundo de Apoio à Madeira
            cQuery := " AND FT_VALFMD  > ?"
        Case cValTributoLegado == '04' // FASE/MT - Fundo Mato-Grossense de Apoio à Cultura da Semente
            cQuery := " AND FT_VALFASE > ?"
        Case cValTributoLegado == '05' // FETHAB - Fundo Estadual de Transporte e Habitação
            cQuery := " AND FT_VALFAB  > ?"
        Case cValTributoLegado == '06' // FUNDERSUL - Fundo de Desenvolvimento do Sistema Rodoviário do Estado de Mato Grosso do Sul
            cQuery := " AND FT_UFERMS  > ?"
        Case cValTributoLegado == '07' // FUNDESA - Fundo de Desenvolvimento e Defesa Sanitária Animal
            cQuery := " AND FT_VALFUND > ?"
        Case cValTributoLegado == '08' // IMA/MT - Instituto Mato-Grossense do Algodão
            cQuery := " AND FT_VALIMA  > ?"
        //Case cValTributoLegado == '09' // TPDP - Taxa de Processamento de Despesa Pública do Estado da Paraíba
            //  nValorTributo := (cAliasQry)->CJ3_VLTRIB
        Case cValTributoLegado == '10' // ICMS - Imposto sobre Circulação de Mercadorias e Serviços
            cQuery := " AND FT_VALICM  > ?"
        Case cValTributoLegado == '11' // ICMS-ST - Imposto sobre Circulação de Mercadorias e Serviços - Substituição Tributária
            cQuery := " AND FT_ICMSRET > ?"
        Case cValTributoLegado == '12' // FEEF - Fundo Estadual de Equilíbrio Fiscal
            cQuery := " AND FT_VALFEEF > ?"
        Case cValTributoLegado == '13' // PROTEG - Fundo de Proteção Social do Estado de Goiás
            cQuery := " AND FT_VALPRO  > ?"
        Case cValTributoLegado == '14' // Crédito Presumido para Icms
            cQuery := " AND FT_CRDPRES  > ?"
        Case cValTributoLegado == '15' // Crédito Presumido sobre prestação Serviço de Transporte com ICMS/ST
            cQuery := " AND FT_CRPRST > ?"
        //Case cValTributoLegado == '16' // FRTAUT - Frete Autônomo
            //  nValorTributo := (cAliasQry)->CJ3_VLTRIB
        Case cValTributoLegado == '17' // FRTEMB - Frete Embarcado  
            cQuery := " AND FT_VALTST  > ?"
        Case cValTributoLegado == '18' // PRESCG - Crédito Presumido pela Carga Tributária
            cQuery := " AND FT_CRDPCTR > ?"
        Case cValTributoLegado == '19' // DIFAL - Diferencial de Alíquota
            cQuery := " AND FT_ICMSDIF > ?"
        Case cValTributoLegado == '20' // Icms Complementar
            cQuery := " AND FT_ICMSCOM > ?"
        Case cValTributoLegado == '21' // Antecipação do ICMS
            cQuery := " AND FT_VALANTI > ?"
        Case cValTributoLegado == '22' // FECP ICMS - Fundo de Combate à Pobreza
            cQuery := " AND FT_VALFECP > ?"
        Case cValTributoLegado == '23' // FECP ICMS ST - Fundo de Combate à Pobreza ICMS-ST
            cQuery := " AND FT_VFECPST > ?"
        Case cValTributoLegado == '24' // FECP ICMS COMPLEMENTAR- Fundo de Combate à Probreza
            cQuery := " AND FT_VFCPDIF > ?"
        Case cValTributoLegado == '25' // ESTICM - Estorno Crédito do ICMS
            cQuery := " AND FT_ESTCRED > ?"
    EndCase
    
Return cQuery


/*/{Protheus.doc} adiconaAjuste
    
    Função que adiciona os ajustes no array aCDAIC

    @type  Static Function
    @author Rafael Oliveira
    @since 08/05/2024
    @version 12.1.2410    
    @param aCDAIC, array, array com os ajustes
    @param jLancAjuste, json, json com os lançamentos de ajuste
    @return aCDAIC, array, array com os ajustes
/*/
Static Function adiconaAjuste(aCDAIC, jLancAjuste )
    Local aAjustes  as array
    Local cChave    as character
    Local cCodLan   as character
    Local cDescLanc as character
    Local cReflexo  as character
    Local cSpace    as character
    Local nCount    as numeric
    Local nValor    as numeric

    aAjustes       := jLancAjuste:GetNames( )
    aPosicaoAjuste := {}
    cSpace         := PadR("", 10)

    if Len(aAjustes) > 0

        For nCount := 1 To Len(aAjustes)
            
            cChave    := aAjustes[nCount]
            cCodLan   := jLancAjuste[cChave]["codigo"]
            cDescLanc := jLancAjuste[cChave]["descricao"]
            nValor    := jLancAjuste[cChave]["valor"]
            cReflexo  := jLancAjuste[cChave]["Reflexo"]

            AADD(aCDAIC,{;
                cReflexo,;
                cSpace,;
                cDescLanc,;
                nValor,;
                "",;
                "",;
                cCodLan,;
                "",;
                .F.,;
                .F.,;
                "",;
                ""})

        Next
    Endif  
Return aCDAIC
/*/{Protheus.doc} adiconaAjuste
    
    Função que verifica as regras de apuração do cancelamento de notas

    @type  Static Function
    @author Jose Willian
    @since 17/12/2024
    @version 12.1.2410    
    @param cTpLan tipo de lançamento configurado no cadastro de regra de apuração
    @param cNrLivro livro informado na rotina de apuração de ICMS
    @param cCodAju código do ajuste 
    @param cTributo código do tributo utilizado 
    @return lRet, retorno logico a depender da regra utilizada 
/*/
Static Function VerificaRegra(cNrLivro,cCodAju,cTributo,cAliasQry)

Local lRet:=.F.

dbSelectArea( "CDO" )
CDO->( dbSetOrder( 1 ) )
If !Empty(cTributo) .And. CDO->( dbSeek( xFilial('CDO') + cCodAju ) )
    If !Empty(CDO->CDO_SUBAP)
        If cNrLivro == '*'
            If CDO->CDO_SUBAP == "1"
                lRet := .T.
            EndIf
        EndIf

        If ( cNrLivro <> '*' .And. cNrLivro <> (cAliasQry)->FT_NRLIVRO ) .Or. (cNrLivro <> '*' .And. CDO->CDO_SUBAP == "3" )
            lRet := .T.
        EndIf
    EndIf 
EndIf 
CDO->(DbcloseArea())
        

return lRet
