#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'totvs.ch'
#include 'mata900cp.ch'

/*/{Protheus.doc} Mata900CP
    Funcoes auxiliares do MATA900 na alteracao dos titulos do contas a pagar
	O Campo E2_EMIS1, apenas sera alterado caso o titulo atenda os seguintes requisitos
	- O Campo E2_LA != S
	- O Campo E2_BAIXA em branco
	- Se o Campo FKF_REINF != 1
	- Campos E1_VENCREA e E1_VENCTO Maior ou igual a nova Data   
    @author Ricardo Henrique de Mello Lima
    @since 13/05/2025
    @version 12.1.2410
	@methodos Usar o Metodo new para instancias a classe , e usar o metodo Destroy para Eliminar a classe
    /*/
NameSpace totvs.protheus.backoffice.fiscal.mata900
Class Mata900CP

	private data cQuery as character
	private data oStatementCP as object
	private data cAliasCP as character
	private data cDocumento as character
	private data cSerie as character
	private data cCliFor as character
	private data cLojaCliFor as character
	Private data cChave as character
	private data dNewData as date
	private data cMenssagem as character
	private data lMenssagem as logical
	Private data cDbMs as character

	public method New() constructor
	protected method Initi(dNewData as date, cDocumento as character , cSerie as character , cCliFor as character, cLojaCliFor as character)
	public method Destroy()
	private method QueryFinanceiroCP()
	private method ValidaTituloFinanceiroCP()
	private method UpdateTituloFinanceiroCP()
	private method EnviaMensagem()
	private method SetNewData(dValue as date)
	private method SetDoc(cValue as character)
	private method SetSerie(cValue as character)
	private method SetCliFor(cValue as character)
	private method SetLojaCliFor(cValue as character)

EndClass

/*/{Protheus.doc} New
    Metodo Inicializador da Classe
    @author Ricardo Henrique de Mello Lima
    @since 13/05/2025
    @version 12.1.2410
    /*/
Method New() class Mata900CP
	self:cDbMs := TcGetDb()
	self:cQuery := ""
	self:oStatementCP := FWExecStatement():New()
	self:cAliasCP := ""
	self:dNewData := date()
	self:cDocumento := ""
	self:cSerie := ""
	self:cCliFor := ""
	self:cLojaCliFor := ""
	self:cChave := "E2_FILIAL || '|' || E2_PREFIXO || '|' || E2_NUM || '|' || E2_PARCELA || '|' || E2_TIPO || '|' || E2_FORNECE || '|' || E2_LOJA " // oracle
	if self:cDbMs $ "MSSQL/POSTGRES"
		self:cChave := "CONCAT(E2_FILIAL, '|', E2_PREFIXO, '|', E2_NUM, '|', E2_PARCELA, '|', E2_TIPO, '|', E2_FORNECE, '|', E2_LOJA) "
	endif
	self:cMenssagem := ""
	self:lMenssagem := .F.
Return

/*/{Protheus.doc} Initi
	metodo que inicializa o processo de Atualicao dos Titulos
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	@return lRetorno, logical, Retorna true caso o titulo possa ser alterado e falso caso contrario
	/*/
Method Initi(dNewData as date, cDocumento as character , cSerie as character , cCliFor as character, cLojaCliFor as character) class Mata900CP

	Local lRetorno := .T. as logical

	lRetorno := !(Empty(dNewData) .or. Alltrim(cDocumento) == "" .or. Alltrim(cSerie) == "" .or. Alltrim(cCliFor) == "" .or. Alltrim(cLojaCliFor) == "")

	if lRetorno
		self:SetNewData(dNewData)
		self:SetDoc(cDocumento)
		self:SetSerie(cSerie)
		self:SetCliFor(cCliFor)
		self:SetLojaCliFor(cLojaCliFor)

		self:QueryFinanceiroCP()
		self:ValidaTituloFinanceiroCP()
		if !self:lMenssagem
			self:UpdateTituloFinanceiroCP()
		else
			self:EnviaMensagem()
		endif
	endif
Return

/*/{Protheus.doc} SetDoc
	Seta o documento fiscal a ser procurado
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	/*/
Method SetDoc(cValue as character) class Mata900CP
	self:cDocumento := cValue
Return

/*/{Protheus.doc} SetSerie
	Seta a serie do documento fiscal
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	/*/
Method SetSerie(cValue as character) class Mata900CP
	self:cSerie := cValue
Return

/*/{Protheus.doc} SetCliFor
	Seta o Cliente / fornecedor do Documento Fiscal
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	/*/
Method SetCliFor(cValue as character) class Mata900CP
	self:cCliFor := cValue
Return

/*/{Protheus.doc} SetLojaCliFor
	Seta a loja do Cliente / fornecedor 
	@author Ricardo henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	/*/
Method SetLojaCliFor(cValue as character) class Mata900CP
	self:cLojaCliFor := cValue
Return

/*/{Protheus.doc} SetNewData
	Seta a nova data 
	@author Ricardo Henrique de Mello Lima
	@since 15/05/2025
	@version 12.1.2410
	/*/
Method SetNewData(dValue as date) class Mata900CP
	self:dNewData := dValue
Return


/*/{Protheus.doc} ValidaTituloFinanceiroCP
    Valida se o Campo E2_EMIS1 pode ser alterado conforme regra de negocio 
    regras definidas com financeiro na ISSUE DSERFISE-10892, caso o documento
	tenha multiplos titulos basta apenas um titulo nao satisfazer as validacoes
	a rotina de alteracao de Data de Entrada sao abortada
    @author Ricardo Henrique de Mello Lima
    @since 13/05/2025
    @version 12.1.2410
    /*/
Method ValidaTituloFinanceiroCP() class Mata900CP

	Local cNewData := "" as character

	cNewData := DToS(self:dNewData)

	(self:cAliasCP)->(DBGOTOP())
	if (self:cAliasCP)->(BOF()) .and. (self:cAliasCP)->(EOF())
		self:lMenssagem := .F.
	else
		(self:cAliasCP)->(DBGOTOP())
		while (self:cAliasCP)->(!EOF())

			if !(Empty((self:cAliasCP)->E2_BAIXA) .and. (self:cAliasCP)->E2_VENCREA >= cNewData .and. (self:cAliasCP)->E2_VENCTO >= cNewData ;
					.and. (self:cAliasCP)->FKF_REINF != '1' .and. (self:cAliasCP)->E2_LA != "S")
				self:lMenssagem := .T.
				self:cMenssagem := STR0002+CHR(13)+CHR(10)+STR0003+CHR(13)+CHR(10)+self:cDocumento+" "+self:cSerie
				exit
			endif
			(self:cAliasCP)->(Dbskip())
		enddo
	endif
Return

/*/{Protheus.doc} UpdateTituloFinanceiroCP
    Executa o Update no titulo do contas a pagar no Campo E1_EMIS1
    @author Ricardo Henrique de Mello Lima
    @since 13/05/2025
    @version 12.1.2410
    /*/
Method UpdateTituloFinanceiroCP() class Mata900CP

	Local cSE2FILIAL := "" as character
	Local cSE2TITPAI := "" as character

	(self:cAliasCP)->(DBGOTOP())

	While (self:cAliasCP)->(!EOF())

		DBSELECTAREA("SE2")
		SE2->(DBGOTO((self:cAliasCP)->R_E_C_N_O_))
		SE2->(RecLock("SE2",.F.))
		SE2->E2_EMIS1 = self:dNewData
		cSE2FILIAL := SE2->E2_FILIAL
		cSE2TITPAI := SE2->(E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO+E2_FORNECE+E2_LOJA)
		SE2->(MSUNLOCK())
		SE2->(DBCLOSEAREA())

		DBSELECTAREA("SE2")
		SE2->(DBSETORDER(17))
		If SE2->(DbSeek(cSE2FILIAL+cSE2TITPAI))
				RecLock("SE2",.F.)
				SE2->E2_EMIS1 := self:dNewData
				SE2->(MsUnlock())			
		EndIf
		SE2->(DBCLOSEAREA())
		(self:cAliasCP)->(DBSKIP())

	enddo

Return

/*/{Protheus.doc} QueryFinanceiroCP
    Monta a Query para validacao da operacao
    @author Ricardo Henrique de Mello Lima
    @since 13/05/2025
    @version 12.1.2410
    /*/
Method QueryFinanceiroCP() class Mata900CP

	self:cQuery := " WITH TITULOCP AS ("
	self:cQuery += "     SELECT"
	self:cQuery += "         SE2.R_E_C_N_O_,"
	self:cQuery += "         SE2.E2_FILIAL,"
	self:cQuery += "         SE2.E2_PREFIXO,"
	self:cQuery += "         SE2.E2_NUM,"
	self:cQuery += "         SE2.E2_PARCELA,"
	self:cQuery += "         SE2.E2_TIPO,"
	self:cQuery += "         SE2.E2_FORNECE,"
	self:cQuery += "         SE2.E2_LOJA,"
	self:cQuery += "         SE2.E2_VALOR,"
	self:cQuery += "         SE2.E2_SALDO,"
	self:cQuery += "         SE2.E2_VENCTO,"
	self:cQuery += "         SE2.E2_VENCREA,"
	self:cQuery += "         SE2.E2_TITPAI,"
	self:cQuery += "         SE2.E2_BAIXA,"
	self:cQuery += "         SE2.E2_LA,"
	self:cQuery += "         ? AS CHAVE"
	self:cQuery += "     FROM"
	self:cQuery += "         "+RetSqlName("SE2")+" SE2"
	self:cQuery += "     WHERE"
	self:cQuery += "         SE2.E2_FILIAL = ?"
	self:cQuery += "         AND SE2.E2_PREFIXO = ?"
	self:cQuery += "         AND SE2.E2_NUM = ?"
	self:cQuery += "         AND SE2.E2_FORNECE = ?"
	self:cQuery += "         AND SE2.E2_LOJA = ?"
	self:cQuery += "         AND SE2.D_E_L_E_T_ = ?"
	self:cQuery += " )"
	self:cQuery += " SELECT"
	self:cQuery += "     TITCP.R_E_C_N_O_,"
	self:cQuery += "     TITCP.E2_FILIAL,"
	self:cQuery += "     TITCP.E2_PREFIXO,"
	self:cQuery += "     TITCP.E2_NUM,"
	self:cQuery += "     TITCP.E2_PARCELA,"
	self:cQuery += "     TITCP.E2_TIPO,"
	self:cQuery += "     TITCP.E2_FORNECE,"
	self:cQuery += "     TITCP.E2_LOJA,"
	self:cQuery += "     TITCP.E2_VALOR,"
	self:cQuery += "     TITCP.E2_SALDO,"
	self:cQuery += "     TITCP.E2_VENCTO,"
	self:cQuery += "     TITCP.E2_VENCREA,"
	self:cQuery += "     TITCP.E2_TITPAI,"
	self:cQuery += "     TITCP.E2_BAIXA,"
	self:cQuery += "     TITCP.E2_LA,"
	self:cQuery += "     TITCP.CHAVE,"
	self:cQuery += "     FK7.FK7_IDDOC,"
	self:cQuery += "     FKF.FKF_REINF"
	self:cQuery += " FROM"
	self:cQuery += "     TITULOCP TITCP"
	self:cQuery += "     LEFT JOIN "+RetSqlName("FK7")+" FK7 ON ? = TITCP.E2_FILIAL AND FK7.FK7_CHAVE = TITCP.CHAVE AND FK7.D_E_L_E_T_ = ?"
	self:cQuery += "     LEFT JOIN "+RetSqlName("FKF")+" FKF ON ? = ? AND FKF.FKF_IDDOC = FK7.FK7_IDDOC AND FKF.D_E_L_E_T_ = ?"

	self:oStatementCP:SetQuery(self:cQuery)

	self:oStatementCP:SetUnSafe(1,self:cChave)
	self:oStatementCP:SetString(2,FWxFilial("SE2"))
	self:oStatementCP:SetString(3,self:cSerie)
	self:oStatementCP:SetString(4,self:cDocumento)
	self:oStatementCP:SetString(5,self:cCliFor)
	self:oStatementCP:SetString(6,self:cLojaCliFor)
	self:oStatementCP:SetString(7,' ')
	self:oStatementCP:SetString(8,FWxFilial("FK7"))
	self:oStatementCP:SetString(9,' ')
	self:oStatementCP:SetString(10,FWxFilial("FKF"))
	self:oStatementCP:SetString(11,FWxFilial("FK7"))
	self:oStatementCP:SetString(12,' ')

	self:cAliasCP := self:oStatementCP:OpenAlias()

Return

/*/{Protheus.doc} EnviaMensagem
	Envia Mensagem na Tela caso nao possa fazer a alteracao da data quando 
	o titulo estiver um uma condicao que nao possa ser alterado o campo E1_EMIS1
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	@param nValue, logical, False = nao pode proseguir com a alteracao 
	@return lValue, .F., Nao permite alterar a data de entrada
	/*/
Method EnviaMensagem() class Mata900CP
	FWAlertInfo(self:cMenssagem, STR0001)
Return

/*/{Protheus.doc} Destructors
	Metodo responsavel por fechar os objetos da classe evitando memory leak
	@author Ricardo Henrique de Mello Lima
	@since 14/05/2025
	@version 12.1.2410
	/*/
Method Destroy() class Mata900CP

	self:oStatementCP:Destroy()
	FreeObj(self:oStatementCP)
	(self:cAliasCP)->(DbCloseArea())

Return
