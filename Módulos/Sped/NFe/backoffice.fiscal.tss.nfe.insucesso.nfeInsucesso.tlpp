#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'xmlxfun.ch'
#include 'fileio.ch'
#include 'nfeinsu.ch'

/*/{Protheus.doc} NfeInsucesso
    Evento de Insucesso na Entrega da NFE
    @author Ricrado Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
namespace backoffice.fiscal.tss.nfe.insucesso
Class NfeInsucesso

	Public Data aParametros as array
	Public Data cVersaoTSS as character
	Public Data cIdEntidade as character
	Public Data oWorkAreaMain as object
	Public Data oOkMonitor as object
	Public Data oNoMonitor as object
	Public Data aBrowserMonitor as array
	Public Data oBrowserMonitor as object
	Public Data oFont as object

	Private Data cQueryDocumentos as character
	Private Data cAliasDocumentos as character
	Private Data aTipoEvento as array
	Private Data cEventoSelecionado as character
	Private Data cEventoTransmissao as character
	Private Data cEventoCancelamento as character
	Private Data cEventoMonitor as character
	Private Data aRetEvento as array
	Private Data aMotivo as array
	Private Data lThemeDark as logical
	Private Data cLatitude as character
	Private Data cLongitude as character
	Private Data cMsgValidaTransmissao as character
	Private Data cHashComprovante as character
	Public Data cMotivo as character
	Public Data cTextoMotivo as character
	Public Data nTentativa as numeric
	Public Data cPathImagem as character
	Public Data dGetDataEntrega as date
	public Data Titulo as character

	Private Data oWorkAreaContainer as object
	Private Data oPanelDadosMonitor as object
	Private Data oPanelDadosNota as object
	Private Data oTSayTransacao as object
	Private Data oComboEvento as object
	Private Data oTSayMotivo as object
	Private Data oComboMotivo as object
	Private Data oTMotivo as object
	Private Data oTSayTentativa as object
	Private Data oGetTentativa as object
	Private Data oTSayImagem as object
	Private Data oGetImagem as object
	Private Data oTSayTpMotivo as object
	Private Data oTButtonImage as object
	Private Data oTGetDataEntrega as object
	Private Data oTSayGetDataEntrega as object

	Public Method New() constructor

	Private Method MontaParametros()
	Private Method ProcessaDados()
	Private Method QueryBuscaNF()
	Private Method CallRetornaEvento(cChave as character, cEvento as character) as array
	Private Method SetArrayMonitor(aValue as array, cEvento as character)
	Private Method ActionCampos()
	Private Method AnalisaXML()
	Private Method ThemaSistema()
	Private Method ValidaTransmissao() as logical
	Private Method HashComprovante() as logical
	Private Method LimpaCampos()

	Protected Method MostraMenssagem(cTitulo as character, cSubTitulo as character, cMenssagem as character)
	Protected Method CallEnviaEvento(cValue as character)
	Protected Method ValidaFormulario() as logical
	Protected Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical)
	Protected Method MontaBrowserMain()
	Protected Method MontaBrowserMonitor()
	Protected Method VisualizaDocumento()
	Protected Method Transmissao()
	Protected Method SetTrocaEvento(cValue as character)
	Protected Method TrocarEvento()
	Protected Method UpdateMonitor(cEvento as character, cChaveDoc as character)
	Protected Method TransmitirEvento()
	Protected Method IncluirEvento()
	Protected Method CancelarEvento()
	Protected method RefreshMonitor()

EndClass

/*/{Protheus.doc} New
    Método Contrutor da Classe
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method New() class NfeInsucesso

	::Titulo := STR0002 //"Insucesso na Entrega da NFe"
	::aParametros := {}
	::cQueryDocumentos := ""
	::cAliasDocumentos := ""

	::cEventoTransmissao := STR0003 //"110192"
	::cEventoCancelamento := STR0004 //"110193"
	::cEventoMonitor := STR0005 //"110000"
	::aRetEvento := {}
	::aMotivo := {}
	::lThemeDark := .F.
	::cLatitude := ""
	::cLongitude := ""
	::cMotivo := ""
	::cTextoMotivo := ""
	::nTentativa := 0
	::cPathImagem := space(500)
	::dGetDataEntrega := date()
	::cEventoSelecionado := ""
	::aBrowserMonitor := {}
	::cMsgValidaTransmissao := ""
	::cHashComprovante := ""

	::oWorkAreaMain := nil
	::oWorkAreaContainer := nil
	::oPanelDadosMonitor := nil
	::oPanelDadosNota := nil
	::oTSayTransacao := nil
	::oComboEvento := nil
	::oTSayMotivo := nil
	::oComboMotivo := nil
	::oTMotivo := nil
	::oTSayTentativa := nil
	::oGetTentativa := nil
	::oTSayImagem := nil
	::oGetImagem := nil
	::oTSayTpMotivo := nil
	::oTButtonImage := nil
	::oTGetDataEntrega := nil
	::oTSayGetDataEntrega := nil

	::oOkMonitor := nil
	::oNoMonitor := nil
	::oBrowserMonitor := nil
	::oFont := TFont():New(,,-12,.T.,.F.,,,,,)

Return

/*/{Protheus.doc} Active
    Metodo que Inicializa O Objeto
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method Active(cIdEnt as character, cVersaoTSS as character, lUsaColab as logical) class NfeInsucesso

	::cIdEntidade := cIdEnt
	::cVersaoTSS := cVersaoTSS
	::aTipoEvento := {STR0006,STR0007,STR0008} // {'110750-Transmitir Evento','110751-Cancelar Evento','110000-Monitor'}

	aAdd(::aMotivo,STR0009) //"1 - Recebedor não encontrado"
	aAdd(::aMotivo,STR0010) //"2 - Recusa do recebedor"
	aAdd(::aMotivo,STR0011) //3 - Endereço inexistente"
	aAdd(::aMotivo,STR0012) //"4 - Outros (exige informar justificativa)"

	::ThemaSistema()
	::MontaParametros()

Return

/*/{Protheus.doc} MontaParametros
    Monta a Tela de Parametros Inicial do Evento
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method MontaParametros() class NfeInsucesso

	Local nTamCli := FWSX3Util():GetFieldStruct("A1_COD")[3]
	Local nTamLojaCli := FWSX3Util():GetFieldStruct("A1_LOJA")[3]
	Local nTamDoc := FWSX3Util():GetFieldStruct("F2_DOC")[3]
	Local nTamSerie := FWSX3Util():GetFieldStruct("F2_SERIE")[3]
	Local aPergunte := {} as array

	aAdd(aPergunte,{1,STR0014,space(nTamCli),"",".T.",, ".T.",50,.T.}) // "Cliente
	aAdd(aPergunte,{1,STR0015,Space(nTamLojaCli),"",".T.","",".T.",30,.T.}) // "Loja"
	aAdd(aPergunte,{1,STR0016,space(nTamSerie),"",".T.","",".T.",30,.T.}) // "Série da Nota Fiscal"
	aAdd(aPergunte,{1,STR0017,Space(nTamDoc),"",".T.","",".T.",30,.T.}) // "Nota Fiscal Inicial"
	aAdd(aPergunte,{1,STR0018,space(nTamDoc),"",".T.","",".T.",30,.T.}) // "Nota Fiscal Final"
	aAdd(aPergunte,{1,STR0019,date(),"",".T.","",".T.",80,.T.}) // "Dt. de emissão inicial"
	aAdd(aPergunte,{1,STR0020,date(),"",".T.","",".T.",80,.T.}) // "Dt. de emissão final"

	if ParamBox(aPergunte, ::cIdEntidade + " - " + ::cVersaoTSS,::aParametros,{|| ::ValidaFormulario()},,,,,,::Titulo+alltrim(FWGrpCompany())+alltrim(FWCodFil()),.T.,.T.)
		::ProcessaDados()
	endif

Return

/*/{Protheus.doc} ValidaFormulario
    Valida o Formulario de Parametros
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method ValidaFormulario() as logical class NfeInsucesso

	Local lValid := .T. as logical

	if ::aParametros[4] > ::aParametros[5]
		lValid := .F.
	endif

	if ::aParametros[6] > ::aParametros[7]
		lValid := .F.
	endif

	if !lValid
		FWAlertInfo(STR0013,::Titulo) // 'Revise os Parâmetros Para Prosseguir' 'Insucesso Na Enttrega'
	endif

Return lValid

/*/{Protheus.doc} ProcessaDados
    ProcessaConforme Parametros Informados para Montagem do Browser de Titulos
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method ProcessaDados() class NfeInsucesso

	::QueryBuscaNF()

	(::cAliasDocumentos)->(DBGOTOP())
	if (::cAliasDocumentos)->(!EOF()) .AND. (::cAliasDocumentos)->(!BOF())
		(::cAliasDocumentos)->(DBCLOSEAREA())
		::MontaBrowserMain()
	else
		(::cAliasDocumentos)->(DBCLOSEAREA())
		FWAlertInfo(STR0021,::Titulo) // "Não existem registros relacionados ao filtro selecionado."  "Insucesso na Entrega da NFe"
	endif

Return

/*/{Protheus.doc} QueryBuscaNF
    Query que Busca os documentos a serem listados no FWBrowser
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method QueryBuscaNF() class NfeInsucesso

	Local oStatement := nil as object

	::cQueryDocumentos := "SELECT SF2.F2_DOC,SF2.F2_EMISSAO,SF2.F2_SERIE,SF2.F2_CLIENTE,SF2.F2_LOJA,SF2.F2_CHVNFE,"
	::cQueryDocumentos += " SF2.R_E_C_N_O_ AS RECNOTA,SA1.A1_NOME,SA1.A1_PESSOA,SA1.A1_CGC,SA1.R_E_C_N_O_ AS RECCLIFOR"
	::cQueryDocumentos += " FROM "+RetSqlName("SF2")+" SF2"
	::cQueryDocumentos += " LEFT JOIN "+RetSqlName("SA1")+" SA1 ON SA1.A1_COD = SF2.F2_CLIENTE AND SA1.A1_LOJA = SF2.F2_LOJA AND SA1.A1_FILIAL = '"+FWxFilial("SA1")+"' AND SA1.D_E_L_E_T_ = ?"
	::cQueryDocumentos += " WHERE SF2.D_E_L_E_T_ = ?"
	::cQueryDocumentos += " AND SF2.F2_FILIAL = ?"
	::cQueryDocumentos += " AND SF2.F2_EMISSAO BETWEEN ? AND ?"
	::cQueryDocumentos += " AND SF2.F2_DOC BETWEEN ? AND ?"
	::cQueryDocumentos += " AND SF2.F2_SERIE = ?"
	::cQueryDocumentos += " AND SF2.F2_CLIENTE = ?"
	::cQueryDocumentos += " AND SF2.F2_LOJA = ?"
	::cQueryDocumentos += " AND SF2.F2_TIPO = ?"
	::cQueryDocumentos += " AND SF2.F2_CHVNFE <> ?"
	::cQueryDocumentos += " ORDER BY F2_DOC ASC"

	oStatement := FWExecStatement():New(ChangeQuery(::cQueryDocumentos))
	oStatement:SetString(1,"")
	oStatement:SetString(2,"")
	oStatement:SetString(3,FWXFilial("SF2"))
	oStatement:SetDate(4,::aParametros[6])
	oStatement:SetDate(5,::aParametros[7])
	oStatement:SetString(6,::aParametros[4])
	oStatement:SetString(7,::aParametros[5])
	oStatement:SetString(8,::aParametros[3])
	oStatement:SetString(9,::aParametros[1])
	oStatement:SetString(10,::aParametros[2])
	oStatement:SetString(11,"N")
	oStatement:SetString(12,"")
	
	::cQueryDocumentos := oStatement:GetFixQuery()
	::cAliasDocumentos := oStatement:OpenAlias()

	oStatement:Destroy()
	FreeObj(oStatement)

Return

/*/{Protheus.doc} MontaBrowser
    Monta o Browser com o Filtro selecionado para seleção de Documentos a serem eviado o Evento
    @author Ricardo Henrique de Mello Lima
    @since 28/10/2024
    @version 12.1.2410
    /*/
Method MontaBrowserMain() class NfeInsucesso

	Local oDlgMain := nil as object
	Local oColumns := nil as object
	Local oBrowseNotas := nil as object
	Local nContFlds := 0 as Numeric
	Local aColumns := {} as Array
	Local aFields := {} as Array
	Local aStructCampo := {} as Array
	Local aSize := {} as array
	Local cTitle := "" as character
	Local aSeek := {} as array
	Local aIndex := {} as array

	aSize := MsAdvSize()
	cTitle := ::Titulo + " - " + ::cIdEntidade + " - " + ::cVersaoTSS // Insucesso na Entrega

	aAdd( aSeek, { "Documento" , {{"","C", GetSX3Cache( "F2_DOC","X3_TAMANHO"), 0 , "Documento",,}}} )
	aAdd( aSeek, { "Chave da NFE" , {{"","C", GetSX3Cache( "F2_CHVNFE","X3_TAMANHO") , 0 , "Chave da NFE",,}}} )
	aAdd( aIndex, "F2_DOC" )
	aAdd( aIndex, "F2_CHVNFE" )

	DEFINE MSDIALOG oDlgMain TITLE cTitle FROM aSize[7],0 TO aSize[6],aSize[5] OF oMainWnd PIXEL

	oBrowseNotas := FWFormBrowse():New()
	oBrowseNotas:SetDataQuery()
	oBrowseNotas:SetOwner(oDlgMain)
	oBrowseNotas:SetAlias(::cAliasDocumentos)
	oBrowseNotas:SetDescription(cTitle)
	oBrowseNotas:SetQuery(::cQueryDocumentos)
	oBrowseNotas:SetQueryIndex(aIndex)
	oBrowseNotas:SetSeek(,aSeek)
	oBrowseNotas:SetMenuDef('')
	oBrowseNotas:DisableDetails()

	aColumns := {}
	aFields := {}

	aAdd(aFields,{"F2_DOC",STR0022}) // "Documento"
	aAdd(aFields,{"F2_SERIE",STR0023})  // "Serie"
	aAdd(aFields,{"F2_CLIENTE",STR0027}) // "Cliente/Fornecedor"
	aAdd(aFields,{"F2_LOJA",STR0024}) // "Loja"
	aAdd(aFields,{"F2_CHVNFE",STR0025}) // "Chave da Nfe"
	aAdd(aFields,{"F2_EMISSAO",STR0026}) // "Data de Emissão"

	For nContFlds := 1 To Len(aFields)

		aStructCampo := FWSX3Util():GetFieldStruct(aFields[nContFlds,1])
		oColumns :=  FWBrwColumn():New()
		if Alltrim(aStructCampo[2]) == "D"
			oColumns:SetData(&("{|| DtoC(StoD("+aFields[nContFlds,1]+"))}"))
		else
			oColumns:SetData(&("{|| "+aFields[nContFlds,1]+" }"))
		endif
		oColumns:SetTitle( aFields[nContFlds,2])
		oColumns:SetSize(aStructCampo[3])
		oColumns:SetType(aStructCampo[2])
		oColumns:SetID(aFields[nContFlds])
		oColumns:SetPicture(Alltrim(GetSX3Cache(aFields[nContFlds,1],"X3_PICTURE")))

		aAdd(aColumns,oColumns)
	Next

	oBrowseNotas:AddButton(STR0028,{||oDlgMain:End()},,,,.F.) // Sair
	if !lUsaColab
		oBrowseNotas:AddButton(STR0029,{|| SpedNFeCfg()},,,,.F.) // "Wiz.Config."
	endif
	oBrowseNotas:AddButton(STR0030,{|| SpedCCePar()},,,,.F.) // "Parametros"
	oBrowseNotas:AddButton(STR0031,{|| ::Transmissao() },,,,.F.) // "Transmissao"
	oBrowseNotas:AddButton(STR0032,{||::VisualizaDocumento()},,,,.F.) // "Visualiza Doc."
	oBrowseNotas:SetColumns(aColumns)
	oBrowseNotas:Activate()

	ACTIVATE MSDIALOG oDlgMain

Return

/*/{Protheus.doc} VisualizaDocumento
    (long_description)
    @author Ricardo Henrique de Mello Lima
    @since 29/10/2024
    @version 12.1.2410
    /*/
Method VisualizaDocumento() class NfeInsucesso

	DBSELECTAREA("SF2")
	SF2->(DBGOTO((::cAliasDocumentos)->RECNOTA))
	Mc090Visual("SF2",SF2->(RECNO()),1)

Return

/*/{Protheus.doc} Transmissao
    Tela Principal de Transmissao de Eventos Monitoramento / Cancelamento / Tramissao
    @author Ricardo Henrique de Mello Lima
    @since 29/10/2024
    @version 12.1.2410
   /*/
Method Transmissao() class NfeInsucesso

	Local cTitulo := "" as character
	Local cEvento := ::cEventoMonitor as character
	Local aButtons := {} as array
	Local cCodCliente := "" as character
	Local cLoja := "" as character
	Local cNomeCliente := "" as character
	Local cCnpjClinete := "" as character
	Local cTipoCli := "" as character
	Local cNumDoc := "" as character
	Local cDocSerie := "" as character
	Local dDocEmissao := date() as date
	Local cChaveDoc := "" as character
	Local cTipoCliDesc := "" as character
	Local lHasButton := .T. as logical
	Local cMaskCNPJ := "@R 999.999.999-99" as character
	Local lValBuild := GetBuild() < "7.00.240223P-20250329" as logical
	Local oSize := nil as object
	Local oGrpDadosNota := nil as object
	Local oGrpNotas := nil as object
	Local oGrpBrowser := nil as object
	Local oSayCodCli := nil as object
	Local oSayNomeCli := nil as object
	Local oSayPessoa := nil as object
	Local oSayNF := nil as object
	Local oSayChave := nil as object

	cTitulo := ::Titulo + STR0033 + ::cIdEntidade + STR0034 + ::cVersaoTSS //" - Entidade " " - Versão "

	cCodCliente := (::cAliasDocumentos)->F2_CLIENTE
	cLoja := (::cAliasDocumentos)->F2_LOJA
	cNomeCliente := (::cAliasDocumentos)->A1_NOME
	cCnpjClinete := (::cAliasDocumentos)->A1_CGC
	cTipoCli := (::cAliasDocumentos)->A1_PESSOA
	cNumDoc := (::cAliasDocumentos)->F2_DOC
	cDocSerie := (::cAliasDocumentos)->F2_SERIE
	dDocEmissao := Stod((::cAliasDocumentos)->F2_EMISSAO)
	cChaveDoc := (::cAliasDocumentos)->F2_CHVNFE

	if cTipoCli == 'F'
		cTipoCliDesc := STR0035 // 'Pessoa Fisica'
	elseif cTipoCli == 'J'
		cTipoCliDesc:= STR0036 // 'Pessoa Juridica'
		cMaskCNPJ := "@R! NN.NNN.NNN/NNNN-99"
	endif

	oSize := FwDefSize():New(.T.,.F.,,)
	oSize:AddObject( "MAIN",100,100,.T.,.T. ) // Totalmente dimensionavel
	oSize:lProp 	:= .T. // Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3
	oSize:Process() // Dispara os calculos

	::oWorkAreaMain := FWDialogModal():New()
	::oWorkAreaMain:SetEscClose(.T.)
	::oWorkAreaMain:SetTitle(cTitulo)
	::oWorkAreaMain:setPos(oSize:aWindSize[1] / 2,oSize:aWindSize[2] / 2 )
	::oWorkAreaMain:setSize(oSize:aWindSize[3] / 2,oSize:aWindSize[4] / 2)
	::oWorkAreaMain:CreateDialog()

	::oWorkAreaContainer := FWUIWorkArea():new(::oWorkAreaMain:GetPanelMain())
	::oWorkAreaContainer:CreateHorizontalBox("MAIN", 100 , .F.)
	::oWorkAreaContainer:SetBoxCols("MAIN" , {"WIDDOC", "WIDMONITOR"} )
	::oWorkAreaContainer:Activate()

	::oPanelDadosNota := ::oWorkAreaContainer:GetPanel("WIDDOC")
	::oPanelDadosMonitor := ::oWorkAreaContainer:GetPanel("WIDMONITOR")

	oSayCodCli := TSay():New(01, 01, {|| STR0037+" "+cCodCliente+" - "+STR0024+" "+cLoja }, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Código" //"Loja"
	oSayNomeCli := TSay():New(02, 01, {|| STR0038+" "+Alltrim(cNomeCliente)}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) // "Razão Social"
	oSayPessoa := TSay():New(03, 01, {|| cTipoCliDesc + " - " +Transform(Alltrim(cCnpjClinete),cMaskCNPJ)}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Documento"
	oSayNF:= TSay():New(03, 18, {|| STR0022+" "+cNumDoc+ " - "+cDocSerie+" - "+ DToc(dDocEmissao)}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont)
	oSayChave := TSay():New(04, 01, {|| STR0039+" "+ cChaveDoc}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Chave"

	oGrpDadosNota := TGroup():New(02,02,75,(::oPanelDadosNota:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDDOC"),,,.T.)
	if !::lThemeDark
		oGrpDadosNota:SetCss("TGroup{background-color: white;}")
	endif
	::oTSayTransacao := TSay():New(07, 01, {||STR0040}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //'Selecionar Evento: '
	::oComboEvento := TComboBox():New(100,07,{|u|if(PCount()>0,cEvento:=u,cEvento)},;
		::aTipoEvento,120,20,::oWorkAreaContainer:GetPanel("WIDDOC"),,{||(::SetTrocaEvento(cEvento),::TrocarEvento())},,,,.T.,,,,,,,,,'cEvento',,)

	::oTSayTpMotivo := TSay():New(09, 01, {||STR0043}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Tipo Motivo"
	::oComboMotivo := TComboBox():New(127,07,{|u|if(PCount()>0,::cMotivo:=u,::cMotivo)},;
		::aMotivo,120,20,::oWorkAreaContainer:GetPanel("WIDDOC"),,{||},,,,.T.,,,,,,,,,'::cMotivo',,)

	::oTSayMotivo := TSay():New(11, 01, {||STR0044}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Motivo"
	::oTMotivo := tMultiget():new(155,07,{| u | if( pCount() > 0, ::cTextoMotivo := u, ::cTextoMotivo )},::oWorkAreaContainer:GetPanel("WIDDOC"),150,40,,,,,,.T.)

	::oTSayGetDataEntrega := TSay():New(07, 25, {||STR0045}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Data da Entrega"
	::oTGetDataEntrega := TGet():New( 100, 200, { | u | If( PCount() == 0, ::dGetDataEntrega, ::dGetDataEntrega := u ) },::oWorkAreaContainer:GetPanel("WIDDOC"),;
		060, 010, "@D",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::dGetDataEntrega",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oTGetDataEntrega:SetCss("TGet{background-color: black;}")
	endif

	::oTSayTentativa := TSay():New(09, 25, {||STR0046}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Nro. da tentativa"
	::oGetTentativa := TGet():New(127,200, { | u | If( PCount() == 0, ::nTentativa, ::nTentativa := u ) },::oWorkAreaContainer:GetPanel("WIDDOC"), ;
		100, 010, "@R 999",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::nTentativa",,,,lHasButton,,,,)
	if ::lThemeDark .and. lValBuild
		::oGetTentativa:SetCss("TGet{background-color: black;}")
	endif

	::oTSayImagem := TSay():New(11, 25, {||STR0047}, ::oWorkAreaContainer:GetPanel("WIDDOC"),,::oFont) //"Imagem Capturada"
	::oGetImagem := TGet():New(155,200, { | u | If( PCount() == 0, ::cPathImagem, ::cPathImagem := u ) },::oWorkAreaContainer:GetPanel("WIDDOC"), ;
		150, 010, "@!",, 0, 16777215,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F. ,,"::cPathImagem",,,,lHasButton,,,,)
	::oGetImagem:Disable()
	if ::lThemeDark
		::oGetImagem:SetCss("TGet{background-color: black;}")
	endif

	::oTButtonImage := TButton():New( 180, 200, STR0048,::oWorkAreaContainer:GetPanel("WIDDOC"),; //Escolha o Arquivo
		{|| ::cPathImagem := tFileDialog( "All Image files (*.jpg) | All Image files (*.jpeg) | All Image files (*.png) | All Image files (*.bmp)",STR0049,,, .F.,)}, 80,15,,,.F.,.T.,.F.,,.F.,,,.F. ) //'Seleção de Arquivos'

	oGrpNotas:= TGroup():New(80,02,(::oPanelDadosNota:NHEIGHT / 2) - 3,(::oPanelDadosNota:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDDOC"),,,.T.)
	if !::lThemeDark
		oGrpNotas:SetCss("TGroup{background-color: white;}")
	endif
	oGrpBrowser := TGroup():New(02,02,(::oPanelDadosMonitor:NHEIGHT / 2) - 3,(::oPanelDadosMonitor:NWIDTH / 2) - 3,,::oWorkAreaContainer:GetPanel("WIDMONITOR"),,,.T.)
	if !::lThemeDark
		oGrpBrowser:SetCss("TGroup{background-color: white;}")
	endif
	::TrocarEvento()
	aAdd(aButtons,{,STR0028,{|| ::oWorkAreaMain:DeActivate()},STR0050,1,.T.,.T.}) // Sair "Sair da Rotina"
	aAdd(aButtons,{,STR0041,{||::RefreshMonitor()},STR0052,1,.T.,.T.}) //"Refresh" Atualiza Browser Monitor
	aAdd(aButtons,{,STR0053,{||::MostraMenssagem(::Titulo+STR0054,"","") },"Insucesso na Entrega - Visualiza Evento",1,.T.,.T.}) // Mensagem 'Insucesso na Entrega - Visualiza Evento'
	aAdd(aButtons,{,STR0055,{||::TransmitirEvento() },STR0056,1,.T.,.T.}) // TRsnamitir "Transmitir Eventos"

	::oWorkAreaMain:addButtons(aButtons)
	::oWorkAreaMain:Activate()

Return

/*/{Protheus.doc} SetTrocaEvento
	Seta o evento Selecionado ao acionar o controle de evento
	@author Ricardo Henrique de Mello Lima
	@since 25/10/2024
	@version 12.1.2410
	/*/
Method SetTrocaEvento(cValue as character) class NfeInsucesso
	::cEventoSelecionado := Substr(Alltrim(cValue),1,6)
Return

/*/{Protheus.doc} TrocarEvento
	Metodo responsalvel por Mudar o comportamento ao trocar o tipo de evento
	@author Ricardo Henrique de Mello Lima
	@since 29/10/2024
	@version 12.1.2410
	/*/
Method TrocarEvento() class NfeInsucesso

	if Empty(::cEventoSelecionado) .OR. ::cEventoSelecionado == ::cEventoMonitor
		::ActionCampos()
		::RefreshMonitor()
	endif

	if ::cEventoSelecionado == ::cEventoTransmissao
		::ActionCampos()
		::RefreshMonitor()
	endif

	if ::cEventoSelecionado == ::cEventoCancelamento
		::ActionCampos()
		::RefreshMonitor()
	endif

Return

/*/{Protheus.doc} MontaBrowserMonitor
	Monta o Browser do Monitor de Eventos
	@author Ricardo Henrique de Mello Lima
	@since 30/10/2024
	@version 12.1.2410
	/*/
Method MontaBrowserMonitor() class NfeInsucesso

	::oOKMonitor := LoadBitmap(GetResources(),'br_verde')
	::oNOMOnitor := LoadBitmap(GetResources(),'br_vermelho')

	if Empty(::aBrowserMonitor)
		::aBrowserMonitor := { {.F.,'','','','','','','','','','',0}}
	endif
	
	aSort(::aBrowserMonitor,,,{|x,y| x[3] < y[3] })

	::oBrowserMonitor := TCBrowse():New( 05 , 05, ::oPanelDadosMonitor:NWIDTH / 2 - 10, ::oPanelDadosMonitor:NHEIGHT / 2 - 10 ,, {'',STR0057,STR0058,STR0022,STR0023,STR0059,STR0060,STR0061,STR0057,STR0062,STR0053},; //{'','Evento','Sequência','Documento','Série','Protocolo','ID','Cod.Evento','Evento','Cod.Ret','Mensagem'}
		{20,40,30,40,25,50,170,45,50,25,170},::oWorkAreaContainer:GetPanel("WIDMONITOR"),,,,,{||},,,,,,,.F.,,.T.,,.F.,,, )
	::oBrowserMonitor:SetArray(::aBrowserMonitor)
	::oBrowserMonitor:bLine := {||{ If(::aBrowserMonitor[::oBrowserMonitor:nAt,01],::oOKMonitor,::oNOMonitor),;
		::aBrowserMonitor[::oBrowserMonitor:nAt,02],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,03],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,04],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,05],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,06],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,07],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,08],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,09],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,10],;
		::aBrowserMonitor[::oBrowserMonitor:nAt,11]}}

	::oBrowserMonitor:DrawSelect()
	::oBrowserMonitor:Refresh()

Return

/*/{Protheus.doc} UpdateMonitor
	Atualiza os dados no Monitor de Eventos
	@author Ricardo Henrique de Mello Lima
	@since 31/10/2024
	@version 12.1.2410
	/*/
Method UpdateMonitor(cEvento as character, cChaveDoc as character) class NfeInsucesso

	Local aEventosRetorno := {} as array
	Local nCount := 0 as numeric

	aSize(::aBrowserMonitor,0)

	if Empty(cEvento) .OR. ::cEventoSelecionado == ::cEventoMonitor

		for nCount := 1 to 2

			if nCount == 1
				cEvento := ::cEventoTransmissao
			elseif nCount == 2
				cEvento := ::cEventoCancelamento
			endif

			aEventosRetorno := ::CallRetornaEvento(cChaveDoc,cEvento)
			::SetArrayMonitor(aEventosRetorno,cEvento)
		next
	else
		aEventosRetorno := ::CallRetornaEvento(cChaveDoc,cEvento)
		::SetArrayMonitor(aEventosRetorno,cEvento)
	endif

	if Empty(::aBrowserMonitor)
		::aBrowserMonitor := { {.F.,'','','','','','','','','','',0}}
	endif
Return

/*/{Protheus.doc} CallRetornaEvento
	Chama o endpoint para retornar os eventos da NFe
	@author Ricardo Henrique de Mello Lima
	@since 01/11/2024
	@version 12.1.2410	
	/*/
Method CallRetornaEvento(cChave as character, cEvento as character) as array class NfeInsucesso
	Local cError := "" as character
	Local cMsg := "" as character

	aValue := TSSMonEven(@cError,,::cIdEntidade,cChave,cChave,cEvento,@cMsg)

Return aValue

/*/{Protheus.doc} SetArrayMonitor
	Popula o array do Browser do Monitor 
	@author Ricardo Henrique de Mello Lima
	@since 01/11/2024
	@version 12.1.2410
	/*/

Method SetArrayMonitor(aValue as array, cEvento as character) class NfeInsucesso

	Local nCount := 0 as numeric
	Local nTamID := 0 as numeric
	Local cSeq := "" as character
	Local cDocumento := "" as character
	Local cSerie := "" as character

	for nCount := 1 To Len(aValue)

		nTamID := Len(aValue[nCount,3])
		cSeq := SubStr(Alltrim(aValue[nCount,3]),nTamID-2,2)
		cDocumento := SubStr(Alltrim(aValue[nCount,3]),34,9)
		cSerie := SubStr(Alltrim(aValue[nCount,3]),31,3)

		aAdd(::aBrowserMonitor,{iif(aValue[nCount,1] == 6,.T.,.f.),;
			iif(cEvento == ::cEventoTransmissao,STR0031,STR0063),; // Transmissão' , Cancelamento'),;
			cSeq,;
			cDocumento,;
			cSerie,;
			aValue[nCount,2],;
			aValue[nCount,3],;
			iif(aValue[nCount,5] == "6",STR0064,STR0065),; // Autorizado","Nao Autorizada,;
			aValue[nCount,6],;
			aValue[nCount,7],;
			aValue[nCount,8]})
	next
Return

/*/{Protheus.doc} CallEnviaEvento
	Chama o EndPoint de Eviar o Evento
	@author Ricardo Henrique de Mello Lima
	@since 01/11/2024
	@version 12.1.2410
	/*/
Method CallEnviaEvento(cValue as character) class NfeInsucesso

	Local cError := "" as character

	aAdd(::aRetEvento,{TSSEnvEven(@cError,,::cIdEntidade,cValue)})

	if !Empty(cError)
		FwAlertInfo(cError+CHR(13)+CHR(10)+STR0066,::Titulo) // "Evento não pode ser enviados" "Insucesso na Entrega da Nfe"
	endif

Return

/*/{Protheus.doc} TransmitirEvento
	Transmitir Evento do Insucesso na Entrega
	@author Ricardio Henrique de Mello Lima
	@since 03/11/2024
	@version 12.1.2410
	/*/
Method TransmitirEvento() class NfeInsucesso

	if ::cEventoSelecionado == ::cEventoTransmissao
		if ::ValidaTransmissao()
			::AnalisaXML()
			if ::HashComprovante()
				::IncluirEvento()
				::LimpaCampos()
			endif
		else
			FwAlertInfo(::cMsgValidaTransmissao,::Titulo)
		endif
	elseif ::cEventoSelecionado == ::cEventoCancelamento
		::CancelarEvento()
	endif
	::RefreshMonitor()
Return

/*/{Protheus.doc} IncluirEvento
	Metodo responsavel pela inclusao do Evendo de Insucesso
	@author Ricardo Henrique de Mello Lima
	@since 03/11/2024
	@version 12.1.2410
	/*/
Method IncluirEvento() class NfeInsucesso

	Local cXml := "" as character

	cXml := '<envEvento>'
	cXml += '<eventos>'
	cXml += '<detEvento>'
	cXml += '<tpEvento>'+::cEventoTransmissao+'</tpEvento>'
	cXml += '<chNFe>'+Alltrim((::cAliasDocumentos)->F2_CHVNFE)+'</chNFe>'
	cXml += '<cOrgaoAutor>'+SubStr(Alltrim((::cAliasDocumentos)->F2_CHVNFE),1,2)+'</cOrgaoAutor>'
	cXml += '<dhEntrega>'+FwTimeStamp(5,::dGetDataEntrega)+'</dhEntrega>'
	cXml += '<nTentativa>'+cValToChar(::nTentativa)+'</nTentativa>'
	cXml += '<tpMotivo>'+SubStr(Alltrim(::cMotivo),1,1)+'</tpMotivo>'
	cXml += '<xJustMotivo>'+::cTextoMotivo+'</xJustMotivo>'
	cXml += '<latGPS>'+iif(Empty(::cLatitude),'-23.560423',Substr(::cLatitude,1,10))+'</latGPS>'
	cXml += '<longGPS>'+iif(Empty(::cLongitude),'-46.632793',SubStr(::cLongitude,1,10))+'</longGPS>'
	cXml += '<hashComprovante>'+::cHashComprovante+'</hashComprovante>'
	cXml += '<dhHashComprovante>'+FwTimeStamp(5,::dGetDataEntrega)+'</dhHashComprovante>'
	cXml += '</detEvento>'
	cXml += '</eventos>'
	cXml += '</envEvento>'

	MsgRun(STR0067,STR0068,{||(::CallEnviaEvento(cXml))}) //"Transmitindo Eventos","Processando"

Return

/*/{Protheus.doc} CancelarEvento
	Envia o Cancelamento do Evento
	@author Ricardo Henrique de Mello Lima
	@since 03/11/2024
	@version 12.1.2410
	/*/
Method CancelarEvento() class NfeInsucesso

	Local cXml := "" as character

	cXml := '<envEvento>'
	cXml += '<eventos>'
	cXml += '<detEvento>'
	cXml += '<tpEvento>'+::cEventoCancelamento+'</tpEvento>'
	cXml += '<chnfe>'+Alltrim((::cAliasDocumentos)->F2_CHVNFE)+'</chnfe>'
	cXml += '<descEvento>Cancelamento do Insucesso na Entrega da NF-e</descEvento>'
	cXml += '</detEvento>'
	cXml += '</eventos>'
	cXml += '</envEvento>'
	MsgRun(STR0069,STR0068,{||(::CallEnviaEvento(cXml))}) //"Transmitindo Eventos","Processando"

Return

/*/{Protheus.doc} methodName
	Mostra Dados do evento no Monitor de eventos
	@author Ricardo Henrique de Mello Lima
	@since 03/11/2024
	@version 12.1.2410
	@param param_name, param_type, param_descr
	@return return_var, return_type, return_description
	/*/
Method MostraMenssagem(cTitulo as character, cSubTitulo as character, cMenssagem as character) class NfeInsucesso

	Local oModal := nil as object
	Local oContainer := nil as object
	Local oTMultLine := nil as Object
	Local nLinha := 0 as numeric

	nLinha := ::oBrowserMonitor:nAt

	cMenssagem := STR0070+" - "+Alltrim(::aBrowserMonitor[nLinha,2])+CHR(13)+CHR(10) //"Evento:"
	cMenssagem += STR0071+":  "+Alltrim(::aBrowserMonitor[nLinha,3])+CHR(13)+CHR(10) //"Sequência:"
	cMenssagem += STR0072+":  "+Alltrim(::aBrowserMonitor[nLinha,4])+CHR(13)+CHR(10) //"Documento:"
	cMenssagem += STR0073+":  "+Alltrim(::aBrowserMonitor[nLinha,5])+CHR(13)+CHR(10) //Serie
	cMenssagem += STR0074+":  "+Alltrim(::aBrowserMonitor[nLinha,6])+CHR(13)+CHR(10) //"Protocolo:"
	cMenssagem += STR0075+":  "+Alltrim(::aBrowserMonitor[nLinha,7])+CHR(13)+CHR(10) //"ID:"
	cMenssagem += STR0076+":  "+Alltrim(::aBrowserMonitor[nLinha,8])+CHR(13)+CHR(10) //"Status:"
	cMenssagem += STR0077+":  "+Alltrim(::aBrowserMonitor[nLinha,9])+CHR(13)+CHR(10) //"Descrição:"
	cMenssagem += STR0078+":  "+cValtoChar(::aBrowserMonitor[nLinha,10])+CHR(13)+CHR(10) //"Cod.Evento:"
	cMenssagem += STR0079+":  "+Alltrim(::aBrowserMonitor[nLinha,11])+CHR(13)+CHR(10) //"Mensagem:"

	oModal := FWDialogModal():New()
	oModal:SetEscClose(.T.)
	oModal:SetTitle(cTitulo)
	oModal:setSubTitle(cSubTitulo)

	oModal:setSize(250,250)

	oModal:CreateDialog()
	oModal:addCloseButton(nil,"Fechar")

	oContainer := TPanel():New(,,,oModal:GetPanelMain())
	if !::lThemeDark
		oContainer:SetCss("TPanel{background-color : white;}")
	endif
	oContainer:Align := CONTROL_ALIGN_ALLCLIENT

	oTMultLine := TMultiget():New(01,01,{| u | if( pCount() > 0, cMenssagem := u, cMenssagem)},oContainer,245,180,,,,,,.T.)
	oTMultLine:lReadOnly := .T.
	oModal:Activate()

Return

/*/{Protheus.doc} RefreshMonitor
	Refresh Monitor
	@author Ricardo Henrique de Mello Lima
	@since 03/11/2024
	@version 12.1.2410
	/*/
Method RefreshMonitor() class NfeInsucesso

	::UpdateMonitor(::cEventoSelecionado,(::cAliasDocumentos)->F2_CHVNFE)
	FreeObj(::oBrowserMonitor)
	::MontaBrowserMonitor()

Return

/*/{Protheus.doc} AnalisaXML
	Recupera o XML do Documento para obter os dados para emitir o evento 
	@author Ricardo Henrique de Mello Lima
	@since 07/11/2024
	@version 12.1.2410
	/*/
Method AnalisaXML() class NfeInsucesso

	Local cXml := "" as character
	Local oWs := nil as object
	Local oXml := nil as object
	Local cURL := PadR(GetNewPar("MV_SPEDURL","http://"),250)
	Local cEndereco := "" as character
	Local cNumero := "" as character
	Local cEstado := "" as character
	Local cCidade := "" as character
	Local cBairro := "" as character
	Local aCoordenadas := {} as array

	oWS:= WSNFeSBRA():New()
	oWS:cUSERTOKEN := "TOTVS"
	oWS:cID_ENT := ::cIdEntidade
	oWS:oWSNFEID := NFESBRA_NFES2():New()
	oWS:oWSNFEID:oWSNotas := NFESBRA_ARRAYOFNFESID2():New()
	aadd(oWS:oWSNFEID:oWSNotas:oWSNFESID2,NFESBRA_NFESID2():New())
	Atail(oWS:oWSNFEID:oWSNotas:oWSNFESID2):cID := Alltrim((::cAliasDocumentos)->F2_SERIE)+Alltrim((::cAliasDocumentos)->F2_DOC)
	oWS:nDIASPARAEXCLUSAO := 0
	oWS:_URL := AllTrim(cURL)+"/NFeSBRA.apw"

	If oWS:RETORNANOTAS()
		If Len(oWs:oWsRetornaNotasResult:OWSNOTAS:oWSNFES3) > 0
			cXml := AllTrim(oWs:oWsRetornaNotasResult:OWSNOTAS:oWSNFES3[1]:oWSNFE:cXML)
		endif
	endif

	FreeObj(oWS)

	if !Empty(cXml)

		cXml := EncodeUtf8(cXml)
		oXml := TXMLManager():new()

		if oXML:Parse(cXml)

			oXml:xPathRegisterNs("ns","http://www.portalfiscal.inf.br/nfe")

			if oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:xLgr") != ""
				cEndereco := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:xLgr")
				cNumero := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:nro")
				cEstado := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:UF")
				cCidade := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:xMun")
				cBairro := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:entrega/ns:xBairro")
			else
				cEndereco := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:dest/ns:enderDest/ns:xLgr")
				cNumero :=oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:dest/ns:enderDest/ns:nro")
				cEstado := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:dest/ns:enderDest/ns:UF")
				cCidade := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:dest/ns:enderDest/ns:xMun")
				cBairro := oXml:XPathGetNodeValue("/ns:NFe/ns:infNFe/ns:dest/ns:enderDest/ns:xBairro")
			endif
		endif
	endif

	FreeObj(oXml)

	aCoordenadas := TECGtCoord(cEndereco+cNumero,cCidade,cEstado,1)

	if !Empty(aCoordenadas)
		::cLatitude := aCoordenadas[1]
		::cLongitude := aCoordenadas[2]
	endif

Return

/*/{Protheus.doc} DisableCampos
	(long_description)
	@author Ricardo Henrique de Mello Lima
	@since 07/11/2024
	@version 12.1.2410
	/*/
Method ActionCampos() class NfeInsucesso

	if ::cEventoSelecionado == ::cEventoTransmissao

		::oComboMotivo:lVisibleControl := .T.
		::oTMotivo:lVisibleControl := .T.
		::oGetTentativa:lVisibleControl := .T.
		::oGetImagem:lVisibleControl := .T.
		::oTSayMotivo:lVisibleControl := .T.
		::oTSayTentativa:lVisibleControl := .T.
		::oTSayImagem:lVisibleControl := .T.
		::oTSayTpMotivo:lVisibleControl := .T.
		::oTButtonImage:lVisibleControl := .T.
		::oTSayGetDataEntrega:lVisibleControl := .T.
		::oTGetDataEntrega:lVisibleControl := .T.

		::UpdateMonitor(::cEventoSelecionado,(::cAliasDocumentos)->F2_CHVNFE)

		FreeObj(::oBrowserMonitor)
		::MontaBrowserMonitor()

	elseif ::cEventoSelecionado == ::cEventoCancelamento

		::oComboMotivo:lVisibleControl := .F.
		::oTMotivo:lVisibleControl := .F.
		::oGetTentativa:lVisibleControl := .F.
		::oGetImagem:lVisibleControl := .F.
		::oTSayMotivo:lVisibleControl := .F.
		::oTSayTentativa:lVisibleControl := .F.
		::oTSayImagem:lVisibleControl := .F.
		::oTSayTpMotivo:lVisibleControl := .F.
		::oTButtonImage:lVisibleControl := .F.
		::oTSayGetDataEntrega:lVisibleControl := .F.
		::oTGetDataEntrega:lVisibleControl := .F.


	elseif Empty(::cEventoSelecionado) .or. ::cEventoSelecionado == ::cEventoMonitor

		::oComboMotivo:lVisibleControl := .F.
		::oTMotivo:lVisibleControl := .F.
		::oGetTentativa:lVisibleControl := .F.
		::oGetImagem:lVisibleControl := .F.
		::oTSayMotivo:lVisibleControl := .F.
		::oTSayTentativa:lVisibleControl := .F.
		::oTSayImagem:lVisibleControl := .F.
		::oTSayTpMotivo:lVisibleControl := .F.
		::oTButtonImage:lVisibleControl := .F.
		::oTSayGetDataEntrega:lVisibleControl := .F.
		::oTGetDataEntrega:lVisibleControl := .F.


	endif

Return

/*/{Protheus.doc} ThemaSistema
	Recebe o Thema selecionado para ajuste de cores do SCSS das Telas
	@author Ricardo Henrique de Mello Lima
	@since 08/11/2024
	@version 12.1.2410
	/*/
Method ThemaSistema() class NfeInsucesso

	Local lRelease2410 	:= GetRpoRelease() >= "12.1.2410" as logical
	Local oTheme := nil as object

	If lRelease2410
		oTheme := totvs.framework.css.ProtheusTheme():New()
		if Alltrim(oTheme:CTHEME) == "DARK"
			::lThemeDark := .T.
		endif
	EndIf
	FreeObj(oTheme)
Return

/*/{Protheus.doc} ValidaTransmissao
	Faz a validação dos campos
	@author Ricardo Henrique de Mello Lima
	@since 09/11/2024
	@version 12.1.2410
	/*/
Method ValidaTransmissao() as logical class NfeInsucesso

	Local lValida := .T. as logical

	::cMsgValidaTransmissao := ""

	if Substr(::cMotivo,1,1) == "4"
		if Empty(::cTextoMotivo) .or. len(Alltrim(::cTextoMotivo)) < 25 .or. len(Alltrim(::cTextoMotivo)) > 250
			::cMsgValidaTransmissao := STR0080+CHR(13)+CHR(10)+STR0081//"Campo (Motivo) obrigatório para Tipo de Motivo = 4." - "Mínimo 25 Carácteres Máximo 250 Carácteres"
			lValida := .F.
		endif
	endif

	if lValida
		if ::nTentativa <= 0
			::cMsgValidaTransmissao := STR0082 // "Campo (Nro.Tentativas) obrigatório, maior que zero."
			lValida := .F.
		endif
	endif

	if lValida
		if ::dGetDataEntrega < Stod((::cAliasDocumentos)->F2_EMISSAO)
			::cMsgValidaTransmissao := STR0083 //"Data deve ser maior ou igual a Emissão do Documento"
			lValida := .F.
		endif
	endif

Return lValida

/*/{Protheus.doc} HashComprovante
	Monta o Hash do Comprovante que compoe da Concatenação da Chave + Base64 da Imagem 
	@author Ricardo Henrique de Mello Lima
	@since 09/11/2024
	@version 12.1.2410
	/*/
Method HashComprovante() as logical class NfeInsucesso

	Local nHandle := 0 as numeric
	Local nTamanho := 0 as numeric
	Local cBufferArquivo := "" as character
	Local lValido := .T. as logical
	Local oFile := nil as object
	Local nTamanhoFile := 0 as numeric

	if !Empty(::cPathImagem)

		oFile := FWFileReader():New(::cPathImagem)
		if oFile:open()
			nTamanhoFile := oFile:GetFileSize()
			oFile:close()
		endif
		FreeObj(oFile)
		
		if nTamanhoFile <= 5000000
			nHandle := fopen(alltrim(::cPathImagem), FO_READWRITE + FO_SHARED )
			If nHandle == -1
				FWAlertInfo(STR0084,::Titulo) //"Problema ao abrir o Arquivo de Imagem"
				lValido := .F.
			Else
				nTamanho := FSeek(nHandle,0,2)
				FSeek(nHandle,0)
				FRead(nHandle,cBufferArquivo,nTamanho)
				FClose(nHandle)

				cBufferArquivo := Encode64(cBufferArquivo)
				::cHashComprovante := Encode64(Sha1(Alltrim((::cAliasDocumentos)->F2_CHVNFE) + cBufferArquivo,1))
			Endif
		else
			lValido := .F. 
			FWAlertInfo(STR0085,::Titulo) //"Arquivo deve ser menor ou igual a 5MB"
		endif

	else
		::cHashComprovante := Encode64(Sha1(Alltrim((::cAliasDocumentos)->F2_CHVNFE),1))
		lValido := .T.
	endif

Return lValido

/*/{Protheus.doc} LimpaCampos
	Limpa Campos apos transmissao
	@author Ricardo Henrique de Mello Lima
	@since 09/11/2024
	@version 12.1.2410
	/*/
Method LimpaCampos() class NfeInsucesso
	::nTentativa := 0
	::cPathImagem := ""
	::dGetDataEntrega := ctod("  \  \    ")
	::cMotivo := ::aMotivo[1]
	::cTextoMotivo := ""
Return

