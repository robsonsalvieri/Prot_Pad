#INCLUDE "wsmat700.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWEBSRV.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT700  ³ Autor ³Luiz Couto             ³ Data ³19.08.2004  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pela previsao de vendas              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSalesForecast DESCRIPTION STR0001 //NAMESPACE "http://webservices.microsiga.com.br/MtSalesForecast.apw" //"Serviço de consulta as previsões de vendas"
	WSDATA UserCode 		AS String
	WSDATA Header 			AS Array Of BrwHeader
	WSDATA HeaderType 		AS String
	WSDATA ProductCode 		AS String OPTIONAL
	WSDATA ForecastDateFrom	As Date OPTIONAL
	WSDATA ForecastDateTo 	As Date OPTIONAL
	WSDATA PageLen 			AS Integer OPTIONAL
	WSDATA PageFirst 		AS Integer OPTIONAL
	WSDATA QueryAddWhere 	As String OPTIONAL
	WSDATA IndexKey 		As String OPTIONAL
	WSDATA SalesForecasts 	AS Array Of SalesForecastView
	WSDATA SalesForecast	AS SalesForecastView
	WSDATA WSNULL			As String
	
	WSMETHOD GetHeader 			DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
	WSMETHOD BrwSalesForecast 	DESCRIPTION STR0003 //"Método de listagem das previsões de venda"
	WSMETHOD GetSalesForecast 	DESCRIPTION STR0004 //"Método de consulta das informacoes da previsão de venda"
	WSMETHOD PutSalesForecast 	DESCRIPTION STR0005 //"Método de atualização das informacoes da previsão de venda"
	WSMETHOD DelSalesForecast 	DESCRIPTION STR0006 //"Método de exclusão da previsão de venda"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³Luiz Couto             ³ Data ³19.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao do header da previsao de vendas        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSalesForecast

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwSalesFo|Autor  ³ Luiz Couto            ³ Data ³19.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao das previsoes de vendas                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³Parametros³ExpC2: Produto                                               ³±±
±±³Parametros³ExpD3: Data da previsao - De                                 ³±±
±±³Parametros³ExpD4: Data da previsao - Ate                                ³±±
±±³Parametros³ExpN5: Numero de paginas por tela                            ³±±
±±³Parametros³ExpN6: Numero da pagina                                      ³±±
±±³Parametros³ExpC7: Where                                                 ³±±
±±³Parametros³ExpC8: Indice                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve as previsoes de vendas por produto ou    ³±±
±±³          ³periodo                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwSalesForecast WSRECEIVE UserCode,ProductCode,ForecastDateFrom,ForecastDateTo,PageLen,PageFirst,QueryAddWhere,IndexKey WSSEND SalesForecasts WSSERVICE MtSalesForecast

Local aArea 	:= GetArea()
Local lRetorno	:= .T.
Local lQuery 	:= .T.
Local cCursor 	:= ""
Local cQuery 	:= ""
Local aStruct 	:= {}
Local nX		:= 0
Local nY		:= 0

DEFAULT ::IndexKey 			:= SC4->( IndexKey() )
DEFAULT ::ForecastDateFrom 	:= FirstDay( dDataBase )
DEFAULT ::ForecastDateTo 	:= LastDay( dDataBase )
DEFAULT ::PageLen 			:= 0
DEFAULT ::PageFirst 		:= 0

If PrtChkUser( ::UserCode, "MTSALESFORECAST", "BRWSALESFORECAST" )
	#IFDEF TOP
		lQuery  := .T.
		cCursor := GetNextAlias()
		cQuery  := "SELECT C4_FILIAL, C4_PRODUTO, C4_LOCAL, C4_DOC, C4_QUANT, C4_VALOR, C4_DATA, C4_OBS "
		cQuery  += GetUserFields( "SC4" )
		cQuery  += "FROM "+RetSqlName( "SC4" )+" SC4 "
		cQuery  += "WHERE SC4.C4_FILIAL='"+xFilial( "SC4" )+"' AND "
		If !Empty( ::ProductCode )
			cQuery  += "SC4.C4_PRODUTO='"+::ProductCode+"' AND "
		EndIf
		If !Empty( ::ForecastDateFrom ) .AND. !Empty( ::ForecastDateTo )
			cQuery  += "SC4.C4_DATA >= '"+DToS(::ForecastDateFrom)+"' AND "
			cQuery  += "SC4.C4_DATA <= '"+DToS(::ForecastDateTo)+"' AND "
		EndIf
		cQuery  += "SC4.D_E_L_E_T_=' ' "
		cQuery  := WsQueryAdd( cQuery, ::QueryAddWhere )
		If !Empty( ::IndexKey )
			cQuery  += "ORDER BY "+SqlOrder( ::IndexKey )
		EndIf
		
		cQuery := ChangeQuery( cQuery )
		
		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cCursor )
		aStruct := SC4->( dbStruct() )
		For nX := 1 To Len( aStruct )
			If aStruct[nX][2] <> "C" .AND. FieldPos( aStruct[nX][1] ) <> 0
				TcSetField( cCursor, aStruct[nX][1], aStruct[nX][2], aStruct[nX][3], aStruct[nX][4] )
		    EndIf
		Next nX
	#ELSE
	#ENDIF
	nX := 0
	While !( cCursor )->( Eof() ) .And. ( cCursor )->C4_FILIAL == xFilial( "SC4" )
		nY++
		If ::PageFirst==0 .Or. nY >= ::PageFirst
			nX++
			aadd( ::SalesForecasts, WSClassNew( "SalesForecastView" ) )
			::SalesForecasts[nX]:ProductCode 	:= ( cCursor )->C4_PRODUTO
			::SalesForecasts[nX]:Warehouse 		:= ( cCursor )->C4_LOCAL
			::SalesForecasts[nX]:Document 		:= ( cCursor )->C4_DOC
			::SalesForecasts[nX]:Quantity 		:= ( cCursor )->C4_QUANT
			::SalesForecasts[nX]:Value 			:= ( cCursor )->C4_VALOR
			::SalesForecasts[nX]:ForecastDate 	:= ( cCursor )->C4_DATA
			::SalesForecasts[nX]:Note 			:= ( cCursor )->C4_OBS
			UserFields( "SC4", @::SalesForecasts[nX]:UserFields, cCursor )
			
			If nX >= ::PageLen .And. ::PageLen <> 0
				Exit
			EndIf
		EndIf
		( cCursor )->( dbSkip() )
	EndDo
	If lQuery
		( cCursor )->( dbCloseArea() )
	Else
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSalesFo|Autor  ³ Luiz Couto            ³ Data ³19.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao de uma previsao de venda               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³Parametros³ExpC2: Produto                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a previsao de venda de um produto        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetSalesForecast WSRECEIVE UserCode,ProductCode WSSEND SalesForecast WSSERVICE MtSalesForecast

Local aArea 	:= GetArea()
Local lRetorno	:= .T.

If PrtChkUser( ::UserCode, "MTSALESFORECAST", "GETSALESFORECAST" )
	SC4->( dbSetOrder( 1 ) )
	If SC4->( MsSeek( xFilial( "SC4" )+::ProductCode ) )
		::SalesForecast := WSClassNew( "SalesForecastView" )
		::SalesForecast:ProductCode 	:= SC4->C4_PRODUTO
		::SalesForecast:Warehouse 		:= SC4->C4_LOCAL
		::SalesForecast:Document 		:= SC4->C4_DOC
		::SalesForecast:Quantity 		:= SC4->C4_QUANT
		::SalesForecast:Value 			:= SC4->C4_VALOR
		::SalesForecast:ForecastDate 	:= SC4->C4_DATA
		::SalesForecast:Note 			:= SC4->C4_OBS
		UserFields( "SC4", @::SalesForecast:UserFields, "SC4" )
	Else
		lRetorno := .F.
		SetSoapFault( "GETSALESFORECAST", STR0007 ) //"Produto não encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSalesFo³Autor  ³ Luiz Couto            ³ Data ³19.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de inclusao de uma previsa de venda                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpO2: Objeto contendo a previsao de venda                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este inclui uma previsao de venda                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutSalesForecast WSRECEIVE UserCode,SalesForecast WSSEND WSNULL WSSERVICE MtSalesForecast

Local aArea 	:= GetArea()
Local lRetorno 	:= .T.
Local aDados 	:= {}
Local aErro 	:= {}
Local nX 		:= 0
Local cErro 	:= ""

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser( ::UserCode, "MTSALESFORECAST", "PUTSALESFORECAST" )
	aAdd( aDados, { "C4_PRODUTO", ::SalesForecast:ProductCode, } )
	aAdd( aDados, { "C4_LOCAL", ::SalesForecast:Warehouse, } )
	aAdd( aDados, { "C4_DOC", ::SalesForecast:Document, } )
	aAdd( aDados, { "C4_QUANT", ::SalesForecast:Quantity, } )
	aAdd( aDados, { "C4_VALOR", ::SalesForecast:Value, } )
	aAdd( aDados, { "C4_DATA", ::SalesForecast:ForecastDate, } )
	aAdd( aDados, { "C4_OBS", ::SalesForecast:Note, } )
	PutUserFields( "SC4", ::SalesForecast:UserFields, @aDados )
	aDados := WsAutoOpc( aDados )

	SC4->( dbSetOrder( 1 ) )
	If !Empty( ::SalesForecast:ProductCode ) .And. SC4->( MsSeek( xFilial( "SC4" )+::SalesForecast:ProductCode ) )
		MATA700( aDados, 4 )
	Else
		MATA700( aDados, 3)
	EndIf

	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len( aErro )
			cErro += aErro[nX] + Chr( 13 )+Chr( 10 )
		Next nX
		SetSoapFault( "PUTSALESFORECAST", cErro )
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelSalesFo³Autor  ³ Luiz Couto            ³ Data ³19.08.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de exclusao de previsao de venda                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Produto                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este exlcui uma previsao de venda                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelSalesForecast WSRECEIVE UserCode,ProductCode WSSEND WSNULL WSSERVICE MtSalesForecast

Local aArea 	:= GetArea()
Local lRetorno 	:= .T.
Local aDados 	:= {}
Local aErro 	:= {}
Local nX 		:= 0
Local cErro 	:= ""

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser( ::UserCode, "MTSALESFORECAST", "DELSALESFORECAST" )
	aAdd( aDados, { "C4_PRODUTO", ::ProductCode, } )

	SC4->( dbSetOrder( 1 ) )
	If SC4->( MsSeek( xFilial( "SC4" )+::ProductCode ) )
		MATA700( aDados, 5 )
	Else
		lRetorno := .F.
		SetSoapFault( "DELSALESFORECAST", STR0007 ) //"Produto não encontrado"
	EndIf             

	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len( aErro )
			cErro += aErro[nX] + Chr( 13 )+Chr( 10 )
		Next nX
		SetSoapFault( "DELSALESFORECAST", cErro )
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

Function wsmat700()
Return