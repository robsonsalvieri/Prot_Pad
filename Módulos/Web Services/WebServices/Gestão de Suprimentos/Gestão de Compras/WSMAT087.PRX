#INCLUDE "WSMAT087.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSMAT087   Autor Eduardo Riera           Data 08.08.2003  
Ĵ
Descrio  Web Service responsavel pelos compradores do sistema         
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE MtPurchaser              DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtpurchaser.apw" //"Servio de consulta a tabela de compradores do sistema"
   WSDATA UserCode                 AS String
   WSDATA PurchaserId              AS String
   WSDATA Purchaser                AS ContactView
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA HeaderType               AS String
   WSDATA TypeOfPhone              AS Array Of GenericStruct

   WSMETHOD GetHeader       DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD GetTypeOfPhone  DESCRIPTION STR0003 //"Mtodo que descreve os tipo de telefones"
   WSMETHOD GetPurchaser    DESCRIPTION "Mtodo de consulta a listagem das informaes de compradores"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao do header das moedas do sistema        
                                                                       
Ĵ
ParametrosNenhum                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtPurchaser

Local nX := 0 

::Header := MtHeader(::HeaderType)

If Empty(::HeaderType)
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("Y1_COD")
	::Header[nX]:HeaderField   := "CONTACTID"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO
	::Header[nX]:HeaderComboBox:= X3Cbox()   
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
	
	aadd(::Header,WSClassNew("BrwHeader"))
	nX++
	::Header[nX]:HeaderTitle  := RetTitle("Y1_NOME")
	::Header[nX]:HeaderField   := "NAME"
	::Header[nX]:HeaderPicture := SX3->X3_PICTURE
	::Header[nX]:HeaderSize    := SX3->X3_TAMANHO
	::Header[nX]:HeaderDec     := SX3->X3_DECIMAL
	::Header[nX]:HeaderType    := SX3->X3_TIPO	
	::Header[nX]:HeaderComboBox:= X3Cbox()   
	::Header[nX]:HeaderOblig   := X3OBRIGAT(SX3->X3_CAMPO)
	
EndIf
Return(.T.)

/*/

Ŀ
Funo    GetPurcharAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao dos contatos do sistema                
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve as moedas do sistema                     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetPurchaser WSRECEIVE UserCode,PurchaserId WSSEND Purchaser WSSERVICE MtPurchaser

Local aArea    := GetArea()
Local lRetorno := .T.
Local nY       := 0

If PrtChkUser(::UserCode,"MTPURCHASER")
	dbSelectArea("SY1")
	dbSetOrder(1)
	If MsSeek(xFilial("SY1")+::PurchaserId)
	    
		::Purchaser := WsClassNew( "ContactView" )	
		::Purchaser:ContactID              := SY1->Y1_COD
		::Purchaser:Name                   := SY1->Y1_NOME
		::Purchaser:Email                  := SY1->Y1_EMAIL			
		UserFields("SY1",@::Purchaser:UserFields,"SY1")

		::Purchaser:Phones:={}
		
		If !Empty(SY1->Y1_TEL)
			aadd(::Purchaser:Phones,WsClassNew("PhoneView"))
			nY++
			::Purchaser:Phones[nY]:TypeOfPhone     := "1"
			::Purchaser:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel(SY1->Y1_TEL)[3],15))
			::Purchaser:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel(SY1->Y1_TEL)[2],15))
			::Purchaser:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel(SY1->Y1_TEL)[1],15))
		EndIf
		If !Empty(SY1->Y1_FAX)
			aadd(::Purchaser:Phones,WsClassNew("PhoneView"))
			nY++
			::Purchaser:Phones[nY]:TypeOfPhone     := "2"
			::Purchaser:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel(SY1->Y1_FAX)[3],15))
			::Purchaser:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel(SY1->Y1_FAX)[2],15))
			::Purchaser:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel(SY1->Y1_FAX)[1],15))
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETPURCHASER",STR0004)	 //"Comprador nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtPurchaser

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:description := "COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:description := "FAX"

Function wsmat087()
Return