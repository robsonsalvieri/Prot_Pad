#INCLUDE "wsmat150.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT150  ³ Autor ³Eduardo Riera          ³ Data ³08.08.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pelas cotacoes do sistema            ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSupplierQuote          DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtsupplierquote.apw" //"Serviço de consulta e atualização das cotações de compras ( <b>Restrição de fornecedor</b> )"
   WSDATA HeaderType               AS String
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA UserCode                 AS String
   WSDATA QuoteHeader              AS Array Of QuoteHeaderView
   WSDATA ExpiryDateFrom           As Date OPTIONAL
   WSDATA ExpiryDateTo             As Date OPTIONAL
   WSDATA Supplier                 As String
   WSDATA QuoteID                  As String
   WSDATA Quote                    AS QuoteView
   WSDATA Quotes                   As Array of QuoteView
   WSDATA WsNull                   AS Integer
   WSDATA QueryAddWhere            AS String

   WSMETHOD GetHeader     DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
   WSMETHOD BrwQuote      DESCRIPTION STR0003 //"Método de listagem das informações das cotações em aberto"
   WSMETHOD GetQuote      DESCRIPTION STR0004 //"Método de consulta das informações de cotação em aberto"
   WSMETHOD PutQuote      DESCRIPTION STR0005 //"Método de atualização das informações de cotação em aberto"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³08.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao do header das cotacoes do sistema      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSupplierQuote

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwQuote  ³Autor  ³ Eduardo Riera         ³ Data ³11.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao das cotacoes do Sistema                ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Fornecedor                                            ³±±
±±³          ³ExpD3: Data Inicial                                          ³±±
±±³          ³ExpD4: Data Final                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve as cotacaoes em aberto do fornecedor     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwQuote WSRECEIVE UserCode,Supplier,ExpiryDateFrom,ExpiryDateTo,QueryAddWhere WSSEND QuoteHeader WSSERVICE MtSupplierQuote

Local aArea    := GetArea()
Local aStru    := dbStruct()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local nPos 	   := 0
Local cQuery   := ""
Local cAliasSC8:= "SC8"
Local cFornece := SubStr(::Supplier,1,Len(SA2->A2_COD))
Local cLoja    := SubStr(::Supplier,Len(SA2->A2_COD)+1)
Local dDataIni := ::ExpiryDateFrom
Local dDataFim := ::ExpiryDateTo
Local oQry 	   := Nil

DEFAULT dDataIni := dDataBase
DEFAULT dDataFim := dDataBase+180

If PrtChkUser(::UserCode,"MtSupplierQuote")
	dbSelectArea("SC8")
	dbSetOrder(2)
	#IFDEF TOP
		lQuery := .T.
		aStru  := SC8->(dbStruct())
		cAliasSC8 := "BrwQuote"

		cQuery := "SELECT DHU.DHU_STATUS, SC8.* "
		cQuery += "FROM "+RetSqlName("SC8")+" SC8 "

		cQuery += "LEFT JOIN "+RetSqlName("DHU")+" DHU "
		cQuery += "ON DHU.DHU_FILIAL = ? "
		cQuery += "AND DHU.DHU_NUM = SC8.C8_NUM "
		cQuery += "AND DHU.D_E_L_E_T_ = ' ' "

		cQuery += "WHERE SC8.C8_FILIAL= ? AND "
		cQuery += "SC8.C8_FORNECE= ? AND "
		cQuery += "SC8.C8_LOJA= ? AND "
		cQuery += "SC8.C8_VALIDA>= ? AND "
		cQuery += "SC8.C8_VALIDA<= ? AND "
		cQuery += "SC8.D_E_L_E_T_=' ' "
		cQuery := (WsQueryAdd(cQuery,::QueryAddWhere))
		cQuery += "ORDER BY SC8.C8_FILIAL, SC8.C8_NUM "

		oQry := FWPreparedStatement():New()
		oQry:SetQuery(cQuery)
		oQry:SetString(1, xFilial("SC8"))
		oQry:SetString(2, xFilial("SC8"))
		oQry:SetString(3, cFornece)
		oQry:SetString(4, cLoja)
		oQry:SetString(5, Dtos(dDataIni))
		oQry:SetString(6, Dtos(dDataFim))

		cQuery := oQry:GetFixQuery()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC8,.T.,.T.)

		For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C"
				TcSetField(cAliasSC8,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf
		Next nX
	#ELSE
		MsSeek(xFilial("SC8")+Dtos(dDataIni),.T.)
	#ENDIF
	nX := 0
	::QuoteHeader := {}
	While !Eof() .And. xFilial("SC8") == (cAliasSC8)->C8_FILIAL .And.;
		(cAliasSC8)->C8_VALIDA<=dDataFim
		If cFornece == (cAliasSC8)->C8_FORNECE .And.;
			cLoja == (cAliasSC8)->C8_LOJA

			nPos := aScan(::QuoteHeader,{|x| x:QuoteID == (cAliasSC8)->C8_NUM})
			If nPos == 0
				aadd(::QuoteHeader,WsClassNew("QuoteHeaderView"))
				nX++

				GetSC8Head(@::QuoteHeader[nX],cAliasSC8)
			ELSE 
				GetSC8Head(@::QuoteHeader[nPos],cAliasSC8)			
			EndIf
		EndIf
		dbSelectArea(cAliasSC8)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSC8)
		dbCloseArea()
		dbSelectArea("SC8")
		FreeObj(oQry)
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetQuote  ³Autor  ³ Eduardo Riera         ³ Data ³11.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao das cotacoes do Sistema                ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Numero da Cotacao                                     ³±±
±±³          ³ExpC3: Fornecedor                                            ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve as cotacaoes em aberto do fornecedor     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetQuote WSRECEIVE UserCode,QuoteID,Supplier,QueryAddWhere WSSEND Quotes WSSERVICE MtSupplierQuote

Local aArea    := GetArea() 
Local aProposta:= {}
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local nY       := 0
Local cAliasSC8:= "SC8"
Local cFornece := SubStr(::Supplier,1,Len(SA2->A2_COD))
Local cLoja    := SubStr(::Supplier,Len(SA2->A2_COD)+1)
#IFDEF TOP
Local aStru    := dbStruct()
Local oQry 	   := Nil
Local cQuery   := ""
#ENDIF

If PrtChkUser(::UserCode,"MTSUPPLIERQUOTE","GETQUOTE","SA2",::Supplier)
	dbSelectArea("SC8")
	dbSetOrder(1)
	#IFDEF TOP
		lQuery := .T.
		aStru  := SC8->(dbStruct())
		cAliasSC8 := "BrwQuote"

		cQuery := "SELECT DHU.DHU_STATUS, SC8.* FROM "+RetSqlName("SC8")+" SC8 "

		cQuery += "LEFT JOIN "+RetSqlName("DHU")+" DHU "
		cQuery += "ON DHU.DHU_FILIAL = ? "
		cQuery += "AND DHU.DHU_NUM = SC8.C8_NUM "
		cQuery += "AND DHU.D_E_L_E_T_ = ' ' "

		cQuery += "WHERE SC8.C8_FILIAL= ? AND "
		cQuery += "SC8.C8_FORNECE= ? AND "
		cQuery += "SC8.C8_LOJA= ? AND "
		cQuery += "SC8.C8_NUM= ? AND "
		cQuery += "SC8.D_E_L_E_T_=' ' "
		cQuery := (WsQueryAdd(cQuery,::QueryAddWhere))
		cQuery += "ORDER BY SC8.C8_FILIAL, SC8.C8_NUM"

		oQry := FWPreparedStatement():New()
		oQry:SetQuery(cQuery)
		oQry:SetString(1, xFilial("SC8"))
		oQry:SetString(2, xFilial("SC8"))
		oQry:SetString(3, cFornece)
		oQry:SetString(4, cLoja)
		oQry:SetString(5, ::QuoteID)

		cQuery := oQry:GetFixQuery()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC8,.T.,.T.)	

		For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C"
				TcSetField(cAliasSC8,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf
		Next nX
	#ELSE
		MsSeek(xFilial("SC8")+::QuoteID)
	#ENDIF
	While !Eof() .And. xFilial("SC8") == (cAliasSC8)->C8_FILIAL .And.;
		(cAliasSC8)->C8_NUM==::QuoteID
		If cFornece == (cAliasSC8)->C8_FORNECE .And.;
			cLoja == (cAliasSC8)->C8_LOJA
			
			//posiciona a SC8 pelo RECNO para não causar conflito com o campo C8_OBS (memo)
			SC8->(DbGoTo((cAliasSC8)->R_E_C_N_O_))

			nY := aScan(aProposta,(cAliasSC8)->C8_NUMPRO)

			If nY == 0
				aadd(aProposta,(cAliasSC8)->C8_NUMPRO)
				aadd(::Quotes,WsClassNew("QuoteView"))
				nY := Len(aProposta)
				::Quotes[nY]:QuoteHeader := WsClassNew("QuoteHeaderView")
				GetSC8Head(@::Quotes[nY]:QuoteHeader,cAliasSC8)
				DEFAULT ::Quotes[nY]:QuoteItem := {}
			ELSE
				GetSC8Head(@::Quotes[nY]:QuoteHeader,cAliasSC8)
				DEFAULT ::Quotes[nY]:QuoteItem := {}
			EndIf
			nX := Len(::Quotes[nY]:QuoteItem)
			nX++
			aadd(::Quotes[nY]:QuoteItem,WsClassNew("QuoteItemView"))
			GetSC8Item(@::Quotes[nY]:QuoteItem[nX],cAliasSC8)
		EndIf
		dbSelectArea(cAliasSC8)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSC8)
		dbCloseArea()
		FreeObj(oQry)
	EndIf
	If !Empty(::Quotes)
		If Empty(::Quotes[1]:QuoteItem)
			SetSoapFault("GETQUOTE","Cotacao invalida")
			lRetorno := .F.	
		EndIf
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutQuote  ³Autor  ³ Eduardo Riera         ³ Data ³11.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao das cotacoes do Sistema                ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Fornecedor                                            ³±±
±±³          ³ExpO3: Cotacao                                               ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve as cotacaoes em aberto do fornecedor     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutQuote WSRECEIVE UserCode,Supplier,Quote WSSEND QuoteID WSSERVICE MtSupplierQuote

Local aArea 	 := GetArea() 
Local aCabec 	 := {}
Local aItens 	 := {}
Local aErro 	 := {}
Local aItensAnt  := {}
Local lRetorno	 := .T.
Local lZerou	 := .F.
Local nI 		 := 0
Local cCotacao	 := ::Quote:QuoteHeader:QuoteID
Local cFornece	 := SubStr( ::Supplier, 1, Len( SC8->C8_FORNECE ) )
Local cLoja 	 := SubStr( ::Supplier, Len( SC8->C8_FORNECE ) + 1 )
Local cItem 	 := ::Quote:QuoteItem[1]:SequentialID
Local cProposta	 := ::Quote:QuoteHeader:ProposalID
Local aUsrFields := ::Quote:QuoteHeader:UserFields
Local cErro    	 := ""
Local nPosCond   := 0
Local cCond		 := ""
Local nPosFil	 := 0
Local nPosVlFr	 := 0
Local nPosTotF	 := 0
Local nX		 := 0
Local nZ		 := 0
Local nPosDesc	 := 0
Local nPosZer	 := 0
Local lPgc 		 := .F.

Local cUltPropos := ""
Local cQuery   	 := ""
Local oQry 		 := Nil
Local cAliasSC8  := ""
Local lPropAtual := .F.

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

WsChangeModule( "SIGACOM" )

If PrtChkUser( ::UserCode, "MTSIMULATIONQUOTE", "PUTQUOTE", "SA2", ::Supplier )
	SC8->( DbSetOrder( 1 ) )
	SC8->( MsSeek( xFilial( "SC8" ) + cCotacao + cFornece + cLoja + cItem  ) )

	lPgc := if(Alltrim(SC8->C8_ORIGEM) $ "PGCA020|NFCA020" .and. !A150GetPGC(),.T., .F.)//Verifica se cotação foi gerada pelo PGC.

	If lPgc 
		cAliasSC8 := GetNextAlias()	
		cQuery := "SELECT MAX(C8_NUMPRO) C8_NUMPRO FROM "+RetSqlName("SC8")+" SC8 "
		cQuery += "WHERE SC8.C8_FILIAL= ? AND "
		cQuery += "SC8.C8_FORNECE = ? AND "
		cQuery += "SC8.C8_LOJA = ? AND "
		cQuery += "SC8.C8_NUM = ? AND "
		cQuery += "SC8.D_E_L_E_T_=' ' "

		oQry := FWPreparedStatement():New()
		oQry:SetQuery(cQuery)
		oQry:SetString(1, xFilial("SC8"))
		oQry:SetString(2, cFornece)
		oQry:SetString(3, cLoja)
		oQry:SetString(4, cCotacao)

		cQuery := oQry:GetFixQuery()

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC8,.T.,.T.)	

		If SC8->(!Eof())
			cUltPropos := (cAliasSC8)->C8_NUMPRO
		EndIf

		If cUltPropos == cProposta 
			lPropAtual := .T. 
		EndIf
	
		dbSelectArea(cAliasSC8)
		dbCloseArea()
		FreeObj(oQry)
	EndIf

	PutSC8Head( ::Quote:QuoteHeader, @aCabec, ::Quote:QuoteHeader:PaymentPlanCode )
	aCabec := WsAutoOpc( @aCabec, .F. )

	// Valida condição de pagamento com código zero
	nPosCond := aScan(aCabec,{|x| AllTrim(x[1])=="C8_COND"})
	If nPosCond > 0
		cCond := aCabec[nPosCond][2]
		aCabec[nPosCond][2] := PADR(cCond,3)
	Else
		aadd(aCabec,{"C8_COND","",NIL})
	EndIf

	// Valida tamanho do campo filial caso tenha sido aumentado pelo Configurador
	nPosFil := aScan(aCabec,{|x| AllTrim(x[1])=="C8_LOJA"})
	If nPosFil > 0
		aCabec[nPosFil][2] := cLoja
	EndIf

	PutSC8Item( ::Quote:QuoteItem, @aItens )

	aItensAnt := aItens

	For nI := 1 To Len( aItens )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava os campos de usuarios do cabecalho da cotacao tambem no array     ³
		//³aItens pois o MATA150 nao grava campos de Usuario passados no aCabec    ³
		//³pois a tabela SC8 de cotacoes so possuem itens e os campos de cabecalho ³
		//³sao definidos diretamente no mata150 atravez de variaveis.              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		PutUserFields("SC8", @aUsrFields , aItens[nI] )

		aItens[nI] := WsAutoOpc( @aItens[nI], .T. )

		nPosVlFr := aScan(aItens[nI],{|x| AllTrim(x[1])=="C8_VALFRE"})
		nPosTotF := aScan(aCabec,{|x| AllTrim(x[1])=="C8_TOTFRE"})
		If nPosVlFr > 0 .And. nPosTotF > 0 .And. ;
		aScan(aUsrFields,{|x| AllTrim(Upper(x:UserName)) == "C8_TOTFRE" }) == 0
			aCabec[nPosTotF][2] += aItens[nI][nPosVlFr][2]
		EndIf
	Next nI

	// Verifica se o usuario zerou manualmente a alíquota padrão do IPI do produto
	For nZ := 1 to Len(aItensAnt)
		lZerou := .F.
	    nPosZer := aScan(aItensAnt[nZ],{|x| x[1] == "MANUAL-IPI"})
		If nPosZer > 0
			lZerou := aItensAnt[nZ][nPosZer][2]
		EndIf
		If lZerou
			Aadd(aItens[nZ],{"C8_BASEIPI",0,Nil})
			Aadd(aItens[nZ],{"C8_ALIIPI" ,0,Nil})
			Aadd(aItens[nZ],{"C8_VALIPI" ,0,Nil})
		EndIf	
	Next nZ
	nPosZer := 0
	// Verifica	 se o usuario zerou manualmente a alíquota padrão do ICM do produto
	For nZ := 1 to Len(aItensAnt)
		lZerou := .F.
	    nPosZer := aScan(aItensAnt[nZ],{|x| x[1] == "MANUAL-ICM"})
		If nPosZer > 0
			lZerou := aItensAnt[nZ][nPosZer][2]
		EndIf
		If lZerou
			Aadd(aItens[nZ],{"C8_BASEICM",0,Nil})
			Aadd(aItens[nZ],{"C8_PICM" ,0,Nil})
			Aadd(aItens[nZ],{"C8_VALICM" ,0,Nil})
		EndIf	
	Next nZ

	// Caso o usuário tenha zerado o valor do desconto, deve passar zero no array
	For nX := 1 To Len(aItens)
		If (nPosDesc := aScan(aItens[nX],{|x| x[1] == "C8_DESC"})) == 0
			AADD(aItens[nX],{"C8_DESC",0,Nil})
		EndIf
	Next nX

	//Adiciona o valor do C8_VALIDA nos Itens apenas se foi gerado pelo NFC
	If lPgc
		For nX := 1 To Len(aItens)
			Aadd(aItens[nX],{"C8_VALIDA", SC8->C8_VALIDA, Nil})
		Next nX 
	EndIf

	If SC8->( MsSeek( xFilial( "SC8" ) + cCotacao + cFornece + cLoja + cItem + PadR( cProposta, 2 ) ) )

		If lPgc
			If lPropAtual 
				Mata150( @aCabec, @aItens, 3 )
			Else 
				SetSoapFault("PUTQUOTE","Cotacao nao é a atual")
			EndIf
		Else
			Mata150( @aCabec, @aItens, 3 )
		EndIf	
	Else
		//Posiciona na cotação do fornecedor e loja
		If SC8->( MsSeek( xFilial( "SC8" ) + cCotacao + cFornece + cLoja ) ) 
			While !SC8->( EOF() ) .AND. SC8->C8_NUM == cCotacao;
				.AND. SC8->C8_FORNECE == cFornece ;
				.AND. SC8->C8_LOJA == cLoja

				cProposta := SC8->C8_NUMPRO

				SC8->( DbSkip() )
			EndDo

			aAdd( aCabec, { "C8_NUMPRO", StrZero( Val( cProposta ) + 1, 2 ), Nil } )

			For nI := 1 To Len( aItens )
				aAdd( aItens[nI], { "C8_NUMPRO", StrZero( Val( cProposta ) + 1, 2 ), Nil } )
			Next nI

			Mata150( @aCabec, @aItens, 4 )
		EndIf
	EndIf

	::QuoteID := cCotacao

	If lMsErroAuto
		aErro := GetAutoGRLog()
		cErro := ""
		For nI := 1 To Len( aErro )
			cErro += aErro[nI] + Chr( 13 ) + Chr( 10 )
		Next nI

		SetSoapFault( "PUTQUOTE", cErro )
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return( lRetorno )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT150  ³ Autor ³Eduardo Riera          ³ Data ³08.08.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pelas simulacoes de cotacao          ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSimulationQuote        DESCRIPTION STR0006 NAMESPACE "http://webservices.microsiga.com.br/mtsimulationquote.apw" //"Serviço de simulação da atualização das cotações de compra <br><br><i>Este serviço pode ser utilizado para recuperar o preenchimento automático de algumas informações da cotações de compra, como por exemplo: o calculo dos impostos. Salientamos que este serviço executa as customização presentes no sistema, possibilitando seu uso.</i>"
   WSDATA UserCode                 As String
   WSDATA QuoteHeader              AS Array Of QuoteHeaderView
   WSDATA Supplier                 As String
   WSDATA OldQuote                 As QuoteView
   WSDATA NewQuote                 As QuoteView

   WSMETHOD PutQuote   DESCRIPTION STR0007 //"Método de simulação da atualização da cotação de compra"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutQuote  ³Autor  ³ Eduardo Riera         ³ Data ³11.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de simulacao da atualizacao das cotacoes do sistema   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Fornecedor                                            ³±±
±±³          ³ExpO3: Cotacao                                               ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo simula a atualizacao de um cotacao               ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutQuote WSRECEIVE UserCode,Supplier,OldQuote WSSEND NewQuote WSSERVICE MtSimulationQuote

Local aArea 	:= GetArea()
Local aCabec 	:= {}
Local aItens 	:= {}
Local aErro 	:= {}
Local lRetorno	:= .T.
Local nI 		:= 0
Local cCotacao	:= ::OldQuote:QuoteHeader:QuoteID
Local cFornece	:= SubStr( ::Supplier, 1, Len( SC8->C8_FORNECE ) )
Local cLoja 	:= SubStr( ::Supplier, Len( SC8->C8_FORNECE ) + 1 )
Local cItem 	:= ::OldQuote:QuoteItem[1]:SequentialID
Local cProposta	:= ::OldQuote:QuoteHeader:ProposalID
Local aUsrFields:= ::OldQuote:QuoteHeader:UserFields
Local cErro    	:= ""
Local nPosZer	:= 0
Local lZerou	:= .F.
Local aItensAnt := {}
Local nZ		:= 0
Local nPosCond  := 0
Local cCond		:= ""
Local nPosFil	:= 0
Local nPosVlFr	:= 0
Local nPosTotF	:= 0
Local nX		:= 0
Local nPosDesc	:= 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

WsChangeModule( "SIGACOM" )

If PrtChkUser( ::UserCode, "MTSIMULATIONQUOTE", "PUTQUOTE", "SA2", ::Supplier )
	SC8->( DbSetOrder( 1 ) )
	SC8->( MsSeek( xFilial( "SC8" ) + cCotacao + cFornece + cLoja + cItem  ) )

	PutSC8Head( ::OldQuote:QuoteHeader, @aCabec, ::OldQuote:QuoteHeader:PaymentPlanCode )
	aCabec := WsAutoOpc( @aCabec, .F. )

	// Valida condição de pagamento com código zero
	nPosCond := aScan(aCabec,{|x| AllTrim(x[1])=="C8_COND"})
	If nPosCond > 0
		cCond := aCabec[nPosCond][2]
		aCabec[nPosCond][2] := PADR(cCond,3)
	Else
		aadd(aCabec,{"C8_COND","",NIL})
	EndIf

	// Valida tamanho do campo filial caso tenha sido aumentado pelo Configurador
	nPosFil := aScan(aCabec,{|x| AllTrim(x[1])=="C8_LOJA"})
	If nPosFil > 0
		aCabec[nPosFil][2] := cLoja
	EndIf

	PutSC8Item( ::OldQuote:QuoteItem, @aItens )

	aItensAnt := aItens

	For nI := 1 To Len( aItens )
		aItens[nI] := WsAutoOpc( @aItens[nI], .T. )

		nPosVlFr := aScan(aItens[nI],{|x| AllTrim(x[1])=="C8_VALFRE"})
		nPosTotF := aScan(aCabec,{|x| AllTrim(x[1])=="C8_TOTFRE"})
		If nPosVlFr > 0 .And. nPosTotF > 0 .And. ;
		aScan(aUsrFields,{|x| AllTrim(Upper(x:UserName)) == "C8_TOTFRE" }) == 0
			aCabec[nPosTotF][2] += aItens[nI][nPosVlFr][2]		
		EndIF
	Next nI

	// Verifica se o usuario zerou manualmente a alíquota padrão do IPI do produto
	For nZ := 1 to Len(aItensAnt)
		lZerou := .F.
	    nPosZer := aScan(aItensAnt[nZ],{|x| x[1] == "MANUAL-IPI"})
		If nPosZer > 0
			lZerou := aItensAnt[nZ][nPosZer][2]
		EndIf
		If lZerou
			Aadd(aItens[nZ],{"C8_BASEIPI",0,Nil})
			Aadd(aItens[nZ],{"C8_ALIIPI" ,0,Nil})
			Aadd(aItens[nZ],{"C8_VALIPI" ,0,Nil})			
		EndIf	
	Next nZ
	nPosZer := 0
	For nZ := 1 to Len(aItensAnt)
		lZerou := .F.
	    nPosZer := aScan(aItensAnt[nZ],{|x| x[1] == "MANUAL-ICM"})
		If nPosZer > 0
			lZerou := aItensAnt[nZ][nPosZer][2]
		EndIf
		If lZerou			
			Aadd(aItens[nZ],{"C8_BASEICM",0,Nil})
			Aadd(aItens[nZ],{"C8_PICM" ,0,Nil})
			Aadd(aItens[nZ],{"C8_VALICM" ,0,Nil})
			
		EndIf	
	Next nZ

	// Caso o usuário tenha zerado o valor do desconto, deve passar zero no array
	For nX := 1 To Len(aItens)
		If (nPosDesc := aScan(aItens[nX],{|x| x[1] == "C8_DESC"})) == 0
			AADD(aItens[nX],{"C8_DESC",0,Nil}) 
		EndIf
	Next nX

	//nao envia o C8_TOTAL no Array para que a rotina automatica calcule o 
	//C8_TOTAL e em seguida retorne para o acols 
	aItensNew:=Aclone(aItens)
	For nI:=1 to Len(aItensNew)                         
		nPosTotal:=aScan(aItensNew[nI],{|x| AllTrim(x[1]) == "C8_TOTAL"}) 
		If nPosTotal>0
			aDel(aItensNew[nI],nPosTotal)
			aSize(aitensNew[nI],Len(aItensNew[nI])-1)
		EndIf                                                                   
	Next nI                                        
	aItens:=aClone(aItensNew)
	
	If SC8->( MsSeek( xFilial( "SC8" ) + cCotacao + cFornece + cLoja + cItem + PadR( cProposta, 2 ) ) )
		Mata150( @aCabec, @aItens, 3, , .T. )
	Else
		//Posiciona na cotação do fornecedor e loja
		If SC8->( MsSeek( xFilial( "SC8" ) + cCotacao  + cFornece + cLoja ) )
			While !SC8->( EOF() )	.AND. SC8->C8_NUM == cCotacao ;
				.AND. SC8->C8_FORNECE == cFornece ;
				.AND. SC8->C8_LOJA == cLoja

				cProposta := SC8->C8_NUMPRO
				SC8->( DbSkip() )
      
			EndDo

			aAdd( aCabec, { "C8_NUMPRO", StrZero( Val( cProposta ) + 1, 2 ), Nil } )

			For nI := 1 To Len( aItens )
				aAdd( aItens[nI], { "C8_NUMPRO", StrZero( Val( cProposta ) + 1, 2 ), Nil } )
			Next nI

			Mata150( @aCabec, @aItens, 4, , .T. )
		EndIf
	EndIf

	If lMsErroAuto
		aErro := GetAutoGRLog()
		cErro := ""
		For nI := 1 To Len( aErro )
			cErro += aErro[nI] + Chr( 13 ) + Chr( 10 )
		Next nI

		SetSoapFault( "PUTQUOTE", cErro )
		lRetorno := .F.
	EndIf

	If lRetorno
		aCabec := WsAutoOpc( @aCabec, .F. )

		For nI := 1 To Len( aItens )
			aItens[nI] := WsAutoOpc( @aItens[nI], .T. )
		Next nI

		GetSC8Head( @::NewQuote:QuoteHeader, "SC8", @aCabec )

		For nI := 1 To Len( aItens )
			aadd( ::NewQuote:QuoteItem, WSClassNew( "QuoteItemView" ) )
			dbSelectArea("SC8")
			dbSetOrder(1)
			MsSeek(xFilial("SC8")+WsProcH(aCabec,"C8_NUM")+WsProcH(aCabec,"C8_FORNECE")+WsProcH(aCabec,"C8_LOJA")+WsProcH(aItens[nI],"C8_ITEM")+WsProcH(aItens[nI],"C8_NUMPRO"))		
			GetSC8Item( @::NewQuote:QuoteItem[nI], "SC8", @aItens[nI] )
		Next nI
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return( lRetorno )


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSC8Head³Autor  ³ Eduardo Riera         ³ Data ³05.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Header da cotacao de compra para a³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Header                                      ³±±
±±³          ³ExpA2: Array com os dados do Cabecalho                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo atualiza a variavel aCab com os dados do objeto  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutSC8Head(oObjeto,aCab,cCondPag)

Local cFornece := SubStr(oObjeto:Supplier,1,Len(SA2->A2_COD))
Local cLoja    := SubStr(oObjeto:Supplier,Len(SA2->A2_COD)+1)

aadd(aCab,{"C8_NUM",oObjeto:QuoteID,Nil})
aadd(aCab,{"C8_NUMPRO",oObjeto:ProposalID,Nil})
aadd(aCab,{"C8_FORNECE",cFornece,Nil})
aadd(aCab,{"C8_LOJA"   ,cLoja,Nil})
aadd(aCab,{"C8_COND"   ,cCondPag,Nil})
aadd(aCab,{"C8_CONTATO",oObjeto:Contact,Nil})
aadd(aCab,{"C8_EMISSAO",oObjeto:RegisterDate,Nil})

// Verifica se o campo de Total de Frete foi informado pelo Usuario
If aScan(oObjeto:UserFields,{|x| AllTrim(Upper(x:UserName)) == "C8_TOTFRE" }) == 0
	aadd(aCab,{"C8_TOTFRE" ,0,Nil})
Endif

aadd(aCab,{"C8_MOEDA"  ,oObjeto:Currency,Nil})
aadd(aCab,{"C8_TXMOEDA",oObjeto:CurrencyRate,Nil})
aadd(aCab,{"C8_DESC1"  ,oObjeto:DiscountInCascade1,Nil})
aadd(aCab,{"C8_DESC2"  ,oObjeto:DiscountInCascade2,Nil})
aadd(aCab,{"C8_DESC3"  ,oObjeto:DiscountInCascade3,Nil})
aadd(aCab,{"C8_MSG",SC8->C8_MSG,Nil})

PutUserFields("SC8",@oObjeto:UserFields,aCab)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutSC8Item³Autor  ³ Eduardo Riera         ³ Data ³05.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento dos itens da cotacao de compra para a³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Item                                        ³±±
±±³          ³ExpA2: Array com os dados dos itens                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo atualiza a variavel aitem com os dados do objeto ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutSC8Item(oObjeto,aItens)

Local nX       := 0
Local nY       := 0
Local nZ       := 0
Local aImposto := {}
Local aLinha   := {}
Local nBase	   := 0
Local nTaxa	   := 0
Local nPBaseIPI := 0		// Posicao do campo C8_BASEIPI no UserFields caso tenha sido incluido via ponto de entrada
Local nPRateICM := 0
Local nPBaseICM := 0		// Posicao do campo C8_BASEICM no UserFields caso tenha sido incluido via ponto de entrada
Local nValBasIPI:= 0
Local nVBaseICM:= 0
Local nVRateICM:= 0
Local lIPI	   := .F.
Local lICM	   := .F.

DEFAULT aItens := {}

For nX := 1 To Len(oObjeto)

	aLinha := {}

	aadd(aLinha,{"C8_ITEM",	oObjeto[nX]:SequentialID,Nil})
	aadd(aLinha,{"C8_NUMPRO",oObjeto[nX]:ProposalID,Nil})
	aadd(aLinha,{"C8_PRODUTO",oObjeto[nX]:ProductCode,Nil})
	aadd(aLinha,{"C8_UM",oObjeto[nX]:MeasureUnit,Nil})
	aadd(aLinha,{"C8_QUANT",oObjeto[nX]:Quantity,Nil})
	aadd(aLinha,{"C8_PRECO",oObjeto[nX]:UnitPrice,Nil})
	aadd(aLinha,{"C8_TOTAL",iif(oObjeto[nX]:TotalValue == 0, (oObjeto[nX]:Quantity*oObjeto[nX]:UnitPrice),oObjeto[nX]:TotalValue),Nil})
	aadd(aLinha,{"C8_DESC",oObjeto[nX]:DiscountPercent,Nil})
	aadd(aLinha,{"C8_VLDESC",oObjeto[nX]:DiscountValue,Nil})
	aadd(aLinha,{"C8_COND",oObjeto[nX]:PaymentPlanCode,Nil})
	aadd(aLinha,{"C8_PRAZO",oObjeto[nX]:DeliveryTermInDays,Nil})
	aadd(aLinha,{"C8_OBS",oObjeto[nX]:Notes,Nil})
	aadd(aLinha,{"C8_VALIDA",oObjeto[nX]:ExpiryDate,Nil})
	aadd(aLinha,{"C8_DATPRF",oObjeto[nX]:DeliveryDate,Nil})
	aadd(aLinha,{"C8_DESPESA",oObjeto[nX]:ExpensesValue,Nil})
	aadd(aLinha,{"C8_SEGURO",oObjeto[nX]:InsuranceValue,Nil})
	aadd(aLinha,{"C8_VALFRE",oObjeto[nX]:FreightValue,Nil})
	aadd(aLinha,{"C8_ALIIPI",oObjeto[nX]:TaxRate,Nil})
	aadd(aLinha,{"C8_FILENT",SC8->C8_FILENT,Nil})
	aadd(aLinha,{cEmpAnt,oObjeto[nX]:Code,Nil})
	aadd(aLinha,{cFilAnt,oObjeto[nX]:Branch,Nil})

	aImposto := MaFisRefLd("SC8")

	If Len(oObjeto[nX]:Taxes) == 0
		oObjeto[nX]:Taxes:={}
	EndIf
	For nY := 1 To Len(oObjeto[nX]:Taxes)
		If oObjeto[nX]:Taxes[nY]:TaxCode $ "IPI"
			lIPI := .T.
		EndIf
		If oObjeto[nX]:Taxes[nY]:TaxCode $ "ICM"
			lICM := .T.
		EndIf
	Next nY

	// Altera a Base IPI caso usuario tenha incluido C8_BASEIPI via ponto de entrada e alterado valor manualmente
	If (nPBaseIPI := aScan(OOBJETO[nX]:USERFIELDS,{|x| x:USERNAME == "C8_BASEIPI"})) > 0
		nValBasIPI := VAL(OOBJETO[nX]:USERFIELDS[nPBaseIPI]:USERTAG)
	EndIf
	// Altera a Base ICM caso usuario tenha incluido C8_BASEICM via ponto de entrada e alterado valor manualmente
	If (nPBaseICM := aScan(OOBJETO[nX]:USERFIELDS,{|x| x:USERNAME == "C8_BASEICM"})) > 0
		nVBaseICM := VAL(OOBJETO[nX]:USERFIELDS[nPBaseICM]:USERTAG)
	EndIf
	// Altera o Rate ICM caso usuario tenha incluido C8_PICM via ponto de entrada e alterado valor manualmente
	If (nPRateICM := aScan(OOBJETO[nX]:USERFIELDS,{|x| AllTrim(x:USERNAME) == "C8_PICM"})) > 0
		nVRateICM := VAL(OOBJETO[nX]:USERFIELDS[nPRateICM]:USERTAG)
	EndIf

	If !lIPI
		aadd(oObjeto[nX]:Taxes,WsClassNew("TaxesView"))
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxCode   := "IPI"
		If nPBaseIPI > 0
			oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase   := nValBasIPI
		Else
			oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase   := oObjeto[nX]:TotalValue
		EndIf
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxRate   := oObjeto[nX]:TaxRate
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxAmount := oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase * ( oObjeto[nX]:TaxRate / 100 )
	EndIf
	
	If !lICM
	aadd(oObjeto[nX]:Taxes,WsClassNew("TaxesView"))
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxCode   := "ICM"
		If nPBaseICM > 0
			oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase   := nVBaseICM
		Else
			oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase   := oObjeto[nX]:TotalValue
		EndIf
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxRate   := oObjeto[nX]:TaxRate
		oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxAmount := oObjeto[nX]:Taxes[Len(oObjeto[nX]:Taxes)]:TaxBase * ( oObjeto[nX]:TaxRate / 100 )
	EndIf

	For nY := 1 To Len(oObjeto[nX]:Taxes)
		nZ := aScan(aImposto,{|x| x[1] == oObjeto[nX]:Taxes[nY]:TaxCode})
		If nZ <> 0
			If oObjeto[nX]:Taxes[nY]:TaxCode $ "IPI"
				If !Empty(aImposto[nZ][2])
					If nPBaseIPI > 0
						nBase := nValBasIPI
					ElseIf oObjeto[nX]:Taxes[nY]:TaxBase <> oObjeto[nX]:TotalValue			// Usuário alterou o preço
						nBase := oObjeto[nX]:TotalValue
					Else
						nBase := oObjeto[nX]:Taxes[nY]:TaxBase
					EndIf
					aadd(aLinha,{aImposto[nZ][2],nBase,Nil})
				EndIf
				If !Empty(aImposto[nZ][3])
					If oObjeto[nX]:Taxes[nY]:TaxRate <> oObjeto[nX]:TaxRate				// Usuário alterou o percentual do IPI
						nTaxa := oObjeto[nX]:TaxRate
					Else
						nTaxa := oObjeto[nX]:Taxes[nY]:TaxRate
					EndIf
					aadd(aLinha,{aImposto[nZ][3],nTaxa,Nil})
					If nTaxa == 0
						aadd(aLinha,{"MANUAL-IPI",.T.,Nil})
					EndIf
				EndIf
				If !Empty(aImposto[nZ][4])
					aadd(aLinha,{aImposto[nZ][4],(nBase * (nTaxa / 100)),Nil})
				EndIf
			ElseIf oObjeto[nX]:Taxes[nY]:TaxCode $ "ICM"
				If !Empty(aImposto[nZ][2])
					If nPBaseICM > 0
						nBase := nVBaseICM
					ElseIf oObjeto[nX]:Taxes[nY]:TaxBase <> oObjeto[nX]:TotalValue			// Usuário alterou o preço
						nBase := oObjeto[nX]:TotalValue
					Else
						nBase := oObjeto[nX]:Taxes[nY]:TaxBase
					EndIf
					aadd(aLinha,{aImposto[nZ][2],nBase,Nil})
				EndIf
				If !Empty(aImposto[nZ][3])
					If  nPRateICM > 0							// Usuário alterou o percentual do ICM
						nTaxa := nVRateICM
					Else
						nTaxa := oObjeto[nX]:Taxes[nY]:TaxRate
					EndIf
					aadd(aLinha,{aImposto[nZ][3],nTaxa,Nil})
					If nTaxa == 0
						aadd(aLinha,{"MANUAL-ICM",.T.,Nil})
					EndIf
				EndIf
				If !Empty(aImposto[nZ][4])
					aadd(aLinha,{aImposto[nZ][4],(nBase * (nTaxa / 100)),Nil})
				EndIf
			Else
				If !Empty(aImposto[nZ][2])
					aadd(aLinha,{aImposto[nZ][2],oObjeto[nX]:Taxes[nY]:TaxBase,Nil})
				EndIf
				If !Empty(aImposto[nZ][3])
					aadd(aLinha,{aImposto[nZ][3],oObjeto[nX]:Taxes[nY]:TaxRate,Nil})
				EndIf
				If !Empty(aImposto[nZ][4])
					aadd(aLinha,{aImposto[nZ][4],oObjeto[nX]:Taxes[nY]:TaxAmount,Nil})
				EndIf
			EndIf
		EndIf
	Next nY

	PutUserFields("SC8",@oObjeto[nX]:UserFields,aLinha)

	aadd(aItens,aLinha)

Next nX

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSC8Head³Autor  ³ Eduardo Riera         ³ Data ³05.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Objeto da cotacao de compra       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Header                                      ³±±
±±³          ³ExpC2: Alias do tabela                                       ³±±
±±³          ³ExpA3: Array com os dados do Cabecalho                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo atualiza a variavel aCab com os dados do objeto  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GetSC8Head(oObjeto,cAliasSC8,aCab)

Local cAliasSAJ := "SAJ"
Local lQuery    := .F.
Local lExsDHU	:= .F.
#IFDEF TOP
Local cQuery    := ""
#ENDIF
If aCab == Nil
	oObjeto:QuoteID            := (cAliasSC8)->C8_NUM
	oObjeto:ProposalID         := (cAliasSC8)->C8_NUMPRO
	oObjeto:Supplier           := (cAliasSC8)->C8_FORNECE+(cAliasSC8)->C8_LOJA
	oObjeto:RegisterDate       := (cAliasSC8)->C8_EMISSAO
	oObjeto:Contact            := (cAliasSC8)->C8_CONTATO
	oObjeto:PaymentPlanCode	   := (cAliasSC8)->C8_COND
	oObjeto:Currency           := (cAliasSC8)->C8_MOEDA
	oObjeto:CurrencyRate       := (cAliasSC8)->C8_TXMOEDA
	oObjeto:DiscountInCascade1 := (cAliasSC8)->C8_DESC1
	oObjeto:DiscountInCascade2 := (cAliasSC8)->C8_DESC2
	oObjeto:DiscountInCascade3 := (cAliasSC8)->C8_DESC3
	lExsDHU := !empty((cAliasSC8)->DHU_STATUS)
	oObjeto:QuoteStatus        := IIf( (!lExsDHU .and. Empty((cAliasSC8)->C8_NUMPED)) .Or. (lExsDHU .and. !(cAliasSC8)->DHU_STATUS $ "4/6"), STR0008, STR0009)	//"Aberto"###"Fechado"
								  
	UserFields("SC8",@oObjeto:UserFields,cAliasSC8)

    //..trata os campos MEMO's
	TrataSC8Memo(@oObjeto,cAliasSC8)

	If !Empty((cAliasSC8)->C8_GRUPCOM)
		dbSelectArea("SAJ")
		dbSetOrder(1)
		#IFDEF TOP
			cAliasSAJ := "BrwQuote2"
			lQuery := .T.
			
			cQuery := "SELECT * "
			cQuery += "FROM "+RetSqlName("SAJ")+" SAJ "
			cQuery += "WHERE SAJ.AJ_FILIAL= ? AND "
			cQuery += "SAJ.AJ_GRCOM = ? AND "
			cQuery += "D_E_L_E_T_=' ' "
			cQuery += "ORDER BY SAJ.AJ_FILIAL, SAJ.AJ_GRCOM, SAJ.AJ_ITEM "

			oQry := FWPreparedStatement():New()
			oQry:SetQuery(cQuery)

			oQry:SetString(1, xFilial("SAJ"))
			oQry:SetString(2, (cAliasSC8)->C8_GRUPCOM)

			cQuery := oQry:GetFixQuery()
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSAJ,.T.,.T.)	
			
		#ELSE
			MsSeek(xFilial("SAJ")+(cAliasSC8)->C8_GRUPCOM)
		#ENDIF
		oObjeto:Purchaseres := {}
		While !Eof() .And. xFilial("SAJ") == (cAliasSAJ)->AJ_FILIAL .And.;
			(cAliasSC8)->C8_GRUPCOM == (cAliasSAJ)->AJ_GRCOM

			aadd(oObjeto:Purchaseres,(cAliasSAJ)->AJ_USER)

			dbSelectArea(cAliasSAJ)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSAJ)
			dbCloseArea()
			dbSelectArea("SAJ")
			FreeObj(oQry)
		EndIf
	Else
		oObjeto:Purchaseres := {""}
	EndIf
Else
	oObjeto:QuoteID            := WsProcH(aCab, "C8_NUM")
	oObjeto:ProposalID         := WsProcH(aCab, "C8_NUMPRO")
	oObjeto:Supplier           := WsProcH(aCab, "C8_FORNECE")+WsProcH(aCab, "C8_LOJA")
	oObjeto:RegisterDate       := WsProcH(aCab, "C8_EMISSAO")
	oObjeto:Contact            := WsProcH(aCab, "C8_CONTATO")
	oObjeto:PaymentPlanCode	   := WsProcH(aCab, "C8_COND" )
	oObjeto:Currency           := WsProcH(aCab, "C8_MOEDA")
	oObjeto:CurrencyRate       := WsProcH(aCab, "C8_TXMOEDA")
	oObjeto:DiscountInCascade1 := WsProcH(aCab, "C8_DESC1")
	oObjeto:DiscountInCascade2 := WsProcH(aCab, "C8_DESC2")
	oObjeto:DiscountInCascade3 := WsProcH(aCab, "C8_DESC3")
	oObjeto:QuoteStatus        := IIf(Empty(WsProcH(aCab, "C8_NUMPED")),STR0008,STR0009) //"Aberto"###"Fechado"
	oObjeto:Purchaseres        := {""}

	UserFields("SC8",@oObjeto:UserFields,cAliasSC8,aCab)

EndIf

Return NIL

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetSC8Item³Autor  ³ Eduardo Riera         ³ Data ³05.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Objeto do item da cotacao de      ³±±
±±³          ³compra                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do Item                                        ³±±
±±³          ³ExpC2: Alias do tabela                                       ³±±
±±³          ³ExpA3: Array com os dados do Item                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1:Indica que nao houve erro nos dados enviados           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo atualiza a variavel aCab com os dados do objeto  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GetSC8Item(oObjeto,cAliasSC8,aItem)

Local nY        := 0
Local nPBaseIPI := 0		// Posicao do campo C8_BASEIPI no UserFields caso tenha sido incluido via ponto de entrada
Local nPBaseICm := 0		// Posicao do campo C8_BASEICM no UserFields caso tenha sido incluido via ponto de entrada
Local nValBasIPI:= 0
Local nValBasICM:= 0
Local aImpostos := {}
Local nX		:= 1

If aItem == Nil
	oObjeto:SequentialID      := (cAliasSC8)->C8_ITEM
	oObjeto:ProposalID        := (cAliasSC8)->C8_NUMPRO
	oObjeto:ProductCode       := (cAliasSC8)->C8_PRODUTO
	oObjeto:DescriptionProduct:= Posicione("SC1",1,xFilial("SC1")+(cAliasSC8)->C8_NUMSC+(cAliasSC8)->C8_ITEMSC,"C1_DESCRI")
	oObjeto:MeasureUnit       := (cAliasSC8)->C8_UM
	oObjeto:Quantity          := (cAliasSC8)->C8_QUANT
	oObjeto:UnitPrice         := (cAliasSC8)->C8_PRECO
	oObjeto:TotalValue        := (cAliasSC8)->C8_TOTAL
	oObjeto:DiscountPercent   := (cAliasSC8)->C8_DESC
	oObjeto:DiscountValue     := (cAliasSC8)->C8_VLDESC
	oObjeto:PaymentPlanCode   := (cAliasSC8)->C8_COND
	oObjeto:DeliveryTermInDays:= (cAliasSC8)->C8_PRAZO
	oObjeto:Notes             := SC8->C8_OBS
	oObjeto:ExpiryDate        := (cAliasSC8)->C8_VALIDA
	oObjeto:DeliveryDate      := (cAliasSC8)->C8_DATPRF
	oObjeto:ExpensesValue     := (cAliasSC8)->C8_DESPESA
	oObjeto:InsuranceValue    := (cAliasSC8)->C8_SEGURO
	oObjeto:FreightValue      := (cAliasSC8)->C8_VALFRE
	oObjeto:TaxRate           := (cAliasSC8)->C8_ALIIPI
	oObjeto:Code           	  := cEmpAnt
	oObjeto:Branch            := cFilAnt

	aImpostos := MaFisImpLd("SC8","IT",cAliasSC8)
	If len(aImpostos) > 0
		For nY := 1 To Len(aImpostos)
			If nY == 1
				oObjeto:Taxes:={}
			EndIf
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
			oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
			oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
			oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
		Next nY
	Else
			oObjeto:Taxes:={}
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			oObjeto:Taxes[1]:TaxCode   := "IPI"
			oObjeto:Taxes[1]:TaxBase   := (cAliasSC8)->C8_BASEIPI
			oObjeto:Taxes[1]:TaxRate   := oObjeto:TaxRate
			oObjeto:Taxes[1]:TaxAmount := oObjeto:Taxes[1]:TaxBase * ( oObjeto:Taxes[1]:TaxRate / 100 )
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			oObjeto:Taxes[2]:TaxCode   := "ICM"
			oObjeto:Taxes[2]:TaxBase   := (cAliasSC8)->C8_BASEICM
			oObjeto:Taxes[2]:TaxRate   := oObjeto:TaxRate
			oObjeto:Taxes[2]:TaxAmount := oObjeto:Taxes[1]:TaxBase * ( oObjeto:Taxes[1]:TaxRate / 100 )
	EndIf

	UserFields("SC8",@oObjeto:UserFields,cAliasSC8)

	//..trata os campos MEMO's
	TrataSC8Memo(@oObjeto,cAliasSC8)
	
Else
	oObjeto:SequentialID      := WsProcH(aItem, "C8_ITEM")
	oObjeto:ProposalID        := WsProcH(aItem, "C8_NUMPRO")
	oObjeto:ProductCode       := WsProcH(aItem, "C8_PRODUTO")
	oObjeto:DescriptionProduct:= Posicione("SC1",1,xFilial("SC1")+(cAliasSC8)->C8_NUMSC+(cAliasSC8)->C8_ITEMSC,"C1_DESCRI")
	oObjeto:MeasureUnit       := WsProcH(aItem, "C8_UM")
	oObjeto:Quantity          := WsProcH(aItem, "C8_QUANT")
	oObjeto:UnitPrice         := WsProcH(aItem, "C8_PRECO")
	oObjeto:TotalValue        := WsProcH(aItem, "C8_TOTAL")
	oObjeto:DiscountPercent   := WsProcH(aItem, "C8_DESC")
	oObjeto:DiscountValue     := WsProcH(aItem, "C8_VLDESC")
	oObjeto:PaymentPlanCode	   := WsProcH(aItem, "C8_COND")
	oObjeto:DeliveryTermInDays:= WsProcH(aItem, "C8_PRAZO")
	oObjeto:Notes             := WsProcH(aItem, "C8_OBS")
	oObjeto:ExpiryDate        := WsProcH(aItem, "C8_VALIDA")
	oObjeto:DeliveryDate      := WsProcH(aItem, "C8_DATPRF")
	oObjeto:ExpensesValue     := WsProcH(aItem, "C8_DESPESA")
	oObjeto:InsuranceValue    := WsProcH(aItem, "C8_SEGURO")
	oObjeto:FreightValue      := WsProcH(aItem, "C8_VALFRE")
	oObjeto:TaxRate           := WsProcH(aItem, "C8_ALIIPI") 
	oObjeto:Code              := cEmpAnt
	oObjeto:Branch            := cFilAnt

	UserFields("SC8",@oObjeto:UserFields,cAliasSC8,aItem)

	aImpostos := MaFisImpLd("SC8","IT",cAliasSC8)
	If len(aImpostos) > 0
		For nY := 1 To Len(aImpostos)
			If nY == 1
				oObjeto:Taxes:={}
			EndIf
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			If aImpostos[nY][1] $ "IPI"

				// Altera a Base IPI caso usuario tenha incluido C8_BASEIPI via ponto de entrada e alterado valor manualmente
				If (nPBaseIPI := aScan(OOBJETO:USERFIELDS,{|x| x:USERNAME == "C8_BASEIPI"})) > 0
					nValBasIPI := VAL(OOBJETO:USERFIELDS[nPBaseIPI]:USERTAG)
				EndIf

				oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
				If nPBaseIPI > 0
					oObjeto:Taxes[nY]:TaxBase	:= nValBasIPI
				ElseIf aImpostos[nY][2] <> oObjeto:TotalValue				// Usuário alterou o preço
					oObjeto:Taxes[nY]:TaxBase   := oObjeto:TotalValue
				Else
					oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
				EndIf
				If aImpostos[nY][3] <> oObjeto:TaxRate					// Usuário alterou o percentual do IPI
					oObjeto:Taxes[nY]:TaxRate   := oObjeto:TaxRate
				Else
					oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
				EndIf
				oObjeto:Taxes[nY]:TaxAmount := ( oObjeto:Taxes[nY]:TaxBase * ( oObjeto:Taxes[nY]:TaxRate / 100 ))
			ElseIf aImpostos[nY][1] $ "ICM"
				// Altera a Base IPI caso usuario tenha incluido C8_BASECM via ponto de entrada e alterado valor manualmente
				If (nPBaseICM := aScan(OOBJETO:USERFIELDS,{|x| x:USERNAME == "C8_BASEICM"})) > 0
					nValBasICM := VAL(OOBJETO:USERFIELDS[nPBaseICM]:USERTAG)
				EndIf

				oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
				If nPBaseICM > 0
					oObjeto:Taxes[nY]:TaxBase	:= nValBasICM
				ElseIf aImpostos[nY][2] <> oObjeto:TotalValue				// Usuário alterou o preço
					oObjeto:Taxes[nY]:TaxBase   := oObjeto:TotalValue
				Else
					oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
				EndIf
				If aImpostos[nY][3] <> oObjeto:TaxRate					// Usuário alterou o percentual do ICM
					oObjeto:Taxes[nY]:TaxRate   := oObjeto:TaxRate
				Else
					oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
				EndIf
				oObjeto:Taxes[nY]:TaxAmount := ( oObjeto:Taxes[nY]:TaxBase * ( oObjeto:Taxes[nY]:TaxRate / 100 ))
			Else
				oObjeto:Taxes[nY]:TaxCode   := aImpostos[nY][1]
				oObjeto:Taxes[nY]:TaxBase   := aImpostos[nY][2]
				oObjeto:Taxes[nY]:TaxRate   := aImpostos[nY][3]
				oObjeto:Taxes[nY]:TaxAmount := aImpostos[nY][4]
			EndIf
		Next nY
	EndIf			
	If Len(aImpostos)== 0
		oObjeto:Taxes:={}	
	EndIf	
	If Len(aImpostos) == 0 .Or. aScan(aImpostos,{|x| AllTrim(x[1])=="IPI"}) == 0
		If oObjeto:TaxRate > 0			
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			nX := len(oObjeto:Taxes)
			oObjeto:Taxes[nX]:TaxCode   := "IPI"
			If WsProcH(aItem, "C8_BASEIPI") <> oObjeto:TotalValue		// Usuário alterou o preço
				oObjeto:Taxes[nX]:TaxBase   := oObjeto:TotalValue
			Else
				oObjeto:Taxes[nX]:TaxBase   := WsProcH(aItem, "C8_BASEIPI")
			EndIf
			oObjeto:Taxes[nX]:TaxRate   := oObjeto:TaxRate
			oObjeto:Taxes[nX]:TaxAmount := oObjeto:Taxes[nX]:TaxBase * ( oObjeto:Taxes[nX]:TaxRate / 100 )
			nX++
		EndIf		
	EndIf
	If Len(aImpostos) == 0 .Or. aScan(aImpostos,{|x| AllTrim(x[1])=="ICM"}) == 0		
		If oObjeto:TaxRate > 0			
			aadd(oObjeto:Taxes,WsClassNew("TaxesView"))
			nX := len(oObjeto:Taxes)
			oObjeto:Taxes[nX]:TaxCode   := "ICM"
			If WsProcH(aItem, "C8_BASEICM") <> oObjeto:TotalValue		// Usuário alterou o preço
				oObjeto:Taxes[nX]:TaxBase   := oObjeto:TotalValue
			Else
				oObjeto:Taxes[nX]:TaxBase   := WsProcH(aItem, "C8_BASEICM")
			EndIf
			oObjeto:Taxes[nX]:TaxRate   := oObjeto:TaxRate
			oObjeto:Taxes[nX]:TaxAmount := oObjeto:Taxes[nX]:TaxBase * ( oObjeto:Taxes[nX]:TaxRate / 100 )
		EndIf		
	EndIf
EndIf
	
Return(.T.)
        
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³TrataSC8Memo ³ Autor ³ Moises Nunes     ³ Data ³ 12/08/2011 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Trata os campos IMAGE,BLOB do banco de Dados que foram      ³±±
±±³          ³criados no configurador como Tipo:MEMO Contexto:REAL        ³±±
±±³          ³-Sem o uso dessa função o conteúdo do campo é exibido       ³±±
±±³          ³ criptografado    										  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function TrataSC8Memo(oObjeto,cAlias)
    
Local i:=0     
Local nRecSC8   := 0
Local cCampoSC8 := ""
        
If oObjeto:UserFields != Nil
	For i:=1 to len(oObjeto:UserFields)    
		If oObjeto:UserFields[i]:UserType=="M"
			nRecSC8:= (cAlias)->R_E_C_N_O_
			cCampoSC8:=oObjeto:UserFields[i]:UserName
			
			SC8->(dbgoto(nRecSC8))
			oObjeto:UserFields[i]:UserTag:=SC8->&cCampoSC8
		EndIf
	Next i
EndIf

Return 
 