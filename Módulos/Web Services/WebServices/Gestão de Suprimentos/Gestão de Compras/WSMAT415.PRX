#INCLUDE "WSMAT415.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT415  ³ Autor ³Kleber Dias Gomes      ³ Data ³27.05.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Orcamento de Venda para o ³±±
±±³          ³ portal de cliente.                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao da estruturas utilizadas                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSTRUCT BudgetView
	WSDATA Header As BudgetHeaderView
	WSDATA Itens  As Array Of BudgetItensView
ENDWSSTRUCT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtCustomerBudget        DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtcustomerbudget.apw" //"Serviço de consulta e atualização dos orçamentos de venda ( <b>Restrição de cliente</b> )"
	WSDATA Header                 AS Array Of BrwHeader
	WSDATA HeaderType             As String
	WSDATA UserCode               As String
	WSDATA CustomerID			  AS String
	WSDATA BudgetID				  AS String
	WSDATA SalesOrder             AS String
	WSDATA WsNull                 AS String
	WSDATA Budget				  AS BudgetView
	WSDATA RegisterDateFrom       AS Date
	WSDATA RegisterDateTo         AS Date
	WSDATA QueryAddWhere          AS String
	WSDATA IndexKey               AS String OPTIONAL
	WSDATA PageLen                AS Integer
	WSDATA PageFirst              AS Integer
	WSDATA ListOfBudget           AS Array Of BudgetHeaderView
	WSDATA QuotationOrOrderIDFrom As String OPTIONAL
	WSDATA QuotationOrOrderIDTo   As String OPTIONAL
	WSDATA TotalBudget			  As Integer OPTIONAL
	
	WSMETHOD GetHeader            DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
	WSMETHOD GetBudget            DESCRIPTION STR0003   //"Método de consulta as informações do orçamento de venda"
	WSMETHOD PutBudget            DESCRIPTION STR0004   //"Método de atualização das informações do orçamento de venda"
	WSMETHOD DelBudget	          DESCRIPTION STR0005   //"Método de exclusão do orçamento de venda"
	WSMETHOD EffectBudget         DESCRIPTION STR0006 //"Método de baixa do orçamento de venda"
	WSMETHOD BrwBudget            DESCRIPTION STR0007 //"Método de listagem das informações do orçamento de venda"
	WSMETHOD BrwTotalBudget       DESCRIPTION STR0018 //"Método de retorno do total de orçamentos do BrwBudget"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Kleber Dias Gomes     ³ Data ³21.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtCustomerBudget

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwBugdet    ³Autor  ³Eduardo Riera       ³Data  ³23.10.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos orcamentos de venda de um cliente  ³±±
±±³          ³em Browse                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Where para Adicao 	                                   ³±±
±±³          ³ExpC6: IndexKey                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos orcamentos de venda de um cliente  ³±±
±±³          ³em Browse                                                    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwBudget WSRECEIVE UserCode,CustomerID,RegisterDateFrom,RegisterDateTo,QuotationOrOrderIDFrom,QuotationOrOrderIDTo,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfBudget WSSERVICE MtCustomerBudget

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSCJ := "SCJ"
Local cQuery    := ""
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cArquivo  := ""
Local aStruSCJ  := {}

SCJ->(dbSetOrder(3))

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase
DEFAULT ::IndexKey         := SCJ->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtCustomerBudget","BrwBudget","SA1",::CustomerID)
	If !Empty(cCliente) .And. !Empty(cLoja)
		dbSelectArea("SCJ")
		dbSetOrder(2)
		
		lQuery	:= .T.
		aStruSCJ	:= SCJ->(dbStruct())
		cAliasSCJ:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SCJ")+" SCJ "
		cQuery	+= "WHERE "
		cQuery	+= "SCJ.CJ_FILIAL = '"+xFilial("SCJ")+"' AND "
		cQuery	+= "SCJ.CJ_CLIENTE='"+cCliente+"' AND " 
		cQuery	+= "SCJ.CJ_LOJA='"+cLoja+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
		If !Empty(QuotationOrOrderIDFrom)
			cQuery	+= "SCJ.CJ_COTCLI>='"+::QuotationOrOrderIDFrom+"' AND "
		EndIf
		If !Empty(QuotationOrOrderIDTo)			
			cQuery	+= "SCJ.CJ_COTCLI<='"+::QuotationOrOrderIDTo+"' AND "
		EndIf
		cQuery	+= "SCJ.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery	+= "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCJ,.T.,.T.)

		For nX := 1 To Len(aStruSCJ)
			If aStruSCJ[nX][2]<>"C" .And. aStruSCJ[nX][2]<>"M"
				TcSetField(cAliasSCJ,aStruSCJ[nX][1],aStruSCJ[nX][2],aStruSCJ[nX][3],aStruSCJ[nX][4])
			EndIf
		Next nX
		
		nX := 0
		dbSelectArea(cAliasSCJ)
		While !Eof() .And. xFilial("SCJ") == (cAliasSCJ)->CJ_FILIAL
			If (cAliasSCJ)->CJ_CLIENTE == cCliente .And.;
					(cAliasSCJ)->CJ_LOJA == cLoja
				nX++
				If ::PageFirst==0 .Or. nX >= ::PageFirst
					If nY == 0
						::ListOfBudGet := {}
					EndIf
					aAdd(::ListOfBudget,WsClassNew("BudgetHeaderView"))
					nY++
					GetOrcHeader(@::ListOfBudget[nY],cAliasSCJ)
				EndIf
			EndIf
			If nY >= ::PageLen .And. ::PageLen <> 0
				Exit
			EndIf
			dbSelectArea(cAliasSCJ)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSCJ)
			dbCloseArea()
			dbSelectArea("SCJ")
		Else
			RetIndex(cAliasSCJ)
			FErase(cArquivo+OrdBagExt())
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWBUDGET",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwTotalBudge³Autor  ³Luiz Couto          ³Data  ³01.09.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao do total de orcamentos                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Where para Adicao 	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao do total de orcamentos                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwTotalBudget WSRECEIVE UserCode,CustomerID,RegisterDateFrom,RegisterDateTo,QuotationOrOrderIDFrom,QuotationOrOrderIDTo,QueryAddWhere WSSEND TotalBudget WSSERVICE MtCustomerBudget

Local aArea     := GetArea()
Local lRetorno  := .T.
Local cAliasSCJ := "SCJ"
Local cQuery    := ""
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase

If PrtChkUser(::UserCode,"MtCustomerBudget","BrwBudget","SA1",::CustomerID)
	If !Empty(cCliente) .And. !Empty(cLoja)
		cAliasSCJ := GetNextAlias()

		cQuery	:= "SELECT COUNT(*) TOTALBUDGET "
		cQuery	+= "FROM "+RetSqlName("SCJ")+" SCJ "
		cQuery	+= "WHERE "
		cQuery	+= "SCJ.CJ_FILIAL = '"+xFilial("SCJ")+"' AND "
		cQuery	+= "SCJ.CJ_CLIENTE='"+cCliente+"' AND "
		cQuery	+= "SCJ.CJ_LOJA='"+cLoja+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
		If !Empty(QuotationOrOrderIDFrom)
			cQuery	+= "SCJ.CJ_COTCLI>='"+::QuotationOrOrderIDFrom+"' AND "
		EndIf
		If !Empty(QuotationOrOrderIDTo)			
			cQuery	+= "SCJ.CJ_COTCLI<='"+::QuotationOrOrderIDTo+"' AND "
		EndIf
		cQuery	+= "SCJ.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCJ,.T.,.T.)

		::TotalBudget := ( cAliasSCJ )->TOTALBUDGET
	Else
		lRetorno := .F.
		SetSoapFault("BRWBUDGET",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetBudget	   ³Autor  ³Kleber Dias Gomes   ³Data  ³27.05.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos orcamentos de um cliente           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos orcamentos de venda de um cliente. ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetBudget WSRECEIVE UserCode,CustomerID,BudgetID WSSEND Budget WSSERVICE MtCustomerBudget

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSCJ := "SCJ"
Local cAliasSCK := "SCK"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local aStruSCK  := {}
Local cQuery    := ""
Local nX        := 0

If PrtChkUser(::UserCode,"MtCustomerBudget","GetBudGet","SA1",::CustomerID)
	If !Empty(cCliente) .And. !Empty(cLoja)

		dbSelectArea("SCJ")
		dbSetOrder(1)
		If MsSeek(xFilial("SCJ")+::BudgetID)

			::Budget:Header	:=WsClassNew('BudgetHeaderView')
			GetOrcHeader(@::Budget:Header,cAliasSCJ)

			dbSelectArea("SCK")
			dbSetOrder(1)
		
			lQuery	:= .T.
			aStruSCK	:= SCK->(dbStruct())
			cAliasSCK:= GetNextAlias()

			cQuery	:= "SELECT * "
			cQuery	+= "FROM "+RetSqlName("SCK")+" SCK "
			cQuery	+= "WHERE "
			cQuery	+= "SCK.CK_FILIAL = '"+xFilial("SCK")+"' AND "
			cQuery	+= "SCK.CK_NUM='"+::BudgetID+"' AND "
			cQuery	+= "SCK.D_E_L_E_T_=' ' "
			cQuery	+= "ORDER BY "+SqlOrder(SCK->(IndexKey()))

			cQuery	:= ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCK,.T.,.T.)

			For nX := 1 To Len(aStruSCK)
				If aStruSCK[nX][2]<>"C"
					TcSetField(cAliasSCK,aStruSCK[nX][1],aStruSCK[nX][2],aStruSCK[nX][3],aStruSCK[nX][4])
				EndIf
			Next nX
		
			dbSelectArea(cAliasSCK)
			While !Eof() .And. xFilial("SCK") == (cAliasSCK)->CK_FILIAL .And.;
					::BudgetID == (cAliasSCK)->CK_NUM

				aAdd(::Budget:Itens,WSClassNew("BudgetItensView"))
				nY++
				GetOrcItem(@::Budget:Itens[nY],cAliasSCK)

				dbSelectArea(cAliasSCK)
				dbSkip()
			EndDo

			If lQuery
				dbSelectArea(cAliasSCK)
				dbCloseArea()
				dbSelectArea("SCK")
			EndIf
		Else
			lRetorno := .F.
			SetSoapFault("GETBUDGET",STR0009) //"Orcamento nao encontrado"
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETBUDGET",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutBudget	   ³Autor  ³Kleber Dias Gomes   ³Data  ³24.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de inclusao dos pedidos de venda de um cliente        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Dados do Pedido de Venda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de inclusao dos orcamentos de venda de um cliente.    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Tatiana C.   ³16/01/07³117058³Inclusao do PADR na verificacao do codigo ³±±
±±³             ³        ³      ³do cliente na gravação do orcamento.      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutBudget WSRECEIVE UserCode,CustomerID,Budget WSSEND BudgetId WSSERVICE MtCustomerBudget

Local aArea		 := GetArea()
Local aCab		 := {}
Local aItens	 := {}
Local aErro      := {}
Local cErro      := ""
Local lRetorno	 := .T.
Local nX         := 0

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtCustomerBudget","PutBudget","SA1",::CustomerID)

	If PadR(::Budget:Header:CustomerCode,Len(SA1->A1_COD)) == SubStr(::CustomerID,1,Len(SA1->A1_COD))

		dbSelectArea("SCJ")
		dbSetOrder(1)
		If !Empty(::Budget:Header:BudgetID) 
			MsSeek(xFilial("SCJ")+::Budget:Header:BudgetID)	
		Else
			MsGoto(0)
		EndIf
		
		dbSelectArea("SCJ")
		PutOrcHead(::Budget:Header,@aCab)

		dbSelectArea("SCK")
		PutOrcItem(::Budget,@aItens)

		dbSelectArea("SCJ")
		dbSetOrder(1)
		If !Empty(::Budget:Header:BudgetID) .And. MsSeek(xFilial("SCJ")+::Budget:Header:BudgetID)
			MATA415(aCab,aItens,4)
		Else
			MATA415(aCab,aItens,3)
		EndIf
		::BudGetID := SCJ->CJ_NUM

		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTBUDGET",cErro)
			lRetorno := .F.
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("PUTBUDGET",STR0008) //"Cliente invalido"
	EndIf	
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelBudget	   ³Autor  ³Kleber Dias Gomes   ³Data  ³30.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de exclusao dos orçamentos de um cliente              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de exclusao dos orcamentos de venda de um cliente.    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelBudget WSRECEIVE UserCode,CustomerID,BudgetID WSSEND WsNull WSSERVICE MtCustomerBudget

Local aCab		:= {}
Local aItens	:= {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtCustomerBudget","DelBudGet","SA1",::CustomerID)
	aAdd(aCab,{"CJ_NUM"		,::BudgetID,})

	MATA415(aCab,aItens,5)

	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELBUDGET",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³EffectBudget ³Autor  ³Kleber Dias Gomes   ³Data  ³30.06.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de efetivacao dos orçamentos de um cliente            ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Cliente Loja                                          ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de efetivacao dos orcamentos de um cliente            ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD EffectBudget WSRECEIVE UserCode,CustomerID,BudgetID WSSEND SalesOrder WSSERVICE MtCustomerBudget

Local aCab		:= {}
Local aItens	:= {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtCustomerBudget","EffectBudGet","SA1",::CustomerID)
	dbSelectArea("SCJ")
	//Cabecalho	
	aAdd(aCab,{"CJ_NUM"    ,::BudGetId,})

	MATA416(aCab,aItens)
	::SalesOrder := SC5->C5_NUM
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELBUDGET",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetOrcHead³Autor  ³ Kleber Dias Gomes     ³ Data ³29.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do Header do Orcamento               ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do orcamento                           ³±±
±±³          ³ExpC2: Alias do SCJ                                          ³±±
±±³          ³ExpA3: Array com os dados da rotina automatica               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SCJ posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetOrcHeader(oObjeto,cAliasSCJ,aCab)

Local aArea    := GetArea()

If aCab == Nil
	oObjeto:BudgetID			   := (cAliasSCJ)->CJ_NUM
	oObjeto:RegisterDate		   := (cAliasSCJ)->CJ_EMISSAO
	oObjeto:CustomerCode		   := (cAliasSCJ)->CJ_CLIENTE
	oObjeto:CustomerUnit		   := (cAliasSCJ)->CJ_LOJA
	oObjeto:DeliveryCustomer	   := (cAliasSCJ)->CJ_CLIENT
	oObjeto:DeliveryUnitCode	   := (cAliasSCJ)->CJ_LOJAENT
	oObjeto:PaymentPlanCode		   := (cAliasSCJ)->CJ_CONDPAG
	oObjeto:PriceListCode		   := (cAliasSCJ)->CJ_TABELA
	oObjeto:Discount1			   := (cAliasSCJ)->CJ_DESC1
	oObjeto:Discount2			   := (cAliasSCJ)->CJ_DESC2
	oObjeto:Discount3			   := (cAliasSCJ)->CJ_DESC3
	oObjeto:Discount4			   := (cAliasSCJ)->CJ_DESC4
	oObjeto:QuotationOrOrderID	   := (cAliasSCJ)->CJ_COTCLI
	oObjeto:FreightValue		   := (cAliasSCJ)->CJ_FRETE
	oObjeto:InsuranceValue		   := (cAliasSCJ)->CJ_SEGURO
	oObjeto:AdditionalExpenseValue := (cAliasSCJ)->CJ_DESPESA
	oObjeto:IndependentFreight	   := (cAliasSCJ)->CJ_FRETAUT
	oObjeto:ExpirationDate		   := (cAliasSCJ)->CJ_VALIDA
	oObjeto:AprobationType		   := (cAliasSCJ)->CJ_TIPLIB
	oObjeto:Load				   := (cAliasSCJ)->CJ_TPCARGA
	oObjeto:IndemnityValue		   := (cAliasSCJ)->CJ_DESCONT
	oObjeto:IndemnityPercentage	   := (cAliasSCJ)->CJ_PDESCAB
	oObjeto:Status                 := (cAliasSCJ)->CJ_STATUS
	Do Case
		Case (cAliasSCJ)->CJ_STATUS == "A"
			oObjeto:DescriptionStatus      := STR0010 //"Aberto"
		Case (cAliasSCJ)->CJ_STATUS == "B"
			oObjeto:DescriptionStatus      := STR0011 //"Baixado"
		Case (cAliasSCJ)->CJ_STATUS == "C"
			oObjeto:DescriptionStatus      := STR0012 //"Cancelado"
		Case (cAliasSCJ)->CJ_STATUS == "D"
			oObjeto:DescriptionStatus      := STR0013 //"Nao Orçado"
		Case (cAliasSCJ)->CJ_STATUS == "E"
			oObjeto:DescriptionStatus      := STR0014 //"Aprovado"
	EndCase	
	UserFields("SCJ",@oObjeto:UserFields,cAliasSCJ)
Else
	oObjeto:BudgetID			   := WsProcH(aCab,"CJ_NUM")
	oObjeto:RegisterDate		   := WsProcH(aCab,"CJ_EMISSAO")
	oObjeto:CustomerCode		   := WsProcH(aCab,"CJ_CLIENTE")
	oObjeto:CustomerUnit		   := WsProcH(aCab,"CJ_LOJA")
	oObjeto:DeliveryCustomer	   := WsProcH(aCab,"CJ_CLIENT")
	oObjeto:DeliveryUnitCode	   := WsProcH(aCab,"CJ_LOJAENT")
	oObjeto:PaymentPlanCode		   := WsProcH(aCab,"CJ_CONDPAG")
	oObjeto:PriceListCode		   := WsProcH(aCab,"CJ_TABELA")
	oObjeto:Discount1			   := WsProcH(aCab,"CJ_DESC1")
	oObjeto:Discount2			   := WsProcH(aCab,"CJ_DESC2")
	oObjeto:Discount3			   := WsProcH(aCab,"CJ_DESC3")
	oObjeto:Discount4			   := WsProcH(aCab,"CJ_DESC4")
	oObjeto:QuotationOrOrderID	   := WsProcH(aCab,"CJ_COTCLI")
	oObjeto:FreightValue		   := WsProcH(aCab,"CJ_FRETE")
	oObjeto:InsuranceValue		   := WsProcH(aCab,"CJ_SEGURO")
	oObjeto:AdditionalExpenseValue := WsProcH(aCab,"CJ_DESPESA")
	oObjeto:IndependentFreight	   := WsProcH(aCab,"CJ_FRETAUT")
	oObjeto:ExpirationDate		   := WsProcH(aCab,"CJ_VALIDA")
	oObjeto:AprobationType		   := WsProcH(aCab,"CJ_TIPLIB")
	oObjeto:Load				   := WsProcH(aCab,"CJ_TPCARGA")
	oObjeto:IndemnityValue		   := WsProcH(aCab,"CJ_DESCONT")
	oObjeto:IndemnityPercentage	   := WsProcH(aCab,"CJ_PDESCAB")
	UserFields("SCJ",@oObjeto:UserFields,cAliasSCJ,@aCab)
EndIf
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetOrcItem³Autor  ³ Kleber Dias Gomes     ³ Data ³29.08.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do item do orcamento                 ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do orcamento                           ³±±
±±³          ³ExpC2: Alias do SCK                                          ³±±
±±³          ³ExpA3: Array com os dados da rotina automatica               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o objeto passado por parametro com os    ³±±
±±³          ³dados do SCK posicionado                                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetOrcItem(oObjeto,cAliasSCK,aItem)

Local aArea    := GetArea()

If aItem == Nil
	oObjeto:BudgetID			:= (cAliasSCK)->CK_NUM
	oObjeto:BudgetItem			:= (cAliasSCK)->CK_ITEM
	oObjeto:ProductID			:= (cAliasSCK)->CK_PRODUTO
	oObjeto:UnitOfMeasure		:= (cAliasSCK)->CK_UM
	oObjeto:Quantity			:= (cAliasSCK)->CK_QTDVEN
	oObjeto:NetUnitPrice		:= (cAliasSCK)->CK_PRCVEN
	oObjeto:NetTotal			:= (cAliasSCK)->CK_VALOR
	oObjeto:ItemOutflowType		:= (cAliasSCK)->CK_TES
	oObjeto:Warehouse			:= (cAliasSCK)->CK_LOCAL
	oObjeto:DiscountPercentage	:= (cAliasSCK)->CK_DESCONT
	oObjeto:ItemDiscountValue	:= (cAliasSCK)->CK_VALDESC
	oObjeto:CustomerBudgetID	:= (cAliasSCK)->CK_PEDCLI
	oObjeto:ProductDescription	:= (cAliasSCK)->CK_DESCRI
	oObjeto:UnitListPrice		:= (cAliasSCK)->CK_PRUNIT
	oObjeto:Note				:= (cAliasSCK)->CK_OBS
	oObjeto:ForecastDelivery	:= (cAliasSCK)->CK_ENTREG
	oObjeto:TaxSituation		:= (cAliasSCK)->CK_CLASFIS
	oObjeto:SalesBranch			:= (cAliasSCK)->CK_FILVEN
	oObjeto:DeliveryBranch		:= (cAliasSCK)->CK_FILENT
	UserFields("SCK",@oObjeto:UserFields,cAliasSCK)
Else 
	oObjeto:BudgetID			:= WsProcH(aItem,"CK_NUM")
	oObjeto:BudgetItem			:= WsProcH(aItem,"CK_ITEM")
	oObjeto:ProductID			:= WsProcH(aItem,"CK_PRODUTO")
	oObjeto:UnitOfMeasure		:= WsProcH(aItem,"CK_UM")
	oObjeto:Quantity			:= WsProcH(aItem,"CK_QTDVEN")
	oObjeto:NetUnitPrice		:= WsProcH(aItem,"CK_PRCVEN")
	oObjeto:NetTotal			:= WsProcH(aItem,"CK_VALOR")
	oObjeto:ItemOutflowType		:= WsProcH(aItem,"CK_TES")
	oObjeto:Warehouse			:= WsProcH(aItem,"CK_LOCAL")
	oObjeto:DiscountPercentage	:= WsProcH(aItem,"CK_DESCONT")
	oObjeto:ItemDiscountValue	:= WsProcH(aItem,"CK_VALDESC")
	oObjeto:CustomerBudgetID	:= WsProcH(aItem,"CK_PEDCLI")
	oObjeto:ProductDescription	:= WsProcH(aItem,"CK_DESCRI")
	oObjeto:UnitListPrice		:= WsProcH(aItem,"CK_PRUNIT")
	oObjeto:Note				:= WsProcH(aItem,"CK_OBS")
	oObjeto:ForecastDelivery	:= WsProcH(aItem,"CK_ENTREG")
	oObjeto:TaxSituation		:= WsProcH(aItem,"CK_CLASFIS")
	oObjeto:SalesBranch			:= WsProcH(aItem,"CK_FILVEN")
	oObjeto:DeliveryBranch		:= WsProcH(aItem,"CK_FILENT")
	UserFields("SCK",@oObjeto:UserFields,cAliasSCK,@aItem)
EndIf	
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutOrcHead³Autor  ³ Eduardo Riera         ³ Data ³25.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento do header do orcamento de venda para ³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do orcamento                           ³±±
±±³          ³ExpA2: Array da rotina automatica                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo preenche o array da rotina automatica com os     ³±±
±±³          ³dados do objeto                                              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutOrcHead(oObjeto,aCab)

aAdd(aCab,{"CJ_NUM"		,oObjeto:BudgetID,})
aAdd(aCab,{"CJ_EMISSAO"	,oObjeto:RegisterDate,})
AAdd(aCab,{"CJ_CLIENTE"	,PADR(oObjeto:CustomerCode,TAMSX3('CJ_CLIENTE')[1]),})
AAdd(aCab,{"CJ_LOJA"	,PADR(oObjeto:CustomerUnit,TAMSX3('CJ_LOJA')[1]),})
AAdd(aCab,{"CJ_CLIENT"	,PADR(oObjeto:DeliveryCustomer,TAMSX3('CJ_CLIENT')[1]),})
AAdd(aCab,{"CJ_LOJAENT"	,PADR(oObjeto:DeliveryUnitCode,TAMSX3('CJ_LOJAENT')[1]),})
aAdd(aCab,{"CJ_CONDPAG"	,oObjeto:PaymentPlanCode,})
aAdd(aCab,{"CJ_TABELA"	,oObjeto:PriceListCode,})
aAdd(aCab,{"CJ_DESC1"	,oObjeto:Discount1,})
aAdd(aCab,{"CJ_DESC2"	,oObjeto:Discount2,})
aAdd(aCab,{"CJ_DESC3"	,oObjeto:Discount3,})
aAdd(aCab,{"CJ_DESC4"	,oObjeto:Discount4,})
If !Empty(oObjeto:QuotationOrOrderID)
	aAdd(aCab,{"CJ_COTCLI"	,oObjeto:QuotationOrOrderID,})
EndIf
aAdd(aCab,{"CJ_FRETE"	,oObjeto:FreightValue,})
aAdd(aCab,{"CJ_SEGURO"	,oObjeto:InsuranceValue,})
aAdd(aCab,{"CJ_DESPESA"	,oObjeto:AdditionalExpenseValue,})
aAdd(aCab,{"CJ_FRETAUT"	,oObjeto:IndependentFreight,})
aAdd(aCab,{"CJ_VALIDA"	,oObjeto:ExpirationDate,})
aAdd(aCab,{"CJ_TIPLIB"	,oObjeto:AprobationType,})
aAdd(aCab,{"CJ_TPCARGA"	,oObjeto:Load,})
aAdd(aCab,{"CJ_DESCONT"	,oObjeto:IndemnityValue,})
aAdd(aCab,{"CJ_PDESCAB"	,oObjeto:IndemnityPercentage,})
PutUserFields("SCJ"		,@oObjeto:UserFields,@aCab)
aCab := WsAutoOpc(aCab)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutOrcItem³Autor  ³ Eduardo Riera         ³ Data ³25.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento dos itens do orcamento de venda para ³±±
±±³          ³rotina automatica                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto do item do orcamento                           ³±±
±±³          ³ExpA2: Array da rotina automatica                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo preenche o array da rotina automatica com os     ³±±
±±³          ³dados do objeto                                              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Norbert Waage ³06/08/07³130250³Ao excluir um item, a linha adicionada ao³±±
±±³              ³        ³      ³aItens utilizava o campo C6_ITEM, em vez ³±±
±±³              ³        ³      ³do campo CK_ITEM.                        ³±±
±±³Norbert Waage ³13/08/07³130060³Quando o valor do item for zerado depois ³±±
±±³              ³        ³      ³de ter sido calculado ao menos uma vez, o³±±
±±³              ³        ³      ³valor total do item sera zerado para for-³±±
±±³              ³        ³      ³car o recalculo do item pelo MATA415.    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function PutOrcItem(oObjeto,aItens)

Local aArea      := GetArea()
Local aItem      := {}
Local aNoDeleted := {}
Local nY         := 0
Local cAliasSCK  := "SCK"
Local lQuery     := .F.
Local lInclui    := .T.
Local aStruSCK   := {}
Local cQuery     := ""
Local nX         := 0


dbSelectArea("SCJ")
dbSetOrder(1)
If !Empty(oObjeto:Itens[1]:BudgetID) .And. MsSeek(xFilial("SCJ")+oObjeto:Itens[1]:BudgetID)
	lInclui := .F.
EndIf
For nY := 1 To Len(oObjeto:Itens)

	aAdd( aNoDeleted , oObjeto:Itens[nY]:BudgetItem)

	If !lInclui .and. !Empty(oObjeto:Itens[nY]:BUDGETID)
		aAdd(aItem,{"LINPOS","CK_ITEM",oObjeto:Itens[nY]:BudgetItem})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se o preco de venda for zero ou a quantidade foi alterada, ³
	//³forca o recalculo do total do item.                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (oObjeto:Itens[nY]:NetUnitPrice == 0) .OR.;
		(A410Arred(oObjeto:Itens[nY]:NetUnitPrice * oObjeto:Itens[nY]:Quantity,"CK_VALOR") <> oObjeto:Itens[nY]:NetTotal)
		oObjeto:Itens[nY]:NetTotal := 0		
	EndIf

	aAdd(aItem,{"CK_NUM"		,oObjeto:Itens[nY]:BudgetID,})
	aAdd(aItem,{"CK_ITEM"		,oObjeto:Itens[nY]:BudgetItem,})
	aAdd(aItem,{"CK_PRODUTO"	,oObjeto:Itens[nY]:ProductID,})
	aAdd(aItem,{"CK_UM"			,oObjeto:Itens[nY]:UnitOfMeasure,})
	aAdd(aItem,{"CK_QTDVEN"		,oObjeto:Itens[nY]:Quantity,})
	aAdd(aItem,{"CK_PRCVEN"		,oObjeto:Itens[nY]:NetUnitPrice,})
	aAdd(aItem,{"CK_VALOR"		,oObjeto:Itens[nY]:NetTotal,})
	aAdd(aItem,{"CK_OPER"		,oObjeto:Itens[nY]:OperationType,})
	aAdd(aItem,{"CK_TES"		,oObjeto:Itens[nY]:ItemOutflowType,})
	aAdd(aItem,{"CK_LOCAL"		,oObjeto:Itens[nY]:Warehouse,})
	aAdd(aItem,{"CK_DESCONT"	,oObjeto:Itens[nY]:DiscountPercentage,})
	aAdd(aItem,{"CK_VALDESC"	,oObjeto:Itens[nY]:ItemDiscountValue,".T."})
	aAdd(aItem,{"CK_PEDCLI"		,oObjeto:Itens[nY]:CustomerBudgetID,})
	aAdd(aItem,{"CK_DESCRI"		,oObjeto:Itens[nY]:ProductDescription,})
	aAdd(aItem,{"CK_PRUNIT"		,oObjeto:Itens[nY]:UnitListPrice,})
	aAdd(aItem,{"CK_OBS"		,oObjeto:Itens[nY]:Note,})
	aAdd(aItem,{"CK_ENTREG"		,oObjeto:Itens[nY]:ForecastDelivery,})
	aAdd(aItem,{"CK_CLASFIS"	,oObjeto:Itens[nY]:TaxSituation,})
	aAdd(aItem,{"CK_FILVEN"		,oObjeto:Itens[nY]:SalesBranch,".T."})
	aAdd(aItem,{"CK_FILENT"		,oObjeto:Itens[nY]:DeliveryBranch,".T."})
	PutUserFields("SCK",			@oObjeto:Itens[nY]:UserFields,@aItem)
	aItem:= WsAutoOpc(aItem)
	aAdd(aItens,aItem)
	aItem:= {}
Next nY

dbSelectArea("SCJ")
dbSetOrder(1)
If !Empty(oObjeto:Header:BudgetID) .And. MsSeek(xFilial("SCJ")+oObjeto:Header:BudgetID)
	dbSelectArea("SCK")
	dbSetOrder(1)
	
	lQuery	:= .T.
	aStruSCK	:= SCK->(dbStruct())
	cAliasSCK:= GetNextAlias()

	cQuery	:= "SELECT * "
	cQuery	+= "FROM "+RetSqlName("SCK")+" SCK "
	cQuery	+= "WHERE "
	cQuery	+= "SCK.CK_FILIAL = '"+xFilial("SCK")+"' AND "
	cQuery	+= "SCK.CK_NUM='"+oObjeto:Header:BudgetID+"' AND "
	cQuery	+= "SCK.D_E_L_E_T_=' ' "
	cQuery	+= "ORDER BY "+SqlOrder(SCK->(IndexKey()))

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCK)

	For nX := 1 To Len(aStruSCK)
		If aStruSCK[nX][2]<>"C" .And. aStruSCK[nX][2]<>"M"
			TcSetField(cAliasSCK,aStruSCK[nX][1],aStruSCK[nX][2],aStruSCK[nX][3],aStruSCK[nX][4])
		EndIf
	Next nX
	
	dbSelectArea(cAliasSCK)
	While !Eof() .And. xFilial("SCK") == (cAliasSCK)->CK_FILIAL .And.;
			oObjeto:Header:BudgetID == (cAliasSCK)->CK_NUM
		If aScan(aNoDeleted,(cAliasSCK)->CK_ITEM) == 0
				aAdd(aItens,{{"LINPOS","CK_ITEM",(cAliasSCK)->CK_ITEM},;
				{"AUTDELETA","S"}})
		EndIf	
		dbSelectArea(cAliasSCK)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSCK)
		dbCloseArea()
		dbSelectArea("SCK")
	EndIf				
EndIf

RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT415  ³ Autor ³Eduardo Riera          ³ Data ³03.11.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Orcamento de Venda para o ³±±
±±³          ³ portal de vendedor.                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSellerBudget          DESCRIPTION STR0015 NAMESPACE "http://webservices.microsiga.com.br/mtsellerbudget.apw" //"Serviço de consulta e atualização dos orçamentos de venda ( <b>Restrição de representante comercial</b> )"
	WSDATA Header                 AS Array Of BrwHeader
	WSDATA HeaderType             As String
	WSDATA UserCode               As String
	WSDATA SellerCode			  AS String
	WSDATA BudgetID				  AS String
	WSDATA SalesOrder             AS String	
	WSDATA WsNull                 AS String
	WSDATA Budget				  AS BudgetView
	WSDATA RegisterDateFrom       AS Date
	WSDATA RegisterDateTo         AS Date
	WSDATA QueryAddWhere          AS String
	WSDATA IndexKey               AS String
	WSDATA PageLen                AS Integer
	WSDATA PageFirst              AS Integer
	WSDATA ListOfBudget           AS Array Of BudgetHeaderView
	WSDATA QuotationOrOrderIDFrom As String OPTIONAL
	WSDATA QuotationOrOrderIDTo   As String OPTIONAL	
	WSDATA TotalBudget			  As Integer OPTIONAL
	WSDATA NextNumber			  As String OPTIONAL
	
	WSMETHOD GetHeader            DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do serviço"
	WSMETHOD GetBudget            DESCRIPTION STR0003   //"Método de consulta as informações do orçamento de venda"
	WSMETHOD PutBudget            DESCRIPTION STR0004   //"Método de atualização das informações do orçamento de venda"
	WSMETHOD DelBudget	          DESCRIPTION STR0005   //"Método de exclusão do orçamento de venda"
	WSMETHOD EffectBudget         DESCRIPTION STR0006 //"Método de baixa do orçamento de venda"
	WSMETHOD BrwBudget            DESCRIPTION STR0007 //"Método de listagem das informações do orçamento de venda"
	WSMETHOD BrwTotalBudget       DESCRIPTION STR0018 //"Método de retorno do total de orçamentos do BrwBudget"
	WSMETHOD GetNextNumber        DESCRIPTION "Metodo de retorno de numeracao de orcamento"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao dos headers deste WS                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ WEB SERVICES                                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerBudget

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwBugdet    ³Autor  ³Eduardo Riera       ³Data  ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos orcamentos de venda de um cliente  ³±±
±±³          ³em Browse                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Where para Adicao 	                                   ³±±
±±³          ³ExpC6: IndexKey                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos orcamentos de venda de um cliente  ³±±
±±³          ³em Browse                                                    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÃ±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Analista     ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³10/04/06³096386³- Tratamento na query para tambem buscar ³±±
±±³              ³        ³      ³os orcamentos de clientes sem vendedor.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwBudget WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,QuotationOrOrderIDFrom,QuotationOrOrderIDTo,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfBudget WSSERVICE MtSellerBudget

Local aArea     := GetArea()	//Guarda a areal atual
Local lRetorno  := .T.			//Indica retorno da funcao
Local lQuery    := .F.			//Indica se usa query
Local lValido   := .F.			//Indica se o registro eh valido
Local nX        := 0			//Usado em lacos For...Next
Local nY        := 0			//Contador                                      	
Local cAliasSCJ := "SCJ"		//Alias da tabela orcamentos
Local cQuery    := ""			//Query a ser executada
Local cArquivo  := ""			//Arquivo de trabalho
Local aStruSCJ  := {}

SCJ->(dbSetOrder(3))

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase
DEFAULT ::IndexKey         := SCJ->(IndexKey())
DEFAULT ::PageLen   := 0
DEFAULT ::PageFirst := 0

If PrtChkUser(::UserCode,"MtSellerBudget","BrwBudget","SA3",::SellerCode)
	dbSelectArea("SCJ")
	dbSetOrder(2)
	
	lQuery	:= .T.
	aStruSCJ	:= SCJ->(dbStruct())
	cAliasSCJ:= GetNextAlias()

	cQuery  := "SELECT SCJ.*,SCJ.R_E_C_N_O_ "
	cQuery	+= "FROM "+RetSqlName("SCJ")+" SCJ,"
	cQuery  += RetSqlName("SA1")+" SA1 "
	cQuery	+= "WHERE "
	cQuery	+= "SCJ.CJ_FILIAL = '"+xFilial("SCJ")+"' AND "
	cQuery	+= "SCJ.CJ_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
	cQuery	+= "SCJ.CJ_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
	If !Empty(QuotationOrOrderIDFrom)
		cQuery	+= "SCJ.CJ_COTCLI>='"+::QuotationOrOrderIDFrom+"' AND "
	EndIf
	If !Empty(QuotationOrOrderIDTo)			
		cQuery	+= "SCJ.CJ_COTCLI<='"+::QuotationOrOrderIDTo+"' AND "
	EndIf
	cQuery	+= "SCJ.D_E_L_E_T_=' ' AND "
	cQuery  += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
	cQuery  += "SA1.A1_COD = SCJ.CJ_CLIENTE AND "
	cQuery  += "SA1.A1_LOJA = SCJ.CJ_LOJA AND "
	cQuery  += "(SA1.A1_VEND = '"+::SellerCode+"' OR "
	cQuery  += "SA1.A1_VEND = ' ') AND "
	cQuery	+= "SA1.D_E_L_E_T_=' ' "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	cQuery	+= "ORDER BY "+WsSqlOrder(::IndexKey)

	cQuery	:= ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCJ,.T.,.T.)

	For nX := 1 To Len(aStruSCJ)
		If aStruSCJ[nX][2]<>"C" .And. aStruSCJ[nX][2]<>"M"
			TcSetField(cAliasSCJ,aStruSCJ[nX][1],aStruSCJ[nX][2],aStruSCJ[nX][3],aStruSCJ[nX][4])
		EndIf
	Next nX

	nX := 0
	dbSelectArea(cAliasSCJ)
	While !Eof() .And. xFilial("SCJ") == (cAliasSCJ)->CJ_FILIAL
	    If lQuery
	    	lValido := .T.
	    Else
	    	dbSelectArea("SA1")
	    	dbSetOrder(1)
	    	MsSeek(xFilial("SA1")+(cAliasSCJ)->CJ_CLIENTE+(cAliasSCJ)->CJ_LOJA)
	    	If SA1->A1_VEND == ::SellerCode .OR. Empty(SA1->A1_VEND)
	    		lValido := .T.
	    	Else
	    		lValido := .F.	    		
	    	EndIf
	    EndIf			
		If lValido
			nX++
			If ::PageFirst==0 .Or. nX >= ::PageFirst
				If nY == 0
					::ListOfBudGet := {}
				EndIf
				aAdd(::ListOfBudget,WsClassNew("BudgetHeaderView"))
				nY++
				GetOrcHeader(@::ListOfBudget[nY],cAliasSCJ)
			EndIf
		EndIf
		If nY >= ::PageLen .And. ::PageLen <> 0
			Exit
		EndIf
		dbSelectArea(cAliasSCJ)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSCJ)
		dbCloseArea()
		dbSelectArea("SCJ")
	Else
		RetIndex(cAliasSCJ)
		FErase(cArquivo+OrdBagExt())
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwTotalBudge³Autor  ³Leonardo Tomiato    ³Data  ³06.01.2005 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao do total de orcamentos                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor Loja                                         ³±±
±±³          ³ExpC3: Data Emissao de   	                                   ³±±
±±³          ³ExpC4: Data Emissao ate                                      ³±±
±±³          ³ExpC5: Where para Adicao 	                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao do total de orcamentos                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Analista     ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³10/04/06³096386³-Alterada a query para considerar cliente³±±
±±³              ³        ³      ³e vendedor no filtro.					   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwTotalBudget WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,QuotationOrOrderIDFrom,QuotationOrOrderIDTo,QueryAddWhere WSSEND TotalBudget WSSERVICE MtSellerBudget

Local aArea     := GetArea()	//Guarda a area atual
Local lRetorno  := .T.			//Indica retorno da funcao
Local cAliasSCJ := "SCJ"		//Alias da query
Local cQuery    := ""			//Query a ser executada

DEFAULT ::RegisterDateFrom := dDataBase
DEFAULT ::RegisterDateTo   := dDataBase

If PrtChkUser(::UserCode,"MtSellerBudget","BrwBudget","SA3",::SellerCode)

		cAliasSCJ := GetNextAlias()

		cQuery	:= "SELECT COUNT(*) TOTALBUDGET"
		cQuery	+= "FROM "+RetSqlName("SCJ")+" SCJ,"
		cQuery  += RetSqlName("SA1")+" SA1 "
		cQuery	+= "WHERE "
		cQuery	+= "SCJ.CJ_FILIAL = '"+xFilial("SCJ")+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO>='"+DtoS(::RegisterDateFrom)+"' AND "
		cQuery	+= "SCJ.CJ_EMISSAO<='"+DtoS(::RegisterDateTo)+"' AND "
		If !Empty(QuotationOrOrderIDFrom)
			cQuery	+= "SCJ.CJ_COTCLI>='"+::QuotationOrOrderIDFrom+"' AND "
		EndIf
		If !Empty(QuotationOrOrderIDTo)			
			cQuery	+= "SCJ.CJ_COTCLI<='"+::QuotationOrOrderIDTo+"' AND "
		EndIf
		cQuery	+= "SCJ.D_E_L_E_T_=' ' AND "
		cQuery  += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
		cQuery  += "SA1.A1_COD = SCJ.CJ_CLIENTE AND "
		cQuery  += "SA1.A1_LOJA = SCJ.CJ_LOJA AND "
		cQuery  += "(SA1.A1_VEND = '"+::SellerCode+"' OR "
		cQuery  += "SA1.A1_VEND = ' ') AND "
		cQuery	+= "SA1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCJ,.T.,.T.)

		::TotalBudget := ( cAliasSCJ )->TOTALBUDGET

Else
		lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetBudget	   ³Autor  ³Eduardo Riera       ³Data  ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de recuperacao dos orcamentos de um Vendedor          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de recuperacao dos orcamentos de venda de um Vendedor ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetBudget WSRECEIVE UserCode,SellerCode,BudgetID WSSEND Budget WSSERVICE MtSellerBudget

Local aArea     := GetArea()
Local lRetorno  := .T.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSCJ := "SCJ"
Local cAliasSCK := "SCK"
Local aStruSCK  := {}
Local cQuery    := ""
Local nX        := 0

If PrtChkUser(::UserCode,"MtSellerBudget","GetBudGet","SA3",::SellerCode)
	dbSelectArea("SCJ")
	dbSetOrder(1)
	If MsSeek(xFilial("SCJ")+::BudgetID)

		::Budget:Header	:=WsClassNew('BudgetHeaderView')
		
		GetOrcHeader(@::Budget:Header,cAliasSCJ)

		dbSelectArea("SCK")
		dbSetOrder(1)

		lQuery	:= .T.
		aStruSCK	:= SCK->(dbStruct())
		cAliasSCK:= GetNextAlias()

		cQuery	:= "SELECT * "
		cQuery	+= "FROM "+RetSqlName("SCK")+" SCK "
		cQuery	+= "WHERE "
		cQuery	+= "SCK.CK_FILIAL = '"+xFilial("SCK")+"' AND "
		cQuery	+= "SCK.CK_NUM='"+::BudgetID+"' AND "
		cQuery	+= "SCK.D_E_L_E_T_=' ' "
		cQuery	+= "ORDER BY "+SqlOrder(SCK->(IndexKey()))

		cQuery	:= ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCK,.T.,.T.)

		For nX := 1 To Len(aStruSCK)
			If aStruSCK[nX][2]<>"C"
				TcSetField(cAliasSCK,aStruSCK[nX][1],aStruSCK[nX][2],aStruSCK[nX][3],aStruSCK[nX][4])
			EndIf
		Next nX
		
		dbSelectArea(cAliasSCK)
		While !Eof() .And. xFilial("SCK") == (cAliasSCK)->CK_FILIAL .And.;
				::BudgetID == (cAliasSCK)->CK_NUM

			aAdd(::Budget:Itens,WSClassNew("BudgetItensView"))
			nY++
			GetOrcItem(@::Budget:Itens[nY],cAliasSCK)

			dbSelectArea(cAliasSCK)
			dbSkip()
		EndDo

		If lQuery
			dbSelectArea(cAliasSCK)
			dbCloseArea()
			dbSelectArea("SCK")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETBUDGET",STR0009) //"Orcamento nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutBudget	   ³Autor  ³Eduardo Riera       ³Data  ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de inclusao dos pedidos de venda de um Vendedor       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Dados do Pedido de Venda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de inclusao dos orcamentos de venda de um Vendedor    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutBudget WSRECEIVE UserCode,SellerCode,Budget WSSEND BudgetId WSSERVICE MtSellerBudget

Local aArea		 := GetArea()
Local aCab		 := {}
Local aItens	 := {}
Local aErro      := {}
Local cErro      := ""
Local lRetorno	 := .T.
Local nX         := 0

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSellerBudget","PutBudget","SA3",::SellerCode)
	
	dbSelectArea("SCJ")
	dbSetOrder(1)
	If !Empty(::Budget:Header:BudgetID) 
		MsSeek(xFilial("SCJ")+::Budget:Header:BudgetID)	
	Else
		MsGoto(0)
	EndIf
	
	dbSelectArea("SCJ")
	PutOrcHead(::Budget:Header,@aCab)

	dbSelectArea("SCK")
	PutOrcItem(::Budget,@aItens)

	dbSelectArea("SCJ")
	dbSetOrder(1)
	If !Empty(::Budget:Header:BudgetID) .And. MsSeek(xFilial("SCJ")+::Budget:Header:BudgetID)
		MATA415(aCab,aItens,4)
	Else
		MATA415(aCab,aItens,3)
	EndIf
	::BudGetID := SCJ->CJ_NUM

	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("PUTBUDGET",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³DelBudget	   ³Autor  ³Eduardo Riera       ³Data  ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de exclusao dos orçamentos de um Vendedor             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de exclusao dos orcamentos de venda de um Vendedor    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD DelBudget WSRECEIVE UserCode,SellerCode,BudgetID WSSEND WsNull WSSERVICE MtSellerBudget

Local aCab		:= {}
Local aItens	:= {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSellerBudget","DelBudget","SA3",::SellerCode)

	aAdd(aCab,{"CJ_NUM"		,::BudgetID,})

	MATA415(aCab,aItens,5)
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELBUDGET",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³EffectBudget ³Autor  ³Eduardo Riera       ³Data  ³03.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de efetivacao dos orçamentos de um Vendedor           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Orcamento                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Metodo de efetivacao dos orçamentos de um Vendedor           ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD EffectBudget WSRECEIVE UserCode,SellerCode,BudgetID WSSEND SalesOrder WSSERVICE MtSellerBudget

Local aCab		:= {}
Local aItens	:= {}
Local aErro    := {}
Local lRetorno := .T.
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSellerBudget","EffectBudget","SA3",::SellerCode)
	dbSelectArea("SCJ")
	aAdd(aCab,{"CJ_NUM"    ,::BudGetID,})

	MATA416(aCab,aItens)
	::SalesOrder := SC5->C5_NUM
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("DELBUDGET",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSMAT415  ³ Autor ³Eduardo Riera          ³ Data ³26.11.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service de simulacao do orcamento de venda               ³±±
±±³          ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtSimulationBudget  DESCRIPTION STR0016 NAMESPACE "http://webservices.microsiga.com.br/mtsimulationbudget.apw" //"Serviço de simulação da atualização do orçamento de venda. <br><br><i>Este serviço pode ser utilizado para recuperar o preenchimento automático de algumas informações do pedido de venda, como por exemplo: as regras de desconto ou bonificação. Salientamos que este serviço executa as customização presentes no sistema, possibilitando seu uso.</i>"
WSDATA UserCode               As String
WSDATA BudGetID				  As String
WSDATA OldBudGet    	      As BudGetView
WSDATA NewBudGet	  	      As BudGetView

WSMETHOD PutBudGet            DESCRIPTION STR0017 //"Método de atualização do orçamento de venda"

ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³PutBudGet    ³Autor  ³Eduardo Riera       ³Data  ³26.11.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo de simulacao de atualizacao de um orcamento de venda  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Dados do orcamento de venda                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo simula a atualizacao de um orcamento de venda    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD PutBudGet WSRECEIVE UserCode,OldBudget WSSEND NewBudGet WSSERVICE MtSimulationBudget

Local aArea		:= GetArea()
Local aCab		:= {}
Local aItens	:= {}
Local aErro     := {}
Local cErro     := ""
Local lRetorno	:= .T.
Local nX        := 0
Local aItensAux := {} 	
Local cCodigoTES:= 0		 
Local nIndiceAux:= 0
Local nPosTes	:= 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSimulationOldBudGet","PutBudGet")

	dbSelectArea("SCJ")
	PutOrcHead(::OldBudget:Header,@aCab)
	aCab := WsAutoOpc(@aCab,.T.)

	dbSelectArea("SCK")
	PutOrcItem(::OldBudget,@aItens)
	For nX := 1 To Len(aItens)
		aItens[nX] := WsAutoOpc(@aItens[nX],.T.)
	Next nX
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se eh alteracao ou inclusao                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	dbSelectArea("SCJ")
	dbSetOrder(1)
	If !Empty(::OldBudget:Header:BudgetID) .And. MsSeek(xFilial("SCJ")+::OldBudget:Header:BudgetID)
		MATA415(aCab,aItens,4,.T.)
		
		cCodigoTES:=GETDTOVAL(SUPERGETMV("MV_BONUSTS", .F., 0))		  
		For nIndiceAux:=1 To Len(aItens)
			nPosTes := aScan( aItens[nIndiceAux], {|x| AllTrim(x[1]) == "CK_TES"} )
			If nPosTes > 0
			    If (Val(aItens[nIndiceAux][nPosTes][2]) != cCodigoTES)
		    		aAdd(aItensAux,aItens[nIndiceAux])
			    EndIf
			Else
				aAdd(aItensAux,aItens[nIndiceAux])
			EndIf
		Next nIndiceAux
		aItens:=aItensAux
	Else
		MATA415(aCab,aItens,3,.T.)
		
		cCodigoTES:=GETDTOVAL(SUPERGETMV("MV_BONUSTS", .F., 0))		  
		For nIndiceAux:=1 To Len(aItens)
			nPosTes := aScan( aItens[nIndiceAux], {|x| AllTrim(x[1]) == "CK_TES"} )
			If nPosTes > 0
			    If (Val(aItens[nIndiceAux][nPosTes][2]) != cCodigoTES)
		    		aAdd(aItensAux,aItens[nIndiceAux])
			    EndIf
			Else
				aAdd(aItensAux,aItens[nIndiceAux])
			EndIf
		Next nIndiceAux
		aItens:=aItensAux
			
	EndIf
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		
		//SetSoapFault("PutBudGet",cErro)
		SetSoapFault("PutBudGet",STR0020) //"Erro na gravação dos dados"
		Conout('*********************************************************************')
		Conout('****************** Log - Portal *************************************') 
		Conout('*********************************************************************')
		Conout(cErro)
		Conout('*********************************************************************')
		Conout('****************** Log - Portal *************************************') 
		Conout('*********************************************************************')
		lRetorno := .F.
	EndIf
	If lRetorno
		aCab := WsAutoOpc(@aCab,.T.)
		For nX := 1 To Len(aItens)
			aItens[nX] := WsAutoOpc(@aItens[nX],.T.)
		Next nX
		GetOrcHeader(::NewBudGet:Header,"SCJ",aCab)		
		For nX := 1 To Len(aItens)
			aAdd(::NewBudGet:Itens,WSClassNew("BudgetItensView"))
			GetOrcItem(::NewBudGet:Itens[nX],"SCK",aItens[nX])
		Next nX
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno) 

//osmar
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±GetNextNumber    ³Autor  ³osmar Cioni Estevam Junior  ³Data  ³18.09.2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Metodo que retorna o pórximo numero do orçamento			   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Não se aplica							                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo retorna o pórximo numero do orçamento    ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetNextNumber WSRECEIVE UserCode WSSEND NextNumber WSSERVICE MtSellerBudget

Local aErro     := {}
Local cErro     := ""
Local lRetorno	:= .T.
Local nX        := 0

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"MtSellerBudget","PutBudGet")

    ::NextNumber:=GetSXENum('SCJ','CJ_NUM')
	If lMsErroAuto
		aErro := GetAutoGRLog()
		For nX := 1 To Len(aErro)
			cErro += aErro[nX] + Chr(13)+Chr(10)
		Next nX
		SetSoapFault("PutBudGet",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf

Return(lRetorno)