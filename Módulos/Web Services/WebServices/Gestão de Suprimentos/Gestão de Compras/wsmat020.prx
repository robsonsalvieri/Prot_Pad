#INCLUDE "wsmat020.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSMAT020   Autor Eduardo Riera           Data 18.03.2003  
Ĵ
Descrio  Web Service responsavel pela manutencao das informacoes      
          de Fornecedores                                               
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Web Service                                                  
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE MtSupplier               DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtsupplier.apw" //"Servio de consulta e atualizao das informaes do fornecedor ( <b>Restrio de fornecedor</b> )"
   WSDATA UserCode                 AS String
   WSDATA SupplierID               AS String
   WSDATA Supplier                 As SupplierView
   WSDATA ListSupplier             As Array Of SupplierView
   WSDATA Header                   As Array Of BrwHeader
   WSDATA HeaderType               As String
   WSDATA WsNull                   As String
   WSDATA TypeOfAddress            As Array Of GenericStruct
   WSDATA TypeOfPhone              As Array Of GenericStruct
      
   WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD GetTypeOfAddress DESCRIPTION STR0003 //"Mtodo que descreve os tipo de endereos"
   WSMETHOD GetTypeOfPhone   DESCRIPTION STR0004 //"Mtodo que descreve os tipo de telefones"
   WSMETHOD GetSupplier      DESCRIPTION STR0005 //"Mtodo de consulta as informaes do fornecedor"
   WSMETHOD GetListSupplier  DESCRIPTION STR0005 //"Mtodo de consulta as informaes do fornecedor"
   WSMETHOD PutSupplier      DESCRIPTION STR0006 //"Mtodo de atualizao as informaes do fornecedor"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 06.06.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICE                                                 
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSupplier

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    GetSupplieAutor   Eduardo Riera          Data 06.06.2003 
Ĵ
          Rotina de obtencao dos dados de fornecedores                 
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do fornecedor                 
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetSupplier WSRECEIVE UserCode,SupplierID WSSEND Supplier WSSERVICE MtSupplier

Local aArea    := GetArea()
Local lRetorno := .T.
Local cFornece := SubStr(::SupplierID,1,Len(SA2->A2_COD))
Local cLoja    := SubStr(::SupplierID,Len(SA2->A2_COD)+1)
Local nX       := 0

If PrtChkUser(::UserCode,"MTSUPPLIER","GetSupplier","SA2",::SupplierID)
	dbSelectArea("SA2")
	dbSetOrder(1)
	If MsSeek(xFilial("SA2")+cFornece+cLoja)	
		::Supplier:SupplierCode     := SA2->A2_COD
		::Supplier:UnitSupplierCode := SA2->A2_LOJA
		::Supplier:Name             := SA2->A2_NOME
		::Supplier:NickName         := SA2->A2_NREDUZ
		::Supplier:FederalID        := SA2->A2_CGC
		::Supplier:StateID          := SA2->A2_INSCR
		::Supplier:DistrictID       := SA2->A2_INSCRM
		::Supplier:HomePage         := SA2->A2_HPAGE
		::Supplier:Email            := SA2->A2_EMAIL
		UserFields("SA2",@::Supplier:UserFields)

		::Supplier:Addresses := {}
		nX := 0
		If !Empty(SA2->A2_END)
			aadd(::Supplier:Addresses,WsClassNew("AddressView"))
			nX++
			::Supplier:Addresses[nX]:TypeOfAddress := "1"
			::Supplier:Addresses[nX]:Address       := FisGetEnd(SA2->A2_END)[1]
			::Supplier:Addresses[nX]:AddressNumber := AllTrim(Str(FisGetEnd(SA2->A2_END)[2],8))
			::Supplier:Addresses[nX]:District      := SA2->A2_MUN
			::Supplier:Addresses[nX]:State         := SA2->A2_EST
			::Supplier:Addresses[nX]:ZipCode       := SA2->A2_CEP
			::Supplier:Addresses[nX]:Zone          := SA2->A2_BAIRRO
		EndIf
					
		::Supplier:Phones := {}
		nX := 0
		If !Empty(SA2->A2_TEL)
			aadd(::Supplier:Phones,WsClassNew("PhoneView"))
			nX++
			::Supplier:Phones[nX]:TypeOfPhone     := "1"
			::Supplier:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA2->A2_TEL,SA2->A2_DDD,SA2->A2_DDI)[3],15))
			::Supplier:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA2->A2_TEL,SA2->A2_DDD,SA2->A2_DDI)[2],15))
			::Supplier:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA2->A2_TEL,SA2->A2_DDD,SA2->A2_DDI)[1],15))
			If ::Supplier:Phones[nX]:CountryAreaCode == "0"
				::Supplier:Phones[nX]:CountryAreaCode := ""
			EndIf
		EndIf
		
		If !Empty(SA2->A2_FAX)
			aadd(::Supplier:Phones,WsClassNew("PhoneView"))
			nX++
			::Supplier:Phones[nX]:TypeOfPhone     := "2"
			::Supplier:Phones[nX]:PhoneNumber     := AllTrim(Str(FisGetTel(SA2->A2_FAX,SA2->A2_DDD,SA2->A2_DDI)[3],15))
			::Supplier:Phones[nX]:LocalAreaCode   := AllTrim(Str(FisGetTel(SA2->A2_FAX,SA2->A2_DDD,SA2->A2_DDI)[2],15))
			::Supplier:Phones[nX]:CountryAreaCode := AllTrim(Str(FisGetTel(SA2->A2_FAX,SA2->A2_DDD,SA2->A2_DDI)[1],15))
			If ::Supplier:Phones[nX]:CountryAreaCode == "0"
				::Supplier:Phones[nX]:CountryAreaCode := ""
			EndIf
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETSUPPLIER",STR0007) //"Fornecedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetListSupplierAutor   Servios CRM      Data 17.08.2015 
Ĵ
          Rotina de obtencao dos dados de fornecedores                 
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura contendo todos 
          fornecedores                                                 
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetListSupplier WSRECEIVE UserCode WSSEND ListSupplier WSSERVICE MtSupplier

Local aArea    	:= GetArea()
Local lRetorno 	:= .T.
Local cQuery   	:= ""
Local cAliasSA2	:= "SA2FOR"
Local nCount		:= 0

	cQuery 	:= "SELECT * "
	cQuery 	+= "FROM " + RetSqlName( "SA2" ) + " SA2 "
	cQuery 	+= "WHERE "
	cQuery    	+= "SA2.D_E_L_E_T_=' ' "
	
	cQuery 	:= ChangeQuery( cQuery )
	
	DbUseArea( .T., "TOPCONN", TcGenQry( ,, cQuery ), cAliasSA2, .T., .T. )
	
	While !(cAliasSA2)->(EOF())
		aadd(::ListSupplier,WsClassNew("SupplierView"))
		nCount++
		::ListSupplier[nCount]:SupplierCode     := (cAliasSA2)->A2_COD
		::ListSupplier[nCount]:UnitSupplierCode := (cAliasSA2)->A2_LOJA
		::ListSupplier[nCount]:Name             := (cAliasSA2)->A2_NOME
		::ListSupplier[nCount]:NickName         := SA2->A2_NREDUZ
		::ListSupplier[nCount]:FederalID        := SA2->A2_CGC
		::ListSupplier[nCount]:StateID          := SA2->A2_INSCR
		::ListSupplier[nCount]:DistrictID       := SA2->A2_INSCRM
		::ListSupplier[nCount]:HomePage         := SA2->A2_HPAGE
		::ListSupplier[nCount]:Email            := SA2->A2_EMAIL
		::ListSupplier[nCount]:Addresses := {}
		::ListSupplier[nCount]:Phones := {}

		If !Empty(SA2->A2_END)
			aadd(::ListSupplier[nCount]:Addresses,WsClassNew("AddressView"))
			::ListSupplier[nCount]:Addresses[1]:TypeOfAddress := "1"
			::ListSupplier[nCount]:Addresses[1]:Address       := FisGetEnd(SA2->A2_END)[1]
			::ListSupplier[nCount]:Addresses[1]:AddressNumber := AllTrim(Str(FisGetEnd(SA2->A2_END)[2],8))
			::ListSupplier[nCount]:Addresses[1]:District      := SA2->A2_MUN
			::ListSupplier[nCount]:Addresses[1]:State         := SA2->A2_EST
			::ListSupplier[nCount]:Addresses[1]:ZipCode       := SA2->A2_CEP
			::ListSupplier[nCount]:Addresses[1]:Zone          := SA2->A2_BAIRRO
		EndIf
		dbSkip()
	EndDo
	(cAliasSA2)->(dbCloseArea())
RestArea(aArea)	
Return lRetorno
/*/

Ŀ
Funo    PutSupplieAutor   Eduardo Riera          Data 06.06.2003 
Ĵ
          Rotina de atualizacao dos dados de fornecedores              
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo de identificacao do fornecedor                 
          ExpO3: Estrutura a ser gravada                               
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICE                                                 
ٱ


/*/
WSMETHOD PutSupplier WSRECEIVE UserCode,SupplierID,Supplier WSSEND WsNull WSSERVICE MtSupplier

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local lRetorno := .T.
Local cFornece := PadR( SubStr(::SupplierID,1,Len(SA2->A2_COD)), Len(SA2->A2_COD) )
Local cLoja    := PadR( SubStr(::SupplierID,Len(SA2->A2_COD)+1), 2 )
Local cNumero  := ""
Local nNumero  := 0
Local cErro    := ""
Local nX       := 0
PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.

DEFAULT WsNull := ""

If PrtChkUser(::UserCode,"MTSupplier","PutSupplier","SA2",::SupplierID)
	dbSelectArea("SA2")
	dbSetOrder(1)
	If MsSeek(xFilial("SA2")+cFornece+cLoja)
		aadd(aDados,{"A2_COD",cFornece,})
		aadd(aDados,{"A2_LOJA",cLoja,})
		aadd(aDados,{"A2_NOME",::Supplier:Name,})
		aadd(aDados,{"A2_NREDUZ",::Supplier:NickName,})
		aadd(aDados,{"A2_CGC",::Supplier:FederalID,})
		aadd(aDados,{"A2_INSCR",::Supplier:StateID,})
		aadd(aDados,{"A2_INSCRM",::Supplier:DistrictID,})
		aadd(aDados,{"A2_HPAGE",::Supplier:HomePage,})
		aadd(aDados,{"A2_EMAIL",::Supplier:Email,})
		If Empty(SA2->A2_ID_FBFN)
			aadd(aDados,{"A2_ID_FBFN","3",}) //-- CONFIGURA VALOR PADRAO: 3 - Todos
		Else
			aadd(aDados,{"A2_ID_FBFN",Left(SA2->A2_ID_FBFN,1),}) //-- CAMPO OBRIGATORIO
		EndIf
		For nX := 1 To Len(::Supplier:Addresses)
			If ::Supplier:Addresses[nX]:TypeOfAddress == "1"
				nNumero := Val(::Supplier:Addresses[nX]:AddressNumber)
				cNumero := IIF( nNumero>0, ", "+AllTrim(::Supplier:Addresses[nX]:AddressNumber), "" )
				aadd(aDados,{"A2_END",AllTrim(::Supplier:Addresses[nX]:Address)+cNumero,})
				aadd(aDados,{"A2_EST",::Supplier:Addresses[nX]:State,})
 				aadd(aDados,{"A2_COD_MUNI",,})
 				aadd(aDados,{"A2_MUN",::Supplier:Addresses[nX]:District,})
				aadd(aDados,{"A2_CEP",::Supplier:Addresses[nX]:ZipCode,})
				aadd(aDados,{"A2_BAIRRO",::Supplier:Addresses[nX]:Zone,})
			EndIf
		Next nX
		
		For nX := 1 To Len(::Supplier:Phones)
			If ::Supplier:Phones[nX]:TypeOfPhone == "1"
				aadd(aDados,{"A2_TEL",::Supplier:Phones[nX]:PhoneNumber,})
				aadd(aDados,{"A2_DDD",::Supplier:Phones[nX]:LocalAreaCode,})
				aadd(aDados,{"A2_DDI",::Supplier:Phones[nX]:CountryAreaCode,})
			EndIf
			If ::Supplier:Phones[nX]:TypeOfPhone == "2"
				aadd(aDados,{"A2_FAX",::Supplier:Phones[nX]:PhoneNumber,})
			EndIf
		Next nX
		//Ŀ
		//Gravacao ao do cadastro de fornecedores                                 
		//
		PutUserFields("SA2",@::Supplier:UserFields,@aDados)
		//Ŀ
		//Atualizacao do cadastro de fornecedores                                 
		//
		aDados := WsAutoOpc(aDados)
		Mata020(aDados,4)
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX	
			SetSoapFault("PUTSUPPLIER",cErro)
			lRetorno := .F.
		EndIf		
	Else
		lRetorno := .F.
		SetSoapFault("PUTSUPPLIER",STR0007) //"Fornecedor nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE MtSupplier

aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0008 //"COMERCIAL"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[2]:Code := "2"
//::TypeOfAddress[2]:Description := "COBRANCA"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[3]:Code := "3"
//::TypeOfAddress[3]:Description := "ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 04.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE MtSupplier

aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0008 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0009 //"FAX"

Return(.T.)

Function wsmat020()
Return
