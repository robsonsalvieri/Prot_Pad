#INCLUDE "WSFAT300.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMBROWSE.CH"
#INCLUDE "FWMVCDEF.CH"


/*/


Ŀ
Funo    WSFAT300   Autor Eduardo Riera           Data 20.11.2003  
Ĵ
Descrio  Web Service das funcionalidades da Oportunidade de Venda do  
           portal de cliente.                                           
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE MtSellerOpportunity     DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/mtselleropportunity.apw" //"Servio de consulta e atualizaa das oportunidades de venda ( <b>Restrio de representante comercial<b> )"
	WSDATA Header                 AS Array Of BrwHeader
	WSDATA HeaderType             As String
	WSDATA UserCode               As String
	WSDATA SellerCode			  AS String
	WSDATA StartDateFrom          AS Date
	WSDATA StartDateTo            AS Date
	WSDATA QueryAddWhere          AS String
	WSDATA IndexKey               AS String
	WSDATA ListOfOpportunity      AS Array Of OpportunityStruct
	WSDATA OpportunityID          As String
	WSDATA Opportunity            As OpportunityView
	WSDATA WsNull                 As String
	
	WSMETHOD GetHeader        DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
	WSMETHOD BrwOpportunity	  DESCRIPTION STR0003 //"Mtodo de listagem das informaes da oportunidade de venda"
	WSMETHOD GetOpportunity   DESCRIPTION STR0004 //"Mtodo de consulta as informaes da oportunidade de venda"
	WSMETHOD PutOpportunity   DESCRIPTION STR0005 //"Mtodo de atualizao das informaes da oportunidade de venda"
	WSMETHOD DelOpportunity   DESCRIPTION STR0006 //"Mtodo de excluso das informaes da oportunidade de venda"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 20.11.2003 
Ĵ
          Rotina de recuperacao dos headers deste WS                   
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtSellerOpportunity

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    BrwOpport Autor   Eduardo Riera          Data 20.11.2003 
Ĵ
          Rotina de pesquisa das oportunidades de venda                
                                                                       
Ĵ
ParametrosExpC1: Codigo do Usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpD3: Data de Inicio Inicial                                
          ExpD4: Data de Inicio Final                                  
          ExpC5: Query a ser adicionada no WS                          
          ExpC6: Chave de Ordenacao em codebase                        
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma lista de oportunidades               
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Analista      Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    31/03/0695206 -Inicializacao da variavel lQuery.       
ٱ


/*/
WSMETHOD BrwOpportunity WSRECEIVE UserCode,SellerCode,StartDateFrom,StartDateTo,QueryAddWhere,IndexKey WSSEND ListOfOpportunity WSSERVICE MtSellerOpportunity

Local aArea    := GetArea()
Local cArquivo := ""
Local cAliasAD1:= "AD1"
Local cQuery   := ""
Local lQuery   := .F.
Local lRetorno := .T.
Local nX       := 0
#IFDEF TOP
Local aStruAD1 := {}
#ENDIF

DEFAULT ::StartDateFrom := dDataBase-90
DEFAULT ::StartDateTo   := dDataBase
DEFAULT ::IndexKey         := AD1->(IndexKey())

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"MTSELLEROPPORTUNITY","BRWOPPORTUNITY","SA3",::SellerCode)
	dbSelectArea("AD1")
	dbSetOrder(1)	
	#IFDEF TOP
		lQuery := .T.
		cAliasAD1 := "BRWOPPORTUNITY"
		aStruAD1  := AD1->(dbStruct())

		cQuery    := "SELECT * "
		cQuery    += "FROM "+RetSqlName("AD1")+" AD1 WHERE "
		cQuery    += "AD1.AD1_FILIAL = '"+xFilial("AD1")+"' AND "
		cQuery    += "AD1.AD1_VEND = '"+::SellerCode+"' AND "
		cQuery    += "AD1.AD1_DTINI >= '"+DToS(::StartDateFrom)+"' AND "
		cQuery    += "AD1.AD1_DTINI <= '"+DToS(::StartDateTo)+"' AND "
		cQuery    += "AD1.D_E_L_E_T_ = ' ' "
		cQuery    += "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAD1)

		For nX := 1 To Len(aStruAD1)
			If aStruAD1[nX][2] <> "C"
				TcSetField(cAliasAD1,aStruAD1[nX][1],aStruAD1[nX][2],aStruAD1[nX][3],aStruAD1[nX][4])
			EndIf
		Next nX
	#ELSE
		cArquivo  := CriaTrab(,.F.)

		cQuery    := "AD1_FILIAL == '"+xFilial("AD1")+"' .AND. "
		cQuery    += "AD1_VEND == '"+::SellerCode+"' .AND. "
		cQuery    += "DToS(AD1_DTINI) >= '"+DToS(::StartDateFrom)+"' .AND. "
		cQuery    += "DToS(AD1_DTINI) <= '"+DToS(::StartDateTo)+"' "

		IndRegua(cAliasAD1,cArquivo,::IndexKey,,cQuery)
		dbGotop()

	#ENDIF
	nX := 0		
	While !Eof()

		aadd(::ListOfOpportunity,WSClassNew("OpportunityStruct"))
		nX++
		GetOport(@::ListOfOpportunity[nX],cAliasAD1)

		dbSelectArea(cAliasAD1)		
		dbSkip()

	EndDo
	If lQuery
		dbSelectArea(cAliasAD1)
		dbCloseArea()
		dbSelectArea("AD1")		
	Else
		RetIndex("AD1")
		FErase(cArquivo+OrdBagExt())
		dbSelectArea("AD1")		
	EndIf
	If Empty(::ListOfOpportunity)
		lRetorno := .F.
		SetSoapFault("BRWOPPORTUNITY",STR0007)	 //"Nao h oportunidades para estes parametros"
	EndIf
Else
	lRetorno := .F.
	SetSoapFault("BRWOPPORTUNITY",STR0008) //"Usuario invalido"
EndIf
RestArea(aArea)
Return(lRetorno)
/*/

Ŀ
Funo    GetOpport Autor   Eduardo Riera          Data 21.11.2003 
Ĵ
          Rotina de recuperacao da oportunidade                        
                                                                       
Ĵ
ParametrosExpC1: Codigo do Usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpC3: Codigo da Oportunidade de venda                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma lista de oportunidades               
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetOpportunity WSRECEIVE UserCode,SellerCode,OpportunityID WSSEND Opportunity WSSERVICE MtSellerOpportunity

Local aArea    	:= GetArea()
Local lRetorno 	:= .T.
Local nX		:= 0

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"MTSELLEROPPORTUNITY","GETOPPORTUNITY","SA3",::SellerCode)
	dbSelectArea("AD1")
	dbSetOrder(1)	
	If MsSeek(xFilial("AD1")+::OpportunityID) .And. AD1->AD1_VEND == ::SellerCode

		::Opportunity := WSClassNew( "OpportunityView" )
		nX++
		GetOport(@::Opportunity,"AD1",2)

	Else
		lRetorno := .F.
		SetSoapFault("GETOPPORTUNITY",STR0009) //"Oportunidade de Venda no encontrada"
	EndIf
Else
	lRetorno := .F.
	SetSoapFault("GETOPPORTUNITY",STR0008) //"Usuario invalido"
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    PutOpport Autor   Eduardo Riera          Data 21.11.2003 
Ĵ
          Rotina de atualizacao da oportunidade de venda               
                                                                       
Ĵ
ParametrosExpC1: Codigo do Usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto da Oportunidade de Venda                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o codigo da oportunidade atualizado      
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                      
Ĵ
 Analista      Data    BOPS   Motivo da Alteracao                    
Ĵ
 Cleber M.    31/03/0695206 -Alteracao/criacao dos arrays que sao    
                             enviados para a MsExecAuto FATA300().   
                            -Correcao no nome do campo AD2_CODVEN p/ 
                             AD2_VEND.                               
 Cleber M.    03/05/0697035 -Verificacao se os campos obrigatorios   
                             das rotinas Time de Vendas, Concorren-  
                             tes, Parceiros e Contatos estao preen-  
                             chidos corretamente.                    
ٱ


/*/
WSMETHOD PutOpportunity WSRECEIVE UserCode,SellerCode,Opportunity WSSEND OpportunityID WSSERVICE MtSellerOpportunity

Local aArea      	:= GetArea()	//Guarda a area atual
Local aCab      	:= {}			//Array com os dados do cabecalho (AD1)
Local aSalesTeam 	:= {}			//Array com os itens do time de vendas (AD2) 
Local aAllSalesTeam	:= {}			//Array usado na MsExecAuto
Local aPartners  	:= {}			//Array com os itens de Parceiros (AD4)
Local aAllPartners  := {}			//Array usado na MsExecAuto
Local aContacts  	:= {}			//Array com os itens de Contatos (AD9)
Local aAllContacts  := {}			//Array usado na MsExecAuto
Local aCompetitor	:= {}			//Array com os itens de Concorrentes (AD3)
Local aAllCompetitor:= {}			//Array usado na MsExecAuto
Local aNoDeleted 	:= {}			//Array dos itens nao deletados
Local aErro      	:= {}			//Array com o retorno do erro
Local cErro      	:= ""			//Armazena o erro
Local lRetorno   	:= .T.			//Retorno da funcao
Local nOperacao  	:= 0			//Operacao a executar (3=inclusao,4=alteracao)
Local nX         	:= 0			//Variavel usada em lacos For...Next

PRIVATE lMsErroAuto := .F.
PRIVATE lAutoErrNoFile := .T.
//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"MTSELLEROPPORTUNITY","PUTOPPORTUNITY","SA3",::SellerCode)
	cAliasAD1 := "AD1"

	//Ŀ
	//Verificacao a operacao                                                  
	//
	dbSelectArea("AD1")
	dbSetOrder(1)
	If Empty(::Opportunity:Header:OpportunityID) .Or. !MsSeek(xFilial("AD1")+::Opportunity:Header:OpportunityID)
		nOperacao := 3
	Else
		nOperacao := 4
	EndIf
	//Ŀ
	//Verificacao o vendedor                                                  
	//
	If nOperacao == 4
		If 	AD1->AD1_VEND <> ::SellerCode
			lRetorno := .F.
			SetSoapFault("PUTOPPORTUNITY",STR0010) //"Vendedor invalido para esta oportunidade de venda"
		EndIf
	EndIf
	
	//FSY - 14/10/2015 - Validao para os campos FCS e FCI
	If Empty(::Opportunity:Header:CriticalSuccessFactor) .and. Empty(::Opportunity:Header:CriticalFailureFactor)
		lRetorno := .F.
		SetSoapFault("PUTOPPORTUNITY",STR0013) //"O campo FCS ou FCI devem ser preenchidos (Cabealho)"
	End If 	

	//FSY - 14/10/2015 - Validao para vendedor, se tiver time o vendedor  obrigatorio
	If Len(::Opportunity:SalesTeam)>0 .and. Empty(::Opportunity:Header:SellerCode)
		lRetorno := .F.
		SetSoapFault("PUTOPPORTUNITY",STR0014) // "O campo do vendedor deve ser preenchido (Cabealho)"
	End If 	
		
	If Empty( ::Opportunity:Header:ProspectCode ) .Or. Empty( ::Opportunity:Header:UnitProspectCode ) 
		lRetorno := .F.
		SetSoapFault("PUTOPPORTUNITY",STR0012) //"Prospect ou Loja Prospect devem ser preenchidos"
	EndIf
	//Ŀ
	//Analise do dados enviados                                               
	//
	If lRetorno
		If !Empty( ::Opportunity:Header:OpportunityID )
			aadd(aCab,{"AD1_NROPOR",::Opportunity:Header:OpportunityID,Nil})
		EndIf
		aadd(aCab,{"AD1_DESCRI",::Opportunity:Header:Description,Nil})
		aadd(aCab,{"AD1_DATA",::Opportunity:Header:IssueDate,Nil})
		aadd(aCab,{"AD1_VEND",::Opportunity:Header:SellerCode,Nil})
		aadd(aCab,{"AD1_DTINI",::Opportunity:Header:StartDate,Nil})
		aadd(aCab,{"AD1_DTFIM",::Opportunity:Header:GoalDate,Nil})
		aadd(aCab,{"AD1_PROSPE",::Opportunity:Header:ProspectCode,Nil})
		aadd(aCab,{"AD1_LOJPRO",::Opportunity:Header:UnitProspectCode,Nil})
		aadd(aCab,{"AD1_PROVEN",::Opportunity:Header:SalesProcess,Nil})
		aadd(aCab,{"AD1_STAGE",::Opportunity:Header:SalesStage,Nil})
		aadd(aCab,{"AD1_PERCEN",::Opportunity:Header:Accomplishment,Nil})
		aadd(aCab,{"AD1_VERBA",::Opportunity:Header:ProjectAllocatedAmount,Nil})
		aadd(aCab,{"AD1_MOEDA",::Opportunity:Header:Currency,Nil})
		aadd(aCab,{"AD1_CODPRO",::Opportunity:Header:ProductCode,Nil})
		aadd(aCab,{"AD1_FCS",::Opportunity:Header:CriticalSuccessFactor,Nil})
		aadd(aCab,{"AD1_FCI",::Opportunity:Header:CriticalFailureFactor,Nil})
		aadd(aCab,{"AD1_STATUS",::Opportunity:Header:OpportunityStatus,Nil})
		aadd(aCab,{"AD1_PRIOR",::Opportunity:Header:Priority,Nil})
		aadd(aCab,{"AD1_NUMORC",::Opportunity:Header:BudgetID,Nil})
		aadd(aCab,{"AD1_MEMO",::Opportunity:Header:Notes,Nil})
		PutUserFields("AD1",@::Opportunity:Header:UserFields,@aCab)
		aCab := WsAutoOpc(aCab)
		//Ŀ
		//Verificacao do time de vendas                                           
		//
		aNoDeleted := {}
		For nX := 1 To Len(::Opportunity:SalesTeam)
			aadd(aNoDeleted,::Opportunity:SalesTeam[nX]:Code)
			If !Empty(::Opportunity:SalesTeam[nX]:Code)
				aadd(aSalesTeam,{"AD2_VEND",::Opportunity:SalesTeam[nX]:Code,Nil})
				PutUserFields("AD2",@::Opportunity:SalesTeam[nX]:UserFields,@aSalesTeam)
				aSalesTeam := WsAutoOpc(aSalesTeam)
				aAdd(aAllSalesTeam, aSalesTeam)			
				aSalesTeam := {}
			EndIf
		Next nX				
		If nOperacao == 4
			dbSelectArea("AD2")	
			dbSetOrder(1)
			MsSeek(xFilial("AD2")+AD1->AD1_NROPOR+AD1->AD1_REVISA)
			While !Eof() .And. xFilial("AD2") == AD2->AD2_FILIAL .And.;
					AD1->AD1_NROPOR == AD2->AD2_NROPOR .And.;
					AD1->AD1_REVISA == AD2->AD2_REVISA

				If aScan(aNoDeleted,AD2->AD2_VEND) == 0
					aadd(aAllSalesTeam,{{"AD2_VEND",AD2->AD2_VEND,Nil},{"AUTDELETA","S"}})
				EndIf

				dbSelectArea("AD2")
				dbSkip()
			EndDo
		EndIf
		//Ŀ
		//Verificacao dos concorrentes                                            
		//
		aNoDeleted := {}
		For nX := 1 To Len(::Opportunity:Competitor)
			aadd(aNoDeleted,::Opportunity:Competitor[nX]:CompetitorCode)
			If !Empty(::Opportunity:Competitor[nX]:CompetitorCode)
				aadd(aCompetitor,{"AD3_CODCON",::Opportunity:Competitor[nX]:CompetitorCode,Nil})
				aadd(aCompetitor,{"AD3_PRECO",::Opportunity:Competitor[nX]:Price,Nil})
				aadd(aCompetitor,{"AD3_MOEDA",::Opportunity:Competitor[nX]:Currency,Nil})
				aadd(aCompetitor,{"AD3_FCS",::Opportunity:Competitor[nX]:CriticalSuccessFactor,Nil})
				aadd(aCompetitor,{"AD3_FCI",::Opportunity:Competitor[nX]:CriticalFailureFactor,Nil})							
				PutUserFields("AD3",@::Opportunity:Competitor[nX]:UserFields,@aCompetitor)
				aCompetitor := WsAutoOpc(aCompetitor)
				aAdd(aAllCompetitor,aCompetitor)
				aCompetitor := {}
			EndIf
		Next nX
		If nOperacao == 4
			dbSelectArea("AD3")	
			dbSetOrder(1)
			MsSeek(xFilial("AD3")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
			While !Eof() .And. xFilial("AD3") == AD3->AD3_FILIAL .And.;
					(cAliasAD1)->AD1_NROPOR == AD3->AD3_NROPOR .And.;
					(cAliasAD1)->AD1_REVISA == AD3->AD3_REVISA

				If aScan(aNoDeleted,AD3->AD3_CODCON) == 0
					aadd(aAllCompetitor,{{"AD3_CODCON",AD3->AD3_CODCON,Nil},{"AUTDELETA","S"}})
				EndIf

				dbSelectArea("AD3")			
				dbSkip()		
			EndDo		
		EndIf
		//Ŀ
		//Verificacao dos parceiros                                               
		//
		aNoDeleted := {}
		For nX := 1 To Len(::Opportunity:Partners)
			aadd(aNoDeleted,::Opportunity:Partners[nX]:Code)
			If !Empty(::Opportunity:Partners[nX]:Code)
				aadd(aPartners,{"AD4_PARTNE",::Opportunity:Partners[nX]:Code,Nil})
				PutUserFields("AD4",@::Opportunity:Partners[nX]:UserFields,@aPartners)
				aPartners := WsAutoOpc(aPartners)
				aAdd(aAllPartners,aPartners)
				aPartners := {}
			EndIf
		Next nX
		If nOperacao == 4
			dbSelectArea("AD4")
			dbSetOrder(1)
			MsSeek(xFilial("AD4")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
			While !Eof() .And. xFilial("AD4") == AD4->AD4_FILIAL .And.;
					(cAliasAD1)->AD1_NROPOR == AD4->AD4_NROPOR .And.;
					(cAliasAD1)->AD1_REVISA == AD4->AD4_REVISA

				If aScan(aNoDeleted,AD4->AD4_PARTNE) == 0
					aadd(aAllPartners,{{"AD4_PARTNE",AD4->AD4_PARTNE,Nil},{"AUTDELETA","S"}})
				EndIf

				dbSelectArea("AD4")
				dbSkip()
			EndDo
		EndIf
		//Ŀ
		//Verificacao dos contatos                                                
		//
		aNoDeleted := {}
		For nX := 1 To Len(::Opportunity:Contacts)
			aadd(aNoDeleted,::Opportunity:Contacts[nX]:ContactID)
			If !Empty(::Opportunity:Contacts[nX]:ContactID)
				aadd(aContacts,{"AD9_CODCON",::Opportunity:Contacts[nX]:ContactID,Nil})
				PutUserFields("AD9",@::Opportunity:Contacts[nX]:UserFields,@aContacts)
				aContacts := WsAutoOpc(aContacts)
				aAdd(aAllContacts,aContacts)
				aContacts := {}
			EndIf
		Next nX
		If nOperacao == 4
			dbSelectArea("AD9")
			dbSetOrder(1)
			MsSeek(xFilial("AD9")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
			While !Eof() .And. xFilial("AD9") == AD9->AD9_FILIAL .And.;
					(cAliasAD1)->AD1_NROPOR == AD9->AD9_NROPOR .And.;
					(cAliasAD1)->AD1_REVISA == AD9->AD9_REVISA

				If aScan(aNoDeleted,AD9->AD9_CODCON) == 0
					aadd(aAllContacts,{{"AD9_CODCON",AD9->AD9_CODCON,Nil},{"AUTDELETA","S"}})
				EndIf

				dbSelectArea("AD9")
				dbSkip()
			EndDo
		EndIf
		
		GravaAC8(::Opportunity:Header:ProspectCode+::Opportunity:Header:UnitProspectCode, ::Opportunity:Contacts)
		
		//Ŀ
		//Atualizacao da oportunidade de venda                                    
		//
		Fata300(nOperacao,		aCab,	aAllSalesTeam,	aAllCompetitor,;
				aAllPartners,	aAllContacts)
		
		::OpportunityID := AD1->AD1_NROPOR

		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("PUTOPPORTUNITY",cErro)
			lRetorno := .F.
		EndIf						
	EndIf		
	
Else
	lRetorno := .F.
	SetSoapFault("PUTOPPORTUNITY","Usuario invalido")
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
Ĵ
Funo    GravaAC8  Autor Servios CRM              Data 20/10/2015  
Ĵ
          Rotina para incluso automatica via WS                       
Ĵ
ParametrosExpC1: Codigo da entidade+loja entidade                      
          ExpC2: Vetor com os contatos para ser vinculado              
Ĵ
Retorno   ExpL1: nil                                                   
Ĵ
Descrio Esta funo ira validar e gravar o vinculo automaticamente   
Ĵ
Uso        WEB SERVICES                                                
ٱ
/*/
Static Function GravaAC8(cCodEntidade, aCodContato)
Local oModel
Local aOrd := SaveOrd("AC8")
Local i 
AC8->(dbSetOrder(1))//AC8_FILIAL+AC8_CODCON+AC8_ENTIDA+AC8_FILENT+AC8_CODENT

If !Empty(cCodEntidade).and. Len(aCodContato)>0 
	For i:= 1 To Len(aCodContato)
		If !AC8->( dbSeek(xFilial("AC8")+aCodContato[i]:CONTACTID +"SUS"+xFilial("SUS")+cCodEntidade) )
			oModel := FWLoadModel("CRMA060")
			oModel:SetOperation(3)
			oModel:Activate() 	
			oModel:GetModel("AC8MASTER")
			oModel:SetValue("AC8MASTER","AC8_FILIAL",xFilial("AC8"))
			//oModel:SetValue("AC8MASTER","AC8_FILENT",xFilial("SUS"))
			oModel:SetValue("AC8MASTER","AC8_ENTIDA","SUS")
			oModel:SetValue("AC8MASTER","AC8_CODENT",cCodEntidade)		
			oModel:SetValue("AC8CONTDET","AC8_CODCON",aCodContato[i]:CONTACTID)
			
			If oModel:VldData()
				oModel:CommitData()
			Else
				VarInfo("",oModel:GetErrorMessage())
			EndIf
		
			oModel:DeActivate()
		End If
	Next 
End If 
RestOrd(aOrd,.T.)
Return
/*/

Ŀ
Funo    DelOpport Autor   Eduardo Riera          Data 21.11.2003 
Ĵ
          Rotina de exclusao da oportunidade de venda                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do Usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpC3: Codigo da Oportunidade de Venda                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o codigo da oportunidade atualizado      
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD DelOpportunity WSRECEIVE UserCode,SellerCode,OpportunityId WSSEND WsNull WSSERVICE MtSellerOpportunity

Local aArea      := GetArea()
Local aCab       := {}
Local aErro      := {}
Local cErro      := ""
Local lRetorno   := .T.
Local nOperacao  := 0
Local nX         := 0

PRIVATE lMsErroAuto := .F.
//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"MTSELLEROPPORTUNITY","DELOPPORTUNITY","SA3",::SellerCode)

	//Ŀ
	//Montagem dos dados                                                      
	//
	dbSelectArea("AD1")
	dbSetOrder(1)
	If MsSeek(xFilial("AD1")+OpportunityID)
		//Ŀ
		//Verificacao o vendedor                                                  
		//
		If nOperacao == 4
			If 	AD1->AD1_VEND <> ::SellerCode
				lRetorno := .F.
				SetSoapFault("DELOPPORTUNITY",STR0010) //"Vendedor invalido para esta oportunidade de venda"
			EndIf
		EndIf
		aadd(aCab,{"AD1_NROPOR",OpportunityID,Nil})
		//Ŀ
		//Atualizacao da oportunidade de venda                                    
		//
		Fata300(5,aCab)
		If lMsErroAuto
			aErro := GetAutoGRLog()
			For nX := 1 To Len(aErro)
				cErro += aErro[nX] + Chr(13)+Chr(10)
			Next nX
			SetSoapFault("DELOPPORTUNITY",cErro)
			lRetorno := .F.
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("DELOPPORTUNITY",STR0011)	 //"Oportunidade nao encontrada"
	EndIf		
Else
	lRetorno := .F.
	SetSoapFault("DELOPPORTUNITY",STR0008) //"Usuario invalido"
EndIf
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    GetOport  Autor   Eduardo Riera          Data 20.11.2003 
Ĵ
          Rotina de recuperacao do dados da Oportunidade de Venda      
                                                                       
Ĵ
ParametrosExpO1: Objeto da Oportunidade                                
          ExpC2: Alias do AD1                                          
          ExpN3: Tipo de Struturuca                                    
                 [1] Para OpportunityStruct                            
                 [2] Para OpportunityView                              
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o objeto da oportunidade preenchido      
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
Ĵ
Analista   Data/Bops/Ver Manutencao Efetuada                          
Ĵ
Andrea F. 13/03/0694291 - Passar como referencia o objeto na funcao  
                        UserFields, para ser possivel atualiza-lo    
                        com os campos customizados.                  
Cleber M. 31/03/0695206 -Inicializacao do oObjeto:Competitor como    
                        array.                                       
ٱ


/*/
Static Function GetOport(oObjeto,cAliasAD1,nTipo)

Local nX := 0
DEFAULT cAliasAD1 := "AD1"
DEFAULT nTipo     := 1

If nTipo == 1
	//Ŀ
	//Carrega cabecalho da oportunidade de venda                              
	//
	oObjeto:OpportunityID          := (cAliasAD1)->AD1_NROPOR
	oObjeto:Description            := (cAliasAD1)->AD1_DESCRI
	oObjeto:IssueDate              := (cAliasAD1)->AD1_DATA
	oObjeto:SellerCode             := (cAliasAD1)->AD1_VEND
	oObjeto:StartDate              := (cAliasAD1)->AD1_DTINI
	oObjeto:GoalDate               := (cAliasAD1)->AD1_DTFIM
	oObjeto:ProspectCode           := (cAliasAD1)->AD1_PROSPE
	oObjeto:UnitProspectCode       := (cAliasAD1)->AD1_LOJPRO
	oObjeto:SalesProcess           := (cAliasAD1)->AD1_PROVEN
	oObjeto:SalesStage             := (cAliasAD1)->AD1_STAGE
	oObjeto:Accomplishment         := Ft300Perc( (cAliasAD1)->AD1_PROVEN, (cAliasAD1)->AD1_STAGE )
	oObjeto:ProjectAllocatedAmount := (cAliasAD1)->AD1_VERBA
	oObjeto:Currency               := (cAliasAD1)->AD1_MOEDA
	oObjeto:ProductCode            := (cAliasAD1)->AD1_CODPRO
	oObjeto:CriticalSuccessFactor   := (cAliasAD1)->AD1_FCS
	oObjeto:CriticalFailureFactor  := (cAliasAD1)->AD1_FCI
	oObjeto:OpportunityStatus	   := (cAliasAD1)->AD1_STATUS
	oObjeto:Priority               := (cAliasAD1)->AD1_PRIOR
	oObjeto:BudgetID               := (cAliasAD1)->AD1_NUMORC
	oObjeto:Notes                  := MsMM((cAliasAD1)->AD1_CODMEM)
	UserFields("AD1",@oObjeto:UserFields,cAliasAD1)
Else
	oObjeto:Header := WsClassNew( "OpportunityStruct" )

	//Ŀ
	//Carrega cabecalho da oportunidade de venda                              
	//
	oObjeto:Header:OpportunityID          := (cAliasAD1)->AD1_NROPOR
	oObjeto:Header:Description            := (cAliasAD1)->AD1_DESCRI
	oObjeto:Header:IssueDate              := (cAliasAD1)->AD1_DATA
	oObjeto:Header:SellerCode             := (cAliasAD1)->AD1_VEND
	oObjeto:Header:StartDate              := (cAliasAD1)->AD1_DTINI
	oObjeto:Header:GoalDate               := (cAliasAD1)->AD1_DTFIM
	oObjeto:Header:ProspectCode           := (cAliasAD1)->AD1_PROSPE
	oObjeto:Header:UnitProspectCode       := (cAliasAD1)->AD1_LOJPRO
	oObjeto:Header:SalesProcess           := (cAliasAD1)->AD1_PROVEN
	oObjeto:Header:SalesStage             := (cAliasAD1)->AD1_STAGE
	oObjeto:Header:Accomplishment         := Ft300Perc( (cAliasAD1)->AD1_PROVEN, AD1_STAGE )
	oObjeto:Header:ProjectAllocatedAmount := (cAliasAD1)->AD1_VERBA
	oObjeto:Header:Currency               := (cAliasAD1)->AD1_MOEDA
	oObjeto:Header:ProductCode            := (cAliasAD1)->AD1_CODPRO
	oObjeto:Header:CriticalSuccessFactor   := (cAliasAD1)->AD1_FCS
	oObjeto:Header:CriticalFailureFactor  := (cAliasAD1)->AD1_FCI
	oObjeto:Header:OpportunityStatus	  := (cAliasAD1)->AD1_STATUS
	oObjeto:Header:Priority               := (cAliasAD1)->AD1_PRIOR
	oObjeto:Header:BudgetID               := (cAliasAD1)->AD1_NUMORC
	oObjeto:Header:Notes                  := MsMM((cAliasAD1)->AD1_CODMEM)
	UserFields("AD1",@oObjeto:Header:UserFields,cAliasAD1)
	//Ŀ
	//Carrega time de vendas                                                  
	//
	nX := 0
	dbSelectArea("AD2")	
	dbSetOrder(1)
	MsSeek(xFilial("AD2")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
	oObjeto:SalesTeam := {}
	While !Eof() .And. xFilial("AD2") == AD2->AD2_FILIAL .And.;
			(cAliasAD1)->AD1_NROPOR == AD2->AD2_NROPOR .And.;
			(cAliasAD1)->AD1_REVISA == AD2->AD2_REVISA

		aadd(oObjeto:SalesTeam,WsClassNew("GenericView"))
		nX++

		oObjeto:SalesTeam[nX]:Code        := AD2->AD2_VEND
		oObjeto:SalesTeam[nX]:Description := Posicione("SA3",1,xFilial("SA3")+AD2->AD2_VEND,"A3_NOME")
		UserFields("AD2",@oObjeto:SalesTeam[nX]:UserFields)

		dbSelectArea("AD2")			
		dbSkip()		
	EndDo
	//Ŀ
	//Carrega concorrentes                                                    
	//
	nX := 0
	dbSelectArea("AD3")	
	dbSetOrder(1)
	MsSeek(xFilial("AD3")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
	oObjeto:Competitor := {}
	While !Eof() .And. xFilial("AD3") == AD3->AD3_FILIAL .And.;
			(cAliasAD1)->AD1_NROPOR == AD3->AD3_NROPOR .And.;
			(cAliasAD1)->AD1_REVISA == AD3->AD3_REVISA

		aadd(oObjeto:Competitor,WsClassNew("CompetitorView"))
		nX++

		oObjeto:Competitor[nX]:CompetitorCode        := AD3->AD3_CODCON
		oObjeto:Competitor[nX]:Price                 := AD3->AD3_PRECO
		oObjeto:Competitor[nX]:Currency              := AD3->AD3_MOEDA
		oObjeto:Competitor[nX]:CriticalSuccessFactor  := AD3->AD3_FCS
		oObjeto:Competitor[nX]:CriticalFailureFactor := AD3->AD3_FCI
		UserFields("AD3",@oObjeto:Competitor[nX]:UserFields)

		dbSelectArea("AD3")			
		dbSkip()		
	EndDo
	//Ŀ
	//Carrega parceiros                                                       
	//
	nX := 0
	dbSelectArea("AD4")
	dbSetOrder(1)
	MsSeek(xFilial("AD4")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
	oObjeto:Partners := {}
	While !Eof() .And. xFilial("AD4") == AD4->AD4_FILIAL .And.;
			(cAliasAD1)->AD1_NROPOR == AD4->AD4_NROPOR .And.;
			(cAliasAD1)->AD1_REVISA == AD4->AD4_REVISA

		aadd(oObjeto:Partners,WsClassNew("GenericView"))
		nX++

		oObjeto:Partners[nX]:Code        := AD4->AD4_PARTNE
		oObjeto:Partners[nX]:Description := Posicione("AC4",1,xFilial("AC4")+AD4->AD4_PARTNE,"AC4_NOME")
		UserFields("AD4",@oObjeto:Partners[nX]:UserFields)

		dbSelectArea("AD4")
		dbSkip()
	EndDo
	//Ŀ
	//Carrega contatos                                                        
	//
	nX := 0
	dbSelectArea("AD9")
	dbSetOrder(1)
	MsSeek(xFilial("AD9")+(cAliasAD1)->AD1_NROPOR+(cAliasAD1)->AD1_REVISA)
	oObjeto:Contacts := {}
	While !Eof() .And. xFilial("AD9") == AD9->AD9_FILIAL .And.;
			(cAliasAD1)->AD1_NROPOR == AD9->AD9_NROPOR .And.;
			(cAliasAD1)->AD1_REVISA == AD9->AD9_REVISA

		aadd(oObjeto:Contacts,WsClassNew("ContactView"))
		nX++

		oObjeto:Contacts[nX]:ContactID        := AD9->AD9_CODCON
		oObjeto:Contacts[nX]:Name := Posicione("SU5",1,xFilial("SU5")+AD9->AD9_CODCON,"U5_CONTAT")
		UserFields("AD9",@oObjeto:Contacts[nX]:UserFields)

		dbSelectArea("AD9")
		dbSkip()
	EndDo		
EndIf

Return(.T.)
