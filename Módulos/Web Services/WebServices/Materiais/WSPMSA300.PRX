#INCLUDE "WSPMSA300.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "APWEBSRV.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSPMSA300 ³ Autor ³Luiz Couto             ³ Data ³04.10.2004  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service responsavel pelo apontamento de Recursos         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao da Estrutura do WebService                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSTRUCT ProjectAnnotationResourcesView
	WSDATA ProjectCode							AS String
	WSDATA ProjectVersion						AS String
	WSDATA ProjectTask							AS String
	WSDATA ResourceCode							AS String
	WSDATA AnnotationDate						AS Date
	WSDATA ExecutedInitialHour					AS String
	WSDATA ExecutedFinalHour					AS String
	WSDATA NumberOfHours						AS Float OPTIONAL
	WSDATA ProductCode							AS String OPTIONAL
	WSDATA AnnotationCost1						AS Float OPTIONAL
	WSDATA AnnotationCost2						AS Float OPTIONAL
	WSDATA AnnotationCost3						AS Float OPTIONAL
	WSDATA AnnotationCost4						AS Float OPTIONAL
	WSDATA AnnotationCost5						AS Float OPTIONAL
	WSDATA CostCalculationType					AS String OPTIONAL
	WSDATA DocumentCode							AS String OPTIONAL
	WSDATA DocumentItem							AS String OPTIONAL
	WSDATA UserFields							AS Array Of UserField OPTIONAL
ENDWSSTRUCT

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE MtProjectAnnotationResources DESCRIPTION STR0001 NAMESPACE "http://webservice.microsiga.com.br/mtprojectannotationresources.apw" //"Serviço de consulta e atualização dos apontamentos dos Recursos do Projeto"
	WSDATA UserCode								AS String
	WSDATA Header                   			AS Array Of BrwHeader
	WSDATA HeaderType               			As String
	WSDATA ProjectCode							AS String
	WSDATA ProjectVersion						AS String
	WSDATA TaskCode								AS String
	WSDATA QueryAddWhere            			AS String OPTIONAL
	WSDATA IndexKey                 			AS String OPTIONAL
	WSDATA ListOfProjectAnnotationResources		AS Array Of ProjectAnnotationResourcesView
	WSDATA ProjectAnnotationResources			AS ProjectAnnotationResourcesView
	              
	WSMETHOD GetHeader						DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do Serviço"
	WSMETHOD BrwProjectAnnotationResources	DESCRIPTION STR0003 //"Método de listagem dos apontamentos dos Recursos do Projeto"
	WSMETHOD GetProjectAnnotationResources	DESCRIPTION STR0004 //"Método de consulta as informações do apontamento dos Recursos do Projeto"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Luiz Couto            ³ Data ³05.10.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao do header                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da Estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve o header de uma estrutura                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE MtProjectAnnotationResources

Local lRetorno := .T.

::Header := MtHeader( ::HeaderType )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwProject³Autor  ³ Luiz Couto            ³ Data ³05.10.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de listagem dos apontamentos de recursos do projeto   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo de Usuario                                     ³±±
±±³          ³ExpC2: Cod. Projeto                                          ³±±
±±³          ³ExpC3: Versao do Projeto                                     ³±±
±±³          ³ExpC4: Cod. Tarefa                                           ³±±
±±³          ³ExpC5: QueryAdd                                              ³±±
±±³          ³ExpC6: Indice                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo devolve a listagem dos apontamentos de recursos  ³±±
±±³          ³do projeto                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwProjectAnnotationResources WSRECEIVE UserCode, ProjectCode, ProjectVersion, QueryAddWhere, IndexKey WSSEND ListOfProjectAnnotationResources WSSERVICE MtProjectAnnotationResources

Local lRetorno 		:= .T.
Local aArea 		:= GetArea()
Local cAliasAFU 	:= ""
Local nX 			:= 0
#IFDEF TOP
Local aStruAFU  	:= {}
#ENDIF

DEFAULT ::IndexKey := AFU->( IndexKey() )

If PrtChkUser( ::UserCode, "MTPROJECTANNOTATIONRESOURCES", "BRWPROJECTANNOTATIONRESOURCES" )
	#IFDEF TOP
		cAliasAFU 	:= GetNextAlias()
		aStruAFU 	:= AFU->( DbStruct() )
		cQuery := "SELECT * FROM " + RetSQLName( "AFU" ) + " AFU "
		cQuery += "WHERE "
		cQuery += "AFU.AFU_FILIAL = '" + xFilial( "AFU" ) + "' AND "
		cQuery += "AFU.AFU_CTRRVS = '1' AND "
		cQuery += "AFU.AFU_PROJET = '" + ::ProjectCode + "' AND "
		cQuery += "AFU.AFU_REVISA = '" + ::ProjectVersion + "' AND "
		cQuery += "AFU.D_E_L_E_T_ = ' ' " 
		cQuery := WsQueryAdd( cQuery, ::QueryAddWhere )
		cQuery += "ORDER BY " + SqlOrder( AFU->( IndexKey() ) )

		cQuery	:= ChangeQuery( cQuery )

		dbUseArea( .T., "TOPCONN", TcGenQry( , , cQuery ), cAliasAFU, .T., .T. )

		For nX := 1 To Len( aStruAFU )
			If aStruAFU[nX][2] <> "C" .AND. aStruAFU[nX][2] <> "M"
				TcSetField( cAliasAFU, aStruAFU[nX][1], aStruAFU[nX][2], aStruAFU[nX][3], aStruAFU[nX][4] )
			EndIf
		Next nX
	#ELSE
	#ENDIF
	
	nX := 0
	While !( cAliasAFU )->( EOF() ) .AND. xFilial( "AFU" ) == ( cAliasAFU )->AFU_FILIAL
		nX++
		aAdd( ::ListOfProjectAnnotationResources, WsClassNew( "ProjectAnnotationResourcesView" ) )
		GetPrjAnnRes( @::ListOfProjectAnnotationResources[nX], cAliasAFU )
	
		( cAliasAFU )->( DbSkip() )
	EndDo
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetProject³Autor  ³ Luiz Couto            ³ Data ³05.10.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de recuperacao da anotacao dos recursos da tarefa     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo de Usuario                                     ³±±
±±³          ³ExpC2: Cod. Projeto                                          ³±±
±±³          ³ExpC3: Versao do Projeto                                     ³±±
±±³          ³ExpC4: Cod. Tarefa                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo recupera a anotacao dos recursos da tarefa       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetProjectAnnotationResources WSRECEIVE UserCode, ProjectCode, ProjectVersion, TaskCode WSSEND ProjectAnnotationResources WSSERVICE MtProjectAnnotationResources

Local lRetorno 		:= .T.
Local aArea 		:= GetArea()
Local cAliasAFU 	:= "AFU"

If PrtChkUser( ::UserCode, "MTPROJECTANNOTATIONRESOURCES", "GETPROJECTANNOTATIONRESOURCES" )
	( cAliasAFU )->( DbSetOrder( 1 ) )
	
	If ( cAliasAFU )->( MsSeek( xFilial( "AFU" ) + "1" + PadR( ::ProjectCode, 10 ) + ;
		PadR( ::ProjectVersion, 4 ) + PadR( ::TaskCode, 10 ) ) )

		::ProjectAnnotationResources := WsClassNew( "ProjectAnnotationResourcesView" )
		GetPrjAnnRes( @::ProjectAnnotationResources, cAliasAFU )
	Else
		lRetorno := .F.
		SetSoapFault( "GETPROJECTANNOTATIONRESOURCES", "Apontamento de Recurso não encontrado." )
	EndIf
Else
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPrjAnnR|Autor  ³ Luiz Couto            ³ Data ³05.10.2004 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de preenchimento da estrutura do apontamento          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto                                                ³±±
±±³          ³ExpC2: Alias                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o metodo foi avaliado com sucesso          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este metodo preenche a estrutura do apontamento              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetPrjAnnRes( oObjeto, cAliasAFU )

Local lRetorno 	:= .T.
Local aArea 	:= GetArea()

oObjeto:ProjectCode 			:= ( cAliasAFU )->AFU_PROJET
oObjeto:ProjectVersion 			:= ( cAliasAFU )->AFU_REVISA
oObjeto:ProjectTask 			:= ( cAliasAFU )->AFU_TAREFA
oObjeto:ResourceCode		 	:= ( cAliasAFU )->AFU_RECURS
oObjeto:AnnotationDate 			:= ( cAliasAFU )->AFU_DATA  
oObjeto:ExecutedInitialHour 	:= ( cAliasAFU )->AFU_HORAI 
oObjeto:ExecutedFinalHour 		:= ( cAliasAFU )->AFU_HORAF 
oObjeto:NumberOfHours 			:= ( cAliasAFU )->AFU_HQUANT
oObjeto:ProductCode 			:= ( cAliasAFU )->AFU_COD     
oObjeto:AnnotationCost1 		:= ( cAliasAFU )->AFU_CUSTO1
oObjeto:AnnotationCost2 		:= ( cAliasAFU )->AFU_CUSTO2
oObjeto:AnnotationCost3 		:= ( cAliasAFU )->AFU_CUSTO3
oObjeto:AnnotationCost4 		:= ( cAliasAFU )->AFU_CUSTO4
oObjeto:AnnotationCost5 		:= ( cAliasAFU )->AFU_CUSTO5
oObjeto:CostCalculationType 	:= ( cAliasAFU )->AFU_TPREAL
oObjeto:DocumentCode 			:= ( cAliasAFU )->AFU_DOCUME
oObjeto:DocumentItem 			:= ( cAliasAFU )->AFU_ITEM
UserFields( "AFU", @oObjeto:UserFields, cAliasAFU )

RestArea( aArea )

Return lRetorno