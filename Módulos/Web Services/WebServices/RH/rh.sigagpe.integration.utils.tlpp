#Include "tlpp-core.th"
#Include "tlpp-rest.th"
#Include "totvs.ch"

class GPEUtils
	@Get("api/rh/v1/utils/filial")
	public method getListaFilial() As Variant

	@Get("api/rh/v1/utils/filial/:CFILIAL")
	public method getFilial() As Variant

	@Get("api/rh/v1/utils/funcionario/:CRAFILIAL")
	public method getListaFuncionario() As Variant

	@Get("api/rh/v1/utils/funcionario/:CRAFILIAL/:CRAMAT")
	public method getFuncionario() As Variant

	@Get("api/rh/v1/utils/processo/:CRCJFILIAL")
	public method getListaProcesso() As Variant

	@Get("api/rh/v1/utils/processo/:CRCJFILIAL/:CRCJCODIGO")
	public method getProcesso() As Variant

	@Get("api/rh/v1/utils/centro-custo/:CCTTFILIAL")
	public method getListaCentroCusto() As Variant

	@Get("api/rh/v1/utils/centro-custo/:CCTTFILIAL/:CCTTCUSTO")
	public method getCentroCusto() As Variant

	@Get("api/rh/v1/utils/periodo")
	public method getListaPeriodos() As Variant

	@Get("api/rh/v1/utils/sx5")
	public method getSX5() As Variant

	@Get("api/rh/v1/utils/roteiro")
    public method getListaRoteiro() As Variant

	public method new() constructor
endclass

/*/{Protheus.doc} new
	Inicializador da classe.
	@type Method
	@version 12.1.2410
	@author arthur.sales
	@since 15/04/2025
	@return Variant, Instancia da classe
/*/
method new() class GPEUtils
return self

/*/{Protheus.doc} getListaFilial
	Retorna uma lista de filiais.
	@type Method
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getListaFilial() As Variant class GPEUtils
	// Declaracao e inicializacao das variaveis locais
	Local aSM0			:= {}						as Array
	Local nSM0			:= 1						as Numeric
	Local cSearch		:= ""						as Character // Termo de busca
	Local jQueryParams	:= oRest:getQueryRequest()	as JSON
	Local nErroCod		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character

	// Declaracao e inicializacao das variaveis privadas
	Private oResponse := JsonObject():New()	as Json

	cSearch := IIf(jQueryParams:hasProperty("filter"), jQueryParams["filter"], "")

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		aSM0 := FwLoadSM0()

		oResponse["items"] := {}

		// Percorre as filiais adicionando cada item no JSON de resposta
		For nSM0 := 1 To Len(aSM0)
			// Pula as filiais de empresa diferente da empresa logada
			If (aSM0[nSM0][SM0_GRPEMP] != cEmpAnt)
				LOOP
			EndIf

			// Se o termo de busca estiver preenchido, filtra as filiais com base nele
			If (!Empty(cSearch) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_CODFIL])) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_NOMRED])))
				LOOP
			EndIf

			oItem := JSonObject():new()
			oItem["CSM0CODFIL"]	:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_CODFIL]))
			oItem["CSM0NOMRED"]	:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_NOMRED]))

			AAdd(oResponse["items"], oItem)
		Next nSM0

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getFilial
	Retorna uma filiai conforme o parametro.
	@type Method
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getFilial() As Variant class GPEUtils
	// Declaracao e inicializacao das variaveis locais
	Local aSM0			:= {}							as Array
	Local nSM0			:= 1							as Numeric
	Local jPathParams	:= oRest:getPathParamsRequest()	as JSON
	Local nErroCod		:= 0 							as Numeric
	Local cMsg			:= ""							as Character
	Local cMsgDesc		:= ""							as Character
	Local cMsgTipo		:= ""							as Character
	Local jQueryParams	:= oRest:getQueryRequest()		as JSON

	// Declaracao e inicializacao das variaveis privadas
	Private oResponse := JsonObject():New()	as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		aSM0 := FwLoadSM0()

		// Percorre as filiais adicionando cada item no JSON de resposta
		For nSM0 := 1 To Len(aSM0)
			// Pula as filiais de empresa diferente da empresa logada
			If (aSM0[nSM0][SM0_GRPEMP] != cEmpAnt)
				LOOP
			EndIf

			If Upper(aSM0[nSM0][SM0_CODFIL]) != jPathParams["CFILIAL"]
				LOOP
			EndIf

			oResponse["CSM0CODFIL"]	:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_CODFIL]))
			oResponse["CSM0NOMRED"]	:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_NOMRED]))

			Exit

		Next nSM0

		If jQueryParams:hasProperty("forcaMsg")
			cMsg 		:= "Success"
			cMsgDesc 	:= "Success"
			cMsgTipo	:= "success"
		Endif

	RECOVER

		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getListaFuncionario
	Retorna uma lista os funcionarios
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getListaFuncionario() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object
	Local oItem			:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		oResponse["items"] := {}
		
		cQuery := " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_CIC, SRA.RA_ADMISSA, SRA.RA_CATFUNC, SRA.RA_CC "
		cQuery += " , SRA.RA_PROCES, SRA.RA_SITFOLH "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " WHERE SRA.RA_FILIAL = ? "

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
		cQuery += "   AND (SRA.RA_NOME LIKE '%?%' OR SRA.RA_MAT = ? OR SRA.RA_CIC = ?)"
		Endif

		cQuery += "   AND SRA.D_E_L_E_T_ 	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, jPathParams['CRAFILIAL'])

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
			oStatement:SetUnsafe(nParamOrder++, jQueryParams['filter'])
			oStatement:SetString(nParamOrder++, jQueryParams['filter'])
			oStatement:SetString(nParamOrder++, jQueryParams['filter'])
		Endif

		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())

			oItem := JSonObject():new()
			oItem["CRAFILIAL"] 	:= RTrim((cAlias)->RA_FILIAL)
			oItem["CRAMAT"] 	:= RTrim((cAlias)->RA_MAT)
			oItem["CRANOME"] 	:= RTrim((cAlias)->RA_NOME)
			oItem["CRACIC"] 	:= RTrim((cAlias)->RA_CIC)
			oItem["CRASITFOLH"] := RTrim((cAlias)->RA_SITFOLH)
			oItem["CRACATFUNC"]	:= RTrim((cAlias)->RA_CATFUNC)
			oItem["CRACC"] 		:= RTrim((cAlias)->RA_CC)
			oItem["CRAPROCES"] 	:= RTrim((cAlias)->RA_PROCES)
			oItem["DRAADMISSA"] := fDateToJson(StoD((cAlias)->RA_ADMISSA))

			aadd(oResponse["items"], oItem)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getFuncionario
	Retorna um funcionario
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getFuncionario() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		cQuery := " SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " WHERE SRA.RA_FILIAL 	= ? "
		cQuery += "   AND SRA.RA_MAT 		= ? "
		cQuery += "   AND SRA.D_E_L_E_T_	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FWxFilial("SRA",jPathParams['CRAFILIAL']))
		oStatement:SetString(nParamOrder++, jPathParams['CRAMAT'])
		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oResponse["CRAFILIAL"] 	:= RTrim((cAlias)->RA_FILIAL)
			oResponse["CRAMAT"] 	:= RTrim((cAlias)->RA_MAT)
			oResponse["CRANOME"] 	:= RTrim((cAlias)->RA_NOME)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getListaProcesso
	Retorna uma lista com os processos
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getListaProcesso() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object
	Local oItem			:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		oResponse["items"] := {}
		
		cQuery := " SELECT RCJ.RCJ_FILIAL, RCJ.RCJ_CODIGO, RCJ.RCJ_DESCRI "
		cQuery += " FROM "+RetSqlName("RCJ")+" RCJ "
		cQuery += " WHERE RCJ.RCJ_FILIAL = ? "

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
		cQuery += "   AND (RCJ.RCJ_DESCRI LIKE '%?%' OR RCJ.RCJ_CODIGO = ?)"
		Endif

		cQuery += "   AND RCJ.D_E_L_E_T_ 	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FWxFilial("RCJ",jPathParams['CRCJFILIAL']))

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
			oStatement:SetUnsafe(nParamOrder++, jQueryParams['filter'])
			oStatement:SetString(nParamOrder++, jQueryParams['filter'])
		Endif

		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oItem := JSonObject():new()
			oItem["CRCJFILIAL"] 	:= RTrim((cAlias)->RCJ_FILIAL)
			oItem["CRCJCODIGO"] 	:= RTrim((cAlias)->RCJ_CODIGO)
			oItem["CRCJDESCRI"] 	:= RTrim((cAlias)->RCJ_DESCRI)

			aadd(oResponse["items"], oItem)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getProcesso
	Retorna um processo
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getProcesso() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		cQuery := " SELECT RCJ.RCJ_FILIAL, RCJ.RCJ_CODIGO, RCJ.RCJ_DESCRI "
		cQuery += " FROM "+RetSqlName("RCJ")+" RCJ "
		cQuery += " WHERE RCJ.RCJ_FILIAL 	= ? "
		cQuery += "   AND RCJ.RCJ_CODIGO	= ? "
		cQuery += "   AND RCJ.D_E_L_E_T_	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FWxFilial("RCJ",jPathParams['CRCJFILIAL']))
		oStatement:SetString(nParamOrder++, jPathParams['CRCJCODIGO'])
		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oResponse["CRCJFILIAL"] 	:= RTrim((cAlias)->RCJ_FILIAL)
			oResponse["CRCJCODIGO"] 	:= RTrim((cAlias)->RCJ_CODIGO)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getListaCentroCusto
	Retorna uma lista com o centro de custo
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getListaCentroCusto() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object
	Local oItem			:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		oResponse["items"] := {}
		
		cQuery := " SELECT CTT.CTT_FILIAL, CTT.CTT_CUSTO, CTT.CTT_DESC01 "
		cQuery += " FROM "+RetSqlName("CTT")+" CTT "
		cQuery += " WHERE CTT.CTT_FILIAL = ? "

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
		cQuery += "   AND (CTT.CTT_DESC01 LIKE '%?%' OR CTT.CTT_CUSTO = ?)"
		Endif

		cQuery += "   AND CTT.D_E_L_E_T_ 	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FWxFilial("CTT",jPathParams['CCTTFILIAL']))

		If jQueryParams:hasProperty("filter") .AND. !Empty(jQueryParams['filter'])
			oStatement:SetUnsafe(nParamOrder++, jQueryParams['filter'])
			oStatement:SetString(nParamOrder++, jQueryParams['filter'])
		Endif

		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oItem := JSonObject():new()
			oItem["CCTTFILIAL"] := RTrim((cAlias)->CTT_FILIAL)
			oItem["CCTTCUSTO"] 	:= RTrim((cAlias)->CTT_CUSTO)
			oItem["CCTTDESC01"] := RTrim((cAlias)->CTT_DESC01)

			aadd(oResponse["items"], oItem)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getCentroCusto
	Retorna um centro de custo
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getCentroCusto() class GPEUtils
	Local jPathParams 	:= oRest:getPathParamsRequest()	as Json
	Local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		cQuery := " SELECT CTT.CTT_FILIAL, CTT.CTT_CUSTO, CTT.CTT_DESC01 "
		cQuery += " FROM "+RetSqlName("CTT")+" CTT "
		cQuery += " WHERE CTT.CTT_FILIAL 	= ? "
		cQuery += "   AND CTT.CTT_CUSTO		= ? "
		cQuery += "   AND CTT.D_E_L_E_T_	= ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FWxFilial("CTT",jPathParams['CCTTFILIAL']))
		oStatement:SetString(nParamOrder++, jPathParams['CCTTCUSTO'])
		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oResponse["CRCJFILIAL"] 	:= RTrim((cAlias)->CTT_FILIAL)
			oResponse["CCTTCUSTO"] 		:= RTrim((cAlias)->CTT_CUSTO)
			oResponse["CCTTDESC01"] 	:= RTrim((cAlias)->CTT_DESC01)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getListaPeriodos
	Retorna uma lista com os dados dos periodos cadastrados no processo do funcionario.
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getListaPeriodos() As Variant class GPEUtils
	Local jQueryParams	:= oRest:getQueryRequest()	as JSON
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object
	Local oItem			:= NIL						as Object

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		oResponse["items"] := {}

		cQuery := " SELECT RCH.RCH_PER, RCH.RCH_NUMPAG, RCH.RCH_ROTEIR, RCH.RCH_DTPAGO "
		cQuery += " FROM "+RetSqlName("RCH")+" RCH "
		cQuery += " WHERE"
		cQuery += "   RCH.RCH_FILIAL = ? "

		If (jQueryParams:hasProperty('CRAPROCES'))
			cQuery += "   AND RCH.RCH_PROCES = ? "
		EndIf

		If (jQueryParams:hasProperty('CRAROTEIR'))
			cQuery += "   AND RCH.RCH_ROTEIR = ? "
		EndIf

		cQuery += "   AND RCH.D_E_L_E_T_ = ? "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetString(nParamOrder++, FwXFilial("RCH", IIf(jQueryParams:hasProperty('CRAFILIAL'), jQueryParams['CRAFILIAL'], cFilAnt)))


		If (jQueryParams:hasProperty('CRAPROCES'))
			oStatement:SetString(nParamOrder++, jQueryParams['CRAPROCES'])
		EndIf

		If (jQueryParams:hasProperty('CRAROTEIR'))
			oStatement:SetString(nParamOrder++, jQueryParams['CRAROTEIR'])
		EndIf

		oStatement:SetString(nParamOrder++, " ")

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oItem := JSonObject():new()
			oItem["CRCHPER"] 	:= RTrim((cAlias)->RCH_PER)
			oItem["CRCHNUMPAG"] := RTrim((cAlias)->RCH_NUMPAG)
			oItem["CRCHROTEIR"] := RTrim((cAlias)->RCH_ROTEIR)
			oItem["CRCHDTPAGO"] := RTrim((cAlias)->RCH_DTPAGO)

			aadd(oResponse["items"], oItem)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return


/*/{Protheus.doc} getSX5
	Retorna uma lista com os dados da SX5 conforme tabela passada como queryparam
	@type Method
	@version 12.1.2410
	@author Caio Kretzer
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
/*/
method getSX5() As Variant class GPEUtils
	Local jQueryParams	:= oRest:getQueryRequest()	as JSON
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	Local nParamOrder	:= 1						as Numeric
	Local cAlias		:= ""						as Character
	Local cQuery		:= ""						as Character
	Local oStatement	:= NIL						as Object
	Local oItem			:= NIL						as Object
	Local cFieldSX5    	:= FDescSX5(2)				as Character
	Local cSearch		:= "" 						as Character
	Local cTable		:= ""						as Character

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	BEGIN SEQUENCE
		If jQueryParams:hasProperty("forcaErro")
			BREAK
		Endif

		If (jQueryParams:hasProperty("filter"))
            cSearch := " AND (UPPER(" + cFieldSX5 + ") LIKE UPPER(?) OR " // 2
            cSearch += " UPPER(X5_CHAVE) LIKE UPPER(?)) "                 // 3
        EndIf

		cTable	:= jQueryParams["CX5TABELA"]

		oResponse["items"] := {}

		cQuery += "SELECT X5_CHAVE, "
		cQuery +=     cFieldSX5 + " AS X5_DESCRI "
		cQuery += "FROM "+ RetSQLName("SX5") + " "
		cQuery += "WHERE "
		cQuery +=     "X5_TABELA = ? " // 1
		cQuery +=     cSearch
		cQuery +=     "AND D_E_L_E_T_ = ? " // 4
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FWExecStatement():New(cQuery)

		// Define o valor dos bind parameters
		oStatement:SetString(nParamOrder++, cTable) // 1

		If (jQueryParams:hasProperty("filter"))
			oStatement:SetString(nParamOrder++, "%" + jQueryParams["filter"] + "%") // 2
			oStatement:SetString(nParamOrder++, "%" + jQueryParams["filter"] + "%") // 3
		EndIf

		oStatement:SetString(nParamOrder++, " ") // 4

		// Executa a query e retorna o alias criado
		cAlias := oStatement:OpenAlias()

		While !(cAlias)->(Eof())
			oItem := JSonObject():new()
			oItem["CX5CHAVE"] 	:= RTrim((cAlias)->X5_CHAVE)
			oItem["CX5DESCRI"] 	:= RTrim(Upper((cAlias)->X5_DESCRI))

			aadd(oResponse["items"], oItem)

			(cAlias)->(DbSkip())
		End

		(cAlias)->(DbCloseArea())

	RECOVER
		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())
	END SEQUENCE

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return

/*/{Protheus.doc} fSetResponse
	Seta a resposta para o PO-UI.
	@type Method
	@version 12.1.2410
	@author arthur.sales
	@since 15/04/2025
	@return Variant, Retorno nulo pre-fixado
	@obs Foi necessaria a criacao para passar no issue x cobertura de todas as APIs.
/*/
Static Function fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
	if nErroCod > 0
		oRest:setStatusCode(nErroCod)
		oRest:setFault(fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo))
	else
		oRest:setResponse(fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo))
	endif
Return

/*/{Protheus.doc} fGetJsonErro
	Retorna o json de erro para o POUI.
	@type Method
	@version 12.1.2410
	@author arthur.sales
	@since 15/04/2025
	@param nErroCod, Numeric, codigo do erro
	@param cMsg, Character, Mensagem do erro
	@param cMsgDesc, Character, Descricao detalhada do erro
	@param cMsgTipo, Character, Tipo da mensagem
	@return Character, Mensagem de erro
/*/
Static Function fGetJsonErro(nErroCod As Numeric, cMsg As Character, cMsgDesc As Character, cMsgTipo As Character) As Character
	Local oPouiError 	:= JsonObject():new() as Json

	oPouiError["message"]         	:= cMsg
	oPouiError["detailedMessage"] 	:= cMsgDesc
	oPouiError["code"]            	:= cValToChar(nErroCod)
	oPouiError["type"]            	:= cMsgTipo
Return EncodeUTF8(FwCutOff(oPouiError:toJson()))

/*/{Protheus.doc} fGetJsonMsg
	Retorna o JSON para o POUI (com alguma mensagem, se necessario).
	@type Method
	@version 12.1.2410
	@author arthur.sales
	@since 15/04/2025
	@param cMsg, Character, Mensagem do erro
	@param cMsgDesc, Character, Descrica	@param cMsgTipo, Character, Tipo da mensagem
	@return Character, Mensagem de erro
/*/
Static Function fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo)
	Local oItemMsg	as Json

	Default cMsg 		:= ""
	Default cMsgDesc 	:= ""
	Default cMsgTipo 	:= ""

	if !Empty(cMsg)
		oResponse["_messages"] 		:= {}
		oItemMsg                 	:= JsonObject():new()
		oItemMsg["code"]            := ""
		oItemMsg["message"]         := cMsg
		oItemMsg["detailedMessage"] := cMsgDesc
		oItemMsg["type"]            := cMsgTipo

		aadd(oResponse["_messages"],oItemMsg)
	Endif
Return EncodeUTF8(FwCutOff(oResponse:toJson()))

/*/{Protheus.doc} fDateToJson
	Converte data para o tipo lido no POUI.
	@type Method
	@version 12.1.2410
	@author arthur.sales
	@since 15/04/2025
	@param cData, Character, Data a ser convertida
	@return Character, Data no formato para o POUI
/*/
Static Function fDateToJson(cData)
	If ValType(cData) == "D"
		cData := DtoS(cData)
	Endif

	If !Empty(cData)
		cData := Left(cData,4) + '-' + Subs(cData,5,2) + '-' + Right(cData,2)
	Endif
Return cData

/*/{Protheus.doc} getListaRoteiro
    Retorna uma lista de roteiros filtrando por filial
    @type Method
    @version 12.1.2410
    @author Guilherme Suttanni
    @since 26/06/2025
    @return Variant, Retorno nulo pre-fixado
/*/
method getListaRoteiro() As Variant class GPEUtils
    Local jQueryParams  := oRest:getQueryRequest() as JSON
    Local nErroCod      := 0                       as Numeric
    Local cMsg          := ""                      as Character
    Local cMsgDesc      := ""                      as Character
    Local cMsgTipo      := ""                      as Character
    Local nParamOrder   := 1                       as Numeric
    Local cAlias        := ""                      as Character
    Local cQuery        := ""                      as Character
    Local oStatement    := NIL                     as Object
    Local oItem         := NIL                     as Object

    Private oResponse := JsonObject():New()        as Json

    oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

    BEGIN SEQUENCE
        If jQueryParams:hasProperty("forcaErro")
            BREAK
        Endif

        oResponse["items"] := {}

        cQuery := "SELECT RY_CALCULO, RY_DESC "
        cQuery += "FROM " + RetSqlName("SRY") + " "
        cQuery += "WHERE RY_FILIAL = ? "
        cQuery += "AND D_E_L_E_T_ = ? "
        cQuery := ChangeQuery(cQuery)

        oStatement := FwExecStatement():New(cQuery)
		oStatement:SetString(nParamOrder++, IIf(jQueryParams:hasProperty('CRAFILIAL'), FwXFilial("SRY", jQueryParams['CRAFILIAL']), cFilAnt))
        oStatement:SetString(nParamOrder++, " ")

        cAlias := oStatement:OpenAlias()

        While !(cAlias)->(Eof())
            oItem := JSonObject():new()
            oItem["SRYCALCULO"] := RTrim((cAlias)->RY_CALCULO)
            oItem["SRYDESC"]    := RTrim((cAlias)->RY_DESC)
            aadd(oResponse["items"], oItem)
            (cAlias)->(DbSkip())
        End

        (cAlias)->(DbCloseArea())

    RECOVER
        nErroCod   := 500
        cMsg       := "Erro"
        cMsgDesc   := "Internal Server Error"
        cMsgTipo   := "error"
        (cAlias)->(dbCloseArea())
    END SEQUENCE

    fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)
return
