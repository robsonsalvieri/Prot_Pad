#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include "totvs.ch"


class GPEFGTSDigital

	@Get("api/rh/v1/fgts-digital")
	public method getLista()

	@Get("api/rh/v1/fgts-digital/branches")
	public method getListaFilial()

	@Get("api/rh/v1/fgts-digital/categorias")
	public method getListaCategorias()

	public method new() constructor

endclass


//-------------------------------------------------------------------------------//
// Método construtor da classe													 //
//-------------------------------------------------------------------------------//
method new() class GPEFGTSDigital
return self

/*/{Protheus.doc} GPEFGTSDigital::getLista
retorna uma lista com os dados do FGTS Digital
@author Caio Kretzer
@since 30/05/2023
/*/
method getLista() class GPEFGTSDigital

    local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character
	local nParamOrder	:= 1						as Numeric
	local nX										as Numeric
	local nDif										as Numeric
	local cAlias									as Character
	local cChave		:= ''						as Character
	local cPerAtu		:= ''						as Character
	local cTrabSemVinc	:= fCatTrabEFD("TSV") 		as Character  //"201|202|305|308|401|410|701|711|712|721|722|723|731|734|738|741|751|761|771|781|901|902|903" //Trabalhador sem vinculo
	local cRaizCnpj		:= ''						as Character
	local cQuery									as Character
	local cFilSRA									as Character
	local nPosCGC									as Numeric
	local aCodFol									as Array
	local aInfo										as Array
	local aBranches									as Array
	local oStatement								as Object
	Local aTransf		:= {}						as Array
	Local aTrFilMat		:= {}						as Array
	Local cTrfAlias		:= GetNextAlias()			as Character
	Local nTrf			:= 0						as Numeric
	Local aErrCodFol	:= {}						as Array
	Local cRaizTrf		:= ''						as Character
	Local cChvTrf		:= ''						as Character

	Private oResponse := JsonObject():New()			as Json

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	begin sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		Endif

		aBranches := strtokarr(jQueryParams['branchCode'], ',')

		oResponse["items"] := {}
		cQuery := " SELECT SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_CIC, SRA.RA_ADMISSA, SRA.RA_CODUNIC, SRA.RA_CATEFD, SRA.RA_DESCEP, SRA.RA_MAT,"
		cQuery += " SRD.RD_PD PD, SRD.RD_PERIODO PERIODO, SRD.RD_VALOR VALOR, '1' TIPO  "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " INNER JOIN "+RetSqlName("SRD")+" SRD "
		cQuery += "  ON SRD.RD_FILIAL 		= SRA.RA_FILIAL "
		cQuery += " AND SRD.RD_MAT 			= SRA.RA_MAT "
		cQuery += " AND SRD.RD_EMPRESA 		= '' "
		cQuery += " AND SRD.D_E_L_E_T_ 		= '' "
		cQuery += " AND SRD.RD_PERIODO 		< '202403' "
		cQuery += " INNER JOIN " + RetSqlName("SRG") + " SRG "
		cQuery += "  ON SRG.RG_FILIAL 		= SRA.RA_FILIAL "
		cQuery += " AND SRG.RG_MAT 			= SRA.RA_MAT "
		cQuery += " AND SRG.D_E_L_E_T_ 		= '' "
		cQuery += " WHERE SRG.RG_EFETIVA	= 'S'
		cQuery += "   AND SRG.RG_RESCDIS	= '0'"
		cQuery += "   AND SRA.D_E_L_E_T_ 	= '' "
		cQuery += "   AND SRA.RA_CIC 		!= '' "
		cQuery += "   AND SRA.RA_CODUNIC 	!= '' "
		cQuery += "   AND SRA.RA_FILIAL 	IN (?) "
		cQuery += "   AND SRG.RG_DATADEM 	>= ? "
		cQuery += "   AND SRG.RG_DATADEM 	<= ? "
		If jQueryParams:hasProperty("matricula") .AND. !Empty(jQueryParams['matricula'])
			cQuery += "   AND SRA.RA_CODUNIC = ? "
		Endif

		cQuery += " UNION ALL "

		cQuery += " SELECT SRA.RA_FILIAL, SRA.RA_NOME, SRA.RA_CIC, SRA.RA_ADMISSA, SRA.RA_CODUNIC, SRA.RA_CATEFD, SRA.RA_DESCEP, SRA.RA_MAT,"
		cQuery += " SRC.RC_PD PD, SRC.RC_PERIODO PERIODO, SRC.RC_VALOR VALOR, '2' TIPO  "
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		cQuery += " INNER JOIN "+RetSqlName("SRC")+" SRC "
		cQuery += "  ON SRC.RC_FILIAL 		= SRA.RA_FILIAL "
		cQuery += " AND SRC.RC_MAT 			= SRA.RA_MAT "
		cQuery += " AND SRC.D_E_L_E_T_ 		= '' "
		cQuery += " AND SRC.RC_PERIODO 		< '202403' "
		cQuery += " INNER JOIN " + RetSqlName("SRG") + " SRG "
		cQuery += "  ON SRG.RG_FILIAL 		= SRA.RA_FILIAL "
		cQuery += " AND SRG.RG_MAT 			= SRA.RA_MAT "
		cQuery += " AND SRG.D_E_L_E_T_ 		= '' "
		cQuery += " WHERE SRG.RG_EFETIVA	= 'S'
		cQuery += "   AND SRG.RG_RESCDIS	= '0'"
		cQuery += "   AND SRA.D_E_L_E_T_ 	= '' "
		cQuery += "   AND SRA.RA_CIC 		!= '' "
		cQuery += "   AND SRA.RA_CODUNIC 	!= '' "
		cQuery += "   AND SRA.RA_FILIAL 	IN (?) "
		cQuery += "   AND SRG.RG_DATADEM 	>= ? "
		cQuery += "   AND SRG.RG_DATADEM 	<= ? "
		If jQueryParams:hasProperty("matricula") .AND. !Empty(jQueryParams['matricula'])
			cQuery += "   AND SRA.RA_CODUNIC = ? "
		Endif
		cQuery += " ORDER BY RA_FILIAL, RA_CODUNIC, PERIODO, TIPO "

		cQuery := ChangeQuery(cQuery)
		// Instancia a classe
		oStatement := FwExecStatement():New(cQuery)

		oStatement:SetIN(nParamOrder++, aBranches)
		oStatement:SetString(nParamOrder++, StrTran(SubStr(jQueryParams["dataDesligamentoDe"],  1, 10), "-", ""))
		oStatement:SetString(nParamOrder++, StrTran(SubStr(jQueryParams["dataDesligamentoAte"],  1, 10), "-", ""))

		If jQueryParams:hasProperty("matricula") .AND. !Empty(jQueryParams['matricula'])
			oStatement:SetString(nParamOrder++, jQueryParams['matricula'])
		Endif

		oStatement:SetIN(nParamOrder++, aBranches)
		oStatement:SetString(nParamOrder++, StrTran(SubStr(jQueryParams["dataDesligamentoDe"],  1, 10), "-", ""))
		oStatement:SetString(nParamOrder++, StrTran(SubStr(jQueryParams["dataDesligamentoAte"],  1, 10), "-", ""))

		If jQueryParams:hasProperty("matricula") .AND. !Empty(jQueryParams['matricula'])
			oStatement:SetString(nParamOrder++, jQueryParams['matricula'])
		Endif

		// Executa a query e retorna o alias criado
		cAlias 	:= oStatement:OpenAlias()
		cFilSRA := "_"

		While !(cAlias)->(Eof())

			If cFilSRA == '_' .OR. (cAlias)->RA_FILIAL != cFilSRA
				If cFilSRA == '_' .OR. FwXFilial("SRV",(cAlias)->RA_FILIAL) != FwXFilial("SRV",cFilSRA)
					FP_CODFOL(@aCodFol,(cAlias)->RA_FILIAL, .F., .F., @aErrCodFol)
				Endif
				cFilSRA := (cAlias)->RA_FILIAL
				aInfo   := FWSM0Util():GetSM0Data(cEmpAnt, (cAlias)->RA_FILIAL, {"M0_CGC"})
				nPosCGC := aScan(aInfo, {|x| x[1] == "M0_CGC"})

				If nPosCGC > 0
					If cRaizCnpj == ''
						cRaizCnpj := SubStr(aInfo[nPosCGC][2],1,8)
					ElseIf cRaizCnpj != SubStr(aInfo[nPosCGC][2],1,8)
						nErroCod 	:= 500
						cMsg 		:= "Erro: filiais com raiz de CNPJ diferentes."
						cMsgDesc 	:= "Ocorreu um erro! As filias selecionadas devem conter a mesma raiz de CNPJ."
						cMsgTipo	:= "error"
						Exit
					Endif
				Endif
			Endif

			If cChave != AllTrim((cAlias)->RA_CODUNIC)

				If !Empty(cPerAtu)
					If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
						oItemB["indAusenciaFGTS"]   := "S"
					Endif
					aadd(oItem["pagamentos"], oItemB)
				Endif
				If !Empty(cChave)
					aadd(oResponse["items"], oItem)
				Endif
				cChave := AllTrim((cAlias)->RA_CODUNIC)
				oItem := JSonObject():new()
				oItem["cnpjEmpregador"]    	:= cRaizCnpj
				oItem["cpfTrabalhador"]    	:= AllTrim((cAlias)->RA_CIC)
				oItem["dataAdmissao"]   	:= fDateToJson(StoD((cAlias)->RA_ADMISSA))
				oItem["matricula"]   	    := If( !((cAlias)->RA_CATEFD $ cTrabSemVinc) .Or. (cAlias)->RA_DESCEP == "1", AllTrim((cAlias)->RA_CODUNIC), '' )
				oItem["identFunc"]   	    := Alltrim(cEmpant) + " " + AllTrim((cAlias)->RA_FILIAL) + " " +  AllTrim((cAlias)->RA_MAT)
				oItem["nomeFunc"]   	    := AllTrim(Substr((cAlias)->RA_NOME,1,30))
				oItem["categoriaTSVE"]	    := Iif((cAlias)->RA_CATEFD $ cTrabSemVinc, Rtrim((cAlias)->RA_CATEFD), '')
				oItem["branchCode"]    		:= AllTrim((cAlias)->RA_FILIAL)
				oItem["pagamentos"]	        := {}

				aTransf		:= {}
				cChvTrf		:= ""
				cRaizTrf	:= ""
				fTransf(@aTransf,,,,,,,,,,,,(cAlias)->RA_FILIAL, (cAlias)->RA_MAT)
				If Len(aTransf) > 0
					aTrFilMat := {}
					For nTrf := 1 to Len(aTransf)
						If aTransf[nTrf,1] == cEmpAnt .And. aTransf[nTrf,2] != aTransf[nTrf,5]
							If cFilSRA != aTransf[nTrf, 8]
								aInfo := FWSM0Util():GetSM0Data(cEmpAnt, aTransf[nTrf,8], {"M0_CGC"})
								If Len(aInfo) < nPosCGC .Or. cRaizCnpj != SubStr(aInfo[nPosCGC,2],1,8)
									Loop
								EndIf
							EndIf
							aAdd(aTrFilMat, { aTransf[nTrf,8], aTransf[nTrf,9] })
						EndIf
					Next nTrf

					If Len(aTrFilMat) > 0
						cPerAtu		:= ""

						cQuery := "SELECT SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_CATEFD, SRA.RA_DESCEP, SRA.RA_CODUNIC, SRD.RD_PD, SRD.RD_PERIODO, SRD.RD_VALOR "
						cQuery += "FROM " + RetSqlName("SRA") + " SRA "
						cQuery += "INNER JOIN " + RetSqlName("SRD") + " SRD "
						cQuery +=	"ON SRD.RD_FILIAL	= SRA.RA_FILIAL "
						cQuery +=	"AND SRD.RD_MAT		= SRA.RA_MAT "
						cQuery +=	"AND SRD.RD_EMPRESA = '' "
						cQuery += "WHERE SRD.RD_PERIODO < '202403' "
						cQuery +=	"AND ("
						For nTrf := 1 to Len(aTrFilMat)
							cQuery += "(SRA.RA_FILIAL = '" + aTrFilMat[nTrf, 1] + "' "
							cQuery += "AND SRA.RA_MAT = '" + aTrFilMat[nTrf, 2] + "')"
							cQuery += If( nTrf < Len(aTrFilMat), " OR ", "" )
						Next nTrf
						cQuery +=	") AND SRA.RA_CIC = '" + AllTrim((cAlias)->RA_CIC) + "' "
						cQuery +=	"AND SRA.D_E_L_E_T_ = ' ' "
						cQuery +=	"AND SRD.D_E_L_E_T_ = ' ' "
						cQuery += "ORDER BY RD_PERIODO, RA_FILIAL, RA_MAT"
						cQuery := ChangeQuery(cQuery)

						dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cTrfAlias,.T.,.T.)

						While (cTrfAlias)->(!Eof())
							If cFilSRA != (cTrfAlias)->RA_FILIAL .Or. Empty(cRaizTrf)
								If FwXFilial("SRV",(cTrfAlias)->RA_FILIAL) != FwXFilial("SRV",cFilSRA)
									FP_CODFOL(@aCodFol,(cTrfAlias)->RA_FILIAL, .F., .F., @aErrCodFol)
								Endif
								cFilSRA := (cTrfAlias)->RA_FILIAL

								aInfo   := FWSM0Util():GetSM0Data(cEmpAnt, (cTrfAlias)->RA_FILIAL, {"M0_CGC"})
								nPosCGC := aScan(aInfo, {|x| x[1] == "M0_CGC"})
								If nPosCGC > 0
									cRaizTrf := SubStr(aInfo[nPosCGC,2],1,8)
								EndIf
							EndIf

							If !Empty((cTrfAlias)->RA_CODUNIC) .And. ( cChvTrf != AllTrim((cTrfAlias)->RA_CODUNIC) .Or. cRaizTrf != oItem["cnpjEmpregador"] )//se tiver vazio nem cria outro cabecalho AGH ten q criar pelos pagamentos
								If !Empty(cPerAtu)
									If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
										oItemB["indAusenciaFGTS"]   := "S"
									Endif
									aadd(oItem["pagamentos"], oItemB)
								Endif
								If !Empty(cChvTrf)
									aadd(oResponse["items"], oItem)
								Endif
								cChvTrf := AllTrim((cTrfAlias)->RA_CODUNIC)

								oItem := JSonObject():new()
								oItem["cnpjEmpregador"]    	:= cRaizTrf
								oItem["cpfTrabalhador"]    	:= AllTrim((cAlias)->RA_CIC)
								oItem["dataAdmissao"]   	:= fDateToJson(StoD((cAlias)->RA_ADMISSA))
								oItem["matricula"]   	    := If( !((cTrfAlias)->RA_CATEFD $ cTrabSemVinc) .Or. (cTrfAlias)->RA_DESCEP == "1", AllTrim((cTrfAlias)->RA_CODUNIC), '' )
								oItem["identFunc"]   	    := Alltrim(cEmpant) + " " + AllTrim((cTrfAlias)->RA_FILIAL) + " " +  AllTrim((cTrfAlias)->RA_MAT)
								oItem["nomeFunc"]   	    := AllTrim(Substr((cAlias)->RA_NOME,1,30))
								oItem["categoriaTSVE"]	    := Iif((cTrfAlias)->RA_CATEFD $ cTrabSemVinc, Rtrim((cTrfAlias)->RA_CATEFD), '')
								oItem["branchCode"]    		:= AllTrim((cAlias)->RA_FILIAL)
								oItem["pagamentos"]	        := {}
							EndIf

							If Empty(cPerAtu)
								oItemB := JSonObject():new()
								oItemB["competencia"]    	:= SubStr((cTrfAlias)->RD_PERIODO,5,2)+'-'+SubStr((cTrfAlias)->RD_PERIODO,1,4)
								oItemB["categoria"]        	:= AllTrim((cTrfAlias)->RA_CATEFD)
								oItemB["valorPrincipal"]   	:= 0
								oItemB["valorDecTerceiro"]  := 0
								oItemB["indAusenciaFGTS"]   := ""

								cPerAtu := (cTrfAlias)->RD_PERIODO
							EndIf

							If cPerAtu == (cTrfAlias)->RD_PERIODO
								If (cTrfAlias)->RD_PD == aCodFol[17,1] .Or. (cTrfAlias)->RD_PD == aCodFol[337,1]
									oItemB["valorPrincipal"] += (cTrfAlias)->RD_VALOR
								Elseif (cTrfAlias)->RD_PD == aCodFol[108,1] .Or. (cTrfAlias)->RD_PD == aCodFol[398,1]
									oItemB["valorDecTerceiro"] += (cTrfAlias)->RD_VALOR
								Endif
							Else
								If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
									oItemB["indAusenciaFGTS"]   := "S"
								Endif
								aadd(oItem["pagamentos"], oItemB)

								oItemB := JSonObject():new()
								oItemB["competencia"]    	:= SubStr((cTrfAlias)->RD_PERIODO,5,2)+'-'+SubStr((cTrfAlias)->RD_PERIODO,1,4)
								oItemB["categoria"]        	:= AllTrim((cTrfAlias)->RA_CATEFD)
								oItemB["valorPrincipal"]   	:= 0
								oItemB["valorDecTerceiro"]  := 0
								oItemB["indAusenciaFGTS"]   := ""

								cPerAtu := (cTrfAlias)->RD_PERIODO
							Endif
							(cTrfAlias)->( dbSkip() )
						EndDo
						(cTrfAlias)->( dbCloseArea() )

						If !Empty(cPerAtu)
							If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
								oItemB["indAusenciaFGTS"]   := "S"
							Endif
							aadd(oItem["pagamentos"], oItemB)
						Endif
						If !Empty(cChvTrf) .And. (cChvTrf <> cChave .Or. cRaizTrf <> cRaizCnpj)
							aadd(oResponse["items"], oItem)
						Endif
						If FwXFilial("SRV",(cAlias)->RA_FILIAL) != FwXFilial("SRV",cFilSRA)
							FP_CODFOL(@aCodFol,(cAlias)->RA_FILIAL, .F., .F., @aErrCodFol)
						Endif
						cFilSRA := (cAlias)->RA_FILIAL

					EndIf
				EndIf

				If !Empty(cChvTrf) .And. (cChvTrf <> cChave .Or. cRaizTrf <> cRaizCnpj)
					oItem := JSonObject():new()
					oItem["cnpjEmpregador"]    	:= cRaizCnpj
					oItem["cpfTrabalhador"]    	:= AllTrim((cAlias)->RA_CIC)
					oItem["dataAdmissao"]   	:= fDateToJson(StoD((cAlias)->RA_ADMISSA))
					oItem["matricula"]   	    := If( !((cAlias)->RA_CATEFD $ cTrabSemVinc) .Or. (cAlias)->RA_DESCEP == "1", AllTrim((cAlias)->RA_CODUNIC), '' )
					oItem["identFunc"]   	    := Alltrim(cEmpant) + " " + AllTrim((cAlias)->RA_FILIAL) + " " +  AllTrim((cAlias)->RA_MAT)
					oItem["nomeFunc"]   	    := AllTrim(Substr((cAlias)->RA_NOME,1,30))
					oItem["categoriaTSVE"]	    := Iif((cAlias)->RA_CATEFD $ cTrabSemVinc, Rtrim((cAlias)->RA_CATEFD), '')
					oItem["branchCode"]    		:= AllTrim((cAlias)->RA_FILIAL)
					oItem["pagamentos"]	        := {}
				EndIf

				oItemB := JSonObject():new()
				oItemB["competencia"]    	:= SubStr((cAlias)->PERIODO,5,2)+'-'+SubStr((cAlias)->PERIODO,1,4)
				oItemB["categoria"]        	:= AllTrim((cAlias)->RA_CATEFD)
				oItemB["valorPrincipal"]   	:= 0
				oItemB["valorDecTerceiro"]  := 0
				oItemB["indAusenciaFGTS"]   := ""

				cPerAtu := (cAlias)->PERIODO
			Endif

			If cPerAtu == (cAlias)->PERIODO
				If (cAlias)->PD == aCodFol[17,1] .Or. (cAlias)->PD == aCodFol[337,1]
					oItemB["valorPrincipal"] += (cAlias)->VALOR
				Elseif (cAlias)->PD == aCodFol[108,1] .Or. (cAlias)->PD == aCodFol[398,1]
					oItemB["valorDecTerceiro"] += (cAlias)->VALOR
				Endif
			Else
				If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
					oItemB["indAusenciaFGTS"]   := "S"
				Endif
				aadd(oItem["pagamentos"], oItemB)
				// verificar se o periodo é o próximo. Se não for, adicionar os items
				nDif := DateDiffMonth( StoD((cAlias)->PERIODO+"01"), StoD(cPerAtu+"01")) - 1
				If nDif > 0
					For nX := 1 To nDif
						dNovoPer := MonthSum(StoD(cPerAtu+"01"), nX)
						oItemB := JSonObject():new()
						oItemB["competencia"]    	:= SubStr(DtoS(dNovoPer),5,2)+'-'+SubStr(DtoS(dNovoPer),1,4)
						oItemB["categoria"]        	:= AllTrim((cAlias)->RA_CATEFD)
						oItemB["valorPrincipal"]   	:= 0
						oItemB["valorDecTerceiro"]  := 0
						oItemB["indAusenciaFGTS"]   := "S"
						aadd(oItem["pagamentos"], oItemB)
					Next nX
				Endif
				oItemB := JSonObject():new()
				oItemB["competencia"]    	:= SubStr((cAlias)->PERIODO,5,2)+'-'+SubStr((cAlias)->PERIODO,1,4)
				oItemB["categoria"]        	:= AllTrim((cAlias)->RA_CATEFD)
				oItemB["valorPrincipal"]   	:= 0
				oItemB["valorDecTerceiro"]  := 0
				oItemB["indAusenciaFGTS"]   := ""

				cPerAtu := (cAlias)->PERIODO
			Endif

			(cAlias)->(DbSkip())
		EndDo
		If !Empty(cPerAtu)
			If oItemB["valorPrincipal"] == 0 .AND. oItemB["valorDecTerceiro"] == 0
				oItemB["indAusenciaFGTS"]   := "S"
			Endif
			aadd(oItem["pagamentos"], oItemB)
		Endif
		If !Empty(cChave)
			aadd(oResponse["items"], oItem)
		Endif

		(cAlias)->(DbCloseArea())

		If jQueryParams:hasProperty("forcaMsg")
			cMsg		:= "Mensagem"
			cMsgDesc	:= "Mensagem Teste"
			cMsgTipo	:= "success"
		Endif

		recover

		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())

	end sequence

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

return


/*/{Protheus.doc} GPEFGTSDigital::getListaFilial
retorna uma lista de filiais
@author Caio Kretzer
@since 30/05/2023
/*/
method getListaFilial() class GPEFGTSDigital

	local aSM0										as Array
    local nSM0         	:= 1						as Numeric
    local cSearch      	:= "" 						as Character // Termo de busca
    local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character

	Private oResponse := JsonObject():New()			as Json

	cSearch := IIf(jQueryParams:hasProperty("filter"), jQueryParams["filter"], "")

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	begin sequence

		If jQueryParams:hasProperty("forcaErro")
			break
		Endif

		aSM0      := FwLoadSM0()

		oResponse["items"] := {}

		// Percorre as filiais adicionando cada item no JSON de resposta
		For nSM0 := 1 To Len(aSM0)
			// Pula as filiais de empresa diferente da empresa logada
			If (aSM0[nSM0][SM0_GRPEMP] != cEmpAnt)
				LOOP
			EndIf

			// Se o termo de busca estiver preenchido, filtra as filiais com base nele
			If (!Empty(cSearch) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_CODFIL])) .and. !(Upper(cSearch) $ Upper(aSM0[nSM0][SM0_NOMRED])))
				LOOP
			EndIf

			oItem := JSonObject():new()
			oItem["branchCode"]		:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_CODFIL]))
			oItem["branchName"]		:= EncodeUTF8(AllTrim(aSM0[nSM0][SM0_NOMRED]))

			aadd(oResponse["items"], oItem)

		Next nSM0

		recover

		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

	end sequence

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

return


/*/{Protheus.doc} GPEFGTSDigital::getListaCategorias
retorna uma lista de categorias
@author Caio Kretzer
@since 30/05/2023
/*/
method getListaCategorias() class GPEFGTSDigital

    local cSearch      	:= "" 						as Character // Termo de busca
    local jQueryParams 	:= oRest:getQueryRequest()	as Json
	Local nErroCod 		:= 0 						as Numeric
	Local cMsg			:= ""						as Character
	Local cMsgDesc		:= ""						as Character
	Local cMsgTipo		:= ""						as Character

	local cRCBBranch    := FwXFilial("RCB", jQueryParams["branchCode"]) as Character
    local nFieldSize    := GetSX3Cache("RCB_CAMPOS", "X3_TAMANHO")		as Numeric
	local nCodeSizeS049 := Posicione("RCB", 3, cRCBBranch + PadR("CODIGO", nFieldSize) + "S049", "RCB_TAMAN") 		as Numeric
	local nSefipSizeS049 := Posicione("RCB", 3, cRCBBranch + PadR("CODSEFIP", nFieldSize) + "S049", "RCB_TAMAN")	as Numeric
    local nDescSizeS049 := Posicione("RCB", 3, cRCBBranch + PadR("DESCRICAO",   nFieldSize) + "S049", "RCB_TAMAN")	as Numeric
	local cFiltro		:= ""						as Character
	local cQuery									as Character
	local nParamOrder 	:= 1						as Numeric
	local oStatement								as Object
	local cAlias									as Character
	Private oResponse := JsonObject():New()			as Json

	cSearch := IIf(jQueryParams:hasProperty("filter"), jQueryParams["filter"], "")

	If jQueryParams:hasProperty("forcaErro")
		break
	Endif

	If !Empty(cSearch)
		cFiltro := " AND (UPPER(SUBSTRING(RCC_CONTEU, 1, ?)) LIKE UPPER(?) OR "
        cFiltro += " UPPER(SUBSTRING(RCC_CONTEU, ?, ?)) LIKE UPPER(?))"
	Endif

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	begin sequence

		// Busca os registros na RCC
		cQuery := "SELECT "
		cQuery +=     "RCC_CONTEU, "
		cQuery +=     "RCC_FIL, "
		cQuery +=     "RCC_FILIAL, "
		cQuery +=     "RCC_SEQUEN "
		cQuery += "FROM "
		cQuery +=     RetSQLName("RCC") + " "
		cQuery += "WHERE "
		cQuery +=     "D_E_L_E_T_ = ' ' "
		cQuery +=     "AND RCC_CODIGO = 'S049' "
		cQuery +=     "AND RCC_FILIAL = ? "
		cQuery +=     cFiltro
		cQuery += "ORDER BY RCC_CONTEU "
		cQuery := ChangeQuery(cQuery)

		// Instancia a classe
		oStatement := FWExecStatement():New(cQuery)
		oStatement:SetString(nParamOrder++, cRCBBranch)

		If !Empty(cSearch)
			oStatement:SetUnsafe(nParamOrder++, CValToChar(nCodeSizeS049))
			oStatement:SetString(nParamOrder++, "%" + cSearch + "%")
			oStatement:SetUnsafe(nParamOrder++, CValToChar(nCodeSizeS049 + 1))
			oStatement:SetUnsafe(nParamOrder++, CValToChar(nDescSizeS049))
			oStatement:SetString(nParamOrder++, "%" + cSearch + "%")
		EndIf

		// Executa a query e retorna o alias criado
		cAlias := oStatement:OpenAlias()

		oResponse["items"] := {}

		While !(cAlias)->(EOF())
			oItem := JSonObject():new()
			oItem["codigo"]		:= AllTrim(SubStr((cAlias)->RCC_CONTEU, 1, nCodeSizeS049))
			oItem["descricao"]  := AllTrim(SubStr((cAlias)->RCC_CONTEU, nCodeSizeS049 + nSefipSizeS049 + 1, nDescSizeS049))
			AAdd(oResponse["items"], oItem)

			(cAlias)->(DBSkip())
		End

		(cAlias)->(dbCloseArea())

		recover

		nErroCod 	:= 500
		cMsg 		:= "Erro"
		cMsgDesc 	:= "Internal Server Error"
		cMsgTipo	:= "error"

		(cAlias)->(dbCloseArea())

	end sequence

	fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

return


/* Seta a resposta para o PO-UI.
Foi necessária a criação para passar no issue x cobertura de todas as APIs */
Static Function fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

	if nErroCod > 0
		oRest:setStatusCode(nErroCod)
		oRest:setFault(fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo))
	else
		oRest:setResponse(fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo))
	endif
Return


/* RETORNA O JSON DE ERRO PARA O POUI */
Static Function fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo)
	Local oPouiError                 		:= JsonObject():new() as Json

	oPouiError["message"]         	:= cMsg
	oPouiError["detailedMessage"] 	:= cMsgDesc
	oPouiError["code"]            	:= cValToChar(nErroCod)
	oPouiError["type"]            	:= cMsgTipo

Return EncodeUTF8(FwCutOff(oPouiError:toJson()))


/* RETORNA O JSON PARA O POUI (COM ALGUMA MENSAGEM, SE NECESSARIO) */
Static Function fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo)

	Local oItemMsg 			  as Json

	Default cMsg 		:= ""
	Default cMsgDesc 	:= ""
	Default cMsgTipo 	:= ""

	if !Empty(cMsg)
		oResponse["_messages"] 		:= {}
		oItemMsg                 	:= JsonObject():new()
		oItemMsg["code"]            := ""
		oItemMsg["message"]         := cMsg
		oItemMsg["detailedMessage"] := cMsgDesc
		oItemMsg["type"]            := cMsgTipo

		aadd(oResponse["_messages"],oItemMsg)
	Endif

Return EncodeUTF8(FwCutOff(oResponse:toJson()))


/* CONVERTE DATA PARA O TIPO LIDO NO POUI */
Static Function fDateToJson(cData)
	If ValType(cData) == "D"
		cData := DtoS(cData)
	Endif

	If !Empty(cData)
		cData := Left(cData,4) + '-' + Subs(cData,5,2) + '-' + Right(cData,2)
	Endif
Return cData
