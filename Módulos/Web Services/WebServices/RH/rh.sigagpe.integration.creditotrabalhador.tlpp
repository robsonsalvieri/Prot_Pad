#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include "totvs.ch"
#Include "rh.sigagpe.integration.creditotrabalhador.ch"

//---------------------------------------------------------------------
/*/{Protheus.doc} GPECreditoTrabalhador
@description	Serviço para importação de informações do Programa Crédito do Trabalhador
@author		    martins.marcio
@since			02/06/2025
/*/
//---------------------------------------------------------------------
class GPECreditoTrabalhador


    // Limpa os dados para fazer o reenvio
	@Post("api/rh/v1/credito-trabalhador")
	public method gravaCreditoTrabalhador()

	public method new() constructor

endclass


//-------------------------------------------------------------------------------//
// new                                                                           //
//-------------------------------------------------------------------------------//
method new() class GPECreditoTrabalhador
return self

/*/{Protheus.doc} GPECreditoTrabalhador::gravaCreditoTrabalhador
Limpa os dados da RJP - Tabela de Fila NG para fazer o reenvio.
@author martins.marcio
@since 02/06/2025
/*/
method gravaCreditoTrabalhador() class GPECreditoTrabalhador

	Local nErroCod     := 0							as Numeric
	Local jQueryParams := oRest:getQueryRequest()		as Json
	Local cBody        := oRest:getBodyRequest()
	Local cMsgErro     := ""							as Character
	Local lSimula      := .F.							as Logical
	Local lAtualiza    := .T.							as Logical
	Local aBranches    := {}							as Array
	Local cBranches    := ""							as Character

	Private cErro										as Character
	Private cErroDesc									as Character
	Private cErroTipo									as Character
	Private oResponse	:= JsonObject():New()			as Json
	Private oJson		:= JsonObject():New()			as Json
	Private cTipoImp	:= "json"						as Character
	Private cJsnVers	:= "1.4"						as Character

	oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

	Begin Sequence

		oJson:fromJson(cBody)
		If oJson:hasProperty("items")
			If oJson:hasProperty("branches")
				aBranches := oJson["branches"]
				If ValType(aBranches) == "A" .And. !Empty(aBranches)
					If !Empty(aBranches[1]) .And. ValType(aBranches[1]) == "C"
						cBranches := "((AllTrim(RA_FILIAL) $ '"
						aEval(aBranches, { |x| cBranches += x + "," })
						cBranches := LEFT(cBranches, Len(cBranches)-1)
						cBranches += "'))"
					EndIf
				EndIf
			EndIf
			If oJson:hasProperty("atualiza")
				lAtualiza := oJson["atualiza"]
			EndIf
			f944IniGrv(oJson['items'], @cMsgErro, lSimula, lAtualiza, @oResponse, cBranches)
		EndIf

		If jQueryParams:hasProperty("forcaErro")
			break
		EndIf

		recover

		nErroCod 	:= 500
		cErro 		:= OemToAnsi(STR0001)  //"Atenção - Inconsistência"
		cErroDesc 	:= OemToAnsi(STR0002)  //"Não foi possível fazer a importação dos empréstimos, tente novamente."
		cErroTipo	:= "error"

	End Sequence

	fSetResponse(nErroCod, cErro, cErroDesc, cErroTipo)

Return

/* Seta a resposta para o PO-UI.
Foi necessária a criação para passar no issue x cobertura de todas as APIs */
Static Function fSetResponse(nErroCod, cMsg, cMsgDesc, cMsgTipo)

	if nErroCod > 0
		oRest:setStatusCode(nErroCod)
		oRest:setFault(fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo))
	else
		oRest:setResponse(fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo))
	endif
Return


/* RETORNA O JSON DE ERRO PARA O POUI */
Static Function fGetJsonErro(nErroCod, cMsg, cMsgDesc, cMsgTipo)
	Local oPouiError                 		:= JsonObject():new() as Json

	oPouiError["message"]         	:= cMsg
	oPouiError["detailedMessage"] 	:= cMsgDesc
	oPouiError["type"]            	:= cMsgTipo

Return EncodeUTF8(FwCutOff(oPouiError:toJson()))


/* RETORNA O JSON PARA O POUI (COM ALGUMA MENSAGEM, SE NECESSARIO) */
Static Function fGetJsonMsg(cMsg, cMsgDesc, cMsgTipo)

	Local oItemMsg 			  as Json

	Default cMsg 		:= ""
	Default cMsgDesc 	:= ""
	Default cMsgTipo 	:= ""

	If !Empty(cMsg)
		// oResponse["_messages"] 		:= {}
		// oItemMsg                 	:= JsonObject():new()
		// oItemMsg["message"]         := cMsg
		// oItemMsg["detailedMessage"] := cMsgDesc
		// oItemMsg["type"]            := cMsgTipo

		// aadd(oResponse["_messages"],oItemMsg)
	EndIf

Return EncodeUTF8(FwCutOff(oResponse:toJson()))
