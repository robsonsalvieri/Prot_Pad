#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include "totvs.ch"
#Include "rh.sigagpe.ahgora.ch"

#DEFINE CodeRespCreated 201
#DEFINE CodeRespBadRequest 400

Static lTemRUM 
Static lTemRUL 
Static __oInstance as Object
Static aResp
Static aSM0

class GPEAhGora

	public method new() as object
	
	@POST(endpoint='/api/rh/v1/ahgora/results', description='API de inclusão dos resultados originados da ahgora') 
	public method postResultsAhgora() as object

	@POST(endpoint='/api/rh/v1/ahgora/funcionarios', description='API de envio dos funcionários para Ahgora') 
	public method postFuncionariosAhgora() as object

	@POST(endpoint='/api/rh/v1/ahgora/afastamentos', description='API de envio dos afastamentos para Ahgora') 
	public method postAfastamentosAhgora() as object

	@Post(endpoint='/api/rh/v1/ahgora/status', description='API de busca dos status das integrações') 
	public method postStatusCompAhgora() as object

	@POST(endpoint='/api/rh/v1/ahgora/afastamentos/exclusao', description='API de envio dos afastamentos para Ahgora') 
	public method postAfastExcAhgora() as object

	@POST(endpoint='/api/rh/v1/ahgora/localizacoes', description='API para gravar as localizações') 
	public method postLocalizacao() as object

	@POST(endpoint='/api/rh/v1/ahgora/configuracoes', description='API para gravar as configurações da tela') 
	public method postConfiguracoesAhgora() as object
	
	@Get(endpoint='/api/rh/v1/ahgora/funcionarios', description='API de busca dos funcionários para integração com ahgora') 
	public method getListaAhgora() as object 

	@Get(endpoint='/api/rh/v1/ahgora/afastamentos', description='API de busca dos afastamentos para integração com ahgora') 
	public method getAfastamentoAhgora() as object 

	@Get(endpoint='/api/rh/v1/ahgora/afastamentos/exclusao', description='API de busca dos afastamentos para integração com ahgora') 
	public method getAfastExcAhgora() as object 

	@Get(endpoint='/api/rh/v1/ahgora/matricula', description='API de busca das matrículas dos funcionários') 
	public method getMatAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/ccusto', description='API de busca dos centros de custo da CTT') 
	public method getCcAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/funcao', description='API de busca das funções da SRJ') 
	public method getFuncaoAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/departamento', description='API de busca dos departamentos da SQB') 
	public method getDeptoAhgora() as object
	
	@Get(endpoint='/api/rh/v1/ahgora/turno', description='API de busca dos Turnos da SR6') 
	public method getTurnoAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/branches', description='API de busca das filiais da SM0') 
	public method getFiliaisAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/branches/:filial', description='API de busca das filiais da SM0') 
	public method getFilialAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/status/:codinterno', description='API de busca dos status das integrações') 
	public method getStatusAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/lote/:codinterno', description='API de busca dos lotes das integrações') 
	public method getLoteAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/opcoes', description='API de busca do combo de opções para filtro do funcionário') 
	public method getOpcoesAhgora() as object

	@Get(endpoint='/api/rh/v1/ahgora/localizacoes', description='API de busca das localizações') 
	public method getLocalizacoes() as object 

	@Get(endpoint='/api/rh/v1/ahgora/campos', description='API de busca dos campos da SRA') 
	public method getCamposSRA() as object 

	@Get(endpoint='/api/rh/v1/ahgora/verbas', description='API de busca das verbas da SRV para integração com Ahgora') 
	public method getVerbasAhgora() as object 
	
	@Get(endpoint='/api/rh/v1/ahgora/sindicatos', description='API de busca dos sindicatos na RCE para integração com Ahgora') 
	public method getSindicatosAhgora() as object 

	@Get(endpoint='/api/rh/v1/ahgora/configuracoes', description='API de busca das configurações usadas na tela de integração com Ahgora') 
	public method getConfigsAhgora() as object 

	@Delete(endpoint='/api/rh/v1/ahgora/localizacoes', description='API de exclusão das localizações') 
	public method delLocalizacoes() as object 

	private method getWherePar() as character
	private method getIdLocal() as character
	private method criaAltRUM() 
	private method getChefia() as array
	private method fMontaSRV() as logical
	private method fMontaRCE() as logical
	private method fCNPJAhgora() as array
	public method getCatAhgora() as character
	public method getDateAhgora() as character
	public method fVldChefia() as array
	public method getLocalFun() as array
	public method getAhgoraSR9() as character
	public method integracaoAhgora() as logical
	public method atualizaStatus() as logical
	public method getAltFunc() as logical
	static method fEmpReqAhgora() as logical
	static method gravaRUM()
	static method gravaRUMTransf()
	static Method getInstance()
	static method getRespDpto() as array
	static method fAhgoraPW() as logical
	static method AhgoraLiderados()
	static method AhgoraLider()

endclass

method new() as object class GPEAhGora
return self

/*-------------------------------------------------------------------
{Protheus.doc} getInstance
Retorna a instância statica da classe

@author	Bruno Costa
@since	23/09/2024
-------------------------------------------------------------------*/
Method getInstance() Class GPEAhGora
	If __oInstance == Nil
        __oInstance := GPEAhGora():new()
	EndIf
Return __oInstance

/*-------------------------------------------------------------------
{Protheus.doc} Post postResultsAhgora
API de integração dos resultados de ponto nos lançamentos do funcionário

@author	Bruno Costa
@since	29/07/2024
-------------------------------------------------------------------*/
method postResultsAhgora() class GPEAhGora
local oJsResponse := JsonObject():new() as object
local oJsResult   := JsonObject():new() as object
local oJsBadResp as object
local oJsResp as object
local oJsBody     := JsonObject():new() as Json
local oJParHeader := oRest:getHeaderRequest() as json
local dDataRef 	  := CtoD("") as date
local __cEmpAnt := cEmpAnt as character
local __cFilAnt := cFilAnt as character
local cPeriodo as character
local cFilProc as character
local cLastFil as character
local cProcAnt as character
local cSemana as character
local cSeqAux as character
local cPdOri as character
local nInRGB as character
local cUser as character
local cPass as character
local cPdTp as character
local cFil as character
local cEmp as character
local cMat as character
local cPer as character
local cPd as character
local lAbortacPer as logical
local lSemana as logical
local lAbortaEmp as logical
local lAbortaReq as logical
local lItemClVl as logical 
local lRCHComp as logical
local lAborta as logical
local lSemPd as logical
local lGrava as logical
local nHoras as numeric
local nItem as numeric
local nI as numeric
local nY as numeric
local aKeyUser as array
local aKey as array
local aPerAberto as array

DEFAULT aSM0 := FwLoadSM0() 

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	nItem      := Len(oJsBody["items"])
	nInRGB 	   := RetOrder("RGB", "RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD+RGB_CC+RGB_SEQ+DTOS(RGB_DTREF)")
	lSemPd     := .F.
	lItemClVl  := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"  
	lRCHComp   := IF(FWModeAccess("RCH", 3) == "C", .T., .F.)
    aKeyUser   := StrTokArr(Decode64(AllTrim(SubStr(oJParHeader["Authorization"], 6, Len(oJParHeader["Authorization"])))), ":")
	cUser      := aKeyUser[1]
	cPass      := aKeyUser[2]
	
	oJsResponse['response'] := {}
	oJsResult['resultados'] := {}

	//Ordena pela propriedade matrícula que é composta por Empresa + Filial + Matrícula
	aSort(oJsBody["items"],,, {|x, y| x["matricula"] < y["matricula"]})

	DbSelectArea("SRA")
	DbSetOrder(1) //RA_FILIAL+RA_MAT+RA_NOME
	
	For nI := 1 To nItem 
		akey   := StrTokArr( oJsBody["items"][nI]["codfuncionario"], "|")
        cEmp   := akey[1]
		cFil   := aKey[2]
		cMat   := aKey[3]
        cPer   := oJsBody["items"][nI]["anocom"]+oJsBody["items"][nI]["mescom"]

		If ((lAborta .Or. lAbortaEmp) .And. cEmp == cEmpAnt) 
			For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
				oJsResp := JSonObject():new()
				oJsResp['matricula']  := cEmp+cFil+cMat
				oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
				oJsResp['status'] 	  := "E"
				oJsResp['mensagem']   := If(lAborta, OemToAnsi(STR0001), OemToAnsi(STR0002)) //"Usuário sem permissão de acesso na Empresa/Filial." ### "Empresa/Filial não encontradas na base de dados."
				oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
				oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
				aAdd(oJsResult['resultados'], oJsResp)
			Next nY
			Loop
		EndIf

		If cEmp <> cEmpAnt
			If aScan( aSM0, { |x| x[1] == cEmp .And. x[2] == cFil} )
				IF !(self:fEmpReqAhgora(cEmp, cFil, cUser, cPass, .F.))
					lAborta  := .T.
					cLastFil := ""
					For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
						oJsResp := JSonObject():new()
						oJsResp['matricula']  := cEmp+cFil+cMat
						oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
						oJsResp['status'] 	  := "E"
						oJsResp['mensagem']   := OemToAnsi(STR0001) //"Usuário sem permissão de acesso na Empresa/Filial."
						oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
						oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
						aAdd(oJsResult['resultados'], oJsResp)
					Next nY
					Loop
				EndIf
				DbSelectArea("SRA")
				DbSetOrder(1) 
				lAborta   := .F.
				lItemClVl := GetMvRH( "MV_ITMCLVL", .F., "2" ) $ "1*3"  
				lRCHComp  := IF(FWModeAccess("RCH", 3) == "C", .T., .F.)
				cProcAnt  := ""
			Else
				lAbortaEmp := .T.
				For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
					oJsResp := JSonObject():new()
					oJsResp['matricula']  := cEmp+cFil+cMat
					oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
					oJsResp['status'] 	  := "E"
					oJsResp['mensagem']   := OemToAnsi(STR0002) //"Empresa/Filial não encontradas na base de dados."
					oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
					oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
					aAdd(oJsResult['resultados'], oJsResp)
				Next nY
				Loop
			EndIf	
		EndIf

		lAbortaEmp := .F.
		lAborta	   := .F.

		If SRA->(DbSeek(cFil+cMat)) 
			If lAbortacPer .And. cProcAnt == SRA->RA_PROCES .AND. cFilProc == SRA->RA_FILIAL 
				For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
					oJsResp := JSonObject():new()
					oJsResp['matricula']  := cEmp+cFil+cMat
					oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
					oJsResp['status'] 	  := "E"
					oJsResp['mensagem']   := OemToAnsi(STR0004) //"Período/Semana não encontrados na base de dados ou o período está fechado. Necessário verificar o cadastro de períodos!"
					oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
					oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
					aAdd(oJsResult['resultados'], oJsResp)
				Next nY
				Loop
			EndIf	
			If cProcAnt != SRA->RA_PROCES .OR. ( !lRCHComp .AND. cProcAnt == SRA->RA_PROCES .AND. cFilProc != SRA->RA_FILIAL )
				lAbortacPer := .F.
				cFilProc 	:= SRA->RA_FILIAL
				cProcAnt 	:= SRA->RA_PROCES 
				cRot 	 	:= If( SRA->RA_CATFUNC $ "A*P", fGetCalcRot('9'), fGetCalcRot('1')) 
				fRetPerComp(SubStr(cPer,5,2), SubStr(cPer,1,4), xFilial("RCH",SRA->RA_FILIAL), SRA->RA_PROCES, cRot, @aPerAberto )
				lSemana := !Empty(oJsBody["items"][nI]["campoAdicional"])
				nPos 	:= aScan(aPerAberto, {|x| x[1] == cPer .AND. (!lSemana .OR. Val(x[2]) == Val(oJsBody["items"][nI]["campoAdicional"]))})
				If nPos > 0 
					cSemana := aPerAberto[nPos,2]
				Else 
					lAbortacPer := .T.
					For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
						oJsResp := JSonObject():new()
						oJsResp['matricula']  := cEmp+cFil+cMat
						oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
						oJsResp['status'] 	  := "E"
						oJsResp['mensagem']   := OemToAnsi(STR0004) //"Período/Semana não encontrados na base de dados ou o período está fechado. Necessário verificar o cadastro de períodos!"
						oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
						oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
						aAdd(oJsResult['resultados'], oJsResp)
					Next nY
					Loop
				EndIf
			EndIf

			lAbortacPer := .F.

			Begin Transaction
				DbSelectArea("RGB")
				DbSetOrder(nInRGB)   
				//Deleta os resultados já enviados no período, pois não temos controle se ocorrer mais envios com resultados diferentes
				If DbSeek(SRA->(RA_FILIAL+RA_PROCES+RA_MAT) + cPer + cSemana + cRot)
					While RGB->(!Eof() .And. RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR == SRA->(RA_FILIAL+RA_PROCES+RA_MAT)+cPer+cSemana+cRot)
						If "AHGORA" $ RGB->RGB_NUMID .And. RGB->(Reclock("RGB", .F.))	
							RGB->(dbDelete())
							RGB->(MsUnlock())	
						EndIf
						RGB->(DbSkip())
					EndDo
				EndIf
				For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) //Resultados
					cPd := cPdOri := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
					cPdTp  := RetValSrv(cPd, SRA->RA_FILIAL, "RV_TIPO") 
					If (lSemPd := Empty(RetValSrv(cPd, SRA->RA_FILIAL, "RV_COD"))) .OR. (cPdTp == "V")
						oJsResp := JSonObject():new()
						oJsResp['matricula']  := cEmp+SRA->RA_FILIAL+SRA->RA_MAT
						oJsResp['rubrica'] 	  := cPdOri
						oJsResp['status'] 	  := "E"
						oJsResp['mensagem']   := If(lSemPd, OemToAnsi(STR0005), OemToAnsi(STR0006)) //"Verba não encontrada na base de dados." ### "Verba configurada como tipo valor, necessário usar uma verba com tipo H(Horas) ou D(Dias)."
						oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
						oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
						aAdd(oJsResult['resultados'], oJsResp)
						Loop
					EndIf 
					dDataRef := If(Empty(Stod(oJsBody["items"][nI]["resultados"][nY]["datafalta"])), sTod(cPeriodo + "01"), Stod(oJsBody["items"][nI]["resultados"][nY]["datafalta"]))
					nHoras   := Val(StrTran(oJsBody["items"][nI]["resultados"][nY]["referencia"], ",", "."))
					lGrava   := .T.
					If DbSeek( SRA->(RA_FILIAL+RA_PROCES+RA_MAT) + cPer + cSemana + cRot + cPd + SRA->RA_CC)
						If RGB->RGB_DTREF == dDataRef .And. "AHGORA" $ RGB->RGB_NUMID 
							RGB->( RecLock( "RGB" , .F. ) )	
							cSeqAux := RGB->RGB_SEQ
						Else
							cSeqAux := " "
							While RGB->(!Eof() .And. RGB_FILIAL+RGB_PROCES+RGB_MAT+RGB_PERIOD+RGB_SEMANA+RGB_ROTEIR+RGB_PD == SRA->(RA_FILIAL+RA_PROCES+RA_MAT)+cPer+cSemana+cRot+cPd)
								If RGB->RGB_DTREF == dDataRef
									cSeqAux := Soma1(cSeqAux)
								EndIf	
								RGB->(DbSkip())
							EndDo
							If RetValSrv(cPd, SRA->RA_FILIAL, "RV_QTDLANC") > cSeqAux
								RGB->( RecLock( "RGB" , .T. ) )
							Else
								lGrava := .F.
								oJsResp := JSonObject():new()
								oJsResp['matricula']  := cEmp+SRA->RA_FILIAL+SRA->RA_MAT
								oJsResp['rubrica'] 	  := cPdOri
								oJsResp['status'] 	  := "E"
								oJsResp['mensagem']   := OemToAnsi(STR0007) //"Verba ultrapassou o limite de lançamentos diários, verificar o cadastro da verba."
								oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
								oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
								aAdd(oJsResult['resultados'], oJsResp)
							EndIf
						EndIf
					Else
						RGB->( RecLock( "RGB" , .T. ) )
						cSeqAux := " "
					EndIf

					If lGrava
						RGB->RGB_FILIAL	:= SRA->RA_FILIAL
						RGB->RGB_MAT 	:= SRA->RA_MAT
						RGB->RGB_CC		:= SRA->RA_CC
						RGB->RGB_PD		:= cPd
						RGB->RGB_HORAS	:= nHoras
						RGB->RGB_SEMANA := cSemana
						RGB->RGB_DTREF	:= dDataRef
						RGB->RGB_TIPO1	:= cPdTp
						RGB->RGB_TIPO2	:= "G"
						RGB->RGB_VALOR	:= 0
						RGB->RGB_PARCEL	:= 0
						RGB->RGB_SEQ	:= cSeqAux
						RGB->RGB_CODFUN := SRA->RA_CODFUNC
						RGB->RGB_DEPTO  := SRA->RA_DEPTO
						RGB->RGB_NUMID  := "AHGORA-"+DtoS(Date())
						If lItemClVl
							RGB->RGB_ITEM := SRA->RA_ITEM
							RGB->RGB_CLVL := SRA->RA_CLVL
						EndIf
						RGB->RGB_PROCES := SRA->RA_PROCES
						RGB->RGB_PERIOD := cPer
						RGB->RGB_ROTEIR := cRot

						RGB->( MsUnLock() )

						oJsResp := JSonObject():new()
						oJsResp['matricula']  := cEmp+SRA->RA_FILIAL+SRA->RA_MAT
						oJsResp['rubrica'] 	  := cPdOri
						oJsResp['status'] 	  := "S"
						oJsResp['mensagem']   := OemToAnsi(STR0008) //"Operação realizada com sucesso!"
						oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
						oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
						aAdd(oJsResult['resultados'], oJsResp)
						
					EndIf
				Next nY  
			End Transaction	
		Else	
			For nY := 1 To Len(oJsBody["items"][nI]["resultados"]) 
				oJsResp := JSonObject():new()
				oJsResp['matricula']  := cEmp+cFil+cMat
				oJsResp['rubrica'] 	  := oJsBody["items"][nI]["resultados"][nY]["rubrica"]
				oJsResp['status'] 	  := "E"
				oJsResp['mensagem']   := OemToAnsi(STR0009) //"Funcionário não localizado na base de dados."
				oJsResp['referencia'] := oJsBody["items"][nI]["resultados"][nY]["referencia"]
				oJsResp['datafalta']  := oJsBody["items"][nI]["resultados"][nY]["datafalta"]
				aAdd(oJsResult['resultados'], oJsResp)
			Next nY
			Loop
		EndIf	
    Next nI

Else
	lAbortaReq := .T.
	oJsBadResp := JSonObject():new()
    oJsBadResp['code']    := CodeRespBadRequest
    oJsBadResp['message'] := OemToAnsi(STR0010) //"Não foi enviado nenhum registro na requisição para ser gravado."
    oRest:setStatusCode(CodeRespBadRequest)
EndIf        

If __cEmpAnt <> cEmpAnt
	self:fEmpReqAhgora(__cEmpAnt, __cFilAnt, cUser, cPass, .T.)
EndIf

If lAbortaReq
	oRest:setResponse(oJsBadResp:toJson())  
Else
	Aadd(oJsResponse['response'], oJsResult)
	oRest:setStatusCode(CodeRespCreated)
	oRest:setResponse(If(Len(oJsResponse["response"][1]["resultados"]) > 0, oJsResponse:toJson(), ""))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getListaAhgora
API de busca dos funcionários para integração com Ahgora

@author	Bruno Costa
@since	02/08/2024
-------------------------------------------------------------------*/
method getListaAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cDtAtual := dToS(Date()) as character
local cSR6Comp as character 
local cCTTComp as character 
local cAcessaSRA as character
local cVldPonto as character
local cLastFil as character
local cDescFun as character
local cStatus0 as character
local cStatus3 as character
local cStatus4 as character
local cStatus6 as character
local cStatus as character
local cDescDe as character
local cDescCC as character
local cDemAte as character
local cDemDe as character
local cAlias as character		
local cQuery as character
local cWhere as character
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nI as numeric
local aCategoria as array    
local aSituacoes as array 
local aBranches as array   
local aParams as array
local aStatus as array
local lHasNext as logical
local lTemSit as logical
local lSitDem as logical

Static __cEmpAntAh := cEmpAnt
Static oStatementFunc

DEFAULT lTemRUM := AliasInDic("RUM")
DEFAULT aResp   := {}

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM
	aCategoria := IIf(jParams:hasProperty("categoria"), StrTokArr(jParams["categoria"], ','), {})
	aSituacoes := IIf(jParams:hasProperty("situacao"), StrTokArr(jParams["situacao"], ','), {})
	aStatus	   := IIf(jParams:hasProperty("tipostatus") .And. !(Len(jParams["tipostatus"]) != 3 .And. Len(jParams["tipostatus"]) != 5), StrTokArr(jParams["tipostatus"], ','), {}) //6 - Integrado; 4 - Aguardando retorno; 3 - Inconsistente; 0 - Não Integrado; 
	cStatus	   := IIf(Empty(aStatus) .And. jParams:hasProperty("tipostatus") .And. Len(jParams["tipostatus"]) == 1, jParams["tipostatus"], "") 
	aBranches  := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams['branchCode'], ','), {}) 
	cDemDe     := IIF(jParams:hasProperty("dataDemissaDe"), StrTran(SubStr(jParams['dataDemissaDe'], 1, 10), "-"), "")
	cDemAte    := IIF(jParams:hasProperty("dataDemissaAte"), StrTran(SubStr(jParams['dataDemissaAte'], 1, 10), "-"), "")
	aParams    := {"dataAdmissaoDe", "dataAdmissaoAte", "branchCode", "matCode", "matCodeAte", "turnoCode", "turnoCodeAte", "codCC", "codCCAte", "codFuncao", "codFuncaoAte", "codDepto", "codDeptoAte", "categoria", "situacao"}
	cAcessaSRA := &("{ || " + ChkRH("GPEA010", "SRA","2") + "}")
	nPage 	   := Val(jParams["page"])
	nPageSize  := Val(jParams["pageSize"])
	nPOrder    := 1
	cVldPonto  := SuperGetMv("MV_INTEGPW",, "1")
	aResp 	   := IIF(Len(aResp) > 0, aResp, IIF(cVldPonto == "3", self:getRespDpto(), {})) 

	For nI := 1 To Len(aStatus) 
		cStatus := ""
		If aStatus[nI] == "6"
			cStatus6 := "6"
		ElseIf aStatus[nI] == "4"
			cStatus4 := "4"	
		ElseIf aStatus[nI] == "3"
			cStatus3 := "3"
		ElseIf aStatus[nI] == "0"
			cStatus0 := "0"
		EndIf	
	Next nI

	For nI := 1 To Len(aSituacoes)
		lTemSit := .T.
		aSituacoes[nI] := IIf(aSituacoes[nI] == "N", " ", aSituacoes[nI])
		If aSituacoes[nI] == "D"
			lSitDem := .T.
		EndIf	
	Next nI

	nStart := (nPage - 1) * nPageSize + 1
	nEnd   :=  nPage * nPageSize + 1

	oResponse["items"] := {}
	
	If oStatementFunc == Nil .Or. __cEmpAntAh <> cEmpAnt .Or. nStart == 1  
		cQuery := "SELECT * FROM ( "
		cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SRA.RA_FILIAL, SRA.RA_MAT ASC) AS LINHA, " 
		cQuery += "SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_NOMECMP, SRA.RA_CIC, SRA.RA_PIS, SRA.RA_CODUNIC, SRA.RA_ADMISSA, SRA.RA_DEMISSA, SRA.RA_TNOTRAB, SRA.RA_NUMCP, SRA.RA_TELEFON, SRA.RA_TPCONTR, SRA.RA_CATEFD, "
		cQuery += "SRA.RA_DEPTO, SRA.RA_CODFUNC, SRA.RA_SEXO, SRA.RA_EMAIL, SRA.RA_RG, SRA.RA_CC, SRA.RA_CATFUNC, SRA.RA_NASC, SRA.RA_HRSMES, SRA.RA_SINDICA, RA_SITFOLH, RUM.RUM_STATUS, RUM.RUM_OPER, RUM.RUM_DATMOV "
		If cVldPonto <> "1"
			cQuery += ", SR9.R9_DESC"
		EndIf
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		If cVldPonto <> "1"
			cQuery += "LEFT JOIN (SELECT R9_FILIAL, R9_MAT, MAX(R_E_C_N_O_) AS MAX_RECNO FROM " + RetSqlName("SR9") + " WHERE R9_CAMPO = 'AHGORA' AND D_E_L_E_T_ = ' ' "
    		cQuery += "GROUP BY R9_FILIAL, R9_MAT) SR9_MAX ON SR9_MAX.R9_FILIAL = SRA.RA_FILIAL AND  SR9_MAX.R9_MAT = SRA.RA_MAT "
			cQuery += "LEFT JOIN "+ RetSqlName("SR9") + " SR9 ON SR9.R9_FILIAL = SR9_MAX.R9_FILIAL AND SR9.R9_MAT = SR9_MAX.R9_MAT AND SR9.R_E_C_N_O_ = SR9_MAX.MAX_RECNO AND SR9.D_E_L_E_T_ = ' '
		EndIf
		If cStatus == "0" .Or. cStatus0 == "0"
			cQuery += "LEFT JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.D_E_L_E_T_ = ' '
			cQuery += "WHERE (RUM.RUM_MAT IS NULL OR (RUM.RUM_MAT IS NOT NULL AND RUM.RUM_TIPO = '1' AND " +;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"', '"+cStatus0+"')", "RUM.RUM_STATUS NOT IN ('3', '4', '6')") + ")) AND SRA.D_E_L_E_T_ = '' "
		ElseIf cStatus $ "3*4*6" .Or. cStatus6 == "6" .OR. cStatus3 == "3" .Or. cStatus4 == "4"
			cQuery += "INNER JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_TIPO = '1' WHERE " +;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"')", "RUM.RUM_STATUS = " + cStatus) + " AND SRA.D_E_L_E_T_ = '' "
		Else
			cQuery += "LEFT JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_TIPO = '1' AND RUM.D_E_L_E_T_ = ' '
			cQuery += " WHERE SRA.D_E_L_E_T_ = '' "
		EndIf

		cWhere := self:getWherePar(jParams) 

		If !Empty(cWhere)
			cQuery += cWhere    
		EndIf

		cQuery  += ") TAB "
		cQuery  += "WHERE LINHA BETWEEN ? AND ?"

		__cEmpAntAh    := cEmpAnt
		cQuery         := ChangeQuery(cQuery) 
		oStatementFunc := FwExecStatement():New(cQuery)
	EndIf

	For nI := 1 To Len(aParams)
		If !Empty(jParams[aParams[nI]]) 
			If(aParams[nI] $ "categoria*situacao*branchCode")
				oStatementFunc:SetIn(nPOrder++, IIf(aParams[nI] == "categoria", aCategoria, IIf(aParams[nI] == "situacao", aSituacoes, aBranches)))
			ElseIf aParams[nI] $ "dataAdmissaoDe*dataAdmissaoAte"
				oStatementFunc:setDate(nPOrder++, SToD(StrTran(SubStr(jParams[aParams[nI]], 1, 10), "-" )))
			Else
				oStatementFunc:SetString(nPOrder++, jParams[aParams[nI]]) 
			EndIf            
		EndIf
	Next nI

	oStatementFunc:SetString(nPOrder++, cValToChar(nStart))
	oStatementFunc:SetString(nPOrder++, cValToChar(nEnd))

	cAlias := oStatementFunc:OpenAlias()

	While !(cAlias)->(Eof())
		If (cAlias)->LINHA > (nEnd - 1)
			lHasNext := .T.
			Exit 
		EndIf   

		If cLastFil != (cAlias)->RA_FILIAL
			If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSRA) 
				(cAlias)->( dbSkip() )
				Loop
			EndIf
			cLastFil := (cAlias)->RA_FILIAL
			cSR6Comp := Rtrim(xFilial("SR6", cLastFil)) 
			cCTTComp := Rtrim(xFilial("CTT", cLastFil))  
		EndIf

		If (!lTemSit .Or. lSitDem) .And. (cAlias)->RA_SITFOLH == "D" .And. !(If(!Empty(cDemDe), (cAlias)->RA_DEMISSA >= cDemDe, .T.) .And. If(!Empty(cDemAte), (cAlias)->RA_DEMISSA <= cDemAte, .T.))
			(cAlias)->(dbSkip())
			Loop
		EndIf 

		If cVldPonto <> "1"
			If cVldPonto == "2" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto
				(cAlias)->(dbSkip())
				Loop
			EndIf

			If cVldPonto == "3" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto e os colaboradores isentos de ponto que são líderes
				If aScan(aResp, {|x| x[1] == cEmpAnt + (cAlias)->RA_FILIAL + (cAlias)->RA_MAT }) == 0 
					(cAlias)->(dbSkip())
					Loop
				EndIf
			EndIf
		EndIf 

		cDescFun := Alltrim(fDesc('SRJ', (cAlias)->RA_CODFUNC, 'RJ_DESC',, (cAlias)->RA_FILIAL))
		cDescCC  := Alltrim(DescCc( (cAlias)->RA_CC, (cAlias)->RA_FILIAL))

		oItem := JsonObject():new()
	
		oItem["statusAhgora"]  := If((cAlias)->RUM_STATUS == "6", "IN", If((cAlias)->RUM_STATUS == "3", "CI", If((cAlias)->RUM_STATUS == "4", "AR", "NI")))
		oItem["matricula"]	   := StrTran(cEmpAnt+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT, " ", "")
		oItem["nome"]		   := RTrim(IIf(!Empty((cAlias)->RA_NOMECMP), (cAlias)->RA_NOMECMP, (cAlias)->RA_NOME))
		oItem["dataAdmissao"]  := self:getDateAhgora((cAlias)->RA_ADMISSA) 
		oItem["dataDemissao"]  := self:getDateAhgora((cAlias)->RA_DEMISSA)	
		oItem["escala_padrao"] := cEmpAnt + cSR6Comp  + (cAlias)->RA_TNOTRAB
		oItem["cargo"] 		   := Rtrim((cAlias)->RA_CODFUNC) + " - " + cDescFun 
		oItem["departamento"]  := If(!Empty((cAlias)->RA_DEPTO), Rtrim((cAlias)->RA_DEPTO) + " - " + cDescDe, "") 
		oItem["centroCusto"]   := cEmpAnt + cCTTComp + RTrim((cAlias)->RA_CC) + " - " + cDescCC
		oItem["codInterno"]	   := cEmpAnt+"|"+(cAlias)->RA_FILIAL+"|"+(cAlias)->RA_MAT
		oItem["tpMovimento"]   := If(Empty((cAlias)->RUM_OPER), "I", (cAlias)->RUM_OPER)
		oItem["dtMovimento"]   := If(Empty((cAlias)->RUM_DATMOV), self:getDateAhgora(cDtAtual), self:getDateAhgora((cAlias)->RUM_DATMOV))

		Aadd(oResponse["items"], oItem)
		
		(cAlias)->(DbSkip())

	EndDo

		(cAlias)->(dbCloseArea())
		oResponse["hasNext"] := lHasNext 

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getAfastamentoAhgora
API de busca dos afastamentos para integração com Ahgora

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getAfastamentoAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cDtAtual := dToS(Date()) as character
local cRCMComp as character
local cAcessaSR8 as character
local cVldPonto as character
local cLastFil as character
local cStatus0 as character
local cStatus3 as character
local cStatus4 as character
local cStatus6 as character
local cStatus as character
local cDemAte as character
local cDemDe as character
local cAlias as character		
local cQuery as character
local cWhere as character
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nI as numeric	
local aCategoria as array    
local aSituacoes as array 
local aBranches as array
local aParams as array 
local lHasNext as logical
local lTemSit as logical
local lSitDem as logical

Static __cEmpAntAf := cEmpAnt
Static oStatementAfast

DEFAULT lTemRUM := AliasInDic("RUM")
DEFAULT aResp   := {}

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM
	aCategoria := IIf(jParams:hasProperty("categoria"), StrTokArr(jParams["categoria"], ','), {})
	aSituacoes := IIf(jParams:hasProperty("situacao"), StrTokArr(jParams["situacao"], ','), {})
	aBranches  := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams['branchCode'], ','), {}) 
	aCategoria := IIf(jParams:hasProperty("categoria"), StrTokArr(jParams["categoria"], ','), {})
	aSituacoes := IIf(jParams:hasProperty("situacao"), StrTokArr(jParams["situacao"], ','), {})
	aStatus	   := IIf(jParams:hasProperty("tipostatus") .And. !(Len(jParams["tipostatus"]) != 3 .And. Len(jParams["tipostatus"]) != 5), StrTokArr(jParams["tipostatus"], ','), {}) //6 - Integrado; 4 - Aguardando retorno; 3 - Inconsistente; 0 - Não Integrado;
	cStatus	   := IIf(Empty(aStatus) .And. jParams:hasProperty("tipostatus") .And. Len(jParams["tipostatus"]) == 1, jParams["tipostatus"], "") 
	aBranches  := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams['branchCode'], ','), {}) 
	cDemDe     := IIF(jParams:hasProperty("dataDemissaDe"), StrTran(SubStr(jParams['dataDemissaDe'], 1, 10), "-"), "")
	cDemAte    := IIF(jParams:hasProperty("dataDemissaAte"), StrTran(SubStr(jParams['dataDemissaAte'], 1, 10), "-"), "")
	aParams    := {"dataAfastDe", "dataAfastAte", "branchCode", "matCode", "matCodeAte", "turnoCode", "turnoCodeAte", "codCC", "codCCAte", "codFuncao", "codFuncaoAte", "codDepto", "codDeptoAte", "categoria", "situacao"}
	cAcessaSR8 := &("{ || " + ChkRH("GPEA240", "SRA","2") + "}")
	nPage 	   := Val(jParams["page"])
	nPageSize  := Val(jParams["pageSize"])
	nPOrder    := 1
	cVldPonto  := SuperGetMv("MV_INTEGPW", .F., "1")
	aResp 	   := IIF(Len(aResp) > 0, aResp, IIF(cVldPonto == "3", self:getRespDpto(), {})) 

	For nI := 1 To Len(aStatus) 
		cStatus := ""
		If aStatus[nI] == "6"
			cStatus6 := "6"
		ElseIf aStatus[nI] == "4"
			cStatus4 := "4"	
		ElseIf aStatus[nI] == "3"
			cStatus3 := "3"
		ElseIf aStatus[nI] == "0"
			cStatus0 := "0"
		EndIf	
	Next nI

	For nI := 1 To Len(aSituacoes)
		lTemSit := .T.
		aSituacoes[nI] := IIf(aSituacoes[nI] == "N", " ", aSituacoes[nI])
		If aSituacoes[nI] == "D"
			lSitDem := .T.
		EndIf	
	Next nI

	nStart := (nPage - 1) * nPageSize + 1
	nEnd   :=  nPage * nPageSize + 1

	oResponse["items"] := {}

	If oStatementAfast == Nil .Or. __cEmpAntAf <> cEmpAnt .Or. nStart == 1
		cQuery := "SELECT * FROM ( "
		cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SRA.RA_FILIAL, SRA.RA_MAT ASC) AS LINHA,"
		cQuery += " SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_NOMECMP, RA_DEMISSA, RA_SITFOLH, SR8.R8_TIPOAFA, SR8.R8_DATAINI, SR8.R8_DATAFIM, RCM.RCM_DESCRI, SR8.R8_SEQ, RUM.RUM_STATUS, RUM.RUM_OPER, RUM.RUM_SEQAFA, RUM_DATMOV " 
		If cVldPonto <> "1"
			cQuery += ", SR9.R9_DESC"
		EndIf
		cQuery += " FROM "+RetSqlName("SRA")+" SRA "
		If cVldPonto <> "1"
			cQuery += "LEFT JOIN (SELECT R9_FILIAL, R9_MAT, MAX(R_E_C_N_O_) AS MAX_RECNO FROM " + RetSqlName("SR9") + " WHERE R9_CAMPO = 'AHGORA' AND D_E_L_E_T_ = ' ' "
    		cQuery += "GROUP BY R9_FILIAL, R9_MAT) SR9_MAX ON SR9_MAX.R9_FILIAL = SRA.RA_FILIAL AND  SR9_MAX.R9_MAT = SRA.RA_MAT "
			cQuery += "LEFT JOIN "+ RetSqlName("SR9") + " SR9 ON SR9.R9_FILIAL = SR9_MAX.R9_FILIAL AND SR9.R9_MAT = SR9_MAX.R9_MAT AND SR9.R_E_C_N_O_ = SR9_MAX.MAX_RECNO AND SR9.D_E_L_E_T_ = ' '
		EndIf
		cQuery += "INNER JOIN " + RetSqlName("SR8") + " SR8 ON " + FWJoinFilial( "SR8", "SRA" ) + " AND SRA.RA_MAT = SR8.R8_MAT AND SR8.D_E_L_E_T_ = '' "
		cQuery += "INNER JOIN " + RetSqlName("RCM") + " RCM ON " + FWJoinFilial( "RCM", "SR8" ) + " AND RCM.RCM_TIPO = SR8.R8_TIPOAFA AND RCM.D_E_L_E_T_ = '' "
		If cStatus == "0" .Or. cStatus0 == "0"
			cQuery += "LEFT JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_SEQAFA = SR8.R8_SEQ AND RUM.D_E_L_E_T_ = ' '
			cQuery += "WHERE (RUM.RUM_MAT IS NULL OR (RUM.RUM_MAT IS NOT NULL AND RUM.RUM_TIPO ='2' AND " +;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"', '"+cStatus0+"')", "RUM.RUM_STATUS NOT IN ('3', '4', '6')") + ")) AND SRA.D_E_L_E_T_ = '' "
		ElseIf cStatus $ "3*4*6" .Or. cStatus6 == "6" .OR. cStatus3 == "3" .Or. cStatus4 == "4"
			cQuery += "INNER JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_TIPO ='2' AND RUM.RUM_SEQAFA = SR8.R8_SEQ WHERE " +;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"')", "RUM.RUM_STATUS = " + cStatus) + " AND SRA.D_E_L_E_T_ = '' "
		Else 
			cQuery += "LEFT JOIN "+RetSqlName("RUM")+" RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_TIPO ='2' AND RUM.RUM_SEQAFA = SR8.R8_SEQ AND RUM.D_E_L_E_T_ = ' '
			cQuery += " WHERE SRA.D_E_L_E_T_ = '' "
		EndIf
		
		cWhere := self:getWherePar(jParams) 

		If !Empty(cWhere)
			cQuery += cWhere    
		EndIf

		cQuery  += ") TAB "
		cQuery  += "WHERE LINHA BETWEEN ? AND ?"

		__cEmpAntAf     := cEmpAnt
		cQuery          := ChangeQuery(cQuery) 
		oStatementAfast := FwExecStatement():New(cQuery)
	EndIf

	For nI := 1 To Len(aParams)
		If !Empty(jParams[aParams[nI]]) 
			If(aParams[nI] $ "categoria*situacao*branchCode")
				oStatementAfast:SetIn(nPOrder++, IIf(aParams[nI] == "categoria", aCategoria, IIf(aParams[nI] == "situacao", aSituacoes, aBranches)))
			ElseIf aParams[nI] == "dataAfastDe"
				oStatementAfast:setDate(nPOrder++, SToD(StrTran(SubStr(jParams["dataAfastAte"], 1, 10), "-" )))
			ElseIf aParams[nI] == "dataAfastAte"
				oStatementAfast:setDate(nPOrder++, SToD(StrTran(SubStr(jParams["dataAfastDe"], 1, 10), "-" )))
				oStatementAfast:SetString(nPOrder++, ' ') 
			Else
				oStatementAfast:SetString(nPOrder++, jParams[aParams[nI]]) 
			EndIf            
		EndIf
	Next nI

	oStatementAfast:SetString(nPOrder++, cValToChar(nStart))
	oStatementAfast:SetString(nPOrder++, cValToChar(nEnd))

	cAlias := oStatementAfast:OpenAlias()

	While !(cAlias)->(Eof())
		If (cAlias)->LINHA > (nEnd - 1)
			lHasNext := .T.
			Exit 
		EndIf   

		If cLastFil != (cAlias)->RA_FILIAL
			If !((cAlias)->RA_FILIAL $ fValidFil()) .Or. !Eval(cAcessaSR8) 
				(cAlias)->( dbSkip() )
				Loop
			EndIf
			cLastFil := (cAlias)->RA_FILIAL
			cRCMComp := Rtrim(xFilial("RCM", cLastFil))
		EndIf

		If (!lTemSit .Or. lSitDem) .And. (cAlias)->RA_SITFOLH == "D" .And. !(If(!Empty(cDemDe), (cAlias)->RA_DEMISSA >= cDemDe, .T.) .And. If(!Empty(cDemAte), (cAlias)->RA_DEMISSA <= cDemAte, .T.))
			(cAlias)->(dbSkip())
			Loop
		EndIf 

		If cVldPonto <> "1"
			If cVldPonto == "2" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto
				(cAlias)->(dbSkip())
				Loop
			EndIf

			If cVldPonto == "3" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto e os colaboradores isentos de ponto que são líderes
				If aScan(aResp, {|x| x[1] == cEmpAnt + (cAlias)->RA_FILIAL + (cAlias)->RA_MAT }) == 0 
					(cAlias)->(dbSkip())
					Loop
				EndIf
			EndIf
		EndIf 

		oItem := JsonObject():new()
		oItem["statusAhgora"] := If((cAlias)->RUM_STATUS == "6", "IN", If((cAlias)->RUM_STATUS == "3", "CI", If((cAlias)->RUM_STATUS == "4", "AR", "NI")))
		oItem["matricula"]    := StrTran(cEmpAnt+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT, " ", "")
		oItem["nome"]         := RTrim(IIf(!Empty((cAlias)->RA_NOMECMP), (cAlias)->RA_NOMECMP, (cAlias)->RA_NOME))
		oItem["motivo"]       := cEmpAnt + cRCMComp + subStr((cAlias)->R8_TIPOAFA + " - " + RTrim((cAlias)->RCM_DESCRI), 1, 50)
		oItem["inicio"]   	  := self:getDateAhgora((cAlias)->R8_DATAINI)
		oItem["fim"]  		  := self:getDateAhgora((cAlias)->R8_DATAFIM) 
		oItem["cod_interno"]  := cEmpAnt+"."+(cAlias)->RA_FILIAL+"."+(cAlias)->RA_MAT+"."+(cAlias)->R8_SEQ 
		oItem["tpMovimento"]  := If(Empty((cAlias)->RUM_OPER), "I", (cAlias)->RUM_OPER)
		oItem["dtMovimento"]  := If(Empty((cAlias)->RUM_DATMOV), self:getDateAhgora(cDtAtual), self:getDateAhgora((cAlias)->RUM_DATMOV))
		
		Aadd(oResponse["items"], oItem)
		
		(cAlias)->(DbSkip())

	EndDo

	(cAlias)->(dbCloseArea())
	oResponse["hasNext"] := lHasNext 

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getAfastExcAhgora
API de busca dos afastamentos excluídos para integração com Ahgora

@author	Bruno Costa
@since	26/08/2024
-------------------------------------------------------------------*/
method getAfastExcAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cRCMComp as character 
local cAcessaSR8 as character
local cLastDesc as character
local cDescAfas as character
local cStatus0 as character
local cStatus3 as character
local cStatus4 as character
local cStatus6 as character
local cStatus as character
local cAlias as character		
local cQuery as character
local cWhere as character
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nI as numeric	
local aCategoria as array    
local aSituacoes as array 
local aBranches as array
local aParams as array
local aKey := {} as array
local lHasNext as logical

Static __cEmpAntAf := cEmpAnt
Static oStAfastExc

DEFAULT lTemRUM := AliasInDic("RUM")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM
	aCategoria := IIf(jParams:hasProperty("categoria"), StrTokArr(jParams["categoria"], ','), {})
	aSituacoes := IIf(jParams:hasProperty("situacao"), StrTokArr(jParams["situacao"], ','), {})
	aBranches  := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams['branchCode'], ','), {}) 
	aCategoria := IIf(jParams:hasProperty("categoria"), StrTokArr(jParams["categoria"], ','), {})
	aSituacoes := IIf(jParams:hasProperty("situacao"), StrTokArr(jParams["situacao"], ','), {})
	aStatus	   := IIf(jParams:hasProperty("tipostatus") .And. !(Len(jParams["tipostatus"]) != 3 .And. Len(jParams["tipostatus"]) != 5), StrTokArr(jParams["tipostatus"], ','), {}) //6 - Integrado; 4 - Aguardando retorno; 3 - Inconsistente; 0 - Não Integrado;
	cStatus	   := IIf(Empty(aStatus) .And. jParams:hasProperty("tipostatus") .And. Len(jParams["tipostatus"]) == 1, jParams["tipostatus"], "") 
	aBranches  := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams['branchCode'], ','), {}) 
	aParams    := {"dataExcDe", "dataExcAte", "branchCode", "matCode", "matCodeAte", "turnoCode", "turnoCodeAte", "codCC", "codCCAte", "codFuncao", "codFuncaoAte", "codDepto", "codDeptoAte", "categoria", "situacao"}
	cAcessaSR8 := &("{ || " + ChkRH("GPEA240", "SRA","2") + "}")
	nPage 	   := Val(jParams["page"])
	nPageSize  := Val(jParams["pageSize"])
	nPOrder    := 1

	For nI := 1 To Len(aStatus) 
		cStatus := ""
		If aStatus[nI] == "6"
			cStatus6 := "6"
		ElseIf aStatus[nI] == "4"
			cStatus4 := "4"	
		ElseIf aStatus[nI] == "3"
			cStatus3 := "3"
		ElseIf aStatus[nI] == "0"
			cStatus0 := "0"
		EndIf	
	Next nI

	For nI := 1 To Len(aSituacoes)
		aSituacoes[nI] := IIf(aSituacoes[nI] == "N", " ", aSituacoes[nI])
	Next nI

	nStart := (nPage - 1) * nPageSize + 1
	nEnd   :=  nPage * nPageSize + 1

	oResponse["items"] := {}

	If oStAfastExc == Nil .Or. __cEmpAntAf <> cEmpAnt .Or. nStart == 1
		cQuery := "SELECT * FROM (SELECT ROW_NUMBER() OVER( ORDER BY  SRA.RA_FILIAL,SRA.RA_MAT ASC) AS LINHA, "
		cQuery += "SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_NOMECMP, RUM.RUM_STATUS, RUM.RUM_OPER, RUM.RUM_CODINT, RUM.RUM_SEQAFA, RUM.RUM_KEY, RUM.RUM_DATMOV "
		cQuery += "FROM "+RetSqlName("SRA")+" SRA " 
		cQuery += "INNER JOIN " + RetSqlName("RUM") + " RUM ON " + FWJoinFilial( "RUM", "SRA" ) + " AND SRA.RA_FILIAL = RUM.RUM_FIL AND SRA.RA_MAT = RUM.RUM_MAT AND RUM.RUM_TIPO ='2' AND  RUM.RUM_OPER = 'E' AND RUM.D_E_L_E_T_ = ' ' "
		If cStatus == "0" .Or. cStatus0 == "0"
			cQuery += "WHERE "+;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"', '"+cStatus0+"')", "RUM.RUM_STATUS NOT IN ('3', '4', '6')") + " AND SRA.D_E_L_E_T_ = '' "
		ElseIf cStatus $ "3*4*6" .Or. cStatus6 == "6" .OR. cStatus3 == "3" .Or. cStatus4 == "4"
			cQuery += "WHERE " +;
				If(!Empty(cStatus6) .Or. !Empty(cStatus3) .Or. !Empty(cStatus4), "RUM.RUM_STATUS IN ('"+cStatus6+"', '"+cStatus3+"', '"+cStatus4+"')", "RUM.RUM_STATUS = " + cStatus) + " AND SRA.D_E_L_E_T_ = '' "
		Else 
			cQuery += "WHERE SRA.D_E_L_E_T_ = '' "
		EndIf
		
		cWhere := self:getWherePar(jParams) 

		If !Empty(cWhere)	
			cQuery += cWhere    
		EndIf
		
		cQuery  += ") TAB "
		cQuery  += "WHERE LINHA BETWEEN ? AND ?"

		__cEmpAntAf     := cEmpAnt
		cQuery          := ChangeQuery(cQuery) 
		oStAfastExc := FwExecStatement():New(cQuery)
	EndIf

	For nI := 1 To Len(aParams)
		If !Empty(jParams[aParams[nI]]) 
			If(aParams[nI] $ "categoria*situacao*branchCode")
				oStAfastExc:SetIn(nPOrder++, IIf(aParams[nI] == "categoria", aCategoria, IIf(aParams[nI] == "situacao", aSituacoes, aBranches)))
			ElseIf aParams[nI] $ "dataExcDe*dataExcAte"
				oStAfastExc:setDate(nPOrder++, SToD(StrTran(SubStr(jParams[aParams[nI]], 1, 10), "-" )))
			Else
				oStAfastExc:SetString(nPOrder++, jParams[aParams[nI]]) 
			EndIf            
		EndIf
	Next nI

	oStAfastExc:SetString(nPOrder++, cValToChar(nStart))
	oStAfastExc:SetString(nPOrder++, cValToChar(nEnd))

	cAlias := oStAfastExc:OpenAlias()
	dbSelectArea(cAlias)

	While !(cAlias)->(Eof())
		If (cAlias)->LINHA > (nEnd - 1)
			lHasNext := .T.
			Exit 
		EndIf   
		
		aKey := StrTokArr((cAlias)->RUM_KEY, '|')
		If cLastDesc != (cAlias)->RA_FILIAL+aKey[5]
			cDescAfas := fDesc( "RCM", aKey[5], "RCM_DESCRI", 50, (cAlias)->RA_FILIAL,, .F. )
			cLastDesc := (cAlias)->RA_FILIAL+aKey[5]
			cRCMComp := Rtrim(xFilial("RCM", (cAlias)->RA_FILIAL))  
		EndIf

		oItem := JsonObject():new()
		oItem["statusAhgora"] := If((cAlias)->RUM_STATUS == "6", "IN", If((cAlias)->RUM_STATUS == "3", "CI", If((cAlias)->RUM_STATUS == "4", "AR", "NI")))
		oItem["matricula"]    := StrTran(cEmpAnt+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT, " ", "")
		oItem["nome"]         := RTrim(IIf(!Empty((cAlias)->RA_NOMECMP), (cAlias)->RA_NOMECMP, (cAlias)->RA_NOME))
		oItem["motivo"]       := cEmpAnt + cRCMComp + aKey[5] + " - " + RTrim(cDescAfas)
		oItem["inicio"]   	  := self:getDateAhgora(RTrim(aKey[6]))
		oItem["fim"]  		  := If(Len(aKey) > 6, self:getDateAhgora(RTrim(aKey[7])), "")
		oItem["cod_interno"]  := cEmpAnt+"."+(cAlias)->RA_FILIAL+"."+(cAlias)->RA_MAT+"."+aKey[4]
		oItem["tpMovimento"]  := (cAlias)->RUM_OPER
		oItem["dtMovimento"]  := self:getDateAhgora((cAlias)->RUM_DATMOV)

		Aadd(oResponse["items"], oItem)
		
		(cAlias)->(DbSkip())

	EndDo

	(cAlias)->(dbCloseArea())
	oResponse["hasNext"] := lHasNext 

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getMatAhgora
Serviço para retornar uma lista de matrículas para filto

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getMatAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local lHasNext as logical
local aFilPar as array

Static __cEmpAntMat := cEmpAnt
Static oStatementMat

cSearch   := Upper(IIf(jParams:hasProperty("filter"), jParams["filter"], ""))
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
nPOrder   := 1

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := If(!Empty(cSearch), 1, (nPage - 1) * nPageSize + 1)
nEnd   := nPage * nPageSize + 1

oResponse["items"] := {}

If oStatementMat == Nil .Or. __cEmpAntMat <> cEmpAnt .Or. nStart == 1 .Or. !Empty(cSearch)
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SRA.RA_FILIAL, SRA.RA_MAT ASC) AS LINHA, "
	cQuery += " SRA.RA_FILIAL, SRA.RA_MAT, SRA.RA_NOME, SRA.RA_NOMECMP "
	cQuery += " FROM "+RetSqlName("SRA")+" SRA "
	cQuery += " WHERE " + If(!Empty(cSearch), "(SRA.RA_MAT = ? OR UPPER(SRA.RA_NOMECMP) LIKE ? OR UPPER(SRA.RA_NOME) LIKE ?) AND ", "") 
	cQuery += "SRA.RA_FILIAL IN (?) AND SRA.D_E_L_E_T_ = ?"
	cQuery += ") TAB "
	cQuery += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAntMat  := cEmpAnt
	cQuery        := ChangeQuery(cQuery) 
	oStatementMat := FwExecStatement():New(cQuery)
EndIf

If !Empty(cSearch)
	oStatementMat:SetString(nPOrder++, cSearch)
	oStatementMat:SetString(nPOrder++, "%"+cSearch+"%")
	oStatementMat:SetString(nPOrder++, "%"+cSearch+"%")
EndIf
oStatementMat:SetIn(nPOrder++, aFilPar)
oStatementMat:SetString(nPOrder++, " ")
oStatementMat:SetString(nPOrder++, cValToChar(nStart))
oStatementMat:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatementMat:OpenAlias()

While !(cAlias)->(Eof())  
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf 
	
	oItem := JSonObject():new()
	oItem["filMat"]	 := EncodeUTF8((cAlias)->RA_FILIAL)
	oItem["matCode"] := EncodeUTF8((cAlias)->RA_MAT)
	oItem["matName"] := EncodeUTF8(RTrim(IIf(!Empty((cAlias)->RA_NOMECMP), (cAlias)->RA_NOMECMP, (cAlias)->RA_NOME)))

	aadd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getCcAhgora
Serviço para retornar uma lista de centro de custo da CTT

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getCcAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nTam as numeric
local nI as numeric
local lHasNext as logical
local aFilPar as array

Static __cEmpAntCC := cEmpAnt
Static oStatementCC

cSearch   := Upper(IIf(jParams:hasProperty("filter"), jParams["filter"], ""))
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
nPOrder   := 1

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := If(!Empty(cSearch), 1, (nPage - 1) * nPageSize + 1)
nEnd   := nPage * nPageSize + 1

oResponse["items"] := {}

If Len(Rtrim(xFilial("CTT"))) > 0
	 nTam := Len(Rtrim(xFilial("CTT")))
	For nI := 1 To Len(aFilPar)
      aFilPar[nI] := SubStr(aFilPar[nI], 1, nTam)
	Next nI
EndIf

If oStatementCC == Nil .Or. __cEmpAntCC <> cEmpAnt .Or. nStart == 1 .Or. !Empty(cSearch)
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY CTT.CTT_FILIAL, CTT.CTT_CUSTO ASC) AS LINHA,
	cQuery += "CTT.CTT_FILIAL, CTT.CTT_CUSTO, CTT.CTT_DESC01
	cQuery += " FROM "+RetSqlName("CTT")+" CTT "
	cQuery += " WHERE CTT.D_E_L_E_T_ = '' AND CTT.CTT_CLASSE = '2' AND CTT.CTT_BLOQ = '2' AND " + If(nTam > 0 , "CTT.CTT_FILIAL IN (?)", "CTT.CTT_FILIAL = ''")
	If !Empty(cSearch) 
		cQuery += " AND (UPPER(CTT.CTT_CUSTO) = ? OR UPPER(CTT.CTT_DESC01) LIKE ?)
	EndIf
	cQuery += ") TAB "
	cQuery += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAntCC  := cEmpAnt
	cQuery       := ChangeQuery(cQuery) 
	oStatementCC := FwExecStatement():New(cQuery)
EndIf

If nTam > 0
	oStatementCC:SetIn(nPOrder++, aFilPar)
EndIf	
If !Empty(cSearch)
	oStatementCC:SetString(nPOrder++, cSearch)
	oStatementCC:SetString(nPOrder++, "%"+cSearch+"%")
EndIf
oStatementCC:SetString(nPOrder++, cValToChar(nStart))
oStatementCC:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatementCC:OpenAlias()

While !(cAlias)->(Eof())  
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf 

	oItem := JSonObject():new()
	oItem["filCC"]  := EncodeUTF8((cAlias)->CTT_FILIAL)
	oItem["codCC"]  := EncodeUTF8((cAlias)->CTT_CUSTO)
	oItem["ccDesc"] := EncodeUTF8(Rtrim((cAlias)->CTT_DESC01))

	aadd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 
	
oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getFuncaoAhgora
Serviço para retornar uma lista de funcões da SRJ

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getFuncaoAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nTam as numeric
local nI as numeric
local lHasNext as logical
local aFilPar as array

Static __cEmpAntFF := cEmpAnt
Static oStatementFF

cSearch   := Upper(IIf(jParams:hasProperty("filter"), jParams["filter"], ""))
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
nPOrder   := 1

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := If(!Empty(cSearch), 1, (nPage - 1) * nPageSize + 1)
nEnd   := nPage * nPageSize + 1

oResponse["items"] := {}

If Len(Rtrim(xFilial("SRJ"))) > 0
	 nTam := Len(Rtrim(xFilial("SRJ")))
	For nI := 1 To Len(aFilPar)
      aFilPar[nI] := SubStr(aFilPar[nI], 1, nTam)
	Next nI
EndIf

If oStatementFF == Nil .Or. __cEmpAntFF <> cEmpAnt .Or. nStart == 1 .Or. !Empty(cSearch)
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SRJ.RJ_FILIAL, SRJ.RJ_FUNCAO ASC) AS LINHA,
	cQuery += " SRJ.RJ_FILIAL, SRJ.RJ_FUNCAO, SRJ.RJ_DESC
	cQuery += " FROM "+RetSqlName("SRJ")+" SRJ "
	cQuery += " WHERE SRJ.D_E_L_E_T_ = '' AND " + If(nTam > 0, "SRJ.RJ_FILIAL IN (?)", "SRJ.RJ_FILIAL = ''")
	If !Empty(cSearch) 
		cQuery += " AND (UPPER(SRJ.RJ_FUNCAO) = ? OR UPPER(SRJ.RJ_DESC) LIKE ?)
	EndIf
	cQuery += ") TAB "
	cQuery += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAntFF  := cEmpAnt
	cQuery       := ChangeQuery(cQuery) 
	oStatementFF := FwExecStatement():New(cQuery)
EndIf

If nTam > 0
	oStatementFF:SetIn(nPOrder++, aFilPar)
EndIf
If !Empty(cSearch)
	oStatementFF:SetString(nPOrder++, cSearch)
	oStatementFF:SetString(nPOrder++, "%"+cSearch+"%")
EndIf
oStatementFF:SetString(nPOrder++, cValToChar(nStart))
oStatementFF:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatementFF:OpenAlias()

While !(cAlias)->(Eof())  
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf 

	oItem := JSonObject():new()
	oItem["filFuncao"]  := EncodeUTF8((cAlias)->RJ_FILIAL)
	oItem["codFuncao"]  := EncodeUTF8((cAlias)->RJ_FUNCAO)
	oItem["funcaoDesc"] := EncodeUTF8(Rtrim((cAlias)->RJ_DESC))

	aadd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 	

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getDeptoAhgora
Serviço para retornar uma lista de departamentos da SQB

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getDeptoAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nTam as numeric
local nI as numeric
local lHasNext as logical
local aFilPar as array

Static __cEmpAntDE := cEmpAnt
Static oStatementDE

cSearch   := Upper(IIf(jParams:hasProperty("filter"), jParams["filter"], ""))
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
nPOrder   := 1

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := If(!Empty(cSearch), 1, (nPage - 1) * nPageSize + 1)
nEnd   := nPage * nPageSize + 1

oResponse["items"] := {}

If Len(Rtrim(xFilial("SQB"))) > 0
	 nTam := Len(Rtrim(xFilial("SQB")))
	For nI := 1 To Len(aFilPar)
      aFilPar[nI] := SubStr(aFilPar[nI], 1, nTam)
	Next nI
EndIf

If oStatementDE == Nil .Or. __cEmpAntDE <> cEmpAnt .Or. nStart == 1 .Or. !Empty(cSearch)
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SQB.QB_FILIAL, SQB.QB_DEPTO ASC) AS LINHA,
	cQuery += " SQB.QB_FILIAL, SQB.QB_DEPTO, SQB.QB_DESCRIC
	cQuery += " FROM "+RetSqlName("SQB")+" SQB "
	cQuery += " WHERE SQB.D_E_L_E_T_ = '' AND " + If(nTam > 0 , "SQB.QB_FILIAL IN (?)", "SQB.QB_FILIAL = ''")
	If !Empty(cSearch) 
		cQuery += " AND (UPPER(SQB.QB_DEPTO) = ? OR UPPER(SQB.QB_DESCRIC) LIKE ?)
	EndIf
	cQuery += ") TAB "
	cQuery += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAntDE  := cEmpAnt
	cQuery       := ChangeQuery(cQuery) 
	oStatementDE := FwExecStatement():New(cQuery)
EndIf

If nTam > 0
	oStatementDE:SetIn(nPOrder++, aFilPar)
EndIf	
If !Empty(cSearch)
	oStatementDE:SetString(nPOrder++, cSearch)
	oStatementDE:SetString(nPOrder++, "%"+cSearch+"%")
EndIf
oStatementDE:SetString(nPOrder++, cValToChar(nStart))
oStatementDE:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatementDE:OpenAlias()

While !(cAlias)->(Eof())  
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf 

	oItem := JSonObject():new()
	oItem["filDepto"]  := EncodeUTF8((cAlias)->QB_FILIAL)
	oItem["codDepto"]  := EncodeUTF8((cAlias)->QB_DEPTO)
	oItem["deptoDesc"] := EncodeUTF8(Rtrim((cAlias)->QB_DESCRIC))

	aadd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 	

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getTurnoAhgora
Serviço para retornar uma lista de turnos da SR6

@author	Bruno Costa
@since	22/08/2024
-------------------------------------------------------------------*/
method getTurnoAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nTam as numeric
local nI as numeric
local lHasNext as logical
local aFilPar as array

Static __cEmpAntDE := cEmpAnt
Static oStatementTU

cSearch   := Upper(IIf(jParams:hasProperty("filter"), jParams["filter"], ""))
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
nPOrder   := 1

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := If(!Empty(cSearch), 1, (nPage - 1) * nPageSize + 1)
nEnd   := nPage * nPageSize + 1

oResponse["items"] := {}

If Len(Rtrim(xFilial("SR6"))) > 0
	 nTam := Len(Rtrim(xFilial("SR6")))
	For nI := 1 To Len(aFilPar)
      aFilPar[nI] := SubStr(aFilPar[nI], 1, nTam)
	Next nI
EndIf

If oStatementTU == Nil .Or. __cEmpAntDE <> cEmpAnt .Or. nStart == 1 .Or. !Empty(cSearch)
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY SR6.R6_FILIAL, SR6.R6_TURNO ASC) AS LINHA,
	cQuery += " SR6.R6_FILIAL, SR6.R6_TURNO, SR6.R6_DESC
	cQuery += " FROM "+RetSqlName("SR6")+" SR6 "
	cQuery += " WHERE SR6.D_E_L_E_T_ = '' AND " + If(nTam > 0, "SR6.R6_FILIAL IN (?)", "SR6.R6_FILIAL = ''")
	If !Empty(cSearch) 
		cQuery += " AND (UPPER(SR6.R6_TURNO) = ? OR UPPER(SR6.R6_DESC) LIKE ?)
	EndIf
	cQuery += ") TAB "
	cQuery += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAntDE  := cEmpAnt
	cQuery       := ChangeQuery(cQuery) 
	oStatementTU := FwExecStatement():New(cQuery)
EndIf

If nTam > 0
	oStatementTU:SetIn(nPOrder++, aFilPar)
EndIf
If !Empty(cSearch)
	oStatementTU:SetString(nPOrder++, cSearch)
	oStatementTU:SetString(nPOrder++, "%"+cSearch+"%")
EndIf
oStatementTU:SetString(nPOrder++, cValToChar(nStart))
oStatementTU:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatementTU:OpenAlias()

While !(cAlias)->(Eof())  
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf 

	oItem := JSonObject():new()
	oItem["filTurno"]  := EncodeUTF8((cAlias)->R6_FILIAL)
	oItem["turnoCode"] := EncodeUTF8((cAlias)->R6_TURNO)
	oItem["turnoDesc"] := EncodeUTF8(Rtrim((cAlias)->R6_DESC))

	aadd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 	

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getOpcoesAhgora
Serviço para retornar um compo com as opções de cateoria e situação

@author	Bruno Costa
@since	22/08/2024
-------------------------------------------------------------------*/
method getOpcoesAhgora() class GPEAhGora
local oItem	as Object
local oResponse := JsonObject():New() as Json
local aCategoria := {} as array
local aSituacao  := {} as array
local nI as numeric

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

dbSelectArea("SX5")
	
If dbSeek(cFilial+"28")
	While !Eof() .AND. SX5->X5_Tabela == "28"
		If Left(SX5->X5_Chave, 1) >= 'A'
			Aadd(aCategoria, {Left(SX5->X5_Chave, 1), Alltrim(X5Descri())})
		EndIf
		dbSkip()
	Enddo
EndIf	

If dbSeek(cFilial+"31")
	While !Eof() .AND. SX5->X5_Tabela == "31"
		Aadd(aSituacao, {Left(SX5->X5_Chave, 1), Alltrim(X5Descri())})
		dbSkip()
	Enddo
EndIf

SX5->(dbCloseArea())
 
oResponse["items"] := JsonObject():New()
oResponse["items"]["sitOptions"] := {}
oResponse["items"]["catOptions"] := {}

For nI := 1 To Len(aSituacao)
	oItem := JsonObject():new()
    oItem["value"] := If(Empty(aSituacao[nI][1]), "N", aSituacao[nI][1])
	oItem["label"] := aSituacao[nI][2]
	aAdd(oResponse["items"]["sitOptions"], oItem)
Next nI

For nI := 1 To Len(aCategoria)
	oItem := JsonObject():new()
    oItem["value"] := aCategoria[nI][1]
	oItem["label"] := aCategoria[nI][2]
	aAdd(oResponse["items"]["catOptions"], oItem)
Next nI

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getDateAhgora
Método para alterar a data para o tipo lido no PO UI

@author	Bruno Costa
@since	06/08/2024
-------------------------------------------------------------------*/
method getDateAhgora(cDataAlt as character) as character class GPEAhgora

local cData as character

If !Empty(cDataAlt)
	cData := Stuff(Stuff(cDataAlt, 5, 0, "-"), 8, 0, "-")
EndIf

return cData

/*-------------------------------------------------------------------
{Protheus.doc} Get getFuncaoAhgora
Método para trocar a categoria pela descrição usada no ahgora

@author	Bruno Costa
@since	07/08/2024
-------------------------------------------------------------------*/
method getCatAhgora(cCatFunc as character, cCatEfd as character, cTpContr as character) as character class GPEAhGora

If cCatEfd == "103"
	return "menoraprendiz"
EndIf

If cTpContr $ "2*3"
	return If(cTpContr == "2", "temporario", "intermitente")
EndIf

Do Case
	Case cCatFunc $ "M*D*I*J*S*T"
		cCatFunc := "CLT"
	Case cCatFunc == "H"
		cCatFunc := "horista"
	Case cCatFunc $ "E*G"
		cCatFunc := "estagiario"
	Case cCatFunc $ "A*P"
		cCatFunc := "autonomo"	
	Case cCatFunc $ "C*6"
		cCatFunc :=	"comissionado"
	Case cCatFunc == "9"
		cCatFunc :=	"pensaovitalicia"	
	OTHERWISE 
		cCatFunc :=	"estatutario"
	EndCase				

return cCatFunc

/*-------------------------------------------------------------------
{Protheus.doc} Get getWherePar
Método que retorna a consulta conforme os parâmetros

@author	Bruno Costa
@since	07/08/2024
-------------------------------------------------------------------*/
method getWherePar( jPar as json) as character class GPEAhGora
    
local cRet as character

If IsInCallStack("GETLISTAAHGORA")
	If !Empty(jPar['dataAdmissaoDe'])
		cRet += " AND SRA.RA_ADMISSA >= ?"
	EndIf
	If !Empty(jPar['dataAdmissaoAte'])
		cRet += "  AND SRA.RA_ADMISSA <= ?"
	EndIf
ElseIf IsInCallStack("GETAFASTEXCAHGORA")
	If !Empty(jPar['dataExcDe'])
		cRet += "  AND RUM.RUM_DATMOV >= ?"
	EndIf 
	If !Empty(jPar['dataExcAte'])
		cRet += "  AND RUM.RUM_DATMOV <= ?"
	EndIf 
Else	 
	If !Empty(jPar['dataAfastDe'])
		cRet += "  AND SR8.R8_DATAINI <= ?"
	EndIf 
	If !Empty(jPar['dataAfastAte'])
		cRet += " AND (SR8.R8_DATAFIM >= ? OR SR8.R8_DATAFIM = ?)"
	EndIf 
EndIf	
If !Empty(jPar['branchCode'])
	cRet += " AND SRA.RA_FILIAL IN (?)"
EndIf
If !Empty(jPar['matCode'])
	cRet += " AND SRA.RA_MAT >= ?"
EndIf
If !Empty(jPar['matCodeAte'])
	cRet += " AND SRA.RA_MAT <= ?"
EndIf
If !Empty(jPar['turnoCode'])
	cRet += " AND SRA.RA_TNOTRAB >= ?"
EndIf
If !Empty(jPar['turnoCodeAte'])
	cRet += " AND SRA.RA_TNOTRAB <= ?"
EndIf
If !Empty(jPar['codCC'])
	cRet += " AND SRA.RA_CC >= ?"
EndIf
If !Empty(jPar['codCCAte'])
	cRet += " AND SRA.RA_CC <= ?"
EndIf
If !Empty(jPar['codFuncao'])
	cRet += " AND SRA.RA_CODFUNC >= ?"
EndIf
If !Empty(jPar['codFuncaoAte'])
	cRet += " AND SRA.RA_CODFUNC <= ?"
EndIf
If !Empty(jPar['codDepto'])
	cRet += " AND SRA.RA_DEPTO >= ?"
EndIf
If !Empty(jPar['codDeptoAte'])
	cRet += " AND SRA.RA_DEPTO <= ?"
EndIf
If !Empty(jPar['categoria'])
	cRet += " AND SRA.RA_CATFUNC IN (?)"
EndIf 
If !Empty(jPar['situacao'])
	cRet += " AND SRA.RA_SITFOLH IN (?)"
EndIf

return cRet

/*-------------------------------------------------------------------
{Protheus.doc} Get fVldChefia
Método que valida as informações da estrutura de departamentos líder do funcionário

@author	Bruno Costa
@since	07/08/2024
-------------------------------------------------------------------*/
method fVldChefia(cCodFil as character, cCodMat as character, cCodDpeto as character) as array class GPEAhGora
local aArea 	:= SRA->(GetArea()) as array
local aDeptos 	:= {} as array
local aSuperior	:= {} as array
local aChefia	:= {} as array
local cVision	:= GetMvRH("MV_APDVIS", .F., "") as character
local cTipoOrg as character
local cEmpResp as character
local cFilResp as character
local cMatResp as character
local nPosDepto	as numeric

DEFAULT aBkpDeptos := {}

DbSelectArea("SQB")
SQB->(dbSetOrder(1)) //QB_FILIAL+QB_DEPTO+QB_DESCRIC

If SQB->(dbSeek(xFilial("SQB", cCodFil) + cCodDpeto ) )
	cEmpResp := SQB->QB_EMPRESP
	cFilResp := SQB->QB_FILRESP
	cMatResp := SQB->QB_MATRESP
	If ( nPosDepto := aScan( aBkpDeptos, { |x| x[1] == SQB->QB_EMPRESP+cCodFil } ) ) > 0
		aDeptos := aClone( aBkpDeptos[nPosDepto, 2] )
	Else
		aDeptos := fEstrutDepto(cCodFil, NIL, NIL, NIL, cCodMat )
		aAdd( aBkpDeptos, { SQB->QB_EMPRESP+cCodFil, aClone(aDeptos) } )
	EndIf

	TipoOrg(@cTipoOrg, cVision)
	aSuperior := fBuscaSuperior(cCodFil, cCodMat, cCodDpeto, aDeptos, cTipoOrg, cVision)
EndIf

If !Empty(aSuperior) .And. !Empty(aSuperior[1, 1]+aSuperior[1, 2])
	aChefia := self:getChefia(aSuperior[1, 9], aSuperior[1, 2], aSuperior[1, 1], aSuperior[1, 7])
ElseIf !Empty(cEmpResp) .And. !Empty(cFilResp) .And. !Empty(cMatResp)
	aChefia := self:getChefia(cEmpResp, cMatResp, cFilResp, cEmpAnt)
EndIf

RestArea(aArea)

return aChefia

/*-------------------------------------------------------------------
{Protheus.doc} Get getChefia
Método que retorna o líder do funcionário

@author	Bruno Costa
@since	07/08/2024
-------------------------------------------------------------------*/
method getChefia(cEmp as character, cMatFunc as character, cFilSRA as character, cEmpFunc as character) as array class GPEAhGora

local cAliasSRA as character
local aRetChefia := {} as array

DEFAULT cLEmpLid := ""

If cEmp != cEmpFunc
	If cLEmpLid != cEmp
		fAbrEmpresa("SRA", 1, cEmp, cFilSRA)
		cLEmpLid := cEmp
	EndIf
	cAliasSRA := "GPESRA"
	If (cAliasSRA)->(dbSeek(xFilial("SRA", cFilSRA) + cMatFunc)) 
		aAdd(aRetChefia, {StrTran(cEmp+cFilSRA+(cAliasSRA)->RA_MAT, " ", ""), RTrim(IIf(!Empty((cAliasSRA)->RA_NOMECMP), (cAliasSRA)->RA_NOMECMP, (cAliasSRA)->RA_NOME)), (cAliasSRA)->RA_EMAIL}) 
	EndIf	
Else
	DbSelectArea("SRA")
	SRA->(dbSetOrder(1))
 	If SRA->(dbSeek(xFilial("SRA", cFilSRA) + cMatFunc)) 
		aAdd(aRetChefia, {StrTran(cEmp+cFilSRA+SRA->RA_MAT, " ", ""), RTrim(IIf(!Empty(SRA->RA_NOMECMP), SRA->RA_NOMECMP, SRA->RA_NOME)), SRA->RA_EMAIL}) 
	EndIf	
EndIf

return aRetChefia

/*-------------------------------------------------------------------
{Protheus.doc} Post postFuncionariosAhgora
API de envio dos funcionários para Ahgora

@author	Bruno Costa
@since	14/08/2024
-------------------------------------------------------------------*/
method postFuncionariosAhgora() class GPEAhGora
local oJsResponse as object
local oJsResp as object
local oJsBody := JsonObject():new() as Json
local aCodInter := {} as array
local aLocalSRA := {} as array
local aLocalBkp := {} as array
local aLocais := {} as array
local aCampos := {} as array
local aKeyFunc as array
local aInfo as array
local cEmailChefia as character
local cNomeChefia as character
local cMatChefia as character
local cLastDepto as character
local cLastCFunc as character
local cBatePonto as character
local cLocalAux as character
local cLastFil as character
local cDescFun as character
local cCatFunc as character
local cDescDe as character
local cMatAnt as character
local cNomeOf as character
local cDtMov as character
local tpMov as character
local cMsn as character
local cFil as character
local cMat as character
local cSR6Comp as character 
local cCTTComp as character 
local cMatSRA as character
local lNomeSoc as logical
local lRet as logical
Local nLoteSize := 1000 as numeric
local nCampos as numeric
local nStart as numeric
local nEnd as numeric
local nSRA as numeric
Local nQtd as numeric
local nI as numeric
local dDHisSind as date
local dDtAltFun as date
local dDtTransf as date
local dDHisSR9 as date
local dDtTurno as date
local uVarAux

Private oRespStatus := JsonObject():new() as object

Static cLEmpLid
Static aBkpDeptos  

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	oRespStatus["items"] := {}
	nQtd := Len(oJsBody["items"]) 

	DbSelectArea("SRA")
	DbSetOrder(1) 

	For nStart := 1 To nQtd Step nLoteSize
		oJsResponse := JsonObject():New()
    	oJsResponse["items"] := {}
    	aCodInter := {}

		nEnd := Min(nStart + nLoteSize - 1, nQtd)
	
		For nI := nStart To nEnd
			aKeyFunc := StrTokArr(oJsBody["items"][nI]["codInterno"], "|")
			cFil     := aKeyFunc[2]
			cMat     := aKeyFunc[3]
			tpMov    := oJsBody["items"][nI]["tpMovimento"]
			cDtMov	 := StrTran(oJsBody["items"][nI]["dtMovimento"], "-", "") 

			If SRA->(DbSeek(cFil+cMat)) 	
				If cLastFil != SRA->RA_FILIAL
					cLastFil  := SRA->RA_FILIAL
					cSR6Comp  := Rtrim(xFilial("SR6", cLastFil))  
					cCTTComp  := Rtrim(xFilial("CTT", cLastFil))
					aLocalSRA := {}  
					fInfo(@aInfo, SRA->RA_FILIAL)
					aLocais   := self:getLocalFun(SRA->RA_FILIAL, @aLocalSRA)
					aLocalBkp := aClone(aLocais)
				EndIf

				If Len(aLocais) > 0
					aLocais := aClone(aLocalBkp)
				EndIf

				For nSRA := 1 To Len(aLocalSRA)
					aCampos := StrTokArr(aLocalSRA[nSRA][3], "|")
					For nCampos := 1 To Len(aCampos)
						uVarAux := SRA->&(aCampos[nCampos])
						IF ValType(uVarAux) == "C"
							cLocalAux += Rtrim(uVarAux)
						ElseIf ValType(uVarAux) == "D"
							cLocalAux += Rtrim(dToS(uVarAux))
						ElseIf ValType(uVarAux) == "N"
							cLocalAux += cValToChar(uVarAux)
						EndIf
					Next nCampos
					If !Empty(cLocalAux)		
						aAdd(aLocais, {cLocalAux})
						cLocalAux := ""
					EndIf	
				Next nSRA
				
				cDescFun := Alltrim(fDesc('SRJ', SRA->RA_CODFUNC, 'RJ_DESC',, SRA->RA_FILIAL))

				If cLastCFunc != SRA->RA_CATFUNC+SRA->RA_CATEFD+SRA->RA_TPCONTR
					cLastCFunc := SRA->RA_CATFUNC+SRA->RA_CATEFD+SRA->RA_TPCONTR
					cCatFunc   := self:getCatAhgora(SRA->RA_CATFUNC, SRA->RA_CATEFD, SRA->RA_TPCONTR)
				EndIf

				If !Empty(SRA->RA_DEPTO) .And. cLastDepto != SRA->RA_FILIAL + SRA->RA_DEPTO
					cMatChefia := cNomeChefia := cEmailChefia := ""
					cLastDepto := SRA->RA_FILIAL + SRA->RA_DEPTO
					aChefia    := self:fVldChefia(SRA->RA_FILIAL, SRA->RA_MAT, SRA->RA_DEPTO)
					cDescDe    := Alltrim(fDesc('SQB', SRA->RA_DEPTO, 'QB_DESCRIC',, SRA->RA_FILIAL))
					If Len(aChefia) > 0
						cMatChefia   := aChefia[1][1]
						cNomeChefia  := RTrim(aChefia[1][2])
						cEmailChefia := RTrim(aChefia[1][3])
					EndIf
				EndIf	

				cMatSRA := StrTran(cEmpAnt+SRA->RA_FILIAL+SRA->RA_MAT, " ", "")
				If cMatChefia == cMatSRA .Or. Empty(SRA->RA_DEPTO)
					cMatChefia := cNomeChefia := cEmailChefia := ""
				EndIf

				dDHisSR9   := dDHisSind := dDtAltFun := dDtTransf := dDtTurno := CtoD("//")
				cBatePonto := self:getAhgoraSR9(Date(), @dDHisSR9, "AHGORA", "1")
				cMatAnt    := ""
				lNomeSoc   := !Empty(SRA->RA_NSOCIAL)
				cNomeOf    := IIF(!Empty(SRA->RA_NOMECMP), SRA->RA_NOMECMP, SRA->RA_NOME)

				If tpMov == "A"
					self:getAhgoraSR9(Date(), @dDHisSind, "RA_SINDICA", "2")
					self:getAltFunc(cDtMov, cEmpAnt+SRA->RA_FILIAL+SRA->RA_MAT, @dDtAltFun, @dDtTransf, @cMatAnt, @dDtTurno)
				EndIf	

				oItem := JsonObject():new()
				oItem["matricula"]			       		:= cMatSRA
				oItem["nome"]				       		:= RTrim(IIf(lNomeSoc, SRA->RA_NSOCIAL, cNomeOf))
				oItem["checkbox_nome_social"]           := lNomeSoc
				If lNomeSoc
					oItem["nome_registro"]				:= RTrim(cNomeOf)
				EndIf 
				oItem["pis"] 				       		:= RTrim(SRA->RA_PIS)
				oItem["matricula_esocial"]         		:= SRA->RA_CODUNIC
				oItem["dataAdmissao"] 		       		:= self:getDateAhgora(DtoS(SRA->RA_ADMISSA)) 
				oItem["dataDemissao"] 		       		:= self:getDateAhgora(DtoS(SRA->RA_DEMISSA))
				If Len(aLocais) > 0
					oItem["localizacoes"]				:= aLocais
				Else
					oItem["localizacoes"]				:= ""
				EndIf
				oItem["escala_padrao"] 			   		:= cEmpAnt + cSR6Comp + SRA->RA_TNOTRAB
				oItem["lastChangeDefaultSchedule"] 		:= self:getDateAhgora(DtoS(dDtTurno)) //Data alteração do turno
				oItem["ctps"] 					   		:= SRA->RA_NUMCP
				oItem["cargo"] 					   		:= Rtrim(SRA->RA_CODFUNC) + " - " + cDescFun 
				oItem["departamento"] 			   		:= If(!Empty(SRA->RA_DEPTO), Rtrim(SRA->RA_DEPTO) + " - " + cDescDe, "") 
				oItem["sexo"] 					   		:= If(SRA->RA_SEXO $ "M*F", SRA->RA_SEXO, "O")  		
				oItem["email"] 					   		:= Rtrim(SRA->RA_EMAIL)
				oItem["cpf"]					   		:= SRA->RA_CIC  
				oItem["rg"]						   		:= SRA->RA_RG
				oItem["cnpj"]					   		:= RTrim(aInfo[08]) 
				oItem["dataCnpj"]				   		:= If(!Empty(cMatAnt), self:getDateAhgora(DtoS(dDtTransf)), "") //Data transferência
				oItem["centroCusto"]	 		   		:= cEmpAnt + cCTTComp + RTrim(SRA->RA_CC)
				oItem["regimeTrabalho"]		       		:= cCatFunc
				oItem["dataNascimento"] 		   		:= self:getDateAhgora(DtoS(SRA->RA_NASC))
				oItem["dataCargo"]						:= self:getDateAhgora(DtoS(dDtAltFun)) //Data Alteração de função
				oItem["carga_horaria"]				    := SRA->RA_HRSMES
				oItem["bate_ponto"]	                    := cBatePonto
				oItem["data_troca_elegibilidade_ponto"]	:= self:getDateAhgora(DtoS(dDHisSR9)) //Data Alteração se bate ponto
				oItem["matricula_chefia"]	            := cMatChefia
				oItem["nome_chefia"]					:= cNomeChefia
				oItem["email_chefia"]	                := cEmailChefia
				oItem["codSindicato"]	                := SRA->RA_SINDICA
				oItem["dataCodSindicato"]				:= self:getDateAhgora(DtoS(dDHisSind)) //Data Alteração do sindicato
				oItem["telefone"]	                    := Rtrim(SRA->RA_TELEFON)
				oItem["sem_pis"]						:= If(Empty(SRA->RA_PIS), "true", "false") 
				oItem["codInterno"]		                := cEmpAnt+"|"+SRA->RA_FILIAL+"|"+SRA->RA_MAT
				oItem["matricula_anterior"]		        := cMatAnt
				oItem["salario"]						:= If(SRA->RA_SALARIO > 0, AllTrim(Transform(SRA->RA_SALARIO, "@E 999,999,999.99")), "")
				oItem["custo_da_hora"]                  := If(SRA->RA_SALARIO > 0, If(SRA->RA_CATFUNC $ "H*T*G", oItem["salario"], AllTrim(Transform(SRA->RA_SALARIO / SRA->RA_HRSMES, "@E 999,999,999.99"))), "")
				
				aAdd(oJsResponse["items"], oItem)
				aAdd(aCodInter, {SRA->RA_FILIAL, SRA->RA_MAT, cEmpAnt+SRA->RA_FILIAL+SRA->RA_MAT, Nil , Nil, "I", oJsBody["items"][nI]["codInterno"]})

			EndIf
		Next nI
		
		If !(lRet := self:integracaoAhgora(oJsResponse, "1", @cMsn, "I", aCodInter))
			Exit  
		EndIf 

	Next nStart

	SRA->(dbCloseArea())
	
	oJsResp := JSonObject():new()
    oJsResp['code']    := If(lRet, CodeRespCreated, CodeRespBadRequest)
    oJsResp['message'] := cMsn
    oRest:setStatusCode(If(lRet, CodeRespCreated, CodeRespBadRequest))
	oRest:setResponse(If(lRet, oRespStatus:toJson(), oJsResp:toJson()))
EndIf        

return 

/*-------------------------------------------------------------------
{Protheus.doc} Post postAfastamentosAhgora
API de envio dos afastamentos para Ahgora

@author	Bruno Costa
@since	15/08/2024
-------------------------------------------------------------------*/
method postAfastamentosAhgora() class GPEAhGora
local oJsResponse as object
local oJsResp as object
local oJsBody := JsonObject():new() as Json
local aCodInter := {} as array
local aKeyFunc as array
local cRCMComp as character 
local cFilLast as character
local cMsn as character
local cFil as character
local cMat as character
local lRet as logical
Local nLoteSize := 1000 as numeric
local nStart as numeric
local nEnd as numeric
Local nQtd as numeric
local nI as numeric

Private oRespStatus := JsonObject():new() as object

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	oRespStatus["items"] := {}
	nQtd := Len(oJsBody["items"]) 

	DbSelectArea("SR8")
	DbSetOrder(2) 

	For nStart := 1 To nQtd Step nLoteSize
		oJsResponse := JsonObject():New()
    	oJsResponse["items"] := {}
    	aCodInter := {}

		nEnd := Min(nStart + nLoteSize - 1, nQtd)
	
		For nI := nStart To nEnd
			aKeyFunc := StrTokArr(oJsBody["items"][nI]["cod_interno"], ".")
			cFil     := aKeyFunc[2]
			cMat     := aKeyFunc[3]
			cSeq     := aKeyFunc[4]

			If cFilLast != cFil
				cFilLast := cFil
				cRCMComp := Rtrim(xFilial("RCM", cFilLast)) 
			EndIf

			If SR8->(DbSeek(cFil+cMat+cSeq)) 	
		
				oItem := JsonObject():new()
				oItem["matricula"]   := StrTran(cEmpAnt+SR8->R8_FILIAL+SR8->R8_MAT, " ", "")
				oItem["motivo"]      := cEmpAnt + cRCMComp + SR8->R8_TIPOAFA
				oItem["inicio"]   	 := self:getDateAhgora(DtoS(SR8->R8_DATAINI))
				oItem["fim"]  		 := If(Empty(SR8->R8_DATAFIM), "2099-12-31", self:getDateAhgora(DtoS(SR8->R8_DATAFIM)))
				oItem["cod_interno"] := oJsBody["items"][nI]["cod_interno"]
				oItem["operation"]   := "INS"

				aAdd(oJsResponse["items"], oItem)
				aAdd(aCodInter, {SR8->R8_FILIAL, SR8->R8_MAT, cEmpAnt+SR8->R8_FILIAL+SR8->R8_MAT+SR8->R8_SEQ, SR8->R8_SEQ, SR8->R8_DATAINI, "I", oJsBody["items"][nI]["cod_interno"]})

			EndIf
		Next nI

		If !(lRet := self:integracaoAhgora(oJsResponse, "2", @cMsn, "I", aCodInter))
			Exit  
		EndIf 

	Next nStart

	SR8->(dbCloseArea())

	oJsResp := JSonObject():new()
    oJsResp['code']    := If(lRet, CodeRespCreated, CodeRespBadRequest)
    oJsResp['message'] := cMsn
    oRest:setStatusCode(If(lRet, CodeRespCreated, CodeRespBadRequest))
	oRest:setResponse(If(lRet, oRespStatus:toJson(), oJsResp:toJson()))
EndIf        

return 

/*-------------------------------------------------------------------
{Protheus.doc} Post postAfastExcAhgora
API de envio das exclusões dos afastamentos para Ahgora

@author	Bruno Costa
@since	26/08/2024
-------------------------------------------------------------------*/
method postAfastExcAhgora() class GPEAhGora
local oJsResponse as object
local oJsResp as object
local oJsBody := JsonObject():new() as Json
local aCodInter := {} as array
local aKeyFunc as array
local cCodInt as character
local cDtMov as character
local cMsn as character
local lRet as logical
Local nLoteSize := 1000 as numeric
local nStart as numeric
local nEnd as numeric
Local nQtd as numeric
local nI as numeric

Private oRespStatus := JsonObject():new() as object

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	oRespStatus["items"] := {}
	nQtd := Len(oJsBody["items"]) 

	DbSelectArea("RUM")
	DbSetOrder(8) 

	For nStart := 1 To nQtd Step nLoteSize
		oJsResponse := JsonObject():New()
    	oJsResponse["items"] := {}
    	aCodInter := {}

		nEnd := Min(nStart + nLoteSize - 1, nQtd)
	
		For nI := nStart To nEnd

			cCodInt := StrTran(oJsBody["items"][nI]["cod_interno"], ".", "")
			cDtMov	:= StrTran(oJsBody["items"][nI]["dtMovimento"], "-", "") 

			If RUM->(dbSeek( xFilial("RUM") +"2"+"E"+cDtMov+cCodInt))
				aKeyFunc := StrTokArr(RUM->RUM_KEY, "|")
				oItem := JsonObject():new()
				oItem["matricula"]   := StrTran(cEmpAnt+RUM->RUM_FIL+RUM->RUM_MAT, " ", "")
				oItem["motivo"]      := cEmpAnt + Rtrim(xFilial("RCM", RUM->RUM_FIL)) + aKeyFunc[5]
				oItem["inicio"]   	 := self:getDateAhgora(RTrim(aKeyFunc[6]))
				oItem["fim"]  		 := If(Len(aKeyFunc) > 6, self:getDateAhgora(RTrim(aKeyFunc[7])), "2099-12-31")
				oItem["cod_interno"] := oJsBody["items"][nI]["cod_interno"]
				oItem["operation"]   := "DEL" 

				aAdd(oJsResponse["items"], oItem)
				aAdd(aCodInter, {RUM->RUM_FIL, RUM->RUM_MAT, cCodInt, aKeyFunc[4], StoD(aKeyFunc[6]), "E", oJsBody["items"][nI]["cod_interno"]})

			EndIf
		Next nI

		If !(lRet := self:integracaoAhgora(oJsResponse, "2", @cMsn, "I", aCodInter))
			Exit  
		EndIf 

	Next nStart	

	RUM->(dbCloseArea())

	oJsResp := JSonObject():new()
    oJsResp['code']    := If(lRet, CodeRespCreated, CodeRespBadRequest)
    oJsResp['message'] := cMsn
    oRest:setStatusCode(If(lRet, CodeRespCreated, CodeRespBadRequest))
	oRest:setResponse(If(lRet, oRespStatus:toJson(), oJsResp:toJson()))
EndIf        

return 

/*-------------------------------------------------------------------
{Protheus.doc} Post integracaoAhgora
Cria body e integra registros para Ahgora

@author	Bruno Costa
@since	19/08/2024
-------------------------------------------------------------------*/
method integracaoAhgora(oJson as json, cTipo as character, cMsn as character, cTpInt as character, aCodInter as array) as logical class GPEAhGora
local oAhgora as object
local oRet as object
local aHeader := {} as array
local cPassAhgora as character
local cAuthAhgora as character
local cUsrAhgora as character
local cRetorno as character
local cSubUniq as character
local cUnique as character
local cError as character
local cBody	as character
local cURL as character
local lRet as logical
local nI as numeric
local nX as numeric

cURL 		:= "https://api.ahgora.com.br"
cUsrAhgora  := AllTrim(GetNewPar("MV_AHGORA1", Space(255)))
cPassAhgora := AllTrim(GetNewPar("MV_AHGORA2", Space(255)))
cAuthAhgora := "Basic " + Encode64(cUsrAhgora + ":" + cPassAhgora)

oAhgora := FwRest():New(cURL)
If cTipo == "1"//"Funcionários"
	oAhgora:SetPath("/people")
ElseIf cTipo == "2"//"Afastamentos"
	oAhgora:SetPath("/absences")
EndIf

aAdd(aHeader, "Authorization: " + cAuthAhgora) 
aAdd(aHeader, "Content-Type: application/json; charset=UTF-8" )
aAdd(aHeader, "User-Agent: Protheus")

If !Empty(oJson["items"])	
	cBody := '['
	If cTipo == "1"
		For nI := 1 To Len(oJson["items"])
			cError := ""
			lRet   := .F.

			cBody += '{'
			cBody += '"matricula": "' + oJson["items"][nI]["matricula"] + '"'
			cBody += ',"nome": "' + oJson["items"][nI]["nome"] + '"'
			cBody += ',"checkbox_nome_social": ' + If(oJson["items"][nI]["checkbox_nome_social"], "true", "false")
			If oJson["items"][nI]["checkbox_nome_social"]
				cBody += ',"nome_registro": "' + oJson["items"][nI]["nome_registro"] + '"'
			EndIf
			cBody += ',"pis": "' + oJson["items"][nI]["pis"] + '"'
			cBody += ',"matricula_esocial": "' + oJson["items"][nI]["matricula_esocial"] + '"'
			cBody += ',"dataAdmissao": "' + oJson["items"][nI]["dataAdmissao"] + '"'
			cBody += ',"dataDemissao": "' + oJson["items"][nI]["dataDemissao"] + '"'
			If !Empty(oJson["items"][nI]["localizacoes"])
				cBody += ',"localizacoes": ['
				For nX := 1 To Len(oJson["items"][nI]["localizacoes"])
					cBody += '"' + oJson["items"][nI]["localizacoes"][nX][1] + '"'
					If nX < Len(oJson["items"][nI]["localizacoes"])
						cBody += ','
					EndIf
				Next nX
				cBody += ']'
			Else 
				cBody += ',"localizacoes": "' + oJson["items"][nI]["localizacoes"] + '"'
			EndIf
			cBody += ',"escala_padrao": "' + oJson["items"][nI]["escala_padrao"] + '"'
			cBody += ',"lastChangeDefaultSchedule": "' + oJson["items"][nI]["lastChangeDefaultSchedule"] + '"'
			cBody += ',"ctps": "' + oJson["items"][nI]["ctps"] + '"'
			cBody += ',"cargo": "' + oJson["items"][nI]["cargo"] + '"'
			cBody += ',"departamento": "' + oJson["items"][nI]["departamento"] + '"'
			cBody += ',"sexo": "' + oJson["items"][nI]["sexo"] + '"'
			cBody += ',"email": "' + oJson["items"][nI]["email"] + '"'
			cBody += ',"cpf": "' + oJson["items"][nI]["cpf"] + '"'
			cBody += ',"rg": "' + oJson["items"][nI]["rg"] + '"'
			cBody += ',"cnpj": "' + oJson["items"][nI]["cnpj"] + '"'
			cBody += ',"dataCnpj": "' + oJson["items"][nI]["dataCnpj"] + '"'
			cBody += ',"centroCusto": "' + oJson["items"][nI]["centroCusto"] + '"'
			cBody += ',"regimeTrabalho": "' + oJson["items"][nI]["regimeTrabalho"] + '"'
			cBody += ',"dataNascimento": "' + oJson["items"][nI]["dataNascimento"] + '"'
			cBody += ',"dataCargo": "' + oJson["items"][nI]["dataCargo"] + '"'
			cBody += ',"carga_horaria": ' + cValToChar(oJson["items"][nI]["carga_horaria"]) 
			If !Empty(oItem["custo_da_hora"])
				cBody += ',"custo_da_hora": "' + oJson["items"][nI]["custo_da_hora"] + '"'
				cBody += ',"financeiro": {'
			    cBody += '"salario": "' + oJson["items"][nI]["salario"] + '"'
				cBody += '}'
			EndIf
			cBody += ',"bate_ponto": "' + oJson["items"][nI]["bate_ponto"] + '"'
			cBody += ',"data_troca_elegibilidade_ponto": "' + oJson["items"][nI]["data_troca_elegibilidade_ponto"] + '"'
			cBody += ',"matricula_chefia": "' + oJson["items"][nI]["matricula_chefia"] + '"'
			cBody += ',"nome_chefia": "' + oJson["items"][nI]["nome_chefia"] + '"'
			cBody += ',"email_chefia": "' + oJson["items"][nI]["email_chefia"] + '"'
			cBody += ',"codSindicato": "' + oJson["items"][nI]["codSindicato"] + '"'
			cBody += ',"dataCodSindicato": "' + oJson["items"][nI]["dataCodSindicato"] + '"'
			cBody += ',"telefone": "' + oJson["items"][nI]["telefone"] + '"'
			cBody += ',"sem_pis": ' + oJson["items"][nI]["sem_pis"]
			cBody += ',"codInterno": "' + oJson["items"][nI]["codInterno"] + '"'
			cBody += ',"matricula_anterior": "' + oJson["items"][nI]["matricula_anterior"] + '"'
			cBody += If(nI < Len(oJson["items"]), '},', '}' )

		Next nI
	Else
		For nI := 1 To Len(oJson["items"])
			cError := ""
			lRet   := .F.
			
			cBody += '{'
			cBody += '"matricula": "' + oJson["items"][nI]["matricula"] + '"'
			cBody += ',"motivo": "' + oJson["items"][nI]["motivo"] + '"'
			cBody += ',"inicio": "' + oJson["items"][nI]["inicio"] + '"'
			cBody += ',"fim": "' + oJson["items"][nI]["fim"] + '"'
			cBody += ',"cod_interno": "' + oJson["items"][nI]["cod_interno"] + '"'
			cBody += ',"operation": "' + oJson["items"][nI]["operation"] + '"'
			cBody += If(nI < Len(oJson["items"]), '},', '}' )

		Next nI

	EndIf	

	cBody	+= ']'
	
	oAhgora:SetPostParams(EncodeUTF8(cBody))
	oAhgora:Post(aHeader)	
	oRet := JsonObject():new()
	FWJsonDeserialize(oAhgora:GetResult(), @oRet)

	If oAhgora:GetResult() <> Nil .And. oAhgora:GetHTTPCode() == "200"
		cRetorno := IIf(DecodeUtf8(oAhgora:GetResult()) <> Nil, DecodeUtf8(oAhgora:GetResult()), oAhgora:GetResult())
		cUnique  := oRet:UNIQUE
		cSubUniq := oRet:SUBUNIQUE
		cMsn     := OemToAnsi(STR0031)
		lRet     := .T.
	ElseIf oAhgora:GetLastError() <> Nil
		cError := If(oAhgora:GetHTTPCode() == "401", OemToAnsi(STR0032), DecodeUtf8(oAhgora:GetLastError()))
		cMsn   := OemToAnsi(STR0033) + cError
    EndIf
	
	If lRet
		self:gravaRUM(cTpInt,,, aCodInter, cTipo, cUnique, cBody, cRetorno, cSubUniq)
	EndIF
	
EndIf

Return lRet

/*-------------------------------------------------------------------
{Protheus.doc} gravaRUM
Metódo para gravar registros na tabela de integração com Ahgora - RUM

@author	Bruno Costa
@since	19/08/2024
-------------------------------------------------------------------*/
method gravaRUM(cTpInt as character, cTab as character, cChave as character, aCodInter as array, cTipo as character, cUnique as character, cBody as character, cRetorno as character, cSubUniq as character, aKeyAfas as array, lDelRes as logical) class GPEAhGora
local oStatus as Object
local nIndex as numeric
local nI as numeric
local cUser 	:= RetCodUsr() as character
local cNameUser := UsrRetName(cUser) as character
local cTpOper as character
local dDtInt as character
local lTab as logical
local lInt as logical
local lPosTunro as logical
local lJob := IsInCallStack("GPEM943") as logical

DEFAULT lTemRUM  := AliasInDic("RUM")
DEFAULT cTab     := ""
DEFAULT cChave   := ""
DEFAULT cUnique  := ""
DEFAULT cTipo    := ""
DEFAULT cBody    := ""
DEFAULT cRetorno := ""
DEFAULT cSubUniq := ""
DEFAULT aKeyAfas := {}
DEFAULT lDelRes  := .F.

lTab := !Empty(cTab)

If lTemRUM .And. Len(aCodInter) > 0

	nIndex := If(IsInCallStack("POSTAFASTEXCAHGORA") .Or. (lJob .And. lDelAfast), 9, 1)
	dDtInt := If(lTab, cToD("//"), Date())

	DbSelectArea("RUM")
	DbSetOrder(nIndex)

	For nI := 1 To Len(aCodInter)
		cTpOper := aCodInter[nI][6] 
		If nIndex == 1
			IF RUM->(dbSeek( xFilial("RUM") + cTipo+cTpInt+aCodInter[nI][3]))
				If cTpOper == "E" 
					If RUM->RUM_STATUS == "6"
						RUM->( RecLock("RUM", .F.) )
						If lDelRes
							cTpOper := "A"
						EndIf
					Else
						RUM->( RecLock("RUM", .F.) )
						RUM->( dbDelete() )
						RUM->( MsUnLock() )
						Loop
					EndIf
				ElseIf !lTab
					cTpOper := RUM->RUM_OPER
					RUM->( RecLock("RUM", .F.) )
				Else
					cTpOper := If(RUM->RUM_STATUS != "6" .And. RUM->RUM_OPER == "I", "I", cTpOper)
					lInt    := RUM->RUM_STATUS == "6" 
					cTab    := If(cTipo == "1" .And. !lInt .And. RUM->RUM_TAB == "SRE", RUM->RUM_TAB, cTab)
					RUM->( RecLock("RUM", .F.) )
				EndIf	
			ElseIf cTpOper != "E"
				cTpOper := "I"
				RUM->( RecLock("RUM", .T.) )
			Else
				Loop	
			EndIf
		Else	
			If RUM->(dbSeek( xFilial("RUM") +cTpOper+dToS(aCodInter[nI][5])+aCodInter[nI][3]))
				RUM->( RecLock("RUM", .F.) )
			Else 
				Loop	
			EndIf	
		EndIf

		RUM->RUM_FIL    := aCodInter[nI][1]
		RUM->RUM_MAT	:= aCodInter[nI][2]
		RUM->RUM_CODINT	:= aCodInter[nI][3]
		RUM->RUM_UNIQUE	:= cUnique
		RUM->RUM_SUBUNQ	:= cSubUniq
		RUM->RUM_TIPO  	:= cTipo
		RUM->RUM_STATUS	:= If(!lTab, "4", "0") 
		If lTab
			RUM->RUM_TAB    := cTab
			RUM->RUM_KEY    := If(Len(aKeyAfas) > 0, aKeyAfas[nI][1], If(Empty(cChave), RUM->RUM_KEY, If(cTab == "SRE", RUM->RUM_KEY, cChave)))
			lPosTunro       := Len(aCodInter[nI]) > 7 .And. !Empty(aCodInter[nI][8])
			RUM->RUM_DATFUN := If(Len(aCodInter[nI]) > 6 .And. !Empty(aCodInter[nI][7]) .And. lInt, aCodInter[nI][7], If(lInt, cToD("//"), RUM->RUM_DATFUN))
			RUM->RUM_DATTUR := If(lPosTunro .And. lInt, aCodInter[nI][8], If(lInt, cToD("//"), If(lPosTunro, aCodInter[nI][8], RUM->RUM_DATTUR)))
		EndIf	
		RUM->RUM_OPER   := If(Empty(cTpOper), cTpInt, cTpOper)
		RUM->RUM_TPINT  := cTpInt
		RUM->RUM_DATINT	:= dDtInt
		RUM->RUM_HORINT	:= If(lTab, "", Time())	
		RUM->RUM_LOTE  	:= cBody
		RUM->RUM_RETORN := cRetorno
		RUM->RUM_STAMSN := cRetorno
		RUM->RUM_USER  	:= cUser + " - " + cNameUser
		If cTipo == "2"
			RUM->RUM_SEQAFA := If(cTpOper == "E", "", aCodInter[nI][4])
			RUM->RUM_DATAIN := aCodInter[nI][5]
		EndIf
		RUM->RUM_DATMOV := If(Empty(RUM->RUM_DATMOV) .Or. lTab, dDataBase, RUM->RUM_DATMOV)

		RUM->( MsUnLock() )

		If !lTab .And. !lJob
			oStatus := JSonObject():new()
			oStatus["codInterno"]   := aCodInter[nI][7]
			oStatus["statusAhgora"] := If(RUM->RUM_STATUS == "6", "IN", If(RUM->RUM_STATUS == "3", "CI", If(RUM->RUM_STATUS == "4", "AR", "NI")))
			aAdd(oRespStatus["items"], oStatus)
		EndIf	
	Next nI	

	RUM->(dbCloseArea())

EndIf

Return

/*-------------------------------------------------------------------
{Protheus.doc} gravaRUMTransf
Metódo para gravar registros da transferência na tabela de integração com Ahgora - RUM

@author	Bruno Costa
@since	16/09/2024
-------------------------------------------------------------------*/
method gravaRUMTransf(cTpInt as character, cTab as character, cChave as character, aCodInter as array, lAborta as logical, lAbrRUM as logical, lRespDp as logical) class GPEAhGora
local cUser 	:= RetCodUsr() as character
local cNameUser := UsrRetName(cUser) as character
local cAliasRUM as character
local cTpOper as character
local dDtInt as character
local lTemRUM as logical
local lTemReg as logical

DEFAULT lAbrRUM := .T.
DEFAULT lRespDp := .F. 

dDtInt := cToD("//")

DbSelectArea("RUM")
DbSetOrder(1)

cTpOper   := aCodInter[1][4] 
cAliasRUM := If(aCodInter[1][7], "GPERUM", "RUM")

If !lRespDp .And. RUM->(dbSeek( xFilial("RUM") + "1"+cTpInt+aCodInter[1][3])) //Se não existir cria como inclusão para não levar matrícula anterior na integração
	If RUM->RUM_STATUS != "6" //Se existir e não estiver integrado na origem aborta
		lAborta := .T.
		RUM->(dbCloseArea())
		Return
	EndIf
	lTemRUM := .T.
EndIf
 
If aCodInter[1][7] //Transferência entre empresas
	RUM->(dbCloseArea()) 
	If !(lAbrRUM := fAbrEmpresa("RUM", 1, aCodInter[1][8], aCodInter[1][1])) //Abre a tabela da outra empresa, caso contrário aborta
		lAborta := .T.
		Return
	EndIf
EndIf

(cAliasRUM)->(dbGoTop())	
lTemReg := !(cAliasRUM)->(dbSeek( xFilial("RUM") + "1"+cTpInt+aCodInter[1][5]))

If lRespDp .And. !lTemReg
	(cAliasRUM)->(dbCloseArea())
	Return
EndIf

(cAliasRUM)->(RecLock((cAliasRUM), lTemReg))

(cAliasRUM)->RUM_FIL    := aCodInter[1][1]
(cAliasRUM)->RUM_MAT	:= aCodInter[1][2]
(cAliasRUM)->RUM_CODINT	:= aCodInter[1][5]
(cAliasRUM)->RUM_UNIQUE	:= ""
(cAliasRUM)->RUM_SUBUNQ	:= ""
(cAliasRUM)->RUM_TIPO  	:= "1"
(cAliasRUM)->RUM_STATUS	:= "0" 
(cAliasRUM)->RUM_TAB    := If(lTemRUM, cTab, "SRA")
(cAliasRUM)->RUM_KEY    := cChave
(cAliasRUM)->RUM_DATFUN := dDtInt
(cAliasRUM)->RUM_DATTUR := dDtInt
(cAliasRUM)->RUM_DATTRA := If(lTemRUM, aCodInter[1][6], dDtInt)
(cAliasRUM)->RUM_OPER   := If(lTemRUM, cTpOper, "I")
(cAliasRUM)->RUM_TPINT  := cTpInt
(cAliasRUM)->RUM_DATINT	:= dDtInt
(cAliasRUM)->RUM_HORINT	:= ""
(cAliasRUM)->RUM_LOTE  	:= ""
(cAliasRUM)->RUM_RETORN := ""
(cAliasRUM)->RUM_STAMSN := ""
(cAliasRUM)->RUM_USER  	:= cUser + " - " + cNameUser
(cAliasRUM)->RUM_DATMOV := dDataBase

(cAliasRUM)->( MsUnLock() )
(cAliasRUM)->(dbCloseArea())

Return

/*-------------------------------------------------------------------
{Protheus.doc} Get getStatusAhgora
Serviço para retornar o status da integração

@author	Bruno Costa
@since	19/08/2024
-------------------------------------------------------------------*/
method getStatusAhgora() class GPEAhGora
local oItem	as Object
local oResponse := JsonObject():New() as json
local jParams   := oRest:getPathParamsRequest() as json
local jQReq     :=  oRest:getQueryRequest() as json
local cCodInt as character

DEFAULT lTemRUM := AliasInDic("RUM")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM .And. Valtype(jParams["codinterno"]) != "U" .And. !Empty(jParams["codinterno"]) 

	DbSelectArea("RUM")
	DbSetOrder(1)

	oResponse["items"] := {}
	
	cCodInt := If(jQReq["cApi"] == "1", StrTran(jParams["codinterno"], "|", ""), StrTran(jParams["codinterno"], ".", ""))

	IF RUM->(dbSeek( xFilial("RUM") + jQReq["cApi"] + "I" + cCodInt))
		oItem := JSonObject():new()
		oItem["status"]  := RUM->RUM_STAMSN
		aAdd(oResponse["items"], oItem)
	Else
		oItem := JSonObject():new()
		oItem["status"]  := "Registro não integrado."
		aAdd(oResponse["items"], oItem)
	EndIf

	RUM->(dbCloseArea())

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
	
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getLoteAhgora
Serviço para retornar o lote da integração

@author	Bruno Costa
@since	19/08/2024
-------------------------------------------------------------------*/
method getLoteAhgora() class GPEAhGora
local oItem	as Object
local oResponse := JsonObject():New() as json
local jParams   := oRest:getPathParamsRequest() as json
local jQReq     :=  oRest:getQueryRequest() as json
local cCodInt as character

DEFAULT lTemRUM := AliasInDic("RUM")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM .And. Valtype(jParams["codinterno"]) != "U" .And. !Empty(jParams["codinterno"]) 

	DbSelectArea("RUM")
	DbSetOrder(1)

	oResponse["items"] := {}

	cCodInt := If(jQReq["cApi"] == "1", StrTran(jParams["codinterno"], "|", ""), StrTran(jParams["codinterno"], ".", ""))

	IF RUM->(dbSeek( xFilial("RUM") + jQReq["cApi"] + "I" + cCodInt))
		oItem := JSonObject():new()
		oItem["lote"] := RUM->RUM_LOTE
		oItem["data"] := self:getDateAhgora(DtoS(RUM->RUM_DATINT)) 
		oItem["hora"] := RUM->RUM_HORINT
		aAdd(oResponse["items"], oItem)
	Else
		oItem := JSonObject():new()
		oItem["status"]  := "Registro não integrado."
		aAdd(oResponse["items"], oItem)
	EndIf

	RUM->(dbCloseArea())

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
	
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} postStatusCompAhgora
Serviço para atualizar todos os status da integração

@author	Bruno Costa
@since	20/08/2024
-------------------------------------------------------------------*/
method postStatusCompAhgora() class GPEAhGora
local oItem	as Object
local oResponse := JsonObject():New() as json
local oJsBody   := JsonObject():new() as Json
local jQReq     := oRest:getQueryRequest() as json
local lRet 		:= .T. as logical
local aCodFunc  := {} as array
local cAliasRUM as character
local cAlias as character
local nPos as numeric
local nI as numeric

Static oStatementStatus
Static oStatementRUM

DEFAULT lTemRUM := AliasInDic("RUM")

oJsBody:fromJson( oRest:GetBodyRequest() )
oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If lTemRUM .And. Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	If oStatementStatus == Nil
		cQuery := "SELECT RUM.RUM_UNIQUE, RUM.RUM_SUBUNQ, MAX(RUM.RUM_DATINT) AS RUM_DATINT, MAX(RUM.RUM_HORINT) AS RUM_HORINT "
		cQuery += "FROM "+RetSqlName("RUM")+" RUM "
		cQuery += "WHERE RUM.RUM_STATUS = '4' AND RUM.RUM_TIPO = ? AND RUM.D_E_L_E_T_ = ' ' "
		cQuery += "GROUP BY RUM.RUM_UNIQUE, RUM.RUM_SUBUNQ, RUM.RUM_STATUS"
		
		cQuery := ChangeQuery(cQuery) 
		oStatementStatus := FwExecStatement():New(cQuery)
	EndIf

    oStatementStatus:SetString(1, jQReq["cApi"])

    cAlias := oStatementStatus:OpenAlias()
    dbSelectArea(cAlias)

    While !(cAlias)->(Eof())
		lRet := self:atualizaStatus(jQReq["cApi"], (cAlias)->RUM_UNIQUE, (cAlias)->RUM_SUBUNQ, (cAlias)->RUM_DATINT+(cAlias)->RUM_HORINT)
		(cAlias)->(DbSkip())
	EndDo	
    
	(cAlias)->(dbCloseArea())

	If lRet
		oResponse["items"] := {}

		For nI := 1 To Len(oJsBody["items"]) 
			aAdd(aCodFunc, {If(jQReq["cApi"] == "1", StrTran(oJsBody["items"][nI]["codInterno"], "|", ""), StrTran(oJsBody["items"][nI]["codInterno"], ".", "")), oJsBody["items"][nI]["codInterno"]})
		Next nI	

		If oStatementRUM == Nil
			cQuery := "SELECT RUM.RUM_STATUS, RUM.RUM_CODINT
			cQuery += "FROM "+RetSqlName("RUM")+" RUM "
			cQuery += "WHERE RUM.RUM_STATUS IN ('6', '3') AND RUM.RUM_TIPO = ? AND RUM.D_E_L_E_T_ = ' ' "
	
			cQuery := ChangeQuery(cQuery) 
			oStatementRUM := FwExecStatement():New(cQuery)
		EndIf
		
		oStatementRUM:SetString(1, jQReq["cApi"])
	    cAliasRUM := oStatementRUM:OpenAlias()
    	dbSelectArea(cAliasRUM)

    	While !(cAliasRUM)->(Eof())
			 nPos := aScan(aCodFunc, { |x| x[1] $ (cAliasRUM)->RUM_CODINT })
             If nPos > 0
				oItem := JSonObject():new()
				oItem["codInterno"]   := If(jQReq["cApi"] == "1", aCodFunc[nPos][2], aCodFunc[nPos][2])
				oItem["statusAhgora"] := If((cAliasRUM)->RUM_STATUS == "6", "IN", If((cAliasRUM)->RUM_STATUS == "3", "CI", If((cAliasRUM)->RUM_STATUS == "4", "AR", "NI")))
				aAdd(oResponse["items"], oItem)
			 EndIf
			(cAliasRUM)->(DbSkip())
		EndDo	
		(cAliasRUM)->(dbCloseArea())
	EndIf
EndIf

If lRet
	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get atualizaStatus
Método para atualizar o status da integração

@author	Bruno Costa
@since	20/08/2024
-------------------------------------------------------------------*/
method atualizaStatus(cTipo as character, cUnique as character, cSubUniq as character, cTempo as character) as logical class GPEAhGora
local oAhgora as object
local oRet as object
local aArea := GetArea() as array
local aMatErros := {} as array
local aHeader := {} as array
local cPassAhgora as character
local cAuthAhgora as character
local cUsrAhgora as character
local cErroMsn as character
local cRetorno as character
local cUnique as character
local cURL as character
local lRet as logical
local lExpired as logical
local lJob := IsInCallStack("GPEM943") as logical
local lProces as logical
local nI as numeric
local nY as numeric
local nPos as numeric

cURL 		:= "https://api.ahgora.com.br"
cUsrAhgora  := AllTrim(GetNewPar("MV_AHGORA1", Space(255)))
cPassAhgora := AllTrim(GetNewPar("MV_AHGORA2", Space(255)))
cAuthAhgora := "Basic " + Encode64(cUsrAhgora + ":" + cPassAhgora)

oAhgora := FwRest():New(cURL)
oAhgora:SetPath("/process/?subunique="+Alltrim(cSubUniq))

aAdd(aHeader, "Accept-Version: v2")
aAdd(aHeader, "Authorization: " + cAuthAhgora) 
aAdd(aHeader, "Content-Type: application/json; charset=UTF-8" )
aAdd(aHeader, "User-Agent: Protheus")

oAhgora:Get(aHeader)

lRet := FWJsonDeserialize(oAhgora:GetResult(), @oRet)
	
If oAhgora:GetResult() <> Nil 
	cRetorno := IIf(DecodeUtf8(oAhgora:GetResult()) <> Nil, DecodeUtf8(oAhgora:GetResult()), oAhgora:GetResult())
	If oAhgora:GetHTTPCode() == "200"
		If ValType(oRet:DATA:ERRORS) != "U" 
			For nI := 1 To Len(oRet:DATA:ERRORS)
				For nY := 1 To Len(oRet:DATA:ERRORS[nI]["MESSAGE"])
					cErroMsn += If(nY == 1 , "", " | ") + DecodeUtf8(oRet:DATA:ERRORS[nI]["MESSAGE"][nY])
				Next nY
				aAdd(aMatErros, {If(cTipo == "1", oRet:DATA:ERRORS[nI]["identifier"], StrTran(oRet:DATA:ERRORS[nI]["identifier"], ".", "")), cErroMsn} )
				cErroMsn := ""
			Next nI
			cUnique := oRet:UNIQUE
			lProces := oRet:STATUS $ "processing"
		EndIf
	ElseIf !(cRetorno $ "Forbidden") .And. oAhgora:GetHTTPCode() == "410" .And. At('Expired', oRet:MESSAGE) > 0
		lExpired := .T.
		cErroMsn := "Código unique "+ Rtrim(cUnique) + " está expirado. Necessário reenviar o registro!"
	EndIf
EndIf

If lRet .And. !lProces
	nPos := 0
	RUM->(DbGoTop())
	RUM->(DbSetOrder(5))

	If RUM->(dbSeek( xFilial("RUM") + cTipo+cSubUniq))
		While RUM->(!Eof() .And. Alltrim(RUM_SUBUNQ) == Alltrim(cSubUniq)) 
			If RUM->RUM_STATUS != "6" .And. RUM->(RecLock("RUM", .F.))
				nPos := aScan(aMatErros, { |x| x[1] $ If(cTipo == "1", StrTran(RUM->RUM_CODINT, " ", ""), RUM->RUM_CODINT) })
				RUM->RUM_STATUS	:= If(nPos > 0 .Or. lExpired, "3", "6")
				RUM->RUM_RETORN := cRetorno
				RUM->RUM_STAMSN := If(nPos > 0, aMatErros[nPos][2], If(lExpired, cErroMsn, "Registro integrado com sucesso"))
				RUM->(MsUnlock())
				If lJob
					aAdd(aLogStatus, {RUM->RUM_FIL, RUM->RUM_MAT, RUM->RUM_STATUS, RUM->RUM_DATAIN, If(nPos > 0, aMatErros[nPos][2], If(lExpired, cErroMsn, ""))} )
				EndIf
			EndIf	
			RUM->(DbSkip())
		EndDo
	EndIf
EndIf

RestArea(aArea)

return lRet

/*-------------------------------------------------------------------
{Protheus.doc} Get getFiliaisAhgora
Serviço para retornar uma lista de filiais da SM0

@author	Bruno Costa
@since	30/08/2024
-------------------------------------------------------------------*/
method getFiliaisAhgora() class GPEAhGora
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as character
local nPageSize as numeric
local nCount as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nI as Numeric
local lHasNext as logical

DEFAULT aSM0 := FwLoadSM0()

cSearch   := IIf(jParams:hasProperty("filter"), jParams["filter"], "")
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := (nPage - 1) * nPageSize + 1
nEnd   :=  nPage * nPageSize

oResponse["items"] := {}

For nI := 1 To Len(aSM0)
	If (aSM0[nI][SM0_GRPEMP] != cEmpAnt)
		LOOP
	EndIf

	If (!Empty(cSearch) .And. !(Upper(cSearch) $ Upper(aSM0[nI][SM0_CODFIL])) .And. !(Upper(cSearch) $ Upper(aSM0[nI][SM0_NOMRED])))
		LOOP
	EndIf

	nCount++
	If nCount < nStart
		LOOP
	EndIf
	
	If nCount > nEnd .And. Empty(cSearch)
		lHasNext := .T.
		Exit
	EndIf

	oItem := JSonObject():new()
	oItem["branchCode"] := EncodeUTF8(AllTrim(aSM0[nI][SM0_CODFIL]))
	oItem["branchName"] := EncodeUTF8(AllTrim(aSM0[nI][SM0_NOMRED]))

	aadd(oResponse["items"], oItem)

Next nI

oResponse["hasNext"] := lHasNext 

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getFilialAhgora
Serviço para retornar uma filial na digitação do lookup

@author	Bruno Costa
@since	30/08/2024
-------------------------------------------------------------------*/
method getFilialAhgora() class GPEAhGora
local oItem	as Object
local jFil      := oRest:getPathParamsRequest() as json
local oResponse := JsonObject():New() as Json
local cGetFil as character
local nPos as Numeric

DEFAULT aSM0 := FwLoadSM0()

cGetFil := IIf(jFil:hasProperty("filial"), jFil["filial"], "")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

oResponse["items"] := {}

If !Empty(cGetFil)
	If Len(aSM0) > 0
		nPos := aScan( aSM0, { |x| x[1] == cEmpAnt .And. Upper(AllTrim(x[2])) == Upper(AllTrim(cGetFil))} )
		If nPos > 0
			oItem := JSonObject():new()
			oItem["branchCode"]	:= EncodeUTF8(AllTrim(aSM0[nPos][SM0_CODFIL]))
			oItem["branchName"]	:= EncodeUTF8(AllTrim(aSM0[nPos][SM0_NOMRED]))
			aadd(oResponse["items"], oItem)
		EndIf
	EndIf
EndIf

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getAhgoraSR9
Método que retorna se o funcionário tem ponto obrigatório ou ponto livre

@author	Bruno Costa
@since	30/08/2024
-------------------------------------------------------------------*/
method getAhgoraSR9(dDataAlt as date, dDtAltRet as date, cCampo as character, cTipo as character) as character class GPEAhGora
local aArea := GetArea() as array
local cPontoAhgora as character 
Local cConteudo as character
local lAchou as logical

dbSelectArea("SR9")

SR9->( dbSetOrder(1) )
SR9->( DbGoTop() )
SR9->( dbSeek(SRA->RA_FILIAL + SRA->RA_MAT + cCampo) )

While SR9->( !Eof() .And. AllTrim(R9_FILIAL + R9_MAT + R9_CAMPO) == AllTrim(SRA->RA_FILIAL + SRA->RA_MAT + cCampo))
	If dDataAlt >= SR9->R9_DATA
		lAchou 	  := .T.
		cConteudo := AllTrim(SR9->R9_DESC)
		dDtAltRet := SR9->R9_DATA
	EndIf		
	SR9->(dbSkip())
EndDo

If cTipo == "1"
	cPontoAhgora := If(lAchou .And. cConteudo == "2", "Ponto Livre", "Ponto Obrigatorio")
EndIf

RestArea(aArea)

Return cPontoAhgora

/*-------------------------------------------------------------------
{Protheus.doc} getAltFunc
Método que retorna as alterações do funcionário para integração

@author	Bruno Costa
@since	02/09/2024
-------------------------------------------------------------------*/
method getAltFunc(cDtMov as character, cCodInt as character, dDtFunc as date, dDtTransf as date, cMatAnt as character, dDtTurno as date) as logical class GPEAhGora
local aArea := GetArea() as array

dbSelectArea("RUM")
RUM->( dbSetOrder(8) )

If RUM->(dbSeek( xFilial("RUM") +"1"+"A"+cDtMov+cCodInt))
	If !Empty(RUM->RUM_DATFUN)
		dDtFunc := RUM->RUM_DATFUN
	EndIf
	If !Empty(RUM->RUM_DATTRA)
		dDtTransf := RUM->RUM_DATTRA
		cMatAnt   := StrTran(StrTran(SubStr(RUM->RUM_KEY, 1, At("SRE", RUM->RUM_KEY) - 1), "|", ""), " ", "")
	EndIf
	If !Empty(RUM->RUM_DATTUR)
		dDtTurno := RUM->RUM_DATTUR
	EndIf
EndIf

RestArea(aArea)

Return .T.

/*-------------------------------------------------------------------
{Protheus.doc} Get getLocalizacoes
Serviço para retornar uma lista de localizações da RUL

@author	Bruno Costa
@since	09/12/2024
-------------------------------------------------------------------*/
method getLocalizacoes() class GPEAhGora
local oJsResp as object
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cSearch as Character
local cAlias as Character		
local cQuery as Character 
local cAllFil := Replicate("*", FWGETTAMFILIAL) as character
local nPageSize as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local lHasNext as logical
local aFilPar as array

Static oStLocal

DEFAULT lTemRUL := AliasInDic("RUL")

cSearch   := IIf(jParams:hasProperty("filter"), jParams["filter"], "")
aFilPar   := IIf(jParams:hasProperty("branchCode"), StrTokArr(jParams["branchCode"], ","), {})
nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

nStart := (nPage - 1) * nPageSize + 1
nEnd   :=  nPage * nPageSize + 1

oResponse["items"] := {}

If lTemRUL
	If oStLocal == Nil .Or. nStart == 1
		cQuery := "SELECT * FROM ( "
		cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY RUL.RUL_FILIAL ASC) AS LINHA,"
		cQuery += " RUL.RUL_FILIAL, RUL.RUL_USAALL, RUL.RUL_IDLOC, RUL.RUL_LOCALI, RUL.RUL_USASRA"
		cQuery += " FROM "+RetSqlName("RUL") + " RUL "
		cQuery += " WHERE RUL.D_E_L_E_T_ = ?"
		cQuery += ") TAB "
		cQuery += "WHERE LINHA BETWEEN ? AND ?"

		cQuery   := ChangeQuery(cQuery) 
		oStLocal := FwExecStatement():New(cQuery)
	EndIf

	oStLocal:SetString(1, " ")
	oStLocal:SetString(2, cValToChar(nStart))
	oStLocal:SetString(3, cValToChar(nEnd))

	cAlias := oStLocal:OpenAlias()

	While !(cAlias)->(Eof())  
		If (cAlias)->LINHA > (nEnd - 1)
			lHasNext := .T.
			Exit 
		EndIf 

		oItem := JSonObject():new()
		oItem["branchCode"]  := If((cAlias)->RUL_USAALL == "1", cAllFil, EncodeUTF8((cAlias)->RUL_FILIAL))
		oItem["id"] 		 := EncodeUTF8((cAlias)->RUL_IDLOC)
		oItem["localizacao"] := Rtrim((cAlias)->RUL_LOCALI)
		oItem["usaBranch"]   := If((cAlias)->RUL_USAALL == "1", "Sim", "Não")

		aAdd(oResponse["items"], oItem)
		
		(cAlias)->(DbSkip())
	EndDo

	(cAlias)->(dbCloseArea())

	oResponse["hasNext"] := lHasNext 	
EndIf

If !lTemRUL 
	oJsResp := JSonObject():new()
	oJsResp['code']    := CodeRespBadRequest
	oJsResp['message'] := STR0011 //"Tabela RUL não existe na base de dados. Contate o administrador do sistema!"
	oRest:setStatusCode(CodeRespBadRequest)
	oRest:setResponse(EncodeUTF8(FwCutOff(oJsResp:toJson())))
Else
	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} postLocalizacao
Serviço para gravar a localização na RUL

@author	Bruno Costa
@since	10/12/2024
-------------------------------------------------------------------*/
method postLocalizacao() class GPEAhGora
local oJsResp as object
local oJsBody   := JsonObject():new() as Json
local cAliasRUL := GetNextAlias() as character
local cUser as character
local cNUser as character
local cFilLocal as character
local cLivre as character
local cCampos as character
local cLocali as character
local cWhere as character
local cFilQry as character
local lLivre as logical
local lGrv as logical
local lTemLocal as logical
local lCriaAlt as logical
local nI as numeric
local aFilial := {} as array
local dDtAt := Date() as date

DEFAULT lTemRUL := AliasInDic("RUL")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If lTemRUL .And. Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0   

lCriaAlt := oJsBody["items"][1]["criaAlteracao"]
cUser    := RetCodUsr()
cNUser   := UsrRetName(cUser)
	
	Begin Transaction

		If !Empty(oJsBody["items"][1]["livre"])
			cLivre := Alltrim(DecodeUTF8(oJsBody["items"][1]["livre"]))
			lLivre := .T.
		Else
			cCampos := oJsBody["items"][1]["campos"]
		EndIf

		If Len(oJsBody["items"][1]["branchCode"]) > 0 
			For nI := 1 To Len(oJsBody["items"][1]["branchCode"]) 
				cFilQry += oJsBody["items"][1]["branchCode"][nI]
			Next nI

			cLocali := If(lLivre, cLivre, cCampos) 
			cWhere  := "%" 
			cWhere  += "(RUL.RUL_FILIAL IN (" + fSqlIn(cFilQry, FwGetTamFilial) + ") OR "
			cWhere  += "RUL.RUL_USAALL = '1') AND "
			cWhere  += "RUL.RUL_LOCALI = '" + If(lLivre, cLivre, cCampos) + "'
			cWhere  += "%"

			BeginSql Alias cAliasRUL
				SELECT RUL_FILIAL, RUL_USAALL
				FROM %table:RUL% RUL
				WHERE %exp:cWhere%
				AND RUL.%notDel%
			EndSql

			While (cAliasRUL)->(!Eof())
				If (cAliasRUL)->RUL_USAALL == "1"
					lTemLocal := .T.
					Exit 
				EndIf
				aAdd(aFilial, {(cAliasRUL)->RUL_FILIAL})
				(cAliasRUL)->(dbSkip())
			EndDo

			(cAliasRUL)->(DbCloseArea()) 
			
			If !lTemLocal
				
				DbSelectArea("RUL")
				
				For nI := 1 To Len(oJsBody["items"][1]["branchCode"]) 
					cFilLocal := oJsBody["items"][1]["branchCode"][nI]
					If aScan(aFilial, { |x| x[1] == oJsBody["items"][1]["branchCode"][nI]}) == 0 .And. RUL->(RecLock("RUL", .T.))
						lGrv := .T.
						RUL->RUL_FILIAL := cFilLocal
						RUL->RUL_IDLOC  := self:getIdLocal()
						RUL->RUL_USAALL := "2"
						RUL->RUL_LOCALI := If(lLivre, cLivre, cCampos)
						RUL->RUL_USASRA := If(lLivre, "2", "1")
						RUL->RUL_DATINC := dDtAt
						RUL->RUL_HORINC := Time()
						RUL->RUL_USER  	:= cUser + " - " + cNUser
					EndIf
				Next nI 

				lTemLocal := If(!lGrv, .T., .F.) 
			EndIf	
		
		Else
			cWhere := "%RUL.RUL_USAALL = '1' AND RUL.RUL_LOCALI = '" + If(lLivre, cLivre, cCampos) + "'%"

			BeginSql alias cAliasRUL
				SELECT RUL_LOCALI 
				FROM %table:RUL% RUL
				WHERE %exp:cWhere%
				AND RUL.%NotDel% 
			EndSql

			If (cAliasRUL)->( !Eof() )
				lTemLocal := .T.
			Else
				DbSelectArea("RUL")
				
				If RUL->(RecLock("RUL", .T.))
					lGrv := .T.	
					RUL->RUL_FILIAL := cFilAnt
					RUL->RUL_IDLOC  := self:getIdLocal()
					RUL->RUL_USAALL := "1"
					RUL->RUL_LOCALI := If(lLivre, cLivre, cCampos)
					RUL->RUL_USASRA := If(lLivre, "2", "1")
					RUL->RUL_DATINC := dDtAt
					RUL->RUL_HORINC := Time()
					RUL->RUL_USER  	:= cUser + " - " + cNUser
					RUL->(MsUnLock())
				EndIf
			EndIf	 
			(cAliasRUL)->(DbCloseArea()) 
		EndIf

		RUL->(dbCloseArea())

	End Transaction
EndIf

If !lTemRUL
	oJsResp := JSonObject():new()
	oJsResp['code']    := CodeRespBadRequest
	oJsResp['message'] := STR0011 //"Tabela RUL não existe na base de dados. Contate o administrador do sistema!"
	oRest:setStatusCode(CodeRespBadRequest)
	oRest:setResponse(EncodeUTF8(FwCutOff(oJsResp:toJson())))
Else
	If lGrv .And. lCriaAlt
		self:criaAltRUM(oJsBody["items"][1]["branchCode"])
	EndIf
	oJsResp := JSonObject():new()
	oJsResp['code']    := If(lGrv .Or. !lTemLocal, CodeRespCreated, CodeRespBadRequest)
	oJsResp['message'] := If(lGrv .Or. !lTemLocal, STR0012, STR0013) //"Localização gravada com sucesso."###"Localização já existe na base de dados."
	oRest:setStatusCode(If(lGrv .Or. !lTemLocal, CodeRespCreated, CodeRespBadRequest))
	oRest:setResponse(EncodeUTF8(FwCutOff(oJsResp:toJson())))
EndIf
return

/*-------------------------------------------------------------------
{Protheus.doc} Get getCamposSRA
Serviço para retornar os campos da SRA

@author	Bruno Costa
@since	09/12/2024
-------------------------------------------------------------------*/
method getCamposSRA() class GPEAhGora
local oItem	as Object 
local oResponse := JsonObject():New() as Json
local aCamposSRA := {} as array
local nI as numeric

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

aCamposSRA := FWSX3Util():GetAllFields("SRA", .F.) 

oResponse["items"] := {}

For nI := 1 To Len(aCamposSRA)
	If !aCamposSRA[nI] == "RA_BITMAP"
		oItem := JsonObject():new() 
		oItem["value"] := aCamposSRA[nI]
		oItem["label"] := FWSX3Util():GetDescription(aCamposSRA[nI]) + " - (" + aCamposSRA[nI] + ")"
		aadd(oResponse["items"], oItem)
	EndIf	
Next nI

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getIdLocal
Método para criar o ID do local

@author	Bruno Costa
@since	11/12/2024
-------------------------------------------------------------------*/
method getIdLocal() as character class GPEAhGora
local cIdReg as character

cIdReg := DtoS(Date()) + StrZero(Val(StrTran(AllTrim(Str(Seconds())), ".", "")), 8, 0)

Return cIdReg

/*-------------------------------------------------------------------
{Protheus.doc} Get delLocalizacoes
Serviço para exclusão das localizações

@author	Bruno Costa
@since	11/12/2024
-------------------------------------------------------------------*/
method delLocalizacoes() class GPEAhGora
local oJsResp as object
local oJsBody := JsonObject():new() as Json
local lDel as logical
local lVldAll as logical
local lCriaAlt as logical
local nI as numeric
local usaFil as character
local aLocalFil := {} as array

DEFAULT lTemRUL := AliasInDic("RUL")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oJsBody:fromJson(oRest:GetBodyRequest())

If lTemRUL .And. Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	lCriaAlt := If(oJsBody:hasProperty("criaAlteracao"), oJsBody["criaAlteracao"], .F.)

	DbSelectArea("RUL")
	RUL->(dbSetOrder(5))

	For nI := 1 To Len(oJsBody["items"])
		usaFil := If(oJsBody["items"][nI]["usaBranch"] == "Sim", "1", "2")
		If RUL->(dbSeek(oJsBody["items"][nI]["id"] + usaFil + DecodeUTF8(oJsBody["items"][nI]["localizacao"]))) .And. RUL->(Reclock("RUL", .F.))
			If aScan(aLocalFil, RUL->RUL_FILIAL ) == 0
				aAdd(aLocalFil, RUL->RUL_FILIAL)
			EndIf
			If usaFil == "1"
				lVldAll := .T.
			EndIf
			RUL->(dbDelete())
			RUL->(MsUnlock())
			lDel := .T.
		EndIf
	Next nI

	RUL->(dbCloseArea())

EndIf

If lDel .And. lCriaAlt .And. Len(aLocalFil) > 0
	self:criaAltRUM(aLocalFil, lVldAll)
EndIf

oJsResp := JSonObject():new()
oJsResp['code']    := If(lDel, CodeRespCreated, CodeRespBadRequest)
oJsResp['message'] := If(lDel, STR0014, STR0015) //"Exclusão realizada com sucesso."###"Não foi possível realizar a exclusão."
oRest:setStatusCode(If(lDel, CodeRespCreated, CodeRespBadRequest))
oRest:setResponse(EncodeUTF8(FwCutOff(oJsResp:toJson())))

Return 

/*-------------------------------------------------------------------
{Protheus.doc} Get getLocalFun
Metódo para buscar a localização do funcionário na integração 

@author	Bruno Costa
@since	12/11/2024
-------------------------------------------------------------------*/
method getLocalFun(cFilFunc as character, aLocalSRA as array) as array class GPEAhGora
local cQuery as character
local cAlias as Character
local aLocais := {} as array

Static oStLocalFun

DEFAULT lTemRUL := AliasInDic("RUL")

If lTemRUL

	If oStLocalFun == Nil
		cQuery := "SELECT RUL.RUL_FILIAL, RUL.RUL_USAALL, RUL.RUL_IDLOC, RUL.RUL_LOCALI, RUL.RUL_USASRA FROM "+RetSqlName("RUL") + " RUL "
		cQuery += "WHERE (RUL.RUL_FILIAL = ? OR RUL.RUL_USAALL = ?) AND RUL.D_E_L_E_T_ = ?  AND NOT EXISTS ( SELECT 1 FROM "+RetSqlName("RUL") + " R2 "
      	cQuery += "WHERE LOWER(R2.RUL_LOCALI) = LOWER(RUL.RUL_LOCALI) AND R2.R_E_C_N_O_ > RUL.R_E_C_N_O_  AND (R2.RUL_FILIAL = ? OR R2.RUL_USAALL = ?) AND R2.D_E_L_E_T_ = ?)

		cQuery   := ChangeQuery(cQuery) 
		oStLocalFun := FwExecStatement():New(cQuery)
	EndIf

	oStLocalFun:SetString(1, cFilFunc)
	oStLocalFun:SetString(2, "1")
	oStLocalFun:SetString(3, " ")
	oStLocalFun:SetString(4, cFilFunc)
	oStLocalFun:SetString(5, "1")
	oStLocalFun:SetString(6, " ")

	cAlias := oStLocalFun:OpenAlias()

	While !(cAlias)->(Eof())  
		If (cAlias)->RUL_USASRA == "1"
			aAdd(aLocalSRA, {(cAlias)->RUL_USAALL, (cAlias)->RUL_USASRA, Rtrim((cAlias)->RUL_LOCALI)})
		Else
			aAdd(aLocais, {Rtrim((cAlias)->RUL_LOCALI)})
		EndIf
		(cAlias)->(DbSkip())
	EndDo

	(cAlias)->(dbCloseArea())
EndIf

Return aLocais

/*-------------------------------------------------------------------
{Protheus.doc} Get criaAltRUM
Cria alteração no registro do funcionário na RUM na criação/exclusão da Localização

@author	Bruno Costa
@since	17/12/2024
-------------------------------------------------------------------*/
method criaAltRUM(aLocalFil as array, lVldAll as logical) class GPEAhGora
local cWhere as character
local cFilQry as character
local cAlias := GetNextAlias() as character
local aCodInt := {} as array
local nI as numeric

DEFAULT lVldAll := .F.

cWhere := "%" 

For nI := 1 To Len(aLocalFil)
	cFilQry += aLocalFil[nI]
Next nI

If !Empty(cFilQry) .And. !lVldAll
	cWhere  += "RUM.RUM_FIL IN (" + fSqlIn(cFilQry, FwGetTamFilial) + ") AND "
EndIf
cWhere += "RUM.RUM_TIPO = '1' AND RUM.RUM_STATUS = '6'%"

BeginSql Alias cAlias
	SELECT RUM_FIL, RUM_MAT, RUM_CODINT
	FROM %table:RUM% RUM
	WHERE %exp:cWhere%
	AND RUM.%notDel%
EndSql

While !(cAlias)->(Eof())  
	aAdd(aCodInt, {(cAlias)->RUM_FIL, (cAlias)->RUM_MAT, Rtrim((cAlias)->RUM_CODINT), Nil, Nil, "A" })
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())

If Len(aCodInt) > 0
	self:gravaRUM("I", "SRA", "", aCodInt, "1")
EndIf

Return 

/*-------------------------------------------------------------------
{Protheus.doc} Get getRespDpto
Busca os responsáveis pelos departamentos

@author	Bruno Costa
@since	26/03/2025
-------------------------------------------------------------------*/
method getRespDpto() as array class GPEAhGora
local cWhere as character
local cAliasSQB := GetNextAlias() as character

DEFAULT aResp := {}

cWhere := "%SQB.QB_FILRESP <> ' ' AND SQB.QB_MATRESP <> ' ' AND SQB.QB_EMPRESP = '" + cEmpAnt + "'%"  

BeginSql Alias cAliasSQB
	SELECT QB_EMPRESP, QB_FILRESP, QB_MATRESP
	FROM %table:SQB% SQB
	WHERE %exp:cWhere%
	AND SQB.%notDel%
EndSql

While !(cAliasSQB)->(Eof())  
	aAdd(aResp, {(cAliasSQB)->QB_EMPRESP + (cAliasSQB)->QB_FILRESP + (cAliasSQB)->QB_MATRESP})
	(cAliasSQB)->(DbSkip())
EndDo

(cAliasSQB)->(dbCloseArea())

Return aResp

/*-------------------------------------------------------------------
{Protheus.doc} Get fAhgoraPW
Valida ponto do funcionário para integração na RUM

@author	Bruno Costa
@since	28/03/2025
-------------------------------------------------------------------*/
method fAhgoraPW(cVldPonto as character, cTpPW as character) as logical class GPEAhGora
local cIntPonto as character

DEFAULT aResp := {}
DEFAULT cTpPW := ""

If cVldPonto == "2"  
	cIntPonto := If(Empty(cTpPW), gpRetSR9("SRA", Date(), "AHGORA", cToD("//")), "")
	If Empty(cIntPonto) .Or. Rtrim(cIntPonto) == "2"
		Return .F.
	EndIf	
EndIf

If cVldPonto == "3" 
	cIntPonto := If(Empty(cTpPW), gpRetSR9("SRA", Date(), "AHGORA", cToD("//")), "")
	If Empty(cIntPonto) .Or. Rtrim(cIntPonto) == "2"
		aResp := IF(Len(aResp) > 0, aResp, GPEAhGora():getRespDpto()) 
		If aScan(aResp, {|x| x[1] == cEmpAnt + SRA->RA_FILIAL + SRA->RA_MAT }) == 0 
			Return .F.
		EndIf
	EndIf	
EndIf 

Return .T.

/*-------------------------------------------------------------------
{Protheus.doc} Get getVerbasAhgora
Serviço para retornar uma lista de verbas por CNPJ

@author	Bruno Costa
@since	09/04/2025
-------------------------------------------------------------------*/
method getVerbasAhgora() class GPEAhGora
local oJsBadResp as object
local oItem	as object
local oErros as object
local oJsResponse := JsonObject():new() as object
local oResponse   := JsonObject():New() as Json
local oJParHeader := oRest:getHeaderRequest() as json
local jParams 	  := oRest:getQueryRequest() as Json
local __cEmpAnt   := cEmpAnt as character
local __cFilAnt   := cFilAnt as character
local cUser as character
local cPass as character
local nPageSize as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nPos as numeric
local nI as numeric
local nY as numeric
local lHasNext as logical
local lAbrEMP as logical
local aCnpjReq  := {} as array
local aCnpj     := {} as array
local aCnpjList := {} as array
local aKeyUser as array

Static aJsonSRV

DEFAULT aSM0 := FwLoadSM0()

aKeyUser := StrTokArr(Decode64(AllTrim(SubStr(oJParHeader["Authorization"], 6, Len(oJParHeader["Authorization"])))), ":")
cUser    := aKeyUser[1]
cPass    := aKeyUser[2]

If !(jParams:hasProperty("page") .And. jParams:hasProperty("pageSize") .And. jParams:hasProperty("cnpjList"))
	oJsBadResp := JSonObject():new()
	oJsBadResp['code']    := CodeRespBadRequest
	oJsBadResp['message'] := OemToAnsi(STR0016) //"Necessário enviar o page, pageSize e o cnpjList na requisição" 
	oRest:setStatusCode(CodeRespBadRequest)
	oRest:setResponse(oJsBadResp:toJson())  
	Return
EndIf 	

nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
aCnpjList := StrTokArr(jParams["cnpjList"], ",")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oJsResponse['response'] := {}
oResponse["erros"]      := {}

nStart := (nPage - 1) * nPageSize + 1
nEnd   :=  nPage * nPageSize + 1
nY     := If(nStart > 1, nStart, 1)	

If nStart == 1 .Or. aJsonSRV == Nil
	aJsonSRV  := {}
	oStVerbas := Nil
	aCnpjReq  := self:fCNPJAhgora(aCnpjList)

	For nI := 1 To Len(aCnpjReq)
		If (nPos := aScan(aSM0, {|x| x[18] == aCnpjReq[nI]})) > 0
			aAdd(aCnpj, {aSM0[nPos][SM0_GRPEMP], aSM0[nPos][SM0_CODFIL], aSM0[nPos][SM0_CGC]})
		ElseIf nStart == 1
			oErros 			   := JSonObject():new()
			oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0026)) //"CNPJ não encontrado na base de dados" 
			oErros["cnpj"]     := Transform(aCnpjReq[nI], "@!R NN.NNN.NNN/NNNN-99")
			aAdd(oResponse["erros"], oErros)
		EndIf
	Next nI

	If Empty(aCnpj)
	    oResponse["erros"] := {}
		oErros 			   := JSonObject():new()
		oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0017)) //"Nenhum CNPJ foi encontrado na base de dados" 
		oErros["cnpj"]     := jParams["cnpjList"]
		aAdd(oResponse["erros"], oErros)
	Else 	
		//Ordena por empresa
		aSort(aCnpj,,, {|x, y| x[1] < y[1]})

		For nI := 1 To Len(aCnpj)
			lAbrEMP := .F.
			If aCnpj[nI][1] <> cEmpAnt
				If !(lAbrEMP := self:fEmpReqAhgora(aCnpj[nI][1], aCnpj[nI][2], cUser, cPass, .F.))
					self:fEmpReqAhgora(__cEmpAnt, __cFilAnt, cUser, cPass, .T.) //Volta para original da requisição, caso contrário da erro
					If nStart == 1
						oErros 			   := JSonObject():new()
						oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0018)) +  aCnpj[nI][1] + " - " + EncodeUTF8(OemToAnsi(STR0019)) + aCnpj[nI][3] //"Não foi possível abrir a empresa: XX" ### "Verifique se o usuário possuí acesso a empresa vinculada ao CNPJ: XX"
						oErros["cnpj"]     := Transform(aCnpj[nI][3], "@!R NN.NNN.NNN/NNNN-99")
						aAdd(oResponse["erros"], oErros)
					EndIf	
					Loop
				EndIf
			EndIf
			//Monta verbas por CNPJ
			If !self:fMontaSRV(aCnpj[nI], lAbrEMP) .And. nStart == 1
				oErros 			   := JSonObject():new()
				oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0027)) //"Não existe cadastro de verbas para o CNPJ"
				oErros["cnpj"]     := Transform(aCnpj[nI][3], "@!R NN.NNN.NNN/NNNN-99")
				aAdd(oResponse["erros"], oErros)
			EndIf
		Next nI
	EndIf	
EndIf 

oResponse["items"] := {}

For nY := nY To Len(aJsonSRV)
	If nY > (nEnd - 1) 
		lHasNext := .T.
		Exit 
	EndIf   

	oItem 			   := JSonObject():new()
	oItem["cod"]       := EncodeUTF8(aJsonSRV[nY][1])
	oItem["desc"] 	   := EncodeUTF8(aJsonSRV[nY][2])
	oItem["cnpj"] 	   := aJsonSRV[nY][3]
	oItem["categoria"] := EncodeUTF8(aJsonSRV[nY][4])

	aAdd(oResponse["items"], oItem)

Next nY

oResponse["hasNext"] := lHasNext 	

If !lHasNext
	aJsonSRV := NIL
EndIf     

If __cEmpAnt <> cEmpAnt
	self:fEmpReqAhgora(__cEmpAnt, __cFilAnt, cUser, cPass, .T.)
EndIf

aAdd(oJsResponse['response'], oResponse)
oRest:setStatusCode(CodeRespCreated)
oRest:setResponse(If(Len(oJsResponse["response"][1]["items"]) > 0 .Or. Len(oJsResponse["response"][1]["erros"]) > 0, oJsResponse:toJson(), ""))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get fCNPJAhgora
Método para formatar CNPJ da requisição
@author	Bruno Costa
@since	09/04/2025
-------------------------------------------------------------------*/
method fCNPJAhgora(aJsCnpj as array) as array class GPEAhGora
local cCGC as character
local cPos as character
local nI as numeric
local nY as numeric

For nI := 1 To Len(aJsCnpj)
	For nY := 1 To Len(aJsCnpj[nI])
		cPos := Substr(aJsCnpj[nI], nY, 1)
		If IsDigit(cPos) .Or. IsAlpha(cPos)
			cCGC +=	cPos
		EndIf
	Next nY	
	If !Empty(cCGC)
		aJsCnpj[nI] := cCGC
		cCGC := ""
	EndIf
Next nI

return aJsCnpj

/*-------------------------------------------------------------------
{Protheus.doc} Get fMontaSRV
Método para criar o array com as verbas conforme CNPJ passado
@author	Bruno Costa
@since	09/04/2025
-------------------------------------------------------------------*/
method fMontaSRV(aCnpj as array, lAbrEMP as logical ) as logical class GPEAhGora
local cAlias as Character		
local cQuery as Character 
local cCnpj := Transform(aCnpj[3], "@!R NN.NNN.NNN/NNNN-99") as character
local aTpPd := {"D", "H"} as array
local lTemSRV as logical

Static oStVerbas

If oStVerbas == Nil .Or. lAbrEMP
	cQuery := "SELECT SRV.RV_FILIAL, SRV.RV_COD, SRV.RV_DESC, SRV.RV_TIPOCOD,SRV.RV_TIPO "
	cQuery += "FROM "+RetSqlName("SRV") + " SRV "
	cQuery += "WHERE SRV.RV_FILIAL = ? AND SRV.RV_TIPO IN (?) AND SRV.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY SRV.RV_COD"

	cQuery    := ChangeQuery(cQuery) 
	oStVerbas := FwExecStatement():New(cQuery)
EndIf

oStVerbas:SetString(1, xFilial("SRV", aCnpj[2]))
oStVerbas:SetIn(2, aTpPd)
oStVerbas:SetString(3, " ")

cAlias := oStVerbas:OpenAlias()

While !(cAlias)->(Eof())  
	lTemSRV := .T.
	aAdd(aJsonSRV, {;
		(cAlias)->RV_COD,;
	 	Rtrim((cAlias)->RV_DESC),;
		cCnpj,;
	  	If((cAlias)->RV_TIPOCOD == "1", OemToAnsi(STR0021), If((cAlias)->RV_TIPOCOD == "2", OemToAnsi(STR0022), OemToAnsi(STR0023))) + " - " +;
		If((cAlias)->RV_TIPO == "H", OemToAnsi(STR0028), OemToAnsi(STR0029))}) //"Provento-Hora/Dia"###"Desconto-Hora/Dia"###"Base de Cálculo"
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())

return lTemSRV

/*-------------------------------------------------------------------
{Protheus.doc} Get fEmpReqAhgora
Método abrir a empresa ou alterar a empresa no fim da requisição para a mesma do ínicio
@author	Bruno Costa
@since	14/04/2025
-------------------------------------------------------------------*/
method fEmpReqAhgora(cEmp as character, cFil as character, cUser as character, cPass as character, lVolta as logical) as logical class GPEAhGora
local lRet as logical

If lVolta
	RpcClearEnv()
	RPCSetType(3)
	RPCSetEnv(cEmp, cFil, cUser, cPass, "GPE", "rh.sigagpe.ahgora")
Else	
	RpcClearEnv()
	RPCSetType(3)
	lRet := RPCSetEnv(cEmp, cFil, cUser, cPass, "GPE", "rh.sigagpe.ahgora")
EndIf

return lRet

/*-------------------------------------------------------------------
{Protheus.doc} Get getSindicatosAhgora
Serviço para retornar uma lista de sindicatos por CNPJ

@author	Bruno Costa
@since	14/04/2025
-------------------------------------------------------------------*/
method getSindicatosAhgora() class GPEAhGora
local oJsBadResp as object
local oItem	as object
local oErros as object
local oJsResponse := JsonObject():new() as object
local oResponse   := JsonObject():New() as Json
local oJParHeader := oRest:getHeaderRequest() as json
local jParams 	  := oRest:getQueryRequest() as Json
local __cEmpAnt   := cEmpAnt as character
local __cFilAnt   := cFilAnt as character
local cUser as character
local cPass as character
local nPageSize as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nPos as numeric
local nI as numeric
local nY as numeric
local lHasNext as logical
local lAbrEMP as logical
local aCnpjReq  := {} as array
local aCnpj     := {} as array
local aCnpjList := {} as array
local aKeyUser as array

Static aJsonRCE

DEFAULT aSM0 := FwLoadSM0()

aKeyUser := StrTokArr(Decode64(AllTrim(SubStr(oJParHeader["Authorization"], 6, Len(oJParHeader["Authorization"])))), ":")
cUser    := aKeyUser[1]
cPass    := aKeyUser[2]

If !(jParams:hasProperty("page") .And. jParams:hasProperty("pageSize") .And. jParams:hasProperty("cnpjList"))
	oJsBadResp := JSonObject():new()
	oJsBadResp['code']    := CodeRespBadRequest
	oJsBadResp['message'] := OemToAnsi(STR0016) //"Necessário enviar o page, pageSize e cnpjList na requisição" 
	oRest:setStatusCode(CodeRespBadRequest)
	oRest:setResponse(oJsBadResp:toJson())  
	Return
EndIf 	

nPage     := Val(jParams["page"])
nPageSize := Val(jParams["pageSize"])
aCnpjList := StrTokArr(jParams["cnpjList"], ",")

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")
oJsResponse['response'] := {}
oResponse["erros"]      := {}

nStart := (nPage - 1) * nPageSize + 1
nEnd   :=  nPage * nPageSize + 1
nY     := If(nStart > 1, nStart, 1)	

If nStart == 1 .Or. aJsonRCE == Nil
	aJsonRCE  	 := {}
	oStSindicato := Nil
	aCnpjReq     := self:fCNPJAhgora(aCnpjList)

	For nI := 1 To Len(aCnpjReq)
		If (nPos := aScan(aSM0, {|x| x[18] == aCnpjReq[nI]})) > 0
			aAdd(aCnpj, {aSM0[nPos][SM0_GRPEMP], aSM0[nPos][SM0_CODFIL], aSM0[nPos][SM0_CGC]})
		ElseIf nStart == 1
			oErros 			   := JSonObject():new()
			oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0026)) //"CNPJ não encontrado na base de dados" 
			oErros["cnpj"]     := Transform(aCnpjReq[nI], "@!R NN.NNN.NNN/NNNN-99")
			aAdd(oResponse["erros"], oErros)
		EndIf
	Next nI 	

	If Empty(aCnpj)
		oResponse["erros"] := {}
		oErros 			   := JSonObject():new()
		oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0017)) //"Nenhum CNPJ foi encontrado na base de dados" 
		oErros["cnpj"]     := jParams["cnpjList"]
		aAdd(oResponse["erros"], oErros)
	Else 	
		//Ordena por empresa
		aSort(aCnpj,,, {|x, y| x[1] < y[1]})

		For nI := 1 To Len(aCnpj)
			lAbrEMP := .F.
			If aCnpj[nI][1] <> cEmpAnt
				If !(lAbrEMP := self:fEmpReqAhgora(aCnpj[nI][1], aCnpj[nI][2], cUser, cPass, .F.))
					self:fEmpReqAhgora(__cEmpAnt, __cFilAnt, cUser, cPass, .T.) //Volta para original da requisição, caso contrário da erro
					If nStart == 1
						oErros 			   := JSonObject():new()
						oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0018)) +  aCnpj[nI][1] + " - " + EncodeUTF8(OemToAnsi(STR0019)) + aCnpj[nI][3] //"Não foi possível abrir a empresa: XX" ### "Verifique se o usuário possuí acesso a empresa vinculada ao CNPJ: XX"
						oErros["cnpj"]     := Transform(aCnpj[nI][3], "@!R NN.NNN.NNN/NNNN-99")
						aAdd(oResponse["erros"], oErros)
					EndIf	
					Loop
				EndIf
			EndIf
			//Monta sindicatos por CNPJ
			If !self:fMontaRCE(aCnpj[nI], lAbrEMP) .And. nStart == 1
				oErros 			   := JSonObject():new()
				oErros["mensagem"] := EncodeUTF8(OemToAnsi(STR0030)) //"Não existe cadastro de sindicatos para o CNPJ"
				oErros["cnpj"]     := Transform(aCnpj[nI][3], "@!R NN.NNN.NNN/NNNN-99")
				aAdd(oResponse["erros"], oErros)
			EndIf
		Next nI
	EndIf	
EndIf 

oResponse["items"] := {}

For nY := nY To Len(aJsonRCE)
	If nY > (nEnd - 1) 
		lHasNext := .T.
		Exit 
	EndIf   

	oItem 		  := JSonObject():new()
	oItem["cod"]  := EncodeUTF8(aJsonRCE[nY][1])
	oItem["desc"] := EncodeUTF8(aJsonRCE[nY][2])
	oItem["cnpj"] := aJsonRCE[nY][3]

	aAdd(oResponse["items"], oItem)

Next nY

oResponse["hasNext"] := lHasNext 	

If !lHasNext
	aJsonRCE := NIL
EndIf      

If __cEmpAnt <> cEmpAnt
	self:fEmpReqAhgora(__cEmpAnt, __cFilAnt, cUser, cPass, .T.)
EndIf

aAdd(oJsResponse['response'], oResponse)
oRest:setStatusCode(CodeRespCreated)
oRest:setResponse(If(Len(oJsResponse["response"][1]["items"]) > 0 .Or. Len(oJsResponse["response"][1]["erros"]) > 0, oJsResponse:toJson(), ""))

return

/*-------------------------------------------------------------------
{Protheus.doc} Get fMontaRCE
Método para criar o array com as verbas conforme CNPJ passado
@author	Bruno Costa
@since	14/04/2025
-------------------------------------------------------------------*/
method fMontaRCE(aCnpj as array, lAbrEMP as logical ) as logical class GPEAhGora
local cAlias as Character		
local cQuery as Character 
local cCnpj := Transform(aCnpj[3], "@!R NN.NNN.NNN/NNNN-99") as character
local lTemRCE as logical

Static oStSindicato

If oStSindicato == Nil .Or. lAbrEMP
	cQuery := "SELECT RCE.RCE_FILIAL, RCE.RCE_CODIGO, RCE.RCE_DESCRI "
	cQuery += "FROM "+RetSqlName("RCE") + " RCE "
	cQuery += "WHERE RCE.RCE_FILIAL = ? AND RCE.D_E_L_E_T_ = ? "
	cQuery += "ORDER BY RCE.RCE_CODIGO"

	cQuery       := ChangeQuery(cQuery) 
	oStSindicato := FwExecStatement():New(cQuery)
EndIf

oStSindicato:SetString(1, xFilial("RCE", aCnpj[2]))
oStSindicato:SetString(2, " ")

cAlias := oStSindicato:OpenAlias()

While !(cAlias)->(Eof())  
	lTemRCE := .T.
	aAdd(aJsonRCE, {;
		Rtrim((cAlias)->RCE_CODIGO),;
	 	Rtrim((cAlias)->RCE_DESCRI),;
		cCnpj;
		})
	
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())

return lTemRCE

/*-------------------------------------------------------------------
{Protheus.doc} Get AhgoraLiderados
Método para criar integração dos líderados na alteração do Departamento
@author	Bruno Costa
@since	28/04/2025
-------------------------------------------------------------------*/
method AhgoraLiderados(cFilDp as character, cDepto as character, cEmpResp as character, cFilResp as character, cMatResp as character) class GPEAhGora
local aAreaSRA   := SRA->(GetArea()) as array
local aAreaSQB   := SQB->(GetArea()) as array
local cAlias     := GetNextAlias() as character
local cVldPt     := SuperGetMv("MV_INTEGPW",, "1") as character
local aCodInt    := {} as array 
local nMatResp   := IF(!Empty(cMatResp), 1, 0)

DEFAULT aResp := {}

aResp := IIF(Len(aResp) > 0, aResp, IIF(cVldPt == "3", GPEAhGora():getRespDpto(), {})) 

cQuery := "SELECT SRA.RA_FILIAL, SRA.RA_MAT, COUNT(*) OVER () AS TOTAL_LINHAS "
If cVldPt <> "1"
	cQuery += ", SR9.R9_DESC "
EndIf
cQuery += "FROM " + RetSqlName("SRA") + " SRA "
cQuery += "INNER JOIN " + RetSqlName("SQB") + " SQB ON " + FWJoinFilial("SQB", "SRA" ) + " AND SQB.QB_DEPTO = SRA.RA_DEPTO AND SQB.QB_DEPTO = '" + cDepto + "' AND SQB.D_E_L_E_T_ = ' ' "
If cVldPt <> "1"
	cQuery += "LEFT JOIN (SELECT R9_FILIAL, R9_MAT, MAX(R_E_C_N_O_) AS MAX_RECNO FROM " + RetSqlName("SR9") + " WHERE R9_CAMPO = 'AHGORA' AND D_E_L_E_T_ = ' ' "
	cQuery += "GROUP BY R9_FILIAL, R9_MAT) SR9_MAX ON SR9_MAX.R9_FILIAL = SRA.RA_FILIAL AND  SR9_MAX.R9_MAT = SRA.RA_MAT "
	cQuery += "LEFT JOIN "+ RetSqlName("SR9") + " SR9 ON SR9.R9_FILIAL = SR9_MAX.R9_FILIAL AND SR9.R9_MAT = SR9_MAX.R9_MAT AND SR9.R_E_C_N_O_ = SR9_MAX.MAX_RECNO AND SR9.D_E_L_E_T_ = ' ' "
EndIf
cQuery += "WHERE SRA.D_E_L_E_T_ = ' ' AND SRA.RA_SITFOLH <> 'D' AND SQB.QB_FILIAL = '" + cFilDp + "' "
cQuery += "ORDER BY SRA.RA_FILIAL, SRA.RA_MAT"

cQuery := ChangeQuery(cQuery) 
DbUseArea(.T., "TOPCONN", TcGenQry(,, cQuery), cAlias)

GPProcRegua(If(!(cAlias)->(Eof()), (cAlias)->TOTAL_LINHAS+nMatResp, nMatResp))

While !(cAlias)->(Eof())  
	If cVldPt <> "1"
		If cVldPt == "2" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto
			(cAlias)->(dbSkip())
			Loop
		EndIf

		If cVldPt == "3" .And. Rtrim((cAlias)->R9_DESC) <> "1" //Somente os colaboradores que batem ponto e os colaboradores isentos de ponto que são líderes
			If aScan(aResp, {|x| x[1] == cEmpAnt + (cAlias)->RA_FILIAL + (cAlias)->RA_MAT }) == 0 
				(cAlias)->(dbSkip())
				Loop
			EndIf
		EndIf
	EndIf 
	aAdd(aCodInt, {(cAlias)->RA_FILIAL, (cAlias)->RA_MAT, cEmpAnt+(cAlias)->RA_FILIAL+(cAlias)->RA_MAT, Nil, Nil, "A"})
	GPIncProc(OemToAnsi(STR0025) + (cAlias)->RA_FILIAL + " - " + (cAlias)->RA_MAT, 20, .T. ) //Ahgora - Processando registro:
	GPEAhGora():gravaRUM("I", "SRA", cEmpAnt + "|" + (cAlias)->RA_FILIAL + "|" + (cAlias)->RA_MAT, aCodInt, "1")
	aCodInt := {}
	(cAlias)->(DbSkip())
EndDo

(cAlias)->(dbCloseArea())

//Integra o responsável
If !Empty(cMatResp)
	If cEmpResp <> cEmpAnt 
		SX6->(DBCloseArea())
		OpenSxs(,,,, cEmpResp, "SX6", "SX6",, .F.)			
		If GetMv("MV_RHAHGOR",, .F.)
			GPEAhGora():AhgoraLider(.T., GetMv("MV_INTEGPW",, "1"), cFilResp, cMatResp, cEmpResp)
		EndIf	
		SX6->(DBCloseArea())
		OpenSxs(,,,, cEmpAnt, "SX6", "SX6",, .F.)
	Else
		GPEAhGora():AhgoraLider(.F., cVldPt, cFilResp, cMatResp)
	EndIf	
EndIf

RestArea(aAreaSRA)
RestArea(aAreaSQB)

return

/*-------------------------------------------------------------------
{Protheus.doc} Get AhgoraLider
Método para criar integração do líder na alteração do Departamento
@author	Bruno Costa
@since	28/04/2025
-------------------------------------------------------------------*/
method AhgoraLider(lEmp as logical, cPonto as character, cFilResp as character, cMatResp as character, cEmpResp as character) class GPEAhGora
local aCodInt  := {} as array
local dDataAlt := Date() as date
local lAbr     := .T. as logical
local cASR9    := "GPESR9" as character
local cConteu as character

If lEmp 
	If cPonto == "2" .And. (lAbr := fAbrEmpresa("SR9", 1, cEmpResp, cFilResp))
		(cASR9)->(dbGoTop())	
		If (cASR9)->(dbSeek(cFilResp + cMatResp + "AHGORA"))
			While (cASR9)->(!Eof() .And. AllTrim(R9_FILIAL + R9_MAT + R9_CAMPO) == AllTrim(cFilResp + cMatResp + "AHGORA"))
				If dDataAlt >= (cASR9)->R9_DATA
					cConteu := Rtrim((cASR9)->R9_DESC)	
				EndIf		
				(cASR9)->(dbSkip())
			EndDo
		EndIf
		(cASR9)->(dbCloseArea())
		If cConteu <> "1"
			Return
		EndIf 
	EndIf 
	If lAbr
		aAdd(aCodInt, {cFilResp, cMatResp, "", "I", cEmpResp+cFilResp+cMatResp, CtoD("//"), .T., cEmpResp})
		GPIncProc(OemToAnsi(STR0025) + cFilResp + " - " + cMatResp, 20, .T. ) //Ahgora - Processando registro:
		GPEAhGora():gravaRUMTransf("I", "SRA", cEmpResp + "|" + cFilResp + "|" + cMatResp, aCodInt, .F., .F., .T.)
	EndIf	
Else 
	DbSelectArea("SRA")
	DbSetOrder(1)

	If cPonto == "2" .And. SRA->(DbSeek(cFilResp+cMatResp)) .And. !GPEAhGora():fAhgoraPW(cPonto)
		Return
	EndIf 

	DbSelectArea("RUM")
	DbSetOrder(1)

	IF !RUM->(dbSeek(xFilial("RUM") + "1"+"I"+cEmpAnt+cFilResp+cMatResp))
		aAdd(aCodInt, {cFilResp, cMatResp, cEmpAnt+cFilResp+cMatResp, Nil, Nil, "A"})
		GPIncProc(OemToAnsi(STR0025) + cFilResp + " - " + cMatResp, 20, .T. ) //Ahgora - Processando registro:
		GPEAhGora():gravaRUM("I", "SRA", cEmpAnt + "|" + cFilResp + "|" + cMatResp, aCodInt, "1")
	EndIF 
EndIf 

Return

/*-------------------------------------------------------------------
{Protheus.doc} Get getConfigsAhgora
Serviço para retornar as configurações que serão usadas na tela de integração com Ahgora

@author	Bruno Costa
@since	10/09/2025
-------------------------------------------------------------------*/
method getConfigsAhgora() class GPEAhGora
local oItem	    := JsonObject():new() as object
local oProfile  := FwProfile():New() as object
local oResponse := JsonObject():New() as Json
local aParams   := {} as array 

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

oResponse["items"] := {}

oProfile:SetUser(RetCodUsr())
oProfile:SetProgram("rh.sv.sigagpe.ahgora")
oProfile:SetTask("AHGORA")

aParams := oProfile:Load()

If Len(aParams) >= 3 
	oItem["actionsRight"]    := aParams[1]
	oItem["poMenuCollapsed"] := aParams[2]
	oItem["pageSize"]        := aParams[3]
Else 
	oItem["actionsRight"]    := .F.
	oItem["poMenuCollapsed"] := .F.
	oItem["pageSize"]        := 100
EndIf

oProfile:DeActivate()

aAdd(oResponse["items"], oItem)

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} postConfiguracoesAhgora
Serviço para gravar as configurações de tela no profile do usuário

@author	Bruno Costa
@since	10/09/2025
-------------------------------------------------------------------*/
method postConfiguracoesAhgora() class GPEAhGora
local oJsResp as object
local oProfile := FwProfile():New() as object
local oJsBody  := JsonObject():new() as Json
local aParams as array
local lGrv as logical

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

//Recupera o conteudo do body
oJsBody:fromJson( oRest:GetBodyRequest() )

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0   
	aParams := {oJsBody["items"][1]["actionsRight"], oJsBody["items"][1]["poMenuCollapsed"], oJsBody["items"][1]["pageSize"]}
	lGrv    := .T.
	oProfile:SetUser(RetCodUsr())
	oProfile:SetProgram("rh.sv.sigagpe.ahgora")
	oProfile:SetTask("AHGORA")
    oProfile:SetProfile(aParams)
    oProfile:Save()
    oProfile:DeActivate()
EndIf

oJsResp := JSonObject():new()
oJsResp['code']    := If(lGrv, CodeRespCreated, CodeRespBadRequest)
oJsResp['message'] := If(lGrv, STR0034, STR0035) //"Configurações gravadas com sucesso."###"Erro ao gravar as configurações."
oRest:setStatusCode(If(lGrv, CodeRespCreated, CodeRespBadRequest))
oRest:setResponse(EncodeUTF8(FwCutOff(oJsResp:toJson())))

return
