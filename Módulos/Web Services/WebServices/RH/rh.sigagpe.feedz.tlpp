#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#Include "totvs.ch"
#Include "rh.sigagpe.feedz.ch"

#DEFINE CodeRespBadRequest 400

/*-------------------------------------------------------------------
{Protheus.doc} GPEFeedz
Classe com os serviços para integração/consulta Feedz e P&M

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
class GPEFeedz

	public method new() as object

	@Get(endpoint='/api/rh/v1/feedz/tipo', description='API Para verificar o tipo da integração') 
	public method getIntAPI() as object 

	@Get(endpoint='/api/rh/v1/feedz/lista', description='API de busca dos registros da tabela REF') 
	public method getLoteREF() as object 

	@Get(endpoint='/api/rh/v1/feedz/lote', description='API de busca dos detalhes das integrações') 
	public method getLoteFeedz() as object

	@Get(endpoint='/api/rh/v1/feedz/consulta/:id', description='API de busca das inconsistencias das integrações') 
	public method getErroFeedz() as object

	@Post(endpoint='/api/rh/v1/feedz/consulta', description='API de consulta dos lotes das integrações na Feedz e P&M') 
	public method postStatusFeedz() as object
	
	private method getWherePar() as character
	private method getDateFeedz() as character

endclass

/*-------------------------------------------------------------------
{Protheus.doc} new
Método construtor da classe GPEFeedz

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
method new() as object class GPEFeedz
return self

/*-------------------------------------------------------------------
{Protheus.doc} getIntAPI
API Para verificar o tipo da integração

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
Method getIntAPI() Class GPEFeedz
local oItem	    := JsonObject():new() as Object
local oResponse := JsonObject():New() as Json
local cFeedzURL	:= SuperGetMv('MV_APIFEE1', Nil, "") as character
local cApi      := If(Empty(cFeedzURL), "1", "2") as character

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

oResponse["items"] := {}
oItem["tipo"]      := cApi

aAdd(oResponse["items"], oItem)

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

Return 

/*-------------------------------------------------------------------
{Protheus.doc} Get getLoteREF
API de busca dos registros da tabela REF

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
method getLoteREF() class GPEFeedz
local oItem	as Object
local jParams := oRest:getQueryRequest() as Json
local oResponse := JsonObject():New() as Json
local cTipoApi as character
local cUserOld as character
local cAlias as character		
local cQuery as character
local cWhere as character
local cTpCod as character
local cuser as character
local cApi as character
local nPageSize as numeric
local nPOrder as numeric
local nStart as numeric
local nPage as numeric
local nEnd as numeric
local nPos as numeric
local nI as numeric
local aTpStatus as array    
local aOpcBox as array
local aTpApi as array  
local aTipo := {} as array
local aParams as array
local lHasNext as logical

Static __cEmpAnt := cEmpAnt
Static oStatREF

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

aTpStatus  := IIf(jParams:hasProperty("tipoStatus"), StrTokArr(jParams["tipoStatus"], ','), {})
aTpApi     := IIf(jParams:hasProperty("tipoApi"), StrTokArr(jParams["tipoApi"], ','), {})
aParams    := {"dataIntDe", "dataIntAte", "tipoStatus", "tipoApi"}
nPage 	   := Val(jParams["page"])
nPageSize  := Val(jParams["pageSize"])
cApi       := jParams["api"]
nPOrder    := 1
aOpcBox    := StrTokArr(OemToAnsi(STR0001), ';') 

For nI := 1 To Len(aOpcBox)
	aAdd(aTipo, {SubStr(aOpcBox[nI], 1, 1), SubStr(aOpcBox[nI], 3, Len(aOpcBox[nI]))})
Next nI

nStart := (nPage - 1) * nPageSize + 1
nEnd   :=  nPage * nPageSize + 1

oResponse["items"] := {}

If oStatREF == Nil .Or. __cEmpAnt <> cEmpAnt .Or. nStart == 1  
	cQuery := "SELECT * FROM ( "
	cQuery += "SELECT ROW_NUMBER() OVER(ORDER BY REF.REF_DATINT, REF.REF_HORINT ASC) AS LINHA, "
	cQuery += "REF.REF_PRCID, REF.REF_TIPO, REF.REF_DATINT, REF.REF_HORINT, REF.REF_LOTE, REF.REF_RETORN, REF.REF_USER, REF.REF_TPINT, REF.REF_STATUS "
	cQuery += "FROM "+RetSqlName("REF")+" REF "
	cQuery += "WHERE REF.D_E_L_E_T_ = ' ' AND " + If(cApi == "2", "REF.REF_TPINT = '2' ", "REF.REF_TPINT IN (' ', '1') ")
	
	cWhere := self:getWherePar(jParams) 

	If !Empty(cWhere)
		cQuery += cWhere    
	EndIf

	cQuery  += ") TAB "
	cQuery  += "WHERE LINHA BETWEEN ? AND ?"

	__cEmpAnt := cEmpAnt
	cQuery    := ChangeQuery(cQuery) 
	oStatREF  := FwExecStatement():New(cQuery)
EndIf

For nI := 1 To Len(aParams)
	If !Empty(jParams[aParams[nI]]) 
		If(aParams[nI] $ "tipoStatus*tipoApi")
			oStatREF:SetIn(nPOrder++, IIf(aParams[nI] == "tipoStatus", aTpStatus, aTpApi))
		Else
			oStatREF:setDate(nPOrder++, SToD(StrTran(SubStr(jParams[aParams[nI]], 1, 10), "-" )))
		EndIf            
	EndIf
Next nI

oStatREF:SetString(nPOrder++, cValToChar(nStart))
oStatREF:SetString(nPOrder++, cValToChar(nEnd))

cAlias := oStatREF:OpenAlias()
dbSelectArea(cAlias)

While !(cAlias)->(Eof())
	If (cAlias)->LINHA > (nEnd - 1)
		lHasNext := .T.
		Exit 
	EndIf   

	If cTpCod != (cAlias)->REF_TIPO
		nPos := aScan(aTipo, { |x| x[1] $ (cAlias)->REF_TIPO })
		If nPos > 0
			cTipoApi := aTipo[nPos][2]
			cTpCod   := aTipo[nPos][1]
		EndIf
	EndIf	

	If cUserOld != (cAlias)->REF_USER
		cUser    := (cAlias)->REF_USER + " - " + UsrRetName((cAlias)->REF_USER)
		cUserOld := (cAlias)->REF_USER
	EndIf

	oItem := JsonObject():new()

	oItem["prcId"]      := (cAlias)->REF_PRCID
	oItem["tipo"]	    := cTipoApi
	oItem["dataInt"]    := self:getDateFeedz((cAlias)->REF_DATINT) 
	oItem["hora"]       := (cAlias)->REF_HORINT
	oItem["user"] 	    := cUser
	oItem["statusLote"] := (cAlias)->REF_STATUS

	aAdd(oResponse["items"], oItem)
	
	(cAlias)->(DbSkip())

EndDo

(cAlias)->(dbCloseArea())
oResponse["hasNext"] := lHasNext 

oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))

return

/*-------------------------------------------------------------------
{Protheus.doc} postStatusFeedz
API de consulta dos lotes das integrações na Feedz e P&M

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
method postStatusFeedz() class GPEFeedz
local oItem	as Object
Local oClient as Object
Local oJson as Object
local oResponse := JsonObject():New() as json
local oJsBody   := JsonObject():new() as Json
Local oError  	:= JsonObject():new() as Json
Local aHeader	:= {} as array
local cFeedzURL := SuperGetMv('MV_APIFEE1', Nil, "") as character
local cPMURL	:= SuperGetMv('MV_APIPEM1', Nil, "") as character
local cFeedzTok	:= SuperGetMv('MV_APIFEE2', Nil, "") as character
Local cErrToken as character
Local cPath	as character
Local cRetPeM as character
Local cStatJson	as character
Local cToken as character
Local lTokOk := .T. as logical
local lRet as logical
local nI as numeric

Private cPMToken  := SuperGetMv('MV_APIPEM0', Nil, "")
Private cPMClient := SuperGetMv('MV_APIPEM2', Nil, "")
Private cPMSecret := SuperGetMv('MV_APIPEM3', Nil, "")

oJsBody:fromJson(oRest:GetBodyRequest())
oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

cPath	:= Iif(Empty(cFeedzURL), "api/v1/process/", "v1/process/")
oClient := FwRest():New(Iif(Empty(cFeedzURL), cPMURL, cFeedzURL))

If Empty(cFeedzURL)
    lTokOk := fTokenPM(@cToken, @cErrToken)
Else
    cToken := cFeedzTok
    lTokOk := .T.
EndIf

If !lTokOk	
	oError["message"]         := OemToAnsi(STR0003) //"Atenção, houve erro na consulta, verifique nos detalhes."
	oError["detailedMessage"] := OemToAnsi(STR0004 + " - " + STR0006 + If(!Empty(cErrToken), " - " + cErrToken, "")) // //"Houve falha ao tentar obter o token de acesso"###"Solicite ao administrador uma revisão dos parâmetros MV_APIPEM0, MV_APIPEM2 e/ou MV_APIPEM3"
    oError["code"]            := cValToChar(CodeRespBadRequest)
	oError["type"]            := OemToAnsi(STR0005)
	oRest:setStatusCode(CodeRespBadRequest)
	oRest:setResponse(EncodeUTF8(FwCutOff(oError:toJson())))
	Return
EndIf

aAdd(aHeader, "Authorization: Bearer " + cToken)
aAdd(aHeader, "User-Agent: Protheus")

If Valtype(oJsBody["items"]) != "U" .And. Len(oJsBody["items"]) > 0  

	DbSelectArea("REF")
	REF->(DbSetOrder(2))

	oResponse["items"] := {}

	For nI := 1 To Len(oJsBody["items"]) 
	
		If REF->(dbSeek( xFilial("REF") + oJsBody["items"][nI]["prcId"]))
			
			oClient:SetPath(cPath+REF->REF_PRCID)
		
			cStatJson := "X"
			cRetPeM   := ""

			If oClient:Get(aHeader)
				oJson := JsonObject():New()
				oJson:fromJson(oClient:GetResult())
				fJsPMSt(@oJson, @cStatJson)
				cRetPeM := oClient:GetResult()
			EndIf    

			If REF->(RecLock("REF", .F.))
				REF->REF_STATUS := cStatJson
				REF->REF_RETORN := cRetPeM
				REF->( MsUnlock() )
				oItem := JSonObject():new()
				oItem["prcId"]      := REF->REF_PRCID
				oItem["statusLote"] := REF->REF_STATUS
				aAdd(oResponse["items"], oItem)
				lRet := .T.
			EndIf
		EndIf	
	Next nI

	REF->(dbCloseArea())

EndIf

If lRet
	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getLoteFeedz
API de busca dos detalhes das integrações

@author	Bruno Costa
@since	28/09/2024
-------------------------------------------------------------------*/
method getLoteFeedz() class GPEFeedz
local oItem	as Object
local oResponse := JsonObject():New() as json
local jQReq     :=  oRest:getQueryRequest() as json

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If Valtype(jQReq["prcId"]) != "U" .And. !Empty(jQReq["prcId"]) 

	DbSelectArea("REF")
	REF->(DbSetOrder(2))

	oResponse["items"] := {}

	IF REF->(dbSeek( xFilial("REF") + jQReq["prcId"]))
		oItem := JSonObject():new()
		oItem["lote"] 	 := REF->REF_LOTE
		oItem["retorno"] := If(Empty(REF->REF_RETORN), OemTOAnsi(STR0002), REF->REF_RETORN)
		oItem["data"] 	 := self:getDateFeedz(DtoS(REF->REF_DATINT)) 
		oItem["hora"] 	 := REF->REF_HORINT
		oItem["id"]      := REF->REF_PRCID
		oItem["status"]  := REF->REF_STATUS
		oItem["tipoApi"] := REF->REF_TIPO
		oItem["user"]	 := REF->REF_USER + " - " + UsrRetName(REF->REF_USER)
		aAdd(oResponse["items"], oItem)
	EndIf

	REF->(dbCloseArea())

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
	
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getErroFeedz
API de busca das inconsistencias das integrações

@author	Bruno Costa
@since	30/09/2024
-------------------------------------------------------------------*/
method getErroFeedz() class GPEFeedz
local oItem	as Object
local oJson as Object
local oResponse := JsonObject():New() as json
local jParams   := oRest:getPathParamsRequest() as json
local aErroPeM  := {} as array
local nI as numeric
local cMsn as character

oRest:updateKeyHeaderResponse("Content-Type", "application/json; charset=utf-8")

If Valtype(jParams["id"]) != "U" .And. !Empty(jParams["id"]) 

	DbSelectArea("REF")
	REF->(DbSetOrder(2))

	oResponse["items"] := {}

	IF REF->(dbSeek( xFilial("REF") + jParams["id"]))

		oJson := JsonObject():New()
		oJson:fromJson( REF->REF_RETORN )
		aErroPeM := oJson["errorMessages"]
		If ValType(aErroPeM) == "A"
        	For nI := 1 To Len(aErroPeM)
				cMsn := OemTOAnsi(STR0007) + Iif(ValType(aErroPeM[nI]["integrationId"]) != "U", DecodeUtf8(aErroPeM[nI]["integrationId"]) , "null" ) + " | " +;
						OemTOAnsi(STR0008) + Iif(ValType(aErroPeM[nI]["code"]) != "U", cValToChar( aErroPeM[nI]["code"] ) , "null") + " | " +;
				 		Iif( ValType(aErroPeM[nI]["message"]) != "U", OemTOAnsi(STR0009) + DecodeUtf8(aErroPeM[nI]["message"]), "null") //"ID de integração: "##"Código da validação: "##"Mensagem: "
        	Next nI
		EndIf	 

		oItem := JSonObject():new()
		oItem["status"]  := cMsn
		aAdd(oResponse["items"], oItem)
	
	EndIf

	REF->(dbCloseArea())

	oRest:setResponse(EncodeUTF8(FwCutOff(oResponse:toJson())))
	
EndIf

return

/*-------------------------------------------------------------------
{Protheus.doc} Get getWherePar
Método que retorna a consulta conforme os parâmetros

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
method getWherePar( jPar as json) as character class GPEFeedz
    
local cRet as character

If !Empty(jPar['dataIntDe']) 
	cRet += " AND REF.REF_DATINT >= ?"
EndIf
If !Empty(jPar['dataIntAte'])
	cRet += "  AND REF.REF_DATINT <= ?"
EndIf
If !Empty(jPar['tipoStatus'])
	cRet += " AND REF.REF_STATUS IN (?)"
EndIf 
If !Empty(jPar['tipoApi'])
	cRet += " AND REF.REF_TIPO IN (?)"
EndIf

return cRet

/*-------------------------------------------------------------------
{Protheus.doc} Get getDateFeedz
Método para alterar a data para o tipo lido no PO UI

@author	Bruno Costa
@since	26/09/2024
-------------------------------------------------------------------*/
method getDateFeedz(cDataAlt as character) as character class GPEFeedz

local cData as character

If !Empty(cDataAlt)
	cData := Stuff(Stuff(cDataAlt, 5, 0, "-"), 8, 0, "-")
EndIf

return cData
