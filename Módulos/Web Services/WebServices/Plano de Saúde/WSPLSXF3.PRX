#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"   
#INCLUDE "AP5MAIL.CH"             
/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤┘o    ЁPLSXF3    Ё Autor ЁTotvs 				    Ё Data Ё23.06.2011  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Web Service										            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/                                                                         
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁStrutura do F3
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
WSSTRUCT SRetF3     //q
	WSDATA Campos           		As String
	WSDATA Header           		As Array Of BrwHeader OPTIONAL
	WSDATA MViewReg		 			AS Array Of SViewReg OPTIONAL
	WSDATA CustomHead           AS String
	WSDATA TableEmpty           AS String // mensagem para quando nЦo sЦo encontrados registros
ENDWSSTRUCT                           
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁStrutura de Registros do F3											   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
WSSTRUCT SViewReg
	WSDATA RegCamp1				AS String  OPTIONAL
	WSDATA RegCamp2				AS String  OPTIONAL
    WSDATA RegCamp3		        AS String  OPTIONAL
    WSDATA RegCamp4		        AS String  OPTIONAL
    WSDATA RegCamp5		        AS String  OPTIONAL
    WSDATA RegCamp6		        AS String  OPTIONAL
ENDWSSTRUCT                                     
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁDefinicao do Web Service de Controle Geral	                           
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
WSSERVICE PLSXF3	 				DESCRIPTION "Web Service - F3"
	WSDATA UserCode					AS String                        
	WSDATA UserPlsCode				AS String
	WSDATA TpPor					AS Integer

	WSDATA RegPagina 	    		As Integer OPTIONAL
	WSDATA PaginaIni   	    		As Integer OPTIONAL
	WSDATA Busca					As String  OPTIONAL
	WSDATA BuscaVin					As String  OPTIONAL
	WSDATA VldGen					As String  OPTIONAL
	WSDATA TpBusca					As String  OPTIONAL
	WSDATA FunName   	    		As String  OPTIONAL
	WSDATA RetF3   	    			As SRetF3  OPTIONAL
	WSDATA CampoOri				AS String OPTIONAL
	WSDATA TpGuia					AS String OPTIONAL
	
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁCampos adicionados para F3 generico																 
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	WSDATA AliasGen            As String OPTIONAL
	WSDATA CamposGen           As String OPTIONAL
	WSDATA CondGen             As String OPTIONAL
	WSDATA CodDesGen           As String OPTIONAL
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁMetodo																 
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
    WSMETHOD getF3					DESCRIPTION  "Consulta F3"
    
ENDWSSERVICE
/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбддддддддддд©╠╠
╠╠ЁFun┤└o    ЁgetF3 	ЁAutor  Ё Totvs		            Ё Data Ё07.07.2011 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддаддддддддддд╢╠╠
╠╠Ё          ЁRotina de recuperacao do header						       Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
WSMETHOD getF3 WSRECEIVE UserCode, UserPlsCode, TpPor, FunName, RegPagina, PaginaIni, TpBusca, Busca, VldGen, AliasGen, CamposGen, CondGen, CodDesGen, CampoOri, TpGuia, BuscaVin WSSEND RetF3 WSSERVICE PLSXF3
LOCAL aArea 	:= GetArea()
LOCAL nI		:= 0     
LOCAL nY		:= 0
LOCAL nQtd		:= 0 //(::PaginaIni-1)
LOCAL nSkip		:= 0
LOCAL cSql	 	:= ""
LOCAL aMatReg 	:= {}
LOCAL aRet		:= {}
LOCAL lRetorno	:= .T.
LOCAL cString   := NIL 
LOCAL cEncode   := NIL 
LOCAL cDecode   := NIL 
LOCAL aRetirar  := {"'", ",", "/","*"}

DEFAULT AliasGen	:= "" 
DEFAULT CamposGen	:= ""    
DEFAULT CondGen	:= ""          
DEFAULT CodDesGen	:= ""
DEFAULT CampoOri 	:= ""   
DEFAULT ::TpGuia	:= ""
DEFAULT ::BuscaVin := "" 
     
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁRetira mascara do conteudo da busca
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд

if ExistBlock("PLSPWPCE")
	::Busca := Execblock("PLSPWPCE",.F.,.F.,{::Busca})

Else	
	::Busca := PLRETCARS(::Busca)
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁSe o UsuАrio tem direito												   
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
If PrtChkUser( ::UserCode, "PLSXF3", "getF3" )
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁExecuta consulta  F3
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	aRet := ExecBlock(::FunName, .F., .F., { ::TpBusca, ::Busca, ::VldGen, ::RegPagina ,::UserPlsCode, ::TpPor, ::AliasGen, ::CamposGen, ::CondGen, ::CodDesGen, ::CampoOri, ::TpGuia, ::BuscaVin,.F. } )
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁRetorno
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	cAlias 	:= aRet[1]
	cCampos	:= aRet[2] + ","
	cSql	:= aRet[3]     
	if(len(aRet) > 3)
		::RetF3:CustomHead := aRet[4]
	else
		::RetF3:CustomHead :=  ""
	endif 
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁMonta matriz de campos												   
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	aMatReg := StrToArray( cCampos, ',' )
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁMonta matriz de campos e regcamp
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	For nI:=1 To Len(aMatReg)
		aMatReg[nI] := 	{ aMatReg[nI], 'RegCamp' + cValToChar(nI) }
	Next                  
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁExecuta query														   
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	cSQL := ChangeQuery(cSQL)
	dbUseArea(.T.,"TOPCONN",TCGENQRY(,,cSQL),"Trb",.F.,.T.)
	
	
	//Preciso sempre ter a estrutura para exibir a grid sem dados para o usuario
	//nЦo serА mais necessАrio carregar uma tela para exibir erro quando nao tiver dados
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//ЁMonta a Strutura conforme campos									       
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	if(len(aRet) > 3)
		::RetF3:CustomHead := aRet[4]
	else
		::RetF3:CustomHead :=  ""
	endif 
	aRet := WCHeader(aMatReg)
	::RetF3:Header	:= aRet[1]
	::RetF3:Campos	:= aRet[2]

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	//Ё Alimenta a matriz														
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	If !Trb->( Eof() )
		
		Trb->(DbGoTop())
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
		//ЁQuantidade de registro que deve pular								  
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
	 	nSkip := (::RegPagina * ::PaginaIni) - ::RegPagina
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
		//ЁElimina registros
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
		If nSkip > 0 
			While !(Trb->(EoF())) .AND. nSkip > 0
				Trb->( dbSkip() )
				nSkip--
			EndDo
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
		//Ё While do resultado
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд	
		aRet := {}
		While !Trb->( Eof() )
		
			nQtd++		                                
				
			If nQtd <= ::RegPagina//( (::PaginaIni + ::RegPagina)-1 ) 
				AaDd(aRet, {"","","","","",""} )
			    For nY := 1 To Len(aMatReg)
					aRet[Len(aRet),nY] := &("Trb->"+aMatReg[nY,1])
				Next	
			Else    
				Exit
			EndIf
		Trb->( DbSkip() )
		EndDo   

		if cAlias == "BB0" 
			For nI := 1 To Len(aRet)
				If Len(aRet[nI]) > 0 .And. ValType(aRet[nI][1]) == "C"
					aRet[nI][1] := StrDelChr(aRet[nI][1], aRetirar)
				EndIf
			Next 
		endif 
		       
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Registros																	Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	 	::RetF3:MViewReg := {}                           
	 	If Len(aRet)>0
			For nI := 1 To Len( aRet )
				AaDd( ::RetF3:MViewReg, WsClassNew( "SViewReg" ) )
				::RetF3:MViewReg[nI]:RegCamp1	:= AllTrim(aRet[nI,1])
				If Alltrim(cAlias) == "BTQ" .or. Alltrim(cAlias) == "BR8"
					::RetF3:MViewReg[nI]:RegCamp2	:= decodeUTF8(AllTrim(aRet[nI,2]))
					
					//a funГЦo decodeUTF8 nЦo estА convertendo determinados caracteres, desta forma И necessАrio utilizar a
					//funГЦo LOWERACE para converter a string para minuscula e converter o caracter para ANSI e em seguida
					//usar o encodeUTF8 para conveter a string para UTF-8 e depois usar a funГЦo decodeUTF8 que converte a string
					//para CP1252.
					If ::RetF3:MViewReg[nI]:RegCamp2 == NIL
						
						cString := LOWERACE(AllTrim(aRet[nI,2]))
						cEncode := encodeUTF8(cString)
						cDecode := decodeUTF8(cEncode)
						::RetF3:MViewReg[nI]:RegCamp2 := UPPER(cDecode)
					EndIf 
				Else

					::RetF3:MViewReg[nI]:RegCamp2	:= StrTran(UPPER(AllTrim(aRet[nI,2])) , "'", " ") //etirando aspas simples da stirng para evitar erros no portal
				EndIf
				::RetF3:MViewReg[nI]:RegCamp3 	:= AllTrim(aRet[nI,3])
				::RetF3:MViewReg[nI]:RegCamp4 	:= AllTrim(aRet[nI,4])
				::RetF3:MViewReg[nI]:RegCamp5 	:= AllTrim(aRet[nI,5])
				::RetF3:MViewReg[nI]:RegCamp6 	:= AllTrim(aRet[nI,6])
			Next
	    EndIf
	Else 
		::RetF3:TableEmpty := "NЦo existe registro para esta consulta"
	EndIf		
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Fecha area de trabalho														Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Trb->( DbCloseArea() )
Else
	lRetorno := .F.
	SetSoapFault( "", "NЦo autorizada a utilizaГЦo do metodo" )
EndIf
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁRest na area															   
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
RestArea(aArea)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁFim da Rotina														   
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
Return(lRetorno)
//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
//ЁSomente para poder compilar											   
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд
Function PLSXF3()
Return
