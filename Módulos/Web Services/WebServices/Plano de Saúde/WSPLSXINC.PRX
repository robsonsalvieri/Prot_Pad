#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    PLSXINC    Autor Alexander               Data 16.05.2006  
Ĵ
Descrio  Web Service das funcionalidades do Plano de Saude            
ٱ


/*/
//
//Strutura da matriz de retorno da inclusao dousuario
//
WSSTRUCT SMatRetUsu
	WSDATA Matricula	AS String OPTIONAL
	WSDATA SMatCri		AS Array Of GenericStruct OPTIONAL
ENDWSSTRUCT
//
//Strutura da matriz de inclusao de usuario
//
WSSTRUCT SIncMat
	WSDATA Matricula		AS String OPTIONAL
	WSDATA Nome				AS String OPTIONAL
	WSDATA Sexo				AS String OPTIONAL
	WSDATA DatNas			AS Date   OPTIONAL
ENDWSSTRUCT
//
//Strutura da matriz de inclusao de profissional de saude
//
WSSTRUCT SIncMatProf
	WSDATA Codigo			AS String OPTIONAL
	WSDATA Nome				AS String OPTIONAL
	WSDATA CodSig			AS String OPTIONAL
	WSDATA NumCr			AS String OPTIONAL
	WSDATA Estado			AS String OPTIONAL
	WSDATA CodOpe			AS String OPTIONAL
	WSDATA Cgc  			AS String OPTIONAL
	WSDATA Vinc  			AS String OPTIONAL
	WSDATA Rda  			AS String OPTIONAL
	WSDATA CodLoc  			AS String OPTIONAL
	WSDATA CodEsp  			AS String OPTIONAL
ENDWSSTRUCT
//
//Strutura da matriz de inclusao de profissional de saude
//
WSSTRUCT SIncMatBio
	WSDATA Alias			AS String OPTIONAL
	WSDATA Vida				AS String OPTIONAL
	WSDATA Dig				AS String OPTIONAL
	WSDATA Dig1				AS String OPTIONAL
	WSDATA Dig2				AS String OPTIONAL
ENDWSSTRUCT
//
//Strutura generica de dados
//
WSSTRUCT SDadField
	WSDATA FIELD					AS String OPTIONAL //Campo
	WSDATA VALUE					AS String OPTIONAL //Valor
ENDWSSTRUCT
//
//Strutura da matriz de inclusao de profissional de saude
//
WSSTRUCT SDadBen
	WSDATA DadBenefic			AS Array Of SDadField OPTIONAL
ENDWSSTRUCT
//
//Definicao do Web Service de Inclusao
//
WSSERVICE PLSXINC		   	DESCRIPTION "Incluso Generica"
	WSDATA UserCode			AS String
	WSDATA IncMat           AS SIncMat
	WSDATA IncMatProf       AS SIncMatProf
	WSDATA IncUsu           AS SMatRetUsu
	WSDATA IncMatBio        AS SIncMatBio
	WSDATA WSNULL			AS String
	WSDATA NumAut			AS String
	WSDATA SexoRet          AS String
	WSDATA Matricula        AS String
	WSDATA IncBenefic		AS SDadBen OPTIONAL
	//
	//Metodo
	//
	WSMETHOD SetBenefic				DESCRIPTION "Manutencao de Beneficiario"
	WSMETHOD IncUsuario				DESCRIPTION "Incluso de Usurio"
	WSMETHOD IncPrSaude				DESCRIPTION "Incluso de Profissional de Saude"
	WSMETHOD IncBio					DESCRIPTION "Incluso de Digital (Biometria)"
	WSMETHOD SexoUser				DESCRIPTION "Retorna Sexo do Usuario"
	WSMETHOD AtuBD5 				DESCRIPTION "Grava BD5_GESTAN"

ENDWSSERVICE
/*/

Ŀ
Funo    INCUSUARIOAutor   Alexander              Data 16.05.2006 
Ĵ
          Matriz com dados para inclusao de usuario			   		   
ٱ


/*/
WSMETHOD IncUsuario WSRECEIVE UserCode,IncMat WSSEND IncUsu WSSERVICE PLSXINC
	LOCAL nI            := 1
	LOCAL aArea    		:= GetArea()
	LOCAL aRet			:= {}
	LOCAL aRetDad 		:= {}
	LOCAL aCritica		:= {}
	LOCAL lRetorno 		:= .T.
	//Ŀ
	//Checa Usuario														   
	//
	If PrtChkUser( ::UserCode, "PLSXINC", "IncUsuario" )
		//Ŀ
		//Inclusao de Usuario													   
		//
		aRet := PLSA235(.F.,::IncMat:Matricula,.F.,.T.,::IncMat:Nome,::IncMat:DatNas,.T.,::IncMat:Sexo,.T.)
		//Ŀ
		//Retorno															   	   
		//
		If aRet[1]
			aRetDad := PLSDADUSR(aRet[2],"1",.T.,dDatabase,nil,nil)
			If !aRetDad[1]
				If Len(aRetDad) >= 2
					aCritica := {aClone(aRetDad[2])}
				Endif
			Endif
		Else
			aCritica := {aClone(aRet[2])}
		Endif
		//Ŀ
		//Retorna matricula													   
		//
		::IncUsu := WsClassNew( "SMatRetUsu" )
		If ValType(aRet[2]) <> "A"
			::IncUsu:Matricula	:= aRet[2]
		Else
			::IncUsu:Matricula	:= ""
		EndIf
		//Ŀ
		//Matriz de Criticas													   
		//
		::IncUsu:SMatCri := {}
		For nI:=1 To Len(aCritica)
			AaDd( ::IncUsu:SMatCri , WsClassNew( "GenericStruct" ) )
			::IncUsu:SMatCri[nI]:Code			:= aCritica[nI,1,1]
			::IncUsu:SMatCri[nI]:Description 	:= aCritica[nI,1,2]
		Next
	Else
		SetSoapFault( "", "Usurio no autorizado" )
		lRetorno := .F.
	EndIf

	RestArea( aArea )
	//
	// Retorno do Metodo
	//
Return lRetorno

/*//-------------------------------------------------------------------
{Protheus.doc} IncPrSaude
Matriz com dados para inclusao de profissional de saude.
@since    15.03.2007.			@author	Alexander.
//-------------------------------------------------------------------*/
WSMETHOD IncPrSaude WSRECEIVE UserCode,IncMatProf WSSEND WSNULL WSSERVICE PLSXINC
LOCAL aArea    	 	:= GetArea()
local aRetGrvB0		:= {}
local aRetGrvQ1		:= {}
LOCAL lRetorno 	 	:= .T.
LOCAL lEntrou 	 	:= .F.
LOCAL cCodigo    	:= ""
LOCAL cCodInt		:= PlsIntPad()
LOCAL oObj  	 	:= nil

DEFAULT ::WSNULL 	:= "true|"

If PrtChkUser( ::UserCode, "PLSXINC", "IncPrSaude" )

	oObj := WSPLSXFUN():New()
	IIf (!Empty(PlsGetAuth()),oObj:_HEADOUT :=  { PlsGetAuth() },)
	WsChgURL( @oObj, "PLSXFUN.APW" )

	oObj:cRdaCode := ::IncMatProf:RDA
	oObj:cCodLoc  := ::IncMatProf:CODLOC

	aRetGrvB0 := PlSveProfAll (::IncMatProf:Nome, ::IncMatProf:CodSig, ::IncMatProf:Estado, ::IncMatProf:NumCr, cCodInt, ::IncMatProf:CGC,;
							   ::IncMatProf:Vinc, {|| FwFldGet("BB0_CODOPE") + FwFldGet("BB0_CODIGO") + Modulo11( FwFldGet("BB0_CODOPE") + FwFldGet("BB0_NUMCR"))}, {})

	if aRetGrvB0[1]
		
		If ::IncMatProf:Vinc =="1"
			BB8->(dbsetorder(1))
			If BB8->(MsSeek(xFilial("BB8") + ::IncMatProf:RDA + cCodInt + IncMatProf:CodLoc))
				BAX->(DbSetOrder(1))
				If BAX->(MsSeek(xFilial("BAX") + ::IncMatProf:RDA + cCodInt + ::IncMatProf:CodLoc))
					While !BAX->(Eof()) .and. xFilial("BAX") + ::IncMatProf:RDA + cCodInt + ::IncMatProf:CodLoc == BAX->(BAX_FILIAL+BAX_CODIGO+BAX_CODINT+BAX_CODLOC)
						BC1->( RecLock("BC1",.T.) )
							BC1->BC1_CODIGO 	:= BB8->BB8_CODIGO
							BC1->BC1_ESTCR   	:= Upper(::IncMatProf:Estado)
							BC1->BC1_NUMCR   	:= ::IncMatProf:NumCr
							BC1->BC1_SIGLCR  	:= ::IncMatProf:CodSig
							BC1->BC1_CODPRF  	:= cCodigo
							BC1->BC1_NOMPRF  	:= Upper(::IncMatProf:Nome)
							BC1->BC1_CODINT  	:= BB8->BB8_CODINT
							BC1->BC1_CODLOC  	:= BB8->BB8_CODLOC
							BC1->BC1_CODESP  	:= BAX->BAX_CODLOC
						BC1->( MsUnLock() )
						BAX->(DbSkip())
						lEntrou := iif(!aRetGrvB0[3], .t., .f.)
					Enddo
				Endif
			Endif
		EndIf
		If !Empty(::IncMatProf:CodEsp) .and. PLSAliasExi("BQ1")
			lEntrou := .f.
			aRetGrvQ1 := PlSveEspQ1(cCodInt, aRetGrvB0[2], ::IncMatProf:CodEsp , '', '')

			lEntrou := iif(!aRetGrvB0[3] .and. aRetGrvQ1[2], .t., .f.) //No incluiu profissional, mas adicionou especialidade
		Endif

		if aRetGrvB0[3] //Incluiu na BB0
		 	If oObj:ProfSaud()
				HttpSession->PROSAUD := oObj:OWSPROFSAUDRESULT:OWSLISTPROFSAUD:oWSPROFSAUD
			EndIf
		Endif

		If !aRetGrvB0[3] .and. !aRetGrvQ1[2] .and. !lEntrou
			::WSNULL := "false|Profissional de Saude ja cadastrado"
		Endif
	endif 
Else
	SetSoapFault( "", "Usurio no autorizado" )
	lRetorno := .F.
EndIf

RestArea( aArea )

Return lRetorno

/*/

Ŀ
Funo    IncBio    Autor   Alexander Santos       Data 20.01.2009 
Ĵ
          Inclusao de digital ( biometria )							   |
ٱ


/*/
WSMETHOD IncBio WSRECEIVE UserCode,IncMatBio WSSEND WSNULL WSSERVICE PLSXINC
	LOCAL aArea    	 	:= GetArea()
	LOCAL lRetorno 	 	:= .T.
	DEFAULT ::WSNULL 	:= "true|"
	//Ŀ
	//Checa Usuario														   
	//
	If PrtChkUser( ::UserCode, "PLSXINC", "IncBio" )
		//Ŀ
		// Inclui de digitais														
		//
		If !GrvDigitais( { {::IncMatBio:Dig1,::IncMatBio:Dig2} }, .F., ::IncMatBio:Alias, ::IncMatBio:Vida, ::IncMatBio:Dig)
			::WSNULL := "false|Ja existe digital para este Usurio"
		Else
			::WSNULL := "true|"
		EndIf

	Else
		SetSoapFault( "", "Usurio no autorizado" )
		lRetorno := .F.
	EndIf

	RestArea( aArea )
	//
	// Retorno do Metodo
	//
Return lRetorno
/*


ͻ
Programa  ATUBD5    Autor  Microsiga            Data   12/18/09   
͹
Desc.      Atualiza o campo BD5_GESTAN                                
ͼ


*/
WSMETHOD AtuBD5 WSRECEIVE UserCode,NumAut WSSEND WSNULL WSSERVICE PLSXINC
	LOCAL aArea    		:= GetArea()
	LOCAL lRetorno 		:= .T.
	DEFAULT ::WSNULL 	:= "true|"
	//Ŀ
	//Checa Usuario														   
	//
	If PrtChkUser( ::UserCode, "PLSXINC", "AtuBD5" )
		//Ŀ
		// Inclui profissional de saude												
		//
		BE2->( DbSetOrder(1) )
		If BE2->( MsSeek( xFilial("BE2")+::NumAut ) )
			If BD5->(MsSeek(xFilial("BD5")+BE2->(BE2_OPEMOV+BE2_CODLDP+BE2_CODPEG+BE2_NUMERO)))
				BD5->(Reclock("BD5",.F.))

				BD5->BD5_GESTAN:= "1"

				BD5->(MsUnlock())
			Else
				::WSNULL := "false|Guia no encontrada"
			Endif
		Else
			::WSNULL := "false|Autorizacao nao encontrada"
		Endif

	Else
		lRetorno:=.F.
		SetSoapFault( "", "Usurio no autorizado" )
	EndIf

	RestArea( aArea )
	//
	// Retorno do Metodo
	//
Return lRetorno
/*


ͻ
Programa  SexoUser  Autor  Microsiga            Data   12/18/09   
͹
Desc.      Verifica o sexo do beneficiario                            
ͼ


*/
WSMETHOD SexoUser WSRECEIVE UserCode,Matricula WSSEND SexoRet WSSERVICE PLSXINC
	LOCAL lRetorno := .T.
	LOCAL aArea    := GetArea()

	//Verifica se tem acesso
	If PrtChkUser( ::UserCode, "PLSXINC", "SexoUser" )

		::SexoRet := ""

		BA1->(DbSetorder(2))
		if BA1->(MsSeek(xFilial("BA1") + ::Matricula))
			::SexoRet := BA1->BA1_SEXO
		endIf

	Else
		lRetorno := .F.
		SetSoapFault( "", "Usurio no autorizado" )
	EndIf

	RestArea( aArea )

Return lRetorno
/*


ͻ
Programa  SetBeneficAutor  Microsiga            Data   12/18/09   
͹
Desc.      Movimentacao de beneficiario								  
ͼ


*/
WSMETHOD SetBenefic WSRECEIVE UserCode,IncBenefic WSSEND WSNULL WSSERVICE PLSXINC
	LOCAL lRetorno 	:= .T.
	LOCAL nI		:= 0
	LOCAL aArea    	:= GetArea()
	LOCAL aDadBen	:= {}
	DEFAULT ::WSNULL:= "true|"
	//
	// Verifica se tem acesso
	//
	If PrtChkUser( ::UserCode, "PLSXINC", "SetBenefic" )
		//
		// Monta matriz de envio para processamento
		//
		For nI:=1 To Len(IncBenefic:DADBENEFIC)
			AaDd(aDadBen,{IncBenefic:DADBENEFIC[nI]:Field,IncBenefic:DADBENEFIC[nI]:Value})
		Next
		//
		// Executa rdmake
		//
		aRet := ExecBlock('PLSPROBEN', .F., .F., { aDadBen } )

		If !aRet[1]

			::WSNULL += "false|Falha no processamento|"

			For nI:=1 To Len(aRet[2])
				::WSNULL += WCTextNeg(aRet[2,nI,1] + " - " + aRet[2,nI,2],.F.)
			Next

		Else
			::WSNULL += "true|Processado com Sucesso|" + aRet[2]
		EndIf

	Else
		lRetorno := .F.
		SetSoapFault( "", "Usurio no autorizado" )
	EndIf

	RestArea( aArea )
	//
	// Retorno do Metodo
	//
Return lRetorno
//Ŀ
//Para poder compilar													   
//
Function WSPLSXINC
Return
