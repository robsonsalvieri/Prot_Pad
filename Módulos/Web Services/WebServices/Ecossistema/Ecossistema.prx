#INCLUDE "Ecossistema.ch"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "SHELL.CH"
#INCLUDE "APWIZARD.CH"

Static Function GetHardLock()

Static __HardLock 

DEFAULT __HardLock:= LS_GetID()

Return(__HardLock)

/*/


Ŀ
Programa  Ecossistem Autor Eduardo Riera           Data 05.12.2003  
Ĵ
Descrio  Rotina de integracao e divulgacao do Ecossistema             
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        Generico                                                     
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
/*/

Ŀ
Funo    ShowEco   Autor   Eduardo Riera          Data 05.12.2003 
Ĵ
          Rotina de divulgacao dos parceiros do sistema                
                                                                       
Ĵ
ParametrosExpC1: Nome do Parceiro                                (OPC) 
                                                                       
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta funcao abre a URL de divulgacao do Ecossistema          
                                                                       
                                                                       
Ĵ
Uso        Generico                                                    
ٱ


/*/
Function ShowEco(cParceiro,cServiceID)

Local aArea         := GetArea()

Local cErroWS       := ""
Local cCodErro      := ""
Local cErroText     := ""              
Local cAviso        := "" 

Local lHelp         := .F.
Local lMicrosiga    := .T.

Local oWsMsEco      := WSMSECOSYSTEM():New()
Local oWSPartnerEco := WSPARTNERECOSYSTEM():New()

DEFAULT cParceiro := ""
DEFAULT cServiceID:= ""
WsChgUrl(@oWSMsEco,"MSECOSYSTEM.apw","UrlEcossistema")
cParceiro := Upper(cParceiro)

//Ŀ
//Posiciona o SIGAMAT                                                     
//
dbSelectArea("SM0")
dbSetOrder(1)
MsSeek(cEmpAnt+cFilAnt)

//Ŀ
//Verifica o HandShake da Microsiga                                       
//
lMicrosiga := EcoHandShake("MICROSIGA","SHOWECO")
If lMicrosiga
	Do Case
		Case !Empty(cParceiro)
			If oWSMsEco:GetPartners(cParceiro)
				oWSPartnerEco:_URL               := oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUrlLocation
				oWSPartnerEco:cHARDLOCK          := AllTrim(Str(Gethardlock(),20))
				EcoSigaMat( oWSPartnerEco:oWSCOMPANY )
				EcoUsuario( oWSPartnerEco:oWSCONTACT )
				oWSPartnerEco:cUserID            := oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUserID
				oWSPartnerEco:cPassWord          := oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cPassWord
				oWSPartnerEco:cServiceID         := cServiceID
				WSSetcAction(oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUrlSoapAction+"/GETPARTNERWEB")  
				
				//Ŀ
				// Se o retorno foi positivo, dispara o browser com a URL do parceiro     
				//
				If oWSPartnerEco:GetPartnerWeb()
					ShellExecute("open",oWSPartnerEco:cGETPARTNERWEBRESULT,"","",SW_SHOW)
				Else
					
					If cParceiro == "ACSP"
						
						cErroWS   := GetWsCError( 3 )
						
						//Ŀ
						// Localiza o codigo de erro apos a palavra "GETPARTNERWEB"               
						//
						cCodErro  := SubStr( cErroWS, 17, 2 ) 
						cErroText := SubStr( cErroWS, 20, Len( cErroWs ) - 19 ) 
						
						//Ŀ
						// Caso o GetPartnerWeb retorne erro, analisa o retorno                          
						//
						If cCodErro == "89"
							//Ŀ
							// Retorno 89 significa que existe alguma restricao por parte da ACSP            
							// Ex: empresa nao idonea, empresa ja eh cliente da ACSP etc...                  				
							//
							cAviso := STR0037 + Chr( 13 ) + Chr( 10 ) + cErroText // "Mensagem da ACSP ( Associacao comercial de Sao Paulo ) : "
						ElseIf cCodErro == "99"
							//Ŀ
							// Retorno 99 significa que existe alguma inconsistencia nos dados enviados      
							// pelo sistema Microsiga.                                                       				   
							// Ex: CNPJ invalido, dados obrigatorios nao enviados ( endereco, etc... )       								
							//
							cAviso := STR0038 // "POR FAVOR ENTRE EM CONTATO COM A MICROSIGA" 
						Else
							cAviso := cErrowS
						EndIf 	                                                                                                
						
						Aviso( STR0039, cAviso, { STR0040 }, 2 ) //"Ecossistema - GetPartnerWeb", "Ok"
						
						lHelp := .F.	
						
					Else
						lHelp := .T.
					EndIf  
					
				EndIf      
				
				
				
			Else
		    	lHelp := .T.
		 	EndIf
		OtherWise
			oWSMsEco:cHARDLOCK := AllTrim(Str(Gethardlock(),20))
			EcoSigaMat( oWSMsEco:oWSCOMPANY )
			EcoUsuario( oWSMsEco:oWSCONTACT )
			
			If oWSMsEco:GetEcoWeb( oWSMsEco:cHARDLOCK, oWSMsEco:oWSCOMPANY, oWSMsEco:oWSCONTACT )
				ShellExecute("open",oWSMsEco:cGETECOWEBRESULT,"","",SW_SHOW)
			Else
				lHelp := .T.
			EndIf
	EndCase
Else
	lHelp := .T.
EndIf
If lHelp
	HelProg(,"ECOSSISTEMA")
EndIf
RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    EcoHandShaAutor   Eduardo Riera          Data 15.12.2003 
Ĵ
          Esta funcao realiza o HandShake com os parceiros do Ecossis- 
          tema                                                         
Ĵ
ParametrosExpC1: Nome do Parceiro                                      
          ExpC2: Codigo do Servico                                (OPC)
          ExpC3: Variavel para retorno do Login do Parceiro       (OPC)
          ExpC4: Variavel para retorno da Senha do Parceiro       (OPC)
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta funcao realiza os procedimentos de HandShake e controle 
          de falha de comunicacao com os parceiros do Ecossistema      
                                                                       
Ĵ
Uso        Generico                                                    
ٱ


/*/
Function EcoHandShake(cParceiro,cServico,cLogin,cSenha,lRemLog)

Local aArea       	:= GetArea()
Local aHandShake  	:= {}
Local aCNPJ       	:= {}                                                	
Local nHandShake  	:= 0
Local cHardLock   	:= AllTrim(Str(Gethardlock(),20))
Local lRetorno    	:= .F.
Local lMicrosiga  	:= .F.
Local lHandPartner	:= .F.
Local lRun        	:= .F.
Local lConectou   	:= .F.
Local nX          	:= 0
Local oHandShake
Local oWsMsEco      := WSMSECOSYSTEM():New()
Local cURLSoap		:= ""

DEFAULT cServico := ""
DEFAULT cLogin   := ""
DEFAULT cSenha   := ""
DEFAULT lRemLog  := .T.

//Ŀ
//Posiciona o SIGAMAT                                                     
//
dbSelectArea("SM0")
dbSetOrder(1)
MsSeek(cEmpAnt+cFilAnt)

//Ŀ
//Verifica se o HandShake ja foi efetuado                                 
//
GetGlbVars("aEcoSystemHandShake",aHandShake)

If lRemLog
	If !Empty(aHandShake)
		If !cParceiro == "MICROSIGA"

			nHandShake := aScan(aHandShake,{|x| x[1] == cParceiro .And.;
			x[2] == SM0->M0_CGC .And.;
			x[3] == cHardLock .And.;
			x[4] == cServico})
			
		Else
			nHandShake := aScan(aHandShake,{|x| x[1] == cParceiro .And.;
			x[2] == SM0->M0_CGC .And.;
			x[3] == cHardLock .And.;
			x[4] == cServico .And.;
			x[5] == dDataBase})
		EndIf
	Else
		aHandShake := {}
	EndIf
EndIf
//Ŀ
//Faz o HandShake                                                         
//
If nHandShake == 0
	//Ŀ
	//Faz o HandShake com a Microsiga                                         
	//
	WsChgUrl(@oWSMsEco,"MSECOSYSTEM.apw","UrlEcossistema")
	lMicrosiga := oWSMsEco:HandShake(cHardLock,SM0->M0_CGC)
	DEFAULT lMicrosiga := .F.
	If lMicrosiga
		lMicrosiga := oWSMsEco:lHandShakeResult
		lConectou := .T.
	EndIf
	//Ŀ
	//Verifica por quanto tempo falhou o HandShake com a Microsiga            
	//
	If !lConectou
		nHandShake := aScan(aHandShake,{|x| x[1] == cParceiro .And. x[2] == SM0->M0_CGC .And. x[3] == cHardLock .And. x[4] == cServico .And. x[5]+7 >= dDataBase})	
		If nHandShake <> 0
			lMicrosiga := .T.
		Else
			Aviso(STR0001,STR0002,{"Ok"}) //"Ecossistema - HandShake"###"Falha de comunicacao com a Microsiga"
			lRetorno := .F.	
		EndIf
	EndIf
	//Ŀ
	//Verifica se nunca foi feito HandShake com a Microsiga                   
	//	
	If !lMicrosiga .And. lConectou
		//Ŀ
		//Wizard de habilitacao das empresas                                      
		//
		EcoWizard(@aCNPJ)
		lMicrosiga := oWSMsEco:HandShake(cHardLock,SM0->M0_CGC)
		DEFAULT lMicrosiga := .F.
		If lMicrosiga
			lMicrosiga := oWSMsEco:lHandShakeResult
		EndIf
		If lMicrosiga
			For nX := 1 To Len(aHandShake)
				If aHandShake[nX] <> Nil .And. aHandShake[nX][1] == "MICROSIGA"
					aHandShake := aDel(aHandShake,nX)
				EndIf
			Next nX
			For nX := Len(aHandShake) To 1 STEP -1
				If aHandShake[nX] == Nil
					aHandShake := aSize(aHandShake,nX-1)
				Else
					nX := 0					
				EndIf
			Next nX
			For nX := 1 To Len(aCNPJ)
				//Ŀ
				//Atualiza a variavel global do HandShake                                 
				//
				aadd(aHandShake,{"MICROSIGA",aCNPJ[nX],cHardLock,"SHOWECO",dDataBase,"","",""})
			Next nX
			PutGlbVars("aEcoSystemHandShake",aHandShake)
		EndIf
	EndIf
	If lMicrosiga
		Do Case
			Case cParceiro == "MICROSIGA"
		 		lRetorno := .T.	
			Case !Empty(cParceiro)
				lRun := oWSMsEco:GetPartners(cParceiro)
				If lRun
					oHandShake:= WSPARTNERECOSYSTEM():New()
					cURLSoap			  := oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUrlSoapAction
					oHandShake:_URL       := oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUrlLocation
					WSSetcAction(oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUrlSoapAction+"/HANDSHAKE")
				    
					oHandShake:cHARDLOCK  := cHardLock
					oHandShake:cFEDERALID := SM0->M0_CGC
					oHandShake:cUSERID    := IIf( Empty( cLogin ), oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cUserID, cLogin )
					oHandShake:cPASSWORD  := IIf( Empty( cSenha ), oWSMsEco:oWSGETPARTNERSRESULT:oWSECOPARTNERSVIEW[1]:cPassWord, cSenha )

					oHandShake:cSERVICEID := cServico
					lHandPartner := oHandShake:HandShake()
					If lHandPartner
						lRetorno := .T.
						//Ŀ
						//Tratamento da Habilitacao do Sistema pelo Parceiro                      
						//
						cSenha := oHandShake:oWSHANDSHAKERESULT:cPassWord
						cLogin := oHandShake:oWSHANDSHAKERESULT:cUserID
					Else
						If cParceiro == "ACSP" 
							Aviso( STR0001, GetWsCError( 3 ), { "Ok" } ) //"Ecossistema - HandShake"
							lRetorno := .F.
						Else
							If oHandShake:GetPartnerWeb(cHardLock,EcoSIGAMAT(),EcoUsuario(),,,cServico)
//								ShellExecute("open",oHandShake:Dynamichomepage,"","",SW_SHOW)
								ShellExecute("open",oHandShake:cGETPARTNERWEBRESULT,"","",SW_SHOW)
							Else
								Aviso(STR0001,STR0003,{"Ok"}) //"Ecossistema - HandShake"###"Falha de comunicacao"
							EndIf
						EndIf
					EndIf
				Else
					Aviso("Ecossistema - HandShake",STR0002,{"Ok"}) //"Falha de comunicacao com a Microsiga"
					lRetorno := .F.
				EndIf
		EndCase
	Else
		lRetorno := .F.	
	EndIf
Else
	//Ŀ
	//Tratamento da Habilitacao do Sistema pelo Parceiro                      
	//
	If !cParceiro == "MICROSIGA"
		lRetorno := EcoHandShake("MICROSIGA","SHOWECO")
		If lRetorno
		
			If Empty( cLogin )                   
				cLogin := aHandShake[nHandShake][6]
				cSenha := aHandShake[nHandShake][7]
			EndIf 
				
		EndIf
	Else
		lRetorno := .T.
	EndIf
EndIf
If lRetorno
	//Ŀ
	//Atualiza a variavel global do HandShake                                 
	//
	If nHandShake == 0 .Or. cParceiro == "MICROSIGA"
		If cParceiro == "MICROSIGA"
			nHandShake := aScan(aHandShake,{|x| x[1] == cParceiro .And. x[2] == SM0->M0_CGC .And. x[3] == cHardLock .And. x[4] == cServico})
			If nHandShake == 0
				aadd(aHandShake,{cParceiro,SM0->M0_CGC,cHardLock,cServico,dDataBase,"","",""})
			Else
				aHandShake[nHandShake][5] := dDataBase
			EndIf
		Else
			If !Empty(cLogin)
				If lRemLog
					aadd(aHandShake,{cParceiro,SM0->M0_CGC,cHardLock,cServico,Ctod(""),cLogin,cSenha,,cURLSoap})
				EndIf
			EndIf
		EndIf
		PutGlbVars("aEcoSystemHandShake",aHandShake)
	EndIf
	//Ŀ
	//Realiza a Bilhetagem                                                    
	//
	If cParceiro <> "MICROSIGA"
		If !EcoPostage(cParceiro,cServico)
			lRetorno := .F.
			Aviso(STR0001,STR0002,{"Ok"}) //"Ecossistema - HandShake" //"Falha de comunicacao com a Microsiga"
		EndIf
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    EcoSigaMatAutor   Eduardo Riera          Data 15.12.2003 
Ĵ
          Inicializa o objeto de empresa com base no SM0 posicionado   
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpO1: Objeto preenchido com os dados do cliente             
                                                                       
Ĵ
Descrio Esta rotina devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
Static Function EcoSigaMat( oCompany ,cCodEmpFil)

DEFAULT oCompany := PARTNERECOSYSTEM_COMPANYVIEW():New()

dbSelectArea("SM0")
If Empty(cCodEmpFil)
	If SetMDi()
		MsSeek(oApp:cMdiEmpFil)
	Else
		MsSeek(cEmpAnt+cFilAnt)
	EndIf
Else
	MsSeek(cCodEmpFil)
EndIf

oCompany:cCode     := SM0->M0_CODIGO+SM0->M0_CODFIL
oCompany:cName     := SM0->M0_NOMECOM
oCompany:cNickName := SM0->M0_NOME
oCompany:cBranch   := SM0->M0_FILIAL
oCompany:cFederalId:= SM0->M0_CGC
oCompany:cStateId  := SM0->M0_INSC
oCompany:oWSAddresses := PARTNERECOSYSTEM_ARRAYOFADDRESSVIEW():New()
oCompany:oWSAddresses:oWSADDRESSVIEW := {}
aadd(oCompany:oWSAddresses:oWSADDRESSVIEW,PARTNERECOSYSTEM_ADDRESSVIEW():New())
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cTypeOfAddress := "1"
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cAddress       := FisGetEnd(SM0->M0_ENDCOB)[1]
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cAddressNumber := Str(FisGetEnd(SM0->M0_ENDCOB)[2],10)
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cDistrict      := SM0->M0_CIDCOB
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cState         := SM0->M0_ESTCOB
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cZipCode       := SM0->M0_CEPCOB
oCompany:oWSAddresses:oWSADDRESSVIEW[1]:cZone          := SM0->M0_BAIRCOB
aadd(oCompany:oWSAddresses:oWSADDRESSVIEW,WsClassNew("PARTNERECOSYSTEM_ADDRESSVIEW"))
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cTypeOfAddress := "2"
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cAddress       := FisGetEnd(SM0->M0_ENDENT)[1]
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cAddressNumber := Str(FisGetEnd(SM0->M0_ENDENT)[2],10)
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cDistrict      := SM0->M0_CIDENT
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cState         := SM0->M0_ESTENT
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cZipCode       := SM0->M0_CEPENT
oCompany:oWSAddresses:oWSADDRESSVIEW[2]:cZone          := SM0->M0_BAIRENT
oCompany:oWSPhones := WsClassNew("PARTNERECOSYSTEM_ARRAYOFPHONEVIEW")
oCompany:oWSPhones:oWsPhoneView := {}
aadd(oCompany:oWSPhones:oWSPhoneView,WsClassNew("PARTNERECOSYSTEM_PHONEVIEW"))
oCompany:oWSPhones:oWSPhoneView[1]:cTypeOfPhone      := "1"
oCompany:oWSPhones:oWSPhoneView[1]:cPhoneNumber      := AllTrim(Str(FisGetTel(SM0->M0_TEL)[3],15))
oCompany:oWSPhones:oWSPhoneView[1]:cLocalAreaCode    := AllTrim(Str(FisGetTel(SM0->M0_TEL)[2],15))
oCompany:oWSPhones:oWSPhoneView[1]:cCountryAreaCode  := AllTrim(Str(FisGetTel(SM0->M0_TEL)[1],15))
aadd(oCompany:oWSPhones:oWSPhoneView,WsClassNew("PARTNERECOSYSTEM_PHONEVIEW"))
oCompany:oWSPhones:oWSPhoneView[2]:cTypeOfPhone      := "2"
oCompany:oWSPhones:oWSPhoneView[2]:cPhoneNumber      := AllTrim(Str(FisGetTel(SM0->M0_FAX)[3],15))
oCompany:oWSPhones:oWSPhoneView[2]:cLocalAreaCode    := AllTrim(Str(FisGetTel(SM0->M0_FAX)[2],15))
oCompany:oWSPhones:oWSPhoneView[2]:cCountryAreaCode  := AllTrim(Str(FisGetTel(SM0->M0_FAX)[1],15))

Return(oCompany)

/*/

Ŀ
Funo    EcoUsuarioAutor   Eduardo Riera          Data 15.12.2003 
Ĵ
          Inicializa o objeto de usuario com base no Logon do sistema  
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpO1: Objeto preenchido com os dados do cliente             
                                                                       
Ĵ
Descrio Esta rotina devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
Function EcoUsuario( oUsuario )

Local cUser    := IIf(SetMdi(),oApp:cUserName,AllTrim(Substr(cUsuario,7,15)))
Local aUser    := {}
PswOrder(2)
PswSeek(cUser)
aUser := PswRet(1)

DEFAULT oUsuario := WsClassNew("PARTNERECOSYSTEM_COMPANYUSERVIEW")
oUsuario:cUserID                      := aUser[1][01]
oUsuario:cName                        := aUser[1][04]
oUsuario:cFederalId                   := ""
oUsuario:cEmail                       := aUser[1][14]
oUsuario:cPositionDescription         := aUser[1][13]
oUsuario:cGroupDescription            := ""
oUsuario:cDepartmentDescription       := aUser[1][12]
oUsuario:oWSAddresses := PARTNERECOSYSTEM_ARRAYOFADDRESSVIEW():New()
oUsuario:oWSAddresses:oWSADDRESSVIEW := {}
aadd(oUsuario:oWSAddresses:oWSADDRESSVIEW,PARTNERECOSYSTEM_ADDRESSVIEW():New())
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cTypeOfAddress := "1"
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cAddress       := FisGetEnd(SM0->M0_ENDCOB)[1]
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cAddressNumber := Str(FisGetEnd(SM0->M0_ENDCOB)[2],10)
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cDistrict      := SM0->M0_CIDCOB
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cState         := SM0->M0_ESTCOB
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cZipCode       := SM0->M0_CEPCOB
oUsuario:oWSAddresses:oWSADDRESSVIEW[1]:cZone          := SM0->M0_BAIRCOB
aadd(oUsuario:oWSAddresses:oWSADDRESSVIEW,WsClassNew("PARTNERECOSYSTEM_ADDRESSVIEW"))
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cTypeOfAddress := "2"
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cAddress       := FisGetEnd(SM0->M0_ENDENT)[1]
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cAddressNumber := Str(FisGetEnd(SM0->M0_ENDENT)[2],10)
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cDistrict      := SM0->M0_CIDENT
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cState         := SM0->M0_ESTENT
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cZipCode       := SM0->M0_CEPENT
oUsuario:oWSAddresses:oWSADDRESSVIEW[2]:cZone          := SM0->M0_BAIRENT
oUsuario:oWSPhones := WsClassNew("PARTNERECOSYSTEM_ARRAYOFPHONEVIEW")
oUsuario:oWSPhones:oWsPhoneView := {}
aadd(oUsuario:oWSPhones:oWSPhoneView,WsClassNew("PARTNERECOSYSTEM_PHONEVIEW"))
oUsuario:oWSPhones:oWSPhoneView[1]:cTypeOfPhone      := "1"
oUsuario:oWSPhones:oWSPhoneView[1]:cPhoneNumber      := AllTrim(Str(FisGetTel(SM0->M0_TEL)[3],15))
oUsuario:oWSPhones:oWSPhoneView[1]:cLocalAreaCode    := AllTrim(Str(FisGetTel(SM0->M0_TEL)[2],15))
oUsuario:oWSPhones:oWSPhoneView[1]:cCountryAreaCode  := AllTrim(Str(FisGetTel(SM0->M0_TEL)[1],15))
aadd(oUsuario:oWSPhones:oWSPhoneView,WsClassNew("PARTNERECOSYSTEM_PHONEVIEW"))
oUsuario:oWSPhones:oWSPhoneView[2]:cTypeOfPhone      := "2"
oUsuario:oWSPhones:oWSPhoneView[2]:cPhoneNumber      := AllTrim(Str(FisGetTel(SM0->M0_FAX)[3],15))
oUsuario:oWSPhones:oWSPhoneView[2]:cLocalAreaCode    := AllTrim(Str(FisGetTel(SM0->M0_FAX)[2],15))
oUsuario:oWSPhones:oWSPhoneView[2]:cCountryAreaCode  := AllTrim(Str(FisGetTel(SM0->M0_FAX)[1],15))


Return(oUsuario)

/*/

Ŀ
Funo    OpenRepo   Autor  Eduardo Riera          Data 10.01.2003 
Ĵ
          Rotina de inicializacao do repositorio do Ecossistema        
                                                                       
Ĵ
ParametrosExpL1: Abre compartilhado                                    
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio                                                              
                                                                       
                                                                       
Ĵ
Uso       Ecossistema                                                  
ٱ


/*/
Static Function OpenRepo(lShared)

Local aArea     := GetArea()
Local aNew      := {}
Local aOld      := {}
Local nX        := 0
Local nY        := 0
Local cRepName  := ""
Local cIndRep   := cRepName
Local lNewStru  := .F.
Local lRetorno  := .T.

DEFAULT lShared := .T.

If ( Select("MSECOSYS") == 0 )
	cRepName := RetArq(__LocalDrive,"MSECOSYS",.T.)
	//Ŀ
	//Inicializa a estrutura do repositorio                                   
	//
	aadd(aNew,{"ECO_HLOCK" ,"C",20,00})
	aadd(aNew,{"ECO_CODEMP","C",02,00})
	aadd(aNew,{"ECO_CODFIL","C",02,00})
	aadd(aNew,{"ECO_DATA"  ,"D",08,00})
	aadd(aNew,{"ECO_TIME"  ,"C",08,00})
	aadd(aNew,{"ECO_STATUS","C",01,00})
	aadd(aNew,{"ECO_PARTNE","C",10,00})
	aadd(aNew,{"ECO_SERVID","C",80,00})
	//Ŀ
	//Verifica se o arquivo existe                                            
	//
	If ( !MsFile(cRepName,,__LocalDrive) )
		dbCreate(cRepName,aNew,__LocalDrive)
	EndIf
	//Ŀ
	//Abre o repositorio                                                      
	//
	dbUseArea(.T.,__LocalDrive,cRepName,"MSECOSYS",lShared,.F.)
	If ( !NetErr() )
		//Ŀ
		//Verifica se a estrutura deve ser ajustada                               
		//
		aOld := dbStruct()
		If ( Len(aNew) <> Len(aOld) )
			lNewStru := .T.
		Else
			For nX := 1 To Len(aNew)
				nY := aScan(aOld,{|x| x[1]==aNew[nX][1]})
				If ( nY <> 0 )
					If (aNew[nX][2]<>aOld[nY][2].Or.aNew[nX][3]<>aOld[nY][3].Or.aNew[nX][4]<>aOld[nY][4])
						lNewStru := .T.
						Exit
					EndIf
				Else
					lNewStru := .T.
					Exit
				EndIf
			Next nX
		EndIf
		If ( lNewStru )
			dbSelectArea("MSECOSYS")
			dbCloseArea()
			dbUseArea(.T.,__LocalDrive,cRepName,"MSECOSYS",.F.,.F.)
			If ( !NetErr() )
				dbCreate("MSECO.NEW",aNew,__LocalDrive)
				dbUseArea(.T.,__LocalDrive,"MSECO.NEW","NEW",.F.,.F.)
				dbSelectArea("MSECOSYS")
				dbGotop()
				While ( !Eof() )
					dbSelectArea("NEW")
					dbAppend(.T.)
					For nX := 1 To FCount()
						nY := MSECOSYS->(FieldPos(NEW->(FieldName(nX))))
						FieldPut(nX,MSECOSYS->(FieldGet(nY)))
					Next nX
					dbRUnLock()
					dbSelectArea("MSECOSYS")
					dbSkip()
				EndDo
				dbSelectArea("NEW")
				dbCloseArea()
				dbSelectArea("MSECOSYS")
				dbCloseArea()
				FRename(cRepName,"MSECO.OLD")
				If ( FError() <> 0 )
					ConOut(STR0004) //"Erro: Falha na tentativa de ajustar o Repositorio"
					lRetorno := .F.
				Else
					FRename("MSECO.NEW",cRepName)
					If ( FError() == 0 )
						FErase("MSECO.OLD")
					EndIf
					dbUseArea(.T.,__LocalDrive,cRepName,"MSECOSYS",.F.,.F.)
				EndIf
			Else
				ConOut(STR0004) //"Erro: Falha na tentativa de ajustar o Repositorio"
				lRetorno := .F.
			EndIf
		EndIf
		//Ŀ
		//Verifica a existencia do indice.                                        
		//
		dbSelectArea("MSECOSYS")
		cIndRep := "MSECOSYS"
		cIndRep := RetArq(__LocalDrive,cIndRep,.F.)
		If ( !MsFile(cRepName,cIndRep,__LocalDrive) )
			INDEX ON ECO_STATUS+ECO_HLOCK+ECO_CODEMP+ECO_CODFIL TAG &(RetFileName(cIndRep)) TO &(FileNoExt(cRepName))
		Else
			dbSetIndex(cIndRep)
		EndIf
	Else
		lRetorno := .F.
	EndIf
EndIf
If ( AllTrim(aArea[1]) <> "" )
	RestArea(aArea)
EndIf
Return(lRetorno)

/*/

Ŀ
Funo    CloseRepo  Autor  Eduardo Riera          Data 10.01.2003 
Ĵ
          Rotina de fechamento do Repositorio                          
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio                                                              
                                                                       
                                                                       
Ĵ
Uso       Ecossistema                                                  
ٱ


/*/
Static Function CloseRepo()

Local aArea    := GetArea()
Local cRepName := RetArq(__LocalDrive,"MSECOSYS",.T.)

dbSelectArea("MSECOSYS")
dbCloseArea()
//Ŀ
//Verifica se o arquivo existe                                            
//
If ( MsFile(cRepName,,__LocalDrive) )
	If OpenRepo(.F.)
		dbSelectArea("MSECOSYS")
		If !MsSeek("1")
			ZAP
		EndIf
		dbCloseArea()
	Else
		If Select("MSECOSYS")
			dbCloseArea()
		EndIf
	EndIf
EndIf
If aArea[1] <> "" .And. aArea[1] <> "MSECOSYS"
	RestArea(aArea)
Else
	dbSelectArea("SX3")
EndIf
Return(.T.)

/*/

Ŀ
Funo    EcoPostage Autor  Eduardo Riera          Data 10.01.2003 
Ĵ
          Rotina de bilhetagem do Ecossistema                          
                                                                       
Ĵ
ParametrosExpC1: Codigo do Parceiro                                    
          ExpC2: Codigo do Servico                                     
Ĵ
Retorno   ExpL1: Indica que a operacao de comunicacao do parceiro pode 
                 ser realizada                                         
Ĵ
Descrio Esta funcao faz a bilhetagem do ecossistema. Em caso de      
          interrupcao da comunicacao com a Microsiga, por mais de cinco
          dias o sistema interrompe a comunicacao com o parceiro       
Ĵ
Uso       Ecossistema                                                  
ٱ


/*/
Function EcoPostage(cParceiro,cServiceID)

Local aArea        := GetArea()
Local aAreaSM0     := SM0->(GetArea())
Local oMsEcoSystem := WSMSECOSYSTEM():New()
Local lEnviou      := .F.
Local lRetorno     := .T.
//Ŀ
//Verifica a comunicacao                                                  
//
WsChgUrl(@oMsEcoSystem,"MSECOSYSTEM.apw")
oMsEcoSystem:cHARDLOCK  := AllTrim(Str(Gethardlock(),20))
EcoSigaMat(oMsEcoSystem:oWSCOMPANY)
oMsEcoSystem:cPARTNERID := cParceiro
oMsEcoSystem:cSERVICEID := cServiceID
If oMsEcoSystem:PutPostage()
	lEnviou := .T.
EndIf
//Ŀ
//Abre o repositorio                                                      
//
OpenRepo()
If !lEnviou
	RecLock("MSECOSYS",.T.)
	MSECOSYS->ECO_HLOCK := AllTrim(Str(Gethardlock(),20))
	MSECOSYS->ECO_CODEMP:= cEmpAnt
	MSECOSYS->ECO_CODFIL:= cFilAnt
	MSECOSYS->ECO_DATA  := Date()
	MSECOSYS->ECO_TIME  := Time()
	MSECOSYS->ECO_STATUS:= "1"
	MSECOSYS->ECO_PARTNE:= cParceiro
	MSECOSYS->ECO_SERVID:= cServiceID
	MsUnLock()
Else
	//Ŀ
	//Envia os pendentes                                                      
	//
	dbSelectArea("MSECOSYS")
	MsSeek("1")
	While !Eof() .And. MSECOSYS->ECO_STATUS == "1"
		dbSelectArea("SM0")
		MsSeek(MSECOSYS->ECO_CODEMP+MSECOSYS->ECO_CODFIL)
		If SimpleLock()
			RecLock("MSECOSYS")			
			oMsEcoSystem:cHARDLOCK  := AllTrim(Str(Gethardlock(),20))
			EcoSigaMat(oMsEcoSystem:oWSCOMPANY)
			oMsEcoSystem:cPARTNERID := cParceiro
			oMsEcoSystem:cSERVICEID := cServiceID
			If oMsEcoSystem:PutPostage()
				MSECOSYS->ECO_STATUS := "2"
			EndIf
			MsUnLock()
			If MSECOSYS->ECO_STATUS<>"2"
				Exit
			EndIf
		EndIf
		dbSelectArea("MSECOSYS")
		dbSkip()
	EndDo
EndIf
//Ŀ
//Calcula o tempo de nao enviou                                           
//
If !lEnviou
	dbSelectArea("MSECOSYS")
	MsSeek("1")
	If MSECOSYS->ECO_DATA+7 <= Date()
		lRetorno := .F.
	EndIf
EndIf
//Ŀ
//Fecha o repositorio                                                     
//
CloseRepo()
RestArea(aAreaSM0)
RestArea(aArea)
Return(lRetorno)


/*/

Ŀ
Funo    EcoWizard Autor   Eduardo Riera          Data 17.02.2004 
Ĵ
          Wizard de habilitacao do Ecossistema por parte dos clientes  
          da Microsiga                                                 
Ĵ
ParametrosExpA1: Array com as empresas participantes do Ecossistema    
                                                                       
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Esta funcao abre a URL de divulgacao do Ecossistema          
                                                                       
                                                                       
Ĵ
Uso        Generico                                                    
ٱ


/*/

Function EcoWizard(aCNPJ)

Local aArea        := GetArea()
Local aAreaSM0     := SM0->(GetArea())
Local aTexto       := {}
Local aHeader      := {}
Local aStruTRB     := {}
Local aEmpresas    := {}
Local cArqTrb      := ""
Local nX           := 0
Local lContinua    := .F.
Local lAtualiza    := .F.
Local lRetorno     := .T.
Local oWsMsEco     := WSMSECOSYSTEM():New()
Local oWizard
Local oPanel 
Local oFont
Local oMark

DEFAULT aCNPJ := {}
//Ŀ
// Verifica as empresas cadastradas.                                      
//
WsChgUrl(@oWSMsEco,"MSECOSYSTEM.apw","UrlEcossistema")
oWSMsEco:cHARDLOCK   := AllTrim(Str(Gethardlock(),20))
If oWSMsEco:GetMemberEcoSystem()
	aEmpresas := oWSMsEco:oWSGetMemberEcoSystemResult:oWSCompanyView
EndIf
//Ŀ
// Montagem do Header das Empresas que serao selecionadas                 
//
aadd(aHeader,{"TRB_OK"    ,"","",""})
aadd(aHeader,{"TRB_CODEMP","",STR0005,""}) //"Codigo"
aadd(aHeader,{"TRB_CODFIL","",STR0006,""}) //"Filial"
aadd(aHeader,{"TRB_NOME"  ,"",STR0007,""}) //"Matriz"
aadd(aHeader,{"TRB_FILIAL","",STR0008,""}) //"N.Filial"
aadd(aHeader,{"TRB_RAZAO" ,"",STR0009,""}) //"Razao Social"
aadd(aHeader,{"TRB_CGC"   ,"",STR0010,""}) //"CNPJ"

aadd(aStruTRB,{"TRB_OK","C",2,0})
aadd(aStruTRB,{"TRB_CODEMP","C",2,0})
aadd(aStruTRB,{"TRB_CODFIL","C",2,0})
aadd(aStruTRB,{"TRB_NOME","C",20,0})
aadd(aStruTRB,{"TRB_FILIAL","C",20,0})
aadd(aStruTRB,{"TRB_RAZAO","C",40,0})
aadd(aStruTRB,{"TRB_CGC","C",14,0})
//Ŀ
// Cria o arquivo temporario                                              
//
cArqTrb := CriaTrab(aStruTrb,.T.)
dbUseArea(.T.,__LocalDrive,cArqTrb,"TRB",.F.,.F.)
//Ŀ
// Preenche o arquivo temporario                                          
//
dbSelectArea("SM0")
dbGotop()
While !Eof() 
    RecLock("TRB",.T.)
	TRB->TRB_OK      := IIf(aScan(aEmpresas,{|x| x:cCode == SM0->M0_CODIGO+SM0->M0_CODFIL})<>0,"XX","")
	TRB->TRB_CODEMP  := SM0->M0_CODIGO
	TRB->TRB_CODFIL  := SM0->M0_CODFIL
	TRB->TRB_NOME    := SM0->M0_NOME
	TRB->TRB_FILIAL  := SM0->M0_FILIAL
	TRB->TRB_RAZAO   := SM0->M0_NOMECOM
	TRB->TRB_CGC     := SM0->M0_CGC
    MsUnLock()	
	dbSelectArea("SM0")
	dbSkip()
EndDo
dbSelectArea("TRB")
dbGotop()
//Ŀ
// Montagem da Interface                                                  
//
aadd(aTexto,{})
aTexto[1] := STR0034+ CRLF //"Voc est acessando o Ecossistema pela primeira vez,"
aTexto[1] += STR0035+ CRLF //" necessrio que voc siga as instruoes para se habilitar junto ao sistema."
aTexto[1] += STR0036+ CRLF  //"Est habilitao s  feita uma  nica vez"
aTexto[1] += "" + CRLF
aTexto[1] += "" + CRLF
aTexto[1] += STR0011+ CRLF //"Este assistente tem como objetivo ajuda-lo na habilitao do Ecossistema, para "
aTexto[1] += STR0012+ CRLF //"as empresas cadastradas no sistema. Para continuar a execuo ser necessrio "
aTexto[1] += STR0013+ CRLF  //"verificar se o Servidor do Protheus foi configurado para acesso a HTTP externo. "
aTexto[1] += "" + CRLF
aTexto[1] += "" + CRLF
aTexto[1] += STR0014 //"Contacte seu administrador de rede para maiores informaes."

DEFINE FONT oFont NAME "Arial" SIZE 0, -11 BOLD
DEFINE WIZARD oWizard ;
	TITLE STR0015 ; //"Assistente de habilitao do Ecossistema"
	HEADER STR0016 ; //"Ateno"
	MESSAGE STR0017 ; //"Siga atentamente os passos para habilitao do Ecossistema"
	TEXT aTexto[1] ;
	NEXT {|| .T.} ;
	FINISH {||.T.} //;
//	PANEL //NOFIRSTPANEL 

CREATE PANEL oWizard  ;
	HEADER STR0018 ; //"Termo de adeso"
	MESSAGE STR0019 ; //"Leia atentamente as condies do termo de adeso"
	BACK {||.T.} ;
	NEXT {|| lContinua } ;
	PANEL

aadd(aTexto,"")
aTexto[2] := PadC(STR0018,160)+CRLF+CRLF //"Termo de Adeso"
aTexto[2] := STR0020 + STR0021 + STR0022 + CRLF + CRLF +;
				STR0023 + STR0024 + STR0025 + CRLF + CRLF +;
				STR0026 + CRLF + CRLF +;
				STR0027 + CRLF +;
				STR0028 + CRLF + CRLF +;
				STR0029

@ 010,010 GET aTexto[2] MEMO SIZE 270, 115 READONLY PIXEL OF oWizard:oMPanel[2]
@ 129,002 CHECKBOX lContinua PROMPT STR0030 FONT oFont PIXEL SIZE 160,10 OF oWizard:oMPanel[2] //"Aceito as condies acima"

CREATE PANEL oWizard  ;
	HEADER STR0031 ;               //"Cadastramento de empresas"
	MESSAGE STR0032 ; //"Selecione as empresas que podero ser habilitadas no ecossistema"
	BACK {||.T.} ;
	FINISH {|| lAtualiza := .T. } ;
	PANEL

oMark := MsSelect():New("TRB","TRB_OK",,aHeader,.F.,"XX",{004,003,140,285},,,oWizard:oMPanel[3])

ACTIVATE WIZARD oWizard CENTERED
//Ŀ
// Envie os dados para a Microsiga                                        
//
If lAtualiza	
	//WsChgUrl(@oWSMsEco,"MSECOSYSTEM.apw")
	dbSelectArea("TRB")
	dbGotop()
	While !Eof() .And. lRetorno
		oWSMsEco:cHARDLOCK   := AllTrim(Str(Gethardlock(),20))
		If !Empty(TRB->TRB_OK)		
			EcoSigaMat( oWSMsEco:oWSCOMPANY , TRB->TRB_CODEMP+TRB->TRB_CODFIL )
			If oWsMsEco:PUTMEMBERECOSYSTEM()
				If oWsMsEco:cPUTMEMBERECOSYSTEMRESULT == "OK"
					lRetorno := .T.	
				Else
					lRetorno := .F.
					Exit
				EndIf
			Else
				lRetorno := .F.
				Exit
			EndIf
		Else
			EcoSigaMat( oWSMsEco:oWSCOMPANY , TRB->TRB_CODEMP+TRB->TRB_CODFIL )
			If oWsMsEco:DELMEMBERECOSYSTEM()
				If oWsMsEco:cDELMEMBERECOSYSTEMRESULT == "OK"
					lRetorno := .T.	
				Else
					lRetorno := .F.
					Exit
				EndIf
			Else
				lRetorno := .F.
				Exit
			EndIf			
		EndIf
		dbSelectArea("TRB")
		dbSkip()
	EndDo
	If lRetorno
		Aviso("Ecossistema - MemberEcoSystem",STR0033,{"Ok"}) //"Habilio efetuada"
		//Ŀ
		// Atualiza as empresas cadastradas                                       
		//
		aEmpresas := {}
		oWSMsEco:cHARDLOCK   := AllTrim(Str(Gethardlock(),20))
		If oWSMsEco:GetMemberEcoSystem()
			aEmpresas := oWSMsEco:oWSGetMemberEcoSystemResult:oWSCompanyView
		EndIf
	Else
		Aviso("Ecossistema - MemberEcoSystem",STR0002,{"Ok"}) //"Falha de comunicacao com a Microsiga"
	EndIf
Else
	lRetorno := .F.
EndIf
//Ŀ
// Montagem de retorno dos parametros da funcao                           
//
For nX := 1 To Len(aEmpresas)
	aadd(aCNPJ,aEmpresas[nX]:cFederalId)
Next nX
//Ŀ
// Restaura a Integridade da rotina                                       
//
dbSelectArea("TRB")
dbCloseArea()

RestArea(aAreaSM0)
RestArea(aArea)
Return(lRetorno)

