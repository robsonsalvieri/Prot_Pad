
#INCLUDE "WSCRM070.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"

/*/


Ŀ
Funo    WSCRM070   Autor Eduardo Riera           Data 08.08.2003  
Ĵ
Descrio  Web Service responsavel pelos contatos do Sistema            
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service                                                
//
WSSERVICE CRMCUSTOMERCONTACT       DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/crmcustomercontact.apw" //"Servio de consulta e atualizao dos contatos ( <b>Restrio de cliente</b> )"
   WSDATA UserCode                 	AS String
   WSDATA Contact                  	As ContactView
   WSDATA ListOfContact            	As Array Of ContactView
   WSDATA CustomerID               	As String
   WSDATA ContactId                	As String
   WSDATA Header                   	AS Array Of BrwHeader
   WSDATA HeaderType               	As String
   WSDATA TypeOfAddress            	As Array Of GenericStruct
   WSDATA TypeOfPhone              	As Array Of GenericStruct
   WSDATA ListOfGroup              	As Array Of GenericView
   WSDATA ListOfDepartment         	As Array Of GenericView
   WSDATA ListOfPosition           	As Array Of GenericView
   WSDATA QueryAddWhere            	As String OPTIONAL
   WSDATA IndexKey                 	As String OPTIONAL   
   WSDATA ContactIN					As String
   WSDATA ContactValidations		As ContactValidations
   WSDATA CustomInformations		As InformationsArray

   WSMETHOD GetHeader         	DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do Servio"
   WSMETHOD GetTypeOfPhone    	DESCRIPTION STR0003 //"Mtodo que descreve os tipos de telefone"
   WSMETHOD GetTypeOfAddress  	DESCRIPTION STR0004 //"Mtodo que descreve os tipos de endereo"
   WSMETHOD BrwContact        	DESCRIPTION STR0005 //"Mtodo de listagem dos contatos do cliente"
   WSMETHOD GetContact        	DESCRIPTION STR0006 //"Mtodo de consulta aos dados do contato"
   WSMETHOD PutContact        	DESCRIPTION STR0007 //"Mtodo de atualizao dos dados do contato"
   WSMETHOD GetGroup          	DESCRIPTION STR0008 //"Mtodo de consulta aos grupos"
   WSMETHOD GetDepartment     	DESCRIPTION STR0009 //"Mtodo de consulta aos departamentos"
   WSMETHOD GetPosition     	DESCRIPTION STR0010 //"Mtodo de consulta aos cargos"
   WSMETHOD IsValidContact		DESCRIPTION "Metdo de validao de contato"
   
ENDWSSERVICE

WSSTRUCT InformationsArray
	WSDATA Informations	As Array Of Information
ENDWSSTRUCT

WSSTRUCT Information
	WSDATA Content As String
ENDWSSTRUCT

WSSTRUCT ContactValidations
	WSDATA ValidContact					As Boolean
	WSDATA ValidIdentificationNumber	As Boolean
	WSDATA OtherValidations				As Array Of String	
ENDWSSTRUCT

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE CRMCUSTOMERCONTACT

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    BrwContactAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao dos contatos do cliente                
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Cliente                                     
          ExpC3: Expressao a ser adicionada na query                   
          ExpC4: Expressao da chave de indice ( CodeBase )             
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os contatos de um cliente                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwContact WSRECEIVE UserCode,CustomerID,QueryAddWhere,IndexKey WSSEND ListOfContact WSSERVICE CRMCUSTOMERCONTACT

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local lValido  := .F.
Local nX       := 0
Local cQuery   := ""
Local cAliasAC8:= "AC8"
Local cAliasSU5 := "SU5"
Local cArquivo := ""
#IFDEF TOP
Local aStru    := {}
#ENDIF

dbSelectArea("AC8")
dbSetOrder(1)
::IndexKey := AC8->(IndexKey())
::ListOfContact := {}
//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMCUSTOMERCONTACT","BRWCONTACT","SA1",::CustomerID)
	#IFDEF TOP
		lQuery := .T.
		cAliasSU5 := "BRWCONTACT"
		cAliasAC8 := "BRWCONTACT"
		aStru  := SU5->(dbStruct())
		cQuery := "SELECT AC8_FILIAL,U5_FILIAL,U5_CLIENTE,U5_LOJA,U5_CODCONT,U5_CONTAT,U5_CPF,U5_EMAIL,U5_URL,U5_FUNCAO,"
		cQuery += "U5_GRUPO,U5_DEPTO,U5_END,U5_MUN,U5_EST,U5_CEP,U5_BAIRRO,U5_FONE,U5_FCOM1,U5_DDD,U5_FAX,U5_CELULAR,U5_CODPAIS "
		cQuery += GetUserField("SU5")
		cQuery += "FROM "+RetSqlName("SU5")+" SU5, "
		cQuery += RetSqlName("AC8")+" AC8 "
		cQuery += "WHERE "
		cQuery += "AC8.AC8_FILIAL='"+xFilial("AC8")+"' AND "
		cQuery += "AC8.AC8_FILENT='"+xFilial("SA1")+"' AND "
		cQuery += "AC8.AC8_ENTIDA='SA1' AND "
		cQuery += "AC8.AC8_CODENT='"+::CustomerID+"' AND "
		cQuery += "AC8.AC8_CODCON=SU5.U5_CODCONT AND "
		cQuery += "AC8.D_E_L_E_T_=' ' AND "
		cQuery += "SU5.U5_FILIAL='"+xFilial("SU5")+"' AND "
		cQuery += "SU5.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSU5)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasSU5,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf	    
	    Next nX
	#ELSE
		cArquivo := CriaTrab(,.F.)
		cQuery   := "AC8_FILIAL='"+xFilial("AC8")+"' .And. "
		cQuery   += "AC8_ENTIDA='SA1' .And. "
		cQuery   += "AC8_FILENT='"+xFilial("SA1")+"' .And. "
		cQuery   += "AC8_CODENT='"+::CustomerID+"' "
		IndRegua("AC8",cArquivo,::IndexKey,,cQuery)
		dbGotop()
	#ENDIF
	nX := 0
	While !Eof() .And. xFilial("AC8") == (cAliasAC8)->AC8_FILIAL 

		If lQuery
			lValido := .T.
		Else
			dbSelectArea("SU5")
			dbSetOrder(1)
			lValido := MsSeek(xFilial("SU5")+(cAliasAC8)->AC8_CODCON)
		EndIf
		
		If lValido
		
			aadd(::ListOfContact,WsClassNew("ContactView"))
			nX++			
			GetContact(cAliasSU5,::ListOfContact[nX])
		
		EndIf	
		dbSelectArea(cAliasAC8)
		dbSkip()
	EndDo

	If lQuery
		dbSelectArea(cAliasAC8)
		dbCloseArea()
		dbSelectArea("AC8")
	Else
		dbSelectArea("AC8")
		RetIndex("AC8")
		FErase(cArquivo+OrdBagExt())		
	EndIf
	
	If Empty(::ListOfContact)
		lRetorno := .F.
		SetSoapFault("BRWCONTACT",STR0011) //"Nao ha contatos cadastrados para este cliente"
	EndIf

Else

	lRetorno := .F.
	
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetContactAutor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao de um contato do cliente               
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Cliente                                     
          ExpC3: Codigo do Contato                                     
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o contato de um cliente                  
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetContact WSRECEIVE UserCode,CustomerID,ContactId WSSEND Contact WSSERVICE CRMCUSTOMERCONTACT

Local aArea    := GetArea()
Local lRetorno := .T.
//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMCUSTOMERCONTACT","GETCONTACT","SA1",::CustomerID)

	dbSelectArea("SU5")
	dbSetOrder(1)
	If MsSeek(xFilial("SU5")+::ContactId)
			
		GetContact("SU5",::Contact)
				
	Else
		
		lRetorno := .F.
		SetSoapFault("GETCONTACT",STR0011) //"Nao ha contatos cadastrados para este cliente"
		
	EndIf

Else

	lRetorno := .F.
	
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE CRMCustomerContact

::TypeOfAddress := {}
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0012 //"COMERCIAL"

Return(.T.)

/*/

Ŀ
Funo    GetTypeOfPAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfPhone                        
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE CRMCustomerContact

::TypeOfPhone := {}
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0012 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0013 //"FAX"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[3]:Code := "3"
::TypeOfPhone[3]:Description := STR0014 //"CELULAR"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[4]:Code := "4"
::TypeOfPhone[4]:Description := STR0015 //"RESIDENCIAL"
Return(.T.)

/*/

Ŀ
Funo    GetPositioAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao das funcoes dos contatos              
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetPosition WSRECEIVE NULLPARAM WSSEND ListOfPosition WSSERVICE CRMCustomerContact

Local aArea := GetArea()
Local nX    := 0

dbSelectArea("SUM")
dbSetOrder(1)
MsSeek(xFilial("SUM"))
While !Eof() .And. xFilial("SUM") == SUM->UM_FILIAL

	nX++	
	aadd(::ListOfPosition,WsClassNew("GenericView"))
	::ListOfPosition[nX]:Code        := SUM->UM_CARGO
	::ListOfPosition[nX]:Description := SUM->UM_DESC
	UserFields("SUM",@::ListOfPosition[nX]:UserFields)
		
	dbSelectArea("SUM")
	dbSkip()
	
EndDo
RestArea(aArea)
Return(.T.) 

/*/

Ŀ
Funo    IsValidContactAutor Vendas CRM           Data 08.08.2003 
Ĵ
          Rotina de verificao da validade do contato.                
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD IsValidContact WSRECEIVE ContactIN, CustomInformations WSSEND ContactValidations WSSERVICE CRMCustomerContact

Local aArea		:= GetArea()
Local aPERet	:= {}
Local nCount	:= 0

::ContactValidations := WSClassNew("ContactValidations")
::ContactValidations:ValidContact := .F.
::ContactValidations:ValidIdentificationNumber := .F.
::ContactValidations:OtherValidations := {""}

DbSelectArea("SU5")
DbSetOrder(8)
                         
If CGC(::ContactIN)
	::ContactValidations:ValidIdentificationNumber	:= .T.
	If MsSeek( xFilial("SU5") + ::ContactIN )
		::ContactValidations:ValidContact := .T.	
	EndIf  
EndIf

If ::ContactValidations:ValidContact .And. FindFunction("P_WTK070VC")
	aPERet := P_WTK070VC(Self)
	
	If ValType(aPERet) == "A"
		::ContactValidations:OtherValidations := Array( Len( aPERet ) )
		For nCount := 1 To Len(aPERet)
			::ContactValidations:OtherValidations[nCount] := aPERet[nCount]
		Next
	EndIf
EndiF

RestArea(aArea)

Return(.T.)



/*/

Ŀ
Funo    GetDepto  Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao dos departamento dos contatos         
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetDepartment WSRECEIVE NULLPARAM WSSEND ListOfDepartment WSSERVICE CRMCUSTOMERCONTACT

Local aArea := GetArea()
Local nX    := 0

dbSelectArea("SQB")
dbSetOrder(1)
MsSeek(xFilial("SQB"))
While !Eof() .And. xFilial("SQB") == SQB->QB_FILIAL
	nX++	
	aadd(::ListOfDepartment,WsClassNew("GenericView"))
	::ListOfDepartment[nX]:Code        := SQB->QB_DEPTO
	::ListOfDepartment[nX]:Description := SQB->QB_DESCRIC
	UserFields("SQB",@::ListOfDepartment[nX]:UserFields)
		
	dbSelectArea("SQB")
	dbSkip()
EndDo

RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    GetGroup  Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao dos grupos de contato                 
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetGroup WSRECEIVE NULLPARAM WSSEND ListOfGroup WSSERVICE CRMCUSTOMERCONTACT

Local aArea := GetArea()
Local nX    := 0
           
dbSelectArea("SQ0")
dbSetOrder(1)
MsSeek(xFilial("SQ0"))
While !Eof() .And. xFilial("SQ0") == SQ0->Q0_FILIAL
	nX++	
	aadd(::ListOfGroup,WsClassNew("GenericView"))
	::ListOfGroup[nX]:Code        := SQ0->Q0_GRUPO
	::ListOfGroup[nX]:Description := SQ0->Q0_DESCRIC
	UserFields("SQB",@::ListOfGroup[nX]:UserFields)
		
	dbSelectArea("SQ0")
	dbSkip()
EndDo

RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    PutContactAutor   Eduardo Riera          Data 02.09.2003 
Ĵ
          Rotina de atualizacao dos dados do contato                   
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Cliente                                               
          ExpO3: Objeto do contato                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao dos contatos do sistema     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutContact WSRECEIVE UserCode,CustomerID,Contact WSSEND ContactID WSSERVICE CRMCUSTOMERCONTACT

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
Local nY       := 0
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.
//Ŀ
//Verificacao do acesso                                                   
//

If PrtChkUser(::UserCode,"CRMCUSTOMERCONTACT","PutContact","SA1",::CustomerID)

	aadd(aDados,{"U5_CODCONT",::Contact:ContactID,})
	aadd(aDados,{"U5_CONTAT" ,::Contact:Name,})
	aadd(aDados,{"U5_CPF"    ,::Contact:FederalId,})
	aadd(aDados,{"U5_EMAIL"  ,::Contact:Email,})
	aadd(aDados,{"U5_URL"    ,::Contact:HomePage,})
	aadd(aDados,{"U5_FUNCAO" ,::Contact:Position,})
	aadd(aDados,{"U5_GRUPO"  ,::Contact:Group,})
	aadd(aDados,{"U5_DEPTO"  ,::Contact:Department,})
	aadd(aDados,{"U5_CLIENTE",cCliente,})
	aadd(aDados,{"U5_LOJA"   ,cLoja,})		
	//Ŀ
	//Valida o endereco                                                       
	//
	For nY := 1 To Len(::Contact:Addresses)
		If ::Contact:Addresses[nY]:TypeOfAddress == "1"
			aadd(aDados,{"U5_END"  ,AllTrim(::Contact:Addresses[nY]:Address)+","+::Contact:Addresses[nY]:AddressNumber,})
			aadd(aDados,{"U5_MUN"    ,::Contact:Addresses[nY]:District,})
			aadd(aDados,{"U5_EST"    ,::Contact:Addresses[nY]:State,})
			aadd(aDados,{"U5_CEP"    ,::Contact:Addresses[nY]:ZipCode,})
			aadd(aDados,{"U5_BAIRRO" ,::Contact:Addresses[nY]:Zone,})
		Else
			lRetorno := .F.
			SetSoapFault("PUTCONTACT",STR0016) //"TypeOfAddress invalido"
		EndIf
	Next nY
	//Ŀ
	//Valida os telefones                                                     
	//
	For nY := 1 To Len(::Contact:Phones)
		Do Case
			Case ::Contact:Phones[nY]:TypeOfPhone == "1"
				aadd(aDados,{"U5_FCOM1",::Contact:Phones[nY]:PhoneNumber,})
				aadd(aDados,{"U5_CODPAIS",::Contact:Phones[nY]:CountryAreaCode,})
				aadd(aDados,{"U5_DDD",::Contact:Phones[nY]:LocalAreaCode,})
			Case ::Contact:Phones[nY]:TypeOfPhone == "2"
				aadd(aDados,{"U5_FAX",::Contact:Phones[nY]:PhoneNumber,})
			Case ::Contact:Phones[nY]:TypeOfPhone == "3"
				aadd(aDados,{"U5_CELULAR",::Contact:Phones[nY]:PhoneNumber,})
			Case ::Contact:Phones[nY]:TypeOfPhone == "4"
				aadd(aDados,{"U5_FONE",::Contact:Phones[nY]:PhoneNumber,})	
			OtherWise
				lRetorno := .F.
				SetSoapFault("PUTCONTACT",STR0017) //"TypeOfPhone invalido"
		EndCase
	Next nX
	PutUserFields("SU5"      ,::Contact:UserFields,@aDados)	
	aDados := WsAutoOpc(aDados)
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("SU5")
	dbSetOrder(1)
	If Empty(::Contact:ContactID) .Or. !MsSeek(xFilial("SU5")+::Contact:ContactID)
		nX := 3
	Else
		nX := 4
	EndIf
	Begin Transaction	
		TmkA070(aDados,nX)
		dbSelectArea("AC8")
		dbSetOrder(1)
		If MsSeek(xFilial("AC8")+SU5->U5_CODCONT+"SA1"+xFilial("SA1")+cCliente+cLoja)
			RecLock("AC8")
		Else
  			RecLock("AC8",.T.)
		EndIf
		AC8->AC8_FILIAL := xFilial("AC8")
		AC8->AC8_CODCON := SU5->U5_CODCONT
		AC8->AC8_ENTIDA := "SA1"
		AC8->AC8_FILENT := xFilial("SA1")
		AC8->AC8_CODENT := cCliente+cLoja
	End Transaction 
	::ContactId := SU5->U5_CODCONT
		
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTCONTACT",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/


Ŀ
Funo    WSCRM070   Autor Eduardo Riera           Data 08.08.2003  
Ĵ
Descrio  Web Service responsavel pelos contatos do Sistema            
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE CRMSellerCustomerContact DESCRIPTION STR0018 NAMESPACE "http://webservices.microsiga.com.br/crmsellercustomercontact.apw" //"Servio de consulta e atualizao dos contatos ( <b>Restrio de representante comercial</b> )"
   WSDATA UserCode                 AS String
   WSDATA Contact                  As ContactView
   WSDATA ListOfContact            As Array Of ContactView
   WSDATA CustomerID               As String
   WSDATA SellerCode               As String
   WSDATA ContactId                As String
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA HeaderType               As String
   WSDATA TypeOfAddress            As Array Of GenericStruct
   WSDATA TypeOfPhone              As Array Of GenericStruct
   WSDATA ListOfGroup              As Array Of GenericView
   WSDATA ListOfDepartment         As Array Of GenericView
   WSDATA ListOfPosition           As Array Of GenericView
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String OPTIONAL   

   WSMETHOD GetHeader         DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do Servio"
   WSMETHOD GetTypeOfPhone    DESCRIPTION STR0003 //"Mtodo que descreve os tipos de telefone"
   WSMETHOD GetTypeOfAddress  DESCRIPTION STR0004 //"Mtodo que descreve os tipos de endereo"
   WSMETHOD BrwContact        DESCRIPTION STR0005 //"Mtodo de listagem dos contatos do cliente"
   WSMETHOD GetContact        DESCRIPTION STR0006 //"Mtodo de consulta aos dados do contato"
   WSMETHOD PutContact        DESCRIPTION STR0007 //"Mtodo de atualizao dos dados do contato"
   WSMETHOD GetGroup          DESCRIPTION STR0008 //"Mtodo de consulta aos grupos"
   WSMETHOD GetDepartment     DESCRIPTION STR0009 //"Mtodo de consulta aos departamentos"
   WSMETHOD GetPosition       DESCRIPTION STR0010 //"Mtodo de consulta aos cargos"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE CRMSellerCustomerContact

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    BrwContactAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de recuperacao dos contatos do sistema                
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpC3: Cliente                                               
          ExpC3: Expressao a ser adicionada na query                   
          ExpC4: Expressao da chave de indice ( CodeBase )             
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os contatos do sistema                   
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwContact WSRECEIVE UserCode,SellerCode,CustomerID,QueryAddWhere WSSEND ListOfContact WSSERVICE CRMSellerCustomerContact

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local cQuery   := ""
Local cArquivo := ""
Local cAliasSU5:= "SU5"
#IFDEF TOP
Local aStru    := {}
#ENDIF
//Ŀ
//Inicializa os parametros DEFAULTs                                       
//
dbSelectArea("SU5")
dbSetOrder(1)
DEFAULT ::IndexKey := SU5->(IndexKey())

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMSELLERCUSTOMERCONTACT","BRWCONTACT","SA3",::SellerCode)
	#IFDEF TOP
		lQuery := .T.
		cAliasSU5 := "GETCONTACT"
		aStru  := SU5->(dbStruct())
		cQuery := "SELECT U5_FILIAL,U5_CLIENTE,U5_LOJA,U5_CODCONT,U5_CONTAT,U5_CPF,U5_EMAIL,U5_URL,U5_FUNCAO,"
		cQuery += "U5_GRUPO,U5_DEPTO,U5_END,U5_MUN,U5_EST,U5_CEP,U5_BAIRRO,U5_FONE,U5_FCOM1,U5_DDD,U5_FAX,U5_CELULAR,U5_CODPAIS "
		cQuery += GetUserField("SU5")
		cQuery += "FROM "+RetSqlName("SU5")+" SU5, "+RetSqlName("AC8")+" AC8 "
		cQuery += "WHERE "
		cQuery += "SU5.U5_FILIAL='"+xFilial("SU5")+"' AND "
		cQuery += "AC8.AC8_FILIAL='"+xFilial("AC8")+"' AND "
		cQuery += "AC8.AC8_ENTIDA = 'SA1' AND "
		cQuery += "AC8.AC8_CODENT = '" + cCliente + cLoja + "' AND "
		cQuery += "AC8.AC8_CODCON = SU5.U5_CODCONT AND "
		cQuery += "SU5.D_E_L_E_T_=' ' AND "
		cQuery += "AC8.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(SU5->(IndexKey()))
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSU5)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasSU5,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf	    
	    Next nX
	#ELSE
		cArquivo := CriaTrab(,.F.)
		cQuery   := "U5_FILIAL='"+xFilial("SU5")+"' .And. "
		cQuery   += "U5_CLIENTE='"+cCliente+"' .And. "
		cQuery   += "U5_LOJA='"+cLoja+"' "
		IndRegua("SU5",cArquivo,::IndexKey,,cQuery)
		dbGotop()
	#ENDIF

	nX := 0
	While !Eof() .And. xFilial("SU5") == (cAliasSU5)->U5_FILIAL 
		aadd(::ListOfContact,WsClassNew("ContactView"))
		nX++
			
		GetContact(cAliasSU5,::ListOfContact[nX])

		dbSelectArea(cAliasSU5)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSU5)
		dbCloseArea()
		dbSelectArea("SU5")
	Else
		dbSelectArea("SU5")
		RetIndex("SU5")
		FErase(cArquivo+OrdBagExt())		
	EndIf
	
	If Empty(::ListOfContact)
		lRetorno := .F.
		SetSoapFault("BRWCONTACT",STR0011) //"Nao ha contatos cadastrados para este cliente"
	EndIf

Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetContactAutor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de recuperacao dos contatos do cliente                
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpC3: Codigo do Contato                                     
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os contatos de um cliente                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetContact WSRECEIVE UserCode,SellerCode,ContactId WSSEND Contact WSSERVICE CRMSELLERCUSTOMERCONTACT

Local aArea    := GetArea()
Local lRetorno := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMSELLERCUSTOMERCONTACT","GETCONTACT","SA3",::SellerCode)

	dbSelectArea("SU5")
	dbSetOrder(1)
	If MsSeek(xFilial("SU5")+::ContactId)
		
		GetContact("SU5",::Contact)
			
	Else
		
		lRetorno := .F.
		SetSoapFault("GETCONTACT",STR0019) //"Codigo do contato invalido"
		
	EndIf

Else

	lRetorno := .F.
	
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE CRMSellerCustomerContact

::TypeOfAddress := {}
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0012 //"COMERCIAL"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[2]:Code := "2"
//::TypeOfAddress[2]:Description := "COBRANCA"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[3]:Code := "3"
//::TypeOfAddress[3]:Description := "ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfPAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ 
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE CRMSellerCustomerContact

::TypeOfPhone := {}
aadd(::TypeOfPhone,WsClassNew("GenericStruct")) 
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0012 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0013 //"FAX"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[3]:Code := "3"
::TypeOfPhone[3]:Description := STR0014 //"CELULAR"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[4]:Code := "4"
::TypeOfPhone[4]:Description := STR0015 //"RESIDENCIAL"
Return(.T.)

/*/

Ŀ
Funo    GetPositioAutor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao das funcoes dos contatos              
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetPosition WSRECEIVE NULLPARAM WSSEND ListOfPosition WSSERVICE CRMSellerCustomerContact

Local aArea := GetArea()
Local nX    := 0
dbSelectArea("SUM")
dbSetOrder(1)
MsSeek(xFilial("SUM"))
While !Eof() .And. xFilial("SUM") == SUM->UM_FILIAL

	nX++	
	aadd(::ListOfPosition,WsClassNew("GenericView"))
	::ListOfPosition[nX]:Code        := SUM->UM_CARGO
	::ListOfPosition[nX]:Description := SUM->UM_DESC
	UserFields("SUM",@::ListOfPosition[nX]:UserFields)	
		
	dbSelectArea("SUM")
	dbSkip()
EndDo
RestArea(aArea)
Return(.T.)
/*/

Ŀ
Funo    GetDepto  Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao dos departamento dos contatos         
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetDepartment WSRECEIVE NULLPARAM WSSEND ListOfDepartment WSSERVICE CRMSellerCustomerContact

Local aArea := GetArea()
Local nX    := 0

dbSelectArea("SQB")
dbSetOrder(1)
MsSeek(xFilial("SQB")) 
While !Eof() .And. xFilial("SQB") == SQB->QB_FILIAL
	nX++	
	aadd(::ListOfDepartment,WsClassNew("GenericView")) 
	::ListOfDepartment[nX]:Code        := SQB->QB_DEPTO
	::ListOfDepartment[nX]:Description := SQB->QB_DESCRIC
	UserFields("SQB",@::ListOfDepartment[nX]:UserFields)
		
	dbSelectArea("SQB")
	dbSkip()
EndDo

RestArea(aArea)

Return(.T.)


/*/

Ŀ
Funo    GetGroup  Autor   Eduardo Riera          Data 08.08.2003 
Ĵ
          Rotina de demonstracao dos grupos de contato                 
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve uma estrutura                            
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ 


/*/
WSMETHOD GetGroup WSRECEIVE NULLPARAM WSSEND ListOfGroup WSSERVICE CRMSellerCustomerContact
Local aArea := GetArea()
Local nX    := 0
           
dbSelectArea("SQ0")
dbSetOrder(1)
MsSeek(xFilial("SQ0"))
While !Eof() .And. xFilial("SQ0") == SQ0->Q0_FILIAL
	nX++	
	aadd(::ListOfGroup,WsClassNew("GenericView"))
	::ListOfGroup[nX]:Code        := SQ0->Q0_GRUPO
	::ListOfGroup[nX]:Description := SQ0->Q0_DESCRIC   
	UserFields("SQ0",@::ListOfGroup[nX]:UserFields)
	
	dbSelectArea("SQ0")
	dbSkip()
EndDo

RestArea(aArea)
Return(.T.)

/*/

Ŀ
Funo    PutContactAutor   Eduardo Riera          Data 02.09.2003 
Ĵ
          Rotina de atualizacao dos dados do contato                   
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Vendedor                                              
          ExpC3: Cliente                                               
          ExpO4: Objeto do contato                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao dos contatos do sistema     
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutContact WSRECEIVE UserCode,SellerCode,CustomerID,Contact WSSEND ContactId  WSSERVICE CRMSellerCustomerContact

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
Local nY       := 0
Local cCliente := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja    := SubStr(::CustomerID,Len(SA1->A1_COD)+1)

PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

If PrtChkUser(::UserCode,"CRMSellerCustomerContact","PutContact","SA3",::SellerCode)

	aadd(aDados,{"U5_CODCONT",::Contact:ContactID,Nil})
	aadd(aDados,{"U5_CONTAT" ,::Contact:Name,Nil})
	aadd(aDados,{"U5_CPF"    ,::Contact:FederalId,Nil})
	aadd(aDados,{"U5_EMAIL"  ,::Contact:Email,Nil})
	aadd(aDados,{"U5_URL"    ,::Contact:HomePage,Nil})
	aadd(aDados,{"U5_FUNCAO" ,::Contact:Position,Nil})
	aadd(aDados,{"U5_GRUPO"  ,::Contact:Group,Nil})
	aadd(aDados,{"U5_DEPTO"  ,::Contact:Department,Nil})
	aadd(aDados,{"U5_CLIENTE",cCliente,Nil})
	aadd(aDados,{"U5_LOJA"   ,cLoja,Nil})	
	PutUserFields("SU5",::Contact:UserFields,@aDados)

	For nY := 1 To Len(::Contact:Addresses)
		If ::Contact:Addresses[nY]:TypeOfAddress == "1"
			aadd(aDados,{"U5_END",AllTrim(::Contact:Addresses[nY]:Address)+","+::Contact:Addresses[nY]:AddressNumber,Nil})
			aadd(aDados,{"U5_MUN",::Contact:Addresses[nY]:District,Nil})
			aadd(aDados,{"U5_EST",::Contact:Addresses[nY]:State,Nil})
			aadd(aDados,{"U5_CEP",::Contact:Addresses[nY]:ZipCode,Nil})
			aadd(aDados,{"U5_BAIRRO",::Contact:Addresses[nY]:Zone,Nil})
		Else
			lRetorno := .F.
			SetSoapFault("PUTCONTACT",STR0016) //"TypeOfAddress invalido"
		EndIf
	Next nY
	
	For nY := 1 To Len(::Contact:Phones)
		Do Case
			Case ::Contact:Phones[nY]:TypeOfPhone == "1"
				aadd(aDados,{"U5_FCOM1",::Contact:Phones[nY]:PhoneNumber,Nil})
				If !Empty( ::Contact:Phones[nY]:LocalAreaCode )
					If Val(::Contact:Phones[nY]:LocalAreaCode) <> 0
						aadd(aDados,{"U5_DDD",::Contact:Phones[nY]:LocalAreaCode,Nil})
					EndIf
				EndIf
				If !Empty( ::Contact:Phones[nY]:CountryAreaCode )
					If Val(::Contact:Phones[nY]:CountryAreaCode) <> 0
						aadd(aDados,{"U5_CODPAIS",::Contact:Phones[nY]:CountryAreaCode,Nil})
					EndIf
				EndIf
			Case ::Contact:Phones[nY]:TypeOfPhone == "2"
				aadd(aDados,{"U5_FAX",::Contact:Phones[nY]:PhoneNumber,Nil})
			Case ::Contact:Phones[nY]:TypeOfPhone == "3"
				aadd(aDados,{"U5_CELULAR",::Contact:Phones[nY]:PhoneNumber,Nil})
			Case ::Contact:Phones[nY]:TypeOfPhone == "4"
				aadd(aDados,{"U5_FONE",::Contact:Phones[nY]:PhoneNumber,Nil})				
			OtherWise
				lRetorno := .F.
				SetSoapFault("PUTCONTACT",STR0017) //"TypeOfPhone invalido"
		EndCase
	Next nX
	aDados := WsAutoOpc(aDados)
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("SU5")
	dbSetOrder(1)
	If Empty(::Contact:ContactID) .OR. !MsSeek(xFilial("SU5")+::Contact:ContactID)
		nX := 3
	Else
		nX := 4
	EndIf
	
	TmkA070(aDados,nX,NIL,NIL,.T.)
	
/*	DbSelectArea( "AC8" )
	DbSetOrder( 1 )
	
	If AC8->( MsSeek( xFilial( "AC8" ) + SU5->U5_CODCONT + "SA1" + xFilial( "SA1" ) + ::CustomerID ) )
		RecLock( "AC8", .F. )
	Else
		RecLock( "AC8", .T. )
	EndIf

	AC8->AC8_FILIAL := xFilial( "AC8" )
	AC8->AC8_FILENT := xFilial( "SA1" )
	AC8->AC8_ENTIDA := "SA1"
	AC8->AC8_CODENT := ::CustomerID
	AC8->AC8_CODCON := ::Contact:ContactID*/

	::ContactID := SU5->U5_CODCONT
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTCONTACT",cErro)
		lRetorno := .F.
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetContactAutor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Metodo de recuperacao dos Contatos                           
                                                                       
Ĵ
ParametrosExpC1: Alias do SU5                                          
          ExpO2: Objeto que sera atualizado                            
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo atualiza o objeto contact passado por referencia 
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Static Function GetContact(cAliasSU5,oObjeto)

Local nY := 0

oObjeto:ContactID              := (cAliasSU5)->U5_CODCONT
oObjeto:Name                   := (cAliasSU5)->U5_CONTAT
oObjeto:FederalId              := (cAliasSU5)->U5_CPF
oObjeto:Email                  := (cAliasSU5)->U5_EMAIL
oObjeto:HomePage               := (cAliasSU5)->U5_URL
oObjeto:Position               := (cAliasSU5)->U5_FUNCAO
oObjeto:PositionDescription    := IIf(Empty((cAliasSU5)->U5_FUNCAO),"",Posicione("SUM",1,xFilial("SUM")+(cAliasSU5)->U5_FUNCAO,"UM_DESC"))
oObjeto:Group                  := (cAliasSU5)->U5_GRUPO
oObjeto:GroupDescription       := IIf(Empty((cAliasSU5)->U5_GRUPO),"",Posicione("SQ0",1,xFilial("SQ0")+(cAliasSU5)->U5_GRUPO,"Q0_DESCRIC"))
oObjeto:Department             := (cAliasSU5)->U5_DEPTO
oObjeto:DepartmentDescription  := IIF(Empty((cAliasSU5)->U5_DEPTO),"",Posicione("SQB",1,xFilial("SQB")+(cAliasSU5)->U5_DEPTO,"QB_DESCRIC"))			

nY := 0			
If !Empty((cAliasSU5)->U5_END)
	DEFAULT oObjeto:Addresses := {}
	aadd(oObjeto:Addresses,WsClassNew("AddressView"))
	nY++
	oObjeto:Addresses[nY]:TypeOfAddress := "1"
	oObjeto:Addresses[nY]:Address       := FisGetEnd((cAliasSU5)->U5_END)[1]
	oObjeto:Addresses[nY]:AddressNumber := AllTrim(Str(FisGetEnd((cAliasSU5)->U5_END)[2],8))
	oObjeto:Addresses[nY]:District      := (cAliasSU5)->U5_MUN
	oObjeto:Addresses[nY]:State         := (cAliasSU5)->U5_EST
	oObjeto:Addresses[nY]:ZipCode       := (cAliasSU5)->U5_CEP
	oObjeto:Addresses[nY]:Zone          := (cAliasSU5)->U5_BAIRRO
EndIf
			
nY := 0
If !Empty((cAliasSU5)->U5_FCOM1)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "1"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FCOM1,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FCOM1,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FCOM1,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[1],15))
	
	If oObjeto:Phones[nY]:LocalAreaCode == "0"
		oObjeto:Phones[nY]:LocalAreaCode := NIL
	Endif

	If oObjeto:Phones[nY]:CountryAreaCode == "0"
		oObjeto:Phones[nY]:CountryAreaCode := NIL
	Endif
EndIf
If !Empty((cAliasSU5)->U5_FAX)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "2"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FAX,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FAX,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FAX,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[1],15))

	If oObjeto:Phones[nY]:LocalAreaCode == "0"
		oObjeto:Phones[nY]:LocalAreaCode := NIL
	Endif

	If oObjeto:Phones[nY]:CountryAreaCode == "0"
		oObjeto:Phones[nY]:CountryAreaCode := NIL
	Endif
EndIf
If !Empty((cAliasSU5)->U5_CELULAR)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "3"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSU5)->U5_CELULAR,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSU5)->U5_CELULAR,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSU5)->U5_CELULAR,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[1],15))

	If oObjeto:Phones[nY]:LocalAreaCode == "0"
		oObjeto:Phones[nY]:LocalAreaCode := NIL
	Endif

	If oObjeto:Phones[nY]:CountryAreaCode == "0"
		oObjeto:Phones[nY]:CountryAreaCode := NIL
	Endif
EndIf
If !Empty((cAliasSU5)->U5_FONE)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "4"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FONE,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FONE,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSU5)->U5_FONE,(cAliasSU5)->U5_DDD,(cAliasSU5)->U5_CODPAIS)[1],15))

	If oObjeto:Phones[nY]:LocalAreaCode == "0"
		oObjeto:Phones[nY]:LocalAreaCode := NIL
	Endif

	If oObjeto:Phones[nY]:CountryAreaCode == "0"
		oObjeto:Phones[nY]:CountryAreaCode := NIL
	Endif
EndIf
UserFields("SU5",@oObjeto:UserFields,cAliasSU5)

Return(.T.) 