#INCLUDE "wscrm260.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/


Ŀ
Funo    WSCRM260   Autor Eduardo Riera           Data 10.11.2003  
Ĵ
Descrio  Web Service responsavel pelos prospects do Sistema           
                                                                        
Ĵ
Sintaxe                                                                 
Ĵ
Parametros                                                              
Ĵ
Uso        CRM/Materiais/Portais                                        
Ĵ
 Atualizacoes sofridas desde a Construcao Inicial.                       
Ĵ
 Programador   Data    BOPS   Motivo da Alteracao                     
Ĵ
                                                                      
ٱ


/*/
//Ŀ
//Definicao do Web Service de Controle do Usuario                         
//
WSSERVICE CRMPROSPECT         DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/crmprospect.apw" //"Mtodo de consulta aos prospects do sistema ( <b>Restrio de representante comercial</b> )"
   WSDATA UserCode                 As String
   WSDATA Prospect                 As ProspectView
   WSDATA ListOfProspect           As Array Of ProspectView
   WSDATA ProspectID               As String
   WSDATA SellerCode               As String
   WSDATA Header                   AS Array Of BrwHeader
   WSDATA HeaderType               As String
   WSDATA TypeOfAddress            As Array Of GenericStruct
   WSDATA TypeOfPhone              As Array Of GenericStruct
   WSDATA QueryAddWhere            As String OPTIONAL
   WSDATA IndexKey                 As String
   WSDATA SupplierCode				As String

   WSMETHOD GetHeader         DESCRIPTION STR0002 //"Mtodo que descreve as estruturas de retorno do servio"
   WSMETHOD BrwProspect       DESCRIPTION STR0003 //"Mtodo de listagem dos prospects do sistema"
   WSMETHOD GetTypeOfPhone    DESCRIPTION STR0004 //"Mtodo que descreve os tipos de telefone"
   WSMETHOD GetTypeOfAddress  DESCRIPTION STR0005 //"Mtodo que descreve os tipos de endereo"
   WSMETHOD GetProspect       DESCRIPTION STR0006 //"Mtodo de consulta ao dados do prospect"
   WSMETHOD PutProspect       DESCRIPTION STR0007 //"Mtodo de atualizacao do prospect"
ENDWSSERVICE

/*/

Ŀ
Funo    GetHeader Autor   Eduardo Riera          Data 10.11.2003 
Ĵ
          Rotina de recuperacao do header                              
                                                                       
Ĵ
ParametrosExpC1: Nome da Estrutura                                     
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE CRMPROSPECT

::Header := MtHeader(::HeaderType)

Return(.T.)

/*/

Ŀ
Funo    BrwProspecAutor   Eduardo Riera          Data 10.11.2003 
Ĵ
          Rotina de recuperacao dos Prospects de um vendedor           
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpC3: Expressao a ser adicionada na Query                   
          ExpC4: Chave de retorno ( Expressao padrao CodeBase )        
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve os Prospects de um vendedor              
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD BrwProspect WSRECEIVE UserCode,SellerCode,QueryAddWhere,IndexKey WSSEND ListOfProspect WSSERVICE CRMPROSPECT

Local aArea    := GetArea()
Local lRetorno := .T.
Local lQuery   := .F.
Local nX       := 0
Local cQuery   := ""
Local cAliasSUS:= "SUS"
Local cArquivo := ""
#IFDEF TOP
Local aStru    := {}
#ENDIF
//Ŀ
//Inicializa os parametros DEFAULTs                                       
//
dbSelectArea("SUS")
dbSetOrder(1)
DEFAULT ::IndexKey := SUS->(IndexKey())

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMPROSPECT","BRWPROSPECT","SA3",::SellerCode)
	
		lQuery := .T.
		cAliasSUS := "GETPROSPECT"
		aStru  := SUS->(dbStruct())
		cQuery := "SELECT US_FILIAL,US_COD,US_LOJA,US_NOME,US_NREDUZ,US_TIPO,US_END,US_MUN,US_BAIRRO,US_CEP,US_EST,US_DDI,US_DDD,US_TEL, "
		cQuery += "US_FAX,US_EMAIL,US_URL,US_CGC,US_CODCLI,US_LOJACLI,US_ULTVIS,US_VEND,US_STATUS,US_ORIGEM,US_ENTORI "
		cQuery += GetUserField("SUS")
		cQuery += "FROM "+RetSqlName("SUS")+" SUS "
		cQuery += "WHERE "
		cQuery += "SUS.US_FILIAL='"+xFilial("SUS")+"' AND "
		cQuery += "SUS.US_VEND IN ('"+Space(Len(SUS->US_VEND))+"','"+::SellerCode+"') AND "
		cQuery += "SUS.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
	    cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)
	    
	    cQuery := ChangeQuery(cQuery)
	    
	    dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSUS)
	    
	    For nX := 1 To Len(aStru)
			If aStru[nX][2]<>"C" .And. FieldPos(aStru[nX][1])<>0
				TcSetField(cAliasSUS,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
			EndIf	    
	    Next nX
		
	::ListOfProspect := {}
	
	nX := 0
	While !Eof() .And. xFilial("SUS") == (cAliasSUS)->US_FILIAL
	
		aadd(::ListOfProspect,WsClassNew("ProspectView"))
		nX++
		
		GetProspect(cAliasSUS,@::ListOfProspect[nX])

		dbSelectArea(cAliasSUS)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSUS)
		dbCloseArea()
		dbSelectArea("SUS")
	Else
		dbSelectArea("SUS")
		RetIndex("SUS")
		FErase(cArquivo+OrdBagExt())
	EndIf
	//Ŀ
	//Verifica se ha dados a serem retornados                                 
	//
	If Empty( ::ListOfProspect )
		lRetorno := .F.
		SetSoapFault("BRWPROSPECT",STR0008) //"Nao encontrado nenhum prospect para este representante"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetProspecAutor   Eduardo Riera          Data 10.11.2003 
Ĵ
          Rotina de recuperacao dos Prospects do sistema               
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do vendedor                                    
          ExpC3: Codigo do Prospect                                    
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve um prospect cadastrado no sistema        
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD GetProspect WSRECEIVE UserCode,SellerCode,ProspectID WSSEND Prospect WSSERVICE CRMPROSPECT

Local aArea    := GetArea()
Local lRetorno := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMPROSPECT","GETPROSPECT","SA3",::SellerCode)

	dbSelectArea("SUS")
	dbSetOrder(1)
	If MsSeek(xFilial("SUS")+::ProspectId)
	
		GetProspect("SUS",@::Prospect)
		
	Else

		lRetorno := .F.
		SetSoapFault("GETPROSPECT",STR0009) //"Prospect invalido"
		
	EndIf	
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 10.11.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfAddress WSRECEIVE NULLPARAM WSSEND TypeOfAddress WSSERVICE CRMPROSPECT

::TypeOfPhone := {}
aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
::TypeOfAddress[1]:Code := "1"
::TypeOfAddress[1]:Description := STR0010 //"COMERCIAL"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[2]:Code := "2"
//::TypeOfAddress[2]:Description := "COBRANCA"
//aadd(::TypeOfAddress,WsClassNew("GenericStruct"))
//::TypeOfAddress[3]:Code := "3"
//::TypeOfAddress[3]:Description := "ENTREGA"

Return(.T.)


/*/

Ŀ
Funo    GetTypeOfAAutor   Eduardo Riera          Data 10.11.2003 
Ĵ
          Rotina de demonstracao do TypeOfAddress                      
                                                                       
Ĵ
ParametrosNenhum                                                       
                                                                       
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo devolve o header de uma estrutura                
                                                                       
                                                                       
Ĵ
Uso        WEB SERVICES                                                
ٱ


/*/
WSMETHOD GetTypeOfPhone WSRECEIVE NULLPARAM WSSEND TypeOfPhone WSSERVICE CRMPROSPECT

::TypeOfPhone := {}
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[1]:Code := "1"
::TypeOfPhone[1]:Description := STR0010 //"COMERCIAL"
aadd(::TypeOfPhone,WsClassNew("GenericStruct"))
::TypeOfPhone[2]:Code := "2"
::TypeOfPhone[2]:Description := STR0011 //"FAX"

Return(.T.)

/*/

Ŀ
Funo    PutProspecAutor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Rotina de atualizacao dos dados do Prospect                  
                                                                       
Ĵ
ParametrosExpC1: Codigo do usuario                                     
          ExpC2: Codigo do Vendedor                                    
          ExpO3: Objeto do Prospect                                    
Ĵ
Retorno   ExpL1: Indica que o metodo foi avaliado com sucesso          
                                                                       
Ĵ
Descrio Este metodo efetua a atualizacao dos Prospects do sistema    
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
WSMETHOD PutProspect WSRECEIVE UserCode,SellerCode,Prospect WSSEND ProspectID WSSERVICE CRMPROSPECT

Local aArea    := GetArea()
Local aDados   := {}
Local aErro    := {}
Local cErro    := ""
Local lRetorno := .T.
Local nX       := 0
Local nY       := 0
PRIVATE lMsErroAuto    := .F.
PRIVATE lAutoErrNoFile := .T.

//Ŀ
//Verificacao do acesso                                                   
//
If PrtChkUser(::UserCode,"CRMPROSPECT","PUTPROSPECT","SA3",::SellerCode)

	aadd(aDados,{"US_COD"     ,::Prospect:ProspectCode,NIL})
	aadd(aDados,{"US_LOJA"    ,::Prospect:UnitProspectCode,NIL})
	aadd(aDados,{"US_NOME"    ,::Prospect:Name,NIL})
	aadd(aDados,{"US_NREDUZ"  ,::Prospect:NickName,NIL})
	aadd(aDados,{"US_TIPO"    ,::Prospect:Type,NIL})
	aadd(aDados,{"US_CGC"     ,::Prospect:FederalID,NIL})
	aadd(aDados,{"US_EMAIL"   ,::Prospect:Email,NIL})
	aadd(aDados,{"US_URL"     ,::Prospect:HomePage,NIL})
	aadd(aDados,{"US_CODCLI"  ,::Prospect:CustomerCode,NIL})
	aadd(aDados,{"US_LOJACLI" ,::Prospect:UnitCustomerCode,NIL})
	aadd(aDados,{"US_ULTVIS"  ,::Prospect:LastVisit,NIL})	
	aadd(aDados,{"US_VEND"    ,::Prospect:SellerCode,NIL})
	aadd(aDados,{"US_STATUS"  ,::Prospect:StatusCode,NIL})
	aadd(aDados,{"US_ORIGEM"  ,::Prospect:OriginCode,NIL})
	aadd(aDados,{"US_ENTORI"  ,::Prospect:Suppliercode,NIL})

	//Ŀ
	//Validacao dos enderecos                                                 
	//
	For nY := 1 To Len(::Prospect:Addresses)
		If ::Prospect:Addresses[nY]:TypeOfAddress == "1"
			aadd(aDados,{"US_END",AllTrim(::Prospect:Addresses[nY]:Address)+","+::Prospect:Addresses[nY]:AddressNumber,NIL})
			aadd(aDados,{"US_MUN",::Prospect:Addresses[nY]:District,NIL})
			aadd(aDados,{"US_EST",Alltrim(::Prospect:Addresses[nY]:State),NIL})
			aadd(aDados,{"US_CEP",::Prospect:Addresses[nY]:ZipCode,NIL})
			aadd(aDados,{"US_BAIRRO",::Prospect:Addresses[nY]:Zone,NIL})
		Else
			lRetorno := .F.
			SetSoapFault("PUTPROSPECT",STR0012) //"TypeOfAddress invalido"
		EndIf
	Next nY

	//Ŀ
	//Validacao dos telefones                                                 
	//	
	For nY := 1 To Len(::Prospect:Phones)
		Do Case
			Case ::Prospect:Phones[nY]:TypeOfPhone == "1"
				aadd(aDados,{"US_TEL",::Prospect:Phones[nY]:PhoneNumber,NIL})
				If !Empty( ::Prospect:Phones[nY]:LocalAreaCode )
					If Val(::Prospect:Phones[nY]:LocalAreaCode) <> 0
						aadd(aDados,{"US_DDD",::Prospect:Phones[nY]:LocalAreaCode,NIL})
					EndIf
				EndIf
				If !Empty( ::Prospect:Phones[nY]:CountryAreaCode )
					If Val(::Prospect:Phones[nY]:CountryAreaCode) <> 0
						aadd(aDados,{"US_DDI",::Prospect:Phones[nY]:CountryAreaCode,NIL})
					EndIf
				EndIf
			Case ::Prospect:Phones[nY]:TypeOfPhone == "2"
				aadd(aDados,{"US_FAX",::Prospect:Phones[nY]:PhoneNumber,NIL})
			OtherWise
				lRetorno := .F.
				SetSoapFault("PUTPROSPECT",STR0013) //"TypeOfPhone invalido"
		EndCase
	Next nX
	
	PutUserFields("SUS",::Prospect:UserFields,@aDados)
	aDados := WsAutoOpc(aDados, .T.)
	
	//Ŀ
	//Atualizacao do cadastro                                                 
	//
	dbSelectArea("SUS")
	dbSetOrder(1)
	If Empty(::Prospect:ProspectCode) .Or. !MsSeek(xFilial("SUS")+::Prospect:ProspectCode+::Prospect:UnitProspectCode)
		nX := 3
	Else
		nX := 4
	EndIf
	TmkA260(aDados,nX)
	If lMsErroAuto
		aErro := GetAutoGRLog()
   		cErro := ""
	    For nX := 1 To Len(aErro)
	 		cErro += aErro[nX] + Chr(13)+Chr(10)
	    Next nX
		SetSoapFault("PUTPROSPECT",cErro)
		lRetorno := .F.
	EndIf
	
	::ProspectID := SUS->US_COD+SUS->US_LOJA
	
Else

	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/

Ŀ
Funo    GetProspecAutor   Eduardo Riera          Data 11.11.2003 
Ĵ
          Metodo de recuperacao dos Prospect                           
                                                                       
Ĵ
ParametrosExpC1: Alias do SUS                                          
          ExpO2: Objeto que sera atualizado                            
Ĵ
Retorno   Nenhum                                                       
                                                                       
Ĵ
Descrio Este metodo atualiza o objeto prospect passado por referencia
                                                                       
                                                                       
Ĵ
Uso        CRM/Materiais/Portais                                       
ٱ


/*/
Static Function GetProspect(cAliasSUS,oObjeto)

Local nY := 0

oObjeto:ProspectCode           := (cAliasSUS)->US_COD
oObjeto:UnitProspectCode       := (cAliasSUS)->US_LOJA
oObjeto:Name                   := (cAliasSUS)->US_NOME
oObjeto:NickName               := (cAliasSUS)->US_NREDUZ
oObjeto:Type                   := (cAliasSUS)->US_TIPO
oObjeto:FederalId              := (cAliasSUS)->US_CGC
oObjeto:Email                  := (cAliasSUS)->US_EMAIL
oObjeto:HomePage               := (cAliasSUS)->US_URL
oObjeto:CustomerCode           := (cAliasSUS)->US_CODCLI
oObjeto:UnitCustomerCode       := (cAliasSUS)->US_LOJACLI
oObjeto:LastVisit              := (cAliasSUS)->US_ULTVIS
oObjeto:SellerCode             := (cAliasSUS)->US_VEND
oObjeto:StatusCode             := (cAliasSUS)->US_STATUS
oObjeto:StatusDescription      := X3Combo("US_STATUS",(cAliasSUS)->US_STATUS)
oObjeto:OriginCode             := (cAliasSUS)->US_ORIGEM
oObjeto:OriginDescription      := X3Combo("US_ORIGEM",(cAliasSUS)->US_ORIGEM)
oObjeto:SupplierCode           := (cAliasSUS)->US_ENTORI
UserFields("SUS",@oObjeto:UserFields,cAliasSUS)

nY := 0			
If !Empty((cAliasSUS)->US_END)
	DEFAULT oObjeto:Addresses := {}
	aadd(oObjeto:Addresses,WsClassNew("AddressView"))
	nY++
	oObjeto:Addresses[nY]:TypeOfAddress := "1"
	oObjeto:Addresses[nY]:Address       := FisGetEnd((cAliasSUS)->US_END)[1]
	oObjeto:Addresses[nY]:AddressNumber := AllTrim(Str(FisGetEnd((cAliasSUS)->US_END)[2],8))
	oObjeto:Addresses[nY]:District      := (cAliasSUS)->US_MUN
	oObjeto:Addresses[nY]:State         := (cAliasSUS)->US_EST
	oObjeto:Addresses[nY]:ZipCode       := (cAliasSUS)->US_CEP
	oObjeto:Addresses[nY]:Zone          := (cAliasSUS)->US_BAIRRO
EndIf			
nY := 0
If !Empty((cAliasSUS)->US_TEL)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "1"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSUS)->US_TEL,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSUS)->US_TEL,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSUS)->US_TEL,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[1],15))
EndIf
nY := 0
If !Empty((cAliasSUS)->US_FAX)
	DEFAULT oObjeto:Phones := {}
	aadd(oObjeto:Phones,WsClassNew("PhoneView"))
	nY++
	oObjeto:Phones[nY]:TypeOfPhone     := "2"
	oObjeto:Phones[nY]:PhoneNumber     := AllTrim(Str(FisGetTel((cAliasSUS)->US_FAX,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[3],15))
	oObjeto:Phones[nY]:LocalAreaCode   := AllTrim(Str(FisGetTel((cAliasSUS)->US_FAX,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[2],15))
	oObjeto:Phones[nY]:CountryAreaCode := AllTrim(Str(FisGetTel((cAliasSUS)->US_FAX,(cAliasSUS)->US_DDD,(cAliasSUS)->US_DDI)[1],15))
EndIf
Return(.T.)

Function WSCRM260Dummy()
Return
