#INCLUDE "PPJSLANGUAGE.CH"     
#include "protheus.ch"
#Include "FILEIO.ch"  
#INCLUDE "APWEBEX.CH"

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณPrograma  PPJSLANGUAGE  Autorณ Cesar Augusto Bianchi ณ Data ณ 25/09/09 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณTraducao das sentencas utilizadas nos arquivos .JS do Portalณฑฑ
ฑฑณ          ณProtheus WEB.                                               ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณSintaxe   ณ Void                                                       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ Uso      ณ Portal Protheus                                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณProgramador ณ Data   ณ BOPS ณ  Motivo da Alteracao                     ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ            ณ        ณ      ณ                                          ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณMakeJsLangบAutor  ณCesar A. Bianchi    บ Data ณ  25/09/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณVerifica a existencia do arquivo .JS que contem as sentencasบฑฑ
ฑฑบ          ณdo portal Protheus. Se ja existe entao apaga e recria.      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal Protheus WEB                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function CriaJsLang() 
Local lRet := .F.
Local cNameJs		:= "PPLanguage.js"
Local cTexto		:= ""
Local nHdl			:= 1
Local cPulaLin		:= chr(13) + chr(10)
Local aStr			:= GetStrPP()
Local nX			:= 0
Local cEnv     		:= GetEnvServer()
Local cPathWebPP	:= ""
Local cRootPath		:= ""
Private cMyWebJOB	:= GetWebJob()
Private cIniFile 	:= GetAdv97()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณBusca o RootPath do Protheus e dos arquivos WEBณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cPathWebPP	:= GetPathPP()
cRootPath   := alltrim(GetPvProfString( cEnv , "RootPath", '', cIniFile ))

if !Empty(cPathWebPP)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณRemove o caminho inicial "RootPath" da string PathWebณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	cPathWebPP := StrTran(Upper(cPathWebPP), Upper(cRootPath),"")
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se possui a barra no final do cPahWebPPณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	if substr(cPathWebPP,len(cPathWebPP),1) != "\"
		cPathWebPP += "\"	
	endif
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณApaga o atual PPLanguage.jsณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	if File(cPathWebPP+cNameJs)
		if FErase(cPathWebPP+cNameJs) < 0
			//FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', STR0021 , 0, 0, {}) //'Erro durante substitui็ใo do arquivo PPLanguage.js. Processo abortado.'
			Return
		endif
	endif

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณCria o arquivo PPLanguage.jsณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	nHdl := FCreate(cPathWebPP+cNameJs)
	if nHdl < 0
		//FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', STR0021 , 0, 0, {}) //'Erro durante substitui็ใo do arquivo PPLanguage.js. Processo abortado.'
		Return	
	Else
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณGrava as novas sentencasณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		cTexto := ""
		For nX := 1 to len(aStr)
			cTexto += 'var ' + aStr[nX,1] + ' = "' + aStr[nX,2] + '";' + cPulaLin
		Next nX
	   	FSeek(nHdl,0,1)
		FWrite(nHdl, cTexto, Len(cTexto))
		FClose(nHdl) 
		
		if File(cPathWebPP+cNameJs)
			//FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', STR0022 , 0, 0, {}) //'Criando arquivo de idiomas do Portal... OK'
			lRet := .T.
		endif

	endif	
else
	//FWLogMsg('WARN',, 'SIGAPLS', funName(), '', '01', STR0023 , 0, 0, {}) //'Erro durante cria็ใo do arquivo PPLanguage.js. Processo abortado.'
endif

Return lRet

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetPathPP บAutor  ณCesar A. Bianchi    บ Data ณ  25/09/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna o raiz dos arquivos do portal protheus              บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal Protheus WEB                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Function GetPathPP()
Local aIniSessions	:= GetIniSessions(GetADV97())
Local nT			:= 0
Local nX			:= 0
Local nPosSeSession	:= 0
Local cPesqWeb 		:= ""
Local cPathWeb   	:= ""

nPosSeSession:=0
For nT:=1 To Len(aIniSessions)
	cPesqWeb:= GetPvProfString( aIniSessions[nT] , "SIGAWEB", '', cIniFile )
	IF alltrim(Upper(cPesqWeb)) == "PP"
		For nX:=1 To Len(aIniSessions)
			cPesqWeb:= GetPvProfString( aIniSessions[nX] , "RESPONSEJOB", '', cIniFile )
			IF Upper(cPesqWeb)==Upper(cMyWebJOB)
				nPosSeSession:=nX
				Exit
			Endif
		Next nX
	Endif
	IF nPosSeSession<>0
		Exit
	Endif
Next nT

If nPosSeSession > 0
	cPathWeb:= alltrim(GetPvProfString( aIniSessions[nPosSeSession] , "PATH", '(empty)', cIniFile ))
EndIf

Return cPathWeb

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetStrPP  บAutor  ณCesar A. Bianchi    บ Data ณ  25/09/09   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna um array com todas as sentencas utilizadas nos arquiบฑฑ
ฑฑบ          ณvos .JS utilizados pelo Portal Protheus WEB                 บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal Protheus WEB                                        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/
Static Function GetStrPP()
Local aRet := {}

aAdd(aRet,{"STR0001",STR0001})
aAdd(aRet,{"STR0002",STR0002})
aAdd(aRet,{"STR0003",STR0003})
aAdd(aRet,{"STR0004",STR0004})
aAdd(aRet,{"STR0005",STR0005})
aAdd(aRet,{"STR0006",STR0006})
aAdd(aRet,{"STR0007",STR0007})
aAdd(aRet,{"STR0008",STR0008})
aAdd(aRet,{"STR0009",STR0009})
aAdd(aRet,{"STR0010",STR0010})
aAdd(aRet,{"STR0011",STR0011})
aAdd(aRet,{"STR0012",STR0012})
aAdd(aRet,{"STR0013",STR0013})
aAdd(aRet,{"STR0014",STR0014})
aAdd(aRet,{"STR0015",STR0015})
aAdd(aRet,{"STR0016",STR0016})
aAdd(aRet,{"STR0017",STR0017})
aAdd(aRet,{"STR0018",STR0018})
aAdd(aRet,{"STR0019",STR0019})
aAdd(aRet,{"STR0020",STR0020})

Return aRet