#INCLUDE "PMSXWEB.ch"
#INCLUDE "PROTHEUS.CH"

#Define	_PL_			chr(13)+chr(10)
#Define	CL_BOTAO		"Botoes"
           
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenBscAvanบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML com texto descrevendo qtde de resgistros         บฑฑ
ฑฑบ          ณ encontrados.                                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenBscAvan( nReg, aChv, cTxt, aButton )

Local   	cHtml		:= ""
Local		nI			:= 0
Local    nB			:= 0
Local		nTamChv	:= 0

Default 	nReg 		:= 0
Default 	aChv 		:= {}
Default 	cTxt 		:= STR0001 //"Busca Avancada"
Default 	aButton	:= {}

nTamChv	:= Len(aChv)
If !( nTamChv > 0 )
	Return( cHtml )
Endif
If nReg <=0
	Return( cHtml )
Endif

cHtml += '<table border="1" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' +CRLF
cHtml +=    '<tr><td bordercolor="#FFFFFF" class="titulo"> ' +CRLF
cHtml +=       '<table border="0" align="center" cellpadding="2" cellspacing="2">' +CRLF
cHtml +=          '<tr>' +CRLF
cHtml +=             '<td>&nbsp;</td>' +CRLF
cHtml +=             '<td class="TextoDestaques">'+cTxt+' : </td>' +CRLF
cHtml +=             '<td><input type="text" name="cBuscaAv" class="combo" size="8"></td>' +CRLF
cHtml +=             '<td align="left">' +CRLF
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณ Texto com Help explicativo da busca por intervalos ณ
			//ณ igual a busca do Word.                             ณ 	
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cHtml += '<div style="position: relative">' +CRLF
cHtml += 	'<img src="./images/interrogacao.gif" border="0" align="middle" onmouseover="show('+"'help'"+')" onmouseout="hide('+"'help'"+')">' +CRLF
cHtml += 	'<div id="help" style="display: none; position: absolute; left: -160px; top:19px;"  onmouseover="show('+"'help'"+')" onmouseout="hide('+"'help'"+') ">' +CRLF
cHtml += 		'<div style="position: relative; width:350px; height:50px;">' +CRLF
cHtml += 			'<div style="position: absolute; width:100%; height:100%; left:2px; top:2px; background:black; filter:progid:DXImageTransform.Microsoft.Blur(PixelRadius='+"'"+'2'+"'"+', MakeShadow='+"'"+'true'+"'"+', ShadowOpacity='+"'"+'0.50'+"'"+');"></div>' +CRLF
cHtml += 			'<div style="position: absolute; width:100%; height:100%; left:1px; top:1px; border: solid 1px #CCCABC; background: #F4F3F3">' +CRLF
cHtml +=	 				'<table border="0" cellpadding="0" cellspacing="0">' +CRLF
cHtml +=			 			'<tr>' +CRLF
cHtml +=		  		 			'<td class="TituloMenorBold" align="center">' +CRLF
cHtml +=					 			'Ajuda !' +CRLF
cHtml +=					 		'</td>' +CRLF
cHtml +=			 			'</tr>' +CRLF
cHtml +=				 		'<tr>' +CRLF
cHtml +=					 		'<td class="TituloMenor">' +CRLF
cHtml +=						 		'&nbsp;'+' Separe com virgula as expressoes e/ou intervalos da busca. '+'&nbsp;' +CRLF
cHtml +=						 		'<br>' +CRLF
cHtml +=					 			'&nbsp;'+' Ex: 1,3,5-12,4' +CRLF
cHtml +=					 		'</td>' +CRLF
cHtml +=				 		'</tr>' +CRLF
cHtml +=					'</table>' +CRLF
cHtml += 			'</div>' +CRLF
cHtml += 		'</div>' +CRLF
cHtml += 	'</div>' +CRLF
cHtml += '</div>' +CRLF
cHtml +=             '</td>' +CRLF
cHtml +=             '<td>' +CRLF
cHtml +=                '<select name="cComboAv" class="comboselect">' +CRLF
						For nI := 1 To nTamChv
cHtml +=                   '<option value="'+StrW(nI)+'">'+aChv[nI]+'</option>' +CRLF
						Next nI
cHtml +=                '</select>' +CRLF
cHtml +=             '</td>' +CRLF
cHtml +=             '<td><input type="button" value="'+STR0002+'" class="'+CL_BOTAO+'" onClick="fBuscar()"></td>'  +CRLF//'Buscar' 

						////////////////////
						// Botoes adicionais 
						// (array multidimencional)
						// aButton[x][1] - Nome do Botao
						// aButton[x][2] - Acao que executara' pelo evento OnClick
						For nB := 1 To Len(aButton)
cHtml +=             '<td><input type="button" value="' + aButton[nB][1] + '" class="'+CL_BOTAO+'" onClick="' + aButton[nB][2] + '"></td>' + CRLF
						Next nB

cHtml +=             '<td>&nbsp;</td>' +CRLF
cHtml +=          '</tr>' +CRLF
cHtml +=       '</table>' +CRLF
cHtml +=    '</td></tr>' +CRLF

cHtml +=    '</td></tr>' +CRLF
cHtml +=    '</td></tr>' +CRLF

cHtml += '</table>' +CRLF

Return( cHtml )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenButton บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML com botoes padronizados baseados em Array        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenButton( aBut, lCenter, lChamaJava )

Local   	cHtml		:= ''
Local		nI			:= 0
Local		nTam		:= 0
Local 	nMaxTam	:= 0
Local    cNewPage := ""

Default	aBut 			:= {}
Default  lCenter		:= .T.
Default  lChamaJava	:= .F.


nTam := Len(aBut)
If !( nTam > 0 )
	Return( cHtml )
Endif

For nI := 1 To nTam
	If ( nMaxTam < Len(Alltrim(aBut[nI][1])) )
		nMaxTam := Len(Alltrim(aBut[nI][1]))
	Endif
Next nI


//ฺฤฤฤฤฤฤฟ
//ณ HTML ณ
//ภฤฤฤฤฤฤู
cHtml += '<table '+ If(lCenter,'align="center"','') +'>'+CRLF
cHtml += 	'<tr>' +CRLF
			For nI := 1 To nTam
cHtml += 		'<td>' +CRLF
						If lChamaJava
							cNewPage := ' onClick="'+aBut[nI][2]+'" '
						Else             
							cNewPage := ' onClick="window.location='+"'"+aBut[nI][2]+"'"+'" '
						EndIf
cHtml += 			'<input type="button" name="button" value="'+CenterStr( nMaxTam, aBut[nI][1] )+'" class="'+CL_BOTAO+'"' + cNewPage + '>' +CRLF
cHtml += 		'</td>' +CRLF
			Next nI
cHtml += 	'</tr>' +CRLF
cHtml += '</table>' +CRLF

Return( cHtml )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenTxtResuบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML com texto descrevendo qtde de resgistros         บฑฑ
ฑฑบ          ณ encontrados.                                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenTxtResu( nQtde, cTxt, cClass )

Local   	cHtml		:= ""

Default 	nQtde 	:= 0
Default 	cClass	:= "TextoDestaques"
Default	cTxt 		:= "Registro"

cHtml += '<table>' +CRLF
cHtml += 	'<tr>' +CRLF
cHtml += 		'<td class="TextoDestaques">' +CRLF

If nQtde > 1
	cHtml += 		STR0003  +CRLF//'Foram Encontrados '
ElseIf nQtde == 1
	cHtml += 		STR0004  +CRLF//'Foi Encontrado '
ElseIf nQtde == 0
	cHtml += 		STR0005	 +CRLF //'Nao Foi Encontrado Nenhum '
Endif
If nQtde <> 0
	cHtml +=		StrW( nQtde )+' '
Endif

cHtml += 			cTxt+If(nQtde>1,'s','')+'.' +CRLF
cHtml += 		'</td>' +CRLF
cHtml += 	'</tr>' +CRLF
cHtml += '</table>' +CRLF

Return( cHtml )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenTxtErroบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML com texto com erro de retorno do WS              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenTxtErro( cTxt, cTit, lTit )

Local   	cHtml		:= ""
Local 	nTdWidth	:= Len(cTxt)+2

Default	cTxt 		:= ""
Default	cTit		:= ""
Default	lTit		:= .T.

If Empty( cTxt )
	Return( cHtml )
Endif
If Empty( cTit )
	cTit := STR0006 //'Erro:'
Endif
cHtml += '<br>' +CRLF
cHtml += '<table width="100%" border="1" align="center" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' +CRLF
cHtml +=    '<tr>' +CRLF
cHtml +=       '<td bordercolor="#FFFFFF" class="titulo">' +CRLF
cHtml +=          '<table width="100%" border="0" align="center" cellpadding="2" cellspacing="1" bgcolor="#EDEDED">' +CRLF
				If lTit
cHtml += 				'<tr>' +CRLF
cHtml += 					'<td bgcolor="#EDEDED" class="TituloMenorBold" width="'+StrW(nTdWidth)+'" valign="top">' +CRLF'
cHtml += 	                	cTit
cHtml +=                 	'</td>' +CRLF
cHtml += 				'</tr>' +CRLF
				Endif
cHtml +=             		'<tr>' +CRLF
cHtml +=             			'<td bgcolor="#EDEDED" class="TituloMenor" width="100%" valign="top">' +CRLF
cHtml +=                      		cTxt +CRLF
cHtml +=                   		'</td>' +CRLF
cHtml +=             		'</tr>' +CRLF
cHtml +=          '</table>' +CRLF
cHtml +=       '</td>' +CRLF
cHtml +=    '</tr>' +CRLF
cHtml += '</table>' +CRLF

Return( cHtml )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenTblFil บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML da tabela com campos para filtro padrao          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenTblFil( aCpos, aDiv )

Local cHtml	:= ''
Local nI	:= 0
Local nTam	:= 0
Local nPos	:= 0
Local cValue:= "" 
Local lDiv	:= .F.
Local lBloqCpo  := .F.
Default aCpos	:= {}     
Default aDiv	:= {}        
               
//---------------------- ESTRUTURA DO ACPOS -------------------------
// aCpos[1] -> Texto que aparecera no HTML como filtro. Ex: "Data de"
// aCpos[2] -> Nome da variavel do INPUT do form HTML
// aCpos[3] -> Valor do INPUT
// aCpos[4] -> Tipo do INPUT
// aCpos[5] -> Tamanho do Input
// aCpos[6] -> dado adicional para o tag do input (Checked por exemplo)
// aCpos[7] -> Dados para F3
//-------------------------------------------------------------------

nTam := Len( aCpos )
lDiv := Len( aDiv  ) > 0

cHtml += '<table width="550" border="1" align="center" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' +CRLF
cHtml +=    '<tr>' +CRLF
cHtml +=       '<td width="435" height="72" bordercolor="#FFFFFF" class="titulo">' +CRLF
cHtml +=          '<table width="546" border="0" cellspacing="0" cellpadding="2">' +CRLF
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=             '</tr>' +CRLF
					For nI := 1 To nTam
						If Lower(aCpos[nI][4]) <> "hidden"
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td align="right" class="TituloMenor">'+aCpos[nI][1]+' : </td>' +CRLF
cHtml +=                '<td class="TituloMenor">' +CRLF
cHtml +=                   '&nbsp;' +CRLF
									If ValType( aCpos[nI][3] ) <> "C"
										cValue := ""
									Else
										cValue := aCpos[nI][3]
									Endif                           
							lBloqCpo := (aCpos[nI][2] $ "cRESOURCEFROM;cRESOURCETO")
cHtml +=                   '<input name="'+aCpos[nI][2]+'" id ="'+aCpos[nI][2]+'" value="'+cValue+'" type="'+aCpos[nI][4]+'" class="combo" size="'+aCpos[nI][5]+'" '+Iif(Len(aCpos[nI])>=6.And.!Empty(aCpos[nI,6]),aCpos[nI,6],"")+' '+Iif(lBloqCpo,"readonly","")+'>' +CRLF
//Coloca F3
If Len(aCpos[nI]) < 7 .Or. Empty(aCpos[nI,7])
//	cHtml +=                '<td>&nbsp;</td>' +CRLF
Else
//cHtml +=    	            '<td align=left>' +CRLF
							cLinkF3	:=	"'"+MntLinkF3(aCpos[nI][7][1])+"'"
							//Configura o valor default para os parametros adicionais
							If	!Empty(aCpos[nI,7,2])
								cParamAdic	:=	aCpos[nI,7,2]
							Else
								cParamAdic	:=	'new Array()'
							Endif	
							//Configura o valor default para a janela
							If	!Empty(aCpos[nI,7,3])
								cWndParam	:=	aCpos[nI,7,3]
							Else
								cWndParam	:=	'width=400,height=300'
							Endif	
							cWndPAram	+=	',scrollbars=yes'  
							cWndPAram	:=	"'"+cWndParam+"'"                                               
                                                                                   
If Empty(HttpSession->ADMINPMS)
	HttpSession->ADMINPMS:= "1"
EndIf

If !(aCpos[nI][2] $ "cRESOURCEFROM;cRESOURCETO") .or. HttpSession->ADMINPMS == "1"
   cHtml +=				 '<img  src="'+GetPrtSkin()+'/ico-zoom.gif" border="0" onClick="AbreF3('+cLinkF3+","+cParamAdic+","+cWndParam+');"'+' style="cursor:hand" align="middle" alt="'+STR0009+'">' + _PL_//'Busca' 
endif

//cHtml +=                '</td>' +CRLF
Endif
cHtml +=                '</td>' +CRLF          
cHtml +=             '</tr>' +CRLF
							nPos := aScan( aDiv, nI )
							If lDiv .And. nPos>0
cHtml +=          	   '<tr>' +CRLF
cHtml +=          	      '<td>&nbsp;</td>' +CRLF
cHtml +=       	         '<td align="center" class="TituloMenor" colspan=2>' +CRLF
cHtml +=       	         	'<hr width="50%" size="1" color="#E5E5E5">' +CRLF
cHtml +=    	            '</td>' +CRLF
cHtml +=             	'</tr>'	+CRLF
							Endif
						Else
cHtml +=            '<input name="'+aCpos[nI][2]+'" id="'+aCpos[nI][2]+'" value="'+cValue+'" type="'+aCpos[nI][4]+'" class="combo" size="'+aCpos[nI][5]+'" '+Iif(Len(aCpos[nI])>=6.And.!Empty(aCpos[nI,6]),aCpos[nI,6],"")+' >' +CRLF
						Endif	
					Next nI
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td colspan="3">' +CRLF
cHtml +=                   '<table align="center">' +CRLF
cHtml +=                      '<tr>' +CRLF
cHtml +=                         '<td>' +CRLF
cHtml +=                            '<div align="center">' +CRLF
cHtml +=                               '<input name="button" type="button" class="'+CL_BOTAO+'" value="'+STR0002+'" onClick="fBuscar()">&nbsp;&nbsp;&nbsp;&nbsp;' +CRLF //'buscar'
cHtml +=                               '<input name="button" type="button" class="'+CL_BOTAO+'" value="'+STR0007+'" onClick="fLimpar()">'  +CRLF//'limpar'
cHtml +=                            '</div>' +CRLF
cHtml +=                         '</td>' +CRLF
cHtml +=                      '</tr>' +CRLF
cHtml +=                '</td>' +CRLF
cHtml +=             '</tr>' +CRLF
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=             '</tr>' +CRLF
cHtml +=          '</table>' +CRLF
cHtml +=       '</td>' +CRLF
cHtml +=    '</tr>' +CRLF
cHtml += '</table>' +CRLF

Return( cHtml )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenTable  บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML da tabela com o formato padrao WEB Portais       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenTable( nReg, aHdr, aBrw, aLink, aClass, lTamTD, aElemen, aForm, aFuncJs,lGeraForm )

/* ------- .: ESTRUTURA aLink :. -------
Array Multidimensional.
POSICAO 1
	Tipo 			- N
	Descricao	- Numero da coluna TD que sera incluido a tag de link <a>
POSICAO 2
	Tipo 			- C
	Descricao	- Nome da pagina para a propriedade "href"
POSICAO 3
	Tipo 			- C
	Descricao	- Parametros fixos para todos os links.
					Usar para qdo o valor desse paramatro se aplica a todos os itens da tabela.
					Ex: ?cCodCli='1212' 
POSICAO 4
	Tipo 			- A
	Descricao	- Parametros variados em cada linha. 
					Usado, por exemplo, para uma lista de produtos, passando o cod. de cada produto em linhas diferentes.
					Posicao 1
						Tipo			- C
						Descricao	- Nome do parametro. 
					Posicao 2
						Tipo			- N
						Descricao	- Posicao do valor da Var no aBrw (coluna).
					Posicao 3
						Tipo			- C
						Descricao	- String de substituicao de espaco
EXEMPLO :
{
	{
		1,
		"W_PWSP022",
		"",
		{
			{"cProjectCode",1},
			{"cWbsFather"  ,1}
			{"cWbsFather"  ,1,"@SPC@"}
		}
	}
}
   ------- .: ESTRUTURA aLink :. ------- */

Local 	cHtml			:= ""
Local 	cVar			:= ""
Local 	nPosVar		:= 0
Local 	nI				:= 0
Local 	nJ				:= 0
Local 	nK				:= 0
Local 	nTamBrw		:= Len( aBrw )-1
Local 	nTamHdr		:= Len( aHdr )
Local 	cColor		:= "#"
Local 	nAux			:= 0
Local 	lLink			:= .F.
Local 	cLink			:= ""
Local 	aClassDft	:= {"titulo","TituloMenorBold","TituloMenor"} // Classes Default
Local 	nLenClass	:= Len(aClassDft)
Local 	nX
Local 	nPosInput

Default 	lTamTD		:= .F.
Default 	aHdr			:= {}
Default 	aBrw			:= {}
Default 	aLink			:= {}
Default 	aClass		:= {}   
Default 	nReg			:= 0
Default	aElemen		:= {} // Elementos que serao criados, Ex: CheckBox, Radio...
Default	aForm			:= {} // Definicoes da Tag FORM caso seja necessario
Default	lGeraForm	:= .T.// Se deve inicializar e fechar ou nao o form
Default	aFuncJs		:= {} // Nome da Func JavaScript e parametros que sera' chamado no hRef
									// 	Ex:
									// 	1 pos - "javascript:abreJanela(",
									// 	2 pos - ",0,0,true);"
									// resultado-> href="javascript:abreJanela('w_PWSP082.apw?cProjectCode=<%=HttpSession->cProjectCode%>&cWbsCode=<%=HttpSession->cProjectCode%>',0,0,true);"

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Verifica se ha links para inserir ao desenhar a tabela ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
lLink := ( Len(aLink) > 0 )
If lLink
	For nI := 1 To Len(aLink)
		If !Empty(cLink)
			cLink += "#"
		Endif
		cLink += Transform( aLink[nI][1], "" )
	Next nI
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ajusta aClass, classes CSS para formatacao ณ
//ณ da tabela.                                 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Type("aClass") == "A"
	nAux := Len(aClass)
Endif
If nAux < nLenClass
	For nI := 1 To nLenClass
		If ( nAux < nI )
			Aadd( aClass, aClassDft[nI] )
		Endif		
	Next nI
Endif


//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ajusta Form caso aElemen possua conteudo e a Form nao ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lGeraForm .And. Len(aElemen) > 0 .And. Len(aForm) <= 0
	aForm := { "genform","post","" }
	// EX:
	// aForm := { Form Name,Method,Action }
	// <form method="post" action="" name="genform">
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ MONTA HTMLณ
//ภฤฤฤฤฤฤฤฤฤฤฤู
If Len(aForm) > 0
	cHtml += '<form name="'+aForm[1]+'" method="'+aForm[2]+'" action="'+aForm[3]+'" >' +CRLF
Endif
cHtml += '<table border="1" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' +CRLF
cHtml +=    '<tr>' +CRLF
cHtml +=       '<td bordercolor="#FFFFFF" class="'+aClass[1]+'">' +CRLF
cHtml +=          '<table border="0" align="center" cellpadding="2" cellspacing="1" bgcolor="#EDEDED">' +CRLF

							//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
							//ณ Cabecalho da tabela ณ
							//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
cHtml += 				'<tr>' +CRLF
           		    	For nI := 1 To nTamHdr
cHtml += 					'<td bgcolor="#EDEDED" class="'+aClass[2]+'"' +CRLF
								If lTamTD
cHtml +=						' width="'+WidthTD( Max(aHdr[nI][7],Len(aHdr[nI][8])), aHdr[nI][9] )+'"' +CRLF
								Endif
cHtml += 					' valign="top">' +CRLF
cHtml += 	                	RetStr( aHdr[nI][8] ) +CRLF
cHtml +=                '</td>' +CRLF
							Next nI
cHtml += 				'</tr>' +CRLF

					  	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
					  	//ณ Registros da tabela ณ
					  	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
						For nI := 1 To nTamBrw
cHtml +=             '<tr>' +CRLF
							If MOD(nI,2) == 0 
								cColor := "EDEDED"
							Else
								cColor := "FFFFFF"
							Endif
						
							For nJ := 1 To nTamHdr
cHtml +=             	'<td bgcolor="#'+cColor+'" class="'+aClass[3]+'" valign="top">' +CRLF
									///////////////////////////
									// Gera Link Conforme aLink
									If lLink .And. (StrW(nJ) $ cLink)
cHtml +=								'<a href="'
										If Len(aFuncJs) == 2
cHtml +=									aFuncJs[1]+"'"
										Endif
cHtml +=								aLink[nJ][2]+'.APW?'
cHtml +=								aLink[nJ][3]
										For nK := 1 To Len( aLink[nJ][4])
											cVar		:= aLink[nJ][4][nK][1] // Nome da Variavel
											nPosVar	:= aLink[nJ][4][nK][2] // Posicao do valor da Var no aBrw (coluna)
											If !Empty(aLink[nJ][3]) .Or. (nK > 1)
cHtml +=										'&'
											Endif
											If Len(aLink[nJ,4,nK])>=3 .AND. !Empty(aLink[nJ,4,nK,3])
cHtml +=										cVar+'='+StrTran(Alltrim(aBrw[nI+1][nPosVar]), " ",aLink[nJ,4,nK,3])
											Else
cHtml +=										cVar+'='+Alltrim(aBrw[nI+1][nPosVar])
											EndIf
										Next nK
										If Len(aFuncJs) == 2
cHtml +=									"'"+aFuncJs[2]
										Endif
cHtml +=								'" class="links-pagina">' +CRLF
									Endif
									// Gera Link Conforme aLink
									///////////////////////////
									
									//////////////////////////////
									// Impressao do valor do campo
cHtml +=             	   RetStr( If( aHdr[nJ][9]<>"C", aBrw[nI+1][nJ], Alltrim( SubStr(aBrw[nI+1][nJ],1,WidthTD(aHdr[nJ][7],aHdr[nJ][9],.T.)) ) ) ,aHdr[nJ][6], aHdr[nJ][1],.F.) +CRLF
									// Impressao do valor do campo
									//////////////////////////////
									If Len(aElemen) >= nI
										If (nPosInput	:=	AScan(aElemen[nI],{|x| x[1]==nJ})) > 0
cHtml +=     	   	     			aElemen[nI][nPosInput][2]+CRLF
										Endif
									Endif

									If lLink .And. (StrW(nJ) $ cLink)
cHtml +=								'</a>' +CRLF
									Endif
cHtml +=             	'</td>' +CRLF
							Next nJ
cHtml +=             '</tr>' +CRLF
						Next nI
						
						//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
						//ณ Desenha tabela com uma linha em branco ณ
						//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
						If nReg <=0
cHtml += 				'</tr>' +CRLF
           		   	For nI := 1 To nTamHdr
cHtml += 					'<td bgcolor="#FFFFFF" >' +CRLF
cHtml += 	      			'&nbsp;' +CRLF
cHtml +=          		'</td>' 	+CRLF
								Next nI
cHtml += 				'</tr>'		+CRLF
						Endif
cHtml +=          '</table>' +CRLF
cHtml +=       '</td>' +CRLF
cHtml +=    '</tr>' +CRLF
cHtml += '</table>' +CRLF
If Len(aForm) > 0
	cHtml += '</form>' +CRLF
Endif
   
Return( cHtml )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenGetCSS บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML para trazer arquivo de estilo CSS					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenGetCSS( lNewWin )

Local cHtml 		:= ''
Local cDir			:= GetPrtSkin()

Default lNewWin	:= .F.

If cDir == Nil   
	cHtml += '<script>' +CRLF
	cHtml += 	'function CssLog()' +CRLF
	cHtml += 	'{    ' +CRLF
	If lNewWin
		cHtml += 	'opener.parent.location="W_PWSX001.APW";' +CRLF
		cHtml += 	'window.close();' +CRLF
	Else
		cHtml += 	'top.window.location="W_PWSX001.APW";' +CRLF
	Endif
	cHtml += 	'}' +CRLF
	cHtml += 	'CssLog();' +CRLF
	cHtml += '</script>' +CRLF
Else                
	cHtml += '<link href="'+cDir+'/estilo11.css" rel="stylesheet" type="text/css">' +CRLF
Endif

Return( cHtml )  

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGtPtUsrCodบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Verifica atraves da funcao GetUsrCode() se sessao ja'		  บฑฑ
ฑฑบ          ณ foi expirada, caso sim, inicia tela de login.				  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GtPtUsrCod()

Local aUsr := {}

If HttpSession->USR_INFO == Nil
	aUsr := {Nil,Nil,Nil}
Else                
	Aadd( aUsr, HttpSession->USR_INFO[1]:cUserCode 	) // Usuario do Portal
	Aadd( aUsr, HttpSession->USR_INFO[1]:cUserName 	) // Usuario do Protheus - Nome
	Aadd( aUsr, HttpSession->USR_INFO[1]:cUserPassWord) // Usuario do Protheus - Senha
Endif

Return( aUsr )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenCpoDistบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera distribuicao de campos em uma regiao do HTML 	        บฑฑ
ฑฑบ          ณ Usado para diagramacao do folder superior de detalhes.     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/   
Function GenCpoDist( aCpos, cAlias ) 

/* ESTRUTURA DE aCpos
{	
	1 - Nome do Campo a ser impresso, 
	2 - Valor a ser impresso, 
	3 - Tamanho do Cpo (X3_SIZE)
	4 - Tipo do dado (C,N,D..)
}
   ESTRUTURA DE aCpos */

Local   	cHtml		:= ""
Local		nQteReg	:= 0
Local 	nA			:= 0
Local 	nLinAtu	:= 1
Local 	nLinAnt	:= 0
Local		nCol		:= 0
Local 	nTam		:= 0
Local		lTd2		:= .T.
Local		cTamTd1	:= ""
Default	cAlias	:= "AFU"
Default	aCpos		:= {}

nQteReg := Len( aCpos )

Do Case
	Case cAlias == "AFU"
		cTamTd1 := "69%"
	Case cAlias == "AFF"
		cTamTd1 := "71%"
EndCase

cHtml += '<table width="100%" border="0" align="center" cellpadding="0" cellspacing="0">' +CRLF

			For nA := 1 To nQteReg
				nCol++
				nTam := Val( WidthTD( aCpos[nA][3], aCpos[nA][4] ) )

				If nLinAtu == 1
cHtml +=    '<tr>' +CRLF
cHtml +=    	'<td ColSpan="5" height="5" >' +CRLF
cHtml +=    	'</td>' +CRLF
cHtml +=    '</tr>' +CRLF
				Endif
				If nLinAtu > nLinAnt
cHtml +=    '<tr>' +CRLF
					nLinAnt := nLinAtu
				Endif
				
				/////////////////////////
				// .T. = Existira segunda 
				//       coluna de dados
				lTd2 := nTam < 300
				
				//If lTd2
				//	nCol += 2
				//Else
					nCol += 2
				//Endif

					////////////////////////////////
					// Primeiro campo ou campo unico
cHtml +=       '<td class="TituloMenorBold" >' +CRLF
cHtml +=       	RetStr( aCpos[nA][1] )
cHtml +=       '</td>' +CRLF
cHtml +=       '<td'+ If(lTd2,'',' ColSpan=4 ') + If(nQteReg==1,' width="100%" ','') + '>' +CRLF
cHtml +=       	'&#58;&nbsp;' +CRLF
cHtml +=       	'<input name="'+aCpos[nA][1]+'" id="'+aCpos[nA][1]+'" value="'+aCpos[nA][2]+'" type="text" class="combo" '+ If(lTd2,'',' size="'+cTamTd1+'" ') + ' readonly >' +CRLF
cHtml +=       '</td>' +CRLF

				If lTd2 
					nA++
					If (nA > Len(aCpos))      
						//////////////////////////
						// Somente finaliza tabela
						// pois e' ultimo Cpo
cHtml +=				'<td ColSpan=3>&nbsp;</td>'	+CRLF
					Else
					///////////////////////////////////////////
					// Coluna divisora de campos na mesma linha
cHtml +=       '<td Width="4%">&nbsp;</td>' +CRLF

					nCol += 1
					nTam := Val( WidthTD( aCpos[nA][3], aCpos[nA][4] ) )
					////////////////////////////////
					// Segundo campo, qdo necessario
cHtml +=       '<td class="TituloMenorBold" >' +CRLF
cHtml +=       	RetStr( aCpos[nA][1] ) +CRLF
cHtml +=       '</td>' +CRLF
cHtml +=       '<td>' +CRLF
cHtml +=       	'&#58;&nbsp;' +CRLF
cHtml +=       	'<input name="'+aCpos[nA][1]+'" id="'+aCpos[nA][1]+'" value="'+aCpos[nA][2]+'" type="text" class="combo" readonly >' +CRLF
cHtml +=       '</td>'				+CRLF
					Endif
				Endif

				If nLinAtu > nLinAnt
cHtml +=    '</tr>' +CRLF
				Endif
            
				If nCol > 2
					nCol := 0
					nLinAtu++
				Endif
			Next nA

cHtml += '</table>' +CRLF

Return( cHtml )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenForm   บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera form com distribuicao de campos em uma regiao do HTML บฑฑ
ฑฑบ          ณ Funcao generica para Incluir-Editar-Excluir-Visualizar     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/   
Function GenForm( aCpos, nOpc )

/* 
	.: ESTRUTURA DE aCpos :.
	------------------------
{	
	 1 - Nome do Campo a ser impresso, 
	 2 - Valor a ser impresso, 
	 3 - Tamanho do Cpo (X3_SIZE)
	 4 - Tipo do dado (C,N,D..)
	 5 - Ordem definida no SX3
	 6 - Define se campo pode ser Editado ou Nao
	 7 - Nome do Cpo no SX3 (Sera' usado para Post, nome do Input)
	 8 - F3
 	 9 - ComboBox ( string )
	10 - Valor inicial do Cpo, caso exista
	11 - Valid de campo em JavaScript
	12 - Maxlength do campo
}
 
 .: Valores do nOpc - Padrao Microsiga :.
 ----------------------------------------
 1 = Pesquisa (Nao usado nessa funcao)
 2 = Visualiza
 3 = Inclui
 4 = Edita
 5 = Exclui
*/   

Local	cHtml			:= ""
Local cValue		:= ""
Local cMes			:= ""
Local cAno			:= ""
Local	nQteReg		:= 0
Local	nA				:= 0
Local	nX				:= 0
Local	nLinAtu		:= 1
Local	nLinAnt		:= 0
Local	nCol			:= 0
Local	nTam			:= 0
Local	nTamProx		:= 0
Local	lTd2			:= .T.
Local	lTd1Normal	:= .T.
Local	lReadOnly	:= .F.
Local	lEditable	:= .F.
Local cFuncJava	:= ""
Local lMemo			:= .F.
Local aCBox			:= {}
Local cWndParam	:=	""
Local cParamAdic	:=	""
Local cLinkF3		:=	""
Local lTemF3		:=	.F.
Local oObjCfg 		:= IIf( FindFunction( 'GetAuthWs' ), GetAuthWs( 'WSCFGDICTIONARY' ), WSCFGDICTIONARY():New() )
Local lEditF3	:=	.F.

WsChgUrl(@oObjCfg,"CFGDICTIONARY.apw")

If oObjCfg:GETPARAM( GetUsrCode(), "MV_PMSW001" )
	lEditF3 := (oObjCfg:cGETPARAMRESULT == "1")
Else
	HttpSession->cLinkErro  := ""
	HttpSession->cBotaoErro := ""
	HttpSession->nNewWin    := 1
	HttpSession->cErro      := StrGantt( GetWscError(3) )
	cHtml                   := ExecInPage( "PWSP001" )
	Return cHtml
EndIf
Default aCpos		:= {}
Default nOpc		:= 2

//////////////////////////////////////////
// Ajusta aCpos para que tenha 11 posicoes
For nA := 1 To Len(aCpos)
	If Len(aCpos[nA]) == 10
		Aadd( aCpos[nA], "" ) // Elemento do Valid
		Aadd( aCpos[nA], "" ) // Elemento do maxlength
	Endif
Next nA

nQteReg 		:= Len( aCpos ) 
lReadOnly	:= ( Alltrim(Str(nOpc,0)) $ "2#5" ) 
lEditable	:= .F.

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ordena Cpos conforme ordem definida no SX3 ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aCpos := aSort( aCpos,,, {|x,y|x[5]<y[5]} )

Do Case
	Case nOpc == 2 
		cFuncJava := "fechaJanela()"
	Case nOpc == 3   
		cFuncJava := "IncluiReg()"
	Case nOpc == 4
		cFuncJava := "EditaReg()"
	Case nOpc == 5        
		cFuncJava := "ExcluiReg()"
EndCase

cHtml += '<form method="post" action="" name="genform">' + _PL_
cHtml += 	'<table width="100%" align="center" border="0" cellpadding="0" cellspacing="0">' + _PL_
cHtml +=    	'<tr>' + _PL_
cHtml +=    		'<td align="left">' + _PL_
cHtml += 				'<table border="1" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' + _PL_
cHtml +=    				'<tr>' + _PL_
cHtml +=    					'<td>' + _PL_
cHtml += 							'<table border="0" cellpadding="0" cellspacing="0" bgcolor="#E5E5E5" >' + _PL_
cHtml +=    							'<tr>' + _PL_
cHtml +=    								'<td width="30" align="center">' + _PL_
cHtml +=    									'<a href="#" onClick="javascript:'+cFuncJava+';" >' + _PL_
cHtml +=    										'<img src="'+GetPrtSkin()+'/ok.gif" border="0" alt="'+STR0011+'" >' + _PL_ //'Ok'
cHtml +=    									'</a>' + _PL_
cHtml +=    								'</td>' + _PL_
cHtml +=    								'<td width="30" align="center">' + _PL_
cHtml +=    									'<a href="#" onClick="javascript:fechaJanela();" >' + _PL_
cHtml +=    										'<img src="'+GetPrtSkin()+'/cancela.gif" border="0" alt="'+STR0012+'" >' + _PL_//'Cancela'
cHtml +=    									'</a>' + _PL_
cHtml +=    								'</td>' + _PL_
cHtml +=    								'<td width="*" align="left">' + _PL_
cHtml +=    									'&nbsp;' + _PL_
cHtml +=    								'</td>' + _PL_
cHtml +=    							'</tr>' + _PL_
cHtml +=   							'</table>' + _PL_
cHtml +=    					'</td>' + _PL_
cHtml +=    				'</tr>' + _PL_
cHtml += 				'</table>' + _PL_
cHtml +=    		'</td>' + _PL_
cHtml +=    	'</tr>' + _PL_
cHtml +=    	'<tr>' + _PL_
cHtml +=    		'<td align="center">' + _PL_
cHtml += 				'<table border="1" width="100%" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">' + _PL_
cHtml += 				'<tr><td>' + _PL_
cHtml += 					'<table width="100%" border="0" align="center" cellpadding="0" cellspacing="0">' + _PL_

							For nA := 1 To nQteReg

								While aCpos[nA][4] == "H"
cHtml +=							'<input type="hidden" name="'+aCpos[nA][7]+'" value="'+AllTrim(Iif(ValType(aCpos[nA][2])=="N",Str(aCpos[nA][2]),aCpos[nA][2]))+'">'
									nA++
									If nA > nQteReg
										Exit
									Endif
								EndDo
								
								/////////////////////////////////////////////
								// Medida de protecao para nao estourar array
								// evita: "array out of bounds"
								If nA > nQteReg
									Exit
								Endif

								//Verifica se tem tecla F3 (consulta padrao) Configurada
								lTemF3	:=	ValType(aCpos[nA][8]) == "A" .And. Len(aCpos[nA][8]) == 3  .And. ValType(aCpos[nA][8][1]) == "A"
                                nCol++
								nTam := Val( WidthTD( aCpos[nA][3], aCpos[nA][4] ) )
				      		If nQteReg >= nA+1
									nTamProx := Val( WidthTD( aCpos[nA+1][3], aCpos[nA+1][4] ) )
								Endif
								If nLinAtu == 1
cHtml +=    					'<tr>' + _PL_
cHtml +=    						'<td ColSpan="3" height="5" >' + _PL_
cHtml +=    							'&nbsp;' + _PL_
cHtml +=    						'</td>' + _PL_
cHtml +=    					'</tr>' + _PL_
								Endif
								                                                        
								lMemo := ( aCpos[nA][4] == "M" )
								
								If nLinAtu > nLinAnt
cHtml +=    					'<tr'+ If(lMemo,' valign="top" ','') + '>' + _PL_
									nLinAnt := nLinAtu
								Endif
								
								/////////////////////////
								// .T. = Existira segunda 
								//       coluna de dados
								If lMemo
									lTd2 := .F.
								Else
									lTd2 := (nTam < 300 .And. nTamProx < 300)
								Endif
									lTd1Normal	:=	(nTam < 300).And. !lMemo
									nCol += 2
										////////////////////////////////
										// Primeiro campo ou campo unico
cHtml +=       					'<td Width="2%">' + _PL_
cHtml +=       					'</td>' + _PL_
cHtml +=       					'<td class="TituloMenorBold" ' + If(lMemo,' valign="top" ','') + ' >' + _PL_
cHtml +=       						RetStr( aCpos[nA][1] ) + _PL_
cHtml +=       					'</td>' + _PL_

cHtml +=       					'<td' + If(lMemo,' valign="top" ','') + ' >' + _PL_
cHtml +=       						'&#58;&nbsp;' + _PL_ // -> ': '
cHtml +=       					'</td>' + _PL_

cHtml +=       					'<td'+ If(lTd1Normal,'',' ColSpan=4 ') + If(nQteReg==1,' width="100%" ','') + If(lMemo,' valign="top" ','') + ' >' + _PL_
										If nOpc == 3
											If aCpos[nA][6]=="-1"
												lEditable := .F.   
											Else
												lEditable := .T.   
											Endif
										Else
											lEditable := aCpos[nA][6]=="1"
										Endif
										If lMemo
cHtml +=									'<TextArea name="'+aCpos[nA][7]+'" cols=73 rows=4 ' + If(lReadOnly,' readonly class="comboRO" ',If(!lEditable,' readonly class="comboRO" ',' class="combo" ')) + ' >' + _PL_
cHtml +=										Alltrim( aCpos[nA][2] )
cHtml +=									'</TextArea>' + _PL_
										Else                        
											/////////////////////////
											// Tratamento de ComboBox
											If Empty( aCpos[nA][9] ) 
												
												/////////////////////////////////////
												// Trata Valor Inicial para Inclusoes
												If Empty( aCpos[nA][10] ) .Or. !Empty( StrTran(aCpos[nA][2],'&nbsp;','') )
											  		cValue := aCpos[nA][2] // Valor do Campo no BD
											 	Else
											  		cValue := Alltrim( RetStr(aCpos[nA][10] ,,,, Empty(aCpos[nA][12])) ) // Valor Inicial
												Endif
												If (lTemF3 .And. !lEditF3) .And. ( !lReadOnly .And. lEditable )                     
cHtml +=       								'<a href="#" title="'+"Utilize a lupa ao lado para preencher o campo"+'" >'
												Endif
												///////////////
												// campo normal
cHtml +=       							'<input name="'+aCpos[nA][7]+'" id="'+aCpos[nA][7]+'" value="'+cValue+'" type="text" '+ If(lTd1Normal,'',' size="71%" ') 
cHtml +=													If( Empty(aCpos[nA][11]), '', ' onChange="' +aCpos[nA][11]+'" ')
cHtml +=													If( Empty(aCpos[nA][12]), '', ' maxlength="'+aCpos[nA][12]+'" ')
cHtml +=													If(lReadOnly .Or. (lTemF3 .And. !lEditF3),' readonly class="comboRO" ',If(!lEditable,' readonly class="comboRO" ',' class="combo" ')) + ' >' + _PL_
												If (lTemF3 .And. !lEditF3) .And. ( !lReadOnly .And. lEditable )                     
cHtml +=       								'</a>'
												Endif
												
												/////////////////////////
												// F3, Calc ou Calendario
												If lTemF3 .And. ( !lReadOnly .And. lEditable )                     

													cLinkF3	:=	"'"+MntLinkF3(aCpos[nA][8][1])+"'"
													//Configura o valor default para os parametros adicionais
													If	!Empty(aCpos[nA,8,2])
														cParamAdic	:=	aCpos[nA,8,2]
													Else
														cParamAdic	:=	'new Array()'
													Endif	
													//Configura o valor default para a janela
													If	!Empty(aCpos[nA,8,3])
														cWndParam	:=	aCpos[nA,8,3]
													Else
														cWndParam	:=	'width=400,height=300'
													Endif	
													cWndPAram	+=	',scrollbars=yes'  
													cWndPAram	:=	"'"+cWndParam+"'"                                               
cHtml +=												'<img src="'+GetPrtSkin()+'/ico-zoom.gif" border="0" onClick="AbreF3('+cLinkF3+","+cParamAdic+","+cWndParam+');"'+' style="cursor:hand" align="middle" alt="'+STR0009+'">' + _PL_//'Busca' 
												ElseIf ( !lReadOnly .And. lEditable )
													Do Case
														Case aCpos[nA][4] == "N" // Calculadora
cHtml +=														'<img src="'+GetPrtSkin()+'/ico-calculadora.gif" border="0" style="cursor:hand" onClick="javascript:window.open('+"'h_PWSXCalc.apw?CMPDEST="+aCpos[nA][7]+"', 'jF3', 'width=180,height=210,scrollbars=no'"+');" align="middle" alt="'+STR0010+'" >' + _PL_ //'Calculadora'
														Case aCpos[nA][4] == "D" // Calendario
															cMes := Alltrim( Str( Month( CtoD(StrTran(aCpos[nA][2],"&nbsp;","")) )-1 ))
															cAno := Alltrim( Str( Year(  CtoD(StrTran(aCpos[nA][2],"&nbsp;","")) ) ))
cHtml +=														'<img src="./images/ico-calendario.gif" border="0" style="cursor:hand" onClick="javascript:window.open('+"'h_PWSXCALE.apw?Month="+cMes+"&Year="+cAno+"&CMPDEST="+aCpos[nA][7]+"', 'jF3', 'width=280,height=180,scrollbars=no'"+');" align="middle" alt="'+STR0008+'" >' + _PL_ //'Calendario'
													EndCase
												Endif
												
											Else
												///////////
												// combobox
												aCBox := StoACBox( aCpos[nA][9], .T. )
cHtml +=       							'<Select name="'+aCpos[nA][7]+'" size="1"' + If(lReadOnly,' disabled class="comboselectRO" ',If(!lEditable,' disabled class="comboselectRO" ',' class="comboselect" ')) + ' >' + _PL_
												For nX := 1 To Len( aCBox )
cHtml +=       								'<option value="'+aCBox[nX][1]+'"'+ If( (Alltrim(StrTran(aCpos[nA][2],'&nbsp;',' '))==aCBox[nX][2]),' selected ','') +'>' + aCBox[nX][2] + '</option>' + _PL_
												Next nX
cHtml +=       							'</select>' + _PL_
											Endif
										Endif
cHtml +=       					'</td>' + _PL_

//									EndIf
								/////////////////////////////////////////
								// Inclui Segunda coluna em branco 
								If !lTd2 .And. lTd1Normal
cHtml +=							'<td ColSpan=3>&nbsp;</td>' + _PL_
								Endif
								If lTd2 

									/////////////////////////////////////////
									// Trata nA para nao exceder tam do array 
									nA++
									If nA > nQteReg
										Exit
									Endif
                                    
									/////////////////////////////////////////
									//Trata campos Hidden
									While aCpos[nA][4] == "H"
cHtml +=							'<input type="hidden" name="'+aCpos[nA][7]+'" value="'+AllTrim(Iif(ValType(aCpos[nA][2])=="N",Str(aCpos[nA][2]),aCpos[nA][2]))+'">'
										nA++
										If nA > nQteReg
											Exit
										Endif
									End
									/////////////////////////////////////////
									// Trata nA para nao exceder tam do array (novamente devido a campo Hidden)
									If nA > nQteReg
										Exit
									Endif

									
									//////////////////////////////////
									// Pula linha caso seja Campo MEMO 
									// na segunda coluna
									If aCpos[nA][4] == "M"
										nA--
										nCol := 0
										nLinAtu++
										Loop
									Endif

									//Verifica se tem tecla F3 Configurada
									lTemF3	:=	ValType(aCpos[nA][8]) == "A" .And. Len(aCpos[nA][8]) == 3  .And. ValType(aCpos[nA][8][1]) == "A"

									If nOpc == 3
										If aCpos[nA][6]=="-1"
											lEditable := .F.   
										Else
											lEditable := .T.   
										Endif
									Else
										lEditable := aCpos[nA][6]=="1"
									Endif
									If (nA > Len(aCpos))      
										//////////////////////////
										// Somente finaliza tabela
										// pois e' ultimo Cpo
cHtml +=								'<td ColSpan=3>&nbsp;</td>' + _PL_
cHtml +=    							'&nbsp;' + _PL_
cHtml +=       					'</tr>' + _PL_
									Else
										///////////////////////////////////////////
										// Coluna divisora de campos na mesma linha
cHtml +=       					'<td Width="3">&nbsp;</td>' + _PL_

										nCol += 1
										nTam := Val( WidthTD( aCpos[nA][3], aCpos[nA][4] ) )
										////////////////////////////////
										// Segundo campo, qdo necessario
cHtml +=       					'<td class="TituloMenorBold" >' + _PL_
cHtml +=       						RetStr( aCpos[nA][1] ) + _PL_
cHtml +=       					'</td>' + _PL_
cHtml +=       					'<td>' + _PL_
cHtml +=       						'&#58;&nbsp;' + _PL_ // -> ': '
											/////////////////////////
											// Tratamento de ComboBox
											If Empty( aCpos[nA][9] ) 

												///////////////
												// campo normal
												If Empty( aCpos[nA][10] ) .Or. !Empty( StrTran(aCpos[nA][2],'&nbsp;','') )
											  		cValue := aCpos[nA][2] // Valor do Campo no BD
											 	Else
											  		cValue := Alltrim( RetStr(aCpos[nA][10],,,, Empty(aCpos[nA][12])) ) // Valor Inicial
												Endif                      
												If (lTemF3 .And. !lEditF3) .And. ( !lReadOnly .And. lEditable )                     
cHtml +=       								'<a href="#" title="'+"Utilize a lupa ao lado para preencher o campo"+'" >'
												Endif
cHtml +=       							'<input name="'+aCpos[nA][7]+'" id="'+aCpos[nA][7]+'" value="'+cValue+'" type="text" ' 
cHtml +=													If( Empty(aCpos[nA][11]), '', ' onChange="' +aCpos[nA][11]+'" ')
cHtml +=													If( Empty(aCpos[nA][12]), '', ' maxlength="'+aCpos[nA][12]+'" ')
cHtml += 												If(lReadOnly.Or.(lTemF3 .And. !lEditF3),' readonly class="comboRO" ',If(!lEditable,' readonly  class="comboRO" ',' class="combo" ')) + ' >' + _PL_
												If (lTemF3 .And. !lEditF3) .And. ( !lReadOnly .And. lEditable )                     
cHtml +=       								'</a>'
												Endif
												/////////////////////////
												// F3, Calc ou Calendario
												If lTemF3 .And. ( !lReadOnly .And. lEditable )                     
													cLinkF3	:=	"'"+MntLinkF3(aCpos[nA][8][1])+"'"
													//Configura o valor default para os parametros adicionais
													If	!Empty(aCpos[nA,8,2])
														cParamAdic	:=	aCpos[nA,8,2]
													Else
														cParamAdic	:=	'new Array()'
													Endif	
													//Configura o valor default para a janela
													If	!Empty(aCpos[nA,8,3])
														cWndParam	:=	aCpos[nA,8,3]
													Else
														cWndParam	:=	'width=400,height=300'
													Endif	
													cWndPAram	+=	',scrollbars=yes'  
													cWndPAram	:=	"'"+cWndParam+"'"                                               
cHtml +=												'<img src="'+GetPrtSkin()+'/ico-zoom.gif" border="0" onClick="AbreF3('+cLinkF3+","+cParamAdic+","+cWndParam+');"'+' style="cursor:hand" align="middle" alt="'+STR0009+'">' + _PL_//'Busca' 
												ElseIf ( !lReadOnly .And. lEditable )
													Do Case
														Case aCpos[nA][4] == "N" // Calculadora
cHtml +=														'<img src="'+GetPrtSkin()+'/ico-calculadora.gif" border="0" style="cursor:hand" onClick="javascript:window.open('+"'h_PWSXCalc.apw?CMPDEST="+aCpos[nA][7]+"', 'jF3', 'width=180,height=210,scrollbars=no'"+');" align="middle" alt="'+STR0010+'" >' + _PL_ //'Calculadora'
														Case aCpos[nA][4] == "D" // Calendario
															cMes := Alltrim( Str( Month( CtoD(StrTran(aCpos[nA][2],"&nbsp;","")) )-1 ))
															cAno := Alltrim( Str( Year(  CtoD(StrTran(aCpos[nA][2],"&nbsp;","")) ) ))
cHtml +=														'<img src="./images/ico-calendario.gif" border="0" style="cursor:hand" onClick="javascript:window.open('+"'h_PWSXCALE.apw?Month="+cMes+"&Year="+cAno+"&CMPDEST="+aCpos[nA][7]+"', 'jF3', 'width=280,height=180,scrollbars=no'"+');" align="middle" alt="'+STR0008+'" >' + _PL_//'Calendario'
													EndCase
												Endif

											Else
												///////////
												// combobox
												aCBox := StoACBox( aCpos[nA][9], .T. )
cHtml +=       							'<Select name="'+aCpos[nA][7]+'" size="1"' + If(lReadOnly,' disabled class="comboselectRO" ',If(!lEditable,' disabled class="comboselectRO" ',' class="comboselect" ')) + ' >' + _PL_
												For nX := 1 To Len( aCBox )
cHtml +=       								'<option value="'+aCBox[nX][1]+'"'+ If( (Alltrim(StrTran(aCpos[nA][2],'&nbsp;',' '))==aCBox[nX][2]),' selected ','') +'>' + aCBox[nX][2] + '</option>' + _PL_
												Next nX
cHtml +=       							'</select>' + _PL_
											Endif
cHtml +=       					'</td>' + _PL_
									Endif
								Endif
								
								///////////////////////////////////////////
								// Coluna divisora de campos na mesma linha
cHtml +=     				'<td Width="3">&nbsp;</td>' + _PL_
cHtml +=   					'</tr>' + _PL_
            
								If nCol > 2
									nCol := 0
									nLinAtu++
								Endif
							Next nA

							If Right(cHtml,5) <> '</tr>'
cHtml +=    				'</tr>' + _PL_								
							Endif

cHtml +=    				'<tr>' + _PL_
cHtml +=    					'<td ColSpan="6" height="5" >' + _PL_
cHtml +=    						'&nbsp;' + _PL_
cHtml +=    					'</td>' + _PL_
cHtml +=    				'</tr>' + _PL_
cHtml += 				'</table>' + _PL_
cHtml += 				'</tr></td>' + _PL_
cHtml += 				'</table>' + _PL_
cHtml +=    		'</td>' + _PL_
cHtml +=    	'</tr>' + _PL_
cHtml += 	'</table>' + _PL_
cHtml += '</form>' + _PL_

Return( cHtml )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGenFolder บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Gera tabela com botoes de folder e efeito com JavaScript   บฑฑ
ฑฑบ          ณ com o SwapImage													     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/   
Function GenFolder( aBut, cNmImg, cTpImg, cDrImg, cTarget )
/* 
	.: ESTRUTURA DE aBut :.
	-----------------------
{	
	 1 - N - Tamanho do width da TD
	 2 - C - Link para a propriedade do hRef 
	 3 - Nome do Botao, sera' usado para nome do Gif, nome e Id da tag <IMG>
}
*/   
Local	cHtml			:= ""
Local nQtdBut		:= 0
Local nA				:= 0
Local nB				:= 0

Default aBut		:= {}
Default cNmImg 	:= "folder_"
Default cTpImg 	:= ".gif"
Default cDrImg 	:= "images/"
Default cTarget	:= "bottomFrame"

/////////////////////////////////////////////
// Definira' qtde de TD's na Tabela e
// qtde de elementos na chamada do JavaScript
nQtdBut := Len( aBut )

cHtml += '<table border="0" cellspacing="0" cellpadding="0">'
cHtml += 	'<tr valign="bottom" >'
	
				For nA := 1 To nQtdBut
cHtml += 		'<td height="24" width="'+RetStr(aBut[nA][1],,,.F.)+'" valign="bottom" >'
cHtml += 			'<a href="'+aBut[nA][2]+'" target="bottomFrame" '
							///////////////
							// Gera Onclick
cHtml += 				'onClick="PMS_swapImage('
						For nB := 1 To nQtdBut
cHtml += 				"'bt_"+aBut[nB][3]+"','','" + cDrImg + cNmImg + aBut[nB][3] + If(nA==nB,'enable','disable') + cTpImg + "'" 
							If nB < nQtdBut
cHtml += 					","
							Endif
						Next nB
cHtml += 				',1)">'
							// Gera Onclick
							///////////////
cHtml += 				'<img src="'+ cDrImg + cNmImg + aBut[nA][3] + If(nA=1,'enable','disable') + cTpImg +'" border="0" name="bt_'+aBut[nA][2]+'" id="bt_'+aBut[nA][3]+'">
cHtml += 			'</a>'
cHtml += 		'</td>'
				Next nA
		
cHtml += 	'</tr>'
cHtml += '</table>'

Return( cHtml )

/*/
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    ณGetPost	ณAutor  ณ Cristiano Denardi     ณ Data ณ11.08.2005 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ          ณOrganiza em um array dados enviados de um form, numero de 	ณฑฑ
ฑฑณ          ณCpos nao eh fixo.														 	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ Portal PMS                                                  ณฑฑ 
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function GetPost( aPost )

Local nI      		:= 0
Local nX      		:= 0
Local aVal	   	:= {}
Local cVarName		:= ""
Local cVarPref		:= "HttpPost->"
Local aAscExc		:= {}
Local cVal			:= ""

Default aPost		:= {} //HttpPost->aPost

//////////////////////////////////////////////////
// Lista de Caracteres a serem excluidos da string
// possivel lixo inserido no momento do Post
Aadd( aAscExc, 9   )
Aadd( aAscExc, 32  )
Aadd( aAscExc, 160 )

For nI := 1 to Len(aPost)
	cVarName := cVarPref + aPost[nI]
	cVal     := &cVarName
	
	For nX := 1 to Len( aAscExc )
		cVal := StrTran( cVal, chr(aAscExc[nX]), ' ' )
	Next nX
	
	Aadd( aVal, {aPost[nI], Alltrim(cVal)} )
Next
							
Return( aVal )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑ																		   						ฑฑ
ฑฑ                            FUNCOES AUXILIARES                           ฑฑ
ฑฑ																		   						ฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRetStr    บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Certifica que retorno sempre sera "C" 	        				  บฑฑ
ฑฑบ          ณ Ajusta retorno caso o campo seja de uma ComboBox.   		  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function RetStr( xValue, cPict, cBox, lStrTran, lNbsp )

Local cRet  		:= ""
Local	aBox			:= {}
Local nPos			:= 0
Local nPosI			:= 0
Local nPosF			:= 0

Default cPict		:= ""
Default cBox		:= ""
Default lStrTran	:= .T.
Default lNbsp		:= .T.

If Empty(cBox)
	Do Case
		Case ValType(xValue) == "C"
			cRet := xValue  
			
		Case ValType(xValue) == "N"
			cRet := Transform( xValue, cPict )
			
		Case ValType(xValue) == "D"
			cRet := DtoC(xValue)
			
	EndCase
Else
	aBox	:= {} 
	nPosI	:= 1
	nPosF	:= Len(cBox)
	
	While .T. .And. !Empty( Alltrim(xValue) )
		nPos := At( xValue, SubStr(cBox,nPosI,nPosF) )
		
		If nPos > 0
			If SubStr(cBox,nPos+1,1) == "="

				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณ Ja' encontrou o numero desejado na string ณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				nPosI := nPos+1
				nPosF := At( ";", SubStr(cBox,nPosI,nPosF) )
				If nPosF <> 0    
					cRet := SubStr( cBox, nPosI+1, nPosF-2   )
					Exit
				Else
					cRet := SubStr( cBox, nPosI+1,Len(cBox)  )
					Exit
				Endif
			Else 
				//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
				//ณ Acerta as variaveis para o Loop de nova busca ณ
				//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
				nPosI := nPos+1  
			Endif
		Else
			cRet := RetStr( xValue )
			Exit
		Endif
	EndDo
Endif

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Ajusta texto para formatacao web ณ
//ณ para evitar quebra do texto      ณ
//ณ dentro de uma tabela.		       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If lStrTran
	cRet := Alltrim( cRet                )
	cRet := StrTran( cRet, "%", "&#37; " )
	cRet := StrTran( cRet, "-", "&#46; " ) // &#45; = hifen / &#45; = ponto
	cRet := StrTran( cRet, " ", "&nbsp;" )
	If lNbsp
		cRet := "&nbsp;" + cRet + "&nbsp;"
	Endif
Endif

Return( cRet )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณRetStrJvSc บAutor  ณCristiano Denardi   บ Data ณ  27.07.06  บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna uma string para ser usada dentro de um Java Script บฑฑ
ฑฑบ          ณ Ela se atenta para que a string tenha '' em torno dela     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function RetStrJvSc( cValue, nEsq, nDir, lAspa )

Local cAspa1	:= "'" // Aspa Simples
Local cAspa2	:= '"' // Aspas Dlupa
Local cRet		:= ""
Local nTam		:= 0

Default cValue	:= ""
Default nDir	:= 0
Default nEsq	:= 0
Default lAspa	:= .T.

/*
Essa funcao foi escrita para proteger o Portal de possiveis erros humanos no AtuSX
Acontece que algumas string foram cadastradas no AtuSX entre aspas Duplas e simples,
Ex: " 'teste'  ", dessa forma, ao passar uma STR0001 para uma funcao JavaScript, nao
era necessario fazer a concatenacao da string com aspas simples, pois em JS sem estas
aspas, o STR0001 seria considerado variavel, causando erro na execucao.
Entao, mesmo que no Atusx esteja com ou sem aspas, essa funcao garante o retorno 
correto para a Funcao em JavaScript.
*/

If !Empty( cValue )
	cRet := cValue
	
	If lAspa
		/////////////////////
		// Tira a ultima aspa
		If	Right( cValue, 1 ) == cAspa1 .Or.; 
			Right( cValue, 1 ) == cAspa2
		
			nTam := Len( cRet )
			cRet := SubStr( cRet, 1, nTam-1 )
		EndIf
		
		///////////////////////
		// Tira a primeira aspa
		If	Left( cValue, 1 ) == cAspa1 .Or.; 
			Left( cValue, 1 ) == cAspa2
		
			nTam := Len( cRet )
			cRet := SubStr( cRet, 2, nTam )
		EndIf
	Endif
		
	///////////////////////////////
	// Insere espacos conforme nEsq
	If nEsq > 0
		cRet := Space(nEsq) + cRet
	EndIf
	
	///////////////////////////////
	// Insere espacos conforme nDir
	If nDir > 0
		cRet := cRet + Space(nDir)
	EndIf
	
	If lAspa
		cRet := "'" + cRet + "'"
	Endif
Endif

Return( cRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณStrW      บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Converte N->C para a web				 	        				  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function StrW( nTam )

Local cTam := ""

cTam := Alltrim(Str(nTam))

Return( cTam )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCenterStr บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Centraliza texto dentro de um certo espaco					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function CenterStr( nMax, cTxt )

Local 	cRet		:= ""
Local		nDif		:= 0
Local		nTamTxt	:= 0

Default 	nMax 		:= 0
Default 	cTxt 		:= ""

nTamTxt	:= Len(Alltrim(cTxt))
If nMax > nTamTxt
	nDif := Int( (nMax-Len(Alltrim(cTxt))) / 2 )
	cRet := Space(nDif)+cTxt+Space(nDif)
	cRet := If( Len(cRet)==nMax, cRet, cRet+Space(1) )
Else
	cRet := cTxt
Endif

Return( cRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณWidthTD   บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Calcula Tamanho em pixel da coluna <TD>						  บฑฑ
ฑฑบ          ณ baseado no Size do SX3 do campo									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function WidthTD( nSize, cType, lInv )

Local	nMaxTam		:= 350
Local	nRelPixel	:= 8

Default nSize 		:= 0
Default cType 		:= "C"
Default lInv 		:= .F.

Do Case
	Case cType == "D"
		nSize += 2
	Case cType == "M"
		nSize := nMaxTam
	Case cType == "N"
		nSize := nSize
	Case cType == "L"
		nSize := 3
EndCase

nSize := Min( nSize*nRelPixel, nMaxTam )
If lInv
	nSize := Int( nMaxTam/nRelPixel )
Endif

Return( If(lInv, nSize, StrW(nSize) ) )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณStoACBox  บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ String to Array de uma cBox do retorno da funcao X3CBox()  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function StoACBox( cBox, lVazio )

Local aRet		:= {}
Local aPosCmp	:= {}
Local nPosCmp	:= 0
Local nA			:= 0
Local cValue	:= ""
Local cText		:= ""
Local nPosSep	:= 0
Local cStrSep	:= ";" // Caractere de separacao  de itens do ComboBox
Local cStrCmp	:= "=" // Caractere de atribuicao de itens do ComboBox
Default cBox 	:= ""
Default lVazio	:= .T. // Indica se sera incluido um item vazio ao array, para a opcao de nao se escolher nenhum item da Combo

aPosCmp := cScan( cBox, cStrCmp )
cBox    := StrTran( cBox, ",", cStrSep )

If lVazio
	Aadd( aRet, {"",""} )
Endif

For nA := 1 To Len( aPosCmp )
	cValue	:= ""
	cText		:= ""
	nPosCmp	:= aPosCmp[nA]
	nPosSep	:= 0
	
	//////////////////////////
	// Somente para o 1o. item
	If (nA == 1)
		cValue := SubStr( cBox, 1, nPosCmp-1 )
		nPosSep:= At(cStrSep,cBox)
		If nPosSep > 0
			cText := Alltrim( SubStr( cBox, nPosCmp+1, nPosSep-(nPosCmp+1)	) )
		Else
			cText := Alltrim( SubStr( cBox, nPosCmp+1, Len(cBox)					) )
		Endif
	Else
	/////////////////////
	// Restante dos itens
	
		// encontra ';' anterior
		nPosSep := At2( cBox, cStrSep, nPosCmp, .T. )
		cValue  := SubStr( cBox, nPosSep+1, nPosCmp-nPosSep-1 )
		
		// encontra ';' posterior
		nPosSep := At2( cBox, cStrSep, nPosCmp, .F. )
		cText   := Alltrim( SubStr( cBox, nPosCmp+1, nPosSep-nPosCmp-1 ) )
		
	Endif	
	
	Aadd( aRet, {cValue,cText} )
Next nA

Return( aRet )             

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณcScan     บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna um Array com todas as posicoes do caractere que 	  บฑฑ
ฑฑบ          ณ existem na String.													  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function cScan( cTxt, cCrt )

Local aRet	 := {}
Local nA		 := 0
Local cStr	 := ""

Default cTxt := ""
Default cCrt := "" 

If !Empty(cTxt) .And. !Empty(cCrt)
	cTxt := Alltrim( cTxt )
	For nA := 1 To Len( cTxt )
		cStr := SubStr( cTxt, nA, 1 )
			If cStr $ cCrt
				Aadd( aRet, nA )
			Endif
	Next nA
Endif
 
Return( aRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณAt2       บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao similar do At, porem pode-se encontrar outras   	  บฑฑ
ฑฑบ          ณ instancias dentro da String, basta passar o nStart         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function At2( cTxt, cCrt, nStart, lReverso )

Local nRet	 		:= {}
Local nPos	 		:= 0
Local nX		 		:= 0

Default cTxt 		:= ""
Default cCrt 		:= "" 
Default nStart 	:= 0
Default lReverso	:= .F.

While .T.
	nX++
	If lReverso
		nPos := nStart-nX
	Else
		nPos := nStart+nX
	Endif
	If ( (SubStr( cTxt, nPos,1 ) == cCrt) .Or. If( lReverso, .F., (nPos == Len(cTxt)) ) )
		nRet := nPos
		Exit
	Endif
EndDo
 
Return( nRet )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณaIntStrFilบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna Array com todos os intervalos para o filtro, usa a บฑฑ
ฑฑบ          ณ mesma convencao usada pela Microsoft no Word(impresao).    บฑฑ
ฑฑบ          ณ Ex: (1-4;7)                      								  บฑฑ
ฑฑบ          ณ      Intervalo de 1 ate' 4       								  บฑฑ
ฑฑบ          ณ      + 7                         								  บฑฑ
ฑฑบ          ณ      Ficando: 1,2,3,4 e 7        								  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function aIntStrFil( cTxt )

Local aRet	 		:= {}
Local nPos1	 		:= 1
Local nPos2	 		:= 0
Local lInt			:= .F.
Local nX		 		:= 0
Local cQuebra		:= "," // Caractere que define a quebra de termos da busca
Local cInterv		:= "-" // Caractere que define o intervalo da busca
Local cSymbol		:= ""

Default cTxt 		:= ""

If !Empty(cTxt)
	While .T.
		If nX > Len(cTxt)
			Exit
		Endif
		nX++

		Do Case
			Case SubStr(cTxt,nX,1) == cInterv
				lInt	  := .T.
				cSymbal := ">="
			Case (SubStr(cTxt,nX,1) == cQuebra) .Or. (nX == Len( cTxt ))
				If lInt
					lInt	  := .F.
					cSymbal := "<="
				Else
					lInt	  := .F.
					cSymbal := "=="
				Endif
			OtherWise
				cSymbol := ""
				nPos2   := nX
				Loop
		EndCase
		Aadd( aRet, {SubStr(cTxt,nPos1,nX-nPos1+(If(nX==Len(cTxt),1,0))),cSymbal} )
		nPos1 := nX+1
	EndDo
Endif
 
Return( aRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณlIntervFilบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retorna logico caso xValue respeitar a regra definida em   บฑฑ
ฑฑบ          ณ aValues, esta segue a convencao escrita em aIntStrFil.     บฑฑ
ฑฑบ          ณ Ps: Usar sempre com retorno da Func.  aIntStrFil.          บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function lIntervFil( xValue, xValues )

Local lRet	 		:= {}
Local nA		 		:= 0
Local nTam			:= 0
Local cExpressao	:= ""

Default xValue		:= ""
Default xValues	:= {}

If !Empty(xValue) .And. !Empty(xValues)

	nTam := Len(xValues)
	For nA := 1 To nTam    
	
		If "<=" $ xValues[nA][2]
			If nA <> 1
				cExpressao += " .And. "
			Endif
			cExpressao += "'"+ Upper(xValue)+"'"+xValues[nA][2]+"'"+Upper(xValues[nA][1])+"'"
			cExpressao += ")"
			
		ElseIf "<=" $ xValues[nA][2] .Or. ">=" $ xValues[nA][2]
			If nA <> 1
				cExpressao += " .Or. "
			Endif
			cExpressao += " ( "
			cExpressao += "'"+ Upper(xValue)+"'"+xValues[nA][2]+"'"+Upper(xValues[nA][1])+"'"
			
		ElseIf xValues[nA][2] == "=="
			If nA <> 1
				cExpressao += " .Or. "
			Endif
			cExpressao += "'"+Upper(xValue)+"'='"+Upper(xValues[nA][1])+"'"
		Endif
		
	Next nA
   lRet := &cExpressao
Endif
 
Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณInsStr    บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Insere caractere especificado.									  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function InsStr( nQtde, cChar, cStr, nDupl )

Local cRet	 		:= ""
Local nA		 		:= 0
Local nX		 		:= 0

Default nQtde	:= 0
Default cChar	:= ""
Default cStr	:= ""
Default nDupl	:= 1

If nDupl <= 0
	nDupl	:= 1
Endif

If nQtde > 0
	For nA := 1 To nQtde
		For nX := 1 To nDupl
			cRet += cChar
		Next nX
	Next nA
Endif
If !Empty(cStr)
	cRet += cStr
Endif
 
Return( cRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณExtraiStr บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Retira caractere especificado.       							  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function ExtraiStr( cStr, cChar )

Default cStr	:= ""
Default cChar	:= ""

If !Empty(cStr) .And. !Empty(cChar)
	cStr := StrTran( cStr, cChar, "" )
Endif
 
Return( cStr )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPadWeb    บAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao Pad que considera Entidades do HTML.					  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PadWeb( cStr, nTam )

Local aEnt		:= {} // Lista de entidades
Local nPosStr	:= 0
Local nPosEnt	:= 0
Local nA			:= 0
Default cStr	:= ""
Default nTam	:= 0

If nTam > 0

	aEnt := {	"&#160;","&#161;","&#162;","&#163;","&#164;","&#165;","&#166;","&#167;","&#168;",;
					"&#169;","&#170;","&#171;","&#172;","&#173;","&#174;","&#175;","&#176;","&#177;",;
					"&#178;","&#179;","&#180;","&#181;","&#182;","&#183;","&#184;","&#185;","&#186;",;
					"&#187;","&#188;","&#189;","&#190;","&#191;","&#192;","&#193;","&#194;","&#195;",;
					"&#196;","&#197;","&#198;","&#199;","&#200;","&#201;","&#202;","&#203;","&#204;",;
					"&#205;","&#206;","&#207;","&#208;","&#209;","&#210;","&#211;","&#212;","&#213;",;
					"&#214;","&#215;","&#216;","&#217;","&#218;","&#219;","&#220;","&#221;","&#222;",;
					"&#223;","&#224;","&#225;","&#226;","&#227;","&#228;","&#229;","&#230;","&#231;",;
					"&#232;","&#233;","&#234;","&#235;","&#236;","&#237;","&#238;","&#239;","&#240;",;
					"&#241;","&#242;","&#243;","&#244;","&#245;","&#246;","&#247;","&#248;","&#249;",;
					"&#250;","&#251;","&#252;","&#253;","&#254;","&#255;","&#402;","&#913;","&#914;",;
					"&#915;","&#916;","&#917;","&#918;","&#919;","&#920;","&#921;","&#922;","&#923;",;
					"&#924;","&#925;","&#926;","&#927;","&#928;","&#929;","&#931;","&#932;","&#933;",;
					"&#934;","&#935;","&#936;","&#937;","&#945;","&#946;","&#947;","&#948;","&#949;",;
					"&#950;","&#951;","&#952;","&#953;","&#954;","&#955;","&#956;","&#957;","&#958;",;
					"&#959;","&#960;","&#961;","&#962;","&#963;","&#964;","&#965;","&#966;","&#967;",;
					"&#968;","&#969;","&#977;","&#978;","&#982;","&#8226;","&#8230;","&#8242;","&#8243;",;
					"&#8254;","&#8260;","&#8472;","&#8465;","&#8476;","&#8482;","&#8501;","&#8592;","&#8593;",;
					"&#8594;","&#8595;","&#8596;","&#8629;","&#8656;","&#8657;","&#8658;","&#8659;","&#8660;",;
					"&#8704;","&#8706;","&#8707;","&#8709;","&#8711;","&#8712;","&#8713;","&#8715;","&#8719;",;
					"&#8721;","&#8722;","&#8727;","&#8730;","&#8733;","&#8734;","&#8736;","&#8743;","&#8744;",;
					"&#8745;","&#8746;","&#8747;","&#8756;","&#8764;","&#8773;","&#8776;","&#8800;","&#8801;",;
					"&#8804;","&#8805;","&#8834;","&#8835;","&#8836;","&#8838;","&#8839;","&#8853;","&#8855;",;
					"&#8869;","&#8901;","&#8968;","&#8969;","&#8970;","&#8971;","&#9001;","&#9002;","&#9674;",;
					"&#9824;","&#9827;","&#9829;","&#9830;","&#34;","&#38;","&#60;","&#62;","&#338;","&#339;",;
					"&#352;","&#353;","&#376;","&#710;","&#732;","&#8194;","&#8195;","&#8201;","&#8204;","&#8205;",;
					"&#8206;","&#8207;","&#8211;","&#8212;","&#8216;","&#8217;","&#8218;","&#8220;","&#8221;",;
					"&#8222;","&#8224;","&#8225;","&#8240;","&#8249;","&#8250;","&#8364;","&nbsp;","&iexcl;",;
					"&cent;","&pound;","&curren;","&yen;","&brvbar;","&sect;","&uml;","&copy;","&ordf;","&laquo;",;
					"&not;","&shy;","&reg;","&macr;","&deg;","&plusmn;","&sup2;","&sup3;","&acute;","&micro;",;
					"&para;","&middot;","&cedil;","&sup1;","&ordm;","&raquo;","&frac14;","&frac12;","&frac34;",;
					"&iquest;","&Agrave;","&Aacute;","&Acirc;","&Atilde;","&Auml;","&Aring;","&AElig;","&Ccedil;",;
					"&Egrave;","&Eacute;","&Ecirc;","&Euml;","&Igrave;","&Iacute;","&Icirc;","&Iuml;","&ETH;",;
					"&Ntilde;","&Ograve;","&Oacute;","&Ocirc;","&Otilde;","&Ouml;","&times;","&Oslash;","&Ugrave;",;
					"&Uacute;","&Ucirc;","&Uuml;","&Yacute;","&THORN;","&szlig;","&agrave;","&aacute;","&acirc;",;
					"&atilde;","&auml;","&aring;","&aelig;","&ccedil;","&egrave;","&eacute;","&ecirc;","&euml;",;
					"&igrave;","&iacute;","&icirc;","&iuml;","&eth;","&ntilde;","&ograve;","&oacute;","&ocirc;",;
					"&otilde;","&ouml;","&divide;","&oslash;","&ugrave;","&uacute;","&ucirc;","&uuml;","&yacute;",;
					"&thorn;","&yuml;","&fnof;","&Alpha;","&Beta;","&Gamma;","&Delta;","&Epsilon;","&Zeta;","&Eta;",;
					"&Theta;","&Iota;","&Kappa;","&Lambda;","&Mu;","&Nu;","&Xi;","&Omicron;","&Pi;","&Rho;","&Sigma;",;
					"&Tau;","&Upsilon;","&Phi;","&Chi;","&Psi;","&Omega;","&alpha;","&beta;","&gamma;","&delta;",;
					"&epsilon;","&zeta;","&eta;","&theta;","&iota;","&kappa;","&lambda;","&mu;","&nu;","&xi;",;
					"&omicron;","&pi;","&rho;","&sigmaf;","&sigma;","&tau;","&upsilon;","&phi;","&chi;","&psi;",;
					"&omega;","&thetasym;","&upsih;","&piv;","&bull;","&hellip;","&prime;","&Prime;","&oline;",;
					"&frasl;","&weierp;","&image;","&real;","&trade;","&alefsym;","&larr;","&uarr;","&rarr;",;
					"&darr;","&harr;","&crarr;","&lArr;","&uArr;","&rArr;","&dArr;","&hArr;","&forall;","&part;",;
					"&exist;","&empty;","&nabla;","&isin;","&notin;","&ni;","&prod;","&sum;","&minus;","&lowast;",;
					"&radic;","&prop;","&infin;","&ang;","&and;","&or;","&cap;","&cup;","&int;","&there4;","&sim;",;
					"&cong;","&asymp;","&ne;","&equiv;","&le;","&ge;","&sub;","&sup;","&nsub;","&sube;","&supe;",;
					"&oplus;","&otimes;","&perp;","&sdot;","&lceil;","&rceil;","&lfloor;","&rfloor;","&lang;",;
					"&rang;","&loz;","&spades;","&clubs;","&hearts;","&diams;","&quot;","&amp;","&lt;","&gt;",;
					"&OElig;","&oelig;","&Scaron;","&scaron;","&Yuml;","&circ;","&tilde;","&ensp;","&emsp;",;
					"&thinsp;","&zwnj;","&zwj;","&lrm;","&rlm;","&ndash;","&mdash;","&lsquo;","&rsquo;",;
					"&sbquo;","&ldquo;","&rdquo;","&bdquo;","&dagger;","&Dagger;","&permil;","&lsaquo;",;
					"&rsaquo;","&euro;"}
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    
	For nA := 1 To Len(cStr)
		Do Case
			Case SubStr(cStr,nA,1) == "&"
				nPosStr := nA
			Case SubStr(cStr,nA,1) == ";"
				nPosEnt := aScan( aEnt, { |x| Upper(x) == Upper(SubStr(cStr,nPosStr,nA-nPosStr+1)) } )
				If nPosEnt > 0
					nTam += Len( aEnt[nPosEnt] )
				Endif
		EndCase
	Next nA
	
	cStr := Pad( cStr, nTam ) 

Endif
 
Return( cStr )


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณBscAvanWebบAutor  ณCristiano Denardi   บ Data ณ  09.08.05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRealiza comparacao com o intervalo digitado e valores de    บฑฑ
ฑฑบ          ณretorno do webservice.                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function BscAvanWeb( cOpc, aInt, aVal )

Local lRet		:= .T.
Local nOpc		:= 0

Default cOpc	:= ""
Default aInt	:= {}
Default aVal	:= {}

If Len( aVal ) > 0
	nOpc := Val(cOpc)

	lRet := lIntervFil( aVal[nOpc], aInt )
Endif

Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออปฑฑ
ฑฑบPrograma  ณGenTblParam บAutor  ณCristiano Denardi   บ Data ณ  09.08.05 บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออนฑฑ
ฑฑบDesc.     ณ Gera HTML da tabela com parametros                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Portal PMS                                                 บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function GenTblParam(aCpos, aDiv,aButtons )

Local   cHtml	:= ''
Local   nY,nI	:= 0
Local   nTam	:= 0
Local   nPos	:= 0
Local   cValue	:= "" 
Local	  lDiv	:= .F.

Default aCpos	:= {}     
Default aDiv	:= {}

//---------------------- ESTRUTURA DO ACPOS -------------------------
// aCpos[1] -> Texto que aparecera no HTML como filtro. Ex: "Data de"
// aCpos[2] -> Nome da variavel do INPUT do form HTML
// aCpos[3] -> Valor do INPUT
// aCpos[4] -> Tipo do INPUT
// aCpos[5] -> Tamanho do Input
// aCpos[6] -> dado adicional para o tag do input (Checked por exemplo)
// aCpos[7] -> Indica se o input tem que estar antes ou depois da descricao
//-------------------------------------------------------------------

nTam := Len( aCpos )
lDiv := Len( aDiv  ) > 0
cHtml += '<table width="550" border="1" align="left" valign="top" cellpadding="0" cellspacing="0" bordercolor="#E5E5E5">'+CRLF
cHtml +=    '<tr>'+CRLF
cHtml +=       '<td width="435" height="72" bordercolor="#FFFFFF" class="titulo">'+CRLF
cHtml +=          '<table width="546" border="0" cellspacing="0" cellpadding="2">'+CRLF
cHtml +=             '<tr>'+CRLF
cHtml +=                '<td>&nbsp;</td>'+CRLF
cHtml +=                '<td>&nbsp;</td>'+CRLF
cHtml +=             '</tr>'+CRLF
					For nI := 1 To nTam
cHtml +=             '<tr>'+CRLF
							If aCpos[nI][7]//Primeiro o Texto
cHtml +=                '<td align="right" class="TituloMenor">'+aCpos[nI][1]+' </td>'+CRLF
cHtml +=                '<td class="TituloMenor" align="left" >'+CRLF
							Else
cHtml +=                '<td class="TituloMenor" align="right" >'+CRLF
							Endif
							If !Empty(aCpos[nI][4] )
								If ValType( aCpos[nI][3] ) <> "C"
									cValue := ""
								Else
									cValue := aCpos[nI][3]
								Endif
cHtml +=   	                '<input name="'+aCpos[nI][2]+'" id="'+aCpos[nI][2]+'" value="'+cValue+'" type="'+aCpos[nI][4]+'" class="combo" size="'+aCpos[nI][5]+'" '+aCpos[nI,6]+' >' +CRLF
							Else
cHtml +=                  '&nbsp;' +CRLF
							Endif
cHtml +=                '</td>' +CRLF
							If !aCpos[nI][7]//Primeiro o Input
cHtml +=      	         '<td align="left" class="TituloMenor">'+aCpos[nI][1]+' </td>' +CRLF
							Endif
cHtml +=             '</tr>' +CRLF
						nPos := aScan( aDiv, nI )
					Next nI
					If Len(aButtons) > 0
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td colspan="2">' +CRLF
cHtml +=                   '<table align="center">' +CRLF
cHtml +=                      '<tr>' +CRLF
											 For nY := 1 To Len(aButtons)
cHtml +=                         '<td>' +CRLF
cHtml +=                            '<div align="center">' +CRLF
cHtml +=                               '<input name="button" type="button" class="'+CL_BOTAO+'" value="'+aButtons[nY,1]+'" onClick="'+aButtons[nY,2]+'" >'  +CRLF
cHtml +=                            '</div>' +CRLF
cHtml +=                         '</td>' +CRLF
											 Next nY
cHtml +=                      '</tr>' +CRLF
cHtml +=                	'</table>' +CRLF
cHtml +=                '</td>' +CRLF
cHtml +=             '</tr>' +CRLF
					Endif	
cHtml +=             '<tr>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=                '<td>&nbsp;</td>' +CRLF
cHtml +=             '</tr>' +CRLF

cHtml +=          '</table>' +CRLF
cHtml +=       '</td>' +CRLF
cHtml +=    '</tr>' +CRLF
cHtml += '</table>' +CRLF
Return( cHtml )
                          
// Fun็ใo do Service PmsInfoUSER - fonte WSPMS010.PRX
Function GetRecCode(cCode)
Local cCodigo	:= ""
Local oObjCod
Local oObjTIP

oObjCod := WSPMSINFOUSER():New()
WsChgURL( @oObjCod, "PmsInfoUSER.APW" )

oObjCod:cUSERCODEPORTAL	:= cCode

if oObjCod:GetResourceCode()
	cCodigo := alltrim(oObjCod:cGETRESOURCECODERESULT)
endif

oObjTIP := WSPMSINFOUSER():New()       
WsChgURL( @oObjTIP, "PmsInfoUSER.APW" )

oObjTIP:cUSERCODEPORTAL := cCode
if oObjTIP:GetResourceType()
	HttpSession->ADMINPMS := alltrim(oObjTIP:cGETRESOURCETYPERESULT)
endif


Return cCodigo
