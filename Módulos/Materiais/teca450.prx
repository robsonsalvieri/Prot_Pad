#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TECA450.CH"
#INCLUDE 'FWMVCDEF.CH'

#DEFINE  CADASTRO STR0001 //"Ordem de Servico"
#DEFINE  NUMITENS 999
#DEFINE  CID_METRIC "totvs-prestadores-de-serviços-terceirização-protheus_utilizacao-funcionalidades-fieldservice_total"

Static cPictureTot		//Picture dos campos de Totais do rodape da tela
Static oDlg				//Objeto dialog principal
Static oSay1			//Obj. Say do Total geral 
Static oSay2			//Obj. Say do Total cliente
Static oSay3			//Obj. Say do Total fabricante 
Static _cAAHAbrang := ""

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TECA450  ³ Autor ³ Vendas Clientes       ³ Data ³ 03.10.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de atualizacao das O.S.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void TECA450(void)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA450( cRotina, xAutoCab, xAutoItens, xAutoApont, nOpcAuto, cNumOS )

Local cAt450Brw		:= ""		// Filtro fornecido pelo ponto de entrada
Local bFiltraBrw	:= {||}		// Bloco de código para execução do filtro
Local aIndexAB6		:= {}		// Indice do AB1
Local cFiltraAB6	:= ""		// Filtro utilizado no bloco de código executado para ativar o filtro
Local aCores		:= {}
Local aCoresNew := {}
Local lTC450COR  := ExistBlock("TC450COR")
Local aCabMile		:= {}
Local aItensAB7		:= {}
Local nOpcMile		:= 0

Private cCadastro := CADASTRO

Private aRotina := MenuDef()

Private cRoda 							              
Private aAutoCab
Private aAutoItens
Private aAutoApont
Private cOSAuto		:= ""

If IsIncallStack("FWMILEAUTO")
	aCabMile 	:= cRotina
	aItensAB7 	:= xAutoCab
	nOpcMile  	:= xAutoItens
	cRotina	   	:= ""
	xAutoCab	:= aCabMile
	xAutoItens	:= aItensAB7
	nOpcAuto	:= nOpcMile
EndIf

aCores	:= { { "AB6_STATUS=='A'" , 'ENABLE' },{ "AB6_STATUS=='E'" , 'DISABLE' }, { "AB6_STATUS=='B'" , 'BR_AMARELO' } }
							
dbSelectArea("AB6")   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ajustes para o Protheus 10 Express³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Pyme_SX6_Ajst("MV_ALOCTEC")


If	xAutoCab <> Nil .And. xAutoItens <> NIL
	Private lAt450Auto := .T.

	aAutoCab  := xAutoCab
	aAutoItens:= xAutoItens
	aAutoApont:= xAutoApont
	
	Default aAutoApont:= {}	
	
	lMsErroAuto := .F. // reinicializa captura de erro
	MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"AB6")
	
	cNumOS := cOSAuto
	If FindFunction("TECTelMets")
		TECTelMets( "execauto_os", CID_METRIC )
	Endif
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a existencia de Filtros na mBrowse                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AB6->(DbSetOrder(1))			
	If (ExistBlock("AT450Brw"))
		cAt450Brw := ExecBlock("AT450BRW",.F.,.F.)
		If ( ValType(cAt450Brw) == "C" ) .And. !Empty(cAt450Brw)
			cFiltraAB6 := cAt450Brw
		EndIf
		bFiltraBrw 	:= {|| FilBrowse("AB6",@aIndexAB6,@cFiltraAB6) }
		Eval(bFiltraBrw)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lTC450COR
		aCoresNew := ExecBlock("TC450COR",.F.,.F.,aCores)
		If ValType( aCoresNew ) == "A"
			aCores := aClone(aCoresNew)
		EndIf
	Endif


   If ValType( cRotina ) == "C"
   		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz tratamento para chamada por outra rotina             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cRotina := Upper( cRotina ) 
		If !Empty( nScan := AScan( aRotina, { |x| Upper( x[2] ) == cRotina } ) ) 
			cRoda := cRotina + "( 'AB6', AB6->( Recno() ), " + Str(nScan,2) + " )" 
			xRet  := Eval( { || &( cRoda ) } ) 
		EndIf 
	Else
		DbSetOrder(1)
		DbSeek(xFilial())
		mBrowse( 6, 1,22,75,"AB6",,,,,,aCores)
	EndIf 
EndIf
DbSelectArea("AB6")
DbSetOrder(1)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MenuDef   ³ Autor ³ Vendas Clientes       ³ Data ³ 08.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRotina := {	{STR0002	,"AxPesqui"		,0	,1	,0	,.F.	}	,;	//"Pesquisar"
					{STR0003	,"AT450Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
					{STR0004	,"AT450Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
					{STR0005	,"AT450Alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
					{STR0006	,"AT450Exclu"	,0	,5	,0	,.T.	}	,;	//"Exclui"
					{STR0007	,"AT450Efet" 	,0	,4	,0	,.T.	}	,; 	//"eFetiva"
					{STR0034	,"At450Leg"  	,0	,2	,0	,.T.	}	} 	//"Legenda"
Local aRotAdic    := {}  //aRotina adicional para o ponto de entrada						
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada - Adiciona rotinas ao aRotina       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("TC450ROT")
	aRotAdic := ExecBlock("TC450ROT", .F., .F.)
	If ValType(aRotAdic) == "A"
		AEval(aRotAdic,{|x| aAdd(aRotina,x)})
	EndIf
EndIf
If IsInCallStack("CFGA530")
	Aadd(aRotina,{STR0008,"At450Visua",0 , 2})
	Aadd(aRotina,{STR0009,"At450GrvPv",0 , 4})
EndIf
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450Inclu³ Autor ³ Vendas Clientes       ³ Data ³03.10.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de Ordem de Servico                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT450Inclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Inclu(cAlias,nReg,nOpcx)

Local nUsado	:= 0
Local nPosItem	:= 0
Local nOpcA		:= 0
Local oGetD		:= Nil
Local lGrava	:= .T. 
Local lBlockRet	:= Nil
Local aObjects	:= {} 
Local aPosObj	:= {} 
Local aSizeAut	:= {}
Local aPosGet	:= {} 
Local nSaveSX8	:= GetSX8Len()
Local nCntFor	:= 0
Local n1		:= 0				//Total Geral
Local n2     	:= 0				//Total Cliente
Local n3 		:= 0				//Total Fabricante

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT450INC")
	Execblock("AT450INC",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aHeaderAB8	:= {}
Private aHeaderAB7	:= {}
Private aCols		:= {}
Private aColsAB8	:= {}
Private nOpcF4		:= nOpcX

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If Alltrim(cField) == "AB6_NUMOS"
			M->&(cField) := GetNumAB6()
		Else
			M->&(cField) := CriaVar(cField,.T.)
		EndIf
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT450Monta()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderAB7)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
aAdd(aCols,Array(nUsado+1))
For nCntFor := 1 To nUsado
	If IsHeadRec(aHeader[nCntFor][2])
		aCols[1][nCntFor] := 0
	Elseif IsHeadAlias(aHeader[nCntFor][2])
		aCols[1][nCntFor] := "AB7"
	Else
		aCols[1][nCntFor] := CriaVar(aHeader[nCntFor][2])
	Endif
Next nCntFor
aCols[1][nPosItem] := "01"
aCols[1][nUsado+1] := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,M->AB6_MOEDA) 

At450StAbr(AB6->AB6_CONTRT)

If Type("lAt450Auto")=="U" .Or. !lAt450Auto
	
	aSizeAut := MsAdvSize()


	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,{|| At450VldApnt(aCols, aHeader) .and. AT450F4(oGetD)})
	                                    
	aObjects := {} 
	aAdd( aObjects, { 315,  70, .T., .t. } )
	aAdd( aObjects, { 100, 100, .t., .t. } )
	aAdd( aObjects, { 100,   7, .t., .f. } )
	
	aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta tela de entrada                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
	
	EnChoice( "AB6" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.T.)
	oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT450LinOk","AT450TudOk","+AB7_ITEM",.T.,,,,NUMITENS)
	
	nLinIni := aPosObj[ 3, 1 ] 
	
	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
				{{005,065,105,165,205,265}} )
	
	@ nLinIni,aPosGet[1,1] SAY STR0011 SIZE 60,09 OF oDlg PIXEL //"Total: "
	@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	@ nLinIni,aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
	@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	@ nLinIni,aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText( Transform(n1, cPictureTot) ),;
								oSay2:SetText( Transform(n2, cPictureTot) ),;
								oSay3:SetText( Transform(n3, cPictureTot) ) }
	At450Total(oDlg,.F.)
	ACTIVATE MSDIALOG oDlg ON INIT AT450Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desabilita a Tecla F4                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
	
Else
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And.;
		MsGetDAuto(aAutoItens,"AT450LinOk",{|| AT450TudOk() },aAutoCab,aRotina[nOpcX][4]) .And. (At450VldApnt(aCols, aHeader) .and. At450F4())
		nOpcA := 1
	EndIf
EndIf

If ( nOpcA == 1 )      

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Roda o ponto de entrada para verificar se pode incluir       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "AT450OKI" )
   		lBlockRet := ExecBlock( "AT450OKI", .F., .F. ) 
   		lGrava    := If( ValType( lBlockRet ) == "L", lBlockRet, lGrava ) 		
    EndIf 
	
	If lGrava 	
		Begin Transaction
			If ( AT450Grava(1) )

				If FindFunction("TECTelMets")
					TECTelMets( "inclui_os", CID_METRIC )
				EndIF

				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				EndDo
			EndIf
		End Transaction
	EndIf 	
		
EndIf

At450StAbr("")

While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
EndDo

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450Visua³ Autor ³ Vendas Clientes       ³ Data ³03.10.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Visualizacao de Ordem de Servico               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT450Visua(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Visua(cAlias,nReg,nOpcx)

Local nUsado	:= 0
Local aArea		:= GetArea()
Local aAux		:= {}
Local nPosItem	:= 0
Local oGetD		:= Nil
Local aObjects	:= {} 
Local aPosObj	:= {} 
Local aSizeAut	:= MsAdvSize()
Local aPosGet	:= {} 
Local nCntFor	:= 0
Local n1		:= 0				//Total Geral
Local n2		:= 0				//Total Cliente
Local n3		:= 0				//Total Fabricante

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT450VIS")
	Execblock("AT450VIS",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aHeaderAB8	:= {}
Private aHeaderAB7	:= {}
Private aCols		:= {}
Private aColsAB8	:= {}
Private nOpcF4		:= nOpcX

If aRotina == Nil
	aRotina := MenuDef()
EndIf

If Type("cCadastro") == "U"
	cCadastro := CADASTRO + " - " + Upper(STR0003)	//"Ordem de Servico"##"Visualizar"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB6->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT450Monta()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderAB7)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})
DbSelectArea("AB7")
DbSetOrder(1)
DbSeek(xFilial("AB7")+AB6->AB6_NUMOS)
While ( !Eof() .And. xFilial("AB7")    == AB7->AB7_FILIAL .And.;
							AB6->AB6_NUMOS == AB7->AB7_NUMOS )
	aAdd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := AB7->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "AB7"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aCols[Len(aCols)][nUsado+1] := .F.
	aAux := {}
	DbSelectArea("AB8")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AB8")+AB6->AB6_NUMOS+AB7->AB7_ITEM) )
		While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
									M->AB6_NUMOS   == AB8->AB8_NUMOS .And.;
									AB7->AB7_ITEM== AB8->AB8_ITEM )
			aAdd(aAux,Array(Len(aHeaderAB8)+1))
			For nCntFor := 1 To Len(aHeaderAB8)
				If IsHeadRec(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := AB8->(RecNo())
				Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "AB8"
				Elseif ( aHeaderAB8[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB8[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
			DbSelectArea("AB8")
			DbSkip()
		EndDo
		aAdd(aColsAB8,aClone(aAux))
	Else
		aAdd(aAux,Array(Len(aHeaderAB8)+1))
		For nCntFor := 1 To Len(aHeaderAB8)
			If IsHeadRec(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB8"
			Else
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := StrZero(1,TamSX3("AB8_SUBITE")[1])
		aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
		aAdd(aColsAB8,aClone(aAux))
	EndIf
	DbSelectArea("AB7")
	DbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Habilita a Tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,{|| At450VldApnt(aCols, aHeader) .and. AT450F4(oGetD)})

aObjects := {} 
aAdd( aObjects, { 315,  70, .T., .t. } )
aAdd( aObjects, { 100, 100, .t., .t. } )
aAdd( aObjects, { 100,   7, .t., .f. } )
aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
aPosObj := MsObjSize( aInfo, aObjects ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,M->AB6_MOEDA) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta tela de entrada                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !IsBlind()
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 

	EnChoice( "AB6" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.T.)
	oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT450LinOk","AT450TudOk","+AB7_ITEM",.F.)

	nLinIni := aPosObj[3,1] 

	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
				{{005,065,105,165,205,265}} )

	@ nLinIni,aPosGet[1,1] SAY STR0011 SIZE 60,09 OF oDlg PIXEL //"Total: "
	@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	@ nLinIni,aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
	@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	@ nLinIni,aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText( Transform(n1, cPictureTot) ),;
								oSay2:SetText( Transform(n2, cPictureTot) ),;
								oSay3:SetText( Transform(n3, cPictureTot) ) }
	At450Total(oDlg,.F.)
	ACTIVATE MSDIALOG oDlg ON INIT AT450Bar(oDlg,{||oDlg:End()},{||oDlg:End()},nOpcx,oGetD)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450Alter³ Autor ³ Vendas Clientes       ³ Data ³03.10.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Ordem de Servico                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT450Alter(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Alter(cAlias,nReg,nOpcx)

Local nUsado	:= 0
Local nOpcA		:= 0
Local aArea		:= GetArea()
Local aAux		:= {}
Local aTravas	:= {}
Local lTravas	:= .T.
Local nPosItem	:= 0 
Local nPosSubI  := 0
Local oGetD		:= Nil
Local lGrava	:= .T.
Local lBlockRet	:= Nil
Local aObjects	:= {} 
Local aPosObj	:= {} 
Local aSizeAut	:= {}
Local aPosGet	:= {} 
Local nSaveSX8	:= GetSX8Len()
Local nCntFor	:= 0  
Local n1		:= 0				//Total Geral
Local n2		:= 0				//Total Cliente
Local n3		:= 0				//Total Fabricante
Local lAtuEqLoc	:= .T.

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT450ALT")
	Execblock("AT450ALT",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aHeaderAB8	:= {}
Private aHeaderAB7	:= {}
Private aCols		:= {}
Private aColsAB8	:= {}
Private nOpcF4		:= nOpcX

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se existir funcionalidade de entrega e montagem e o campo ³
//³que indica que foi criado pelo módulo de Controle de Lojas³
//³estiver preenchido, não permite a alteração da OS.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Lj7HasDtEM()
	If !Empty(AB6->AB6_NUMLOJ)
		Alert( STR0043 ) // "Não é possível alterar essa ordem de serviço pois ela foi criada pelo módulo 'Controle de Lojas'"
		Return .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB6->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava Registros                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !AtTravaReg("AB6", aTravas) )
	lTravas := .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT450Monta()

nPosSubI  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderAB7)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
DbSelectArea("AB7")
DbSetOrder(1)
DbSeek(xFilial("AB7")+AB6->AB6_NUMOS)
While ( !Eof() .And. xFilial("AB7")    == AB7->AB7_FILIAL .And.;
							AB6->AB6_NUMOS == AB7->AB7_NUMOS )
	If ( !AtTravaReg("AB7", aTravas) )
		lTravas := .F.
	Endif

	aAdd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := AB7->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "AB7"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aCols[Len(aCols)][nUsado+1] := .F.
	aAux := {}
	DbSelectArea("AB8")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AB8")+AB6->AB6_NUMOS+AB7->AB7_ITEM) )
		While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
									M->AB6_NUMOS   == AB8->AB8_NUMOS .And.;
									AB7->AB7_ITEM== AB8->AB8_ITEM )
			If ( !AtTravaReg("AB8", aTravas) )
				lTravas := .F.
			Endif

			aAdd(aAux,Array(Len(aHeaderAB8)+1))
			For nCntFor := 1 To Len(aHeaderAB8)
				If IsHeadRec(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := AB8->(RecNo())
				Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "AB8"
				Elseif ( aHeaderAB8[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB8[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
			DbSelectArea("AB8")
			DbSkip()
		EndDo
		aAdd(aColsAB8,aClone(aAux))
	Else
		aAdd(aAux,Array(Len(aHeaderAB8)+1))
		For nCntFor := 1 To Len(aHeaderAB8)
			If IsHeadRec(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB8"
			Else
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosSubI] := StrZero(1,TamSX3("AB8_SUBITE")[1])
		aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
		aAdd(aColsAB8,aClone(aAux))
	EndIf
	DbSelectArea("AB7")
	DbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona o cliente                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SA1->( DbSetOrder( 1 ) ) 
SA1->( DbSeek( xFilial( "SA1" ) + M->AB6_CODCLI + M->AB6_LOJA ) ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,M->AB6_MOEDA) 

At450StAbr(AB6->AB6_CONTRT)

If ( lTravas )
	If Type("lAt450Auto")=="U" .Or. !lAt450Auto
		
		aSizeAut := MsAdvSize()	

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Habilita a Tecla F4                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		SetKey(VK_F4,{|| At450VldApnt(aCols, aHeader) .and. AT450F4(oGetD)})
	
		aObjects := {} 
		aAdd( aObjects, { 315,  70, .T., .t. } )
		aAdd( aObjects, { 100, 100, .t., .t. } )
		aAdd( aObjects, { 100,   7, .t., .f. } )
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
	
		// DEFINE MSDIALOG oDlg TITLE CADASTRO From 9,0 to 28,80
		EnChoice( "AB6" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.T.)
		oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT450LinOk","AT450TudOk","+AB7_ITEM",.T.,,,,NUMITENS, "At450FOK", , , "At450DeLin")
		
		nLinIni := aPosObj[3,1]
		
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
				{{005,065,105,165,205,265}} )
		
		@ nLinIni,aPosGet[1,1] SAY STR0011 SIZE 60,09 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText( Transform(n1, cPictureTot) ),;
									oSay2:SetText( Transform(n2, cPictureTot) ),;
									oSay3:SetText( Transform(n3, cPictureTot) ) }
		At450Total(oDlg,.F.)
		ACTIVATE MSDIALOG oDlg ON INIT AT450Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcx,oGetD)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desabilita a Tecla F4                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,Nil)
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And.;
			MsGetDAuto(aAutoItens,"AT450LinOk",{|| AT450TudOk() },aAutoCab,aRotina[nOpcX][4]) .And. (At450VldApnt(aCols, aHeader) .and. At450F4())
			nOpcA := 1
		EndIf
	EndIf
EndIf
If ( nOpcA == 1 )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Roda o ponto de entrada para verificar se pode alterar       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "AT450OKA" )
   		lBlockRet := ExecBlock( "AT450OKA", .F., .F. ) 
   		lGrava    := If( ValType( lBlockRet ) == "L", lBlockRet, lGrava ) 		
    EndIf 

	If lGrava 		
		Begin Transaction           
			If ( AT450Grava(2,,,,@lAtuEqLoc)  )
				If FindFunction("TECTelMets")
					TECTelMets( "altera_os", CID_METRIC )
				EndIF
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				EndDo
				
				// Faz atualização do movimento de locação de equipamentos
				If lAtuEqLoc
					lGrava := At450AtuEqloc() 
				EndIf 
			EndIf
		End Transaction
	EndIf 				
EndIf

At450StAbr("")

While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o destravamento dos registros travados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )
RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450Exclu³ Autor ³ Vendas Clientes       ³ Data ³03.10.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Exclusao  de Ordem de Servico                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT450Exclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Exclu(cAlias,nReg,nOpcx)

Local nUsado		:= 0
Local nOpcA		:= 0
Local aArea		:= GetArea()
Local aAux			:= {}
Local aTravas		:= {}
Local lTravas		:= .T.
Local lExclui		:= .T.
Local nPosItem	:= 0
Local oGetD		:= Nil                        
Local lGrava		:= .T. 
Local lBlockRet	:= Nil 
Local aObjects	:= {} 
Local aPosObj		:= {} 
Local aSizeAut	:= {}
Local aPosGet		:= {}
Local nSaveSX8	:= GetSX8Len()
Local nCntFor		:= 0 
Local n1			:= 0				//Total Geral
Local n2			:= 0				//Total Cliente
Local n3			:= 0				//Total Fabricante  
Local cAliasABF	:= ""
Local cQuery		:= ""

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT450EXC")
	Execblock("AT450EXC",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aHeaderAB8	:= {}
Private aHeaderAB7	:= {}
Private aCols		:= {}
Private aColsAB8	:= {}
Private nOpcF4		:= nOpcX

If aRotina == Nil
	aRotina := MenuDef()
EndIf

If ( Eof() .Or. Bof() )
	Return(.F.)
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se existir funcionalidade de entrega e montagem e o campo ³
//³que indica que foi criado pelo módulo de Controle de Lojas³
//³estiver preenchido, não permite a alteração da OS.        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Lj7HasDtEM()
	If !Empty(AB6->AB6_NUMLOJ)
		Alert( STR0044 ) // "Não é possível excluir essa ordem de serviço pois ela foi criada pelo módulo 'Controle de Lojas'"
		Return .F.
	EndIf
EndIf 

#IFDEF TOP		
	cAliasABF := GetNextAlias()
	cQuery := "SELECT COUNT(*) QTD_REQ "
	cQuery += "FROM " + RetSqlName("ABF")+ " ABF "
	cQuery += "WHERE "
	cQuery += "ABF_FILIAL='"+xFilial("ABF")+"' AND "
	cQuery += "ABF_NUMOS='"+AB6->AB6_NUMOS+"' AND "
	cQuery += "ABF.D_E_L_E_T_=' '"
	
	cQuery := ChangeQuery(cQuery)
	dBUseArea(.T.,"TOPCONN",TCGENQRY(,,cQuery),cAliasABF,.F.,.T.)	
	
	If (cAliasABF)->QTD_REQ > 0
	  Help(" ",1,"NODELETA",,STR0087,1,0) //"Esta O.S. esta sendo utilizado por uma tabela - Requisições da O.S. e nao podera ser excluida."  
	  Return .F.				
	Endif     	
	(cAliasABF)->(DbCloseArea()) 	
#ENDIF  

//-------------------------------------------------------
//  Bloqueia a exclusão de O.S. de manutenção preventiva de locação de equipamento
// somente quando a origem não for a exclusão de uma nota de devolução do equipamento
If !IsInCallStack('MATA103') .And. !IsInCallStack('At800FechOs') .And. !IsInCallStack('At800CancRet')

	DbSelectArea('TEW')
	TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS
	
	If TEW->( DbSeek( xFilial('TEW')+AB6->AB6_NUMOS ) )
		// 'A exclusão dessa O.S. só pode ser realizada pela exclusão da Nota de devolução do equipamento de locação
		Help(" ",1,"NODELOSLOC",,STR0089,1,0) // 'A exclusão dessa O.S. só pode ser realizada pela exclusão da Nota de devolução do equipamento de locação'  
		Return .F.
	EndIf
	
EndIf

//-------------------------------------------------------
// Bloqueia a exclusão de O.S. gerada que está associada a um contrato que fora gerado a partir de um orçamento de serviços
If ! IsInCallStack('At820Grv') .AND. (! Empty(AB6->AB6_CDORCS) .OR. ! Empty(AB6->AB6_ITORCS))
	Help(,, "NODELOSORCSERV",,STR0102,1,0)	//"Esta O.S. está associada a um Orçamento de Serviços"
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB6")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB6->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava Registros                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !AtTravaReg("AB6", aTravas) )
	lTravas := .F.
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT450Monta()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader  := aClone(aHeaderAB7)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
DbSelectArea("AB7")
DbSetOrder(1)
DbSeek(xFilial("AB7")+AB6->AB6_NUMOS)
While ( !Eof() .And. xFilial("AB7")    == AB7->AB7_FILIAL .And.;
							AB6->AB6_NUMOS == AB7->AB7_NUMOS .And. lExclui )
	If ( AtTravaReg("AB7", aTravas) )
		If ( AB7->AB7_TIPO<>"1" )
			lExclui := .F.
			Help(" ",1,"AT450DEL01")
		EndIf
	Else
		lTravas := .F.
	EndIf
	aAdd(aCols,Array(nUsado))
	aAdd(aColsAB8,aClone(aAux))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := AB7->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "AB7"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aAux := {}
	DbSelectArea("AB8")
	DbSetOrder(1)
	If ( DbSeek(xFilial("AB8")+AB6->AB6_NUMOS+AB7->AB7_ITEM) )
		While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
									M->AB6_NUMOS   == AB8->AB8_NUMOS .And.;
									AB7->AB7_ITEM== AB8->AB8_ITEM )
			If ( !AtTravaReg("AB8" , aTravas) )
				lTravas := .F.
			EndIf
			aAdd(aAux,Array(Len(aHeaderAB8)+1))
			For nCntFor := 1 To Len(aHeaderAB8)
				If IsHeadRec(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := AB8->(RecNo())
				Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "AB8"
				Elseif ( aHeaderAB8[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB8[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
			DbSelectArea("AB8")
			DbSkip()
		EndDo
		aAdd(aColsAB8,aClone(aAux))
	Else
		aAdd(aAux,Array(Len(aHeaderAB8)+1))
		For nCntFor := 1 To Len(aHeaderAB8)
			If IsHeadRec(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB8"
			Else		
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
		aAdd(aColsAB8,aClone(aAux))
	EndIf
	DbSelectArea("AB7")
	DbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,M->AB6_MOEDA) 

If ( lTravas .And. lExclui )
	If Type("lAt450Auto")=="U" .Or. !lAt450Auto

		aSizeAut := MsAdvSize()	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Habilita a Tecla F4                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		
		SetKey(VK_F4,{|| At450VldApnt(aCols, aHeader) .and. AT450F4(oGetD)})
	  
		aObjects := {} 
		aAdd( aObjects, { 315,  70, .T., .t. } )
		aAdd( aObjects, { 100, 100, .t., .t. } )
		aAdd( aObjects, { 100,   7, .t., .f. } )
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
		
		EnChoice( "AB6" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.T.)
		oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT450LinOk","AT450TudOk","+AB7_ITEM",.F.)
		
		nLinIni := aPosObj[ 3, 1 ] 
		
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
			{{005,065,105,165,205,265}} )
		
		@ nLinIni,aPosGet[1,1] SAY STR0011 SIZE 60,09 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,3] SAY STR0012 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,5] SAY STR0013 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText( Transform(n1, cPictureTot) ),;
									oSay2:SetText( Transform(n2, cPictureTot) ),;
									oSay3:SetText( Transform(n3, cPictureTot) ) }
		At450Total(oDlg,.F.)
		ACTIVATE MSDIALOG oDlg ON INIT AT450Bar(oDlg,{||nOpca:=1,oDlg:End()},{||oDlg:End()},nOpcx,oGetD)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desabilita a Tecla F4                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,Nil)
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And.;
			MsGetDAuto(aAutoItens,"AT450LinOk",{|| AT450TudOk() },aAutoCab,aRotina[nOpcX][4]) .And. (At450VldApnt(aCols, aHeader) .and. At450F4())
			nOpcA := 1
		EndIf
	EndIf
EndIf
If ( nOpcA == 1 )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Roda o ponto de entrada para verificar se pode excluir       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "AT450OKD" )
		lBlockRet := ExecBlock( "AT450OKD", .F., .F. ) 
		lGrava    := If( ValType( lBlockRet ) == "L", lBlockRet, lGrava ) 		
    EndIf 
	
	If lGrava
		Begin Transaction     	
			If ( AT450Grava(3)  )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSX8()
				EndDo
			EndIf
			If FindFunction("TECTelMets")
				TECTelMets( "exclui_os", CID_METRIC )
			EndIF
		End Transaction 	
	EndIf 			
EndIf

While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Efetua o destravamento dos registros travados.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AtDestravaReg( aTravas )
RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Efet ³ Autor ³ Vendas Clientes       ³ Data ³ 13.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetiva OS                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Efet(cAlias,nReg,nOpc)

Local aSavRot  := aClone(aRotina)		//Backup do array aRotina
Local cQuery   := ""					//Variav. com a query a ser executada
Local cArqInd  := CriaTrab(,.F.)		//Nome do arq. temporario
Local cMarca   := GetMark()				//Indica se a OS esta marcada
Local nIndex   := 0						//Indice do arq. temporario a ser utilizado  
Private cFiltroAB6  := ""

aRotina := {   { STR0008 ,"At450Visua",0,2},; //"Visual   "
					{ STR0009 ,"At450GrvPv",0,4} } //"Pedido   "
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros                                                             ³
//³                                                                        ³
//³ MV_PAR01 - Cliente de                                                  ³
//³ MV_PAR02 - Cliente ate                                                 ³
//³ MV_PAR03 - Emissao de                                                  ³
//³ MV_PAR04 - Emissao ate                                                 ³
//³ MV_PAR05 - Chamado de                                                  ³
//³ MV_PAR06 - Chamado ate                                                 ³
//³ MV_PAR07 - Gera pedidos para situacao		                           ³
//³ MV_PAR08 - Qual moeda                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Pergunte("ATA450",.T.) )
	If Recmoeda( dDatabase, MV_PAR08 ) <= 0
		//"Não existe taxa cadastrada para a moeda selecionada: " # 
		//"Atenção"
		MsgAlert(STR0038 + StrZero(MV_PAR08,1), STR0039) 
		aRotina := aClone(aSavRot)
		Return .F.
	EndIf
	cQuery += "AB6_FILIAL=='"+xFilial("AB6")+"'.And."
	cQuery += "AB6_STATUS<>'E'.And."
	cQuery += "AB6_CODCLI>='"+MV_PAR01+"'.And."
	cQuery += "AB6_CODCLI<='"+MV_PAR02+"'.And."
	cQuery += "Dtos(AB6_EMISSA)>='"+Dtos(MV_PAR03)+"'.And."
	cQuery += "Dtos(AB6_EMISSA)<='"+Dtos(MV_PAR04)+"'.And."
	cQuery += "AB6_NUMOS>='"+MV_PAR05+"'.And."
	cQuery += "AB6_NUMOS<='"+MV_PAR06+"'.And."
	cQuery += "AB6_CONPAG<>'" + Space( Len( AB6->AB6_CONPAG ) ) + "'"
	
	If ExistBlock("AT450FIL")
		cQuery := ExecBlock("AT450FIL",.F.,.F.,{cQuery})
	EndIf
	
	DbSelectArea("AB6")
	DbSetOrder(1)
	IndRegua("AB6",cArqInd,IndexKey(),,cQuery)
	nIndex := RetIndex("AB6")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	cFiltroAB6 := AB6->(DbFilter())
	DbSetOrder(nIndex+1)
	DbGoTop()
	If ( !Eof() .And. !Bof() )
		MarkBrow("AB6","AB6_OK",,,,cMarca) 
	Else
		Help(" ",1,"REGNOIS")
	EndIf
	DbSelectArea("AB6")
	RetIndex("AB6")
	dbClearFilter()
	Ferase(cArqInd+OrdBagExt())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna as Situacao de Entrada                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRotina := aClone(aSavRot)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450MONTA  ³ Autor ³ Vendas Clientes     ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta aHeader                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT450MONTA(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Monta()

Local nUsado   := 0
Local aArea    := GetArea()
Local aAreaAB7 := AB7->(GetArea())
Local aAreaAB8 := AB8->(GetArea())
Local aHeader  := {}

Local aStruField := {}
Local cField     := ""
Local nC         := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do AB7                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
nUsado := 0
aStruField := FWSX3Util():GetAllFields("AB7")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		aRet   := FwTamSx3(cField)
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							AllTrim(GetSx3Cache(cField,"X3_PICTURE")),;
							aRet[1],;
							aRet[2],;
							AllTrim(GetSx3Cache(cField,"X3_VALID")),;
							GetSx3Cache(cField,"X3_USADO"),;
							aRet[3],;
							"AB7",;
							GetSx3Cache(cField,"X3_CONTEXT") } )
		Endif
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("AB7", aHeader)

nUsado := Len(aHeader)
aHeaderAB7 := aClone(aHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do AB8                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}
nUsado := 0
aStruField := FWSX3Util():GetAllFields("AB8")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		aRet   := FwTamSx3(cField)
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							AllTrim(GetSx3Cache(cField,"X3_PICTURE")),;
							aRet[1],;
							aRet[2],;
							AllTrim(GetSx3Cache(cField,"X3_VALID")),;
							GetSx3Cache(cField,"X3_USADO"),;
							aRet[3],;
							"AB8",;
							GetSx3Cache(cField,"X3_CONTEXT") } )
		Endif
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("AB8", aHeader)

nUsado := Len(aHeader)
aHeaderAB8 := aClone(aHeader)

RestArea(aAreaAB7)
RestArea(aAreaAB8)
RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450LinOk  ³ Autor ³ Vendas Clientes     ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Linha na GetDados                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT450LinOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450LinOk(oGet)

Local lRetorno := .T.
Local nUsado   := Len(aHeader)
Local nPosIt   := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_ITEM"})
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_CODPRO"})
Local nPosnSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_NUMSER"})
Local nPosPrb  := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_CODPRB"})
Local nPosTipo := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_TIPO"})

Local nCntFor  := 0
Local lOSxCtrt := SuperGetMv("MV_OSXCTRT",.F.,.F.)        // Amarracao O/S X Contrato
Local cAliasAA3 := GetNextAlias()
Local lAA3 := .F.
Local lAAH := .F.
local lAA3Blq := AA3->( ColumnPos('AA3_MSBLQL')) > 0
Local cWhere := ""

If ( !aCols[n][nUsado+1] .And. ( Len(aCols) > 1.Or.!Empty(aCols[n][nPosPrd]) ))
	If (  Empty(aCols[n][nPosPrd]) .Or.;
			Empty(aCols[n][nPosnSer]) .Or.;
			Empty(aCols[n][nPosPrb]) )
		lRetorno := .F.
		Help(" ",1,"AT450LIN01")
	EndIf
	If ( lRetorno )
		For nCntFor := 1 To Len(aCols)
			If (  aCols[n][nPosPrd]+aCols[n][nPosNSer]+aCols[n][nPosPrb]==;
					aCols[nCntFor][nPosPrd]+aCols[nCntFor][nPosNSer]+aCols[nCntFor][nPosPrb] .And.;
					n <> nCntFor .And. !aCols[nCntFor][nUsado+1])
				Help(" ",1,"AT450LIN02")
				lRetorno := .F.
			EndIf
		Next nCntFor
	EndIf
	If ( lRetorno )		
		
		If (IsInCallStack('At450Alter') .OR. IsInCallStack('At450Inclu'))
			
			//Posiciono no item da O.S. corretamente
			DbSelectArea("AB7")
			DbSetOrder(1)			
			DbSeek(xFilial("AB7")+M->AB6_NUMOS+aCols[n][nPosIt])			
			
			//Posiciono na base corretamente
			DbSelectArea("AA3")
			DbSetOrder(6)
			DbSeek(xFilial("AA3")+aCols[n][nPosnSer])		
					
		EndIf
		
		// Somente executa a validação quando o equipamento não for para locação de equipamento
		If  AA3->AA3_EQALOC == '2' 
			
			
			DbSelectArea("AAH")
			DbSetOrder(1)
			If !Empty(AA3->AA3_CONTRT)
				lAAH := DbSeek(xFilial("AAH")+AA3->AA3_CONTRT)
			EndIf
			
			If lAAH
				If AAH->AAH_ABRANG == "2" 
					cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
							" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"
				Else
					cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
							" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"+;
							" AND AA3.AA3_LOJA = '"+M->AB6_LOJA+"'"
				EndIf
			Else
				cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
						" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"+;
						" AND AA3.AA3_LOJA = '"+M->AB6_LOJA+"'"
			EndIf
			
			cWhere += " AND AA3.AA3_CODPRO = '"+aCols[n][nPosPrd]+"'"+;
				" AND AA3.AA3_NUMSER = '"+aCols[n][nPosNSer]+"'"
				
			If lAA3Blq
				cWhere += " AND AA3.AA3_MSBLQL <> '1'"
			EndIf
			cWhere		:= '%' + cWhere + '%'
					
			BeginSql Alias cAliasAA3
				SELECT R_E_C_N_O_ FROM %table:AA3% AA3
					WHERE AA3.%NotDel% %Exp:cWhere%
			EndSql
				
			If (cAliasAA3)->(!Eof())
				AA3->(DBGoTo((cAliasAA3)->R_E_C_N_O_))
				lAA3 := .T.
			EndIf
			(cAliasAA3)->(DbCloseArea())
			
			If lAA3
			
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Verifica se o parametro MV_OSXCTRT está habilitado  ³
				//³e se o tipo do contrato e manutencao ou serviços.   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( lOSxCtrt )
						
					If ( M->AB6_TPCONT == "1" )
						If !lAAH
							lAAH := DbSeek(xFilial("AAH")+AA3->AA3_CONTRT)
						EndIf
						
						If !(  lAAH .AND. AAH->AAH_CONTRT == M->AB6_CONTRT )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se a base de atendimento esta associada ao ³
							//³ao um contrato de manutencao informado no cabecalho ³
							//³da O/S.						                       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If Empty(M->AB6_CONTRT)
								//"Atenção"##"Número do contrato não informado."##"OK"
								Aviso(STR0055,STR0056,{STR0057},1)
								lRetorno := .F.  
							Else
								/*
						   			"Atenção"#"Esta base de atendimento não está coberta para este contrato de manutenção."
						   			"Selecione uma base de atendimento coberta."#"OK"
								*/
						   		Aviso(STR0055,STR0062+chr(10)+STR0063,{STR0057},2)
						   		lRetorno := .F.		
							EndIf
						EndIf
						
					ElseIf ( M->AB6_TPCONT == "2" )
						
						If Empty(M->AB6_CONTRT)
							//"Atenção"##"Número do contrato não informado."##"OK"
							Aviso(STR0055,STR0056,{STR0057},1)
							lRetorno := .F.  
						EndIf		
						
					EndIf	
				EndIf
			Else
				AA3->(DbSetOrder(6))
               	If AA3->(ColumnPos('AA3_MSBLQL')) > 0.And. AA3->(DbSeek(xFilial("AA3")+aCols[n][nPosNSer])) .And. AA3->AA3_MSBLQL == "1"                                                                                                                                                                                                                                                                                                                                                                                           
				    Help(" ",1,"HELP", , STR0116,3,1 )  // "O Equipamento selecionado esta bloqueado para utilização.Verifique a situação do equipamento na Base de Atendimento."  
					lRetorno := .F.
				Elseif  AA3->(DbSeek(xFilial("AA3")+aCols[n][nPosNSer])) .And. AA3->AA3_STATUS <> "02"                                                                                                                                                                                                                                                                                                                                                                                              
					Help(" ",1,"AT450LIN03")
					Retorno := .F.
				Endif	
			EndIf
		EndIf
	EndIf
Else
	If ( aCols[n][nPosTipo] $ "234" )
		Help(" ",1,"AT450LIN04")
		lRetorno := .F.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Usuario                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno 
	If ExistBlock( "AT450LOK" ) 
	   lRetorno := ExecBlock( "AT450LOK", .F., .F. ) 
	EndIf 	   
EndIf	   

If Type("lAt450Auto")=="U" .Or. !lAt450Auto
	At450Total(oGet:oWnd,.F.)
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450TudOk  ³ Autor ³ Vendas Clientes     ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da GetDados                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT450TudOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450TudOk()

Local lRetorno := .T.
Local nPosIt   := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_ITEM"})
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_CODPRO"})
Local nPosnSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_NUMSER"})
Local nCntFor  := 0
Local lOSxCtrt := SuperGetMv("MV_OSXCTRT",.F.,.F.)        // Amarracao O/S X Contrato
Local cAtprPos := SuperGetMv("MV_ATPRPOS", ,"2")
Local cAliasAA3 := GetNextAlias()
local lAA3 := .F.
Local lAAH := .F.
local lAA3Blq := AA3->( ColumnPos('AA3_MSBLQL')) > 0
Local cWhere := ""

If INCLUI .And. AB6->(DbSeek(xFilial("AB6")+M->AB6_NUMOS))      
	Help(" ",1,"NODELETA",,STR0088,2,0)
	lRetorno := .F. 
EndIf

// Verifica se foram incluidos itens na OS.
If lRetorno
	lRetorno := At450VldApnt(aCols, aHeader, .T.)
EndIf

If ( lOSxCtrt  ) .And. lRetorno
	
	If lRetorno
		
		If ( !Empty(M->AB6_TPCONT) .AND. !Empty(M->AB6_CONTRT) )
			lRetorno := At450VCtrt()
		ElseIf	( !Empty(M->AB6_TPCONT) .AND. Empty(M->AB6_CONTRT) )
			
			If ( Empty(M->AB6_CODCLI) .OR. Empty(M->AB6_LOJA) )
				//"Atenção"#"Informe o cliente / loja para esta Ordem de Serviço."#"OK"
				Aviso(STR0055,STR0081,{STR0057},1)
				lRetorno := .F.
			ElseIf !Empty(M->AB6_TPCONT)
				//"Atenção"##"Número do contrato não informado."##"OK"
				Aviso(STR0055,STR0056,{STR0057},1)
				lRetorno := .F.
			EndIf
			
		EndIf
		
	EndIf
	
EndIf

If ( lRetorno )
	
	For nCntFor := 1 To Len(aCols)
		
		If !( aCols[ nCntFor, Len( aHeader ) + 1 ] )
			If (IsInCallStack('At450Alter') .OR. IsInCallStack('At450Inclu'))
			
				//Posiciono no item da O.S. corretamente
				DbSelectArea("AB7")
				DbSetOrder(1)			
				DbSeek(xFilial("AB7")+M->AB6_NUMOS+aCols[nCntFor][nPosIt])			
				
				//Posiciono na base corretamente
				DbSelectArea("AA3")
				DbSetOrder(6)
				DbSeek(xFilial("AA3")+aCols[nCntFor][nPosNSer])		
						
			EndIf
			// Somente realiza a validação, quando não for locação de equipamentos
			
			If AA3->AA3_EQALOC == '2'
			
				DbSelectArea("AAH")
				DbSetOrder(1)
				If !Empty(AA3->AA3_CONTRT)
					lAAH := DbSeek(xFilial("AAH")+AA3->AA3_CONTRT)
				EndIf
				
				If lAAH
					If AAH->AAH_ABRANG == "2"
						cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
								" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"
					Else
						cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
								" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"+;
								" AND AA3.AA3_LOJA = '"+M->AB6_LOJA+"'"
					EndIf
				Else
					cWhere :=  " AND AA3.AA3_FILIAL = '"+xFilial('AA3')+"'"+;
							" AND AA3.AA3_CODCLI = '"+M->AB6_CODCLI+"'"+;
							" AND AA3.AA3_LOJA = '"+M->AB6_LOJA+"'"
				EndIf
				
				cWhere += " AND AA3.AA3_CODPRO = '"+aCols[n][nPosPrd]+"'"+;
					" AND AA3.AA3_NUMSER = '"+aCols[n][nPosNSer]+"'"
					
				If lAA3Blq
					cWhere += " AND AA3.AA3_MSBLQL <> '1'"
				EndIf
				cWhere		:= '%' + cWhere + '%'
						
				BeginSql Alias cAliasAA3
					SELECT R_E_C_N_O_ FROM %table:AA3% AA3
						WHERE AA3.%NotDel% %Exp:cWhere%
				EndSql
				
				If (cAliasAA3)->(!Eof())
					AA3->(DBGoTo((cAliasAA3)->R_E_C_N_O_))
					lAA3 := .T.
				EndIf
				(cAliasAA3)->(DbCloseArea())
				
				If lAA3
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Verifica se o parametro MV_OSXCTRT está habilitado  ³
					//³e se o tipo do contrato e manutencao.	           ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If ( lOSxCtrt .AND. M->AB6_TPCONT == "1")
						
						If !lAAH
							lAAH := DbSeek(xFilial("AAH")+AA3->AA3_CONTRT)
						EndIf
						
						If !(  lAAH  .AND. AAH->AAH_CONTRT == M->AB6_CONTRT )
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se a base de atendimento esta associada ao ³
							//³ao um contrato de manutencao informado no cabecalho ³
							//³da O/S.						                       ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If !Empty(M->AB6_CONTRT)
								/*
								"Atenção"# "No item " XX " a base de atendimento " XXXX-XXXX " não está coberta para este contrato de manutenção."
								"Selecione uma base de atendimento coberta." # OK
								*/
								Aviso(STR0055,STR0058+aCols[nCntFor][nPosIt]+STR0059+Alltrim(aCols[nCntFor][nPosPrd])+" - ";
								+Alltrim(aCols[nCntFor][nPosNSer])+STR0060+chr(10)+STR0061,{STR0057},2)
								lRetorno := .F.
							EndIf
						EndIf
						
					EndIf
				Else
					Help(" ",1,"AT450TUDOK")
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
	Next nCntFor
	
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe relação Ordem de Serviço com Item   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cAtprPos == "2" .AND. M->AB6_TPORCS $ "2|3"
	If Empty(M->AB6_ITORCS)
		Help(,, "AT450TudOk",,STR0097,1,0,,,,,,{STR0098}) //"Item do Orçamento de Serviço em branco." ## "Informe o Item do Orçamento de Serviço."
		lRetorno := .F.
	Else
		If !At450VldF3() //Verifica se existe vinculo o Item do Orçamento de Serviço
			lRetorno := .F.
		Endif
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Usuario                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	If ExistBlock( "AT450TOK" )
		lRetorno := ExecBlock( "AT450TOK", .F., .F. )
	EndIf
EndIf

Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450GrvPV³ Autor ³ Vendas Clientes       ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Gera a Pedido de Venda                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450GrvPV()

Processa({|| At450Proc() })
CloseBrowse()
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Proc ³ Autor ³ Vendas Clientes       ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Processamento da Efetivacao do Pedido de Venda             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450Proc()

Local nCntFor        := 0
Local lTravas        := .T.
Local aTravas        := {}
Local nUsado         := 0
Local nPosItem       := 0
Local nPosTipo       := 0
Local aAux           := {}
Local uCampo         := ""
Local lGrava         := .T.
Local lAt450OKE      := ExistBlock( "AT450OKE", .F., .F. ) 
Local bFiltroPV      := { || .T. } 
Local nSaveSX8			:= GetSX8Len()

Local lFiltroPV		:= ExistBlock( "AT450FIT", .F., .F. ) 
Local lLiberaIT		:= .F.

Private aHeader		:= {}
Private aHeaderAB8	:= {}
Private aHeaderAB7	:= {}
Private aCols		:= {}
Private aColsAB8	:= {}

Do Case 
Case MV_PAR07 == 1   
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Todos                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	bFiltroPV := { || .T. } 
Case MV_PAR07 == 2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atendidos                                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	bFiltroPV := { || AB7->AB7_TIPO == "4" }
Case MV_PAR07 == 3
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Em atendimento                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	bFiltroPV := { || AB7->AB7_TIPO == "3" }
Case MV_PAR07 == 4	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ O.S.                                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	bFiltroPV := { || AB7->AB7_TIPO == "1" }	
EndCase

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT450Monta()
aHeader  := aClone(aHeaderAB7)
nUsado   := Len(aHeader)
nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
nPosTipo := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_TIPO"})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processamento da MarkBrowse                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("AB6")
AB6->(DbSetFilter({|| &cFiltroAB6}, cFiltroAB6))
DbGoTop() //IndRegua()
ProcRegua(LastRec())
While ( !Eof() )
	If ( IsMark("AB6_OK",ThisMark(),ThisInv()) .AND. SoftLock("AB6")) .AND. At450MoedaOK()
	
    	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Roda o ponto de entrada para verificar se pode efetivar      ³	
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lAT450OKE
			lBlockRet := ExecBlock( "AT450OKE", .F., .F. ) 
			lGrava    := If( ValType( lBlockRet ) == "L", lBlockRet, lGrava ) 		
		EndIf               
        
		If lGrava 				
	
			aTravas := {}
			aCols   := {}
			aColsAb8:= {}
			aAux    := {}
			aHeader  := aClone(aHeaderAB7)
			lTravas := .T.
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Cria Variaveis de Memoria da Enchoice                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB6")
			aAdd(aTravas , { Alias() , RecNo() })
			For nCntFor := 1 To FCount()
				uCampo := FieldName(nCntFor)
				M->&(uCampo) := AB6->(FieldGet(FieldPos(uCampo)))
			Next nCntFor
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Monta aCols                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			DbSelectArea("AB7")
			DbSetOrder(1)
			DbSeek(xFilial("AB7")+AB6->AB6_NUMOS)
			While ( !Eof() .And. xFilial("AB7")    == AB7->AB7_FILIAL .And.;
											AB6->AB6_NUMOS == AB7->AB7_NUMOS )
				If ( SoftLock("AB7") )
					aAdd(aTravas, { Alias() , RecNo() })
				Else
					lTravas := .F.
				EndIf
				aAdd(aCols,Array(nUsado+1))
				For nCntFor := 1 To nUsado
					If IsHeadRec(aHeader[nCntFor][2])
						aCols[Len(aCols)][nCntFor] := AB7->(RecNo())
					Elseif IsHeadAlias(aHeader[nCntFor][2])
						aCols[Len(aCols)][nCntFor] := "AB7"
					Elseif ( aHeader[nCntFor][10] <> "V" )
						aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
					Else
						aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
					EndIf
				Next nCntFor
				aCols[Len(aCols)][nUsado+1] := .F.
				aAux := {}
				DbSelectArea("AB8")
				DbSetOrder(1)
				If ( DbSeek(xFilial("AB8")+AB6->AB6_NUMOS+AB7->AB7_ITEM) )           
				
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Filtra a geracao do pedido                                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Eval( bFiltroPV ) 
						//Executa PE para definir quais itens serão enviados para Geração de Pedido de Venda
						If lFiltroPV 
							lLiberaIT := ExecBlock( "AT450FIT", .F., .F. )
							If ValType( lLiberaIT ) == "L" .And. lLiberaIT
								aCols[Len(aCols)][nPosTipo] := "2"
							EndIf
						Else
							aCols[Len(aCols)][nPosTipo] := "2"
						EndIf	
					EndIf 		
					
					While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
												AB6->AB6_NUMOS == AB8->AB8_NUMOS .And.;
												AB7->AB7_ITEM== AB8->AB8_ITEM )
						If ( SoftLock("AB8") )
							aAdd(aTravas, { Alias() , RecNo() })
						Else
							lTravas := .F.
						EndIf
						aAdd(aAux,Array(Len(aHeaderAB8)+1))
						For nCntFor := 1 To Len(aHeaderAB8)
							If IsHeadRec(aHeaderAB8[nCntFor][2])
								aAux[Len(aAux)][nCntFor] := AB8->(RecNo())
							Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
								aAux[Len(aAux)][nCntFor] := "AB8"
							Elseif ( aHeaderAB8[nCntFor][10] <> "V" )
								aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB8[nCntFor][2]))
							Else
								aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
							EndIf
						Next nCntFor
						aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
						DbSelectArea("AB8")
						DbSkip()
					EndDo
					aAdd(aColsAB8,aClone(aAux))
				Else
					aAdd(aAux,Array(Len(aHeaderAB8)+1))
					For nCntFor := 1 To Len(aHeaderAB8)
						If IsHeadRec(aHeaderAB8[nCntFor][2])
							aAux[Len(aAux)][nCntFor] := 0
						Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
							aAux[Len(aAux)][nCntFor] := "AB8"
						Else
							aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2])
						Endif
					Next nCntFor
					aAux[Len(aAux)][nPosItem] := "01"
					aAux[Len(aAux)][Len(aHeaderAB8)+1] := .F.
					aAdd(aColsAB8,aClone(aAux))
				EndIf
				DbSelectArea("AB7")
				DbSkip()
			EndDo
			Begin Transaction
				If ( AT450Grava(2)  )
					If FindFunction("TECTelMets")
						TECTelMets( "efetiva_os", CID_METRIC )
					EndIF
					EvalTrigger()
					While ( GetSX8Len() > nSaveSx8 )
						ConfirmSX8()
					EndDo

					// Faz atualização do movimento de locação de equipamentos
					At450AtuEqloc()
				EndIf
			End Transaction
			For nCntFor := 1 To Len(aTravas)
				DbSelectArea(aTravas[nCntFor][1])
				DbGoTo(aTravas[nCntFor][2])
				MsUnLock()
			Next nCntFor
		EndIf 			
	EndIf
	DbSelectArea("AB6")
	DbSkip()
	IncProc()
EndDo
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450F4     ³ Autor ³ Vendas Clientes     ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento da tecla F4.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT450F4(Void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ a,b,c                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450F4(oGetD)

Local oDlg			:= Nil
Local oGetF4		:= Nil
Local oSay1			:= Nil
Local oSay2			:= Nil
Local oSay3			:= Nil
Local nOpcA    := 0
Local aArea    := GetArea()
Local aSavAcols:= {}
Local nSavN    := If(Type("N")=="U",1,N)
Local aAux     := {}
Local bSetKey		:= Nil
Local cCadastro:= STR0010 //"Apontamento(s)"
Local nCntFor  := 0
Local nUsado   := Len(aHeaderAB8)
Local cItemAB8	:= ""
Local nPosItAB8  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_ITEM"})
Local nPosSubI := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})
Local nPosPrd  := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_CODPRO"})
Local nPosNSer := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_NUMSER"})
Local nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
Local nPosTipo := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_TIPO"})
Local lRetorno := .T.
Local cItem	   := ""	
Local nItAcols := If(Type("N")=="U",1,N)
Local cProduto := cNumSer := ""
Local aCpos    	:= {}  
Local aCposAlt	:= {}
Local lTipo		:= .F.

Private aColsMain	:= Nil 
Private nMain		:= Nil

//Busca campos editaveis da tabela AB8	
aCposAlt	:= At450Struct("AB8")

aSavAcols 	:= aClone(aCols)
aColsMain 	:= aClone(aCols) 
nMain     	:= nSavN
cProduto  	:= aSavaCols[nItAcols][nPosPrd]
cNumSer  	:= aSavaCols[nItAcols][nPosNSer]
cItem		:= aSavaCols[nItAcols][nPosItem]

//Recupera o item da GetD para adicionar o item
If Valtype(oGetD) == "O"
	cItemAB8 := StrZero(oGetD:oBrowse:nAt,2)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona a tabela de Base Instalada ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AA3->( DbSetOrder( 1 ) ) 
AA3->( DbSeek( xFilial( "AA3" ) + M->AB6_CODCLI + M->AB6_LOJA + ;
       cProduto + cNumSer ) )

aHeader     := aClone(aHeaderAB8)
If ( Empty(aColsAB8) .Or. Len(aColsAB8)<=nItAcols )
	aAdd(aAux,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeaderAB8[nCntFor][2])
			aAux[1][nCntFor] := 0
		Elseif IsHeadAlias(aHeaderAB8[nCntFor][2])
			aAux[1][nCntFor] := "AB8"
		Else	
			aAux[1][nCntFor] := CriaVar(aHeaderAB8[nCntFor][2],.T.)
		Endif
	Next nCntFor
	aAux[1][nUsado+1] := .F.
	aAux[1][nPosSubI] := StrZero(1,TamSX3("AB8_SUBITE")[1])
	aAux[1][nPosItem] := cItem
	For nCntFor := Len(aColsAB8)+1 To ( nItAcols )
		aAdd(aColsAB8,aClone(aAux))
	Next nCntFor
EndIf

//Atribui o valor do item para o acols de acordo com o posicionamento
For nCntFor := 1 To Len(aColsAB8[nItAcols])
	If Empty(aColsAB8[nItAcols][nCntFor][nPosItAB8])
		aColsAB8[nItAcols][nCntFor][nPosItAB8] := cItemAB8
	EndIf
Next nCntFor

aCols := aColsAB8[nItAcols]

//Atribiu 1 para o N para evitar erro na abertura
N := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Registros                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB6_CODCLI+M->AB6_LOJA)
    
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,M->AB6_MOEDA) 

//Verifica se o Status é Encerrado ou Pedido Gerado e não habilita o acols para edição
If Type("lAt450Auto")=="U" .Or. !lAt450Auto 
	If aSavaCols[nItAcols][nPosTipo]=="2" .Or. aSavaCols[nItAcols][nPosTipo]=="5"
		Aviso(STR0039, STR0096,{STR0057},2)//"Atenção"##"Não é possivel realizar a edição do apontamento para itens encerrados e com pedido de venda"##"OK"
		lTipo := .T.	
	EndIf	
EndIf 

If Type("lAt450Auto")=="U" .Or. !lAt450Auto
	bSetKey  := SetKey(VK_F4,)
	SetKey(VK_F4,Nil)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Salva o aCols   de Entrada e Desabilita o Paint da Primeira GetDados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oGetD:oBrowse:lDisablePaint := .T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta a GetDados                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cCadastro From 10,0 to 30,80
	@ 035,007 SAY RetTitle("AB6_CODCLI")+" "+M->AB6_CODCLI+"-"+M->AB6_LOJA+"/"+SA1->A1_NOME  SIZE 120,09 OF oDlg PIXEL 
	@ 045,007 SAY RetTitle("AB7_CODPRO")+" "+cProduto+RetTitle("AB7_NUMSER")+" "+Alltrim(cNumSer)  SIZE 180,09 OF oDlg PIXEL
	oGetF4 := MsGetDados():New(55 ,001,125,316,nOpcF4,"AT450F4LOk","AllwaysTrue","+AB8_SUBITE",.T.,IIf(nOpcF4==4 .And. aSavaCols[nItAcols][nPosTipo]=="2",aCpos,aCposAlt),,,NUMITENS )
	@ 128,005 SAY STR0011 SIZE 60,09 OF oDlg PIXEL //"Total: "
	@ 128,065 SAY oSay1 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL 
	@ 128,105 SAY STR0012 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
	@ 128,165 SAY oSay2 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL 
	@ 128,205 SAY STR0013 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ 128,265 SAY oSay3 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL 
	oDlg:Cargo := {|n1,n2,n3| 	oSay1:SetText(n1),;
								oSay2:SetText(n2),;
								oSay3:SetText(n3) }
	At450Total(oDlg,.T.)
	ACTIVATE MSDIALOG oDlg ON INIT At450Bar3(oDlg,{||nOpca:=1,if(oGetF4:TudoOk(),oDlg:End(),nOpca:=0)},{||oDlg:End()},,oGetF4)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ajusta o aCols para gravar na tabela de saldos do grupo de cobertura  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	At450AjAco(AA3->AA3_CONTRT,M->AB6_NUMOS,cItem) 

			
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Volta a situacao anterior                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
	SetKey(VK_F4,bSetKey)
	If ( nOpcA == 1 .And. !lTipo)
		aColsAB8[nItAcols] := aClone(aCols)
		At450Total(oGetD:oWnd,.F.)
	ElseIf nOpcA == 1 .And. lTipo
		Aviso(STR0039, STR0096,{STR0057},2)//"Atenção"##"Não é possivel realizar a edição do apontamento para itens encerrados e com pedido de venda"##"OK"	
	EndIf
	oGetD:oBrowse:lDisablePaint := .F.
Else
	If MsGetDAuto(aAutoApont,"AT450F4LOk",{|| .T. },aAutoCab,aRotina[nOpcF4][4])
		aColsAB8[nItAcols] := aClone(aCols)
	Else
		lRetorno := .F.
	EndIf
EndIf
aHeader   := aClone(aHeaderAB7)
aCols     := aClone(aSavACols)
	
aColsMain := NIL 
n := nSavN
RestArea(aArea)
Return lRetorno

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450F4LOk  ³ Autor ³ Vendas Clientes     ³ Data ³ 03.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Linha na GetDados 2                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT450F4LOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1: Objeto do MsGetDados                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Se .T. permite a troca de linha, .F. não permite    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450F4LOk(oGet)

Local lRetorno := .T.													// Retorno da função
Local nUsado   := Len(aHeaderAB8)										// Campos utilizados
Local nPosPrd  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODPRO"})	// Posição no aHeaderAB8 do campo Produto
Local nPosTipo := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODSER"})	// Posição no aHeaderAB8 do campo Serviço
Local nPosQtd  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_QUANT"})	// Posição no aHeaderAB8 do campo Quantidade
Local nPosUnit := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_VUNIT"})	// Posição no aHeaderAB8 do campo Valor Unitário
Local nPosLista:= aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_PRCLIS"})	// Posição no aHeaderAB8 do campo Preço de Lista
Local nPosVlr  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_TOTAL"})	// Posição no aHeaderAB8 do campo Valor Total 
Local nPosLocal:= aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_LOCAL"})	// Posição no aHeaderAB8 do campo Armazem
Local nPosVlDes:= aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_VLDESC"})	// Posição no aHeaderAB8 do campo Valor do Desconto Aplicado.
Local lAT450VLD:= ExistBlock( "AT450VLD" )								// Se existe ou não o P.E. AT450VLD

If ( !aCols[n][nUsado+1] )
	If (  Empty(aCols[n][nPosPrd]) .Or.;
		Empty(aCols[n][nPosTipo]) .Or.;
		Empty(aCols[n][nPosQtd]) .Or.;
		Empty(aCols[n][nPosUnit]) .Or.;
		Empty(aCols[n][nPosLista]) .Or.;
		Empty(aCols[n][nPosVlr]) .Or.;
		Empty(aCols[n][nPosLocal]) );
		.And. ( Len(aCols) > 1 .Or. !Empty(aCols[n][nPosPrd]))
		lRetorno := .F.
		// Há campos obrigatórios que não foram preenchidos. Verifique os campos PRODUTO, QTD, SERVIÇO, VLR UNITARIO, PREÇO LISTA, VLR TOTAL. 
		Help(" ",1,"AT450F4L01",,STR0104,1,0)
	EndIf                                                      
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida o Total conferindo o arredondamento que eh o³
	//³mesmo utilizado na gravacao do campo AB8_TOTAL     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( Round( aCols[n][nPosQtd]*aCols[n][nPosUnit] ,2) <> Round(aCols[n][nPosVlr] ,2) )
		Help(" ",1,"AT450F4L02")
		lRetorno := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida se o valor do Desconto eh MAIOR que o valor ³
	//³Total do item.                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nPosVlDes > 0
		If ( aCols[n][nPosVlDes] > aCols[n][nPosVlr] )
			Help(NIL, NIL, STR0113, NIL, STR0114, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0115})
			lRetorno := .F.
		EndIf
	EndIf

	If ( lRetorno )
		DbSelectArea("AA5")
		DbSetOrder(1)
		DbSeek(xFilial("AA5")+aCols[n][nPosTipo])
		DbSelectArea("SF4")
		DbSetOrder(1)
		DbSeek(xFilial("SF4")+AA5->AA5_TES)
		DbSelectArea("SB2")
		DbSetOrder(1)
		If (!DbSeek(xFilial("SB2")+aCols[n][nPosPrd]+aCols[n][nPosLocal]) .And. SF4->F4_ESTOQUE=="S" )
			Help(" ",1,"AT450F4L03")
		EndIf
	EndIf
EndIf

If Type("lAt450Auto")=="U" .Or. !lAt450Auto
	At450Total(oGet:oWnd,.T.)
EndIf

If lRetorno .AND. lAT450VLD
	If !ExecBlock( "AT450VLD", .F., .F. )
		lRetorno := .F.
	EndIf
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT450Eqpto³ Autor ³ Vendas Clientes       ³ Data ³ 29.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida a amarracao Produto X Equipamento                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT450Eqpto()                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Eqpto()

Local lRetorno    := .F.
Local cCampo      := ReadVar()
Local uConteudo   := &(ReadVar())
Local nPosProd    := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_CODPRO" })
Local aArea       := GetArea()
Local lOSxCtrt    := SuperGetMv("MV_OSXCTRT",.F.,.F.)            			// Amarracao O/S X Contrato
Local lCancLib	  := IsInCallStack("At800CancLib")
Local lSepara	  := IsInCallStack("TECA820")

Do Case
	Case ( "AB7_CODPRO"$cCampo )
		If lCancLib
			DbSelectArea("AA3")
			AA3->(DbSetOrder(6)) //AA3_FILIAL+AA3_NUMSER
			AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))
			lRetorno := .T.
		Elseif lSepara
			DbSelectArea("AA3")
			AA3->(DbSetOrder(9)) //AA3_FILIAL+AA3_CODPRO
			AA3->(DbSeek(xFilial("AA3")+uConteudo))
			lRetorno := .T.			
		Else
			DbSelectArea("AA3")
			AA3->(dbSetOrder(1))
			If AA3->(dbSeek(xFilial("AA3")+M->AB6_CODCLI+M->AB6_LOJA+uConteudo) )
				lRetorno := .T.
			Else
				AA3->(dbSetOrder(9))
				If AA3->(dbSeek(xFilial("AA3")+uConteudo) )
					lRetorno := .T.
  			Else
					Help(" ",1,"REGNOIS")
				Endif
			EndIf
		Endif
	Case ( "AB7_NUMSER"$cCampo )
		If lCancLib
			DbSelectArea("AA3")
			AA3->(DbSetOrder(6)) //AA3_FILIAL+AA3_NUMSER
			AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))
			M->AB7_NUMSER := AB7->AB7_NUMSER
			lRetorno := .T.
		Else
			DbSelectArea("AA3")
			If IsInCallStack("AT820GRV") .Or. IsInCallStack("At800SepCan")
				AA3->(dbSetOrder(6))
				If AA3->(dbSeek(xFilial("AA3")+uConteudo) )
					lRetorno := .T.
				Else
					Help(" ",1,"REGNOIS")
				EndIf
			Else
  			DbSetOrder(1)
  	
  			If ( DbSeek(xFilial("AA3")+M->AB6_CODCLI+M->AB6_LOJA+aCols[n][nPosProd]+uConteudo) )
  				
  				lRetorno := .T.	                             
  				
  			 	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  				//³Verifica se o parametro MV_OSXCTRT está habilitado  ³
  				//³e se o tipo do contrato e manutencao.	           ³
  				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
  				If ( lOSxCtrt .AND. M->AB6_TPCONT == "1")
  					
  					DbSelectArea("AAH")
  					DbSetOrder(1)
  						
  					If !( ( DbSeek(xFilial("AAH")+AA3->AA3_CONTRT) ) .AND. AAH->AAH_CONTRT == M->AB6_CONTRT )		
  						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  				  		//³Verifica se a base de atendimento esta associada ao ³
  				   		//³ao um contrato de manutencao informado no cabecalho ³
  				   		//³da O/S.						                       ³    
  				   		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
  						
  						/*
  					   		"Atenção"#"Esta base de atendimento não está coberta para este contrato de manutenção."
  					   		"Selecione uma base de atendimento coberta."#"OK"
  						*/
  						Aviso(STR0055,STR0062+chr(10)+STR0063,{STR0057},2)
  						lRetorno := .F.			
  					EndIf
  				EndIf  				
  			Else
					//Verifica s eexiste a base.
					DbSelectArea("AA3")
					AA3->(dbSetOrder(6)) //AA3_FILIAL+AA3_NUMSER
					If AA3->(dbSeek(xFilial("AA3")+uConteudo) )
						DbSelectArea("SB5")
						SB5->(DbSetOrder(1)) //B5_FILIAL+B5_COD
						//Verifica se o equipamento não é controlado por id unico.
						If SB5->(DbSeek(xFilial("SB5")+aCols[n][nPosProd]))
							If SB5->B5_ISIDUNI == "2"
								DbSelectArea("TWI")
								TWI->(DbSetOrder(6)) //TWI_FILIAL+TWI_BASE+TWI_CODCLI+TWI_LOJA 
								//Verifica se existe movimentação do equipamento no cliente e loja.
								If TWI->(DbSeek(xFilial("TWI")+AA3->AA3_NUMSER+M->AB6_CODCLI+M->AB6_LOJA))
									lRetorno := .T.
								Else
  									Help(" ",1,"REGNOIS")
								Endif
							Endif
						Endif
					Endif
				EndIf
  			EndIf
		Endif
EndCase
RestArea(aArea)
Return(lRetorno)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT450Bar  ³ Autor ³ Vendas Clientes       ³ Data ³ 21.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Mostra a EnchoiceBar na tela                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AT450Bar(oDlg,bOk,bCancel,nOpc,oGetD)
Local aButtons   := {} 
Local aUsButtons := {} 
Local lTECA450 := AtIsRotina("ATC010CON").Or.AtIsRotina("ATC020CON")
Local cCodMontagem:= SuperGetMv("MV_LJOCOMO",,.F.)    //Codigo da ocorrencia no SIGATMK que corresponde a montagem.

If ( nOpc <> 5 )
	aAdd(aButtons,{"S4SB014N" , {|| At450Aloc(oGetD)} , STR0025, STR0028 }) //"Programa‡„o da OS"
	aAdd(aButtons,{"RELATORIO", {|| At450Laudo(oGetD)}, STR0026, STR0029 }) //"Atendimento da OS"
	aAdd(aButtons,{"PRODUTO"  , {|| At450VldApnt(aCols, aHeader) .and.  AT450F4(oGetD)   }, STR0021, STR0030 }) //"Apontamento..."
	If TableInDic("TXM") .And. FindFunction("Tec450AImg")
		aAdd(aButtons,{"PRODUTO", {|| A450ShowCk()}, STR0108, STR0109 }) //"Fotos"/Fotos App"
	EndIf	
EndIf            

If nOpc == 2 .And. !AtIsRotina("AT450TRACK") 	
	aAdd(aButtons,{"bmpord1",{|| At450Track()}, STR0027, STR0031 }) // "System Tracker"
EndIf 	

If !lTECA450
	aAdd(aButtons,{"IMPRESSAO", {|| TECR450(M->AB6_NUMOS)}, STR0022, STR0032}) //"Impressao da OS gravada"
EndIf   
 
If FindFunction("HisImpUMov")
	If !Empty(cCodMontagem)
		aAdd(aButtons,{"Imp.uMov", {|| HisImpUMov(M->AB6_NUMLOJ)},STR0092,STR0093 })//"Visualiza"##"Imp.uMov"
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistBlock( "AT450BUT" ) 
	aUsButtons := ExecBlock( "AT450BUT", .F., .F. ) 
	aEval( aUsButtons, { |x| aAdd( aButtons, x ) } ) 	 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³AT450Tab    ³ Autor ³ Vendas Clientes     ³ Data ³ 25/06/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de Calculo do Preco de Tabela                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := AT450Tab(ExpC1,ExpC2,ExpN2,ExpC3,ExpC4)           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Produto                                           ³±± 
±±³          ³ ExpC2 -> Tabela de preco                                   ³±±
±±³          ³ ExpN2 -> Quantidade                                        ³±±
±±³          ³ ExpC3 -> Cliente                                           ³±±
±±³          ³ ExpC4 -> Loja do Cliente                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 := Preco de tabela                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 
Function AT450Tab(	cProduto,	cTabprec,	nQtde,	cCliente,;
					cLoja	,	nMoeda)

Local aArea   := GetArea() 
Local nPrcVen := 0 

DbSelectArea("SB1")
DbSetOrder(1)
If ( !Empty(cProduto) .And. DbSeek(xFilial("SB1")+cProduto,.F.) )
	nPrcVen := MaTabPrVen(cTabPrec,cProduto,nQtde,cCliente,cLoja,nMoeda)
EndIf

RestArea( aArea ) 

Return (nPrcVen)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³aT450Desc ³ Autor ³ Vendas Clientes       ³ Data ³ 10.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calcula o Preco com Desconto                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Preco com Desconto em cascata                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Desc()

Local nPPrcLis    := aScan(aHeader,{|x| AllTrim(x[2])=="AB8_PRCLIS"})
Local nPPrcVen    := aScan(aHeader,{|x| AllTrim(x[2])=="AB8_VUNIT"})
Local nPrcVen     := 0

nPrcVen := aCols[n][nPPrcLis]
If ( M->AB6_DESC1 > 0 )
	nPrcVen *= (( 100 - M->AB6_DESC1 )/100)
EndIf
If ( M->AB6_DESC2 > 0 )
	nPrcVen *= (( 100 - M->AB6_DESC2 )/100)
EndIf
If ( M->AB6_DESC3 > 0 )
	nPrcVen *= (( 100 - M->AB6_DESC3 )/100)
EndIf
If ( M->AB6_DESC4 > 0 )
	nPrcVen *= (( 100 - M->AB6_DESC4 )/100)
EndIf
nPrcVen := NoRound(nPrcVen,TamSx3("AB8_VUNIT")[2])

Return(nPrcVen)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450RDesc³ Autor ³ Vendas Clientes       ³ Data ³ 10.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recalcula os descontos em cascata                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .t.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450RDesc()

Local nCtnFor  := 0
Local nCntFor2 := 0
Local nPPrcLis := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_PRCLIS"})
Local nPVunit  := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_VUNIT"})
Local nPTotal  := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_TOTAL"})
Local nPQuant  := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_QUANT"})
Local nPrcVen  := 0
Local nCntFor  := 0
Local cReadVar := ReadVar()

//Tratamento efetuado para quando retirado o desconto do campo AB6_DESC? informando 0(zero) 
If "M->AB6_DESC" $ cReadVar .And. &cReadVar == 0 .And. &cReadVar <> &("AB6->AB6_DESC"+ SubStr(cReadVar,12,1))
	For nCntFor := 1 To Len(aColsAB8)
		For nCntFor2 := 1 To Len(aColsAB8[nCntFor])
			nPrcVen := aColsAB8[nCntFor][nCntFor2][nPPrcLis]
			If ( M->AB6_DESC1 >= 0 )
				nPrcVen *= (( 100 - M->AB6_DESC1 )/100)
			EndIf
			If ( M->AB6_DESC2 >= 0 )
				nPrcVen *= (( 100 - M->AB6_DESC2 )/100)
			EndIf
			If ( M->AB6_DESC3 >= 0 )
				nPrcVen *= (( 100 - M->AB6_DESC3 )/100)
			EndIf
			If ( M->AB6_DESC4 >= 0 )
				nPrcVen *= (( 100 - M->AB6_DESC4 )/100)
			EndIf
			nPrcVen := NoRound(nPrcVen,TamSx3("AB8_VUNIT")[2])
			If ( nPrcVen <> 0 )
				aColsAB8[nCntFor][nCntFor2][nPVunit] := nPrcVen
				aColsAB8[nCntFor][nCntFor2][nPTotal] := aColsAB8[nCntFor][nCntFor2][nPQuant]*aColsAB8[nCntFor][nCntFor2][nPVunit]
			Endif
		Next nCntFor2
	Next nCntFor  				
Else    
	For nCntFor := 1 To Len(aColsAB8)
		For nCntFor2 := 1 To Len(aColsAB8[nCntFor])
			If ( M->AB6_DESC1 > 0 .Or.;
					M->AB6_DESC2 > 0 .Or.;
					M->AB6_DESC3 > 0 .Or.;
					M->AB6_DESC4 > 0 )
				nPrcVen := aColsAB8[nCntFor][nCntFor2][nPPrcLis]
			EndIf
			If ( M->AB6_DESC1 > 0 )
				nPrcVen *= (( 100 - M->AB6_DESC1 )/100)
			EndIf
			If ( M->AB6_DESC2 > 0 )
				nPrcVen *= (( 100 - M->AB6_DESC2 )/100)
			EndIf
			If ( M->AB6_DESC3 > 0 )
				nPrcVen *= (( 100 - M->AB6_DESC3 )/100)
			EndIf
			If ( M->AB6_DESC4 > 0 )
				nPrcVen *= (( 100 - M->AB6_DESC4 )/100)
			EndIf
			nPrcVen := NoRound(nPrcVen,TamSx3("AB8_VUNIT")[2])
			If ( nPrcVen <> 0 )
				aColsAB8[nCntFor][nCntFor2][nPVunit] := nPrcVen
				aColsAB8[nCntFor][nCntFor2][nPTotal] := aColsAB8[nCntFor][nCntFor2][nPQuant]*aColsAB8[nCntFor][nCntFor2][nPVunit]
			Endif
		Next nCntFor2
	Next nCntFor
EndIf	

If "M->AB6_DESC" $ cReadVar .And. oDlg <> Nil
	At450Total(oDlg,.F.)
EndIf		
Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Tipo ³ Autor ³ Vendas Clientes       ³ Data ³ 13.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o Tipo da OS                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Logico                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Tipo()

Local lRetorno := .T.
Local nPosItem := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_ITEM"})
Local nPosNSer := aScan(aHeaderAB7,{|x| AllTrim(x[2])=="AB7_NUMSER"})
Local cTipo    := &(ReadVar())
Local aArea 	:= GetArea()
Local aAreaAA3 	:= AA3->(GetArea())
Local aAreaTEW 	:= TEW->(GetArea())
Local lAA3TEW 	:= .T.

DbSelectArea("AB7")
DbSetOrder(1)
If ( DbSeek(xFilial("AB7")+M->AB6_NUMOS+aCols[n][nPosItem]) )
	If ( AB7->AB7_TIPO $ "234" .And. cTipo <> "2" )
		Help(" ",1,"AT450LIN04")
		lRetorno := .F.
	Else 
		DbSelectArea("AA3")
		AA3->( DbSetOrder( 6 ) ) // AA3_FILIAL + AA3_NUMSER + AA3_FILORI
		lAA3TEW := AA3->( DbSeek( xFilial("AA3")+aCols[n][nPosNSer] ) )

		DbSelectArea("TEW")
		TEW->( DbSetOrder( 1 ) ) // TEW_FILIAL + TEW_CODMV
		lAA3TEW := lAA3TEW .And. TEW->( DbSeek( xFilial("TEW") ) )
		
		/* Precisa realizar essa validação?
		Situação gravada igual a 2-Atendido ou 5-Encerrado?
		Situação atual está sendo movida para 1-Aberta?
		Existe movimentação desta Base de Atendimento mais recente? */
		If lAA3TEW .And. AB7->AB7_TIPO $ "2/5" .And. cTipo == "1" .And. At800HasMov( .T., AB7->( Recno() ) )
			lRetorno := .F.
			Help(" ",1,"NODELOSLOC",,STR0103,1,0) // 'Essa O.S. não pode mais sofrer alteração pois o equipamento já foi reutilizado'
		EndIf
	EndIf
EndIf

RestArea(aAreaTEW)
RestArea(aAreaAA3)
RestArea(aArea)

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Total³ Autor ³ Vendas Clientes       ³ Data ³ 07.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Totalizacao da Janela                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³oDlg    : Objeto da Janela                                  ³±±
±±³          ³nItem   : Item da Getdados Principal                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450Total(oDlg,lF4)

Local nUsado	:= Len(aHeaderAb8)
Local nCntFor	:= 0
Local nCntFor2	:= 0
Local nPosProd  := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_CODPRO"})
Local nPosTotal := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_TOTAL"})
Local nPosSer	:= aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_CODSER"})
Local nPosDesc  := aScan(aHeaderAb8,{|x| AllTrim(x[2])=="AB8_VLDESC"})
Local nTotGer	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0

If ( lF4 )
	For nCntFor := 1 To Len(aCols)
		If ( Len(aCols[nCntFor])==nUsado .Or. !aCols[nCntFor][nUsado+1] )
			nTotCli	+= AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosTotal],"C")
			nTotFab	+= AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosTotal],"F")
			nTotGer  += aCols[nCntFor][nPosTotal]
			//-> Aplicando desconto ao item, qdo informado.
			If nPosDesc > 0
			   nTotGer -= aCols[nCntFor][nPosDesc]
			EndIf
		EndIf
	Next nCntFor
Else
	For nCntFor := 1 To Len(aColsAB8)
		For nCntFor2 := 1 To Len(aColsAB8[nCntFor])
			If ( Len(aColsAB8[nCntFor][nCntFor2])==nUsado .Or. !aColsAB8[nCntFor][nCntFor2][nUsado+1] )
				nTotCli	+= AtVlrPagto(aColsAB8[nCntFor][nCntFor2][nPosSer],aColsAB8[nCntFor][nCntFor2][nPosTotal],"C")
				nTotFab	+= AtVlrPagto(aColsAB8[nCntFor][nCntFor2][nPosSer],aColsAB8[nCntFor][nCntFor2][nPosTotal],"F")
				nTotGer  += aColsAB8[nCntFor][nCntFor2][nPosTotal]
				//-> Aplicando desconto ao item, qdo informado.
				If nPosDesc > 0
				   nTotGer -= aColsAB8[nCntFor][nCntFor2][nPosDesc]
				EndIf
			EndIf
		Next nCntFor2
	Next nCntFor
EndIf
Eval(oDlg:Cargo,nTotGer,nTotCli,nTotFab)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Aloc ³ Autor ³ Vendas Clientes       ³ Data ³28.01.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra as alocacoes da Ordem de Servico.                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³oGetd : Objeto da Getdados                                  ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450Aloc(oGetd)

Local aArea		:= GetArea()
Local oDlg		:= Nil
Local oGetd2	:= Nil
Local cCadastro	:= STR0025		//"Programação da OS"
Local aSavCols	:= aClone(aCols)
Local aSavHead	:= aClone(aHeader)
Local nSavN		:= If(Type("N")=="U",1,N)
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader

Private aCols	:= {}
Private aHeader	:= {}
Private INCLUI	:= .F.
Private ALTERA	:= .F.
Private dData	:= dDataBase

oGetD:oBrowse:lDisablePaint:= .T.

DbSelectArea("SA1")
DbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB6_CODCLI+M->AB6_LOJA)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cSeek	:= xFilial("ABB")+M->AB6_NUMOS
cWhile	:= "ABB->ABB_FILIAL+ABB->ABB_NUMOS"

FillGetDados(	2   			,"ABB"			,3				,cSeek				,;
  				{|| &(cWhile) }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/		,; 
				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/			,;
				/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/	)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se a 1a. linha estiver em branco, limpa o aCols ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCols) > 0 .AND. Empty( aCols[1][GdFieldPos("ABB_NUMOS")] )
	aCols := {}
EndIf	

If ( Len(aCols) <> 0 )
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 09,0 TO 28,80 OF oMainWnd
	@ 015,010 SAY   RetTitle("AB6_CODCLI") 	SIZE 040,009 	OF oDlg PIXEL
	@ 015,050 MSGET SA1->A1_NOME					SIZE 136,009 	OF oDlg PIXEL WHEN .F.
	oGetd2:=MsGetDados():New(030,005,138,314,1,"","","",.F.)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||oDlg:End()},{||oDlg:End()})
Else
	Help(" ",1,"AT450NALO") //Hedit2
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a entrada da rotina                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCols 	:= aClone(aSavCols)
aHeader	:= aClone(aSavHead)
N		:= nSavN
oGetD:oBrowse:lDisablePaint:= .F.

RestArea(aArea)

Return(Nil)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Laudo³ Autor ³ Vendas Clientes       ³ Data ³29.01.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Demonstrativo dos Laudo Tecnicos                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Laudo(oGetD, cEtapa )

Local aArea	    := GetArea()					//Guarda a area atual
Local nCntFor   := 0							//Usada em lacos For...Next                                               
Local nUsado    := 0							//Nr. de campos no aHeader
Local oDlg		:= Nil							//Obj. Dialog Principal
Local oGetd2	:= Nil							//Obj. GetDados2

Local cCadastro := STR0026 						//"Atendimento da OS"
Local cTarefa   := ""							//Cod. da tarefa
Local cDescTrf  := ""							//Descr. da tarefa
Local cSeekAB9  := ""							//Seek no arquivo AB9
Local cSeek		:= ""							//Seek para montagem da aCols
Local cWhile	:= ""							//While para montagem da aCols
Local aSavCols	:= aClone(aCols)				//Copia do aCols
Local aSavHead	:= aClone(aHeader)				//Copia do aHeader
Local nSavN		:= If(Type("N")=="U",1,N)		//Copia de N
Local nOrdem	:= 0							//Indice para FillGetDados

lTarefa := ( ValType( cEtapa ) == "C" ) 

If lTarefa
	cTarefa  := cEtapa + GDFieldGet( "ABJ_SUBITE" ) 
	cDescTrf := GDFieldGet( "ABJ_DESCRI" ) 
EndIf   

Private aCols	:= {}
Private aHeader	:= {}
Private INCLUI	:= .F.
Private ALTERA  := .F.
Private dData	:= dDataBase

oGetD:oBrowse:lDisablePaint:= .T.

If !lTarefa
	DbSelectArea("SA1")
	DbSetOrder(1)
	DbSeek(xFilial("SA1")+M->AB6_CODCLI+M->AB6_LOJA)
EndIf 	

If lTarefa 
	cSeek	:= xFilial("AB9") + cTarefa
	cWhile	:= "AB9->AB9_FILIAL + AB9->AB9_TAREFA"
	nOrdem	:= 4
Else                         	
	cSeek	:= xFilial("AB9") + M->AB6_NUMOS
	cWhile	:= "AB9->AB9_FILIAL + SubStr(AB9->AB9_NUMOS,1,Len(M->AB6_NUMOS))"	
	nOrdem	:= 1
EndIf 	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Montagem aHeader, aCols³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
FillGetDados(	2   			,"AB9"			,nOrdem			,cSeek				,;
  				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/		,; 
				/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/			,;
				/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/	)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se a 1a. linha estiver em branco, limpa o aCols ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aCols) > 0 .AND. Empty( aCols[1][GdFieldPos("AB9_NUMOS")] )
	aCols := {}
EndIf

If ( Len(aCols) <> 0 )
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 09,0 TO 28,80 OF oMainWnd
	
	If lTarefa 
		@ 035,010 SAY   RetTitle("ABJ_TAREFA") 	SIZE 040,009 	OF oDlg PIXEL
		@ 035,050 MSGET cDescTrf     			SIZE 136,009 	OF oDlg PIXEL WHEN .F.
	Else 		
		@ 035,010 SAY   RetTitle("AB6_CODCLI") 	SIZE 040,009 	OF oDlg PIXEL
		@ 035,050 MSGET SA1->A1_NOME			SIZE 136,009 	OF oDlg PIXEL WHEN .F.
	EndIf 	
		
	oGetd2:=MsGetDados():New(052,005,128,314,1,"","","",.F.)
	ACTIVATE MSDIALOG oDlg ON INIT At450Bar2(oDlg,{||oDlg:End()},{||oDlg:End()},,oGetD2)
Else
	Help(" ",1,"AT450LAUDO") //Hedit2
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a entrada da rotina                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCols 	:= aClone(aSavCols)
aHeader	:= aClone(aSavHead)
N		:= nSavN
oGetD:oBrowse:lDisablePaint:= .F.

RestArea( aArea ) 

Return(Nil)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT450Bar2 ³ Autor ³ Vendas Clientes       ³ Data ³ 29.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Mostra a EnchoiceBar na tela                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AT450Bar2(oDlg,bOk,bCancel,nOpc,oGetD)

Local aButtons := {}

aAdd(aButtons,{"PESQUISA",{ || At450Laud2(oGetD)}, STR0003, STR0033}) //"Visualizar"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "AT450BU2" ) 
	aUsButtons := ExecBlock( "AT450BU2", .F., .F. ) 
	aEval( aUsButtons, { |x| aAdd( aButtons, x ) } ) 	 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Laud2³ Autor ³ Vendas Clientes       ³ Data ³ 29.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Visualizacao Individual do Laudo ( TECA460 )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExcO1 : Objeto Getdados                                     ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450Laud2(oGetD)

Local aArea 		:= GetArea()
Local aSavHeader	:= aClone(aHeader)
Local aSavCols    := aClone(aCols)
Local nSavN			:= If(Type("N")=="U",1,N)
Local nPosNumOs   := aScan(aHeader,{|x| AllTrim(x[2])=="AB9_NUMOS"})
Local cLaudo      := aCols[nSavN][nPosNumOs] + GDFieldGet( "AB9_CODTEC", nSavN ) + GDFieldGet( "AB9_SEQ", nSavN ) 

oGetD:oBrowse:lDisablePaint := .T.

DbSelectArea("AB9")
DbSetOrder(1)
If ( DbSeek(xFilial("AB9")+cLaudo) )
	At460Visua("AB9",AB9->(RecNo()),2)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a Rotina                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := aClone(aSavHeader)
oGetD:oBrowse:lDisablePaint := .F.
RestArea(aArea)
Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Track³ Autor ³ Vendas Clientes       ³ Data ³09/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AT450Track() 

Local aEnt     := {}								// Itens para rastreamento
Local cNumOs   := M->AB6_NUMOS 						// Numero da OS
Local nPosItem := GDFieldPos( "AB7_ITEM" ) 		    // Posicao do item AB7_ITEM
Local nLoop    := 0 								// Utilizado no loop
Local oOldDlg        := oDlg						// Salva as variaveis estaticas
Local oOldSay1       := oSay1						// Salva as variaveis estaticas
Local oOldSay2       := oSay2                  		// Salva as variaveis estaticas
Local oOldSay3       := oSay3						// Salva as variaveis estaticas
Local cOldPictureTot := cPictureTot					// Salva as variaveis estaticas

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os itens para rastreamento          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aCols ) 
	aAdd( aEnt, { "AB7", cNumOs + aCols[ nLoop, nPosItem ] } ) 
Next nLoop 

MaTrkShow( aEnt ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura as variaveis estaticas, pois a rotina MaTrShow ³
//³reabre a janela TECA450 novamente, caso solicitado pelo ³
//³usuario.                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oDlg		:= oOldDlg
oSay1		:= oOldSay1
oSay2		:= oOldSay2
oSay3		:= oOldSay3
cPictureTot	:= cOldPictureTot
           
Return( .T. ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ At450Leg ³ Autor ³ Vendas Clientes       ³ Data ³12/04/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Exibe a legenda da Ordem de Servico                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT450Leg() 

Local aCoresNew := {}
Local aCores    := {}

aCores    := {	{ "ENABLE"	   	,  STR0036 },;	//"Aberta"
				{ "BR_VERMELHO"	,  STR0037 },;	//"Encerrada"  
				{ "BR_AMARELO"	,  STR0054 } }	//"Atendida"             

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores da legenda    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("TC450LEG")
	aCoresNew := ExecBlock("TC450LEG",.F.,.F.,aCores)
	If ValType( aCoresNew ) == "A"
		aCores := aClone(aCoresNew)
	EndIf
Endif

BrwLegenda(cCadastro,STR0035,aCores)

Return( Nil ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³At450Moeda³ Autor ³ Vendas Clientes       ³ Data ³19/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a moeda do Orcamento esta cadastrada           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT450MoedaOK()

Local lRet 		:= .T.					//Indica o retorno da funcao
Local aAreaAB6	:= AB6->(GetArea()) 	//Guarda a area corrente

If (AB6->AB6_MOEDA <> 0) 
	If AB6->AB6_TXMOED <= 0
		If Recmoeda( dDatabase, AB6->AB6_MOEDA ) <= 0
			//"Não existe taxa para a Moeda " #
			//" informada nesta OS. Favor cadastrá-la antes de efetivar."
			//"OS: " ######
			MsgAlert( STR0040 + StrZero(AB6->AB6_MOEDA,1) + STR0041 , STR0042 + AB6->AB6_NUMOS ) 
			lRet := .F.
		EndIf
	EndIf
EndIf  

RestArea(aAreaAB6)

Return (lRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At450Pict ³ Autor ³ Vendas Clientes       ³ Data ³ 12.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Atualiza picture dos valores do Rodape conforme Moeda      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450Pict()

Local nMoeda := M->AB6_MOEDA			// Moeda informada no cabecalho

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB8","AB8_TOTAL",14,nMoeda) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua um refresh nos objetos c/ a nova Picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSay1:Refresh()
oSay2:Refresh()
oSay3:Refresh()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recalcula os totais do Rodape da tela  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
At450Total(oDlg, .F.)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³AT450Contatos ³ Autor ³Vendas Clientes        ³ Data ³ 09/04/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Mostra os Contatos do cliente selecinado 			              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ TkContatos(ExpN1, ExpL2, ExpL3)    		                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = nFolder (Numero da pasta usada no CallCenter para utili³±±
±±³          ³         a rotina no loja o folder tem que ser 12)			  ³±±
±±³          ³ ExpL2 = .T. se a Entidade e um prospect ou .F. se dor Cliente  ³±±
±±³          ³ ExpL3 = .T. Considera o parametro MV_LJF3CNT para definir o    ³±±
±±³          ³ tipo de exibicao da consulta. ou .F. Nao considera o Parametro ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso   	 ³Sigatmk - Com fun‡”es do TeleMarketing     				      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At450Contatos(cEntField, cLojField)
Local oLbx							 					// Listbox com os nomes dos contatos
Local aCont		:= {}									// Array com os contatos
Local cDFuncao	:= CRIAVAR("U5_FUNCAO",.F.)			// Funcao do contato na empresa	
Local cEntCod	:= ""									// Código da entidade
Local cEntLoja	:= ""									// Loja da entidade
Local cDesc		:= ""									// Descrição da entidade
Local cEntidade	:= ""									// Alias da entidade
Local nOpcao	:= 0									// Opcao 
Local nContato	:= 0									// Posicao do contato dentro do array na selecao
Local oDlg												// Tela
Local lRet		:= .F.									// Retorno da tela
Local aTel 		:= {}
Local nX 		:= 0

cEntidade := "SA1"

cEntCod		:= &(cEntField)
cEntLoja	:= &(cLojField)
cDesc		:= Posicione( "SA1", 1, xFilial("SA1") + &(cEntField) + &(cLojField), "A1_NREDUZ" )

If Empty(cEntCod)
	Help(" ",1,"SEM CLIENT")
	Return(lRet)
Endif

DbSelectArea("AC8")
AC8->(DbSetOrder(2))		//AC8_FILIAL+AC8_ENTIDA+AC8_FILENT+AC8_CODENT+AC8_CODCON

DbSelectArea("SU5")
SU5->(DbSetOrder(1))

If AC8->(DbSeek(xFilial("AC8") + cEntidade + xFilial(cEntidade) + cEntCod + cEntLoja)) 

	While AC8->(!EOF()) .AND. AC8->AC8_FILIAL == xFilial("AC8") .AND. Alltrim(AC8->AC8_CODENT) == cEntCod + cEntLoja
			
		If SU5->(DbSeek(xFilial("SU5") + AC8->AC8_CODCON))
			cDFuncao := Posicione("SUM",1,xFilial("SUM")+SU5->U5_FUNCAO,"UM_DESC")

			aTel := TkGetAGB("SU5",SU5->U5_CODCONT)

			If Len(aTel) > 0 
				For nX := 1 To Len(aTel)
				Aadd(aCont, {	SU5->U5_CODCONT,;		//C¢digo
								SU5->U5_CONTAT,;		//Nome 
								cDFuncao,;				//Fun‡„o
								At450TpFone(aTel[nX][2]),;			//Tipo
								aTel[nX][4],;			//DDD
								aTel[nX][5],;			//Telefone
								SU5->U5_OBS;			//Observacao
								} )
				Next nX				
			EndIf 
		Else
			Aadd(aCont,{"","","","","","",""})
		Endif
		AC8->(DbSkip())
	EndDo	
Else
	If AT450IncCt(@oLbx,@aCont,cEntidade,cEntCod,cEntLoja,cDesc) == 3 // Cancelou a Inclusao
		Return(lRet)
	Else
		lRet := .T.	
		Return(lRet)
	Endif
Endif

If Empty(aTel)
	Aadd(aCont,{"","","","","","",""})
EndIf 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Mostra dados dos Contatos 								     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg FROM  48,171 TO 230,800 TITLE STR0045 + " - " + cDesc PIXEL STYLE WS_DLGFRAME	// "Cadastro de Contatos"
	
	@  3,2 TO  73, 310 LABEL STR0045 + ":" OF oDlg  PIXEL

	@ 10,5	LISTBOX oLbx FIELDS ;
			HEADER	;
			STR0046	,;	// "Código"
			STR0047	,;	// "Nome"
			STR0048	,;	// "Função"
			STR0118  ,;  //Tipo
			STR0119	,;	// "DDD"
			STR0049	,;	// "Telefone"
			STR0050 ;	// "Observação"
			SIZE 303,60  NOSCROLL OF oDlg PIXEL ;
			ON DBLCLICK( nOpcao:= 1,nContato := oLbx:nAt,oDlg:End() )
	
			oLbx:SetArray(aCont)
			oLbx:bLine:={ || {	aCont[oLbx:nAt,1],;		// C¢digo
								aCont[oLbx:nAt,2],;		// Nome 
								aCont[oLbx:nAt,3],;		// Fun‡„o
								aCont[oLbx:nAt,4],;		// Tipo
								aCont[oLbx:nAt,5],;		// DDD
								aCont[oLbx:nAt,6],;		// Telefone
								aCont[oLbx:nAt,7];		// Observacao
								}}
	
	DEFINE SBUTTON FROM 74,162 TYPE 4	ENABLE OF oDlg ACTION AT450IncCt(	@oLbx		, @aCont	, cEntidade	, cEntCod	,;
																		cEntLoja	, cDesc 	)
	DEFINE SBUTTON FROM 74,192 TYPE 11	ENABLE OF oDlg ACTION AT450AltCt(	@oLbx		, 1			, @aCont	, cEntCod	,;
																		cEntLoja	)
	DEFINE SBUTTON FROM 74,222 TYPE 15	ENABLE OF oDlg ACTION AT450VisCt(	oLbx		, 1			, @aCont	, cEntCod	,;
																		cEntLoja	)
	
	DEFINE SBUTTON FROM 74,252 TYPE 1	ENABLE OF oDlg ACTION (nOpcao:= 1,nContato:= oLbx:nAt,oDlg:End())
	DEFINE SBUTTON FROM 74,282 TYPE 2	ENABLE OF oDlg ACTION (nOpcao:= 0,oDlg:End())

ACTIVATE MSDIALOG oDlg CENTERED 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona no registro correto para ser atualizado o campo de codigo do contato.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DbSelectArea("SU5")
DbSetOrder(1)
If (nOpcao == 1)
	lRet := .T.
	DbSeek(xFilial("SU5") + aCont[nContato,1])
Endif

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³AT450IncCt³ Autor ³ Vendas Clientes  		³ Data ³11/03/04  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Executa a rotina de Inclusao de novos contatos              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³CALL CENTER                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450IncCt(oLbx	, aCont	, cEntidade	, cCliente	,;
						cLoja	, cDesc )

Local aArea   		:= GetArea()							// Salva a area atual
Local nOpca     	:= 0									// Opcao de OK ou CANCELA
Local cDFuncao  	:= CRIAVAR("U5_FUNCAO",.F.)			// Cargo da funcao do contato
Local cAlias    	:= "SA1"								// Alias 
Local lIncluiAnt	:= INCLUI                          		// Guarda o conteudo da variavel para restaurar apos a inclusao do contato

Private cCadastro 	:= STR0051								// "Inclusão de contatos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Na Inclusao, a variavel INCLUI devera estar como .T.  para executar  o inicializador ³
//³padrao de alguns campos que sao preenchidos somente se a variavel estiver .T. 		³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
INCLUI:= .T.

DbSelectArea("SU5")
nOpcA := A70INCLUI("SU5",0,3)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restauva a variavel com o conteudo original³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
INCLUI:= lIncluiAnt

If (nOpca == 1)
	DbSelectArea("AC8")
	RecLock("AC8",.T.)
	REPLACE AC8_FILIAL With xFilial("AC8")
	REPLACE AC8_FILENT With xFilial(cEntidade)
	REPLACE AC8_ENTIDA With cEntidade
	REPLACE AC8_CODENT With cCliente + cLoja
	REPLACE AC8_CODCON With SU5->U5_CODCONT
	MsUnLock()
	DbCommit()
Endif

// Se houve inclusao do registro atualizo o listbox de contatos
If nOpcA == 1
	
	aCont := {}
	
	DbSelectArea("AC8")
	DbSetOrder(2)
	If DbSeek(xFilial("AC8") + cAlias + xFilial(cAlias) + cCliente + cLoja,.T.)

		While (!Eof()) 								.AND. ;
			  (AC8->AC8_FILIAL == xFilial("AC8")) 	.AND.;
			  (AC8->AC8_ENTIDA == cAlias) 			.AND.;
			  (AC8->AC8_FILENT == xFilial(cAlias)) .AND. ;
			  (AllTrim(AC8->AC8_CODENT) == AllTrim(cCliente + cLoja))
		
			DbSelectArea("SU5")
			DbSetOrder(1)
			If DbSeek(xFilial("SU5") + AC8->AC8_CODCON)
				cDFuncao := Posicione("SUM",1,xFilial("SUM")+SU5->U5_FUNCAO,"UM_DESC")

				Aadd(aCont,{SU5->U5_CODCONT,;		//C¢digo
							SU5->U5_CONTAT,;		//Nome 
							cDFuncao,;				//Fun‡„o
							SU5->U5_FONE,;			//Telefone
							SU5->U5_OBS} )			//Observacao
			Else
				Aadd(aCont,{"","","","",""})
			Endif
		
			DbSelectArea("AC8")
			DbSkip()
		End
	Endif	
	
	If ValType(oLbx) == "O"	
		oLbx:SetArray(aCont)
		oLbx:nAt:= Len(aCont)
		oLbx:bLine:={||{aCont[oLbx:nAt,1],;  //C¢digo
						aCont[oLbx:nAt,2],;  //Nome 
						aCont[oLbx:nAt,3],;	 //Fun‡„o
						aCont[oLbx:nAt,4],;	 //Telefone
						aCont[oLbx:nAt,5] }} //Observacao
		oLbx:Refresh()
	 EndIf
	    
Endif

RestArea(aArea)
Return(nOpcA)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³AT450AltCt³ Autor ³ Vendas Clientes  		³ Data ³12/03/04  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Executa a rotina de Altera‡ao de contatos                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³CALL CENTER                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450AltCt(oLbx	, nPos	, aCont	, cCliente	,;
						cLoja	)

Local aArea		  := GetArea()						// Salva a area atual
Local cCod	      := ""								// Codigo do contato	
Local cDFuncao    := ""								// Cargo do contato
Local cAlias	  := "SA1"							// Alias 
Local nOpcA       := 0								// Opcao de retorno OK ou CANCELA
Local aRots       := aClone(aRotina)				// Copia do array aRotina 
Local lRet		  := .T.							// Retorno da funcao

Private cCadastro := STR0052						// "Alteração de Contatos"
Private lRefresh  := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ A ocorrencia 82 (ACS), verifica se o usu rio poder  ou n„o alterar cadastros ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ChkPsw(82)
	HELP(" ",1,"TMKACECAD")
	lRet := .F.	
	Return(lRet)
Endif

cCod := Eval(oLbx:bLine)[nPos]

DbSelectArea("SU5")
DbSetOrder(1)
If DbSeek(xFilial("SU5")+ cCod)

	BEGIN TRANSACTION

		aRotina:={}	
		aRotina		:= MenuDef()

		If lRet
			nOpcA:=A70ALTERA("SU5",RECNO(),4)
		Endif

		aRotina:= aClone(aRots)	

	END TRANSACTION

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Se houve altera‡ao do registro atualizo o listbox de contatos³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpcA == 1
		lRet  := .T.
		aCont := {}
		DbSelectArea("AC8")
		DbSetOrder(2)
		If DbSeek(xFilial("AC8") + cAlias + xFilial(cAlias) + cCliente + cLoja,.T.)
			While (!Eof()) 							  	.AND.;
				  (AC8->AC8_FILIAL == xFilial("AC8")) 	.AND.;
				  (AC8->AC8_ENTIDA == cAlias) 		  	.AND.;
				  (AC8->AC8_FILENT == xFilial(cAlias))	.AND.;
				  (AllTrim(AC8->AC8_CODENT) == AllTrim(cCliente + cLoja))
		
				DbSelectArea("SU5")
				DbSetOrder(1)
				If DbSeek(xFilial("SU5") + AC8->AC8_CODCON)
					cDFuncao := Posicione("SUM",1,xFilial("SUM")+SU5->U5_FUNCAO,"UM_DESC")
					Aadd(aCont,{SU5->U5_CODCONT,;		//C¢digo
								SU5->U5_CONTAT,;		//Nome 
								cDFuncao,;				//Fun‡„o
								SU5->U5_FONE,;			//Telefone
								SU5->U5_OBS} )			//Observacao
				Else
					Aadd(aCont,{"","","","",""})
				Endif
		
				DbSelectArea("AC8")
				DbSkip()
			EndDo
		Endif	
		
		oLbx:SetArray(aCont)
		oLbx:bLine:={||{aCont[oLbx:nAt,1],;  //C¢digo
						aCont[oLbx:nAt,2],;  //Nome 
						aCont[oLbx:nAt,3],;	 //Fun‡„o
						aCont[oLbx:nAt,4],;	 //Telefone
						aCont[oLbx:nAt,5] }} //Observacao
		oLbx:Refresh()

	Endif
Endif

RestArea(aArea)

Return(lRet)        

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o	 ³AT450VisCt³ Autor ³ Vendas Clientes  		³ Data ³09/03/00  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Executa a rotina de Visualizacao de contatos                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso		 ³CALL CENTER                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT450VisCt(oLbx	, nPos	, aCont	, cCliente	,;
						cLoja	)
						
Local cAliasOld   := Alias()
Local cCod		  := ""
Private cCadastro := STR0053	// "Visualização de Contatos"

cCod := Eval(oLbx:bLine)[nPos]
DbSelectArea("SU5")
DbSetOrder(1)

If DbSeek(xFilial("SU5")+ cCod)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia para processamento dos Gets          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A70Visual("SU5",RECNO(), 2)
Endif

DbSelectArea(cAliasOld)
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ AT450Bar3 ³ Autor ³ Caio Sergio Ferreira ³ Data ³14/12/2010³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡Æo ³ Mostra a EnchoiceBar na tela                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso      ³ TECA450                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AT450Bar3(oDlg,bOk,bCancel,nOpc,oGetD)

Local aButtons := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "AT450BU3" ) 
	aUsButtons := ExecBlock( "AT450BU3", .F., .F. ) 
	aEval( aUsButtons, { |x| aAdd( aButtons, x ) } ) 	 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³At460AjAco³ Autor ³ Rodrigo Toledo        ³ Data ³ 27/05/2011 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Ajusta o acols para gravar o conteudo das informacoes nas 	³±± 
±±³			 ³ tabelas AB8 e AAR.											³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ At460AjAco(ExpC1,ExpC2,ExpC3,ExpC4)                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Codigo do contrato                                	³±± 
±±³			 ³ ExpC2 -> Numero da OS                                		³±± 
±±³			 ³ ExpC3 -> Item da OS	                                		³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil						            						³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ At460AjAco                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 

Static Function At450AjAco(cContrato,cNumOs,cItem)
Local aAreaAAR := AAR->(GetArea())		
Local aAreaAAQ := AAQ->(GetArea())		
Local nPos	   := 0
Local nNovoSal := 0  
Local nSaldo   := 0
Local nLoop	   := 0
Local nX	   := 0
Local aProd    := {}
Local cMaxItem := ""
Local nPosSubI := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_SUBITE"})
Local nPosProd := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODPRO"})
Local nPosServ := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_CODSER"})
Local nPosQtde := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_QUANT"})
Local nPosDesc := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_DESPRO"})
Local nPosLoca := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_LOCAL"})
Local nPosLoc  := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_LOCALI"})
Local nPosUnit := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_VUNIT"})
Local nPosTota := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_TOTAL"})
Local nPosPCLi := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_PRCLIS"})
Local nPosSeri := aScan(aHeaderAB8,{|x| AllTrim(x[2])=="AB8_NUMSER" })

AAQ->(DbSetOrder(1))
AAR->(DbSetOrder(1))   
AB8->(DbSetOrder(1))

For nLoop := 1 To Len(aCols)
	If (nPos := aScan(aProd,{|x| x[1] == aCols[nLoop,nPosProd]})) == 0
		aAdd(aProd,{aCols[nLoop,nPosProd],Array(2)})
		nPos := Len(aProd)
	EndIf
	If AtBuscServ(cContrato,.T.) == aCols[nLoop,nPosServ]
		aProd[nPos,2,1] := nLoop
	ElseIf AtBuscServ(cContrato,.F.) == aCols[nLoop,nPosServ]
		aProd[nPos,2,2] := nLoop
	EndIf
Next nLoop   

For nLoop := 1 To Len(aProd)
	If Empty(aProd[nLoop,2,1]) .Or. !AAQ->(DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]))
		Loop
	EndIf		      
	AB8->(DbSeek(xFilial("AB8")+cNumOs+cItem+aCols[nLoop,nPosSubI]))
	If AAQ->AAQ_MODLIB == "1"
		If AAR->(!DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]))
			nNovoSal := AAQ->AAQ_QTDLIB - aCols[aProd[nLoop,2,1],nPosQtde]
		Else			
			nSaldo 	 := ABS(AAR->AAR_SALDO + AB8->AB8_QUANT)
			nNovoSal := nSaldo - aCols[aProd[nLoop,2,1],nPosQtde]
		EndIf
	ElseIf AAQ->AAQ_MODLIB == "2"
		If AAR->(!DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]+LEFT(DTOS(dDataBase),6)))
			nNovoSal := AAQ->AAQ_QTDLIB - aCols[aProd[nLoop,2,1],nPosQtde]
		Else
			nSaldo 	 := ABS(AAR->AAR_SALDO + AB8->AB8_QUANT)
			nNovoSal := nSaldo - aCols[aProd[nLoop,2,1],nPosQtde]
		EndIf
	EndIf
	
	If nNovoSal >= 0
		If !Empty(aProd[nLoop,2,2])
			If !(aCols[aProd[nLoop,2,2],Len(aHeader)+1])
				aCols[aProd[nLoop,2,1],nPosQtde] += Min(nNovoSal,aCols[aProd[nLoop,2,2],nPosQtde])
				aCols[aProd[nLoop,2,1],nPosTota] := aCols[aProd[nLoop,2,1],nPosQtde] * aCols[aProd[nLoop,2,1],nPosUnit]
				If nNovoSal >= aCols[aProd[nLoop,2,2],nPosQtde]
					nNovoSal -= aCols[aProd[nLoop,2,2],nPosQtde]
			   		aCols[aProd[nLoop,2,2],Len(aHeader)+1]	:= .T.
				Else
					aCols[aProd[nLoop,2,2],nPosQtde] -= nNovoSal
				EndIf
			EndIf 
		EndIf		
	ElseIf nNovoSal < 0
		aCols[aProd[nLoop,2,1],nPosQtde] += nNovoSal 
		aCols[aProd[nLoop,2,1],nPosTota] := aCols[aProd[nLoop,2,1],nPosQtde] * aCols[aProd[nLoop,2,1],nPosUnit]
		If !Empty(aProd[nLoop,2,2])
			If (aCols[aProd[nLoop,2,2],Len(aHeader)+1])
				aCols[aProd[nLoop,2,2],Len(aHeader)+1]	:= .F.
				aCols[aProd[nLoop,2,2],nPosQtde] := 0
			EndIf
			aCols[aProd[nLoop,2,2],nPosQtde] += ABS(nNovoSal)
		Else
			aEval(aCols,{|x| If(x[nPosSubI] > cMaxItem,cMaxItem := x[nPosSubI],NIL)})
			aAdd(aCols,Array(Len(aHeader)+1))
			For nX := 1 To Len(aHeader)
				If IsHeadRec(aHeader[nx,2])
					ATAIL(aCols)[nX] := 0
				ElseIf IsHeadAlias(aHeader[nx,2])
					ATAIL(aCols)[nX] := "AB8"
				Else
					ATAIL(aCols)[nX] := CriaVar(aHeader[nX,2],.T.)
				EndIf
			Next nX						
			ATAIL(aCols)[nPosSubI] := Soma1(cMaxItem)
		 	ATAIL(aCols)[nPosProd] := aProd[nLoop,1]
		 	ATAIL(aCols)[nPosDesc] := Posicione("SB1",1,xFilial("SB1")+aProd[nLoop,1],"B1_DESC")
		  	ATAIL(aCols)[nPosServ] := AtBuscServ(AA3->AA3_CONTRT,.F.)
		  	ATAIL(aCols)[nPosQtde] := ABS(nNovoSal)
		  	ATAIL(aCols)[nPosLoca] := RetFldProd(aProd[nLoop,1],"B1_LOCPAD")
		  	ATAIL(aCols)[nPosLoc]  := aCols[nLoop,nPosLoc]
		  	ATAIL(aCols)[nPosUnit] := aCols[nLoop, nPosUnit]
		  	ATAIL(aCols)[nPosTota] := (ABS(nNovoSal) * aCols[nLoop, nPosUnit])
		  	ATAIL(aCols)[nPosPCLi] := aCols[nLoop,nPosPCLi]
		  	ATAIL(aCols)[nPosSeri] := aCols[nLoop,nPosSeri]
			ATAIL(aCols)[Len(aHeader)+1] := .F.
		Endif			
	EndIf	
Next nLoop

RestArea(aAreaAAR)
RestArea(aAreaAAQ)
Return()

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³AT450EdAco³ Autor ³ Vendas Clientes       ³ Data ³13.12.11  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Tratamento para alteração quando item for P.gerado/Encerra ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AT450EdAco                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA450                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450EdAco()
Local lRet		:= .T.
Local nPosItem := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_TIPO"})

If Altera
	If Acols[n][nPosItem] $"2|5"  // 2 = Pedido gerado 5 = Encerrado
		lRet := .F.
	EndIf	 
EndIf
Return lRet  
            
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450F3CtrºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Consulta padrao especifica para listar os contratos de manu-º±±  
±±º          ³tencao / contrato de prestacao de servicos.				  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL - Verdadeiro                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³Nenhum													  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
               
Function At450F3Ctr()

Local cPesq	 	:= Space(50)                         					// Inicializa espacos em branco no objeto MsGet.
Local oDlg		:= Nil													// Objeto oDlg.
Local oLstBx	:= Nil													// Objeto ListBox.
Local oPesq		:= Nil 													// Objeto Pesquisa.
Local aContrato	:= {}						 							// Array com os contratos.
Local bRet		:= {|| IIF(At450CtrOk(oLstBx),oDlg:End(),Nil) }		// Bloco de codigo para o botao OK.
Local cNomeCtrt := ""													// Nome do Contrato.
Local cConsulta := ""
Local lRetorno  := .T.       											// Tipo do contrato.
Local lFiltro 	:= .T.       											//Variavel que indica se função foi chamada do filtro da MBROWSE 
Local aRotinas 	:= MenuDef()
Local nCount 		:= 0

for nCount := 1 to len(aRotinas)
	If IsInCallStack(aRotinas[nCount,2])
		lFiltro  := .F.
		Exit
	EndIf
Next nCount

// Se chamado do filtro da MBrowse, deverá ser tratada pela ConPad1, pois os campos da ainda não estão
// em memória. 
If lFiltro 
	ConPad1( ,,,"AAH", , , .F. ) 
Else
	If Empty(M->AB6_CODCLI) .AND. Empty(M->AB6_LOJA)		
		//"Atenção"#"Informe o cliente / loja para esta Ordem de Serviço."#"OK"
		Aviso(STR0055,STR0081,{STR0057},1)
		lRetorno := .F. 
	EndIf	

	If ( lRetorno )
	
		// Tratamento para listar os tipos de contrato
		Do Case
			Case M->AB6_TPCONT == '1'
				cNomeCtrt  := STR0076	   			//"contrato de manutenção"	 
				aContrato  := At450F3AAH()
				cConsulta  := STR0064      			//"Consulta Contrato de Manutenção" 
			Case M->AB6_TPCONT == '2'      		
				cNomeCtrt  := STR0077      			//"contrato de prestação de serviços"   
				aContrato  := At450F3AAM()
				cConsulta  := STR0065				//"Consulta Contrato de Prestação de Serviços"	
			OtherWise
				Aviso(STR0055,STR0066,{STR0057},1)  //"Atenção"##"Consulta não disponivel para este tipo de contrato!"###"OK"
				lRetorno := .F.
		EndCase
			
		
		If ( lRetorno )
			
			If Len(aContrato) > 0   
			
				DEFINE MSDIALOG oDlg TITLE cConsulta FROM 268,260 TO 630,796 PIXEL //"Consulta"
				    
					//Texto de pesquisa
					@ 003,002 MsGet oPesq Var cPesq Size 175,009 COLOR CLR_BLACK PIXEL OF oDlg
				            
					//Interface para selecao de indice e filtro
					@ 003,185 Button STR0002 Size 037,012 PIXEL OF oDlg	 ACTION ( At260Busca(@oLstBx,cPesq,@oPesq,.T.) ) //Pesquisar
					
					@ 003,230 Button STR0067 Size 037,012 PIXEL OF oDlg ACTION ( At260Busca(@oLstBx,cPesq,@oPesq,.F.) ) //Proximo
					
							
					//ListBox Contratos ## "Número","Cliente","Loja","Tipo do Contrato","Inicio da Vigência","Fim da Vigência","Situação"  
					@ 20,03 LISTBOX oLstBx FIELDS HEADER STR0068,STR0069,STR0070,STR0071,STR0072,STR0073,STR0074 SIZE 264,139 OF oDlg PIXEL
					oLstBx:bLDblClick := bRet
					
					//Botoes inferiores
					DEFINE SBUTTON FROM 162,002 TYPE 1	ENABLE OF oDlg Action( Eval(bRet) ) 			 //OK
					DEFINE SBUTTON FROM 162,035 TYPE 2	ENABLE OF oDlg Action( oDlg:End() )   			 //Cancelar 
					DEFINE SBUTTON FROM 162,068 TYPE 15	ENABLE OF oDlg Action( At450CtrVs(oLstBx) ) 	 //Visualizar
					
					ASort (aContrato,/*nInicio*/,/*nCont*/,{|a,b| a[1] < b[1]})
					
					//Metodos da ListBox
					oLstBx:SetArray(aContrato)
					oLstBx:bLine := {|| {	aContrato[oLstBx:nAt,1] ,;		// "Numero"
											aContrato[oLstBx:nAt,2] ,;		// "Cliente"
											aContrato[oLstBx:nAt,3] ,;		// "Loja"
											aContrato[oLstBx:nAt,4] ,; 		// "Tipo do Contrato"
											aContrato[oLstBx:nAt,5] ,;		// "Inicio da Vigência"
											aContrato[oLstBx:nAt,6] ,;		// "Fim da Vigência"
											aContrato[oLstBx:nAt,7]}} 		// "Situação"
				
				ACTIVATE MSDIALOG oDlg CENTERED
			Else
				Aviso(STR0055,STR0078+cNomeCtrt,{STR0057},1)  //"Atenção"##"Não há contrato de manutencao / contrato de prestacao de servico "###"OK"     
			EndIf
		
		EndIf	
	EndIf	
EndIf

Return ( .T. ) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450CtrOkºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Botao OK para confirmar a selecao do contrato.	          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL - Verdadeiro / Falso       	                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto ListBox Contratos 							  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
         
Static Function At450CtrOk(oLstBx)

Local lRet	  := .T.		// Retorno da validacao.
Local nNrCtrt := ""			// Numero do contrato.  

If oLstBx:aArray[oLstBx:nAt][8] == '1' 
	// Valida o contrato 
	nNrCtrt := oLstBx:aArray[oLstBx:nAt][1]
	lRet := At450VCtrt(nNrCtrt)
	If lRet
		M->AB6_CONTRT := oLstBx:aArray[oLstBx:nAt][1] 
	EndIf
	
Else
	Aviso(STR0055,STR0075,{STR0057},2) 
	lRet := .F.
EndIf	

Return( lRet )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450F3AAHºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca todos os contratos de manutencao.			          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpA - Array contem os contratos de manutencao.	       	  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³Nenhum													  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function At450F3AAH()

Local aAreaAAH	:= AAH->(GetArea())	// Guarda a area corrente.
Local aBxStatus := {}					// Opcoes do campo Tp. Status.				
Local aBxContrt	:= {}					// Opcoes do campo Nr. Contrato.
Local aRet		:= {}					// Armazena os contratos de manutencao.
Local nPContrt	:= 0 					// Posicao no array do tipo de contrato.
Local nPStatus	:= 0				    // Posicao no array do status do contrato.

DbSelectArea("AAH")
DbSetOrder(2)

DbSeek(xFilial("AAH")+M->AB6_CODCLI+M->AB6_LOJA)

While ( AAH->(!Eof()) .AND. AAH->AAH_FILIAL == xFilial("AAH") .AND.;
	    AAH->AAH_CODCLI == M->AB6_CODCLI .AND. AAH->AAH_LOJA == M->AB6_LOJA )
	
	
	aBxContrt := At260CBox("AAH_TPCONT")
	nPContrt  := aScan(aBxContrt,{|x| x[1] == AAH->AAH_TPCONT})
	
	aBxStatus := At260CBox("AAH_STATUS")
	nPStatus  := aScan(aBxStatus,{|x| x[1] == AAH->AAH_STATUS}) 
	
	aAdd(aRet,{	AAH->AAH_CONTRT			,; 	// Nr. Contrato 
				AAH->AAH_CODCLI			,;	// Cod. Cliente
				AAH->AAH_LOJA			,;	// Loja
				aBxContrt[nPContrt][2]	,;	// Tipo do Contrato                          
				AAH->AAH_INIVLD			,;	// Inicio da Vigencia
				AAH->AAH_FIMVLD			,;	// Fim da Vigencia
				aBxStatus[nPStatus][2]	,; 	// Situacao
				AAH->AAH_STATUS			,;	// Nr. Situacao
				"AAH" })					// Alias
				
	AAH->(DbSkip())
End

RestArea(aAreaAAH)     

Return ( aRet )  


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450F3AAMºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Busca todos os contratos de prestacao de servicos.		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpA - Array contem os contratos de prestacao de servicos.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³Nenhum													  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function At450F3AAM()

Local aAreaAAM	:= AAM->(GetArea())	// Guarda a area corrente.
Local aBxStatus := {}					// Opcoes do campo Tp. Status.			
Local aBxContrt	:= {}					// Opcoes do campo Nr. Contrato.
Local aRet		:= {}					// Armazena os contratos de manutencao.
Local nPContrt	:= 0 					// Posicao no array do tipo de contrato.
Local nPStatus	:= 0				    // Posicao no array do status do contrato.

DbSelectArea("AAM")
DbSetOrder(4)

DbSeek(xFilial("AAM")+M->AB6_CODCLI+M->AB6_LOJA)

While ( AAM->(!Eof()) .AND. AAM->AAM_FILIAL == xFilial("AAM") .AND.;
		AAM->AAM_CODCLI == M->AB6_CODCLI .AND. AAM->AAM_LOJA == M->AB6_LOJA )
	
	aBxContrt := At260CBox("AAM_TPCONT")
	nPContrt  := aScan(aBxContrt,{|x| x[1] == AAM->AAM_TPCONT})

	aBxStatus := At260CBox("AAM_STATUS")
	nPStatus  := aScan(aBxStatus,{|x| x[1] == AAM->AAM_STATUS})
	
	aAdd(aRet,{	AAM->AAM_CONTRT			,; 	// Nr. Contrato 
				AAM->AAM_CODCLI			,;	// Cod. Cliente
				AAM->AAM_LOJA			,; 	// Loja	 
				aBxContrt[nPContrt][2]	,;	// Tipo do Contrato
				AAM->AAM_INIVIG			,;	// Inicio da Vigencia
				AAM->AAM_FIMVIG			,;	// Fim da Vigencia
				aBxStatus[nPStatus][2]	,; 	// Situacao
				AAM->AAM_STATUS			,;	// Nr. Situacao
				"AAM" })  			   		// Alias
				
	AAM->(DbSkip())
End     
                                           
RestArea(aAreaAAM) 

Return ( aRet )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450VCtrtºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Validacao para o contrato de manutencao / contrato de pres- º±± 
±±º          ³tacao de servicos.										  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpA - Array contem os contratos de prestacao de servicos.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpN1 - Numero do contrato								  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At450VCtrt(cNrContrt)

Local lRet := .T.  			// Retorno da validacao.
Local aAreaAlias := Nil		// Guarda a area corrente.

Default cNrContrt := M->AB6_CONTRT


If M->AB6_TPCONT == '1'
	
	aAreaAlias := AAH->(GetArea())
	
	DbSelectArea("AAH")
	DbSetOrder(2)
	
	If DbSeek(xFilial("AAH")+M->AB6_CODCLI+M->AB6_LOJA+cNrContrt)
		If ( AAH->AAH_STATUS <> '1' )
			//"Atenção"#"Somente contrato ativo poderá ser associado a uma Ordem de Serviço."#"OK"
			Aviso(STR0055,STR0079,{STR0057},2)
			lRet := .F.
		EndIf
	Else
		If !Empty(cNrContrt)
			Help(" ",1,"REGNOIS")
			lRet := .F. 
		EndIf	
	EndIf
	
ElseIf M->AB6_TPCONT == '2'
	
	aAreaAlias := AAM->(GetArea())
	
	DbSelectArea("AAM")
	DbSetOrder(4)
	
	If DbSeek(xFilial("AAM")+M->AB6_CODCLI+M->AB6_LOJA+cNrContrt)
		If ( AAM->AAM_STATUS <> '1' )
			//"Atenção"#"Somente contrato ativo poderá ser associado a uma Ordem de Serviço."#"OK"
			Aviso(STR0055,STR0082,{STR0057},2)
			lRet := .F.
		EndIf
	Else
		If !Empty(cNrContrt)
			Help(" ",1,"REGNOIS")
			lRet := .F. 
		EndIf		
	EndIf


ElseIf M->AB6_TPCONT == '3'

	If Empty(cNrContrt)
		Help(" ",1,"REGNOIS")
		lRet := .F.	
	EndIf
Else 
	
	If !Empty(cNrContrt)
		Help(" ",1,"REGNOIS")
		lRet := .F. 
	EndIf
	
EndIf

//Configura a abrangencia do contrato
If lRet
	At450StAbr(cNrContrt)
EndIf

If aAreaAlias <> Nil
	RestArea(aAreaAlias)
EndIf

Return ( lRet )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450CtrVsºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Visualiza o contrato de manutencao / contrato de prestacao  º±± 
±±º          ³de servicos.						 						  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL - Verdadeiro 			                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³ExpO1 - Objeto ListBox contratos.						      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
    
Static Function At450CtrVs(oLstBx)

Local nReg		   := 0         // Numero do registro.

Private cCadastro  := ""		// Nome do formulario.
Private Inclui	   := .F. 		// Caso for inclusao.
Private Altera	   := .F.		// Caso for alteracao.
Private aRotina    := {}		// Variavel com a opcao visualizar.

Do Case
	
	Case ( oLstBx:aArray[oLstBx:nAt][9] == "AAH" )
		
		DbSelectArea("AAH")
		DbSetOrder(1)
		DbSeek(xFilial("AAH")+oLstBx:aArray[oLstBx:nAt][1])
		cCadastro := STR0085		// "Contrato de Manutenção"
		aRotina	  := {{STR0003,"At200Manut",0,2}}
		nReg := AAH->(Recno())
		At200Manut( "AAH",nReg, 1 )
		
	Case ( oLstBx:aArray[oLstBx:nAt][9] == "AAM" )
		
		DbSelectArea("AAM")
		DbSetOrder(1)
		DbSeek(xFilial("AAM")+oLstBx:aArray[oLstBx:nAt][1])
		nReg := AAM->(Recno())
		cCadastro := STR0086		// "Contrato de Prestação de Serviços"
		aRotina := {{STR0003,"At250Manut",0,2}}
		
		At250Manut( "AAM",nReg, 1 )
		
EndCase

Return ( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450WTpCtºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Bloqueia para edicao o campo AB6_TPCONT, caso o usuario nao º±± 
±±º          ³deseja amarrar a O/S com o contrato de manutencao.		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL - Verdadeiro / Falso	   		                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³Nenhum												      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At450WTpCt()

Local lRet		:= .F.                               	// Retorno da validacao.
Local lOSxCtrt	:= SuperGetMv("MV_OSXCTRT",.F.,.F.)	// Amarracao O/S X Contrato

If ( lOSxCtrt )
	lRet := IIF(INCLUI,.T.,.F.)
EndIf	

Return (lRet)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³At450WCtrtºAutor  ³Vendas CRM          º Data ³  01/02/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Bloqueia para edicao o campo AB6_CONTRT, caso o usuario se- º±± 
±±º          ³lecionar a opcao Manutencao ou Servico no campo AB6_TPCONT  º±±  
±±º          ³o campo sera liberado para edicao.						  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ExpL - Verdadeiro / Falso		      	                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³Nenhum					  							      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³TECA450	                                                  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At450WCtrt()

Local lRet := .F.		// Retorno da validacao.

lRet := IIF(INCLUI,( M->AB6_TPCONT $ '1|2' ),.F.) 

Return (lRet)  

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³GetNumAB6 ³ Autor ³Vendas e CRM           ³ Data ³ 02/08/13 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializa o Numero de Ordem de Serviço                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Numero da Ordem de Serviço                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function GetNumAB6()
Local aArea		:= GetArea()
Local aAreaAB6	:= AB6->(GetArea())
Local cNum		:= GetSx8Num("AB6","AB6_NUMOS")
Local nSaveSX8 := GetSX8Len()

dbSelectArea("AB6")
dbSetOrder(1)
While AB6->(DbSeek(xFilial("AB6")+cNum))
	While ( GetSX8Len() > nSaveSX8 )
		ConfirmSX8()
	EndDo
	cNum := GetSx8Num("AB6","AB6_NUMOS")
EndDo

RestArea(aAreaAB6)
RestArea(aArea)
Return(cNum)

//-------------------------------------------------------------------
/*/{Protheus.doc} At450DeLin
	Atualiza os dados da base de atendimento conforme os dados recebidos
por parâmetro
	
@sample 	At450DeLin()
@since  	29/10/2013
@version  	P11.90

@return 	lRet, Logico, permite (.T.) ou não a exclusão da linha
/*/
//-------------------------------------------------------------------
Function At450DeLin( )

Local lExclui      := .T.
Local nPosIt       := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_ITEM"})

Local aArea        := GetArea()

DbSelectArea('TEW')
TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS

If TEW->( DbSeek( xFilial('TEW')+M->AB6_NUMOS+aCols[n][nPosIt] ) )
	lExclui := .F.
	Help(,,'AT450DELIT',,STR0090,1,0) // 'Não é permitido excluir item vinculado à manutenção preventiva de equipamento para locação'
EndIf

RestArea( aArea )

Return lExclui

//-------------------------------------------------------------------
/*/{Protheus.doc} At450FOK
	Atualiza os dados da base de atendimento conforme os dados recebidos
por parâmetro
	
@sample 	At450FOK()
@since  	29/10/2013
@version  	P11.90

@return 	lRet, Logico, permite (.T.) ou não a exclusão da linha
/*/
//-------------------------------------------------------------------
Function At450FOK( )

Local lExclui      := .T.
Local nPosIt       := aScan(aHeader,{|x| AllTrim(x[2])=="AB7_ITEM"})
Local cCpoEdit     := ReadVar()
Local aArea        := GetArea()

If !( 'AB7_TIPO' $ cCpoEdit )
	DbSelectArea('TEW')
	TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS
	
	If TEW->( DbSeek( xFilial('TEW')+M->AB6_NUMOS+aCols[n][nPosIt] ) )
		lExclui := .F.
		Help(,,'AT450ALTIT',,STR0091,1,0) // 'Somente é permitida a alteração do campo de "Tipo" por ser uma O.S. gerada pelo processo de locação de equipamentos'
	EndIf
EndIf

RestArea( aArea )

Return lExclui

//-------------------------------------------------------------------
/*/{Protheus.doc} At450AtuEqloc
	Rotina para atualizar os dados de equipamentos de locação
	
@sample 	At450AtuEqloc()
@since  	01/11/2013
@version  	P11.90

@return 	lRet, Logico, atualizou ou não as informações de Base e Movimentos
/*/
//-------------------------------------------------------------------
Static Function At450AtuEqloc()

Local lGrava := .F.
Local lSeekMan	:= .F.
Local lSeekAut	:= .F.

//Tratativa para validar a rotina manual e automatica 
If Empty(AB6->AB6_ORCAME)
	lSeekMan := AB7->( DbSeek( xFilial('AB7')+AB6->AB6_NUMOS ) )
Else
	DbSelectArea('TEW')
	TEW->( DbSetOrder( 9 ) )  // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS

	lSeekAut := AB7->( DbSeek( xFilial('AB7')+AB6->AB6_NUMOS ) ) .And. ;
				TEW->( DbSeek( xFilial('TEW')+AB7->(AB7_NUMOS+AB7_ITEM) ) )
EndIf

lGrava := lSeekMan .Or. lSeekAut

// indicam o fechamento da OS e atualiza a data de fechamento no movimento de locação
If lGrava
	While (AB7->AB7_NUMOS == AB6->AB6_NUMOS)		
		If lSeekAut 
			TEW->( DbSeek( xFilial('TEW')+AB7->(AB7_NUMOS+AB7_ITEM) ) )
		EndIf		
		
		// Os tipos 
		// 		4 = Atendido 
		//  	5 = Encerrado 
		//  	2 = Pedido Gerado
		// indicam o fechamento da OS e atualiza a data de fechamento no movimento de locação
		// quando não for esses tipos, o fechamento deve ser 'limpado' do movimento de locação
		 	
		If !At800FechOs( !(AB7->AB7_TIPO$'4#5#2') /*lExclusao*/ , ,AB6->AB6_NUMOS)	
			lGrava := .F.
			DisarmTransaction()
		EndIf
		
		AB7->( DbSkip() )
	EndDo
EndIf

Return lGrava

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³Teca450Tbl³ Autor ³Vendas e CRM           ³ Data ³ 30/05/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validação de tabela de preço                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Lógico                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Teca450Tbl()
Local lRet := .T.
Local cCodTab := M->AB6_TABELA
Local cCondPG := M->AB6_CONPAG
Local dDataVld := dDataBase

dbSelectArea("DA0")
dbSetOrder(1)
If MsSeek(xFilial("DA0")+cCodTab)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifico se a tabela de preço está ativa e depois faço a validação da  ³
	//³ vigencia da tabela de preço, conforme o tipo de hora.                  ³
	//³ DA0_TPHORA = (1-Unico ou 2-Recorrente)                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (DA0->DA0_ATIVO == "2") .Or.;
		(DA0->DA0_TPHORA == "1" .And. !(SubtHoras(dDataVld,Time(),If(Empty(DA0->DA0_DATATE),dDataVld,DA0->DA0_DATATE),DA0->DA0_HORATE) >= 0 .And.;
		SubtHoras(DA0->DA0_DATDE,DA0->DA0_HORADE,dDataVld,Time()) >= 0)) .Or.;
		(DA0->DA0_TPHORA == "2" .And. !(dDataVld >= DA0->DA0_DATDE .And. dDataVld <= If(Empty(DA0->DA0_DATATE),dDataVld,DA0->DA0_DATATE) .And.;
		(SubStr(Time(),1,5) >= DA0->DA0_HORADE .And. SubStr(Time(),1,5) <= DA0->DA0_HORATE)))
		
		Help(" ",1,"OMSTABPRC1")   		//"Tabela de preço fora da vigência"	
		lRet := .F.
				
	Else
		If !Empty(cCondPG) .And. !Empty(DA0->DA0_CONDPG) .And. cCondPG <> DA0->DA0_CONDPG			
			Help(" ",1,"OMSTABPRC2")  	//"Condição de pagamento inválida para esta tabela de preços"		
			lRet := .F.
		EndIf	
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³AT450ItApont³ Autor ³Vendas e CRM         ³ Data ³ 07/07/14 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializador do campo AB8_ITEM no apontamento da O.S       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Caracter                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT450ItApont()
Local cItem := " "

If IsInCallStack("AT450F4")
	cItem := StrZero(nMain,2)
EndIf

Return(cItem)

//-------------------------------------------------------------------
/*/{Protheus.doc} At450Struct()
Adicionar os campos da AB8 para edição

@author Serviços
@since 24/10/2014
@version 11.80
@return NIL
/*/
//-------------------------------------------------------------------
Static Function At450Struct(cAlias)
Local aRet  := {}
Local aArea := GetArea()

Local aStruField := {}
Local cField     := ""
Local nC         := 0
aStruField := FWSX3Util():GetAllFields(cAlias)
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL") .And. ;
			!AllTrim(cField) $ "AB8_ITEM" )
			aAdd(aRet,AllTrim(cField))
		EndIf
	Next nC
EndIf

RestArea(aArea)

Return(aRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} At450VldApnt()
Valida a inclusão de apontamentos ou o comando de gravação da O.S. verificando se existe ao menos 1 item válido na O.S.

@author Serviços
@since 23/01/2015
@version 12
@param [ARRAY] Representando o aCols dos Itens ds Ordem de Serviço
@param [ARRAY]] Representando o aHeader dos Títulos relativos às colunas do aCols
@param [LOGIC] Indica se a ação é proveniente do Botão Salvar (.T.) ou da inclusão de apontamentos (default .F.)  
@return [LOGIC] Retorna Verdadeiro se o aCols dos Itens tiver ao menos 1 retistro com código preenchido e não deletado.  
/*/
//-------------------------------------------------------------------
Static Function At450VldApnt(aCols_, aHeader_, lConfirma)
Local lRet       := .F.
Local nI         := 0
Local nPosCodPro := 0
Local nPosCodPrb := 0

Default aCols_   := {}
Default aHeader_ := {}
Default lConfirma := .F.

nPosCodPro := AScan(aHeader,{|x| x[2] == 'AB7_CODPRO'})
nPosCodPrb := AScan(aHeader,{|x| x[2] == 'AB7_CODPRB'})

If !Empty(aCols_) .and. (nPosCodPro > 0) .and. (nPosCodPrb > 0)  
	For nI := 1 To Len(aCols_)
 		If lRet := (!aCols_[nI][Len(aCols_[nI])] .AND. !Empty(aCols_[nI][nPosCodPro]) .AND. !Empty(aCols_[nI][nPosCodPrb]))
			Exit
		EndIf
	Next nI
	If !lRet
		If lConfirma
			Help('',1,'NOINCAPONT',Nil,STR0094,2,0) //'Para confirmar a inclusão/alteração é necessário ter ao menos 1 item na OS. Verifique!'
		Else
			Help('',1,'NOINCAPONT',nil,STR0095,2,0) // 'Para incluir apontamentos é necessário ter ao menos 1 item na OS. Verifique!'
		EndIf
	EndIf
EndIf

If FindFunction("TECTelMets")
	TECTelMets( "apont_os", CID_METRIC )
EndIF
Return(lRet)

//-------------------------------------------------------------------
/*/{Protheus.doc} At450When()		 
Bloqueia campos conforme a situação.

@author Serviços
@since 29/03/2016
@return [LOGIC]
/*/
//-------------------------------------------------------------------
Function At450When(cCamp)
Local lRet 		:= .T.
Local cAtprPos	:= SuperGetMv("MV_ATPRPOS", , "2")
Default cCamp 	:= ""

If cAtprPos == "1"
	If cCamp == "AB6_TPORCS"
		lRet := .F.
	Elseif cCamp == "AB6_ITORCS"
		lRet := .F.
	Endif
ElseIf cAtprPos $ "2|3"
	If cCamp == "AB6_ITORCS"
		If M->AB6_TPORCS == "1"
			lRet := .F.
		Elseif Empty(M->AB6_CDORCS)
			lRet := .F.
		Endif
	Endif
Endif

Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} At450VldF3()
Validação para verificar se o código do item esta vinculado ao 
orçamento de serviços selecionado.

@author Serviços
@since 29/03/2016
@return [CARACTER]
/*/
//-------------------------------------------------------------------
Function At450VldF3()
Local lRet 		:= .T.
Local cSlc			:= ""
Local cJoin		:= ""
Local cWhere		:= ""
Local cTpOrcs		:= M->AB6_TPORCS
Local cCodOrc		:= M->AB6_CDORCS
Local cItenOrc	:= M->AB6_ITORCS
Local cNewAlias	:= GetNextAlias()
Local cFilOrcs 	:= M->AB6_FIORCS
Local cFilTFJ 	:= cFilTFJ := xFilial("TFJ",cFilOrcs)
Local cFIlTFL 	:= ""

If !Empty(cItenOrc) .AND. !Empty(cCodOrc)
	If cTpOrcs == "2" //Loc. Equipamentos. TFI
	
		cSlc  := "TFI_COD"
		
		cJoin := " INNER JOIN " + RetSQLName("TFI") + " TFI "
		cJoin += " ON TFL_FILIAL  = '" + xFilial("TFI",cFilOrcs) + "' "
		cJoin += " AND TFL_CODIGO = TFI_CODPAI "
		cJoin += " AND TFI.D_E_L_E_T_ = ' ' "

		cWhere := "	TFI_FILIAL = '" + xFilial("TFI",cFilOrcs) + "' "
    	cWhere += "	AND TFJ_FILIAL = '" + cFilTFJ + "' "
		cWhere += " AND TFJ_CODIGO = '" + cCodOrc + "'"
		cWhere += "	AND TFI_COD = '"+ cItenOrc + "'"
		cWhere += "AND TFI.D_E_L_E_T_ = ' ' "

		cSlc	:= '%' + cSlc + '%'
		cJoin	:= '%' + cJoin + '%'
		cWhere	:= '%' + cWhere + '%'
			
	Elseif cTpOrcs == "3" //Rec. Humanos. TFF
		
		cSlc  := "TFF_COD"
		
		cJoin := " INNER JOIN " + RetSQLName("TFF") + " TFF "
		cJoin += " ON TFF_FILIAL  = '" + xFilial("TFF",cFilOrcs) + "' "
		cJoin += " AND TFL_CODIGO = TFF_CODPAI "
		cJoin += " AND TFF.D_E_L_E_T_ = ' ' "
		
		cWhere := "	TFF_FILIAL = '" + xFilial("TFF",cFilOrcs) + "' "
   		cWhere += "	AND TFJ_FILIAL = '" + cFilTFJ + "' "
    	cWhere += " AND TFJ_CODIGO = '" + cCodOrc + "'"
		cWhere += "	AND TFF_COD = '"+ cItenOrc + "'"
		cWhere += "AND TFF.D_E_L_E_T_ = ' ' "

		cSlc	:= '%' + cSlc + '%'
		cJoin	:= '%' + cJoin + '%'
		cWhere	:= '%' + cWhere + '%'			
	Endif
Endif

If !Empty(cSlc)
  cFIlTFL := xFilial("TFL",cFilOrcs)
	BeginSql Alias cNewAlias
	
		SELECT %Exp:cSlc%
		  FROM %Table:TFJ% TFJ
		INNER JOIN %Table:TFL% TFL
			ON TFL_FILIAL  = %Exp:cFIlTFL%
			AND TFJ_CODIGO = TFL_CODPAI
      		AND TFL.%NotDel%
      		%Exp:cJoin%
		 WHERE %Exp:cWhere%
	
	EndSql

	DbSelectArea(cNewAlias)

	If (cNewAlias)->(Eof())
		Help(,, "At450VldF3",,STR0099,1,0,,,,,,{STR0100}) //"O Item do Orçamento de Serviço não esta vinculado com o Orçamento de Serviço" ## "Verifique se existe o vinculo." 
		lRet := .F.
	Endif
	
	(cNewAlias)->(dbCloseArea())

Endif

Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} At450F3TFJ()
Função que pertence a consulta especifica TFJ_IT.

@author Serviços
@since 29/03/2016
@return [LOGIC]
/*/
//-------------------------------------------------------------------
Function At450F3TFJ()
Local lRet		:= .T.
Local aArea	:= GetArea()
Local cTpOrcs	:= M->AB6_TPORCS
 
SaveInter()

Do Case
	Case cTpOrcs == "2"
		lRet := Conpad1( NIL,NIL,NIL,"TFI")
                              
	Case cTpOrcs == "3"
		lRet := ConPad1(Nil,Nil,Nil,"TFF")
EndCase

RestInter()
 
RestArea(aArea)

Return (lRet)


//-------------------------------------------------------------------
/*/{Protheus.doc} At450RetTFJ()
Função que retorna conteudo da consulta especifica TFJ_IT.

@author Serviços
@since 29/03/2016
@return [CARACTER]
/*/
//-------------------------------------------------------------------
Function At450RetTFJ()
Local cCodigo	:= ""
Local cTpOrcs	:= M->AB6_TPORCS

Do Case
	Case cTpOrcs == "2"
		cCodigo := TFI->TFI_COD
	Case cTpOrcs == "3"
		cCodigo := TFF->TFF_COD
EndCase

Return(cCodigo)
//-------------------------------------------------------------------
/*/{Protheus.doc} At450Filter()
Realiza o filtro da consulta padrão AA3, quando não for id unico.

@author Serviços
@since 08/09/2016
@return [LOGIC]
/*/
//-------------------------------------------------------------------
Function At450Filter()
Local aArea		  := GetArea()
Local aAreaAA3 := AA3->(GetArea())
Local aAreaSB5 := SB5->(GetArea())
Local aAreaTWI := TWI->(GetArea())
Local lRet		  := .F.

If (FunName()=="TECA450")
	DbSelectArea("SB5")
	SB5->(DbSetOrder(1))
	If SB5->(DbSeek(xFilial("SB5")+AA3->AA3_CODPRO))
		If SB5->B5_ISIDUNI == "2"
			DbSelectArea("TWI")
			TWI->(DbSetOrder(6)) //TWI_FILIAL+TWI_BASE+TWI_CODCLI+TWI_LOJA 
			If TWI->(DbSeek(xFilial("TWI")+AA3->AA3_NUMSER+M->AB6_CODCLI+M->AB6_LOJA))
				lRet := .T.
			Endif
		Endif
	Endif
Endif

RestArea(aAreaTWI)
RestArea(aAreaSB5)
RestArea(aAreaAA3)
RestArea(aArea)

Return lRet

/*/{Protheus.doc} At450Orcs()
	Função para a validação de existência dos códigos de orçamento de serviços (TFJ) e itens (TFI e TFF)
	É chamada na validação do campos AB6_CDORCS e AB6_ITORCS

@author Inovação Gestão de Serviços
@since 30/09/2016
@return Lógico, determina se o conteúdo é válido ou não
/*/
Function At450Orcs()
Local lRet := .F.
Local cFilOrcs 	:= M->AB6_FIORCS
Local cTpOrcs	:= M->AB6_TPORCS
Local cCodOrc	:= M->AB6_CDORCS
Local cItenOrc	:= M->AB6_ITORCS
Local cCpoValid := ReadVar()
Local cTabela := ""
Local aSave := GetArea()
Local aSaveTFJ := TFJ->(GetArea())
Local aSaveTFI := TFI->(GetArea())
Local aSaveTFF := TFF->(GetArea())

If "AB6_CDORCS" $ cCpoValid
	cTabela := "TFJ"
	DbSelectArea("TFJ")
	TFJ->( DbSetOrder( 1 ) )  //TFJ_FILIAL+TFJ_CODIGO
	lRet := ( TFJ->(DbSeek( xFilial("TFJ", cFilOrcs)+cCodOrc ) ) )
ElseIf "AB6_ITORCS" $ cCpoValid
	
	If cTpOrcs == "2"  // TFI
		cTabela := "TFI"
		DbSelectArea("TFI")
		TFI->( DbSetOrder( 1 ) )  //TFI_FILIAL+TFI_COD
		lRet := ( TFI->( DbSeek( xFilial("TFI", cFilOrcs)+cItenOrc ) ) )
	ElseIf cTpOrcs == "3"  // TFF
		cTabela := "TFF"
		DbSelectArea("TFF")
		TFF->( DbSetOrder( 1 ) )  //TFF_FILIAL+TFF_COD
		lRet := ( TFF->( DbSeek( xFilial("TFF", cFilOrcs)+cItenOrc ) ) )
	EndIf
	
EndIf

If !lRet
	Help(,, "AT450ORCS",,i18n("Registro não encontrado na tabela #1", {cTabela} ),1,0,,,,,,;
				{i18N("Insira um código válido e que exista na filial #1",{cFilOrcs})})
EndIf

lRet := lRet .And. At450VldF3()

RestArea(aSaveTFF)
RestArea(aSaveTFI)
RestArea(aSaveTFJ)
RestArea(aSave)

Return lRet


/*/{Protheus.doc} AT450FlPr()
	Função para exibição das bases de atendimento baseando-se no
	tipo de abrangência do contrato de manutenção (AAH_ABRANG)

@author Diego Bezerra
@since 27/05/2019
@return Lógico, determina se o registro será exibido ou não pela consulta padrão
/*/
Function AT450FlPr()

Local lRet := .F.
Local cFilAA3 := xFilial("AA3") 

If  At450RtAbr() == '2'

	lRet := (AA3->AA3_FILIAL == cFilAA3 .And. AA3->AA3_CODCLI==SA1->A1_COD);
						.Or. (AA3->AA3_FILIAL== cFilAA3 .And. Empty(AA3->AA3_CODCLI))	
Else
	lRet := (AA3->AA3_FILIAL == cFilAA3 .And. AA3->AA3_CODCLI==SA1->A1_COD .And. AA3->AA3_LOJA == SA1->A1_LOJA);
					.Or. (AA3->AA3_FILIAL == cFilAA3 .And. Empty(AA3->AA3_CODCLI) .And. Empty(AA3->AA3_LOJA))	
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} At450RtAbr()
Função que retorna conteudo da variavel estática que indica o tipo do CTT de Manutenção 

@author fabiana.silva
@since 26/11/2019
@return _cAAHAbrang [CARACTER]
/*/
//-------------------------------------------------------------------
Static Function At450RtAbr()

Return _cAAHAbrang

//-------------------------------------------------------------------
/*/{Protheus.doc} At450RtAbr()
Função que alimenta a variável estática _cAAHAbrang que indica o tipo do CTT de Manutenção 

@author fabiana.silva
@since 26/11/2019
@return _cAAHAbrang [CARACTER]
/*/
//-------------------------------------------------------------------
Static Function At450StAbr(cContr)

If !Empty(cContr)
	AAH->(DbSetOrder(1))
	AAH->(DbSeek(xFilial("AAH")+cContr))
	_cAAHAbrang := AAH->AAH_ABRANG

Else	
	_cAAHAbrang := ""
	
EndIf

Return _cAAHAbrang

//-------------------------------------------------------------------
/*/{Protheus.doc} AT450Fl2()
Função que retorna o filtro padrão

@author fabiana.silva
@since 27/11/2019
@return cFiltro
/*/
//-------------------------------------------------------------------
Function AT450Fl2()
Local cFilAA3 := xFilial("AA3") 
Local cFiltro := "@#"

If  At450RtAbr() == '2'
	
	cFiltro += '(AA3->AA3_FILIAL == "' + cFilAA3 +'"  .And. AA3->AA3_CODCLI == "' + SA1->A1_COD+ '" ) '+;
						' .Or. (AA3->AA3_FILIAL == "'  + cFilAA3 + '" .And. Empty(AA3->AA3_CODCLI))'	
Else
	cFiltro += ' (AA3->AA3_FILIAL == "' + cFilAA3 +'"  .AND.  AA3->AA3_CODCLI =="' + SA1->A1_COD+ '" .And. AA3->AA3_LOJA == "' + SA1->A1_LOJA + '" ) '+;
					' .Or. (AA3->AA3_FILIAL == "' + cFilAA3 +'"  .AND. Empty(AA3->AA3_CODCLI) .And. Empty(AA3->AA3_LOJA))'	
EndIf

Return cFiltro+"@#"

//-------------------------------------------------------------------
/*/{Protheus.doc} A450ShowCk()
Retorna as fotos utilizadas no atendimento da ordem de serviço

@author Luiz Gabriel
@since 22/10/2020
/*/
//-------------------------------------------------------------------
Function A450ShowCk()
Local aArea			:= GetArea()
Local aSaveLines	:= FWSaveRows()
Local oModel		:= FwLoadModel('TECA450A')
Local aButtons	:= {	{.F.,Nil},;			//- Copiar
							{.F.,Nil},;			//- Recortar
							{.F.,Nil},;			//- Colar
							{.F.,Nil},;			//- Calculadora
							{.F.,Nil},;			//- Spool
							{.F.,Nil},;			//- Imprimir
							{.F.,STR0106},;		//- Confirmar
							{.T.,STR0107},;		//- Fechar
							{.F.,Nil},;			//- WalkThrough
							{.F.,Nil},;			//- Ambiente
							{.F.,Nil},;			//- Mashup
							{.F.,Nil},;			//- Help
							{.F.,Nil},;			//- Formulário HTML
							{.F.,Nil}			}	//- ECM
DbSelectArea("TXM")
TXM->(DbSetOrder(1))

If TXM->(DbSeek(xFilial("TXM")+AB6->AB6_NUMOS))
	oModel:Activate()
	FWExecView( STR0117 , "TECA450A", MODEL_OPERATION_UPDATE, /*oDlg*/, {|| .T. } ,, 0, aButtons, /*bCancel*/ ) //"Fotos"
Else
	If !IsBlind()
		Aviso(STR0055,STR0105,{STR0057},1)//"Atenção"//"Não há fotos a serem exibidas"//"OK"
	EndIf
EndIf

FWRestRows( aSaveLines )
RestArea(aArea)

Return                                                      

//-------------------------------------------------------------------
/*/{Protheus.doc} A450ShowCk()
Retorna a descrição do tipo de telefone do contato
1=;2=Residencial;3=Fax comercial;4=Fax residencial;5=Celular 

@author Luiz Gabriel
@since 19/09/2023
/*/
//-------------------------------------------------------------------
Static Function At450TpFone(cTipo)
Local cTipoFone	:= ""

Do Case 
 Case cTipo == "1"
	cTipoFone	:= "Comercial"
 Case cTipo == "2"
	cTipoFone	:= "Residencial"
 Case cTipo == "3"
	cTipoFone	:= "Fax comercial"
 Case cTipo == "4"
	cTipoFone	:= "Fax residencial"
 Case cTipo == "5"
	cTipoFone	:= "Celular"
EndCase

Return cTipoFone
