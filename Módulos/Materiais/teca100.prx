#Include "TECA100.ch"
#Include "PROTHEUS.CH"
#DEFINE CADASTRO STR0001 // Amarracao Produto x Ocorrencia   

/*


Ŀ           	
Funo     TECA100   Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Cadastro de Amarracao Produto x Equipamento                
Ĵ
Sintaxe    TECA100()                                                  
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    Nenhum                                                     
Ĵ
Uso        SIGATEC                                                    
ٱ


*/

Function TECA100( )

Private aRotina := MenuDef()
							
Private cCadastro := CADASTRO 

mBrowse( 6, 1, 22, 75, "AA7" )

Return( nil )

/*/

Ŀ
Funo     MenuDef   Autor  Conrado Q. Gomes       Data  08.12.06 
Ĵ
Descrio  Definio do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA100                                                    
ٱ


/*/
Static Function MenuDef()
	//Ŀ
	// Define Array contendo as Rotinas a executar do programa      
	// ----------- Elementos contidos por dimensao ------------     
	// 1. Nome a aparecer no cabecalho                              
	// 2. Nome da Rotina associada                                  
	// 3. Usado pela rotina                                         
	// 4. Tipo de Transao a ser efetuada                          
	//    1 - Pesquisa e Posiciona em um Banco de Dados             
	//    2 - Simplesmente Mostra os Campos                         
	//    3 - Inclui registros no Bancos de Dados                   
	//    4 - Altera o registro corrente                            
	//    5 - Remove o registro corrente do Banco de Dados          
	//
	Local aRotina := aRotina := {	{ STR0002	, "AxPesqui"	,0	,1	,0	,.F.	}	,;  // "Pesquisar"
									{ STR0003	, "At100Visu"	,0	,2	,0	,.T.	}	,;  // "Visualizar"
									{ STR0004	, "At100Incl"	,0	,3	,0	,.T.	}	,;  // "Incluir"
									{ STR0005	, "At100Alte"	,0	,4	,0	,.T.	}	,;  // "Alterar"
									{ STR0006	, "At100Excl"	,0	,2	,0	,.T.	}	}   // "Excluir"
Return(aRotina)


/*


Ŀ
Funo    At100Incl  Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Inclusao de Amarracao Produto x Equipamento                
Ĵ
Sintaxe    At100Incl( ExpC1, ExpN1, ExpN2 )                           
Ĵ
Parametros ExpC1->Alias / ExpN1 -> Num. Registro / ExpN2 -> Opcao     
Ĵ
Retorno    Nil                                                        
Ĵ
Uso                                                                   
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
20/12/2006 Conrado Q.    Bops 115736: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


*/

Function At100Incl( cAlias, nReg, nOpcx )

Local aPos		:= { 15, 1, 70, 315 }
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosObj   := {}
Local nSaveSX8	:= GetSX8Len()
Local nLoop     := 0
Local bCampo	:= {||}
Local nOpca		:= Nil
Local oDlg		:= Nil
Local oGet		:= Nil

Private aTela	:= {}
Private aGets	:= {}
Private aHeader	:= {}
Private aCols	:= {}

Inclui := .T.

//Ŀ
// Verifica se existe algum dado no arquivo                     
//
dbSelectArea( cAlias )
dbSetOrder( 1 )

//Ŀ
// Monta a entrada de dados do arquivo                          
//
bCampo   := { |nCPO| FieldName(nCPO) }

//Ŀ
// Monta as variaveis da Enchoice 
//
For nLoop := 1 To FCount()
	M->&( Eval( bCampo, nLoop ) ) := CriaVar( Eval( bCampo, nLoop ) )
Next nLoop

//Ŀ
//Montagem aHeader, aCols
//
If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpcx			,"AAC"			,1				,/*cSeek*/		,;
					/*{||cWhile}*/	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,.T.			,;
					/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/)
Endif

aCols[1][GdFieldPos("AAC_ITEM")] := "01"

nOpca       := 0

aSize := MsAdvSize()

aObjects := {}
AAdd( aObjects, { 100,100,.T.,.T. } )
AAdd( aObjects, { 100,100,.T.,.T. } )

aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3}

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE MSDIALOG oDlg TITLE cCadastro From ;
			aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME // Amarracao Produto x Ocorrencia   
	oDlg:Cargo := { Len( aCols ) }
	nLin := oDlg:Cargo[1]
	EnChoice( cAlias, nReg, nOpcx, , , , , aPosObj[1], , 3 )
	oGet := MSGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"At100LOK","At100TOK", "+AAC_ITEM",.T.)

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg,{ || nOpcA:=If(Obrigatorio(aGets,aTela).And.oGet:TudoOk(),1,0),If(nOpcA==1,oDlg:End(),Nil)},{||oDlg:End()})

If nOpca == 1
	//Ŀ
	// Efetua a gravacao              
	//
	At100Grava(1,nSaveSX8)
EndIf


Return( nOpca )

/*


Ŀ
Funo     At100Visu Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Visualizacao da Amarracao Produto x Equipamento            
Ĵ
Sintaxe    At100Visu( cAlias, nReg, nOpcx                             
Ĵ
Parametros ExpC1->Alias / ExpN1 -> Num. Registro / ExpN2 -> Opcao     
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        SIGATEC                                                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
20/12/2006 Conrado Q.    Bops 115736: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


*/

Function At100Visu( cAlias, nReg, nOpcx )

Local aPos		:= { 15, 1, 70, 315 }
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosObj   := {}
Local bCampo	:= {||}
Local nOpca		:= 0
Local nLoop		:= 0
Local oDlg		:= Nil
Local oGet		:= Nil
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader

Private aTela	:= {}
Private aGets	:= {}
Private aHeader	:= {}
Private aCols	:= {}

Inclui := .F.
Altera := .F.

//Ŀ
// Verifica se existe algum dado no arquivo                     
//

dbSelectArea( cAlias )
dbSetOrder( 1 )

//Ŀ
// Monta a entrada de dados do arquivo                          
//

bCampo   := { |nCPO| FieldName(nCPO) }

//Ŀ
// Monta as variaveis da Enchoice 
//
For nLoop := 1 To FCount()
	M->&( Eval( bCampo, nLoop ) ) := FieldGet( nLoop )
Next nLoop

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial( "AAC" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE
cWhile	:= "AAC->AAC_FILIAL + AAC->AAC_CODPRO + AAC->AAC_CODPRB + AAC->AAC_GRPATE"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpcx			,"AAC"			,1				,cSeek			,;
					{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,; 
					/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/		,;
					/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/)
Endif

//Ŀ
// Desenha a janela               
//

aSize := MsAdvSize()

aObjects := {}
AAdd( aObjects, { 100,100,.T.,.T. } )
AAdd( aObjects, { 100,100,.T.,.T. } )

aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3}

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE MSDIALOG oDlg TITLE cCadastro From ;
			aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME // Amarracao Produto x Ocorrencia   

	EnChoice( cAlias, nReg, nOpcx, , , , , aPosObj[1], , 3 )
	
	oGet := MSGetDados():New( aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4], nOpcx,"At100LOK","At100TOK", , .F. )
	
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg,{ || nOpca := 1,oDlg:End() }, {|| nOpca := 0, oDlg:End()})

Return( nOpca )


/*


Ŀ
Funo     At100Alte Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Alteracao Amarracao Produto x Equipamento                  
Ĵ
Sintaxe    At100Alte( ExpC1, ExpN1, ExpN2 )                           
Ĵ
Parametros ExpC1->Alias / ExpN1 -> Num. Registro / ExpN2 -> Opcao     
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        SIGATEC                                                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
20/12/2006 Conrado Q.    Bops 115736: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


*/

Function At100Alte( cAlias, nReg, nOpcx )

Local aPos		:= { 15, 1, 70, 315 }
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosObj   := {}
Local nSaveSX8	:= GetSX8Len()
Local bCampo	:= {||}
Local nOpca		:= 0  
Local nLoop		:= 0
Local oDlg		:= Nil
Local oGet 		:= Nil
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader

Private aTela	:= {}
Private aGets	:= {}
Private aHeader	:= {}
Private aCols	:= {}
             
Inclui := .F.
Altera := .T.

//Ŀ
// Verifica se existe algum dado no arquivo     \                
//

dbSelectArea( cAlias )
dbSetOrder( 1 )

//Ŀ
// Monta a entrada de dados do arquivo                          
//

bCampo   := { |nCPO| FieldName(nCPO) }

//Ŀ
// Monta as variaveis da Enchoice 
//

For nLoop := 1 To FCount()
	M->&( Eval( bCampo, nLoop ) ) := FieldGet( nLoop )
Next nLoop

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial( "AAC" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE
cWhile	:= "AAC->AAC_FILIAL + AAC->AAC_CODPRO + AAC->AAC_CODPRB + AAC->AAC_GRPATE"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpcx			,"AAC"		   	,1				,cSeek				,;
	  				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/		,; 
					/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/			,;
					/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/	)
Endif

If Empty(aCols[1][GdFieldPos("AAC_ITEM")])
	aCols[1][GdFieldPos("AAC_ITEM")] := "01"
Endif

nOpca       := 0

aSize := MsAdvSize()

aObjects := {}
AAdd( aObjects, { 100,100,.T.,.T. } )
AAdd( aObjects, { 100,100,.T.,.T. } )

aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3}

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE MSDIALOG oDlg TITLE cCadastro From ;
			aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME // Amarracao Produto x Ocorrencia   
	EnChoice( cAlias, nReg, nOpcx, , , , , aPosObj[1], , 3 )
	oGet := MSGetDados():New( aPosObj[2,1], aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"At100LOK","At100TOK","+AAC_ITEM", .T.;
		, , , , , , , , "At100G1DOK" )
ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg,{ || nOpcA:=If(Obrigatorio(aGets,aTela).And.oGet:TudoOk(),1,0),If(nOpcA==1,oDlg:End(),Nil) }, { || oDlg:End() } )

If nOpca == 1
	//Ŀ
	// Faz a gravacao dos dados       
	//
	At100Grava(2,nSaveSX8)
EndIf

Return( nOpca )

/*


Ŀ
Funo     At100Excl Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Exclusao da Amarracao                                      
Ĵ
Sintaxe    At100Excl( ExpC1, ExpN1, ExpN2 )                           
Ĵ
Parametros ExpC1->Alias / ExpN1 -> Num. Registro / ExpN2 -> Opcao     
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        SIGATEC                                                    
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
20/12/2006 Conrado Q.    Bops 115736: Montagem do aCols e aHeader    
                         atravs da rotina FillGetDados.             
ٱ


*/

Function At100Excl( cAlias, nReg, nOpcx )

Local aPos		:= { 15, 1, 70, 315 }
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosObj   := {}
Local bCampo    := {||}
Local nLoop 	:= 0
Local oDlg		:= Nil
Local oGet		:= Nil

Private aTela	:= {}
Private aGets	:= {}
Private aHeader	:= {}
Private aCols	:= {}

Inclui := .F.
Altera := .F.

//Ŀ
// Verifica se existe algum dado no arquivo                     
//

dbSelectArea( cAlias )
dbSetOrder( 1 )

//Ŀ
// Monta a entrada de dados do arquivo                          
//

bCampo   := { |nCPO| FieldName(nCPO) }

//Ŀ
// Monta as variaveis da Enchoice 
//

For nLoop := 1 To FCount()
	M->&( Eval( bCampo, nLoop ) ) := FieldGet( nLoop )
Next nLoop

//Ŀ
//Montagem aHeader, aCols
//
cSeek	:= xFilial( "AAC" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE
cWhile	:= "AAC->AAC_FILIAL + AAC->AAC_CODPRO + AAC->AAC_CODPRB + AAC->AAC_GRPATE"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpcx			,"AAC"		   	,1				,cSeek				,;
	  				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/		,; 
					/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/			,;
					/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/	)
Endif

//Ŀ
// Desenha a janela               
//

nOpca := 0

aSize := MsAdvSize()

aObjects := {}
AAdd( aObjects, { 100,100,.T.,.T. } )
AAdd( aObjects, { 100,100,.T.,.T. } )

aInfo := { aSize[1],aSize[2],aSize[3],aSize[4],3,3}

aPosObj := MsObjSize( aInfo, aObjects ) 

DEFINE MSDIALOG oDlg TITLE cCadastro From ;
			aSize[7],0 To aSize[6],aSize[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME // Amarracao Produto x Ocorrencia   

	EnChoice( cAlias, nReg, nOpcx, , , , , aPosObj[1], , 3 )

	oGet := MSGetDados():New( aPosObj[2,1], aPosObj[2,2],aPosObj[2,3],aPosObj[2,4], nOpcx,"At100LOK","At100TOK", , .F. )

ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar( oDlg,{ || nOpca := 1,;
		oDlg:End()}, {||oDlg:End()})

//Ŀ
// Se confirmou a exclusao        
//
If nOpca == 1
	At100Grava(3) 
EndIf

Return( nOpca )


/*


Ŀ
Funo     At100LOK  Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  LinOK da Getdados                                          
Ĵ
Sintaxe    ExpL1 := At100LOK()                                        
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        TECA100                                                    
ٱ


*/

Function At100LOK( oDlg )

Local nCntFor  := 0          
Local nPosHabil:= GDFieldPos( "AAC_HABIL" ) 
Local nLoop    := 0

Local lRetorno := .T.
//Ŀ
//Verifica os campos obrigatrios
//

If ( !GDDeleted() ) .And. Len( aCols ) > 1 
	For nCntFor := 1 To Len(aHeader)
		If ( AllTrim(aHeader[nCntFor][2]) == "AAC_HABIL" .Or.;
			 AllTrim(aHeader[nCntFor][2]) == "AAC_CODPRB" ) 
 			If ( Empty(aCols[n][nCntFor]) )
				Help(" ",1,"OBRIGAT")
				lRetorno := .F.
				Exit
			EndIf				 
		EndIf
	Next nCntfor   
	
	//Ŀ
	//Verifica ocorrencias duplicadas 
	//
	
	cHabil := GDFieldGet( "AAC_HABIL" ) 
	
	For nLoop := 1 To Len( aCols ) 		
		If n <> nLoop .And. !GDDeleted( nLoop ) 
			If cHabil == aCols[ nLoop, nPosHabil ] 	
				Help( " ", 1, "ITDUPLIC" ) 	                            
				lRetorno := .F. 	                 
			EndIf 	                 
		EndIf 
	Next nLoop 	
	
EndIf

Return( lRetorno )

/*


Ŀ
Funo     At100TOK  Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  TudoOk da Getdados                                         
Ĵ
Sintaxe    ExpL1 := At100TOK()                                        
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        TECA100                                                    
ٱ


*/

Function At100TOK( )

LOCAL lRet := .T.     

//Ŀ
// Codigo de produto ou ocorrencia devem estar preenchidos 
//
If Empty( M->AA7_CODPRO ) .And. Empty( M->AA7_CODPRB ) 
	Help( " ", 1, "OBRIGAT" ) 
	lRet := .F. 	
EndIf 
                 
If Inclui                                                        
	If lRet
		//Ŀ
		// Verifica quanto a chave duplicada                       
		//
		AA7->( dbSetOrder( 1 ) ) 
		If AA7->( dbSeek( xFilial( "AA7" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE ) ) 
			Help( " ", 1, "JAGRAVADO" ) 
			lRet := .F. 
		EndIf 		
	EndIf 
EndIf 
	
Return( lRet )


/*


Ŀ
Funo    At100Grava Autor  Sergio Silveira        Data 28/07/2000
Ĵ
Descrio  Faz a gravacao da amarracao produto x ocorrencia           
Ĵ
Sintaxe    At100Grava                                                 
Ĵ
Parametros nTipo                                                      
Ĵ
Retorno    Nenhum                                                     
Ĵ
Uso        TECA100                                                    
ٱ


*/

Function At100Grava( nTipo, nSaveSX8 )

Local cSeekAA7  := "" 
Local cSeekAAC  := ""
Local cCampoAh  := ""
Local lFoundAAC := .F.
Local lGravou	 := .F.
Local nLoop     := 0 
Local nLoop2    := 0 
Local nPosItem  := GDFieldPos( "AAC_ITEM"  ) 
Local nPosHabil := GDFieldPos( "AAC_HABIL" ) 
Local nPosNivel := GDFieldPos( "AAC_NIVEL" ) 
Local nPosArq   := 0 

Default nSaveSX8:= 0

Begin Transaction

Do Case
Case nTipo <> 3 

	cSeekAA7 := xFilial( "AA7" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE

	//Ŀ
	// Efetua a gravacao do cabecalho               
	//
	AA7->( dbSetOrder( 1 ) )
	
	If AA7->( DbSeek( cSeekAA7 ) )
		RecLock( "AA7", .F. )
	Else
		RecLock( "AA7", .T. )
		AA7->AA7_FILIAL  := xFilial( "AA7" )
		AA7->AA7_CODPRB  := M->AA7_CODPRB
		AA7->AA7_CODPRO  := M->AA7_CODPRO		
		AA7->AA7_GRPATE  := M->AA7_GRPATE
	EndIf
	
	dbSelectArea( "AA7" )
	
	//Ŀ
	// Grava os demais campos inclusive especificos 
	//
	
	For nLoop := 1 To FCount()
		cCampo := FieldName( nLoop )
		If !( cCampo $ "AA7_FILIAL/AA7_CODPRO/AA7_CODPRB/AA7_GRPATE" )
			FieldPut( nLoop, M->&cCampo )
		EndIf
	Next nLoop
	
	AA7->( MsUnlock() )
	AA7->( FKCommit() ) 

	//Ŀ
	// Gravacao dos itens             
	//
	
	For nLoop := 1 To Len( aCols )
		If !Empty( aCols[ nLoop, nPosHabil ] ) .And. !Empty( aCols[ nLoop, nPosNivel ] )
			cSeekAAC := xFilial( "AAC" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE + aCols[ nLoop, nPosItem ]
		
			AAC->( dbSetOrder( 1 ) )
		
			lFoundAAC := AAC->( dbSeek( cSeekAAC ) )
		
			If	aCols[ nLoop, Len( aHeader ) + 1 ] 
		
				//Ŀ
				// Se o registro estiver excluido 
				//
				If lFoundAAC
		
					RecLock( "AAC", .F., .T. )
					AAC->( dbDelete() )
					AAC->( MsUnLock() )
		
				EndIf
		
			Else
		
				//Ŀ
				// Se nao estiver excluido        
				//
		        lGravou := .T.
		        
				If lFoundAAC
					RecLock( "AAC", .F. )
				Else
					//Ŀ
					// Grava campos chave             
					//
					RecLock( "AAC", .T. )
		
					AAC->AAC_FILIAL  := xFilial( "AAC" )
					AAC->AAC_CODPRO  := M->AA7_CODPRO
					AAC->AAC_CODPRB  := M->AA7_CODPRB 
					AAC->AAC_GRPATE  := M->AA7_GRPATE
					AAC->AAC_ITEM    := aCols[ nLoop, nPosItem  ]
				EndIf
		
				//Ŀ
				// Faz a gravacao dos demais campos ( inclusive especificos ) 
				//
		
				dbSelectArea( "AAC" )
		
				For nLoop2 := 1 To Len( aHeader )
		
					cCampoAh  := AllTrim( aHeader[ nLoop2, 2 ] )
		
					If !( cCampoAh $ "AAC_CODPRO/AAC_CODPRB/AAC_ITEM/AAC_FILIAL/AAC_GRPATE" )
		
						nPosArq := FieldPos( cCampoAh )
		
						If !Empty( nPosArq )
							FieldPut( nPosArq, aCols[ nLoop, nLoop2 ] )
						EndIf
		
					EndIf
		
				Next nLoop2
		
				AAC->( MsUnLock() )
		
			EndIf
		EndIf 			
	Next nLoop
	
	EvalTrigger()
	While ( GetSX8Len() > nSaveSx8 )
		ConfirmSx8()
	EndDo   
	
Otherwise 	
    
	cSeekAAC := xFilial( "AAC" ) + M->AA7_CODPRO + M->AA7_CODPRB + M->AA7_GRPATE

	AAC->( dbSetOrder( 1 ) )
	//Ŀ
	// Exclui todos os itens          
	//
	If AAC->( dbSeek( cSeekAAC ) )
		AAC->( dbEval( { || RecLock( "AAC", .F., .T. ),;
			AAC->( dbDelete() ), AAC->( MsUnLock() ) }, ,{ || cSeekAAC;
			== AAC->AAC_FILIAL + AAC->AAC_CODPRO + AAC->AAC_CODPRB + AAC->AAC_GRPATE }, , ,.T. ) )
	EndIf   
	
	AAC->( FKCommit() ) 

	//Ŀ
	// Exclui os cabecalho da Amarracao 
	//
	RecLock( "AA7", .F., .T. )
	AA7->( dbDelete() )
	AA7->( MsUnLock() )
		
	EvalTrigger() 
	
EndCase	

End Transaction 
	
Return( Nil )

/*


Ŀ
Funo    At100G1DOK Autor  Sergio Silveira        Data  16/09/99 
Ĵ
Descrio  Validacao de exclusao da GetDados                          
Ĵ
Sintaxe    ExpL1 := At100G1DOK()                                      
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        SIGATEC                                                    
ٱ


*/

Function At100G1DOK( )

LOCAL lRet := .T. 

Return( lRet )

/*


Ŀ
Funo    At100VlHr  Autor  Sergio Silveira        Data 31/08/2001
Ĵ
Descrio  Validacao da hora digitada                                 
Ĵ
Sintaxe    ExpL1 := At100VlHr()                                       
Ĵ
Parametros                                                            
Ĵ
Retorno    ExpL1 -> Validacao OK                                      
Ĵ
Uso        TECA100                                                    
ٱ


*/

Function At100VlHr() 

Local cHora    := &( ReadVar() ) 
Local lRetorno := .F.

lRetorno := AtVldHora( cHora, .T. )

Return( lRetorno )