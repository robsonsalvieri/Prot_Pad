#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA400.CH"

STATIC __oTempTable  := NIL
STATIC __aHeadTrbADA := NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ FATA400  ³ Autor ³ Eduardo Riera         ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Contrato de Parceria ( Venda )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FATA400()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ CRM - Venda                                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.  ³21/11/05³ 88450³- Criado Ponto de Entrada FT400FIL para   ³±±
±±³            ³        ³      ³  permitir filtro no Browse.    		  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FATA400()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define variaveis                                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aCores := {{"ADA_STATUS=='A'",'ENABLE' },;		//Contrato em Aberto
	{ "ADA_STATUS=='B'",'BR_AZUL'            },;	   	//Contrato Aprovado
	{ "ADA_STATUS=='C'",'BR_AMARELO'         },;		//Contrato Parcialmente Entregue
	{ "ADA_STATUS=='E'",'BR_CINZA'           },;		//Contrato encerrado Manualmente
	{ "ADA_STATUS=='D'",'DISABLE'            }} 		//Contrato Totalmente Entregue

LOCAL cFiltraADA		//Expressao de filtro da mBrowse
LOCAL aIndexADA := {}	//Array com indices da tabela ADA

// Variaveis utilizadas em novos helps 
Local aHlpPor1  := {"Nao e possivel alterar contrato    ","parcialmente entregue!       "}
Local aHlpIng1  := {"No es possivel alterar contrato    ","parcialmente entregue.       "}
Local aHlpEsp1  := {"He is not possivel to modify       ","contract partially delivers. "}

Local aHlpPor2  := {"Somente contrato parcialmente      ","entregue pode ser encerrado  "}
Local aHlpIng2  := {"Contract partially only delivers   ","can be locked up.            "}
Local aHlpEsp2  := {"Solamente contrato parcialmente    ","entregue puede ser concluido."}

Local aHlpPor3  := {"Nao e possivel alterar contrato    ","encerrado.                   "}
Local aHlpIng3  := {"He is not possivel to modify locked","up contract.                 "}
Local aHlpEsp3  := {"No es possivel alterar contrato    ","concluido.                   "}

Local aHlpPor4  := {"O valor total informado não confere ","com o valor calculado.      "}
Local aHlpIng4  := aHlpPor4
Local aHlpEsp4  := aHlpPor4
				 
Local aHlpPor5  := {"Verifique o valor total do contrato."}
Local aHlpIng5  := aHlpPor5
Local aHlpEsp5  := aHlpPor5

//Ajuste de Novos Helps
PutHelp("PFT400NAOAL",aHlpPor1,aHlpIng1,aHlpEsp1,.F.)
PutHelp("PFT400NAOEN",aHlpPor2,aHlpIng2,aHlpEsp2,.F.)
PutHelp("PFT400NAOAE",aHlpPor3,aHlpIng3,aHlpEsp3,.F.)
PutHelp("PFT400TOTAL",aHlpPor4,aHlpIng4,aHlpEsp4,.F.)
PutHelp("SFT400TOTAL",aHlpPor5,aHlpIng5,aHlpEsp5,.F.)

PRIVATE bFiltraBrw:= {|| Nil}	//conteudo do codeblock
Private cCadastro	:= STR0007 //"Contrato de Parceria - Venda"
Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a existencia de Filtros na mBrowse                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ADA->(dbSetOrder(1))
If (ExistBlock("FT400FIL"))
	cFiltraADA := ExecBlock("FT400FIL",.f.,.f.)
	bFiltraBrw := {|| FilBrowse("ADA",@aIndexADA,@cFiltraADA) }
	Eval(bFiltraBrw)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("FT400COR")
	aCores := ExecBlock("FT400COR",.F.,.F.,aCores)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
mBrowse( 6,1,22,75,"ADA",,,,,,aCores)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Finaliza o uso da funcao FilBrowse e retorna os indices padroes.       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
EndFilBrw("ADA",aIndexADA)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve os indices padroes do SIGA.                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ADA")
dbSetOrder(1)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Inclu³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de Tratamento para Inclusao                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do arquivo                                     ³±±
±±³          ³ExpN2: Registro do Arquivo                                  ³±±
±±³          ³ExpN3: Opcao da MBrowse                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Inclu(cAlias,nReg,nOpcx)

Local nX        := 0
Local nUsado    := 0
Local nGetLin   := 0
Local nOpcA     := 0   
Local nSaveSx8  := GetSx8Len()
//Local cCadastro := STR0007 //"Contrato de Parceria - Venda"
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosGet   := {}
Local aPosObj   := {}
Local oDlg
Local oGetD
Local oSAY1
Local oSAY2
Local oSAY3

PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE aHeader := {}
PRIVATE aCols   := {}
PRIVATE N       := 1
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os dados da Enchoice                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RegToMemory( "ADA", .T., .T. )
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do aheader                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
MsSeek("ADB")
While !Eof() .And. SX3->X3_ARQUIVO=="ADB"
	If ( X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL )
		nUsado++
		Aadd(aHeader,{ AllTrim(X3Titulo()),;
			SX3->X3_CAMPO,;
			SX3->X3_PICTURE ,;
			SX3->X3_TAMANHO ,;
			SX3->X3_DECIMAL ,;
			SX3->X3_VALID	,;
			SX3->X3_USADO	,;
			SX3->X3_TIPO	,;
			SX3->X3_ARQUIVO ,;
			SX3->X3_CONTEXT } )
	EndIf
	dbSelectArea("SX3")
	dbSkip()
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do acols                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aadd(aCOLS,Array(nUsado+1))
For nX := 1 To nUsado
	aCols[1][nX] := CriaVar(aHeader[nX][2])
	If ( AllTrim(aHeader[nX][2]) == "ADB_ITEM" )
		aCols[1][nX] := "01"
	EndIf
Next nX
aCOLS[1][nUsado+1] := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calculo do tamanho dos objetos                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()
aObjects := {}
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100, 015, .t., .f. } )	
aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )
aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{160,200,240,265}} )
nGetLin := aPosObj[3,1]

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
EnChoice( "ADA", nReg, nOpcX, , , , , aPosObj[1],,3)
@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(STR0016)				SIZE 020,09 OF oDlg	PIXEL //"Total :"
@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR 0 PICTURE TM(0,16,2)		SIZE 040,09 OF oDlg PIXEL
@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0017)				SIZE 020,09 OF oDlg PIXEL //"Desc. :"
@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,16,2)		SIZE 040,09 OF oDlg	PIXEL	
@ nGetLin+10,aPosGet[1,3]  SAY OemToAnsi("=")					SIZE 020,09 OF oDlg PIXEL
@ nGetLin+10,aPosGet[1,4]  SAY oSAY3 VAR 0						SIZE 040,09 PICTURE TM(0,16,2) OF oDlg PIXEL
oDlg:Cargo	:= {|n1,n2,n3| oSay1:SetText(n1),oSay2:SetText(n2),oSay3:SetText(n3) }

SetKey(VK_F4,{|| MaViewSB2(GdFieldGet("ADB_CODPRO",n))})
oGetD:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcX,"Ft400LinOk","Ft400TudOk","+ADB_ITEM",.T.,,1,,300)
PRIVATE oGetDad := oGetD
Ft400Rodap(oGetD)
ACTIVATE MSDIALOG oDlg ON INIT Ft400Bar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),If(!Obrigatorio(aGets,aTela),nOpcA:=0,oDlg:End()),nOpcA:=0)},{||oDlg:End()},nOpcX)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)
If nOpcA==1
	Begin Transaction
		If Ft400Grava(1)
			EvalTrigger()
			While (GetSx8Len() > nSaveSx8)
				ConfirmSx8()
			EndDo
		Else
			While (GetSx8Len() > nSaveSx8)
				RollBackSx8()
			EndDo
		EndIf
	End Transaction
Else
	If ExistBlock("FT400CLOSE")
		ExecBlock("FT400CLOSE",.F.,.F.,nOpcx)
	EndIf
	While (GetSx8Len() > nSaveSx8)
		RollBackSx8()
	EndDo
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Alter³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de Tratamento para Visualizacao,Alteracao e Exclusao ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do arquivo                                     ³±±
±±³          ³ExpN2: Registro do Arquivo                                  ³±±
±±³          ³ExpN3: Opcao da MBrowse                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Alter(cAlias,nReg,nOpcx)

Local aArea     := GetArea()
Local aStruADB  := {}
Local aSize     := {}
Local aObjects  := {}
Local aInfo     := {}
Local aPosGet   := {}
Local aPosObj   := {}
Local aRegADB   := {}
Local nX        := 0
Local nUsado    := 0
Local nGetLin   := 0
Local nOpcA     := 0
Local nSaveSx8  := GetSx8Len()
//Local cCadastro := STR0007 //"Contrato de Parceria - Venda"
Local cQuery    := ""
Local cAliasADB := "ADB"
Local cAliasADA := "ADA"
Local cStatus	:= ""
Local lQuery    := .F.
Local lAlter    := .F. 
Local lCopia    := (aRotina[nOpcX][4]==6 .And. nOpcX == 6)
Local lVisual   := aRotina[nOpcX][4]==2
Local lEncerra	  := (aRotina[nOpcX][4]==6 .And. nOpcX == 7)
Local lExcl	  := (aRotina[nOpcX][4]==5 .And. nOpcX == 5)  
Local lContinua := .T.                              
Local lCanDel   := (SuperGetMv("MV_DELCTR",.F.,"2") == "1") //Indica se sera validada a exclusao de um contrato de parceria caso tenha sido gerado pedido de venda 1=Sim 2=Não
Local lDeleta   := .T.
Local lEncerOk	:= .F.
Local oDlg
Local oGetD
Local oSAY1
Local oSAY2
Local oSAY3
Local lParcial 	:= .F.
Local lNewCont 	:= .F.
Local lFt400VCP := ExistBlock("FT400VCP")

PRIVATE aTELA[0][0]
PRIVATE aGETS[0]
PRIVATE aHeader := {}
PRIVATE aCols   := {}
PRIVATE N       := 1


If lEncerra .And. (cAliasADA)->ADA_STATUS <> "C"  //Verifica se o status do contrato permite o encerramento.
	Help(" ",1,"FT400NAOEN")              //Somente contratos com o status "C" Parcialmente Entregue
	lContinua := .F.                      
EndIf

If !lVisual
	If !SoftLock("ADA")
		lContinua := .F.
	EndIf
EndIf

If lCanDel .And. nOpcx == 5

	cAliasSC6 := "SC6QRY"		

	cQuery := "SELECT COUNT(*) CONTRATO FROM "
	cQuery += RetSqlName("SC6") + " SC6 "
	cQuery += "WHERE "
	cQuery += "SC6.C6_FILIAL  = '" + xFilial("SC6") + "' AND "
	cQuery += "SC6.C6_CONTRAT = '" + ADA->ADA_NUMCTR + "' AND "
	cQuery += "SC6.D_E_L_E_T_ = ' ' "		
	
	cQuery := ChangeQuery(cQuery)
	
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC6,.T.,.T.)

	If 	(cAliasSC6)->CONTRATO > 0
		Help(" ",1,"NODELETA")
		lContinua := .F.
		lDeleta   := .F.
	Else
		If Existblock("FT400DEL")
			If !Execblock("FT400DEL",.F.,.F.)
				Help(" ",1,"NODELETA")
				lContinua := .F.
				lDeleta   := .F.					
			EndIf
		EndIf
	Endif

	dbSelectArea(cAliasSC6)
	dbCloseArea()
	dbSelectArea("SC6")
		
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Encerramento do Contrato de Parceria                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ADB->(DbSetOrder(1))
ADB->(MsSeek(xFilial("ADB")+ADA->ADA_NUMCTR) )
While !Eof() .And. ADB->ADB_NUMCTR == ADA->ADA_NUMCTR
	If (cAliasADB)->ADB_QTDEMP > 0 .And. ((cAliasADB)->ADB_QTDEMP <= (cAliasADB)->ADB_QUANT)
		lParcial := .T.
		Exit
	EndIf
	ADB->(dbSkip())
EndDo

If lEncerra .And. ADA->ADA_STATUS == "C"  .And. lParcial
	lEncerOk := .T.
	If MsgYesNo(STR0034+" "+(cAliasADA)->ADA_NUMCTR+"."+" ";
				+STR0035+" ";
				+STR0036)
		lCopia := .T.
		lContinua := .T.
		lNewCont  := .T.
	Else
		If MsgYesNo(STR0037+" "+(cAliasADA)->ADA_NUMCTR+" "+STR0038)
			lContinua := .F.
		Else 
			lContinua := .F.
			lEncerOk := .F.			
		EndIf
	EndIf
EndIf
				

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os dados da Enchoice                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RegToMemory( "ADA", .F., .T. )
If lCopia
	M->ADA_NUMCTR := CriaVar("ADA_NUMCTR",.T.)
	M->ADA_EMISSA := dDataBase
	M->ADA_NOMCLI := Posicione("SA1",1,xFilial("SA1")+M->ADA_CODCLI+M->ADA_LOJCLI,"A1_NOME")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do aheader                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SX3")
dbSetOrder(1)
MsSeek("ADB")
While !Eof() .And. SX3->X3_ARQUIVO=="ADB"
	If ( X3USO(SX3->X3_USADO) .And. cNivel >= SX3->X3_NIVEL )
		nUsado++
		Aadd(aHeader,{ AllTrim(X3Titulo()),;
			SX3->X3_CAMPO,;
			SX3->X3_PICTURE ,;
			SX3->X3_TAMANHO ,;
			SX3->X3_DECIMAL ,;
			SX3->X3_VALID	,;
			SX3->X3_USADO	,;
			SX3->X3_TIPO	,;
			SX3->X3_ARQUIVO ,;
			SX3->X3_CONTEXT } )
	EndIf
	dbSelectArea("SX3")
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem do acols                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ADB")
dbSetOrder(1)

If aScan(aHeader,{|x| x[8] == "M"})==0
	lQuery    := .T.
	aStruADB  := ADB->(dbStruct())
	cAliasADB := "ADB"

	cQuery := "SELECT ADB.*,ADB.R_E_C_N_O_ ADBRECNO "
	cQuery += "FROM "+RetSqlName("ADB")+" ADB "
	cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
	cQuery += "ADB.ADB_NUMCTR='"+ADA->ADA_NUMCTR+"' AND "
	cQuery += "ADB.D_E_L_E_T_=' ' "
	cQuery += "ORDER BY "+SqlOrder(ADB->(IndexKey()))

	cQuery := ChangeQuery(cQuery)

	dbSelectArea("ADB")
	dbCloseArea()

	dbUseArea(.T.,cAliasADB,TcGenQry(,,cQuery),cAliasADB,.T.,.T.)

	For nX := 1 To Len(aStruADB)
		If aStruADB[nX][2]<>"C"
			TcSetField(cAliasADB,aStruADB[nX][1],aStruADB[nX][2],aStruADB[nX][3],aStruADB[nX][4])
		EndIf
	Next nX
Else
	MsSeek(xFilial("ADB")+ADA->ADA_NUMCTR)
EndIf

While !Eof() .And. (cAliasADB)->ADB_FILIAL == xFilial("ADB") .And.;
	(cAliasADB)->ADB_NUMCTR == ADA->ADA_NUMCTR
	If (!lEncerra) .Or. ( lEncerra .And. (cAliasADB)->(FieldGet(FieldPos("ADB_QUANT"))) - (cAliasADB)->(FieldGet(FieldPos("ADB_QTDEMP"))) > 0)
		aadd(aCOLS,Array(nUsado+1))
		For nX := 1 To nUsado
			If aHeader[nX][10]=="V"
				aCols[Len(aCols)][nX] := CriaVar(aHeader[nX][2])
			Else      
				If lEncerra .And. AllTrim(aHeader[nX][2]) == "ADB_QUANT"
					aCols[Len(aCols)][GdFieldPos("ADB_QUANT")] := (cAliasADB)->(FieldGet(FieldPos(aHeader[nX][2]))) - (cAliasADB)->(FieldGet(FieldPos("ADB_QTDEMP")))
				ElseIf lEncerra .And. AllTrim(aHeader[nX][2]) == "ADB_ITEM"	// Renumera itens caso algum item ja tenha sido entregue totalmente e nao e apresentado no aCols.
					aCols[Len(aCols)][nX] := StrZero(Len(aCols),TamSx3("ADB_ITEM")[1])
				Else
					aCols[Len(aCols)][nX] := (cAliasADB)->(FieldGet(FieldPos(aHeader[nX][2])))
				EndIf
			EndIf
		Next nX 
		If lEncerra
			aCols[Len(aCols)][GdFieldPos("ADB_TOTAL")] := (cAliasADB)->(FieldGet(FieldPos("ADB_PRCVEN"))) * ((cAliasADB)->(FieldGet(FieldPos("ADB_QUANT"))) - (cAliasADB)->(FieldGet(FieldPos("ADB_QTDEMP"))))	
		EndIf
	
		aCOLS[Len(aCols)][nUsado+1] := .F.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calculo do tamanho dos objetos                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lVisual .Or. lCopia .Or. ((cAliasADB)->ADB_QTDEMP < (cAliasADB)->ADB_QUANT .And. Empty((cAliasADB)->ADB_PEDCOB))
			lAlter := .T.
		EndIf
		aadd(aRegADB,IIf(lQuery,(cAliasADB)->ADBRECNO,ADB->(RecNo())))
	EndIf
	dbSelectArea(cAliasADB)
	dbSkip()
EndDo

If lFt400VCP
	Execblock("FT400VCP",.F.,.F.,{nOpcx,lNewCont})
Endif

If lQuery
	dbSelectArea(cAliasADB)
	dbCloseArea()
	ChkFile("ADB",.F.)
EndIf
lContinua := lAlter .And. lContinua

cStatus := (cAliasADA)->ADA_STATUS
If cStatus == "C" .And. nOpcx == 4
	Help(" ",1,"FT400NAOAL")
	lContinua := .F.
EndIf

If cStatus == "E" .And. nOpcx == 4
	Help(" ",1,"FT400NAOAE")
	lContinua := .F.
EndIf

If lContinua
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calculo do tamanho dos objetos                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MsAdvSize()
	aObjects := {}
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100, 015, .t., .f. } )	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )
	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,{{160,200,240,265}} )
	nGetLin := aPosObj[3,1]

	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	EnChoice( "ADA", nReg, nOpcX, , , , , aPosObj[1],,3,,,,,,.T.)
	@ nGetLin,aPosGet[1,1]  SAY OemToAnsi(STR0016)				SIZE 020,09 OF oDlg	PIXEL //"Total :"
	@ nGetLin,aPosGet[1,2]  SAY oSAY1 VAR 0 PICTURE TM(0,16,2)		SIZE 040,09 OF oDlg PIXEL
	@ nGetLin,aPosGet[1,3]  SAY OemToAnsi(STR0017)				SIZE 020,09 OF oDlg PIXEL //"Desc. :"
	@ nGetLin,aPosGet[1,4]  SAY oSAY2 VAR 0 PICTURE TM(0,16,2)		SIZE 040,09 OF oDlg	PIXEL	
	@ nGetLin+10,aPosGet[1,3]  SAY OemToAnsi("=")					SIZE 020,09 OF oDlg PIXEL
	@ nGetLin+10,aPosGet[1,4]  SAY oSAY3 VAR 0						SIZE 040,09 PICTURE TM(0,16,2) OF oDlg PIXEL
	oDlg:Cargo	:= {|n1,n2,n3| oSay1:SetText(n1),oSay2:SetText(n2),oSay3:SetText(n3) }
	SetKey(VK_F4,{|| MaViewSB2(GdFieldGet("ADB_CODPRO",n))})
	oGetD:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcX,"Ft400LinOk","Ft400TudOk","+ADB_ITEM",!lVisual,,1,,300,"Ft400FldOk")
	PRIVATE oGetDad := oGetD
	Ft400Rodap(oGetD)
	ACTIVATE MSDIALOG oDlg ON INIT Ft400Bar(oDlg,{||nOpcA:=1,If(oGetd:TudoOk(),If(!Obrigatorio(aGets,aTela),nOpcA:=0,oDlg:End()),nOpcA:=0)},{||oDlg:End()},nOpcX)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desabilita a Tecla F4                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
	If nOpcA==1 .And. !lVisual
		Begin Transaction
			If Ft400Grava(IIf(lExcl,3,2),IIf(!lCopia,aRegADB,Nil))   
				EvalTrigger()
				While (GetSx8Len() > nSaveSx8)
					ConfirmSx8()
				EndDo
			EndIf
		    // Encerra o contrato e gera um novo contrato.
			If lEncerOk
				RecLock("ADA")
				cStatus := "E"
				(cAliasADA)->ADA_STATUS := cStatus
				MsUnLock()
			EndIf			
		End Transaction
	Else
		If ExistBlock("FT400CLOSE")
			ExecBlock("FT400CLOSE",.F.,.F.,nOpcx)
		EndIf
	EndIf
Else
	If !lAlter .And. lDeleta
		Help(" ",1,"FT400TOEMP")
	EndIf
	Begin Transaction
	    // Encerra o contrato, mas nao gera um novo contrato.
		If lEncerOk
			RecLock("ADA")
			cStatus := "E"
			(cAliasADA)->ADA_STATUS := cStatus
			MsUnLock()
		EndIf
	End Transaction	
EndIf
While (GetSx8Len() > nSaveSx8)
	RollBackSx8()
EndDo
MsUnLockAll()
RestArea(aArea)
Return(.T.)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400LinOk³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de validacao da LinhaOk da Getdados                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Getdados                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400LinOk(oGetd)

Local lRetorno := .T.
Local nPTES    := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_TES"})
Local nPTESCOB := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_TESCOB"})
Local nPosPrd    := aScan(aHeader,{|x| AllTrim(x[2]) == "ADB_CODPRO"})
Local nPosLocal  := aScan(aHeader,{|x| AllTrim(x[2]) == "ADB_LOCAL"})
Local nUsado   := Len(aHeader)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica os campo obrigatorios                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lRetorno := MaCheckCols(aHeader,aCols,N)


If lRetorno
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a permissao do armazem. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRetorno := MaAvalPerm(3,{aCols[n][nPosLocal],aCols[n][nPosPrd]})
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a integridade das TES                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno .And. !aCols[n][nUsado+1]
		If aCols[n][nPTesCob] == aCols[n][nPTes]
			Help(" ",.F.,"FT400LITES")
			lRetorno := .F.		
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o item já teve remessa                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Ft400Rodap(oGetd)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada na validacao da linha³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
		If ExistBlock("FT400LOK")
			lRetorno := ExecBlock( "FT400LOK", .F.,.F.)
		Endif
	EndIf
EndIf

Return(lRetorno)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400TudOk³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de validacao da TudoOk da Getdados                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Getdados                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400TudOk()

Local lRetorno := .T.
Local lFt400TOK:= Existblock("FT400TOK")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Agroindustria  									                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If FindFunction("OGXUtlOrig") //Encontra a função
	If OGXUtlOrig()
		If FindFunction("OGA280")
		   IF ADA->( FieldPos('ADA_CTRCOM') ) > 0
	          lRetorno:= oGa280TrOk() //--<< Verifica se o Contrato informado pode ser usado >>--
	          If lRetorno 
	             lRetorno:= oGa280Parc() //--<< Verifica se o ADB_PREREF foi preenchido >>--
	          EndIF
	       EndIF
	   EndIf
	   IF lRetorno == .f.
	      Return ( lRetorno )
	   EndIF
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para validar ao clicar botao OK          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lFt400TOK
	lRetorno := Execblock("FT400TOK",.F.,.F.)
Endif

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Multt³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Inicializacao do campos vinculados                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Campo que foi alterado                          (OPC)³±±
±±³          ³ExpC2: Valor que foi alterado                          (OPC)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400MultT(cReadVar,xConteudo)

Local aArea     := GetArea()
Local lRetorno  := .T.
Local nPosCpo   := 0
Local nPCodPro  := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_CODPRO"})
Local nPUM      := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_UM"})
Local nPSegUM   := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_SEGUM"})
Local nPTES     := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_TES"})
Local nPLocal   := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_LOCAL"})
Local nPQuant   := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_QUANT"})
Local nPQuant2  := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_UNSVEN"})
Local nPPrUnit  := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_PRUNIT"})
Local nPPrcVen  := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_PRCVEN"})
Local nPTotal   := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_TOTAL"})
Local nPDesc    := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_DESC"})
Local nPValDes  := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_VALDES"})
Local nDesPro   := aScan(aHeader,{|x| Alltrim(x[2])=="ADB_DESPRO"})
Local cReadVar2 := ""

DEFAULT cReadVar  := AllTrim(ReadVar())
DEFAULT xConteudo := &(ReadVar())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os campos interdependentes                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cReadVar2 := StrTran(cReadVar,"M->","")
nPosCpo := aScan(aHeader,{|x| AllTrim(x[2])==cReadVar2})
If nPosCpo<>0 .And. xConteudo <> aCols[N][nPosCpo]
	Do Case
	Case "ADB_CODPRO" == cReadVar2

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Posiciona registros                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SB1")
		dbSetOrder(1)
		MsSeek(xFilial("SB1")+xConteudo)

		If nPUM <> 0
			aCols[N][nPUM] := SB1->B1_UM
		EndIf
		If nPSegUM <> 0
			aCols[N][nPSEGUM] := SB1->B1_SEGUM
		EndIf
		If nPTES <> 0
			aCols[N][nPTES] := RetFldProd(SB1->B1_COD,"B1_TS")
		EndIf
		If nPLocal <> 0
			aCols[N][nPLocal] := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
		EndIf
		If nPQuant <> 0
			aCols[N][nPQuant] := 0
		EndIf
		If nPQuant2 <> 0
			aCols[N][nPQuant2] := 0
		EndIf
		If nPPrUnit <> 0
			aCols[N][nPPrUnit] := MaTabPrVen(M->ADA_TABELA,SB1->B1_COD,0,M->ADA_CODCLI,M->ADA_LOJCLI,M->ADA_MOEDA,M->ADA_EMISSA)
		EndIf
		If nPPrcVen <> 0 .And. nPPrUnit <> 0
			If aCols[N][nPPrUnit] == 0 //Se o preço de lista estiver zerado não há porque calcular desconto.
				aCols[N][nPPrcVen] := 0
			Else
				aCols[N][nPPrcVen] := FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@aCols[n,nPDesc],@aCols[n,nPValDes],@aCols[n,nPValDes],1)
			EndIf
		EndIf
		If nPTotal <> 0
			aCols[N][nPTotal] := 0
		EndIf
		If nPDesc <> 0
			aCols[N][nPDesc] := 0
		EndIf
		If nPValDes <> 0
			aCols[N][nPValDes] := 0
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Trazer descricao do Produto                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SA7")
		dbSetOrder(1)
		If ( MsSeek(xFilial("SA7")+M->ADA_CODCLI+M->ADA_LOJCLI+xConteudo,.F.) ) .And. !Empty(SA7->A7_DESCCLI)
			aCols[N][nDespro] := SA7->A7_DESCCLI
		Else
			aCols[N][nDespro] := SB1->B1_DESC
		EndIf
		
	Case "ADB_QUANT" == cReadVar2
		If nPQuant2 <> 0
			aCols[N][nPQuant2] := ConvUm(aCols[n][nPCodPro],xConteudo,0,2)
		EndIf
		If nPPrUnit <> 0
			aCols[N][nPPrUnit] := MaTabPrVen(M->ADA_TABELA,aCols[n][nPCodPro],xConteudo,M->ADA_CODCLI,M->ADA_LOJCLI,M->ADA_MOEDA,M->ADA_EMISSA)
		EndIf		
		If nPPrcVen <> 0 .And. nPPrUnit <> 0
			aCols[N][nPPrcVen] := FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],xConteudo,@aCols[n,nPTotal],@aCols[n,nPDesc],@aCols[n,nPValDes],@aCols[n,nPValDes],1,aCols[n,nPQuant])
		EndIf			
		If nPTotal <> 0 .And. nPPrcVen<>0
			aCols[N][nPTotal] := a410Arred(xConteudo*aCols[N][nPPrcVen],"D2_TOTAL")
		EndIf
	Case "ADB_UNSVEN" == cReadVar2 
		If nPQuant <> 0
			aCols[N][nPQuant] := ConvUm(aCols[n][nPCodPro],0,xConteudo,1)
		EndIf
		If nPPrcVen <> 0 .And. nPPrUnit <> 0
			aCols[N][nPPrcVen] := FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@aCols[n,nPDesc],@aCols[n,nPValDes],@aCols[n,nPValDes],1)
		EndIf
		If nPTotal <> 0 .And. nPPrcVen<>0
			aCols[N][nPTotal] := a410Arred(aCols[N][nPQuant]*aCols[N][nPPrcVen],"D2_TOTAL")
		EndIf						
	Case "ADB_PRCVEN" == cReadVar2
		If nPValDes <> 0
			aCols[n,nPValDes] := 0
		EndIf
		If nPDesc <> 0
			aCols[n,nPDesc] := 0
		EndIf		
			
		If nPPrUnit <> 0 .And. aCols[N][nPPrUnit] == 0
			aCols[N][nPPrUnit] :=  MaTabPrVen(M->ADA_TABELA,SB1->B1_COD,0,M->ADA_CODCLI,M->ADA_LOJCLI,M->ADA_MOEDA,M->ADA_EMISSA)//xConteudo
		EndIf 
		
		If nPPrcVen <> 0 .And. nPPrUnit <> 0
		   FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@aCols[n,nPDesc],@aCols[n,nPValDes],@aCols[n,nPValDes],1)
		EndIf
		
		If nPTotal <> 0 .And. nPPrcVen<>0
		   aCols[N][nPTotal] := a410Arred(xConteudo*aCols[N][nPQuant],"D2_TOTAL")
		EndIf

	Case "ADB_PRUNIT" == cReadVar2
		If nPValDes <> 0
			aCols[n,nPValDes] := 0
		EndIf
		If nPDesc <> 0
			aCols[n,nPDesc] := 0
		EndIf
		If nPPrcVen <> 0 .And. nPPrUnit <> 0
			aCols[N][nPPrcVen] := FtDescItem(FtDescCab(xConteudo,{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@aCols[n,nPDesc],@aCols[n,nPValDes],@aCols[n,nPValDes],1)
		EndIf			
		If nPTotal <> 0 .And. nPPrcVen<>0
			aCols[N][nPTotal] := a410Arred(aCols[N][nPQuant]*aCols[N][nPPrcVen],"D2_TOTAL")
		EndIf
	Case "ADB_VALDES" == cReadVar2
	aCols[n,nPPrcVen] := FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@aCols[n,nPDesc],@xConteudo,aCols[n,nPValDes],2)
	Case "ADB_DESC" == cReadVar2
		aCols[n,nPPrcVen] := FtDescItem(FtDescCab(aCols[n,nPPrUnit],{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4}),@aCols[n,nPPrcVen],aCols[n,nPQuant],@aCols[n,nPTotal],@xConteudo,@aCols[n,nPValDes],@aCols[n,nPValDes],1)
	Case "ADB_TOTAL" == cReadVar2
		If xConteudo  <> a410Arred(aCols[n][nPPrcVen]*aCols[n][nPQuant],"D2_TOTAL")
			Help(" ",1,"FT400TOTAL")
			lRetorno := .F.			
		Endif 
	
	EndCase
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ft400VldFl  ³ Autor ³Eduardo Riera        ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de validacao das filiais de entrega/Venda            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Dados validos?                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta rotina efetua a validacao das filiais de entrega/Venda ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FATA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400VldFl()

Local lRetorno := .F.

lRetorno := FWModeAccess("ADA",3) == "C" .AND. FWModeAccess("SF4",3) == "C" .AND. FWModeAccess("SB1",3) == "C"	.AND.;
			FWModeAccess("SE4",3) == "C" .AND. FWModeAccess("DA0",3) == "C" .AND. FWModeAccess("SBM",3) == "C"

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Recal³ Autor ³Eduardo Riera          ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Recalculo dos dados da getdados baseado na enchoice         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Recal()

Local aArea     := GetArea()
Local nX        := 0
Local nTmp      := 0
Local nPProd    := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_CODPRO"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_PRUNIT"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_PRCVEN"})
Local nPDescon  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_DESC"})
Local nPVlDesc  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_VALDES"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_QUANT"})
Local nPValor   := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_TOTAL"})
Local nVlrTabela:= 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recalculo do preco de tabela                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTmp	:=	n
For nX := 1 to Len(aCols)     
	If Empty(M->ADA_TABELA)
   		nVlrTabela := aCols[nX][nPPrUnit]
	Else
   		nVlrTabela := MaTabPrVen(M->ADA_TABELA,aCols[nX][nPProd],aCols[nX][nPQtdVen],M->ADA_CODCLI,M->ADA_LOJCLI,M->ADA_MOEDA,M->ADA_EMISSA)
	EndIf

	If ( nPPrUnit > 0 .And. nVlrTabela > 0)
		aCols[nX][nPPrUnit] := nVlrTabela
	Else
		nVlrTabela := aCols[nX][nPPrcVen]
	EndIf  
	
	nVlrTabela := FtDescCab(nVlrTabela,{M->ADA_DESC1,M->ADA_DESC2,M->ADA_DESC3,M->ADA_DESC4})	
	
	If ( nPPrcVen > 0 )
		aCols[nX][nPPrcVen] := nVlrTabela
	EndIf	
	n	:=	nX
	If ( nPDescon > 0 .And. nPVlDesc > 0 .And. nPPrcVen > 0 .And. nPValor > 0 .And. nPPrUnit>0 )
		aCols[nX][nPPrcVen] := FtDescItem(@aCols[nX][nPPrcVen],;
			@aCols[nX,nPPrcVen],;
			aCols[nX,nPQtdVen],;
			@aCols[nX,nPValor],;
			@aCols[nX,nPDescon],;
			@aCols[nX,nPVlDesc],;
			@aCols[nX,nPVlDesc],1)
	EndIf
Next nX
n	:=	nTmp
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna o estado de entrada da rotina                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type('oGetDad')<>"U"
	Ft400Rodap(oGetDad)
	oGetDad:oBrowse:Refresh()
EndIf
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Rodap³ Autor ³ Eduardo Riera         ³ Data ³19.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao para preenchimento do Rodape.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Sempre .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto da Getdados                                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Rodap(oGetDad)

Local oDlg
Local nX     	:= 0
Local nTotal   := 0
Local nTotDes   := 0
Local nMaxFor	:= Len(aCols)
Local nUsado    := Len(aHeader)
Local lTestaDel := nUsado <> Len(aCols[1])
Local nPosTotal := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_TOTAL"})
Local nPosDesc  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_VALDES"})
Local nPPrUnit  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_PRUNIT"})
Local nPQtdVen  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_QUANT"})
Local nPPrcVen  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_PRCVEN"})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso nao seja passado o objeto da getdados deve-se pegar a janela default³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( oGetDad == Nil )
	oDlg		:= GetWndDefault()
	If ( ValType(oDlg:Cargo)<>"B" )
		oDlg := oDlg:oWnd
	EndIf
Else
	oDlg := oGetDad:oWnd
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma as variaveis do Rodape                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( nTotal == 0 )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Soma as variaveis do aCols                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To nMaxFor
		If ( (lTestaDel .And. !aCols[nX][nUsado+1]) .Or. !lTestaDel )
			If ( nPosDesc > 0 .And. nPPrUnit > 0 .And. nPPrcVen > 0 .And. nPQtdVen > 0)
				If ( aCols[nX][nPPrUnit]==0 )
					nTotDes	+= aCols[nX][nPosDesc ]
				Else
					nTotDes += A410Arred(aCols[nX][nPPrUnit]*aCols[nX][nPQtdVen],"ADB_VALDES")-A410Arred(aCols[nX][nPPrcVen]*aCols[nX][nPQtdVen],"ADB_VALDES")
				EndIf
			EndIf
			If ( nPosTotal > 0 )
				nTotal	+=	aCols[nX][nPosTotal]
			EndIf
		EndIf
	Next nX
EndIf
If ( ValType(oDlg:Cargo)=="B" )
	Eval(oDlg:Cargo,nTotal+nTotDes,nTotDes,nTotal)
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Grava³ Autor ³ Eduardo Riera         ³ Data ³21.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de gravacao do contrato de parceria                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Sempre .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: 1 - Inclusao                                         ³±±
±±³          ³       2 - Alteracao                                        ³±±
±±³          ³       3 - Exclusao                                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Grava(nOpcA,aRegADB)

Local aArea    := GetArea()
Local nX       := 0
Local nY       := 0
Local nMaxFor  := Len(aCols)
Local nDeleted := Len(aHeader)+1
Local bCampo   := {|nCPO| Field(nCPO) }
Local lTravou  := .F.
Local lGravou  := .F.
Local cMay     := ""
Local cNumCtr  := ""

DEFAULT aRegADB := {}  

If ExistBlock("FT400AGR")
	Execblock("FT400AGR",.F.,.F.,{nOpcA})
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a operacao                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nOpcA <> 3     

	If nOpca == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a integridade do numero do contrato                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("ADA")
		cNumCtr := M->ADA_NUMCTR
		cMay := "ADA"+ Alltrim(xFilial("ADA"))
		ADA->(dbSetOrder(1))
		While ( ADA->( dbSeek(xFilial("ADA")+cNumCtr) ) .or. !MayIUseCode(cMay+cNumCtr) )
			cNumCtr := Soma1(cNumCtr,Len(M->ADA_NUMCTR))
		EndDo
		M->ADA_NUMCTR := cNumCtr
	EndIf 

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava o cabecalho                                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("ADA")
	dbSetOrder(1)
	If MsSeek(xFilial("ADA")+M->ADA_NUMCTR)
		RecLock("ADA")
	Else
		RecLock("ADA",.T.)		
	EndIf
	
	For nY := 1 TO FCount()
		FieldPut(nY,M->&(EVAL(bCampo,nY)))
	Next nY
	ADA->ADA_FILIAL := xFilial("ADA")
	
	ADA->( FKCommit() ) 	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava os itens do contrato de parceria                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To nMaxFor
		If ( Len(aRegADB) >= nX )
			dbSelectArea("ADB")
			MsGoto(aRegADB[nX])
			RecLock("ADB")	
			lTravou := .T.
		Else
			If ( !aCols[nX][nDeleted] )
				RecLock("ADB",.T.)
				lTravou := .T.
			Else
				lTravou := .F.
			EndIf
		EndIf
		If aCols[nX][nDeleted]
			If lTravou
				dbSelectArea("ADB")
				dbDelete()
				MsUnLock()
			EndIf
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza os itens do contrato de parceria                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lGravou := .T.
			For nY := 1 to Len(aHeader)
				If aHeader[nY][10] <> "V"
					ADB->(FieldPut(FieldPos(aHeader[nY][2]),aCols[nX][nY]))
				EndIf
			Next nY
			ADB->ADB_FILIAL := xFilial("ADB")
			ADB->ADB_NUMCTR := ADA->ADA_NUMCTR
			ADB->ADB_CODCLI := ADA->ADA_CODCLI
			ADB->ADB_LOJCLI := ADA->ADA_LOJCLI
		EndIf			
	Next nX	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Recalcula os status do contrato de parceria                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Ft400StatCt()
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Exclucao dos itens do contrato de parceria                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To nMaxFor
		dbSelectArea("ADB")
		MsGoto(aRegADB[nX])
		If FindFunction("del400nlp")
			del400nlp( ADA->ADA_NUMCTR, ADB->ADB_CODPRO, ADB->ADB_ITEM )
		Endif
		RecLock("ADB")
		ADB->(dbDelete())
		ADB->(MsUnlock())
	Next nX 
	
	ADB->( FKCommit() ) 
	If AliasInDic( 'NLP' )
		NLP->( FKCommit() )
	EndIf
	
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Exclucao dos Cabecalho do contrato de parceria                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lGravou
	dbSelectArea("ADA")
	dbSetOrder(1)
	If MsSeek(xFilial("ADA")+M->ADA_NUMCTR)
		RecLock("ADA")
		dbDelete()		
		MsUnLock()
	EndIf
EndIf

If ExistBlock("FT400GRV")
	Execblock("FT400GRV",.F.,.F.,{nOpcA})
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Agroindustria  									                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If FindFunction("OGXUtlOrig") //Encontra a função
	If OGXUtlOrig()
		If FindFunction("OGA280")
	       OGA280NK6(nOPca)
	   EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da rotina                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(.T.)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Bar  ³ Autor ³ Eduardo Riera         ³ Data ³24.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ EnchoiceBar especifica do FATA400                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oDlg: 	Objeto Dialog                                     ³±±
±±³          ³ bOk:  	Code Block para o Evento Ok                       ³±±
±±³          ³ bCancel: Code Block para o Evento Cancel                   ³±±
±±³          ³ nOpc:	nOpc transmitido pela mbrowse                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±ºHanna C.  |30/03/07|9.12  ³Bops 118469 - Alterado o nome dos Bitmaps   ³±±
±±º        	 ³        |      ³definidos pela Engenharia para o Protheus 10³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ft400Bar(oDlg,bOk,bCancel,nOpc)

Local aButtons  := {}
Local aButtonUsr:= {}
Local nI        := 0

If ( aRotina[ nOpc, 4 ] <> 2 )
	aadd(aButtons,{"POSCLI",{|| If(!Empty(M->ADA_CODCLI),a450F4Con(),.F.)},STR0018, STR0028}) //"Posi‡„o de Cliente"
	if nModulo == 67
		If TableInDic('NLP')
			AADD(aButtons,{ STR0039,{||Ft400Rsrv()},STR0039,STR0039})
		Endif		
	EndIf
EndIf

If ( aRotina[ nOpc, 4 ] == 2 ) .And. !AtIsRotina("A400TRACK")
	AAdd(aButtons,{ "bmpord1", {|| A400Track() }, STR0030, STR0031 } )  // "System Tracker"###"Tracker"
EndIf 	

If ExistBlock("FT400BAR")
	aButtonUsr := ExecBlock("FT400BAR",.F.,.F.)
	If ValType(aButtonUsr) == "A"
		For nI   := 1  To  Len(aButtonUsr)
			Aadd(aButtons,aClone(aButtonUsr[nI]))
		Next nI
	EndIf
EndIf
Return(EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400FldOk³ Autor ³ Eduardo Riera         ³ Data ³24.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da alteracao dos campos da GetDados              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Expl1: Campo pode ser alterado                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400FldOk()

Local aArea    := GetArea()
Local lRetorno := .T.
Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_ITEM"})
Local nPQuant  := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_QUANT"})
Local cReadVar := ReadVar()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o item do contrato pode ser alterado               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ADB")
dbSetOrder(1)
If MsSeek(xFilial("ADB")+M->ADA_NUMCTR+aCols[n][nPItem])
	If ADB->ADB_QTDEMP >= ADB->ADB_QUANT .Or. IIf("ADB_QUANT"$cReadVar,&cReadVar,aCols[n][nPQuant])<ADB->ADB_QTDEMP
		lRetorno := .F.
		Help(" ",1,"FT400LIEMP")
	EndIf
	If !Empty(ADB->ADB_PEDCOB)
		lRetorno := .F.
		Help(" ",1,"FT400LICOB")
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a integridade da rotina                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400StaCt³ Autor ³ Eduardo Riera         ³ Data ³03.07.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de calculo do status do contrato de parceria        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O ADA deve estar posicionado                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400StatCt()

Local aArea    := GetArea()
Local aAreaADB := ADB->(GetArea())

Local cStatus  := ""
Local lQtdEmp  := .F.
Local lPedido  := .T.
Local lPedCob  := .T.
Local cQuery   := ""
	
ADB->(dbCommit())

cQuery := "SELECT COUNT(*) PEDCOB "
cQuery += "FROM "+RetSqlName("ADB")+" ADB "
cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
cQuery += "ADB.ADB_NUMCTR='"+ADA->ADA_NUMCTR+"' AND "
cQuery += "ADB.ADB_PEDCOB='"+Space(Len(ADB->ADB_PEDCOB))+"' AND "
cQuery += "ADB.ADB_TESCOB<>'"+Space(Len(ADB->ADB_TESCOB))+"' AND "
cQuery += "ADB.D_E_L_E_T_=' ' "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"FT400STATCT")

lPedCob := PEDCOB==0

dbCloseArea()

cQuery := "SELECT COUNT(*) PEDIDO "
cQuery += "FROM "+RetSqlName("ADB")+" ADB "
cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
cQuery += "ADB.ADB_NUMCTR='"+ADA->ADA_NUMCTR+"' AND "
cQuery += "ADB.ADB_QTDEMP<ADB.ADB_QUANT AND "
cQuery += "ADB.D_E_L_E_T_=' ' "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"FT400STATCT")	

lPedido := PEDIDO==0

dbCloseArea()

cQuery := "SELECT SUM(ADB_QTDEMP) PEDIDO "
cQuery += "FROM "+RetSqlName("ADB")+" ADB "
cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
cQuery += "ADB.ADB_NUMCTR='"+ADA->ADA_NUMCTR+"' AND "
cQuery += "ADB.D_E_L_E_T_=' ' "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"FT400STATCT")	

lQtdEmp := PEDIDO > 0

dbCloseArea()	

dbSelectArea("ADB")	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza o status do contrato de parceria                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Do Case
	Case !lPedCob
		cStatus := "A"
	Case lPedCob .And. !( lQtdEmp .Or. lPedido )
		cStatus := "B"
	Case lPedido
		cStatus := "D"
	Case lQtdEmp
		cStatus := "C"
EndCase
RecLock("ADA")
ADA->ADA_STATUS := cStatus

RestArea(aAreaADB)
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Ft400Legen³Autor  ³ Eduardo Riera         ³ Data ³04.07.2002 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Demonstra a legenda das cores da mbrowse                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina monta uma dialog com a descricao das cores da    ³±±
±±³          ³Mbrowse.                                                     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Legen()

Local aLegendas := {{"ENABLE",STR0019},; //"Legenda"###"Contrato em aberto"
	{"BR_AZUL",STR0020}   ,; //"Contrato aprovado"
	{"BR_AMARELO",STR0021},; //"Contrato parcialmente entregue"
	{"BR_CINZA",STR0033}  ,; //"Contrato encerrado Manualmente"	
	{"DISABLE",STR0022}}

Local aRetPE := {}
Local nCnt := 1
	
If (ExistBlock("FT400LEG"))
	aRetPE := ExecBlock("FT400LEG",.f.,.f.)
	If ValType(aRetPE) == 'A'
		For nCnt := 1 to Len(aRetPE)
			AADD(aLegendas, aRetPE[nCnt])
		Next nCnt
	EndIf	
EndIf

BrwLegenda(cCadastro,STR0015, aLegendas)	 //"Contrato totalmente entregue"
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Aprov³ Autor ³ Eduardo Riera         ³ Data ³08.07.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Aprovacao do contrato de parceria - Venda                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Aprov()

Local aArea    := GetArea()
Local aAreaADA := ADA->(GetArea())
Local aAreaADB := ADB->(GetArea())

Local lCobranca:= .F.
Local cQuery   := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o contrato esta em aberto                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ADA")
If ADA->ADA_STATUS=="A"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe itens a serem cobrados                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	cQuery := "SELECT COUNT(*) NRCOB "
	cQuery += "FROM "+RetSqlName("ADB")+" ADB "
	cQuery += "WHERE ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
	cQuery += "ADB.ADB_NUMCTR='"+ADA->ADA_NUMCTR+"' AND "
	cQuery += "ADB.ADB_TESCOB<>'"+Space(Len(ADB->ADB_TESCOB))+"' AND "
	cQuery += "ADB.D_E_L_E_T_=' ' "
	
	cQuery := ChangeQuery(cQuery)
	
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"FT400Aprov")
	
	lCobranca := NRCOB > 0
	dbCloseArea()		
	dbSelectArea("ADB")	
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza o status do contrato de parceria para aprovado                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
	If lCobranca
		Ft400Pv(ADA->ADA_NUMCTR,1)
	Else
		Begin Transaction
			RecLock("ADA")
			ADA->ADA_STATUS := "B"
			MsUnLock()
		End Transaction
	EndIf	
Else
	Help(" ",1,"FT400JAAPR")
EndIf
RestArea(aAreaADB)
RestArea(aAreaADA)
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Remes³ Autor ³ Eduardo Riera         ³ Data ³08.07.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Remessa dos produtos do contrato de parceria - Venda       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Remes()    
       
Local cContrato := ""
Local lFT400CON := Existblock("FT400CON")

If lFT400CON
	cContrato := ExecBlock("FT400CON",.F.,.F.)
EndIf
        
Ft400Pv(IIF((lFT400CON .and. !Empty(cContrato)),cContrato,Nil),2)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400PV   ³ Autor ³ Eduardo Riera         ³ Data ³24.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Geracao do Pedido de Venda por contrato de parceria        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Contrato de Parceira                                ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400PV(cContrato,nTipo)

Local aColsC6   := {}
Local aHeadC6   := {}
Local aPedCob   := {}
Local aArea     := GetArea()
Local aDadosCfo := {} 

Local bCampo    := {|nCPO| Field(nCPO) }

Local cFilOld   := cFilAnt
Local cVend     := ""
Local cCampo    := ""
Local cItSC6    := StrZero(0,Len(SC6->C6_ITEM))
Local cTes      := ""

Local lFT400Lib := ExistBlock("FT400LIB")
Local lFt400Ped := ExistBlock("FT400PED")
Local lFt400Lin := ExistBlock("FT400LIN")
Local lFt400Cab := ExistBlock("FT400CAB")
Local lFt400Vlc := ExistBlock("FT400VLC")
Local lValContr := .T.
Local lValItCon := .T.

Local nX        := 0
Local nY        := 0
Local nMaxFor   := 0
Local nMaxVend  := Fa440CntVen() 
Local nUsado    := 0
Local aRotinaBkp:= {}
Local nStack    := GetSX8Len()

Local lLibPV	:= .F.

Private aheadGrade := {}
Private aColsGrade := {}
Private aheader    := {}
Private acols      := {}
Private n          := 1
PRIVATE aMemoSC6   := { { 'C6_CODINF', 'C6_INFAD' } }

DEFAULT nTipo      := 1

If Empty(cContrato)
	cContrato := Ft400BCtr(ADA->ADA_CODCLI,ADA->ADA_LOJCLI)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Preparar pedido de venda com base no contrato de parceria               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(cContrato)

	If ExistBlock("FT400VAL")
		lValContr := Execblock("FT400VAL",.F.,.F.,{cContrato,nTipo})
	Endif

	If lValContr
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta aHeader do SC6                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aHeadC6 := {}
		dbSelectArea("SX3")
		dbSetOrder(1)
		MsSeek("SC6",.T.)
		While ( !Eof() .And. SX3->X3_ARQUIVO == "SC6" )
			If ( X3Uso(SX3->X3_USADO) .And.;
					!Trim(SX3->X3_CAMPO)=="C6_NUM" .And.;
					Trim(SX3->X3_CAMPO) != "C6_QTDEMP" .And.;
					Trim(SX3->X3_CAMPO) != "C6_QTDENT" .And.;
					cNivel >= SX3->X3_NIVEL ) .Or.;
					Trim(SX3->X3_CAMPO)=="C6_CONTRAT" .Or. ;
					Trim(SX3->X3_CAMPO)=="C6_ITEMCON"

				Aadd(aHeadC6,{X3Titulo(),;
					SX3->X3_CAMPO,;
					SX3->X3_PICTURE,;
					SX3->X3_TAMANHO,;
					SX3->X3_DECIMAL,;
					SX3->X3_VALID,;
					SX3->X3_USADO,;
					SX3->X3_TIPO,;
					SX3->X3_ARQUIVO,;
					SX3->X3_CONTEXT })
			EndIf
			dbSelectArea("SX3")
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona registros                                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("ADA")
		dbSetOrder(1)
		MsSeek(xFilial("ADA")+cContrato)
	
		dbSelectArea("SA1")
		dbSetOrder(1)
		MsSeek(xFilial("SA1")+ADA->ADA_CODCLI+ADA->ADA_LOJCLI)
	
		dbSelectArea("SE4")
		dbSetOrder(1)
		MsSeek(xFilial("SE4")+ADA->ADA_CONDPG)
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Cria as variaveis do Pedido de Venda                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC5")
		nMaxFor := FCount()
		For nX := 1 To nMaxFor
			M->&(EVAL(bCampo,nX)) := CriaVar(FieldName(nX),.T.)
		Next nX
		M->C5_TIPO    := "N"
		M->C5_CLIENTE := ADA->ADA_CODCLI
		M->C5_LOJACLI := ADA->ADA_LOJCLI
		M->C5_LOJAENT := ADA->ADA_LOJCLI
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza as informacoes padroes a partir do Cliente                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		a410Cli("C5_CLIENTE",M->C5_CLIENTE,.F.)
		a410Loja("C5_LOJACLI",M->C5_LOJACLI,.F.)
		M->C5_TIPOCLI := SA1->A1_TIPO
		M->C5_CONDPAG := ADA->ADA_CONDPG
		M->C5_TABELA  := ADA->ADA_TABELA
		M->C5_DESC1   := ADA->ADA_DESC1
		M->C5_DESC2   := ADA->ADA_DESC2
		M->C5_DESC3   := ADA->ADA_DESC3
		M->C5_DESC4   := ADA->ADA_DESC4
		M->C5_MOEDA   := ADA->ADA_MOEDA
		M->C5_TIPLIB  := ADA->ADA_TIPLIB
		//	M->C5_TPCARGA := ADA->ADA_TPCARGA
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Transfere vendedores                                                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cVend := "1"
		For nX := 1 To nMaxVend
			cCampo := "ADA_VEND"+cVend
			If ADA->(FieldPos(cCampo)) > 0
				nY := ADA->(FieldPos(cCampo))
				cCampo := "C5"+SubStr(cCampo,4)
				M->&(cCampo) := ADA->(FieldGet(nY))
			EndIf
			cCampo := "ADA_COMIS"+cVend
			If ADA->(FieldPos(cCampo)) > 0
				nY := ADA->(FieldPos(cCampo))
				cCampo := "C5"+SubStr(cCampo,4)	
				M->&(cCampo) := ADA->(FieldGet(nY))
			EndIf
			cVend := Soma1(cVend,1)
		Next nX
		
		If lFt400Cab
			ExecBlock("FT400CAB",.F.,.F.)
		Endif	
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Preenche o Acols do Pedido de Venda                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nUsado := Len(aHeadC6)
	
		dbSelectArea("ADB")
		dbSetOrder(1)
		MsSeek(xFilial("ADB")+cContrato)
	
		Pergunte("MTA410",.F.)
		lLibPV := MV_PAR01==1

		While ( !Eof() .And. xFilial("ADB") == ADB->ADB_FILIAL .And.;
				cContrato == ADB->ADB_NUMCTR )

			If lFt400Vlc
				lValItCon := Execblock("FT400VLC",.F.,.F.,{cContrato,ADB->ADB_ITEM})
			Endif

			If (lValItCon) .And. (ADB->(FieldGet(FieldPos("ADB_QUANT"))) - ADB->(FieldGet(FieldPos("ADB_QTDEMP"))) > 0)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Posiciona registros                                                     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SB1")
				dbSetOrder(1)
				MsSeek(xFilial("SB1")+ADB->ADB_CODPRO)
		
				If !Empty(ADB->ADB_TESCOB) .And. Empty(ADB->ADB_PEDCOB)
					cTes := ADB->ADB_TESCOB
				Else
					cTes := ADB->ADB_TES
				EndIf
						
				dbSelectArea("SF4")
				dbSetOrder(1)
				MsSeek(xFilial("SF4")+cTes)
		
				aadd(aColsC6,Array(nUsado+1))
				nY := Len(aColsC6)		
				aColsC6[nY,nUsado+1] := .F.
		
				For nX := 1 To nUsado
					Do Case
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_ITEM" )
						cItSC6 := Soma1(cItSC6)
						aColsC6[nY,nX] := cItSC6
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_PRODUTO" )
						aColsC6[nY,nX] := ADB->ADB_CODPRO
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_UM" )
						aColsC6[nY,nX] := SB1->B1_UM	
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_QTDVEN" )
						aColsC6[nY,nX] := ADB->ADB_QUANT-ADB->ADB_QTDEMP-ADB->ADB_QTDENT
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_PRCVEN" )
						aColsC6[nY,nX] := ADB->ADB_PRCVEN
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_VALOR" )
						aColsC6[nY,nX] := a410Arred(GDFieldGet("C6_QTDVEN",nY,NIL,aHeadC6,aColsC6)*;
								GDFieldGet("C6_PRCVEN",nY,NIL,aHeadC6,aColsC6),"C6_VALOR")
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_TES" )
						aColsC6[nY,nX] := cTes
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CF" )
					
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Define o CFO                                         ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						aDadosCFO := {}
					 	Aadd(aDadosCfo,{"OPERNF"  ,"S"})
					 	Aadd(aDadosCfo,{"TPCLIFOR",SA1->A1_TIPO})
					 	Aadd(aDadosCfo,{"UFDEST"  ,SA1->A1_EST})
					 	Aadd(aDadosCfo,{"INSCR"   ,SA1->A1_INSCR})
					 	Aadd(aDadosCfo,{"CONTR"	  ,SA1->A1_CONTRIB})
						
						aColsC6[nY,nX] := MaFisCfo(,SF4->F4_CF,aDadosCfo)
						
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_SEGUM" )
						aColsC6[nY,nX] := SB1->B1_SEGUM
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_LOCAL" )
						aColsC6[nY,nX] := ADB->ADB_LOCAL
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_VALDESC" )
					    If ADB->ADB_PRUNIT > 0
							aColsC6[nY,nX] := A410Arred((ADB->ADB_PRUNIT*aColsC6[nY,Ascan(aHeadC6,{|x| Trim(x[2])=="C6_QTDVEN"})]*ADB->ADB_DESC)/100,"C6_VALDESC")
						Else
							aColsC6[nY,nX] := A410Arred((ADB->ADB_VALDES / ADB->ADB_QUANT)*aColsC6[nY,Ascan(aHeadC6,{|x| Trim(x[2])=="C6_QTDVEN"})],"C6_VALDESC")
						EndIf	
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_DESCONT" )
						aColsC6[nY,nX] := ADB->ADB_DESC
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_ENTREG" )
						aColsC6[nY,nX] := dDataBase
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_DESCRI" )
						aColsC6[nY,nX] := ADB->ADB_DESPRO
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_PRUNIT" )
						aColsC6[nY,nX] := ADB->ADB_PRUNIT
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CONTRAT" )
						aColsC6[nY,nX] := ADB->ADB_NUMCTR
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_ITEMCON" )
						aColsC6[nY,nX] := ADB->ADB_ITEM				
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_QTDLIB" )
						If lLibPV
							aColsC6[nY,nX] := ADB->ADB_QUANT-ADB->ADB_QTDEMP-ADB->ADB_QTDENT
						Else
							aColsC6[nY,nX] := 0
						EndIf
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_UNSVEN" )
						aColsC6[nY,nX] := ADB->ADB_UNSVEN
						//				Case ( AllTrim(aHeadC6[nX,2]) == "C6_CLASFIS" )
						//					aColsC6[nAcols,nCntFor] := ADB->ADB_CLASFIS
					Case ( "C6_COMIS" $ AllTrim(aHeadC6[nX,2])  )
						If	ADB->(FieldPos("ADB"+SubStr(AllTrim(aHeadC6[nX,2]),3)))>0
							aColsC6[nY,nX] := ADB->(FieldGet(FieldPos("ADB"+SubStr(AllTrim(aHeadC6[nX,2]),3))))
						Else
							aColsC6[nY,nX] := CriaVar(aHeadC6[nX,2],.T.)
						EndIf
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CODISS" )
						aColsC6[nY,nX] := SB1->B1_CODISS
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CC" )
						aColsC6[nY,nX] := SB1->B1_CC	
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CONTA" )
						aColsC6[nY,nX] := SB1->B1_CONTA	
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_ITEMCTA" )
						aColsC6[nY,nX] := SB1->B1_ITEMCC	
					Case ( AllTrim(aHeadC6[nX,2]) == "C6_CLVL" )
						aColsC6[nY,nX] := SB1->B1_CLVL	
					OtherWise
						aColsC6[nY,nX] := CriaVar(aHeadC6[nX,2],.T.)
					EndCase
					
					If lFt400Lin
						aColsC6 := Execblock("FT400LIN",.F.,.F.,{aHeadC6,aColsC6})
					Endif	
					
				Next nX
				If !Empty(ADB->ADB_TESCOB) .And. Empty(ADB->ADB_PEDCOB)
					aadd(aPedCob,aColsC6[nY])
				EndIf
				cFilAnt := IIf(Empty(ADB->ADB_FILENT),cFilAnt,ADB->ADB_FILENT)
			EndIf
			dbSelectArea("ADB")
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica se ha pedido para cobranca                                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Len(aPedCob) > 0
			aColsC6 := aPedCob
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Grava o Pedido de Venda                                                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(aColsC6)		
			Begin Transaction
				aCols   := aColsC6
				aHeader := aHeadC6
				For nX := 1 To Len(aCols)
					MatGrdMont(nX)
				Next nX
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Variaveis Utilizadas pela Funcao a410Inclui          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				PRIVATE ALTERA := .F.
				PRIVATE INCLUI := .T.
				PRIVATE cCadastro := STR0023 //"Pedido de Venda"
				
				If lFt400Ped
					Execblock("FT400PED",.F.,.F.)
				Endif	
				
				Pergunte("MTA410",.F.)
				aRotinaBkp := aRotina
				SC5->(a410Inclui(Alias(),Recno(),4,.T.,nStack,,.T.,nTipo))
				aRotina := aRotinaBkp
			End Transaction
			cFilAnt := cFilOld
		EndIf
	EndIf
	
	If lFt400Lib
		ExecBlock("FT400LIB", .F.,.F.,nTipo)
	EndIf
EndIf
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da rotina                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFilAnt := cFilOld
RestArea(aArea)
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400BCtr ³ Autor ³ Eduardo Riera         ³ Data ³24.06.2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de apresentacao do browse de contratos do cliente   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1: Numero do contrato selecionado                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Codigo do cliente                                   ³±±
±±³          ³ ExpC2: loja do cliente                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400BCtr(cCliente,cLoja)

Local aArea     	:= GetArea()
Local aOrdem    	:= {AllTrim(RetTitle("ADA_NUMCTR")),AllTrim(RetTitle("ADA_EMISSA"))}
Local aPesq     	:= {{Space(Len(ADA->ADA_NUMCTR)),"@!"},{Ctod(""),"@!"}}
Local aStruTrb  	:= {}
Local aSize     	:= MsAdvSize( .F. )
Local aObjects  	:= {}
Local aInfo     	:= {}
Local aPosObj   	:= {}
Local aPDFields		:= {"A1_NOME"}
Local cContrato 	:= ""
Local cAliasADA 	:= "F4PODER3_SQL"
Local cAliasADB 	:= "F4PODER3_SQL"
Local cAliasTrb 	:= GetNextAlias()
Local cQuery    	:= ""
Local cCombo    	:= ""
Local cTexto1   	:= ""
Local nHandle   	:= GetFocus()
Local nX        	:= 0
Local nOpcA     	:= 0
Local lValido   	:= .T.
Local xPesq     	:= ""
Local oDlg		  	:= Nil
Local oPanel	  	:= Nil
Local oCombo	  	:= Nil
Local oGet			:= Nil
Local oGetDB		:= Nil
Local aCposADA		:= {}
Local aCmpsAux1		:= {}
Local aCmpsAux2		:= {}
Local nQtdCpos		:= 0
Local cRealName		:= ""

If __oTempTable <> NIL
	cAliasTRB := __oTempTable:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cAliasTRB) == 0
		__oTempTable:Delete()
		FreeObj(__oTempTable)
		__aHeadTrbADA := NIL
	EndIf
EndIf

If __oTempTable == NIL
	__aHeadTrbADA := {}
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Montagem do arquivo temporario					                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCmpsAux1 := FWSX3Util():GetListFieldsStruct("ADA",.F.)
	nQtdCpos := Len(aCmpsAux1)
	//Ordenar pelo campo X3_ORDEM
	For nX := 1 To nQtdCpos
		aAdd(aCmpsAux2,{aCmpsAux1[nX],GetSX3Cache(aCmpsAux1[nX][1],"X3_ORDEM")})
	Next nX
	aSort(aCmpsAux2,,,{|x,y| x[2] < y[2] })
	For nX := 1 To nQtdCpos
		aAdd(aCposADA,aCmpsAux2[nX][1])
	Next nX
	For nX := 1 To nQtdCpos
		If (X3Uso(GetSX3Cache(aCposADA[nX][1],"X3_USADO")) .And.;
			cNivel >= GetSX3Cache(aCposADA[nX][1],"X3_NIVEL") .And. aCposADA[nX][2] <> "M")
				aAdd(__aHeadTrbADA,{ AllTrim(FWX3Titulo(aCposADA[nX][1])),;
							aCposADA[nX][1],;
							GetSX3Cache(aCposADA[nX][1],"X3_PICTURE"),;
							aCposADA[nX][3],;
							aCposADA[nX][4],;
							GetSX3Cache(aCposADA[nX][1],"X3_VALID"),;
							GetSX3Cache(aCposADA[nX][1],"X3_USADO"),;
							aCposADA[nX][2],;
							GetSX3Cache(aCposADA[nX][1],"X3_ARQUIVO"),;
							GetSX3Cache(aCposADA[nX][1],"X3_CONTEXT"),;
							GetSX3Cache(aCposADA[nX][1],"X3_ORDEM") } )
				aAdd(aStruTRB,{aCposADA[nX][1],aCposADA[nX][2],aCposADA[nX][3],aCposADA[nX][4]})
		EndIf
	Next nX

	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oTempTable := FWTemporaryTable():New(cAliasTRB)

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oTempTable:SetFields(aStruTRB)
	__oTempTable:AddIndex("1",{"ADA_NUMCTR"})
	__oTempTable:AddIndex("2",{"ADA_EMISSA"})

	//------------------
	//Criação da tabela
	//------------------
	__oTempTable:Create()
Else
	cRealName := __oTempTable:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruTRB := __oTempTable:GetStruct():GetFields()
EndIf

(cAliasTRB)->( DBSetOrder( 1 ) ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verificacao do aHeader atual                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualizacao do arquivo temporario com base nos itens do SD1         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("ADA")
dbSetOrder(2)
dbSelectArea("ADB")
dbSetOrder(1)

cQuery := "SELECT ADA.R_E_C_N_O_ ADARECNO,ADA_FILIAL,ADB_FILIAL,ADB_NUMCTR,ADB_QUANT,ADB_QTDEMP "
For nX := 1 To Len(__aHeadTrbADA)
	cQuery += ","+__aHeadTrbADA[nX][2]+" "
Next nX
cQuery += "FROM "+RetSqlName("ADA")+" ADA ,"
cQuery += RetSqlName("ADB")+" ADB "
cQuery += "WHERE ADA.ADA_FILIAL='"+xFilial("ADA")+"' AND "
cQuery += "ADA.ADA_CODCLI='"+cCliente+"' AND "
cQuery += "ADA.ADA_LOJCLI='"+cLoja+"' AND "
cQuery += "ADA.ADA_STATUS IN('B','C') AND "
cQuery += "ADA.D_E_L_E_T_=' ' AND "
cQuery += "ADB.ADB_FILIAL='"+xFilial("ADB")+"' AND "
cQuery += "ADB.ADB_NUMCTR=ADA.ADA_NUMCTR AND "
cQuery += "ADB.ADB_QUANT>ADB.ADB_QTDEMP AND "
cQuery += "ADB.D_E_L_E_T_=' ' "

If ExistBlock("FT400FLT")		
	cQuery += ExecBlock("FT400FLT",.F.,.F., {cCliente, cLoja, cQuery})
EndIf

cQuery += "ORDER BY "+SqlOrder(ADB->(IndexKey()))

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasADA,.T.,.T.)

For nX := 1 To Len(aStruTRB)
	If aStruTRB[nX][2] <> "C" .And. FieldPos(aStruTRB[nX][1])<>0
		TcSetField(cAliasADA,aStruTRB[nX][1],aStruTRB[nX][2],aStruTRB[nX][3],aStruTRB[nX][4])
	EndIf
Next nX

While (cAliasADA)->( !Eof() ) .And. (cAliasADA)->ADA_FILIAL==xFilial("ADA") .And. (cAliasADA)->ADA_CODCLI==cCliente .And. (cAliasADA)->ADA_LOJCLI==cLoja

	lValido := .T.
	
	While ( lValido .And. (cAliasADB)->( !Eof() ) .And. (cAliasADB)->ADB_FILIAL==xFilial("ADB") .And.;
			 (cAliasADB)->ADB_NUMCTR==(cAliasADA)->ADA_NUMCTR )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza o arquivo temporario com os dados do contrato              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (cAliasTRB)->( !MsSeek( (cAliasADA)->ADA_NUMCTR ) )
			If (cAliasADB)->ADB_QUANT > (cAliasADB)->ADB_QTDEMP
				RecLock(cAliasTRB,.T.)
				For nX := 1 To Len(aStruTRB)
					If (cAliasADA)->(FieldPos(aStruTRB[nX][1]))<>0
						(cAliasTRB)->(FieldPut(nX,(cAliasADA)->(FieldGet(FieldPos(aStruTRB[nX][1])))))
					EndIf
				Next nX
				MsUnLock()
			EndIf 
		EndIf
		
		(cAliasADB)->( DBSkip() )
		
	EndDo

	(cAliasADA)->( DBSkip() )

EndDo

(cAliasADA)->( DBCloseArea() )
	
PRIVATE aHeader := __aHeadTrbADA
xPesq := aPesq[(cAliasTRB)->(IndexOrd())][1]

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona registros                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DBSelectArea("SA1")
DBSetOrder(1)
MsSeek(xFilial("SA1")+cCliente+cLoja)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Calcula as coordenadas da interface                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize[1] /= 1.5
aSize[2] /= 1.5
aSize[3] /= 1.5
aSize[4] /= 1.3
aSize[5] /= 1.5
aSize[6] /= 1.2
aSize[7] /= 1.5

AAdd( aObjects, { 100, 020,.T.,.F.,.T.} )
AAdd( aObjects, { 100, 060,.T.,.T.} )
AAdd( aObjects, { 100, 020,.T.,.F.} )
aInfo   := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects,.T.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Interface com o usuario                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !(cAliasTRB)->(Eof())
	FATPDLoad(Nil,Nil,aPDFields)	 
	DEFINE MSDIALOG oDlg TITLE STR0024 FROM aSize[7],000 TO aSize[6],aSize[5] OF oMainWnd PIXEL //"Documentos de Origem"
	@ aPosObj[1,1],aPosObj[1,2] MSPANEL oPanel PROMPT "" SIZE aPosObj[1,3],aPosObj[1,4] OF oDlg CENTERED 
	cTexto1 := AllTrim(RetTitle("F2_CLIENTE"))+"/"+AllTrim(RetTitle("F2_LOJA"))+": "+SA1->A1_COD+"/"+SA1->A1_LOJA+"  -  "+RetTitle("A1_NOME")+": "+ FATPDObfuscate(SA1->A1_NOME,"A1_NOME")
	@ 002,005 SAY cTexto1 SIZE aPosObj[1,3],008 OF oPanel PIXEL  
	//cTexto2 := AllTrim(RetTitle("B1_COD"))+": "+SB1->B1_COD+"/"+SB1->B1_DESC 
	//@ 012,005 SAY cTexto2 SIZE aPosObj[1,3],008 OF oPanel PIXEL	
	@ aPosObj[3,1]+05,aPosObj[3,2]+00 SAY STR0025 PIXEL //"Pesquisar por:"
	@ aPosObj[3,1]+25,aPosObj[3,2]+00 SAY STR0026      PIXEL //"Localizar"
	@ aPosObj[3,1]+05,aPosObj[3,2]+40 MSCOMBOBOX oCombo VAR cCombo ITEMS aOrdem SIZE 100,044 OF oDlg PIXEL ;
		VALID ((cAliasTRB)->(dbSetOrder(oCombo:nAt)),(cAliasTRB)->(dbGotop()),xPesq := aPesq[(cAliasTRB)->(IndexOrd())][1],.T.)
	@ aPosObj[3,1]+25,aPosObj[3,2]+40 MSGET oGet VAR xPesq Of oDlg PICTURE aPesq[(cAliasTRB)->(IndexOrd())][2] PIXEL ;
		VALID ((cAliasTRB)->(MsSeek(Iif(ValType(xPesq)=="C",AllTrim(xPesq),xPesq),.T.)),.T.).And.IIf(oGetDb:obrowse:refresh()==NIL,.T.,.T.)
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],1,"Allwaystrue","allwaystrue","",.F., , ,.F., ,cAliasTRB)
	DEFINE SBUTTON FROM aPosObj[3,1]+005,aPosObj[3,4]-030 TYPE 1 ACTION (nOpcA := 1,oDlg:End()) ENABLE OF oDlg
	DEFINE SBUTTON FROM aPosObj[3,1]+025,aPosObj[3,4]-030 TYPE 2 ACTION (nOpcA := 0,oDlg:End()) ENABLE OF oDlg

	FATPDLogUser("FT400BCTR")

	ACTIVATE MSDIALOG oDlg CENTERED
	FATPDUnload()
Else
	Help(" ",1,"FT400NOCTR")  
EndIf
If nOpcA == 1
	cContrato := (cAliasTRB)->ADA_NUMCTR
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Restaura a integridade da rotina                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
RestArea(aArea)
SetFocus(nHandle)
Return(cContrato)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ft400Vend ³ Autor ³Henry Fila             ³ Data ³30.09.03  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao  do  Codigo  do  Vendedor                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Logico                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ft400Vend(cCampoGet,cConteudo)

Local aArea        := GetArea()
Local aAreaSA3     := SA3->(GetArea())
Local nNumVend     := Fa440CntVen()
Local cVendedores  := ""
Local nCntFor	   := 0
Local cVend		   := "1"
Local bVendedor    := {|x| "M->ADA_VEND"+x}
Local lRetorna     := .T.
Local cComissao    := ""
Local nEndereco    := 0

DEFAULT cCampoGet    := ReadVar()
DEFAULT cConteudo    := &(ReadVar())  

cConteudo := If(Empty(cConteudo),"",cConteudo)

dbSelectArea("SA3")
dbSetOrder(1)
If ( !MsSeek(xFilial("SA3")+cConteudo) )
	Help(" ",1,"REGNOIS")
	lRetorna := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Analisa todos os vendedores                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To nNumVend
	If ( !Empty(&(Eval(bVendedor,cVend))) .And. cCampoGet != EVAL(bVendedor,cVend) )
		cVendedores := cVendedores + "\" + &(EVAL(bVendedor,cVend))
	EndIf
	cVend  :=  Soma1(cVend,1)
Next nCntFor
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Valida o Vendedor, para nao  permitir duplicacao de codigos             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( cConteudo $ cVendedores )
	Help(" ",1,"VENDED")
	lRetorna	:= .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Coloca na tela o percentual da comissao conforme o vendedor   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF ( lRetorna )
	If ( !Empty(cConteudo) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza comissao       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cVend		 := Substr(cCampoGet,12,1)
		M->&("ADA_COMIS"+cVend) := SA3->A3_COMIS
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limpa campo de comissao      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cVend		 := Substr(cCampoGet,12,1)
		M->&("ADA_COMIS"+cVend) := 0.00
	EndIf
	cComissao :="M->ADA_COMIS" + cVend
	cComissao := Substr(cComissao,4,9)
	nEndereco := Ascan(aGets,{ |x| Subs(x,9,9) ==cComissao  } )
	If ( nEndereco > 0 )
		aTela[Val(Subs(aGets[nEndereco],1,2))][Val(Subs(aGets[nEndereco],3,1))*2] := If(!Empty(cConteudo),TransNum("A3_COMIS"),TransNum("ADA_COMIS"+cVend))
	EndIf
EndIf
RestArea(aAreaSA3)
RestArea(aArea)
Return(lRetorna)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A400Track ³ Autor ³ Kleber Dias Gomes     ³ Data ³23/12/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function A400Track()

Local aEnt     := {}
Local cNumCtr  := M->ADA_NUMCTR
Local nPosItem := GDFieldPos( "ADB_ITEM" )
Local nLoop    := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa a funcao fiscal                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aCols )
	AAdd( aEnt, { "ADB", cNumCtr + aCols[ nLoop, nPosItem ] } )
Next nLoop

MaTrkShow( aEnt )

Return( .T. )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
     
Private aRotina := {	{ STR0008,"AxPesqui"  ,0,1,0,.F.},;	// "Pesquisar"
							{ STR0009,"Ft400Alter",0,2,0,NIL},;	// "Visualizar"
							{ STR0010,"Ft400Inclu",0,3,0,NIL},;	// "Incluir"
							{ STR0011,"Ft400Alter",0,4,0,NIL},;	// "Alterar"
							{ STR0012,"Ft400Alter",0,5,0,NIL},;	// "Excluir"
							{ STR0013,"Ft400Alter",0,6,0,NIL},;	// "Copia"
							{ STR0032,"Ft400Alter",0,6,0,NIL},;	// "Encerrar"							
							{ STR0027,"Ft400Aprov",0,6,0,NIL},;	// "aProvar"
							{ STR0014,"Ft400Remes",0,6,0,NIL},;	// "Remessa"
							{ STR0029,"MsDocument",0,4,0,NIL},;	// "Conhecimento"	
							{ STR0015,"Ft400Legen",0,2,0,.F.}}	// "Legenda"


If nModulo == 67 
	AADD(aRotina,{ "Autoriza. Carrega.","Ft400AutC",0,6,0,NIL})
EndIf 

If ExistBlock("FT400MNU")
	ExecBlock("FT400MNU",.F.,.F.)
EndIf

Return(aRotina)

/*/{Protheus.doc} Ft400AutC
Chama tela de inclusão da rotina de Autorização de carregamento

@param: Nil
@author: Aecio Ferreira Gomes
@since: 20/02/2013
@Uso: FATA400
/*/
// -------------------------------------------------------------------------------------
Function Ft400AutC()
Local cContrato := ""

Private aheadGrade := {}
Private aColsGrade := {}
Private aheader    := {}
Private acols      := {}
Private n          := 1
	
cContrato := Ft400BCtr(ADA->ADA_CODCLI,ADA->ADA_LOJCLI)

If !Empty(cContrato)                                       

	// Chama tela de inclusão da autorização de carregamento
	AGRA890INC(cContrato)
EndIf	

Return

// -------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft400DsPro()
Inicializa a descrição do produto

@param: Nil
@author: Serviços CRM
@since: 27/10/2014
@Uso: FATA400
/*/
// -------------------------------------------------------------------------------------

Function Ft400DsPro()

Local nPosProd	:= 0
Local cDescProd	:= ""

If !INCLUI 

	nPosProd := aScan(aHeader,{|x| AllTrim(x[2])=="ADB_CODPRO"})
	
	If !Empty(aCols[n][nPosProd])
		cDescProd:= Posicione("SB1",1,xFilial("SB1")+ADB->ADB_CODPRO,"B1_DESC")
	EndIf	  

EndIf  

Return cDescProd                                                                                                                

// -------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft400LjCli()
Carrega vendedor vinculado ao cliente

@param: Nil
@author: Serviços CRM
@since: 27/10/2014
@Uso: FATA400
/*/
// -------------------------------------------------------------------------------------

Function Ft400LjCli()

Local aArea		:= GetArea()
Local cVendedor	:= ""
  
dbSelectArea("SA1")
dbSetOrder(1)
If (MsSeek(xFilial()+M->ADA_CODCLI+M->ADA_LOJCLI))
	If !Empty(SA1->A1_VEND)
		cVendedor := SA1->A1_VEND
		Ft400Vend("M->ADA_VEND1",cVendedor)
	EndIf
EndIf

RestArea(aArea)

Return cVendedor




//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDLoad
    @description
    Inicializa variaveis com lista de campos que devem ser ofuscados de acordo com usuario.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cUser, Caractere, Nome do usuário utilizado para validar se possui acesso ao 
        dados protegido.
    @param aAlias, Array, Array com todos os Alias que serão verificados.
    @param aFields, Array, Array com todos os Campos que serão verificados, utilizado 
        apenas se parametro aAlias estiver vazio.
    @param cSource, Caractere, Nome do recurso para gerenciar os dados protegidos.
    
    @return cSource, Caractere, Retorna nome do recurso que foi adicionado na pilha.
    @example FATPDLoad("ADMIN", {"SA1","SU5"}, {"A1_CGC"})
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDLoad(cUser, aAlias, aFields, cSource)
	Local cPDSource := ""

	If FATPDActive()
		cPDSource := FTPDLoad(cUser, aAlias, aFields, cSource)
	EndIf

Return cPDSource


//-----------------------------------------------------------------------------------
/*/{Protheus.doc} FATPDUnload
    @description
    Finaliza o gerenciamento dos campos com proteção de dados.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @author Squad CRM & Faturamento
    @since  05/12/2019
    @version P12.1.27
    @param cSource, Caractere, Remove da pilha apenas o recurso que foi carregado.
    @return return, Nulo
    @example FATPDUnload("XXXA010") 
/*/
//-----------------------------------------------------------------------------------
Static Function FATPDUnload(cSource)    

    If FATPDActive()
		FTPDUnload(cSource)    
    EndIf

Return Nil

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDObfuscate
    @description
    Realiza ofuscamento de uma variavel ou de um campo protegido.
	Remover essa função quando não houver releases menor que 12.1.27

    @type  Function
    @sample FATPDObfuscate("999999999","U5_CEL")
    @author Squad CRM & Faturamento
    @since 04/12/2019
    @version P12
    @param xValue, (caracter,numerico,data), Valor que sera ofuscado.
    @param cField, caracter , Campo que sera verificado.
    @param cSource, Caractere, Nome do recurso que buscar dados protegidos.
    @param lLoad, Logico, Efetua a carga automatica do campo informado

    @return xValue, retorna o valor ofuscado.
/*/
//-----------------------------------------------------------------------------
Static Function FATPDObfuscate(xValue, cField, cSource, lLoad)
    
    If FATPDActive()
		xValue := FTPDObfuscate(xValue, cField, cSource, lLoad)
    EndIf

Return xValue   


//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDLogUser
    @description
    Realiza o log dos dados acessados, de acordo com as informações enviadas, 
    quando a regra de auditoria de rotinas com campos sensíveis ou pessoais estiver habilitada
	Remover essa função quando não houver releases menor que 12.1.27

   @type  Function
    @sample FATPDLogUser(cFunction, nOpc)
    @author Squad CRM & Faturamento
    @since 06/01/2020
    @version P12
    @param cFunction, Caracter, Rotina que será utilizada no log das tabelas
    @param nOpc, Numerico, Opção atribuída a função em execução - Default=0

    @return lRet, Logico, Retorna se o log dos dados foi executado. 
    Caso o log esteja desligado ou a melhoria não esteja aplicada, também retorna falso.

/*/
//-----------------------------------------------------------------------------
Static Function FATPDLogUser(cFunction, nOpc)

	Local lRet := .F.

	If FATPDActive()
		lRet := FTPDLogUser(cFunction, nOpc)
	EndIf 

Return lRet  

//-----------------------------------------------------------------------------
/*/{Protheus.doc} FATPDActive
    @description
    Função que verifica se a melhoria de Dados Protegidos existe.

    @type  Function
    @sample FATPDActive()
    @author Squad CRM & Faturamento
    @since 17/12/2019
    @version P12    
    @return lRet, Logico, Indica se o sistema trabalha com Dados Protegidos
/*/
//-----------------------------------------------------------------------------
Static Function FATPDActive()

    Static _lFTPDActive := Nil
  
    If _lFTPDActive == Nil
        _lFTPDActive := ( GetRpoRelease() >= "12.1.027" .Or. !Empty(GetApoInfo("FATCRMPD.PRW")) )  
    Endif

Return _lFTPDActive

/*/{Protheus.doc} Ft400Rsrv
Reserva de Produtos para o contrato

@param: Nil
@author: Gustavo Baptista
@since: 01/2021
@Uso: FATA400
/*/
// -------------------------------------------------------------------------------------
Function Ft400Rsrv()
	Local aTmpHeader:= aheader
	Local aTmpCols:= acols
	Private aheader    := {}
	Private acols      := {}
	
	aRotina:={}		
	// Chama tela de inclusão de reserva de contrato
	UBSA050(aTmpHeader,aTmpCols)
	aRotina := MenuDef()
Return

// -------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft400VldLj()	
Valida a execução do gatilho do campo ADA_CODCLI.

@param: cCodCli Caracter, Código do cliente.

@Return lret - Retorna verdadeiro, caso o campo ADA_CODCLI não esteja vazio. 
@author: CRM/FAT
@since: 12/09/2025
/*/
// -------------------------------------------------------------------------------------

Function Ft400VldLj(cCodCli AS Character) As logical

	Local lRet	As Logical
	
	Default cCodCli = ""

	lRet	:= .F.

	If !Empty(M->ADA_CODCLI)
		lRet := .T.
	EndIf	
	
Return lRet
