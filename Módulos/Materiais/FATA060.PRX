#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA060.CH"
#INCLUDE "FWMVCDEF.CH"

PUBLISH MODEL REST NAME FATA060 source FATA060

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณProgram   ณFATA060   ณ Autor ณSergio Silveira        ณ Data ณ12/02/2001	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณCadastro de Grupos de representantes                       	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณNenhum                                                      	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณNenhum                                                      	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ PROGRAMADOR  ณ DATA   ณ BOPS ณ  MOTIVO DA ALTERACAO                   	ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Fata060()
Private cCadastro	:= STR0001
Private aRotina		:= MenuDef()

//----------------------------------
// Browse Grupos de representantes.     
//----------------------------------
BrowseDef( /*oMBrowse*/ )

Return(.T.)

//------------------------------------------------------------------------------
/*/{Protheus.doc} BrowseDef

Browse de Grupos de representantes.

@sample	BrowseDef( oMBrowse )

@param		oMBrowse	, Objeto	, Browse criado pelo Widget da Area de Trabalho.
		
@return	oMBrowse	, Objeto	, Retorna o objeto FWMBrowse.

@author	Anderson Silva
@since		05/12/2015
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function BrowseDef( oMBrowse )

Local oTableAtt		:= Nil
Local lWidget		:= .F.

Default oMBrowse	:= Nil

If Empty( oMBrowse )
	oMBrowse := FWMBrowse():New()
	oMBrowse:SetAlias('ACA')
Else
	lWidget := .T.
EndIf

oMBrowse:SetCanSaveArea(.T.) 
oMBrowse:SetDescription(STR0001) //"Grupo de Representantes"

If !lWidget 
	oTableAtt := TableAttDef()
	oMBrowse:SetAttach( .T. ) //Habilita as vis๕es do Browse
	oMBrowse:SetViewsDefault( oTableAtt:aViews )
	//Se nใo for SIGACRM inibe a exibi็ใo do grแfico
	If nModulo <> 73
		oMBrowse:SetOpenChart( .F. )
	EndIf
	
	oMBrowse:SetTotalDefault('ACA_FILIAL','COUNT',STR0016) // 'Total de Registros'
	oMBrowse:Activate()
EndIf

Return Nil

//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as vis๕es e grแficos.

@sample	TableAttDef()

@param		Nenhum

@return	ExpO - Objetos com as Visoes e Grแficos.

@author	Cristiane Nishizaka
@since		28/04/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt 	:= FWTableAtt():New()
// Vis๕es  
Local oAtivos		:= Nil // Equipes de Venda Ativas 
Local oInativos	:= Nil // Equipes de Venda Inativas

oTableAtt:SetAlias("ACA")

//----------
// Vis๕es
//---------- 

// Equipes de Venda Ativas 
oAtivos := FWDSView():New()
oAtivos:SetName(STR0014) //"Equipes de Venda Ativas"
oAtivos:SetID("Ativos") 
oAtivos:SetOrder(1) // ACA_FILIAL+ACA_GRPREP
oAtivos:SetCollumns({"ACA_GRPREP","ACA_DESCRI","ACA_GRPSUP"})
oAtivos:SetPublic( .T. )
oAtivos:AddFilter(STR0014, "ACA_MSBLQL == '2'") // "Equipes de Venda Ativas"

oTableAtt:AddView(oAtivos)

// Equipes de Venda Inativas
oInativos := FWDSView():New()
oInativos:SetName(STR0015) // "Equipes de Venda Inativas"
oInativos:SetID("Inativos") 
oInativos:SetOrder(1) // ACA_FILIAL+ACA_GRPREP
oInativos:SetCollumns({"ACA_GRPREP","ACA_DESCRI","ACA_GRPSUP"})
oInativos:SetPublic( .T. )
oInativos:AddFilter(STR0015, "ACA_MSBLQL == '1'") // "Equipes de Venda Inativas"

oTableAtt:AddView(oInativos)

Return (oTableAtt)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณModelDef  บAutor  ณVendas CRM          บ Data ณ  17/09/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine o modelo de dados do grupo de Representantes (MVC)   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA060                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ModelDef()

Local oModel		:= Nil
Local oStruACA	:= FWFormStruct(1,"ACA", /*bAvalCampo*/,/*lViewUsado*/ )
Local oStruA0A	:= FWFormStruct(1,"A0A", /*bAvalCampo*/,/*lViewUsado*/ )
Local bPosValid	:= { |oMdl| Ft060TudoOk( oMdl ) }
		
If nModulo == 73 
	oStruACA:SetProperty("ACA_ACCLIE",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACCLIE'))"))
	oStruACA:SetProperty("ACA_ACPROS",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACPROS'))"))
	oStruACA:SetProperty("ACA_ACAPON",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACAPON'))"))
	oStruACA:SetProperty("ACA_ACOPOR",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACOPOR'))"))
	oStruACA:SetProperty("ACA_ACSUSP",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACSUSP'))"))
	oStruACA:SetProperty("ACA_ACCONT",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACCONT'))"))
	oStruACA:SetProperty("ACA_ACPROD",MODEL_FIELD_VALID,FwBuildFeature( STRUCT_FEATURE_VALID,"Pertence('123456789A') .AND. Ft60VdCRM(FwFldGet('ACA_ACPROD'))"))
EndIf

oModel := MPFormModel():New("FATA060", /*bPreValidacao*/,bPosValid,/*bCommit*/,/*bCancel*/ )
oModel:AddFields("ACAMASTER",/*cOwner*/,oStruACA, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )
oModel:AddGrid("A0ADETAIL","ACAMASTER",oStruA0A,/*bLinePre*/,/*bLinePost*/,/*bPreVal*/,/*bPosVal*/,/*bLoad*/)

oModel:SetRelation("A0ADETAIL",{{"A0A_FILIAL","xFilial('A0A')"},{"A0A_CODACA","ACA_GRPREP"}},A0A->( IndexKey(1)))

oModel:GetModel( "A0ADETAIL" ):SetOptional( .T. )
oModel:GetModel( "A0ADETAIL" ):SetUniqueLine( { "A0A_CODUSR" } )

oModel:SetDescription(STR0001)

Return oModel

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณViewDef   บAutor  ณVendas CRM          บ Data ณ  17/09/10   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDefine a interface para cadastro de grupo de Representantes บฑฑ
ฑฑบ          ณem MVC.                                                     บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA060                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function ViewDef()

Local oView		:= Nil
Local oModel		:= FWLoadModel("FATA060")
Local oStruACA	:= FWFormStruct( 2,"ACA")
Local oStruA0A	:= Nil


If nModulo == 73 
	oStruA0A	:= FWFormStruct( 2,"A0A")
	oStruA0A:RemoveField("A0A_CODACA")
	oStruACA:RemoveField("ACA_ACPIPE")	
	oStruACA:RemoveField("ACA_ACESTR")
	oStruACA:RemoveField("ACA_MODCLI")
	oStruACA:RemoveField("ACA_MODPRO")	
	oStruACA:RemoveField("ACA_MODOPO")		
	oStruACA:RemoveField("ACA_MODEST")	
	oStruACA:RemoveField("ACA_LIBORC")
	oStruACA:RemoveField("ACA_PROPOS")
	oStruACA:RemoveField("ACA_SCRIPT")
	oStruACA:RemoveField("ACA_QUASUS")	
	oStruACA:RemoveField("ACA_ACMETA")
	oStruACA:RemoveField("ACA_ACPROS")
	oStruACA:RemoveField("ACA_ACSUSP")
	oStruACA:RemoveField("ACA_ACPROD")
	
	oStruACA:SetProperty("ACA_ACCLIE",	MVC_VIEW_FOLDER_NUMBER,"")
	oStruACA:SetProperty("ACA_ACAPON",	MVC_VIEW_FOLDER_NUMBER,"")
	oStruACA:SetProperty("ACA_ACOPOR",	MVC_VIEW_FOLDER_NUMBER,"")
	oStruACA:SetProperty("ACA_ACCONT",	MVC_VIEW_FOLDER_NUMBER,"")
EndIf

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField("VIEW_ACA",oStruACA,"ACAMASTER")
oView:CreateHorizontalBox("EQUIPE",100)	
oView:SetOwnerView("VIEW_ACA","EQUIPE") 
	
Return oView

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuno    |MenuDef   ณ Autor ณVendas CRM             ณ Data ณ08/12/06  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณ Funcao de defini็ใo do aRotina                             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณ aRotina   retorna a array com lista de aRotina             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณUso       ณ SIGAFAT                                                    ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Static Function MenuDef()

Local aRotina 		:= {}

ADD OPTION aRotina TITLE STR0002 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0
ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.FATA060'	OPERATION 2	ACCESS 0
ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.FATA060'	OPERATION 3	ACCESS 0
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.FATA060'	OPERATION 4	ACCESS 0
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.FATA060'	OPERATION 5	ACCESS 0

If IsInCallStack("FATA060")    
	aRotina:= CRMXINCROT("ACA",aRotina)
EndIf

Return(aRotina)

/*/

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFuncao    ณFt060Del  ณ Autor ณSergio Silveira        ณ Data ณ12/02/2001ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDescrio ณFuncao de Tratamento da Exclusao                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณRetorno   ณNenhum                                                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณParametrosณNenhum                                                      ณฑฑ
ฑฑณ          ณ                                                            ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ   DATA   ณ Programador   ณManutencao Efetuada                         ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ          ณ               ณ                                            ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
/*/
Function Ft060DEL( oModel )

Local lRet := .T.
Local nOperation := oModel:GetOperation()

If nOperation == MODEL_OPERATION_DELETE
	AO5->( DbSetOrder( 1 ) ) //AO5_FILIAL+AO5_ENTPAI+AO5_CODPAI+AO5_ENTANE+AO5_CODANE
	If AO5->( DbSeek( xFilial( "AO5" ) + "ACA" + ACA->ACA_GRPREP ) ) .Or. !( Empty( ACA->ACA_GRPSUP ) )
		lRet :=.F.
		Help( ,, 'FT060DEL',, STR0008, 1, 0 )//"Esta equipe nใo pode ser excluํda, pois estแ sendo utilizada em uma estrutura de neg๓cios."
	EndIf
EndIf

//--------------------------
// Exclusao de Anota็๕es
//--------------------------
If lRet .And. nModulo == 73
	CRMA090Del( "ACA", ACA->( Recno() ) )
EndIf

Return( lRet )

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFT060PermiบAutor  ณVendas CRM          บ Data ณ  05/09/08   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetorna as permissoes do usuario, de acordo com o grupo de  บฑฑ
ฑฑบ          ณrepresentantes.                                             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณEXPC1 - Codigo do usuario do sistema                        บฑฑ
ฑฑบ          ณEXPC2 - Codigo do campo a ter seu conteudo verificado       บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA060                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function FT060Permi(cUser, cCampo)

Local lInclui	:= .T.			// Se pode efetuar a inclusใo
Local lAltera	:= .T.			// Se pode efetuar a altera็ใo
Local lExclui	:= .T.			// Se pode efetuar a exclusใo
Local lVisualiza:= .T.			// Se pode efetuar a exclusใo
Local aArea		:= GetArea()	// Salva a area atual

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica se o usuแrio ้ um representante comercial.ณ
//ณe quais permiss๕es ele tem.                        ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If Select("SX2") > 0
	DbSelectArea("SA3")
	DbSetOrder(7)
	If SA3->(DbSeek( xFilial("SA3") + Alltrim(cUser) ))
		DbSelectArea("ACA")
		DbSetOrder(1)
		If ACA->(DbSeek( xFilial("ACA") + SA3->A3_GRPREP ))
			If ACA->(FieldGet(FieldPos(cCampo))) == "2"
				lInclui := lAltera := lExclui := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "4"
				lVisualiza := lExclui := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "5"
				lAltera := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "6"
				lInclui := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "7"
				lAltera := lExclui := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "8"
				lInclui := lExclui := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "9"
				lInclui := lAltera := .F.
			ElseIf ACA->(FieldGet(FieldPos(cCampo))) == "A"
				lInclui := lAltera := lExclui := .F.
			EndIf
		EndIf
	EndIf
EndIf

RestArea( aArea )

Return {lInclui, lAltera, lExclui, lVisualiza}

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFt60VdCRM บAutor  ณVendas CRM          บ Data ณ  10/11/13   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRetira a opcao 2 dos campos de permissoes.				  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณFATA060                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Ft60VdCRM(cOpcao)
Local lRetorno := .T.

If cOpcao == "2"
	Help("",1,STR0010,STR0011,STR0012,1)//"HELP""Acesso CRM""Op็ใo indisponํvel para o m๓dulo CRM. Para bloquear o acesso, por favor retire a op็ใo do menu CRM."
	lRetorno := .F.
EndIf

Return( lRetorno )

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} Ft060TudoOk

P๓s valida็ใo do modelo de dados - TudoOk

@sample     Ft060TudoOk( ) 

@param 		ExpO = Modelo de Dados

@return	ExpL = Verdadeiro ou falso      

@author     Thamara Villa 
@since      24/10/2015
@version    P12.1.7
/*/
//-----------------------------------------------------------------------------------------
Static Function Ft060TudoOk( oModel )

Local lRet := .T.

lRet := Ft060DEL( oModel )

Return( lRet )
