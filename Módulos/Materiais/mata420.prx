#INCLUDE "MATA420.CH"
#INCLUDE "PROTHEUS.CH"
STATIC lMta420
/*


Ŀ
Funo    MATA420    Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Atualiza o preco de venda do produto, baseado em uma das   
           planilhas de formacao de preco ou um porcentual informado  
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
Viviani       08/12/986854A Pergunta sobre decimais                 
Viviani       07/01/9919127APonto de Entrada MTA420                 
Viviani       08/01/99MelhorNova criaao de dialogos (Protheus)     


*/
Function MATA420
//Ŀ
// Cria variaveis do programa                                   
//
Local cSavScr3,nOpca
Local oDlg
Local cCadastro
Local aSays:={}, aButtons := {}

Private lComDecimais := .T.
Private lCalcProd:=.F.,nCasasPrcVen := 0
Private cProg    := "A420"

//Ŀ
// Nova forma de criar dialogos para processos Batch            
// COMPATIVEL COM PROTHEUS (BOF)                                
//
AADD(aSays,OemToAnsi( STR0002 ) )
AADD(aSays,OemToAnsi( STR0003 ) )
nOpca:= 0
a420Perg()
cCadastro:=OemToAnsi(STR0001)		//"Atualizao Preos de Venda"

AADD(aButtons, { 5,.T.,{|| a420Perg() } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1, o:oWnd:End() } } )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )

FormBatch( cCadastro, aSays, aButtons )

IF nOpcA == 1
	Processa({|lEnd| fa420Processa()})
Endif

/*


Ŀ
Funo    A420Proces Autor                         Data           
Ĵ
Descrio  Processamento                                              
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Static Function Fa420Processa()		

If lCalcProd
	If mv_par09 == 1
		//Ŀ
		// Envia para rotina que atualiza os precos p/planilha      
		//
		A420AtPlan(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par06,mv_par07,mv_par08)
	ElseIf mv_par09 == 2
		//Ŀ
		// Envia para rotina que atualiza os precos p/porcentual    
		//
		A420AtPorc(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par06,mv_par07,mv_par08)
	Endif
Else
	If mv_par06 == 1
		//Ŀ
		// Envia para rotina que atualiza os precos p/planilha X Cliente 
		//
		A420CliTab(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par07,mv_par08)
	Else
		//Ŀ
		// Envia para rotina que atualiza os precos p/Porcent X Cliente  
		//
		A420CliPor(mv_par01,mv_par02,mv_par03,mv_par04,mv_par05,mv_par07,mv_par08)
	Endif
Endif	
Return

/*

Ŀ
Funo    A420AtPlan Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Atualiza os precos dos produtos a partir de uma planilha de
           formacao de precos                                         
Ĵ
Sintaxe    A420AtPorc(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6,ExpC7,ExpN1)
Ĵ
Parametros ExpC1 = Produto Inicial                                    
           ExpC2 = Produto Final                                      
           ExpC3 = Grupo Inicial                                      
           ExpC4 = Grupo Final                                        
           ExpC5 = Tipo Inicial                                       
           ExpC6 = Tipo Final                                         
           ExpC7 = Tabela a Atualizar                                 
           ExpN1 = Atualiza Pedidos de Venda em Aberto                
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Static Function A420AtPlan(cProdIni,cProdFin,cGruIni,cGruFin,cTipIni,cTipFin,cTab,nAtu)
Local aArray := {},aPrecos:={},nSavRec
Local nX,nPos,cTabela,cPreco
Local lRet       := .T.
Local aAreaAnt   := {}
Local lMTA420FI := ExistBlock("MTA420FI")

//Ŀ
// Variaveis privadas exclusivas deste programa                 
//
//Ŀ
// Custo a ser considerado nos calculos                           
// 1 = STANDARD    2 = MEDIO     3 = MOEDA2     4 = MOEDA3        
// 5 = MOEDA4      6 = MOEDA5    7 = ULTPRECO   8 = PLANILHA      
//
Private nQualCusto := 1

//Ŀ
// Nome do arquivo que contem a memoria de calculo desta planilha 
//
Private cArqMemo

//Ŀ
// Direcao do calculo .T. para baixo .F. para cima                
//
Private lDirecao := .T.

While .T.
	
	pergunte("MTA421",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	// mv_par01 - Nome da planilha selecionada                      
	//
	If !pergunte("MTA421",.T.)
		Exit
	EndIf
	
	If !File(AllTrim(mv_par01)+".PDV")
		Help(" ",1,"MR430NOPLA")
		Exit
	EndIf
	
	//Ŀ
	// Posiciona no registro inicial mais proximo               
	//
	dbSelectArea("SB1")
	dbSeek(cFilial+cProdIni,.T.)
	ProcRegua(SB1->(RecCount()),21,6)
	//Ŀ
	// Inicializa o nome padrao da planilha                     
	//
	cArqMemo := mv_par01
	//Ŀ
	// Loop principal de atualizacao de precos p/porcentual     
	//
	While !Eof() .and. B1_FILIAL+B1_COD <= cFilial+cProdFin
		
		IncProc()
		//Ŀ
		// Consistencia de Produto, Grupo e Tipo                    
		//
		If (SB1->B1_GRUPO < cGruIni .or. SB1->B1_GRUPO > cGruFin) .or.;
				(SB1->B1_TIPO < cTipIni .or. SB1->B1_TIPO > cTipFin)
			dbSkip()
			Loop
		Endif
		
		If lMTA420FI
			aAreaAnt := GetArea()
			lRet := ExecBlock("MTA420FI",.F.,.F.,Alias())
			If ValType(lRet) # "L"
				lRet := .T.
			EndIf
			RestArea(aAreaAnt)
			If !lRet
				dbSkip()
				Loop
			EndIf
		EndIf
		
		//Ŀ
		// Direciona para a funcao que monta o array calculando os  
		// precos de venda do produto                               
		//
		nSavRec := RecNo()

		Pergunte("MTC010",.F.)
		aArray := MC010Forma("SB1",RecNo(),98)
		Pergunte("MTA421",.F.)
		//Ŀ
		// Procurar no array devolvido as linhas de preco de venda, 
		// ou seja , as linhas que contenham #n ,onde n e' o numero 
		// da tabela a atualizar.                                   
		//
		aPrecos:={}
		For nX := 1 To Len(aArray)
			nPos := At("#",aArray[nX][3])
			cTabela := SubStr(aArray[nX][3],nPos+1,1)
			If nPos > 0
				AADD(aPrecos,{cTabela,aArray[nX][6]})
			EndIf
		Next nX
		//Ŀ
		// Chamada da rotina de Gravacao de novos precos            
		//
		dbSelectArea("SB1")
		dbGoTo( nSavRec )
		For nX := 1 To Len(aPrecos)
			If aPrecos[nX,2] > 0
				cPreco := aPrecos[nX,1]
				If cPreco$cTab
					Begin Transaction
						A420Grava(0,nAtu,cPreco,aPrecos[nX,2])
					End Transaction
				EndIf
			EndIf
		Next nX
		dbSelectArea("SB1")
		dbSkip()
	End
	Exit
End

/*

Ŀ
Funo    A420AtPorc Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Atualiza os precos dos produtos a partir de um porcentual  
           informado                                                  
Ĵ
Sintaxe    A420AtPorc(ExpC1,ExpC2,ExpC3,ExpC4,ExpC5,ExpC6,ExpC7,ExpN1)
Ĵ
Parametros ExpC1 = Produto Inicial                                    
           ExpC2 = Produto Final                                      
           ExpC3 = Grupo Inicial                                      
           ExpC4 = Grupo Final                                        
           ExpC5 = Tipo Inicial                                       
           ExpC6 = Tipo Final                                         
           ExpC7 = Tabela a Atualizar                                 
           ExpN1 = Atualiza Pedidos de Venda em Aberto                
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Static Function A420AtPorc(cProdIni,cProdFin,cGruIni,cGruFin,cTipIni,cTipFin,cTab,nAtu)

//Ŀ
// Cria variaveis do programa                                   
//
Local cTabAtu := ""
Local nPos    := 0
Local lRet       := .T.
Local aAreaAnt   := {}
Local lMTA420FI := ExistBlock("MTA420FI")

While .T.
	
	pergunte("MTA422",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	// mv_par01 - Fator de Multiplicacao                            
	//
	If !pergunte("MTA422",.T.)
		Exit
	EndIf
	
	If  mv_par01 > 0
		
		//Ŀ
		// Posiciona no registro inicial mais proximo               
		//
		dbSelectArea("SB1")
		dbSeek(cFilial+cProdIni,.T.)
		
		//Ŀ
		// Loop principal de atualizacao de precos p/porcentual     
		//
		While !Eof() .and. B1_FILIAL+B1_COD <= cFilial+cProdFin
			
			//Ŀ
			// Consistencia de Produto, Grupo e Tipo                    
			//
			If (SB1->B1_GRUPO < cGruIni .or. SB1->B1_GRUPO > cGruFin) .or.;
					(SB1->B1_TIPO < cTipIni .or. SB1->B1_TIPO > cTipFin)
				dbSkip()
				Loop
			Endif
			
			If lMTA420FI
				aAreaAnt := GetArea()
				lRet := ExecBlock("MTA420FI",.F.,.F.,Alias())
				If ValType(lRet) # "L"
					lRet := .T.
				EndIf
				RestArea(aAreaAnt)
				If !lRet
					dbSkip()
					Loop
				EndIf
			EndIf

			//Ŀ
			// Chamada da rotina de Gravacao de novos precos            
			//
			cTabAtu := Alltrim(cTab)
			For nPos := 1 To Len(cTabAtu)
			Begin Transaction
				A420Grava(mv_par01,nAtu,Subs(cTabAtu,nPos,1))
			End Transaction
			Next nX
			
			dbSelectArea("SB1")
			dbSkip()
		End
	EndIf
	Exit
End

/*

Ŀ
Funo    A420CliTab Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Atualiza os precos dos produtos a partir de uma planilha de
           formacao de precos em relacao a um Cliente                 
Ĵ
Sintaxe    A420CliTab(ExpC1,ExpC2,ExpC3,ExpC4,ExpN1,ExpC5,ExpC6)      
Ĵ
Parametros ExpC1 = Produto Inicial                                    
           ExpC2 = Produto Final                                      
           ExpC3 = Cliente Inicial                                    
           ExpC4 = Cliente Final                                      
           ExpN1 = Atualiza Pedidos de Venda em Aberto                
           ExpC5 = Regio Inicial                                     
           ExpC6 = Regio Final                                       
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Static Function A420CliTab(cProdIni,cProdFin,cCliIni,cCliFin,nAtu,cRegIni,cRegFim)
Local nOpca,aArray := {},nPreco,nSavRec
Local nX
Local lRet       := .T.
Local aAreaAnt   := {}
Local lMTA420FI := ExistBlock("MTA420FI")

While .T.
	
	pergunte("MTA426",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	// mv_par01 - Tabela a Ser Atualizada                           
	//
	If !pergunte("MTA426",.T.)
		Exit
	EndIf
	
	//Ŀ
	// Posiciona no registro inicial mais proximo               
	//
	dbSelectArea("SA7")
	dbSetOrder(1)
	dbSeek(cFilial+cCliIni,.T.)
	//Ŀ
	// Loop principal de atualizacao de precos p/porcentual     
	//
	While !Eof() .and. A7_FILIAL+A7_CLIENTE <= cFilial+cCliFin
		
		//Ŀ
		// Consistencia de Produto                                  
		//
		If (SA7->A7_PRODUTO < cProdIni .or. SA7->A7_PRODUTO > cProdFin)
			dbSkip()
			Loop
		Endif

		If lMTA420FI
			aAreaAnt := GetArea()
			lRet := ExecBlock("MTA420FI",.F.,.F.,Alias())
			If ValType(lRet) # "L"
				lRet := .T.
			EndIf
			RestArea(aAreaAnt)
			If !lRet
				dbSkip()
				Loop
			EndIf
		EndIf

		//Ŀ
		// Consistencia de Regiao                                   
		//
		dbSelectArea("SA1")
		dbSetOrder(1)
		dbSeek(xFilial()+SA7->A7_CLIENTE+SA7->A7_LOJA)
		If SA1->A1_REGIAO < cRegIni .or. SA1->A1_REGIAO > cRegFim
			dbSelectArea("SA7")
			dbSkip()
			Loop
		Else
			dbSelectArea("SA7")
		Endif
		nPreco := 0
		dbSelectArea("SB5")
		cPreco := mv_par01
		If mv_par01 == "1"
			dbSelectArea("SB1")
			dbSeek(cFilial+SA7->A7_PRODUTO)
			nPreco := SB1->B1_PRV1
		Else
			dbSelectArea("SB5")
			dbSeek(cFilial+SA7->A7_PRODUTO)
			If !Eof()
				cVar := "B5_PRV"+mv_par01
				nPreco := If(FieldPos(cVar) >0,&cVar,0)
			EndIf
		EndIf
		
		If nPreco > 0
			Begin Transaction
				A420GrPrec(0,nPreco)
				If nAtu == 1
					A420GrPed()
				Endif
			End Transaction
		Endif
		
		dbSelectArea("SA7")
		dbSkip()
	End
	Exit
End

/*

Ŀ
Funo    A420CliPor Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Atualiza os precos dos produtos a partir de um porcentual  
           informado em Relacao a um Cliente                          
Ĵ
Sintaxe    A420CliPor(ExpC1,ExpC2,ExpC3,ExpC4,ExpN1,ExpC5,ExpC6)      
Ĵ
Parametros ExpC1 = Produto Inicial                                    
           ExpC2 = Produto Final                                      
           ExpC3 = Cliente Inicial                                    
           ExpC4 = Cliente Final                                      
           ExpN1 = Atualiza Pedidos de Venda em Aberto                
           ExpC5 = Regio Inicial                                     
           ExpC6 = Regio Final                                       
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Static Function A420CliPor(cProdIni,cProdFin,cCliIni,cCliFin,nAtu,cRegIni,cRegFim)
Local lRet       := .T.
Local aAreaAnt   := {}
Local lMTA420FI := ExistBlock("MTA420FI")

//Ŀ
// Cria variaveis do programa                                   
//

While .T.
	
	pergunte("MTA425",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	// mv_par01 - Porcentual de atualizacao                         
	//
	If !pergunte("MTA425",.T.)
		Exit
	EndIf
	
	//Ŀ
	// Posiciona no registro inicial mais proximo               
	//
	dbSelectArea("SA7")
	dbSetOrder(1)
	dbSeek(cFilial+cCliIni,.T.)
	
	//Ŀ
	// Loop principal de atualizacao de precos p/porcentual     
	//
	While !Eof() .and. A7_FILIAL+A7_CLIENTE <= cFilial+cCliFin
		
		//Ŀ
		// Consistencia de Produto                                  
		//
		If (SA7->A7_PRODUTO < cProdIni .or. SA7->A7_PRODUTO > cProdFin)
			dbSkip()
			Loop
		Endif

		If lMTA420FI
			aAreaAnt := GetArea()
			lRet := ExecBlock("MTA420FI",.F.,.F.,Alias())
			If ValType(lRet) # "L"
				lRet := .T.
			EndIf
			RestArea(aAreaAnt)
			If !lRet
				dbSkip()
				Loop
			EndIf
		EndIf

		//Ŀ
		// Consistencia de Regiao                                   
		//
		dbSelectArea("SA1")
		dbSetOrder(1)
		dbSeek(xFilial()+SA7->A7_CLIENTE+SA7->A7_LOJA)
		If SA1->A1_REGIAO < cRegIni .or. SA1->A1_REGIAO > cRegFim
			dbSelectArea("SA7")
			dbSkip()
			Loop
		Else
			dbSelectArea("SA7")
		Endif
		//Ŀ
		// Chamada da rotina de Gravacao de novos precos            
		//
		Begin Transaction
			If mv_par01 > 0
				A420GrPrec(mv_par01)
			Endif
			If nAtu == 1
				A420GrPed()
			Endif
		End Transaction
		dbSelectArea("SA7")
		dbSkip()
	End
	Exit
End
Return

/*

Ŀ
Funo    A420Grava  Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Grava o novo preco calculado no SB1,SB5 e SC6              
Ĵ
Sintaxe    A420Grava(ExpN1,ExpN2,ExpC1,ExpN3)                         
Ĵ
Parametros ExpN1 = Porcentual de aumento do preco                     
           ExpN2 = 1 - Deve atualizar os pedidos de venda             
                 = 2 - Nao atualiza os pedidos de venda               
           ExpC1 = String com a tabela a atualizar                    
           ExpN3 = Novo preco calculado pela planilha                 
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Function A420Grava(nPorcento,nAtu,cTab,nPreco)
Local nX,nDescPed,cData
//Ŀ
// Coloca o SC6 em ordem de codigo de produto                    
//
dbSelectArea("SC6")
dbSetOrder(2)
dbSelectArea("SB5")
dbSeek(cFilial+SB1->B1_COD)
If Subs(cTab,1,1) == "1"
	RecLock("SB1",.F.)
	If nPorcento == 0
		If lComDecimais
			Replace B1_PRV1 With nPreco
		Else
			Replace B1_PRV1 With Int(nPreco)
		EndIf					
	Else
		If lComDecimais
			Replace B1_PRV1 With (B1_PRV1 * nPorcento)
		Else
			Replace B1_PRV1 With Int(B1_PRV1 * nPorcento)
		EndIf
	EndIf	
	Replace B1_DTREFP1 With dDataBase
	MsUnLock()
Else
	dbSelectArea("SB5")
	If !Eof()
		cVar  := "B5_PRV"+cTab
		cData := "B5_DTREFP"+cTab
		If FieldPos(cVar) > 0
			RecLock("SB5",.F.)
			If nPorcento == 0
				If lComDecimais
					Replace &(cVar) With (nPreco)
				Else
					Replace &(cVar) With Int(nPreco)
				EndIf	
			Else
				If lComDecimais
					Replace &(cVar) With (&(cVar) * nPorcento)
				Else
					Replace &(cVar) With Int(&(cVar) * nPorcento)
				EndIf	
			EndIf
			If FieldPos(cData) > 0
				Replace &(cData) With dDataBase
			EndIf
			MsUnlock()
		EndIf
	EndIf
EndIf
//Ŀ
// Reajusta os Precos no SC5 e no SC6 para pedidos em Aberto            
//
If nAtu == 1 .And. !Empty(cTab)
	dbSelectArea("SC6")
	dbSeek(cFilial+SB1->B1_COD)
	While !Eof() .And. C6_FILIAL+C6_PRODUTO == cFilial+SB1->B1_COD
		//Ŀ
		// Verifica se a tabela do pedido e' igual a tabela a atualizar  
		//
		dbSelectArea("SC5")
		dbSeek(cFilial+SC6->C6_NUM)
		If C5_TABELA != cTab .Or. At(C5_TIPO,"CIP") != 0
			dbSelectArea("SC6")
			dbSkip()
			Loop
		Endif
		//Ŀ
		// Busca o Numero de Casas decimais do campo C6_PRCVEN           
		//
		If nCasasPrcVen == 0
			dbSelectArea("SX3")
			dbSetOrder(2)
			dbSeek("C6_PRCVEN")
			If Found()
				nCasasPrcVen := X3_DECIMAL
			EndIf
			dbSetOrder(1)
		EndIf
		//Ŀ
		// Atualiza o preco no pedido considerando Descontos no Pedido   
		// e Descontos no Item.                                          
		//
		dbSelectArea("SC6")
		If (C6_QTDENT+C6_QTDEMP) < C6_QTDVEN
			RecLock("SC6",.F.)
			If nPorcento == 0
				Replace C6_PRUNIT With nPreco
				Replace C6_PRCVEN With Round(A420Desc1(C6_PRUNIT) * (1-(C6_DESCONT/100)),nCasasPrcVen)
			Else
				If C6_PRUNIT != 0
					Replace C6_PRUNIT With (C6_PRUNIT * nPorcento)
					Replace C6_PRCVEN With Round(A420Desc1(C6_PRUNIT) * (1-(C6_DESCONT/100)),nCasasPrcVen)
					If C6_DESCONT != 0
						Replace C6_VALDESC With Round(Round(( A420Desc1(C6_PRUNIT) * C6_DESCONT / 100 ),2) * C6_QTDVEN,2)
					EndIf
				Else
					Replace C6_PRCVEN With (C6_PRCVEN * nPorcento)
				EndIf
			Endif
			Replace C6_VALOR With Round(C6_QTDVEN * C6_PRCVEN ,2)
			MsUnLock()
		Endif
		dbSkip()
	EndDo
Endif

//Ŀ
// Ponto de Entrada na atualizacao de precos                     
// Obs : So carrega lMta420 na primeira vez (STATIC)             
//
If lMta420 == Nil
	lMta420 := (existblock("MTA420"))
Endif

If lMta420
	Execblock("MTA420",.F.,.F.)
Endif

//Ŀ
// Devolve a ordem do SC6                                        
//
dbSelectArea("SC6")
dbSetOrder(2)

/*


Ŀ
Funo    MA420Plan  Autor  Eveli Morasco          Data  30/03/93 
Ĵ
Descrio  Verifica se a Planilha escolhida existe                    
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Function MA420Plan()
Local cArq := AllTrim(mv_par01)+".PDV"

If !File(cArq)
	Help(" ",1,"MR430NOPLA")
	Return .F.
EndIf
Return .T.

/*


Ŀ
Funo    A420Tabela Autor  Marcos Bregantim       Data  16/05/94 
Ĵ
Descrio  Devolve o Preco da Tabela sugerida no Pedido de Venda      
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Function A420Tabela()
Local nPreco := 0
Local bBloco := { |nV,nX| Trim(nV)+nX }

If M->C5_TABELA == "1"
	nPreco := SB1->B1_PRV1
Elseif Val(M->C5_TABELA) > 1
	nPreco := &(Eval(bBloco,"SB5->B5_PRV",M->C5_TABELA))
	If nPreco == 0
		If lComDecimais
			nPreco := SB1->B1_PRV1
		Else
			nPreco := Int(SB1->B1_PRV1)
		EndIf	
	Endif
EndIf
Return nPreco

/*

Ŀ
Funo    A420GrPed  Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Grava o novo preco calculado no SC6                        
Ĵ
Sintaxe    A420GrPed(ExpN1,ExpN2,ExpC1,ExpN3)                         
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Function A420GrPed()
Local nOrd
Local nPreco
Local nCntFor
Local nPosPreco

//Ŀ
// Busca o Numero de Casas decimais do campo C6_PRCVEN           
//
If nCasasPrcVen == 0
	dbSelectArea("SX3")
	dbSetOrder(2)
	dbSeek("C6_PRCVEN")
	If Found()
		nCasasPrcVen := X3_DECIMAL
	EndIf
	dbSetOrder(1)
EndIf

//Ŀ
// Reajusta os Precos no SC6 para pedidos em Aberto                     
//
dbSelectArea("SC6")
nOrd := IndexOrd()
dbSetOrder(2)
dbSeek(cFilial+SA7->A7_PRODUTO)

While !Eof() .And. C6_FILIAL+C6_PRODUTO == cFilial+SA7->A7_PRODUTO
	//Ŀ
	// Atualiza o preco no pedido                                    
	//
	dbSelectArea("SC6")
	If (C6_QTDENT+C6_QTDEMP) < C6_QTDVEN .And. C6_CLI == SA7->A7_CLIENTE
		
		RecLock("SC6",.F.)
		nPreco := 0
		For nCntfor := 12 To 1 Step -1
			If ( nPreco == 0 )
				nPosPreco := SA7->(FieldPos("A7_PRECO"+StrZero(nCntFor,2)))
				nPreco := If(SA7->(FieldGet(nPosPreco))!=0,SA7->(FieldGet(nPosPreco)),nPreco)
			EndIf
		Next nCntFor
		Replace C6_PRUNIT With If(nPreco!=0,nPreco,SC6->C6_PRUNIT)
		Replace C6_PRCVEN With Round(A420Desc1(C6_PRUNIT) * (1-(C6_DESCONT/100)),nCasasPrcVen)
		
		If C6_DESCONT != 0
			Replace C6_VALDESC With Round(Round(( A420Desc1(C6_PRUNIT) * C6_DESCONT / 100 ),2) * C6_QTDVEN,2)
		EndIf
		Replace C6_VALOR With Round(C6_QTDVEN * C6_PRCVEN ,2)
		MsUnLock()
	Endif
	dbSkip()
EndDo

//Ŀ
// Devolve a ordem do SC6                                        
//
dbSelectArea("SC6")
dbSetOrder(nOrd)

/*

Ŀ
Funo    A420GrPrec Autor  Marcos Bregantim       Data  04/05/93 
Ĵ
Descrio  Grava o novo preco calculado no SA7                        
Ĵ
Sintaxe    A420GrPrec(ExpN1,ExpN2)                                    
Ĵ
Parametros ExpN1 = Porcentual de aumento do preco                     
           ExpN2 = Preco da Planilha de Precos                        
Ĵ
 Uso       MATA420                                                    
ٱ


*/
Function A420GrPrec(nPorcento,nPreco)
Local nj, cCampoRead, cCampoWrit, cFieldName
Local nPosicao

//Ŀ
// Grava Preco, Data Referencia do Preco de Venda em 
// SA7 para armazenar os 12 ultimos Precos.          	
//
RecLock("SA7",.F.)
If ( SA7->A7_PRECO12 != 0 )
	For nj := 1 TO 11
		dbSelectArea("SA7")
		cCampoWrit := "A7_PRECO"+StrZero(nj,2)
		cCampoRead := "A7_PRECO"+StrZero(nj+1,2)
		If ( FieldGet(FieldPos(cCampoRead)) != 0 )
			cFieldName := Eval(FieldBlock(cCampoRead))
			Eval(FieldBlock(cCampoWrit),cFieldName)
			cCampoWrit := "A7_DTREF"+StrZero(nj,2)
			cCampoRead := "A7_DTREF"+StrZero(nj+1,2)
			cFieldName := Eval(FieldBlock(cCampoRead))
			Eval(FieldBlock(cCampoWrit),cFieldName)
		EndIf
	Next nj
EndIf
For nj := 1 TO 11
	cCampoRead := "A7_PRECO"+StrZero(nj+1,2)
	cCampoWrit := "A7_PRECO"+StrZero(nj,2)
	nPosicao := FieldPos(cCampoRead)
	If ( FieldGet(nPosicao)==0 .Or. nj+1==12 )
		dbSelectArea("SA7")
		If ( nPorcento != 0 )
			FieldPut(nPosicao,FieldGet(FieldPos(cCampoWrit))*nPorcento)
		Else
			FieldPut(nPosicao,nPreco)
		EndIf
		nPosicao := FieldPos("A7_DTREF"+StrZero(nj+1,2))
		FieldPut(nPosicao,dDataBase)
		nj := 13
	EndIf
Next
MsUnLock()
/*

Ŀ
Funo    A420Desc1  Autor  Waldemiro L. Lustosa   Data  23.05.94 
Ĵ
Descrio  Retorna preco unitario com desconto cascata                
Ĵ
Uso        MatA420                                                    
ٱ


*/
Function A420Desc1(nValor)
Local bCpoDesc := { |x| "C5_DESC"+STR(x,1) }
Local i := 0

For i := 1 to 4
	If !Empty(SC5->&(Eval(bCpoDesc,i)))
		nValor := nValor * (1 - (SC5->&(Eval(bCpoDesc,i))/100))
	Endif
Next i
nValor := NoRound(nValor,nCasasPrcVen)
Return (nValor)

/*

Ŀ
Funo    A420Perg   Autor  Rodrigo de A. Sartorio Data  24.06.96 
Ĵ
Descrio  Chama as perguntes iniciais do programa.                   
Ĵ
Uso        MATA420                                                    
ٱ


*/
Function A420Perg()
pergunte("MTA423",.F.)
//Ŀ
// Carrega as perguntas selecionadas                            
//
//Ŀ
// mv_par01 - Produtos ou Clientes                                
// mv_par02 - Considera casas decimais                            
//
pergunte("MTA423",.T.)

If mv_par01 == 1
	lCalcProd:=.T.
Else
	lCalcProd:=.F.
EndIf

If mv_par02 == 2
	lComDecimais := .F.   // Desconsidera casas decimais
Endif

If mv_par01 == 1
	pergunte("MTA420",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	//
	//Ŀ
	// mv_par01 - Produto Inicial                                     
	// mv_par02 - Produto Final                                       
	// mv_par03 - Grupo Inicial                                       
	// mv_par04 - Grupo Final                                         
	// mv_par05 - Tipo Inicial                                        
	// mv_par06 - Tipo Final                                          
	// mv_par07 - Tabela a Atualizar                                  
	// mv_par08 - Atualiza Pedidos em aberto                          
	// mv_par09 - Utiliza Planilha ou Fator Multiplicativo            
	//
	pergunte("MTA420",.T.)
Else
	pergunte("MTA424",.F.)
	//Ŀ
	// Carrega as perguntas selecionadas                            
	//
	//Ŀ
	// mv_par01 - Produto Inicial                                     
	// mv_par02 - Produto Final                                       
	// mv_par03 - Cliente Inicial                                     
	// mv_par04 - Cliente Final                                       
	// mv_par05 - Atualiza Pedidos em aberto                          
	// mv_par06 - Utiliza Planilha ou Porcentual                      
	// mv_par07 - De  Regiao                                          
	// mv_par08 - Ate Regiao                                          
	//
	pergunte("MTA424",.T.)
Endif	
Return
