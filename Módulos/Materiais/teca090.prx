#Include 'Protheus.ch'
#INCLUDE "TECA090.CH"  
#INCLUDE 'FWMVCDEF.CH'

/*/


Ŀ
Program   TECA090    Autor Vendas e CRM            Data  09/03/12 
Ĵ
Descricao  Manutencao das Habilidades do Tecnico                      
Ĵ
Retorno    Nenhum                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function TECA090()

Local oBrowse
Private cCadastro:= STR0001		//"Habilidades dos TEcnicos"
Private aRotina := MenuDef()

oBrowse := FWMBrowse():New()
oBrowse :SetAlias('AA2')
oBrowse :SetDescription(STR0001) //"Habilidades dos TEcnicos"
oBrowse :Activate()
	
Return(Nil)

/*/

Ŀ
Funcao     MenuDef   Autor  Vendas e CRM           Data  09/03/12 
Ĵ
Descricao  Definicao do aRotina (Menu funcional)                      
Ĵ
Sintaxe    MenuDef()                                                  
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA090                                                    
ٱ


/*/

Static Function MenuDef()  

Local aRotina := {}

	//Ŀ
	// Define Array contendo as Rotinas a executar do programa      
	// ----------- Elementos contidos por dimensao ------------     
	// 1. Nome a aparecer no cabecalho                              
	// 2. Nome da Rotina associada                                  
	// 3. Usado pela rotina                                         
	// 4. Tipo de Transacao a ser efetuada                          
	//    1 - Pesquisa e Posiciona em um Banco de Dados             
	//    2 - Simplesmente Mostra os Campos                         
	//    3 - Inclui registros no Bancos de Dados                   
	//    4 - Altera o registro corrente                            
	//    5 - Remove o registro corrente do Banco de Dados          
	//    6 - Alteracao sem inclusao de registro                    
	//

ADD OPTION aRotina TITLE STR0002 ACTION 'PesqBrw'	 			OPERATION 1	ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0003 ACTION 'VIEWDEF.TECA090'	OPERATION 2	ACCESS 0 //"Visualizar"
ADD OPTION aRotina TITLE STR0004 ACTION 'VIEWDEF.TECA090'	OPERATION 3	ACCESS 0 //"Incluir"
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.TECA090'	OPERATION 4	ACCESS 0 //"Alterar"
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.TECA090'	OPERATION 5	ACCESS 0 //"Excluir"

Return (aRotina)

/*


Ŀ
Funcao    ModelDef   Autor Vendas CRM              Data 09/03/12  
Ĵ
Descricao  Definicao do modelo do cadastro Kit de atendimento    (MVC)
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA090                                                    
ٱ


*/
Static Function ModelDef()

// Cria a estrutura a ser usada no Modelo de Dados
Local oModel
Local oStruAA2		:= FWFormStruct(1,'AA2',{|cCampo| AllTrim(cCampo)+"|" $ "AA2_FILIAL|AA2_CODTEC|AA2_NOMTEC|"})
Local oStruGrid  		:= FWFormStruct(1,'AA2')
Local aAux	:= {}

aAux := FwStruTrigger("AA2_CODTEC","AA2_NOMTEC","At090NomTec()",.F.,Nil,Nil,Nil)
oStruAA2:AddTrigger(aAux[1],aAux[2],aAux[3],aAux[4])
                                           
oStruGrid:RemoveField("AA2_CODTEC")
oStruGrid:RemoveField("AA2_NOMTEC")

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New('TECA090',/*bPreValidacao*/,/*bPosValidacao*/,/*bCommit*/,/*bCancel*/)

// Adiciona ao modelo uma estrutura de formulario de edicao por campo
oModel:AddFields('AA2MASTER',/*cOwner*/,oStruAA2,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)

// Adiciona ao modelo uma estrutura de formulario de edicao por grid
oModel:AddGrid( 'AA2GRID','AA2MASTER',oStruGrid,/*bPreValidacao*/,/*bLinePost*/,/*bCarga*/,/*bPost*/) // Adiciona uma Grid ao modelo

// Faz relaciomanto entre os compomentes do model
oModel:SetRelation("AA2GRID",{{"AA2_FILIAL",'xFilial("AA2")'},{"AA2_CODTEC","AA2_CODTEC"}},AA2->(IndexKey(1)))

oModel:GetModel("AA2GRID"):SetUniqueLine({"AA2_HABIL"})

//Chave primaria para a entidade principal do modelo de dados.
oModel:SetPrimaryKey({'AA2_FILIAL','AA2_CODTEC'}) 

Return oModel

/*                               


ͻ
Programa  ViewDef    Autor  Vendas CRM       Data  12/03/12 
͹
Desc.     Define a interface para Manutencao do Kit de          
          atendimento(MVC).                                     
͹
Uso       TECA090                                               
ͼ


*/
Static Function ViewDef()

Local oView
Local oModel   	:= FWLoadModel('TECA090')
Local oStruAA2 	:= FWFormStruct(2,'AA2',{|cCampo| AllTrim(cCampo)+"|" $ "AA2_FILIAL|AA2_CODTEC|AA2_NOMTEC|"})
Local oStruGrid	:= FWFormStruct(2,'AA2')
Local oMdlGrid	:= oModel:GetModel('AA2GRID')
					 
oStruGrid:RemoveField("AA2_CODTEC")
oStruGrid:RemoveField("AA2_NOMTEC")

oView := FWFormView():New()
oView:SetModel(oModel)

oView:AddField('VIEW_CAB',oStruAA2,'AA2MASTER')
oView:AddGrid('VIEW_GRID',oStruGrid,'AA2GRID' )
oView:AddIncrementField('VIEW_GRID','AA2_ITEM')
 
oView:CreateHorizontalBox('SUPERIOR',8)
oView:CreateHorizontalBox('INFERIOR',92)

oView:SetOwnerView( 'VIEW_CAB','SUPERIOR' )
oView:SetOwnerView( 'VIEW_GRID','INFERIOR' )

Return (oView)
 
/*


Ŀ
Funcao    At090NomTe Autor Vendas CRM              Data 20/03/12  
Ĵ
Descricao  Preenche o FwStruTrigger com o nome do tecnico no MVC		   
Ĵ
Parametros                                                            
Ĵ
 Uso       TECA090                                                    
ٱ


*/
Function At090NomTec()

Local cNome := ""
Local oMdl := FwModelActive()
Local oMdlAA2 := oMdl:GetModel("AA2MASTER")

cNome := Alltrim( Posicione("AA1",1,xFilial("AA1")+oMdlAA2:GetValue("AA2_CODTEC"),"AA1_NOMTEC") )

Return( cNome )

/*/


Ŀ
Funcao    At090Grava Autor  Eduardo Riera          Data  22.12.98 
Ĵ
Descricao  Gravacao das Habilidades do Tecnico                        
Ĵ
Retorno    Indica se a gravacao foi efetuada ou nao                   
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function At090Grava(nOpcao)

Local nCntFor := 0
Local nUsado  := Len(aHeader)
Local lGravou := .F.
Local nCntFor2:= 0
Local nPosItem:= aScan(aHeader,{|x| AllTrim(x[2])=="AA2_ITEM" })
Local nPosHabi:= aScan(aHeader,{|x| AllTrim(x[2])=="AA2_HABIL" })

Do Case
	Case ( nOpcao <> 3 )
		If ( !Empty(M->CAT090TEC) )
			For nCntFor := 1 To Len(aCols)
				If ( !aCols[nCntFor][nUsado+1] .AND. !Empty(aCols[nCntFor][nPosHabi]) )
					dbSelectArea("AA2")
					dbSetOrder(1)
					If ( dbSeek(xFilial("AA2")+M->CAT090TEC+aCols[nCntFor][nPosItem]) )
						RecLock("AA2")
					Else
						RecLock("AA2",.T.)
					EndIf
					For nCntFor2 := 1 To nUsado
						If ( aHeader[nCntFor2][10] <> "V" )
							AA2->(FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2]))
						EndIf
					Next nCntFor2
					AA2->AA2_FILIAL 	:= xFilial("AA2")
					AA2->AA2_CODTEC 	:= M->CAT090TEC
					lGravou := .T.
				Else
					dbSelectArea("AA2")
					dbSetOrder(1)
					If ( dbSeek(xFilial("AA2")+M->CAT090TEC+aCols[nCntFor][nPosItem]) )
						RecLock("AA2")
						dbDelete()
					EndIf
				EndIf
			Next nCntFor
		EndIf
	OtherWise
		For nCntFor := 1 To Len(aCols)
			dbSelectArea("AA2")
			dbSetOrder(1)
			If ( dbSeek(xFilial("AA2")+M->CAT090TEC+aCols[nCntFor][nPosItem]) )
				RecLock("AA2")
				dbDelete()
			EndIf
		Next
EndCase
Return(lGravou)

/*/


Ŀ
Funcao    At090LinOk Autor  Eduardo Riera          Data  21.12.98 
Ĵ
Descricao  Validacao da LinhaOk                                       
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function At090LinOk()

Local nUsado 	:= Len(aHeader)
Local lRetorno	:= .T.
Local nPosHabil	:= aScan(aHeader,{|x| AllTrim(x[2])=="AA2_HABIL"})
Local nPosNivel	:= aScan(aHeader,{|x| AllTrim(x[2])=="AA2_NIVEL"})
Local nCntFor  	:= 0

//Ŀ
//Verifica os campos em Branco                                            
//
If ( !aCols[n][nUsado+1] )
	If ( Empty(aCols[n][nPosHabil]) .OR. Empty(aCols[n][nPosNivel]) ) .OR. Empty(M->CAT090TEC)
		Help(" ",1,"AT090LIN01")
		lRetorno := .F.
	EndIf
	//Ŀ
	//Verifica se as habilidades nao estao repetidas                          
	//
	For nCntFor := 1 To Len(aCols)
		If ( !aCols[nCntFor][nUsado+1] )
			If ( aCols[n][nPosHabil] == aCols[nCntFor][nPosHabil] .AND. n <> nCntFor)
				Help(" ",1,"AT090LIN02")
				lRetorno := .F.
			EndIf
		EndIf
	Next nCntFor
EndIf
Return(lRetorno)

/*/


Ŀ
Funcao    At090TudOk Autor  Eduardo Riera          Data  21.12.98 
Ĵ
Descricao  Validacao da TudoOk                                        
Ĵ
Retorno    Logico                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function At090TudOk()

Return(.T.)

