#INCLUDE "MATA050.CH"
#include "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"                 
#INCLUDE "FWEVENTVIEWCONSTS.CH"                            
#INCLUDE "FWADAPTEREAI.CH"

PUBLISH MODEL REST NAME MATA050 SOURCE MATA050

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATA050  ³ Autor ³ Eduardo Riera         ³ Data ³29.04.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de atualizacao de Transportadoras                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void MATA050(void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo Pim.³24/10/01³PYME  ³ Adequacao do fonte para utilizacao do    ³±±
±±³            ³        ³PYME  ³ Siga PyMe.                               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MATA050(aRotAuto,nOpcAuto)
Local cDel         := "M050Del()"
Local cOk	       := "M050TudOk()"
Local aButtons     := {}
Local aIntSA4      := {}
//-- Variavel usada para verificar se o disparo da funcao IntegDef() pode ser feita manualmente
Local lIntegDef    :=  FindFunction("GETROTINTEG") .And. FindFunction("FWHASEAI") .And. FWHasEAI("MATA050",.T.,,.T.)

PRIVATE aRotina    := MenuDef()

PRIVATE cCadastro  :=OemtoAnsi(STR0001)		//"Atualiza‡„o de Transportadoras"
PRIVATE aCpoAltSA4 := {} 					//Array utilizado na gravaçãoo do histórico de alteracões

Default nOpcAuto  := 3


aButtons := Ma050But(aButtons)

AxCadastro(	"SA4",;
			STR0001,;
			cDel,;
			cOk,;
			aRotina,;
			{|nOpc| Ma050Pre(nOpc), .T.},;
			{|nOpc| M050Int(1, nOpc, aIntSA4), .T. },;
			{|nOpc| Ma050TTS(nOpc)},;
			{|nOpc| M050Int(2, nOpc, aIntSA4), .T.,Iif(lIntegDef,FwIntegDef("MATA050"),.T.) },;
			aRotAuto,;
			nOpcAuto,;
			aButtons)
Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ M050DEL  ³ Autor ³ Armando M. Tessaroli  ³ Data ³ 21/05/2002 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de validacao de exclusao de transportadosa com movimen³±±
±±³          ³ to no atendimento do telemarketing                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA050                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function M050Del()
Local lM050Del   := .T.
Local nBusca
Local nRecnoSM0  := SM0->(RecNo())
Local aSM0CodFil := {}
Local aArea 	 := GetArea()
Local cAliasSC5  := ""
Local cAliasSA1  := ""
Local cAliasTB2	 := ""
Local cQuery     := ""
Local lRetPE     := .T.
Local lPodeApagar:= .F.

// Preenche um array com as filiais
DBSelectArea("SM0")
DBGoTop()
Do While ! Eof()
	If SM0->M0_CODIGO == cEmpAnt
		Aadd(aSM0CodFil, FWGETCODFILIAL)
	Endif
	DBSkip()
EndDo
DBGoTo(nRecnoSM0)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe algum movimento desta entidade no telemarketing ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DBSelectArea("SUC")
DBSetOrder(2)
aFiliais := If(!Empty(xFilial()) .and. Empty(xFilial("SA4")), aClone(aSM0CodFil), {xFilial()})
For nBusca := 1 to Len(aFiliais)
   	If DBSeek(aFiliais[nBusca]+'SA4'+SA4->A4_COD)
   	   MsgStop(OemToAnsi(STR0004)) // 'Este fornecedor possui movimento de telemarketing e nao podera ser excluido'
   	   lM050Del := .F.
    EndIf
Next

// Verifica se existe Atendimento no Teleatendimento ADE
If lM050Del .And. FindFunction("Tk510TAxEn")
	lPodeApagar := Tk510TAxEn("SA4", SA4->A4_FILIAL, SA4->A4_COD)
	If !lPodeApagar
		lM050Del := .F.
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se existe Pedido de Venda                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If lM050Del
	cAliasSC5 := GetNextAlias()
	cQuery    := ""

	cQuery += "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "SC5" ) + " "
	cQuery += "WHERE "
	cQuery += "C5_FILIAL='" + xFilial( "SC5" ) + "' AND "
	cQuery += "C5_TRANSP='" + SA4->A4_COD      + "' OR "
	cQuery += "C5_REDESP='" + SA4->A4_COD      + "' AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.F.,.T. )

	If (cAliasSC5)->QTDBASE > 0
		Help(" ",1,"NODELETA")
		lM050Del := .F.
	Endif

	dbSelectArea(cAliasSC5)
	dbCloseArea()
	dbSelectArea("SC5")
	EndIf
	
   If lM050Del
	   cAliasSA1 := GetNextAlias()
	   
		cQuery := "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "SA1" ) + " "
	cQuery += "WHERE "
	cQuery += "A1_FILIAL='" + xFilial( "SA1" ) + "' AND "
	cQuery += "A1_TRANSP='" + SA4->A4_COD      + "' AND "
	cQuery += "D_E_L_E_T_=' '"

	cQuery := ChangeQuery( cQuery )
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1,.F.,.T. )

	If (cAliasSA1)->QTDBASE > 0
		Help(" ",1,"NODELETA")
		lM050Del := .F.
	Endif

	(cAliasSA1)->(dbCloseArea())
EndIf

If lM050Del
   cAliasTB2 := GetNextAlias()
   
	cQuery := "SELECT COUNT(*) QTDBASE FROM " + RetSqlName( "TB2" ) + " "
	cQuery += "WHERE "
	cQuery += "TB2_FILIAL='" + xFilial( "TB2" ) + "' AND "
	cQuery += "TB2_CODTRA='" + SA4->A4_COD      + "' AND "
	cQuery += "D_E_L_E_T_=' '"
               
	cQuery := ChangeQuery( cQuery )
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTB2,.F.,.T. )

	If (cAliasTB2)->QTDBASE > 0
		Help(" ",1,"NODELETA")
		lM050Del := .F.
	Endif

	(cAliasTB2)->(dbCloseArea())
EndIf		

IF ExistBlock("MTA050E") .And. lM050Del
	lRetPE   := ExecBlock("MTA050E",.F.,.F.)
	lM050Del := IIf(ValType(lRetPE) <> "L",lM050Del,lRetPE) 
Endif


If lM050Del
	//-- Integração Protheus x GFE (EXCLUSAO)
	lM050Del := MATA050IPG(5)
Endif

RestArea(aArea)

Return(lM050Del)


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ M050ESTR ³ Autor ³ Henry Fila            ³ Data ³ 17/06/2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Funcao de validacao da estrutura para ser sempre DOCA        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA050                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function M050Estr()

Local lRet := .T.

If !Empty(M->A4_ESTFIS)
	DC8->(dbSetOrder(1))
	If DC8->(MsSeek(xFilial("DC8")+M->A4_ESTFIS))
		If DC8->DC8_TPESTR <> "5"
			Help(" ",1,"MT050ESTRU") //A estrutura fisica deve ser somente DOCA
			lRet := .F.
		Endif
	Else
		Help(" ",1,"REGNOIS")
		lRet := .F.		
	Endif
Endif

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡ao    ³Ma050But  ³ Autor ³ Henry Fila            ³ Data | 17/01/05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de botoes de usuario                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Ma050But(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA040                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma050But(aButtons)
Local   aUsrBut  := {}

If ExistBlock( "MA050BUT" )
	If Valtype( aUsrBut := Execblock( "MA050BUT", .F., .F. ) ) == "A"
		Default aButtons := {}
		AEval( aUsrBut, { |x| AAdd( aButtons, x ) } ) 
	EndIf
EndIf

Return aButtons
/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Ma050TTS  ³Autor  ³ Eduardo Riera         ³ Data ³25/08/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Executado apos a gravacao dos dados e durante a transacao    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina é chamada apos a confirmação da interface e permi³±±
±±³          ³te completar dados na transacao do programa                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma050TTS(nOpc)

Local lRetorno := .T.
Local aAreaSA4   := GetArea("SA4")
Local lHistTab   := GetNewPar("MV_HISTTAB", .F.)
Local cFilialAIF := xFilial("AIF")
Local cFilialSA4 := xFilial("SA4")
Local nX		 := 0
Local cFuncTAF   := "TafIntegTranspUpd"
Local dDataAlt
Local cHoraAlt

If Altera		//Alteração
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravar o historico das alteracoes realizadas pelo usuario   ³
	//³ na tabela AIF usando o vetor aCpoAltSA4 que foi carregado   ³
	//³ na funcao M050TudOk().                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lHistTab .And. Len(aCpoAltSA4) > 0 .And. FindFunction("MSGrvHist")
		dDataAlt := Date()
		cHoraAlt := Time()
		For nX := 1 To Len(aCpoAltSA4)
			MSGrvHist(cFilialAIF,;			// Filial de AIF
			          cFilialSA4,;			// Filial da tabela SA4
			          "SA4",;					// Tabela SA4
			          "",;					// Codigo do cliente
			          "",;					// Loja do cliente
			          aCpoAltSA4[nX][1],;	// Campo alterado
			          aCpoAltSA4[nX][2],;	// Conteudo antes da alteracao
			          dDataAlt,;				// Data da alteracao
			          cHoraAlt,;				// Hora da alteracao
			          "",;					// Codigo do produto
			          SA4->A4_COD)			// Codigo da transportadora
		Next nX

	EndIf

	//Integração das alterações do cadastro de transportadora para o TAF
	If tlpp.ffunc(cFuncTAF)
		tlpp.call( cFuncTAF + "(SA4->A4_COD, aCpoAltSA4 )")
	Endif
	aCpoAltSA4 := {}

EndIf

If lRetorno .AND. ExistBlock("MA050TTS")
	ExecBlock("MA050TTS",.F.,.F.)
EndIf

// Exclusão do De/Para de Códigos
If nOpc == 5 .And. FindFunction("CFGA070Mnt") .And. FWXX4Seek("MATA050") .AND. FWHasEAI( "MATA050" /*cFunction*/, .T. /*lVerifySend*/, .F. /*lVerifyRec*/, .F. /*lVerifyUMess*/ )
	CFGA070Mnt( , 'SA4', 'A4_COD', ,SA4->A4_COD, .T. )
EndIf

RestArea(aAreaSA4)
// Integracao SIGATMS X SIGAGFE
If nOpc == 3
	nOpc := 4
EndIf

If FindFunction("MaEnvEAI")
	MaEnvEAI(,,nOpc,"MATA050",{ { "SA4", "MATA050_SA4" , NIL, NIL, NIL, NIL} } )
EndIf
Return (lRetorno)
                       
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A050CGC   ³ Autor ³ Marcio Menon	        ³ Data ³ 05/03/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Validacao do campo A4_CGC para permitir ou não a inclusão  ³±±
±±³          ³ de CPF ou CNPJ duplicados.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Cadastro de transportadoras                                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/ 
Function A050CGC(cCNPJ)

Local aArea     	:= GetArea()
Local aAreaSA4  	:= SA4->(GetArea())
Local lRetorno  	:= .T.
Local cCNPJBase 	:= ""
Local cMv_ValCNPJ 	:= GetNewPar("MV_VALCNPJ","1")
Local cMv_ValCPF 	:= GetNewPar("MV_VALCPF","1")
Local lAchou		:= .F.
Local lEleMesmo		:= .F.
DEFAULT cCNPJ   	:= &(ReadVar())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o tipo de pessoa                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(AllTrim(cCNPJ)) < 14
	cTipPes := "F"
Else
	cTipPes := "J"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida o tipo de pessoa                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cTipPes == "F" .AND. !(Len(AllTrim(cCNPJ))==11)
	Help(" ",1,"CPFINVALID")
	lRetorno := .F.
ElseIf cTipPes == "J" .AND. !(Len(AllTrim(cCNPJ))==14)  
	Help(" ",1,"CGC")     
	lRetorno := .F.
EndIf     
			
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Valida a duplicidade do CGC                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .AND. Pcount() > 0 
	If cTipPes == "J" .Or. Empty(cTipPes)
		dbSelectArea("SA4")
		dbSetOrder(3)
		lAchou := dbSeek(xFilial("SA4")+cCNPJ)
		If lAchou
			lEleMesmo := M->A4_COD == SA4->A4_COD
		EndIf
		If lAchou .AND. !lEleMesmo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³O parametro MV_VALCNPJ verifica se a validacao do CNPJ deve ser feita:                            ³
			//³1 = informando ao usuario que ja existe o CNPJ na base e verificando se deseja incluir mesmo assim³
			//³2 = nao permitindo que o usuario insira o mesmo CNPJ                                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cMv_ValCNPJ == "1" 
				If !_SetAutoMode()
					If Aviso(STR0007,STR0008+" "+SA4->A4_COD+" - "+AllTrim(SA4->A4_NOME)+" - "+AllTrim(RetTitle("A4_INSEST"))+": "+SA4->A4_INSEST,{STR0009,STR0010},2) == 2		//"Atenção"###"O CNPJ informado já foi utilizado na transportadora"###"Aceitar"###"Cancelar"
						lRetorno := .F.
					EndIf
				EndIf
			Else
				Aviso(STR0007,STR0008+" "+SA4->A4_COD+" - "+AllTrim(SA4->A4_NOME)+".",{"Ok"})		//"Atenção"###"O CNPJ informado já foi utilizado na transportadora"
				lRetorno := .F.
			Endif
		ElseIf lRetorno			
			cCNPJBase := SubStr(cCNPJ,1,8)
			dbSelectArea("SA4")
			dbSetOrder(3)
		  	If dbSeek(xFilial("SA4")+cCNPJBase) .And. M->A4_COD <> SA4->A4_COD
				If !_SetAutoMode()
					If Aviso(STR0007,STR0011+" "+SA4->A4_COD+" - "+AllTrim(SA4->A4_NOME)+".",{STR0009,STR0010},2) == 2		//"Atenção"###"A base do CNPJ foi utilizado em outra transportadora:"
					   lRetorno := .F.
					EndIf
				EndIf
			EndIf
		EndIf
	Else
		dbSelectArea("SA4")
		dbSetOrder(3)
		If dbSeek(xFilial("SA4")+cCNPJ) .And. M->A4_COD <> SA4->A4_COD
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³O parametro MV_VALCPF verifica se a validacao do CPF deve ser feita:                              ³
			//³1 = informando ao usuario que ja existe o CPF na base e verificando se deseja incluir mesmo assim ³
			//³2 = nao permitindo que o usuario insira o mesmo CPF                                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cMv_ValCPF == "1"
				If !_SetAutoMode()
					If Aviso(STR0007,STR0012+" "+SA4->A4_COD+" - "+AllTrim(SA4->A4_NOME)+".",{STR0009,STR0010},2) == 2		//"Atenção"###"O CPF informado já foi utilizado na transportadora"###"Aceitar"###"Cancelar"
					   lRetorno := .F.
					EndIf
				EndIf
			Else 
				Aviso(STR0007,STR0012+" "+SA4->A4_COD+" - "+AllTrim(SA4->A4_NOME)+".",{"Ok"})		//"Atenção"###"O CPF informado já foi utilizado na transportadora"
				lRetorno := .F.
			Endif
		EndIf		
	EndIf	
EndIf


If lRetorno .And. SA4->(ColumnPos('A4_TPTRANS')) > 0
	If cTipPes == 'F'
		M->A4_TPTRANS := '3' //-- Autonomo
	Else
		M->A4_TPTRANS := '1' //-- Inscrito
	EndIf
EndIf	
RestArea(aAreaSA4)
RestArea(aArea)

Return lRetorno

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ M050TudOk   ³ Autor ³  Marcio Menon		³ Data ³ 09/04/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validação dos campos após a confirmação da tela.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                             				  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA050                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M050TudOk()

Local aArea 	:= GetArea()
Local lRet      := .T. 
Local lHistTab  := GetNewPar("MV_HISTTAB", .F.)
Local bCampoSA4 := { |x| SA4->(Field(x)) }
Local nX		:= 0
Local lRetPt   := .T.
Local lM050TOK := ExistBlock("M050TOK")


// ====================================================================
// VERIFICA INTEGRACAO COM SIGATAF 
Local lIntTAF  := FindFunction("TAFExstInt") .AND. TAFExstInt()


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carregar o vetor aCpoAltSA4 com os campos que sofreram ³
//³ alguma alteracao. Somente serao gravados na tabela AIF ³
//³ apos a validacao completa da rotina AxCadastro()       ³
//³ executada pela funcao Ma050TTS().                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Altera .And. lHistTab .And. FindFunction("MSGrvHist")		//Alteração
	aCpoAltSA4 := {}
	For nX := 1 to SA4->(FCount())
		If !(M->&( eVal( bCampoSA4, nX) ) == SA4->(&( eVal( bCampoSA4,nX) )))
			aAdd( aCpoAltSA4, { eVal( bCampoSA4, nX), SA4->(&( eVal( bCampoSA4, nX) ) )} )
		EndIf
	Next nX
EndIf

If lRet .And. lM050TOK
	lRetPt := ExecBlock( "M050TOK",.F.,.F.)
	If ValType( lRetPt ) == "L"
		lRet := lRetPt
	EndIf
EndIf
RestArea(aArea)

If lRet
	//-- Integração Protheus x GFE (INCLUSAO,ALTERACAO)
	lRet := MATA050IPG( IIF(INCLUI,3,4) )
EndIf

// ==============================================
// Demetrio - 11/2014 - Integração TAF 
// ==============================================
If lRet .AND. lIntTAF
	MsgRun( "Relizando integração do produto com SIGATAF","Aguarde..." , {|| TAFIntOnLn("T003TRA",/*nOpc*/NIL,cFilAnt) } ) // "Aguarde" "Anotando registros para integração"
EndIf 

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³M050Int³ Autor ³ Vendas CRM               ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Realiza integracao com a criterium ou outra integracao       ³±±
±±³          ³que utiliza o framework do SIGALOJA de integracao.            ³±±
±±³          ³ O parâmetro aIntSB1 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³M050Int()                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Momento da chamada, sendo:                             ³±±
±±³          ³           1: Antes de qualquer alteração                     ³±±
±±³          ³           2: Depois das alterações                           ³±±
±±³          ³ExpN2: Opção da rotina                                        ³±±
±±³          ³ExpA3: Array contendo o número do registro e adaptador do SE4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function M050Int( nMomento, nOpc, aIntSA4 )
	Local lIntegra 		:= SuperGetMv("MV_LJGRINT", .F., .F.)	// Se há integração ou não
	Local aArea			:= GetArea()

	If lIntegra
		If nMomento == 1
			MsgRun( STR0014, STR0013, {|| M050IniInt( nOpc, aIntSA4 ) } ) // "Aguarde" "Anotando registros para integração"
		ElseIf nMomento == 2			
			MsgRun( STR0015, STR0013, {|| M050FimInt( nOpc, aIntSA4 ) } ) // "Aguarde" "Executando integração"
		EndIf
	EndIf
	
	If nMomento == 2
	     MATA050POS(nOpc)
	EndIf
	
	RestArea( aArea )
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³M050IniInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Faz o cache dos itens antes de serem excluídos, possibilitan-³±±
±±³          ³do o envio dos mesmos, mesmo após de serem apagados.          ³±±
±±³          ³ O parâmetro aIntSE4 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³M050IniInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SE4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function M050IniInt( nOpc, aIntSA4 )
	Local oFactory		:= LJCAdapXmlEnvFactory():New()
	Local cChave		:= ""
	
	// Se houver integração e não for inclusão, anota todos os registros para exclusão, caso algum seja excluído
	If nOpc != 3
		aIntSA4 :=	{ SA4->(Recno()), oFactory:Create( "SA4" ) }		
		cChave 	:= xFilial( "SA4" ) + SA4->A4_COD
	    aIntSA4[2]:Inserir( "SA4", cChave, "1", "5" )
	    aIntSA4[2]:Gerar()
	EndIf	
Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³M050FimInt   ³ Autor ³ Vendas CRM         ³ Data ³ 15/09/2009 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Envia os itens apagados e todos os outros itens.             ³±±
±±³          ³ O parâmetro aIntSE4 normalmente é vazio.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³M050FimInt()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Opção da rotina                                        ³±±
±±³          ³ExpA2: Array contendo o número do registro e adaptador do SA4.³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function M050FimInt( nOpc, aIntSA4 )
Local oFactory		:= LJCAdapXmlEnvFactory():New( )	// Cria a fabrica de Adaptadores de envio
Local cChave		:= ""

// Verifica se houve algum registro apagado, e gera a integração desse registro
If nOpc != 3
	// Procura pelo registro do cabeçalho
	SA4->(DbGoTo( aIntSA4[1] ) ) 
	
	// Se não encontrar, significa que o cabeçalho foi apagado, então envia somente a exclusão do cabeçalho
	If SA4->( Deleted() )
		aIntSA4[2]:Finalizar()
	EndIf
EndIf

// Independente de ter registros apagados ou não, gera quando não for exclusão, todos os outros registros
If nOpc != 5
	aIntSA4 := { SA4->( Recno() ), oFactory:Create( "SA4" ) }		
	cChave 	:= xFilial( "SA4" ) + SA4->A4_COD
    aIntSA4[2]:Inserir( "SA4", cChave, "1", cValToChar( nOpc ) )
    aIntSA4[2]:Gerar()
	aIntSA4[2]:Finalizar()
EndIf
Return
//-------------------------------------
/*	Modelo de Dados
@author  	Leandro Paulino / Jefferson Tomaz
@version 	P10 R1.4
@build		7.00.101202A
@since 		06/04/2011
@return 		oModel Objeto do Modelo*/
//-------------------------------------
Static Function ModelDef()

Local oModel 	   	:= Nil
Local oStructSA4	:= Nil
Local lIntGFE     	:= SuperGetMv("MV_INTGFE",,.F.)
Local lMile		  	:= IsInCallStack("CFG600LMdl") .Or. IsInCallStack("FWMILEIMPORT") .Or. IsInCallStack("FWMILEEXPORT")

If GetBuild() >= '7.00.101202A'

oStructSA4 := FwFormStruct(1,"SA4")

If lIntGFE
	oStructSA4:AddField( ;                    // Ord. Tipo Desc.
	"IBGE Compl"                     , ;      // [01]  C   Titulo do campo
	"Cod.IBGE Compl "                , ;      // [02]  C   ToolTip do campo
	"A4_CDIBGE"                      , ;      // [03]  C   Id do Field
	'C'                              , ;      // [04]  C   Tipo do campo
	7                                , ;      // [05]  N   Tamanho do campo
	0                                , ;      // [06]  N   Decimal do campo
	NIL                              , ;      // [07]  B   Code-block de validação do campo
	NIL                              , ;      // [08]  B   Code-block de validação When do campo
	NIL                              , ;      // [09]  A   Lista de valores permitido do campo
	NIL                              , ;      // [10]  L   Indica se o campo tem preenchimento obrigatório
	FwBuildFeature( STRUCT_FEATURE_INIPAD,'TMS120CdUf(SA4->A4_EST, "1") + SA4->A4_COD_MUN' ), ;   // [11]  B   Code-block de inicializacao do campo
	NIL                              , ;      // [12]  L   Indica se trata-se de um campo chave
	NIL                              , ;      // [13]  L   Indica se o campo pode receber valor em uma operação de update.
	.T.                              )        // [14]  L   Indica se o campo é virtual
	
	oStructSA4:AddTrigger( ;
	"A4_COD_MUN", ;                                                      // [01] Id do campo de origem
	"A4_CDIBGE" , ;                                                      // [02] Id do campo de destino
	{|| .T. }   , ;                                                      // [03] Bloco de codigo de validação da execução do gatilho
	&( ' { | oModel |  TMS120CdUf(M->A4_EST, "1") + M->A4_COD_MUN } ' ) )   // [04] Bloco de codigo de execução do gatilho
	
EndIf
             
// Tira validação da estrutura, mas se for chamada pelas rotinas do MILE, tem que ser feitas as validações
If !lMile
	oStructSA4:SetProperty( '*', MODEL_FIELD_VALID, FWBuildFeature( STRUCT_FEATURE_VALID, '.T.' ) )
EndIf
oStructSA4:SetProperty( '*', MODEL_FIELD_WHEN,  NIL )             

oModel:= MpFormModel():New("MATA050",/*bPreValid*/,/*bPosValid*/,/*Commit*/,/*Cancel*/)
oModel:AddFields("MATA050_SA4",Nil,oStructSA4,/*bPreValid*/,/*bPosValid*/,/*Carga*/)
oModel:SetDescription(NoAcentoCte(STR0001)) 
oModel:GetModel("MATA050_SA4"):SetDescription(NoAcentoCte(STR0001))
oModel:SetPrimaryKey({"A4_COD"})    

EndIf	

Return ( oModel )


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±º Funcao ³ IntegDef º Autor ³ Caio Murakami y Cruces º Data ³ 22/03/2012  º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Desc.  ³ Funcao de integracao com o adapter EAI para envio e            º±±
±±º        ³ recebimento do cadastro de transportadoras (SA4) utilizando    º±±
±±º        ³ o conceito de mensagem unica.                                  º±±
±±ÌÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±º Uso    ³ MATA050                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/    

Static Function IntegDef( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction, lJSon )

Local aRet := {}

Default lJSon := .F.

//a funcao integdef original foi transferida para o fonte mati030, conforme novas regras de mensagem unica.

If lJSon 
	aRet:= MATI050O( xEnt, nTypeTrans, cTypeMessage )
Else
	aRet:= MATI050( xEnt, nTypeTrans, cTypeMessage, cVersion, cTransaction )
Endif

Return aRet


//-----------------------------------------------------
/*/	Valid após a confirmação de inclusão/alteração/exclusão
@author Felipe Machado de Oliveira
@version P11
@since 18/04/2013
/*/
//------------------------------------------------------
Function MATA050IPG(nOperation)
Local lRet := .T.
Local lIntGFE := SuperGetMv("MV_INTGFE",.F.,.F.)
Local cIntGFE2 := SuperGetMv("MV_INTGFE2",.F.,"2")
	
//Integração Protheus com SIGAGFE
If lIntGFE == .T. .And. cIntGFE2 $ "1S"
	If !InterGU3T(nOperation)
		lRet := .F.
	EndIf	
EndIf
	
Return lRet
//-----------------------------------------------------
/*/	Integra a tabela SA4(Protheus) com GU3(SIGAGFE) a cada registro novo
@author Felipe Machado de Oliveira
@version P11
@since 18/04/2013
/*/
//------------------------------------------------------
Static Function InterGU3T(nOperation)
Local lRet       :=  .T.
Local aAreaGU3   := GU3->(GetArea())
Local cAliasGU3  := Nil
Local oModelGU3  := FWLoadModel("GFEA015")
Local nTpOpSetad := 0
Local cMsg       := ""
Local cIdTrans   := ""
Local cRet       := "2"
Local lNumProp   := SuperGetMv("MV_EMITMP",.F.,"0") == "1" .And. SuperGetMv("MV_INTGFE2",.F.,"2") == "1"
Local cCgcSA4    := IIF(nOperation != MODEL_OPERATION_DELETE,M->A4_CGC,SA4->A4_CGC)
Local cInsest    := IIF(nOperation != MODEL_OPERATION_DELETE,M->A4_INSEST,SA4->A4_INSEST)
Local lANTT      := GU3->(ColumnPos("GU3_ANTT")) > 0 .AND. SA4->(ColumnPos("A4_ANTT"))> 0 //Registro Nacional de Transportadores Rodoviários de Cargas
Local lA4_MSBLQL := SA4->(ColumnPos("A4_MSBLQL")) > 0

If lNumProp
	cAliasGU3 := GetNextAlias()
	BeginSql Alias cAliasGU3
		SELECT GU3.R_E_C_N_O_ RECNOGU3
		FROM %Table:GU3% GU3
		WHERE GU3.GU3_FILIAL = %xFilial:GU3%
		AND GU3.GU3_IDFED = %Exp:AllTrim(cCgcSA4)%
		AND GU3.GU3_IE = %Exp:AllTrim(cInsest)%
		AND GU3.GU3_SIT = '1'
		AND GU3.GU3_CLIEN = '2'
		AND GU3.GU3_FORN = '1'
		AND GU3.%NotDel%
	EndSql
	If (cAliasGU3)->(!Eof())
		GU3->(dbGoTo((cAliasGU3)->RECNOGU3))
		oModelGU3  := FWLoadModel("GFEA015")
		oModelGU3:SetOperation( MODEL_OPERATION_UPDATE )
		oModelGU3:Activate()
		
		If nOperation == MODEL_OPERATION_DELETE
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_CDTERP', " " )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', "2" )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , "2" )
			
			cRet := IIF(GU3->GU3_FORN == "2" .And. GU3->GU3_EMFIL == "2" .And. GU3->GU3_CLIEN == "2","2","1")
			If cRet <> "1" 
				oModelGU3:LoadValue( "GFEA015_GU3", "GU3_SIT", cRet )
			EndIf
			If lANTT
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_ANTT'  , '' )
			EndIf
		Else
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NATUR' ,  IIF(M->A4_TPTRANS == "3","F","J") )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_CDTERP', " " )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', "2" )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , "2" )
			If Empty(FwFldGet("GU3_CDTERP")) .And. (!lA4_MSBLQL .Or. (lA4_MSBLQL .And. M->A4_MSBLQL == '2'))
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_CDTERP', M->A4_COD )
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', IIF(M->A4_TPTRANS == "3","2","1") )
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , IIF(M->A4_TPTRANS == "3","1","2") )
			EndIf
		EndIf

		If lANTT .and. nOperation <> MODEL_OPERATION_DELETE
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_ANTT'  , M->A4_ANTT )
		EndIf

		If oModelGU3:VldData()
			oModelGU3:CommitData()
		Else
			cMsg := STR0017+CRLF+CRLF+oModelGU3:GetErrorMessage()[6]+oModelGU3:GetErrorMessage()[4]//"Inconsistência com o Frete Embarcador (SIGAGFE): "##
			lRet := .F.
		EndIf
		If !lRet
			Help( ,, STR0007,,cMsg, 1, 0 ) //"Atenção"
		EndIf
		oModelGU3:Deactivate()
	EndIf
	(cAliasGU3)->(dbCloseArea())
Else
	cIdTrans := RegraIdSA4(nOperation,@cMsg,@lRet)
	
	If FindFunction("GFECodEmit")
		cIdTrans := GFECodEmit(M->A4_COD, M->A4_EST, M->A4_CGC, cIdTrans)
	EndIf
	
	If lRet
		oModelGU3 := FWLoadModel("GFEA015")
		cAliasGU3 := GetNextAlias()
		BeginSql Alias cAliasGU3
			SELECT GU3.R_E_C_N_O_ RECNOGU3
			FROM %Table:GU3% GU3
			WHERE GU3.GU3_FILIAL = %xFilial:GU3%
			AND GU3.GU3_CDEMIT = %Exp:AllTrim(cIdTrans)%
			AND GU3.%NotDel%
		EndSql
		If (cAliasGU3)->(!Eof())
			oModelGU3:SetOperation( MODEL_OPERATION_UPDATE )
			nTpOpSetad := MODEL_OPERATION_UPDATE
			
			GU3->(dbGoTo((cAliasGU3)->RECNOGU3))
		Else
			oModelGU3:SetOperation( MODEL_OPERATION_INSERT )
			nTpOpSetad := MODEL_OPERATION_INSERT
		EndIf			
		oModelGU3:Activate()
		
		If nOperation <> MODEL_OPERATION_DELETE
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NMEMIT', M->A4_NOME )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NMFAN' , M->A4_NREDUZ )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NATUR' , IIF(M->A4_TPTRANS == "3","F","J") )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', "2" )
			oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , "2" )
			If Empty( FwFldGet("GU3_CDTERP")) .And. (!lA4_MSBLQL .Or. (lA4_MSBLQL .And. M->A4_MSBLQL == '2'))
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', IIF(M->A4_TPTRANS == "3","2","1") )
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , IIF(M->A4_TPTRANS == "3","1","2") )
			EndIf
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_ENDER' , M->A4_END )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_COMPL' , M->A4_COMPLEM )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_BAIRRO', M->A4_BAIRRO )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_CEP'   , M->A4_CEP )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NRCID' , TMS120CDUF(M->A4_EST,'1')+M->A4_COD_MUN )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_NMCID' , M->A4_MUN )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_UF'    , M->A4_EST )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_IDFED' , M->A4_CGC )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_IE'    , M->A4_INSEST )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_EMAIL' , M->A4_EMAIL )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_FONE1' , M->A4_TEL )
			oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_WSITE' , M->A4_HPAGE )
			
			If lANTT 
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_ANTT'  , M->A4_ANTT )
			EndIf
			
			If nTpOpSetad == MODEL_OPERATION_UPDATE
				If nOperation == MODEL_OPERATION_INSERT
					oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_SIT', "1" )
				EndIf
			Else
				oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_FILIAL', xFilial("SA4") )
				oModelGU3:SetValue( 'GFEA015_GU3', 'GU3_CDEMIT', cIdTrans )
				oModelGU3:LoadValue( "GFEA015_GU3", "GU3_ORIGEM", "2" )
			EndIf
		Else
			If nTpOpSetad <> MODEL_OPERATION_INSERT
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_TRANSP', "2" )
				oModelGU3:LoadValue( 'GFEA015_GU3', 'GU3_AUTON' , "2" )
				
				cRet := IIf(GU3->GU3_FORN == "2" .And. GU3->GU3_EMFIL == "2" .And. GU3->GU3_CLIEN == "2","2","1")
				If cRet <> "1" 
					oModelGU3:LoadValue( "GFEA015_GU3", "GU3_SIT", cRet )
				EndIf
			EndIf
		EndIf

		If oModelGU3:VldData()
			oModelGU3:CommitData()
		Else
			cMsg := STR0017+CRLF+CRLF+oModelGU3:GetErrorMessage()[6]+oModelGU3:GetErrorMessage()[4]//"Inconsistência com o Frete Embarcador (SIGAGFE): "##
			lRet := .F.
		EndIf
		oModelGU3:Deactivate()
		(cAliasGU3)->(dbCloseArea())
	EndIf
	If !lRet
		Help( ,, STR0007,,cMsg, 1, 0 ) //"Atenção"
	EndIf
EndIf
RestArea(aAreaGU3)
Return lRet
//-----------------------------------------------------
/*/	Verifica valores dos campos SA4 e retorna valor correspondente
@author Felipe Machado de Oliveira
@version P11
@since 18/04/2013
/*/
//------------------------------------------------------
Static Function RegraIdSA4(nOperation,cMsg,lRet) 
Local cRetCgc := ""

Default nOperation := MODEL_OPERATION_INSERT

If nOperation <> MODEL_OPERATION_UPDATE
	If nOperation <> MODEL_OPERATION_DELETE
		cRetCgc := M->A4_CGC
	Else
		cRetCgc := SA4->A4_CGC
	EndIf
Else
	If SA4->A4_CGC <> M->A4_CGC
		cMsg := STR0016 //"CNPJ/CPF (A4_CGC) não pode ser alterado por servir como identificador em outro módulo."
		lRet := .F.
		M->A4_CGC := SA4->A4_CGC 
	Else
		cRetCgc := M->A4_CGC
	EndIf
Endif

Return cRetCgc

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Aline Damasceno       ³ Data ³30/09/2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
Local aRotUser := {}
Local lPyme    := Iif(Type("__lPyme") <> "U",__lPyme,.F.)
PRIVATE aRotina:= FWLoadMenuDef("MATXATU") 

If !lPyme
	Aadd(aRotina,{OemToAnsi(STR0003),"MsDocument", 0 , 4})  //"Conhecimento"
EndIf
         
Aadd(aRotina,{OemToAnsi(STR0002),"FtContato", 0 , 4})  //"Contatos"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Adiciona rotinas ao aRotina                                  |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MA050ROT" )
	aRotUser := ExecBlock( "MA050ROT",.F.,.F.)
	If ValType( aRotUser ) == "A"
		AEval( aRotUser, { |x| aadd( aRotina, x ) } )
	EndIf
EndIf


Return(aRotina)

/*/{Protheus.doc} MATA050POS
A chamada desta função é feita na função M050Int e é executado somente quando a varivel nmomento == 2 
desta forma a sua execução é feita somente uma vez, ja que a função M050Int é chamada duas vezes no AxCadastro. 
A função MATA050POS so integra durante a inclusão ou alteração de registro, e se o MV_CPLINT/MV_CPLTRA esta ativo. 
@author Mohamed Saido Ba Djalo
@since 27/09/2016
@version version
@param nOpc
@return return, True
@example
(examples)
@see (links_or_references)
/*/

Function MATA050POS(nOpc)
	If (nOpc == 3 .Or. nOpc == 4) .And. SuperGetMv("MV_CPLINT",.F.,"2") == "1" .And. SuperGetMv("MV_CPLTRA",.F.,"2") == "1"
		CPLENVBATCH("SA4", nOpc)
	EndIf
Return .T.

/*/{Protheus.doc} Ma050Pre
Função utilizada para eftuar pré-vaidações, antes da abertura das rotinas de inclusão, alteração e exclusão.
@author Márcio Menon
@since 10/01/2017
@version 1.0
@param nOpc
@return Nulo
/*/
Static Function Ma050Pre(nOpc)

Local lMA050PRE := ExistBlock("MA050PRE")

If lMA050PRE
	ExecBlock("MA050PRE",.F.,.F.,nOpc)
EndIf

Return Nil
