#INCLUDE "MATA123.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

#DEFINE VALMERC  	 1  // Valor total do mercadoria
#DEFINE VALDESC 	 2  // Valor total do desconto
#DEFINE FRETE   	 3  // Valor total do Frete
#DEFINE VALDESP 	 4  // Valor total da despesa
#DEFINE TOTF1		 5  // Total de Despesas Folder 1
#DEFINE TOTPED		 6  // Total do Pedido
#DEFINE SEGURO		 7  // Valor total do seguro
#DEFINE TOTF3		 8  // Total utilizado no Folder 3
#DEFINE IMPOSTOS    9  // Array contendo Os Valores de Impostos Exibidos no ListBox
#DEFINE PACKING     10 // Valor total do packing
#DEFINE MAXGETDAD 999

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Mata123  ³Revisor³Nereu / Alexandre Lemes³Data  ³28/02/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Purchase Order                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void MATA123(ExpN1,ExpA1,ExpA2,ExpN2,ExpL1)                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array Cabecalho para Rotina Automatica             ³±±
±±³          ³ ExpA2 = Array Itens para Rotina Automatica                 ³±±
±±³          ³ ExpN2 = Opcao do aRotina para Rotina Automatica            ³±±
±±³          ³ ExpA1 = Apresenta a Dialog da Rotina em Rotina Automatica  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³Data    ³ BOPS     ³ Motivo da Alteracao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Jonathan Glz³22/07/15³PCREQ-4256³Se elimina la funcion AjustaSIX() y   ³±±
±±³            ³        ³          ³A123AjSX3() que hacen modificaciones  ³±±
±±³            ³        ³          ³a SX3 por motivo adecuacion a fuentes ³±±
±±³            ³        ³          ³a nuevas estructuras SX para v12.     ³±±
±±³Alf. Medrano³19/11/15³PCREQ-4263³ se realiza merge 12.1.8              ³±±
±±³Alf. Medrano³09/12/15³PCREQ-8917³ se redimenciona encabezado y grid    ³±±
±±³            ³        ³          ³ y pie de formulario                  ³±±
±±³A.Rodriguez ³03/07/18³DMINA-2951³Status PO vinculada a despacho, antes ³±±
±±³            ³        ³          ³de generar factura de entrada. COL    ³±±
±±³Thiago Berna³03/01/19³DMICNS-   ³Ajuste de numeracao de itens quando   ³±±
±±³            ³        ³5245      ³importado itens das solicitacoes      ³±±
±±³Eduardo Prz ³12/06/20³DMINA-9061³Ajuste en la funcion A123Forn para    ³±±
±±³            ³        ³          ³actualizar el campo Modalidad en PO   ³±±
±±³            ³        ³          ³al informar el proveedor.             ³±±
±±³Vero Flores ³16/07/20³DMINA-9280³Se realiza modificacion en la funcion ³±±
±±³            ³        ³          ³A123PID para la correcta visualizacion³±±
±±³            ³        ³          ³de la ventana de sol. de importacion  ³±±
±±³  Marco A.  ³27/10/20³DMINA-9555³Se modifica la funcion A123MovCampos()³±±
±±³            ³        ³          ³para agregar el valor del campo B1_TE,³±±
±±³            ³        ³          ³al campo C7_TES. (MI)                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Mata123(xAutoCab,xAutoItens,nOpcAuto,lWhenGet)

Local aFixe        := {{STR0001,"C7_NUM"},{STR0002,"C7_EMISSAO"},{STR0003,"C7_FORNECE"}} //"Numero do PC"#"Data Emissao"#"Fornecedor  "
Local aGrupo 	   := {}
Local aIndexSC7	   := {}
Local aCores       := {}
Local aCoresUsr    := {}
Local cFiltro	   := ""
Local cMt123Fil    := ""
Local nPos		   := 0
Local nX    	   := 0
Local bBlock
Local lImportOk    := SuperGetMv("MV_COMPINT") == .T. .And. SuperGetMv("MV_EASY") == "N" .And. cPaisLoc <> "BRA"

DEFAULT lWhenGet   := .F.

PRIVATE aRotina    := MenuDef()
PRIVATE aBackSC7   := {}
PRIVATE aAutoCab   := xAutoCab
PRIVATE aAutoItens := xAutoItens
PRIVATE bFiltraBrw := {|| Nil }
PRIVATE nTipoPed   := 3
PRIVATE cCadastro  := STR0004 //"Purchase Order"
PRIVATE l123Auto   := ValType(xAutoCab)=="A" .And. ValType(xAutoItens) == "A"
PRIVATE lPedido    := .T.
PRIVATE lGatilha   := .T.                          // Para preencher aCols em funcoes chamadas da validacao (X3_VALID)
PRIVATE lVldHead   := GetNewPar( "MV_VLDHEAD",.T. )// O parametro MV_VLDHEAD e' usado para validar ou nao o aCols (uma linha ou todo), a partir das validacoes do aHeader -> VldHead()

aAdd(aCores,    { '!Empty(C7_RESIDUO)'                      , 'BR_CINZA'})	 //-- Eliminado por Residuo
aAdd(aCores,    { 'C7_CONAPRO=="B".And.C7_QUJE < C7_QUANT'	, 'BR_AZUL' })	 //-- Bloqueado
aAdd(aCores,    { 'C7_QUJE==0 .And. C7_QTDACLA==0'   		, 'ENABLE'})	 //-- Pendente
aAdd(aCores,    { 'C7_QUJE<>0.And.C7_QUJE<C7_QUANT'			, 'BR_AMARELO'}) //-- Pedido Parcialmente Atendido
aAdd(aCores,    { 'C7_QUJE>=C7_QUANT'   					, 'DISABLE'})	 //-- Pedido Atendido
aAdd(aCores,    { 'C7_QTDACLA >0' 							, 'BR_LARANJA'}) //-- En proceso de recepcion

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica a permissao do programa em relacao aos modulos ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If AMIIn(2,10,11,14,12,41,97,98,69,72) .And. lImportOk

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ mv_par01 - Incluir na Amarracao ProdxFornecedor     ³
	//³ mv_par02 - Utiliza descri‡„o: Normal ou Cient¡fica  ³
	//³ mv_par03 - Filtra Comprador ? Sim    Nao            ³
	//³ mv_par04 - Tipo do Rodape - Por Aliquota/Por Imposto³
	//³ mv_par05 - Contabiliza On-Line? Sim/Nao             ³
	//³ mv_par06 - Aglutina Lct.Contab? Sim/Nao             ³
	//³ mv_par07 - Digita  Lct. Contab? Sim/Nao             ³
	//³ mv_par08 - Amarracao Produto X Fornecedor           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Pergunte("MTA123",.F.)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o tipo de rotina a ser executada           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case
	Case lWhenGet .Or. ( !l123Auto .And. nOpcAuto <> Nil )

		Do Case
		Case nOpcAuto == 3
			INCLUI := .T.
			ALTERA := .F.
		Case nOpcAuto == 4
			INCLUI := .F.
			ALTERA := .T.
		OtherWise
			INCLUI := .F.
			ALTERA := .F.
		EndCase

		dbSelectArea("SC7")
		nPos := Ascan(aRotina,{|x| x[4] == nOpcAuto})
		If ( nPos <> 0 )
			bBlock := &( "{ |x,y,z,k,m,n| " + aRotina[ nPos,2 ] + "(x,y,z,k,m,n) }" )
			Eval( bBlock, Alias(), (Alias())->(Recno()),nPos,Nil,Nil,lWhenGet)
		EndIf

	Case l123Auto

		DEFAULT nOpcAuto := 3
		MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SC7")

	OtherWise

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aciona a tecla de atalho da pergunte F12        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F12,{||A123Ativa()})
		If cPaisLoc $ "ARG|POR|EUA"
			If (ExistBlock("A123FILT"))
				cFileRdMk := AllTrim("A123FILT")
				nOrderSC7 := ExecBlock( cFileRdMk,.F.,.F.)
				If ValType( nOrderSC7 ) <> "N"
					nOrderSC7 := 10
				EndIf
			Else
				nOrderSC7 := 10
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa a funcao FilBrowse                                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SC7")
		dbSetOrder(1)

		cFiltro := "C7_FILIAL=='"+xFilial("SC7")+"'.And.C7_TIPO == 3 "

		If SuperGetMv("MV_RESTPED")=="S" .And. nTipoPed==3 .And. MV_PAR03 == 1
			aGrupo := UsrGrComp(RetCodUsr())
			If ( Ascan(aGrupo,"*") == 0 )
				cFiltro := "C7_FILIAL=='"+xFilial("SC7")+"' .And.C7_TIPO == 3 .And.(C7_GRUPCOM=='"+Space(Len(SC7->C7_GRUPCOM))+"'"
				For nX := 1 To Len(aGrupo)
					If nX == 1
						cFiltro += ".Or.C7_GRUPCOM $ '"+aGrupo[nX]+""
					Else
						cFiltro += ","+aGrupo[nX]
					Endif
				Next nX
				If Len(aGrupo) > 0
					cFiltro  += "'"
				Endif
				cFiltro += ")"
			EndIf
		EndIf

		If ExistBlock("MT123FIL")
			cMt123Fil := ExecBlock("MT123FIL",.F.,.F.)
			If ( ValType(cMt123Fil) == "C" ) .And. !Empty(cMt123Fil)
				cFiltro += IIf(Empty(cFiltro),"",".AND.")+cMt123Fil
			EndIf
		EndIf

		If !Empty(cFiltro)
			bFiltraBrw := {|| FilBrowse("SC7",@aIndexSC7,@cFiltro) }
			Eval(bFiltraBrw)
		EndIf

		If l123Auto
			dbSelectArea('SC7')
			nPos := Ascan(aRotina,{|x| x[4] == nOpcAuto})
			If ( nPos # 0 )
				bBlock := &( "{ |x,y,z,k,m,n| " + aRotina[ nPos,2 ] + "(x,y,z,k,m,n) }" )
				Eval( bBlock, Alias(), (Alias())->(Recno()),nPos,Nil,Nil,lWhenGet)
			EndIf
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Ponto de entrada para inclusão de nova COR da legenda       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( ExistBlock("MT123COR") )
				aCoresUsr := ExecBlock("MT123COR",.F.,.F.,{aCores})
				If ( ValType(aCoresUsr) == "A" )
					aCores := aClone(aCoresUsr)
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada para inclusao de novas opcoes no aRotina  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistTemplate("MT123BRW")
				ExecTemplate("MT123BRW",.F.,.F.)
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Endereca a funcao de BROWSE                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			mBrowse(006,001,022,075,"SC7",aFixe,,,,,aCores,,,,,,,,,,,,cFiltro)

		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desativa tecla que aciona perguntas                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F12,Nil)
		If ( Len(aIndexSC7)>0 )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Finaliza o uso da FilBrowse e retorna os indices padroes.  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			EndFilBrw("SC7",aIndexSC7)
		EndIf

	EndCase
Else
	Help(" ",1,"COMPINT")
EndIf

dbSelectArea("SC7")
dbSetOrder(1)
lPedido := .F.

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123Pedido³Revisor³Nereu / Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Rotina de Inclusao,Alteracao,Exclusao e Consulta dos       ³±±
±±³          ³ Pedidos de Compras e Autorizacoes de Entrega.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±³          ³ ExpC2 =                                                    ³±±
±±³          ³ ExpL1 = Ativa a opcao de copia                             ³±±
±±³          ³ ExpL2 = Apresenta a Dialog em rotina automatica            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Pedido(cAlias,nReg,nOpcX,xFiller,lCopia,lWhenGet)

Local aAreaSM0   := SM0->(GetArea())
Local aArea      := GetArea()
Local aRefImpos  := MaFisRelImp('MT100',{"SC7"})
Local aCpoValid  := {"C7_NUMSC","C7_ITEMSC","C7_DATPRF","C7_NUMCOT","C7_APROV","C7_QTDSOL","C7_RESIDUO"}
Local aCombo	 := {"CC-Collect","PP-Prepaid"}
Local aModal	 := {"1=House to House","2=House to Pier","3=Pier to House","4=Pier to Pier"}
Local aButtons   := {}
Local aObj[24]	 // Array com os objetos utilizados no Folder
Local aObj2[2]	 // Array 2 com objetos utilizados no Folder
Local aSizeAut   := MsAdvSize(,.F.,400)
Local aUsButtons := {}
Local aObjects	 := {}
Local aInfo 	 := {}
Local aPosGet	 := {}
Local aPosObj	 := {}
Local aPosObjPE  := {}
Local aStruSC7   := {}
Local aCt5       := {}

Local bCtbOnLine := {|| .T.}
Local bWhenCond  := {|| .T.}
Local bWhenMoed  := {|| .T.}

Local cItCop     := StrZero(0,Len(SC7->C7_ITEM))
Local cQuery     := ""
Local cPedido    := ""
Local cArqCtb    := ""
Local cLoteCtb   := ""
Local c652       := ""
Local c657       := ""
Local cWhenCond  := ""
Local cWhenMoed  := ""
Local cSeek      := ""
Local cWhile     := ""

Local nTam		 := 110
Local nPosAcols  := 18
Local nPosDespa	:= 081
Local nPosComp	:= 081
Local nPosExpo	:= 093
Local nOpcA		 := 0
Local nX         := 0
Local nY         := 0
Local nJ		 := 0
Local nHdlPrv    := 0
Local nTotalCtb  := 0
Local nTpRodape  := 0
Local nSaveSX8   := GetSX8Len()

Local lQuery     := .F.
Local l123Visual := .F.
Local l123Inclui := .F.
Local l123Deleta := .F.
Local l123Altera := .F.
Local lContinua	 := .T.
Local lGravaOk 	 := .T.
Local lCtbOnLine := .F.
Local lDigita    := .F.
Local lAglutina  := .F.
Local lWhenCond  := .T.
Local lWhenMoed  := .T.
Local lMt123Alt  := .T.
Local lMta123E   := .T.
Local lMt123GRV  := .T.
Local lMt123Scr  := ExistBlock("MT123SCR")
Local lMt123TEL  := ExistBlock("MT123TEL")
Local lMt123Get  := ExistBlock("MT123GET")
Local lPcFilEnt	 := SuperGetMv("MV_PCFILEN")
Local lGrade	 := MaGrade()

Local oDlg
Local oGetDados
Local oca123Forn
Local oca123Loj
Local oCond
Local oDescCond
Local oDescMoed
Local oGetMoeda
Local oDescForn
Local oca123Impo
Local oDescImpo
Local oca123Cons
Local oDescCons
Local oca123Compr
Local oDescCompr
Local oca123Exp
Local oDescExp
Local oca123LojEx
Local oca123Desp
Local oDescDesp
Local oca123Emb
Local oDescEmb
Local oca123Forw
Local oDescForw
Local oca123Via
Local oDescVia
Local oca123Ori
Local oca123Dest
Local oca123Inc
Local oca123Mod
Local oca123Pes
Local oca123Cub
Local oca123C20
Local oca123C40
Local oca123CHC
Local oca123Dpt
Local oDescDpt
Local aCtbDia := {}
Local nResHor   := IIF(!l123Auto,GetScreenRes()[1],0) //Tamanho resolucao de video horizontal
Local dDataBloq	:= GetNewPar("MV_ATFBLQM",CTOD("")) //Data de Bloqueio da Movimentação - MV_ATFBLQM
DEFAULT lCopia   := .F.
DEFAULT l123Auto := .F.

PRIVATE aRatAJ7     := {}
PRIVATE aA123PID    := {}
PRIVATE aColsBkp    := {}
PRIVATE aInfForn	:= {"","",CTOD(""),CTOD(""),"","",""}
PRIVATE aValores	:= {0,0,0,0,0,0,0,0,{{'','',0,0,0}},0,0}
PRIVATE aTitles     := {STR0011,; //"Totais"
						STR0012,; //"Inf. Fornecedor"
					 	STR0013,; //"Frete/Despesas"
					 	STR0014,; //"Descontos"
					 	STR0015,; //"Impostos"
					 	STR0016,; //"Mensagem/Reajuste"
					 	STR0048 } //Transporte
PRIVATE bPMSDlgPC	:= {||PmsDlgPC(nOpcx,cA120Num)}
PRIVATE cDescMsg	:= ""
PRIVATE cDescFor	:= ""
PRIVATE oFolder

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto para validar se continua ou nao a Rotina.                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MT123ALT")
	lMt123Alt := Execblock("MT123ALT",.F.,.F.,{aRotina[nOpcX,4]})
	If ValType( lMt123Alt ) == "L" .And. !lMt123Alt
		lContinua := .F.
	EndIf
EndIf

If lContinua

	Pergunte("MTA123",.F.)

	lWhenGet := IIf(ValType(lWhenGet) <> "L" , .F. , lWhenGet)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define a funcao utilizada ( Incl.,Alt.,Visual.,Exclu.)  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case
	Case aRotina[nOpcX][4] == 2
		l123Visual := .T.
	Case aRotina[nOpcX][4] == 3 .Or. lCopia
		l123Inclui	:= .T.
	Case aRotina[nOpcX][4] == 4
		l123Altera	:= .T.
	Case aRotina[nOpcX][4] == 5
		l123Deleta	:= .T.
		l123Visual	:= .T.
	EndCase

	If l123Inclui .Or. l123Altera .Or. l123Deleta
		lCtbOnLine := ((MV_PAR05==1.And.VerPadrao("652")) .Or. VerPadrao("657"))
		lAglutina  := MV_PAR06==1
		lDigita    := MV_PAR07==1
		nTpRodape  := MV_PAR04
	Else
		lCtbOnLine := .F.
		lAglutina  := .F.
		lDigita    := .F.
		nTpRodape  := 1
	EndIf

	PRIVATE bFolderRefresh:= {|| If(l123Auto,.T.,((A123Refresh(@aValores)),(A123FRefresh(aObj))))}
	PRIVATE bGDRefresh    := {|| If(l123Auto,.T.,(oGetDados:oBrowse:Refresh())) }
	PRIVATE bZeraDesc     := {|| (nDesc1:=0),(nDesc2:=0),(nDesc3:=0)}
	PRIVATE bRefresh      := {|| (A123VDesc(nDesc1,nDesc2,nDesc3,@aValores)),(Eval(bFolderRefresh))}
	PRIVATE bListRefresh  := {|| (MaFisToCols(aHeader,aCols,,"MT120")),(Eval(bRefresh),Eval(bGDRefresh)) }
	PRIVATE cA120Num   	  := '' //-- O Tratamento desta variavel serah feito logo abaixo...
	PRIVATE dA120Emis     := If(l123Inclui,CriaVar("C7_EMISSAO"),SC7->C7_EMISSAO)
	PRIVATE dA123Imp      := If(l123Inclui,dDataBase/*CriaVar("C7_DT_IMP")*/,SC7->C7_DT_IMP)
	PRIVATE dA123Cpe      := If(l123Inclui,dDataBase/*CriaVar("C7_CONF_PE")*/,SC7->C7_CONF_PE)
	PRIVATE cA120Forn     := If(l123Inclui.And. !lCopia,CriaVar("C7_FORNECE"),SC7->C7_FORNECE)
	PRIVATE cA120Loj      := If(l123Inclui.And. !lCopia,CriaVar("C7_LOJA"),SC7->C7_LOJA)
	PRIVATE cDescForn     := If(l123Inclui.And. !lCopia,CriaVar("A2_NOME"),Posicione("SA2",1,xFilial("SA2")+SC7->C7_FORNECE+SC7->C7_LOJA,"A2_NOME"))
	PRIVATE cA123Impo     := If(l123Inclui.And. !lCopia,CriaVar("C7_IMPORT"),SC7->C7_IMPORT)
	PRIVATE cDescImpo     := If(l123Inclui.And. !lCopia,CriaVar("DB7_NOME"),Posicione("DB7",1,xFilial("DB7")+SC7->C7_IMPORT,"DB7_NOME"))
	PRIVATE cA123Cons     := If(l123Inclui.And. !lCopia,CriaVar("C7_CONSIG"),SC7->C7_CONSIG)
	PRIVATE cDescCons     := If(l123Inclui.And. !lCopia,CriaVar("DB7_NOME"),Posicione("DB7",1,xFilial("DB7")+SC7->C7_CONSIG,"DB7_NOME"))
	PRIVATE cA123Compr    := If(l123Inclui.And. !lCopia,CriaVar("C7_COMPRA"),SC7->C7_COMPRA)
	PRIVATE cDescCompr    := If(l123Inclui.And. !lCopia,CriaVar("Y1_NOME"),Posicione("SY1",1,xFilial("SY1")+SC7->C7_COMPRA,"Y1_NOME"))
	PRIVATE cA123Exp      := If(l123Inclui.And. !lCopia,CriaVar("C7_EXPORTA"),SC7->C7_EXPORTA)
	PRIVATE cA123LojEx    := If(l123Inclui.And. !lCopia,CriaVar("C7_LOJAEXP"),SC7->C7_LOJAEXP)
	PRIVATE cDescExp      := If(l123Inclui.And. !lCopia,CriaVar("A2_NOME"),Posicione("SA2",1,xFilial("SA2")+SC7->C7_EXPORTA+SC7->C7_LOJAEXP,"A2_NOME"))
	PRIVATE cA123Desp     := If(l123Inclui.And. !lCopia,CriaVar("C7_DESP"),SC7->C7_DESP)
	PRIVATE cDescDesp     := If(l123Inclui.And. !lCopia,CriaVar("DB9_NOME"),Posicione("DB9",1,xFilial("DB9")+SC7->C7_DESP,"DB9_NOME"))
	PRIVATE cCondicao     := If(l123Inclui.And. !lCopia,CriaVar("C7_COND"),SC7->C7_COND)
	PRIVATE cDescCond     := If(l123Inclui.And. !lCopia,CriaVar("E4_DESCRI"),SE4->E4_DESCRI)
	PRIVATE cContato      := If(l123Inclui.And. !lCopia,CriaVar("C7_CONTATO"),SC7->C7_CONTATO)
	PRIVATE cFilialEnt    := If(l123Inclui.And. !lCopia,CriaVar("C7_FILENT"),SC7->C7_FILENT)
	PRIVATE cMsg          := If(l123Inclui.And. !lCopia,CriaVar("C7_MSG"),SC7->C7_MSG)
	PRIVATE cReajuste     := If(l123Inclui.And. !lCopia,CriaVar("C7_REAJUST"),SC7->C7_REAJUST)
	PRIVATE cTpCCPP      := If(l123Inclui.And. !lCopia,If(CriaVar("C7_FREPPCC",.T.)=="CC","CC-Collect","PP-Prepaid"),SC7->C7_FREPPCC+If(SC7->C7_FREPPCC="CC","-Collect","-Prepaid"))
	PRIVATE nDesc1        := If(l123Inclui.And. !lCopia,CriaVar("C7_DESC1"),SC7->C7_DESC1)
	PRIVATE nDesc2        := If(l123Inclui.And. !lCopia,CriaVar("C7_DESC2"),SC7->C7_DESC2)
	PRIVATE nDesc3	      := If(l123Inclui.And. !lCopia,CriaVar("C7_DESC3"),SC7->C7_DESC3)
	PRIVATE lNaturez      := SC7->(ColumnPos("C7_NATUREZ")) > 0
	PRIVATE cCodNatu      := If(lNaturez,If(l123Inclui .And. !lCopia,CriaVar("C7_NATUREZ"),SC7->C7_NATUREZ),"")
	PRIVATE cA123Emb      := If(l123Inclui.And. !lCopia,CriaVar("C7_AGENTE"),SC7->C7_AGENTE)
	PRIVATE cDescEmb      := If(l123Inclui.And. !lCopia,CriaVar("DB8_NOME"),Posicione("DB8",1,xFilial("DB8")+SC7->C7_AGENTE,"DB8_NOME"))
	PRIVATE cA123Forw     := If(l123Inclui.And. !lCopia,CriaVar("C7_FORWARD"),SC7->C7_FORWARD)
	PRIVATE cDescForw     := If(l123Inclui.And. !lCopia,CriaVar("DB8_NOME"),Posicione("DB8",1,xFilial("DB8")+SC7->C7_FORWARD,"DB8_NOME"))
	PRIVATE cA123Via      := If(l123Inclui.And. !lCopia,CriaVar("C7_TIPO_EM"),SC7->C7_TIPO_EM)
	PRIVATE cDescVia      := If(l123Inclui.And. !lCopia,CriaVar("DBF_DESCR"),Posicione("DBF",1,xFilial("DBF")+SC7->C7_TIPO_EM,"DBF_DESCR"))
	PRIVATE cA123Ori      := If(l123Inclui.And. !lCopia,CriaVar("C7_ORIGIMP"),SC7->C7_ORIGIMP)
	PRIVATE cA123Dest     := If(l123Inclui.And. !lCopia,CriaVar("C7_DEST"),SC7->C7_DEST)
	PRIVATE cA123Inc      := If(l123Inclui.And. !lCopia,CriaVar("C7_INCOTER"),SC7->C7_INCOTER)
	PRIVATE cA123Mod      := If(l123Inclui.And. !lCopia,CriaVar("C7_CONTAIN"),SC7->C7_CONTAIN)
	PRIVATE cA123Pes      := If(l123Inclui.And. !lCopia,CriaVar("C7_PESO_B"),SC7->C7_PESO_B)
	PRIVATE cA123Cub      := If(l123Inclui.And. !lCopia,CriaVar("C7_MT3"),SC7->C7_MT3)
	PRIVATE cA123C20      := If(l123Inclui.And. !lCopia,CriaVar("C7_CONTA20"),SC7->C7_CONTA20)
	PRIVATE cA123C40      := If(l123Inclui.And. !lCopia,CriaVar("C7_CONTA40"),SC7->C7_CONTA40)
	PRIVATE cA123CHC      := If(l123Inclui.And. !lCopia,CriaVar("C7_CON40HC"),SC7->C7_CON40HC)
	PRIVATE cA123Dpt      := If(l123Inclui.And. !lCopia,CriaVar("C7_ARMAZEM"),SC7->C7_ARMAZEM)
	PRIVATE cDescDpt      := If(l123Inclui.And. !lCopia,CriaVar("DBE_DESCR"),Posicione("DBE",1,xFilial("DBE")+SC7->C7_ARMAZEM,"DBE_DESCR"))
	PRIVATE aCols         := {}
	PRIVATE aHeader    	  := {}
	PRIVATE n             := 1
	PRIVATE nMoedaPed     := If(l123Inclui.And. !lCopia,1,Max(SC7->C7_MOEDA,1))
	PRIVATE cDescMoed     := SuperGetMv("MV_MOEDA"+AllTrim(Str(nMoedaPed,2)))
	PRIVATE nTxMoeda 	  := If(l123Inclui.And. !lCopia,0,SC7->C7_TXMOEDA)
	PRIVATE oGrade	      := MsMatGrade():New('oGrade',,"C7_QUANT",,'A123GValid()' ,,;
		{ 	{"C7_QUANT",.T., {{"C7_QTSEGUM",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),aCols[nLinha][nColuna],0,2) } }} },;
		{"C7_ITEM"   ,NIL,NIL},;
		{"C7_DATPRF" ,NIL,NIL},;
		{"C7_TOTAL"  ,NIL,NIL},;
		{"C7_QTDSOL" ,.T.,{ || A120Quant(aCols[nLinha][nColuna]) }},;
		{"C7_QTSEGUM",NIL, {{"C7_QUANT",{|| ConvUm(AllTrim(oGrade:GetNameProd(,nLinha,nColuna)),0,aCols[nLinha][nColuna],1) }}} },;
		{"C7_VLDESC" ,NIL,NIL}})
	PRIVATE aDesc := {}
	PRIVATE acDesc1
	PRIVATE acDesc2
	PRIVATE acDesc3
	PRIVATE lOpc		:= .T.
	PRIVATE nOldDesc1 := 0
	PRIVATE nOldDesc2 := 0
	PRIVATE nOldDesc3 := 0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tratamento para que o sistema nao gere documentos com numeracao saltada quando executadas via rotina automatica ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If l123Inclui
		If l123Auto .And. !Empty(aAutoCab[ProcH('C7_NUM'),2])
			cA120Num := aAutoCab[ProcH('C7_NUM'),2] //-- Considera o numero passado na rotina automatica
		Else
			cA120Num := CriaVar('C7_NUM', .T.) //-- Incrementa a numeracao quando o numero nao eh definido na rotina automatica
		EndIf
	Else
		cA120Num := SC7->C7_NUM
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Se integrado ao SIGAPMS adiciona o Botao                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If IntePms()
		aadd(aButtons,{'PROJETPMS',{||Eval(bPmsDlgPC)},STR0104 })
	Endif

	If !l123Visual
		aadd(aButtons,{"SOLICITA",     {|| a123PID(oGetDados,oCond,oDescCond) },STR0070,STR0106})	//"Solicitacoes"
		aadd(aButtons,{"PEDIDO",       {|| a123F4(oCond,oDescCond) }			,STR0074,STR0107})	//"Solicitacoes por item"
		aadd(aButtons,{"S4WB005N",     {|| A120ComView() }					    ,STR0091,STR0108})	//Historico de Produtos

		SetKey( VK_F4, { || A123PID(oGetDados,oCond,oDescCond) } )
		SetKey( VK_F5, { || A123F4(oCond,oDescCond) } )
	Else

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ATENCAO!!!Se for PYME retira a consulta a aprovacao do PC   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !__lPyme
			aadd(aButtons,{"BUDGET",   {|| a120Posic(cAlias,nReg,nOpcx)},STR0069,STR0110 }) //"Consulta Aprovacao"
		EndIf

		aadd(aButtons,{"S4WB005N",     {|| A120ComView() },              STR0091,STR0108 })  //Historico de Produtos

		If !AtIsRotina("A123TRACK")
			aadd(aButtons,{"BMPORD1",  {|| A123Track() },                STR0103, STR0109})  // "System Tracker"
		EndIf

	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Botao para exportar dados para EXCEL                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If FindFunction("RemoteType") .And. RemoteType() == 1
		aAdd(aButtons,{PmsBExcel()[1],{|| DlgToExcel({{"CABECALHO","",{RetTitle("C7_NUM") ,RetTitle("C7_EMISSAO"),RetTitle("C7_FORNECE"),RetTitle("C7_LOJA"),RetTitle("C7_COND"),RetTitle("C7_CONTATO"),RetTitle("C7_FILENT"),RetTitle("C7_MOEDA" ),RetTitle("C7_TXMOEDA")},{cA120Num,dA120Emis,cA120Forn,cA120Loj,cCondicao,cContato,cFilialEnt,nMoedaPed,nTxMoeda}},{"GETDADOS","",aHeader,aCols}})},PmsBExcel()[2],PmsBExcel()[3]})
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Avalia botoes do usuario                                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA123BUT" )
		If ValType( aUsButtons := ExecBlock( "MA123BUT", .F., .F. ) ) == "A"
			AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
		EndIf
	EndIf

	dbSelectArea("SX3")
	dbSetOrder(2)
	cWhenCond  :=If(MsSeek("C7_COND"),AllTrim(SX3->X3_WHEN),.T.)
	cWhenMoed  :=If(MsSeek("C7_MOEDA"),AllTrim(SX3->X3_WHEN),.T.)
	bWhenCond  := { || !l123Visual .And. VisualSX3('C7_COND') }
	bWhenMoed  := { || !l123Visual .And. VisualSX3('C7_MOEDA') }

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Valida a operacao com o pedido de compra ou AE                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !l123Inclui .Or. lCopia
		If l123Altera .Or. l123Deleta .Or. lCopia
			If !A123VldAlt()
				lContinua := .F.
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inicializa as veriaveis utilizadas na exibicao do Pedido   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A123Forn(SC7->C7_FORNECE,SC7->C7_LOJA,@aInfForn,.F.)
		A123CabOk(@oCond,@oca123Forn,@oca123Loj,aRefImpos)
		A123FormDesc(cMsg,@cDescMsg)
		A123FormReaj(cReajuste,@cDescFor)
		A123DescCnd(cCondicao,,@cDescCond)
		A123DescMoed(nMoedaPed,,@cDescMoed)
	EndIf

	If lContinua

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta aHeader e aCols utilizando a funcao FillGetDados.    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If l123Inclui .And. !lCopia

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpcX,"SC7",1,,,,,,,,,.T.,,,,,{|aHeaderX| A123AfterH(l123Auto,aHeaderX)} )
			aCols[1][aScan(aHeader,{|x| Trim(x[2])=="C7_ITEM"})] := StrZero(1,Len(SC7->C7_ITEM))

		Else

			nX      := 0
			nY      := 0
			cPedido := SC7->C7_NUM
			dbSelectArea("SC7")
			dbSetOrder(1)
			#IFDEF TOP
				aStruSC7 := SC7->(dbStruct())

				If !InTransaction() .And. !(l123Altera .Or. l123Deleta) .And. Empty(Ascan(aStruSC7,{|x| x[2]=="M"}))

					lQuery := .T.
					cQuery := "SELECT SC7.*,SC7.R_E_C_N_O_ SC7RECNO "
					cQuery += "FROM "+RetSqlName("SC7")+" SC7 "
					cQuery += "WHERE SC7.C7_FILIAL='"+xFilial("SC7")+"' AND "
					cQuery += "SC7.C7_NUM = '"+cPedido+"' AND "
					cQuery += "SC7.D_E_L_E_T_ = ' ' "
					cQuery += "ORDER BY "+SqlOrder(SC7->(IndexKey()))

					cQuery := ChangeQuery(cQuery)

					dbSelectArea("SC7")
					dbCloseArea()

				Else
			#ENDIF
				MsSeek(xFilial("SC7")+cPedido)
			#IFDEF TOP
				EndIf
			#ENDIF

			cSeek  := xFilial("SC7")+cPedido
			cWhile := "SC7->C7_FILIAL+SC7->C7_NUM"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Sintaxe da FillGetDados(nOpcX,Alias,nOrdem,cSeek,bSeekWhile,uSeekFor,aNoFields,aYesFields,lOnlyYes,cQuery,bMontCols,lEmpty,aHeaderAux,aColsAux,bAfterCols,bBeforeCols,bAfterHeader,cAliasQry |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FillGetDados(nOpcX,"SC7",1,cSeek,{|| &cWhile },,,,,cQuery,,,,, {|aColsX| A123AfterC(aColsX,aRefImpos,l123Altera,l123Deleta,lCopia,@lContinua,@cItCop,lGrade)},,,"SC7")

			If lQuery
				dbSelectArea("SC7")
				dbCloseArea()
				ChkFile("SC7",.F.)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Executa o Refresh nos valores de impostos.                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A123Refresh(@aValores)
		EndIf
		//Verifica se existe bloqueio contábil
		If lContinua .And. (l123Deleta .Or. l123Altera)
			lContinua := CtbValiDt(Nil,SC7->C7_EMISSAO,/*.T.*/ ,Nil ,Nil ,{"COM001"}/*,"Data de apuração bloqueada pelo calendário contábil."*/)
			If lContinua .And. !Empty(dDataBloq) .AND. (SC7->C7_EMISSAO <= dDataBloq)
				//Help(" ",1,"AF012ABLQM",,"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil.",1,0) //"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil."
				Help(" ",1,"ATFCTBBLQ") //P: Processo bloqueado pelo Calendário Contábil ou parâmetro de bloqueio nesta data ou período. S: Caso possível altere a data de referência do processo, verifique o parâmetro ou contate o responsável pelo Módulo Contábil.)
				lContinua := .F.
			End
		EndIf

		If lContinua

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Se o Recurso de Grade de Produtos estiver Ativado, o aCols   ³
			//³sera processado pela funcao aColsGrade e a MatxFis sera      ³
			//³Finalizada e Reiniciada para sincronizar o novo aCols formado³
			//³pela aColsGrade().                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lGrade
				If !l123Inclui .Or. lCopia
					aColsBkp := aClone(acols)
					aCols    := aColsGrade(oGrade, aCols, aHeader, "C7_PRODUTO", "C7_ITEM", "C7_ITEMGRD")
				Endif

				MaFisEnd()
				MaFisIni(cA120Forn,cA120Loj,"F","N",Nil,aRefImpos,,.T.,,,,,,,)

				For nX := 1 to Len(aCols)
					MaFisIniLoad(nX,,.T.)
					For nY	:= 1 To Len(aHeader)
						cValid	:= AllTrim(UPPER(aHeader[nY][6]))
						cRefCols := MaFisGetRf(cValid)[1]
						If !Empty(cRefCols) .AND. MaFisFound("IT",nX)
							MaFisLoad(cRefCols,aCols[nX][nY],nX)
						EndIf
					Next nY
					MaFisEndLoad(nX,1)
				Next nX

				MaFisAlt("NF_FRETE",avalores[FRETE])
				MaFisAlt("NF_DESPESA",avalores[VALDESP])
				MaFisAlt("NF_SEGURO",avalores[SEGURO])
				MaFisAlt("NF_DESCONTO",avalores[VALDESC])
//				MaFisAlt("NF_VALEMB",avalores[PACKING])
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Inicializa a gravacao dos lancamentos do SIGAPCO            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			PcoIniLan("000052")

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Tratamentos para Rotina automatica.                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If l123Auto

					//Verifica se existe bloqueio contábil
					If lContinua .And. (l123Deleta .Or. l123Altera)
						lContinua := CtbValiDt(Nil,dA120Emis,/*.T.*/ ,Nil ,Nil ,{"COM001"}/*,"Data de apuração bloqueada pelo calendário contábil."*/)
						If lContinua .And. !Empty(dDataBloq) .AND. (dA120Emis <= dDataBloq)
							//Help(" ",1,"AF012ABLQM",,"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil.",1,0) //"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil."
							Help(" ",1,"ATFCTBBLQ") //P: Processo bloqueado pelo Calendário Contábil ou parâmetro de bloqueio nesta data ou período. S: Caso possível altere a data de referência do processo, verifique o parâmetro ou contate o responsável pelo Módulo Contábil.)
							lContinua := .F.
						End
					EndIf

				If nOpcX <> 5
					aValidGet := {}
					If nOpcX == 3
						aAdd(aValidGet,{'cA120Num'    , If(Empty(aAutoCab[ProcH('C7_NUM'), 2]), cA120Num, aAutoCab[ProcH('C7_NUM'),2]), "CheckSX3('C7_NUM',cA120Num)", .T.})
						AADD(aValidGet,{"dA120Emis"   ,aAutoCab[ProcH("C7_EMISSAO"),2],"CheckSX3('C7_EMISSAO',dA120Emis)"        ,.T.})
						AADD(aValidGet,{"cA120Forn"   ,aAutoCab[ProcH("C7_FORNECE"),2],"CheckSX3('C7_FORNECE',cA120Forn)"        ,.T.})
						AADD(aValidGet,{"cA120Loj"    ,aAutoCab[ProcH("C7_LOJA"   ),2],"A123Forn(cA120Forn,cA120Loj,@aInfForn) .And. CheckSX3('C7_LOJA',cA120Loj)",.T.})
					EndIf
					AADD(aValidGet,{"cA123Impo"  ,aAutoCab[ProcH("C7_IMPORT"	),2],"CheckSX3('C7_IMPORT',	cA123Impo)"  ,.T.})
					AADD(aValidGet,{"cA123Compr" ,aAutoCab[ProcH("C7_COMPRA"	),2],"CheckSX3('C7_COMPRA',	cA123Compr)" ,.T.})
					AADD(aValidGet,{"cA123Via"   ,aAutoCab[ProcH("C7_TIPO_EM"	),2],"CheckSX3('C7_TIPO_EM',cA123Via)"   ,.T.})
					AADD(aValidGet,{"cA123Emb"   ,aAutoCab[ProcH("C7_AGENTE"	),2],"CheckSX3('C7_AGENTE',	cA123Emb)"   ,.T.})
					AADD(aValidGet,{"cA123Ori"   ,aAutoCab[ProcH("C7_ORIGIMP"	),2],"CheckSX3('C7_ORIGIMP',cA123Ori)"   ,.T.})
					AADD(aValidGet,{"cA123Dest"  ,aAutoCab[ProcH("C7_DEST"		),2],"CheckSX3('C7_DEST',	cA123Dest)"  ,.T.})
					AADD(aValidGet,{"cA123Inc"   ,aAutoCab[ProcH("C7_INCOTER"	),2],"CheckSX3('C7_INCOTER',cA123Inc)"   ,.T.})

					AADD(aValidGet,{"cCondicao"       ,aAutoCab[ProcH("C7_COND"   ),2],"CheckSX3('C7_COND',cCondicao)"           ,.T.})
					AADD(aValidGet,{"cContato"        ,aAutoCab[ProcH("C7_CONTATO"),2],"CheckSX3('C7_CONTATO',cContato)"         ,.F.})
					AADD(aValidGet,{"cFilialEnt"      ,aAutoCab[ProcH("C7_FILENT" ),2],"CheckSX3('C7_FILENT',cFilialEnt)",.T.})
					If ProcH("C7_MOEDA")<>0
						AADD(aValidGet,{"nMoedaPed"   ,aAutoCab[ProcH("C7_MOEDA"  ),2],"CheckSX3('C7_MOEDA',nMoedaPed)",.T.})
						AADD(aValidGet,{"nTxMoeda"    ,aAutoCab[ProcH("C7_TXMOEDA"),2],"CheckSX3('C7_TXMOEDA',nTxMoeda)",.T.})
					EndIf
					If lNaturez .And. ProcH("C7_NATUREZ")<>0
						Aadd(aValidGet,{"cCodNatu"    ,aAutoCab[ProcH("C7_NATUREZ"),2],"ExistCpo('SED')",.T.})
					EndIf

					If !lWhenGet
						nOpcA := 1
					Endif

					If ! SC7->(MsVldGAuto(aValidGet)) // consiste os gets
						nOpcA := 0
					EndIf
					If nOpcA == 0 .Or.!A123CabOk(@oCond,@oca123Forn,@ocA123Loj,aRefImpos)
						nOpcA := 0
					EndIf

					If ( nOpcA == 1 .Or. lWhenGet ) .And. l123Inclui
						MaFisIni(cA120Forn,cA120Loj,"F","N",Nil,aRefImpos,,.T.)
						If cPaisLoc == 'ARG'
							SA2->(DbSetOrder(1))
							SA2->(MsSeek(xFilial("SA2")+cA120Forn+cA120Loj))
							MaFisAlt('NF_SERIENF',LocXTipSer('SA2',MVNOTAFIS))
						Endif
					EndIf

					If nOpcA <> 0 .Or. lWhenGet
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Verifica o preenchimento do campo C7_ITEM                  ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						cItem := StrZero(1,Len(SC7->C7_ITEM))
						For nX := 1 To Len(aAutoItens)
							nY := aScan(aAutoItens[nX],{|x| AllTrim(x[1])=="C7_ITEM"})
							If nY == 0
								aadd(aAutoItens[nX],{"C7_ITEM",cItem,Nil})
							EndIf
							cItem := Soma1(cItem)
						Next nX

						If !MsGetDAuto(aAutoItens,"A123LinOk",{|| A123TudOk()},aAutoCab,aRotina[nOpcx][4])
							nOpcA := 0
						EndIf
					EndIf

					If nOpcA <> 0
						aValidGet := {}
						If ProcH("C7_FRETE")<>0
							AADD(aValidGet,{"aValores[3]" ,aAutoCab[ProcH("C7_FRETE"  ),2],"A123VFold('NF_FRETE',aValores[3])"                                    ,.F.})
						EndIf
						If ProcH("C7_DESPESA")<>0
							AADD(aValidGet,{"aValores[4]" ,aAutoCab[ProcH("C7_DESPESA"),2],'A123VFold("NF_DESPESA",aValores[4])'                                ,.F.})
						EndIf
						If ProcH("C7_SEGURO")<>0
							AADD(aValidGet,{"aValores[7]" ,aAutoCab[ProcH("C7_SEGURO" ),2],'A123VFold("NF_SEGURO",aValores[7])'                                  ,.F.})
						EndIf
						If ProcH("C7_DESC1")<>0
							AADD(aValidGet,{"nDesc1"      ,aAutoCab[ProcH("C7_DESC1"  ),2],"A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores)"                         ,.F.})
						EndIf
						If ProcH("C7_DESC2")<>0
							AADD(aValidGet,{"nDesc2"      ,aAutoCab[ProcH("C7_DESC2"  ),2],"A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores)"                         ,.F.})
						EndIf
						If ProcH("C7_DESC3")<>0
							AADD(aValidGet,{"nDesc3"      ,aAutoCab[ProcH("C7_DESC3"  ),2],"A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores)"                         ,.F.})
						EndIf
						If ProcH("C7_MSG")<>0
							AADD(aValidGet,{"cMsg"        ,aAutoCab[ProcH("C7_MSG"    ),2],"CheckSX3('C7_MSG',cMsg).And.A123FormDesc(cMsg,@cDescMsg)"                       ,.F.})
						EndIf
						If ProcH("C7_REAJUST")<>0
							AADD(aValidGet,{"cReajuste"   ,aAutoCab[ProcH("C7_REAJUST"),2],"CheckSX3('C7_REAJUST',cReajuste).And.A123FormReaj(cReajuste,@cDescFor)",.F.})
						EndIf
						If lNaturez .And. ProcH("C7_NATUREZ")<>0
							Aadd(aValidGet,{"cCodNatu",aAutoCab[ProcH("C7_NATUREZ"),2],"ExistCpo('SED')",.T.})
						EndIf
						If !Empty(aValidGet) .And. !SC7->(MsVldGAuto(aValidGet))
							nOpcA := 0
						EndIf
					EndIf
				Else
					nOpcA := 1
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Quando a lWhenGet for .T. neste ponto e desligada a Roti ³
				//³na automatica l123Auto .F. para a Apresentacao da Dialog.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lWhenGet
					l123Auto := .F.
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Inicio da Construcao da Dialog do Pedido de Compras / AE    ³
			//³-------------------------------------------------------------³
			//³                          ATENCAO !!!                        ³
			//³                                                             ³
			//³Quando for feita manutencao em alguma VALIDACAO dos GETs,    ³
			//³atualize as funcoes que se encontram no array aValidGet      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !l123Auto .Or. lWhenGet

				aObjects := {}
				AAdd( aObjects, { 0,    91, .T., .F. } )
				AAdd( aObjects, { 100, 100, .T., .T. } )
				AAdd( aObjects, { 0,    75, .T., .F. } )
				aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }
				aPosObj := MsObjSize( aInfo, aObjects )
				aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],305,;
					{{10,40,68,95,120,204,133,171,210,260,285,242,270},;
					{10,40,105,120,223,268,63},;
					{5,70,160,205,295},;
					{6,34,200,215},;
					{6,34,80,113,160,185},;
					{6,34,245,268,260},;
					{8,40,100,130,200,220},;
					{273,180,220},;
					{8,45,80,103,139,173,200,235,270},;
					{133,190,144,190,289,293},;
					{142,293,140},;
					{9,47,188,148,9,146},;
					{10,2,33,303},;
					{34,2,055,110,111,303},;
					{5,26,95,140,200,234,68,48,113,132,164,155,208,226,248},;
					{5,26,46,89,107,130,150,177,200,240,260,51,65} } )

				lWhenCond := If( !Empty( cWhenCond ), &( cWhenCond ), .T. ) .And. Eval( bWhenCond )
				lWhenMoed := If( !Empty( cWhenMoed ), &( cWhenMoed ), .T. ) .And. Eval( bWhenMoed )

				DEFINE MSDIALOG oDlg FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] TITLE cCadastro OF oMainWnd PIXEL

					If !SetMDIChild()
					    aPosGet[13][1]:= 12
					    aPosGet[13][3]:= 58
					    aPosGet[14][1]:= 60
					    aPosGet[14][3]:= 105
					Else
						If  nResHor > 1024
						    aPosGet[13][1]:= 35
						    aPosGet[13][3]:= 76
						    aPosGet[14][1]:= 78
						    aPosGet[14][3]:= 107
						Else
						    aPosGet[13][1]:= 30
						    aPosGet[13][3]:= 73
						    aPosGet[14][1]:= 75
						    aPosGet[14][3]:= 112
						    aPosGet[14][5]:= 247
						    aPosGet[14][4]:= 246

						    aPosGet[15][1]:= 10
							aPosGet[15][2]:= 46
							aPosGet[15][3]:= 138
							aPosGet[15][4]:= 203
							aPosGet[15][5]:= 313
							aPosGet[15][6]:= 406
							aPosGet[15][7]:= 93
							aPosGet[15][8]:= 96
							aPosGet[15][9]:= 250
							aPosGet[15][10]:=290
					   		aPosGet[15][11]:=345
							aPosGet[15][12]:=330
							aPosGet[15,13] := aPosGet[15,9]
							aPosGet[15,14] := aPosGet[15,10]
							aPosGet[15,15] := aPosGet[15,11]
							nPosAcols := 15
							nPosDespa := 100
							nPosComp := 078
							nPosExpo := 089
						EndIf
					Endif

					Iif(nResHor == 1120,nTam := 65,nTam := 90)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ponto de Entrada Disnibiliza  cordenadas da Dialog.           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lMt123Get
					aPosObj := If(ValType(aPosObjPE:=ExecBlock("MT123GET",.F.,.F.,{aPosObj,nOpcx}))== "A",aPosObjPE,aPosObj)
				Endif

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Definicao dos MsGETS do Cabecalho do Pedido de Compras        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				@ aPosGet[13][1],aPosGet[13][2] TO aPosGet[13][3],aPosGet[13][4] LABEL '' OF oDlg PIXEL
				@ aPosGet[14][1],aPosGet[14][2] TO aPosGet[14][3],aPosGet[14][4] LABEL '' OF oDlg PIXEL
				@ aPosGet[14][1],aPosGet[14][5] TO aPosGet[14][3],aPosGet[14][6] LABEL '' OF oDlg PIXEL

				@ 038,aPosGet[1,1] SAY   STR0017 OF oDlg PIXEL SIZE 031,009 COLOR CLR_HBLUE // "Numero"
				@ 038,aPosGet[1,2] MSGET cA120Num ;
				PICTURE PesqPict('SC7','C7_NUM') F3 CpoRetF3('C7_NUM');
				WHEN    l123Inclui .And. VisualSX3('C7_NUM') ;
				VALID   CheckSX3('C7_NUM',cA120Num)  .And. !ChkChaveSC7(cA120Num,.T.) OF oDlg PIXEL SIZE 031,006

				@ 038,aPosGet[1,3] SAY   STR0019    OF oDlg PIXEL SIZE 036,009 COLOR CLR_HBLUE   // "Fornecedor"
				@ 038,aPosGet[1,4] MSGET oca123Forn VAR cA120Forn;
				PICTURE PesqPict('SC7','C7_FORNECE') F3 CpoRetF3('C7_FORNECE');
				WHEN    l123Inclui .And. VisualSX3('C7_FORNECE');
				VALID   A123Forn(cA120Forn,@cA120Loj,@aInfForn,IIF(lWhenGet,.F.,.T.),lCopia,@oDescForn,@cDescforn) .And. CheckSX3('C7_FORNECE',cA120Forn) .And. A123VFold('NF_CODCLIFOR',cA120Forn) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ 038,aPosGet[1,5] MSGET ocA123Loj VAR cA120Loj;
				PICTURE PesqPict('SC7','C7_LOJA')  F3 CpoRetF3('C7_LOJA');
				WHEN    l123Inclui.And. VisualSX3('C7_LOJA');
				VALID   A123Forn(cA120Forn,@cA120Loj,@aInfForn,IIF(lWhenGet,.F.,.T.),lCopia,@oDescForn,@cDescforn) .And. CheckSX3('C7_LOJA',cA120Loj) .And. A123VFold('NF_LOJA',cA120Loj) OF oDlg PIXEL SIZE 019,006

				@ 038,aPosGet[1,7] MSGET oDescForn VAR cDescForn PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oDlg PIXEL SIZE 130,006

				@ 038,aPosGet[1,12] SAY   STR0018 OF oDlg PIXEL SIZE 050,009 COLOR CLR_HBLUE // "Data de Emissao"
				@ 038,aPosGet[1,13] MSGET dA120Emis ;
				PICTURE PesqPict('SC7','C7_EMISSAO') F3 CpoRetF3('C7_EMISSAO');
				WHEN    l123Inclui .And. VisualSX3('C7_EMISSAO') ;
				VALID   CheckSX3('C7_EMISSAO',dA120Emis) .And. A123VldDta(dA120Emis,dA123Cpe) OF oDlg PIXEL SIZE 042,006 HASBUTTON

				@ 050,aPosGet[2,1] SAY   STR0020 OF oDlg PIXEL SIZE 050,009 COLOR CLR_HBLUE  // "Cond. Pagto"
				@ 050,aPosGet[2,2] MSGET oCond   VAR cCondicao  ;
				PICTURE PesqPict('SC7','C7_COND') F3 CpoRetF3('C7_COND');
				VALID   CheckSX3('C7_COND',cCondicao) .And.A123DescCnd(cCondicao,@oDescCond,@cDescCond,oGetDados);
				WHEN    lWhenCond OF oDlg PIXEL SIZE 025,006 HASBUTTON

				@ 050,aPosGet[2,7] MSGET oDescCond VAR cDescCond PICTURE PesqPict('SE4','E4_DESCRI') WHEN .F. OF oDlg PIXEL SIZE 055,006

				@ 050,aPosGet[2,3] SAY   STR0021 OF oDlg PIXEL SIZE 038,009               // "Contato"
				@ 050,aPosGet[2,4] MSGET cContato  ;
				PICTURE PesqPict('SC7','C7_CONTATO') F3 CpoRetF3('C7_CONTATO');
				WHEN    !l123Visual .And. VisualSX3('C7_CONTATO') ;
				VALID   CheckSX3('C7_CONTATO',cContato) OF oDlg PIXEL SIZE 074,006

				@ 050,aPosGet[1,8] SAY   STR0022 OF oDlg PIXEL SIZE 070,009               // "Filial p/ Entrega"
				@ 050,aPosGet[1,6] MSGET cFilialEnt  ;
				PICTURE PesqPict('SC7','C7_FILENT') F3 CpoRetF3('C7_FILENT');
				WHEN    !l123Visual .And. lPcFilEnt .And. VisualSX3('C7_FILENT').And.!Empty(xFilial("SC7"))  ;
				VALID   CheckSX3('C7_FILENT',cFilialEnt).And.	Eval(bGDRefresh) OF oDlg PIXEL SIZE 019,006 HASBUTTON

				@ 050,aPosGet[1,12] SAY   STR0005    OF oDlg PIXEL SIZE 036,009            // "Dt.Import."
				@ 050,aPosGet[1,13] MSGET dA123Imp ;
				PICTURE PesqPict('SC7','C7_DT_IMP') F3 CpoRetF3('C7_DT_IMP');
				WHEN    l123Inclui .And. VisualSX3('C7_DT_IMP') ;
				VALID   CheckSX3('C7_DT_IMP',dA123Imp) OF oDlg PIXEL SIZE 042,006 HASBUTTON

				@ 062,aPosGet[2,1] SAY   STR0089   OF oDlg PIXEL SIZE 030,009 COLOR CLR_HBLUE // "Moeda"
				@ 062,aPosGet[2,2] MSGET oGetMoeda VAR nMoedaPed ;
				PICTURE PesqPict("SC7","C7_MOEDA") ;
				VALID   M->NMOEDAPED <= MoedFin().And. M->NMOEDAPED <> 0 .And. A123DescMoed(nMoedaPed,@oDescMoed,@cDescMoed,@nTxMoeda,aObj) .And. A123VldMoeda() ;
				WHEN    lWhenMoed PIXEL SIZE 25,06 OF oDlg

				@ 062,aPosGet[2,7] MSGET oDescMoed VAR cDescMoed  WHEN .F. OF oDlg PIXEL SIZE 055,006

				@ 062,aPosGet[2,3] SAY   STR0090 OF oDlg PIXEL SIZE 030,009               // "Taxa da Moeda"
				@ 062,aposget[2,4] MSGET nTxMoeda OF oDlg ;
				PICTURE PesqPict("SC7","C7_TXMOEDA",11) F3 CpoRetF3('C7_TXMOEDA');
				WHEN    !l123Visual .And. VisualSX3('C7_TXMOEDA') ;
				VALID   CheckSX3('C7_TXMOEDA',nMoedaped) PIXEL SIZE 074,006 HASBUTTON

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Gets especificos para Versao Localizada Testa os Campos.      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lNaturez
					@ 062,aPosGet[1,8] SAY Alltrim(RetTitle("C7_NATUREZ"))	OF oDlg PIXEL SIZE 45,10	//"&Cod. Comprador"
					@ 062,aPosGet[1,9] MSGET cCodNatu F3 CpoRetF3("C7_NATUREZ") PICTURE PesqPict("SC7","C7_NATUREZ");
					WHEN !l123Visual .And. VisualSX3("C7_NATUREZ") VALID Vazio() .Or. ExistCpo("SED") OF oDlg PIXEL SIZE 45,006 HASBUTTON
				EndIf

				@ 062,aPosGet[1,12] SAY   STR0047    OF oDlg PIXEL SIZE 056,009            // "Dt.Conf.Ped."
				@ 062,aPosGet[1,13] MSGET dA123Cpe ;
				PICTURE PesqPict('SC7','C7_CONF_PE') F3 CpoRetF3('C7_CONF_PE');
				WHEN    l123Inclui .And. VisualSX3('C7_CONF_PE') ;
				VALID   CheckSX3('C7_CONF_PE',dA123Cpe) .And. A123VldDta(dA120Emis,dA123Cpe) OF oDlg PIXEL SIZE 042,006 HASBUTTON

				@ 081,aPosGet[15,1] SAY   STR0025    OF oDlg PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Importador"
				@ 081,aPosGet[15,2] MSGET oca123Impo VAR cA123Impo;
				PICTURE PesqPict('SC7','C7_IMPORT') F3 CpoRetF3('C7_IMPORT');
				WHEN    l123Inclui .And. VisualSX3('C7_IMPORT');
				VALID   A123ImpC(1,cA123Impo,@oDescImpo,@cDescImpo) .And. CheckSX3('C7_IMPORT',cA123Impo) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ 081,aPosGet[15,8] MSGET oDescImpo VAR cDescImpo PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oDlg PIXEL SIZE nTam,003

				@ 093,aPosGet[15,1] SAY   STR0026    OF oDlg PIXEL SIZE 036,009            // "Consignatario"
				@ 093,aPosGet[15,2] MSGET oca123Cons VAR cA123Cons;
				PICTURE PesqPict('SC7','C7_CONSIG') F3 CpoRetF3('C7_CONSIG');
				WHEN    l123Inclui .And. VisualSX3('C7_CONSIG');
				VALID   A123ImpC(2,cA123Cons,,,@oDescCons,@cDescCons) .And. CheckSX3('C7_CONSIG',cA123Cons) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ 093,aPosGet[15,8] MSGET oDescCons VAR cDescCons PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oDlg PIXEL SIZE nTam,003

				@ nPosComp,aPosGet[15,9] SAY   STR0027    OF oDlg PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Comprador"
				@ nPosComp,aPosGet[15,10] MSGET oca123Compr VAR cA123Compr;
				PICTURE PesqPict('SC7','C7_COMPRA') F3 CpoRetF3('C7_COMPRA');
				WHEN    l123Inclui .And. VisualSX3('C7_COMPRA');
				VALID   A123Comp(cA123Compr,@oDescCompr,@cDescCompr) .And. CheckSX3('C7_COMPRA',cA123Compr) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ nPosComp,aPosGet[15,11] MSGET oDescCompr VAR cDescCompr PICTURE PesqPict('SY1','Y1_NOME') WHEN .F. OF oDlg PIXEL SIZE nTam,003

				@ nPosExpo,aPosGet[15,9] SAY   STR0028    OF oDlg PIXEL SIZE 036,009            // "Exportador"
				@ nPosExpo,aPosGet[15,10] MSGET oca123Exp VAR cA123Exp;
				PICTURE PesqPict('SC7','C7_EXPORTA') F3 CpoRetF3('C7_EXPORTA');
				WHEN    l123Inclui .And. VisualSX3('C7_EXPORTA');
				VALID   A123Exp(cA123Exp,@cA123LojEx,@oDescExp,@cDescExp,lCopia) .And. CheckSX3('C7_EXPORTA',cA123Exp) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ nPosExpo,aPosGet[15,12] MSGET ocA123LojEx VAR cA123LojEx;
				PICTURE PesqPict('SC7','C7_LOJAEXP')  F3 CpoRetF3('C7_LOJAEXP');
				WHEN    l123Inclui.And. VisualSX3('C7_LOJAEXP');
				VALID   A123Exp(cA123Exp,@cA123LojEx,@oDescExp,@cDescExp,lCopia,@ocA123LojEx) .And. CheckSX3('C7_LOJAEXP',cA123LojEx) OF oDlg PIXEL SIZE 013,006

				@ nPosExpo,aPosGet[15,11] MSGET oDescExp VAR cDescExp PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oDlg PIXEL SIZE nTam,003

				@ nPosDespa,aPosGet[15,13] SAY   STR0046    OF oDlg PIXEL SIZE 036,009            // "Despachante"
				@ nPosDespa,aPosGet[15,14] MSGET oca123Desp VAR cA123Desp;
				PICTURE PesqPict('SC7','C7_DESP') F3 CpoRetF3('C7_DESP');
				WHEN    l123Inclui .And. VisualSX3('C7_DESP');
				VALID   A123Desp(cA123Desp,@oDescDesp,@cDescDesp) .And. CheckSX3('C7_DESP',cA123Desp) OF oDlg PIXEL SIZE 040,006 HASBUTTON

				@ nPosDespa,aPosGet[15,15] MSGET oDescDesp VAR cDescDesp PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oDlg PIXEL SIZE nTam,003

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ponto de Entrada Disnibiliza o Objeto da Dialog e cordenadas  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lMt123TEL
					ExecBlock("MT123TEL",.F.,.F.,{@oDlg, aPosGet, aObj, nOpcx, nReg} )
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Criacao da Area da MsGetDados do PC                           ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If l123Visual
					oGetdados := msGetDados():New(aPosObj[2,1]-nPosAcols,aPosObj[2,2],aPosObj[2,3]-19,aPosObj[2,4],4,,,"",,{"C7_QUANT","C7_QTSEGUM","C7_DATPRF"},,,,,,,,,)
				Else
					oGetDados := MSGetDados():New(aPosObj[2,1]-nPosAcols,aPosObj[2,2],aPosObj[2,3]-22,aPosObj[2,4],nOpcX,'A123LinOk','A123TudOk','+C7_ITEM',!l123Visual,,,,MAXGETDAD,'A123FldOk()',,,'A123Del')
				EndIf
				oGetDados:oBrowse:bGotFocus	:= {||A123CabOk(@oCond,@oca123Forn,@ocA123Loj,aRefImpos) }

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Criacao dos Folders da Area do Rodape do PC                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder := TFolder():New(aPosObj[3,1]-20,aPosObj[3,2],aTitles,{"HEADER"},oDlg,,,, .T., .F.,aPosObj[3,4]-aPosObj[3,2],aPosObj[3,3]-aPosObj[3,1]+10,)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³acerto no folder para nao perder o foco                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				For nX := 1 to Len(oFolder:aDialogs)
					DEFINE SBUTTON FROM 5000,5000 TYPE 5 ACTION Allwaystrue() ENABLE OF oFolder:aDialogs[nX]
				Next nX

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder dos totais                                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[1]:oFont := oDlg:oFont

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ponto de Entrada para Incluir Folders no Rodape P.E. usado em ³
				//³conjunto com o P.E. MT123Tel e a variavel aTitles.            ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ExistBlock("MT123FOL")
					ExecBlock("MT123FOL",.F.,.F.,{nOpcx,aPosGet})
				EndIf

				@ 006,aPosGet[3,1] SAY   STR0023  OF  oFolder:aDialogs[1] PIXEL SIZE 065,009 // "Valor da Mercadoria"
				@ 005,aPosGet[3,2] MSGET aObj[01] VAR aValores[VALMERC]   PICTURE    PesqPict('SC7','C7_TOTAL',17,nMoedaPed)   OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 006,aPosGet[3,3] SAY   STR0024  OF  oFolder:aDialogs[1] PIXEL SIZE 049,009 // "Descontos"
				@ 005,aPosGet[3,4] MSGET aObj[02] VAR aValores[VALDESC]   PICTURE    PesqPict('SC7','C7_VLDESC',17,nMoedaPed)  OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 020,aPosGet[3,1] SAY   STR0072  OF  oFolder:aDialogs[1] PIXEL SIZE 050,009  // "Frete"
				@ 019,aPosGet[3,2] MSGET aObj[03] VAR aValores[FRETE]     PICTURE    PesqPict('SC7','C7_FRETE',17,nMoedaPed)   OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 020,aPosGet[3,3] SAY   STR0073  OF  oFolder:aDialogs[1] PIXEL SIZE 050,009  // "Despesas"
				@ 019,aPosGet[3,4] MSGET aObj[22] VAR aValores[VALDESP]   PICTURE    PesqPict('SC7','C7_DESPESA',17,nMoedaPed) OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 034,aPosGet[3,3] SAY   STR0040  OF  oFolder:aDialogs[1] PIXEL SIZE 050,009  // "Seguro"
				@ 033,aPosGet[3,4] MSGET aObj[23] VAR aValores[SEGURO]    PICTURE    PesqPict('SC7','C7_DESPESA',17,nMoedaPed) OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 051,aPosGet[3,3] SAY   STR0029  OF  oFolder:aDialogs[1] PIXEL SIZE 078,009 // "Total do Pedido"
				@ 049,aPosGet[3,4] MSGET aObj[04] VAR aValores[TOTPED]    PICTURE    PesqPict('SC7','C7_TOTAL',17,nMoedaPed)   OF oFolder:aDialogs[1] PIXEL WHEN .F. SIZE 080,009
				@ 046,003 TO 48 ,aPosGet[3,5] LABEL '' OF oFolder:aDialogs[1] PIXEL

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder com as informacoes do fornecedor             ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[2]:oFont := oDlg:oFont
				@ 006,aPosGet[4,1] SAY STR0030    OF   oFolder:aDialogs[2] PIXEL SIZE 037,009 // "Nome"
				@ 005,aPosGet[4,2] MSGET aObj[5]  VAR  aInfForn[1]         PICTURE PesqPict('SA2','A2_NOME')   WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 159,009
				@ 006,aPosGet[4,3] SAY STR0031    OF   oFolder:aDialogs[2] PIXEL SIZE 023,009 // "Tel."
				@ 005,aPosGet[4,4] MSGET aObj[6]  VAR  aInfForn[2]                                             WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 074,009
				@ 043,aPosGet[5,1] SAY STR0032    OF   oFolder:aDialogs[2] PIXEL SIZE 032,009 // "1a Compra"
				@ 042,aPosGet[5,2] MSGET aObj[7]  VAR  aInfForn[3]         PICTURE PesqPict('SA2','A2_PRICOM') WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 040,009
				@ 043,aPosGet[5,3] SAY STR0033    OF   oFolder:aDialogs[2] PIXEL SIZE 036,009 // "Ult. Compra"
				@ 042,aPosGet[5,4] MSGET aObj[8]  VAR  aInfForn[4]         PICTURE PesqPict('SA2','A2_ULTCOM') WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 040,009
				@ 043,aPosGet[5,5] SAY RTrim(RetTitle("A2_CGC")) OF oFolder:aDialogs[2] PIXEL SIZE 31 ,009 // "CNPJ / CPF"
				@ 042,aPosGet[5,6] MSGET aObj[21] VAR  aInfForn[7]         PICTURE PesqPict('SA2','A2_CGC')    WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 76 ,009
				@ 024,aPosGet[6,1] SAY STR0034    OF   oFolder:aDialogs[2] PIXEL SIZE 049,009 // "Endereco"
				@ 023,aPosGet[6,2] MSGET aObj[9]  VAR  aInfForn[5]         PICTURE PesqPict('SA2','A2_END')    WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 205,009
				@ 024,aPosGet[6,3] SAY STR0035    OF   oFolder:aDialogs[2] PIXEL SIZE 032,009 // "Estado"
				@ 023,aPosGet[6,4] MSGET aObj[10] VAR  aInfForn[6]         PICTURE PesqPict('SA2','A2_EST')    WHEN .F. OF oFolder:aDialogs[2] PIXEL SIZE 021,009
				@ 042,aPosGet[6,5] BUTTON STR0036 SIZE 030,010  FONT oDlg:oFont ACTION A123ToFC030()  OF oFolder:aDialogs[2] PIXEL // "Mais Inf."

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder das despesas acessorias                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[3]:oFont := oDlg:oFont
				@ 009,aPosGet[7,1] SAY STR0037    OF  oFolder:aDialogs[3] PIXEL SIZE 035,009 // "Tp. Frete"
				@ 008,aPosGet[7,2] MSCOMBOBOX aObj[11] VAR cTpCCPP       ITEMS      aCombo ON CHANGE A123VFold("NF_FRETE",aValores[FRETE]) WHEN !l123Visual SIZE 045,050 OF oFolder:aDialogs[3] PIXEL
				@ 009,aPosGet[7,3] SAY STR0038    OF  oFolder:aDialogs[3] PIXEL SIZE 035,009 //"Valor do Frete"
				@ 008,aPosGet[7,4] MSGET aObj[12] VAR aValores[FRETE]     PICTURE    PesqPict('SC7','C7_FRETE',14,nMoedaPed) OF oFolder:aDialogs[3] PIXEL WHEN !l123Visual VALID A123VFold("NF_FRETE",aValores[FRETE]) SIZE 080,009 HASBUTTON

//				@ 009,aPosGet[7,5] SAY STR0084    OF  oFolder:aDialogs[3] PIXEL SIZE 035,009 //"Packing"
//				@ 008,aPosGet[7,6] MSGET aObj[25] VAR aValores[PACKING]   PICTURE    PesqPict('SC7','C7_VALEMB',14,nMoedaPed) OF oFolder:aDialogs[3] PIXEL WHEN !l123Visual VALID A123VFold("NF_VALEMB",aValores[PACKING]) SIZE 080,009 HASBUTTON

				@ 026,aPosGet[7,1] SAY STR0039    OF  oFolder:aDialogs[3] PIXEL SIZE 052,009 // "Despesas"
				@ 025,aPosGet[7,2] MSGET aObj[13] VAR aValores[VALDESP]   PICTURE    PesqPict('SC7','C7_FRETE',14,nMoedaPed) OF oFolder:aDialogs[3] PIXEL WHEN !l123Visual VALID A123VFold("NF_DESPESA",aValores[VALDESP]) SIZE 080,009 HASBUTTON
				@ 026,aPosGet[7,3] SAY STR0040    OF  oFolder:aDialogs[3] PIXEL SIZE 035,009 // "Seguro"
				@ 025,aPosGet[7,4] MSGET aObj[14] VAR aValores[SEGURO]    PICTURE    PesqPict('SC7','C7_FRETE',14,nMoedaPed) OF oFolder:aDialogs[3] PIXEL WHEN !l123Visual VALID A123VFold("NF_SEGURO",aValores[SEGURO]) SIZE 080,009 HASBUTTON
				@ 038,011 TO 40 ,aPosGet[8,1] LABEL '' OF oFolder:aDialogs[3] PIXEL
				@ 048,aPosGet[8,2] SAY STR0056    OF  oFolder:aDialogs[3] PIXEL SIZE 060,009 // "Total ( Frete+Despesas)"
				@ 047,aPosGet[8,3] MSGET aObj[24] VAR aValores[TOTF3]     PICTURE    PesqPict('SC7','C7_FRETE',14,nMoedaPed) OF oFolder:aDialogs[3] PIXEL WHEN .F. SIZE 080,009 HASBUTTON

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder dos Descontos de rodape                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[4]:oFont := oDlg:oFont
				@ 009,aPosGet[09,1] SAY STR0041    OF  oFolder:aDialogs[4] PIXEL SIZE 040,009 // "Desconto 1"
				@ 008,aPosGet[09,2] MSGET aObj[15] VAR nDesc1              PICTURE    PesqPict('SC7','C7_DESC1',,nMoedaPed) OF oFolder:aDialogs[4] PIXEL WHEN !l123Visual .And. MaFisFound("NF") VALID A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores,oGetDados) SIZE 030,009 HASBUTTON
				@ 009,aPosGet[09,3] SAY '%'        OF  oFolder:aDialogs[4] PIXEL SIZE 011,009
				@ 009,aPosGet[09,4] SAY STR0042    OF  oFolder:aDialogs[4] PIXEL SIZE 036,009 // "Desconto 2"
				@ 008,aPosGet[09,5] MSGET aObj[16] VAR nDesc2              PICTURE    PesqPict('SC7','C7_DESC2',,nMoedaPed) OF oFolder:aDialogs[4] PIXEL WHEN !l123Visual .And. MaFisFound("NF") VALID A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores,oGetDados) SIZE 030,009 HASBUTTON
				@ 009,aPosGet[09,6] SAY '%'        OF  oFolder:aDialogs[4] PIXEL SIZE 009,009
				@ 009,aPosGet[09,7] SAY STR0043    OF  oFolder:aDialogs[4] PIXEL SIZE 042,009 // "Desconto 3"
				@ 008,aPosGet[09,8] MSGET aObj[17] VAR nDesc3              PICTURE    PesqPict('SC7','C7_DESC3',,nMoedaPed) OF oFolder:aDialogs[4] PIXEL WHEN !l123Visual .And. MaFisFound("NF") VALID A123VDesc(@nDesc1,@nDesc2,@nDesc3,@aValores,oGetDados) SIZE 030,009 HASBUTTON
				@ 009,aPosGet[09,9] SAY '%'        OF  oFolder:aDialogs[4] PIXEL SIZE 012,009
				@ 027,aPosGet[10,1] SAY STR0044    OF  oFolder:aDialogs[4] PIXEL SIZE 068,009 //"Valor do Desconto"
				@ 026,aPosGet[10,2] MSGET aObj[18] VAR aValores[VALDESC]   PICTURE    PesqPict('SC7','C7_VLDESC',14,nMoedaPed) OF oFolder:aDialogs[4] PIXEL WHEN !l123Visual .And. MaFisFound("NF") .And.(nDesc1+nDesc2+nDesc3==0) VALID A123VFold("NF_DESCONTO",aValores[VALDESC]) SIZE 075,009 HASBUTTON
				@ 049,aPosGet[10,3] SAY STR0045    OF  oFolder:aDialogs[4] PIXEL SIZE 073,009 // "Total do Pedido"
				@ 048,aPosGet[10,4] MSGET aObj[19] VAR aValores[TOTPED]    PICTURE    PesqPict('SC7','C7_TOTAL',14,nMoedaPed) OF oFolder:aDialogs[4] PIXEL WHEN .F. SIZE 075,009 HASBUTTON
				@ 038,005 TO 040,aPosGet[10,5] LABEL '' OF oFolder:aDialogs[4] PIXEL

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder do Resumo de Impostos                        ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[5]:oFont := oDlg:oFont
				aObj[20] := MaFisRodape(nTpRodape,oFolder:aDialogs[5],,{5,3,aPosGet[10,6],53},bListRefresh,l123Visual)

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder de Menssagem e Reajuste                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[6]:oFont := oDlg:oFont
				@ 005,aPosGet[11,1] TO 055,aPosGet[11,2] LABEL STR0053 OF oFolder:aDialogs[6] PIXEL //"Reajuste"
				@ 005,003 TO 055,aPosGet[11,3] LABEL STR0054 OF oFolder:aDialogs[6] PIXEL // "Mensagem"
				@ 015,aPosGet[12,1] SAY   STR0055   OF oFolder:aDialogs[6] PIXEL SIZE 040,009 // "Cod. Formula"
				@ 014,aPosGet[12,2] MSGET cMsg      PICTURE PesqPict('SC7','C7_MSG')     F3 CpoRetF3('C7_MSG')     WHEN !l123Visual .And.VisualSX3('C7_MSG')     VALID CheckSX3('C7_MSG',cMsg).And.A123FormDesc(cMsg,@cDescMsg) .And. A123FRefresh(aObj2) OF oFolder:aDialogs[6] PIXEL SIZE 023,009 HASBUTTON
				@ 014,aPosGet[12,3] MSGET cReajuste PICTURE PesqPict('SC7','C7_REAJUST') F3 CpoRetF3('C7_REAJUST') WHEN !l123Visual .And.VisualSX3('C7_REAJUST') VALID CheckSX3('C7_REAJUST',cReajuste).And.A123FormReaj(cReajuste,@cDescFor) .And. A123FRefresh(aObj2) OF oFolder:aDialogs[6] PIXEL SIZE 023,009 HASBUTTON
				@ 015,aPosGet[12,4] SAY   STR0057   OF oFolder:aDialogs[6] PIXEL SIZE 040,009 // "Cod.Formula"
				@ 032,aPosGet[12,5] MSGET aObj2[1]  VAR cDescMsg  PICTURE "@!" OF oFolder:aDialogs[6] WHEN .F. PIXEL SIZE 124,009
				@ 031,aPosGet[12,6] MSGET aObj2[2]  VAR cDescFor  PICTURE "@!" OF oFolder:aDialogs[6] WHEN .F. PIXEL SIZE 140,009

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³MsGets do Folder de Transporte                                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				oFolder:aDialogs[7]:oFont := oDlg:oFont

				@ 006,aPosGet[16,1] SAY   STR0049  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Ag.Embarc."
				@ 005,aPosGet[16,2] MSGET ocA123Emb VAR cA123Emb;
				PICTURE PesqPict('SC7','C7_AGENTE') F3 CpoRetF3('C7_AGENTE');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_AGENTE');
				VALID   A123Emb(cA123Emb,@oDescEmb,@cDescEmb,@oDescForw,@cDescForw,1) .And. CheckSX3('C7_AGENTE',cA123Emb) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 005,aPosGet[16,3] MSGET oDescEmb VAR cDescEmb PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oFolder:aDialogs[7] PIXEL SIZE 130,006

				@ 020,aPosGet[16,1] SAY   STR0050  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Ag.Embarc."
				@ 019,aPosGet[16,2] MSGET ocA123Forw VAR cA123Forw;
				PICTURE PesqPict('SC7','C7_FORWARD') F3 CpoRetF3('C7_FORWARD');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_FORWARD');
				VALID   A123Emb(cA123Forw,@oDescEmb,@cDescEmb,@oDescForw,@cDescForw,2) .And. CheckSX3('C7_FORWARD',cA123Forw) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 019,aPosGet[16,3] MSGET oDescForw VAR cDescForw PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oFolder:aDialogs[7] PIXEL SIZE 130,006

				@ 034,aPosGet[16,1] SAY   STR0051  OF  oFolder:aDialogs[7] PIXEL SIZE 056,009 COLOR CLR_HBLUE // "Via Transp."
				@ 033,aPosGet[16,2] MSGET ocA123Via VAR cA123Via;
				PICTURE PesqPict('SC7','C7_TIPO_EM') F3 CpoRetF3('C7_TIPO_EM');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_TIPO_EM');
				VALID   A123Via(cA123Via,@oDescVia,@cDescVia,@cA123Ori,@cA123Dest) .And. CheckSX3('C7_TIPO_EM',cA123Via) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 033,aPosGet[16,3] MSGET oDescVia VAR cDescVia PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oFolder:aDialogs[7] PIXEL SIZE 130,006

				@ 047,aPosGet[16,1] SAY   STR0052  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Origem"
				@ 046,aPosGet[16,2] MSGET oca123Ori VAR cA123Ori;
				PICTURE PesqPict('SC7','C7_ORIGIMP') F3 CpoRetF3('C7_ORIGIMP');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_ORIGIMP');
				VALID   CheckSX3('C7_ORIGIMP',cA123Ori) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 047,aPosGet[16,12] SAY   STR0062  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Destino"
				@ 046,aPosGet[16,13] MSGET oca123Dest VAR cA123Dest;
				PICTURE PesqPict('SC7','C7_DEST') F3 CpoRetF3('C7_DEST');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_DEST');
				VALID   CheckSX3('C7_DEST',cA123Dest) OF oFolder:aDialogs[7] PIXEL SIZE 025,009	HASBUTTON

				@ 047,aPosGet[16,4] SAY   STR0064  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 COLOR CLR_HBLUE // "Incoterm"
				@ 046,aPosGet[16,5] MSGET oca123Inc  VAR cA123Inc;
				PICTURE PesqPict('SC7','C7_INCOTER') F3 CpoRetF3('C7_INCOTER');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_INCOTER');
				VALID   CheckSX3('C7_INCOTER',cA123Inc) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 006,aPosGet[16,6] SAY   STR0066  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Modal.CNTR"
				@ 005,aPosGet[16,7] MSCOMBOBOX oca123Mod  VAR cA123Mod ITEMS aModal ON CHANGE CheckSX3('C7_CONTAIN',cA123Mod);
				WHEN l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_CONTAIN') SIZE 052,050 OF oFolder:aDialogs[7] PIXEL

				@ 006,aPosGet[16,10] SAY   STR0067  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Peso Bruto"
				@ 005,aPosGet[16,11] MSGET oca123Pes  VAR cA123Pes;
				PICTURE PesqPict('SC7','C7_PESO_B');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_PESO_B');
				VALID   CheckSX3('C7_PESO_B',cA123Pes) OF oFolder:aDialogs[7] PIXEL SIZE 060,009 HASBUTTON

				@ 020,aPosGet[16,6] SAY   STR0068  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Vol.Cubado"
				@ 019,aPosGet[16,7] MSGET oca123Cub  VAR cA123Cub;
				PICTURE PesqPict('SC7','C7_MT3');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_MT3');
				VALID   CheckSX3('C7_MT3',cA123Cub) OF oFolder:aDialogs[7] PIXEL SIZE 060,009 HASBUTTON

				@ 020,aPosGet[16,10] SAY   STR0071  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Contain.20'"
				@ 019,aPosGet[16,11] MSGET oca123C20  VAR cA123C20;
				PICTURE PesqPict('SC7','C7_CONTA20');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_CONTA20');
				VALID   CheckSX3('C7_CONTA20',cA123C20) OF oFolder:aDialogs[7] PIXEL SIZE 060,009 HASBUTTON

				@ 034,aPosGet[16,6] SAY   STR0075  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Contain.40'"
				@ 033,aPosGet[16,7] MSGET oca123C40  VAR cA123C40;
				PICTURE PesqPict('SC7','C7_CONTA40');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_CONTA40');
				VALID   CheckSX3('C7_CONTA40',cA123C40) OF oFolder:aDialogs[7] PIXEL SIZE 060,009 HASBUTTON

				@ 034,aPosGet[16,10] SAY   STR0076  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Cont.40hc"
				@ 033,aPosGet[16,11] MSGET oca123CHC  VAR cA123CHC;
				PICTURE PesqPict('SC7','C7_CON40HC');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_CON40HC');
				VALID   CheckSX3('C7_CON40HC',cA123CHC) OF oFolder:aDialogs[7] PIXEL SIZE 060,009 HASBUTTON

				@ 047,aPosGet[16,6] SAY   STR0082  OF  oFolder:aDialogs[7] PIXEL SIZE 036,009 // "Deposito"
				@ 046,aPosGet[16,7] MSGET ocA123Dpt VAR cA123Dpt;
				PICTURE PesqPict('SC7','C7_ARMAZEM') F3 CpoRetF3('C7_ARMAZEM');
				WHEN    l123Inclui .Or. l123Altera .and. SC7->C7_QUJE==0 .And. SC7->C7_QTDACLA==0 .And. VisualSX3('C7_ARMAZEM');
				VALID   A123Dpt(cA123Dpt,@oDescDpt,@cDescDpt) .And. CheckSX3('C7_ARMAZEM',cA123Dpt) OF oFolder:aDialogs[7] PIXEL SIZE 025,009 HASBUTTON

				@ 046,aPosGet[16,8] MSGET oDescDpt VAR cDescDpt PICTURE PesqPict('SA2','A2_NOME') WHEN .F. OF oFolder:aDialogs[7] PIXEL SIZE 199,006

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Execucao de Refresh necessario para Rotina automatica lWhenGet³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lWhenGet
					Eval(bRefresh)
				EndIf

				ACTIVATE MSDIALOG oDlg ON INIT (IIf(lWhenGet,oGetDados:oBrowse:Refresh(),Nil),EnchoiceBar(oDlg,{||If(oGetDados:TudoOk(),(nOpcA:=1,oDlg:End()),nOpcA:=0)},{|| oDlg:End()},,aButtons))

			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Ponto de Entrada para Continuar ou nao a Exclusao             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If l123Deleta .And. ExistBlock("MTA123E")
				lMta123E := ExecBlock("MTA123E",.f.,.f.,{nOpcA})
				If ValType( lMta123E ) == "L" .And. !lMta123E
					nOpcA := 0
				EndIf
			EndIf

			If nOpcA == 1
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Ponto de Entrada para Continuar ou nao Inclusao/Alteracao.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If (l123Inclui .Or. l123Altera .Or. l123Deleta) .And. ExistTemplate("MT123GRV")
					lMt123GRV := ExecTemplate("MT123GRV",.F.,.F.,{cA120Num,l123Inclui,l123Altera,l123Deleta })
					If ValType( lMt123GRV ) == "L" .And. !lMt123GRV
						l123Inclui := .F.
						l123Altera := .F.
						l123Deleta := .F.
					EndIf
				EndIf

				If (l123Inclui .Or. l123Altera .Or. l123Deleta) .And. ExistBlock("MT123GRV")
					lMt123GRV := Execblock("MT123GRV",.F.,.F.,{cA120Num,l123Inclui,l123Altera,l123Deleta })
					If ValType( lMt123GRV ) == "L" .And. !lMt123GRV
						l123Inclui := .F.
						l123Altera := .F.
						l123Deleta := .F.
					EndIf
				EndIf

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Inicio para chamada da A123Grava() para Manutencao do PO     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If l123Inclui .Or. l123Altera .Or. l123Deleta

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicializa a gravacao atraves das funcoes MATXFIS       ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					MaFisWrite(1)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Prepara a contabilizacao On-Line                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lCtbOnLine

						dbSelectArea("SX5")
						dbSetOrder(1)
						If MsSeek(xFilial()+"09COM")          // Verifica o numero do lote contabil
							cLoteCtb := AllTrim(X5Descri())
						Else
							cLoteCtb := "COM "
						EndIf

						If At(UPPER("EXEC"),X5Descri()) > 0   // Executa um execblock
							cLoteCtb := &(X5Descri())
						EndIf

						nHdlPrv:=HeadProva(cLoteCtb,"MATA120",Subs(cUsuario,7,6),@cArqCtb) // Inicializa o arquivo de contabilizacao
						If nHdlPrv <= 0
							HELP(" ",1,"SEM_LANC")
							lCtbOnLine := .F.
						EndIf

						If lCtbOnLine
							bCtbOnLine := {|x| nTotalCtb += IIf(x==1,DetProva(nHdlPrv,"652","MATA120",cLoteCtb,,,,,@c652,@aCT5),;
								IIf(!Empty(SC7->C7_DTLANC),DetProva(nHdlPrv,"657","MATA120",cLoteCtb,,,,,@c657,@aCT5),0)),;
								SC7->C7_DTLANC := dDataBase}
						EndIf

					EndIF

					If !l123Inclui

						Begin Transaction

							A123Grava(l123Deleta,cReajuste,nDesc1,nDesc2,nDesc3,cMsg,Substr(cTpCCPP,1,2),bCtbOnLine)

							If SA2->(ColumnPos("A2_IMPIP")) # 0 .And. !l123Deleta
								If (SA2->A2_IMPIP == '1') .Or. (SA2->A2_IMPIP $ '03 ' .And. SuperGetMV('MV_IMPIP',.F.,'3') == '1' )
									T_ACDI10PD(cA120Num,.T.)
								EndIf
							EndIf

							EvalTrigger()

							While ( GetSX8Len() > nSaveSX8 )
								ConFirmSX8()
							EndDo

						End Transaction

					Else

						lGravaOk := A123Grava(l123Deleta,cReajuste,nDesc1,nDesc2,nDesc3,cMsg,Substr(cTpCCPP,1,2),bCtbOnLine)

						If SA2->(ColumnPos("A2_IMPIP")) # 0 .and. !l123Deleta
							If (SA2->A2_IMPIP == '1') .or. (SA2->A2_IMPIP $ '03 ' .and. SuperGetMV('MV_IMPIP',.F.,'3') == '1' )
								T_ACDI10PD(cA120Num,.t.)
							EndIf
						EndIf

						If !lGravaOk

							Help(" ",1,"A120NAOREG")
							While ( GetSX8Len() > nSaveSX8 )
								RollBackSX8()
							EndDo

						Else

							EvalTrigger()

							While ( GetSX8Len() > nSaveSX8 )
								ConFirmSX8()
							EndDo

						EndIf

					EndIf

					If ExistBlock("MT123GOK")
						Execblock("MT123GOK",.F.,.F.,{cA120Num,l123Inclui,l123Altera,l123Deleta})
					EndIf

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Envia os dados para o modulo contabil             ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If lCtbOnLine
						RodaProva(nHdlPrv,nTotalCtb)
						If nTotalCtb > 0
							If ( FindFunction( "UsaSeqCor" ) .And. UsaSeqCor() )
								cCodDiario := CTBAVerDia()
								aCtbDia := {{"SC7",SC7->(RECNO()),cCodDiario,"C7_NODIA","C7_DIACTB"}}
							Else
							    aCtbDia := {}
							EndIF

							cA100Incl(cArqCtb,nHdlPrv,1,cLoteCtb,lDigita,lAglutina,,,,,,aCtbDia)
						EndIf
					EndIf

				EndIf

			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Finaliza a gravacao dos lancamentos do SIGAPCO            ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			PcoFinLan("000052")

		EndIf
	EndIf

	SetKey( VK_F4,Nil )
	SetKey( VK_F5,Nil )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Libera os bloqueios do SIGAPCO que nao foram efetivados   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PcoFreeBlq("000052",,,,,(l123Altera.And.nOpca!=1)/*lCancela*/)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Destrava os registros na aletaracao e exclusao            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While ( GetSX8Len() > nSaveSX8 )
		RollBackSX8()
	EndDo

	MsUnLockAll()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Destrava os registros utilizados na funcao A123PID        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ValType(aA123PID) == "A" .And. Len(aA123PID) > 0
		For nX := 1 to Len(aA123PID)
			SC1->(MsGoto(aA123PID[nx]))
			If SimpleLock("SC1")
				SC1->(MsRUnlock())
			Endif
		Next nX
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Finaliza o uso das funcoes MATXFIS                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisEnd()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Libera chaves obtidas por FreeForUse                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lQuery .And. Type( "bFiltraBRW" ) == "B" .And. Empty( SC7->( dbFilter() ) )
		Eval( bFiltraBRW )
		SC7->( dbSetOrder( 1 ) )
	EndIf

	FreeUsedCode()

EndIf

RestArea(aArea)
RestArea(aAreaSM0)

Return(nOpcA)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123AfterH ³ Autor ³Nereu /Alexandre Lemes³Autor ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Acrescenta campos no aHeader construido pela FillGetDados   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Logica indicando se Rotina Automatica              ³±±
±±³          ³ ExpA1 = aHeader de Referencia utilizado na FillGetDados    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123AfterH(l123Auto,aHeaderX)

Local aAreaSX3 := SX3->(GetArea())

If l123Auto
	dbSelectArea("SX3")
	dbSetOrder(2)
	dbSeek("C7_TPOP")
	AADD(aHeaderX,{TRIM(X3Titulo()),SX3->X3_CAMPO,SX3->X3_PICTURE,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_VALID,SX3->X3_USADO,SX3->X3_TIPO,SX3->X3_ARQUIVO,SX3->X3_CONTEXT } )
	dbSeek("C7_ORIGEM")
	AADD(aHeaderX,{TRIM(X3Titulo()),SX3->X3_CAMPO,SX3->X3_PICTURE,SX3->X3_TAMANHO,SX3->X3_DECIMAL,SX3->X3_VALID,SX3->X3_USADO,SX3->X3_TIPO,SX3->X3_ARQUIVO,SX3->X3_CONTEXT } )
EndIf

RestArea(aAreaSX3)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123AfterC ³ Autor ³Nereu /Alexandre Lemes³Data  ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³A funcao trata a excecao na montagem do aCols pela FillGetDa³±±
±±³          ³dos tratando os campos que deverao ser alterados.           ³±±
±±³          ³Realiza a carga do aCols para a MatxFis.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpA1: aCols de Referencia para uso da funcao.              ³±±
±±³          ³ExpA2: Array contendo as referencias de Impostos da Tabela  ³±±
±±³          ³ExpL1: Logica indicando a opcao de Alteracao                ³±±
±±³          ³ExpL2: Logica indicando a opcao de Exclusao                 ³±±
±±³          ³ExpL3: Logica indicando a opcao de Copia                    ³±±
±±³          ³ExpL4: Logica condicional para continuar o processo.        ³±±
±±³          ³ExpC1: Variavel para Renumeracao dos itens do PC na Copia   ³±±
±±³          ³ExpL5: Indica se a Grade de Produtos esta sendo Utilizada.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: lContinua .T. ou .F.                            	  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123AfterC(aColsX,aRefImpos,l123Altera,l123Deleta,lCopia,lContinua,cItCop,lGrade)

Local nX := 0
Local nY := 0
Local nPosItGrd := GDFieldPos("C7_ITEMGRD")
Local cCpoValid := "C7_NUMSC/C7_ITEMSC/C7_DATPRF/C7_NUMCOT/C7_APROV/C7_QTDSOL/C7_RESIDUO"

If l123Altera .Or. l123Deleta
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Trava os registros na alteracao e  exclusao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !SoftLock("SC7")
		lContinua := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o PC/AE pode ser excluido       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If l123Deleta .And. !MaCanDelPC("SC7")
		lContinua := .F.
	EndIf
EndIf

If lContinua
	For nY := 1 to Len(aHeader)
		If lCopia .And. !( Trim(aHeader[nY][2]) == "C7_ITEM" ) .And. Trim(aHeader[nY][2]) $ cCpoValid
			aColsX[Len(aColsX)][nY] := CriaVar(aHeader[nY][2],.T.)
		EndIf
		If lCopia .And. Trim(aHeader[nY][2]) == "C7_ITEM"
			If Alltrim(aColsX[Len(aColsX)][nPosItGrd]) == "001" .Or. Empty(aColsX[Len(aColsX)][nPosItGrd])
				cItCop := Soma1(cItCop)
			Endif
			aColsX[Len(aColsX)][nY] := cItCop

		Endif
		If lCopia .And. Trim(aHeader[nY][2]) $ "C7_REC_WT"
			aColsX[Len(aColsX)][nY] := 0
		EndIf
	Next nY

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicia a Carga do item nas funcoes MATXFIS  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisIniLoad(Len(aColsX))
	For nX := 1 To Len(aRefImpos)
		MaFisLoad(aRefImpos[nX][3],SC7->(FieldGet(ColumnPos(aRefImpos[nX][2]))),Len(aColsX))
	Next nX
	MaFisEndLoad(Len(aColsX),2)

EndIf

Return lContinua

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123Refresh³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa o Refresh do Folder.                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array com os valores do Rodape                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Refresh(aValores)

Local aArea	:= GetArea()

aValores[VALMERC]	:= MaFisRet(,"NF_VALMERC")
aValores[VALDESC]	:= MaFisRet(,"NF_DESCONTO")
aValores[FRETE]		:= MaFisRet(,"NF_FRETE")
aValores[TOTPED]	:= MaFisRet(,"NF_TOTAL")
aValores[SEGURO]	:= MaFisRet(,"NF_SEGURO")
aValores[VALDESP]	:= MaFisRet(,"NF_DESPESA")
//aValores[PACKING]   := MaFisRet(,"NF_VALEMB")
aValores[TOTF1]		:= aValores[VALDESP]+aValores[SEGURO] //+aValores[PACKING]
aValores[TOTF3]		:= aValores[FRETE]+aValores[SEGURO]+aValores[VALDESP] //+aValores[PACKING]
aValores[IMPOSTOS]	:= aClone(MaFisRet(,"NF_IMPOSTOS"))

RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123FRefre³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa o refresh nos objetos do array.                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array contendo os Objetos                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123FRefresh(aObj)

Local nX := 0
If Len(aObj)>=23
	aObj[01]:Picture := PesqPict('SC7','C7_TOTAL'  ,17,nMoedaPed)
	aObj[02]:Picture := PesqPict('SC7','C7_VLDESC' ,17,nMoedaPed)
	aObj[03]:Picture := PesqPict('SC7','C7_FRETE'  ,17,nMoedaPed)
	aObj[04]:Picture := PesqPict('SC7','C7_TOTAL'  ,17,nMoedaPed)
	aObj[22]:Picture := PesqPict('SC7','C7_DESPESA',17,nMoedaPed)
	aObj[23]:Picture := PesqPict('SC7','C7_DESPESA',17,nMoedaPed)
EndIf

For nX := 1 to Len(aObj)
	aObj[nX]:Refresh()
Next nX

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123CabOk ³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Executa  as validacoes dos Gets.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Expo1 = Objeto da condicao de pagamento                    ³±±
±±³          ³ Expo2 = Objeto do codigo do fonecedor                      ³±±
±±³          ³ Expo3 = Objeto da loja do fornecedor                       ³±±
±±³          ³ ExpA1 = Array contendo os impostos suportados              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123CabOk(oCond,ocA123Forn,ocA123Loj,aRelImp)

Local lRet 	:= .T.

If !(A123Forn(cA120Forn,cA120Loj,,.F.) .And. CheckSX3('C7_FORNECE',cA120Forn))
	If ocA123Forn<>Nil
		ocA123Forn:SetFocus()
	EndIf
	lRet := .F.
EndIf

If lRet .And.!( A123Forn(cA120Forn,cA120Loj,,.F.) .And. CheckSX3('C7_LOJA',cA120Loj))
	If ocA123Loj<>Nil
		ocA123Loj:SetFocus()
	EndIf
	lRet := .F.
EndIf

If lRet
	If !MaFisFound("NF")
		MaFisIni(cA120Forn,cA120Loj,"F","N",Nil,aRelImp,,.T.)
		If cFilialEnt <> Criavar("C7_FILENT")
			A120FilEnt(cFilialEnt)
		EndIf
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123VFold ³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Exucuta o calculo de valores para campos Totalizadores.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Referencia ( vide MATXFIS)                         ³±±
±±³          ³ ExpC2 = Valor da Referencia                                ³±±
±±³          ³ ExpL3 = .T./.F.- Executa o Refresh do folder               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Campos Totalizadores do MATA120                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VFold(cReferencia,xValor,lRefre,lAtuCols)

Local aArea	   := GetArea()
Local lRetorno := .T.
Default lAtuCols := .T.

If ValType(xValor)== "N" .And. xValor < 0
	lRetorno := .F.
EndIf

If lRetorno

	If lRefre == Nil
		lRefre := .T.
	EndIf

	If MaFisFound("NF")
		MaFisAlt(cReferencia,xValor)
		If cPaisLoc == "ARG" .And. (cReferencia=="NF_CODCLIFOR" .Or.cReferencia=="NF_LOJA")
			SA2->(DbSetOrder(1))
			SA2->(MsSeek(xFilial("SA2")+MaFisRet(,"NF_CODCLIFOR")+MaFisRet(,"NF_LOJA")))
			MaFisAlt("NF_SERIENF",LocXTipSer("SA2",MVNOTAFIS))
		Endif
		If lAtuCols
			MaFisToCols(aHeader,aCols,,"MT120")
		Endif
		If lRefre
			Eval(bRefresh)
			Eval(bGDRefresh)
		EndIf
	EndIf
EndIf

RestArea(aArea)

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123ToFC030 ³Autor³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Compatibilizacao de variaveis utilizadas no FINC030.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123TOFC030()

Local aArea	:= GetArea()

PRIVATE cCadastro  := STR0060 //"Consulta Posi‡„o Fornecedores"
PRIVATE nVlGeralNf := 0
PRIVATE nJuros     := 0
PRIVATE nTotal1    := 0
PRIVATE nTotal2    := 0
PRIVATE nTotal3    := 0
PRIVATE aTotal5    := {0.00,0,0.00,0}
PRIVATE dBaixa     := dDataBase

M->nJuros := 0

If Pergunte("FIC030",.T.)
	Finc030("Fc030Con")
EndIf
Pergunte("MTA123",.F.)

RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123VDesc  ³Autor³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao e calculo do Desconto.                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Primeiro Valor do desconto                         ³±±
±±³          ³ ExpN2 = Segundo Valor do Desconto                          ³±±
±±³          ³ ExpN3 = Terceiro Valor do Desconto                         ³±±
±±³          ³ ExpA1 = Array contendo os valores do rodape                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VDesc(nDesc1,nDesc2,nDesc3,aValores,oGetDados)

Local aArea	  	:= GetArea()
Local cReadVar  := ReadVar()
Local nTotal  	:= MaFisRet(,"NF_VALMERC")
Local nPosDesc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_DESC"})
Local nPosVDesc   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})
Local nPosTotal := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local lDescItem	:= .F.
Local lRet 		:= .T.
Local nTotDesc := nDesc1+nDesc2+nDesc3
Local nTotVDesc := 0
Local nPosNDesc := {}
Local nLine := 1
Default oGetDados := Nil

If "NDESC" $ cReadVar
	If &(cReadVar) == 0 .AND. !lOpc
		nPosnDesc := aScan(aDesc,{|x| x[1] == cReadVar})
		If nPosnDesc > 0
			A123VFold("NF_TOTAL",Round(aDesc[nPosnDesc,3],2),.F.,.F.)
			A123VFold("NF_DESCONTO",Round(MaFisRet(,"NF_DESCONTO") - aDesc[nPosnDesc,4],2),.F.,.F.)
			aCols := &("aCDesc" + SubStr(cReadVar,6,1))
			aEval(aCols,{|x| MaFisAlt("IT_DESCONTO",x[nPosVDesc],nLine), nLine++ })
			MaFisToCols(aHeader,aCols,,"MT120")
			Eval(bGDRefresh)
			Eval(bFolderRefresh)
			If oGetDados<>Nil
				oGetDados:oBrowse:SetFocus()
			EndIf
			Return lRet
		Endif
	ElseIf (&(cReadVar) > 0 .AND. &(cReadVar) <> &("NOLDDESC" + cValToChar(SubStr(cReadVar,6,1))))
		nPosnDesc := aScan(aDesc,{|x| x[1] == cReadVar})
		If nPosnDesc > 0
			A123VFold("NF_TOTAL",Round(aDesc[nPosnDesc,3],2),.F.,.F.)
			A123VFold("NF_DESCONTO",Round(MaFisRet(,"NF_DESCONTO") - aDesc[nPosnDesc,4],2),.F.,.F.)
			aCols := &("aCDesc" + SubStr(cReadVar,6,1))
			aEval(aCols,{|x| MaFisAlt("IT_DESCONTO",x[nPosVDesc],nLine), nLine++ })
			Eval(bGDRefresh)
			Eval(bFolderRefresh)
		Endif
	Endif
Endif

If nDesc1+nDesc2+nDesc3 > 0
	aEval(aCols,{|x| lDescItem := If(!Empty(x[nPosDesc]),.T.,lDescItem)})
	If lDescItem .And. !l123Auto
		lOpc := MsgYesNo(STR0058,STR0059)
	EndIf
	If lOpc
		aValores[VALDESC] := 0
		aEval(aCols,{|x| x[nPosDesc]:= 0 })
		aEval(aCols,{|x| x[nPosVDesc]:= 0 })
		aEval(aCols,{|x| x[nPosVDesc] := (CalcDesc(x[nPosTotal],nDesc1,nDesc2,nDesc3))})
		aValores[VALDESC] := (CalcDesc(nTotal,nDesc1,nDesc2,nDesc3))
		A123VFold("NF_DESCONTO",aValores[VALDESC],.F.,.F.)
		Eval(bGDRefresh)
		Eval(bFolderRefresh)
	ElseIf !lOpc
		aAdd(aDesc,{cReadVar,&(cReadVar),Round(MaFisRet(,"NF_TOTAL"),2),Round(MaFisRet(,"NF_TOTAL") * (&(cReadVar)/100),2)})
		A123aCDesc(cReadVar)
		A123VFold("NF_TOTAL",Round(aValores[TOTPED] - (aValores[TOTPED] * (&(cReadVar)/100)),2),.F.,.F.)
		aValores[VALDESC] += MaFisRet(,"NF_TOTAL") * (&(cReadVar)/100)
		A123VFold("NF_DESCONTO",Round(aValores[VALDESC],2),.F.,.F.)
		aEval(aCols,{|x| x[nPosDesc] := Round(x[nPosVDesc] / nTotal,2) })
		aEval(aCols,{|x| x[nPosVDesc] += Round((x[nPosTotal] - x[nPosVDesc] ) * (&(cReadVar)/100),2)})
		aEval(aCols,{|x| MaFisAlt("IT_DESCONTO",x[nPosVDesc],nLine), nLine++ })
		Eval(bGDRefresh)
		Eval(bFolderRefresh)
	EndIf
ElseIf !l123Auto
	aValores[VALDESC] := 0
	If "NDESC" $ cReadVar
		aEval(aCols,{|x| x[nPosDesc]:= 0 })
		aEval(aCols,{|x| x[nPosvDesc]:= 0 })
		aEval(aCols,{|x| x[nPosVDesc] := (CalcDesc(x[nPosTotal],nDesc1,nDesc2,nDesc3))})
		A123VFold("NF_DESCONTO",aValores[VALDESC],.F.)
		Eval(bGDRefresh)
		Eval(bFolderRefresh)
	EndIf
	If nDesc1 < 0 .Or. nDesc2 < 0 .Or. nDesc3 < 0
		lRet := .F.
	EndIf
EndIf

	If oGetDados<>Nil
		oGetDados:oBrowse:SetFocus()
	EndIf
	If "NDESC" $ cReadVar
		nOldDesc1 := nDesc1
		nOldDesc2 := nDesc2
		nOldDesc3 := nDesc3
	Endif
	RestArea(aArea)
Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123aCDesc   ³ Autor ³Alexander Leite	³ Data ³13.02.2019	 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Clonar aCols após aplicação de cada desconto		           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Variável com valor do desconto atual                  ³±±
±±³          ³														            ³±±
±±³          ³											                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Não há retorno.							               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³																    ³±±
±±³          ³									                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A123aCDesc(cReadVar)

Local nPosDesc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_DESC"})
Local nPosVDesc   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})

 	If cReadVar == UPPER("nDesc1")
 		aCDesc1:= aClone(aCols)
 	ElseIf cReadVar == UPPER("nDesc2")
 		aCDesc2:= aClone(aCols)
 	Else
 		aCDesc3:= aClone(aCols)
 	Endif


Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³  A123F4    ³Autor³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Monta uma tela com as Solicitacoes/Contratos em Aberto      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Expo1 : Objeto da condicao de pagamento                    ³±±
±±³          ³ Expo2 = Objeto da descricao da condicao de pagamento       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123F4(oCond,oDescCond)

Local aArea	    := GetArea()
Local aAreaSC1	:= SC1->(GetArea())
Local aAreaSB1	:= SB1->(GEtArea())
Local aGrupo	:= UsrGrComp(RetCodUsr())
Local aCampos	:= {"C1_NUM","C1_ITEM","C1_PRODUTO","C1_QUANT","C1_DESCRI","C1_GRADE","C1_ITEMGRD"}
Local aButEnc	:= {{"relatorio",{||a123VisuSC(aSavRec[oQual:nAt])},STR0007}}	//"Visualizar"
Local aHeadCpos	:= {" "}
Local aHeadUser := {}
Local aNew      := {}
Local aLineNew  := {}
Local aNewF4    := {}
Local aLineF4   := {}
Local aArrayF4	:= {}
Local aSavRec	:= {}
Local aFiltro   := {}
Local aFilUsr   := {}
Local bWhile    := { || .T. }
Local bIf       := { || .T. }
Local nPosQtSol	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QTDSOL"})
Local nPosNumSc := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_NUMSC"})
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEM"})
Local nPosItSc  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMSC"})
Local nPosCod	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosPrc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nPosQuant := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosDsc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})
Local nPosTotal	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local nPosGrade	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_GRADE"})
Local nPosItGra	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMGRD"})
Local nPosCC	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CC"})
Local nOpcA     := 0
Local nSavQual  := 0
Local nX        := 0
Local nY        := 0
Local nInclui   := 0
Local cVar		:= aCols[n][nPosCod]
Local cCadastro := ""
Local cAliasQry := ""
#IFDEF TOP
	Local cQuery := ""
#ENDIF
Local cDescr    := ""
Local cLine     := ""
Local bLine     := ""
Local cItem     := Strzero(1,len(SC7->C7_ITEM))
Local oOk		:= LoadBitMap(GetResources(), "LBOK")
Local oNo		:= LoadBitMap(GetResources(), "LBNO")
Local oSize
Local oDlg
Local oQual
Local lSolic	:= IF(SuperGetMv("MV_RESTCOM")=="S",.T.,.F.)
Local lQuery    := .F.
Local lAprovado := .T.
Local lRet      := .T.
Local bKeyF5	:=  SetKey( VK_F5 )
Local cChave 	:= " "
Local cFlag     := " "
Local cProdRef  := " "
Local i
Local lGrade    := MaGrade()
Local aRefImpos := MaFisRelImp('MT100',{"SC7"})
Local lReferencia := .F.
Local aAMarcaF4   := {}
Local bKList    := bListRefresh
Local bKGDR		:= bGDRefresh
Local nColNmSc  := 0
Local nColItSc  := 0

Static lMt123C1D  := Nil, lMt123C1C := Nil

SetKey( VK_F5,Nil )

If ValType(aA123PID) == "U"
	aA123PID := {}
EndIf

If lMt123C1D == Nil .Or. lMt123C1C == Nil
	lMt123C1D := ExistBlock("MT123C1D")
	lMt123C1C := ExistBlock("MT123C1C")
Endif

If cPaisLoc <> "BRA"
	If (ExistBlock("A123F4CP"))
		ExecBlock("A123F4CP",.F.,.F.)
		lRet := .F.
	EndIf
EndIf

If !Empty(cVar) .And. lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o Header com os titulos do MsSelect             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX3")
	dbSetOrder(2)
	For nx	:= 1 to Len(aCampos)
		If MsSeek(aCampos[nx])
			AADD(aHeadCpos,AllTrim(X3Titulo()) )
		EndIf
	Next

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o filtro de usuario                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "A123F4FI" )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Estrutura do array                                           ³
		//³ 1 - String - Filtro no SC1 para ISAM ( Sintaxe xBase )       ³
		//³ 2 - String - Filtro no SC1 para SQL  ( Sintaxe SQL   )       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aFilUsr := 	ExecBlock( "A123F4FI", .F., .F. )
		If ValType( aFilUsr ) == "A"
			aFiltro := aClone(aFilUsr)
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o produto e uma referencia de grade               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lGrade
		cProdRef := cVar
		lReferencia := MatGrdPrrf(@cProdRef)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o arquivo a ser pesquisado                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !lReferencia

		dbSelectArea("SC1")
		dbgotop()
		dbSetOrder(2)
		If MsSeek(xFilial("SC1")+cVar)

			bFiltro := { || .T. }

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona filtro de usuario para ISAM                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty( aFiltro )
				If ValType( aFiltro[ 1 ] ) == "C"
					bFiltro := { || &( aFiltro[ 1 ] ) }
				EndIf
			EndIf

			#IFDEF TOP

				If TcSrvType() <> "AS/400"

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Bloco para SQL                                        ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

					cAliasQry := CriaTrab( , .F. )

					cQuery := ""
					cQuery += "SELECT R_E_C_N_O_ SC1RECNO FROM " + RetSqlName( "SC1" ) + " "
					cQuery += "WHERE "
					cQuery += "C1_FILIAL='"  + xFilial( "SC1" ) + "' AND "
					cQuery += "C1_PRODUTO='" + cVar             + "' AND "
					cQuery += "C1_TIPO = 2 AND "
			  		cQuery += "C1_MOEDA = CAST('" + AllTrim(Str(nMoedaPed,2)) + "' AS INTEGER) AND "
					cQuery += "C1_QUJE<C1_QUANT AND "
					cQuery += "C1_TPOP<>'P' AND "
					cQuery += "C1_APROV IN(' ','L') AND "
					cQuery += "(C1_COTACAO = 'IMPORT' OR C1_COTACAO = '"+Replicate("X",Len(C1_COTACAO))+"') AND "
					//-- Integracao com o Modulo de Gestao de Contratos
					If nTipoPed == 1
						cQuery += "((C1_QUJE > 0 AND C1_FLAGGCT = '') OR (C1_QUJE = 0)) AND "
					EndIF
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Adiciona filtro de usuario para SQL                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Len( aFiltro ) >= 2
						If ValType( aFiltro[ 2 ] ) == "C"
							cQuery  += aFiltro[ 2 ] + " AND "
							bFiltro := { || .T. }
						EndIf
					EndIf
					cQuery += "  C1_RESIDUO = '' AND "
					cQuery += "D_E_L_E_T_ = '' "
					cQuery += "ORDER BY "+SqlOrder(SC1->(IndexKey()))
					cQuery := ChangeQuery( cQuery )

					dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )

					bWhile  := { || !( cAliasQry )->( Eof() ) }
					bIf     := { || .T. }

					lQuery := .T.

				Else

			#ENDIF

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Bloco para ISAM                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cAliasQry := "SC1"

				bWhile  := { || !EOF() .And. xFilial("SC1")+cVar == C1_FILIAL+C1_PRODUTO }
				bIf		:= { ||  C1_TIPO = 2 .And. C1_MOEDA == nMoedaPed .And. C1_QUJE < C1_QUANT .And. C1_TPOP <> "P" .And. C1_APROV $ " ,L" .And.(C1_COTACAO == "IMPORT" .Or. C1_COTACAO == Replicate("X",Len(C1_COTACAO)))}
				lQuery  := .F.

				#IFDEF TOP
				EndIf
				#ENDIF

			While Eval( bWhile )

				If lQuery
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Posiciona o SC1                                       ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SC1->( MsGoto( ( cAliasQry )->SC1RECNO ) )
					dbSelectArea( "SC1" )
				EndIf

				If Eval( bIf ) .And. Eval( bFiltro )

					If !( lSolic .And. aScan(aGrupo,SC1->C1_GRUPCOM) <= 0 ) .Or. Empty(SC1->C1_GRUPCOM)

						nSaldo	:= A123SaldoSC(nPosCod,nPosNumSC,nPosItSC,nPosItem,cA120Num,aGrupo,lSolic,nPosQtSol)
						If nSaldo > 0

							AADD(aArrayF4,{SC1->C1_NUM,SC1->C1_ITEM,SC1->C1_PRODUTO,TransForm(nSaldo,PesqPictQt("C1_QUANT")),SC1->C1_DESCRI," "," "})
							AADD(aSavRec,RecNo())

							If lMT123C1D
								aNew := ExecBlock("MT123C1D", .f., .f., aArrayF4[Len(aArrayF4)])
								If ValType(aNew) == "A"
									aArrayF4[Len(aArrayF4)] := aNew
								Endif
							Endif

						EndIf

					EndIf

				EndIf

				dbSelectArea( cAliasQry )
				dbSkip()
			EndDo
		Else
			Help(" ",1,"A123F4")
		EndIf

	Else // produto eh referência

		dbSelectArea("SC1")
		dbgotop()
		dbSetOrder(2)
		If MsSeek(xFilial("SC1")+Alltrim(cVar),.T.)

			bFiltro := { || .T. }

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Adiciona filtro de usuario para ISAM                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !Empty( aFiltro )
				If ValType( aFiltro[ 1 ] ) == "C"
					bFiltro := { || &( aFiltro[ 1 ] ) }
				EndIf
			EndIf

			#IFDEF TOP

				If TcSrvType() <> "AS/400"

					cAliasQry := CriaTrab( , .F. )

					cQuery := ""
					cQuery += "SELECT R_E_C_N_O_ SC1RECNO FROM " + RetSqlName( "SC1" ) + " "
					cQuery += "WHERE "
					cQuery += "C1_FILIAL='"  + xFilial( "SC1" ) + "' AND "
					cQuery += "C1_PRODUTO LIKE'" + ALLTRIM(cVar) + "%' AND "
					cQuery += "C1_QUJE<C1_QUANT AND "
					cQuery += "C1_TPOP<>'P' AND "
					cQuery += "C1_APROV IN(' ','L') AND "
					cQuery += "(C1_COTACAO ='"+Space(Len(C1_COTACAO))+"' OR C1_COTACAO = '"+Replicate("X",Len(C1_COTACAO))+"') AND "
					//-- Integracao com o Modulo de Gestao de Contratos
					If nTipoPed == 1
						cQuery += "((C1_QUJE > 0 AND C1_FLAGGCT = '') OR (C1_QUJE = 0)) AND "
					EndIF

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Adiciona filtro de usuario para SQL                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If Len( aFiltro ) >= 2
						If ValType( aFiltro[ 2 ] ) == "C"
							cQuery  += aFiltro[ 2 ] + " AND "
							bFiltro := { || .T. }
						EndIf
					EndIf

					cQuery += "D_E_L_E_T_ = '' "
					cQuery := ChangeQuery( cQuery )

					dbUseArea( .T., "TOPCONN", TcGenQry( ,,cQuery ), cAliasQry, .F., .T. )

					bWhile  := { || !( cAliasQry )->( Eof() ) }
					bIf     := { || .T. }

					lQuery := .T.

				Else

			#ENDIF

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Bloco para ISAM                                       ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cAliasQry := "SC1"

				bWhile  := { || !EOF() .And. xFilial("SC1")+alltrim(cVar) == C1_FILIAL+LEFT(C1_PRODUTO,Len(Alltrim(cVar))) }
				bIf		:= { || C1_QUJE < C1_QUANT .And. C1_TPOP <> "P" .And. C1_APROV $ " ,L" .And.(C1_COTACAO == Space(Len(C1_COTACAO)) .Or. C1_COTACAO == Replicate("X",Len(C1_COTACAO)))}
				lQuery := .F.

				#IFDEF TOP
				EndIf
				#ENDIF

			While Eval( bWhile )

				If lQuery
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Posiciona o SC1                                       ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					SC1->( MsGoto( ( cAliasQry )->SC1RECNO ) )
					dbSelectArea( "SC1" )
				EndIf

				If Eval( bIf ) .And. Eval( bFiltro )

					If !( lSolic .And. aScan(aGrupo,SC1->C1_GRUPCOM) <= 0 ) .Or. Empty(SC1->C1_GRUPCOM)

						nSaldo	:= A123SaldoSC(nPosCod,nPosNumSC,nPosItSC,nPosItem,cA120Num,aGrupo,lSolic,nPosQtSol)
						If nSaldo > 0

							AADD(aArrayF4,{SC1->C1_NUM,SC1->C1_ITEM,SC1->C1_PRODUTO,TransForm(nSaldo,PesqPictQt("C1_QUANT")),SC1->C1_DESCRI,SC1->C1_GRADE,SC1->C1_ITEMGRD})
							AADD(aSavRec,RecNo())

							If lMT123C1D
								aNew := ExecBlock("MT123C1D", .f., .f., aArrayF4[Len(aArrayF4)])
								If ValType(aNew) == "A"
									aArrayF4[Len(aArrayF4)] := aNew
								Endif
							Endif

						EndIf

					EndIf

				EndIf

				dbSelectArea( cAliasQry )
				dbSkip()
			EndDo
		Else
			Help(" ",1,"A123F4")
		EndIf

	Endif

	dbSelectArea("SB1")
	MsSeek(xFilial("SB1")+cVar)
	cDescr := Substr(SB1->B1_DESC,1,15)

	If mv_par02 == 2
		dbSelectArea("SB5")
		MsSeek(xFilial("SB5")+cVar)
		If !Empty(B5_CEME)
			cDescr := substr(SB5->B5_CEME,1,15)
		EndIf
	EndIf

	cCadastro := STR0061	//"Solicita‡„o de Compras"

	aNewF4 := {}

	For nX := 1 To Len( aArrayF4 )
		aLineF4 := aArrayF4[ nX ]
		aLineNew := { .F. }
		AEval( aLineF4, { |x| AAdd( aLineNew, x ) } )
		AAdd( aNewF4, aLineNew )
	Next nX

	aArrayF4 := AClone( aNewF4 )

	If Len(aArrayF4) > 0
		nOpcA := 0

		cLine := "{If(aArrayF4[oQual:nAt,1],oOk,oNo)"

		If lMT123C1C
			aHeadUser := AClone( aHeadCpos )
			aLineNew  := aHeadUser[ 1 ]
			ADel( aHeadUser, 1 )
			ASize( aHeadUser, Len( aHeadUser ) - 1 )

			dbSelectArea("SC1")
			If ValType( aNew := ExecBlock( "MT123C1C", .F., .F., aHeadUser ) ) == "A"
				aHeadUser := aNew
				aHeadCpos := {}
				AAdd( aHeadCpos, aLineNew )
				AEval( aHeadUser, { |x| AAdd( aHeadCpos, x ) } )
			EndIf
		EndIf

		For nX := 2 To Len( aHeadCpos )
			cLine += ",aArrayF4[oQual:nAT][" + AllTrim( Str( nX ) ) + "]"
		Next nX

		cLine += " } "

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta dinamicamente o bline do CodeBlock                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		bLine := &( "{ || " + cLine + " }" )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula dimensões                                            ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oSize := FwDefSize():New()
		oSize:aWorkArea := {000,000, 435, 135 }
		oSize:AddObject( "CABECALHO",  100, 10, .T., .T. ) // Totalmente dimensionavel
		oSize:AddObject( "GETDADOS" ,  100, 80, .T., .T. ) // Totalmente dimensionavel

       	oSize:lProp 	:= .T. // Proporcional
		oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

		oSize:Process() 	   // Dispara os calculos

		DEFINE MSDIALOG oDlg FROM 030,030 TO 300,900 TITLE cCadastro OF oMainWnd PIXEL

		@ oSize:GetDimension("CABECALHO","LININI") ,oSize:GetDimension("CABECALHO","COLINI")	 SAY   STR0094 PIXEL //"Produto"
		@ oSize:GetDimension("CABECALHO","LININI") ,oSize:GetDimension("CABECALHO","COLINI")+33 MSGET cDescr  WHEN .F. PIXEL
		oQual:= TWBrowse():New( oSize:GetDimension("GETDADOS","LININI"),oSize:GetDimension("GETDADOS","COLINI"),;
								 	oSize:GetDimension("GETDADOS","COLEND"),oSize:GetDimension("GETDADOS","LINEND")-32,;
									,aHeadCpos,,oDlg,,,,,,,,,,,,.F.,,.T.,,.F.,,,)
		oQual:SetArray(aArrayF4)
		oQual:bLDblClick := { || A123MarF4(@aArrayF4,@oQual) }
		oQual:bLine := bLine

		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||(nOpcA := 1,nSavQual:= oQual:nAT,oDlg:End())},{||(nOpcA := 0,nSavQual:= oQual:nAt,oDlg:End())},,aButEnc)

		If nOpcA == 1
			nColNmSc := SC1->(ColumnPos(aCampos[1]))
			nColItSc := SC1->(ColumnPos(aCampos[2]))
			For nX := 1 to Len(aArrayF4)

				If aArrayF4[nX][1]

					dbSelectArea("SC1")
					dbSetOrder(2)
					MsSeek(xFilial("SC1")+aArrayF4[nx][4]+aArrayF4[nx][2]+aArrayF4[nx][3])

					nInclui := aScan(aCols,{|x| x[nPosQuant] = 0 .And. x[nPosTotal] = 0})

					AAdd(aAMarcaf4,Recno())

					AAdd(aA123PID,Recno())

					cFlag := A123Flag()

					If !Empty(cFlag)
						MsgInfo( STR0092+" - "+STR0063+": "+aArrayF4[nx][2]+"/"+aArrayF4[nx][3], STR0102 ) //Registro utilizado por outro usuário.
						Loop
					Endif

					If nInclui == 0

						cItem := IIf(Empty(aCols),StrZero(1,Len(SC7->C7_ITEM)),SomaIt(aCols[Len(aCols)][nPosItem]))

                        If lReferencia
							nExistGra := aScan(aCols,{|x| x[nPosNumSc] == FieldGet(nColNmSc) .And. x[nPosItSc] == FieldGet(nColItSc)})
							If nExistGra > 0
								cItem := aCols[nExistGra][nPosItem]
							Endif
                        EndIf

						AADD(aCols,Array(Len(aHeader)+1))

						For nY := 1 to Len(aHeader)
							If Trim(aHeader[nY][2]) == "C7_ITEM"
								aCols[Len(aCols)][nY] 	:=cItem
							Elseif Trim(aHeader[nY][2]) == "C7_ALI_WT"
								aCols[Len(aCols)][nY] 	:="SC7"
							Elseif Trim(aHeader[nY][2]) == "C7_REC_WT"
								aCols[Len(aCols)][nY] 	:= 0
							ElseIf ( aHeader[nY][10] != "V")
								aCols[Len(aCols)][nY] := CriaVar(aHeader[nY][2])
							EndIf
							aCols[Len(aCols)][Len(aHeader)+1] := .F.
						Next nY
						N := Len(aCols)
					EndIf

					A123MovCampos(SuperVal(aArrayF4[nX][5]))

					aCols[n,nPosGrade]:=SC1->C1_GRADE
					aCols[n,nPosItGra]:=SC1->C1_ITEMGRD

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicia a Carga do item nas funcoes MATXFIS  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If MaFisFound("IT",n)
						MaFisAlt("IT_QUANT",SuperVal(aArrayF4[nx][5]),N)
						MaFisAlt("IT_PRCUNI",aCols[n,nPosPrc],N)
						MaFisAlt("IT_VALMERC",aCols[n,nPosTotal],N)
						MaColsToFis(aHeader,aCols,n,"MT120",.T.)
					EndIf

					If nInclui == 0
						MaFisAdd(aCols[n][nPosCod],"",aCols[n][nPosQuant],aCols[n][nPosPrc],aCols[n][nPosDsc],"","",,0,0,0,0,aCols[n][nPosTotal])
					EndIf
					A120Tabela("C7_QUANT",SuperVal(aArrayF4[nx][5]))
				Endif
			Next

			//Restaura variaveis
			bListRefresh:=bKList
			bGDRefresh:=bKGDR

			Eval(bListRefresh)
			Eval(bGDRefresh)
		Else
			For nX := 1 to Len(aArrayF4)
				dbSelectArea("SC1")
				dbSetOrder(1)
				MsSeek(xFilial()+aArrayF4[nx][2]+aArrayF4[nx][3])

				If SimpleLock("SC1")
					SC1->(MsRUnlock())
				Endif
			Next nX

		EndIf
	EndIf
EndIf

RestArea(aAreaSC1)
RestArea(aAreaSB1)
RestArea(aArea)

If lGrade .And. Len(aAMarcaF4) > 0

	aCols := aColsGrade(oGrade, aCols, aHeader, "C7_PRODUTO", "C7_ITEM", "C7_ITEMGRD")

	MaColsToFis(aHeader,aCols,Len(aCols),"MT120",.T.)

	MaFisAlt("NF_FRETE",avalores[FRETE])
	MaFisAlt("NF_DESPESA",avalores[VALDESP])
	MaFisAlt("NF_SEGURO",avalores[SEGURO])
	MaFisAlt("NF_DESCONTO",avalores[VALDESC])
Endif

SetKey( VK_F5,bKeyF5 )

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123SaldoSC ³Autor³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Retorna o saldo da solicitacao/contrato posicionado.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Pedido de compras / autorizacao de entrega         ³±±
±±³          ³ ExpN2 = Posicao do aCols do codigo do produto              ³±±
±±³          ³ ExpN3 = Posicao do aCols do numero da solicitacao          ³±±
±±³          ³ ExpN4 = Posicao do aCols do item da solicitacao            ³±±
±±³          ³ ExpN5 = Posicao do aCols do item do Pc                     ³±±
±±³          ³ ExpC1 = Codigo do pedido de compras                        ³±±
±±³          ³ ExpA1 = Array grupo de compras do usuario                  ³±±
±±³          ³ ExpL1 = conteudo do paramento MV_RESTCOM                   ³±±
±±³          ³ ExpN6 = Posicao do aCols da quantidade Solicitada          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123SaldoSC(nPosCod,nPosNumSC,nPosItSC,nPosItem,cNumPed,aGrupo,lSolic,nPosQtSol)

Local aArea		:= GetArea()
Local nSaldo	:= 0
Local nX        := 0

nSaldo	:= SC1->C1_QUANT - SC1->C1_QUJE
For nX := 1 to Len(aCols)
	If !aCols[nX][Len(aCols[nX])].And.!ATail(aCols[nX]) .And. ;
			(aCols[ nX, nPosCod  ] == C1_PRODUTO) .And. ;
			(aCols[ nX, nPosNumSC] == C1_NUM) .And. ;
			(aCols[ nX, nPosItSC ] == C1_ITEM)
		dbSelectArea("SC7")
		dbSetOrder(1)
		If MsSeek(xFilial("SC7")+cNumPed+aCols[nX,nPosItem])
			If aCols[n,nPosItem] == SC7->C7_ITEM
				nSaldo +=  SC7->C7_QTDSOL
			Else
				nSaldo -= aCols[nX][nPosQtSol] - SC7->C7_QTDSOL
			EndIf
		Else
			If n != nX
				nSaldo -= aCols[nX][nPosQtSol]
			EndIf
		EndIf
	EndIf
Next nX

nSaldo := If(nSaldo > 0, nSaldo , 0 )

RestArea(aArea)

Return(nSaldo)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123Forn ³ Autor ³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica o Fornecedor                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A123Fornec(@ExpC1,@ExpC2,@ExpA1,ExpL1,ExpL2)               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do Fornecedor                               ³±±
±±³          ³ ExpC2 = Loja do Fornecedor                                 ³±±
±±³          ³ ExpA1 = Array contendo as informacoes sobre o fonecedor    ³±±
±±³          ³         utilizado no folder de dados do fornecedor.        ³±±
±±³          ³ ExpL1 = Indica se deve disparar os gatilhos com informacoes³±±
±±³          ³         do fornecedor como Condicao de Pagamento,Contato,  ³±±
±±³          ³         etc. ( DEFAULT = .T. )                             ³±±
±±³          ³ ExpL2 = Na COPIA, indica se deve consistir o bloqueio do   ³±±
±±³          ³ 		  fornecedor (somente ao passar pelo campo forn./loja)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Forn(cFornece,cCodLoja,aInfForn,lAtuInfo,lCopia,oDescForn,cDescforn)

Local 	lRet		:= .F.
Local 	cFilSA2		:= xFilial("SA2")

DEFAULT lAtuInfo	:= .T.
DEFAULT lCopia		:= .F.

dbSelectArea("SA2")

If !Empty(cFornece)
	If Empty(cCodLoja)
		SA2->(dbSetOrder(1))	//A2_FILIAL+A2_COD+A2_LOJA
		SA2->(MsSeek(cFilSA2+cFornece))
		If SA2->(Found())
			cCodLoja  := SA2->A2_LOJA
			While !SA2->(EoF()) .And. SA2->(A2_FILIAL+A2_COD) == cFilSA2+cFornece
				If !(SA2->A2_MSBLQL == "1")
					cCodLoja  := SA2->A2_LOJA
					Exit
				EndIf
				SA2->(dbSkip())
			EndDo
			SA2->(MsSeek(cFilSA2+cFornece+cCodLoja))
			If oDescForn != Nil
				cDescforn := SA2->A2_NOME
				oDescForn:Refresh()
			Endif
			lRet := .T.
		EndIf
	Else
		SA2->(dbSetOrder(1))	//A2_FILIAL+A2_COD+A2_LOJA
		SA2->(MsSeek(xFilial("SA2")+cFornece+cCodLoja))
		If SA2->(Found())
			cCodLoja  := SA2->A2_LOJA
			If oDescForn != Nil
				cDescforn := SA2->A2_NOME
				oDescForn:Refresh()
			Endif
			lRet := .T.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o Registro esta Bloqueado.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If INCLUI .Or. lCopia
			If lRet .And. !RegistroOk("SA2")
				lRet := .F.
			EndIf
		EndIf
	EndIf
EndIf

If lRet .And. !Empty(cCodLoja)
    If ( SA2->A2_FABRICA <> "2" .And. SA2->A2_FABRICA <> "3" )
		Aviso("A123NOFOR",STR0113,{"Ok"},1) // "O Fornecedor informado consta como fabricante, exportador ou benificiario, consulte o conteudo do campo fabricante no cadastro de fornecedores"
    	lRet := .F.
    EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o array que contem os dados do Fornecedor      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. aInfForn != Nil
	aInfForn[1]	:= SA2->A2_NOME						// Nome
	aInfForn[2] := IIF(!Empty(SA2->A2_DDI),"( "+TransForm(SA2->A2_DDI,PesqPict("SA2","A2_DDI"))+") ","");
	+TransForm(alltrim(SA2->A2_DDD),PesqPict("SA2","A2_DDD"))+" "+TransForm(SA2->A2_TEL,PesqPict("SA2","A2_TEL")) // Telefone
	aInfForn[3]	:= SA2->A2_PRICOM	    			//Primeira Compra
	aInfForn[4] := SA2->A2_ULTCOM      				//Ultima Compra
	aInfForn[5]	:= SA2->A2_END+" - "+SA2->A2_MUN	//Endereco
	aInfForn[6]	:= SA2->A2_EST         				//Estado
	aInfForn[7]	:= SA2->A2_CGC         				//cnpj
	If lAtuInfo
		cCondicao 	:= SA2->A2_COND
		cContato	:= SA2->A2_CONTATO
		cCodNatu    := SA2->A2_NATUREZ
		SE4->(dbSetOrder(1))
		If SE4->(MsSeek(xFilial("SE4")+cCondicao))
			cDescCond := SE4->E4_DESCRI
        Else
	        cDescCond := Space(Len(SE4->E4_DESCRI))
		EndIf
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123Grava ³ Rev.  ³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava as informacoes do pedido de compra                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = .F. - Inclusao/Alteracao , .T. - Exclusao          ³±±
±±³          ³ ExpC2 = Formula de Reajuste                                ³±±
±±³          ³ ExpN3 = Percentual de Desconto 1                           ³±±
±±³          ³ ExpN4 = Percentual de Desconto 2                           ³±±
±±³          ³ ExpN5 = Percentual de Desconto 3                           ³±±
±±³          ³ ExpC6 = Formula da Mensagem                                ³±±
±±³          ³ ExpC7 = Tipo de Frete                                      ³±±
±±³          ³ ExpB8 = Codeblock de contabilizacao                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Grava(lDeleta,cReajuste,nDesc1,nDesc2,nDesc3,cMsg,cTpCCPP,bCtbOnLine)

Local aArea     := GetArea()
Local aAreaSC7  := {}
Local aRetG1    := {}
Local cGrupo	:= SuperGetMv("MV_PCAPROV")
Local cGrComPad := Space(Len(SC7->C7_GRUPCOM))
#IFDEF TOP
	Local cQuery     := ""
	Local cBanco     := ""
#ENDIF
Local cPedAnt	:= ""
Local cVerSCY   := ""
Local cMT123APV := ""
Local nPosRecno := Len(aHeader)
Local nA		:= 0
Local nB		:= 0
Local nX		:= 0
Local nY		:= 0
Local lGeraSCR	:= .F.
Local lFirstNiv := .F.
Local lTravou   := .F.
Local lMta123G3 := ExistBlock("MTA123G3",,.T.)
Local lGrade    := MaGrade()

Local aRefImpos := MaFisRelImp('MT100',{"SC7"})
Local nPosCod	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosPrc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nPosQuant := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosDsc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})
Local nPosTotal	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local nPosQtSol	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QTDSOL"})
Local nPosTes	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TES"})
Local nCont     := 1
Local nLoop     := 1
Local nLoop2    := 1
Local nLoop3    := 1
Local lMTA123G2 := ExistBlock("MTA123G2",,.T.)
Local cMensaje	:= ""

Private n123TotLib:= 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada MTA123G1                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (ExistBlock("MTA123G1"))
	aRetG1 := ExecBlock("MTA123G1",.F.,.F.)
	If ValType( aRetG1 ) <> "A"
		aRetG1 := {}
	EndIf
EndIf

If INCLUI
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o numero do pedido a ser atualizado                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC7")
	dbSetOrder(1)
	If MsSeek(xFilial("SC7")+cA120Num)
		cPedAnt := NextNumero("SC7",1,"C7_NUM",.F.,cA120Num)
		If cA120Num # cPedAnt
			HELP(" ",1,"NUMSEQ",,cPedAnt,4,15)
			cA120Num := cPedAnt
		EndIf
	EndIf
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a ultima versao de alteracao do pedido no SCY e   ³
	//³ disponibiliza a proxima versao disponivel para a alteracao.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cVerSCY := StrZero(0,Len(SCY->CY_VERSAO))

		#IFDEF TOP
			If ( TcSrvType()<>"AS/400" )
				cQuery := "SELECT MAX(CY_VERSAO) VERSCY "
				cQuery += "FROM "+RetSqlName("SCY")+" SCY "
				cQuery += "WHERE CY_FILIAL='"+xFilial("SCY")+"' AND "
				cQuery += "CY_NUM ='"+cA120Num+"' AND "
				cQuery += "SCY.D_E_L_E_T_=' '"
				cQuery := ChangeQuery(cQuery)
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"MA123GRVQ2",.T.,.T.)
				cVerSCY := Substr(VERSCY,1,TamSX3("CY_VERSAO")[1])
				dbCloseArea()
				dbSelectArea("SCY")
			Else
		#ENDIF
			dbSelectArea("SCY")
			dbSetOrder(1)
			If MsSeek(xFilial("SCY")+cA120Num, .F.)
				Do While ( !Eof() .And. CY_FILIAL == xFilial("SCY") .And. CY_NUM == cA120Num )
					cVerSCY  := CY_VERSAO
					dbSkip()
				EndDo
			EndIf
			#IFDEF TOP
			EndIf
			#ENDIF

		cVerSCY := Soma1(cVerSCY,Len(SCY->CY_VERSAO))

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o grupo de perguntas                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTA123",.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicio da gravacao do Pedido :                             ³
//³ 1 - Estorna todos os itens   (Alteracao,Exclusao)          ³
//³ 2 - Grava os itens do Pedido (Inclusao,Alteracao)          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SC7")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento do aCols para Grade de Produtos, quando a grade ³
//³ estiver ativa o aCols devera ser restaurado (Expandido)    ³
//³ pela funcao a123Grd2Acols()                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lGrade
	If INCLUI .Or. ALTERA
		a123Grd2Acols()
	Else
		aCols:=aClone(aColsBkp)
	Endif
Endif

For nX :=1 to Len(aCols)
	Begin Transaction
		lTravou := .F.
		If aCols[nX][nPosRecno] > 0
			dbSelectArea("SC7")
			MsGoto(aCols[nX][nPosRecno])
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Antes de atualizar o Pedido de Compras SC7 com a alteracao ³
			//³ atual, grava o SCY com os dados do SC7 gerando assim um    ³
			//³ historico com as versoes anteriores da alteracoes do PC.   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock("SCY",.T.)
			dbSelectArea("SCY")
			For nA := 1 to SCY->(FCount())
				nB := SC7->(ColumnPos("C7"+SubStr(SCY->(FieldName(nA)),3)))
				If ( nB <> 0 )
					FieldPut(nA,SC7->(FieldGet(nB)))
				EndIf
			Next nA
			SCY->CY_VERSAO := cVerSCY

			RecLock("SC7")
			MaAvalPC("SC7",2,Nil,Nil,Nil,Nil,bCtbOnLine)
			lTravou := .T.
		Else
			If !(aCols[nX][Len(aCols[nX])])
				RecLock("SC7",.T.)
				lTravou := .T.
			EndIf
		EndIf
		If lTravou
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Efetua a Exclusao do Item                                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (aCols[nX][Len(aCols[nX])] .Or. lDeleta)

				MaAvalPC("SC7",3,(nX == Len(aCols)))
				dbDelete()
				MsUnlock()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Caso o PC tenha sido gerado por sugestao de compras reabre a sugestao    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lDeleta .And. nX == Len(aCols)
					SFJ->(dbSetOrder(2))
					If SFJ->(dbSeek(xFilial("SFJ")+cA120Num))
						RecLock("SFJ",.F.)
						SFJ->FJ_SOLICIT:=""
						SFJ->FJ_FORNECE:=""
						SFJ->FJ_LOJA   :=""
						SFJ->FJ_FILENT :=""
						SFJ->FJ_TIPGER :=""
						SFJ->FJ_COND   :=""
						MsUnlock()
						SDF->(MsSeek(xFilial("SDF")+SFJ->FJ_CODIGO))
						While (SDF->DF_CODIGO == SFJ->FJ_CODIGO) .and. xFilial("SDF")==SDF->DF_FILIAL .and. ! SDF->(Eof())
							RecLock("SDF",.F.)
							SDF->DF_FLAG    := "A"
							SDF->(MsUnlock())
							SDF->(DbSkip())
						EndDo
					EndIf
				EndIf

			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza os dados contidos na GetDados                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				For nY := 1 To Len(aHeader)
					If aHeader[nY][10] <> "V"  .And. Trim(aHeader[nY][2]) != "C7_USERLGI" .And. Trim(aHeader[nY][2]) != "C7_USERLGA"
						If aCols[nX][nY] <> Nil
							SC7->(FieldPut(ColumnPos(aHeader[nY][2]),aCols[nX][nY]))
						EndIf
					EndIf
				Next nY
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza os Campos do Cabecalho/Rodape          ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SC7->C7_NUM     := cA120Num
				SC7->C7_FILIAL  := xFilial("SC7")
				SC7->C7_FILENT  := xFilEnt(cFilialEnt)

				If Empty(SC7->C7_FISCORI)
					If !Empty(SC7->C7_NUMSC)
						SC7->C7_FISCORI := xFilial("SC1")
					Else
						SC7->C7_FISCORI := SC7->C7_FILIAL
					Endif
				EndIf

				SC7->C7_TIPO    := 3
				SC7->C7_EMISSAO := dA120Emis
				SC7->C7_FORNECE := cA120Forn
				SC7->C7_LOJA    := cA120Loj
				SC7->C7_COND    := cCondicao
				SC7->C7_CONTATO := cContato
				SC7->C7_MOEDA   := nMoedaped
				SC7->C7_TxMoeda := nTxMoeda
				SC7->C7_REAJUST := cReajuste
				SC7->C7_DESC1   := nDesc1
				SC7->C7_DESC2   := nDesc2
				SC7->C7_DESC3   := nDesc3
				SC7->C7_COND    := cCondicao
				SC7->C7_MSG 	:= cMSG
				SC7->C7_IPIBRUT := "B"
				SC7->C7_TPFRETE := "F"
				SC7->C7_FREPPCC := cTpCCPP
				SC7->C7_DT_IMP  := dA123Imp
				SC7->C7_AGENTE  := cA123Emb
				SC7->C7_FORWARD := cA123Forw
				SC7->C7_TIPO_EM := cA123Via
				SC7->C7_ORIGIMP := cA123Ori
				SC7->C7_DEST    := cA123Dest
				SC7->C7_COMPRA  := cA123Compr
				SC7->C7_PESO_B  := cA123Pes
				SC7->C7_INCOTER := cA123Inc
				SC7->C7_IMPORT  := cA123Impo
				SC7->C7_CONSIG  := cA123Cons
				SC7->C7_CONF_PE := dA123Cpe
				SC7->C7_DESP    := cA123Desp
				SC7->C7_EXPORTA := cA123Exp
				SC7->C7_LOJAEXP := cA123LojEx
				SC7->C7_CONTAIN := cA123Mod
				SC7->C7_MT3     := cA123Cub
				SC7->C7_CONTA20 := cA123c20
				SC7->C7_CONTA40 := cA123c40
				SC7->C7_CON40HC := cA123CHC
				SC7->C7_ARMAZEM := cA123Dpt

				If lNaturez
					SC7->C7_NATUREZ := cCodNatu
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza os Campos tratados pela funcao Fiscal  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SF4->(MaFisWrite(2,"SC7",nX))
				SF4->(MaAvalPC("SC7",1,nX==Len(aCols),NIL,Nil,MV_PAR01==1,bCtbOnLine,,MV_PAR08==2))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ So Atualiza o valor da nova liberacao se o item nao possue Residuo Eliminado³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If C7_RESIDUO <> "S"
					n123Totlib += MaFisRet(nX,"IT_TOTAL")
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se deve gerar o arquivo de pedidos bloqueados   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SC7->C7_QUJE < SC7->C7_QUANT
					SC7->C7_CONAPRO := If(Empty(SC7->C7_APROV),"L","B")
					lGeraSCR := .T.
				EndIf

				MsUnlock()

				IF lMTA123G2
					ExecBlock("MTA123G2",.F.,.F.,aRetG1)
				EndIf

				If lMta123G3
					ExecBlock("MTA123G3",.F.,.F.,{aRetG1,lGeraSCR})
				EndIf

			EndIf
		EndIf
	End Transaction
Next nX

If lGrade
	For nLoop:=1 to Len(oGrade:aRecno)
		For nLoop2:=1 to len(oGrade:aRecno[nLoop])
			For nLoop3:=1 to len(oGrade:aRecno[nloop,nLoop2])
				If Valtype(oGrade:aRecno[nLoop,nLoop2,nLoop3])=="N"
					nRecno:=oGrade:aRecno[nLoop,nLoop2,nLoop3]
				Else
					nRecno:=0
				Endif
				If  nRecno>0

					If Ascan(acols,{|x| x[nPosRecno]==nRecno})==0

						dbSelectArea("SC7")
						MsGoto(nRecno)

						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Antes de atualizar o Pedido de Compras SC7 com a alteracao ³
						//³ atual, grava o SCY com os dados do SC7 gerando assim um    ³
						//³ historico com as versoes anteriores da alteracoes do PC.   ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						RecLock("SCY",.T.)
						dbSelectArea("SCY")
						For nA := 1 to SCY->(FCount())
							nB := SC7->(ColumnPos("C7"+SubStr(SCY->(FieldName(nA)),3)))
							If ( nB <> 0 )
								FieldPut(nA,SC7->(FieldGet(nB)))
							EndIf
						Next nA

						SCY->CY_VERSAO := cVerSCY

						RecLock("SC7",.F.)
						dbDelete()
						MsUnlock()

					Endif
				Endif
			Next
		Next
	Next
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ao excluir o PC verifica se ha versao do PC no historico de alteracoes (SCY) e exclui.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lDeleta
	dbSelectArea("SCY")
	dbSetOrder(1)
	If MsSeek(xFilial("SCY")+cA120Num)
		Do While ( !Eof() .And. CY_FILIAL == xFilial("SCY") .And. CY_NUM == cA120Num )
			Reclock("SCY",.F.,.T.)
			dbDelete()
			MsUnlock()
			dbSkip()
		EndDo
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo de controle de alcadas SCR.                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Begin Transaction

	If lGeraSCR .And. n123TotLib > 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Limpa o Filtro do SCR caso ele exista                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SCR")
		DbClearFilter()
		aAreaSC7:= SC7->(GetArea())
		dbSelectArea("SC7")
		If !lDeleta
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica o grupo de aprovacao do Comprador.                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SY1")
			dbSetOrder(3)
			If MsSeek(xFilial()+RetCodUsr())
				cGrupo		:= If(!Empty(Y1_GRAPROV),SY1->Y1_GRAPROV,cGrupo)
				cGrComPad	:= If(!Empty(Y1_GRUPCOM),SY1->Y1_GRUPCOM,Space(Len(SC7->C7_GRUPCOM)))
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada para alterar o Grupo de Aprovacao.          								³
			//³ Obs.: Na alteracao do pedido, pode ser usado para alterar o saldo do mesmo (var. n123TotLib)³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("MT123APV")
				cMT123APV := ExecBlock("MT123APV",.F.,.F.)
				If ValType(cMT123APV) == "C"
					cGrupo := cMT123APV
				EndIf
			EndIf
		EndIf
		cGrupo:= If(Empty(SC7->C7_APROV),cGrupo,SC7->C7_APROV)  //retirara depois
		If Altera .Or. lDeleta // Estorna as liberacoes
			MaAlcDoc({cA120Num,"PC",n123TotLib,,,SC7->C7_APROV,,SC7->C7_MOEDA,SC7->C7_TXMOEDA,SC7->C7_EMISSAO},SC7->C7_EMISSAO,3)
		EndIf
		If !lDeleta
			lFirstNiv := MaAlcDoc({cA120Num,"PC",n123TotLib,,,cGrupo,,SC7->C7_MOEDA,SC7->C7_TXMOEDA,SC7->C7_EMISSAO},,1)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua a gravacao do campo de controle de aprovacao C7_CONAPRO  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		#IFDEF TOP
			cBanco := Alltrim(Upper(TCGetDb()))
			SC7->(DBCOMMIT())
			cQuery := "UPDATE "+RetSqlname("SC7")+" "
			cQuery += "SET C7_GRUPCOM = '"+cGrComPad+"' "
			cQuery += "WHERE C7_FILIAL='"+xFilial("SC7")+"' AND "
			cQuery += "C7_NUM='"+cA120Num+"' AND "
			cQuery += "C7_GRUPCOM = '"+Space(Len(SC7->C7_GRUPCOM))+"' "
			If TcSrvType() <> "AS/400"
				cQuery += "AND D_E_L_E_T_=' ' "
			Else
				cQuery += "AND @DELETED@=' ' "
			Endif

			TcSqlExec(cQuery)

			cQuery := "UPDATE "+RetSqlname("SC7")+" "
			cQuery += "SET C7_APROV = '"+cGrupo+"' "
			cQuery += "WHERE C7_FILIAL='"+xFilial("SC7")+"' AND "
			cQuery += "C7_NUM='"+cA120Num+"' AND "
			cQuery += "C7_APROV = '"+Space(Len(SC7->C7_APROV))+"' "
			If TcSrvType() <> "AS/400"
				cQuery += "AND D_E_L_E_T_=' ' "
			Else
				cQuery += "AND @DELETED@=' ' "
			Endif

			TcSqlExec(cQuery)

			cQuery := "UPDATE "+RetSqlname("SC7")+" "
			If !lFirstNiv
				cQuery += "SET C7_CONAPRO = 'B' "
			Else
				cQuery += "SET C7_CONAPRO = 'L' "
			EndIf
			cQuery += "WHERE C7_FILIAL='"+xFilial("SC7")+"' AND "
			cQuery += "C7_NUM='"+cA120Num+"' AND "
			cQuery += "C7_APROV <> '"+Space(Len(SC7->C7_APROV))+"' "
			If TcSrvType() <> "AS/400"
				cQuery += "AND D_E_L_E_T_=' ' "
			Else
				cQuery += "AND @DELETED@=' ' "
			Endif

			TcSqlExec(cQuery)

			If Substr(cBanco,1,6) == "ORACLE"
				TcSqlExec("COMMIT")
			Endif
		#ELSE
			dbSelectArea("SC7")
			dbSetOrder(1)
			MsSeek(xFilial("SC7")+cA120Num)
			While !Eof() .And. C7_FILIAL+C7_NUM == xFilial("SC7")+cA120Num
				RecLock("SC7",.F.)
				SC7->C7_GRUPCOM	:= If(Empty(C7_GRUPCOM),cGrComPad,C7_GRUPCOM)
				If !lFirstNiv
					SC7->C7_APROV := If(Empty(C7_APROV),cGrupo,C7_APROV)
					SC7->C7_CONAPRO := If(!Empty(C7_APROV),"B",C7_CONAPRO)
				Else
					SC7->C7_APROV := If(Empty(C7_APROV),cGrupo,C7_APROV)
					SC7->C7_CONAPRO := If(!Empty(C7_APROV),"L",C7_CONAPRO)
				EndIf
				MsUnlock()
				dbSkip()
			EndDo
		#ENDIF
		RestArea(aAreaSC7)

		If ExistBlock("WFW123P")
			ExecBlock("WFW123P",.f.,.f.,xFilial("SC7")+cA120Num)
		EndIf

	EndIf

	If ExistBlock("MT123F")
		ExecBlock("MT123F",.f.,.f.,xFilial("SC7")+cA120Num)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Envia e-mail na inclusao/exclusao do Pedido de Compras.          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Inclui
		If FindFunction("COMTemSXI") .And. COMTemSXI("037")
			SC7->(MsSeek(xFilial("SC7")+cA120Num))
			cMensaje += STR0117 + SC7->C7_NUM + " " + STR0118 + Subs(cUsuario,7,15)
			EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, "037", FW_EV_LEVEL_INFO,""/*cCargo*/, STR0120, cMensaje, .T./*lPublic*/) //"Inclusión de Pedido de Compra"
		Else	
			SC7->(MsSeek(xFilial("SC7")+cA120Num))
			MEnviaMail("037",{SC7->C7_NUM,SC7->C7_NUMCOT,SC7->C7_APROV,SC7->C7_CONAPRO,Subs(cUsuario,7,15)})
		EndIf	
	ElseIf lDeleta
		If FindFunction("COMTemSXI") .And. COMTemSXI("043")
			cMensaje += STR0117 + cA120Num + " " + STR0119 + Subs(cUsuario,7,15)
			EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, "043", FW_EV_LEVEL_INFO,""/*cCargo*/, STR0121, cMensaje, .T./*lPublic*/) //"Exclusão de Pedido de Compra"
		Else
			MEnviaMail("043",{cA120Num})
		EndIf	
	Endif

End Transaction

RestArea(aArea)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123MovCampos³ Autor ³Nereu/Alexandre Lemes³Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Move os campos das solicitacoes de compra em aberto.       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A123MovCampos()					                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Quantidade                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123MovCampos(nQuant)

Local aArea		:= GetArea()
Local aAreaSB1	:= SB1->(GetArea())
Local aTam      := TamSX3("C7_TOTAL")
Local nPosQtd   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosQtd2  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QTSEGUM"})
Local nPosPrc   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nX        := 0

DEFAULT nQuant	:= 0

If cPaisLoc != "BRA"
	aTam[2] := MsDecimais(nMoedaPed)
EndIf

SB1->(dbSetOrder(1))
SB1->(MsSeek(xFilial("SB1")+SC1->C1_PRODUTO))

For nX := 1 To Len(aHeader)
	Do Case
	Case Trim(aHeader[nX][2]) == "C7_PRODUTO"
		aCols[n][nX] := SC1->C1_PRODUTO
	Case Trim(aHeader[nX][2]) == "C7_NUMSC"
		aCols[n][nX] := SC1->C1_NUM
	Case Trim(aHeader[nX][2]) == "C7_ITEMSC"
		aCols[n][nX] := SC1->C1_ITEM
	Case Trim(aHeader[nX][2]) == "C7_LOCAL"
		aCols[n][nX] := SC1->C1_LOCAL
	Case Trim(aHeader[nX][2]) $ "C7_QUANT#C7_QTDSOL"
		If nQuant == 0
			aCols[n][nX] := SC1->C1_QUANT-SC1->C1_QUJE
		Else
			aCols[n][nX] := nQuant
		EndIf
	Case Trim(aHeader[nX][2]) == "C7_DESCRI"
		If Len(aCols[n][nX]) > Len(SC1->C1_DESCRI)
			aCols[n][nX] := SC1->C1_DESCRI+Space(Len(aCols[n][nX])-Len(SC1->C1_DESCRI))
		Else
			aCols[n][nX] := SubStr(SC1->C1_DESCRI,1,Len(aCols[n][nX]))
		EndIf
	Case Trim(aHeader[nX][2]) == "C7_OBS"
		If Len(aCols[n][nX]) > Len(SC1->C1_OBS)
			aCols[n][nX] := SC1->C1_OBS+Space(Len(aCols[n][nX])-Len(SC1->C1_OBS))
		Else
			aCols[n][nX] := SubStr(SC1->C1_OBS,1,Len(aCols[n][nX]))
		EndIf
	Case Trim(aHeader[nX][2]) == "C7_UM"
		aCols[n][nX] := SC1->C1_UM
	Case Trim(aHeader[nX][2]) == "C7_SEGUM"
		aCols[n][nX] := SC1->C1_SEGUM
	Case Trim(aHeader[nX][2]) == "C7_QTSEGUM"
		If SB1->B1_CONV > 0 .And. QtdComp(SC1->C1_QTSEGUM) == QtdComp(0)
			aCols[n][nX] := ConvUm(SB1->B1_COD,aCols[n][nPosQtd],aCols[n][nPosQtd2],2)
		Else
			aCols[n][nX] := SC1->C1_QTSEGUM
		EndIf
	Case Trim(aHeader[nX][2]) == "C7_DATPRF"
		aCols[n][nX] := SC1->C1_DATPRF
	Case Trim(aHeader[nX][2]) == "C7_CC"
		aCols[n][nX] := SC1->C1_CC
	Case Trim(aHeader[nX][2]) == "C7_ITEMCTA"
		aCols[n][nX] := SC1->C1_ITEMCTA
	Case Trim(aHeader[nX][2]) == "C7_CONTA"
		aCols[n][nX] := SC1->C1_CONTA
	Case Trim(aHeader[nX][2]) == "C7_GRUPCOM"
		aCols[n][nX] := SC1->C1_GRUPCOM
	Case Trim(aHeader[nX][2]) == "C7_CLVL"
		aCols[n][nX] := Iif( Empty(SC1->C1_CLVL), SB1->B1_CLVL, SC1->C1_CLVL )
	Case Trim(aHeader[nX][2]) == "C7_SEQMRP"
		aCols[n][nX] := SC1->C1_SEQMRP
	Case Trim(aHeader[nX][2]) == "C7_OP"
		aCols[n][nX] := SC1->C1_OP
	Case Trim(aHeader[nX][2]) == "C7_PRECO"
		aCols[n][nX] := SC1->C1_PRECO
	Case Trim(aHeader[nX][2]) == "C7_TOTAL"
		aCols[n][nX] := NoRound( aCols[n][nPosQtd] * aCols[n][nPosPrc] , TamSX3("C7_TOTAL")[2] )
	Case Trim(aHeader[nX][2]) == "C7_FABRICA"
		aCols[n][nX] := SC1->C1_FABRICA
	Case Trim(aHeader[nX][2]) == "C7_LOJFABR"
		aCols[n][nX] := SC1->C1_LOJFABR
	Case Trim(aHeader[nX][2]) == "C7_TES"
		aCols[n][nX] := SB1->B1_TE
	EndCase
Next nX
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua a chamada do ponto de entrada                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MT123ISC" )
	ExecBlock( "MT123ISC", .F., .F. )
EndIf

RestArea(aAreaSB1)
RestArea(aArea)

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123Ativa³ Autor ³Nereu / Alexandre Lemes ³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chama a pergunte                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A123Ativa()

pergunte("MTA123",.T.)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123LinOk ³ Autor ³Nereu / Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se a linha digitada esta' Ok                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123LinOk(o)

Local aInfGrd    := {}
Local cProdRef   := ""
Local nPosPrd    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosItem   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEM"})
Local nPosPrf    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_DATPRF"})
Local nPosQt     := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosPrc    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nPosNumSC  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_NUMSC"})
Local nPosItemSC := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMSC"})
Local nPosLocal  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_LOCAL"})
Local nPosVDesc  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})
Local nPosTotal  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local nPosConta  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CONTA"})
Local nPosCC	 := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CC"})
Local nPosCLVL	 := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CLVL"})
Local nPosItemCTA:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMCTA"})
Local nPosGrade  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_GRADE"})
Local n_SaveLin  := n
Local nX		 := 0
Local nLinha     := 0
Local nColuna    := 0
Local lRet       := .T.
Local lRet123LOK := .T.
Local lReferencia:= .F.

lVldHead:= If(ValType(lVldHead) == "L",lVldHead, GetNewPar( "MV_VLDHEAD",.T. ) )

If aCols[n][Len(aCols[n])]  // Verifica se esta deletado

	dbSelectArea("SC7")
	dbSetOrder(4)
	If MsSeek(xFilial("SC7")+aCols[n][nPosPrd]+cA120Num+aCols[n][nPosItem])
		If (C7_QUJE > 0) .Or. (C7_RESIDUO == "S") .Or. (C7_QTDACLA > 0)
			Help(" ",1,"A123NDEL")
			lRet := .F.
		EndIf
	EndIf

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a permissao do armazem. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If (VAL(GetVersao(.F.)) == 11 .And. GetRpoRelease() >= "R6" .Or. VAL(GetVersao(.F.))  > 11) .And. FindFunction("MaAvalPerm")
		lRet := MaAvalPerm(3,{aCols[n][nPosLocal],aCols[n][nPosPrd]})
	EndIf

	If lRet.And.(Empty(aCols[n][nPosPrd]).Or.Empty(aCols[n][nPosPrf]);
			.Or. Empty(aCols[n][nPosQt]) .Or. Empty(aCols[n][nPosPrc]) )
		Help(" ",1,"A12006")
		lRet := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Validacao para Itens de Grade                                           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lReferencia := MatGrdPrrf(@(aCols[n][nposPrd]))
	If lRet .And. lReferencia

		If MaGrade() .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade)>0

			cItemGrd := "000"
			cProdRef := aCols[n][nPosPrd]

			If ( !Empty(cProdRef) )
				oGrade:nPosLinO := n

				If ( Len(oGrade:aHeadGrade)>0 .And. oGrade:aHeadGrade[n][1] == "R" ) .And. aCols[n, nPosGrade]  == "S"
					For nLinha := 1 To Len(oGrade:aColsGrade[n])
						For nColuna := 2 To Len(oGrade:aHeadGrade[n])

							If(oGrade:aColsFieldByName("C7_QUANT",n,nLinha,nColuna) <> 0) .And. ;
							If( Len(aCols[1])==Len(aHeader) , !aCols[n][Len(aHeader)+1] , .T. )

								cProdRef := aCols[n][nPosPrd]

								If !Empty(cProdRef)
									aInfGrd := {cProdRef}
									Exit
								EndIf
							Endif
						Next nColuna
					Next nLinha
				Endif
			Endif
		EndIf

		If Empty(aInfGrd) .Or. aInfGrd[1]== Nil
			Aviso(STR0085,STR0086,{"Ok"})
			lRet := .F.
		Endif

	Endif

	If lRet .And. !Empty(aCols[n][nPosNumSC])

		If ( lReferencia )
			If ( nPosGrade > 0 )
				aCols[n][nPosGrade] := "S"
				lReferencia := .T.
			EndIf
		Else
			If ( nPosGrade > 0 )
				aCols[n][nPosGrade] := " "
			EndIf
		EndIf

		If !lReferencia
			dbSelectArea("SC1")
			dbSetOrder(2)
			MsSeek(xFilial("SC1")+aCols[n][nPosPrd]+aCols[n][nPosNumSC]+aCols[n][nPosItemSC])

			If Eof()
				Help(" ",1,"A12005")
				lRet := .F.
			EndIf
		EndIf

	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Quando Informado Armazem em branco considerar o B1_LOCPAD       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. Empty(aCols[n][nPosLocal])
		SB1->(dbSetOrder(1))
		If SB1->(MsSeek(xFilial("SB1")+aCols[n][nPosPrd]))
			aCols[n][nPosLocal] := SB1->B1_LOCPAD
			If Valtype(l123Auto) == "L" .And. !l123Auto
				Aviso(STR0102,STR0093,{"Ok"}) //"O Armazem informado e Invalido, o campo sera ajustando com o armazem padrão do cadastro de produtos"
			EndIf
		EndIf
	EndIf

	If lRet.And.!Empty(aCols[n][nPosPrd])
		lRet := A123ChkAlmox(PADR(aCols[n][nPosPrd],TamSX3("B1_COD")[1]),aCols[n][nPosLocal])
	EndIf

	If lRet .And. !A120Total(aCols[n][nPosTotal])
		lRet := .F.
	EndIf

	If lRet
		If Empty(aCols[n][nPosNumSC])
			HELP("   ",1,"A123PONOSI") // Não e possivel incluir PO sem SI
			lRet := .F.
		EndIf
	EndIf

	If lRet.And.aCols[n][nPosVDesc] > aCols[n][nPosTotal]
		Help(" ",1,"A12003")
		lRet := .F.
	EndIf

	If lRet .And. !PcoVldLan('000052','01','MATA121',/*lUsaLote*/,/*lDeleta*/, .T./*lVldLinGrade*/)
		lRet := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se valida ou nao o aCols a partir das validacoes do aHeader ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. lVldHead .And. FindFunction("VldHead") .And. ( Type("l123Auto") == "U" .Or. !l123Auto )
		If nPosConta <> 0 .And. nPosCC > 0 .And. nPosItemCta <> 0 .And. nPosClVl <> 0 .And.;
				!CtbAmarra(aCols[n,nPosConta],aCols[n,nPosCC],aCols[n,nPosItemCTA],aCols[n,nPosCLVL])
			lRet := .F.
		Endif
		If lRet
			lRet := VldHead(aCols,aHeader,n)
			If lRet
				n := n_SaveLin
			EndIf
		Endif
	ElseIf lRet .And. nPosConta <> 0 .And. nPosCC>0 .And. nPosItemCta <> 0 .And. nPosClVl <> 0 .And.;
			(!CtbAmarra(aCols[n,nPosConta],aCols[n,nPosCC],aCols[n,nPosItemCTA],aCols[n,nPosCLVL]) .Or.;
			(!Empty(aCols[n,nPosConta]) .And. !Ctb105Cta(aCols[n,nPosConta])) .Or.;
			(!Empty(aCols[n,nPosCC]) .And. !Ctb105CC(aCols[n,nPosCC])) .Or.;
			(!Empty(aCols[n,nPosItemCTA]) .And. !Ctb105Item(aCols[n,nPosItemCTA])) .Or.;
			(!Empty(aCols[n,nPosCLVL]) .And. !Ctb105ClVl(aCols[n,nPosCLVL])))

		lRet := .F.
	Endif

EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ-Ä¿
//³ Pontos de Entrada 											 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ-ÄÄÙ
If !aCols[n][Len(aCols[n])] .And. lRet .And. ExistTemplate("MT123LOK")
	lRet123LOK := ExecTemplate("MT123LOK",.F.,.F.)
	If Valtype( lRet123LOK ) == "L"
		lRet := lRet123LOK
	EndIf
EndIf

If !aCols[n][Len(aCols[n])] .And. lRet .And. ExistBlock("MT123LOK")
	lRet123LOK := ExecBlock("MT123LOK",.F.,.F.)
	If Valtype( lRet123LOK ) == "L"
		lRet := lRet123LOK
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123TudOk ³ Autor ³Nereu / Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica se todo o pedido esta Ok                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123TudOk(o)

Local aTam       := TamSX3("C7_TOTAL")
Local nPosPrd    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosQtd    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosPrc    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nPosTotal  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local nPosCodTab := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CODTAB"})
Local nPosNumSC  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_NUMSC"})
Local nPosItemSC := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMSC"})
Local nPosConta  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CONTA"})
Local nPosCC	 := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CC"})
Local nPosCLVL	 := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CLVL"})
Local nPosItemCTA:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMCTA"})
Local nPosGrade  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_GRADE"})
Local n_SaveLin  := n
Local nX         := 0
Local lRet123OK  := .T.
Local lRet       := .T.
Local dDataBloq	:= GetNewPar("MV_ATFBLQM",CTOD("")) //Data de Bloqueio da Movimentação - MV_ATFBLQM
lVldHead:= If(ValType(lVldHead) == "L",lVldHead, GetNewPar( "MV_VLDHEAD",.T. ) )

l123Auto := IIf(Valtype(l123Auto) == "L", l123Auto, .F.)

If cPaisLoc != "BRA"
	aTam[2] := MsDecimais(nMoedaPed)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o cabecalho foi preenchido                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty(cA120Forn).Or.Empty(cA120Loj).Or.Empty(cCondicao).Or.Empty(cA123Impo).Or.Empty(cA123Compr).Or.;
    Empty(cA123Emb).Or.Empty(cA123Via).Or.Empty(cA123Ori).Or.Empty(cA123Dest).Or.Empty(cA123Inc)
	Help(" ",1,"A12310")
	lRet := .F.
EndIf

If lRet
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Validacao de todas as linhas da GetDados               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX :=1 To Len( aCols )
		If ( !aCols[nX][Len(aCols[nX])] .And. !Empty(aCols[nX][nPosPrd]) )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se a quantidade e o preco foram preenchidos                                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Empty(aCols[nX][nPosQtd]) .Or. Empty(aCols[nX][nPosPrc]) )
				Help(" ",1,"A12006")
				lRet := .F.
				Exit
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o produto nao e canal                                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet
				SB1->(dbSetOrder(1))
				SB1->(MsSeek(xFilial("SB1")+aCols[nX][nPosPrd]))
				If (SB1->B1_MONO=="S" .And. SB1->B1_PROC != cA120Forn)
					Help(" ",1,"A120CANAL")
					lRet := .F.
					Exit
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica a integridade com a solicitacao de compra                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. !Empty(aCols[nX][nPosNumSc]) .And. !Empty(aCols[nX][nPosItemSc])
				lReferencia := MatGrdPrrf(@(aCols[nX][nPosPrd]))

				If ( lReferencia )
					If ( nPosGrade > 0 )
						aCols[nX][nPosGrade] := "S"
						lReferencia := .T.
					EndIf
				Else
					If ( nPosGrade > 0 )
						aCols[nX][nPosGrade] := "N"
					EndIf
				EndIf

				If !lReferencia
					dbSelectArea("SC1")
					dbSetOrder(2)
					If !MsSeek(xFilial()+aCols[nX][nPosPrd]+aCols[nX][nPosNumSC]+aCols[nX][nPosItemSC])
						Help(" ",1,"A12005")
						lRet := .F.
						Exit
					Endif
				Endif
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se não existe SI informada no aCols                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet
				If Empty(aCols[nX][nPosNumSC])
					Help("   ",1,"A123PONOSI")
					lRet := .F.
					Exit
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica o valor total                                                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet
				If Abs(NoRound(aCols[nX][nPosTotal],aTam[2]) - NoRound(aCols[nX][nPosQtd]*aCols[nX][nPosPrc],aTam[2]))  > (9 * (10**(-aTam[2])))
					Help(" ",1,"A12003")
					lRet := .F.
					Exit
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica a integridade com a tabela de preco                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lRet .And. nPosCodTab > 0 .And. !Empty(aCols[nX][nPosCodTab])
				If ( !MaVldTabCom(cA120Forn,cA120Loj,aCols[nX][nPosCodTab],cCondicao,/*cHelp*/,dA120Emis) )
					lRet := .F.
					Exit
				EndIf
			EndIf

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Verifica se o produto x fornecedor foi bloqueado pela Qualidade.	                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If !QieSitFornec(cA120Forn,cA120Loj,aCols[nX][nPosPrd],.F.)
				Help(" ",1,"A120BLQFO",,STR0111+Alltrim(aCols[nX][nPosPrd])+STR0112,1,11) //"O produto " ### " foi bloqueado pela Qualidade para esse Fornecedor."
				lRet := .F.
				Exit
			EndIF

			If lRet
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se valida ou nao o aCols a partir das validacoes do aHeader ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lVldHead .And. FindFunction("VldHead") .And. !l123Auto
					If nPosConta <> 0 .And. nPosCC>0 .And. nPosItemCta <> 0 .And. nPosClVl <> 0 .And.;
							!CtbAmarra(aCols[nX,nPosConta],aCols[nX,nPosCC],aCols[nX,nPosItemCTA],aCols[nX,nPosCLVL])
						lRet := .F.
						Exit
					Endif
					If !VldHead(aCols,aHeader,nX,o)
						lRet := .F.
						Exit
					Endif

					n := n_SaveLin

				ElseIf nPosConta <> 0 .And. nPosCC>0 .And. nPosItemCta <> 0 .And. nPosClVl <> 0 .And.;
						(!CtbAmarra(aCols[nX,nPosConta],aCols[nX,nPosCC],aCols[nX,nPosItemCTA],aCols[nX,nPosCLVL]) .Or.;
						(!Empty(aCols[nX,nPosConta]) .And. !Ctb105Cta(aCols[nX,nPosConta])) .Or.;
						(!Empty(aCols[nX,nPosCC]) .And. !Ctb105CC(aCols[nX,nPosCC])) .Or.;
						(!Empty(aCols[nX,nPosItemCTA]) .And. !Ctb105Item(aCols[nX,nPosItemCTA])) .Or.;
						(!Empty(aCols[nX,nPosCLVL]) .And. !Ctb105ClVl(aCols[nX,nPosCLVL])))

					lRet := .F.
					Exit
				Endif
			Endif

		Else
			aCols[nX][Len(aHeader)+1] := .T.
		EndIf
	Next nX
EndIf

//Verifica se existe bloqueio contábil
If lRet .And. (lRet := CtbValiDt(Nil,dA120Emis,/*.T.*/ ,Nil ,Nil ,{"COM001"}/*,"Data de apuração bloqueada pelo calendário contábil."*/))
	If!Empty(dDataBloq) .AND. (dA120Emis <= dDataBloq)
		//Help(" ",1,"AF012ABLQM",,"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil.",1,0) //"Processo bloqueado pelo Calendário Contábil nesta data ou período. Caso possível altere a data de referência do processo ou contate o responsável pelo Módulo Contábil."
		Help(" ",1,"ATFCTBBLQ") //P: Processo bloqueado pelo Calendário Contábil ou parâmetro de bloqueio nesta data ou período. S: Caso possível altere a data de referência do processo, verifique o parâmetro ou contate o responsável pelo Módulo Contábil.)
		lRet := .F.
	End
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se validacao do bloqueio do pco e no final, ³
//³em caso positivo valida todas as linhas              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If PcoBlqFim({{"000052","01"}, {"000053","01"}})
	n_SaveLin := n
	For nX := 1 To Len(aCols)
		If !aCols[nX][Len(aCols[nX])]
			n := nX
			If lRet
				lRet	:=	PcoVldLan('000052','01','MATA121',/*lUsaLote*/,/*lDeleta*/, .F./*lVldLinGrade*/)
				If !lRet
					Exit
				EndIf
			EndIf
		EndIf
	Next
	n := n_SaveLin
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacoes dos pontos de bloqueio SIGAPCO - P/ TOTAL PC/AE³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet
	If !PcoVldLan('000052','03','MATA121',/*lUsaLote*/,/*lDeleta*/, .F./*lVldLinGrade*/)
		lRet := .F.
	EndIf
EndIf


If lRet .And. ExistBlock("MT123OK")
	lRet123OK := ExecBlock("MT123OK",.F.,.F.)
	If Valtype( lRet123OK ) == "L"
		lRet := lRet123OK
	EndIf
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123Del  ³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica a delecao da linha                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Objeto a ser verificado.                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Del(o)

Local lDeleted := .F.

If ValType(aCols[n,Len(aCols[n])]) == "L"  /// Verifico se posso Deletar
	lDeleted := aCols[n,Len(aCols[n])]      /// Se esta Deletado
End

If !MaFisFound("IT",n)
	MaFisAdd("","",0,0,0,"","",,0,0,0,0,0)
EndIf

MaFisDel(n,aCols[n][Len(aCols[n])])
Eval(bRefresh)

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ-ÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123FormDesc³ Autor ³Nereu/Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄ-ÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a formula digitada e atualiza a descricao.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da Formula                                  ³±±
±±³          ³ ExpC2 = Descricao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123FormDesc(cFormula,cDescri)

Local lRet := .T.

If !Empty(cFormula)
	If SM4->(MsSeek(xFilial("SM4")+cFormula))
		cDescri := Sm4->M4_DESCR
	Else
		lRet := .F.
	EndIf
Else
	cDescri := ""
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ-ÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123FormReaj³ Autor ³Nereu/Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄ-ÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida a formula digitada e atualiza a descricao.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da Formula                                  ³±±
±±³          ³ ExpC2 = Descricao                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123FormReaj(cFormula,cDescri)

Local vFormula
Local lRet     := .T.

If !Empty(cFormula)
	If SM4->(MsSeek(xFilial("SM4")+cFormula))
		cDescri := SM4->M4_DESCR
		vFormula := Formula(cFormula)
		If ValType(vFormula) <> "N"
			cDescri := ""
			lRet := .F.
			Aviso( STR0102, STR0088, {"Ok"}, 2 )
		EndIf
	Else
		lRet := .F.
	EndIf
Else
	cDescri := ""
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123VldAlt ³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida se o pedido pode ser alterado.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VldAlt()

Local lRet := .T.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o usuario tem permissao para este pedido         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. !A123Restr()
	lRet := .F.
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se origem do pedido foi pela importa‡„o             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRet .And. Alltrim(SC7->C7_ORIGEM) == "EICPO400"
	Help(" ",1,"ORIGIMP",,STR0101+SC7->C7_NUMIMP,3,0) //"Este pedido nao pode ser alterado/excluido pelo modulo de Compras, pois foi emitido pelo modulo Easy Import Control atraves da P.O. --> "
	lRet := .F.
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ a123Restr ³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Restringe o pedido de compras de acordo com MV_RESTPED     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Restr()

Local aGrupo   := {}
Local cGrupo   := ""
Local cUser    := RetCodUsr()
Local nLoop    := 0
Local lRet     := .T.
Local lSolic   := .T.
Local lRestPed := If(SuperGetMv("MV_RESTPED")=="S", .T. , .F. )

aGrupo := UsrGrComp(cUser)

If aScan(aGrupo,"*") != 0
	lSolic := .F.
Else
	For nLoop := 1 to Len(aGrupo)
		cGrupo += aGrupo[nLoop]
	Next nLoop
EndIf

If ( lSolic .And. lRestPed .And. !(SC7->C7_GRUPCOM $ cGrupo) .And. SC7->C7_USER != cUser .And. !Empty(SC7->C7_USER) )
	If Empty(SC7->C7_GRUPCOM)
		Help("  ",1,"A123RSPED",,UsrRetName(SC7->C7_USER),4,11)
	Else
		Help("  ",1,"USUNAOAUT",,SC7->C7_GRUPCOM,3,25)
	EndIf
	lRet := .F.
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123PID    ³Autor³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta uma tela de Integracao com as Solicitacoes/Contratos ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Perguntas ³ MTA121                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Expo1 = Objeto da getdados                                 ³±±
±±³          ³ Expo2 = Objeto da Condicao de pagamento                    ³±±
±±³          ³ Expo3 = Objeto da descricao da condicao de pagamento       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123PID(oGetDados,oCond,oDescCond)

Local aArea		:= GetArea()
Local aAreaSX3	:= SX3->(GetArea())
Local aAreaSC1	:= SC1->(GetArea())
Local aRefImpos := MaFisRelImp('MT100',{"SC7"})
Local aCpos		:= {}
Local aMarca	:= {}
Local aCampos	:= {"C1_OK","C1_NUM","C1_ITEM","C1_PRODUTO","C1_DESCRI","C1_QUANT","C1_UM","C1_PRECO","C1_MOEDA","C1_QUJE","C1_GRUPCOM","C1_GRADE","C1_ITEMGRD"}
Local aButEnc	:= {}
Local aMark     := {}
Local aFiltro   := {}

Local cAliasPID	:= "SC1"
Local cCampoOk	:= "C1_OK"
Local cAliasSX3 := "SC1"
Local cMarca	:= GetMark()
Local cItem     := StrZero(1,Len(SC7->C7_ITEM))
Local cCadastro := ""
Local cFilter   := ""
Local cFilSC1   := 'C1_FILIAL == "'+xFilial('SC1')+'".And. C1_TIPO = 2 .And. C1_MOEDA == nMoedaPed .And. C1_QUJE < C1_QUANT .And. C1_TPOP<>"P" .And. C1_APROV$" ,L" .And. C1_RESIDUO==" "  .And.( C1_COTACAO == "IMPORT" .Or. C1_COTACAO == "'+Replicate("X",Len(SC1->C1_COTACAO))+'")'+IIF(SC1->(ColumnPos("C1_FLAGGCT"))>0,' .And. ((SC1->C1_QUJE > 0 .And. SC1->C1_FLAGGCT == " ") .Or. (SC1->C1_QUJE == 0 ))','')

Local nPosCod	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosPrc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRECO"})
Local nPosQuant := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_QUANT"})
Local nPosDsc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_VLDESC"})
Local nPosTotal	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_TOTAL"})
Local nPosNmSc	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_NUMSC"})
Local nPosItSc  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMSC"})
Local nPosCodTab:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CODTAB"})
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEM"})
Local nPosCC	:= aScan(aHeader,{|x| AllTrim(x[2]) == "C7_CC"})
Local nPosGrade := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_GRADE"})
Local nPosItGra := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMGRD"})
Local nExistIt  := 0
Local nDeleta   := 0
Local nOpcA     := 0
Local nX        := 0
Local nY        := 0
Local cItPed    := If(Empty(aCols[1,nPosCod]),StrZero(1,Len(SC7->C7_ITEM)),Soma1(aCols[Len(aCols)][nPosItem]))
Local lMarkAll  := .F.
local lGrade    := MaGrade()
Local lMT123Mak := ExistBlock("MT123MAK")
Local lInverte	:= .F.

Local oDlg
Local oMark
Local oChk1
Local oSize
Local bKeyF4	:=  SetKey( VK_F4 )
Local bKList    := bListRefresh
Local bKGDR		:= bGDRefresh
Local nColNmSc  := 0
Local nColItSc  := 0
Local nColItGra := 0

SetKey( VK_F4,Nil )

If ValType(aA123PID) == "U"
	aA123PID := {}
EndIf

If lMT123Mak
	nX    := 0
	aMark := ExecBlock("MT123MAK",.F.,.F.,{"SC1"})
	If ValType( aMark ) <> "A"
		aMark := {}
	EndIf

	If Len(aMark) > 0
		For nX:= 1 to Len(aMark)
			AADD(aCampos,aMark[nX])
		Next nX
	EndIf
EndIf

nX := 0

If MaFisFound("NF")

	cCadastro := STR0061
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica o filtro de usuario                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "A123PIDF" )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Estrutura do array                                           ³
		//³ 1 - String - Filtro no SC1 para ISAM ( Sintaxe xBase )       ³
		//³ 2 - String - Filtro no SC1 para SQL  ( Sintaxe SQL   )       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aFiltro := 	ExecBlock( "A123PIDF", .F., .F. )
		If ValType( aFiltro ) <> "A"
			aFiltro := {}
		EndIf

		dbSelectArea("SC1")
		dbSetOrder(1)
		Set Filter To &(afiltro[1])
		cFilter := afiltro[1]
	Else
		dbSelectArea("SC1")
		dbSetOrder(1)
		Set Filter To &(cFilSC1)
		cFilter := cFilSC1
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta os botoes da enchoice passando o filtro para ser restaurado na saida da visualizacao ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aButEnc	:={	{"PESQUISA"	,{|| axPesqui() }					,STR0006} ,;	//"Pesquisar"
				{"relatorio",{||a123VisuSC(RecNo(),cFilter)}	,STR0007} }		//"Visualizar"

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o Header com os titulos do MsSelect             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SX3")
	dbSetOrder(2)
	For nX := 1 to Len(aCampos)
		If MsSeek(aCampos[nX])
			If AllTrim(aCampos[nX])$"C1_OK"
				AADD(aCpos,{aCampos[nX],""," ",X3_PICTURE})
			Else
				AADD(aCpos,{aCampos[nX],"",AllTrim(X3Titulo()),X3_PICTURE})
			EndIf
		EndIf
	Next nX

	dbSelectArea("SX3")
	dbSetOrder(1)
	MsSeek(cAliasSX3)
	While !Eof() .And. SX3->X3_ARQUIVO == cAliasSX3
		IF ( SX3->X3_BROWSE=="S".And.SX3->X3_CONTEXT <> "V" .And. X3Uso(SX3->X3_USADO).And. Ascan(aCpos,{|x| Alltrim(x[1]) == Alltrim(SX3->X3_CAMPO)})==0 )
			Aadd(aCpos,{SX3->X3_CAMPO,"",AllTrim(X3Titulo()),X3_PICTURE})
		EndIf
		dbSkip()
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula dimensões                                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oSize := FwDefSize():New()
	oSize:aWorkArea := {000,000, 450, 150 }
	oSize:AddObject( "GETDADOS" ,  100, 80, .T., .T. ) // Totalmente dimensionavel
	oSize:AddObject( "RODAPE"   ,  100, 20, .T., .T. ) // Totalmente dimensionavel

    oSize:lProp 	:= .T. // Proporcional
	oSize:aMargins 	:= { 3, 3, 3, 3 } // Espaco ao lado dos objetos 0, entre eles 3

	oSize:Process() 	   // Dispara os calculos

	DEFINE MSDIALOG oDlg TITLE cCadastro FROM 000,000 TO 300,900 OF oMainWnd PIXEL

	oMark := MsSelect():New(cAliasPID,cCampoOk,"A123FLAG()",aCpos,@lInverte,@cMarca,;
						{ oSize:GetDimension("GETDADOS","LININI")+30 ,oSize:GetDimension("GETDADOS","COLINI"),;
						  oSize:GetDimension("GETDADOS","LINEND")+20,oSize:GetDimension("GETDADOS","COLEND")+3})
	oMark:bMark := {|| A123AddMark(cMarca, aMarca, oMark, lMarkAll)}
	oMark:oBrowse:lHasMark    :=.T.
	oMark:oBrowse:lCanAllMark :=.T.
	oMark:oBrowse:bAllMark    := {|| A123AllMark(cMarca, aMarca, oMark)}
	oMark:oBrowse:GoTop()
	oMark:oBrowse:Refresh()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Check para Marcar / Desmarcar Todos os itens de um CS ou AE ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oChk1                 := TCHECKBOX():Create(oDlg)
	oChk1:cName           := 'oChk1'
	oChk1:cCaption        := STR0105 //'Marca / Desmarca todos los ítems relacionados a la Solicitud de Importación'
	oChk1:nLeft           := 05
	oChk1:nTop            := 275
	oChk1:nWidth          := 400
	oChk1:nHeight         := 17
	oChk1:lShowHint       := .T.
	oChk1:lReadOnly       := .F.
	oChk1:cVariable       := 'lMarkAll'
	oChk1:bSetGet         := {|u| If(PCount()>0, lMarkAll:=u, lMarkAll) }
	oChk1:Align           := 0
	oChk1:bChange           := {|| A123AllMark(cMarca, aMarca, oMark)}
	oChk1:lVisibleControl := .T.
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||(nOpcA:=1),(oDlg:End())},{||oDlg:End()},,aButEnc)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desfaz o Filtro do Arquivo SC1                       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC1")
	dbSetOrder(1)
	dbClearFilter()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta o aCols com as SCs/Contratos selecionados      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nOpcA== 1
		nColNmSc  := SC1->(ColumnPos(aCampos[2]))
		nColItSc  := SC1->(ColumnPos(aCampos[3]))
		nColItGra := SC1->(ColumnPos(aCampos[13]))
		For nX := 1 to Len(aMarca)

			dbSelectArea(cAliasPID)
			MsGoto(aMarca[nX])
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Varre o aCols e compara se ja existe o item da solicitacao no pedido ³
			//³ se existir, nao sera inclusa no pedido.                              ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nExistIt := aScan(aCols,{|x| x[nPosNmSc] == FieldGet(nColNmSc) .And. x[nPosItSc] == FieldGet(nColItSc) .And.x[nPosItGra] == FieldGet(nColItGra) })

			If nExistIt = 0

				If aMarca[nX] > 0

					nDeleta := Ascan(aCols,{|x| ALLTRIM(x[nPosCod]) == ""})
					If (cPaisLoc $ "ARG|BOL" .AND. nDeleta > 0 .AND. Empty(aCols[nDeleta][nPosCod] ))  .OR. ;
						(!(cPaisLoc $ "ARG|BOL") .AND. nDeleta > 0 )
						Adel(aCols,nDeleta)
						Asize(aCols,Len(aCols)-1)
						MaColsToFis(aHeader,aCols)
					Endif

					nExistGra := Ascan(aCols,{|x| x[nPosNmSc] == FieldGet(nColNmSc) .And. x[nPosItSc] == FieldGet(nColItSc)})
					If nExistGra == 0
						cItem := cItPed
						cItPed:= Soma1(cItPed)
					Else
						cItem:= acols[nExistGra,nPosItem]
					Endif

					AADD(aCols,Array(Len(aHeader)+1))
					For nY := 1 to Len(aHeader)
						If Trim(aHeader[nY][2]) == "C7_ITEM"
							aCols[Len(aCols)][nY] 	:= cItem
						Elseif Trim(aHeader[nY][2]) == "C7_ALI_WT"
							aCols[Len(aCols)][nY] 	:= "SC7"
						ElseIf Trim(aHeader[nY][2])=="C7_REC_WT"
							aCols[Len(aCols)][nY] 	:= 0
						Else
							aCols[Len(aCols)][nY] := CriaVar(aHeader[nY][2],(aHeader[nY][10] <> "V") )
						EndIf
						aCols[Len(aCols)][Len(aHeader)+1] := .F.
					Next nY

					n := Len(aCols)

					A123MovCampos(SC1->C1_QUANT-SC1->C1_QUJE)

					aCols[n,nPosGrade]:=SC1->C1_GRADE
					aCols[n,nPosItGra]:=SC1->C1_ITEMGRD
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Carrega a Tabela de Preco se a mesma estiver informada no cadastro Produto X Fornecedor³
					//³e for valida conforme a condicao de pagamento informada no cabecalho do Pedido Compras.³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If nPosCodTab > 0
						dbSelectArea("SA5")
						dbSetOrder(1)
						lAchou := MsSeek(xFilial("SA5")+cA120Forn+cA120Loj+aCols[n][nPosCod])
						If lAchou
							If !Empty(SA5->A5_CODTAB)
								If MaVldTabCom(cA120Forn,cA120Loj,SA5->A5_CODTAB,cCondicao,/*cHelp*/,dA120Emis)
									aCols[n][nPosCodTab] := SA5->A5_CODTAB
								EndIf
							EndIf
						Else
							cProdRef	:= aCols[n][nPosCod]
							lReferencia	:= MatGrdPrRf(@cProdRef,.T.)
							If lReferencia
								dbSelectArea("SA5")
								dbSetOrder(9)
								If MsSeek(xFilial("SA5")+cA120Forn+cA120Loj+cProdRef)
									If !Empty(SA5->A5_CODTAB)
										If MaVldTabCom(cA120Forn,cA120Loj,SA5->A5_CODTAB,cCondicao,/*cHelp*/,dA120Emis)
											aCols[n][nPosCodTab] := SA5->A5_CODTAB
										EndIf
									EndIf
								EndIf
							EndIf
						EndIf
					EndIf
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicia a Carga do item nas funcoes MATXFIS  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					MaFisAdd(aCols[n][nPosCod],"",aCols[n][nPosQuant],aCols[n][nPosPrc],aCols[n][nPosDsc],"","",,0,0,0,0,aCols[n][nPosTotal])

					A120Tabela("C7_QUANT",SC1->C1_QUANT-SC1->C1_QUJE)
					MaFisAlt("IT_PRCUNI",aCols[n,nPosPrc],N)
					MaFisAlt("IT_VALMERC",aCols[n,nPosTotal],N)
					MaColsToFis(aHeader,aCols,n,"MT120",.T.)

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Impede que o item do PC seja deletado pela getdados da NFE na movimentacao das setas. ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					oGetDados:lNewLine := .F.
					AAdd(aA123PID,aMarca[nx])
				EndIf
			EndIf
		Next
	Else
		For nX := 1 to Len(aMarca)
			dbSelectArea(cAliasPID)
			MsGoto(aMarca[nx])
			If aMarca[nx] > 0
				If SimpleLock(cAliasPID)
					(cAliasPID)->(MsRUnlock())
				Endif
			Endif
		Next nX
	EndIf

	RestArea(aAreaSC1)
	RestArea(aAreaSX3)

	If lGrade .And. Len(aMarca) > 0

		For nX := 1 To Len(aCols)
			lReferencia := MatGrdPrrf(aCols[nx,2])
			If lReferencia
				Exit
			EndIf
		Next
		If lReferencia
			aCols := aSort(aCols,,,{|x,y| x[nPosItem]+x[nPosItGra] < y[nPosItem]+y[nPosItGra] })
			aCols := aColsGrade(oGrade, aCols, aHeader, "C7_PRODUTO", "C7_ITEM", "C7_ITEMGRD",,.F.)
        EndIf
		MaColsToFis(aHeader,aCols,,"MT120",.T.)

		MaFisAlt("NF_FRETE",avalores[FRETE])
		MaFisAlt("NF_DESPESA",avalores[VALDESP])
		MaFisAlt("NF_SEGURO",avalores[SEGURO])
		MaFisAlt("NF_DESCONTO",avalores[VALDESC])
		N := Len(aCols)
	Endif

	//Restaura variaveis
	bListRefresh:=bKList
	bGDRefresh:=bKGDR

	Eval(bListRefresh)
	Eval(bGDRefresh)

Else
	Help("  ",1,"A123CAB") // Campos obrigatorios do cabecalho nao preenchidos.
EndIf

RestArea(aArea)
SetKey( VK_F4,bKeyF4 )

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123Flag   ³ Autor ³Nereu /Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Valida o Controle de marcas da Solicitacao de Importacao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Flag()

Local aGrupo := UsrGrComp(RetCodUsr())
Local lSolic := If(SuperGetMv("MV_RESTCOM")=="S",.T.,.F.)

Local cFlag	 := (If(C1_QUJE>=C1_QUANT,'X',' ')+If(C1_TPOP=='P'.And.C1_APROV$"R,B",'X',' ');
	+If(Empty(SC1->C1_GRUPCOM),' ',If(lSolic.And.aScan(aGrupo,SC1->C1_GRUPCOM) <= 0,'X',' ')))

Return cFlag

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123AddMark³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Adiciona o Recno() do registro no controle de marcas        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Marca                                              ³±±
±±³          ³ ExpA1 = Array da Marca                                     ³±±
±±³          ³ ExpO1 = Objeto da Marca                                    ³±±
±±³          ³ ExpL1 = Variavel logica que indica se marca todos ou nao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A123AddMark(cMarca, aMarca, oMark, lMarkAll)

Local cSeek      := C1_FILIAL+C1_NUM
Local cCompara   := 'C1_FILIAL+C1_NUM'
Local cCpoMarca  := 'C1_OK'
Local nOrderSeek := 1 //C1_FILIAL+C1_NUM+C1_ITEM
Local cAlias     := Alias()
Local nRecno     := Recno()
Local nPos       := 0
Local nCpoMarca  := SC1->(ColumnPos(cCpoMarca))

If !(SC1->C1_OK == cMarca)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Desmarca registro posicionado                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nPos := (aScan(aMarca, RecNo()))
	If nPos > 0
		aDel(aMarca, nPos); aSize(aMarca, Len(aMarca)-1)
		If SimpleLock(cAlias)
			(cAlias)->(MsRUnlock())
		Endif
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Checa se deve desmarcar todos os registros                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMarkAll
		dbSelectArea(cAlias)
		dbSetOrder(nOrderSeek)
		If MsSeek(cSeek, .F.)
			Do While !Eof() .And. cSeek == &(cCompara)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Desmarca registro posicionado                   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				nPos := (aScan(aMarca, RecNo()))
				If nPos > 0
					aDel(aMarca, nPos); aSize(aMarca, Len(aMarca)-1)
				EndIf
				If !(Recno()==nRecno)
					Reclock(cAlias, .F.)
					Fieldput(nCpoMarca, '')
					MsUnlock()
				EndIf
				If SimpleLock(cAlias)
					(cAlias)->(MsRUnlock())
				Endif
				dbSkip()
			EndDo
		EndIf
		dbGoto(nRecno)
		oMark:oBrowse:Refresh()
	EndIf

Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Marca registro posicionado                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aScan(aMarca, Recno()) == 0
		aAdd(aMarca, Recno())
		SimpleLock(cAlias)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Checa se deve marcar todos os registros	                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lMarkAll
		dbSetOrder(nOrderSeek)
		If MsSeek(cSeek, .F.)
			Do While !Eof() .And. cSeek == &(cCompara)
				If aScan(aMarca, Recno()) == 0
					aAdd(aMarca, Recno())
				EndIf
				If !(Recno()==nRecno)
					Reclock(cAlias,.F.)
					Fieldput(nCpoMarca, cMarca)
					MsUnlock()
					SimpleLock(cAlias)
				EndIf
				dbSkip()
			EndDo
		EndIf
		dbGoto(nRecno)
		oMark:oBrowse:Refresh()
	EndIf
EndIf

Return Nil

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123VisuSC³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Visualiza a Solicitacao/Contrato posicionado.              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Recno()                                            ³±±
±±³          ³ ExpC1 = Filtro                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VisuSC(nRecNo,cFilter)

Local aArea		 := GetArea()
Local aAreaSC1	 := SC1->(GetArea())
Local aSavHeader := aClone(aHeader)
Local aSavCols	 := aClone(aCols)
Local nSavPed	 := MaFisSave()
Local nSavN      := N

DEFAULT cFilter  := ""

MaFisEnd()
N := 1

dbSelectArea("SC1")
MsGoto(nRecNo)
MATA113(,,2)
Pergunte("MTA123",.F.)
If !Empty(cFilter)
	dbSelectArea("SC1")
	dbSetOrder(1)
	Set Filter To &(cFilter)
Endif

aCols	:= aClone(aSavCols)
aHeader := aClone(aSavHeader)
N       := nSavN

MaFisRestore(nSavPed)

RestArea(aAreaSC1)
RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123ChkAlmox³Autor³Nereu/Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Aviso de que a almoxarifado nao existe                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do produto                                ³±±
±±³          ³ ExpC2 = Codigo do almoxarifado                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A123ChkAlmox(cProduto,cAlmox)

Local aArea	 	  := GetArea()
Local cEntrega	  := If(SuperGetMv("MV_PCFILEN"),xFilEnt(cFilialEnt),xFilial("SB2"))
Local cLinha1     := STR0095+ cAlmox + STR0096	//"O Armanzen "###" nao existe para este produto. "
Local cLinha2     := STR0097  					//"Deseja criar agora ? "
Local lGrade      := MaGrade()
Local lReferencia := .F.
Local lRet 		  := .T.

If lGrade
	lReferencia := MatGrdPrrf(@cProduto)
Endif

If !lReferencia

	If Empty(cEntrega) .And. !Empty(xFilial("SB2"))
		cEntrega := xFilial("SB2")
	EndIf

	dbSelectArea("SB2")
	dbSetOrder(1)
	If !MsSeek(cEntrega+cProduto+cAlmox,.F.)
		TONE(3500,1)
		If ( Type("l123Auto") != "U" .And. l123Auto )
			CriaSB2(cProduto,cAlmox,cEntrega)
			lRet := .T.
		Else
			If MsgYesNo(cLinha1+cLinha2,STR0098+cProduto) //"Aten‡„o - "
				CriaSB2(cProduto,cAlmox,cEntrega)
			Else
				lRet := .F.
			EndIf
		EndIf
	EndIf
Endif

RestArea(aArea)

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123Leg  ³Autor  ³Nereu / Alexandre Lemes³Data  ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cria uma janela contendo a legenda da mBrowse              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Legend()

Local aLegenda	 := {}
Local aLegeUsr   := {}

aAdd(aLegenda,{"ENABLE" 		,STR0079})
aAdd(aLegenda,{"BR_AMARELO"		,STR0083})
aAdd(aLegenda,{"DISABLE"		,STR0080})
aAdd(aLegenda,{"BR_AZUL"		,STR0081})
aAdd(aLegenda,{"BR_CINZA"		,STR0099})
aAdd(aLegenda,{"BR_LARANJA"		,STR0116})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de entrada para inclusão de novo STATUS da legenda    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( ExistBlock("MT123LEG") )
	aLegeUsr := ExecBlock("MT123LEG",.F.,.F.,{aLegenda})
	If ( ValType(aLegeUsr) == "A" )
		aLegenda := aClone(aLegeUsr)
	EndIf
EndIf

BrwLegenda(cCadastro,STR0078, aLegenda )

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ProcH      ³ Autor ³ Microsiga S/A        ³ Data ³ 14.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Pesquisa campo dentro do array aAutoCab			          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ProcH(cCampo)
Return aScan(aAutoCab,{|x|Trim(x[1])== cCampo })

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123DescCnd³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preenche a descricao da condicao de pagto.                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A123DescCnd(@ExpC1,@Expo1,ExpC2)                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo da Condicao de Pagamento                    ³±±
±±³          ³ Expo1 = Objeto da Descricao da Condicao de Pagamento       ³±±
±±³          ³ ExpC2 = Decricao da condicao de pagamento                  ³±±
±±³          ³ Expo2 = Objeto da GetDados                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123DescCnd(cCondicao,oDescCond,cDescCond,oGetDados)

dbSelectArea("SE4")
dbSetOrder(1)
If MsSeek(xFilial("SE4")+cCondicao)
	cDescCond := SE4->E4_DESCRI
	If oDescCond != Nil
		oDescCond:Refresh()
	EndIf
EndIf

If oGetDados <> Nil
	oGetDados:oBrowse:Refresh()
EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123Impri ³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetua a chamada do relatorio padrao ou do usuario         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpX1 := A123Impri( ExpC1, ExpN1, ExpN2 )                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Alias do arquivo                                  ³±±
±±³          ³ ExpN1 -> Recno do registro                                 ³±±
±±³          ³ ExpN2 -> Opcao do Menu                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpX1 -> Retorno do relatorio                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A123Impri( cAlias, nRecno, nOpc )

Local aArea    := GetArea()
Local cPrinter := SuperGetMv("MV_PCOMPRA" ,, "" )
Local cPedido  := ""
Local lBackPed := lPedido
Local lRet     := .T.

If !Empty( cPrinter ) .And. Existblock( cPrinter )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a chamada do relatorio de usuario                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ExecBlock( cPrinter, .F., .F., { cAlias, nRecno, nOpc } )
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz a chamada do relatorio padrao                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc == "BRA"
		lRet := MATR110( cAlias, nRecno, nOpc )
	Else
		lRet := MATR111( cAlias, nRecno, nOpc )
	EndIf
EndIf

RestArea( aArea )
lPedido := lBackPed
cPedido := SC7->C7_NUM

If Type( "bFiltraBRW" ) == "B"
	Eval( bFiltraBRW )
EndIf

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123Copia ³ Rev.  ³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de inclusao/alteracao/exclusao de Pedidos         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A123Pedido(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Copia(cAlias,nReg,nOpcX)

Local lRet

If !SC7->(Eof())
	lRet := A123Pedido(cAlias,nReg,nOpcX,,.T.)
EndIf

Return( lRet )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MyMata123 ³ Autor ³Nereu / Alexandre Lemes³ Data ³03.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de teste da rotina automatica do programa MATA123     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar testes na rotina de    ³±±
±±³          ³documento de entrada                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MyMata123()

Local aCabec := {}
Local aItens := {}
Local aLinha := {}
Local nX     := 0
Local nY     := 0
Local cDoc   := ""
Local lOk    := .T.

PRIVATE lMsErroAuto := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Abertura do ambiente                                         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de 10 pedidos de compra com 30 itens cada",80))
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "COM" TABLES "SC7","SA2","SB1","SB2","SF4"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verificacao do ambiente para teste                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)
If !SB1->(MsSeek(xFilial("SB1")+"PA001"))
	lOk := .F.
	ConOut("Cadastrar produto: PA001")
EndIf
dbSelectArea("SF4")
dbSetOrder(1)
If !SF4->(MsSeek(xFilial("SF4")+"001"))
	lOk := .F.
	ConOut("Cadastrar TES: 001")
EndIf
dbSelectArea("SE4")
dbSetOrder(1)
If !SE4->(MsSeek(xFilial("SE4")+"001"))
	lOk := .F.
	ConOut("Cadastrar condicao de pagamento: 001")
EndIf
If !SB1->(MsSeek(xFilial("SB1")+"PA002"))
	lOk := .F.
	ConOut("Cadastrar produto: PA002")
EndIf
dbSelectArea("SA2")
dbSetOrder(1)
If !SA2->(MsSeek(xFilial("SA2")+"F0000101"))
	lOk := .F.
	ConOut("Cadastrar fornecedor: F0000101")
EndIf
If lOk
	ConOut("Inicio: "+Time())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Verifica o ultimo documento valido para um fornecedor        |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SC7")
	dbSetOrder(1)
	MsSeek(xFilial("SC7")+"zzzzzz",.T.)
	dbSkip(-1)
	cDoc := SC7->C7_NUM
	For nY := 1 To 10
		aCabec := {}
		aItens := {}

		If Empty(cDoc)
			cDoc := StrZero(1,Len(SC7->C7_NUM))
		Else
			cDoc := Soma1(cDoc)
		EndIf

		aadd(aCabec,{"C7_NUM"     ,cDoc})
		aadd(aCabec,{"C7_EMISSAO" ,dDataBase})
		aadd(aCabec,{"C7_FORNECE" ,"F00001"})
		aadd(aCabec,{"C7_LOJA"    ,"01"})
		aadd(aCabec,{"C7_COND"    ,"001"})
		aadd(aCabec,{"C7_CONTATO" ,"AUTO"})
		aadd(aCabec,{"C7_FILENT"  ,cFilAnt})
		aadd(aCabec,{"C7_MOEDA"   ,1})
		aadd(aCabec,{"C7_FRETE"   ,0})
		aadd(aCabec,{"C7_DESPESA" ,0})
		aadd(aCabec,{"C7_SEGURO"  ,0})
		aadd(aCabec,{"C7_DESC1"   ,0})
		aadd(aCabec,{"C7_DESC2"   ,0})
		aadd(aCabec,{"C7_DESC3"   ,0})
		aadd(aCabec,{"C7_MSG"     ,""})
		aadd(aCabec,{"C7_REAJUST" ,""})
		For nX := 1 To 30
			aLinha := {}
			aadd(aLinha,{"C7_PRODUTO"  ,"PA001",Nil})
			aadd(aLinha,{"C7_QUANT",1,Nil})
			aadd(aLinha,{"C7_PRECO",100,Nil})
			aadd(aLinha,{"C7_TOTAL",100,Nil})
			aadd(aLinha,{"C7_TES","001",Nil})
			aadd(aItens,aLinha)
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Teste de Inclusao                                            |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MATA123(1,aCabec,aItens)
		If !lMsErroAuto
			ConOut("Incluido com sucesso! "+cDoc)
		Else
			ConOut("Erro na inclusao!")
		EndIf
	Next nY
	ConOut("Fim  : "+Time())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de exclusao                                            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCabec := {}
	aItens := {}
	aadd(aCabec,{"C7_NUM"     ,cDoc})
	aadd(aCabec,{"C7_EMISSAO" ,dDataBase})
	aadd(aCabec,{"C7_FORNECE" ,"F00001"})
	aadd(aCabec,{"C7_LOJA"    ,"01"})
	aadd(aCabec,{"C7_COND"    ,"001"})
	aadd(aCabec,{"C7_CONTATO" ,"AUTO"})
	aadd(aCabec,{"C7_FILENT"  ,cFilAnt})
	aadd(aCabec,{"C7_MOEDA"   ,1})
	aadd(aCabec,{"C7_MOEDA"   ,1})
	aadd(aCabec,{"C7_FRETE"   ,0})
	aadd(aCabec,{"C7_DESPESA" ,0})
	aadd(aCabec,{"C7_SEGURO"  ,0})
	aadd(aCabec,{"C7_DESC1"   ,0})
	aadd(aCabec,{"C7_DESC2"   ,0})
	aadd(aCabec,{"C7_DESC3"   ,0})
	aadd(aCabec,{"C7_MSG"     ,""})
	aadd(aCabec,{"C7_REAJUST" ,""})
	For nX := 1 To 30
		aLinha := {}
		aadd(aLinha,{"C7_ITEM",StrZero(nX,Len(SC7->C7_ITEM)),Nil})
		aadd(aLinha,{"C7_PRODUTO","PA001",Nil})
		aadd(aLinha,{"C7_QUANT",2,Nil})
		aadd(aLinha,{"C7_PRECO",100,Nil})
		aadd(aLinha,{"C7_TOTAL",200,Nil})
		aadd(aItens,aLinha)
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de Exclusao                                            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ConOut(PadC("Teste de exclusao",80))
	ConOut("Inicio: "+Time())
	MATA123(1,aCabec,aItens,5)
	If !lMsErroAuto
		ConOut("Exclusao com sucesso! "+cDoc)
	Else
		ConOut("Erro na exclusao!")
	EndIf
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
EndIf
RESET ENVIRONMENT

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ A123Track³ Autor ³Nereu / Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A123Track()

Local aEnt     := {}
Local cKey     := cA120Num
Local nPosItem := GDFieldPos( "C7_ITEM" )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa a funcao fiscal                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AAdd( aEnt, { "SC7", Iif(cFilialEnt <> xFilial("SC7") , cFilialEnt , xFilial("SC7")) + cKey + aCols[1][nPosItem]} )

MaFisSave()
MaFisEnd()

MaTrkShow( aEnt )

MaFisRestore()

Return( .T. )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123DescMoed|Autor ³Nereu /Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preenche a descricao da Moeda                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1 = Moeda do pedido                                     ³±±
±±³          ³Expo1 = Objeto da descricao da moeda do pc                  ³±±
±±³          ³ExpC1 = Descricao da moeda do PC                            ³±±
±±³          ³ExpN2 = Taxa da moeda do PC                                 ³±±
±±³          ³ExpA1 = Array com os objetos do cabecalho                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123DescMoed(nMoedaPed,oDescMoed,cDescMoed,nTxMoeda,aObj)

Local lMoedLocTx := GetNewPar("MV_MOEDLOC",.F.) //Param criado para atualizar a TX da moeda no PC em outros paises o DEFAULT nao atualiza a Taxa.

cDescMoed := SuperGetMv("MV_MOEDA"+AllTrim(Str(nMoedaPed,2)))

If oDescMoed != Nil
	oDescMoed:Refresh()
EndIf

IF cPaisLoc=="BRA" .Or. lMoedLocTx
	nTxMoeda:=RecMoeda(dDataBase,nMoedaPed)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Redefine as Pictures dos objetos dos Folders do PC quando a Moeda e  ³
//³ alterada, necessaria para paises onde nao existe o uso de casas deci ³
//³ mais na moeda corrente. Exemplo: PESOS no Chile com PC em Dolar.     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aObj != Nil .and. cPaisLoc != "BRA"
	aObj[01]:PICTURE:=PesqPict('SC7','C7_TOTAL',17,nMoedaPed)
	aObj[02]:PICTURE:=PesqPict('SC7','C7_VLDESC',17,nMoedaPed)
	aObj[03]:PICTURE:=PesqPict('SC7','C7_FRETE',17,nMoedaPed)
	aObj[04]:PICTURE:=PesqPict('SC7','C7_TOTAL',17,nMoedaPed)
	aObj[12]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
	aObj[13]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
	aObj[14]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
	aObj[15]:PICTURE:=PesqPict('SC7','C7_DESC1',,nMoedaPed)
	aObj[16]:PICTURE:=PesqPict('SC7','C7_DESC2',,nMoedaPed)
	aObj[17]:PICTURE:=PesqPict('SC7','C7_DESC3',,nMoedaPed)
	aObj[18]:PICTURE:=PesqPict('SC7','C7_VLDESC',14,nMoedaPed)
	aObj[19]:PICTURE:=PesqPict('SC7','C7_TOTAL',14,nMoedaPed)
	aObj[22]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
	aObj[23]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
	aObj[24]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
//	aObj[25]:PICTURE:=PesqPict('SC7','C7_FRETE',14,nMoedaPed)
EndIf

Return .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123VldMoeda|Autor ³Nereu /Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Valida se a moeda do PO pode ser alterada apos a carga da SI³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VldMoeda()

Local aArea      := GetArea()
Local nPosPrd    := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_PRODUTO"})
Local nPosNumSC  := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_NUMSC"})
Local nPosItemSC := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_ITEMSC"})
local nX         := 0
Local lRet       := .T.

For nX :=1 To Len(aCols)
	If !aCols[nX][Len(aCols[nX])] .And. !Empty(aCols[nX][nPosPrd])
		dbSelectArea("SC1")
		dbSetOrder(2)
		If MsSeek(xFilial("SC1")+aCols[nX][nPosPrd]+aCols[nX][nPosNumSC]+aCols[nX][nPosItemSC])
			If SC1->C1_MOEDA <> nMoedaPed
				Help(" ",1,"A123DIFMOE")
	            lRet := .F.
	            Exit
			EndIf
        EndIf
	EndIf
Next nX

RestArea(aArea)

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³CalcDesc  ³ Autor ³Nereu / Alexandre Lemes³ Data ³ 03/03/08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Calcula descontos em cascatas de um valor.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³CalcDesc(Exp1,Exp2,Exp3,Exp4)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³nValor - valor a ser calculado o desconto                   ³±±
±±³          ³nDesc1 - desconto 1                                         ³±±
±±³          ³nDesc2 - desconto 2                                         ³±±
±±³          ³nDesc3 - desconto 3                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ valor do desconto                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function CalcDesc( nValor, nDesc1, nDesc2, nDesc3)

Local nTotDesc:= 0

nTotDesc := NoROUND(nValor*nDesc1/100,2)
nTotDesc := nTotDesc + ROUND((nValor-nTotDesc)*nDesc2/100,2)
nTotDesc := nTotDesc + ROUND((nValor-nTotDesc)*nDesc3/100,2)

Return (nTotDesc)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³a123AllMark³ Autor ³Nereu /Alexandre Lemes³ Data ³ 03.03.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Adiciona o Recno() do registro no controle de marcas        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Marca                                              ³±±
±±³          ³ ExpA1 = Array da Marca                                     ³±±
±±³          ³ ExpO1 = Objeto da Marca                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A123AllMark(cMarca, aMarca, oMark)

Local cAlias       := Alias()
Local cCpoMarca    := 'C1_OK'
Local nOrderSeek   := 1 //C1_FILIAL+C1_NUM+C1_ITEM
Local nPos         := 0
Local nRecno       := Recno()
Local lMarca	   := .T.
Local nCpoMarca  := SC1->(ColumnPos(cCpoMarca))

dbSelectArea(cAlias)
dbSetOrder(nOrderSeek)
dbGoTop()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se Marca ou Desmarca Todos                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (SC1->C1_OK == cMarca)
	lMarca := .F.
EndIf

Do While !Eof()
	Reclock(cAlias, .F.)
	If lMarca
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Marca registro posicionado                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If aScan(aMarca, Recno()) == 0
			aAdd(aMarca, Recno())
			SimpleLock(cAlias)
		EndIf
		Fieldput(nCpoMarca, cMarca)
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Desmarca registro posicionado                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nPos := (aScan(aMarca, RecNo()))
		If nPos > 0
			aDel(aMarca, nPos)
			aSize(aMarca, Len(aMarca)-1)
			SimpleLock(cAlias)
			Fieldput(nCpoMarca, '')
		EndIf

	EndIf
	MsUnlock()
	dbSkip()
EndDo

dbGoto(nRecno)
oMark:oBrowse:Refresh()

Return Nil

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123MarF4 ³ Autor ³Nereu /Alexandre Lemes³ Data ³ 21.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verifica se a SC/AE nao foi selecionada em outra estacao   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = Array da Marca                                     ³±±
±±³          ³ ExpO1 = Objeto da Marca                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß*/
Function A123MarF4(aArrayF4,oQual)

Local aArea := GetArea()
Local cFlag := " "

dbSelectArea("SC1")
dbSetOrder(1)

If MsSeek(xFilial()+aArrayF4[oQual:nAt,2]+aArrayF4[oQual:nAt,3])

	cFlag := A123Flag()

	If SoftLock("SC1") .And. Empty(cFlag)
		aArrayF4[oQual:nAt,1] := !aArrayF4[oQual:nAt,1]
		SimpleLock("SC1")
	ElseIf !Empty(cFlag)
		MsgInfo( STR0092 , STR0102 ) //Registro utilizado por outro usuário.
		If aArrayF4[oQual:nAt,1]
			aArrayF4[oQual:nAt,1] := !aArrayF4[oQual:nAt,1]
		Endif
	Endif

	If !aArrayF4[oQual:nAt,1]
		If SimpleLock("SC1")
			SC1->(MsRUnlock())
		Endif
	Endif
Endif

RestArea(aArea)

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a123Grd2Acols³Autor³Patricia Duca         ³ Data ³12.12.06  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Se o recurso Grade MV_GRADE estiver ativo a funcao e chamada³±±
±±³          ³no inicio da funcao A123Grava para compatibilizar o aCols na³±±
±±³          ³existencia de um produto de grade no aCols. Os itens desta  ³±±
±±³          ³grade serao explodidos no aCols repetindo o mesmo numero de ³±±
±±³          ³item C7_ITEM porem com o numero de item de grade diferente  ³±±
±±³          ³C7_ITEMGRD, a funcao fiscal sera reaberta para o recalculo  ³±±
±±³          ³de todos os item da grade do produto, e o rateio das despes ³±±
±±³          ³as entre todos os itens do aCols. Esse procedimento e nece  ³±±
±±³          ³ssario para compatibilizar o aCols para funcao de gravacao. ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a123Grd2Acols()

Local aRefImpSC7:= MaFisRelImp('MT100',{"SC7"})
Local aColsOrig := aClone(aCols)
Local cProdRef  := ""
Local cItem     := ""
Local cValid    := ""
Local cRefCols  := ""
Local nMaxFor   := Len(aColsOrig)
Local nPProduto := aScan(aHeader,{|x| AllTrim(x[2])=="C7_PRODUTO"})
Local nPItem    := aScan(aHeader,{|x| AllTrim(x[2])=="C7_ITEM"})
Local nPItGrade := aScan(aHeader,{|x| AllTrim(x[2])=="C7_ITEMGRD"})
Local nPPrcCom  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_PRECO"})
Local nPQtdCom  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_QUANT"})
Local nPQtdCom2 := aScan(aHeader,{|x| AllTrim(x[2])=="C7_QTSEGUM"})
Local nPValor   := aScan(aHeader,{|x| AllTrim(x[2])=="C7_TOTAL"})
Local nPGrade   := aScan(aHeader,{|x| AllTrim(x[2])=="C7_GRADE"})
Local nPValDesc := aScan(aHeader,{|x| AllTrim(x[2])=="C7_VLDESC"})
Local nPQtdSol  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_QTDSOL"})
Local nPDtEntr  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_DATPRF"})
Local nPBasIpi  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_BASEIPI"})
Local nPBasIcm  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_BASEICM"})
Local nPValIpi  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_VALIPI"})
Local nPValIcm  := aScan(aHeader,{|x| AllTrim(x[2])=="C7_VALICM"})
Local nCntFor   := 0
Local nLinha    := 0
Local nColuna   := 0
Local nAcols    := 0
Local nX        := 0
Local nY        := 0
Local dDataEnt  := Nil
Local lTestaDel := If(Len(aColsOrig[1])==Len(aHeader),.F.,.T.)

If MaGrade() .And. Type("oGrade")=="O" .And. Len(oGrade:aColsGrade)>0

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Varre o acols original para atualizar a variavel aCols                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols := {}

	For nCntFor:=1 To nMaxFor
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o Controle de Itens do Pedido de Venda e da Grade              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cItem    := aColsOrig[nCntFor][nPitem]
		cItemGrd := "000"
		cProdRef := aColsOrig[nCntFor][nPProduto]
		If ( !Empty(cProdRef) )
			oGrade:nPosLinO := nCntFor
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se foi digitado uma referencia                                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Len(oGrade:aHeadGrade)>0 .And. oGrade:aHeadGrade[nCntFor][1] == "R" ) .And. aColsOrig[nCntFor, nPGrade]  == "S"
				For nLinha := 1 To Len(oGrade:aColsGrade[nCntFor])
					For nColuna := 2 To Len(oGrade:aHeadGrade[nCntFor])
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³Verifica se ha valor a ser gravado                                      ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						If ( oGrade:aColsFieldByName("C7_QUANT",nCntFor,nLinha,nColuna) <> 0) .And. If(lTestaDel,!aColsOrig[nCntFor][Len(aHeader)+1],.T.)
							cItemGrd := Soma1(cItemGrd,Len(aColsOrig[nCntFor][nPItGrade]))
							cProdRef := aColsOrig[nCntFor][nPProduto]
							MatGrdPrRf(@cProdRef)
							cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna)
							aadd(aCols,aClone(aColsOrig[nCntFor]))
							nAcols := Len(aCols)
							aCols[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
							aCols[nAcols][nPItem    ]  := cItem
							n:=nAcols
							If ( nPItGrade <> 0 )
								// verifica se já existe item+itemgrd no acolsBkp
								nExistIt := Ascan(aColsBkp,{|x| Alltrim(x[nPItem]) == cItem .And. Alltrim(x[nPProduto]) ==Alltrim(cProdRef)})

								If nExistIt > 0
									cItemGrd:=aColsBkp[nExistIt,nPItGrade]
								Else
									cMax := Nil
									aEval(aColsBkp, {|z| If(z[nPItem] == cItem, cMax := If(cMax == Nil .Or. z[nPItGrade] > cMax , cMax := z[nPItGrade], Nil),Nil)})
									If cMax <> Nil
										cItemGrd:=Soma1(Alltrim(cMax),Len(aColsOrig[nCntFor][nPItGrade]))
									Endif
								Endif

								aCols[nAcols][nPItGrade ]  := cItemGrd
								If cItemGrd >"001"
									aCols[nAcols][Len(aHeader)]:=0
								Endif
							EndIf
							If ( nPQtdCom <> 0 )
								aCols[nAcols][nPQtdCom  ]  := oGrade:aColsFieldByName("C7_QUANT",nCntFor,nLinha,nColuna)
							EndIf
							If ( nPQtdSol <> 0 )
								a120quant(aCols[nAcols][nPQtdCom],2)
							EndIf
							If ( nPQtdCom2 <> 0 )
								aCols[nAcols][nPQtdCom2 ]  := oGrade:aColsFieldByName("C7_QTSEGUM",nCntFor,nLinha,nColuna)
							EndIf
							If ( nPDtEntr <> 0 )
								dDataEnt:= oGrade:aColsFieldByName("C7_DATPRF",nCntFor,nLinha,nColuna)
								aCols[nAcols][nPDtEntr ]  := If (!empty(dDataEnt),dDataEnt,dDataBase)
							EndIf
							If ( nPValor <> 0 )
								aCols[nAcols][nPValor   ]  := a410Arred(oGrade:aColsFieldByName("C7_QUANT",nCntFor,nLinha,nColuna)*aColsOrig[nCntFor][nPPrcCom],"C7_TOTAL")
							EndIf
							If  ( nPGrade <>  0 )
								aCols[nAcols][nPGrade   ]  := "S"
							Endif
							If  ( nPValDesc !=  0 )
								aCols[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdCom])*aCols[nAcols][nPQtdCom	]
							EndIf
							If !INCLUI
								If aCols[nAcols][Len(aHeader)]==0
									aCols[nAcols][Len(aHeader)]:=oGrade:GetRecNo(nCntFor,nLinha,nColuna)
								Endif
							Endif

						Else
							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Verifica se o item ja foi gravado para deleta-lo                        ³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
							If ( !Empty(oGrade:aColsFieldByName("C7_ITEM",nCntFor,nLinha,nColuna)) )
								cProdRef := aColsOrig[nCntFor][nPProduto]
								cItemGrd := Soma1(cItemGrd,Len(aColsOrig[nCntFor][nPItGrade]))
								MatGrdPrRf(@cProdRef)
								cProdRef := oGrade:GetNameProd(cProdRef,nLinha,nColuna)
								aadd(aCols,aClone(aColsOrig[nCntFor]))
								nAcols := Len(aCols)
								n:=nAcols
								aCols[nAcols][nPProduto ]  := PadR(cProdRef,Len(SB1->B1_COD))
								aCols[nAcols][nPItem    ]  := cItem
								If ( nPItGrade <> 0 )
									aCols[nAcols][nPItGrade ]  := cItemGrd
									If cItemGrd >"001"
										aCols[nAcols][Len(aHeader)]:=0
									Endif
								EndIf

								If ( nPQtdCom <> 0 )
									aCols[nAcols][nPQtdCom  ]  := oGrade:aColsFieldByName("C7_QUANT",nCntFor,nLinha,nColuna)
								EndIf
								If ( nPDtEntr <> 0 )
									dDataEnt:= oGrade:aColsFieldByName("C7_DATPRF",nCntFor,nLinha,nColuna)
									aCols[nAcols][nPDtEntr ]  := If (!empty(ddataent),ddataent,ddatabase)
								EndIf
								If ( nPValor <> 0 )
									aCols[nAcols][nPValor   ]  := a410Arred(oGrade:aColsFieldByName("C7_QUANT",nCntFor,nLinha,nColuna)*aColsOrig[nCntFor][nPPrcCom],"C7_TOTAL")
								EndIf

								If ( nPQtdSol <> 0 )
									a120quant(aCols[nAcols][nPQtdCom],2)
								EndIf

								If  ( nPGrade <>  0 )
									aCols[nAcols][nPGrade   ]  := "S"
								EndIf

								If  ( nPValDesc !=  0 )
									aCols[nAcols][nPValDesc]	:= (aColsOrig[nCntFor][nPValDesc]/aColsOrig[nCntFor][nPQtdCom])*aCols[nAcols][nPQtdCom]
								EndIf
								If !INCLUI
									If aCols[nAcols][Len(aHeader)]==0
										aCols[nAcols][Len(aHeader)]:=oGrade:GetRecNo(nCntFor,nLinha,nColuna)
									Endif
								Endif
								aCols[nAcols][Len(aHeader)+1] := .T.

							EndIf
						EndIf
					Next nColuna
				Next nLinha
			Else

				Aadd(aCols,aClone(aColsOrig[nCntFor]))
				nAcols := Len(aCols)
				aCols[nAcols][nPItem    ]  := cItem

			EndIf

		EndIf

	Next nCntFor
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ordena o aCols                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCols := aSort(aCols,,,{|x,y| x[nPItem]+x[nPItGrade] < y[nPItem]+y[nPItGrade] })

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicia a Carga do item nas funcoes MATXFIS  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisEnd()
	MaFisIni(cA120Forn,cA120Loj,"F","N",Nil,aRefImpSC7,,.T.,,,,,,,)

	For nX := 1 to Len(aCols)

		nScan := aScan(aColsOrig,{|x| AllTrim(x[nPItem]) == AllTrim(aCols[nX][nPItem]) })

		MaFisIniLoad(nX,,.T.)

		For nY	:= 1 To Len(aHeader)
			cValid	 := AllTrim(UPPER(aHeader[nY][6]))
			cRefCols := MaFisGetRf(cValid)[1]
			If !Empty(cRefCols) .AND. MaFisFound("IT",nX)

				If aCols[nX][nPGrade] == "S"
					MaFisLoad(cRefCols,aCols[nX][nY],nX)
				Else
					MaFisLoad(cRefCols,aColsOrig[nScan][nY],nX)
				EndIf

			EndIf
		Next nY

		If aCols[nX][nPGrade] == "S"

			MaFisRecal("IT_",nX)
			For nY	:= 1 To Len(aHeader)
				cValid	 := AllTrim(UPPER(aHeader[nY][6]))
				cRefCols := MaFisGetRf(cValid)[1]
				If !Empty(cRefCols) .AND. MaFisFound("IT",nX)
					aCols[nX][nY]:= MaFisRet(nX,cRefCols)
				EndIf
			Next nY

		EndIf

		MaFisEndLoad(nX,1)

	Next nX

	MaFisAlt("NF_FRETE",avalores[FRETE])
	MaFisAlt("NF_DESPESA",avalores[VALDESP])
	MaFisAlt("NF_SEGURO",avalores[SEGURO])
	MaFisAlt("NF_DESCONTO",avalores[VALDESC])
//	MaFisAlt("NF_VALEMB",avalores[PACKING])

	oGrade:aColsGrade  := {}
	oGrade:aHeadGrade  := {}
	aColsOrig          := {}

EndIf

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A123GValid ³Autor  ³Patricia D. Aguiar    ³Data  ³14/12/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao dos elementos da grade de Produtos (Grid)        ³±±
±±³          ³ Realiza a validacao da Quatidade e Data de Entrega)        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: .T. Edicao Valida / .F. Edicao nao valida           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123GValid()

Local nx         := 0
Local nPosLocal  := aScan(ograde:aHeadAux,{|x| AllTrim(x[2])=="C7_LOCAL"})
Local nPNumSC    := aScan(ograde:aHeadAux,{|x| AllTrim(x[2])=="C7_NUMSC"})
Local cNumSC     := ograde:aColsAux[ograde:nPosLinO,nPNumSC]
Local nPIteSC    := aScan(ograde:aHeadAux,{|x| AllTrim(x[2])=="C7_ITEMSC"})
Local cItemSC    := ograde:aColsAux[ograde:nPosLinO,nPIteSC]
Local nPosData   := aScan(ograde:acposCtrlGrd,{|x| AllTrim(x[1])=="C7_DATPRF"})
Local nPosQuant	 := aScan(ograde:acposCtrlGrd,{|x| AllTrim(x[1])=="C7_QUANT"})
Local nPosQTD2	 := aScan(ograde:acposCtrlGrd,{|x| AllTrim(x[1])=="C7_QTSEGUM"})
Local nLinha     := oGrade:oGetdados:oBrowse:Nat
Local nColuna    := oGrade:oGetdados:oBrowse:nColpos
Local nQtd       := oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposQuant]
Local nQtd2      := oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposQTD2]
Local cProduto   := PAD(ograde:GetNameProd(ograde:cprodref,ograde:ogetdados:obrowse:nat,ograde:ogetdados:obrowse:ncolpos),Len(SC7->C7_PRODUTO))
Local lIncPrdCtr := SuperGetMv("MV_PRODCTR")
Local lRet       := .T.

lGatilha := If(ValType(lGatilha) == "L",lGatilha,.T.) // .T.=Permite preencher aCols /  .F.=Executando via VldHead()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o Fornecedor x Produto possui bloqueio da Qualidade.  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lRet := QieSitFornec(cA120Forn,cA120Loj,cProduto,.T.)

dbSelectArea("SB1")
dbSetOrder(1)
If MsSeek(xFilial("SB1")+cProduto)

	If (nTipoPed == 2 .And. SB1->B1_CONTRAT <> "S") .Or. (nTipoPed != 2 .And. SB1->B1_CONTRAT == "S" .And. !lIncPrdCtr)
		If SB1->B1_CONTRAT <> "A"
			If nTipoPed == 2
				Help(" ",1,"A125CONTR")
			Else
				Help(" ",1,"A120CONTR")
			EndIf
			lRet := .F.
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o Produto tem Fornecedor exclusivo(Canal).       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. SB1->B1_MONO=="S" .And. SB1->B1_PROC != cA120Forn
		Help(" ",1,"A12020")
		lRet := .F.
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o Registro esta Bloqueado.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet .And. !RegistroOk("SB1")
		lRet := .F.
	EndIf

Else
	Help("  ",1,"REGNOIS")
	lRet := .F.
Endif

If lRet .And. lGatilha

	If oGrade:cCpo=="C7_DATPRF"

		If nQtd==0
			lRet:=.F.
			Help(' ',1,'A120DATPRD')
		Endif

	Elseif oGrade:cCpo $"C7_QUANT/C7_QTSEGUM"

		lRet:= A123ChkAlmox(cProduto,ograde:aColsAux[oGrade:nPosLinO,nPosLocal])

		If lRet
			If !Empty(cNumSc)
				dbSelectarea("SC1")
				dbSetorder(2)
				If MsSeek(Xfilial("SC1")+cProduto+cNumSc+cItemSc)
					If &(ReadVar()) > (SC1->C1_QUANT - SC1->C1_QUJE)
						Help(' ',1,"A120NOSLD")
						lRet:=.F.
					Endif
				Else
					Help(' ',1,"A120NOSOL")
					lRet:=.F.
				Endif

			Endif
		Endif
		// atualiza data de entrega
		If lRet
			If &(ReadVar())==0
				oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposData]:=ctod(" ")
			Else
				If Empty(oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposData])
					oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposData]:=ddatabase
				Endif
			Endif
		Endif
		If oGrade:cCpo $"C7_QTSEGUM"

			oGrade:aColsGrade[oGrade:nPosLinO][nlinha][ncoluna][nposQuant]:= ConvUM(cProduto,0,&(ReadVar()),1)

		Endif

	Endif
Endif

Return lRet

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123FldOk ³ Autor ³Nereu / Alexandre Lemes³ Data ³03/03/2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Em itens de grade de produto as Bases e Valores dos Impostos³±±
±±³          ³nao devem ser editados pois antes da gravacao do PC a Funcao³±±
±±³          ³Ma123GraGr() ao compatibilizar o aCols para gravacao realiza³±±
±±³          ³o recalculo de todos impostos proporcionalmente aos itens do³±±
±±³          ³Grid conforme as quantidades informadas no mesmo.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A123FldOk()

Local cItRefImp := ""
Local cFieldSC7 := ReadVar()
Local cFieldEdit:= SubStr(cFieldSC7,4,Len(cFieldSC7))
Local nPosField := aScan(aHeader,{|x| AllTrim(x[2])== cFieldEdit})
Local nPosGrade := aScan(aHeader,{|x| AllTrim(x[2])== "C7_GRADE"})
Local lEdita    := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se o campo editado pela MsGetdados for um dos campos abaixo a funcao nao  ³
//³permitira a edicao do mesmo, porem como o X3_VALID dos campos referente a ³
//³Impostos e executada antes da funcao A123FldOk e realiza a alimentacao da ³
//³MatxFis pela MaFisRef e necessario recarregar para a MatxFis o valor      ³
//³anterior do campo antes da edicao do mesmo contida na variavel cCampo.    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cFieldEdit $ "C7_BASEICM/C7_BASEIPI/C7_BASEIR/C7_BASESOL/C7_VALICM/C7_VALIPI/C7_VALIR/C7_VALSOL/C7_ICMSRET/C7_ICMCOMP" ;
	.And. aCols[N][nPosGrade] == "S"

	cItRefImp :=MaFisGetRF(AllTrim(Upper(aHeader[nPosField][6])))[1]
	MaFisRef(cItRefImp,"MT120",cCampo)

	Help(" ",1,"A123FISCAL")
	lEdita := .F.
EndIf

Return( lEdita )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123ImpC ³ Autor ³Nereu / Alexandre Lemes³ Data ³03.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica o Importador/Consignatario                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A123ImpC(@ExpC1,@ExpC2,@ExpA1,ExpL1,ExpL2)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 = Tipo 1-Importador / 2-Consignatario                ³±±
±±³          ³ ExpC1 = Codigo do Importador/Consignatario                 ³±±
±±³          ³ ExpO1 = Objeto do Importador/Consignatario                 ³±±
±±³          ³ ExpC2 = Descricao do Importador/Consignatario              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123ImpC(nTipo,cImport,oDescImpo,cDescImpo,oDescCons,cDescCons)

Local aArea	:= GetArea()
Local lRet  := .T.

dbSelectArea("DB7")
dbSetOrder(1)
If !Empty(cImport)
	If MsSeek(xFilial("DB7")+cImport)

		If nTipo == 1
			If DB7->DB7_IMPCON == "1"
				If oDescImpo != Nil
					cDescImpo:= Left(DB7->DB7_NOME,40)
					oDescImpo:Refresh()
				EndIf
	        Else
	        	Help("", 1, "A123IMPORT") //"Código informado não é de importador"###"Informação"
	            lRet := .F.
			Endif
		Else
			If DB7->DB7_IMPCON == "2"
				If oDescCons != Nil
					cDescCons:= Left(DB7->DB7_NOME,40)
					oDescCons:Refresh()
				EndIf
	        Else
	        	Help("", 1, "A123CONSIG")//"Código informado não é de consignatário"###"Informação"
	            lRet := .F.
			Endif
		Endif
	Else
		Help("  ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If nTipo == 2
		If oDescCons != Nil
			cDescCons:= Space(40)
			oDescCons:Refresh()
		EndIf
	Endif
Endif

RestArea(aArea)

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Comp  ³ Autor ³Nereu / Alexandre Lemes³ Data ³04.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do codigo do comprador                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do comprador                                   ³±±
±±³          ³ExpO2: Objeto da descricao do comprador                      ³±±
±±³          ³ExpC2: Descricao do comprador                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Indica se o comprador eh valido                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar o comprador quanto a   ³±±
±±³          ³integridade referencial                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Comp(cCodCompr,oDescCompr,cDescCompr)

Local lRet  := .T.
Local aArea := GetArea()

If ( !Empty(cCodCompr) )
	dbSelectArea("SY1")
	dbSetOrder(1)
	If !(MsSeek(xFilial("SY1")+cCodCompr))
		Help(" ",1,"A110COMPR")
		lRet := .F.
	Else
		If oDescCompr!= Nil
			cDescCompr:= SY1->Y1_NOME
			oDescCompr:Refresh()
		Endif
	EndIf
EndIf

RestArea(aArea)

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ A123Exp  ³ Autor ³Nereu / Alexandre Lemes³ Data ³04.03.2008³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Critica o Exportador                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ A123ImpC(@ExpC1,@ExpC2,@ExpA1,ExpL1,ExpL2)                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Codigo do Exportador                               ³±±
±±³          ³ ExpC2 = Loja do Exportador                                 ³±±
±±³          ³ ExpO1 = Objeto do Exportador                               ³±±
±±³          ³ ExpC3 = Descricao do Exportador                            ³±±
±±³          ³ ExpL1 = Na COPIA, indica se deve consistir o bloqueio do   ³±±
±±³          ³ 		  fornecedor (somente ao passar pelo campo forn./loja)³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ COMPRAS INTERNACIONAL                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Exp(cExport,cA123LojEx,oDescExp,cDescExp,lCopia,ocA123LojEx)

Local lRet  := .F.

dbSelectArea("SA2")

If !Empty(cExport)
	If Empty(cA123LojEx)
		SA2->(dbSetOrder(1))
		SA2->(MsSeek(xFilial("SA2")+cExport))
		If SA2->(Found())
 			cA123LojEx  := SA2->A2_LOJA
			If oDescExp != Nil
				cDescExp := SA2->A2_NOME
				oDescExp:Refresh()
			Endif
			If ocA123LojEx != Nil
				ocA123LojEx:Refresh()
			Endif
            lRet := .T.
		Else
			Help("  ",1,"REGNOIS")
			lRet := .F.
		EndIf
	Else
		SA2->(dbSetOrder(1))
		SA2->(MsSeek(xFilial("SA2")+cExport+cA123LojEx))
		If SA2->(Found())
			cA123LojEx  := SA2->A2_LOJA
			If oDescExp != Nil
				cDescExp := SA2->A2_NOME
				oDescExp:Refresh()
			Endif
			If ocA123LojEx != Nil
				ocA123LojEx:Refresh()
			Endif
			lRet := .T.
		Else
			Help("  ",1,"REGNOIS")
			lRet := .F.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o Registro esta Bloqueado.³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If INCLUI .Or. lCopia
			If lRet .And. !RegistroOk("SA2")
				lRet := .F.
			EndIf
		EndIf
	EndIf
Else
	cA123LojEx := Space(Len(SA2->A2_LOJA))
	If oDescExp != Nil
		cDescExp := Space(Len(SA2->A2_NOME))
		oDescExp:Refresh()
	Endif
	If ocA123LojEx != Nil
		ocA123LojEx:Refresh()
	Endif
EndIf

If lRet .And.  IIF(cPaisLoc =="PAR",!( SA2->A2_FABRICA $ "3|4" ) ,( SA2->A2_FABRICA <> "4" ))
	Aviso("A123NOEXP",STR0114,{"Ok"},1) // "O Exportador Informado não consta como exportador no cadastro de fornecedores, consulte o conteudo do campo fabricante no cadastro de fornecedores"
	lRet := .F.
EndIf

If Empty(cExport) .And. Empty(cA123LojEx)
	lRet := .T.
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Desp  ³ Autor ³Nereu / Alexandre Lemes³ Data ³04.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do codigo do despachante                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do despachante                                 ³±±
±±³          ³ExpO2: Objeto da descricao do despachante                    ³±±
±±³          ³ExpC2: Descricao do despachante                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Indica se o despachante e valido                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar o despachante quanto a ³±±
±±³          ³integridade referencial                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Desp(cCodDesp,oDescDesp,cDescDesp)

Local aArea := GetArea()
Local lRet  := .T.

If !Empty(cCodDesp)
	dbSelectArea("DB9")
	dbSetOrder(1)
	If MsSeek(xFilial("DB9")+cCodDesp)
		If oDescDesp!= Nil
			cDescDesp := DB9->DB9_NOME
			oDescDesp:Refresh()
		Endif
	Else
		Help("  ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If oDescDesp!= Nil
		cDescDesp := Space(Len(DB9->DB9_NOME))
		oDescDesp:Refresh()
	Endif
Endif

RestArea(aArea)

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Emb   ³ Autor ³Nereu / Alexandre Lemes³ Data ³05.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do codigo do embarcador                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do embarcador                                  ³±±
±±³          ³ExpO2: Objeto da descricao do embarcador                     ³±±
±±³          ³ExpC2: Descricao do embarcador                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Indica se o embarcador e valido                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar o embarcador quanto a  ³±±
±±³          ³integridade referencial                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Emb(cCodEmb,oDescEmb,cDescEmb,oDescForw,cDescForw,nTpAg)

Local aArea := GetArea()
Local lRet  := .T.

If !Empty(cCodEmb)
	dbSelectArea("DB8")
	dbSetOrder(1)
	If MsSeek(xFilial("DB8")+cCodEmb)
		If nTpAg == 1
			If oDescEmb!= Nil
				cDescEmb := DB8->DB8_NOME
				oDescEmb:Refresh()
			Endif
		Else
			If oDescForw!= Nil
				cDescForw := DB8->DB8_NOME
				oDescForw:Refresh()
			Endif
		Endif
	Else
		Help("  ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If nTpAg == 1
		If oDescEmb!= Nil
			cDescEmb := Space(Len(DB8->DB8_NOME))
			oDescEmb:Refresh()
		Endif
	Else
		If oDescForw!= Nil
			cDescForw := Space(Len(DB8->DB8_NOME))
			oDescForw:Refresh()
		Endif
	Endif
Endif

RestArea(aArea)

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Via   ³ Autor ³Nereu / Alexandre Lemes³ Data ³05.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do codigo da via de transporte           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo da via de transporte                           ³±±
±±³          ³ExpO2: Objeto da descricao da via de transporte              ³±±
±±³          ³ExpC2: Descricao da via de transporte                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Indica se a via de transporte e valida               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar a via de transporte    ³±±
±±³          ³quanto a integridade referencial                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Via(cCodVia,oDescVia,cDescVia,cA123Ori,cA123Dest)

Local aArea := GetArea()
Local lRet  := .T.

If !Empty(cCodVia)
	dbSelectArea("DBF")
	dbSetOrder(1)
	If MsSeek(xFilial("DBF")+cCodVia)
		If oDescVia!= Nil
			cDescVia  := DBF->DBF_DESCR
			cA123Ori  := DBF->DBF_ORIGEM
			cA123Dest := DBF->DBF_DESTIN
			oDescVia:Refresh()
		Endif
	Else
		Help("  ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If oDescVia!= Nil
		cDescVia  := Space(Len(DBF->DBF_DESCR))
		cA123Ori  := Space(Len(DBF->DBF_ORIGEM))
		cA123Dest := Space(Len(DBF->DBF_DESTIN))
		oDescVia:Refresh()
	Endif
Endif

RestArea(aArea)

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Dpt   ³ Autor ³Nereu / Alexandre Lemes³ Data ³05.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do codigo do deposito                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do deposito                                    ³±±
±±³          ³ExpO2: Objeto da descricao do deposito                       ³±±
±±³          ³ExpC2: Descricao do deposito                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Indica se o deposito e valido                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar o deposito             ³±±
±±³          ³quanto a integridade referencial                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Dpt(cCodDep,oDescDpt,cDescDpt)

Local aArea := GetArea()
Local lRet  := .T.

If !Empty(cCodDep)
	dbSelectArea("DBE")
	dbSetOrder(1)
	If MsSeek(xFilial("DBE")+cCodDep)
		If oDescDpt!= Nil
			cDescDpt := DBE->DBE_DESCR
			oDescDpt:Refresh()
		Endif
	Else
		Help("  ",1,"REGNOIS")
		lRet := .F.
	Endif
Else
	If oDescDpt!= Nil
		cDescDpt := Space(Len(DBE->DBE_DESCR))
		oDescDpt:Refresh()
	Endif
Endif

RestArea(aArea)

Return(lRet)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123Peso  ³ Autor ³Nereu / Alexandre Lemes³ Data ³10.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao do peso bruto e cubado.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Peso informado                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar o peso e o volume.     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123Peso(nPeso)

Local nPesoCub := 0

If nPeso > 0 .And. cA123Pes == 0
	nPesoCub := ( nPeso / 0.006 )
	cA123Pes := IF( cA123Pes > nPesoCub, cA123Pes, nPesoCub )
Endif

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123DtEmb ³ Autor ³Nereu / Alexandre Lemes³ Data ³10.03.2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao da data de embarque.                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpD1: Data de Embarque                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar a data de embarque.    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123DtEmb(dDataEmb)

Local aArea     := GetArea()
Local dtEntCalc := dA120Emis
Local nPosPrf   := aScan(aHeader,{|x| AllTrim(x[2]) == "C7_DATPRF"})
Local lRet      := .T.

If dDataEmb < dA120Emis
	Help("", 1, "A123DTEMB") //'Data de embarque deve ser maior que a data do P.O.'###"Atenção"
	lRet := .F.
EndIf

If !Empty(cA123Via) .And. lRet

	DBF->(dbSetOrder(1))
	DBF->(dbSeek(xFilial("DBF")+cA123Via))
	//Sugere data de entrega conforme lead times de desembaraco + transporte
	DBD->(dbSetOrder(2))
	dtEntCalc := dDataEmb + DBF->DBF_TIME + IIf(DBD->(dbSeek(xFilial("DBD")+cA123Dest)),(DBD->DBD_LT_DES + DBD->DBD_LT_TRA),SuperGetMv("MV_LT_DESE"))
	DBD->(dbSetOrder(1))

	If dtEntCalc <> aCols[n][nPosPrf]
		aCols[n][nPosPrf]:= dtEntCalc
 	EndIf

EndIf

RestArea(aArea)

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A123VldDta³ Autor ³Alexandre Inacio Lemes ³ Data ³27/06/2008 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de validacao das datas de emissao e confirmacao do PO ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpD1: Data de Emissao                                       ³±±
±±³          ³ExpD2: Data de Confirmacao do PO                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo validar as datas do PO         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ COMPRAS INTERNACIONAL                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A123VldDta(dA120Emis,dA123Cpe)

Local lRet := .T.

If !Empty(dA123Cpe) .And. dA123Cpe < dA120Emis
	Aviso("A123CONF",STR0115,{"Ok"},1) // "A data de Confirmação do Purchase Order não pode ser inferior a data de emissao."
	lRet := .F.
EndIf

Return lRet
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Aline Damasceno       ³ Data ³30/09/2013³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()
PRIVATE aRotina:= {}


aAdd(aRotina,{STR0006,"PesqBrw"   , 0, 1  }) //"Pesquisar"
aAdd(aRotina,{STR0007,"A123Pedido", 0, 2  }) //"Visualizar"
aAdd(aRotina,{STR0008,"A123Pedido", 0, 3  }) //"Incluir"
aAdd(aRotina,{STR0009,"A123Pedido", 0, 4,6}) //"Alterar"
aAdd(aRotina,{STR0010,"A123Pedido", 0, 5,7}) //"Excluir"
aAdd(aRotina,{STR0077,"A123Impri" , 0, 2  }) //"Imprimir"
aAdd(aRotina,{STR0078,"A123Legend", 0, 2  }) //"Legenda"
aAdd(aRotina,{STR0087,"MsDocument", 0, 4  }) //"Conhecimento"
If ExistBlock("MT123BRW")
	ExecBlock("MT123BRW",.F.,.F.)
EndIf

Return(aRotina)
