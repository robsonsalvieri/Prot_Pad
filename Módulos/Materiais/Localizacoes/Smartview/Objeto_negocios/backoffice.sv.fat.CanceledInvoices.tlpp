#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fat.CanceledInvoices.ch'

namespace custom.faturamento.CanceledInvoices.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fat.CanceledInvoices
@description Classe para creación del Objeto de Negocio TReports
@author Marcelo Hruschka
@since 24/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFAT', tables='SF3,SA1', name='Documentos de salida cancelados', country='ALL', initialRelease='12.1.2410', customTables='SF3,SA1' )
class CanceledInvoicesTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecnoSF3 as numeric
	protected data nRecnoSE4 as numeric
	protected data nRecnoORI as numeric
	protected data cTabORI as character
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class CanceledInvoicesTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Documentos de salida cancelados'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Documentos de salida cancelados'

	// Define a Área
	self:appendArea( STR0002 ) // 'Facturación'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FATSV101' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'M0_CGC'	    , STR0007 , 'string'	, STR0007 } ) // 'RFC Emisor'


	// demais campos
	self:aFields := { 'M0_CGC', 'F3_FILIAL', 'F3_ESPECIE', 'F3_NFISCAL', 'F3_SERIE', 'F3_EMISSAO', 'A1_CGC' , 'F3_VALCONT' , 'F3_DTCANC','F3_TIPO',  'A1_COD', 'A1_LOJA', 'A1_NOME', 'A1_EMAIL', 'A1_TEL', 'F3_NFISCAN', 'F3_BASIMP1', 'F3_ALQIMP1', 'F3_VALIMP1', 'F3_BASIMP2', 'F3_ALQIMP2', 'F3_VALIMP2', 'F3_BASIMP3', 'F3_ALQIMP3', 'F3_VALIMP3', 'F3_BASIMP4', 'F3_ALQIMP4', 'F3_VALIMP4', 'F3_BASIMP5', 'F3_ALQIMP5', 'F3_VALIMP5' }

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class CanceledInvoicesTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character
	Local cExp 			as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local nSeq          as numeric

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			    := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSF3 := getCpoUser( aCustomFields, 'SF3', 'F3', ',' )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SM0.M0_CGC, SF3.F3_FILIAL, SF3.F3_ESPECIE, SF3.F3_NFISCAL, SF3.F3_SERIE, SF3.F3_EMISSAO, SF3.F3_TIPO, SF3.F3_VALCONT, SF3.F3_DTCANC,"
	cQuery += " SA1.A1_COD, SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_CGC, SA1.A1_EMAIL, SA1.A1_DDD||SA1.A1_TEL AS A1_TEL, SF3.F3_NFISCAN, "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " SF3.F3_BASIMP1, SF3.F3_ALQIMP1, SF3.F3_VALIMP1,"
	cQuery += " SF3.F3_BASIMP2, SF3.F3_ALQIMP2, SF3.F3_VALIMP2,"
	cQuery += " SF3.F3_BASIMP3, SF3.F3_ALQIMP3, SF3.F3_VALIMP3,"
	cQuery += " SF3.F3_BASIMP4, SF3.F3_ALQIMP4, SF3.F3_VALIMP4,"
	cQuery += " SF3.F3_BASIMP5, SF3.F3_ALQIMP5, SF3.F3_VALIMP5,"
	cQuery += " SF3.R_E_C_N_O_ AS REGSF3"
	cQuery += " FROM " + RetSqlName("SF3") + " SF3"
	cQuery += " LEFT JOIN SYS_COMPANY SM0 ON SM0.M0_CODIGO = ? AND SM0.M0_CODFIL = ? AND SM0.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = SF3.F3_CLIEFOR AND SA1.A1_LOJA = SF3.F3_LOJA AND SA1.D_E_L_E_T_ = ?"
	cQuery += " WHERE SF3.F3_ENTRADA BETWEEN ? AND ?" 
	cQuery += " AND SF3.F3_DTCANC BETWEEN ? AND ?"	
	cQuery += " AND SF3.F3_CLIEFOR BETWEEN ? AND ?"	
	cQuery += " AND SF3.F3_LOJA BETWEEN ? AND ?"	
	cQuery += " AND SF3.F3_TIPOMOV= ?"
	cQuery += " AND SF3.D_E_L_E_T_ = ?"
	
	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	
	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSF3 ) 
	oExecA:SetUnSafe( nSeq += 1, cCpoSA1 )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SA1") )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR08'][1] )
	oExecA:SetString( nSeq += 1, 'V' )
	oExecA:SetString( nSeq += 1, ' ' )
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	ConOut(oExecA:GetFixQuery())
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecnoSF3 := ( cAliasA )->REGSF3
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class CanceledInvoicesTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
