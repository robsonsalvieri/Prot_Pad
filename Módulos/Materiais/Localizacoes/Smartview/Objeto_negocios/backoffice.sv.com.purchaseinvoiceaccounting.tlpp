#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.com.purchaseinvoiceaccounting.ch'

namespace custom.compras.purchaseinvoiceaccounting.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.purchaseinvoiceaccounting.accounting
@description Classe para creación del Objeto de Negocio de Facturas de compras conciliadas para SMARTVIEW
@author Leonardo Pereira
@since 31/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACOM', tables='SF1,CT2,CV3,SE4,SA2', name='Facturas de Compras Conciliada', country='ALL', initialRelease='12.1.2410', customTables='SF1,CT2' )
class purchaseinvoiceaccountingTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecnoSF1 as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class purchaseinvoiceaccountingTReportsBusinessObject

	Local aCpos := { } as array

	_Super:new( )

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Facturas de Compras Conciliada'

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Facturas de Compras Conciliada'

	// Define a Área
	self:appendArea( STR0002 ) // 'Compras'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'MATSV100' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'M0_CGC'	   , STR0007 , 'string', STR0007 } ) // 'RFC Receptor'
	aAdd( aCpos, { 'M0_NOMECOM', STR0008 , 'string', STR0008 } ) // 'Nombre Receptor'
	aAdd( aCpos, { 'F1_VALBRUP', STR0009 , 'number', STR0009 } ) // 'Val Fac Peso'
	aAdd( aCpos, { 'F1_DESCONP', STR0010 , 'number', STR0010 } ) // 'Desc Pesos'
	aAdd( aCpos, { 'F1_VALMERP', STR0011 , 'number', STR0011 } ) // 'Val Merc Peso'
	aAdd( aCpos, { 'F1_BASIM1P', STR0012 , 'number', STR0012 } ) // 'Base Imp Peso'
	aAdd( aCpos, { 'F1_VALIM1P', STR0013 , 'number', STR0013 } ) // 'Val Imp Peso'
	aAdd( aCpos, { 'CT2_DOCLSL', STR0014 , 'string', STR0014 } ) // 'Doc/Lote/Sublote'
	aAdd( aCpos, { 'CT2_DTACTB', STR0015 , 'date'  , STR0015 } ) // 'Fecha de Asiento'
	aAdd( aCpos, { 'CT2_TPSLD' , STR0016 , 'string', STR0016 } ) // 'Tipo Saldo'
	aAdd( aCpos, { 'CT2_HISTO' , STR0017 , 'string', STR0017 } ) // 'Historial'

	// demais campos
	self:aFields := { 'M0_CGC','M0_NOMECOM','A2_CGC','A2_NOME','F1_EMISSAO','F1_VALBRUP','F1_DESCONP','F1_VALMERP','F1_BASIM1P','F1_VALIM1P','F1_MOEDA','F1_TXMOEDA','F1_DOC','F1_SERIE','A2_COD','A2_LOJA','F1_TIPO','F1_TIPODOC','F1_FILIAL','F1_ESPECIE','CT2_DOCLSL','CT2_DTACTB','CT2_TPSLD','CT2_HISTO' }

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class purchaseinvoiceaccountingTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		  as character
	Local cFiltro 		  as character
	Local cExp 			  as character

	Local nX			     as numeric
	Local n1			     as numeric
	Local nPosMon		  as numeric
	Local nPosExp		  as numeric
	Local nDecs 		  as numeric

	Local jParams 		  as json

	Local aPDFields	  as array
	Local aFiltro		  as array
	Local aCustomFields as array

	Local lObfuscated	  as logical

	Local oExecA		  as object
	Local oExecB        as object
	Local cAliasA       as character
	Local cAliasB       as character
	Local cChaveCTL     as character

	Local cFilialCTL    as character
	Local cFilialCV3    as character
	

	cFilialCTL := xFilial( "CTL" )
	cFilialCV3 := xFilial( "CV3" )

	cChaveCTL := AllTrim( Posicione( "CTL", 1, FWxFilial( "CTL" ) + "660", "CTL_KEY" ) )
	cChaveCTL := IIF("SQL" $ TcGetDB(),cChaveCTL,StrTran(cChaveCTL,"+","||"))
	   
	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			:= ''
	cAliasB			:= ''

	nPosMon   		:= 0
	nPosExp 		   := 0
	nDecs 			:= 0
	nX 				:= 0
	n1				   := 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSF1 := getCpoUser( aCustomFields, 'SF1', 'F1', ',' )
	cCpoCT2 := getCpoUser( aCustomFields, 'CT2', 'CT2', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "
	cQuery += " F1_UUID,M0_CGC,M0_NOMECOM,A2_CGC,A2_NOME,F1_EMISSAO, ? "
	cQuery += " F1_VALBRUT, F1_DESCONT, F1_VALMERC, F1_BASIMP1, F1_VALIMP1, "
	cQuery += " F1_MOEDA,F1_TXMOEDA,F1_TIPODOC,F1_DOC,F1_SERIE, "
	cQuery += " A2_COD,A2_LOJA,F1_TIPO,F1_FILIAL,F1_ESPECIE, SF1.R_E_C_N_O_ AS REGSF1, ? AS CHVS "
	cQuery += "FROM " + RetSqlName( "SF1" ) + " SF1 "
	cQuery += "INNER JOIN " + RetSqlName( "SA2" ) + " SA2 "
	cQuery += "   ON A2_FILIAL = ? "
	cQuery += "      AND SA2.A2_COD = SF1.F1_FORNECE "
	cQuery += "      AND SA2.A2_LOJA = SF1.F1_LOJA "
	cQuery += "      AND SA2.D_E_L_E_T_ = ? "
	cQuery += "LEFT JOIN  " + RetSqlName( "SE4" ) + " SE4 "
	cQuery += "   ON E4_FILIAL = ? "
	cQuery += "      AND SE4.E4_CODIGO = SF1.F1_COND "
	cQuery += "      AND SE4.D_E_L_E_T_ = ? "
	cQuery += "INNER JOIN SYS_COMPANY SM0 "
	cQuery += "   ON M0_CODIGO = ? "
	cQuery += "      AND M0_CODFIL = ? "
	cQuery += "      AND SM0.D_E_L_E_T_ = ? "
	cQuery += "WHERE SF1.F1_FILIAL= ? "
	cQuery += "   AND SF1.F1_EMISSAO BETWEEN ? AND ? "
	cQuery += "   AND SF1.F1_FORNECE BETWEEN ? AND ? "
	cQuery += "   AND SF1.F1_TIPO = ? "
	cQuery += "   AND SF1.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery( cQuery )

	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSF1 )
	oExecA:SetUnSafe( nSeq += 1, cChaveCTL )
	oExecA:SetString( nSeq += 1, FWxFilial( "SA2" ) )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, FWxFilial( "SE4" ) )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, FWxFilial( "SF1" ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )
	oExecA:SetString( nSeq += 1, IIF(jParams['MV_PAR05'][1]==1,"N",IIF(jParams['MV_PAR05'][1]==2,"B",IIF(jParams['MV_PAR05'][1]==3,"D","C"))))
	oExecA:SetString( nSeq += 1, ' ' )
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next

	cQuery := oExecA:getFixQuery( )

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		self:jItems := JsonObject():new( )

		self:nRecnoSF1 := ( cAliasA )->REGSF1

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf self:aStruct[nX][5] == "F1_VALBRUP"
				self:jItems[self:aStruct[nX][1]] := Round(( cAliasA )->F1_VALBRUT * ( cAliasA )->F1_TXMOEDA,2)
			ElseIf self:aStruct[nX][5] == "F1_DESCONP" 
				self:jItems[self:aStruct[nX][1]] := Round(( cAliasA )->F1_DESCONT * ( cAliasA )->F1_TXMOEDA,2)
			ElseIf self:aStruct[nX][5] == "F1_VALMERP" 
				self:jItems[self:aStruct[nX][1]] := Round(( cAliasA )->F1_VALMERC * ( cAliasA )->F1_TXMOEDA,2)
			ElseIf self:aStruct[nX][5] == "F1_BASIM1P"
				self:jItems[self:aStruct[nX][1]] := Round(( cAliasA )->F1_BASIMP1 * ( cAliasA )->F1_TXMOEDA,2)
			ElseIf self:aStruct[nX][5] == "F1_VALIM1P"
				self:jItems[self:aStruct[nX][1]] := Round(( cAliasA )->F1_VALIMP1 * ( cAliasA )->F1_TXMOEDA,2)								
			ElseIf ( LEFT( self:aStruct[nX][5],4 ) == "CT2_" )
				cQuery := "SELECT CT2_LOTE,CT2_SBLOTE,CT2_DOC, CT2_DATA AS CT2_DTACTB,? CT2_TPSALD CT2_TPSLD, CT2_HIST CT2_HISTO "
				cQuery += " FROM " + RetSQLName( "CT2" ) + " CT2 "
				cQuery += " INNER JOIN " + RetSQLName( "CTL" ) + " CTL "
				cQuery += "   ON CTL_FILIAL = ? "
				cQuery += "      AND CTL_LP = ? "
				cQuery += "      AND CTL.D_E_L_E_T_ = ? "
				cQuery += " INNER JOIN " + RetSQLName( "CV3" ) + " CV3 "
				cQuery += "   ON CV3_FILIAL = ? "
				cQuery += "      AND CV3_KEY = ? "
				cQuery += "      AND CV3.D_E_L_E_T_ = ? "
				cQuery += "      AND CV3.CV3_RECDES <> ? "
				cQuery += " WHERE CT2.R_E_C_N_O_ = CAST(CV3.CV3_RECDES AS " + IIF("ORACLE" $ TcGetDB(),"INT","BIGINT") + ")"   
				cQuery += "   AND CT2.D_E_L_E_T_ = ? "

				cQuery := ChangeQuery( cQuery )

				oExecB := FwExecStatement():New( cQuery )

				oExecB:SetUnSafe( 1, cCpoCT2 )
				oExecB:SetString( 2, cFilialCTL )
				oExecB:SetString( 3, "660" )
				oExecB:SetString( 4, ' ' )
				oExecB:SetString( 5, cFilialCV3 )
				oExecB:SetString( 6, ( cAliasA )->CHVS )
				oExecB:SetString( 7, ' ' )
				oExecB:SetString( 8, ' ' )
				oExecB:SetString( 9, ' ' )

				cQuery := oExecB:getFixQuery( )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cAliasB := oExecB:OpenAlias( )

				If ( self:aStruct[nX][3] == 'date' )
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasB )->&( self:aStruct[nX][5] ) ) )
				ElseiF self:aStruct[nX][5] == "CT2_DOCLSL"
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->CT2_LOTE+"/"+( cAliasB )->CT2_SBLOTE+"/"+( cAliasB )->CT2_DOC
				Else
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
				EndIf

				// fecha tabela
				( cAliasB )->( DbCloseArea( ) )
				oExecB:Destroy( )
				oExecB := Nil
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:processData( )
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )
	End

	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class purchaseinvoiceaccountingTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
