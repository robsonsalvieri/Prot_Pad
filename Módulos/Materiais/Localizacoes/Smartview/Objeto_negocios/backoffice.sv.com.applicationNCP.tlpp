#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.com.applicationNCP.ch'

namespace custom.compras.applicationNCP.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.applicationNCP
@description Classe para creación del Objeto de Negocio de Solicitud de Notas de credito para SMARTVIEW
@author Leonardo Pereira
@since 03/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACOM', tables='SCU,SA2,SX5', name='Solicitud de Notas de credito', country='ALL', initialRelease='12.1.2410', customTables='SCU,SA2' )
class applicationNCPTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class applicationNCPTReportsBusinessObject

	Local aCpos := { } as array

	_Super:new( )

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Solicitud de Notas de credito'

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Solicitud de Notas de credito'

	// Define a Área
	self:appendArea( STR0002 ) // 'Compras'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'COMSV503' )
		IIf( !self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'' ) // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'CU_DESMOT' , STR0007, 'string', STR0007 } ) // 'Descripción del motivo'
	aAdd( aCpos, { 'M0_CGC'	   , STR0008, 'string', STR0008 } ) // 'Emisor'
	aAdd( aCpos, { 'M0_NOMECOM', STR0009, 'string', STR0009 } ) // 'Nombre Emisor'
	aAdd( aCpos, { 'M0_ENDCOB' , STR0010, 'string', STR0010 } ) // 'Localidad'
	aAdd( aCpos, { 'M0_BAIRCOB', STR0011, 'string', STR0011 } ) // 'Barrio'
	aAdd( aCpos, { 'M0_CIDCOB' , STR0012, 'string', STR0012 } ) // 'Municipio'
	aAdd( aCpos, { 'M0_TEL'	   , STR0013, 'string', STR0013 } ) // 'Telefono'

	// demais campos
	self:aFields := { 'CU_FILIAL', 'M0_CGC', 'M0_NOMECOM', 'M0_ENDCOB', 'M0_BAIRCOB', 'M0_CIDCOB', 'M0_TEL', 'CU_NUMERO','CU_FORNECE','CU_LOJA','A2_NOME','CU_EMISSAO','CU_VALMIN','CU_MOEDA','CU_NFISCAL','CU_SERNF','CU_MOTIVO','CU_DESMOT','CU_NCRED','CU_SERNCP' }

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class applicationNCPTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		  as character
	Local cFiltro 		  as character
	Local cExp 			  as character
	Local cCpoSCU		  as character
	Local cCpoSA2 		  as character

	Local nX			     as numeric
	Local n1			     as numeric

	Local jParams 		  as json

	Local aPDFields	  as array
	Local aFiltro		  as array
	Local aCustomFields as array

	Local lObfuscated	  as logical

	Local oExecA		  as object
	Local cAliasA       as character
	Local cMotivo       as character

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			:= ''
	cMotivo			:= ''

	nX 				:= 0
	n1				   := 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSCU := getCpoUser( aCustomFields, 'SCU', 'SCU', ',' )
	cCpoSA2 := getCpoUser( aCustomFields, 'SA2', 'SA2', ',' )

	ConOut('--> SCU: ' + cCpoSCU)
	ConOut('--> SA2: ' + cCpoSA2)

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { | x, y | x[1] + x[3] < y[1] + y[3] } )
	EndIf
	ConOut('--> cFiltro: '+cFiltro)

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "

	cQuery += " SCU.CU_FILIAL, SM0.M0_CGC, SM0.M0_NOMECOM, SCU.CU_NUMERO, SCU.CU_FORNECE, SCU.CU_LOJA, SA2.A2_NOME, SA2.A2_MUN, " + cCpoSA2 + " SCU.CU_EMISSAO, SCU.CU_VALMIN, SCU.CU_MOEDA, SCU.CU_NFISCAL, SCU.CU_SERNF, SCU.CU_MOTIVO, SCU.CU_NCRED, " + cCpoSCU + " SCU.CU_SERNCP, M0_ENDCOB, M0_BAIRCOB, M0_CIDCOB, M0_TEL "

	cQuery += "FROM " + RetSqlName( 'SCU' ) + " SCU "

	cQuery += "INNER JOIN " + RetSqlName( "SA2" ) + " SA2 "
	cQuery += "   ON A2_FILIAL = ? "
	cQuery += "      AND SA2.A2_COD = SCU.CU_FORNECE "
	cQuery += "      AND SA2.A2_LOJA = SCU.CU_LOJA "
	cQuery += "      AND SA2.D_E_L_E_T_ = ? "

	cQuery += "INNER JOIN SYS_COMPANY SM0 "
	cQuery += "   ON SM0.M0_CODIGO = ? "
	cQuery += "      AND SM0.M0_CODFIL = ? "
	cQuery += "      AND SM0.D_E_L_E_T_ = ? "

	cQuery += "WHERE SCU.CU_FILIAL = ? "
	cQuery += "   AND SCU.CU_NUMERO BETWEEN ? AND ? "
	cQuery += "   AND SCU.CU_FORNECE BETWEEN ? AND ? "
	cQuery += "   AND SCU.CU_EMISSAO BETWEEN ? AND ? "

	cQuery += "   AND SCU.CU_MOEDA = ? "
	cQuery += "   AND SCU.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += "   AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery += "ORDER BY ? "

	cQuery := ChangeQuery( cQuery )

	oExecA := FwExecStatement( ):New( cQuery )

	oExecA:SetString( 1, FWxFilial( "SA2" ) )
	oExecA:SetString( 2, ' ' )
	oExecA:SetString( 3, cEmpAnt )
	oExecA:SetString( 4, cFilAnt )
	oExecA:SetString( 5, ' ' )
	oExecA:SetString( 6, FWxFilial( "SCU" ) )
	oExecA:SetString( 7, jParams['MV_PAR01'][1] )
	oExecA:SetString( 8, jParams['MV_PAR02'][1] )
	oExecA:SetString( 9, jParams['MV_PAR03'][1] )
	oExecA:SetString( 10, jParams['MV_PAR04'][1] )
	oExecA:SetString( 11, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )
	oExecA:SetString( 12, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )
	oExecA:SetNumeric( 13, jParams['MV_PAR08'][1] )
	oExecA:SetString( 14, ' ' )
	oExecA:SetUnsafe( 15, 'SCU.CU_FILIAL, SCU.CU_NCRED, SCU.CU_NUMERO ' )

	cQuery := oExecA:getFixQuery( )
	ConOut(cQuery)

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		self:jItems := JsonObject():new( )

		cMotivo := Posicione( 'SX5', 1, FWxFilial( 'SX5' ) + 'O1' + ( cAliasa )->CU_MOTIVO, 'X5DESCRI()' )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][5] == 'CU_VALMIN' )
				self:jItems[self:aStruct[nX][1]] := xMoeda( ( cAliasA )->CU_VALMIN, ( cAliasA )->CU_MOEDA, jParams['MV_PAR08'][1], ( cAliasA )->CU_EMISSAO, MSDecimais( jParams['MV_PAR08'][1] ) )
			ElseIf ( self:aStruct[nx][5] == 'CU_DESMOT' )
				self:jItems[self:aStruct[nX][1]] := cMotivo
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			EndIf
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:processData( )
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )
	End

	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class applicationNCPTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
