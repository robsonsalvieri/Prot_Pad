#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fat.DeliveryNote.ch'

namespace custom.faturamento.DeliveryNote.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fat.DeliveryNote
@description Classe para creación del Objeto de Negocio TReports
@author Marcelo Hruschka
@since 24/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFAT', tables='SF2,SD2,SB1,SA1', name='Remisión de salida', country='ALL', initialRelease='12.1.2410', customTables='SF2,SD2,SB1,SA1' )
class DeliveryNoteTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class DeliveryNoteTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Remisión de salida'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Remisión de salida'

	// Define a Área
	self:appendArea( STR0002 ) // 'Facturación'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FATSV503' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'M0_CGC'     , 'M0_CGC'      , 'string'	, 'M0_CGC'      } ) 
    aAdd( aCpos, { 'M0_NOME'    , 'M0_NOME'     , 'string'	, 'M0_NOME'     } ) 
	aAdd( aCpos, { 'M0_NOMECOM' , 'M0_NOMECOM'  , 'string'	, 'M0_NOMECOM'  } ) 
    aAdd( aCpos, { 'M0_ENDCOB'  , 'M0_ENDCOB'   , 'string'	, 'M0_ENDCOB'   } ) 
    aAdd( aCpos, { 'M0_BAIRCOB' , 'M0_BAIRCOB'  , 'string'	, 'M0_BAIRCOB'  } ) 
    aAdd( aCpos, { 'M0_CIDCOB'  , 'M0_CIDCOB'   , 'string'	, 'M0_CIDCOB'   } ) 
    aAdd( aCpos, { 'M0_ESTCOB'  , 'M0_ESTCOB'   , 'string'	, 'M0_ESTCOB'   } ) 
    aAdd( aCpos, { 'M0_CEPCOB'  , 'M0_CEPCOB'   , 'string'	, 'M0_CEPCOB'   } ) 
	aAdd( aCpos, { 'M0_TEL'     , 'M0_TEL'      , 'string'	, 'M0_TEL'      } ) 

	// demais campos
	self:aFields := {'M0_CGC','M0_NOME','M0_ENDCOB','M0_BAIRCOB','M0_CIDCOB','M0_ESTCOB','M0_CEPCOB','M0_NOMECOM','M0_TEL','F2_SERIE','F2_DOC','F2_EMISSAO','F2_VALMERC','F2_FRETE','F2_DESPESA','F2_SEGURO','F2_DESCONT','F2_VALBRUT','A1_NOME','A1_END','A1_BAIRRO','A1_MUN','A1_EST','A1_CEP','A1_TEL','A1_CGC','D2_COD','D2_QUANT','D2_TOTAL','D2_UM','B1_DESC'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class DeliveryNoteTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character
	Local cExp 			as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local nSeq          as numeric

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			    := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSF2 := getCpoUser( aCustomFields, 'SF2', 'F2', ',' )
	cCpoSD2 := getCpoUser( aCustomFields, 'SD2', 'D2', ',' )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )
	cCpoSB1 := getCpoUser( aCustomFields, 'SB1', 'B1', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "
	cQuery += " SM0.M0_NOME, SM0.M0_ENDCOB, SM0.M0_BAIRCOB, SM0.M0_CIDCOB, SM0.M0_ESTCOB, SM0.M0_CEPCOB, SM0.M0_CGC, SM0.M0_NOMECOM, SM0.M0_TEL,"
	cQuery += " SF2.F2_SERIE, SF2.F2_DOC, SF2.F2_EMISSAO, SF2.F2_VALMERC, SF2.F2_FRETE, SF2.F2_DESPESA, SF2.F2_SEGURO, SF2.F2_DESCONT, SF2.F2_VALBRUT,"
	cQuery += " SA1.A1_NOME, SA1.A1_END, SA1.A1_BAIRRO, SA1.A1_MUN, SA1.A1_EST, SA1.A1_CEP, SA1.A1_TEL, SA1.A1_CGC,"
	cQuery += " SD2.D2_COD, SD2.D2_QUANT, SD2.D2_TOTAL, SD2.D2_UM,"
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " SB1.B1_DESC"
	cQuery += " FROM " + RetSqlName("SF2") + " SF2"
	cQuery += " LEFT JOIN SYS_COMPANY SM0 ON SM0.M0_CODIGO = ? AND SM0.M0_CODFIL = ? AND SM0.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = SF2.F2_CLIENTE AND SA1.A1_LOJA = SF2.F2_LOJA AND SA1.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("SD2") + " SD2 ON SD2.D2_FILIAL = ? AND SD2.D2_DOC = SF2.F2_DOC AND SD2.D2_SERIE = SF2.F2_SERIE AND SD2.D2_CLIENTE = SF2.F2_CLIENTE AND SD2.D2_LOJA = SF2.F2_LOJA AND SD2.D2_ESPECIE = SF2.F2_ESPECIE AND SD2.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("SB1") + " SB1 ON SB1.B1_FILIAL = ? AND SB1.B1_COD = SD2.D2_COD AND SB1.D_E_L_E_T_ = ?"
	cQuery += " WHERE SF2.F2_FILIAL = ?"
	cQuery += " AND SF2.F2_DOC BETWEEN ? AND ?"
    cQuery += " AND SF2.F2_SERIE BETWEEN ? AND ?"
    cQuery += " AND SF2.F2_CLIENTE BETWEEN ? AND ?"
    cQuery += " AND SF2.F2_LOJA BETWEEN ? AND ?"
    cQuery += " AND SF2.F2_EMISSAO BETWEEN ? AND ?"
    cQuery += " AND SF2.F2_ESPECIE = ?"
    cQuery += " AND SF2.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSF2 )
    oExecA:SetUnSafe( nSeq += 1, cCpoSD2 )
    oExecA:SetUnSafe( nSeq += 1, cCpoSA1 )
    oExecA:SetUnSafe( nSeq += 1, cCpoSB1 )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SA1") )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SD2") )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SB1") )
	oExecA:SetString( nSeq += 1, ' ' )
    oExecA:SetString( nSeq += 1, xFilial("SF2") )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR08'][1] )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR09'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR10'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, 'RFN' )
	oExecA:SetString( nSeq += 1, ' ' )
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	ConOut(oExecA:GetFixQuery())
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )
		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 24/05/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class DeliveryNoteTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
