#INCLUDE "PROTHEUS.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "TECA400.CH"

#DEFINE	 CADASTRO STR0001 //"Orçamentos"
#DEFINE NUMITENS 999

Static cPictureTot		//Picture dos campos de Totais do rodape da tela
Static oDlg				//Objeto dialog principal
Static oSay1			//Obj. Say do Total geral 
Static oSay2			//Obj. Say do Total cliente
Static oSay3			//Obj. Say do Total fabricante
Static aRecAB5It := {}	 //Array com os recnos              

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
Funcao    ³ TECA400  ³ Autor ³ Eduardo Riera         ³ Data ³ 30.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Programa de atualizacao dos Orçamentos                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void TECA400(void)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Aline C.Vale  ³01/06/99³Prothe³Ver chamada para controle de botoes      ³±± 
±±³Andrea Farias ³20/06/05³811   ³BOPS 83192 - Alterada expressao logica do³±± 
±±³              ³        ³      ³9. parametro da Mbrowse.                 ³±± 
±±³Cleber M.     ³12/04/06³96040 ³Inclusao de legenda na rotina.           ³±± 
±±³Cleber M.     ³09/06/06³98346 ³Localizacao do mod. SIGATEC.             ³±± 
±±³Conrado Q.    ³18/10/06³110586³Posiciona o SA1 para consulta padrao do  ³±±
±±³              ³        ³      ³campo AB4_CODPROD nas rotinas de altera  ³±± 
±±³              ³        ³      ³cao, visualizacao, exclusao e efetivacao.³±± 
±±³Cleber M.     ³23/11/06³113603³Alteracao para permitir tambem incluir   ³±± 
±±³              ³        ³      ³ate 300 itens no Apont. do Orcamento(AB5)³±± 
±±³Conrado Q.    ³28/11/06³102284³Criação do P.E. para adição de rotinas no³±± 
±±³              ³        ³      ³aRotinas.                                ³±± 
±±³Conrado Q.    ³26/04/07³124187³Inclusão do ponto de entrada AT400Fil    ³±± 
±±³              ³        ³      ³para filtrar os registros exibidos.      ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA400( cRotina, xAutoCab, xAutoItens, xAutoApont, nOpcAuto )

Local cAt400Fil		:= ""						// Filtro fornecido pelo ponto de entrada
Local bFiltraBrw	:= {||}						// Bloco de código para execução do filtro
Local aIndexAB3		:= {}						// Indice do AB3
Local cFiltraAB3	:= ""						// Filtro utilizado no bloco de código executado para ativar o filtro
Local aCores   		:= {}						// Array com as cores da mBrowse

Private aRotina 	:= MenuDef()							
Private cCadastro 	:= CADASTRO					// Nome da janela
Private cRoda     	:= ""						// Bloco de código quando não é rotina automática
Private aAutoCab	:= {}						// Cabeçalho automático
Private aAutoItens	:= {}						// Itens automáticos
Private aAutoApont	:= {}						// Apontamentos automáticos

aCores :=	{{"AB3_STATUS=='A'",'ENABLE' },;		//Orcamento Aberto
			{ "AB3_STATUS=='E'",'DISABLE'}}			//Orcamento Encerrado

DbSelectArea("AB3")

aRecAB5It := {}

If	xAutoCab <> Nil .AND. xAutoItens <> NIL
	PRIVATE lAt400Auto := .T.

	aAutoCab  := xAutoCab
	aAutoItens:= xAutoItens
	aAutoApont:= xAutoApont
	
	Default aAutoApont:= {}	

	MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"AB3")

Else

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a existencia de Filtros na mBrowse                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AB3->(dbSetOrder(1))			
	If (ExistBlock("AT400FIL"))
		cAt400Fil := ExecBlock("AT400FIL",.F.,.F.)
		If ( ValType(cAt400Fil) == "C" ) .And. !Empty(cAt400Fil)
			cFiltraAB3 := cAt400Fil
		EndIf
		bFiltraBrw 	:= {|| FilBrowse("AB3",@aIndexAB3,@cFiltraAB3) }
		Eval(bFiltraBrw)
	EndIf

    If ValType( cRotina ) == "C"
   		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz tratamento para chamada por outra rotina             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cRotina := Upper( cRotina ) 
		If !Empty( nScan := AScan( aRotina, { |x| Upper( x[2] ) == cRotina } ) ) 
			cRoda := cRotina + "( 'AB3', AB3->( Recno() ), " + Str(nScan,2) + " )" 
			xRet  := Eval( { || &( cRoda ) } ) 
		EndIf 
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock("AT400COR")
			aCores := ExecBlock("AT400COR",.F.,.F.,aCores)
			If ValType(aCores) <> "A"
				aCores :=	{{"AB3_STATUS=='A'",'ENABLE' },;
							{ "AB3_STATUS=='E'",'DISABLE'}}	
			EndIf
		EndIf
		dbSetOrder(1)
		DbSeek(xFilial())
		mBrowse( 6, 1,22,75,"AB3",,,,,,aCores)
	EndIf 
EndIf
dbSelectArea("AB3")
dbSetOrder(1)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³MenuDef   ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 08.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transacao a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := {	{ STR0002	,"AxPesqui"  	,0	,1	,0	,.F.	}	,;		//"Pesquisar"
						{ STR0003	,"AT400Visua"	,0	,2	,0	,.T.	}	,;		//"Visualizar"
						{ STR0004	,"AT400Inclu"	,0	,3	,0	,.T.	}	,;		//"Incluir"
						{ STR0005	,"AT400Alter"	,0	,4	,0	,.T.	}	,;		//"Alterar"
						{ STR0006	,"AT400Exclu"	,0	,5	,0	,.T.	}	,;		//"Exclui"
						{ STR0007	,"At400Efet" 	,0	,4	,0	,.T.	}	,;		//"eFetivar"
						{ STR0025	,"MsDocument"	,0	,4	,0	,.T.	}	,; 		//"Conhecimento"
						{ STR0030	,"At400Leg"  	,0	,2	,0	,.T.	}	} 		//"Legenda" 
	Local aRotAdic		:= {}						// Botoes adicionais da janela
	Local lAT400ROT		:= ExistBlock("AT400ROT")	// P.E. Utilizado para adicionar botoes a janela
	Local nX			:= 0
						
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ P.E. Utilizado para adicionar botoes a janela ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lAT400ROT
		aRotAdic := ExecBlock("AT400ROT",.F.,.F.)
		If ValType(aRotAdic) == "A"
			For nX := 1 to LEN(aRotAdic)
		      If LEN(aRotAdic[nX]) == 4
		         AADD(aRotina,aRotAdic[nX])
		      Endif
			Next nX	
		EndIf
	EndIf
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400Inclu³ Autor ³ Eduardo Riera         ³ Data ³30.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Programa de Inclusao de Orçamentos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void AT400Inclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400Inclu(cAlias,nReg,nOpcx)

Local nUsado  	:= 0
Local nPosItem	:= 0
Local nOpcA   	:= 0
Local aArea   	:= { Alias(), IndexOrd() , Recno() }
Local oGetD		:= Nil
Local aObjects	:= {} 
Local aPosObj 	:= {} 
Local aSizeAut	:= {}
Local aPosGet 	:= {} 
Local nSaveSX8	:= GetSX8Len()
Local nCntFor 	:= 0
Local n1		:= 0				//Total Geral
Local n2     	:= 0				//Total Cliente
Local n3 		:= 0				//Total Fabricante

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT400INC")
	Execblock("AT400INC",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderAB5 := {} , aHeaderAB4 := {}
Private aCols	:= {} , aColsAB5 := {}
Private nOpcF4 := nOpcX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField,.T.)
	Next nC
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT400Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader 	:= aClone(aHeaderAB4)
nUsado 	:= Len(aHeader)
nPosItem	:= aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_ITEM"})
aadd(aCols,Array(nUsado+1))
For nCntFor := 1 To nUsado
	If IsHeadRec(aHeader[nCntFor][2])
		aCols[1][nCntFor] := 0
	Elseif IsHeadAlias(aHeader[nCntFor][2])
		aCols[1][nCntFor] := "AB4"
	Else
		aCols[1][nCntFor] := CriaVar(aHeader[nCntFor][2])
	Endif
Next nCntFor
aCols[1][nPosItem] := "01"
aCols[1][nUsado+1] := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,M->AB3_MOEDA) 

If Type("lAt400Auto")=="U" .OR. !lAt400Auto
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,{||AT400F4(oGetD)})
	
	aSizeAut := MsAdvSize( .T. ) 
	aObjects := {}             
	
	AAdd( aObjects, { 315,  70, .T., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100,   8, .t., .f. } )
	
	aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
	
	aPosObj := MsObjSize( aInfo, aObjects ) 
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta tela de entrada                                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
	
	EnChoice( "AB3" ,nReg,nOpcx,,,,,aPosObj[1])
	oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT400LinOk","AT400TudOk","+AB4_ITEM",.T.,,,,NUMITENS)
	                            
	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
			{{005,072,115,178,223,287}} )
	
	nLinIni := aPosObj[3,1]
	
	@ nLinIni,aPosGet[1,1] SAY STR0008 SIZE 60,09 OF oDlg PIXEL //"Total: "
	@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL
	@ nLinIni,aPosGet[1,3] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
	@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	@ nLinIni,aPosGet[1,5] SAY STR0010 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
	oDlg:Cargo  := {|n1,n2,n3| oSay1:SetText( Transform(n1,cPictureTot) ),;
								oSay2:SetText( Transform(n2,cPictureTot) ),;
								oSay3:SetText( Transform(n3,cPictureTot) ) }
	At400Total(oDlg,.F.)
	ACTIVATE MSDIALOG oDlg ON INIT At400Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().AND.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcX,oGetD)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desabilita a Tecla F4                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
Else
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
		MsGetDAuto(aAutoItens,"AT400LinOk",{|| AT400TudOk() },aAutoCab,aRotina[nOpcX][4]) .AND. At400F4()
		nOpcA := 1
	EndIf
EndIf

If ( nOpcA == 1 )
	Begin Transaction
		If ( AT400Grava(1) )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSX8()
			End
		EndIf
	End Transaction
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
End

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Visua³ Autor ³ Eduardo Riera         ³ Data ³30.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Programa de Visualizacao de Orçamentos                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void At400Visua(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400Visua(cAlias,nReg,nOpcx)

//Local oDlg
Local nUsado  	:= 0
Local nOpcA   	:= 0
Local aArea	  	:= { Alias(), IndexOrd() , Recno() }
Local aAux		:= {}
Local nPosItem	:= 0
Local oGetD

Local aObjects	:= {} 
Local aPosObj 	:= {} 
Local aSizeAut	:= {}
Local aPosGet 	:= {}
Local nCntFor	:= 0
Local n1		:= 0				//Total Geral
Local n2     	:= 0				//Total Cliente
Local n3 		:= 0				//Total Fabricante

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT400VIS")
	Execblock("AT400VIS",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderAB5 := {} , aHeaderAB4 := {}
Private aCols	:= {} , aColsAB5 := {}
Private nOpcF4 := nOpcX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SA1 para consulta do AB4_CODPROD³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SA1->(DbSetOrder(1))
SA1->(DbSeek(xFilial("SA1") + AB3->AB3_CODCLI + AB3->AB3_LOJA))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB3->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT400Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader 	:= aClone(aHeaderAB4)
nUsado 	:= Len(aHeader)
nPosItem	:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})
dbSelectArea("AB4")
dbSetOrder(1)
DbSeek(xFilial("AB4")+AB3->AB3_NUMORC)
While ( !Eof() .AND. xFilial("AB4") 	== AB4->AB4_FILIAL .AND.;
							AB3->AB3_NUMORC	== AB4->AB4_NUMORC )
	aadd(aCols,Array(nUsado))
	For nCntFor := 1 To nUsado
   		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := AB4->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "AB4"
		Elseif ( aHeader[nCntFor][10] <> "V" )		
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aAux := {}
	dbSelectArea("AB5")
	dbSetOrder(1)
	If ( DbSeek(xFilial("AB5")+AB3->AB3_NUMORC+AB4->AB4_ITEM) )
      While ( !Eof() .AND. xFilial("AB5") == AB5->AB5_FILIAL .AND.;
									M->AB3_NUMORC	== AB5->AB5_NUMORC .AND.;
									AB4->AB4_ITEM== AB5->AB5_ITEM )
			aadd(aAux,Array(Len(aHeaderAB5)+1))
			For nCntFor := 1 To Len(aHeaderAB5)
				If IsHeadRec(aHeaderAB5[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := AB5->(RecNo())
				Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "AB5"
				Elseif ( aHeaderAB5[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB5[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
         dbSelectArea("AB5")
			dbSkip()					
		End
		aadd(aColsAB5,aClone(aAux))
	Else
		aadd(aAux,Array(Len(aHeaderAB5)+1))
		For nCntFor := 1 To Len(aHeaderAB5)
			If IsHeadRec(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB5"
			Else		
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
		aadd(aColsAB5,aClone(aAux))
	EndIf
	dbSelectArea("AB4")
	dbSkip()					
End
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Habilita a Tecla F4                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,{||AT400F4(oGetD)})
                      
aSizeAut := MsAdvSize( .T. )                                       
aObjects := {} 
AAdd( aObjects, { 315,  70, .T., .t. } )
AAdd( aObjects, { 100, 100, .t., .t. } )
AAdd( aObjects, { 100,   8, .t., .f. } )

aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 

aPosObj := MsObjSize( aInfo, aObjects ) 
    
aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
		{{005,072,115,178,223,287}} )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,M->AB3_MOEDA) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta tela de entrada                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 

EnChoice( "AB3" ,nReg,nOpcx,,,,,APOSOBJ[1],,3,,,,,,.T.)
oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT400LinOk","AT400TudOk","+AB4_ITEM",.F.)

nLinIni := aPosObj[3,1] 

@ nLinIni,aPosGet[1,1] SAY STR0008 SIZE 60,09 OF oDlg PIXEL //"Total: "
@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
@ nLinIni,aPosGet[1,3] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
@ nLinIni,aPosGet[1,5] SAY STR0010 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
oDlg:Cargo  := {|n1,n2,n3| oSay1:SetText( Transform(n1, cPictureTot) ),;
							oSay2:SetText( Transform(n2, cPictureTot) ),;
							oSay3:SetText( Transform(n3, cPictureTot) ) }
At400Total(oDlg,.F.)
ACTIVATE MSDIALOG oDlg ON INIT At400Bar(oDlg,{||oDlg:End()},{||oDlg:End()},nOpcX,oGetD)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Desabilita a Tecla F4                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SetKey(VK_F4,Nil)
dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Alter³ Autor ³ Eduardo Riera         ³ Data ³30.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Programa de Alteracao de Orçamentos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void At400Alter(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400Alter(cAlias,nReg,nOpcx)

//Local oDlg
Local nUsado  	:= 0
Local nOpcA   	:= 0
Local aArea   	:= { Alias(), IndexOrd() , Recno() }
Local aAux		:= {}
Local aTravas 	:= {}
Local lTravas 	:= .T.
Local nPosItem	:= 0
Local lAltera	:= .T.
Local oGetD

Local aObjects	:= {} 
Local aPosObj 	:= {} 
Local aSizeAut	:= {}
Local aPosGet 	:= {}
Local nSaveSX8	:= GetSX8Len()
Local nCntFor 	:= 0
Local n1		:= 0				//Total Geral
Local n2     	:= 0				//Total Cliente
Local n3 		:= 0				//Total Fabricante

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If AB3->AB3_STATUS == "E"	//Encerrado
	Help(" ",1,"AT400ALTERA",,STR0039,1,1)	//"O Orçamento não pode ser alterado, pois o mesmo já foi encerrado."
	Return .F.
EndIf

If ExistBlock("AT400ALT")
	Execblock("AT400ALT",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderAB5 := {} , aHeaderAB4 := {}
Private aCols	:= {} , aColsAB5 := {}
Private nOpcF4 := nOpcX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SA1 para consulta do AB4_CODPROD³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SA1->(DbSetOrder(1))
SA1->(DbSeek(xFilial("SA1") + AB3->AB3_CODCLI + AB3->AB3_LOJA))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB3->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava Registros                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SoftLock("AB3") )
	aadd(aTravas,{ Alias() , RecNo() })
Else
	lTravas := .F.
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT400Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader 	:= aClone(aHeaderAB4)
nUsado 	:= Len(aHeader)
nPosItem	:= aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_ITEM"})
dbSelectArea("AB4")
dbSetOrder(1)
DbSeek(xFilial("AB4")+AB3->AB3_NUMORC)
While ( !Eof() .AND. xFilial("AB4") 	== AB4->AB4_FILIAL .AND.;
							AB3->AB3_NUMORC	== AB4->AB4_NUMORC )
	If ( SoftLock("AB4") )
		aadd(aTravas, { Alias() , RecNo() })
	Else
		lTravas := .F.
	EndIf
	aadd(aCols,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec( aHeader[nCntFor][2] )
			aCols[Len(aCols)][nCntFor] := AB4->(RecNo())
		Elseif IsHeadAlias( aHeader[nCntFor][2] )
			aCols[Len(aCols)][nCntFor] := "AB4"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aCols[Len(aCols)][nUsado+1] := .F.
	aAux := {}
	dbSelectArea("AB5")
	dbSetOrder(1)
	If ( DbSeek(xFilial("AB5")+AB3->AB3_NUMORC+AB4->AB4_ITEM) )
      While ( !Eof() .AND. xFilial("AB5") == AB5->AB5_FILIAL .AND.;
									M->AB3_NUMORC	== AB5->AB5_NUMORC .AND.;
									AB4->AB4_ITEM	== AB5->AB5_ITEM )
			If ( SoftLock("AB5") )
				aadd(aTravas, { Alias() , RecNo() })
			Else
				lTravas := .F.
			EndIf
			aadd(aAux,Array(Len(aHeaderAB5)+1))
			For nCntFor := 1 To Len(aHeaderAB5)
				If IsHeadRec( aHeaderAB5[nCntFor][2] )
					aAux[Len(aAux)][nCntFor] := AB5->(RecNo())
				Elseif IsHeadAlias( aHeaderAB5[nCntFor][2] )
					aAux[Len(aAux)][nCntFor] := "AB5"
				Elseif ( aHeaderAB5[nCntFor][10] <> "V" )
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB5[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
			dbSelectArea("AB5")
			dbSkip()					
		End
		aadd(aColsAB5,aClone(aAux))
	Else
		aadd(aAux,Array(Len(aHeaderAB5)+1))
		For nCntFor := 1 To Len(aHeaderAB5)
			If IsHeadRec(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB5"
			Else			
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
		aadd(aColsAB5,aClone(aAux))
	EndIf
	dbSelectArea("AB4")
	dbSkip()					
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,M->AB3_MOEDA) 

If ( lTravas .AND. lAltera)
	If Type("lAt400Auto")=="U" .OR. !lAt400Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Habilita a Tecla F4                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,{||AT400F4(oGetD)})
                         
		aSizeAut := MsAdvSize( .T. ) 
		aObjects := {}                       
		AAdd( aObjects, { 315,  70, .T., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100,   8, .t., .f. } )
		
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
			{{005,072,115,178,223,287}} )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
		EnChoice( "AB3" ,nReg,nOpcx,,,,,APOSOBJ[1],,3,,,,,,.T.)
		oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT400LinOk","AT400TudOk","+AB4_ITEM",.T.,,,,NUMITENS, "At400GrVld")
		
		nLinIni := aPosObj[3,1]
		
		@ nLinIni,aPosGet[1,1] SAY STR0008 SIZE 60,09 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,3] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,5] SAY STR0010 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		oDlg:Cargo  := {|n1,n2,n3| oSay1:SetText( Transform(n1, cPictureTot) ),;
									oSay2:SetText( Transform(n2, cPictureTot) ),;
									oSay3:SetText( Transform(n3, cPictureTot) ) }
		At400Total(oDlg,.F.)
		ACTIVATE MSDIALOG oDlg ON INIT At400Bar(oDlg,{||nOpca:=1,if(oGetD:TudoOk().AND.Obrigatorio(aGets,aTela),oDlg:End(),nOpca:=0)},{||oDlg:End()},nOpcX,oGetD)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desabilita a Tecla F4                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,Nil)
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
			MsGetDAuto(aAutoItens,"AT400LinOk",{|| AT400TudOk() },aAutoCab,aRotina[nOpcX][4]) .AND. At400F4()
			nOpcA := 1
		EndIf
	EndIf
EndIf
If ( nOpcA == 1 )
	Begin Transaction
		If Len(aRecAB5It) > 0 .And. lAltera
			At400Ajust(aRecAB5It)
			aRecAB5It := {}	
		EndIf	
		If ( AT400Grava(2)  )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSX8()
			End
		EndIf
	End Transaction
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
End
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Destrava os registros                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To Len(aTravas)
	dbSelectArea(aTravas[nCntFor,1])
	dbGoto(aTravas[nCntFor,2])
	MsUnLock()
Next nCntFor

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Exclu³ Autor ³ Eduardo Riera         ³ Data ³30.09.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Programa de Exclusao  de Orçamentos                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void At400Exclu(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400Exclu(cAlias,nReg,nOpcx)

//Local oDlg
Local nUsado  	:= 0
Local nOpcA  	:= 0
Local aArea   	:= { Alias(), IndexOrd() , Recno() }
Local aAux		:= {}
Local aTravas 	:= {}
Local lTravas 	:= .T.
Local nPosItem	:= 0
Local lExclui	:= .T.
Local oGetD

Local aObjects	:= {} 
Local aPosObj 	:= {} 
Local aSizeAut	:= {}
Local aPosGet 	:= {}
Local nSaveSX8	:= GetSX8Len()
Local nCntFor 	:= 0
Local n1		:= 0				//Total Geral
Local n2     	:= 0				//Total Cliente
Local n3 		:= 0				//Total Fabricante
Local lRet 		:= .T.				//Permite exclusão 

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT400EXC")
	Execblock("AT400EXC",.F.,.F.)
Endif

Private aTela[0][0],aGets[0]
Private aHeader:= {} , aHeaderAB5 := {} , aHeaderAB4 := {}
Private aCols	:= {} , aColsAB5 := {}
Private nOpcF4 := nOpcX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SA1 para consulta do AB4_CODPROD³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SA1->(DbSetOrder(1))
SA1->(DbSeek(xFilial("SA1") + AB3->AB3_CODCLI + AB3->AB3_LOJA))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aStruField := FWSX3Util():GetAllFields("AB3")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField,.T.)
		Else
			M->&(cField) := AB3->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Trava Registros                                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SoftLock("AB3") )
	aadd(aTravas,{ Alias() , RecNo() })
Else
	lTravas := .F.
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AT400Monta()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader 	:= aClone(aHeaderAB4)
nUsado 		:= Len(aHeader)
nPosItem	:= aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_ITEM"})
dbSelectArea("AB4")
dbSetOrder(1)
DbSeek(xFilial("AB4")+AB3->AB3_NUMORC)
While ( !Eof() .AND. xFilial("AB4") 	== AB4->AB4_FILIAL .AND.;
							AB3->AB3_NUMORC	== AB4->AB4_NUMORC )
	If ( SoftLock("AB4") )
		aadd(aTravas, { Alias() , RecNo() })
	Else
		lTravas := .F.
	EndIf
	aadd(aCols,Array(nUsado))
   aadd(aColsAB5,aClone(aAux))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := AB4->(RecNo())
		Elseif IsHeadAlias(aHeader[nCntFor][2])
			aCols[Len(aCols)][nCntFor] := "AB4"
		Elseif ( aHeader[nCntFor][10] <> "V" )
			aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
		Else
			aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
		EndIf
	Next nCntFor
	aAux := {}
	dbSelectArea("AB5")
	dbSetOrder(1)
	If ( DbSeek(xFilial("AB5")+AB3->AB3_NUMORC+AB4->AB4_ITEM) )
      While ( !Eof() .AND. xFilial("AB5") == AB5->AB5_FILIAL .AND.;
									M->AB3_NUMORC	== AB5->AB5_NUMORC .AND.;
									AB4->AB4_ITEM	== AB5->AB5_ITEM )
			If ( SoftLock("AB5") )
				aadd(aTravas, { Alias() , RecNo() })
			Else
				lTravas := .F.
			EndIf
			aadd(aAux,Array(Len(aHeaderAB5)+1))
			For nCntFor := 1 To Len(aHeaderAB5)
				If IsHeadRec(aHeaderAB5[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := AB5->(RecNo())
				Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
					aAux[Len(aAux)][nCntFor] := "AB5"
				Elseif ( aHeaderAB5[nCntFor][10] <> "V" )							
					aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB5[nCntFor][2]))
				Else
					aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
				EndIf
			Next nCntFor
			aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
			dbSelectArea("AB5")
			dbSkip()					
		End
		aadd(aColsAB5,aClone(aAux))
	Else
		aadd(aAux,Array(Len(aHeaderAB5)+1))
		For nCntFor := 1 To Len(aHeaderAB5)
			If IsHeadRec(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := 0
			Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
				aAux[Len(aAux)][nCntFor] := "AB5"
			Else
				aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
			Endif
		Next nCntFor
		aAux[Len(aAux)][nPosItem] := "01"
		aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
		aadd(aColsAB5,aClone(aAux))
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aqui eh verificado se o item esta amarrado a alguma OS que foi baixada.³
	//³ Quando isto acontece a alteracao nao eh permitida.                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("AB7")
	dbSetOrder(1)
	If ( !Empty(AB4->AB4_NUMOS) .AND. DbSeek(xFilial("AB7")+AB4->AB4_NUMOS) )
      If ( AB7->AB7_TIPO<>"1" )
			If ( lExclui )
				Help(" ",1,"AT400TIPOS")
			EndIf
			lExclui := .F.
		EndIf
   EndIf

	dbSelectArea("AB4")
	dbSkip()					
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,M->AB3_MOEDA) 

If ( lTravas .AND. lExclui )
	If Type("lAt400Auto")=="U" .OR. !lAt400Auto

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Habilita a Tecla F4                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,{||AT400F4(oGetD)})
	    
		aSizeAut := MsAdvSize( .T. ) 
		aObjects := {}                       
		AAdd( aObjects, { 315,  70, .T., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100,   8, .t., .f. } )
		
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 } 
		aPosObj := MsObjSize( aInfo, aObjects ) 
		
		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],315,;
				{{005,072,115,178,223,287}} )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta tela de entrada                                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] PIXEL STYLE WS_DLGFRAME 
		EnChoice( "AB3" ,nReg,nOpcx,,,,,APOSOBJ[1],,3,,,,,,.T.)
		oGetD := MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"AT400LinOk","AT400TudOk","+AB4_ITEM",.F.)
		
		nLinIni := aPosObj[3,1] 
		
		@ nLinIni,aPosGet[1,1] SAY STR0008 SIZE 60,09 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay1 VAR Transform(n1, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,3] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay2 VAR Transform(n2, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		@ nLinIni,aPosGet[1,5] SAY STR0010 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay3 VAR Transform(n3, cPictureTot) SIZE 40,09 OF oDlg PIXEL 
		oDlg:Cargo  := {|n1,n2,n3| oSay1:SetText( Transform(n1, cPictureTot) ),;
									oSay2:SetText( Transform(n2, cPictureTot) ),;
									oSay3:SetText( Transform(n3, cPictureTot) ) }
		At400Total(oDlg,.F.)
		ACTIVATE MSDIALOG oDlg ON INIT At400Bar(oDlg,{||nOpca:=1,oDlg:End()},{||oDlg:End()},nOpcX,oGetD)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Desabilita a Tecla F4                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SetKey(VK_F4,Nil)
	Else
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND.;
			MsGetDAuto(aAutoItens,"AT400LinOk",{|| AT400TudOk() },aAutoCab,aRotina[nOpcX][4]) .AND. At400F4()
			nOpcA := 1
		EndIf
	EndIf	
EndIf

If ExistBlock( "AT400DEL" ) 
	lRet := ExecBlock( "AT400DEL", .F., .F. ) 
EndIf 

If lRet .And. ( nOpcA == 1 )
	Begin Transaction
		If ( AT400Grava(3)  )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSX8()
			End
		EndIf
	End Transaction
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSX8()
End
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Destrava os registros                                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nCntFor := 1 To Len(aTravas)
	dbSelectArea(aTravas[nCntFor,1])
	dbGoto(aTravas[nCntFor,2])
	MsUnLock()
Next nCntFor

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Efet ³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Efetiva Orçamentos                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Numero da opcao selecionada                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Analista      ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³16/05/2006³Cleber Martinez³Bops 98346: Localizacao do modulo SIGATEC.  ³±±
±±³16/02/2007³Conrado Quilles³Bops 119542: Retirado ajuste no SX1.        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At400Efet(cAlias,nReg,nOpc)

Local aSavRot 	:= aClone(aRotina)		//Backup do array aRotina
Local cQuery  	:= ""					//Query a ser executada
Local cArqInd 	:= CriaTrab(,.F.)		//Nome do arq. temporario
Local cMarca	:= GetMark()			//Indica se o orcam. esta marcado
Local nIndex	:= 0					//Indice do arq. temporario a ser utilizado
Private cFiltroAB3 := ""
aRotina := { 	{ STR0011,"At400Visua",0,2},;	//"Visualiza"
					{ STR0012,"At400GrvOs",0,4} }	//"Ord.Servi‡o"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros                                                             ³
//³                                                                        ³
//³ MV_PAR01 - Cliente de                                                  ³
//³ MV_PAR02 - Cliente ate                                                 ³
//³ MV_PAR03 - Emissao de                                                  ³
//³ MV_PAR04 - Emissao ate                                                 ³
//³ MV_PAR05 - Chamado de                                                  ³
//³ MV_PAR06 - Chamado ate                                                 ³
//³ MV_PAR07 - Qual moeda                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Pergunte("ATA400",.T.) )
	If Recmoeda( dDatabase, MV_PAR07 ) <= 0
		//"Não existe taxa cadastrada para a moeda selecionada: " # "Atenção"
		MsgAlert(STR0034 + StrZero(MV_PAR07,1), STR0035)
		aRotina := aClone(aSavRot)
		Return .F.
	EndIf
	cQuery += "AB3_FILIAL=='"+xFilial("AB3")+"'.AND."
	cQuery += "AB3_STATUS=='A'.AND."
	cQuery += "AB3_CODCLI>='"+MV_PAR01+"'.AND."
	cQuery += "AB3_CODCLI<='"+MV_PAR02+"'.AND."
	cQuery += "Dtos(AB3_EMISSA)>='"+Dtos(MV_PAR03)+"'.AND."
	cQuery += "Dtos(AB3_EMISSA)<='"+Dtos(MV_PAR04)+"'.AND."
	cQuery += "AB3_NUMORC>='"+MV_PAR05+"'.AND."
	cQuery += "AB3_NUMORC<='"+MV_PAR06+"'"
    dbSelectArea("AB3")
	dbSetOrder(1)
	IndRegua("AB3",cArqInd,IndexKey(),,cQuery)
	nIndex := RetIndex("AB3")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	cFiltroAB3 := AB3->(DbFilter())
	dbSetOrder(nIndex+1)
	dbGotop()
	If ( !Eof() .AND. !Bof() )
		MarkBrow("AB3","AB3_OK",,,,cMarca)
	Else
		Help(" ",1,"REGNOIS")
	EndIf
	dbSelectArea("AB3")
    RetIndex("AB3")
	dbClearFilter()
	Ferase(cArqInd+OrdBagExt())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Retorna as Situacao de Entrada                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aRotina := aClone(aSavRot)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400MONTA  ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Monta aHeader                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT400MONTA(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aHeader adaptada   ³±±
±±³          ³               ³para a utilização do Walk-Thru.             ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400MONTA()

Local nUsado   := 0
Local aArea    := { Alias() , IndexOrd() , Recno() }
Local aAreaAB4 := { AB4->(IndexOrd()), AB4->(Recno()) }
Local aAreaAB5 := { AB5->(IndexOrd()), AB5->(Recno()) }
Local aHeader  := {}

Local aStruField := {}
Local cField     := ""
Local nC         := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do AB4                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}

nUsado := 0
aStruField := FWSX3Util():GetAllFields("AB4")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"AB4",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("AB4"	,aHeader)
nUsado := Len(aHeader)

aHeaderAB4 := aClone(aHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do AB5                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}

nUsado := 0
aStruField := FWSX3Util():GetAllFields("AB5")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		If (X3USO(GetSx3Cache(cField, "X3_USADO")) .And. ;
			cNivel >= GetSx3Cache(cField, "X3_NIVEL"))
			aAdd(aHeader,{ AllTrim(FWX3Titulo(cField)),;
							cField,;
							GetSx3Cache(cField, "X3_PICTURE"),;
							GetSx3Cache(cField, "X3_TAMANHO"),;
							GetSx3Cache(cField, "X3_DECIMAL"),;
							GetSx3Cache(cField, "X3_VALID"),;
							GetSx3Cache(cField, "X3_USADO"),;
							GetSx3Cache(cField, "X3_TIPO"),;
							"AB5",;
							GetSx3Cache(cField, "X3_CONTEXT") } )
		EndIf
	Next nC
EndIf

// Inclui coluna de registro atraves de funcao generica
ADHeadRec("AB5"	,aHeader)
nUsado := Len(aHeader)

aHeaderAB5 := aClone(aHeader)

dbSelectArea("AB4")
dbSetOrder(aAreaAB4[1])
dbGoto(aAreaAB4[2])

dbSelectArea("AB5")
dbSetOrder(aAreaAB5[1])
dbGoto(aAreaAB5[2])

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400GrvOs³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Gera a Ordem de Servi‡o                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Void                                                       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At400GrvOs()

Processa({|| At400Proc() })

If ExistBlock("AT400GOS")
	Execblock("AT400GOS",.F.,.F.)
Endif

CloseBrowse()
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Proc ³ Autor ³ Eduardo Riera         ³ Data ³ 02.10.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Processamento da Efetivacao de OS                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Void                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function At400Proc()

Local nCntFor 			:= 0
Local uCampo			:= ""
Local lTravas			:= .T.
Local aTravas			:= {}
Local nUsado			:= 0
Local nPosItem			:= 0
Local aAux				:= {}
Local nSaveSX8       := GetSX8Len()
Local lContinua			:= .T.
Local lAT400EFT 		:= ExistBlock("AT400EFT")
Private aCols			:= {}
Private aHeader		:= {}
Private aHeaderAB4   := {}
Private AHeaderAB5	:= {}
Private aColsAB5		:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta aHeader                                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aT400Monta()
aHeader	:= aClone(aHeaderAB4)
nUsado	:= Len(aHeader)
nPosItem := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Processamento da MarkBrowse                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("AB3")
AB3->(DbSetFilter({|| &cFiltroAB3}, cFiltroAB3))
dbGotop() //IndRegua()
ProcRegua(LastRec())
While ( !Eof() )
	If ( IsMark("AB3_OK",ThisMark(),ThisInv()) .AND. SoftLock("AB3")) .AND. At400MoedaOK()
		If lAT400EFT
			lContinua := Execblock("AT400EFT",.F.,.F.)
		Endif
		If lContinua
		lTravas	:= .T.
		aTravas	:= {}
		aCols		:= {}
		aColsAb5 := {}
		aAux     := {}
		aHeader	:= aClone(aHeaderAB4)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Cria Variaveis de Memoria da Enchoice                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("AB3")
		aadd(aTravas , { Alias() , RecNo() })
		For nCntFor := 1 To FCount()
			uCampo := FieldName(nCntFor)
        	M->&(uCampo) := AB3->(FieldGet(FieldPos(uCampo)))
		Next nCntFor
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Monta Acols                                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("AB4")
		dbSetOrder(1)
		DbSeek(xFilial("AB4")+M->AB3_NUMORC)
		While ( !Eof() .AND. xFilial("AB4") == AB4->AB4_FILIAL .AND.;
									M->AB3_NUMORC	== AB4->AB4_NUMORC )
			aadd(aCols,Array(nUsado+1))
			For nCntFor := 1 To nUsado
				If IsHeadRec(aHeader[nCntFor][2])
					aCols[Len(aCols)][nCntFor] := AB4->(RecNo())
				Elseif IsHeadAlias(aHeader[nCntFor][2])
					aCols[Len(aCols)][nCntFor] := "AB4"
				Elseif ( aHeader[nCntFor][10] <> "V" )
					aCols[Len(aCols)][nCntFor] := FieldGet(FieldPos(aHeader[nCntFor][2]))
				Else
					aCols[Len(aCols)][nCntFor] := CriaVar(aHeader[nCntFor][2])
				EndIf
				If ( AllTrim(aHeader[nCntFor][2])=="AB4_TIPO" )
					If AB4->AB4_TIPO == "1" 
						aCols[Len(aCols)][nCntFor] := "2"
					EndIf 	
				EndIf
			Next nCntFor
			aCols[Len(aCols)][nUsado+1] := .F.
			aAux := {}
			dbSelectArea("AB5")
			dbSetOrder(1)
			If ( DbSeek(xFilial("AB5")+M->AB3_NUMORC+AB4->AB4_ITEM) )
				While ( !Eof() .AND. xFilial("AB5") == AB5->AB5_FILIAL .AND.;
											M->AB3_NUMORC	== AB5->AB5_NUMORC .AND.;
											AB4->AB4_ITEM	== AB5->AB5_ITEM )
					aadd(aAux,Array(Len(aHeaderAB5)+1))
					For nCntFor := 1 To Len(aHeaderAB5)
						If IsHeadRec(aHeaderAB5[nCntFor][2])
							aAux[Len(aAux)][nCntFor] := AB5->(RecNo())
						Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
							aAux[Len(aAux)][nCntFor] := "AB5"
						Elseif ( aHeaderAB5[nCntFor][10] <> "V" )
							aAux[Len(aAux)][nCntFor] := FieldGet(FieldPos(aHeaderAB5[nCntFor][2]))
						Else
							aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
						EndIf
					Next nCntFor
					aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
					dbSelectArea("AB5")
					If ( SoftLock("AB5") )
						aadd(aTravas,{Alias(),RecNo()})
					Else
						lTravas := .F.
					EndIf
					dbSkip()					
				End
				aadd(aColsAB5,aClone(aAux))
			Else
				aadd(aAux,Array(Len(aHeaderAB5)+1))
				For nCntFor := 1 To Len(aHeaderAB5)
					If IsHeadRec(aHeaderAB5[nCntFor][2])
						aAux[Len(aAux)][nCntFor] := 0
					Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
						aAux[Len(aAux)][nCntFor] := "AB5"
					Else
						aAux[Len(aAux)][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
					Endif
				Next nCntFor
				aAux[Len(aAux)][nPosItem] := "01"
				aAux[Len(aAux)][Len(aHeaderAB5)+1] := .F.
				aadd(aColsAB5,aClone(aAux))
			EndIf
			dbSelectArea("AB4")
			If ( SoftLock("AB4") )
				aadd(aTravas,{Alias(),RecNo()})
			Else
				lTravas := .F.
			EndIf
			dbSkip()					
		End
      Begin Transaction
			at400Grava(2)
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			End	
		End Transaction
		For nCntFor := 1 To Len(aTravas)
			dbSelectArea(aTravas[nCntFor][1])
			dbGoto(aTravas[nCntFor][2])
			MsUnLock()
		Next nCntFor
		EndIf	
	EndIf
	dbSelectArea("AB3")
	dbSkip()
	IncProc()
End
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400LinOk  ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Validacao da Linha na GetDados                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT400LinOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 05/03/07 ³ Cleber M.	 ³Bops 120351: Alteracao para validar tambem  ³±±
±±³			 ³				 ³a primeira linha do aCols.	          	  ³±± 
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400LinOk(oGet)

Local lRetorno := .T.
Local nUsado   := Len(aHeader)
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_CODPRO"})
Local nPosnSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_NUMSER"})
Local nPosPrb  := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_CODPRB"})
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_ITEM"})
Local nCntFor  := 0
Local l400LinOk:= ExistBlock("AT400LOK")
Local aAreaAB7 := {}
Local aAreaAB4	:= {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Aqui eh verificado se o item esta amarrado a alguma OS que foi baixada.³
//³ Quando isto acontece a alteracao nao eh permitida.                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAreaAB4 := AB4->(GetArea())
AB4->(DbSetOrder(1))

If (ALTERA .OR. (!ALTERA .and. !INCLUI)) .AND.   ( ( Type("lAt400Auto")=="L" .AND. lAt400Auto)   .OR.  aCols[n][nUsado+1] ) .AND. ;
    ( n > 0 .AND.  n <= Len(aCols)  )  .AND. ;
     AB4->(DbSeek(xFilial("AB4")+M->AB3_NUMORC+aCols[n][nPosItem])) .AND. !Empty(AB4->AB4_NUMOS) 
	
	aAreaAB7 := AB7->(GetArea())
	AB7->(DbSetOrder(1))
	If  AB7->(DbSeek(xFilial("AB7")+AB4->AB4_NUMOS) ) .AND. AB7->AB7_TIPO <>"1"
		Help(" ",1,"AT400LinOk",,STR0043,1,1) //"Não é permitido alterar ou excluir um Item do Orçamento que possui uma Ordem de Serviço vinculada com Situação diferente de 1"
		lRetorno := .F.
   EndIf

   RestArea(aAreaAB7)
EndIf
RestArea(aAreaAB4)

If lRetorno .AND. ( !aCols[n][nUsado+1] .AND. ( Len(aCols)>=1 .OR. !Empty(aCols[n][nPosPrd]) ))
	If ( 	Empty(aCols[n][nPosPrd]) .OR.;
			Empty(aCols[n][nPosnSer]) .OR.;
			Empty(aCols[n][nPosPrb]) )
		lRetorno := .F.
		Help(" ",1,"AT400LIN01")
	EndIf
	If ( lRetorno )
		For nCntFor := 1 To Len(aCols)
			If (  aCols[n][nPosPrd]+aCols[n][nPosNSer]+aCols[n][nPosPrb]==;
					aCols[nCntFor][nPosPrd]+aCols[nCntFor][nPosNSer]+aCols[nCntFor][nPosPrb] .AND.;
					n <> nCntFor .AND. !aCols[nCntFor][nUsado+1])
				Help(" ",1,"AT400LIN02")
				lRetorno := .F.
			EndIf
		Next nCntFor
	EndIf
	If ( lRetorno )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( !DbSeek(xFilial("AA3")+M->AB3_CODCLI+M->AB3_LOJA+aCols[n][nPosPrd]+aCols[n][nPosNSer]) )
			Help(" ",1,"AT400LIN03")
			lRetorno := .F.
		EndIf
	EndIf
	If ( lRetorno )
	    AA3->(DbSetOrder(6))
		If AA3->(ColumnPos('AA3_MSBLQL')) > 0
	       If !At400VerBl(aCols[n][nPosPrd], aCols[n][nPosnSer], M->AB3_CODCLI,  M->AB3_LOJA)  //Cod. produto/equipamento, Id.Unico, Cod.Cliente, Cod.Loja                                                                                                                                                                                                                                                                                                                                                                                            
			    Help(" ",1,"HELP", , STR0047,3,1 ) // "O Equipamento selecionado esta bloqueado para utilização.Verifique a situação do equipamento na Base de Atendimento."  
		       lRetorno := .F.
			Endif   
	    Endif
	Endif	
EndIf

If Type("lAt400Auto")=="U" .OR. !lAt400Auto
	At400Total(oGet:oWnd,.F.)
EndIf

If lRetorno .AND. l400LinOk
	lRetorno := Execblock("AT400LOK",.f.,.f.)
Endif

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400TudOk  ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Validacao da GetDados                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT400TudOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400TudOk()

Local lRetorno := .T.
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_CODPRO"})
Local nPosnSer := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_NUMSER"})
Local nUsado   := Len( aHeader )  
Local nCntFor  := 0

For nCntFor := 1 To Len(aCols)
	If ( lRetorno ) .AND. !aCols[ nCntFor, nUsado + 1 ] 
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( !DbSeek(xFilial("AA3")+M->AB3_CODCLI+M->AB3_LOJA+aCols[nCntFor][nPosPrd]+aCols[nCntFor][nPosNSer]) )
			Help(" ",1,"AT400TUDOK")
			lRetorno := .F.
		EndIf
	EndIf
Next nCntFor   

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do Usuario                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno 
	If ExistBlock( "AT400TOK" ) 
		lRetorno := ExecBlock( "AT400TOK", .F., .F. ) 
	EndIf 
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400F4     ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Tratamento da tecla F4.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void AT400F4(Void)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ a,b,c                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/01/2007³ Conrado Q.    ³Bops 115748: Montagem do aCols adaptada para³±±
±±³          ³               ³utilização do Walk-Thru.                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400F4(oGetD)

Local oDlg												//Objeto Dialog
Local oGetF4											//Objeto GetDados
Local oSay1												//Objeto SAY
Local oSay2												//Objeto SAY
Local oSay3												//Objeto SAY
Local nOpcA    := 0										//Opcao escolhida
Local aArea    := GetArea()								//Dados da area atual
Local aSavAcols:= {}									//Array copia do aCols
Local nSavN		:= 0									//Copia da variavel N
Local aAux		:= {} 									//Array auxiliar
Local bSetKey											//Bloco de execucao dos SetKeys
Local cPrompt  := STR0013 								//"Apontamento(s)"
Local nCntFor  := 0										//Usada em lacos For...Next
Local nUsado   := Len(aHeaderAB5)						//Tamanho do Header do AB5
Local nPosItem := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})		//Posicao do campo Item no aHeaderAB5
Local nPosPrd  := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_CODPRO"})		//Posicao do campo Produto no aHeaderAB4
Local nPosNSer := aScan(aHeaderAB4,{|x| AllTrim(x[2])=="AB4_NUMSER"})		//Posicao do campo Nr Serie no aHeaderAB4
Local nItAcols := If(Type("N")=="U",1,N)									//Item selecionado no aCols
Local cProduto := ""									//Codigo do produto
Local cNumSer  := "" 									//Nr. de serie
Local lAltera  := .F.									//Indica se e alteracao do Orcamento
Local lInclui  := .F. 									//Indica se e inclusao do Orcamento
Local lRetorno := .T.     								//Indica o retorno da funcao
Local aButtons := {}
Local cSubItem 	:= StrZero(0,TamSX3("AB5_SUBITE")[1])
Local nTamSub  	:= TamSX3("AB5_SUBITE")[1]	//Tamanho do campo AB5_SUBITE
Local nPRecn   	:= aScan(aHeaderAB5,{|x|"_REC_WT" $ x[2]}) //Busca o campo Recno

PRIVATE aColsMain 										//Copia do aCols principal
PRIVATE nMAIN := nItAcols								//Guarda item do aCols selecionado

aadd(aButtons,{"RELATORIO",{|| AT400Impos()},STR0040, STR0040 })	

If aRotina[ nOpcF4, 4 ] == 3
	lInclui := .T. 
ElseIf aRotina[ nOpcF4, 4 ] == 4 	 
	lAltera := .T.      
EndIf 

aSavAcols	:= aClone(aCols)
aColsMain   := aClone(aCols) 
nSavN			:= nItAcols
cProduto    := aSavaCols[nItAcols][nPosPrd]
cNumSer		:= aSavaCols[nItAcols][nPosNSer]
aHeader		:= aClone(aHeaderAB5)
If ( Empty(aColsAB5) .OR. Len(aColsAB5)<=nItAcols )
	aadd(aAux,Array(nUsado+1))
	For nCntFor := 1 To nUsado
		If IsHeadRec(aHeaderAB5[nCntFor][2])
			aAux[1][nCntFor] := 0
		Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
			aAux[1][nCntFor] := "AB5"
		Else	
			aAux[1][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2],.T.)
		Endif
	Next nCntFor
	aAux[1][nUsado+1] := .F.
	If nTamSub == 2
		aAux[1][nPosItem] := "01"
	Else	
		aAux[1][nPosItem] := Soma1(RTRIM(cSubItem),2)
	EndIf	
	For nCntFor := Len(aColsAB5)+1 To ( nItAcols )
		aadd(aColsAB5,aClone(aAux))
	Next nCntFor
EndIf
aCols	:= aClone(aColsAB5[nItAcols])

//Ajusta o subitem para o novo tamanho
If nTamSub <> 2 .And. lAltera
	For nCntFor := 1 To Len(aCols)
		If Len(Alltrim(acols[nCntFor][nPosItem])) == 2
			aCols[nCntFor][nPosItem] := StrZero(0,TamSX3("AB5_SUBITE")[1]-2) + Alltrim(acols[nCntFor][nPosItem])
			aadd(aRecAB5It,acols[nCntFor][nPRecn])
		EndIf 
	Next nCntFor
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Registros                                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1")
dbSetOrder(1)
DbSeek(xFilial("SA1")+M->AB3_CODCLI+M->AB3_LOJA)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³ 
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,M->AB3_MOEDA) 

If Type("lAt400Auto")=="U" .OR. !lAt400Auto
	bSetKey  := SetKey(VK_F4,)
	SetKey(VK_F4,Nil)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Salva o aCols   de Entrada e Desabilita o Paint da Primeira GetDados    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oGetD:oBrowse:lDisablePaint := .T.

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta a GetDados                                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE cPrompt From 05,00 to 30,80
	@ 040,04 SAY RetTitle("AB3_CODCLI")+" "+M->AB3_CODCLI+"-"+M->AB3_LOJA+"/"+SA1->A1_NOME PIXEL
	@ 050,04 SAY RetTitle("AB4_CODPRO")+" "+cProduto+RetTitle("AB4_NUMSER")+" "+cNumSer PIXEL  
	oGetF4 := MsGetDados():New(69,4,155,313,nOpcF4,"At400F4LOk","AllwaysTrue","+AB5_SUBITE",.T.,NIL,NIL,NIL,NUMITENS)
	@ 158,005 SAY STR0008 SIZE 60,09 OF oDlg PIXEL //"Total: "
	@ 158,065 SAY oSay1 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL
	@ 158,105 SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Cliente: "
	@ 158,165 SAY oSay2 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL 
	@ 158,205 SAY STR0010 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ 158,265 SAY oSay3 PROMPT 0 PICTURE cPictureTot SIZE 40,09 OF oDlg PIXEL 
	oDlg:Cargo  := {|n1,n2,n3| oSay1:SetText(n1),;
								oSay2:SetText(n2),;
								oSay3:SetText(n3) }
	At400Total(oDlg,.T.)
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,if(oGetF4:TudoOk(),oDlg:End(),nOpca:=0)      },{||oDlg:End()},,aButtons)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Volta a situacao anterior                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
	SetKey(VK_F4,bSetKey)  

	If ( nOpcA == 1 ) .AND. ( lAltera .OR. lInclui ) 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada apos a confirmacao do apontamento                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock( "AT400APN" ) 
			ExecBlock( "AT400APN", .F., .F. ) 
		EndIf 			
		aColsAB5[nItAcols] := aClone(aCols)
		At400Total(oGetD:oWnd,.F.)
	EndIf
	oGetD:oBrowse:lDisablePaint := .F.
Else
	If MsGetDAuto(aAutoApont,"AT400F4LOk",{|| .T. },aAutoCab,aRotina[nOpcF4][4])
		aColsAB5[nItAcols] := aClone(aCols)
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Ponto de entrada apos a confirmacao do apontamento                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistBlock( "AT400APN" ) 
			ExecBlock( "AT400APN", .F., .F. ) 
		EndIf 			
	Else
		lRetorno := .F.
	EndIf
EndIf

aHeader 	:= aClone(aHeaderAB4)
aCols		:= aClone(aSavACols) 
aColsMain:= NIL 
N			:= nSavN

RestArea( aArea )

Return lRetorno


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400F4LOk  ³ Autor ³Eduardo Riera        ³ Data ³ 30.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Validacao da Linha na GetDados 2                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical AT400F4LOk(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT400F4LOk(oGet)

Local lRetorno := .T.
Local nUsado   := Len(aHeaderAB5)
Local nPosPrd  := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODPRO"})
Local nPosTipo := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODSER"})
Local nPosQtd  := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_QUANT"})
Local nPosUnit := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_VUNIT"})
Local nPosVlr  := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_TOTAL"})
Local nPosLista:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_PRCLIS"})
Local nPosVlDes:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_VLDESC"})	// Posição no aHeaderAB5 do campo Valor do Desconto Aplicado.

If ( !aCols[n][nUsado+1] )
	If ( 	Empty(aCols[n][nPosPrd]) .OR.;
			Empty(aCols[n][nPosTipo]) .OR.;
			Empty(aCols[n][nPosQtd]) .OR.;
			Empty(aCols[n][nPosUnit]) .OR.;
			Empty(aCols[n][nPosLista]) .OR.;
         Empty(aCols[n][nPosVlr]) ) ;
			.AND. ( Len(aCols) > 1 .OR. !Empty(aCols[n][nPosPrd]))
		lRetorno := .F.
		Help(" ",1,"AT400F4L01")
	EndIf
	If ( NoRound(aCols[n][nPosQtd]*aCols[n][nPosUnit]) <> NoRound(aCols[n][nPosVlr]) )
		Help(" ",1,"AT400F4L02")
		lRetorno := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Valida se o valor do Desconto eh MAIOR que o valor ³
	//³Total do item.                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nPosVlDes > 0
		If ( aCols[n][nPosVlDes] > aCols[n][nPosVlr] )
			Help(NIL, NIL, STR0044, NIL, STR0045, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0046})
			lRetorno := .F.
		EndIf
	EndIf
EndIf
If Type("lAt400Auto")=="U" .OR. !lAt400Auto
	At400Total(oGet:oWnd,.T.)
EndIf
Return(lRetorno)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
Funcao    ³at400Eqpto³ Autor ³ Eduardo Riera         ³ Data ³ 29.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Valida a amarracao Produto X Equipamento                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void at400Eqpto()                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function at400Eqpto()

Local lRetorno		:= .F.
Local cCampo		:= ReadVar()
Local uConteudo   := &(ReadVar())
Local nPosProd    := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_CODPRO" })
Local aArea       := { Alias() , IndexOrd() , RecNo() }

Do Case
	Case ( "AB4_CODPRO"$cCampo )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AA3")+M->AB3_CODCLI+M->AB3_LOJA+uConteudo) )
			lRetorno := .T.
		Else
			Help(" ",1,"REGNOIS")
		EndIf
	Case ( "AB4_NUMSER"$cCampo )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( DbSeek(xFilial("AA3")+M->AB3_CODCLI+M->AB3_LOJA+aCols[n][nPosProd]+uConteudo) )
			lRetorno := .T.
		Else
			Help(" ",1,"REGNOIS")
		EndIf
EndCase

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])
Return(lRetorno)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
Funcao	 ³At400Bar  ³ Autor ³ Eduardo Riera         ³ Data ³ 21.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Mostra a EnchoiceBar na tela						       	  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso		 ³ TECA400  										          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Conrado Q.    ³23/10/06³111294³Quando for visualizacao do system tracke³±± 
±±³              ³        ³      ³nao exibe o botao "System Tracker"      ³±± 
±±ºHanna C.      |30/03/07|9.12  ³Bops 118469 -Alterado o nome dos Bitmaps³±±
±±º        	     ³        |      ³definidos pela Engenharia p/ Protheus 10³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static ;
Function At400Bar(oDlg,bOk,bCancel,nOpc,oGetD)
Local aButtons   := {}               
Local aUsButtons := {} 
Local lTECA400 := AtIsRotina("ATC010CON").OR.AtIsRotina("ATC020CON")

If ( nOpc <> 5 )
	Aadd(aButtons,{"PRODUTO"  ,{|| At400F4(oGetD)}, STR0021, STR0027})  //"Apontamento..."
EndIf            

If nOpc == 2 .AND. !AtIsRotina("AT400TRACK") 	
	Aadd(aButtons,{"bmpord1",{|| At400Track()}, STR0026, STR0028 }) // "System Tracker"
EndIf 	

If ( nOpc <> 3 ) .AND. !lTeca400
	Aadd(aButtons,{"IMPRESSAO",{|| TECR400(M->AB3_NUMORC)}, STR0022, STR0029})	//"Impressao do Orçamento Gravado"
EndIf
         
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistBlock( "AT400BUT" ) 
	aUsButtons := ExecBlock( "AT400BUT", .F., .F. )  
	AEval( aUsButtons, { |x| AAdd( aButtons, x ) } ) 	 	
EndIf 	

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))
 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400Tab    ³ Autor ³Sergio Silveira      ³ Data ³ 26/06/01 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Funcao de Calculo do Preco de Tabela                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpN1 := AT400Tab(ExpC1,ExpC2,ExpN2,ExpC3,ExpC4)           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 -> Produto                                           ³±± 
±±³          ³ ExpC2 -> Tabela de preco                                   ³±±
±±³          ³ ExpN2 -> Quantidade                                        ³±±
±±³          ³ ExpC3 -> Cliente                                           ³±±
±±³          ³ ExpC4 -> Loja do Cliente                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpN1 := Preco de tabela                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/ 

Function AT400Tab(cProduto,cTabprec,nQtde,cCliente,cLoja,nMoeda)

Local aArea   	:= GetArea() 
Local nPrcVen 	:= 0
Local nValDesc  := 0 

dbSelectArea("SB1")
dbSetOrder(1)
If ( !Empty(cProduto) .AND. DbSeek(xFilial("SB1")+cProduto,.F.) )
	nPrcVen := MaTabPrVen(cTabPrec,cProduto,nQtde,cCliente,cLoja,nMoeda)
EndIf

nValDesc := At400Desc()
If nValDesc > 0 .And. Empty(cTabPrec)
	nPrcVen := nValDesc
EndIf 

RestArea( aArea ) 

Return (nPrcVen)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400RDesc³ Autor ³ Eduardo Riera         ³ Data ³ 10.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Recalcula os descontos em cascata                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .t.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At400RDesc()

Local nCtnFor  := 0
Local nCntFor2 := 0
Local nPPrcLis := aScan(aHeaderAb5,{|x| AllTrim(x[2])=="AB5_PRCLIS"})
Local nPVunit  := aScan(aHeaderAb5,{|x| AllTrim(x[2])=="AB5_VUNIT"})
Local nPTotal  := aScan(aHeaderAb5,{|x| AllTrim(x[2])=="AB5_TOTAL"})
Local nPQuant  := aScan(aHeaderAb5,{|x| AllTrim(x[2])=="AB5_QUANT"})
Local nPrcVen  := 0
Local nCntFor  := 0

For nCntFor := 1 To Len(aColsAB5)
	For nCntFor2 := 1 To Len(aColsAB5[nCntFor])
		If ( M->AB3_DESC1 >= 0 .OR.;
				M->AB3_DESC2 >= 0 .OR.;
				M->AB3_DESC3 >= 0 .OR.;
				M->AB3_DESC4 >= 0 )
			nPrcVen := aColsAB5[nCntFor][nCntFor2][nPPrcLis]
		EndIf
		If ( M->AB3_DESC1 >= 0 )
			nPrcVen *= (( 100 - M->AB3_DESC1 )/100)
		EndIf
		If ( M->AB3_DESC2 >= 0 )
			nPrcVen *= (( 100 - M->AB3_DESC2 )/100)
		EndIf
		If ( M->AB3_DESC3 >= 0 )
			nPrcVen *= (( 100 - M->AB3_DESC3 )/100)
		EndIf
		If ( M->AB3_DESC4 >= 0 )
			nPrcVen *= (( 100 - M->AB3_DESC4 )/100)
		EndIf
		nPrcVen := NoRound(nPrcVen,TamSx3("AB5_VUNIT")[2])
		If ( nPrcVen <> 0 )
			aColsAB5[nCntFor][nCntFor2][nPVunit] := nPrcVen
			aColsAB5[nCntFor][nCntFor2][nPTotal] := aColsAB5[nCntFor][nCntFor2][nPQuant]*aColsAB5[nCntFor][nCntFor2][nPVunit]
		EndIf
	Next nCntFor2
Next nCntFor2
Return(.T.)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³at400Desc ³ Autor ³ Eduardo Riera         ³ Data ³ 10.07.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Calcula o Preco com Desconto                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Preco com Desconto em cascata                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function At400Desc()

Local nPPrcLis 	:= aScan(aHeader,{|x| AllTrim(x[2])=="AB5_PRCLIS"})
Local nPPrcVen    := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_VUNIT"})
Local nPrcVen		:= 0

nPrcVen := aCols[n][nPPrcLis]
If ( M->AB3_DESC1 >= 0 )
	nPrcVen *= (( 100 - M->AB3_DESC1 )/100)
EndIf
If ( M->AB3_DESC2 >= 0 )
	nPrcVen *= (( 100 - M->AB3_DESC2 )/100)
EndIf
If ( M->AB3_DESC3 >= 0 )
	nPrcVen *= (( 100 - M->AB3_DESC3 )/100)
EndIf
If ( M->AB3_DESC4 >= 0 )
	nPrcVen *= (( 100 - M->AB3_DESC4 )/100)
EndIf
nPrcVen := NoRound(nPrcVen,TamSx3("AB5_VUNIT")[2])
If ( nPrcVen == 0 )
	nPrcVen := aCols[n][nPPrcVen]
Endif
Return(nPrcVen)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Total³ Autor ³ Eduardo Riera         ³ Data ³ 07.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³Totalizacao da Janela                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³oDlg    : Objeto da Janela                                  ³±±
±±³          ³nItem   : Item da Getdados Principal                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function At400Total(oDlg,lF4)

Local nUsado	:= Len(aHeaderAB5)
Local nCntFor	:= 0
Local nCntFor2	:= 0
Local nPosProd 	:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODPRO"})
Local nPosTotal	:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_TOTAL"})
Local nPosSer	:= aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_CODSER"})
Local nPosDesc  := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_VLDESC"})
Local nTotGer	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0
If ( lF4 )
	For nCntFor := 1 To Len(aCols)
		If ( Len(aCols[nCntFor])==nUsado .OR. !aCols[nCntFor][nUsado+1] )
			nTotCli	+= AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosTotal],"C")
			nTotFab	+= AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosTotal],"F")
			nTotGer  += aCols[nCntFor][nPosTotal]
			//-> Aplicando desconto ao item, qdo informado.
			If nPosDesc > 0
			   nTotGer  -= aCols[nCntFor][nPosDesc]			
			EndIf
		EndIf
	Next nCntFor
Else
	For nCntFor := 1 To Len(aColsAB5)
		For nCntFor2 := 1 To Len(aColsAB5[nCntFor])
			If ( Len(aColsAB5[nCntFor][nCntFor2])==nUsado .OR. !aColsAB5[nCntFor][nCntFor2][nUsado+1] )
				nTotCli	+= AtVlrPagto(aColsAB5[nCntFor][nCntFor2][nPosSer],aColsAB5[nCntFor][nCntFor2][nPosTotal],"C")
				nTotFab	+= AtVlrPagto(aColsAB5[nCntFor][nCntFor2][nPosSer],aColsAB5[nCntFor][nCntFor2][nPosTotal],"F")
				nTotGer  += aColsAB5[nCntFor][nCntFor2][nPosTotal]
				//-> Aplicando desconto ao item, qdo informado.
				If nPosDesc > 0
                   nTotGer  -= aColsAB5[nCntFor][nCntFor2][nPosDesc]
				EndIf
			EndIf
		Next nCntFor2
	Next nCntFor
EndIf
Eval(oDlg:Cargo,nTotGer,nTotCli,nTotFab)
Return(.T.) 


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Track³ Autor ³ Sergio Silveira       ³ Data ³09/01/2002³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³10/09/2006³Conrado Q.     ³-BOPS: 111294: Salva e restaura as variaveis³±±
±±³          ³               ³estaticas utilizadas em Dialogos.           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function At400Track() 

Local aEnt     := {}								// Itens para rastreamento
Local cNumOrc  := M->AB3_NUMORC						// Numero do orcamento
Local nPosItem := GDFieldPos( "AB4_ITEM" ) 			// Posicao do item AB4_ITEM
Local nLoop    := 0 								// Utilizado no loop
Local oOldDlg        := oDlg						// Salva as variaveis estaticas
Local oOldSay1       := oSay1						// Salva as variaveis estaticas
Local oOldSay2       := oSay2                  		// Salva as variaveis estaticas
Local oOldSay3       := oSay3						// Salva as variaveis estaticas
Local cOldPictureTot := cPictureTot					// Salva as variaveis estaticas

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Carrega os itens para rastreamento          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nLoop := 1 To Len( aCols ) 
	AAdd( aEnt, { "AB4", cNumOrc + aCols[ nLoop, nPosItem ] } ) 
Next nLoop 

MaTrkShow( aEnt ) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura as variaveis estaticas, pois a rotina MaTrShow ³
//³reabre a janela TECA400 novamente, caso solicitado pelo ³
//³usuario.                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oDlg		:= oOldDlg
oSay1		:= oOldSay1
oSay2		:= oOldSay2
oSay3		:= oOldSay3
cPictureTot	:= cOldPictureTot
           
Return( .T. ) 
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³ At400Leg ³ Autor ³ Cleber Martinez       ³ Data ³12/04/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao ³ Exibe a legenda do Orcamento                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function At400Leg() 
Local aCores := {}				//Array com as cores da Legenda

aCores :=	{{"ENABLE",	STR0032},;  // Aberto
			{"DISABLE",	STR0033}}   // Encerrado

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores da legenda    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("AT400LGD")
	aCores := ExecBlock("AT400LGD",.F.,.F.,aCores)
	If ValType(aCores) <> "A"
		aCores :=	{{"ENABLE",	STR0032},;  	// Aberto
					{"DISABLE",	STR0033}}   	// Encerrado
	EndIf
EndIf

BrwLegenda( cCadastro, STR0031 , aCores ) 

Return( Nil ) 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Moeda³ Autor ³ Cleber Martinez       ³ Data ³19/05/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Verifica se a moeda do Orcamento esta cadastrada           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA400                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function At400MoedaOK()
Local lRet 		:= .T.					//Indica retorno da funcao
Local aAreaAB3	:= AB3->(GetArea()) 	//Guarda a area corrente

If (AB3->AB3_MOEDA <> 0)
	If AB3->AB3_TXMOED <= 0 
		If Recmoeda( dDatabase, AB3->AB3_MOEDA ) <= 0         
			//"Não existe taxa para a Moeda " #
		    //" informada neste Orçamento. Favor cadastrá-la antes de efetivá-lo."
		    //"Orçamento " ######
			MsgAlert(  STR0036 + StrZero(AB3->AB3_MOEDA,1) + STR0037 , ;
					 STR0038 + AB3->AB3_NUMORC )
			lRet := .F.
		EndIf
	EndIf
	
EndIf  

RestArea(aAreaAB3)

Return (lRet)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400Pict ³ Autor ³ Cleber Martinez       ³ Data ³ 05.06.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Atualiza picture dos valores do Rodape conforme Moeda      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At400Pict()

Local nMoeda := M->AB3_MOEDA			// Moeda informada no cabecalho

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cPictureTot	:= PesqPict("AB5","AB5_TOTAL",14,nMoeda) 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Efetua um refresh nos objetos c/ a nova Picture ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
oSay1:Refresh()
oSay2:Refresh()
oSay3:Refresh()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recalcula os totais do Rodape da tela  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
At400Total(oDlg, .F.)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AT400Impos³ Autor ³Alexandre Inacio Lemes ³ Data ³08/01/2014³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Dialog com os impostos gerados nos apontamentos d Orcamento³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AT400Impos()

Local aArea	   := GetArea()
Local aDadosCFO:= {}
Local cTes     := ""
Local cCFO     := ""
Local nPosItem := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_SUBITE"})
Local nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_CODPRO"})
Local nPosQtd  := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_QUANT" })
Local nPosTot  := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_TOTAL" })
Local nPosLis  := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_PRCLIS"})
Local nPosServ := aScan(aHeader,{|x| AllTrim(x[2])=="AB5_CODSER"})
Local nItem     := 0
Local nPrcLista := 0
Local nValMerc  := 0
Local nDesconto := 0
Local oDlg      := Nil
Local nTotal	  := 0
Local dIni		:= Stod("") 

Private _nTotOper_ := 0		//total de operacoes (vendas) realizadas com um cliente - calculo de IB - Argentina
Private _aValItem_ := {}

AA5->(dbSetOrder(1))
SB1->(dbSetOrder(1))
SF4->(dbSetOrder(1))
SA1->(dbSetOrder(1))

SA1->(MsSeek(xFilial("SA1")+M->AB3_CODCLI+M->AB3_LOJA))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Garante que os Arrays da Matxfis estao Limpos a cada chamada.³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MaFisSave()
MaFisEnd()  
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicia a carga da MATXFIS com os dados do Cabecalho³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aEval(aCols,{|x| nTotal += x[nPosTot]})
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicia a carga da MATXFIS com os dados do Cabecalho³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MaFisIni(SA1->A1_COD,SA1->A1_LOJA,"C","N",SA1->A1_TIPO,,,,,"MATA461",,,,,SA1->A1_RECISS,,,,,,,,,nTotal,,)

If cPaisLoc == 'ARG'
	MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Tratamento de IB para monotributistas - Argentina           ³
	//³ AGIP 177/2009                                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If SA1->A1_TIPO == "M"
		dIni := (dDatabase + 1) - 365
		_nTotOper_ := RetTotOper(SA1->A1_COD,SA1->A1_LOJA,"C",dIni,dDatabase,1)
	Endif 
Endif

If cPaisLoc<>"BRA"
	MaFisAlt('NF_MOEDA',M->AB3_MOEDA)
EndIf	

Aadd(aDadosCFO,{"OPERNF"  , "S"})
Aadd(aDadosCFO,{"TPCLIFOR", SA1->A1_TIPO})
Aadd(aDadosCFO,{"UFDEST"  , SA1->A1_EST})
Aadd(aDadosCFO,{"INSCR"   , SA1->A1_INSCR})
Aadd(aDadosCFO,{"CONTR"   , SA1->A1_CONTRIB})

For nItem := 1 To Len(aCols)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Obtem a TES e posiciona o cadastro de TES SF4³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AA5->(MsSeek(xFilial("AA5")+aCols[nItem][nPosServ]))
    
    If !Empty(AA5->AA5_TES)
		cTes := AA5->AA5_TES
    Else
		SB1->(MsSeek(xFilial("SB1")+aCols[nItem][nPosPrd]))
		cTes := SB1->B1_TS
    EndIf

	cCFO := ""    
    If !Empty(cTes)
		SF4->(MsSeek(xFilial("SF4")+cTes))
		cCFO := MaFisCfo(,SF4->F4_CF,aDadosCFO)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calcula se ha desconto a partir do preco de lista³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nValMerc  := aCols[nItem][nPosTot] 
	nPrcLista := aCols[nItem][nPosLis]
	nDesconto := ( a410Arred( nPrcLista * aCols[nItem][nPosQtd] , "D2_DESCON" ) - nValMerc )
	nDesconto := Max(0,nDesconto)
	//Para os outros paises, este tratamento e feito no programas que calculam os impostos.
	If cPaisLoc=="BRA" .or. GetNewPar('MV_DESCSAI','1') == "2"
		nValMerc  += nDesconto
	Endif
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Carrega os itens para a MATXFIS. O uso do MaFisAdd em detrimento ao uso do MaFisIniLoad foi adotado afim de ³
	//³manter compatibilidade com o Pedido Vendas que utiliza o MaFisAdd na ocasiao desta implementacao no TECA400.³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisAdd(aCols[nItem][nPosPrd],; // 1-Codigo do Produto ( Obrigatorio )
	cTes,;                            // 2-Codigo do TES ( Opcional )
	aCols[nItem][nPosQtd],;  	      // 3-Quantidade ( Obrigatorio )
	nPrcLista,;		  				  // 4-Preco Unitario ( Obrigatorio )
	nDesconto,; 		              // 5-Valor do Desconto ( Opcional )
	"",;	   			              // 6-Numero da NF Original ( Devolucao/Benef )
	"",;				              // 7-Serie da NF Original ( Devolucao/Benef )
	0 ,;					          // 8-RecNo da NF Original no arq SD1/SD2
	0 ,;					          // 9-Valor do Frete do Item ( Opcional )
	0 ,;					          // 10-Valor da Despesa do item ( Opcional )
	0 ,;					          // 11-Valor do Seguro do item ( Opcional )
	0 ,;					          // 12-Valor do Frete Autonomo ( Opcional )
	nValMerc,;			              // 13-Valor da Mercadoria ( Obrigatorio )
	0 ,;					          // 14-Valor da Embalagem ( Opiconal )
	0 ,;					          // 15-RecNo do SB1
	0 ,;					          // 16-RecNo do SF4
	Iif(nPosItem > 0 , aCols[nItem,nPosItem] , "" ),; //17-Item
	0 ,;					          // 18-Despesas nao tributadas - Portugal
	0 ,;					          // 19-Tara - Portugal
	cCFO,;                            // 20-CFO
	{},;            	              // 21-Array para o calculo do IVA Ajustado (opcional)
	"",;				              // 22-Codigo Retencao - Equador
	0 ,;                              // 23-Valor Abatimento ISS
	"",;                              // 24-Lote Produto
	"",;                              // 25-Sub-Lote Produto
	0 ,;                              // 26-Valor do Abatimento ISS
	"",;                              // 27-Codigo ISS
	"")                               // 28-Classificação fiscal
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Calculo do ISS conforme SA1->A1_INCISS       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( !Empty(cTes) .And. SA1->A1_INCISS == "N" .And. SF4->F4_ISS == "S" )
		nPrcLista := a410Arred(nPrcLista/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
		nValMerc  := a410Arred(nValMerc/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
		MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
		MaFisAlt("IT_VALMERC",nValMerc,nItem)
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Marca no MATXFIS se o Item estiver DELETADO  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If aCols[nItem][Len(aHeader)+1]
		MaFisDel(nItem,aCols[nItem][Len(aHeader)+1])
	EndIf
	
Next nItem

MaFisWrite(1)

DEFINE MSDIALOG oDlg TITLE STR0040 FROM 009,000 TO 028,080 

MaFisRodape(1,oDlg,,{005,005,308,080},Nil,.T.)

@ 095,005 SAY   RetTitle("F2_FRETE")    SIZE 040,010 PIXEL OF oDlg  
@ 095,112 SAY   RetTitle("F2_SEGURO")   SIZE 040,010 PIXEL OF oDlg  
@ 095,218 SAY   RetTitle("F2_DESCONT")  SIZE 040,010 PIXEL OF oDlg  
@ 110,005 SAY   RetTitle("F2_FRETAUT")  SIZE 040,010 PIXEL OF oDlg  
@ 110,112 SAY   RetTitle("F2_DESPESA")  SIZE 040,010 PIXEL OF oDlg  
@ 110,218 SAY   RetTitle("F2_VALFAT")   SIZE 040,010 PIXEL OF oDlg  
@ 125,005 SAY   STR0041                 SIZE 040,010 PIXEL OF oDlg   //"Total da Nota"
@ 095,050 MSGET MaFisRet(,"NF_FRETE")   PICTURE PesqPict("SF2","F2_FRETE"   ,16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 095,157 MSGET MaFisRet(,"NF_SEGURO")  PICTURE PesqPict("SF2","F2_SEGURO"  ,16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 095,263 MSGET MaFisRet(,"NF_DESCONTO")PICTURE PesqPict("SF2","F2_DESCONTO",16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 110,050 MSGET MaFisRet(,"NF_AUTONOMO")PICTURE PesqPict("SF2","F2_FRETAUT" ,16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 110,157 MSGET MaFisRet(,"NF_DESPESA") PICTURE PesqPict("SF2","F2_DESPESA" ,16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 110,263 MSGET MaFisRet(,"NF_BASEDUP") PICTURE PesqPict("SF2","F2_VALFAT"  ,16,2) SIZE 050,007 PIXEL WHEN .F. OF oDlg  
@ 125,050 MSGET MaFisRet(,"NF_TOTAL")   PICTURE Iif(cPaisLoc=="CHI",TM(0,16,NIL),PesqPict("SF2","F2_VALBRUT",16,2)) SIZE 050,007 PIXEL WHEN .F. OF oDlg
@ 128,270 BUTTON STR0042 SIZE 042,11 FONT oDlg:oFont ACTION oDlg:End() OF oDlg PIXEL // Botao Fechar	

ACTIVATE MSDIALOG oDlg CENTERED ON INIT CursorArrow()

MaFisEnd()

RestArea(aArea)

Return


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³At400GrVld ³ Autor ³fabiana.silva          ³ Data 30/10/2018³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descricao³ Verifica se a linha do orçamento pode ser alterada         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function At400GrVld()

Local lRetorno 	:= .T. //Retorno do valid do campo
Local nPosItem  := aScan(aHeader,{|x| AllTrim(x[2])=="AB4_ITEM"}) //codigo do item
Local aAreaAB7 	:= {} //WorkArea AB7
Local aAreaAB4	:= {} //WorkArea AB7
Local nUsado   	:= Len(aHeader) //Campo deletado

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Aqui eh verificado se o item esta amarrado a alguma OS que foi baixada.³
//³ Quando isto acontece a alteracao nao eh permitida.                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAreaAB4 := AB4->(GetArea())
AB4->(DbSetOrder(1))
If (ALTERA .OR. (!ALTERA .and. !INCLUI)) .AND.   !aCols[n][nUsado+1]  .AND. ;
    ( n > 0 .AND.  n <= Len(aCols)  )  .AND. ;
     AB4->(DbSeek(xFilial("AB4")+M->AB3_NUMORC+aCols[n][nPosItem])) .AND. !Empty(AB4->AB4_NUMOS) 
	
	aAreaAB7 := AB7->(GetArea())
	AB7->(DbSetOrder(1))
	If  AB7->(DbSeek(xFilial("AB7")+AB4->AB4_NUMOS) ) .AND. AB7->AB7_TIPO <>"1"
		Help(" ",1,"At400GrVld",,STR0043,1,1) //"Não é permitido alterar ou excluir um Item do Orçamento que possui uma Ordem de Serviço vinculada com Situação diferente de 1"
		lRetorno := .F.
   EndIf

   RestArea(aAreaAB7)
EndIf
RestArea(aAreaAB4)

Return lRetorno

//------------------------------------------------------------------------------
/*/{Protheus.doc} At400Ajust

Deletar os registros que foram gravados com o subitem com tamanho antigo

@author     Luiz Gabriel
@since      06/01/2022
/*/
//------------------------------------------------------------------------------
Static Function At400Ajust(aRecAB5It)
Local aAreaAB5	:= AB5->(GetArea())
Local nRecAB5	:= 0

AB5->(DbSetOrder(1))
For nRecAB5 := 1 To Len(aRecAB5It)
	If aRecAB5It[nRecAB5] > 0
		AB5->( DbGoTo( aRecAB5It[nRecAB5] ))
		AB5->(RecLock("AB5",.F.))
			AB5->(DbDelete()) 
		AB5->(MsUnlock())
	EndIf	
Next nRecAB5

RestArea(aAreaAB5)
Return

//------------------------------------------------------------------------------
/*/{Protheus.doc} At400VerBl
	(long_description)
	@type  Function
	@author Natacha Romeiro
	@since 28/09/2022
	@version version
	Consulta para verificar se equipamento possui registro de bloqueado para uso
	/*/
//------------------------------------------------------------------------------	
Static Function At400VerBl(cCdPro, cIdUnic, cCdCli, cLoja)

Local cAliasAA3 := GetNextAlias()
Local lRet      := .T.

BeginSql Alias cAliasAA3    
   SELECT AA3.AA3_MSBLQL
     FROM %Table:AA3% AA3 
    WHERE AA3.AA3_FILIAL = %xFilial:AA3%
      AND AA3.%NotDel%
      AND AA3.AA3_NUMSER = %Exp:cIdUnic%
      AND AA3.AA3_CODPRO = %Exp:cCdPro%   
      AND AA3.AA3_CODCLI = %Exp:cCdCli%
      AND AA3.AA3_LOJA   = %Exp:cLoja%      
EndSql

if !(cAliasAA3)->(EOF()) 
    If (cAliasAA3)->AA3_MSBLQL == "1"
        lRet := .F.
    EndIf       
endif

(cAliasAA3)->(DbCloseArea())

Return lRet
