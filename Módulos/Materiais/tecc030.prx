#INCLUDE "DBTREE.CH"
#INCLUDE "FIVEWIN.CH"                      
#INCLUDE "TECC030.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECC030   ³ Autor ³ Eduardo Riera         ³ Data ³ 25.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao FAQ                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function TECC030()

Private cCadastro := STR0001	//"FAQ - Frequently Asked Question"

Private aRotina := MenuDef()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endereca a funcao de BROWSE                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("AAG")
dbSetOrder(1)
mBrowse( 6, 1,22,75,"AAG")

dbSelectArea("AAG")
dbSetOrder(1)
Return(Nil)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ MenuDef  ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 12.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECC030                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Define Array contendo as Rotinas a executar do programa      ³
	//³ ----------- Elementos contidos por dimensao ------------     ³
	//³ 1. Nome a aparecer no cabecalho                              ³
	//³ 2. Nome da Rotina associada                                  ³
	//³ 3. Usado pela rotina                                         ³
	//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
	//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
	//³    2 - Simplesmente Mostra os Campos                         ³
	//³    3 - Inclui registros no Bancos de Dados                   ³
	//³    4 - Altera o registro corrente                            ³
	//³    5 - Remove o registro corrente do Banco de Dados          ³
	//³    6 - Alteracao sem inclusao de registro                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local aRotina := {	{ STR0002,"AxPesqui" 	,0	,1	,0	,.F.	}	,;	//"Pesquisar"
						{ STR0003,"Atc030Con"	,0	,2	,0	,.T.	}	}	//"Consulta"
Return(aRotina)


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc030Con ³ Autor ³ Eduardo Riera         ³ Data ³ 25.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao FAQ                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Atc030Con(cAlias,nReg,nOpc)

Local cCodPrbI := AAG->AAG_CODPRB
Local cCodPrbF := AAG->AAG_CODPRB
Local cCodProI := ""
// Local cCodProF := Repl(Chr(255),Len(SB1->B1_COD))
Local cCodProF := Replicate( "z",Len(SB1->B1_COD)) // Provisorio, aguardar correcao Protheus 

Atc030FAQ(cCodPrbI,cCodPrbF,cCodProI,cCodProF)

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc030FAQ ³ Autor ³ Eduardo Riera         ³ Data ³ 25.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao FAQ                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Codigo do Problema Inicial                         ³±±
±±³          ³ ExpC2 : Codigo do Problema Final                           ³±±
±±³          ³ ExpC3 : Codigo do Produto caso for vazio sera assumido     ³±±
±±³          ³         todos os produtos.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 01/03/07 ³ Conrado Q.    ³ Otimizada a consulta através de Querys.    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function Atc030FAQ(cCodPrbI,cCodPrbF,cCodPro,cCodProF)

Local aArea		:= GetArea()
Local aAreaSB1	:= SB1->(GetArea())
Local oDlg		:= Nil
Local oTree		:= Nil
Local oMemo		:= Nil
Local nCntFor 	:= 0
Local nTam  	:= Len(AAI->AAI_SOLUC)
Local cGrupo	:= ""
Local cProduto	:= ""
Local cQuebra	:= ""
Local cMemo		:= ""
#IFDEF TOP
	Local cQuery	:= ""
	Local cAliasAAG	:= "TECC030QRYAAG"
	Local cAliasAAI	:= "TECC030QRYAAI"
	Local aStruAAG	:= {}
	Local aStruAAI	:= {}
	Local nX		:= 0
#ENDIF

cCodProF := If( ValType( cCodProF ) == "C", cCodProF, cCodPro ) 

DbSelectArea("AAI")

DEFINE MSDIALOG oDlg FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd

oTree := DbTree():New(3, 3, 97, 314, oDlg,,,.T.)
oTree:bChange := {|| Atc030Memo(oTree,oMemo,@cMemo) }
@ 100,003 SAY AAI->(RetTitle( "AAI_MEMO2" )) of oDlg PIXEL 
@ 110,003 GET oMemo VAR cMemo SIZE 273,030 PIXEL READONLY MEMO

	
DEFINE SBUTTON FROM 125,285 TYPE 1 ENABLE OF oDlg ACTION ( oDlg:End() )
	
#IFDEF TOP

	DbSelectArea("AAG")
	
	aStruAAG  := AAG->(dbStruct())
	
	cQuery := "SELECT AAG_FILIAL	, AAG_CODPRB	,AAG_DESCRI, R_E_C_N_O_ RECAAG "
	cQuery += " FROM "+RetSqlName("AAG")+" AAG "
	cQuery += "WHERE "
	cQuery += "AAG_FILIAL = '"+xFilial("AAG")+"' AND "
	cQuery += "AAG_CODPRB >= '"+cCodPrbI+"'	AND "
	cQuery += "AAG_CODPRB <= '"+cCodPrbF+"'	AND "
	cQuery += "AAG.D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY "+SqlOrder(AAG->(IndexKey()))					
	cQuery := ChangeQuery(cQuery)
	
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAG,.T.,.T.)
	
	For nX := 1 To Len(aStruAAG)
		If aStruAAG[nX,2] <> "C"
			TcSetField(cAliasAAG,aStruAAG[nX,1],aStruAAG[nX,2],aStruAAG[nX,3],aStruAAG[nX,4])
		EndIf						
	
	Next nX
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Pega a estrutura do AAI³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DbSelectArea("AAI")
	aStruAAI  := AAI->(dbStruct())
		
	dbSelectArea(cAliasAAG)
	If !(cAliasAAG)->(Eof())
		While !(cAliasAAG)->(Eof())
		DBADDTREE oTree PROMPT (cAliasAAG)->AAG_CODPRB+"-"+(cAliasAAG)->AAG_DESCRI+Space(nTam) RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Genericos                                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cGrupo := STR0004	//"Grupo: "
			cGrupo += STR0005	//"Gen‚rico "
			cGrupo += Space(nTam)
			DBADDTREE oTree PROMPT cGrupo RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)			
			
				cQuery := "SELECT	AAI_FILIAL	, AAI_CODPRB	, AAI_GRUPO	, AAI_CODPRO	, "
				cQuery += "			AAI_SOLUC	, R_E_C_N_O_ RECAAI "
				cQuery += " FROM "+RetSqlName("AAI")+" AAI "
				cQuery += "	WHERE "
				cQuery += "		AAI_FILIAL = '"+xFilial("AAI")+"' AND "
				cQuery += "		AAI_CODPRB = '"+(cAliasAAG)->AAG_CODPRB+"' AND "
				cQuery += "		((AAI_GRUPO IS NULL OR LTRIM(AAI_GRUPO) = '') AND "
				cQuery += "		(AAI_CODPRO IS NOT NULL OR LTRIM(AAI_CODPRO) != '')) AND "
				cQuery += "		((AAI_GRUPO IS NOT NULL OR LTRIM(AAI_GRUPO) != '') AND "
				cQuery += "		(AAI_CODPRO IS NULL OR LTRIM(AAI_CODPRO) = '')) AND "				
				cQuery += "		AAI.D_E_L_E_T_ = ' ' "
				cQuery += "	ORDER BY "+SqlOrder(AAI->(IndexKey(2)))					
				cQuery := ChangeQuery(cQuery)
				
				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAI,.T.,.T.)
				
				For nX := 1 To Len(aStruAAI)
					If aStruAAI[nX,2] <> "C"
						TcSetField(cAliasAAI,aStruAAI[nX,1],aStruAAI[nX,2],aStruAAI[nX,3],aStruAAI[nX,4])
					EndIf									
				Next nX
	
				dbSelectArea(cAliasAAI)
				If !(cAliasAAI)->(Eof())
					While !(cAliasAAI)->(Eof())
						DBADDITEM oTree PROMPT (cAliasAAI)->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero((cAliasAAI)->RECAAI,12)
						(cAliasAAI)->(DbSkip())
					EndDo
				EndIf
			DBENDTREE oTree
			
			(cAliasAAI)->(DbCloseArea()	)		
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Grupos                                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuery := "SELECT	AAI_FILIAL	, AAI_CODPRB	, AAI_GRUPO	, AAI_CODPRO	, "
			cQuery += "			AAI_SOLUC	, AAI.R_E_C_N_O_ RECAAI, BM_DESC, B1_COD, B1_DESC "
			cQuery += " FROM "+RetSqlName("AAI")+" AAI "
			cQuery += "	LEFT JOIN "+RetSqlName("SBM")+" SBM "
			cQuery += "		ON  SBM.BM_FILIAL = '" + xFilial("SBM") + "' AND "
			cQuery += "		SBM.BM_GRUPO = AAI_GRUPO AND "	
			cQuery += "		SBM.D_E_L_E_T_ = ' ' "				
			cQuery += "	LEFT JOIN "+RetSqlName("SB1")+" SB1 "
			cQuery += "		ON  SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
			cQuery += "		SB1.B1_COD = AAI_CODPRO AND "	
			cQuery += "		SB1.D_E_L_E_T_ = ' ' "				
			cQuery += "	WHERE "		
			cQuery += "		AAI_FILIAL = '"+xFilial("AAI")+"' AND "
			cQuery += "		AAI_CODPRB = '"+(cAliasAAG)->AAG_CODPRB+"' AND "
			cQuery += "		(AAI_GRUPO IS NOT NULL AND LTRIM(AAI_GRUPO) <> '') AND "
			cQuery += "		AAI.D_E_L_E_T_ = ' ' "
			cQuery += "	ORDER BY "+SqlOrder(AAI->(IndexKey(2)))
			cQuery := ChangeQuery(cQuery)
			
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAI,.T.,.T.)
			
			For nX := 1 To Len(aStruAAI)
				If aStruAAI[nX,2] <> "C"
					TcSetField(cAliasAAI,aStruAAI[nX,1],aStruAAI[nX,2],aStruAAI[nX,3],aStruAAI[nX,4])
				EndIf								
			Next nX
			
			cQuebra := ""
			If !(cAliasAAI)->(Eof())
				While !(cAliasAAI)->(Eof())					
					If (( (cAliasAAI)->B1_COD == cCodPro .Or.;
							Empty(cCodPro) ) .And. Empty((cAliasAAI)->AAI_CODPRO))
						If ( cQuebra <> (cAliasAAI)->AAI_GRUPO )
							If ( !Empty(cQuebra) )
								DBENDTREE oTree
							EndIf
							cGrupo := STR0006	//"Grupo: "	
							cGrupo += (cAliasAAI)->BM_DESC
							cGrupo += Space(nTam)
							DBADDTREE oTree PROMPT cGrupo RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
							cQuebra := (cAliasAAI)->AAI_GRUPO
						EndIf
						DBADDITEM oTree PROMPT (cAliasAAI)->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero((cAliasAAI)->RECAAI,12)
					EndIf
				
					(cAliasAAI)->(DbSkip())
				EndDo
			EndIf
			If ( !Empty(cQuebra) )
				DBENDTREE oTree
			EndIf
			
			(cAliasAAI)->(DbCloseArea()	)
					
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Produtos                                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
			cQuery := "SELECT	AAI_FILIAL	, AAI_CODPRB			, AAI_GRUPO	, AAI_CODPRO	, "
			cQuery += "			AAI_SOLUC	, AAI.R_E_C_N_O_ RECAAI	, B1_DESC	, B1_COD "
			cQuery += " FROM "+RetSqlName("AAI")+" AAI "
			cQuery += "	JOIN "+RetSqlName("SB1")+" SB1 "
			cQuery += "		ON  SB1.B1_FILIAL = '" + xFilial("SB1") + "' AND "
			cQuery += "		SB1.B1_COD = AAI_CODPRO AND "	
			cQuery += "		SB1.D_E_L_E_T_ = ' ' "		
			cQuery += "	WHERE "
			cQuery += "		AAI_FILIAL = '"+xFilial("AAI")+"' AND "
			cQuery += "		AAI_CODPRB >= '"+(cAliasAAG)->AAG_CODPRB+"' AND "
			cQuery += "		AAI_CODPRB <= '"+cCodPrbF+"' AND "
			cQuery += "		AAI_CODPRO >= '"+cCodPro+"' AND "
			cQuery += "		AAI_CODPRO <= '"+cCodProF+"' AND "
			cQuery += "		(AAI_CODPRO IS NOT NULL AND LTRIM(AAI_CODPRO) <> '') AND "
			cQuery += "		AAI.D_E_L_E_T_ = ' ' "
			cQuery += "	ORDER BY AAI_CODPRO"
			cQuery := ChangeQuery(cQuery)
	
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasAAI,.T.,.T.)
			
			For nX := 1 To Len(aStruAAI)
				If aStruAAI[nX,2] <> "C"
					TcSetField(cAliasAAI,aStruAAI[nX,1],aStruAAI[nX,2],aStruAAI[nX,3],aStruAAI[nX,4])
				EndIf								
			Next nX		
			
			cQuebra := ""
			If !(cAliasAAI)->(Eof())
				While !(cAliasAAI)->(Eof())				
					If ( cQuebra <> (cAliasAAI)->AAI_CODPRO )
						If ( !Empty(cQuebra) )
							DBENDTREE oTree
						EndIf
						cProduto := STR0007 //"Produto: "
						cProduto += (cAliasAAI)->B1_COD+" - "+(cAliasAAI)->B1_DESC
						cProduto += Space(nTam)
						DBADDTREE oTree PROMPT cProduto RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
						cQuebra := (cAliasAAI)->AAI_CODPRO
					EndIf
					DBADDITEM oTree PROMPT (cAliasAAI)->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero((cAliasAAI)->RECAAI,12)
					(cAliasAAI)->(DbSkip())
				EndDo
			EndIf
			If ( !Empty(cQuebra) )
				DBENDTREE oTree
			EndIf
			DBENDTREE oTree
	
			(cAliasAAI)->(DbCloseArea())
			(cAliasAAG)->(DbSkip())
		EndDo
	EndIf
	
	(cAliasAAG)->(DbCloseArea())
#ELSE
	dbSelectArea("AAG")
	dbSetOrder(1)
	dbSeek(xFilial("AAG")+cCodPrbI,.T.)
	While ( !Eof() .And. xFilial("AAG")	==	AAG->AAG_FILIAL .And.;
								AAG->AAG_CODPRB >= cCodPrbI .And.;
								AAG->AAG_CODPRB <= cCodPrbF )
		DBADDTREE oTree PROMPT AAG->AAG_CODPRB+"-"+AAG->AAG_DESCRI+Space(nTam) RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Genericos                                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cGrupo := STR0004	//"Grupo: "
			cGrupo += STR0005	//"Gen‚rico "
			cGrupo += Space(nTam)
			DBADDTREE oTree PROMPT cGrupo RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
				dbSelectArea("AAI")
				dbSetOrder(2)
				dbSeek(xFilial("AAI")+AAG->AAG_CODPRB,.T.)
				While ( !Eof() .And. xFilial("AAI") == AAI->AAI_FILIAL .And.;
						AAI->AAI_CODPRB <= AAG->AAG_CODPRB .And.;
						Empty(AAI->AAI_GRUPO) .And. Empty(AAI->AAI_CODPRO) )
					DBADDITEM oTree PROMPT AAI->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero(AAI->(RecNo()),12)
					dbSelectArea("AAI")
					dbSkip()
				EndDo
			DBENDTREE oTree
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Grupos                                                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuebra := ""
			While ( !Eof() .And. xFilial("AAI") == AAI->AAI_FILIAL .And.;
					AAI->AAI_CODPRB <= AAG->AAG_CODPRB )
				dbSelectArea("SB1")
				dbSetOrder(4)
				dbSeek(xFilial("SB1")+AAI->AAI_GRUPO+AllTrim(cCodPro))
				
				If (( SB1->B1_COD == cCodPro .Or.;
						Empty(cCodPro) ) .And. Empty(AAI->AAI_CODPRO))
					If ( cQuebra <> AAI->AAI_GRUPO )
						If ( !Empty(cQuebra) )
							DBENDTREE oTree
						EndIf
						cGrupo := STR0006	//"Grupo: "
	
						cGrupo += Posicione("SBM",1,xFilial("SBM")+AAI->AAI_GRUPO,"BM_DESC")  
						cGrupo += Space(nTam)
						DBADDTREE oTree PROMPT cGrupo RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
						cQuebra := AAI->AAI_GRUPO
					EndIf
					DBADDITEM oTree PROMPT AAI->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero(AAI->(RecNo()),12)
				EndIf
				
				dbSelectArea("AAI")
				dbSkip()
			EndDo
			If ( !Empty(cQuebra) )
				DBENDTREE oTree
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Monta os Produtos                                                       ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cQuebra := ""
			dbSelectArea("AAI")
			dbSetOrder(3)
			dbSeek(xFilial("AAI")+AAG->AAG_CODPRB+cCodPro,.T.)
			While ( !Eof() .And.	xFilial("AAI") == AAI->AAI_FILIAL .And.;
									AAI->AAI_CODPRB <= cCodPrbF 			)
				If ( 	AAI->AAI_CODPRO >= cCodPro  .And.;
						AAI->AAI_CODPRO <= cCodProF .And.;
						!Empty(AAI->AAI_CODPRO))
					If ( cQuebra <> AAI->AAI_CODPRO )
						If ( !Empty(cQuebra) )
							DBENDTREE oTree
						EndIf
						dbSelectArea("SB1")
						dbSetOrder(1)
						dbSeek(xFilial("SB1")+AAI->AAI_CODPRO)
						cProduto := STR0007 //"Produto: "
						cProduto += SB1->B1_COD+" - "+SB1->B1_DESC
						cProduto += Space(nTam)
						DBADDTREE oTree PROMPT cProduto RESOURCE "FOLDER5","FOLDER6" CARGO StrZero(0,12)
						cQuebra := AAI->AAI_CODPRO
					EndIf
					DBADDITEM oTree PROMPT AAI->AAI_SOLUC RESOURCE "DBG3" CARGO StrZero(AAI->(RecNo()),12)
				EndIf
				dbSelectArea("AAI")
				dbSkip()
			EndDo
			If ( !Empty(cQuebra) )
				DBENDTREE oTree
			EndIf
		DBENDTREE oTree
		dbSelectArea("AAG")
		dbSkip()
	EndDo		
#ENDIF

ACTIVATE MSDIALOG oDlg

#IFNDEF PROTHEUS
	oTree:End()
#ENDIF
RestArea(aAreaSB1)
RestArea(aArea)
Return(Nil)

/*/	
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Atc030Memo³ Autor ³Eduardo Riera          ³ Data ³16.07.1999³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Funcao de Atualizacao dos textos da Dica.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Tree                                          ³±±
±±³          ³ExpO2: Objeto Get                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function Atc030Memo(oTree,oMemo,cMemo)

Local aArea    := GetArea()
Local aAreaAAI := AAI->(GetArea())

If ( Val(oTree:GetCargo())<>0 )
	AAI->(dbGoto(Val(oTree:GetCargo())))
	cMemo := MsMM(AAI->AAI_MEMO)           
Else
	cMemo := ""
EndIf               

oMemo:Refresh() 

RestArea(aAreaAAI)
RestArea(aArea)

