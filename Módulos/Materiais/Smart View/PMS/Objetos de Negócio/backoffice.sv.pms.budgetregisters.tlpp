#Include "PROTHEUS.CH"
#Include "FWLIBVERSION.CH"
#Include "totvs.ch"
#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-rest.th"
#Include "tlpp-core.th"
#Include "backoffice.sv.pms.budgetregisters.ch"

Static __lLookUp 	:= FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.pms.budgetregisters.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPMS", tables="AF1,AF2,AF3,AF4,AF7,SA1,SB1,AE8", name="Orçamentos", country="ALL", customTables="AF1,SA1")

/*/{Protheus.doc} budgetregistersSmartViewBusinessObject
	Classe para criação do Objeto de Negócio para Relação de Orçamentos.
	@author Squad CRM/Faturamento
    @since 27/11/2023
    @version 12.1.2410
*/
Class budgetregistersSmartViewBusinessObject from integratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object
	public method PMSSV007CreateSQL()
	public method Sv030_AF2()

    protected data aFields		 as array
    protected data aStruct		 as array
    protected data aPrepaAF3	 as array
    protected data aPrepaAF4	 as array
    protected data aPrepaAF7	 as array
	protected data aAF3_PRD 	 as array
	protected data aAF4_DES 	 as array
	protected data aAF7_PRE 	 as array
    protected data jItems		 as json
    protected data cSelectLoc	 as character
    protected data cWhereLoc	 as character
    protected data cAliasAF1	 as character
    protected data cAliasAF2     as character
    protected data cMD5     	 as character
    protected data nDecCst		 as numeric

Endclass

/*{Protheus.doc} new
	Método de instância da classe
	@return object: self
	@author Squad CRM/Faturamento
    @since 27/11/202
    @version 12.1.2410
*/
Method new() class budgetregistersSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 		//"Gestão de Proejtos"
    self:setDisplayName(STR0002)	//"Orçamentos"
    self:setDescription(STR0003)	//"Relação de Orçamentos"
    self:setIsLookUp(.T.)

	self:cSelectLoc := ""
	self:cWhereLoc 	:= ""
	self:nDecCst	:= 0 
	self:aPrepaAF3  := {}
	self:aPrepaAF4  := {}
	self:aPrepaAF7  := {}
	self:aAF3_PRD   := {}
	self:aAF4_DES   := {}
	self:aAF7_PRE   := {}

Return self


/*{Protheus.doc} getData
	Retorna os dados do objeto de negócio
	@param nPage, numérico, indica a página atual do relatório
	@param oFilter, objeto, contém o filtro do SmartView
	@return object: self:oData
    @author Squad CRM/Faturamento
    @since 27/11/2023
    @version 12.1.2410
*/
Method getData(nPage as numeric, oFilter as object) as object class budgetregistersSmartViewBusinessObject

    Local cAliasAF5     := "" as character
	Local cOrcAnt		:= "" as character
	Local cEDT			:= "" as character
	Local cQryAF5		:= "" as character
	Local cQryAF2		:= "" as character
	Local cValue		:= "" as character

	Local nX            := 0  as numeric
	Local nY            := 0  as numeric
	Local nZ            := 0  as numeric
	Local nW			:= 0  as numeric
	Local nScan         := 0  as numeric
	Local nScanStru		:= 0  as numeric
	Local nPosAF2		:= 0  as numeric
	Local nPosAF5		:= 0  as numeric
	Local nAF5EDT		:= 0  as numeric
	Local nPosPrepAF2  	:= 0  as numeric
	Local nPosPrepAF5  	:= 0  as numeric
	Local nParam 		:= 0  as numeric

    Local lObfuscated  := .F. as logical
	Local lQuebra	   := .F. as logical

    Local aPDFields     := {} as array
	Local aFieldsOrc 	:= {} as array
	Local aFieldsEdt 	:= {} as array
	Local aFieldsTrf 	:= {} as array
	Local aFieldsDet 	:= {} as array
	Local aJsonOrc		:= {} as array
	Local aJsonEdt		:= {} as array
	Local aJsonTrf		:= {} as array
	Local aJsonDet		:= {} as array
	Local aAF5_EDT		:= {} as array
	Local aAF2_TAR		:= {} as array
	Local aPrepaAF2 	:= {}  as array
	Local aPrepaAF5 	:= {}  as array

	MV_FIL := ""

	self:cAliasAF2 := GetNextAlias()
    cAliasAF5 := GetNextAlias()

	//metodo para retorno do json dos parametros
	FatSetValueMVPAR(oFilter:GetParameters(),"PMR030") 

	//Arrays com campos por quebra dos campos do schema
	aFieldsOrc := {"AF1_FILIAL","AF1_ORCAME","AF1_DESCRI","AF1_CLIENT","AF1_LOJA","A1_NOME"}
	aFieldsEdt := {"AF5_EDT","AF5_DESCRI","AF5_UM","AF5_QUANT","AF5_CUSTO"}
	aFieldsTrf := {"AF2_TAREFA","AF2_DESCRI","AF2_UM","AF2_QUANT","AF2_HDURAC","AF2_CUSTO"}
	aFieldsDet := {"TB_TPTAREF","TB_TIPOREG","TB_CODIGO","TB_DESCRI","TB_UMTIPO","TB_TIPOD1","TB_VLRHR","AF3_QUANT","AF3_CUSTD","TB_CUSTD1"}

	//Arrays com estrutura do json dos campos fixos
	aJsonOrc := {{"AF1FILIAL",	Nil},;
				 {"AF1ORCAME",	Nil},;
				 {"AF1DESCRI",	Nil},;
				 {"AF1CLIENT",	Nil},;
				 {"AF1LOJA", 	Nil},;
				 {"A1NOME",		Nil} }

	aJsonEdt := {{"AF5EDT",		Nil},;
				 {"AF5DESCRI",	Nil},;
				 {"AF5UM",		Nil},;
				 {"AF5QUANT",	Nil},;
				 {"AF5CUSTO",	Nil} }

	aJsonTrf := {{"AF2TAREFA",	Nil},;
				 {"AF2DESCRI",	Nil},;
				 {"AF2UM",		Nil},;
				 {"AF2QUANT",	Nil},;
				 {"AF2HDURAC",	Nil},;
				 {"AF2CUSTO",	Nil} }

	aJsonDet := {{"TBTPTAREF",	Nil},;
				 {"TBTIPOREG",	Nil},;
				 {"TBCODIGO",	Nil},;
				 {"TBDESCRI",	Nil},;
				 {"TBUMTIPO",	Nil},;
				 {"TBTIPOD1",	Nil},;
				 {"TBVLRHR",	Nil},;
				 {"AF3QUANT",	Nil},;
				 {"AF3CUSTD",	Nil},;
				 {"TBCUSTD1",	Nil} }

	//Adiciona campos personalizados nos Arrays
    aEval( self:getCustomFields(), {|x| ( nX++, aAdd(aFieldsOrc, self:getCustomFields()[nX][1] ) ) })
	aEval( self:getCustomFields(), {|x| ( nW++, aAdd(aJsonOrc, { self:getCustomFields()[nW][1], Nil} ) ) })

    //Método que monta a Query
	self:PMSSV007CreateSQL()

	//Posiciona no registro inicial
	(self:cAliasAF1)->(dbGoTop())
	If !(self:cAliasAF1)->(Eof())
	
		nAF5EDT	:= FWSX3Util():GetFieldStruct("AF5_EDT")[3]
		self:nDecCst	:= FWSX3Util():GetFieldStruct("AF2_CUSTO")[4]

		//Defini indices das tabelas
		AF2->(DbSetOrder(2)) //AF2_FILIAL, AF2_ORCAME, AF2_EDTPAI, AF2_ORDEM
		AF3->(DbSetOrder(1)) //AF3_FILIAL, AF3_ORCAME, AF3_TAREFA, AF3_ITEM
		AF4->(DbSetOrder(1)) //AF4_FILIAL, AF4_ORCAME, AF4_TAREFA, AF4_ITEM
		AF5->(DbSetOrder(1)) //AF5_FILIAL, AF5_ORCAME, AF5_EDT, AF5_ORDEM
		AF7->(DbSetOrder(1)) //AF7_FILIAL, AF7_ORCAME, AF7_TAREFA, AF7_ITEM
		AE8->(DbSetOrder(1)) //AE8_FILIAL, AE8_RECURS, AE8_DESCRI
		SB1->(DbSetOrder(1)) //B1_FILIAL, B1_COD

		//Verifica se precisa fazer o tratamento para LGPD   
    	aPDFields	:= FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    	lObfuscated	:= Len(aPDFields) > 0

		While !(self:cAliasAF1)->(Eof())

			//Validação da fase do orçamento
			If !PmrPertence((self:cAliasAF1)->AF1_FASE, MV_PAR06)
				(self:cAliasAF1)->(dbSkip())
				Loop
			Endif

			//Inicaliza variáveis de controle
			aAF5_EDT := {}
			cEDT := Padr((self:cAliasAF1)->AF1_ORCAME,nAF5EDT)

			//Busca EDTs do projeto
			If Empty(cOrcAnt) .Or. (self:cAliasAF1)->AF1_ORCAME <> cOrcAnt

				cEDT := Padr((self:cAliasAF1)->AF1_ORCAME, nAF5EDT)

				//EDT Pai do projeto
				If AF5->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF1)->AF1_ORCAME + cEDT))
					If PmrPertence(AF5->AF5_NIVEL, MV_PAR05)
						aAdd(aAF5_EDT, {AF5->AF5_EDT, AF5->AF5_DESCRI, AF5->AF5_UM, AF5->AF5_QUANT, AF5->AF5_CUSTO, AF5->AF5_ORCAME, Iif(Empty(AF5->AF5_ORDEM), "000", AF5->AF5_ORDEM), {}, .T.})
					Endif
				Endif

				nPosAF5	:= Len(aAF5_EDT)

				For nX := 1 To Len(aFieldsOrc)
					//Busca campo posiçao do campo na aStruct
					If (nScanStru := aScan(self:aStruct,  {|x| x[5] == aFieldsOrc[nX] } ) ) > 0

						//Verifica se existe tratamento para valores do JSON
						cValue := (self:cAliasAF1)->(FieldGet(FieldPos(self:aStruct[nScanStru][5])))

						//Verificação para tratamento de LGPD e campos tipo Data antes informar no JSON
						aJsonOrc[nX][2] := IIf( lObfuscated .And. (nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nScanStru][5] } ) )  > 0,;
												aPDFields[nScan][2],;
												IIf( UPPER(self:aStruct[nScanStru][3]) == "DATE",;
													totvs.framework.treports.date.stringToTimeStamp( cValue) ,;
													cValue) )
					Endif
				Next

				//Busca EDTs filhas do projeto
				If AF5->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF1)->AF1_ORCAME)) 

					nParam := 1
					cQryAF5 := " SELECT AF5_FILIAL, AF5_ORCAME, AF5_EDTPAI, AF5_EDT, AF5_ORDEM, AF5_DESCRI, AF5_UM, AF5_QUANT, AF5_CUSTO, AF5_UM "
					cQryAF5 += " FROM " + RetSqlName("AF5") + " AF5 "
					cQryAF5 += " WHERE AF5.AF5_FILIAL = ? "
					cQryAF5 += " AND AF5.AF5_ORCAME = ? "
					cQryAF5 += " AND AF5.AF5_EDTPAI = ? "
					cQryAF5 += " AND AF5.D_E_L_E_T_ = ? "

					self:cMD5 := MD5(cQryAF5)

					If (nPosPrepAF5 := Ascan(aPrepaAF5,{|x| x[2] == self:cMD5})) == 0 
						Aadd(aPrepaAF5,{FwExecStatement():New(ChangeQuery(cQryAF5)),self:cMD5})
						nPosPrepAF5 := Len(aPrepaAF5)
					Endif

					aPrepaAF5[nPosPrepAF5][1]:SetString(nParam++, (self:cAliasAF1)->AF1_FILIAL)
					aPrepaAF5[nPosPrepAF5][1]:SetString(nParam++, (self:cAliasAF1)->AF1_ORCAME)
					aPrepaAF5[nPosPrepAF5][1]:SetString(nParam++, cEDT)
					aPrepaAF5[nPosPrepAF5][1]:SetString(nParam++, ' ')

					aPrepaAF5[nPosPrepAF5][1]:OpenAlias(cAliasAF5)

					While !(cAliasAF5)->(Eof()) .And. (self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF1)->AF1_ORCAME + cEDT == (cAliasAF5)->(AF5_FILIAL + AF5_ORCAME + AF5_EDTPAI)

						//EDT filho Cabeçalho
						aAdd(aAF5_EDT, {(cAliasAF5)->AF5_EDT, (cAliasAF5)->AF5_DESCRI, (cAliasAF5)->AF5_UM, (cAliasAF5)->AF5_QUANT, ;
						(cAliasAF5)->AF5_CUSTO, (cAliasAF5)->AF5_ORCAME, Iif(Empty((cAliasAF5)->AF5_ORDEM), "000", (cAliasAF5)->AF5_ORDEM), {}, .T.})

						aAF2_TAR := {}
						nPosAF2	 := 0
						cEDT	 := Padr((cAliasAF5)->AF5_EDT, nAF5EDT)
						nPosAF5	 := Len(aAF5_EDT)

						//Busca Tarefas
						If AF2->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF1)->AF1_ORCAME + cEDT)) 

							nParam := 1
							cQryAF2 := " SELECT AF2_FILIAL, AF2_ORCAME, AF2_EDTPAI, AF2_ORDEM, AF2_TAREFA, AF2_DESCRI, AF2_UM, AF2_QUANT, AF2_HDURAC, AF2_CUSTO "
							cQryAF2 += " FROM " + RetSqlName("AF2") + " AF2 "
							cQryAF2 += " WHERE AF2.AF2_FILIAL = ? "
							cQryAF2 += " AND AF2.AF2_ORCAME = ? "
							cQryAF2 += " AND AF2.AF2_EDTPAI = ? "
							cQryAF2 += " AND AF2.D_E_L_E_T_ = ? "

							self:cMD5 := MD5(cQryAF2)

							If (nPosPrepAF2 := Ascan(aPrepaAF2,{|x| x[2] == self:cMD5})) == 0 
								Aadd(aPrepaAF2,{FwExecStatement():New(ChangeQuery(cQryAF2)),self:cMD5})
								nPosPrepAF2 := Len(aPrepaAF2)
							Endif

							aPrepaAF2[nPosPrepAF2][1]:SetString(nParam++, (self:cAliasAF1)->AF1_FILIAL)
							aPrepaAF2[nPosPrepAF2][1]:SetString(nParam++, (self:cAliasAF1)->AF1_ORCAME)
							aPrepaAF2[nPosPrepAF2][1]:SetString(nParam++, cEDT)
							aPrepaAF2[nPosPrepAF2][1]:SetString(nParam++, ' ')

							aPrepaAF2[nPosPrepAF2][1]:OpenAlias(self:cAliasAF2)

							While !(self:cAliasAF2)->(Eof()) .And. (self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF1)->AF1_ORCAME + cEDT == (self:cAliasAF2)->(AF2_FILIAL + AF2_ORCAME + AF2_EDTPAI)

								//Tarefas da EDT filha cabeçalho
								aAdd(aAF2_TAR, {(self:cAliasAF2)->AF2_TAREFA,(self:cAliasAF2)->AF2_DESCRI,(self:cAliasAF2)->AF2_UM,(self:cAliasAF2)->AF2_QUANT,(self:cAliasAF2)->AF2_HDURAC,;
								(self:cAliasAF2)->AF2_CUSTO,(self:cAliasAF2)->AF2_ORCAME, If(Empty((self:cAliasAF2)->AF2_ORDEM), "000", (self:cAliasAF2)->AF2_ORDEM), {}, {}, {}, .T.})

								nPosAF2 := Len(aAF2_TAR)

								self:aAF3_PRD := {}
								self:aAF4_DES := {}
								self:aAF7_PRE := {}

								//Busca Produtos/Recursos(AF3), Despesas(AF4) e Predecessoras(AF7)
								lQuebra := self:Sv030_AF2()

								If lQuebra
									//Tarefas da EDT filha quebra
									aAdd(aAF2_TAR, {(self:cAliasAF2)->AF2_TAREFA,(self:cAliasAF2)->AF2_DESCRI,(self:cAliasAF2)->AF2_UM,0,0,0,(self:cAliasAF2)->AF2_ORCAME,;
									IIf(Empty((self:cAliasAF2)->AF2_ORDEM), "000", (self:cAliasAF2)->AF2_ORDEM), {}, {}, {}, .F.})

									nPosAF2 := Len(aAF2_TAR)

									aAF2_TAR[nPosAF2][9]	:= self:aAF3_PRD
									aAF2_TAR[nPosAF2][10]	:= self:aAF4_DES
									aAF2_TAR[nPosAF2][11]	:= self:aAF7_PRE
								Endif

								(self:cAliasAF2)->(dbSkip())

							Enddo

						Endif 

						cEDT := Padr((cAliasAF5)->AF5_EDTPAI, nAF5EDT)

						//Valida se foi encontrado tarefa para vincular com a EDT
						If nPosAF2 > 0
							//EDT filha Quebra
							aAdd(aAF5_EDT, {(cAliasAF5)->AF5_EDT, (cAliasAF5)->AF5_DESCRI, (cAliasAF5)->AF5_UM, 0, 0, 0, Iif(Empty((cAliasAF5)->AF5_ORDEM), "000", (cAliasAF5)->AF5_ORDEM), {}, .F.})
							nPosAF5	:= Len(aAF5_EDT)

							aAF5_EDT[nPosAF5][8] := aAF2_TAR
						Endif

						(cAliasAF5)->(dbSkip())

					Enddo

				Endif

				If nPosAF5 > 0

					For nY := 1 To Len(aAF5_EDT)

						//Carrega valor fixos da EDT para quebra de detalhes
						aJsonEdt[1][2] := aAF5_EDT[nY][1]
						aJsonEdt[2][2] := aAF5_EDT[nY][2]
						aJsonEdt[3][2] := aAF5_EDT[nY][3]
						aJsonEdt[4][2] := aAF5_EDT[nY][4]
						aJsonEdt[5][2] := aAF5_EDT[nY][5]

						//Campos das informações do EDT Pai e EDT Filhas - Cabeçalho
						If aAF5_EDT[nY][9]

							self:jItems := JsonObject():new()

							//Monta json com os campos do orçamentos fixo
							SvGetJsonFix(aJsonOrc, @self:jItems)

							//Monta json com os campos do EDT fixo
							SvGetJsonFix(aJsonEdt, @self:jItems)

							//Tratamento para commit localizado
							self:processData(1)
							self:oData:appendData(self:jItems)

						Else

							For nZ := 1 To Len(aAF5_EDT[nY][8])

								//Carrega valor fixos da EDT para quebra de detalhes
								aJsonTrf[1][2] := aAF5_EDT[nY][8][nZ][1]
								aJsonTrf[2][2] := aAF5_EDT[nY][8][nZ][2]
								aJsonTrf[3][2] := aAF5_EDT[nY][8][nZ][3]
								aJsonTrf[4][2] := aAF5_EDT[nY][8][nZ][4]
								aJsonTrf[5][2] := aAF5_EDT[nY][8][nZ][5]
								aJsonTrf[6][2] := aAF5_EDT[nY][8][nZ][6]

								//Campos das informações do EDT Pai, EDT Filha e Tarefas - Cabeçalho
								If aAF5_EDT[nY][8][nZ][12]

									self:jItems := JsonObject():new()

									//Monta json com os campos do orçamentos fixo
									SvGetJsonFix(aJsonOrc, @self:jItems)

									//Monta json com os campos do EDT fixo
									SvGetJsonFix(aJsonEdt, @self:jItems)

									//Monta json com os campos da tarefa fixa
									SvGetJsonFix(aJsonTrf, @self:jItems)

									//Tratamento para commit localizado
									self:processData(1)
									self:oData:appendData(self:jItems)

								Else

									//Carrega Produtos/Recursos(AF3)
									For nX := 1 To Len(aAF5_EDT[nY][8][nZ][9])

										//Carrega valor dos detalhes da tafera
										aJsonDet[1][2]	:= aAF5_EDT[nY][8][nZ][9][nX][1]
										aJsonDet[2][2]	:= aAF5_EDT[nY][8][nZ][9][nX][2]
										aJsonDet[3][2]	:= aAF5_EDT[nY][8][nZ][9][nX][3]
										aJsonDet[4][2]	:= aAF5_EDT[nY][8][nZ][9][nX][4]
										aJsonDet[5][2]	:= aAF5_EDT[nY][8][nZ][9][nX][5]
										aJsonDet[6][2]	:= aAF5_EDT[nY][8][nZ][9][nX][6]
										aJsonDet[7][2]	:= aAF5_EDT[nY][8][nZ][9][nX][7]
										aJsonDet[8][2]	:= aAF5_EDT[nY][8][nZ][9][nX][8]
										aJsonDet[9][2]	:= aAF5_EDT[nY][8][nZ][9][nX][9]
										aJsonDet[10][2]	:= aAF5_EDT[nY][8][nZ][9][nX][10]

										self:jItems := JsonObject():new()

										//Monta json com os campos do orçamentos fixo
										SvGetJsonFix(aJsonOrc, @self:jItems)

										//Monta json com os campos do EDT fixo
										SvGetJsonFix(aJsonEdt, @self:jItems)

										//Monta json com os campos da tarefa fixa
										SvGetJsonFix(aJsonTrf, @self:jItems)

										//Monta json com os campos dos detalhes
										SvGetJsonFix(aJsonDet, @self:jItems)

										//Tratamento para commit localizado
										self:processData(1)
										self:oData:appendData(self:jItems)

									Next

									//Carrega Despesas(AF4)
									For nX := 1 To Len(aAF5_EDT[nY][8][nZ][10])

										//Carrega valor dos detalhes da tafera
										aJsonDet[1][2]	:= aAF5_EDT[nY][8][nZ][10][nX][1]
										aJsonDet[2][2]	:= aAF5_EDT[nY][8][nZ][10][nX][2]
										aJsonDet[3][2]	:= aAF5_EDT[nY][8][nZ][10][nX][3]
										aJsonDet[4][2]	:= aAF5_EDT[nY][8][nZ][10][nX][4]
										aJsonDet[5][2]	:= aAF5_EDT[nY][8][nZ][10][nX][5]
										aJsonDet[6][2]	:= aAF5_EDT[nY][8][nZ][10][nX][6]
										aJsonDet[7][2]	:= aAF5_EDT[nY][8][nZ][10][nX][7]
										aJsonDet[8][2]	:= aAF5_EDT[nY][8][nZ][10][nX][8]
										aJsonDet[9][2]	:= aAF5_EDT[nY][8][nZ][10][nX][9]
										aJsonDet[10][2]	:= aAF5_EDT[nY][8][nZ][10][nX][10]

										self:jItems := JsonObject():new()

										//Monta json com os campos do orçamentos fixo
										SvGetJsonFix(aJsonOrc, @self:jItems)

										//Monta json com os campos do EDT fixo
										SvGetJsonFix(aJsonEdt, @self:jItems)

										//Monta json com os campos da tarefa fixa
										SvGetJsonFix(aJsonTrf, @self:jItems)

										//Monta json com os campos dos detalhes
										SvGetJsonFix(aJsonDet, @self:jItems)

										//Tratamento para commit localizado
										self:processData(1)
										self:oData:appendData(self:jItems)

									Next

									//Carrega Predecessoras(AF7)
									For nX := 1 To Len(aAF5_EDT[nY][8][nZ][11])

										//Carrega valor dos detalhes da tafera
										aJsonDet[1][2]	:= aAF5_EDT[nY][8][nZ][11][nX][1]
										aJsonDet[2][2]	:= aAF5_EDT[nY][8][nZ][11][nX][2]
										aJsonDet[3][2]	:= aAF5_EDT[nY][8][nZ][11][nX][3]
										aJsonDet[4][2]	:= aAF5_EDT[nY][8][nZ][11][nX][4]
										aJsonDet[5][2]	:= aAF5_EDT[nY][8][nZ][11][nX][5]
										aJsonDet[6][2]	:= aAF5_EDT[nY][8][nZ][11][nX][6]
										aJsonDet[7][2]	:= aAF5_EDT[nY][8][nZ][11][nX][7]
										aJsonDet[8][2]	:= aAF5_EDT[nY][8][nZ][11][nX][8]
										aJsonDet[9][2]	:= aAF5_EDT[nY][8][nZ][11][nX][9]
										aJsonDet[10][2]	:= aAF5_EDT[nY][8][nZ][11][nX][10]

										self:jItems := JsonObject():new()

										//Monta json com os campos do orçamentos fixo
										SvGetJsonFix(aJsonOrc, @self:jItems)

										//Monta json com os campos do EDT fixo
										SvGetJsonFix(aJsonEdt, @self:jItems)

										//Monta json com os campos da tarefa fixa
										SvGetJsonFix(aJsonTrf, @self:jItems)

										//Monta json com os campos dos detalhes
										SvGetJsonFix(aJsonDet, @self:jItems)

										//Tratamento para commit localizado
										self:processData(1)
										self:oData:appendData(self:jItems)

									Next
								Endif
							Next
						Endif
					Next
				Endif
			Endif

			(self:cAliasAF1)->(dbSkip())

		Enddo

	Endif 

	(self:cAliasAF1)->(DBCloseArea())

	aSize(aPDFields,0)
	aSize(aFieldsOrc,0)
	aSize(aFieldsEdt,0)
	aSize(aFieldsTrf,0)
	aSize(aFieldsDet,0)
	aSize(aJsonOrc,0)
	aSize(aJsonEdt,0)
	aSize(aJsonTrf,0)
	aSize(aJsonDet,0)
	aSize(aAF5_EDT,0)
	aSize(aAF2_TAR,0)
	aSize(aPrepaAF2,0)
	aSize(aPrepaAF5,0)
	aSize(self:aAF3_PRD,0)
	aSize(self:aAF4_DES,0)
	aSize(self:aAF7_PRE,0)
	aSize(self:aPrepaAF3,0)
	aSize(self:aPrepaAF4,0)
	aSize(self:aPrepaAF7,0)
	aSize(self:getCustomFields(),0)

Return self:oData

/*{Protheus.doc} Sv030_AF2
	Retorna detalhes Tarefas do Orçamento(AF2)
    @author Squad CRM/Faturamento
    @since 27/11/2023
    @version 12.1.2410
	@return lRet
*/
Method Sv030_AF2() Class budgetregistersSmartViewBusinessObject 

	Local aProduto		:= {"","","","",0} 	as array
	Local cAliasAF3		:= "ALIASAF3" as character
	Local cAliasAF4		:= "ALIASAF4" as character
	Local cAliasAF7		:= "ALIASAF7" as character
	Local cQryAF3		:= ""  as character
	Local cQryAF4		:= ""  as character
	Local cQryAF7		:= ""  as character
	Local cTipo			:= ""  as character
	Local lRet			:= .F. as logical
	Local nQuantAF3 	:= 0   as numeric
	Local nParam 		:= 1   as numeric
	Local nPosPrepAF3 	:= 0   as numeric
	Local nPosPrepAF4 	:= 0   as numeric
	Local nPosPrepAF7 	:= 0   as numeric

	//verifica se existem produtos na tarefa
	If AF3->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA)) 

		cQryAF3 := " SELECT AF3_FILIAL, AF3_ORCAME, AF3_TAREFA, AF3_PRODUT, AF3_QUANT, AF3_CUSTD, AF3_RECURS "
		cQryAF3 += " FROM " + RetSqlName("AF3") + " AF3"
		cQryAF3 += " LEFT JOIN " + RetSqlName("AE8") + " AE8 ON ? "
		cQryAF3 += " AND AE8.D_E_L_E_T_ = ? "
		cQryAF3 += " AND AE8.AE8_RECURS = AF3.AF3_RECURS "
		cQryAF3 += " WHERE AF3_FILIAL = ? "
		cQryAF3 += " AND AF3_ORCAME = ? "
		cQryAF3 += " AND AF3_TAREFA = ? "
		cQryAF3 += " AND AF3.D_E_L_E_T_ = ? "

		self:cMD5 := MD5(cQryAF3)

		If (nPosPrepAF3 := Ascan(self:aPrepaAF3,{|x| x[2] == self:cMD5})) == 0 
			Aadd(self:aPrepaAF3,{FwExecStatement():New(ChangeQuery(cQryAF3)), self:cMD5})
			nPosPrepAF3 := Len(self:aPrepaAF3)
		Endif

		self:aPrepaAF3[nPosPrepAF3][1]:SetUnsafe(nParam++,  FwJoinFilial("AE8", "AF3"))
		self:aPrepaAF3[nPosPrepAF3][1]:SetString(nParam++, ' ')
		self:aPrepaAF3[nPosPrepAF3][1]:SetString(nParam++, (self:cAliasAF1)->AF1_FILIAL)
		self:aPrepaAF3[nPosPrepAF3][1]:SetString(nParam++, (self:cAliasAF2)->AF2_ORCAME)
		self:aPrepaAF3[nPosPrepAF3][1]:SetString(nParam++, (self:cAliasAF2)->AF2_TAREFA)
		self:aPrepaAF3[nPosPrepAF3][1]:SetString(nParam++, ' ')

		self:aPrepaAF3[nPosPrepAF3][1]:OpenAlias(cAliasAF3)


		While !(cAliasAF3)->(Eof()) .And. ((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA) == (cAliasAF3)->(AF3_FILIAL + AF3_ORCAME + AF3_TAREFA)

			//valida os produtos
			If Empty((cAliasAF3)->AF3_RECURS) .And. SB1->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (cAliasAF3)->AF3_PRODUT))
				aProduto[1] := (cAliasAF3)->AF3_PRODUT
				aProduto[2] := SB1->B1_DESC
				aProduto[3] := SB1->B1_UM
				aProduto[4] := SB1->B1_TIPO
				aProduto[5] := 1
			ElseIf AE8->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (cAliasAF3)->AF3_RECURS))
				aProduto[1] := (cAliasAF3)->AF3_RECURS
				aProduto[2] := AE8->AE8_DESCRI
				aProduto[3] := "HR"
				aProduto[4] := ""
				aProduto[5] := 2
			Endif

			nQuantAF3 := PmsAF3Quant((self:cAliasAF2)->AF2_ORCAME, (self:cAliasAF2)->AF2_TAREFA, (cAliasAF3)->AF3_PRODUT, (self:cAliasAF2)->AF2_QUANT,(cAliasAF3)->AF3_QUANT)

			If ((cAliasAF3)->AF3_PRODUT >= MV_PAR07) .And. ((cAliasAF3)->AF3_PRODUT <= MV_PAR08) .And. (Empty(MV_PAR09) .Or. aProduto[4] == MV_PAR09)
			
				aAdd(self:aAF3_PRD, {STR0018,; //"Produto/Recurso"
								Iif(aProduto[5] == 1, STR0019, STR0020),; //"Produto"#"Recurso"
								aProduto[1],;
								aProduto[2],;
								aProduto[3],;
								NIL,;
								NIL,;
								nQuantAF3,;
								(cAliasAF3)->AF3_CUSTD,;
								PmsTrunca((self:cAliasAF1)->AF1_TRUNCA,(nQuantAF3 * (cAliasAF3)->AF3_CUSTD), self:nDecCst, (self:cAliasAF2)->AF2_QUANT) })
				lRet := .T.

			Endif

			(cAliasAF3)->(dbSkip())

		Enddo

		If lRet
			aSort(self:aAF3_PRD, , , {|x, y| x[2] < y[2]})
		Endif

	Endif

	//verifica se existem despesas na tarefa
	If AF4->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA))
		
		nParam := 1

		cQryAF4 := " SELECT AF4_FILIAL, AF4_ORCAME, AF4_TAREFA, AF4_ITEM, AF4_TIPOD, AF4_DESCRI, AF4_VALOR"
		cQryAF4 += " FROM " + RetSqlName("AF4") + " AF4 "
		cQryAF4 += " WHERE AF4.AF4_FILIAL = ? "
		cQryAF4 += " AND AF4.AF4_ORCAME = ? "
		cQryAF4 += " AND AF4.AF4_TAREFA = ? "
		cQryAF4 += " AND AF4.AF4_TIPOD BETWEEN ? AND ? "
		cQryAF4 += " AND AF4.D_E_L_E_T_ = ? "
		
		self:cMD5 := MD5(cQryAF4)

		If (nPosPrepAF4 := Ascan(self:aPrepaAF4,{|x| x[2] == self:cMD5})) == 0 
			Aadd(self:aPrepaAF4,{FwExecStatement():New(ChangeQuery(cQryAF4)), self:cMD5})
			nPosPrepAF4 := Len(self:aPrepaAF4)
		Endif

		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, (self:cAliasAF1)->AF1_FILIAL)
		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, (self:cAliasAF2)->AF2_ORCAME)
		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, (self:cAliasAF2)->AF2_TAREFA)
		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, MV_PAR10)
		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, MV_PAR11)
		self:aPrepaAF4[nPosPrepAF4][1]:SetString(nParam++, ' ')

		self:aPrepaAF4[nPosPrepAF4][1]:OpenAlias(cAliasAF4)

		While !(cAliasAF4)->(Eof()) .And. ((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA) == (cAliasAF4)->(AF4_FILIAL + AF4_ORCAME + AF4_TAREFA)

			aAdd(self:aAF4_DES, {STR0021,;	//"Despesa"
							STR0021,;		//"Despesa"
							(cAliasAF4)->AF4_ITEM,;
							(cAliasAF4)->AF4_DESCRI,;
							(cAliasAF4)->AF4_TIPOD,;
							FWGetSX5("FD", (cAliasAF4)->AF4_TIPOD)[1][4],;
							PmsTrunca((self:cAliasAF1)->AF1_TRUNCA,PmsAF4Valor((self:cAliasAF2)->AF2_QUANT,(cAliasAF4)->AF4_VALOR),self:nDecCst,(self:cAliasAF2)->AF2_QUANT),;
							NIL,;
							NIL,;
							NIL })
			lRet := .T.

			(cAliasAF4)->(dbSkip())

		Enddo
	Endif
	
	//verifica se existem predecessoras na tarefa
	If AF7->(MsSeek((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA)) 

		nParam := 1

		cQryAF7 := " SELECT AF7_FILIAL, AF7_ORCAME, AF7_TAREFA, AF7_ITEM, AF7_TIPO, AF7_PREDEC, AF7_HRETAR "
		cQryAF7 += " FROM " + RetSqlName("AF7") + " AF7  "
		cQryAF7 += " WHERE AF7.AF7_FILIAL = ? "
		cQryAF7 += " AND AF7.AF7_ORCAME = ? "
		cQryAF7 += " AND AF7.AF7_TAREFA = ? "
		cQryAF7 += " AND AF7.D_E_L_E_T_ = ? "

		self:cMD5 := MD5(cQryAF7)

		If (nPosPrepAF7 := Ascan(self:aPrepaAF7,{|x| x[2] == self:cMD5})) == 0 
			Aadd(self:aPrepaAF7,{FwExecStatement():New(ChangeQuery(cQryAF7)),self:cMD5})
			nPosPrepAF7 := Len(self:aPrepaAF7)
		Endif

		self:aPrepaAF7[nPosPrepAF7][1]:SetString(nParam++, (self:cAliasAF1)->AF1_FILIAL)
		self:aPrepaAF7[nPosPrepAF7][1]:SetString(nParam++, (self:cAliasAF2)->AF2_ORCAME)
		self:aPrepaAF7[nPosPrepAF7][1]:SetString(nParam++, (self:cAliasAF2)->AF2_TAREFA)
		self:aPrepaAF7[nPosPrepAF7][1]:SetString(nParam++, ' ')

		self:aPrepaAF7[nPosPrepAF7][1]:OpenAlias(cAliasAF7)

		While !(cAliasAF7)->(Eof()) .And. ((self:cAliasAF1)->AF1_FILIAL + (self:cAliasAF2)->AF2_ORCAME + (self:cAliasAF2)->AF2_TAREFA) == (cAliasAF7)->(AF7_FILIAL + AF7_ORCAME + AF7_TAREFA)

			cTipo := IIF((cAliasAF7)->AF7_TIPO == "1", STR0023,; 		//"Fim-no-Inicio"
						IIF((cAliasAF7)->AF7_TIPO == "2", STR0024,; 	//"Inicio-no-Inicio"
							IIF((cAliasAF7)->AF7_TIPO == "3", STR0025,;	//"Fim-no-Fim"
							 	STR0026)))								//"Inicio-no-Fim"


			aAdd(self:aAF7_PRE, {STR0022,;	//"Predecessora"
							STR0022,;		//"Predecessora"
							(cAliasAF7)->AF7_PREDEC,;
							(self:cAliasAF2)->AF2_DESCRI,;
							cTipo,;
							NIL,;
							(cAliasAF7)->AF7_HRETAR,;
							NIL,;
							NIL,;
							NIL })

			lRet := .T.

			(cAliasAF7)->(dbSkip())

		Enddo
	Endif

	aSize(aProduto,0)
	aSize(aProduto,0)

Return lRet

/*{Protheus.doc} SvGetJsonFix
	Monta campos no Json
    @author Squad CRM/Faturamento
    @since 27/11/2023
    @version 12.1.2410
	@return Nil
*/
Static Function SvGetJsonFix(aFieldsJson as array, jItems as json)

	Local nX := 0 as numeric

	For nX := 1 To Len(aFieldsJson)
		jItems[aFieldsJson[nX][1]] := aFieldsJson[nX][2]
	Next

Return		

/*{Protheus.doc} getSchema
	Retorna a estrutura dos campos
    @author Squad CRM/Faturamento
    @since 20/06/2025
    @version 12.1.2410
	@return object: self:oSchema
*/
Method getSchema() as object class budgetregistersSmartViewBusinessObject

	Local nX := 0 as numeric
	Local aCposFake := {} as array
	Local aParameters := {} as array

	self:aFields := {"AF1_FILIAL","AF1_ORCAME","AF1_DESCRI","AF1_CLIENT","AF1_LOJA","A1_NOME",;
					 "AF5_EDT","AF5_DESCRI","AF5_UM","AF5_QUANT","AF5_CUSTO",;
					 "AF2_TAREFA","AF2_DESCRI","AF2_UM","AF2_QUANT","AF2_HDURAC","AF2_CUSTO",;
					 "TB_TPTAREF","TB_TIPOREG","TB_CODIGO","TB_DESCRI","TB_UMTIPO","TB_TIPOD1","TB_VLRHR","AF3_QUANT","AF3_CUSTD","TB_CUSTD1"}

	aCposFake := {	{"AF5_UM"	  , STR0004, 'string', STR0004},; //"UM"
					{"AF5_QUANT"  , STR0005, 'number', STR0005},; //"Previsto Quantidade"
					{"AF5_CUSTO"  , STR0006, 'number', STR0006},; //"Previsto Custo"
					{"AF2_UM"	  , STR0004, 'string', STR0004},; //"UM"
					{"AF2_QUANT"  , STR0005, 'number', STR0005},; //"Previsto Quantidade"
					{"AF2_HDURAC" , STR0007, 'number', STR0007},; //"Previsto Duração"
					{"AF2_CUSTO"  , STR0006, 'number', STR0006},; //"Previsto Custo"
					{"TB_TPTAREF" , STR0008, 'string', STR0008},; //"Tipo Tarefa" Novo #"Produto/Recurso"#"Despesa"#"Predecessora"
					{"TB_TIPOREG" , STR0009, 'string', STR0009},; //"Tipo do Registro" Novo #"Produto"#"Recurso"#"Despesa"#"Predecessora"
					{"TB_CODIGO"  , STR0010, 'string', STR0010},; //"Código" (AF3_PRODUT ou AF3_RECURS)/AF4_ITEM/AF7_PREDEC
					{"TB_DESCRI"  , STR0011, 'string', STR0011},; //"Descrição" (B1_DESC ou AE8_DESCRI)/AF4_ITEM/AF7_PREDEC
					{"TB_UMTIPO"  , STR0012, 'string', STR0012},; //"UM/Tipo" (B1_UM ou "HR")/AF4_TIPOD/AF7_TIPO
					{"TB_TIPOD1"  , STR0013, 'string', STR0013},; //"Descrição do Tipo(Despesa)"
					{"TB_VLRHR"	  , STR0014, 'number', STR0014},; //"Vlr.Previsto(Desp)/Hr.Retardo(Pred)" AF4_VALOR/AF7_HRETAR
					{"AF3_QUANT"  , STR0015, 'number', STR0015},; //"Qtd.Previsto(Prod/Recur)"
					{"AF3_CUSTD"  , STR0016, 'number', STR0016},; //"Unitário Médio"
					{"TB_CUSTD1"  , STR0017, 'number', STR0017} } //"Custo Estandar"

	self:aStruct := FatTrGetStruct(self:aFields, aCposFake)

	//Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	//Busca no SX1 os parâmetros que serão utilizados.
	aParameters := FtSVSetParam('PMR030',,,,{'MV_FIL','01','02','03','04','05','06','07','08','09','10','11','12','14','13','15'})

	self:oSchema:addParameter("MV_FIL",   STR0027 + " ?" ,"string",  .T.,,,,,, FTSVHelp(,,,.T.)) //Filial   

    For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX
	
    If __lLookUp
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericQuery?tables=AF1&fields=af1_filial,af1_orcame,af1_descri,af1_data&DeletedFilter=true&Format=smartview&KeyProperty=af1_orcame", 2) //Orcamento  de ?               
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericQuery?tables=AF1&fields=af1_filial,af1_orcame,af1_descri,af1_data&DeletedFilter=true&Format=smartview&KeyProperty=af1_orcame", 2) //Orcamento ate ?               
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SB1", 2) //Produto de ?                  
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/SB1", 2) //Produto ate ?                 
        self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/FD", 2)  //Tipo de despesa de ?                           
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/FD", 2)  //Tipo de despesa ate ?                          
        self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/SA1", 2) //Cliente de ?                  
        self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/SA1", 2) //Cliente ate ?    
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial
    Endif

	FwFreeArray(aParameters)
	FwFreeArray(aCposFake)

Return self:oSchema


/*{Protheus.doc} PMSSV007CreateSQL
	Retorna todas as querys montadas
	@author Squad CRM/Faturamento
	@since 18/06/2025
	@version 12.1.2410
	@return Nil
*/
Method PMSSV007CreateSQL() Class budgetregistersSmartViewBusinessObject 
    
	Local cFieldsQry := "" as character
    Local cQuery     := "" as character
    Local cNameTmp   := "" as character
    Local nParam     := 1  as numeric
    Local aFiliais	 := {} as array
    Local oQuery     := Nil as object
	Local oTempTableBranch := Nil as object
	
	// Função que verifica se há filiais informadas no parâmetro
	aFiliais := FatGetMVFiliais(MV_FIL)
    
	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "AF1", "AF1_FILIAL")

    cNameTmp := oTempTableBranch:GetTableNameForQuery()
	
	//Trata campos do aFields para utilizar na query
	cFieldsQry := "AF1_FILIAL, AF1_ORCAME, AF1_DESCRI, AF1_CLIENT, AF1_LOJA, A1_NOME, AF1_DATA, AF1_FASE, AF1_TRUNCA"

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({},@self:aStruct, @cFieldsQry,self:getCustomFields(), .T., .F., .T.)

	cQuery := " SELECT ? "
	cQuery += " ? " //Select para objeto localizado
	cQuery += " FROM " + RetSqlName("AF1") + " AF1 "
	cQuery += " INNER JOIN ? TMPFIL"
	cQuery += " ON TMPFIL.TMP_FILIAL = AF1.AF1_FILIAL"	
	cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 ON ? "
	cQuery += " AND A1_COD = AF1_CLIENT "
	cQuery += " AND A1_LOJA = AF1_LOJA "
	cQuery += " AND SA1.D_E_L_E_T_ = ? "
	cQuery += " WHERE AF1_FILIAL = TMPFIL.TMP_FILIAL "
	cQuery += " AND AF1_CLIENT BETWEEN ? AND ? "
	cQuery += " AND AF1_LOJA BETWEEN ? AND ? "
	cQuery += " AND AF1_ORCAME BETWEEN ? AND ? "
	cQuery += " AND AF1_DATA BETWEEN ? AND ? "
	cQuery += " ? " //Where que foi populado no objeto localizado
	cQuery += " AND AF1.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY AF1_FILIAL, AF1_ORCAME, AF1_DESCRI "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery) )
	oQuery:SetUnsafe(nParam++ , cFieldsQry)
	oQuery:SetUnsafe(nParam++ ,  self:cSelectLoc ) //Select para objeto localizado
	oQuery:SetUnsafe(nParam++ , cNameTmp)
	oQuery:SetUnsafe(nParam++ , FwJoinFilial("SA1", "AF1"))
	oQuery:SetString(nParam++ , ' ')
	oQuery:SetString(nParam++ , MV_PAR12)
	oQuery:SetString(nParam++ , MV_PAR14)
	oQuery:SetString(nParam++ , MV_PAR13)
	oQuery:SetString(nParam++ , MV_PAR15)
	oQuery:SetString(nParam++ , MV_PAR01)
	oQuery:SetString(nParam++ , MV_PAR02)
	oQuery:SetDate(nParam++ , MV_PAR03)
	oQuery:SetDate(nParam++ , MV_PAR04)
	oQuery:SetUnsafe(nParam++ ,  self:cWhereLoc)
	oQuery:SetString(nParam++ , ' ')

	self:cAliasAF1 := oQuery:OpenAlias()

    oTempTableBranch:Delete()
    FWFreeObj(oTempTableBranch)

    oQuery:Destroy()
    FreeObj(oQuery)
	oQuery:= Nil
    
	aSize(aFiliais,0) 

Return
