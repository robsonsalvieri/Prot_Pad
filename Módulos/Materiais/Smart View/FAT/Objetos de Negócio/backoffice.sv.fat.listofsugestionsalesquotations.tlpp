#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofsugestionsalesquotations.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofsugestionsalesquotations.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SB1,SBH,SBG", name="Sugestões de Orçamento", country="ALL", customTables="SBH")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofsugestionsalesquotationsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para sugestões de orçamentos. 

@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class listofsugestionsalesquotationsSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV030CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data ojItems      as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listofsugestionsalesquotationsSmartViewBusinessObject
	
	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Sugestões de Orçamento"
    self:setDescription(STR0003)	//"Listagem Sugestões de Orçamento"
    self:setIsLookUp(.T.)  

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listofsugestionsalesquotationsSmartViewBusinessObject

    Local cDescri       := ""               as character
    Local cProduto      := ""               as character

    Local nX            := 0                as numeric
    Local nScan         := 0                as numeric

    Local aPDFields     := {}               as array
    Local aValFields    := {}               as array

    Local lObfuscated   := .F.              as logical

    Local xValue		:= Nil

    Private aCposCustom := self:getCustomFields() as array

    Private MV_FIL		:= ""  as character

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR025")

    //Seta indice das tabelas para pesquisa
    SB1->(dbSetOrder(1))
    SBG->(dbSetOrder(1))

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCposCustom)
    lObfuscated := Len(aPDFields) > 0

	//Irá montar as queries do objeto de negócio
    self:FatSV030CreateSQL()

    //Posiciona no registro inicial
    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())

        If cProduto <> (self:cAlias)->BH_PRODUTO
    	    SB1->(MsSeek(FatSVFilial('SB1', 'B1_FILIAL', (self:cAlias)->BH_FILIAL)+(self:cAlias)->BH_PRODUTO) )
            cDescri := Substr(SB1->B1_DESC,1,30)
        Else
            SB1->(MsSeek(FatSVFilial('SB1', 'B1_FILIAL', (self:cAlias)->BH_FILIAL)+(self:cAlias)->BH_CODCOMP) )
        EndIf

        SBG->(MsSeek(FatSVFilial('SBG', 'BG_FILIAL', (self:cAlias)->BH_FILIAL)+(self:cAlias)->BH_PRODUTO))
        aValFields  := {;    
                            SBG->BG_FILIAL,;
                            (self:cAlias)->BH_PRODUTO,;
                            cDescri,;
                            SBG->BG_GERAPV,;
                            SBG->BG_GERAOP,;
                            SBG->BG_GERAOPI,;
                            SBG->BG_GERAEMP,;
                            (self:cAlias)->BH_SEQUENC,;
                            (self:cAlias)->BH_CODCOMP,;
                            SB1->B1_DESC,;
                            (self:cAlias)->BH_QUANT;
                       }

        cProduto := (self:cAlias)->BH_PRODUTO

        self:ojItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)

            xValue := IIf(nX <= Len(aValFields), aValFields[nX], (self:cAlias)->&(self:aStruct[nX][5]))

            self:ojItems[self:aStruct[nX][1]] := IIf( lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
                                                    aPDFields[nScan][2],;
                                                    IIf( UPPER(self:aStruct[nX][3]) == "DATE",;
                                                        IIf(ValType(xValue) == "C", totvs.framework.treports.date.stringToTimeStamp(xValue), totvs.framework.treports.date.dateToTimeStamp(xValue)),;
                                                        xValue ) )

        Next

        self:processData(1)
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(dbSkip())

    EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields,   0)
    aSize(aValFields,  0)
    aSize(aCposCustom, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 18/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class listofsugestionsalesquotationsSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local aCmpsRep      := {} as array
    Local aParameters 	:= {} as array

    self:aFields := {"BH_FILIAL", "BH_PRODUTO", "B1_DESC", "BG_GERAPV", "BG_GERAOP", "BG_GERAOPI", "BG_GERAEMP","BH_SEQUENC","BH_CODCOMP","DESCCOMP","BH_QUANT"}
    self:aStruct := FatTrGetStruct(self:aFields)

    aadd(aCmpsRep, {"DESCCOMP",	STR0004, "string", STR0004, "B1_DESC"} )

    For nX := 1 to Len(aCmpsRep)
        aEval(self:aStruct, {|x| IIF(X[1] == aCmpsRep[nX][1] , x := aCmpsRep[nX] , .T.  )})
    Next

    For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam("MTR025")

    self:oSchema:addParameter("MV_FIL", STR0005 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) //Filial

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nx][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial ?
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SB1", 2) //Produto de ?
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SB1", 2) //Produto ate ?
	EndIf

    aSize(aCmpsRep, 0)
    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv029CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 18/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV030CreateSQL() Class listofsugestionsalesquotationsSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	//Monta campos do Select
    cFieldsQry := "SBH.BH_FILIAL,SBH.BH_PRODUTO,SBH.BH_SEQUENC,SBH.BH_CODCOMP,SBH.BH_QUANT"
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCposCustom, .T., .F., .T.)

    // Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        cQuery  += "SELECT ? "
        cQuery  += "  FROM " + RetSqlName("SBH") + " SBH "
        cQuery  += "WHERE SBH.BH_FILIAL   = ? "
        cQuery  += "  AND SBH.BH_PRODUTO >= ? "
        cQuery  += "  AND SBH.BH_PRODUTO <= ? "
        cQuery  += " ? "    //Where que foi populado no objeto localizado
        cQuery  += "  AND SBH.D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY SBH.BH_FILIAL, SBH.BH_PRODUTO"
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)
        oQuery:SetString(nParam++, FatSVFilial('SBH', 'BH1_FILIAL', aFiliais[nX] ) )
        oQuery:SetString(nParam++, MV_PAR01)
        oQuery:SetString(nParam++, MV_PAR02)
        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
