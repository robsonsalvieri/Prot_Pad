#include "protheus.ch"
#include "fwmvcdef.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "msobject.ch"

namespace totvs.protheus.backoffice.sv.fat.paramutil

//-------------------------------------------------------------------
/*/{Protheus.doc} FatSvParamUtil
Classe da API REST para retorno dos parâmetros tipo combo/list a serem utilziados no SmartView.
@author CRM/FAT
@since 08/11/2024
@version 1.0    
/*/
//-------------------------------------------------------------------
Class FatSvParamUtil

    private data cFuncao as character
    private data cModulo as character
    private data cObjeto as character
    private data cData   as character
    private data cOpcao  as character
    private data jPath   as object
    private data aOptions as array

    public method New()

    @Get("/api/faturamento/smartview/v1/options/:modulo/:objeto/:funcao/:opcao")
    public method getComboParam()
    
EndClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
Construtor da classe

@author CRM/FAT
@since 08/11/2024
@version 1.0
/*/
//-------------------------------------------------------------------
Method new() Class FatSvParamUtil

    ::cFuncao   := ''
    ::cModulo   := ''
    ::cObjeto   := ''
    ::cData     := ''
    ::cOpcao    := ''
    ::jPath     := Nil
    ::aOptions  := {}

Return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} getComboParam
Retorna o combo conforme Path Params passado.

@return oRest:setResponse (com JSON de retorno da api)

@author CRM/FAT
@since 08/11/2024
@version 1.0    
/*/
//-------------------------------------------------------------------
Method getComboParam() Class FatSvParamUtil

    ::jPath := oRest:getPathParamsRequest()
    If ( ::jPath <> Nil )
        ::cModulo  := ::jPath[ 'modulo' ]
        ::cObjeto  := ::jPath[ 'objeto' ]        
        ::cfuncao  := ::jPath[ 'funcao' ]
        ::cOpcao   := ::jPath[ 'opcao' ]  
    EndIf    
    
    ::aOptions := GetOpcoes( ::cModulo , ::cObjeto, ::cfuncao, ::cOpcao )
    ::cData    := processGetCombo( ::aOptions )

    aSize(::aOptions, 0)

Return oRest:setResponse( ::cData )

// ------------------------------------------------------------------ //
/*/{Protheus.doc} GetOpcoes
Busca o array com as opções do combo, conforme definido no
no objeto de negócio.

@param		cModulo Código do módulo (FAT/CRM/TMK)	
@param		cObjeto Nome do objeto de negócio.
@param		cFuncao Função do objeto de negócio.
@param		cOpcao  Parâmetro que será envaido na função.        

@return aReturn: Array com as opções do combo.

@author CRM/FAT
@since 08/11/2024
@version 1.0    
/*/
// ------------------------------------------------------------------ //
Static Function GetOpcoes( cModulo as character, cObjeto as character, cFuncao as character, cOpcao as character )

    Local aReturn   := {}  as array
    Local lFound    := .F. as logical
    Local cFunc     := ''  as character
    Local cInName   := 'totvs.protheus.backoffice.sv.' as character
    Local cFnName   := '.treportsintegratedprovider.'  as character

    cFunc   := cInName + cModulo + '.' + cObjeto + cFnName + cFuncao
    lFound  := tlpp.ffunc( cFunc )

    If lFound
        aReturn := tlpp.call( cFunc, cOpcao )
    EndIf

Return aReturn

// ------------------------------------------------------------------ //
/*/{Protheus.doc} processGetCombo
Processa o array formatando o JSON a ser retornado na API, para
os combos.

@param		aOpcoes Array com as opções que serão retornadas.
        
@return cReturn: JSON formatado para retornar na API.

@author CRM/FAT
@since 08/11/2024
@version 1.0
/*/
// ------------------------------------------------------------------ //
Static Function processGetCombo( aOpcoes as array)

    Local cReturn := '' as character
    Local nX      := 0  as numeric

    cReturn := '{ "data": ['
 
    For nX := 1 to Len( aOpcoes )
        cReturn += '{'
        cReturn += '"key": "'   + AllTrim(Str(nX)) + '",' 
        cReturn += '"label": "' +  Alltrim(aOpcoes[nX]) +'" '
        cReturn += '}'
        cReturn += IIf(nX < Len( aOpcoes ), ',', "")
    Next nX

    cReturn += ']}'

Return cReturn
