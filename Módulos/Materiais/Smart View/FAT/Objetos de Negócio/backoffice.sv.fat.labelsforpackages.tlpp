#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.labelsforpackages.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.labelsforpackages.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF2,SA1,SA2,SA4", name="Etiquetas para embalagens", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} labelsforpackagesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Impressão de Etiquetas
para embalagens

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class labelsforpackagesSmartViewBusinessObject From IntegratedProvider

    public method new()			as object
    public method getData()		as object
    public method getSchema()	as object

	public method FatSV023CreateSQL()

    protected data aFields		as array
    protected data aStruct		as array
    protected data jItems		as json
    protected data cAlias		as character
    protected data cSelectLoc	as character
    protected data cWhereLoc	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class labelsforpackagesSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//Faturamento
    self:setDisplayName(STR0002)	//Etiquetas para embalagens
    self:setDescription(STR0003)	//Emissão de etiquetas para embalagens
    self:setIsLookUp(.T.)  			//Define que terá consulta de LookUp

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class labelsforpackagesSmartViewBusinessObject

	Local cPedi			:= ""	as character
	Local cX			:= ""	as character
	Local cCliFor		:= ""	as character
	Local cInfVol		:= "" 	as character
	Local cEndCom		:= ""	as character
	Local xValue		:= ""	as character

    Local G				:= 0    as numeric
	Local nX            := 0    as numeric
	Local nVolume		:= 0    as numeric
	Local nVolumex		:= 0    as numeric
	Local nScan			:= 0    as numeric

    Local lObfuscated   := .F.  as logical

    Local aPDFields     := {}   as array

	Private aFieldsValue:= {} 	as array
	Private aCpoCustom	:= self:getCustomFields() as array

	Private MV_FIL		:= "" as character

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR710") 

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := Len(aPDFields) > 0

    aFieldsValue:= {{"D2_PEDIDO", 	{"D2_PEDIDO",	"", 'cPedi'}},;
					{"F2_VOLUME1", 	{"F2_VOLUME1", 	"", 'nVolumex'}},;
					{"F2_ESPECI1", 	{"F2_ESPECI1", 	"", 'cX'}},;
                    {"CLIFOR",	  	{"CLIFOR", 		"", 'cCliFor'}},;
					{"INFVOL",	  	{"INFVOL", 		"", 'cInfVol'}},;
					{"ENDCOM",	  	{"ENDCOM", 		"", 'cEndCom'}}}

	//Irá montar as queries do objeto de negócio
    self:FatSV023CreateSQL()

    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

	SD2->(dbSetOrder(3))

	While !(self:cAlias)->(Eof())

		//Verifica se mesmo mudando a Nota fiscal o pedido é o mesmo
		//imprime apenas uma vez a etiqueta de volume
		SD2->(MsSeek((self:cAlias)->(F2_FILIAL+F2_DOC+F2_SERIE+F2_CLIENTE+F2_LOJA)))

		If !(MV_PAR04 == 1 .And. cPedi == SD2->D2_PEDIDO)

			cPedi := SD2->D2_PEDIDO

			cCliFor := IIf((self:cAlias)->F2_TIPO $ "DB",STR0005,STR0004) //"Fornecedor:"#"Cliente:"

			For G := 1 to 4

				nVolume := (self:cAlias)->&("F2_VOLUME"+str(G,1))
				nVolumex := nVolume
				While nVolume != 0
					cX 		:= (self:cAlias)->&("F2_ESPECI"+str(G,1))
					cEndCom := Trans(AllTrim((self:cAlias)->&("A1_CEP")),"@R 99999-999") + " " + AllTrim((self:cAlias)->&("A1_MUN"))+ "/" + AllTrim((self:cAlias)->&("A1_EST"))
					cInfVol := AllTrim(STR((nVolumex-nVolume)+1))+"/"+AllTrim(STR(nVolumex))

					//Montagem json para Append
					self:jItems := JsonObject():new()
					For nX := 1 To Len(self:aStruct)

						xValue := IIF((nScan := aScan(aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
									FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2]),;
									"(self:cAlias)->"+self:aStruct[nX][5])

						self:jItems[self:aStruct[nX][1]] := IIF(lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0,;
																aPDFields[nScan][2],;
																IIF(lObfuscated .and. self:aStruct[nX][1] == "ENDCOM",;
																	FwProtectedDataUtil():ValueAsteriskToAnonymize(cEndCom),;
																	IIF(UPPER(self:aStruct[nX][3]) == "DATE",;
																		IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue))),;
																		&(xValue))))

					Next nX

					self:processData(1) 				//Método para inclusão dos campos localizados no "self:jItems"
					self:oData:appendData(self:jItems) 	//Efetua append dos registros para o objeto do SmartView

					nVolume--

				EndDo

			Next

			cPedi := ""
			
		EndIf

		(self:cAlias)->(dbSkip())

	EndDo

    //Fechamento de tabelas e limpeza do objeto/variável
    (self:cAlias)->(DBCloseArea())

    aSize(aPDFields,    0)
	aSize(aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class labelsforpackagesSmartViewBusinessObject

    Local nX 		   := 0 as numeric
	Local aStruCpoFake := {} as array
	Local aParameters  := {} as array

    self:aFields := {"F2_FILIAL","D2_PEDIDO","F2_DOC","F2_SERIE","F2_TIPO","F2_TIPODOC","F2_CLIENTE","F2_LOJA","F2_TRANSP","F2_VOLUME1","F2_ESPECI1","A1_NOME",;
					 "A1_ENDENT","A1_END","A1_CEP","A1_MUN","A1_EST","A2_NOME","A2_END","A2_CEP","A2_MUN","A2_EST","A4_NOME","CLIFOR","ENDCOM","INFVOL"}

	//Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"CLIFOR", STR0006, "string"},; //Cliente/Fornecedor
					 {"INFVOL", STR0007, "string"},; //Volume
                     {"ENDCOM", STR0008, "string"}}  //CEP

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][2], self:aStruct[nX][5])
    Next nX

	// Busca na SX1 os parâmetros que serão utilizados
	aParameters := FtSVSetParam('MTR710')

	// Parâmetros para filtro do relatório
    self:oSchema:addParameter("MV_FIL", STR0009 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nx][5] )
	Next nX


	If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2) 						//Filial ?
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SF2", 2) 						//Nota Fiscal de ?
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SF2", 2) 						//Nota Fiscal ate ?
		self:setCustomURL("MV_PAR04", "api/faturamento/smartview/v1/options/fat/labelsforpackages/SVCBFATSV023/1", 1)	//Emite por ?
    EndIf

	aSize(aStruCpoFake, 0)
	aSize(aParameters,  0)

Return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} SVCBFATSV023
Retorna as opçoes disponível para parametros do tipo combo.

@return array: Array com as opçoes

@author FAT/CRM/
@since 08/11/2024
@version 1.0
*/
//-------------------------------------------------------------------   
Function SVCBFATSV023(cOpcao)

	Local aRet := {} as array

	If cOpcao == "1"
		aRet := FatGetCboX1('MTR710', 4)
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv023CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 08/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV023CreateSQL() class labelsforpackagesSmartViewBusinessObject

	Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	cFieldsQry := "F2_FILIAL,F2_DOC,F2_SERIE,F2_TIPO,F2_TIPODOC,F2_CLIENTE,F2_LOJA,F2_TRANSP,F2_VOLUME1,F2_ESPECI1,A1_NOME,A1_ENDENT,A1_END,A1_CEP,A1_MUN, "
	cFieldsQry += "A1_EST,A2_NOME,A2_END,A2_CEP,A2_MUN,A2_EST,A4_NOME,F2_VOLUME2,F2_ESPECI2,F2_VOLUME3,F2_ESPECI3,F2_VOLUME4,F2_ESPECI4 "
	cFieldsQry += self:cSelectLoc

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .T., .T.)

    // Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

		cQuery += "SELECT ? "
		cQuery += "	FROM " 	 	+ RetSqlName("SF2") + " SF2 "
		cQuery += "LEFT JOIN " 	+ RetSqlName("SA1") + " SA1 "
		cQuery += "  ON SA1.A1_FILIAL  = ? "
		cQuery += " AND SA1.A1_COD     = SF2.F2_CLIENTE "
		cQuery += " AND SA1.A1_LOJA    = SF2.F2_LOJA "
		cQuery += " AND SA1.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " 	+ RetSqlName("SA2") + " SA2 "
		cQuery += "  ON SA2.A2_FILIAL  = ? "
		cQuery += " AND SA2.A2_COD     = SF2.F2_CLIENTE "
		cQuery += " AND SA2.A2_LOJA    = SF2.F2_LOJA "
		cQuery += " AND SA2.D_E_L_E_T_ = ? "
		cQuery += "LEFT JOIN " 	+ RetSqlName("SA4") + " SA4 "
		cQuery += "  ON SA4.A4_FILIAL  = ? "
		cQuery += " AND SA4.A4_COD     = SF2.F2_TRANSP "
		cQuery += " AND SA4.D_E_L_E_T_ = ? "
		cQuery += "WHERE SF2.F2_FILIAL = ? "
		cQuery += " AND ? = ? "
		cQuery += " AND SF2.F2_DOC 	  >= ? "
		cQuery += " AND SF2.F2_DOC    <= ? "
		cQuery += " AND NOT (?) "
		cQuery += " ? "
		cQuery += "	AND SF2.D_E_L_E_T_ = ? "

		If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

		oQuery:SetUnsafe(nParam++ , cFieldsQry )
		oQuery:SetString(nParam++ , FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++ , FatSVFilial('SA2', 'A2_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++ , FatSVFilial('SA4', 'A4_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++ , FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]) )
		oQuery:SetUnsafe(nParam++, SerieNfId("SF2",3,"F2_SERIE") )
		oQuery:SetString(nParam++ , MV_PAR01 )
		oQuery:SetString(nParam++ , MV_PAR02 )
		oQuery:SetString(nParam++ , MV_PAR03 )
		oQuery:SetUnsafe(nParam++, IsRemito(2,'F2_TIPODOC'))
		oQuery:SetUnsafe(nParam++, self:cWhereLoc)
		oQuery:SetString(nParam++, ' ')

    Next nX

	oQuery:SetUnsafe(nParam++, IIf( MV_PAR04 == 1, "SF2.F2_DOC, SF2.F2_SERIE", "SF2.F2_DOC, SF2.F2_SERIE, SF2.F2_CLIENTE, SF2.F2_LOJA" ) )

	self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
