#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.customerlist.ch"

Static __LookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.customerlist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1", name="Clientes", country="ALL", customTables="SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} customerlistSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Clientes

@author FAT/CRM
@since 20/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class customerlistSmartViewBusinessObject From IntegratedProvider

    public method new()               as object
    public method getData()           as object
    public method getSchema()         as object

    public method FatSV001CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
	protected data cAlias       as character
    protected data ojItems      as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 21/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class customerlistSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Clientes"
    self:setDescription(STR0003)	//"Relação de Clientes"
    self:setIsLookUp(.T.) 

    self:cAlias         := ""
	self:cWhereLoc      := ""
	self:aFields        := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class customerlistSmartViewBusinessObject

    Local aPDFields     := {}  as array

    Local nX            := 0   as numeric

    Local lObfuscated   := .F. as logical

    Private aCpoCustom  := self:getCustomFields() as array

    Private MV_FIL		:= ""  as character
    Private CODDE		:= ""  as character
    Private CODATE		:= ""  as character
    Private LOJADE		:= ""  as character
    Private LOJAATE		:= ""  as character

    //Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "")

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := Len( aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
    self:FatSV001CreateSQL()

    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())

        self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            self:ojItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias)

        Next

        self:processData(1) //Tratamento para localizados
        self:oData:appendData(self:ojItems)

        (self:cAlias)->(DBSkip())

    EndDo 

    //Fechamento da tabela
    (self:cAlias)->(DBCloseArea())

    //Limpeza variaveis
    aSize(aPDFields,  0)
    aSize(aCpoCustom, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class customerlistSmartViewBusinessObject

    Local nX := 0 as numeric

    self:aFields := {"A1_FILIAL", "A1_COD", "A1_LOJA", "A1_NOME", "A1_NREDUZ", "A1_EST", "A1_TIPO", "A1_CGC", "A1_DTCAD"}
    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

    self:oSchema:addParameter("MV_FIL", STR0010, "string", .T.,,,,,, FTSVHelp(,,,.T.))      //Filial
    self:oSchema:addParameter("CODDE",  STR0006, "string", .F.,,,,,, FTSVHelp("A1_COD"))    //Código de
    self:oSchema:addParameter("CODATE", STR0007, "string", .F.,,,,,, FTSVHelp("A1_COD"))    //Código até
    self:oSchema:addParameter("LOJADE", STR0008, "string", .F.,,,,,, FTSVHelp("A1_LOJA"))   //Loja de
    self:oSchema:addParameter("LOJAATE",STR0009, "string", .F.,,,,,, FTSVHelp("A1_LOJA"))   //Loja até

    //Validação para versão da Lib
    If __LookUp
		self:setCustomURL("MV_FIL", "api/framework/v1/genericLookupService/smartview/SM0", 2)
		self:setCustomURL("CODDE",  "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("CODATE", "api/framework/v1/genericLookupService/smartview/SA1", 2)
	EndIf

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv001CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 21/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV001CreateSQL() class customerlistSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := "SA1.A1_FILIAL,SA1.A1_COD,SA1.A1_LOJA,SA1.A1_NOME,SA1.A1_NREDUZ,SA1.A1_EST,SA1.A1_TIPO,SA1.A1_CGC,SA1.A1_DTCAD" 
    cFieldsQry += self:cSelectLoc

	// Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, aCpoCustom, .T., .T., .T.)

    // Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        //-----------------------------------------------------
        // Query Principal
        // BIND - Indice SA1 (1): A1_FILIAL, A1_COD, A1_LOJA
        //-----------------------------------------------------
        cQuery  += " SELECT ? "
        cQuery  += " FROM ? SA1 "
        cQuery  += " WHERE SA1.A1_FILIAL = ? "
        cQuery  += "    AND SA1.A1_COD BETWEEN ? AND ? "
        cQuery  += "    AND SA1.A1_LOJA BETWEEN ? AND ? "
        cQuery  += "    ? "
        cQuery  += "    AND SA1.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += "  UNION ALL "
		Else
			cQuery += "  ORDER BY SA1.A1_FILIAL, SA1.A1_COD, SA1.A1_LOJA "
		EndIf

    Next nX

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++ , cFieldsQry)
        oQuery:SetUnsafe(nParam++ , RetSqlName("SA1"))
        oQuery:SetString(nParam++ , FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++ , CODDE)
        oQuery:SetString(nParam++ , CODATE)
        oQuery:SetString(nParam++ , LOJADE)
        oQuery:SetString(nParam++ , LOJAATE)
        oQuery:SetUnsafe(nParam++ , self:cWhereLoc)
        oQuery:SetString(nParam++ , " ")

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
