#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.commissionsreport.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.commissionsreport.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SA3,SE3", name="Relação de Comissões", country="ALL", customTables="SA1,SA3,SE3")

//-------------------------------------------------------------------
/*/{Protheus.doc} commissionsreportSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Comissões

@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class commissionsreportSmartViewBusinessObject From IntegratedProvider

    public method new()          as object
    public method getData()      as object
    public method getSchema()    as object

    public method FatSV019CreateSQL()

    protected data aFields       as array
    protected data aStruct       as array
    protected data cAlias        as character
    protected data cSelectLoc    as character
    protected data cLocLeftJoin1 as character
    protected data cLocLeftJoin2 as character
    protected data cWhereLoc     as character
    protected data ojItems       as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class commissionsreportSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0003)     //Faturamento
    self:setDisplayName(STR0001) //Comissões
    self:setDescription(STR0002) //Relação de Comissões
    self:setIsLookUp(.T.)        //Define que terá consulta de LookUp

    self:aFields        := {}
    self:cSelectLoc     := ""
    self:cWhereLoc      := ""
    self:cLocLeftJoin1  := ""
    self:cLocLeftJoin2  := ""
    self:cAlias         := ""

Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 23/05/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class commissionsreportSmartViewBusinessObject

    Local cLiquid       := "" as character
    Local cNumLiq       := "" as character
    Local cTipoReg      := "" as character
    Local cTipoPag      := "" as character
    Local xValue        := "" as character
    Local cPrefRec      := "" as character
    Local cNuTiRec      := "" as character
    Local cParcRec      := "" as character
    Local cClieRec      := "" as character
    Local cTipoRec      := "" as character
    Local cLojaRec      := "" as character
    Local cNomClRed     := "" as character
    Local cNomeCliLiq   := "" as character

    Local nX            := 0 as numeric
    Local nY            := 0 as numeric
    Local nTaxa         := 0 as numeric
    Local nTaxaE1       := 0 as numeric
    Local nMoedaE1      := 0 as numeric
    Local nDecs         := 0 as numeric
    Local nBasePrt      := 0 as numeric
    Local nComPrt       := 0 as numeric
    Local nVlrTitulo    := 0 as numeric
    Local nPercent      := 0 as numeric
    Local nVlrTotE1     := 0 as numeric
    Local nVlrCruz      := 0 as numeric
    Local nTotLiq       := 0 as numeric
    Local nScan         := 0 as numeric
    Local nVlrLiq       := 0 as numeric
    Local nVlrBaseLiq   := 0 as numeric
    Local nVlrTitLiq    := 0 as numeric
    Local nTamCOMIS     := FWSX3Util():GetFieldStruct("E3_COMIS")[3] as numeric

    Local dEmissao      := CTOD("") as date
    Local dVencto       := CTOD("") as date
    Local dBaixa        := CTOD("") as date
    Local dLiq          := CTOD("") as date

    Local lFA440LIQSE1  := FindFunction("FA440LIQSE1") as logical
    Local lObfuscated   := .F. as logical

    Local aLiquid       := {} as array
    Local aValLiq       := {} as array
    Local aLiqProp      := {} as array
    Local aPDFields     := {} as array

    Private aFieldsValue := {} as array
    Private aCpoCustom   := self:getCustomFields() as array

    Private MV_FIL		 := ""       as character
    Private MV_DOVEN	 := ""       as character
    Private MV_ATVEN	 := ""       as character
    Private MV_DEMAJ	 := 1        as numeric
    Private MV_TPVEN	 := 1        as numeric
    Private MV_DATDE     := Date()   as date
    Private MV_DATAT     := Date()   as date

    //Metodo para retorno do json dos parâmetros
    FatSetValueMVPAR(oFilter:getParameters(), "MTR540")

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_PAR08 := IIf(MV_PAR08 <= 0 .Or. MV_PAR08 > ContaMoeda(), 1, MV_PAR08)

    aFieldsValue:= {{"E1_TIPREG",       {"E1_TIPREG",       "", 'cTipoReg'}},;
                    {"E1_VENCTO",       {"E1_VENCTO",       "", 'dVencto'}},;
                    {"E1_BAIXA",        {"E1_BAIXA",        "", 'dBaixa'}},;
                    {"E1_VLRREAL",      {"E1_VLRREAL",      "", 'nVlrTitulo'}},;
                    {"E3_BASE",         {"E3_BASE",         "", 'nBasePrt'}},;
                    {"E3_PORC",         {"E3_PORC",         "", 'nPercent'}},;
                    {"E3_COMIS",        {"E3_COMIS",        "", 'nComPrt'}},;
                    {"E3_BAIEMI",       {"E3_BAIEMI",       "", 'cTipoPag'}},;
                    {"E3_AJUSTE",       {"E3_AJUSTE",       "", 'IIf(((self:cAlias)->E3_AJUSTE == "S" .And. MV_DEMAJ == 1),"AJUSTE","")'}},;
                    {"E1_NUMLIQ",       {"E1_NUMLIQ",       "", 'cLiquid'}},;
                    {"E1_PREFIXO",      {"E1_PREFIXO",      "", "cPrefRec"}},;
                    {"E1_NUM",          {"E1_NUM",          "", "cNuTiRec"}},;
                    {"E1_PARCELA",      {"E1_PARCELA",      "", "cParcRec"}},;
                    {"E1_TIPO",         {"E1_TIPO",         "", "cTipoRec"}},;
                    {"E1_CLIENTE",      {"E1_CLIENTE",      "", "cClieRec"}},;
                    {"E1_LOJA",         {"E1_LOJA",         "", "cLojaRec"}},;
                    {"NOMECLIENTELIQ",  {"NOMECLIENTELIQ",  "", 'cNomeCliLiq'}},;
                    {"E1_NOMCLI",       {"E1_NOMCLI",       "", "cNomClRed"}},;
                    {"E1_VALOR",        {"E1_VALOR",        "", 'nVlrTitLiq'}},;
                    {"E1_DTACRED",      {"E1_DTACRED",      "", 'dLiq'}},;
                    {"E1_VALLIQ",       {"E1_VALLIQ",       "", 'nVlrLiq'}},;
                    {"VALORBASELIQ",    {"VALORBASELIQ",    "", 'nVlrBaseLiq'}}}

    nDecs      := GetMv("MV_CENT"+(IIF(MV_PAR08 > 1 , STR(MV_PAR08,1),"")))

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := Len( aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
    self:FatSV019CreateSQL()

    //Posiciona no registro inicial
	(self:cAlias)->(dbGoTop())

    SA1->(dbSetOrder(1))
    SE1->(dbSetOrder(1))
    SA3->(dbSetOrder(2))
    SF1->(dbSetOrder(1))
    SF2->(dbSetOrder(1))

	While !(self:cAlias)->(Eof())

        dBaixa      := CTOD("")
        dVencto     := CTOD("")
		nVlrTitulo  := 0
        nVlrTotE1   := 0
        nVlrCruz    := 0
        nMoedaE1    := 0
        nTaxaE1     := 0
        nTaxa       := 0
        nTotLiq     := 0
        nVlrBaseLiq := 0
        nVlrLiq     := 0
        nBasePrt    := 0
        nComPrt     := 0
        nVlrTitLiq  := 0
        nPercent    := 0
        cPrefRec    := ""
        cLiquid     := ""
        cNumLiq     := ""
        cNuTiRec    := ""
        cParcRec    := ""
        cClieRec    := ""
        cTipoRec    := ""
        cLojaRec    := ""
        cTipoPag    := ""
        cNomClRed   := ""
        cNomeCliLiq := ""

		If SE1->(MsSeek((self:cAlias)->E3_FILIAL+(self:cAlias)->E3_PREFIXO+(self:cAlias)->E3_NUM+(self:cAlias)->E3_PARCELA+(self:cAlias)->E3_TIPO))
    		nVlrTitulo  := Round(xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR08,SE1->E1_EMISSAO,nDecs+1),nDecs)
            dEmissao    := SE1->E1_EMISSAO
            dVencto     := SE1->E1_VENCTO
            nMoedaE1    := SE1->E1_MOEDA
            nTaxaE1     := SE1->E1_TXMOEDA
            nTaxa       := SE1->E1_VLCRUZ / SE1->E1_VALOR
            aLiquid     := {}
            aValLiq     := {}
            aLiqProp    := {}

            If MV_PAR13 == 1 .And. !Empty(SE1->E1_NUMLIQ) .And. lFA440LIQSE1
                cNumLiq := SE1->E1_NUMLIQ
                // Obtem os registros que deram origem ao titulo gerado pela liquidacao
                Fa440LiqSe1(SE1->E1_NUMLIQ, @aLiquid, @aValLiq)
                For nX := 1 to Len(aValLiq)
                    nTotLiq += aValLiq[nX,2]
                Next
                For nX := 1 to Len(aValLiq)
                    aAdd(aLiqProp,(nVlrTitulo/nTotLiq)*aValLiq[nX,2])
                Next
            EndIf

            dBaixa := IIf( (self:cAlias)->E3_BAIEMI == "B", STOD((self:cAlias)->E3_EMISSAO), SE1->E1_BAIXA )

            dbSelectArea(self:cAlias)
		Else
			If AllTrim((self:cAlias)->E3_TIPO) == "NCC"
                dbSelectArea("SF1")
				SF1->(MsSeek((self:cAlias)->E3_FILIAL+(self:cAlias)->E3_NUM+(self:cAlias)->E3_PREFIXO+(self:cAlias)->E3_CODCLI+(self:cAlias)->E3_LOJA,.t.))
			    nVlrTitulo := Round(xMoeda(SF1->F1_VALBRUT,SF1->F1_MOEDA,MV_PAR08,SF1->F1_DTDIGIT,nDecs+1,SF1->F1_TXMOEDA),nDecs)
			    dEmissao   := SF1->F1_DTDIGIT
			Else
                dbSelectArea("SF2")
		   		SF2->(MsSeek((self:cAlias)->E3_FILIAL+(self:cAlias)->E3_NUM+(self:cAlias)->E3_PREFIXO))
				nVlrTitulo := Round(xMoeda(SF2->F2_VALFAT,SF2->F2_MOEDA,MV_PAR08,SF2->F2_EMISSAO,nDecs+1,SF2->F2_TXMOEDA),nDecs)
		 		dEmissao   := SF2->F2_EMISSAO
			EndIf

			If Eof()
                nVlrTotE1   := 0
                nVlrCruz    := 0
				nVlrTitulo  := 0
				SE1->(MsSeek((self:cAlias)->E3_FILIAL+(self:cAlias)->E3_PREFIXO+(self:cAlias)->E3_NUM))
				While ( !SE1->(Eof()) .And. (self:cAlias)->E3_PREFIXO == SE1->E1_PREFIXO .And.;
					(self:cAlias)->E3_NUM == SE1->E1_NUM .And.;
					(self:cAlias)->E3_FILIAL == FatSVFilial('SE1', 'E1_FILIAL', (self:cAlias)->E3_FILIAL) )

					If ( SE1->E1_TIPO == (self:cAlias)->E3_TIPO     .And. ;
						SE1->E1_CLIENTE == (self:cAlias)->E3_CODCLI .And. ;
						SE1->E1_LOJA    == (self:cAlias)->E3_LOJA )

                        dVencto    := SE1->E1_VENCTO
                        nMoedaE1   := SE1->E1_MOEDA
                        nTaxaE1    := SE1->E1_TXMOEDA
						nVlrTotE1  += SE1->E1_VALOR
                        nVlrCruz   += SE1->E1_VLCRUZ
                        nVlrTitulo += Round(xMoeda(SE1->E1_VALOR,SE1->E1_MOEDA,MV_PAR08,SE1->E1_EMISSAO,nDecs+1),nDecs)
                        dEmissao   := IIf( Empty(dEmissao), SE1->E1_EMISSAO, CTOD("") )

					EndIf
					SE1->(dbSkip())
				EndDo
                nTaxa := nVlrCruz / nVlrTotE1
			EndIf
            dbSelectArea(self:cAlias)
		EndIf

		nBasePrt := IIf( MV_PAR08 == 1 .Or. nMoedaE1 == 1,;
                        Round(xMoeda((self:cAlias)->E3_BASE ,1,MV_PAR08,IIf(Empty(dEmissao),Nil,dEmissao),nDecs+1),nDecs),;
                        Round(xMoeda((self:cAlias)->E3_BASE ,1,MV_PAR08,IIf(Empty(dEmissao),Nil,dEmissao),nDecs+1,nTaxaE1,nTaxa),nDecs) )

        nComPrt  := IIf( MV_PAR08 == 1 .Or. nMoedaE1 == 1,;
                        Round(xMoeda((self:cAlias)->E3_COMIS,1,MV_PAR08,IIf(Empty(dEmissao),Nil,dEmissao),nTamCOMIS+1),nTamCOMIS),;
                        Round(xMoeda((self:cAlias)->E3_COMIS,1,MV_PAR08,IIf(Empty(dEmissao),Nil,dEmissao),nTamCOMIS+1,nTaxaE1,nTaxa),nTamCOMIS) )

        //Não apresenta comissões zeradas em outras moedas
		If (MV_PAR09 != 2 .And. MV_PAR08 == 1) .And. (nVlrTitulo != 0 .And. nBasePrt != 0 .And. nComPrt != 0)

            nVlrTitulo := IIf( nBasePrt < 0 .And. nComPrt < 0, nVlrTitulo * -1, nVlrTitulo)

            self:ojItems := JsonObject():new()
            For nX := 1 To Len(self:aStruct)

                cTipoReg := STR0007
                cTipoPag := IIf((self:cAlias)->E3_BAIEMI == "B", STR0008, STR0009)
                nPercent := (self:cAlias)->E3_PORC
                dLiq     := CTOD("")

                If (nScan := aScan(aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
                    xValue := FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2])
                Else
                    xValue := "(self:cAlias)->" + self:aStruct[nX][5]
                EndIf

                If lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
                    self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
                ElseIf lObfuscated .and. self:aStruct[nX][1] == "NOMECLIENTELIQ"
                    self:ojItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->A1_NOME )
                ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                    self:ojItems[self:aStruct[nX][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
                Else
                    self:ojItems[self:aStruct[nX][1]] := &(xValue)
                EndIf

            Next

            self:processData(1)                 //Método para inclusão dos campos localizados no "self:ojItems"
            self:oData:appendData(self:ojItems) //Efetuar append dos registros para o objeto do SmartView

            If mv_par13 == 1
                For nX := 1 To Len(aLiquid)
                    SE1->(MsGoto(aLiquid[nX]))
                    If SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', SE1->E1_FILIAL)+SE1->E1_CLIENTE+SE1->E1_LOJA))

                        cTipoPag    := ""
                        nComPrt     := 0
                        nBasePrt    := 0
                        nVlrTitulo  := 0
                        nPercent    := 0
                        cTipoReg    := STR0010
                        nVlrLiq     := aValLiq[nX,2]
                        nVlrBaseLiq := aLiqProp[nX]
                        cNomeCliLiq := SA1->A1_NOME
                        cNomClRed   := SA1->A1_NREDUZ
                        cPrefRec    := SE1->E1_PREFIXO
                        cNuTiRec    := SE1->E1_NUM
                        cParcRec    := SE1->E1_PARCELA
                        cClieRec    := SE1->E1_CLIENTE
                        cTipoRec    := SE1->E1_TIPO
                        cLojaRec    := SE1->E1_LOJA
                        nVlrTitLiq  := SE1->E1_VALOR
                        dLiq        := aValLiq[nX,1]
                        cLiquid     := cNumLiq

                        self:ojItems := JsonObject():new()
                        For nY := 1 To Len(self:aStruct)

                            If (nScan := aScan(aFieldsValue, {|x| x[1] == self:aStruct[nY][5]})) > 0
                                xValue := FatGetValueFields(aFieldsValue[nScan][1], "self:cAlias", aFieldsValue[nScan][2])
                            Else
                                xValue := "(self:cAlias)->" + self:aStruct[nY][5]
                            EndIf

                            If lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nY][5]})) > 0
                                self:ojItems[self:aStruct[nY][1]] := aPDFields[nScan][2]
                            ElseIf lObfuscated .and. self:aStruct[nY][1] == "NOMECLIENTELIQ"
                                self:ojItems[self:aStruct[nY][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize((self:cAlias)->A1_NOME )
                            ElseIf UPPER(self:aStruct[nY][3]) == "DATE"
                                self:ojItems[self:aStruct[nY][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
                            Else
                                self:ojItems[self:aStruct[nY][1]] := &(xValue)
                            EndIf

                        Next

                        self:processData(1)                 // Método para inclusão dos campos localizados no "self:ojItems"
                        self:oData:appendData(self:ojItems) // Efetuar append dos registros para o objeto do SmartView

                    EndIf
                Next
            EndIf

        EndIf

		(self:cAlias)->(dbSkip())

	EndDo

	(self:cAlias)->(dbCloseArea())

    aSize(aFieldsValue, 0)
    aSize(aCpoCustom,   0)
    aSize(aPDFields,    0)
    aSize(aLiqProp,     0)
    aSize(aValLiq,      0)
    aSize(aLiquid,      0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 18/05/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() As object Class commissionsreportSmartViewBusinessObject

    Local nY            := 0  as numeric
    Local nX            := 0  as numeric
    Local cTituloCpo    := "" as character
    Local aStruCpoFake  := {} as array
    Local aParameters   := {} as array

    self:aFields := {"E3_FILIAL", "E1_TIPREG", "E3_VEND", "A3_NOME", "E3_PREFIXO", "E3_NUM", "E3_PARCELA", "E3_CODCLI", "E3_LOJA", "A1_NOME", "A1_NREDUZ", "E3_EMISSAO",;
                     "E1_VENCTO", "E1_BAIXA", "E3_DATA", "E3_PEDIDO", "E1_VLRREAL", "E3_BASE", "E3_PORC", "E3_COMIS", "E3_BAIEMI", "E3_AJUSTE", "E1_NUMLIQ", "E1_PREFIXO",;
                     "E1_NUM",  "E1_PARCELA", "E1_TIPO", "E1_CLIENTE", "E1_LOJA", "NOMECLIENTELIQ", "E1_NOMCLI", "E1_VALOR", "E1_DTACRED", "E1_VALLIQ", "VALORBASELIQ"}

    //Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"NOMECLIENTELIQ", "", "string"},; //"Nome Cliente da Liquidação"
                     {"VALORBASELIQ",   "", "number"}}  //"Valor Base da Liquidação"

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

    For nX := 1 To Len(self:aStruct)
        If "NOMECLIENTELIQ" $ AllTrim(self:aStruct[nX][05])
            For nY := 1 To Len(self:aStruct)
                // Define o título para o campo fake de acordo com o nome da tabela SA1
                If "A1_NOME" $ AllTrim(self:aStruct[nY][05])
                    cTituloCpo := self:aStruct[nY][02]
                EndIf
            Next
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "VALORBASELIQ" $ AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0010
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        Else
            self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][02], self:aStruct[nX][05])
        EndIf
    Next

    //Retorna do SX1 os parâmetros que serão utilizados, no array os que não serão utilizados
    aParameters := FtSVSetParam('MTR540',,,{'02','03','04','05','07','10','11','12','14','15','16'})

    // Adiciona novos parametros
    self:oSchema:addParameter("MV_FIL"  , STR0027 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.))         // Filial ?
    self:oSchema:addParameter("MV_DATDE", STR0025 + " ?", "date",   .F.,,,,,, FTSVHelp("MTR540","02"))  // Data de ?
    self:oSchema:addParameter("MV_DATAT", STR0026 + " ?", "date",   .F.,,,,,, FTSVHelp("MTR540","03"))  // Data ate ?
    self:oSchema:addParameter("MV_DOVEN", STR0022 + " ?", "string", .F.,,,,,, FTSVHelp("MTR540","04"))  // Do vendedor ?
    self:oSchema:addParameter("MV_ATVEN", STR0023 + " ?", "string", .F.,,,,,, FTSVHelp("MTR540","05"))  // Até vendedor ?
    self:oSchema:addParameter("MV_TPVEN", STR0021 + " ?", "number", .F.,,,,,, FTSVHelp("MTR540","15"))  // Tipo de vendedor ?
    self:oSchema:addParameter("MV_DEMAJ", STR0024 + " ?", "number", .F.,,,,,, FTSVHelp("MTR540","07"))  // Demonstra ajuste ?

    For nX := 1 To Len(aParameters)
        self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nX][5] )
    Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL"  , "api/framework/v1/genericLookupService/smartview/SM0", 2)                     // Filial ?
        self:setCustomURL("MV_DOVEN", "api/framework/v1/genericLookupService/smartview/SA3", 2)                     // Do Vendedor ?
        self:setCustomURL("MV_ATVEN", "api/framework/v1/genericLookupService/smartview/SA3", 2)                     // Até o Vendedor ?
        self:setCustomURL("MV_PAR01", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/1", 1)  // Lista pela ?
        self:setCustomURL("MV_PAR06", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/2", 1)  // Considera quais ?
        self:setCustomURL("MV_DEMAJ", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/3", 1)  // Demonstra Ajuste ?
        self:setCustomURL("MV_PAR09", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/3", 1)  // Comissões Zeradas ?
        self:setCustomURL("MV_PAR13", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/3", 1)  // Imprime detalhes origem ?
        self:setCustomURL("MV_TPVEN", "api/faturamento/smartview/v1/options/fat/commissionsreport/SVFTCr001/4", 1)  // Tipo de vendedor ?
    EndIf

    aSize(aStruCpoFake, 0)
    aSize(aParameters,  0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFTCr001
Retorna as opçoes disponível para o parâmetro de ordem.

@return array: Array com as opçoes.

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFTCr001(cOpcao) 
	Local aRet := {} as array

	If cOpcao == "1" 
	 	aRet := { STR0009, STR0008, STR0011 }   //Emissão, Baixa, Ambos
    ElseIf cOpcao == "2" 
		aRet := { STR0012, STR0013, STR0011 }	//A Pagar, Pagas, Ambos
    ElseIf cOpcao == "3" 
		aRet := { STR0019, STR0020 }	        //Sim, Não
    ElseIf cOpcao == "4"
		aRet := FatGetCboX1('MTR540', 15 )	    //Todos, Parceiros, Vendedores
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv019CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 25/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV019CreateSQL() class commissionsreportSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := " SE3.E3_FILIAL, SE3.E3_VEND, SA3.A3_NOME, SE3.E3_BASE, SE3.E3_COMIS, SE3.E3_PORC, SE3.E3_PREFIXO, SE3.E3_NUM, SE3.E3_PARCELA, SE3.E3_TIPO, "
    cFieldsQry += " SE3.E3_CODCLI, SE3.E3_LOJA, SE3.E3_AJUSTE, SE3.E3_BAIEMI, SE3.E3_EMISSAO, SE3.E3_DATA, SE3.E3_PEDIDO, SE3.E3_SEQ, SA1.A1_NOME, SA1.A1_NREDUZ "
    cFieldsQry += self:cSelectLoc

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry,aCpoCustom , .T., .T., .T.)

    For nX := 1 To nNumFils

        cQuery += "SELECT ? "
        cQuery += " FROM "     + RetSqlName("SE3") + " SE3 "
        cQuery += "LEFT JOIN " + RetSqlName("SA3") + " SA3 "
        cQuery += " ON  SA3.A3_FILIAL   = ? "
        cQuery += " AND SA3.A3_COD      = SE3.E3_VEND "
        cQuery += " ? " // Ajuste de LEFT JOIN para objeto localizado
        cQuery += " AND SA3.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
        cQuery += " ON  SA1.A1_FILIAL   = ? "
        cQuery += " AND SA1.A1_COD      = SE3.E3_CODCLI "
        cQuery += " AND SA1.A1_LOJA     = SE3.E3_LOJA "
        cQuery += " ? " // Ajuste de LEFT JOIN para objeto localizado
        cQuery += " AND SA1.D_E_L_E_T_  = ? "
        cQuery += "WHERE SE3.E3_FILIAL  = ? "
        cQuery += " AND SE3.E3_EMISSAO  >= ? "
        cQuery += " AND SE3.E3_EMISSAO  <= ? "
        cQuery += " AND SE3.E3_VEND     >= ? "
        cQuery += " AND SE3.E3_VEND     <= ? "

        If MV_PAR01 == 1
            cQuery +=  " AND SE3.E3_BAIEMI <> ? "
        ElseIf MV_PAR01 == 2
            cQuery +=  " AND SE3.E3_BAIEMI  = ? "
        EndIf

        If MV_PAR06 == 1
            cQuery +=  " AND SE3.E3_DATA  = ? "
        ElseIf MV_PAR06 == 2
            cQuery +=  " AND SE3.E3_DATA <> ? "
        EndIf

        If MV_TPVEN == 2
            cQuery +=  " AND SA3.A3_TIPO  = ? "
        ElseIf MV_TPVEN == 3
            cQuery +=  " AND SA3.A3_TIPO <> ? "
        EndIf

        cQuery += " ? "
        cQuery += " AND SE3.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
            cQuery += " ORDER BY SE3.E3_FILIAL, SE3.E3_VEND, SE3.E3_PREFIXO, SE3.E3_NUM, SE3.E3_PARCELA, SE3.E3_SEQ "
        EndIf
    Next nX

    nParam := 1

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry )
        oQuery:SetString(nParam++, FatSVFilial('SA3', 'A3_FILIAL', aFiliais[nX]))
        oQuery:SetUnsafe(nParam++, self:cLocLeftJoin1 )
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQuery:SetUnsafe(nParam++, self:cLocLeftJoin2 )
        oQuery:SetString(nParam++, ' ' )
        oQuery:SetString(nParam++, FatSVFilial('SE3', 'E3_FILIAL', aFiliais[nX]))
        oQuery:SetString(nParam++, DtoS(MV_DATDE))
        oQuery:SetString(nParam++, DtoS(MV_DATAT))
        oQuery:SetString(nParam++, MV_DOVEN)
        oQuery:SetString(nParam++, MV_ATVEN)

        If MV_PAR01 == 1 .Or. MV_PAR01 == 2
            oQuery:SetString(nParam++, 'B')
        EndIf

        If MV_PAR06 == 1 .Or. MV_PAR06 == 2
            oQuery:SetString(nParam++, ' ')
        EndIf

        If MV_TPVEN == 3 .Or. MV_TPVEN == 2
            oQuery:SetString(nParam++, 'P')
        EndIf

        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
