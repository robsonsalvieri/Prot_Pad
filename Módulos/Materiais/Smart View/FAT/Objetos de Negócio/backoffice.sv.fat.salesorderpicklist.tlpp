#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesorderpicklist.ch"

Static __LookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.salesorderpicklist.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SC9,SDC,SB1", name="Pick-List de Pedidos de Venda", country="ALL", customTables="SC9")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesorderpicklistSmartViewBusinessObject
Classe para criação do Objeto de Pick-List

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class salesorderpicklistSmartViewBusinessObject From IntegratedProvider

    public method new() 		 		as object
    public method getData() 	 		as object
    public method getSchema() 	 		as object
	public method FatSv036CreateSQL()

    protected data ojItems 		as json
    protected data aFields 		as array
    protected data aStruct 		as array
	protected data aFieldsValue as array
    protected data cLocSelect	as character
    protected data cLocWhere	as character
	protected data cAliasSC9	as character
	protected data cQrySC9 		as character
	protected data cMVFilial	as character
	protected data lUsaLocal	as logical

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salesorderpicklistSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 	//Faturamento
    self:setDisplayName(STR0002)	//Pick-List de Pedidos de Venda
    self:setDescription(STR0003) 	//Relação de Pick-List de Pedidos de Venda
    self:setIsLookUp(.T.)			//Define que terá consulta de LookUp

	self:cLocSelect 	:= ""
	self:cLocWhere		:= ""
	self:cAliasSC9		:= ""
	self:cQrySC9 		:= ""
	self:cMVFilial		:= ""
	self:aFieldsValue 	:= {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class salesorderpicklistSmartViewBusinessObject

	Local aPDFields		:= {} as array
	Local cEndereco  	:= "" as character
	Local xValue		:= "" as character
	Local nX         	:= 0 as numeric
	Local nQtde      	:= 0 as numeric
    Local nScan         := 0 as numeric
	Local lObfuscated	:= .F. as logical

    // Tratativa para limpar o Cache do SuperGetMv()
    SuperGetMV() 										//Realiza a limpeza do cache
    self:lUsaLocal := SuperGetMV("MV_LOCALIZ") == "S"	//Busca o valor do parametro

	self:aFieldsValue := {{"DC_LOCALIZ", {"DC_LOCALIZ", "", 'cEndereco'}},;
					 	 {"DC_QUANT",   {"DC_QUANT",   "", 'nQtde'}}}

	MV_FIL := self:cMVFilial

	// Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR777")

	// Cria a query
    self:FatSv036CreateSQL()

    // Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len(aPDFields) > 0

    dbSelectArea(self:cAliasSC9)

	(self:cAliasSC9)->(DbGoTop())

	While !(self:cAliasSC9)->(Eof())

		If self:lUsaLocal
			cEndereco := AllTrim((self:cAliasSC9)->DC_LOCALIZ)
			nQtde     := Iif((self:cAliasSC9)->DC_QUANT > 0,(self:cAliasSC9)->DC_QUANT, (self:cAliasSC9)->C9_QTDLIB)
		Else
			cEndereco := ""
			nQtde     := (self:cAliasSC9)->C9_QTDLIB
		EndIf

		self:ojItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)
			If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
				xValue := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAliasSC9", self:aFieldsValue[nScan][2])
			Else
				xValue := "(self:cAliasSC9)->"+self:aStruct[nX][5]
			EndIf

			If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
				xValue := &(xValue)
				self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
				self:ojItems[self:aStruct[nX][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
			Else
				self:ojItems[self:aStruct[nX][1]] := &(xValue)
			EndIf
		Next

		self:processData(1)
		self:oData:appendData(self:ojItems)

		(self:cAliasSC9)->(dbSkip())

	EndDo

	(self:cAliasSC9)->(DbCloseArea())

	FwFreeArray(self:aFieldsValue)
	FwFreeArray(aPDFields)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() Class salesorderpicklistSmartViewBusinessObject

	Local nX 			:= 0 as numeric
	Local aParameters 	:= {} as array

	// Adiciona campos Padroes
	self:aFields := {"C9_FILIAL","C9_PRODUTO","B1_DESC","B1_UM","C9_QTDLIB","C9_LOCAL","DC_LOCALIZ",;
				  	 "C9_LOTECTL","C9_NUMLOTE","C9_DTVALID","C9_POTENCI","C9_PEDIDO"}

	self:aStruct := FatTrGetStruct(self:aFields)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	// Retorna do SX1 os parâmetros que serão utilizados
	aParameters := FtSVSetParam("MTR777")

	// Adiciona os parâmetros do pergunte no Schema
    self:oSchema:addParameter("MV_FIL", STR0004 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial ?

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	If __LookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SC5", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SC5", 2)
		self:setCustomURL("MV_PAR03", "api/framework/treports/integratedprovider/v1/options/MTR777/MV_PAR03", 1)
	EndIf

	FwFreeArray(aParameters)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv036CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv036CreateSQL() Class salesorderpicklistSmartViewBusinessObject

    Local nParam	 		:= 1   as numeric
	Local cFieldsQry 		:= ""  as character
	Local cNameTmp			:= ""  as character
    Local oQuery 	 		:= Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SC9", "C9_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := " SC9.C9_FILIAL, SC9.C9_PRODUTO, SB1.B1_DESC, SB1.B1_UM, SC9.C9_QTDLIB, SC9.C9_LOCAL, "
	cFieldsQry += " SC9.C9_LOTECTL, SC9.C9_NUMLOTE, SC9.C9_DTVALID, SC9.C9_POTENCI, SC9.C9_PEDIDO, "
	cFieldsQry += " SC9.C9_CLIENTE, SC9.C9_LOJA "
	cFieldsQry += IIf(self:lUsaLocal, ", SDC.DC_LOCALIZ, SDC.DC_QUANT ", "")
	cFieldsQry += self:cLocSelect // Adição de campos para objeto localizado no select

	// Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	self:cQrySC9 := "SELECT ? "
	self:cQrySC9 += " FROM " 		+ RetSqlName("SC9") + " SC9 "
	self:cQrySC9 += "INNER JOIN ? TMPFIL "
	self:cQrySC9 += " ON TMPFIL.TMP_FILIAL = SC9.C9_FILIAL "
	self:cQrySC9 += "LEFT JOIN "  + RetSqlName("SB1") + " SB1 "
	self:cQrySC9 += " ON  ? "
	self:cQrySC9 += " AND SB1.B1_COD     = SC9.C9_PRODUTO "
	self:cQrySC9 += " AND SB1.D_E_L_E_T_ = ? "

	If self:lUsaLocal
		self:cQrySC9 += "LEFT JOIN " + RetSqlName("SDC") + " SDC "
		self:cQrySC9 += " ON  ? "
		self:cQrySC9 += " AND SDC.DC_PEDIDO  = SC9.C9_PEDIDO "
		self:cQrySC9 += " AND SDC.DC_ITEM    = SC9.C9_ITEM "
		self:cQrySC9 += " AND SDC.DC_SEQ     = SC9.C9_SEQUEN "
		self:cQrySC9 += " AND SDC.DC_PRODUTO = SC9.C9_PRODUTO "
		self:cQrySC9 += " AND SDC.D_E_L_E_T_ = ? "
	EndIf

	self:cQrySC9 += "WHERE SC9.C9_FILIAL 	= TMPFIL.TMP_FILIAL "
	self:cQrySC9 += " AND  SC9.C9_PEDIDO >= ? "
	self:cQrySC9 += " AND  SC9.C9_PEDIDO <= ? "

	If MV_PAR03 == 1 .Or. MV_PAR03 == 3
		self:cQrySC9 += " AND SC9.C9_BLEST = ? "
	EndIf

	If MV_PAR03 == 2 .Or. MV_PAR03 == 3
		self:cQrySC9 += " AND SC9.C9_BLCRED = ? "
	EndIf

	self:cQrySC9 += " ? " // Adição de condição para objeto localizado no where
	self:cQrySC9 += " AND SC9.D_E_L_E_T_ = ? "
	self:cQrySC9 += "ORDER BY SC9.C9_FILIAL, SC9.C9_PEDIDO, SC9.C9_CLIENTE, SC9.C9_LOJA, "
	self:cQrySC9 += " SC9.C9_PRODUTO, SC9.C9_LOTECTL, SC9.C9_NUMLOTE, SC9.C9_DTVALID "

	oQuery := FwExecStatement():New(ChangeQuery(self:cQrySC9))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SC9"))
	oQuery:SetString(nParam++, ' ')

	If self:lUsaLocal
		oQuery:SetUnsafe(nParam++, FwJoinFilial("SDC", "SC9"))
		oQuery:SetString(nParam++, ' ')
	EndIf

	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)

	If MV_PAR03 == 1 .Or. MV_PAR03 == 3
		oQuery:SetString(nParam++, '  ')
	EndIf

	If MV_PAR03 == 2 .Or. MV_PAR03 == 3
		oQuery:SetString(nParam++, '  ')
	EndIf

	oQuery:SetUnsafe(nParam++, self:cLocWhere)
	oQuery:SetString(nParam++, ' ')

	self:cAliasSC9 := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	FwFreeArray(aFiliais)

Return
