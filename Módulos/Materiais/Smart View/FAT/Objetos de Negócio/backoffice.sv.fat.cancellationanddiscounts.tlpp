
#include "protheus.ch"
#INCLUDE "FWLIBVERSION.CH"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.cancellationanddiscounts.ch"

Static __lLookUp := FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.cancellationanddiscounts.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SB1,SBM,SF4,SF1,SD1,SF2,SD2,SE1,DHI,ADK,ACA,AOY,AOV,CT1,CTT", name="Cancelamentos e Descontos", country="ALL", customTables="SA1,SB1,SF1,SD1,SF2,SD2")

//-------------------------------------------------------------------
/*/{Protheus.doc} cancellationanddiscountsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Cancelamentos
e Descontos

@author SQUAD CRM/FATURAMENTO
@since 21/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
class cancellationanddiscountsSmartViewBusinessObject from integratedProvider

    public method new()				as object
    public method getData()			as object
    public method getSchema()		as object

	public method FatSV018CreateSQL()

    protected data aFields      	as array
    protected data aStruct      	as array
    protected data aCpoFake    		as array
    protected data jItems       	as json
    protected data cAlias 			as character
    protected data cSelectLoc   	as character
    protected data cWhereLoc    	as character
	protected data cGroupByLoc		as character
	protected data cCpoPersonSQL    as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author SQUAD CRM/FATURAMENTO
@since 21/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
method new() class cancellationanddiscountsSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Cancelamentos e Descontos"
    self:setDescription(STR0003)	//"Relação de Cancelamentos e Descontos"
    self:setIsLookUp(.T.)

	self:aFields    := {}
	self:aStruct    := {}
	self:aCpoFake   := {}
	self:cSelectLoc	:= ""
	self:cWhereLoc 	:= ""
	self:cAlias		:= ""

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author SQUAD CRM/FATURAMENTO
@since 21/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class cancellationanddiscountsSmartViewBusinessObject

    Local aPDFields     := {}						as array
	Local aEstrutura	:= {}  						as array
	Local aCpoPerson 	:= self:getCustomFields() 	as array

	Local lObfuscated   := .F.	as logical
	Local lLGPD   		:= .T.	as logical

	Local nX			:= 1 	as numeric
	Local nSaldoNFS     := 0	as numeric
	Local nValFim       := 0	as numeric
	Local nDescont      := 0	as numeric
	Local nTamCampo 	:= 0	as numeric
	Local nDecTotal		:= 0	as numeric
	Local nDecValbrut	:= 0	as numeric

	Local cChave        := ""	as character
	Local cCNPJP		:= ""	as character
	Local cCNPJR		:= ""	as character
	Local cDescMot      := ""	as character
	Local cHistori      := ""	as character
	Local cSerieRef     := ""	as character
	Local cDocRef       := ""	as character
	Local cCpoVldLGPD	:= ""	as character
	Local cTipMembro	:= ""	as character
	Local cNomMembro	:= ""	as character
	Local cTipo     	:= ""	as character

	Private MV_FIL 		:= "" 	as character 

    FatSetValueMVPAR(oFilter:GetParameters(),"FATR080") 	//Metodo para retorno do json dos parametros

	//Adiciona os campos personalizados na estrutura do aStruct
	For nX := 1 To Len(aCpoPerson)
		aAdd(self:aStruct,{aCpoPerson[nX][1],aCpoPerson[nX][2],aCpoPerson[nX][3],aCpoPerson[nX][4],aCpoPerson[nX][1],1})
	Next nX

	//Verifica se precisa fazer o tratamento para LGPD
    aPDFields 	:= FatGetfieldsLGPD(self:aFields, aCpoPerson )
    lObfuscated := Len(aPDFields) > 0

	nDecTotal	:= FWSX3Util():GetFieldStruct("D1_TOTAL")[4]
	nDecValbrut	:= FWSX3Util():GetFieldStruct("F2_VALBRUT")[4]
	
	//Montar a Query
	self:FatSV018CreateSQL()

	SF1->(dbSetOrder(1))

	//Posiciona no registro inicial
	(self:cAlias)->(DbGoTop())

	While !(self:cAlias)->(Eof())

		cTipMembro	:= ""
		cNomMembro	:= ""
		cCNPJP		:= IIf((self:cAlias)->A1_PESSOA = 'F', Left(Right('000000000'+(self:cAlias)->A1_CGC,11),9), Left(Right('000000000'+(self:cAlias)->A1_CGC,16),8))
		cCNPJR		:= IIf((self:cAlias)->A1_PESSOA = 'F', ' ', Left(Right((self:cAlias)->A1_CGC,6),4))

		If cChave == (self:cAlias)->F2_SERIE+(self:cAlias)->F2_DOC+(self:cAlias)->A1_COD+(self:cAlias)->A1_LOJA
			nSaldoNFS	-= nDescont  // abater o desconto anterior quando repete NF Saida
			nDescont	:= xMoeda(((self:cAlias)->D1_TOTAL - (self:cAlias)->D1_VALDESC),(self:cAlias)->F1_MOEDA,1,dDataBase,nDecTotal)
			nValFim		:= nSaldoNFS - nDescont
		Else
			nDescont	:= xMoeda(((self:cAlias)->D1_TOTAL - (self:cAlias)->D1_VALDESC),(self:cAlias)->F1_MOEDA,1,dDataBase,nDecTotal)
			nSaldoNFS	:= xMoeda((self:cAlias)->F2_VALBRUT,(self:cAlias)->F1_MOEDA,1,dDataBase,nDecValbrut)
			nValFim		:= xMoeda((self:cAlias)->F2_VALBRUT - ((self:cAlias)->D1_TOTAL-(self:cAlias)->D1_VALDESC),(self:cAlias)->F1_MOEDA,1,dDataBase,nDecTotal)
			cChave		:= (self:cAlias)->F2_SERIE+(self:cAlias)->F2_DOC+(self:cAlias)->A1_COD+(self:cAlias)->A1_LOJA
		Endif

		cDescMot   := 	AllTrim((self:cAlias)->DHI_DESCRI)
		cHistori   := 	IIf( SF1->(DbSeek((self:cAlias)->F1_FILIAL+((self:cAlias))->F1_DOC+((self:cAlias))->F1_SERIE+((self:cAlias))->A1_COD+((self:cAlias))->A1_LOJA+((self:cAlias))->F1_TIPO)), AllTrim(SF1->F1_HISTRET), "" )
		cTipMembro := 	IIf( (self:cAlias)->A1_TPMEMB == "1", STR0004,;
						IIf( (self:cAlias)->A1_TPMEMB == "2", STR0005,;
						IIf( (self:cAlias)->A1_TPMEMB == "3", STR0006, cTipMembro ) ) )
		cNomMembro := 	IIf( (self:cAlias)->A1_TPMEMB == "1",; 
							AllTrim((self:cAlias)->ADK_NOME),; // "1-Unidade de Negócio"
						IIf( (self:cAlias)->A1_TPMEMB == "2",; 
							UsrRetName((self:cAlias)->A1_CODMEMB),;	// "2-Usuário do CRM"
						IIf( (self:cAlias)->A1_TPMEMB == "3",; 
							IIf( !Empty(AllTrim((self:cAlias)->ACA_DESCRI)), AllTrim((self:cAlias)->ACA_DESCRI), cNomMembro), cNomMembro ) ) ) // "3-Equipe"
		cSerieRef := 	IIf( (self:cAlias)->RECNOREF > 0, (SD2->(DbGoto((self:cAlias)->RECNOREF)), SD2->D2_SERIE ), "" )
		cDocRef   := 	IIf( (self:cAlias)->RECNOREF > 0, (SD2->(DbGoto((self:cAlias)->RECNOREF)), SD2->D2_DOC   ), "" )

		self:jItems := JsonObject():new()

		For nX := 1 To Len(self:aStruct)

			lLGPD := .T.

			If self:aStruct[nX][1] == "TBCNPJRAI" .Or. self:aStruct[nX][1] == "TBCNPJFIL"
				cCpoVldLGPD := "A1_CGC"
			ElseIf self:aStruct[nX][6] == 2
				lLGPD := .F.
				cCpoVldLGPD := SubStr(self:aStruct[nX][1],1,2) + "_" + SubStr(self:aStruct[nX][1],3)
			Else
				cCpoVldLGPD := self:aStruct[nX][5]
			EndIf

			If lObfuscated .And. aScan(aPDFields, cCpoVldLGPD) == 0 .And. lLGPD

				aEstrutura := FWSX3Util():GetFieldStruct(cCpoVldLGPD)
				nTamCampo  := aEstrutura[3]
				cTipo      := aEstrutura[2]

				If cTipo == 'D'
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp(CTOD('01/01/1950'))
				ElseIf cTipo == 'N'
					self:jItems[self:aStruct[nX][1]] := Val( Repl('9',nTamCampo) )
				Else
					self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Repl("X", nTamCampo))
				EndIf
			Else
				If AllTrim(self:aStruct[nX][3]) == "date"
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.stringToTimeStamp(iif(self:aStruct[nX][6] == 1, (self:cAlias)->&(self:aStruct[nX][5]), &(self:aStruct[nX][5])))
				Else
					self:jItems[self:aStruct[nX][1]] := iif(self:aStruct[nX][6] == 1, (self:cAlias)->&(self:aStruct[nX][5]), &(self:aStruct[nX][5]))
				EndIf
			EndIf

		Next nX

		self:processData(1)
		self:oData:appendData(self:jItems)
		(self:cAlias)->(DbSkip()) 

	EndDo

	//Fechamento de tabelas e limpeza do objeto/variável
	(self:cAlias)->(DbCloseArea())

	FwFreeArray(aCpoPerson)
	FwFreeArray(aPDFields)
	FwFreeArray(aEstrutura)


return self:oData


//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author SQUAD CRM/FATURAMENTO
@since 21/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class cancellationanddiscountsSmartViewBusinessObject

	Local aCpoAll	  := {}	 as array
	Local aDescrAll	  := {}	 as array
	Local aStruCpo	  := {}	 as array
    Local nX          := 0	 as numeric
	Local nPos		  := 0	 as numeric
	Local nTypeReg	  := 0	 as numeric
	Local lContEmp	  := .T. as logical
	Local lContUni	  := .T. as logical

	aCpoAll := {"TB_EMPRES","TB_UNIDAD","D1_FILIAL","TB_NOMFIL","A1_COD","A1_NOME","A1_PESSOA","TB_CNPJRAI",;
				"TB_CNPJFIL","A1_CODSEG","AOV_DESSEG","F1_SERIE","F1_DOC","F1_DTDIGIT","D2_SERIE","D2_DOC","F2_SERIE","F2_DOC",;
				"F2_EMISSAO","D2_ITEM","E1_VENCREA","BM_GRUPO","BM_DESC","TB_VLDESC","TB_SALDO","TB_VLNFOR",;
				"F1_TIPO","D2_CONTA","CT1_DESC01","B1_COD","B1_DESC","D2_CLVL","D2_CCUSTO","CTT_DESC01",;
				"D2_ITEMCC","A1_CODTER","AOY_NMTER","TB_TIPMEMB","TB_CODMEMB","TB_NOMMEMB","DHI_CODIGO","DHI_DESCRI","TB_HISTORI"}
				
	aDescrAll := {STR0007,STR0008,STR0009,STR0010,STR0013,STR0014,STR0015,STR0016,;	//"Empresa"#"Unidade"#"Filial"#"Nome Filial"#"Cliente"#"Nome"#"Pessoa"#"Raiz CPF CNPJ"
				STR0017,STR0018,STR0019,STR0020,STR0021,STR0022,STR0023,STR0024,STR0025,STR0026,;	//"Filial CNPJ"#"Codigo Segmento"#"Descricao Segmento"#"Serie NFE"#"Nr NFE"#"Emissao NFE"#"Serie Ref"#"Refaturado"#"Serie NFS"#"Nr NFS"
				STR0027,STR0028,STR0029,STR0030,STR0031,STR0032,STR0033,STR0034,;	//"Emissao NFS"#"Item Orig"#"Vencimento"#"Grupo Prd"#"Descricao Grupo"#"Vl.Desc/Canc"#"Saldo"#"Vl. NF Or"
				STR0039,STR0040,STR0041,STR0042,STR0043,STR0044,STR0045,STR0046,;	//"Tipo"#"Conta Contabil"#"Descricao Conta"#"Produto"#"Descricao Produto"#"Classe de Valor"#"Centro de Custo"#"Descricao Centro de Custo"
				STR0047,STR0048,STR0049,STR0050,STR0051,STR0052,STR0053,STR0054,STR0055} //"Item Contabil"#"Territorio"#"Nome Territorio"#"Tipo Membro"#"Codigo Membro"#"Nome Membro"#"Motivo"#"Descricao do Motivo"#"Historico do Retorno"

	aAdd(self:aCpoFake, {"TB_EMPRES",	"string",	"FwCompanyName(cEmpAnt,(self:cAlias)->F1_FILIAL)"})
	aAdd(self:aCpoFake, {"TB_UNIDAD",	"string",	"FwUnitName(cEmpAnt,(self:cAlias)->F1_FILIAL)"})
	aAdd(self:aCpoFake, {"TB_NOMFIL",	"string",	"FwFilialName(cEmpAnt,(self:cAlias)->F1_FILIAL)"})
	aAdd(self:aCpoFake, {"D1_FILIAL",	"string",	"(self:cAlias)->F1_FILIAL"})
	aAdd(self:aCpoFake, {"TB_CNPJRAI",	"string",	"cCNPJP"})
	aAdd(self:aCpoFake, {"TB_CNPJFIL",	"string",	"cCNPJR"})
	aAdd(self:aCpoFake, {"D2_SERIE",	"string",	"cSerieRef"})
	aAdd(self:aCpoFake, {"D2_DOC",		"string",	"cDocRef"})
	aAdd(self:aCpoFake, {"TB_VLDESC",	"number",	"nDescont"})
	aAdd(self:aCpoFake, {"TB_SALDO",	"number",	"nValFim"})
	aAdd(self:aCpoFake, {"TB_VLNFOR",	"number",	"nSaldoNFS"})
	aAdd(self:aCpoFake, {"DHI_CODIGO",	"string",	"AllTrim((self:cAlias)->F1_MOTRET)"})
	aAdd(self:aCpoFake, {"DHI_DESCRI",	"string",	"cDescMot"})
	aAdd(self:aCpoFake, {"TB_TIPMEMB",	"string",	"cTipMembro"})
	aAdd(self:aCpoFake, {"TB_CODMEMB",	"string",	"AllTrim((self:cAlias)->A1_CODMEMB)"})
	aAdd(self:aCpoFake, {"TB_NOMMEMB",	"string",	"cNomMembro"})
	aAdd(self:aCpoFake, {"TB_HISTORI",	"string",	"cHistori"})

	aAdd(self:aFields, "A1_CGC")

	For nX := 1 To Len(aCpoAll)

		lContEmp := IIf( aCpoAll[nX] == "TB_EMPRES" .And. Empty(FwCompany()), .F., .T. )
		lContUni := IIf( aCpoAll[nX] == "TB_UNIDAD" .And. Empty(FwUnitBusiness()), .F., .T. )

		If lContEmp .And. lContUni

			aStruCpo := {}

			If (nPos := aScan(self:aCpoFake,{|x| x[1] == aCpoAll[nX]})) > 0
				aAdd(aStruCpo, {strTran(self:aCpoFake[nPos][1], "_", ""), aDescrAll[nX], self:aCpoFake[nPos][2], aDescrAll[nX], self:aCpoFake[nPos][3]})
				nTypeReg := 2
			Else
				aStruCpo := FatTrGetStruct({aCpoAll[nX]})
				nTypeReg := 1
			EndIf

			aAdd(self:aFields, aCpoAll[nX])
			aAdd(self:aStruct, {aStruCpo[1][1], aStruCpo[1][2], aStruCpo[1][3], aStruCpo[1][4], aStruCpo[1][5], nTypeReg})

			nPos := Len(self:aStruct)
			self:addProperty(self:aStruct[nPos][1], self:aStruct[nPos][2], self:aStruct[nPos][3], self:aStruct[nPos][4], self:aStruct[nPos][5])
		
		EndIf

	Next nX

	self:oSchema:addParameter("MV_FIL", STR0009 + " ?", "string"	, .T.,,,,,, FTSVHelp(,,, .T.))	// Filial ?
	self:oSchema:addParameter("MV_PAR01", STR0058 + " ?", "string"	, .F.,,,,,, FTSVHelp("FATR080", "02"))	// Da NF de Saída Original ?     
	self:oSchema:addParameter("MV_PAR02", STR0059 + " ?", "string"	, .F.,,,,,, FTSVHelp("FATR080", "03"))	// Até NF de Saída Original ?    
	self:oSchema:addParameter("MV_PAR03", STR0060 + " ?", "date"	, .F.,,,,,, FTSVHelp("FATR080", "04"))	// Da Emissão NF Saída Original ?
	self:oSchema:addParameter("MV_PAR04", STR0061 + " ?", "date"	, .F.,,,,,, FTSVHelp("FATR080", "05"))	// Até Emissão NF Saída Original ?
	self:oSchema:addParameter("MV_PAR05", STR0062 + " ?", "string"	, .F.,,,,,, FTSVHelp("FATR080", "06"))	// Da NF de Entrada ?            
	self:oSchema:addParameter("MV_PAR06", STR0063 + " ?", "string"	, .F.,,,,,, FTSVHelp("FATR080", "07"))	// Até NF de Entrada ?           
	self:oSchema:addParameter("MV_PAR07", STR0064 + " ?", "date"	, .F.,,,,,, FTSVHelp("FATR080", "08"))	// Da Emissão da NF de Entrada ? 
	self:oSchema:addParameter("MV_PAR08", STR0065 + " ?", "date"	, .F.,,,,,, FTSVHelp("FATR080", "09"))	// Até Emissão da NF de Entrada ?

	If __lLookUp	//Validação para versão da Lib
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
	EndIf

	FwFreeArray(aCpoAll)
	FwFreeArray(aDescrAll)
	FwFreeArray(aStruCpo)

return self:oSchema


//------------------------------------------------------------------
/*{Protheus.doc} FatSV018CreateSQL
    Método responsável por montar a query SQL.

@return character: Nil.
 
@author SQUAD CRM/FATURAMENTO
@since 24/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method FatSV018CreateSQL(aSeries) class cancellationanddiscountsSmartViewBusinessObject

	Local aFiliais			:= {}				as array
	Local aSeries 	 		:= StrTokArr(SuperGetMv("MV_FATSCAN", .F., ""), "/") as array
	Local cNameTmp	 		:= ""  				as character
	local cFieldsQry		:= "" 				as character
	local cQuery			:= ""				as character
	Local oTempTableBranch 	:= Nil				as object
	Local oQuery			:= Nil				as object
	Local nParam	 		:= 1   				as numeric
	Local nSeries			:= Len(aSeries) 	as numeric
	Local nX            	:= 1				as numeric

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SF1", "F1_FILIAL")
	cNameTmp 		 := oTempTableBranch:GetTableNameForQuery()

	self:cCpoPersonSQL := self:getSQLFields(.T.,Nil,.T.,.F.)
	
	//CAMPOS FISICOS
	cFieldsQry := " A1_COD, A1_LOJA, A1_NOME, A1_PESSOA, A1_CGC, A1_CODSEG, A1_CODTER, A1_TPMEMB, A1_CODMEMB, " //Campos SA1
	cFieldsQry += " B1_COD, B1_DESC, " 		//Campos SB1
	cFieldsQry += " BM_GRUPO, BM_DESC, " 	//Campos SBM
	cFieldsQry += " F4_CODIGO, " 			//Campos SF4
	cFieldsQry += " F1_FILIAL, F1_SERIE, F1_DOC, F1_DTDIGIT, F1_MOEDA, F1_MOTRET, F1_TIPO," //Campos SF1
	cFieldsQry += " D1_FORNECE, " 			//Campos SD1
	cFieldsQry += " F2_SERIE, F2_DOC, F2_EMISSAO, F2_VALBRUT, " //Campos SF2
	cFieldsQry += " SD2.D2_SERIE, SD2.D2_ITEM, SD2.D2_CONTA, SD2.D2_CLVL, SD2.D2_CCUSTO, SD2.D2_ITEMCC," //Campos SD2
	cFieldsQry += " CT1.CT1_DESC01, " 		//Campos CT1
	cFieldsQry += " CTT_DESC01, " 			//Campos CTT
	cFieldsQry += " AOV_DESSEG, " 			//Campos AOV
	cFieldsQry += " AOY_NMTER, " 			//Campos AOY
	cFieldsQry += " DHI_DESCRI, "			//Campos DHI
	cFieldsQry += " ADK_NOME, "				//Campos ADK
	cFieldsQry += " ACA_DESCRI, "			//Campos ACA
	cFieldsQry += " TMP_FILORI "			//Campos Tabela Temporária
	cFieldsQry += self:cSelectLoc			//Campos localizado

	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	cQuery := " SELECT ? "
	cQuery += " , MIN(REF.R_E_C_N_O_) RECNOREF, " 
	cQuery += " MIN(E1_VENCREA) E1_VENCREA, " 		
	cQuery += " MIN(D1_TOTAL) D1_TOTAL, MIN(D1_VALDESC) D1_VALDESC, SD1.R_E_C_N_O_ RECNOSD1 " 
	cQuery += " FROM " 		+ RetSqlName("SF1") + " SF1 " 
	cQuery += " INNER JOIN ? TMPFIL " 
	cQuery += " ON SF1.F1_FILIAL   		= TMPFIL.TMP_FILIAL " 
	cQuery += " INNER JOIN " + RetSqlName("SD1") + " SD1 " 
	cQuery += 	" ON  SD1.D1_FILIAL 	= SF1.F1_FILIAL "  
	cQuery += 	" AND SD1.D1_FORNECE 	= SF1.F1_FORNECE " 
	cQuery += 	" AND SD1.D1_LOJA 		= SF1.F1_LOJA " 
	cQuery += 	" AND SD1.D1_DOC 		= SF1.F1_DOC " 
	cQuery += 	" AND SD1.D1_SERIE		= SF1.F1_SERIE " 
	cQuery += 	" AND SD1.D1_TES 	   <> ? " 
	cQuery += 	" AND SD1.D_E_L_E_T_	= ? " 
	cQuery +=	" LEFT JOIN "+ RetSqlName("DHI") + " DHI "
	cQuery +=	" ON  ? "
	cQuery +=	" AND DHI.DHI_CODIGO  	= SF1.F1_MOTRET "
	cQuery +=	" AND DHI.D_E_L_E_T_  	= ? "
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 " 
	cQuery += 	" ON ? "  
	cQuery += 	" AND SA1.A1_COD 		= SF1.F1_FORNECE " 
	cQuery += 	" AND SA1.A1_LOJA 		= SF1.F1_LOJA " 
	cQuery += 	" AND SA1.D_E_L_E_T_	= ? "
	cQuery += " LEFT JOIN " + RetSqlName("ADK") + " ADK " 
	cQuery += 	" ON ? "  
	cQuery += 	" AND ADK.ADK_COD 		= SA1.A1_CODMEMB " 
	cQuery += 	" AND ADK.D_E_L_E_T_	= ? "
	cQuery += " LEFT JOIN " + RetSqlName("ACA") + " ACA " 
	cQuery += 	" ON ? "  
	cQuery += 	" AND ACA.ACA_GRPREP	= SA1.A1_CODMEMB " 
	cQuery += 	" AND ACA.D_E_L_E_T_	= ? "
	cQuery += " INNER JOIN " +	RetSqlName("SF2") + " SF2 " 
	cQuery += 	" ON  SF2.F2_FILIAL 	= SF1.F1_FILIAL "  
	cQuery += 	" AND SF2.F2_DOC 		= SD1.D1_NFORI " 
	cQuery += 	" AND SF2.F2_SERIE 		= SD1.D1_SERIORI " 
	cQuery += 	" AND SF2.F2_EMISSAO 	BETWEEN ? AND ? "  
	cQuery += 	" AND SF2.F2_DOC 		BETWEEN ? AND ? "  
	cQuery += 	" AND SF2.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("SBM") + " SBM " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND BM_GRUPO 			= SD1.D1_GRUPO " 
	cQuery += 	" AND SBM.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("SD2") + " SD2 " 
	cQuery += 	" ON  SD2.D2_FILIAL 	= SF1.F1_FILIAL "  
	cQuery += 	" AND SD2.D2_DOC 		= SF2.F2_DOC " 
	cQuery += 	" AND SD2.D2_SERIE 		= SF2.F2_SERIE " 
	cQuery += 	" AND SD2.D2_CLIENTE 	= SF2.F2_CLIENTE " 
	cQuery += 	" AND SD2.D2_LOJA 		= SF2.F2_LOJA " 
	cQuery += 	" AND SD2.D2_COD 		= SD1.D1_COD " 
	cQuery += 	" AND ((SD2.D2_ITEM 	= SD1.D1_ITEMORI AND SD1.D1_ITEMORI <> ? ) OR SD1.D1_ITEMORI = ?) "
	cQuery += 	" AND SD2.D_E_L_E_T_	= ?  " 
	cQuery += " LEFT JOIN " + RetSqlName("SE1") + " SE1 " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND SE1.E1_PREFIXO 	= SF2.F2_SERIE " 
	cQuery += 	" AND SE1.E1_NUM 		= SF2.F2_DOC " 
	cQuery += 	" AND SE1.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("SB1") +" SB1 " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND B1_COD 			= SD1.D1_COD " 
	cQuery += 	" AND SB1.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("SF4") + " SF4 " 
	cQuery += 	" ON  ? " 
	cQuery += 	" AND F4_CODIGO 		= SD2.D2_TES " 
	cQuery += 	" AND SF4.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("AOY") + " AOY " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND AOY_CODTER 		= SA1.A1_CODTER " 
	cQuery += 	" AND AOY.D_E_L_E_T_	= ?  "  
	cQuery += " LEFT JOIN " + RetSqlName("AOV") + " AOV " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND AOV_CODSEG 		= SA1.A1_CODSEG " 
	cQuery += 	" AND AOV.D_E_L_E_T_	= ? "  
	cQuery += " LEFT JOIN " + RetSqlName("CT1") + " CT1 " 
	cQuery += 	" ON  ? " 
	cQuery += 	" AND CT1.CT1_CONTA 	= SD2.D2_CONTA " 
	cQuery += 	" AND CT1.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("CTT") + " CTT " 
	cQuery += 	" ON  ? "  
	cQuery += 	" AND CTT_CUSTO 		= SD2.D2_CCUSTO " 
	cQuery += 	" AND CTT.D_E_L_E_T_	= ? " 
	cQuery += " LEFT JOIN " + RetSqlName("SD2") + " REF " 
	cQuery += 	" ON  ? " 
	cQuery += 	" AND REF.D2_NFORI 		= SD2.D2_DOC " 
	cQuery += 	" AND REF.D2_SERIORI 	= SD2.D2_SERIE " 
	cQuery += 	" AND REF.D_E_L_E_T_	= ? " 
	cQuery += " WHERE SF1.F1_FILIAL 	= TMPFIL.TMP_FILIAL "  

	//Na funcao SetString da classe FWPreparedStatement, deve-se passar a string normalmente, sem uso 
	//de aspas simples (') para o banco. Caso seja encontrado esse caracter, será considerado parte da 
	//string para o banco, e não como delimitador de string em SQL. E com isso foi necessario utilizar 
	//OR para cada Serie, ao inves de IN com todas juntas.
	If !(Empty(aSeries))
		cQuery += " AND ( "
		For nX := 1 To nSeries
			cQuery += " SF1.F1_SERIE = ? "  //XX - Series
			If nSeries == nX
				cQuery += " ) "
			Else
				cQuery += " OR "
			EndIf
		Next nX
	EndIF

	cQuery += " AND SF1.F1_TIPO 	= ? "   
	cQuery += " AND SF1.F1_DOC 		BETWEEN ? AND ? "  
	cQuery += " AND SF1.F1_DTDIGIT 	BETWEEN ? AND ? "
	cQuery += " ? "
	cQuery += " AND SF1.D_E_L_E_T_	= ? "
	cQuery += " GROUP BY SF1.F1_FILIAL, SD1.D1_FORNECE, SA1.A1_COD, SA1.A1_LOJA, SA1.A1_NOME, SA1.A1_PESSOA, SA1.A1_CGC, " 
	cQuery += " SA1.A1_CODSEG, AOV_DESSEG, SA1.A1_CODTER, AOY_NMTER, SA1.A1_TPMEMB, SA1.A1_CODMEMB, " 
	cQuery += " SF1.F1_SERIE, SF1.F1_DOC, SF1.F1_DTDIGIT, SF1.F1_MOEDA, SF1.F1_MOTRET, SF1.F1_TIPO,"
	cQuery += " SF2.F2_SERIE, SF2.F2_DOC, SF2.F2_EMISSAO, SF2.F2_VALBRUT, SD2.D2_SERIE, SD2.D2_ITEM, " 
	cQuery += " BM_GRUPO, BM_DESC, B1_COD, B1_DESC, F4_CODIGO, SD1.R_E_C_N_O_, " 
	cQuery += " SD2.D2_CONTA, CT1.CT1_DESC01, SD2.D2_CLVL, SD2.D2_CCUSTO, SD2.D2_ITEMCC, CTT_DESC01, ADK_NOME, ACA_DESCRI, DHI_DESCRI, TMPFIL.TMP_FILORI "
	If !Empty(self:cCpoPersonSQL)
		cQuery += ", ? "
	EndIf
	cQuery += " ? "
	cQuery += " ORDER BY SF1.F1_FILIAL, SA1.A1_NOME, SF2.F2_SERIE, SF2.F2_DOC, SF2.F2_EMISSAO, SA1.A1_COD, SA1.A1_LOJA "

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("DHI", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("ADK", "SA1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("ACA", "SA1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, DToS(MV_PAR03))
	oQuery:SetString(nParam++, DToS(MV_PAR04))
	oQuery:SetString(nParam++, MV_PAR01)
	oQuery:SetString(nParam++, MV_PAR02)
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SBM", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SE1", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SF4", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AOY", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("AOV", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("CT1", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("CTT", "SF1"))
	oQuery:SetString(nParam++, ' ')
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SD2", "SF1"))
	oQuery:SetString(nParam++, ' ')
					
	//Na funcao SetString da classe FWPreparedStatement, deve-se passar a string normalmente, sem uso 
	//de aspas simples (') para o banco. Caso seja encontrado esse caracter, será considerado parte da 
	//string para o banco, e não como delimitador de string em SQL. E com isso foi necessario utilizar 
	//OR para cada Serie, ao inves de IN com todas juntas.
	If !(Empty(aSeries))
		For nX := 1 To nSeries
	 		oQuery:SetString(nParam++, aSeries[nX]) 
	 	Next nX
	EndIf

	oQuery:SetString(nParam++, "D")
	oQuery:SetString(nParam++, MV_PAR05)
	oQuery:SetString(nParam++, MV_PAR06)
	oQuery:SetString(nParam++, DToS(MV_PAR07))
	oQuery:SetString(nParam++, DToS(MV_PAR08))
	oQuery:SetUnsafe(nParam++, self:cWhereLoc)
	oQuery:SetString(nParam++, ' ')
	If !Empty(self:cCpoPersonSQL)
		oQuery:SetUnsafe(nParam++, self:cCpoPersonSQL) 
	EndIf
	oQuery:SetUnsafe(nParam++, self:cGroupByLoc) 

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQuery)

	FwFreeArray(aFiliais)
	FwFreeArray(aSeries)

return
