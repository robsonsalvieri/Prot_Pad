#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.salesstatistics.ch"

Static __lLookUp  := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.salesstatistics.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SD1,SD2", name="Faturamento por Cliente X Quantidade de Produtos", country="ALL", customTables="SA1")

//-------------------------------------------------------------------
/*/{Protheus.doc} salesstatisticsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Faturamento
por Cliente X Quantidade de Produtos

@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class salesstatisticsSmartViewBusinessObject From IntegratedProvider

    public method new()             as object
    public method getData()         as object
    public method getSchema()       as object
    public method FatSv009CreateSQL()
 
    protected data aFields          as array
    protected data aStruct          as array
    protected data ojItems          as json
    protected data nVlrRetornLoc    as numeric
    protected data nTotQuantLoc     as numeric
    protected data nVlrUnitLoc      as numeric
    protected data cAliasSA1        as character
    protected data cAliasSD1        as character
    protected data cAliasSD2        as character
    protected data cQrySD1Loc       as character
    protected data cQrySD2Loc       as character
    protected data cQrySA1Loc       as character
    protected data cSD2Loc          as character
    protected data cWhereSD1        as character
    protected data cWhereSD2        as character
    protected data cWhereSA1        as character
    protected data cFieldsQry       as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salesstatisticsSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)     // "Faturamento"
    self:setDisplayName(STR0002) // "Faturamento por Cliente X Quantidade de Produtos"
    self:setDescription(STR0003) // "Relação de Faturamento por Cliente X Quantidade de Produtos"
    self:setIsLookUp(.T.)

    self:nVlrRetornLoc := 0
    self:nTotQuantLoc  := 0
    self:nVlrUnitLoc   := 0
    self:cAliasSA1     := ""
    self:cAliasSD1     := ""
    self:cAliasSD2     := ""
    self:cQrySD1Loc    := ""
    self:cQrySD2Loc    := ""
    self:cQrySA1Loc    := ""
    self:cSD2Loc       := ""
    self:cWhereSD1     := ""
    self:cWhereSD2     := ""
    self:cWhereSA1     := ""
    self:cFieldsQry    := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class salesstatisticsSmartViewBusinessObject

    Local aDev		    := {} as array
    Local aStru		    := {} as array
    Local aPDFields     := {} as array

    Local cProdAnt	    := "" as character 
    Local cLojaAnt      := "" as character
    Local cProdRef	    := "" as character
    Local cEstoq	    := "" as character
    Local cDupli	    := "" as character		
    Local cClieAnt	    := "" as character
    Local cNumPed       := "" as character
    Local cFiltro       := "" as character
    Local cFieldMasc    := "" as character
    Local cVendedores   := "" as character
    Local cFilSD1	    := "" as character
    Local cFilSD2	    := "" as character
    Local cFilSF1	    := "" as character
    Local cFilSF2	    := "" as character
    Local cFilSB1       := "" as character
    Local cFilSA7       := "" as character

    Local nReg		    := 0 as numeric
    Local nSavOrd       := 0 as numeric
    Local nDevQtd	    := 0 as numeric
    Local nDevVal	    := 0 as numeric
    Local nX	        := 0 as numeric
    Local nY	        := 0 as numeric
    Local nZ	        := 0 as numeric
    Local nParam		:= 1 as numeric
    Local nRecSD2	    := 0 as numeric
    Local nVend		    := 0 as numeric
    Local nTamRef	    := 0 as numeric
    Local nTamMasc      := 0 as numeric

    Local lNfD2Ori	    := .F. as logical
    Local lCtrlVndDev   := .F. as Logical
    Local lObfuscated   := .F. as logical
    Local lGrade        := .F. as logical

    Private nDecs	    := 0 as numeric
    Private nTotQuant   := 0 as numeric
    Private nVlrUnit    := 0 as numeric
    Private nVlrTot     := 0 as numeric
    Private nNumFils    := 0 as numeric

    Private cFieldsSD1  := ""  as character
    Private cFieldsSD2  := ""  as character
    Private cCodProd    := "" as character
    Private cDescProd   := "" as character
    Private cDoc        := "" as character
    Private cSerie      := "" as character
    Private cUM         := "" as character
    Private cVends      := "" as character

    Private aFiliais	:= {} as array
    Private aFieldsValue:= {} as array
    Private aCposCustom := self:getCustomFields() as array

    Private dEmissao    := CToD("//") as Date
    Private cSD1        := "SD1TMP" as character
    Private cSD2        := "SD2TMP" as character

    Private oTmpTab_1 	:= Nil as object
    Private oTmpTab_2 	:= Nil as object
    Private oQrySD1 	:= Nil as object
    Private oQrySD2 	:= Nil as object

    aFieldsValue:= {{"CCODPROD" ,   {"CCODPROD" ,	"", 'cCodProd'}},;
                    {"CDESCPROD",   {"CDESCPROD",	"", 'cDescProd'}},;
                    {"CDOC"     ,   {"CDOC"     ,   "", 'cDoc' }},;
                    {"CSERIE"   ,   {"CSERIE"   ,	"", 'cSerie' }},;
                    {"DEMISSAO" ,   {"DEMISSAO" ,	"", 'dEmissao' }},;
                    {"CUM"      ,	{"CUM"      ,	"", 'cUM' }},;
                    {"NTOTQUANT",	{"NTOTQUANT",	"", 'nTotQuant' }},;
                    {"NVLRUNIT" ,	{"NVLRUNIT" ,	"", 'nVlrUnit' }},;
                    {"NVLRTOT"  ,	{"NVLRTOT"  ,	"", 'nVlrTot' }},;
                    {"CVENDS"   ,   {"CVENDS"   ,	"", 'cVends'}}}

    MV_MOEDA    := 1
	MV_FIL      := ""
    MV_LOJDE    := ""
    MV_LOJAT    := ""

	// Carrega os parametros
    FatSetValueMVPAR(oFilter:getParameters(), "MR780A")

	// Criacao das Querys
    self:FatSv009CreateSQL()

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCposCustom)
    lObfuscated := Len(aPDFields) > 0

    SF1->(dbsetorder(1))
    SF2->(dbsetorder(1))
    SB1->(dbSetOrder(1))
    SA7->(dbSetOrder(2))

    nVend       := FA440CntVen()
    nTamRef     := Val(Substr(GetMv("MV_MASCGRD"),1,2))
    nTamMasc    := GetSx3Cache("D2_COD", "X3_TAMANHO")
    cFieldMasc  := MV_PAR11 + Space(nTamMasc - Len(MV_PAR11))
    cEstoq      := IIf((MV_PAR13 == 1),"S",IIf((MV_PAR13 == 2),"N","SN" ))
    cDupli	    := IIf((MV_PAR14 == 1),"S",IIf((MV_PAR14 == 2),"N","SN" ))
    nDecs       := msdecimais(MV_MOEDA)

    // Filtra nota de devolucao
    dbSelectArea("SD1")
    aStru := dbStruct()

    // Executa a Query (SD1)
    For nX := 1 To nNumFils

        oQrySD1:SetUnsafe(nParam++ , cFieldsSD1)
        oQrySD1:SetString(nParam++ , FatSVFilial('SD1', 'D1_FILIAL', aFiliais[nX]) )
        oQrySD1:SetString(nParam++ , MV_PAR01)
        oQrySD1:SetString(nParam++ , MV_PAR02)
        oQrySD1:SetString(nParam++ , MV_LOJDE)
        oQrySD1:SetString(nParam++ , MV_LOJAT)
        oQrySD1:SetString(nParam++ , MV_PAR05)
        oQrySD1:SetString(nParam++ , MV_PAR06)
        oQrySD1:SetString(nParam++ , DtoS(MV_PAR03))
        oQrySD1:SetString(nParam++ , DtoS(MV_PAR04))
        oQrySD1:SetString(nParam++ , "D")
        oQrySD1:SetUnsafe(nParam++,  self:cWhereSD1)
        oQrySD1:SetString(nParam++ , " ")

    Next nX

    self:cAliasSD1 := oQrySD1:OpenAlias()

    SVA780CriaTmp({"D1_FILIAL","D1_FORNECE","D1_LOJA","D1_COD"}, aStru, cSD1, self:cAliasSD1)

     //Posiciona no registro inicial 
    (cSD1)->(dbGoTop())

    // Monta filtro para processar as vendas por cliente
    DbSelectArea("SD2")
    cFiltro := IIf(Empty(SD2->(dbFilter())), SD2->(dbFilter()), "{ || " + SD2->(dbFilter()) + " }")
    bFiltro := IIf(Empty(SD2->(dbFilter())), { || .T. }, &(cFiltro))

    //Monta filtro para processar as vendas por cliente
    DbSelectArea("SD2")
    aStru := dbStruct()

    // Executa a Query (SD2)
    nParam := 1
    For nX := 1 To nNumFils

        oQrySD2:SetUnsafe(nParam++ , cFieldsSD2)
        oQrySD2:SetString(nParam++ , FatSVFilial('SD2', 'D2_FILIAL', aFiliais[nX]))
        oQrySD2:SetString(nParam++ , MV_PAR01)
        oQrySD2:SetString(nParam++ , MV_PAR02)
        oQrySD2:SetString(nParam++ , MV_LOJDE)
        oQrySD2:SetString(nParam++ , MV_LOJAT)
        oQrySD2:SetString(nParam++ , MV_PAR05)
        oQrySD2:SetString(nParam++ , MV_PAR06)
        oQrySD2:SetString(nParam++ , DtoS(MV_PAR03))
        oQrySD2:SetString(nParam++ , DtoS(MV_PAR04))
        oQrySD2:SetIn(nParam++ , {'B','D'})
        oQrySD2:SetUnsafe(nParam++,  self:cWhereSD2)
        oQrySD2:SetString(nParam++ , " ")

    Next nX

    self:cAliasSD2 := oQrySD2:OpenAlias()

    SVA780CriaTmp({"D2_FILIAL","D2_CLIENTE","D2_LOJA","D2_COD","D2_SERIE","D2_DOC","D2_ITEM"}, aStru, cSD2, self:cAliasSD2)

    //Posiciona no registro inicial
    (cSD2)->(dbGoTop())

    //Posiciona no registro inicial
    (self:cAliasSA1)->(dbGoTop())

    While (self:cAliasSA1)->(!Eof())

        cFilSD1 := FatSVFilial('SD1', 'D1_FILIAL', (self:cAliasSA1)->(FIL_D1_D2))
        cFilSD2 := FatSVFilial('SD2', 'D2_FILIAL', (self:cAliasSA1)->(FIL_D1_D2))
        cFilSF1 := cFilSD1
        cFilSF2	:= cFilSD2
        cFilSB1	:= FatSVFilial('SB1', 'B1_FILIAL', (self:cAliasSA1)->(FIL_D1_D2))
        cFilSA7	:= FatSVFilial('SA7', 'A7_FILIAL', (self:cAliasSA1)->(FIL_D1_D2))

        //Procura pelas saidas daquele cliente
        DbSelectArea(cSD2)
        If MsSeek(cFilSD2+(self:cAliasSA1)->A1_COD+(self:cAliasSA1)->A1_LOJA)

            //Montagem da quebra do relatorio por  Cliente
            cClieAnt := (self:cAliasSA1)->A1_COD
            cLojaAnt := (self:cAliasSA1)->A1_LOJA

            //Verifica Se eh uma tipo de nota valida
            //Verifica intervalo de Codigos de Vendedor
            //Valida o produto conforme a mascara
            While !Eof() .And. ((cSD2)->(D2_FILIAL+D2_CLIENTE+D2_LOJA)) == (cFilSD2+cClieAnt+cLojaAnt)

                If !Eval(bFiltro) .Or. !SVA780Vend(@cVends,nVend,cFilSF2) .Or. !ValidMasc((cSD2)->D2_COD, cFieldMasc)
                    DbSkip()
                    Loop
                EndIf

                //Verifica se aglutinara produtos de Grade
                lGrade := IIf( (cSD2)->D2_GRADE == "S" .And. MV_PAR12 == 1, .T., .F. )
                bGrade := IIf( (cSD2)->D2_GRADE == "S" .And. MV_PAR12 == 1, { || Substr((cSD2)->D2_COD, 1, nTamref) }, { || (cSD2)->D2_COD } )

                //Impressao da quebra por produto e NF
                cProdAnt := Eval(bGrade)
                While !Eof() .And. (cSD2)->(D2_FILIAL + D2_CLIENTE + D2_LOJA + cProdAnt ) == (cFilSD2 + cClieAnt + cLojaAnt + cProdAnt)

                    If !AvalTes((cSD2)->D2_TES,cEstoq,cDupli) .Or. !Eval(bFiltro) .Or. !ValidMasc((cSD2)->D2_COD, cFieldMasc) .Or. !SVA780Vend(@cVends,nVend,cFilSF2)
                        DbSkip()
                        Loop
                    EndIf

                    //Caso seja grade aglutina todos produtos do mesmo Pedido
                    If (cSD2)->D2_GRADE == "S" .And. lGrade
                        bGrade  := { || Substr((cSD2)->D2_COD, 1, nTamref) }
                        cProdRef:= Substr((cSD2)->D2_COD, 1, nTamRef)
                        cNumPed := (cSD2)->D2_PEDIDO
                        nReg    := 0
                        nDevQtd := 0
                        nDevVal := 0

                        While !Eof() .And. cProdRef == Eval(bGrade) .And. (cSD2)->D2_GRADE == "S" .And.;
                            cNumPed == (cSD2)->D2_PEDIDO .And. (cSD2)->D2_FILIAL == cFilSD2

                            nReg := Recno()

                            If !(!ValidMasc((cSD2)->D2_COD, cFieldMasc) .Or. !Eval(bFiltro))

                                //Tratamento das Devolucoes
                                If MV_PAR10 == 1 //inclui Devolucoes
                                    SVSomaDev(@nDevQtd, @nDevVal, @aDev, cEstoq, cDupli,cFilSD1, cFilSD2, cFilSF1, @nRecSD2)
                                EndIf

                                (cSD2)->(DbGoTo(nReg))
                                nTotQuant += (cSD2)->D2_QUANT
                                dbSkip()

                            EndIf

                        EndDo

                        //Verifica se processou algum registro
                        If nReg > 0
                            dbGoto(nReg)
                            nReg:=0
                        EndIf

                    Else

                        //Tratamento das devolucoes
                        bGrade  := { || (cSD2)->D2_COD }
                        nDevQtd := 0
                        nDevVal := 0
                        nReg    := Recno()

                        If MV_PAR10 == 1 //inclui Devolucoes
                            SVSomaDev(@nDevQtd, @nDevVal, @aDev, cEstoq, cDupli, cFilSD1, cFilSD2, cFilSF1, @nRecSD2)
                        EndIf

                        (cSD2)->(DbGoTo(nReg))
                        nTotQuant := (cSD2)->D2_QUANT

                    EndIf

                    //Imprime os dados da NF
                    SB1->(MsSeek(cFilSB1+(cSD2)->D2_COD))
                    cDescProd := IIf( MV_PAR16 == 1, SB1->B1_DESC, cDescProd)
                    cDescProd := IIf( MV_PAR16 != 1 .And. SA7->(MsSeek(cFilSA7+(cSD2)->(D2_COD+D2_CLIENTE+D2_LOJA))), SA7->A7_DESCCLI, cDescProd)

                    SF2->(MsSeek(cFilSF2+(cSD2)->(D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA)))
                    cUM      := (cSD2)->D2_UM
                    cDoc     := (cSD2)->D2_DOC
                    cSerie   := (cSD2)->&(SerieNfId("SD2",3,"D2_SERIE"))
                    dEmissao := totvs.framework.treports.date.dateToTimeStamp((cSD2)->D2_EMISSAO)

                    //Faz Verificacao da Moeda Escolhida e Imprime os Valores
                    nVlrUnit := xMoeda((cSD2)->D2_PRCVEN,SF2->F2_MOEDA,MV_MOEDA,(cSD2)->D2_EMISSAO,nDecs+1,SF2->F2_TXMOEDA)

                    If (cSD2)->D2_TIPO $ "CIP" // Tratamento para objeto de negócios localizado -Brasil ou outros-

                        self:nTotQuantLoc  := nTotQuant
                        self:nVlrUnitLoc   := nVlrUnit
                        self:cSD2Loc       := cSD2
                        self:processData(2)

                        nVlrTot := self:nVlrRetornLoc

                    Else
                        nVlrTot := IIf( (cSD2)->D2_GRADE == "S" .And. MV_PAR12 == 1, (nVlrUnit * nTotQuant), xMoeda((cSD2)->D2_TOTAL,SF2->F2_MOEDA,MV_MOEDA,(cSD2)->D2_EMISSAO,nDecs+1,SF2->F2_TXMOEDA))
                    EndIf

                    bGrade := IIf( (cSD2)->D2_GRADE == "S" .And. lGrade, { || Substr((cSD2)->D2_COD, 1, nTamref) }, { || (cSD2)->D2_COD } )

                    SVA780Vend(@cVends,nVend,cFilSF2)
                    cCodProd    := Eval(bGrade)
                    cVendedores := cVends
                    cVends 		:= Subs(cVendedores,1,7)

                    self:ojItems := JsonObject():new()

                        For nX := 1 To Len(self:aStruct)

                            self:ojItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAliasSA1, aFieldsValue)
                        
                        Next

                    self:processData(1) 
                    self:oData:appendData(self:ojItems)

                    //Imprime as devolucoes do produto selecionado
                    If nDevQtd != 0
                        ncSD2RecOri := (cSD2)->(Recno())
                        SD2->(DbGoTo(nRecSD2))
                        (cSD2)->(MsSeek(cFilSD2+SD2->D2_FILIAL+SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_COD+SD2->D2_SERIE+SD2->D2_DOC+SD2->D2_ITEM))
                        cSerie 	  := "DEV"	// "DEV"
                        nVlrTot   := nDevVal
                        nTotQuant := nDevQtd

                        self:ojItems := JsonObject():new()

                        For nY := 1 To Len(self:aStruct)

                            self:ojItems[self:aStruct[nY][1]] := FATSVProcessData(self:aStruct[nY], lObfuscated, aPDFields, self:cAliasSA1, aFieldsValue)
                            
                        Next

                        (cSD2)->(DbGoTo(ncSD2RecOri))

                        self:processData(1)
                        self:oData:appendData(self:ojItems)
                                            
                    EndIf

                    nTotQuant := 0
                    DbSkip()
                    
                EndDo

            EndDo
            
        EndIf

        //Procura pelas devolucoes dos clientes que nao tem NF SAIDA
        DbSelectArea(cSD1)
        If MsSeek(cFilSD1+(self:cAliasSA1)->A1_COD+(self:cAliasSA1)->A1_LOJA)

            //Procura as devolucoes do periodo, mas que nao pertencem
            //as NFS ja impressas do cliente selecionado
            If MV_PAR10 == 1  // Inclui Devolucao

                //Soma Devolucoes
                While (cSD1)->(D1_FILIAL + D1_FORNECE + D1_LOJA) == ( cFilSD1 + (self:cAliasSA1)->A1_COD+ (self:cAliasSA1)->A1_LOJA) .And. !Eof()

                    //Verifica Vendedores da N.F.Original
                    lCtrlVndDev := .F.
                    lNfD2Ori    := .F.

                    If AvalTes((cSD1)->D1_TES,cEstoq,cDupli)
                        dbSelectArea("SD2")
                        nSavOrd := IndexOrd()
                        dbSetOrder(3)

                        MsSeek(cFilSD2+(cSD1)->(D1_NFORI+D1_SERIORI+D1_FORNECE+D1_LOJA+D1_COD))
                        While !SD2->(Eof()) .And. ((cSD1)->(D1_FILIAL+D1_NFORI+D1_SERIORI+D1_FORNECE+D1_LOJA+D1_COD)) == SD2->(D2_FILIAL+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA+D2_COD)

                            If !Empty((cSD1)->D1_ITEMORI) .And. (AllTrim((cSD1)->D1_ITEMORI) != D2_ITEM) .Or. !ValidMasc((cSD1)->D1_COD, cFieldMasc) .Or. !Eval(bFiltro)
                                DbSkip()
                                Loop
                            Else
                                lCtrlVndDev := SVA780Vend(@cVends,nVend,cFilSF2)
                                If Ascan(aDev, D2_CLIENTE + D2_LOJA + D2_COD + D2_DOC + D2_SERIE + D2_ITEM) > 0
                                    lNfD2Ori := .T.
                                EndIf
                            EndIf

                            SD2->(dbSkip())
                        EndDo

                        //retorna o valor anterior apos while
                        SD2->(dbSetOrder(3))

                        SD2->(dbSeek(cFilSD2+(cSD1)->(D1_NFORI+D1_SERIORI+D1_FORNECE+D1_LOJA+D1_COD)))
                        SD2->(dbSetOrder(nSavOrd))
                        dbSelectArea(cSD1)

                        If !lCtrlVndDev .Or. lNfD2Ori
                            dbSkip()
                            Loop
                        EndIf

                        SF1->(MsSeek(cFilSF1+(cSD1)->(D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA)))
                        cUM         := (cSD1)->D1_UM
                        cDoc        := (cSD1)->D1_DOC
                        cSerie      := (cSD1)->&(SerieNfId("SD1",3,"D1_SERIE"))
                        dEmissao    := totvs.framework.treports.date.dateToTimeStamp((cSD1)->D1_EMISSAO)
                        nVlrTot     := xMoeda((cSD1)->(D1_TOTAL-D1_VALDESC),SF1->F1_MOEDA,MV_MOEDA,(cSD1)->D1_DTDIGIT,nDecs,SF1->F1_TXMOEDA)

                        If (SD2TMP->(Eof()) .And. SD1TMP->(!Eof())) .Or. (SD2TMP->(!Eof()) .And. SD1TMP->(!Eof()))

                            cClieAnt    := (self:cAliasSA1)->A1_COD
                            cLojaAnt    := (self:cAliasSA1)->A1_LOJA
                            cCodProd    := (cSD1)->D1_COD
                            cDescProd   := STR0014	// "Devolução"
                            cDoc        := (cSD1)->D1_DOC
                            cSerie      := SerieNfId(cSD1,2,"D1_SERIE")
                            dEmissao    := totvs.framework.treports.date.dateToTimeStamp((cSD1)->D1_EMISSAO)
                            cUM         := (cSD1)->D1_UM
                            nTotQuant   := (cSD1)->D1_QUANT * -1
                            nVlrUnit    := ((cSD1)->D1_VUNIT - (cSD1)->D1_VALDESC) * -1
                            nVlrTot     := ((cSD1)->D1_TOTAL - (cSD1)->D1_VALDESC) * -1
                            cVends      := cVends 

                        EndIf

                        self:ojItems := JsonObject():new()

                        For nZ := 1 To Len(self:aStruct)

                            self:ojItems[self:aStruct[nZ][1]] := FATSVProcessData(self:aStruct[nZ], lObfuscated, aPDFields, self:cAliasSA1, aFieldsValue)
                        
                        Next

                        self:processData(1)
                        self:oData:appendData(self:ojItems)

                    EndIf

                    (cSD1)->(dbSkip())

                EndDo

            EndIf

        EndIf
        
        DbSelectArea(self:cAliasSA1)
        (self:cAliasSA1)->(DBSkip())

    EndDo 

    If( valtype(oTmpTab_1) == "O")
        oTmpTab_1:Delete()
        freeObj(oTmpTab_1)
        oTmpTab_1 := Nil
    EndIf

    If( valtype(oTmpTab_2) == "O")
        oTmpTab_2:Delete()
        freeObj(oTmpTab_2)
        oTmpTab_2 := Nil
    EndIf

    (self:cAliasSA1)->(DBCloseArea())
    
    aSize(aDev          ,0)
    aSize(aStru         ,0)
    aSize(aPDFields     ,0)
    aSize(aFieldsValue  ,0)
    aSize(aCposCustom   ,0)
    aSize(aFiliais      ,0)

    FreeObj(oQrySD1)
    FreeObj(oQrySD2)

Return self:oData
 
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object class salesstatisticsSmartViewBusinessObject

    Local nX            := 0  as numeric
    Local aParameters   := {} as array
    Local aStruCpoFake  := {} as array
    Local aMV_Filia		:= {'MV_FIL',   STR0015 + " ?", 'string', .T., FTSVHelp(,,,.T.)} as array
    Local aMV_LojDe		:= {'MV_LOJDE', STR0017 + " ?", 'string', .F., FTSVHelp('MTR770','05')} as array
    Local aMV_LojAt		:= {'MV_LOJAT', STR0018 + " ?", 'string', .F., FTSVHelp('MTR770','06')} as array
    Local aMV_Moeda		:= {"MV_MOEDA", STR0016 + " ?", "number", .F., FTSVHelp("MR780A","09")} as array

    self:aFields := { "A1_FILIAL", "A1_COD", "A1_LOJA", "A1_NOME", "A1_OBSERV", "CCODPROD", "CDESCPROD", "CDOC",;
                      "CSERIE", "DEMISSAO", "CUM", "NTOTQUANT", "NVLRUNIT", "NVLRTOT", "CVENDS" }

    //Array com estrutura dos campos Fake para montar o addproperty
	aStruCpoFake :={{"CCODPROD" , STR0004, "string", STR0004, "CCODPROD" 	},;//Produto
					{"CDESCPROD", STR0005, "string", STR0005, "SALANT"   	},;//Descrição
                    {"CDOC"     , STR0006, "string", STR0006, "CDOC"     	},;//Num. Docto
					{"CSERIE"   , STR0007, "string", STR0007, "CSERIE"   	},;//Série
					{"DEMISSAO" , STR0008, "date", , STR0008, "DEMISSAO" 	},;//Emissão
					{"CUM"      , STR0009, "string", STR0009, "CUM"      	},;//Unidade
					{"NTOTQUANT", STR0010, "number", STR0010, "NTOTQUANT"   },;//Quantidade
					{"NVLRUNIT" , STR0011, "number", STR0011, "NVLRUNIT" 	},;//Vlr.Unitário
					{"NVLRTOT"  , STR0012, "number", STR0012, "NVLRTOT"  	},;//Vlr.Total
					{"CVENDS"   , STR0013, "string", STR0013, "CVENDS"   	}} //Vendedor

    self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][04], self:aStruct[nX][05])
    Next

    // Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('MR780A',,,, {'MV_FIL','03','04','01','02','MV_LOJDE','MV_LOJAT','05','06','07','08','10','11','12','13','14','15','16','17','MV_MOEDA'},;
                                                {aMV_Filia, aMV_LojDe, aMV_LojAt, aMV_Moeda})

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

	If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                         // Filial
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SA1", 2)	                        // Cliente de
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SA1", 2)	                        // Cliente ate
		self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SB1", 2)	                        // Produto de
		self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SB1", 2)	                        // Produto ate
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SA3", 2)	                        // Vendedor de
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/SA3", 2)	                        // Vendedor ate
		self:setCustomURL("MV_PAR10", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/10", 1)   // Inclui Devolução
		self:setCustomURL("MV_PAR12", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/12", 1)   // Aglutina prod.Grade
		self:setCustomURL("MV_PAR13", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/13", 1)   // Quanto a Estoque
		self:setCustomURL("MV_PAR14", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/14", 1)   // Quanto a Duplicata
        self:setCustomURL("MV_PAR15", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/15", 1)   // Quanto a Devolucao
        self:setCustomURL("MV_PAR16", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/16", 1)   // Quanto a Descricao
        self:setCustomURL("MV_PAR17", "api/faturamento/smartview/v1/options/fat/salesstatistics/ComboFATSV009/17", 1)   // Converte Moeda da Devolucäo
	EndIf

    aSize(aStruCpoFake, 0)
    aSize(aParameters, 0)
    aSize(aMV_Filia, 0)
    aSize(aMV_LojDe, 0)
    aSize(aMV_LojAt, 0)
    aSize(aMV_Moeda, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} ComboFATSV009
Retorna as opçoes disponível para os parâmetros tipo combobox

@return array: Array com as opçoes

@author FAT/CRM
@since 29/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function ComboFATSV009(cOpcao) 

	Local aRet := {} as array

	aRet := FatGetCboX1('MR780A', Val(cOpcao))

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} SVA780Vend
Verifica Intervalo de Vendedores

@return logical: lVend

@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVA780Vend(cVends as character, nVend as numeric, cFilSF2 as character)

    Local cAlias    := Alias() as character
    Local cSVend    := ""   as character
    Local cSCampo   := ""   as character
    Local lVend     := .F.  as logical
    Local cVend     := ""   as character 
    Local cBusca    := ""   as character
    Local nx        := 0    as numeric

    cVends := ""

    // Nao tem Alias na frente dos campos do SD2 para poder trabalhar em DBF e TOP
    cBusca := cFilSF2+D2_DOC+D2_SERIE+D2_CLIENTE+D2_LOJA
    dbSelectArea("SF2")

    If MsSeek(cBusca)
        cVend := "1"
        For nx := 1 to nVend
            cSCampo := "F2_VEND" + cVend
            cSVend  := AllTrim(FieldGet(FieldPos(cSCampo)))
            cVends  += IIf((cSVend >= MV_PAR07 .And. cSVend <= MV_PAR08) .And. (!Empty(cSVend)), IIf(Len(cVends) > 0, "/", "") + cSVend, "")
            lVend   := IIf((cSVend >= MV_PAR07 .And. cSVend <= MV_PAR08) .And. (nX == 1 .Or. !Empty(cSVend)), .T., lVend)
            cVend   := Soma1(cVend, 1)
        Next
    EndIf

    dbSelectArea(cAlias)

Return(lVend)

//-------------------------------------------------------------------
/*{Protheus.doc} SVSomaDev
Verifica Intervalo de Vendedores

@return logical

@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVSomaDev(nDevQtd as numeric, nDevVal as numeric, aDev as array, cEstoq as character, cDupli as character, cFilSD1 as character, cFilSD2 as character, cFilSF1 as character, nRecSD2 as numeric)

    Local DtMoedaDev := (cSD2)->D2_EMISSAO as date

    Default nRecSD2 := 0

    If (cSD1)->(MsSeek(cFilSD1+(cSD2)->(D2_CLIENTE + D2_LOJA + D2_COD )))

        //Soma Devolucoes
        While (cSD1)->(D1_FILIAL+D1_FORNECE+D1_LOJA+D1_COD) == (cSD2)->( cFilSD2 +D2_CLIENTE+D2_LOJA+D2_COD).AND.!(cSD1)->(Eof())

            // Avalia TES
            If !AvalTes((cSD1)->D1_TES,cEstoq,cDupli)
	            (cSD1)->(dbSkip())
	    		Loop
    		EndIf

            DtMoedaDev  := IIF(MV_PAR17 == 1,(cSD1)->D1_DTDIGIT,(cSD2)->D2_EMISSAO)

		    SF1->(dbSeek(cFilSF1+(cSD1)->(D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA)))

            If (cSD1)->(D1_NFORI + D1_SERIORI + AllTrim(D1_ITEMORI)) == (cSD2)->(D2_DOC + D2_SERIE + D2_ITEM )

                Aadd(aDev, (cSD1)->(D1_FORNECE + D1_LOJA + D1_COD + D1_NFORI + D1_SERIORI + AllTrim(D1_ITEMORI)))
                nDevQtd -= (cSD1)->D1_QUANT
                nDevVal -= xMoeda((cSD1)->(D1_TOTAL-D1_VALDESC),SF1->F1_MOEDA,MV_MOEDA,DtMoedaDev,nDecs+1,SF1->F1_TXMOEDA)

            ElseIf mv_par15 == 2 .And. (cSD1)->D1_DTDIGIT < (cSD2)->D2_EMISSAO .And.;
                (cSD1)->(D1_NFORI + D1_SERIORI + AllTrim(D1_ITEMORI)) < ;
                (cSD2)->(D2_DOC   + D2_SERIE   + D2_ITEM ) .And.;
                Ascan(aDev, (cSD1)->(D1_FORNECE + D1_LOJA + D1_COD + D1_NFORI + D1_SERIORI + AllTrim(D1_ITEMORI))) == 0

                SD2->(DbSeek(cFilSD2+(cSD1)->(D1_NFORI + D1_SERIORI + D1_FORNECE + D1_LOJA + D1_COD + AllTrim(D1_ITEMORI))))
                nRecSD2 := SD2->(Recno())
                Aadd(aDev, (cSD1)->(D1_FORNECE + D1_LOJA + D1_COD + D1_NFORI + D1_SERIORI + AllTrim(D1_ITEMORI)))
                nDevQtd -= (cSD1)->D1_QUANT
                nDevVal -= xMoeda((cSD1)->(D1_TOTAL-D1_VALDESC),SF1->F1_MOEDA,MV_MOEDA,DtMoedaDev,nDecs+1,SF1->F1_TXMOEDA)

            EndIf

            (cSD1)->(dbSkip())

        EndDo

    EndIf

Return .T.

//-------------------------------------------------------------------
/*{Protheus.doc} SVA780CriaTmp
Verifica Intervalo de Vendedores

@return Nil

@author FAT/CRM
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVA780CriaTmp(aIndice as array, aStruTmp as array, cAliasTmp as character, cAlias as character)

    Local nI         := 0 as numeric
    Local nF         := 0 as numeric
    Local nPos       := 0 as numeric
	Local cFieldName := "" as character
    
	nF := (cAlias)->(Fcount())

	// Instancia tabela temporária
	If( valtype(oTmpTab_1) == "O")
		oTmpTab_2:= FWTemporaryTable():New( cAliasTmp )
		oTmpTab_2:SetFields( aStruTmp )
		oTmpTab_2:AddIndex("1",aIndice)
		oTmpTab_2:Create()
	Else
		oTmpTab_1:= FWTemporaryTable():New( cAliasTmp )
		oTmpTab_1:SetFields( aStruTmp )
		oTmpTab_1:AddIndex("1",aIndice)
		oTmpTab_1:Create()
	EndIf

	(cAlias)->(DbGoTop())

	While ! (cAlias)->(Eof())
        (cAliasTmp)->(DbAppend())

		For nI := 1 To nF 
			cFieldName := (cAlias)->( FieldName( nI ))
		    If (nPos := (cAliasTmp)->(FieldPos(cFieldName))) > 0
		   		    (cAliasTmp)->(FieldPut(nPos,(cAlias)->(FieldGet((cAlias)->(FieldPos(cFieldName))))))
            EndIf   		
		Next

		(cAlias)->(DbSkip())

	EndDo

	(cAlias)->(dbCloseArea())
    DbSelectArea(cAliasTmp)

Return Nil

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv009CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 08/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv009CreateSQL() Class salesstatisticsSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nParam     := 1   as numeric
    Local cQuerySA1  := ""  as character
	Local cQuerySD1  := ""  as character
    Local cQuerySD2  := ""  as character
    Local oQrySA1 	 := Nil as object

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    //Campos da clausula Select na Query
    self:cFieldsQry := " A1_FILIAL, A1_COD, A1_LOJA, A1_NOME, A1_OBSERV "
    self:cFieldsQry += self:cQrySA1Loc

    cFieldsSD1 := " SD1.D1_FILIAL, SD1.D1_COD, SD1.D1_FORNECE, SD1.D1_LOJA, SD1.D1_TES, SD1.D1_NFORI, SD1.D1_TIPO, SD1.D1_SERIORI, SD1.D1_ITEMORI, "
    cFieldsSD1 += " SD1.D1_QUANT, SD1.D1_DOC, SD1.D1_UM, SD1.D1_SERIE,SD1.D1_EMISSAO, SD1.D1_TOTAL, SD1.D1_VALDESC, SD1.D1_DTDIGIT, SD1.D1_VUNIT "
    cFieldsSD1 += self:cQrySD1Loc //Campos localizados SD1

    cFieldsSD2 := " SD2.D2_FILIAL, SD2.D2_COD, SD2.D2_CLIENTE, SD2.D2_LOJA, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_ITEM, SD2.D2_PEDIDO, SD2.D2_GRADE, "
    cFieldsSD2 += " SD2.D2_TES, SD2.D2_QUANT, SD2.D2_UM,SD2.D2_EMISSAO, SD2.D2_PRCVEN, SD2.D2_TIPO, SD2.D2_TOTAL "
    cFieldsSD2 += self:cQrySD2Loc //Campos localizados SD2

	FatInjCposPerson(@aFieldsValue, @self:aStruct, @self:cFieldsQry, aCposCustom, .T., .F., .T.)

	For nX := 1 To nNumFils

        cQuerySA1 += "SELECT ? FIL_D1_D2, "
        cQuerySA1 += " ? "
        cQuerySA1 += " FROM "  + RetSqlName("SA1") + " SA1 "
        cQuerySA1 += "WHERE SA1.A1_FILIAL  = ? "
        cQuerySA1 += " AND SA1.A1_COD  BETWEEN ? AND ? "
        cQuerySA1 += " AND SA1.A1_LOJA BETWEEN ? AND ? "
        cQuerySA1 += " ? "
        cQuerySA1 += " AND SA1.D_E_L_E_T_  = ? "

        cQuerySD1 += "SELECT ? "
        cQuerySD1 += " FROM "  + RetSqlName("SD1") + " SD1 "
        cQuerySD1 += "WHERE SD1.D1_FILIAL  = ? "
        cQuerySD1 += " AND SD1.D1_FORNECE BETWEEN ? AND ? "
        cQuerySD1 += " AND SD1.D1_LOJA    BETWEEN ? AND ? "
        cQuerySD1 += " AND SD1.D1_COD     BETWEEN ? AND ? "
        cQuerySD1 += " AND SD1.D1_DTDIGIT BETWEEN ? AND ? "
        cQuerySD1 += " AND SD1.D1_TIPO     = ? "
        cQuerySD1 += " ? "
        cQuerySD1 += " AND SD1.D_E_L_E_T_  = ? "

        cQuerySD2 += "SELECT ? "
        cQuerySD2 += " FROM "  + RetSqlName("SD2") + " SD2 "
        cQuerySD2 += "WHERE SD2.D2_FILIAL  = ? "
        cQuerySD2 += " AND SD2.D2_CLIENTE BETWEEN ? AND ? "
        cQuerySD2 += " AND SD2.D2_LOJA    BETWEEN ? AND ? "
        cQuerySD2 += " AND SD2.D2_COD     BETWEEN ? AND ? "
        cQuerySD2 += " AND SD2.D2_EMISSAO BETWEEN ? AND ? "
        cQuerySD2 += " AND SD2.D2_TIPO NOT IN (?) "
        cQuerySD2 += " ? "
        cQuerySD2 += " AND SD2.D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
            cQuerySA1 += " UNION ALL "
            cQuerySD1 += " UNION ALL "
            cQuerySD2 += " UNION ALL "
        Else
            cQuerySA1 += " ORDER BY A1_FILIAL, A1_COD "
            cQuerySD1 += " ORDER BY D1_FILIAL, D1_FORNECE, D1_LOJA, D1_COD "
            cQuerySD2 += " ORDER BY D2_FILIAL, D2_CLIENTE, D2_LOJA, D2_COD, D2_ITEM "
        EndIf

    Next nX

    oQrySA1 := FwExecStatement():New(ChangeQuery(cQuerySA1))
    oQrySD1 := FwExecStatement():New(ChangeQuery(cQuerySD1))
    oQrySD2 := FwExecStatement():New(ChangeQuery(cQuerySD2))

    For nX := 1 To nNumFils

        oQrySA1:SetString(nParam++, aFiliais[nX])
        oQrySA1:SetUnsafe(nParam++, self:cFieldsQry)
        oQrySA1:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))
        oQrySA1:SetString(nParam++, MV_PAR01)
        oQrySA1:SetString(nParam++, MV_PAR02)
        oQrySA1:SetString(nParam++, MV_LOJDE)
        oQrySA1:SetString(nParam++, MV_LOJAT)
        oQrySA1:SetUnsafe(nParam++, self:cWhereSA1)
        oQrySA1:SetString(nParam++, ' ')

	Next nX

	self:cAliasSA1 := oQrySA1:OpenAlias()

   	FreeObj(oQrySA1)

Return
