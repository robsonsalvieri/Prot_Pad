#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.sv.fat.salessummary.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGAFAT", tables="SF2,SD2,SF1,SD1,SF4,SBM,SI1,SB1", name="Resumo de Vendas", country="BRA")

//-------------------------------------------------------------------
/*/{Protheus.doc} salessummarySmartViewBusinessObjectBRA
Classe para criação do Objeto de Negócio para Resumo de Vendas Brasil

@author FAT/CRM
@since 04/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class salessummarySmartViewBusinessObjectBRA From totvs.protheus.backoffice.sv.fat.salessummary.treportsintegratedprovider.salessummarySmartViewBusinessObject
    
    public method new()         as object
    public method preData()     as object
    public method preSchema()   as object
    public method processData() as object
 
    protected data aFields      as array
    protected data aStruct      as array

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 04/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class salessummarySmartViewBusinessObjectBRA 
	
    _Super:new()

    //Adicionando um novo campo para o objeto localizado
    aAdd(self:aQrySD2Loc, "D2_ICMSDIF")

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método chamado antes do getData do objeto principal
 
@return object: self:oData
 
@author FAT/CRM
@since 04/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method preData() as object Class salessummarySmartViewBusinessObjectBRA

    //Aqui é adicionada uma condição no where que será utilizado na query do objeto padrão

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Deverá ser chamado no momento do processamento da query
 
@return object: self:oData
 
@author FAT/CRM
@since 04/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method processData(nOperLoc as numeric) as object Class salessummarySmartViewBusinessObjectBRA

     Do Case
        //---------------------------------------------------
        //Operação 1 será utilizado somente para commit dos
        //campos antes do appendData - Case (nOperLoc == 1)
        //---------------------------------------------------

        //---------------------------------------------------
        //Operação e/ou tratamento específico para localizado
        //---------------------------------------------------
        Case (nOperLoc == 2)

            Aadd(self:aCpoLoc, {"D2_ICMSDIF", "", 0, 0})
            Aadd(self:aCpoLoc, {"D2_VALIPI",  "", 0, 0})

        //-----------------------------------------------------
        //Complemento de IPI e Aplicação do Diferimento do ICMS
        //conforme ocorre no MATXFIS (issue DSERFIS1=8601)
        //-----------------------------------------------------
        Case (nOperLoc == 3)

            //Complemento de IPI
            If (self:cAliasSD2Loc)->D2_TIPO <> "P"
	    		Replace (self:cAliasLoc)->D2_PRCVEN With (self:cAliasSD2Loc)->D2_PRCVEN
				// Aplicação do Diferimento do ICMS conforme ocorre no MATXFIS (issue DSERFIS1=8601)
                Replace (self:cAliasLoc)->D2_TOTAL With IIf( SF4->F4_ICMSDIF == "6", (self:cAliasSD2Loc)->D2_TOTAL - (self:cAliasSD2Loc)->D2_ICMSDIF, (self:cAliasSD2Loc)->D2_TOTAL )
            EndIf

			Replace (self:cAliasLoc)->D2_VALIPI With (self:cAliasSD2Loc)->D2_VALIPI

        //------------------------------------------------
        //Verifica nota fiscal de origem e vendedor no SF2
        //------------------------------------------------
        Case (nOperLoc == 4) 

            SF2->(MsSeek(self:cFilLoc+(self:cAliasLoc)->D1_NFORI+(self:cAliasLoc)->D1_SERIORI))

        //------------------------------------------------
        //Localização com valores de IPI 
        //------------------------------------------------
        Case (nOperLoc == 5)

            Replace (self:cAliasLoc)->D2_VALIPI With - (self:cAliasSD1Loc)->D1_VALIPI

        //------------------------------------------------
        //Localização com valores de IPI 
        //------------------------------------------------
        Case (nOperLoc == 6)

            self:nValIPILoc += xMoeda((self:cAliasLoc)->D2_VALIPI, 1, MV_MOEDA, (self:cAliasLoc)->D2_EMISSAO) - self:nDevIpiLoc

        //--------------------------------------------------
        //Localização com valores de IPI de acordo com a TES
        //--------------------------------------------------
        Case (nOperLoc == 7)

            self:nValIPI1Loc := xMoeda((self:cAliasLoc)->D2_VALIPI, (self:cAliasLoc)->D2_MOEDA, MV_MOEDA, (self:cAliasLoc)->D2_EMISSAO) - self:nDevIpiLoc

        //-------------------------------------------------------
        //Localização com valores de IPI no Calculo de Devolucoes
        //-------------------------------------------------------
        Case (nOperLoc == 8)

            self:nDevIpiLoc += xMoeda(SD1->D1_VALIPI, 1, MV_MOEDA, SD1->D1_DTDIGIT)

    EndCase

Return

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Método chamada antes do getSchema do objeto principal
 
@return object: self:oSchema
 
@author FAT/CRM
@since 04/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method preSchema() as object Class salessummarySmartViewBusinessObjectBRA

    //Adição de novos campos personalizados (não existem na tabela)

Return self:oSchema
