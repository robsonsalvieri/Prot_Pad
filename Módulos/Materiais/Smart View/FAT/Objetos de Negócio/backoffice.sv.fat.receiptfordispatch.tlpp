#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.receiptfordispatch.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.receiptfordispatch.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SF2,SA1,SA2,SA4", name="Recibos para Despacho", country="ALL", customTables="SF2")

//-------------------------------------------------------------------
/*/{Protheus.doc} receiptfordispatchSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Recibo para despacho

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class receiptfordispatchSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV014CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data oJItems      as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character
    protected data cAliasQry    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class receiptfordispatchSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//Faturamento
    self:setDisplayName(STR0002)	//Recibos para Despacho
    self:setDescription(STR0003)	//Relação de Recibos para Despacho
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class receiptfordispatchSmartViewBusinessObject 

    Local cFilSF2	    := "" as character

    Local nX            := 0  as numeric

    Local lObfuscated   := .F. as logical

    Local aPDFields     := {}  as array

    Private nValorBruto := 0  as numeric
    Private cCliFornec  := "" as character
    Private cEnder      := "" as character
    Private cEnderEnt   := "" as character
    Private cBairro     := "" as character
    Private cMunic      := "" as character
    Private cEstado     := "" as character
    Private cNomeTransp := "" as character
    Private cEndTransp  := "" as character

    Private aFieldsValue := {}  as array
    Private aCustomFiels := self:getCustomFields() as array

    Private MV_FIL       := "" as character

    // Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR720")

    aFieldsValue:= {{"F2_VALBRUT",  {"F2_VALBRUT",  "", 'nValorBruto'}},;
                    {"A1_NOME",     {"A1_NOME",     "", 'cCliFornec'}},;
                    {"A1_END",      {"A1_END",      "", 'cEnder'}},;
                    {"A1_ENDENT",   {"A1_ENDENT",   "", 'cEnderEnt'}},;
                    {"A1_BAIRRO",   {"A1_BAIRRO",   "", 'cBairro'}},;
                    {"A1_MUN",      {"A1_MUN",      "", 'cMunic'}},;
                    {"A1_EST",      {"A1_EST",      "", 'cEstado'}},;
                    {"A4_NOME",     {"A4_NOME",     "", 'cNomeTransp'}},;
                    {"A4_END",      {"A4_END",      "", 'cEndTransp'}}}

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCustomFiels )
    lObfuscated := Len(aPDFields) > 0

    //Irá montar as queries do objeto de negócio
	self:FatSV014CreateSQL()

    //Seta indices das tabelas para seek
    SA1->(DbSetOrder(1))
    SA2->(DbSetOrder(1))
    SA4->(DbSetOrder(1))

    //Posiciona no registro inicial
	(self:cAliasQry )->(dbGoTop())

	While !(self:cAliasQry )->(Eof())

        cFilSF2 := (self:cAliasQry)->F2_FILIAL

        SA4->(MsSeek(FatSVFilial('SA4', 'A4_FILIAL', cFilSF2) +(self:cAliasQry )->F2_TRANSP))
        If !((self:cAliasQry )->F2_TIPO $ "DB") 
            //Cliente
            SA1->(MsSeek(FatSVFilial('SA1', 'A1_FILIAL', cFilSF2) + (self:cAliasQry )->F2_CLIENTE + (self:cAliasQry )->F2_LOJA))
                cCliFornec  := SA1->A1_NOME
                cEnder      := SA1->A1_END
                cEnderEnt   := SA1->A1_ENDENT
                cBairro     := SA1->A1_BAIRRO
                cMunic      := SA1->A1_MUN
                cEstado     := SA1->A1_EST
                cNomeTransp := SA4->A4_NOME
                cEndTransp  := SA4->A4_END
        Else
            //Fornecedor
            SA2->(MsSeek(FatSVFilial('SA2', 'A2_FILIAL', cFilSF2) + (self:cAliasQry )->F2_CLIENTE + (self:cAliasQry )->F2_LOJA))
                cCliFornec  := SA2->A2_NOME
                cEnder      := SA2->A2_END
                cEnderEnt   := ""
                cBairro     := SA2->A2_BAIRRO
                cMunic      := SA2->A2_MUN
                cEstado     := SA2->A2_EST
                cNomeTransp := SA4->A4_NOME
                cEndTransp  := SA4->A4_END
        EndIf

        //Controle LGPD
        If lObfuscated
            cCliFornec  := FwProtectedDataUtil():ValueAsteriskToAnonymize(cCliFornec)
            cEnder      := FwProtectedDataUtil():ValueAsteriskToAnonymize(cEnder)
            cEnderEnt   := FwProtectedDataUtil():ValueAsteriskToAnonymize(cEnderEnt)
            cBairro     := FwProtectedDataUtil():ValueAsteriskToAnonymize(cBairro)
            cMunic      := FwProtectedDataUtil():ValueAsteriskToAnonymize(cMunic)
            cEstado     := FwProtectedDataUtil():ValueAsteriskToAnonymize(cEstado)
            cNomeTransp := FwProtectedDataUtil():ValueAsteriskToAnonymize(cNomeTransp)
            cEndTransp  := FwProtectedDataUtil():ValueAsteriskToAnonymize(cEndTransp)
        EndIf

        nValorBruto := (self:cAliasQry )->F2_VALMERC+(self:cAliasQry )->F2_VALIPI+(self:cAliasQry )->F2_FRETE+(self:cAliasQry )->F2_SEGURO+(self:cAliasQry )->F2_ICMSRET

        //Montagem dos dados
        self:oJItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)

            self:oJItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAliasQry, aFieldsValue)

		Next nX

        self:processData(1)
        self:oData:appendData(self:oJItems)

        (self:cAliasQry )->(DbSkip())

	EndDo

    (self:cAliasQry )->(dbCloseArea())

    aSize(aPDFields,    0) 
    aSize(aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class receiptfordispatchSmartViewBusinessObject 

    Local nX            := 0  as numeric
    Local cTituloCpo    := "" as character
    Local aParameters   := {} as array

    self:aFields := { "F2_FILIAL", "F2_DOC", "F2_VALBRUT", "A1_NOME", "A1_END", "A1_ENDENT", "A1_BAIRRO", "A1_MUN", "A1_EST", "A4_NOME", "A4_END", "F2_PBRUTO",;
                      "F2_PLIQUI", "F2_ESPECI1", "F2_VOLUME1", "F2_ESPECI2", "F2_VOLUME2", "F2_ESPECI3", "F2_VOLUME3", "F2_ESPECI4", "F2_VOLUME4" }

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        If "A1_NOME" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0006
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "A1_END" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0007
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "A1_ENDENT" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0008
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "A1_BAIRRO" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0009
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "A1_MUN" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0010
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        ElseIf "A1_EST" = AllTrim(self:aStruct[nX][05])
            cTituloCpo := STR0011
            self:oSchema:addProperty(self:aStruct[nX][01], cTituloCpo, self:aStruct[nX][03], cTituloCpo, self:aStruct[nX][05])
        Else
            self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][04], self:aStruct[nX][05])
        EndIf
    Next nX

    // Busca no SX1 os parâmetros que serão utilizados
    aParameters := FtSVSetParam('MTR720',,,,{'MV_FIL','03','01','02'})

    // Adiciona os parâmetros do pergunte no Schema
	self:oSchema:addParameter("MV_FIL", STR0024 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.)) //Filial

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

    // Validação para versão da Lib
    If __lLookUp
        self:setCustomURL("MV_FIL", "api/framework/v1/genericLookupService/smartview/SM0", 2) //Filial
    EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv014CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 29/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV014CreateSQL() class receiptfordispatchSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    cFieldsQry := "F2_FILIAL,F2_DOC,F2_CLIENTE,F2_LOJA,F2_VALBRUT,F2_PBRUTO,F2_PLIQUI,F2_VALMERC,F2_VALIPI,F2_FRETE, F2_SEGURO,F2_ICMSRET, "
    cFieldsQry += "F2_ESPECI1,F2_ESPECI2,F2_ESPECI3,F2_ESPECI4,F2_VOLUME1,F2_VOLUME2,F2_VOLUME3,F2_VOLUME4,F2_TIPO,F2_TRANSP,F2_SERIE "

    //Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCustomFiels, .T., .T., .T.)

    For nX := 1 To nNumFils

        cQuery += " SELECT ? "
        cQuery += " ? "
        cQuery += " FROM " + RetSqlName("SF2") + " SF2 "
        cQuery += " WHERE F2_FILIAL    = ? "
        cQuery += " AND F2_DOC        >= ? "
        cQuery += " AND F2_DOC        <= ? "
        cQuery += " AND ? = ? "
        cQuery += " ? "
        cQuery += " AND SF2.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
            cQuery += " UNION ALL "
        Else
            cQuery += " ORDER BY F2_FILIAL, F2_DOC, F2_SERIE, F2_CLIENTE, F2_LOJA "
        EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++ , cFieldsQry )
        oQuery:SetUnsafe(nParam++ , self:cSelectLoc )  // SELECT para objeto localizado
        oQuery:SetString(nParam++ , FatSVFilial('SF2', 'F2_FILIAL', aFiliais[nX]) )
        oQuery:SetString(nParam++ , MV_PAR02 )
        oQuery:SetString(nParam++ , MV_PAR03 )
        oQuery:SetUnsafe(nParam++ , SerieNfId("SF2",3,"F2_SERIE") )
        oQuery:SetString(nParam++ , MV_PAR01 )
        oQuery:SetUnsafe(nParam++ , self:cWhereLoc)     // WHERE para objeto localizado
        oQuery:SetString(nParam++ , " " )

    Next nX

    self:cAliasQry := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais,0)

Return
