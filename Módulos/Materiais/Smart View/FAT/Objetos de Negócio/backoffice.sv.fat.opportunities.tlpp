#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.opportunities.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.opportunities.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="AC1,AC3,AC4,AD1,AD2,AD3,AD4,AD9,AIJ,SUS,SA3,SU5", name="Oportunidades", country="ALL", customTables = "AD1,SA3")

//-------------------------------------------------------------------
/*/{Protheus.doc} opportunitiesSmartViewBusinessObject
Classe para criação do Objeto de opportunities

@author FAT/CRM
@since 04/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class opportunitiesSmartViewBusinessObject From integratedprovider

    public method new() 	 	as object
    public method getData()   	as object
    public method getSchema() 	as object

	public method FatSv033CreateSQL()

    protected data aFields 	  	as array
    protected data aStruct    	as array
    protected data ojItems  	as json
    protected data cLocSelect 	as character
    protected data cLocWhere 	as character
    protected data cLocAlias 	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author FAT/CRM
@since 04/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class opportunitiesSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 	//Faturamento
    self:setDisplayName(STR0002)	//Oportunidades
    self:setDescription(STR0003) 	//Relação de Oportunidades
    self:setIsLookUp(.T.)

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 04/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class opportunitiesSmartViewBusinessObject

	Local nX         	:= 0  as numeric
	Local nScan			:= 0  as numeric

	Local cVerba		:= "" as character
	Local cStatus		:= "" as character
	Local cNomVend		:= "" as character
	Local cTempPerm		:= "" as character
	Local cStatusAIJ	:= "" as character
	Local xValue		:= "" as character

	Local aPDFields		:= {} as array

	Local lObfuscated	:= .F. as logical

	Private cAlias 		:= "" as character

	Private aFieldsValue:= {} as array	
	Private aFieldsCpo	:= self:getCustomFields() as array

	Private NORDER		:= 0  as numeric
    Private MV_FIL 		:= "" as character

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "FTR010")

	aFieldsValue:= {{"A3NOME2",   	{"A3NOME2",   	"", 'cNomVend'}},;
					{"AD1_VERBA", 	{"AD1_VERBA", 	"", 'cVerba'}},;
					{"AD1_STATUS", 	{"AD1_STATUS", 	"", 'cStatus'}},;
					{"AIJ_STATUS", 	{"AIJ_STATUS", 	"", 'cStatusAIJ'}},;
					{"AIJDUREST", 	{"AIJDUREST", 	"", 'cTempPerm'}}}

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_PAR22 := IIf(MV_PAR22 <= 0 .Or. MV_PAR22 > ContaMoeda(), 1, MV_PAR22)

	// Criacao das Querys
    self:FatSv033CreateSQL()

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aFieldsCpo)
    lObfuscated := Len( aPDFields ) > 0

    dbSelectArea(cAlias)

	(cAlias)->(DbGoTop())

	SA3->(DbSetOrder(1))

	While !(cAlias)->(Eof()) .And. !Empty((cAlias)->AD1_NROPOR)

		cStatus	 	:= (cAlias)->AD1_STATUS
		cVerba		:= IIf( (cAlias)->AD1_MOEDA <= MOEDFIN(),;
							xMoeda( (cAlias)->AD1_VERBA, (cAlias)->AD1_MOEDA, MV_PAR22, dDataBase ),;
							xMoeda( (cAlias)->AD1_VERBA, 1, MV_PAR22, dDataBase))
		cTempPerm 	:= TKCalcPer( STOD((cAlias)->AIJ_DTINIC),(cAlias)->AIJ_HRINIC,;
							IIf( !Empty(STOD((cAlias)->AIJ_DTENCE)),STOD((cAlias)->AIJ_DTENCE),dDataBase ),;
							IIf( !Empty((cAlias)->AIJ_HRENCE),(cAlias)->AIJ_HRENCE,SubStr(Time(),1,5) ) )
		cStatusAIJ 	:= SVFTR10AIJSt((cAlias)->AIJ_PROVEN, (cAlias)->AIJ_STAGE, STOD((cAlias)->AIJ_DTINIC), (cAlias)->AIJ_HRINIC, (cAlias)->AIJ_STATUS, FatSVFilial('AC2', 'AC2_FILIAL', (cAlias)->AIJ_FILIAL) )
		cStatus		:= IIf( cStatus == "1", STR0005,;				//Aberto
							IIf( cStatus == "2", STR0006,;			//Perdido
								IIf( cStatus == "3", STR0007,;		//Suspenso
									IIf( cStatus == "9", STR0008,;	//Ganha
									"" ) ) ) )
		cNomVend 	:= IIf(SA3->(MsSeek((cAlias)->A3_FILIAL + (cAlias)->AD2_VEND)), SA3->A3_NOME, "")

		self:ojItems := JsonObject():new()

        For nX := 1 To Len(self:aStruct)

            If (nScan := aScan(aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
                xValue := FatGetValueFields(aFieldsValue[nScan][1], "cAlias", aFieldsValue[nScan][2])
            Else
               	xValue := "(cAlias)->"+self:aStruct[nX][5]
            EndIf

            If lObfuscated .And. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
                self:ojItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
			ElseIf lObfuscated .And. self:aStruct[nX][1] == "A3NOME2"
				self:ojItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize(Space(FWSX3Util():GetFieldStruct("A3_NOME")[3]))
            ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                self:ojItems[self:aStruct[nX][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
            Else
                self:ojItems[self:aStruct[nX][1]] := &(xValue)
            EndIf

		Next

		self:ProcessData(1)
		self:oData:appendData(self:ojItems)

		(cAlias)->(dbSkip())

	EndDo

	aSize(aFieldsCpo,   0)
	aSize(aPDFields,    0)
	aSize(aFieldsValue, 0)

Return self:oData

//------------------------------------------------------------------------------
/*/{Protheus.doc} SVFTR10AIJSt	
Adiciona o status da evolucao da venda no relatorio.
@sample 	FTR10AIJSt(cProVen, cStage, cDtIni, cHrIni, cStatus)

@param		ExpC1	Processo de Venda
@param		ExpC2	Estagio
@param		ExpC3	Data Inicial
@param		ExpC4	Hora Final
@param		ExpC4	Status encerrado

@return	ExpC Status da Evolucao

@author	Anderson Silva
@since		21/05/2013
@version	P12
/*/
//------------------------------------------------------------------------------
Static Function SVFTR10AIJSt(cProVen, cStage, cDtIni, cHrIni, cStatus, cFilialAC2)

	Local aArea 		:= GetArea() 	as array		// Guarda a area atual
	Local cHrNotif		:= "" 			as character	// Hora que comecara a notificacao
	Local cHrLimit		:= "" 			as character  	// Hora limite
	Local cRetorno		:= "" 			as character	// Status da evolucao
	Local dDtNotif		:= cTod("//") 	as date 		// Data que comecara a notificacao
	Local dDtLimit		:= cTod("//") 	as date 		// Data limite
	Local nDtHrNotif	:= 0 			as numeric		// Data / Hora de notificacao
	Local nDtHrLimit	:= 0 			as numeric 		// Data / Hora limite para avancar o estagio
	Local nDtHrBase		:= 0 			as numeric  	// Database do sistema (Data/Hora)
	Local nHrsInt		:= 0 			as numeric		// Hora em inteiro
	Local cCalcStatus   := ""   		as character

	If AC2->(MsSeek(cFilialAC2 + cProVen + cStage))

		// Calcula a data e hora limite para avancar o estagio
		Ft300LtEst(cDtIni, cHrIni, @dDtLimit,@cHrLimit)

		IIf( ( AC2->AC2_DNOTIF <> 0 .OR. ( !Empty(AC2->AC2_HNOTIF) .AND. AC2->AC2_HNOTIF <> "00:00" ) ),;
			(   dDtNotif := dDtLimit - AC2->AC2_DNOTIF,;
				cHrNotif :=	cHrLimit,;
				nHrsInt  := HoraToInt(AC2->AC2_HNOTIF),;
				SubtDiaHor(@dDtNotif,@cHrNotif,nHrsInt),;
				nDtHrNotif	:= Val(DtoS(dDtNotif)+StrTran(cHrNotif,":",""));
			),;	
		)

		nDtHrLimit	:= Val(DtoS(dDtLimit)+StrTran(cHrLimit,":",""))
		nDtHrBase	:= Val(DtoS(dDataBase)+StrTran(SubStr(Time(),1,5),":",""))

		// Legenda do estagio atual
		cCalcStatus := IIf( nDtHrLimit <> 0,;
						IIf( nDtHrNotif <> 0 .AND. nDtHrBase >=  nDtHrNotif  .AND. nDtHrNotif <= nDtHrLimit  .AND. nDtHrLimit > nDtHrBase, STR0009,; // "Em alerta"
							IIf (nDtHrBase > nDtHrLimit, STR0010, cRetorno ) ),; // "Em atraso"
							STR0011 )                                         	// "Em dia" 

	EndIf

	cRetorno := IIf(Empty(cStatus), cCalcStatus, X3Combo("AIJ_STATUS", cStatus))

	RestArea(aArea)
	aSize(aArea,0)

Return( cRetorno )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() class opportunitiesSmartViewBusinessObject

	Local nX 		   := 0  as numeric
	Local aStruCpoFake := {} as array
	Local aParameters  := {} as array

	// Adiciona campos Padroes
	self:aFields := {"AD1_FILIAL",;		// Filial
					 "AD1_NROPOR",;		// Oportunidade
					 "AD1_REVISA",;		// Revisao
					 "AD1_DESCRI",;		// Descrição
					 "AD1_VEND",;		// Vendedor
					 "A3_NOME",;		// Nome do Vendedor
					 "AD1_PROVEN",;		// Processo de Venda
					 "AD1_STAGE",;		// Estagio
					 "AD1_VERBA",;		// Verba
					 "AD1_MOEDA",;		// Moeda
					 "AD1_STATUS",;		// Status da Oportunidade
					 "AD1_CODPRO",;		// Codigo do Produto
					 "US_COD",;			// Codigo do Prospect
					 "US_LOJA",;		// Loja
					 "US_NOME",;		// Razao Social do Prospect
					 "AC1_DESCRI",;		// Descrição do Processo
					 "AC2_DESCRI",;		// Descrição do Estágio
					 "AD3_CODCON",;		// Concorrente
					 "AC3_NOME",;		// Nome Razao Social do Concorrente
					 "AD4_PARTNE",;		// Parceiro
					 "AC4_NOME",;		// Nome do Parceiro
					 "AD2_VEND",;		// Codigo do Vendedor
					 "A3NOME2",;		// Nome do Vendedor
					 "AD9_CODCON",;		// Contato
					 "U5_CONTAT",;		// Nome do Contato
					 "AIJ_PROVEN",;		// Processo de Venda (AIJ)
					 "AIJ_STAGE",;		// Estagio (AIJ)
					 "AIJ_DTINIC",;		// Data Início
					 "AIJ_HRINIC",;		// Hora Início
					 "AIJ_DTLIMI",;		// Data Limite
					 "AIJ_HRLIMI",;		// Hora Limite
					 "AIJ_DTENCE",;		// Data de Encerramento
					 "AIJ_HRENCE",;		// Hora de Encerramento
					 "AIJ_STATUS",;		// Status do Est. Encerrado
					 "AIJDUREST"}		// Tempo de Permanência

	// Array com estrutura dos campos Fake para montar o addproperty
    aStruCpoFake := {{"A3NOME2",   FWSX3Util():GetDescription("A3_NOME"),    "string", FWSX3Util():GetDescription("A3_NOME"),    "A3NOME2"},;
                     {"AIJDUREST", FWSX3Util():GetDescription("AIJ_DUREST"), "string", FWSX3Util():GetDescription("AIJ_DUREST"), "AIJDUREST"}}

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	// Busca no SX1 os parâmetros que serão utilizados e remove os que estão no array
	aParameters := FtSVSetParam('FTR010', {'MV_PAR21'} )

	// Cria a estrutura dos Parametros Customizados
	self:oSchema:addParameter("MV_FIL", STR0015 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))  // Filial
	self:oSchema:addParameter("NORDER", STR0004, "number", .F.,,,,,, FTSVHelp(,, .T.)) //Ordem

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nX][5] )
	Next nX

    If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)							// Filial
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/NJ0SUS", 2)                      // Prospect de
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/NJ0SUS", 2)                      // Prospect ate
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SA3", 2)                         // Representante de
        self:setCustomURL("MV_PAR06", "api/framework/v1/genericLookupService/smartview/SA3", 2)                         // Representante ate
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/AC2", 2)                         // Processo/Venda de
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/AC2", 2)                         // Processo/Venda ate
        self:setCustomURL("MV_PAR13", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         // Produto de
        self:setCustomURL("MV_PAR14", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         // Produto ate
        self:setCustomURL("MV_PAR15", "api/framework/v1/genericLookupService/smartview/02",  2)                         // Tipo de
        self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/02",  2)                         // Tipo ate
		self:setCustomURL("MV_PAR17", "api/framework/v1/genericLookupService/smartview/SBM", 2)                         // Grupo de
        self:setCustomURL("MV_PAR18", "api/framework/v1/genericLookupService/smartview/SBM", 2)                         // Grupo ate
        self:setCustomURL("MV_PAR19", "api/framework/treports/integratedprovider/v1/options/FTR010/MV_PAR19", 1)        // Considerar Oportunidades
        self:setCustomURL("MV_PAR20", "api/framework/treports/integratedprovider/v1/options/FTR010/MV_PAR20", 1)        // Desconsiderar Oportunidades
        self:setCustomURL("NORDER",   "api/faturamento/smartview/v1/options/fat/opportunities/SVFATOport/1",  1)   		// Ordem
    EndIf

	aSize(aStruCpoFake, 0)
	aSize(aParameters,  0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFATOport
Retorna as opçoes disponível para o parâmetro de ordem

@return array: Array com as opçoes

@author FAT/CRM
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFATOport(cOpcao)
	Local aRet := {} as array

	If cOpcao == "1"
        aRet := { FatGetStrCpos({'AD1_NROPOR'}),; 	//Oportunidade
				  FatGetStrCpos({'AD1_PROSPE'}),; 	//Prospect
				  FatGetStrCpos({'AD1_CODPRO'}),; 	//Produto
				  FatGetStrCpos({'AD1_VEND'}),;		//Vendedor
				  FatGetStrCpos({'AD1_PROVEN'}) } 	//Processo
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv033CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 06/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv033CreateSQL() Class opportunitiesSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
    Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array
	Local aOrdem	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

	cFieldsQry 	:= "AD1_FILIAL,AD1_NROPOR,AD1_REVISA,AD1_DESCRI,AD1_VEND,AD1_PROVEN,AD1_STAGE,AD1_VERBA,AD1_MOEDA,"
	cFieldsQry 	+= "AD1_CODPRO,AD1_STATUS,AD1_PROSPE,AD1_LOJPRO,AD1_DTINI,AD1_DTFIM,AD2_VEND,A3_NOME,AC3_NOME,"
	cFieldsQry 	+= "AD3_CODCON,AD4_PARTNE,AC4_NOME,AD9_CODCON,U5_CONTAT,AIJ_NROPOR,AIJ_REVISA,AIJ_PROVEN,"
	cFieldsQry 	+= "AIJ_FILIAL,AIJ_STAGE,AIJ_DTINIC,AIJ_HRINIC,AIJ_DTLIMI,AIJ_HRLIMI,AIJ_DTENCE,AIJ_HRENCE,AIJ_STATUS,"
	cFieldsQry 	+= "AC1_DESCRI,AC2_DESCRI,US_COD,US_LOJA,US_NOME,A3_FILIAL"
	cFieldsQry 	+= self:cLocSelect

	NORDER := IIf( NORDER == 0, 1, NORDER )

    aOrdem := { " AD1_FILIAL, AD1_NROPOR, AD2_VEND ",;
                " AD1_FILIAL, AD1_PROSPE, AD1_LOJPRO ",;
                " AD1_FILIAL, AD1_CODPRO ",;
                " AD1_FILIAL, AD1_VEND", "AD1_PROVEN, AD1_STAGE"}

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aFieldsCpo, .T., .T., .T.)

    For nX := 1 To nNumFils

		cQuery += " SELECT ? "
		cQuery += "		FROM " 	+ RetSqlName("AD1") + " AD1 "
		cQuery += " LEFT JOIN " + RetSqlName("AD2") + " AD2 "
		cQuery += " 	ON  AD2.AD2_FILIAL = ? "
		cQuery += " 	AND AD2.AD2_NROPOR = AD1.AD1_NROPOR "
		cQuery += " 	AND AD2.AD2_REVISA = AD1.AD1_REVISA "
		cQuery += " 	AND AD2.D_E_L_E_T_ = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AD3") + " AD3 "
		cQuery += " 	ON  AD3.AD3_FILIAL = ? "
		cQuery += " 	AND AD3.AD3_NROPOR = AD1.AD1_NROPOR "
		cQuery += " 	AND AD3.AD3_REVISA = AD1.AD1_REVISA "
		cQuery += " 	AND AD3.D_E_L_E_T_ = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AD4") + " AD4 "
		cQuery += " 	ON  AD4.AD4_FILIAL = ? "
		cQuery += " 	AND AD4.AD4_NROPOR = AD1.AD1_NROPOR "
		cQuery += " 	AND AD4.AD4_REVISA = AD1.AD1_REVISA "
		cQuery += " 	AND AD4.D_E_L_E_T_ = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AD9") + " AD9 "
		cQuery += " 	ON  AD9.AD9_FILIAL = ? "
		cQuery += " 	AND AD9.AD9_NROPOR = AD1.AD1_NROPOR "
		cQuery += " 	AND AD9.AD9_REVISA = AD1.AD1_REVISA "
		cQuery += " 	AND AD9.D_E_L_E_T_ = ? "
		cQuery += " LEFT JOIN " + RetSqlName("SA3") + " SA3 "
		cQuery += "    	ON  SA3.A3_FILIAL  = ? "
		cQuery += "    	AND SA3.A3_COD     = AD1.AD1_VEND "
		cQuery += "    	AND SA3.D_E_L_E_T_ = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AC1") + " AC1 "
		cQuery += "  	ON  AC1.AC1_FILIAL  = ? "
		cQuery += "  	AND AC1.AC1_PROVEN  = AD1.AD1_PROVEN "		
		cQuery += "  	AND AC1.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AC2") + " AC2 "
		cQuery += "  	ON  AC2.AC2_FILIAL  = ? "
		cQuery += "  	AND AC2.AC2_PROVEN  = AD1.AD1_PROVEN "
		cQuery += "  	AND AC2.AC2_STAGE   = AD1.AD1_STAGE "
		cQuery += "  	AND AC2.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AC3") + " AC3 "
		cQuery += " 	ON  AC3.AC3_FILIAL  = ? "
		cQuery += " 	AND AC3.AC3_CODCON  = AD3.AD3_CODCON "
		cQuery += " 	AND AC3.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AC4") + " AC4 "
		cQuery += " 	ON  AC4.AC4_FILIAL  = ? "
		cQuery += " 	AND AC4.AC4_PARTNE  = AD4.AD4_PARTNE "
		cQuery += " 	AND AC4.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("SU5") + " SU5 "
		cQuery += " 	ON  SU5.U5_FILIAL   = ? "
		cQuery += " 	AND SU5.U5_CODCONT  = AD9.AD9_CODCON "
		cQuery += " 	AND SU5.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("AIJ") + " AIJ "
		cQuery += " 	ON  AIJ.AIJ_FILIAL  = ? "
		cQuery += " 	AND AIJ.AIJ_NROPOR  = AD1.AD1_NROPOR "
		cQuery += " 	AND AIJ.AIJ_REVISA  = AD1.AD1_REVISA "
		cQuery += " 	AND AIJ.D_E_L_E_T_  = ? "
		cQuery += " LEFT JOIN " + RetSqlName("SUS") + " SUS "
		cQuery += "  	ON  SUS.US_FILIAL   = ? "
		cQuery += "  	AND SUS.US_COD      = AD1.AD1_PROSPE"
		cQuery += "  	AND SUS.US_LOJA     = AD1.AD1_LOJPRO"
		cQuery += "  	AND SUS.D_E_L_E_T_  = ? "
		cQuery += " WHERE AD1.AD1_FILIAL    = ? "
		cQuery += "		AND AD1_PROSPE >= ? AND AD1_PROSPE <= ? "
		cQuery += " 	AND AD1_LOJPRO >= ? AND AD1_LOJPRO <= ? "
		cQuery += " 	AND AD1_VEND   >= ? AND AD1_VEND   <= ? "
		cQuery += " 	AND AD1_DTINI  >= ? AND AD1_DTFIM  <= ? "
		cQuery += " 	AND AD1_PROVEN >= ? AND AD1_PROVEN <= ? "
		cQuery += " 	AND AD1_STAGE  >= ? AND AD1_STAGE  <= ? "
		cQuery += " 	AND AD1_CODPRO >= ? AND AD1_CODPRO <= ? "
		cQuery += " 	AND AD1_NROPOR != ? "
		cQuery += " AND ( AD1_CODPRO = ? OR "
		cQuery += " EXISTS ( "
		cQuery += " 	SELECT B1_COD FROM " + RetSqlName("SB1") + " SB1 "
		cQuery += " 	WHERE SB1.B1_FILIAL = ? "
		cQuery += " 	AND SB1.B1_COD 	    = AD1_CODPRO "
		cQuery += " 	AND SB1.B1_TIPO    >= ? AND SB1.B1_TIPO  <= ? "
		cQuery += " 	AND SB1.B1_GRUPO   >= ? AND SB1.B1_GRUPO <= ? "
		cQuery += " 	AND SB1.D_E_L_E_T_  = ? ) ) "

		If !Empty(MV_PAR19) .And. MV_PAR19 <> 1
			cQuery += " AND AD1_STATUS =  ? "	//Considera Oportunidades
		EndIf

		If !Empty(MV_PAR20) .And. MV_PAR20 <> 1
			cQuery += " AND AD1_STATUS <> ? "	//Desconsidera Oportunidades
		EndIf

		cQuery += " ? " //Ajuste de WHERE para objeto localizado
		cQuery += " AND AD1.D_E_L_E_T_  = ? "

		If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY ? "
		EndIf

	Next nX

	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

		oQuery:SetUnsafe(nParam++, cFieldsQry )
		oQuery:SetString(nParam++, FatSVFilial('AD2', 'AD2_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AD3', 'AD3_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AD4', 'AD4_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AD9', 'AD9_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SA3', 'A3_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AC1', 'AC1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AC2', 'AC2_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AC3', 'AC3_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AC4', 'AC4_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SU5', 'U5_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AIJ', 'AIJ_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('SUS', 'US_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, FatSVFilial('AD1', 'AD1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, MV_PAR01 )							//Prospect de
		oQuery:SetString(nParam++, MV_PAR03 )							//Prospect de
		oQuery:SetString(nParam++, MV_PAR02 )							//Loja de
		oQuery:SetString(nParam++, MV_PAR04 )							//Loja ate
		oQuery:SetString(nParam++, MV_PAR05 )							//Representante de
		oQuery:SetString(nParam++, MV_PAR06 )							//Representante ate
		oQuery:SetString(nParam++, DToS(MV_PAR07) )						//Data de
		oQuery:SetString(nParam++, DToS(MV_PAR08) )						//Data ate
		oQuery:SetString(nParam++, MV_PAR09 )							//Processo/Venda de
		oQuery:SetString(nParam++, MV_PAR11 )							//Processo/Venda ate
		oQuery:SetString(nParam++, MV_PAR10 )							//Etapa/Venda de
		oQuery:SetString(nParam++, MV_PAR12 )							//Etapa/Venda ate
		oQuery:SetString(nParam++, MV_PAR13 )							//Produto de
		oQuery:SetString(nParam++, MV_PAR14 )							//Produto ate
		oQuery:SetString(nParam++, ' ')
		oQuery:SetString(nParam++, Space( Len( AD1->AD1_CODPRO ) ) )	//Filtro de Campo
		oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX]) )
		oQuery:SetString(nParam++, MV_PAR15 )							//Tipo de
		oQuery:SetString(nParam++, MV_PAR16 )							//Tipo ate
		oQuery:SetString(nParam++, MV_PAR17 )							//Grupo de
		oQuery:SetString(nParam++, MV_PAR18 )							//Grupo ate
		oQuery:SetString(nParam++, ' ')

		If !Empty(MV_PAR19) .And. MV_PAR19 == 5
			oQuery:SetString(nParam++, '9')
		ElseIf !Empty(MV_PAR19) .And. MV_PAR19 <> 1 .And. MV_PAR19 <> 5
			oQuery:SetString(nParam++, Str(MV_PAR19 - 1, 1))
		EndIf

		If !Empty(MV_PAR20) .And. MV_PAR20 == 5
			oQuery:SetString(nParam++, '9')
		ElseIf !Empty(MV_PAR20) .And. MV_PAR20 <> 1 .And. MV_PAR20 <> 5
			oQuery:SetString(nParam++, Str(MV_PAR20 - 1, 1))
		EndIf

		oQuery:SetUnsafe(nParam++, self:cLocWhere)
		oQuery:SetString(nParam++, ' ')

	Next nX

	oQuery:SetUnsafe(nParam++, aOrdem[NORDER])

	self:cLocAlias := cAlias := oQuery:OpenAlias()

   	FreeObj(oQuery)

    aSize(aFiliais,0)
	aSize(aOrdem,0)

Return
