#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listoftradenotes.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listoftradenotes.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SE1,SA1", name="Relação de Duplicatas", country="ALL", customTables="SA1,SE1")

//-------------------------------------------------------------------
/*/{Protheus.doc} listoftradenotes
Classe para criação do Objeto de Negócio para Relação de duplicatas

@author FAT/CRM
@since 20/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listoftradenotesSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV006CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data cLocSelect   as character
    protected data cLocWhere    as character
    protected data cAlias       as character
    protected data ojItems      as json
 
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 20/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class listoftradenotesSmartViewBusinessObject

    _Super:new()
    self:appendArea(STR0001)     //Faturamento
    self:setDisplayName(STR0002) //Relação de Duplicatas
    self:setDescription(STR0003) //Listagem de Relação de Duplicatas
    self:setIsLookUp(.T.)        //Define que terá consulta de LookUp

    self:cLocSelect := ""
    self:cLocWhere  := ""
    self:cAlias     := ""
    self:aFields    := {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 20/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getData(nPage as numeric, oFilter as object) as object Class listoftradenotesSmartViewBusinessObject

    Local aPDFields     := {}  as array

    Local nX            := 0   as numeric

    Local lObfuscated   := .F. as logical

    Private aFieldsValue:= {}  as array
    Private aCpoCustom  := self:getCustomFields() as array

    Private nValor      := 0   as numeric

    Private MV_FIL		:= ""  as character

    //Metodo para retorno do json dos parâmetros
    FatSetValueMVPAR(oFilter:getParameters(), "MTR640")

    aFieldsValue := { {'E1_VALOR', {'E1_VALOR', "", 'nValor'}} }

    //Verifica se precisa fazer o tratamento para LGPD   
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCpoCustom )
    lObfuscated := Len( aPDFields ) > 0

    //Irá montar as queries do objeto de negócio
    self:FatSV006CreateSQL()

	//Posiciona no registro inicial
	(self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

		// Desconsidera Abatimentos
		If !(((self:cAlias)->E1_TIPO $ MVABATIM) .And. (MV_PAR16 == 2))

            nValor := xMoeda((self:cAlias)->E1_VALOR,(self:cAlias)->E1_MOEDA,MV_PAR13,(self:cAlias)->E1_EMISSAO)
            nValor := IIf( ((self:cAlias)->E1_TIPO $ MVABATIM .And. MV_PAR16 == 3), -nValor, nValor)

            self:ojItems := JsonObject():new()

            For nX := 1 To Len(self:aStruct)

                self:ojItems[self:aStruct[nX][1]] := FATSVProcessData(self:aStruct[nX], lObfuscated, aPDFields, self:cAlias, aFieldsValue)

            Next nX

            self:processData(1)
            self:oData:appendData(self:ojItems)

            (self:cAlias)->(DbSkip())

        EndIf

	EndDo

	(self:cAlias)->(DbCloseArea())

	//Limpeza do objeto/variável
    aSize(aPDFields,   0)
    aSize(aCpoCustom,  0)
    aSize(aFieldsValue,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 20/07/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listoftradenotesSmartViewBusinessObject

	Local nX          := 0  as numeric
    Local aParameters := {} as array
    
    self:aFields := {"E1_FILIAL","E1_PREFIXO","E1_NUM","E1_TIPO","E1_PARCELA","E1_CLIENTE","E1_LOJA","A1_NOME","E1_EMISSAO",;
                     "E1_VENCREA","E1_VALOR","E1_SUPERVI","E1_PEDIDO","E1_VEND1","E1_VEND2","E1_VEND3","E1_VEND4","E1_VEND5"}

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        self:oSchema:addProperty(self:aStruct[nX][01], self:aStruct[nX][02], self:aStruct[nX][03], self:aStruct[nX][02], self:aStruct[nX][05])
    Next

    // Busca no SX1 os parâmetros que serão utilizados reordenando e não passa os parametros 14 e 15
	aParameters := FtSVSetParam('MTR640',,,,{'MV_FIL','01','02','06','17','07','08','09','10','03','04','11','12','05','16','13'})

    // Adiciona os parâmetros do pergunte no Schema
    self:oSchema:addParameter("MV_FIL", STR0005 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial ?

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter( aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5] )
	Next nX

    //Validação para versão da Lib
    If __lLookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                     // Filial
        self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SA3", 2)                     // Do Vendedor
        self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SA3", 2)                     // Ate o Vendedor
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/SA1", 2)                     // Do Cliente
        self:setCustomURL("MV_PAR12", "api/framework/v1/genericLookupService/smartview/SA1", 2)                     // Ate o Cliente
        self:setCustomURL("MV_PAR05", "api/faturamento/smartview/v1/options/fat/listoftradenotes/SVListTrN/2", 1)   // Considera
        self:setCustomURL("MV_PAR16", "api/faturamento/smartview/v1/options/fat/listoftradenotes/SVListTrN/3", 1)   // Considerar Titulos
    EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVListTrN
Retorna as opçoes disponível para os parametros tipo combo

@return array: Array com as opçoes

@author FAT/CRM
@since 10/05/2024
@version 1.0
*/
//-------------------------------------------------------------------
Function SVListTrN(cOpcao) 

	Local aRet := {} as array

	If cOpcao == "1"
        aRet := { FatGetStrCpos({'E1_PREFIXO','E1_NUM'}),; // Prefixo + Titulo
                  FatGetStrCpos({'E1_EMISSAO'}),;          // Emissao
                  FatGetStrCpos({'E1_VENCREA'}),;          // Vencimento Real
                  FatGetStrCpos({'E1_CLIENTE'}) }          // Cliente
	ElseIf cOpcao == "2"
		aRet := FatGetCboX1('MTR640', 5)	               // Considera
    ElseIf cOpcao == "3"
		aRet := FatGetCboX1('MTR640', 16)	               // Considerar Titulos
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv006CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 23/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV006CreateSQL() class listoftradenotesSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nY 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
    Local nVend 	 := Fa440CntVen() as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

    //Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

    For nX := 1 To Len(self:aFields)
		cFieldsQry += IIf( SubStr(self:aFields[nX],1,3) $ 'E1_', "SE1."+self:aFields[nX]+" ," ,"SA1."+self:aFields[nX]+" ,")
	Next nX	

    cFieldsQry += "SE1.E1_MOEDA "

    //Adiciona campos localizados
    cFieldsQry += self:cLocSelect

    //Adiciona campos customizados (no array da estrutura, )
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCpoCustom)

    For nX := 1 To nNumFils

        cQuery += " SELECT ? "
        cQuery += " FROM "      + RetSqlName("SE1") + " SE1 "
        cQuery += " LEFT JOIN " + RetSqlName("SA1") + " SA1 "
        cQuery += "     ON  SA1.A1_FILIAL   = ? "
        cQuery += "     AND SA1.A1_COD      = SE1.E1_CLIENTE "
        cQuery += "     AND SA1.A1_LOJA     = SE1.E1_LOJA "
        cQuery += "     AND SA1.D_E_L_E_T_  = ? "
        cQuery += " WHERE SE1.E1_FILIAL     = ? "
        cQuery += "     AND SE1.E1_PREFIXO BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_NUM     BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_EMISSAO BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_VENCREA BETWEEN ? AND ? "
        cQuery += "     AND SE1.E1_CLIENTE BETWEEN ? AND ? "

        If MV_PAR05 == 2
            cQuery += "     AND SE1.E1_SALDO > ? "
        EndIf

        // Processa todos Vendedores existentes da SE1
        For nY := 1 To nVend
            cQuery  += "     AND SE1.E1_VEND? BETWEEN ? AND ? "
        Next nY

        cQuery += " ? "
        cQuery += "     AND SE1.D_E_L_E_T_ = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += "  UNION ALL "
		EndIf

    Next nX      

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry)                                      // Campos a serem selecionados (SELECT)
        oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX]))   // SA1.A1_FILIAL
        oQuery:SetString(nParam++, " ")                                             // SA1.D_E_L_E_T_
        oQuery:SetString(nParam++, FatSVFilial('SE1', 'E1_FILIAL', aFiliais[nX]))   // SE1.E1_FILIAL
        oQuery:SetString(nParam++, MV_PAR06)                                        // SE1.E1_PREFIXO BETWEEN (início do intervalo)
        oQuery:SetString(nParam++, MV_PAR17)                                        // SE1.E1_PREFIXO BETWEEN (fim do intervalo)
        oQuery:SetString(nParam++, MV_PAR01)                                        // SE1.E1_NUM BETWEEN (início do intervalo)
        oQuery:SetString(nParam++, MV_PAR02)                                        // SE1.E1_NUM BETWEEN (fim do intervalo)
        oQuery:SetString(nParam++, DTOS(MV_PAR07))                                  // SE1.E1_EMISSAO BETWEEN (início do intervalo)
        oQuery:SetString(nParam++, DTOS(MV_PAR08))                                  // SE1.E1_EMISSAO BETWEEN (fim do intervalo)
        oQuery:SetString(nParam++, DTOS(MV_PAR09))                                  // SE1.E1_VENCREA BETWEEN (início do intervalo)
        oQuery:SetString(nParam++, DTOS(MV_PAR10))                                  // SE1.E1_VENCREA BETWEEN (fim do intervalo)
        oQuery:SetString(nParam++, MV_PAR11)                                        // SE1.E1_CLIENTE BETWEEN (início do intervalo)
        oQuery:SetString(nParam++, MV_PAR12)                                        // SE1.E1_CLIENTE BETWEEN (fim do intervalo)

        If MV_PAR05 == 2
            oQuery:setNumeric(nParam++, 0)                                          // SE1.E1_SALDO
        EndIf

        For nY := 1 To nVend
            oQuery:SetUnsafe(nParam++, AllTrim(cValToChar(nY)))                     // Sequencial do Vendedor
            oQuery:SetString(nParam++, MV_PAR03)                                    // SE1.E1_VEND BETWEEN (início do intervalo)
            oQuery:SetString(nParam++, MV_PAR04)                                    // SE1.E1_VEND BETWEEN (fim do intervalo)
        Next nY

        oQuery:SetUnsafe(nParam++, self:cLocWhere)                                  // Campos Localizados
        oQuery:SetString(nParam++, " ")                                             // SE1.D_E_L_E_T_

    Next nX

    self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
