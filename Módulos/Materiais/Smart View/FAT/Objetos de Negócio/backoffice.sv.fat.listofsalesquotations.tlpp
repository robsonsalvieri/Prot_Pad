#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofsalesquotations.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofsalesquotations.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SCJ,SCK,SCL,SA1,SB1", name="Orçamentos de Venda", country="ALL", customTables="ALL")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofsalesquotationsSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Relação de Orçamentos de Venda.

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofsalesquotationsSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV029CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data jItems       as json
	protected data cAlias       as character
    protected data cSelectLoc   as character
    protected data cWhereLoc    as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class listofsalesquotationsSmartViewBusinessObject 

	_Super:new()
    self:appendArea(STR0001) 		//"Faturamento"
    self:setDisplayName(STR0002)	//"Orçamentos de Venda"
    self:setDescription(STR0003)	//"Relação de Orçamentos de Venda"
    self:setIsLookUp(.T.)  

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author FAT/CRM
@since 02/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class listofsalesquotationsSmartViewBusinessObject 

    Local cPrefix       := ""  as character
    Local cTabela       := ""  as character
    Local xValue        := ""  as character

    Local lObfuscated   := .F. as logical

    Local aPDFields     := {}  as array

    Local nX 			:= 0   as numeric
    Local nScan 		:= 0   as numeric

    Private lComponente := .F. as logical

    Private cPrefSelect := ""  as character

    Private aFieldsValue:= {}  as array
    Private aCposCustom := self:getCustomFields() as array

    Private MV_FIL		:= ""  as character

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR605")

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_PAR13    := IIf(MV_PAR13 <= 0 .Or. MV_PAR13 > ContaMoeda(), 1, MV_PAR13)
    lComponente := MV_PAR11 == 1

    //Array para processamento
    aFieldsValue    := { {"CK_PRCVEN", {"CK_PRCVEN"	,  "", 'xMoeda((self:cAlias)->CK_PRCVEN,(self:cAlias)->CJ_MOEDA,MV_PAR13,(self:cAlias)->CJ_EMISSAO)'}},;
                         {"CK_VALOR",  {"CK_VALOR"  ,  "", 'xMoeda((self:cAlias)->CK_VALOR,(self:cAlias)->CJ_MOEDA,MV_PAR13,(self:cAlias)->CJ_EMISSAO)'}},;
						 {"CL_PRODUTO",{"CL_PRODUTO",  "", 'IIf(lComponente , (self:cAlias)->CL_PRODUTO, "" )'}},;
                         {"CL_DESCRI", {"CL_DESCRI" ,  "", 'IIf(lComponente , (self:cAlias)->CL_DESCRI, "" )'}},;
                         {"CL_QUANT",  {"CL_QUANT"  ,  "", 'IIf(lComponente , (self:cAlias)->CL_QUANT, 0 )'}},;
						 {"TB_NECESS", {"TB_NECESS" ,  "", 'IIf(lComponente , (self:cAlias)->CL_QUANT * (self:cAlias)->CK_QTDVEN , 0 )'}};
						}

    //Tratamento LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, aCposCustom ) //Função criada p/ retornar somente campos que devem ser ofuscados,
    lObfuscated := Len(aPDFields) > 0                           //o inverso do que a função utilizada anteriormente nos retorna.

	//Irá montar as queries do objeto de negócio
    self:FatSV029CreateSQL()

    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

	While !(self:cAlias)->(Eof())

        self:jItems := JsonObject():new()
        For nX := 1 To Len(self:aStruct)
			cPrefix := SubStr( self:aStruct[nX][5], 1, 2 )
			cTabela := IIf(cPrefix $ cPrefSelect , self:cAlias, 'S'+cPrefix )

			If (nScan := aScan(aFieldsValue,  {|x| x[1] == self:aStruct[nX][5] } )) > 0
				xValue  := FatGetValueFields(aFieldsValue[nScan][1], "cTabela", aFieldsValue[nScan][2])
			Else
				xValue := "(cTabela)->" + self:aStruct[nX][5]
			EndIf

            If !lComponente .And. cTabela == 'SCL' .And. nScan == 0
                self:jItems[self:aStruct[nX][1]] := Nil
            Else
                If lObfuscated .And. ( nScan := aScan(aPDFields,  {|x| x[1] == self:aStruct[nX][5] } ) )  > 0
                    self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
                Else
                    If Alltrim(self:aStruct[nX][3]) == "date"
                        self:jItems[self:aStruct[nX][1]] := IIf( ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp( &(xValue) ) , totvs.framework.treports.date.dateToTimeStamp( &(xValue) ))
                    Else
                        self:jItems[self:aStruct[nX][1]] := &(xValue)
                    EndIf
                EndIf
            EndIf

        Next nX

        self:processData(1)
        self:oData:appendData(self:jItems)

        (self:cAlias)->(dbSkip())

	EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields,   0)
    aSize(aCposCustom, 0)
    aSize(aFieldsValue,0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@return object: self:oSchema

@author FAT/CRM
@since 05/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() as object Class listofsalesquotationsSmartViewBusinessObject

    Local nX 		    := 0  as numeric
    Local aCposFake	    := {} as array
    Local aParameters 	:= {} as array

    self:aFields := {'CK_FILIAL','CK_NUM','CK_ITEM','CK_CLIENTE','CK_LOJA','A1_NOME',;
					 'CK_PRODUTO','B1_DESC','CK_QTDVEN','CK_PRCVEN','CK_VALOR','CL_PRODUTO',;
                     'CL_DESCRI','CL_QUANT','TB_NECESS','CJ_STATUS'}

    aCposFake := {{"TB_NECESS", STR0017, "number", STR0017, "TB_NECESS"}} 

    self:aStruct := FatTrGetStruct(self:aFields, aCposFake )

    For nX := 1 To Len(self:aStruct)
        self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
    Next nX

	// Busca no SX1 os parâmetros que serão utilizados
	aParameters := FtSVSetParam("MTR605")

    self:oSchema:addParameter("MV_FIL", STR0019 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) //Filial

    // Adiciona os parâmetros do pergunte no Schema
	For nX := 1 To Len(aParameters) 
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nx][5])
	Next nX

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                         //Filial ?
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SA1", 2)                         //Cliente de ?
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SA1", 2)                         //Cliente ate ?
        self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         //Produto de ?
        self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         //Produto ate ?
        self:setCustomURL("MV_PAR09", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         //Componente de ?
        self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/SB1", 2)                         //Componente ate ?
        self:setCustomURL("MV_PAR11", "api/faturamento/smartview/v1/options/fat/listofsalesquotations/SVFatOrcPa/1", 1) //Lista Componente ?
        self:setCustomURL("MV_PAR12", "api/faturamento/smartview/v1/options/fat/listofsalesquotations/SVFatOrcPa/2", 1) //Lista Quais ?
	EndIf

    aSize(aCposFake, 0)
    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFatOrcPa
Retorna as opçoes disponível para parametros do tipo combo.

@return array: Array com as opçoes.

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFatOrcPa(cOpcao) 
	Local aRet := {} as array

	If cOpcao == "1"
		aRet := FatGetCboX1('MTR605', 11)
    ElseIf cOpcao == "2"
		aRet := FatGetCboX1('MTR605', 12)
    EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv029CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 18/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV029CreateSQL() class listofsalesquotationsSmartViewBusinessObject

    Local nX 	     := 0   as numeric
    Local nNumFils   := 0   as numeric
    Local nParam     := 1   as numeric
	Local cQuery     := ""  as character
    Local cFieldsQry := ""  as character
    Local cStatus    := ""  as character
    Local oQuery 	 := Nil as object
    Local aFiliais	 := {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)
	nNumFils := Len(aFiliais)

        //Adiciona apenas os campos
    cPrefSelect := IIf(lComponente, 'CK_|CJ_|CL_|A1_|B1_','CK_|CJ_|A1_|B1_')

	For nX := 1 To Len(self:aFields)
		cFieldsQry += IIf( SubStr(self:aFields[nX],1,3) $ cPrefSelect , self:aFields[nX] + " ," ,"")
	Next nX

    cFieldsQry += 'CJ_MOEDA,CJ_EMISSAO'
    cFieldsQry +=  self:cSelectLoc

    If MV_PAR12 == 2
        cStatus := "A" 
    ElseIf MV_PAR12 == 3 
        cStatus := "B"
    ElseIf MV_PAR12 == 4
        cStatus := "C"
    EndIf

    //Adiciona campos customizados (no array da estrutura, campos e query)
	FatInjCposPerson(@aFieldsValue, @self:aStruct, @cFieldsQry, aCposCustom, .T., .F. , .T., IIf(!lComponente, 'CL_','' ) )

	// Faz o union das tabelas de acordo com a quantidade de filiais informadas
	For nX := 1 To nNumFils

        cQuery += "SELECT ? "    
        cQuery += " FROM "      + RetSqlName("SCJ") + " SCJ " 
        cQuery += "INNER JOIN " + RetSqlName("SCK") + " SCK "
        cQuery += " ON  SCK.CK_FILIAL   = ? "
        cQuery += " AND SCK.CK_PRODUTO >= ? "
        cQuery += " AND SCK.CK_PRODUTO <= ? "
        cQuery += " AND SCK.CK_NUM      = SCJ.CJ_NUM "
        cQuery += " AND SCK.D_E_L_E_T_  = ? "

        If lComponente
            cQuery += "LEFT JOIN " + RetSqlName("SCL") + " SCL " 
            cQuery += " ON  SCL.CL_FILIAL   = ? "
            cQuery += " AND SCL.CL_NUM      = SCK.CK_NUM "
            cQuery += " AND SCL.CL_ITEMORC  = SCK.CK_ITEM "
            cQuery += " AND SCL.CL_PRODUTO >= ? "
            cQuery += " AND SCL.CL_PRODUTO <= ? "
            cQuery += " AND SCL.D_E_L_E_T_  = ? "
        EndIf

        cQuery += "LEFT JOIN "  + RetSqlName("SA1")+ " SA1 " 
        cQuery += " ON  SA1.A1_FILIAL   = ? "
        cQuery += " AND SA1.A1_COD      = SCK.CK_CLIENTE "
        cQuery += " AND SA1.A1_LOJA     = SCK.CK_LOJA "
        cQuery += " AND SA1.D_E_L_E_T_  = ? "
        cQuery += "LEFT JOIN "  + RetSqlName("SB1")+ " SB1 " 
        cQuery += " ON  SB1.B1_FILIAL   = ? "
        cQuery += " AND SB1.B1_COD      = SCK.CK_PRODUTO "
        cQuery += " AND SB1.D_E_L_E_T_  = ? "
        cQuery += "WHERE SCJ.CJ_FILIAL  = ? "
        cQuery += " AND SCJ.CJ_NUM     >= ? "
        cQuery += " AND SCJ.CJ_NUM     <= ? "
        cQuery += " AND SCJ.CJ_CLIENTE >= ? "
        cQuery += " AND SCJ.CJ_CLIENTE <= ? "
        cQuery += " AND SCJ.CJ_EMISSAO >= ? "
        cQuery += " AND SCJ.CJ_EMISSAO <= ? "

        If !Empty(cStatus)
            cQuery += " AND SCJ.CJ_STATUS = ? "
        EndIf

        cQuery  += " ? "    //Where que foi populado no objeto localizado
        cQuery += " AND SCJ.D_E_L_E_T_  = ? "

        If nNumFils > 1 .And. nX < nNumFils
			cQuery += " UNION ALL "
		Else
			cQuery += " ORDER BY CK_NUM, CK_ITEM "
		EndIf

    Next nX

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))

    For nX := 1 To nNumFils

        oQuery:SetUnsafe(nParam++, cFieldsQry )
        oQuery:SetString(nParam++, FatSVFilial('SCK', 'CK_FILIAL', aFiliais[nX] ) )
        oQuery:SetString(nParam++, MV_PAR07 )
        oQuery:SetString(nParam++, MV_PAR08 )
        oQuery:SetString(nParam++, ' ')

        If lComponente
            oQuery:SetString(nParam++, FatSVFilial('SCL', 'CL_FILIAL', aFiliais[nX] ) )
            oQuery:SetString(nParam++, MV_PAR09 )
            oQuery:SetString(nParam++, MV_PAR10 )
            oQuery:SetString(nParam++, ' ')
        EndIf

        oQuery:SetString(nParam++, FatSVFilial('SA1', 'A1_FILIAL', aFiliais[nX] ) )
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SB1', 'B1_FILIAL', aFiliais[nX] ) )
        oQuery:SetString(nParam++, ' ')
        oQuery:SetString(nParam++, FatSVFilial('SCJ', 'CJ_FILIAL', aFiliais[nX] ) )
        oQuery:SetString(nParam++, MV_PAR05 )
        oQuery:SetString(nParam++, MV_PAR06 )
        oQuery:SetString(nParam++, MV_PAR01 )
        oQuery:SetString(nParam++, MV_PAR02 )
        oQuery:SetString(nParam++, DtoS(MV_PAR03) )
        oQuery:SetString(nParam++, DtoS(MV_PAR04) )

        If !Empty( cStatus )
            oQuery:SetString(nParam++, cStatus )
        EndIf

        oQuery:SetUnsafe(nParam++, self:cWhereLoc)
        oQuery:SetString(nParam++, ' ')
        
    Next nX

	self:cAlias := oQuery:OpenAlias()

    FreeObj(oQuery)

    aSize(aFiliais, 0)

Return
