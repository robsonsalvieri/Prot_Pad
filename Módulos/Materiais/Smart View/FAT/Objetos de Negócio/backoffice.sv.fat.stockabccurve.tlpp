#Include "protheus.ch"
#Include "FwLibVersion.ch"
#Include "totvs.ch"
#Include "msobject.ch"
#Include "totvs.framework.treports.integratedprovider.th"
#Include "tlpp-rest.th"
#Include "tlpp-core.th"
#Include "backoffice.sv.fat.stockabccurve.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.stockabccurve.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SA1,SB1,SJ3", name="Curva ABC por Região", country="ALL", customTables="SJ3")

//-------------------------------------------------------------------
/*/{Protheus.doc} stockabccurveSmartViewBusinessObject
Classe para criação do Objeto de Curva ABC por Região

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------  
Class stockabccurveSmartViewBusinessObject from IntegratedProvider
    
    public method new() as object
    public method getData() as object
    public method getSchema() as object
	public method FatSv038CreateSQL()

    protected data aFields 			as array
    protected data aStruct 			as array
    protected data aColunas 		as array
    protected data aFieldsCustom	as array
    protected data ojItems 			as object
    protected data cLocSelect		as character
    protected data cLocWhere		as character
	protected data cAlias 			as character
	protected data cMVFilial 		as character
	protected data cMVNumTab		as character
	protected data nMVOrder			as numeric

 Endclass
 
//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class stockabccurveSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001) 	 	//"Faturamento"
    self:setDisplayName(STR0002)	//"Curva ABC por Região"
    self:setDescription(STR0003) 	//"Relação de Curva ABC por Região"
    self:setIsLookUp(.T.)

	self:cAlias 		:= ""
    self:cMVFilial 	 	:= ""
	self:cMVNumTab 	 	:= ""
	self:nMVOrder 	 	:= 1
	self:aColunas 	 	:= {}
	self:aFieldsCustom  := {}
	
Return self
 
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object class stockabccurveSmartViewBusinessObject

	Local lObfuscated	:= .F. as logical
	Local dIniCons 		:= Ctod("") as date
	Local dFimCons 		:= dIniCons as date
	Local cRegiaoC   	:= "" as character
	Local cTipPrec		:= "" as character
	Local cFil			:= "" as character
	Local cQuebraReg 	:= "" as character
	Local cName			:= "" as character
	Local cRealName		:= "" as character
	Local cProduto   	:= "" as character
	Local cAno			:= "" as character
	Local cMesInic 		:= "" as character
	Local cAnoAnte 		:= "" as character
	Local cRec	   		:= "" as character
	Local cNomReg		:= "" as character
	Local nMes			:= 0  as numeric
	Local nI         	:= 0  as numeric
	Local nX         	:= 0  as numeric
	Local nZ        	:= 0  as numeric
	Local nTtGeral   	:= 0  as numeric
	Local nElement   	:= 0  as numeric
	Local nContaIt   	:= 0  as numeric
	Local nPorcPrd 		:= 0  as numeric
	Local nAcPorce 		:= 0  as numeric
	Local nPrecoUn 		:= 0  as numeric
	Local nAno     		:= 0  as numeric
	Local nNMesCons		:= 0  as numeric
	Local nValor   		:= 0  as numeric
	Local nRegQtde 		:= 0  as numeric
	Local nTamCol 		:= 0  as numeric
	Local nTamAtu 		:= 0  as numeric
	Local nRegSJ3 		:= 0  as numeric
	Local nScan			:= 0  as numeric
	Local aRegQtde 		:= {} as array
	Local aPDFields		:= {} as array
	Local aRegQtde1 	:= {} as array
	Local aValArray		:= {} as array
	Local aValuePerson	:= {} as array
	Local aMesAno 		:= {} as array
	Local aRegSJ3 		:= {} as array
	Local aCamposPers	:= self:getCustomFields() as array
	Local aCabec 		:= {"","","","","","","","","","","",""} as array

	//Atribui o parâmetro novo
	MV_FIL 		:= self:cMVFilial
	MV_ORDER 	:= self:nMVOrder
	MV_NUMTAB	:= self:cMVNumTab

	//metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR755")

	MV_ORDER := IIf(MV_ORDER == 0, 1, MV_ORDER)

	// Cria a estrutura dos campos de Mes/Ano 
	For nX := 11 To 0 Step -1

		nMes := Val(StrZero(mv_par03,2)) - nX
		nMes := IIF(nMes < 1, 12 + nMes, nMes)
		cAno := IIF(nMes < 1, Str(Val(StrZero(mv_par04,4))-1,4), StrZero(mv_par04,4))

		aCabec[12-nX] := MesExtenso(nMes) + "/" + cAno
		aAdd(aMesAno, {aCabec[12-nX]})

	Next

	mv_par03 := IIF(mv_par03 > 12 .Or. mv_par03 < 1, 12, mv_par03)

	If mv_par08 == 2
		cTipPrec := "S" // Preco Standard
	ElseIf mv_par08 == 3
		cTipPrec := "C"// Preco de tabela informado no cad. de cliente
	ElseIf mv_par08 == 4                                                           
		cTipPrec := IIF(!Empty(MV_NUMTAB), MV_NUMTAB, "") // mv_par01 e Nro. Tabela 
	Else
		cTipPrec := "M" // Preco Medio
	Endif

	// Os 12 meses estao contidos em 1 ano ou 2 anos
	If StrZero(mv_par03,2) == "12"
		cAnoAnte := StrZero(mv_par04,4)
		cMesInic := "01"
	Else
		cAnoAnte := StrZero((Val(StrZero(mv_par04,4))-1),4)
		cMesInic := StrZero((Val(StrZero(mv_par03,2))+1),2)
	EndIf

	nAno := Val(cAnoAnte)
	nMes := Val(cMesInic)

	//  Calcula o MES/ANO de cada coluna do relatorio                 
	For nX := 1 To 12
		aAdd(self:aColunas,{ StrZero(nAno,4) , StrZero(nMes,2) , StrZero(nX,2) } )
		nMes++
		If ( nMes > 12 )
			nMes := 1
			nAno++
		EndIf
		dFimCons := LastDay(Ctod("01/"+StrZero(nMes,2)+"/"+StrZero(nAno,4)))
	Next nX
							
	// Cria a query
    self:FatSv038CreateSQL(cAnoAnte)

	DbSelectArea(self:cAlias)
	(self:cAlias)->(DbGoTop())

	SB1->(DbSetOrder(1)) //B1_FILIAL, B1_COD

	While (self:cAlias)->(!Eof())

		SB1->(MsSeek(FatSVFilial('SB1', 'B1_FILIAL', (self:cAlias)->TMP_FILORI) + (self:cAlias)->J3_PRODUTO))
		
		// Busca nome da Regiao atraves da SX5                             
		If !Empty((self:cAlias)->J3_REGVEND)
			cNomReg := (self:cAlias)->J3_REGVEND + " - " + FWGetSX5("A2", (self:cAlias)->J3_REGVEND)[1][4]
		Else
			cNomReg := ""
		EndIf
		
		//  Pesquisa a Filial/Regiao                                      
		nRegQtde := Ascan(aRegQtde1,{|x| x[1] == cNomReg .And. x[3]==(self:cAlias)->J3_FILIAL })

		If ( nRegQtde == 0 )
			aadd(aRegQtde1,{cNomReg,0,(self:cAlias)->J3_FILIAL})
			nRegQtde := Len(aRegQtde1)
		EndIf

		If (self:cAlias)->J3_PRODUTO <> cProduto

			If SubStr((self:cAlias)->J3_PRODUTO,1,15) <> SubStr(cProduto,1,15)

				//  Calcula o numero de meses de consumo                          
				dIniCons  := LastDay(RetFldProd(SB1->B1_COD,"B1_CONINI"))
				nNmesCons := 0

				While dIniCons < dFimCons
					nNmesCons++
					dIniCons++
					dIniCons := LastDay(dIniCons)
				EndDo

			EndIf

		EndIf

		cProduto := (self:cAlias)->J3_PRODUTO

		// Valida se o rgistro da posicao ja foi processado
		nRegSJ3 := Ascan(aRegSJ3,{|x| x[1] + x[2] + x[3] == (self:cAlias)->J3_FILIAL + cNomReg + (self:cAlias)->J3_PRODUTO})

		If nRegSJ3 == 0

			//  Calcula o Valor Unitario conforme os parametros
			Do Case
				Case cTipPrec == "S"
					nPrecoUn := xMoeda( RetFldProd((self:cAlias)->J3_PRODUTO,"B1_CUSTD"),Val(RetFldProd((self:cAlias)->J3_PRODUTO,"B1_MCUSTD")),mv_par10,ddatabase)

				Case cTipPrec == "M" .Or. cTipPrec == "1"
					nPrecoUn := xMoeda(SB1->B1_PRV1,1,mv_par10,ddatabase)

				Case cTipPrec == "C"
					nPrecoUn := IIF(Empty((self:cAlias)->A1_TABELA) .Or. (self:cAlias)->A1_TABELA == "1",;
								xMoeda(SB1->B1_PRV1,1,mv_par10,ddatabase),;
								MaTabPrVen((self:cAlias)->A1_TABELA,cProduto,0,(self:cAlias)->J3_CLIENTE,(self:cAlias)->J3_LOJA,mv_par10,ddatabase))
				OtherWise
					nPrecoUn := MaTabPrVen(cTipPrec,cProduto,0,SJ3->J3_CLIENTE,SJ3->J3_LOJA,mv_par10,ddatabase)
			EndCase

			cRec := StrZero(Val(cRec) + 1, 3)

			//  Adiciona o registro no array de dados                                 
			aAdd(aRegSJ3,{	(self:cAlias)->J3_FILIAL,;	//01 - "Filial"
							cNomReg,;				//02 - "Região de Venda + Nome da Região"
							(self:cAlias)->J3_PRODUTO,;	//03 - "Cod. do Produto"          
							SB1->B1_DESC,;			//04 - "Desc. do Produto" (PRODESC)
							nPrecoUn,;				//05 - "Valor Unitário" (VALUNIT)
							nNmesCons,;				//06 - "Num. Meses de Consumo" (MESCONS)
							cRec,;					//07 - "Sequencial" (REC)
							"",;					//08 - "Classe" (CLASSE)
							0,;						//09 - "Valor Médio" (VALCMED)
							0,;						//10 - "Quantidade Real" (QTDECT)
							0,;						//11 - "C.M.M." (QTDECM)
							"",;					//12 - "Valor Chave" (VALCHAV)
							0,;						//13 - "% Part" (PORCPROD)
							0,;						//14 - "% Acum" (NACPORCE)
							0})						//15 - "Tot.Cons." (NTOTACUM)

			//Adiciona conteúdo de campos customizados
			aValArray := {}
			For nX := 1 to len(aCamposPers)
				aAdd(aValArray, (self:cAlias)->&(aCamposPers[nX][1]))
			Next nX

			If Len(aValArray) > 0
				aAdd(aValuePerson, aValArray)
			EndIf

			//Posiciona no registro recem adicionado para as proximas tratativas 
			nRegSJ3 := Ascan(aRegSJ3,{|x| x[1] + x[2] + x[3] == (self:cAlias)->J3_FILIAL + cNomReg + (self:cAlias)->J3_PRODUTO})

		EndIf

		If nRegSJ3 > 0

			// Estorna o valor acumulado por regiao                            
			aRegQtde1[nRegQtde][2] -= aRegSJ3[nRegSJ3][9]

			// Atualiza o consumo dos ultimos 12 meses e concatena os
			// campos de Quantidade Real dos meses no array principal (aRegSJ3)                       
			nTamAtu := Len(aRegSJ3[nRegSJ3])

			If nTamAtu == 15
				nTamCol := Len(self:aColunas)
				aSize(aRegSJ3[nRegSJ3], nTamAtu + (nTamCol+Len(aMesAno)))
				For nX := 1 To nTamCol
					aRegSJ3[nRegSJ3][nTamAtu+nX] := 0
					aRegSJ3[nRegSJ3][nTamAtu+nTamCol+nX] := aMesAno[nX][1]
				Next
			Else
				nTamAtu := 15
			EndIf

			For nX := 1 To Len(self:aColunas)

				If ( self:aColunas[nX,1] == (self:cAlias)->J3_ANO )

					nValor := (self:cAlias)->(FieldGet(FieldPos("J3_QUAR"+self:aColunas[nX,2])))

					aRegSJ3[nRegSJ3][nTamAtu+nX] := nValor + aRegSJ3[nRegSJ3][nTamAtu+nX]
					aRegSJ3[nRegSJ3][10] := aRegSJ3[nRegSJ3][10] + nValor //Replace QTDECT With QTDECT + nValor

				EndIf

			Next nX

			aRegSJ3[nRegSJ3][11] := (aRegSJ3[nRegSJ3][10] / aRegSJ3[nRegSJ3][6])   							//Replace QTDECM  With QTDECT/MESCONS
			aRegSJ3[nRegSJ3][9]  := (aRegSJ3[nRegSJ3][11] * nPrecoUn)										//Replace VALCMED With QTDECM * nPrecoUn
			aRegSJ3[nRegSJ3][12] := Right(StrZero(10000000000000-Max(aRegSJ3[nRegSJ3][9],0.01),17,2),16)	//Replace VALCHAV With Right(StrZero(10000000000000-Max(TRB->VALCMED,0.01),17,2),16)////
			//Soma o valor acumulado por regiao
			aRegQtde1[nRegQtde][2] += aRegSJ3[nRegSJ3][9]

		EndIf

		(self:cAlias)->(dbSkip())
		
	EndDo

	aRegQtde := aRegQtde1

	nTtGeral := 0
	For nI := 1 To len(aRegQtde)
		nTtGeral += aRegQtde[nI][2]
	Next

	// A classificacao ABC dos produtos sempre deve ser calculada   
	// sobre o % de participacao em ordem decrescente.
	// Esta rotina calcula o % e a classe do produto.
	If !Empty(aRegQtde)
		SVMR755Clas(aRegQtde,@aRegSJ3)
	EndIf

	// Processamento do relatorio.
	If !Empty(aRegQtde) .And. !Empty(aRegSJ3)

		If MV_ORDER == 1 // por % de participacao- "FILIAL","REC"
			aSort(aRegSJ3,,,{|x,y| x[1]+x[7] < y[1]+y[7]})
		ElseIf MV_ORDER == 2 //"Por codigo (numerico)" - "FILIAL","REGIAO","PRODUTO"
			aSort(aRegSJ3,,,{|x,y| x[1]+x[2]+x[3] < y[1]+y[2]+y[3]})
		ElseIf MV_ORDER == 3 // "Por Regiao + %Participacao" - "FILIAL","REGIAO","CLASSE","PRODUTO"
			aSort(aRegSJ3,,,{|x,y| x[1]+x[2]+x[8]+x[3] < y[1]+y[2]+y[8]+y[3]})
		EndIf

		//Verifica se precisa fazer o tratamento para LGPD  
		aPDFields := FatGetfieldsLGPD(self:aFields, aCamposPers )
		lObfuscated := len( aPDFields ) > 0

		For nX := 1 To Len(aRegSJ3)

			cFil  := aRegSJ3[nX][1] 	//01 - "Filial"
			cRegiaoC := aRegSJ3[nX][2]	//02 - "Regiao"

			nElement := aScan(aRegQtde, { | x | x[1] == cRegiaoC .And. x[3] == cFil})
			If nElement > 0
				nPorcPrd := IIf(nTtGeral == 0, 100, (aRegQtde[nElement][2] / nTtGeral) * 100)
			EndIf

			nContaIt := 1

			If cQuebraReg <> aRegSJ3[nX][1] + aRegSJ3[nX][2]
				cQuebraReg := aRegSJ3[nX][1] + aRegSJ3[nX][2]
				nAcPorce := 0
			EndIf	
			
			If nContaIt <= mv_par09
				
				cProduto := aRegSJ3[nX][3] //03 - Produto

				If aRegSJ3[nX][13] >= 0 //13 - "% Part" (PORCPROD)
					nAcPorce += aRegSJ3[nX][13] //13 - "% Part" (PORCPROD)
				Endif
				
				aRegSJ3[nX][15] := 0 		//15 - "Tot.Cons." (NTOTACUM)
				aRegSJ3[nX][14] := nAcPorce //14 - "% Acum" (NACPORCE)
					
				For ni = 1 To 12
					aRegSJ3[nX][15] += aRegSJ3[nX][15+ni] //15 - "Tot.Cons." (NTOTACUM)
				Next ni

				//Soma conteudo de campos customizados no array de gravação
				If Len(aValuePerson) > 0 .And. Len(aValuePerson) >= nX
					For nI = 1 to Len(aValuePerson[nX])
						aAdd(aRegSJ3[nX], aValuePerson[nX][ni])
					Next
				EndIf

				// Geracao dos dados no relatorio
				self:ojItems := JsonObject():new()

				For nZ := 1 To Len(aRegSJ3[nX])

					cName := self:aStruct[nZ][1]
					cRealName := self:aStruct[nZ][5]

					If lObfuscated .and. (nScan := aScan(aPDFields,  {|x| x[1] == cRealName } ) )  > 0
						self:ojItems[cName] := aPDFields[nScan][2]
					Else
						self:ojItems[cName] := IIF(self:aStruct[nZ][3] == "date",;
												iif(ValType(aRegSJ3[nX][nZ]) == "C",totvs.framework.treports.date.stringToTimeStamp(aRegSJ3[nX][nZ]),totvs.framework.treports.date.dateToTimeStamp(aRegSJ3[nX][nZ])),;
												aRegSJ3[nX][nZ])
					EndIf

				Next nZ

				self:ProcessData(1)
				self:oData:appendData(self:ojItems)

				nContaIt++

			Endif

		Next nX

	EndIf
	
	FwFreeArray(aCabec)
	FwFreeArray(aRegSJ3)
	FwFreeArray(aMesAno)
	FwFreeArray(aRegQtde)
	FwFreeArray(aValArray)
	FwFreeArray(aPDFields)
	FwFreeArray(aRegQtde1)
	FwFreeArray(aCamposPers)
	FwFreeArray(aValuePerson)
	FwFreeArray(self:aColunas)
	FwFreeArray(self:aFieldsCustom)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} SVMR755Clas
Descricao - Gera arquivo de trabalho com dados do SJ3.
 
MR755Trab(ExpC1)
@param - ExpC1 = 

@return aRegQtde
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Static Function SVMR755Clas(aRegQtde,aRegSJ3)

	Local cRegiaoC 		:= "" as character
	Local cClassif 		:= "" as character
	Local cFil 			:= "" as character
	Local nPorcPrd		:= 0 as numeric
	Local nAcPorce 		:= 0 as numeric
	Local nX 			:= 0 as numeric
	Local nElement 		:= 0 as numeric
	Local nDiferen 		:= 0 as numeric
 
	//  Variaveis para movimentacao do cursor
	If !Empty(aRegSJ3)

		//Ordena pelo indice 2 ("FILIAL","REGIAO","VALCHAV")
		aSort(aRegSJ3,,,{|x,y| x[1]+x[2]+x[12] < y[1]+y[2]+y[12]})

		cFil := aRegSJ3[1][1] 		//01 - "Filial"
		cRegiaoC := aRegSJ3[1][2]	//02 - "Regiao de Venda + Nome da Regiao"

		nElement := AScan( aRegQtde, { | x | x[1] == cRegiaoC .And. x[3] == cFil } )
		nAcPorce := 0

		For nX := 1 To Len(aRegSJ3)

			If  aRegSJ3[nX][1] != cFil .Or. aRegSJ3[nX][2] != cRegiaoC
				
				cFil  := aRegSJ3[nX][1]
				cRegiaoC := aRegSJ3[nX][2]

				nElement := AScan( aRegQtde, { | x | x[1] == cRegiaoC .And. x[3] == cFil } )
				nAcPorce := 0

			Endif

			If aRegSJ3[nX][1] == cFil .Or. aRegSJ3[nX][2] == cRegiaoC

				//  Calcula classificacao ABC dos produtos
				If nAcPorce <= mv_par05
					cClassif := "A"
				ElseIf nAcPorce <= (mv_par05 + mv_par06)
					cClassif := "B"
				Else
					cClassif := "C"
				Endif

				If !Empty( aRegQtde ) .And.  nElement > 0

					nPorcPrd := IIF(aRegQtde[nElement][2] <= 0, 100, Round(aRegSJ3[nX][9] / aRegQtde[nElement][2],4) * 100)
					nAcPorce += nPorcPrd

				EndIf

				lAcPorce := nAcPorce > 100
				nDiferen := IIF(lAcPorce, nAcPorce - 100, 0)
				nAcPorce := IIF(lAcPorce, 100, nAcPorce)
				nPorcPrd := IIF(lAcPorce, Iif( nPorcPrd < nDiferen, 0, (nPorcPrd - nDiferen) ), nPorcPrd)

				aRegSJ3[nX][8]  := cClassif //08 - "Classe" (CLASSE)
				aRegSJ3[nX][13] := nPorcPrd	//13 - "% Part" (PORCPROD)

			Endif

		Next

	EndIf

Return .T.

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() class stockabccurveSmartViewBusinessObject

	Local nX 			:= 0  as numeric
	Local nI 			:= 0  as numeric
	Local nTamAtu		:= 0  as numeric
	Local cSeq			:= "" as character
	Local aParameters 	:= {} as array

	// Adiciona campos Padroes
	self:aFields := {"J3_FILIAL",;	//01 - Filial
					 "J3_REGVEND",;	//02 - Regiao
					 "J3_PRODUTO"}	//03 - Produto
	
	self:aStruct := FatTrGetStruct(self:aFields)

	// Adiciona campos Customizados
	self:aFieldsCustom :={	{"PRODESC"	,STR0004	,"string"	,STR0004	,"PRODESC"},;	//04 - "Desc. do Produto"
							{"VALUNIT"	,STR0005	,"number"	,STR0005	,"VALUNIT"},;	//05 - "Valor Unitário"
							{"MESCONS"	,STR0006	,"number"	,STR0006	,"MESCONS"},;	//06 - "Num. Meses de Consumo"
							{"REC"		,STR0007	,"string"	,STR0007	,"REC"		},;	//07 - "Sequencial"
							{"CLASSE"	,STR0008	,"string"	,STR0008	,"CLASSE"	},;	//08 - "Classe"
							{"VALCMED"	,STR0009	,"number"	,STR0009	,"VALCMED"	},;	//09 - "Valor Médio"
							{"QTDECT"	,STR0010	,"number"	,STR0010	,"QTDECT"	},;	//10 - "Quantidade Real"
							{"QTDECM"	,STR0011	,"number"	,STR0011	,"QTDECM"	},;	//11 - "C.M.M."
							{"VALCHAV"	,STR0012	,"string"	,STR0012	,"VALCHAV"	},;	//12 - "Valor Chave"
							{"PORCPROD"	,STR0013	,"number"	,STR0013	,"PORCPROD"},;	//13 - "% Part."
							{"NACPORCE"	,STR0014	,"number"	,STR0014	,"NACPORCE"},;	//14 - "% Acum."
							{"NTOTACUM"	,STR0015	,"number"	,STR0015	,"NTOTACUM"}}	//15 - "Tot. Cons."

	// Adiciona campos Customizados - Campos de Mes e Ano
	For nI := 1 To 12
		cSeq := Alltrim(Str(nI))
		aAdd(self:aFieldsCustom,{"QTDREAL"+cSeq	,STR0016+cSeq ,"number" ,STR0016+cSeq ,"QTDREAL"+cSeq }) //"Qtd. Real "+"Sequencial"
	Next

	For nI := 1 To 12
		cSeq := Alltrim(Str(nI))
		aAdd(self:aFieldsCustom,{"MESANO"+cSeq	,STR0017+cSeq ,"string"  ,STR0017+cSeq ,"MESANO"+cSeq }) //"Mes/Ano "+"Sequencial"
	Next

	// Junta os campos Padroes e Customizados no aStruct
	nTamAtu := Len(self:aStruct)
	aSize(self:aStruct,  nTamAtu + len(self:aFieldsCustom))
	aCopy(self:aFieldsCustom, self:aStruct, 1, len(self:aFieldsCustom), nTamAtu + 1)

	// Cria a estrutura dos campos Padroes
	If !Empty(self:aStruct)
		For nX := 1 To Len(self:aStruct)
			self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
		Next nX
	EndIf

	aParameters := FtSVSetParam("MTR755", {"MV_PAR11","MV_PAR12"})

	// Cria a estrutura dos parametros Customizados      
	self:oSchema:addParameter("MV_FIL"  	,STR0023 +" ?"	, "string"	, .T.,,,,,, FTSVHelp(,,,.T.)) 	// Filial                             
	self:oSchema:addParameter("MV_ORDER"	,STR0018 +" ?"	, "number"	, .F.,,,,,, FTSVHelp(,,.T.))	//"Ordem"
	self:oSchema:addParameter("MV_NUMTAB" 	,STR0019 +" ?"	, "string"	, .F.,,,,,, FTSVHelp(,,,.T.))	//"Nro. da tabela"

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

    If __lLookUp

        //Retorna opções de ordem do parâmetro
        self:setCustomURL("MV_ORDER", "api/faturamento/smartview/v1/options/fat/stockabccurve/SVFAT038/1", 1)
        self:setCustomURL("MV_PAR03", "api/faturamento/smartview/v1/options/fat/stockabccurve/SVFAT038/2", 1)
        self:setCustomURL("MV_PAR08", "api/framework/treports/integratedprovider/v1/options/MTR755/MV_PAR08", 1)
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)

    EndIf

	FwFreeArray(aParameters)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} SVFAT038
Retorna as opçoes disponível para o parâmetro de ordem

@return array: Array com as opçoes

@author FAT/CRM
@since 21/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
Function SVFAT038(cOpcao)

	Local aRet := {} as array
	Local nX   := 0 as numeric
	Local aMes := {} as array

	For nX := 1 to 12
		aAdd(aMes,MesExtenso(nX))
	Next nX

	If cOpcao == "1"
        aRet := {STR0020, STR0021, STR0022}
	elseif cOpcao == "2"
		aRet := aMes
	EndIf

Return aRet

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv038CreateSQL
	Retorna todas as querys montadas
	@author FAT/CRM
	@since 26/01/2025
	@version 1.0
	@return Nil
*/
//-------------------------------------------------------------------
Method FatSv038CreateSQL(cAnoAnte) Class stockabccurveSmartViewBusinessObject

	Local cQuery 			:= ""  as character
	Local nParam 			:= 1   as numeric
	Local cNameTmp 			:= ""  as character
	Local cFieldsQry 		:= ""  as character
	Local oTempTableBranch	:= Nil as object
	Local oQuery 	 		:= Nil as object
	Local aFiliais	 		:= {}  as array
	Local nX	 			:= 0   as numeric

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SJ3", "J3_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	// Verifica o Filtro
	cFieldsQry := " J3_FILIAL, J3_REGVEND, J3_ANO, J3_FATVEN, J3_CLIENTE, J3_LOJA, J3_PRODUTO,TMP_FILORI,A1_TABELA "+self:cLocSelect

	For nX := 1 To Len(self:aColunas)
		cFieldsQry += ", "+"J3_QUAR"+StrZero(nX,2)
	Next

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	cQuery += "SELECT ? "
	cQuery += " FROM " 		+ RetSqlName("SJ3") + " SJ3 "
	cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SJ3.J3_FILIAL "
	cQuery += "LEFT JOIN "	+ RetSqlName("SA1") + " SA1 "
	cQuery += " ON ? "
	cQuery += "	AND SA1.A1_COD 		=  SJ3.J3_CLIENTE  "
	cQuery += "	AND SA1.A1_LOJA 	=  SJ3.J3_LOJA "
	cQuery += "	AND SA1.D_E_L_E_T_ 	= ? "
	cQuery += "WHERE SJ3.J3_FILIAL 	= TMPFIL.TMP_FILIAL "
	cQuery += " AND SJ3.J3_REGVEND >= ? AND SJ3.J3_REGVEND <= ?  "
	cQuery += " AND (SJ3.J3_ANO = ? OR SJ3.J3_ANO = ? ) "
	cQuery += " ? "
	cQuery += " AND SJ3.D_E_L_E_T_ 	= ? "
	cQuery += "ORDER BY J3_FILIAL, J3_REGVEND, J3_PRODUTO, J3_ANO, J3_FATVEN "

	IIf( !Empty(self:cLocWhere) , self:cLocWhere, '')

	//Parâmetros da Query
	oQuery := FwExecStatement():New(ChangeQuery(cQuery))

	oQuery:SetUnsafe(nParam++, cFieldsQry)
	oQuery:SetUnsafe(nParam++, cNameTmp)
	oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "SJ3"))
	oQuery:SetString(nParam++, ' ')
    oQuery:SetString(nParam++, mv_par01)
    oQuery:SetString(nParam++, mv_par02)
    oQuery:SetString(nParam++, StrZero(mv_par04, 4))
    oQuery:SetString(nParam++, cAnoAnte)
	oQuery:SetUnsafe(nParam++, self:cLocWhere)
    oQuery:SetString(nParam++, ' ')

	self:cAlias := oQuery:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oQuery)
	FreeObj(oTempTableBranch)
	FwFreeArray(aFiliais)

Return
