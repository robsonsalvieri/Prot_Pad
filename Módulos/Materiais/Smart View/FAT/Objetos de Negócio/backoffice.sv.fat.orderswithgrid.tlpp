#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.orderswithgrid.ch"

Static __aSVFAT037_SC6 	:= {} as array
Static __LookUp 		:= FwLibVersion() >= "20231121" as logical

namespace totvs.protheus.backoffice.sv.fat.orderswithgrid.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SB4,SBV,SC6,SC5", name="Pedidos com Grade", country="ALL", customTables="SB4")

//-------------------------------------------------------------------
/*/{Protheus.doc} orderswithgridSmartViewBusinessObject
Classe para criação do Objeto de Pedidos com Grade

@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class orderswithgridSmartViewBusinessObject From integratedprovider

    public method new() 		 		as object
    public method getData() 	 		as object
    public method getSchema()			as object
	public method FatSv037CreateSQL()

    protected data ojItems 		as json
    protected data aFields 		as array
    protected data aStruct 		as array
    protected data cLocSelect 	as character
    protected data cLocWhere 	as character
	protected data cAliasSB4 	as character
	protected data cAliasTmpLoc as character
	protected data cQrySC6 		as character
	protected data cQrySB4 		as character
	protected data cMVFilial	as character

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method new() Class orderswithgridSmartViewBusinessObject

	_Super:new()
    self:appendArea(STR0001)     //Faturamento
    self:setDisplayName(STR0002) //Pedidos com Grade
    self:setDescription(STR0003) //Relação de Pedidos com Grade
    self:setIsLookUp(.T.)		 //Define que terá consulta de LookUp

	self:cLocSelect 	:= ""
	self:cLocWhere 		:= ""
	self:cAliasSB4 		:= ""
	self:cAliasTmpLoc 	:= ""
	self:cQrySC6 		:= ""
	self:cQrySB4 		:= ""
	self:cMVFilial		:= ""
	self:aFields 		:= {}

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 15/08/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class orderswithgridSmartViewBusinessObject

	Local aDados		:= {} as array
	Local aPDFields		:= {} as array
	Local aInsSC6		:= {} as array
	Local aCpoPerson 	:= self:getCustomFields() as array
	Local cLinha 		:= "" as character
	Local cColuna 		:= "" as character
	Local cDescLin 		:= "" as character
	Local cDescCol 		:= "" as character
	Local cProdLin		:= "" as character
	Local cAliasSC6 	:= GetNextAlias() as character
	Local cFilSC6		:= "" as character
	Local cMD5SC6 		:= "" as character
	Local nX			:= 0  as numeric
	Local nZ			:= 0  as numeric
	Local nPos			:= 0  as numeric
	Local nPos1			:= 0  as numeric
	Local nRegLin 		:= 0  as numeric
	Local nY			:= 1  as numeric
	Local nP			:= 0  as numeric
	Local nS			:= 0  as numeric
	Local nTamAtu		:= 0  as numeric
	Local nPosPrepSC6   := 0  as numeric
	Local nTamRef 		:= Val(Substr(GetMv("MV_MASCGRD"),1,2))	as numeric
    Local nTamProd	    := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric
	Local lObfuscated	:= .F. as logical

	//Tratamento tamanho do campo mascara do produto
    MV_PAR10 := PADR(MV_PAR10, nTamProd)

	MV_FIL := self:cMVFilial

	//Metodo para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR795")

	//Cria a query
	self:FatSv037CreateSQL()

	//Verifica se precisa fazer o tratamento para LGPD
    aPDFields 	:= FatGetfieldsLGPD(self:aFields, aCpoPerson)
    lObfuscated := Len(aPDFields) > 0

	DbSelectArea(self:cAliasSB4)

	(self:cAliasSB4)->(DbGoTop())

	While !(self:cAliasSB4)->(Eof())

		SBV->(DbSetOrder(1)) //BV_FILIAL, BV_TABELA
		SBV->(MsSeek((self:cAliasSB4)->B4_FILIAL+(self:cAliasSB4)->B4_LINHA))

		cFilSC6 := FatSVFilial('SC6', 'C6_FILIAL', (self:cAliasSB4)->(TMP_FILORI))

		While !Eof() .And. SBV->BV_FILIAL == (self:cAliasSB4)->B4_FILIAL .And. (SBV->BV_TABELA == (self:cAliasSB4)->B4_LINHA)

			cProdLin := SubStr((self:cAliasSB4)->B4_COD,1,nTamRef)+Alltrim(SBV->BV_CHAVE)
			cLinha   := Alltrim(SBV->BV_CHAVE)
			cDescLin := Alltrim(SBV->BV_DESCRI)

			DbSelectArea("SBV")
			nRegLin := SBV->(Recno())
			SBV->(MsSeek((self:cAliasSB4)->B4_FILIAL+(self:cAliasSB4)->B4_COLUNA))

			While !Eof() .And. SBV->BV_FILIAL == (self:cAliasSB4)->B4_FILIAL .And. (SBV->BV_TABELA == (self:cAliasSB4)->B4_COLUNA)

				cColuna := Alltrim(SBV->BV_CHAVE)
				cDescCol:= Alltrim(SBV->BV_DESCRI)
				nParam 	:= 1

				If (Alltrim(cProdLin+cColuna) >= MV_PAR03 .Or. Alltrim(cProdLin+cColuna) <= MV_PAR04)

					aAdd(aInsSC6, ' ')							// Delete SC5
					aAdd(aInsSC6, cFilSC6) 						// Filial
					aAdd(aInsSC6, MV_PAR01)				 		// Num. Pedido de Venda De
					aAdd(aInsSC6, MV_PAR02)				 		// Num. Pedido de Venda Ate
					aAdd(aInsSC6, DToS(MV_PAR05))				// Data emissao De
					aAdd(aInsSC6, DToS(MV_PAR06))				// Data emissao Ate
					aAdd(aInsSC6, MV_PAR07)				 		// Cliente De 
					aAdd(aInsSC6, MV_PAR08)				 		// Cliente Ate
					aAdd(aInsSC6, 'S')					 		// Grade
					aAdd(aInsSC6, Alltrim(cProdLin+cColuna)) 	// Codigo do Produto
					aAdd(aInsSC6, ' ')							// Delete SC6

					cMD5SC6 := MD5(self:cQrySC6)

					If (nPosPrepSC6 := Ascan(__aSVFAT037_SC6,{|x| x[2] == cMD5SC6})) == 0
						self:cQrySC6 := ChangeQuery(self:cQrySC6)
						Aadd(__aSVFAT037_SC6,{FwExecStatement():New(self:cQrySC6), cMD5SC6})
						nPosPrepSC6 := Len(__aSVFAT037_SC6)
					EndIf

					For nX := 1 To Len(aInsSC6)
						__aSVFAT037_SC6[nPosPrepSC6][1]:SetString(nX, aInsSC6[nX])
					Next nX

					__aSVFAT037_SC6[nPosPrepSC6][1]:OpenAlias(cAliasSC6)

					aSize(aInsSC6, 0)

					DbSelectArea(cAliasSC6)
					(cAliasSC6)->(dbGoTop())

					While (cAliasSC6)->(!Eof())

						If ValidMasc((cAliasSC6)->C6_PRODUTO, MV_PAR10) //Valida o produto conforme a mascara

							// Valida se existe as mesmas caracteristica do produto com Grade
							// no mesmo Pedido de Venda, caso sim, aclumina e soma na
							// Qtd a Faturar, Qtde Faturada e Qtde Total.
							nPos := Ascan(aDados,{|x| x[1] == (cAliasSC6)->C6_FILIAL;
												.And. x[2] == (self:cAliasSB4)->B4_COD;
												.And. x[4] == cLinha;
												.And. x[5] == cColuna;
												.And. x[6] == (cAliasSC6)->C6_NUM})

							If nPos == 0

								nS++

								aAdd(aDados,{	(cAliasSC6)->C6_FILIAL,; 							//01 - "Filial do Sistema"
												(self:cAliasSB4)->B4_COD,;							//02 - "Codigo do Produto"
												(self:cAliasSB4)->B4_DESC,;							//03 - "Descricao do Produto"
												cLinha,;											//04 - "Tabela que indica a linha"
												cColuna,;											//05 - "Tabela que indica a colun"
												(cAliasSC6)->C6_NUM,;								//06 - "Numero do Pedido de Venda"
												cDescLin,;											//07 - "Descricao Linha da Grade do Produto"
												cDescCol,;											//08 - "Descricao Coluna da Grade do Produto"
												(cAliasSC6)->C6_QTDEMP,;							//09 - "Qtde a Faturar"
												(cAliasSC6)->C6_QTDENT,;							//10 - "Qtde Faturada"
												(cAliasSC6)->C6_QTDEMP+(cAliasSC6)->C6_QTDENT})		//11 - "Qtde Total"
							EndIf

							nPos1 := IIF(nPos == 0, nS, nPos)

							//Aglutina valores do item
							aDados[nPos1][9]  += IIF(nPos > 0, (cAliasSC6)->C6_QTDEMP, 0) //09 - "Qtde a Faturar"
							aDados[nPos1][10] += IIF(nPos > 0, (cAliasSC6)->C6_QTDENT, 0) //10 - "Qtde Faturada"
							aDados[nPos1][11] := IIF(nPos > 0, (aDados[nPos][9] + aDados[nPos][10]), aDados[nPos1][11])	//11 - "Qtde Total"

							// Soma os registros dos campos personalizados no array de aDados
							If !Empty(aCpoPerson)

								nTamAtu := len(aDados[nY])
								asize(aDados[nY], nTamAtu + len(aCpoPerson))

								nTamAtu++

								For nP := 1 To Len(aCpoPerson)
									aDados[nY][nTamAtu] := &("(self:cAliasSB4)->"+aCpoPerson[nP][1])
									nTamAtu++
								Next nP
								
							EndIf

							nY++

						EndIf

						(cAliasSC6)->(DbSkip())

					EndDo

					(cAliasSC6)->(DbCloseArea())

				EndIf

				DbSelectArea("SBV")
				SBV->(DbSkip())

			EndDo

			DbSelectArea("SBV")
			SBV->(DbGoto(nRegLin))
			SBV->(DbSkip())

		EndDo

		DbSelectArea(self:cAliasSB4)
		(self:cAliasSB4)->(DbSkip())

	EndDo

	(self:cAliasSB4)->(DbCloseArea())

	// Geracao dos dados no relatorio
	If !Empty(aDados)

		For nX := 1 To Len(aDados)

			self:ojItems := JsonObject():new()

			For nZ := 1 To Len(aDados[nX])

				If lObfuscated .And. aScan(aPDFields, {|x| x[1] == self:aStruct[nZ][5]}) > 0

					aEstrutura := FWSX3Util():GetFieldStruct(self:aStruct[nZ][5])
					nTamCampo  := aEstrutura[3]
					cTipo      := aEstrutura[2]

					self:ojItems[self:aStruct[nZ][1]] := IIF(cTipo == 'D',;
															totvs.framework.treports.date.dateToTimeStamp(CTOD('01/01/1950')),;
															IIF(cTipo == 'N',;
																Val( Repl('9',nTamCampo) ),;
																FwProtectedDataUtil():ValueAsteriskToAnonymize(Repl("X", nTamCampo))))
				Else
					self:ojItems[self:aStruct[nZ][1]] := IIF(self:aStruct[nZ][3] == "date",;
																IIf(ValType(aDados[nX][nZ]) == "C", ;
																	totvs.framework.treports.date.stringToTimeStamp(aDados[nX][nZ]),;
																	totvs.framework.treports.date.dateToTimeStamp(aDados[nX][nZ])),;
															aDados[nX][nZ])
				EndIf

			Next nZ

			self:ProcessData(1)
			self:oData:appendData(self:ojItems)

		Next nX

	EndIf

	FwFreeArray(aDados)
	FwFreeArray(aInsSC6)
	FwFreeArray(aPDFields)
	FwFreeArray(aCpoPerson)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 26/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
Method getSchema() Class orderswithgridSmartViewBusinessObject

	Local nX 		  	:= 0  as numeric
	Local aParameters 	:= {} as array
	Local aStruCpoFake  := {} as array

	// Adiciona campos Padroes
	self:aFields := {"B4_FILIAL",;	// Filial do Sistema
					 "B4_COD",;		// Codigo do Produto 
					 "B4_DESC",;	// Descricao do Produto
					 "B4_LINHA",;	// Tabela que indica a linha
					 "B4_COLUNA",;	// Tabela que indica a coluna
					 "C6_NUM",;		// Numero do Pedido de Venda
					 "DESCLIN",;	// Descrição Linha
					 "DESCCOL",;	// Descrição Coluna
					 "NQTDEMP",;	// Quantidade a Faturar
					 "NQTDENT",;	// Quantidade Faturada 
					 "NQTDTOT"}		// Quantidade Total 

	aStruCpoFake := {{"DESCLIN", STR0004, "string", STR0004, "DESCLIN"},;	// Descrição Linha
					 {"DESCCOL", STR0005, "string", STR0005, "DESCCOL"},;	// Descrição Coluna
					 {"NQTDEMP", STR0006, "number", STR0006, "NQTDEMP"},;	// Quantidade a Faturar
					 {"NQTDENT", STR0007, "number", STR0007, "NQTDENT"},;	// Quantidade Faturada
					 {"NQTDTOT", STR0008, "number", STR0008, "NQTDTOT"}}	// Quantidade Total

	self:aStruct := FatTrGetStruct(self:aFields, aStruCpoFake)

	// Cria a estrutura dos campos Padroes
	For nX := 1 To Len(self:aStruct)
		self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
	Next nX

	// Motivo da retirada do Parametro MV_PAR09:
	// Para Visao de Dados, nao se faz necessario filtrar por tipo
	// de impressao (Detalhado, Analitico, Sintetico ou Resumido).
	// Caso haja necessidade de utilizar o filtro, podera utilizar
	// o parametro e definir no layout de Relatorio (Design).
	aParameters := FtSVSetParam("MTR795", {"MV_PAR09"})

	// Adiciona os parâmetros do pergunte no Schema
    self:oSchema:addParameter("MV_FIL", STR0009 + " ?", "string", .T.,,,,,, FTSVHelp(,,, .T.))	// Filial ?

	For nX := 1 To Len(aParameters)
		self:oSchema:addParameter(aParameters[nX][1], aParameters[nX][2], aParameters[nX][3], aParameters[nX][4],,,,,, aParameters[nX][5])
	Next nX

	If __LookUp
		self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)
		self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SC5", 2)
		self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SC5", 2)
		self:setCustomURL("MV_PAR03", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR04", "api/framework/v1/genericLookupService/smartview/SB1", 2)
		self:setCustomURL("MV_PAR07", "api/framework/v1/genericLookupService/smartview/SA1", 2)
		self:setCustomURL("MV_PAR08", "api/framework/v1/genericLookupService/smartview/SA1", 2)
	EndIf

	FwFreeArray(aParameters)
	FwFreeArray(aStruCpoFake)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv037CreateSQL
Retorna todas as querys montadas

@return Nil

@author FAT/CRM
@since 09/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSv037CreateSQL() Class orderswithgridSmartViewBusinessObject

    Local nParam	 		:= 1   as numeric
	Local cFieldsQry 		:= ""  as character
	Local cNameTmp	 		:= ""  as character
    Local oQrySB4 	 		:= Nil as object
	Local oTempTableBranch	:= Nil as object
	Local aFiliais	 		:= {}  as array

	// Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SB4", "B4_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

	cFieldsQry := "B4_FILIAL, B4_COD, B4_DESC, B4_LINHA, B4_COLUNA, "
	cFieldsQry += "TMP_FILIAL, TMP_FILORI "
	cFieldsQry += self:cLocSelect

	//Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson({}, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

	self:cQrySB4 := "SELECT ? "
	self:cQrySB4 += " FROM " + RetSqlName("SB4") + " SB4 "
	self:cQrySB4 += "INNER JOIN ? TMPFIL "
	self:cQrySB4 += " ON SB4.B4_FILIAL   = TMPFIL.TMP_FILIAL "
	self:cQrySB4 += "WHERE SB4.B4_FILIAL = TMPFIL.TMP_FILIAL "
	self:cQrySB4 += " AND SB4.B4_COD    >= ? "
	self:cQrySB4 += " AND SB4.B4_COD    <= ? "
	self:cQrySB4 += " ? "
	self:cQrySB4 += " AND SB4.D_E_L_E_T_ = ? "
	self:cQrySB4 += "ORDER BY B4_FILIAL, B4_COD "

	self:cQrySC6 := "SELECT C6_FILIAL, C6_NUM, C6_CLI, C6_GRADE, C6_PRODUTO, C6_QTDEMP, C6_QTDENT "
	self:cQrySC6 += " FROM " 	+ RetSqlName("SC6") + " SC6 "
	self:cQrySC6 += "LEFT JOIN "+ RetSqlName("SC5") + " SC5 "
	self:cQrySC6 += " ON  SC5.C5_FILIAL 		= SC6.C6_FILIAL "
	self:cQrySC6 += " AND SC5.C5_NUM    		= SC6.C6_NUM "
	self:cQrySC6 += " AND SC5.D_E_L_E_T_   	   	= ? "
	self:cQrySC6 += "WHERE SC6.C6_FILIAL 	   	= ? "
	self:cQrySC6 += " AND SC6.C6_NUM     	   >= ? "
	self:cQrySC6 += " AND SC6.C6_NUM     	   <= ? "
	self:cQrySC6 += " AND SC5.C5_EMISSAO 	   >= ? "
	self:cQrySC6 += " AND SC5.C5_EMISSAO 	   <= ? "
	self:cQrySC6 += " AND SC6.C6_CLI     	   >= ? "
	self:cQrySC6 += " AND SC6.C6_CLI     	   <= ? "
	self:cQrySC6 += " AND SC6.C6_GRADE   	   	= ? "
	self:cQrySC6 += " AND RTRIM(SC6.C6_PRODUTO) = ? "
	self:cQrySC6 += " AND SC6.D_E_L_E_T_   	   	= ? "

	oQrySB4 := FwExecStatement():New(ChangeQuery(self:cQrySB4))

	oQrySB4:SetUnsafe(nParam++, cFieldsQry)
	oQrySB4:SetUnsafe(nParam++, cNameTmp)
	oQrySB4:SetString(nParam++, MV_PAR11)
	oQrySB4:SetString(nParam++, MV_PAR12)
	oQrySB4:SetUnsafe(nParam++, self:cLocWhere)
	oQrySB4:SetString(nParam++, ' ')

	self:cAliasTmpLoc := self:cAliasSB4 := oQrySB4:OpenAlias()

	oTempTableBranch:Delete()

	FreeObj(oTempTableBranch)
	FreeObj(oQrySB4)

	FwFreeArray(aFiliais)

Return
