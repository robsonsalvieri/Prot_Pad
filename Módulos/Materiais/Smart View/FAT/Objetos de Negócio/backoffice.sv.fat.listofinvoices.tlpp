#include "protheus.ch"
#include "fwlibversion.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include "backoffice.sv.fat.listofinvoices.ch"

Static __lLookUp := FwLibVersion() >= "20231121"

namespace totvs.protheus.backoffice.sv.fat.listofinvoices.treportsintegratedprovider
using namespace totvs.framework.treports.integratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAFAT", tables="SD2", name="Notas Fiscais", country="ALL", customTables="SD2")

//-------------------------------------------------------------------
/*/{Protheus.doc} listofinvoicesSmartViewBusinessObject
Classe para criação do Objeto de Negócio para Notas Fiscais.

@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
Class listofinvoicesSmartViewBusinessObject From IntegratedProvider

    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    public method FatSV025CreateSQL()

    protected data aFields      as array
    protected data aStruct      as array
    protected data aDescCp      as array
    protected data aFieldsValue as array
    protected data aCpoQry      as array
    protected data aProdQuery   as array
    protected data aGrupQuery   as array
    protected data aTipoQuery   as array
    protected data aClieQuery   as array
    protected data cAlias       as character
    protected data cAliasTmpQry as character
 	protected data cWhereSA1    as character
    protected data cWhereSB1    as character
    protected data cWhereSF4    as character
    protected data cWhereSF2    as character
	protected data cWhereSD2    as character
    protected data jItems       as json

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method new() Class listofinvoicesSmartViewBusinessObject 
	
	_Super:new()
    self:appendArea(STR0001) 		//Faturamento
    self:setDisplayName(STR0002)	//Notas Fiscais
    self:setDescription(STR0003)	//Listagem de Notas Fiscais
    self:setIsLookUp(.T.)           //Define que terá consulta de LookUp

    self:aFields        := {}
    self:aDescCp        := {}
    self:aFieldsValue   := {}
    self:aCpoQry        := {}
    self:aProdQuery     := {}
    self:aGrupQuery     := {}
    self:aTipoQuery     := {}
    self:aClieQuery     := {}
    self:cWhereSD2      := ""

Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getData(nPage as numeric, oFilter as object) as object Class listofinvoicesSmartViewBusinessObject 

	Local aPDFields     := {} as array
    Local cTpCliFor     := "" as character
    Local cNome         := "" as character
    Local cProdRef      := "" as character
    Local cNumPed		:= "" as character
    Local cTipoNF		:= "" as character
    Local cCodCli		:= "" as character
    Local cCodLoj		:= "" as character
    Local cDoc			:= "" as character
    Local cItem			:= "" as character
    Local cCodProd		:= "" as character
    Local cDesc			:= "" as character
    Local cLocal		:= "" as character
    Local cCF			:= "" as character
    Local cTES			:= "" as character
    Local cCodISS		:= "" as character
    Local cIncSol		:= "" as character
    Local cAgreg		:= "" as character
    Local cIss			:= "" as character
    Local cItemPV		:= "" as character
    Local cRemito		:= "" as character
    Local cItemRem		:= "" as character
    Local cFilSD2       := "" as character
    Local xValue        := "" as variant
    Local nTamD2_TOTAL	:= 0 as numeric
    Local nTamD2_PRCVEN	:= 0 as numeric
    Local nX            := 0 as numeric
    Local nTamProd	    := GetSx3Cache("B1_COD", "X3_TAMANHO") as numeric
    Local nTamRef		:= Val(Substr(GetMv("MV_MASCGRD"),1,2)) as numeric
    Local nReg		    := 0 as numeric
    Local nTotRetIt		:= 0 as numeric
    Local nValFre		:= 0 as numeric
    Local nSeguro		:= 0 as numeric
    Local nDespesa		:= 0 as numeric
    Local nQtdProd		:= 0 as numeric
    Local nPrcVen		:= 0 as numeric
    Local nTotal		:= 0 as numeric
    Local nVlrIPI		:= 0 as numeric
    Local nVlrICM		:= 0 as numeric
    Local nVlrISS		:= 0 as numeric
    Local nDifal		:= 0 as numeric
    Local nFCPDif		:= 0 as numeric
    Local nScan         := 0 as numeric
    Local dEmiDia		:= dDataBase as date
    Local dDtEmisD2     := SToD("")  as date
    Local dEmissao		:= SToD("")  as date
    Local lObfuscated   := .F. as logical

    MV_FIL	 := ""
    MV_MOEDA := 0

    //Método para retorno do json dos parametros
    FatSetValueMVPAR(oFilter:GetParameters(), "MTR550P9R1")

    self:aFieldsValue:= {{"D2_FILIAL",   {"D2_FILIAL",   "", 'cFilSD2'}},;
                    {"A1_NOME",     {"A1_NOME",     "", 'cNome'}},;
                    {"F2_ESPECI1",  {"F2_ESPECI1",  "", 'cTpCliFor'}},;
                    {"F2_TIPO",     {"F2_TIPO",     "", 'cTipoNF'}},;
                    {"F2_CLIENTE",  {"F2_CLIENTE",  "", 'cCodCli'}},;
                    {"F2_LOJA",     {"F2_LOJA",     "", 'cCodLoj'}},;
                    {"F2_EMISSAO",  {"F2_EMISSAO",  "", 'dEmissao'}},;
                    {"D2_DOC",      {"D2_DOC",      "", 'cDoc'}},;
                    {"D2_SERIE",    {"D2_SERIE",    "", 'Alltrim((self:cAlias)->&(SerieNfId("SD2",3,"D2_SERIE")))'}},;
                    {"D2_ITEM",     {"D2_ITEM",     "", 'cItem'}},;
                    {"D2_COD",      {"D2_COD",      "", 'cCodProd'}},;
                    {"B1_DESC",     {"B1_DESC",     "", 'cDesc'}},;
                    {"D2_QUANT",    {"D2_QUANT",    "", 'nQtdProd'}},;
                    {"D2_PRCVEN",   {"D2_PRCVEN",   "", 'xMoeda(nPrcVen,1,MV_PAR13,dDtEmisD2, nTamD2_PRCVEN)'}},;
                    {"D2_TOTAL",    {"D2_TOTAL",    "", 'xMoeda(nTotal,1,MV_PAR13,dDtEmisD2, nTamD2_TOTAL)'}},;
                    {"D2_LOCAL",    {"D2_LOCAL",    "", 'cLocal'}},;
                    {"D2_CF",       {"D2_CF",       "", 'cCF'}},;
                    {"D2_TES",      {"D2_TES",      "", 'cTES'}},;
                    {"D2_CODISS",   {"D2_CODISS",   "", 'cCodISS'}},;
                    {"F4_INCSOL",   {"F4_INCSOL",   "", 'cIncSol'}},;
                    {"F4_AGREG",    {"F4_AGREG",    "", 'cAgreg'}},;
                    {"F4_ISS",      {"F4_ISS",      "", 'cIss'}},;
                    {"D2_PEDIDO",   {"D2_PEDIDO",   "", 'cNumPed'}},;
                    {"D2_ITEMPV",   {"D2_ITEMPV",   "", 'cItemPV'}},;
                    {"D2_REMITO",   {"D2_REMITO",   "", 'cRemito'}},;
                    {"D2_ITEMREM",  {"D2_ITEMREM",  "", 'cItemRem'}},;
                    {"D2_VALIPI",   {"D2_VALIPI",   "", 'xMoeda(nVlrIPI, 1, MV_PAR13,  dDtEmisD2)'}},;
                    {"D2_VALICM",   {"D2_VALICM",   "", 'xMoeda(nVlrICM, 1, MV_PAR13,  dDtEmisD2)'}},;
                    {"D2_VALISS",   {"D2_VALISS",   "", 'xMoeda(nVlrISS, 1, MV_PAR13,  dEmiDia  )'}},;
                    {"D2_CUSTO1",   {"D2_CUSTO1",   "", '0'}},;
                    {"D2_ICMSRET",  {"D2_ICMSRET",  "", 'xMoeda(nTotRetIt, 1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_VALFRE",   {"D2_VALFRE",   "", 'xMoeda(nValFre,  1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_SEGURO",   {"D2_SEGURO",   "", 'xMoeda(nSeguro,  1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_DESPESA",  {"D2_DESPESA",  "", 'xMoeda((self:cAlias)->D2_DESPESA, 1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_DIFAL",    {"D2_DIFAL",    "", 'xMoeda(nDifal, 1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_VFCPDIF",  {"D2_VFCPDIF",  "", 'xMoeda(nFCPDif, 1, MV_PAR13, dDtEmisD2)'}},;
                    {"D2_CUSTO2",   {"D2_CUSTO2",   "", 'IIf((self:cAlias)->D2_TIPO $ "P", xMoeda(nVlrIPI, 1, MV_PAR13, dDtEmisD2), xMoeda(nTotal + nVlrIPI + nTotRetIt, 1, MV_PAR13, dDtEmisD2, nTamD2_TOTAL))'}}}

    //Tratamento para permitir apenas o número de moedas utilizadas.
    MV_PAR13 := IIf(MV_PAR13 <= 0 .Or. MV_PAR13 > ContaMoeda(), 1, MV_PAR13)

    //Tratamento tamanho do campo mascara do produto
    MV_PAR08 := PADR(MV_PAR08, nTamProd)

    //Tratamento para filtrar por produto
    If !Empty(MV_PAR05)
        self:aProdQuery := StrTokArr(MV_PAR05, ";")
    EndIf

    //Tratamento para filtrar por grupo de produtos
    If !Empty(MV_PAR10)
        self:aGrupQuery := StrTokArr(MV_PAR10, ";")
    EndIf

    //Tratamento para filtrar por tipo de produtos
    If !Empty(MV_PAR11)
        self:aTipoQuery := StrTokArr(MV_PAR11, ";")
    EndIf

    //Tratamento para filtrar por cliente
    If !Empty(MV_PAR16)
        self:aClieQuery := StrTokArr(MV_PAR16, ";")
    EndIf

    //Verifica se precisa fazer o tratamento para LGPD
    aPDFields   := FatGetfieldsLGPD(self:aFields, self:getCustomFields())
    lObfuscated := Len( aPDFields ) > 0

    nTamD2_TOTAL  := FWSX3Util():GetFieldStruct("D2_TOTAL")[3]
    nTamD2_PRCVEN := FWSX3Util():GetFieldStruct("D2_PRCVEN")[3]

    self:FatSV025CreateSQL()

    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbGoTop())

    While !(self:cAlias)->(Eof())

        If ValidMasc((self:cAlias)->D2_COD,MV_PAR08)

            cFilSD2 := (self:cAlias)->D2_FILIAL
 
            If (self:cAlias)->F2_TIPO $ "BD"
                cTpCliFor := STR0018
                cNome := (self:cAlias)->A2_NOME
            Else
                cTpCliFor := STR0019
                cNome := (self:cAlias)->A1_NOME
            EndIf

            nReg        := 0
            dDtEmisD2   := stod((self:cAlias)->D2_EMISSAO)
            cTipoNF     := (self:cAlias)->F2_TIPO
            cCodCli     := (self:cAlias)->F2_CLIENTE
            cCodLoj     := (self:cAlias)->F2_LOJA
            dEmissao    := STOD((self:cAlias)->F2_EMISSAO)
            cDoc        := (self:cAlias)->D2_DOC
            cItem       := (self:cAlias)->D2_ITEM
            cDesc       := IIF(MV_PAR12 == 1, (self:cAlias)->B1_DESC, IIf( !Empty((self:cAlias)->A7_DESCCLI), (self:cAlias)->A7_DESCCLI, (self:cAlias)->B1_DESC ))
            nPrcVen     := (self:cAlias)->D2_PRCVEN
            cLocal      := (self:cAlias)->D2_LOCAL
            cCF         := (self:cAlias)->D2_CF
            cTES        := (self:cAlias)->D2_TES
            cCodISS     := (self:cAlias)->D2_CODISS
            cIncSol     := (self:cAlias)->F4_INCSOL
            cAgreg      := (self:cAlias)->F4_AGREG
            cIss        := (self:cAlias)->F4_ISS
            cNumPed     := (self:cAlias)->D2_PEDIDO
            cItemPV     := (self:cAlias)->D2_ITEMPV
            cRemito     := (self:cAlias)->D2_REMITO
            cItemRem    := (self:cAlias)->D2_ITEMREM

            If (self:cAlias)->D2_GRADE == "S" .And. MV_PAR09 == 1

                cProdRef    := Substr((self:cAlias)->D2_COD,1,nTamRef)
                cCodProd    := cProdRef
                nTotal      := 0
                nDifal      := 0
                nFCPDif     := 0
                nQtdProd    := 0
                nVlrIPI     := 0
                nVlrICM     := 0
                nVlrISS     := 0
                nTotRetIt   := 0
                nValFre     := 0
                nSeguro     := 0
                nDespesa    := 0

                While !(self:cAlias)->(Eof()) .And. cProdRef == Substr((self:cAlias)->D2_COD,1,nTamRef) .And. (self:cAlias)->D2_GRADE == "S" .And. cNumPed == (self:cAlias)->D2_PEDIDO

                    nTotal      += (self:cAlias)->D2_TOTAL
                    nQtdProd    += (self:cAlias)->D2_QUANT
                    nVlrIPI     += (self:cAlias)->D2_VALIPI
                    nVlrICM     += (self:cAlias)->D2_VALICM
                    nVlrISS     += (self:cAlias)->D2_VALISS
                    nTotRetIt   += IIf( (self:cAlias)->F4_INCSOL == "S", (self:cAlias)->D2_ICMSRET, 0 )
                    nValFre     += (self:cAlias)->D2_VALFRE
                    nSeguro     += (self:cAlias)->D2_SEGURO
                    nDespesa    += (self:cAlias)->D2_DESPESA
                    nDifal      += (self:cAlias)->D2_DIFAL
                    nFCPDif     += (self:cAlias)->D2_VFCPDIF

                    nReg := (self:cAlias)->(Recno())

                    (self:cAlias)->(dbSkip())

                EndDo

            Else

                nTotal    := (self:cAlias)->D2_TOTAL
                nQtdProd  := (self:cAlias)->D2_QUANT
                nVlrIPI   := (self:cAlias)->D2_VALIPI
                nVlrICM   := (self:cAlias)->D2_VALICM
                nVlrISS   := (self:cAlias)->D2_VALISS
                nTotRetIt := IIf( (self:cAlias)->F4_INCSOL == "S", (self:cAlias)->D2_ICMSRET, 0 )
                nValFre   := (self:cAlias)->D2_VALFRE
                nSeguro   := (self:cAlias)->D2_SEGURO
                nDespesa  := (self:cAlias)->D2_DESPESA
                cCodProd  := (self:cAlias)->D2_COD
                nDifal    := (self:cAlias)->D2_DIFAL
                nFCPDif   := (self:cAlias)->D2_VFCPDIF

            EndIf

            self:jItems := JsonObject():new()
            For nX := 1 To Len(self:aStruct)

                If (nScan := aScan(self:aFieldsValue, {|x| x[1] == self:aStruct[nX][5]})) > 0
                    xValue := FatGetValueFields(self:aFieldsValue[nScan][1], "self:cAlias", self:aFieldsValue[nScan][2])
                Else
                    xValue := "(self:cAlias)->"+self:aStruct[nX][5]
                EndIf

                If lObfuscated .and. (nScan := aScan(aPDFields, {|x| x[1] == self:aStruct[nX][5]})) > 0
                    self:jItems[self:aStruct[nX][1]] := aPDFields[nScan][2]
                ElseIf UPPER(self:aStruct[nX][3]) == "DATE"
                    self:jItems[self:aStruct[nX][1]] := IIf(ValType(&(xValue)) == "C", totvs.framework.treports.date.stringToTimeStamp(&(xValue)), totvs.framework.treports.date.dateToTimeStamp(&(xValue)))
                Else
                    self:jItems[self:aStruct[nX][1]] := &(xValue)
                EndIf

            Next nX

            self:processData()
            self:oData:appendData(self:jItems)

            dEmiDia := SToD((self:cAlias)->D2_EMISSAO)
            
        EndIf

        dbSelectArea(self:cAlias)

		If nReg == 0
			(self:cAlias)->(dBSkip())
		Endif

    EndDo

    (self:cAlias)->(dbCloseArea())

    aSize(aPDFields, 0)
    aSize(self:aFieldsValue, 0)

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos
 
@return object: self:oSchema
 
@author FAT/CRM
@since 14/06/2023
@version 1.0
*/
//-------------------------------------------------------------------   
Method getSchema() as object Class listofinvoicesSmartViewBusinessObject 

    Local nX          := 0  as numeric
    Local aParameters := {} as array

    self:aFields := {   "D2_FILIAL","F2_TIPO","F2_ESPECI1","F2_CLIENTE","F2_LOJA","A1_NOME","F2_EMISSAO","D2_DOC","D2_SERIE","D2_ITEM","D2_COD","B1_DESC",;
                        "D2_QUANT","D2_PRCVEN","D2_TOTAL","D2_LOCAL","D2_CF","D2_TES","D2_CODISS","F4_INCSOL","F4_AGREG","F4_ISS","D2_PEDIDO",;
                        "D2_ITEMPV","D2_REMITO","D2_ITEMREM","D2_VALIPI","D2_VALICM","D2_VALISS","D2_DESCICM","D2_CUSTO1","D2_ICMSRET",;
                        "D2_VALFRE","D2_SEGURO","D2_DESPESA","D2_CUSTO2","D2_DIFAL","D2_VFCPDIF"}

    self:aDescCp := {   STR0020,"F2_TIPO",STR0004,STR0005,"F2_LOJA","A1_NOME","F2_EMISSAO",;                    //Filial#"Cli/For"#"Codigo"
                        "D2_DOC",SerieNfId("SD2",7,"D2_SERIE"),STR0006,"D2_COD","B1_DESC","D2_QUANT",STR0007,;  //"It NF"#"Valor Unitario"
                        STR0008,"D2_LOCAL",STR0009,STR0010,"D2_CODISS","F4_INCSOL","F4_AGREG","F4_ISS",;        //"Valor Mercadoria"#"Cfo"#"Tes"
                        STR0011,STR0012,;                                                                       //"Pedido"#"It"
                        "D2_REMITO","D2_ITEMREM",STR0013,STR0014,STR0015,"D2_DESCICM",STR0016,;                 //"Valor IPI"#"Valor Icms"#"Valor Iss"#"Desp.Acessorias"
                        "D2_ICMSRET","D2_VALFRE","D2_SEGURO","D2_DESPESA",STR0017,"D2_DIFAL","D2_VFCPDIF" }     //"Total"

    self:aStruct := FatTrGetStruct(self:aFields)

    For nX := 1 To Len(self:aStruct)
        If AllTrim(self:aStruct[nX][5]) == self:aDescCp[nX]
            self:addProperty(self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5])
        Else
            self:addProperty(self:aStruct[nX][1], self:aDescCp[nX], self:aStruct[nX][3], self:aDescCp[nX], self:aStruct[nX][5])
        EndIf
    Next nX

    //Retorna do SX1 os parâmetros que serão utilizados, no array os que não serão utilizados
    aParameters := FtSVSetParam('MTR550P9R1',,,,{'03','04','16','01','02','06','07','05','08','09','10','11','12','15','14'})

	// Parâmetros para filtro do relatório
    self:oSchema:addParameter("MV_FIL", STR0020 + " ?", "string", .T.,,,,,, FTSVHelp(,,,.T.)) // Filial

    For nX := 1 To Len(aParameters)
        If Upper(aParameters[nx][1]) == "MV_PAR16"
            self:oSchema:addParameter( aParameters[nx][1], STR0019 + " ?", aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nx][5] )
        ElseIf Upper(aParameters[nx][1]) == "MV_PAR05"
            self:oSchema:addParameter( aParameters[nx][1], STR0022 + " ?", aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nx][5] )
        Else
            self:oSchema:addParameter( aParameters[nx][1], aParameters[nx][2], aParameters[nx][3], aParameters[nx][4],,,,,, aParameters[nx][5] )
        EndIf
    Next nX

    self:oSchema:addParameter("MV_MOEDA", STR0021 + " ?", "number", .F.,,,,,, FTSVHelp("MTR550P9R1","13")) // Moeda

    If __lLookUp
        self:setCustomURL("MV_FIL",   "api/framework/v1/genericLookupService/smartview/SM0", 2)                      // Filial ?
        self:setCustomURL("MV_PAR01", "api/framework/v1/genericLookupService/smartview/SF2", 2)                      // Da Nota Fiscal ?
        self:setCustomURL("MV_PAR02", "api/framework/v1/genericLookupService/smartview/SF2", 2)                      // Até da Nota Fiscal ?
        self:setCustomURL("MV_PAR05", "api/framework/v1/genericLookupService/smartview/SB1", 2)                      // Produto De ?
        self:setCustomURL("MV_PAR10", "api/framework/v1/genericLookupService/smartview/SBM", 2)                      // De Grupo ?
        self:setCustomURL("MV_PAR11", "api/framework/v1/genericLookupService/smartview/02",  2)                      // Do Tipo ?
        self:setCustomURL("MV_PAR16", "api/framework/v1/genericLookupService/smartview/CLI", 2)                      // Do Cliente ?
        self:setCustomURL("MV_PAR09", "api/framework/treports/integratedprovider/v1/options/MTR550P9R1/MV_PAR09", 1) // Aglutina prod.Grade ?
        self:setCustomURL("MV_PAR12", "api/framework/treports/integratedprovider/v1/options/MTR550P9R1/MV_PAR12", 1) // /Utiliza Descricao ?
        self:setCustomURL("MV_PAR14", "api/framework/treports/integratedprovider/v1/options/MTR550P9R1/MV_PAR14", 1) // Outras Moedas ?
        self:setCustomURL("MV_PAR15", "api/framework/treports/integratedprovider/v1/options/MTR550P9R1/MV_PAR15", 1) // Lista Dev. Compras ?
    EndIf

    aSize(aParameters, 0)

Return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} FatSv025CreateSQL
Retorna toda a query montada

@return Nil

@author FAT/CRM
@since 12/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method FatSV025CreateSQL() Class listofinvoicesSmartViewBusinessObject

    Local nParam            := 1   as numeric
	Local cQuery            := ""  as character
    Local cFieldsQry        := ""  as character
    Local cNameTmp	 		:= ""  as character
    Local oQuery 	        := Nil as object
    Local oTempTableBranch 	:= Nil as object
    Local aFiliais	        := {}  as array

    // Função que verifica se há filiais informadas no parâmetro
    aFiliais := FatGetMVFiliais(MV_FIL)

	// Função que irá criar a tabela temporaria para fazer o Join de filiais
	oTempTableBranch := FatSVTableTempBranch(aFiliais, "SD2", "D2_FILIAL")
	cNameTmp := oTempTableBranch:GetTableNameForQuery()

    cFieldsQry := "D2_FILIAL,F2_DOC,F2_SERIE,F2_SDOC,D2_SDOC,F2_EMISSAO,F2_TIPO,F2_CLIENTE,F2_LOJA,F2_FRETE,F2_FRETAUT,F2_SEGURO,F2_DESPESA,"
    cFieldsQry += "F2_VALBRUT,F2_ICMSRET,F2_ICMAUTO,F2_VALIPI,F2_VALICM,F2_VALISS,D2_DOC,D2_SERIE,D2_REMITO,D2_COD,D2_GRUPO,D2_TP,D2_TIPO,"
    cFieldsQry += "D2_CLIENTE,D2_LOJA,D2_GRADE,D2_CF,D2_TES,D2_LOCAL,D2_PRCVEN,D2_QUANT,D2_TOTAL,D2_EMISSAO,D2_ITEM,D2_ITEMREM,D2_PEDIDO,"
    cFieldsQry += "D2_ITEMPV,D2_ICMSRET,D2_VALIPI,D2_CODISS,D2_VALISS,D2_VALICM,D2_VALFRE,D2_SEGURO,D2_DESPESA,D2_DESCICM,D2_DIFAL,D2_VFCPDIF,"
    cFieldsQry += "F4_CODIGO,F4_INCSOL,F4_AGREG,F4_ICM,F4_ISS,B1_DESC,A1_COD,A1_LOJA,A1_NOME,A7_DESCCLI,A2_NOME,"
    cFieldsQry += ArrTokStr(self:aCpoQry, ",")

    // Adiciona campos customizados (no array da estrutura)
	FatInjCposPerson(@self:aFieldsValue, @self:aStruct, @cFieldsQry, self:getCustomFields(), .T., .T., .T.)

    cQuery := "SELECT ? "
    cQuery += " FROM "      + RetSqlName("SD2") + " SD2 "
    cQuery += "INNER JOIN ? TMPFIL "
	cQuery += " ON TMPFIL.TMP_FILIAL = SD2.D2_FILIAL "
    cQuery += "INNER JOIN " + RetSqlName("SF2") + " SF2 "
    cQuery += " ON  ? "
    cQuery += " AND SF2.F2_DOC      = SD2.D2_DOC "
    cQuery += " AND SF2.F2_SERIE    = SD2.D2_SERIE "
    cQuery += " AND SF2.F2_EMISSAO  = SD2.D2_EMISSAO "
    cQuery += " AND SF2.F2_CLIENTE  = SD2.D2_CLIENTE "
    cQuery += " AND SF2.F2_LOJA     = SD2.D2_LOJA    "

    If MV_PAR15 == 2
        cQuery += " AND SF2.F2_TIPO <> ? "
    EndIf

    If MV_PAR14 == 2
        cQuery += " AND SF2.F2_MOEDA = ? "
    EndIf

    cQuery += " ? "     //Where para SF2 que foi populado no objeto localizado
    cQuery += " AND SF2.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SA1") + " SA1 "
    cQuery += " ON  ? "
    cQuery += " AND SA1.A1_COD      = SD2.D2_CLIENTE "
    cQuery += " AND SA1.A1_LOJA     = SD2.D2_LOJA "
    cQuery += " ? "     //Where para SA1 que foi populado no objeto localizado
    cQuery += " AND SA1.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SB1") + " SB1 "
    cQuery += " ON  ? "
    cQuery += " AND SB1.B1_COD      = SD2.D2_COD "
    cQuery += " ? "     //Where para SB1 que foi populado no objeto localizado
    cQuery += " AND SB1.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SF4") + " SF4 "
    cQuery += " ON  ? "
    cQuery += " AND SF4.F4_CODIGO   = SD2.D2_TES "
    cQuery += " ? "     //Where para SF4 que foi populado no objeto localizado
    cQuery += " AND SF4.D_E_L_E_T_  = ? "
    cQuery += "LEFT JOIN " + RetSqlName("SA7") + " SA7 "
    cQuery += " ON  ? "
    cQuery += " AND SA7.A7_PRODUTO  = SD2.D2_COD "
    cQuery += " AND SA7.A7_CLIENTE  = SD2.D2_CLIENTE "
    cQuery += " AND SA7.A7_LOJA     = SD2.D2_LOJA "
    cQuery += " AND SA7.D_E_L_E_T_  = ? "
    cQuery += " LEFT JOIN " + RetSqlName("SA2") + " SA2 "
    cQuery += " ON  ? "
    cQuery += " AND SA2.A2_COD      = SD2.D2_CLIENTE "
    cQuery += " AND SA2.A2_LOJA     = SD2.D2_LOJA "
    cQuery += " AND SA2.D_E_L_E_T_  = ? "
    cQuery += "WHERE SD2.D2_FILIAL  = TMPFIL.TMP_FILIAL "
    cQuery += " AND SD2.D2_DOC BETWEEN ? AND ? "
    cQuery += " AND ? BETWEEN ? AND ? "
    cQuery += " AND SD2.D2_EMISSAO BETWEEN ? AND ? "

    If !Empty(self:aProdQuery)
        cQuery += " AND SD2.D2_COD IN (?) "
    EndIf

    If !Empty(self:aGrupQuery)
        cQuery += " AND SD2.D2_GRUPO IN (?) "
    EndIf

    If !Empty(self:aTipoQuery)
        cQuery += " AND SD2.D2_TP IN (?) "
    EndIf

    If !Empty(self:aClieQuery)
        cQuery += " AND SD2.D2_CLIENTE IN (?) "
    EndIf

    cQuery += " ? "      //Where para SD2 que foi populado no objeto localizado
    cQuery += " AND SD2.D_E_L_E_T_ = ? "      
    cQuery += "ORDER BY SD2.D2_FILIAL, SD2.D2_EMISSAO, SD2.D2_DOC, SD2.D2_SERIE, SD2.D2_COD, SD2.D2_ITEM "

    oQuery := FwExecStatement():New(ChangeQuery(cQuery))
    oQuery:SetUnsafe(nParam++, cFieldsQry )
    oQuery:SetUnsafe(nParam++, cNameTmp)
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SF2", "SD2"))

    If MV_PAR15 == 2
        oQuery:SetString(nParam++, 'D' )
    EndIf

    If MV_PAR14 == 2
        oQuery:SetString(nParam++, Alltrim(Str(MV_PAR13)) )
    EndIf

    oQuery:SetUnsafe(nParam++, self:cWhereSF2)
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SA1", "SD2"))
    oQuery:SetUnsafe(nParam++, self:cWhereSA1)
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SB1", "SD2"))
    oQuery:SetUnsafe(nParam++, self:cWhereSB1)
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SF4", "SD2"))
    oQuery:SetUnsafe(nParam++, self:cWhereSF4)
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SA7", "SD2"))
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetUnsafe(nParam++, FwJoinFilial("SA2", "SD2"))
    oQuery:SetString(nParam++, ' ' )
    oQuery:SetString(nParam++, MV_PAR01 )
    oQuery:SetString(nParam++, MV_PAR02 )
    oQuery:SetUnsafe(nParam++, SerieNfId("SD2", 3, "D2_SERIE") )
    oQuery:SetString(nParam++, MV_PAR06 )
    oQuery:SetString(nParam++, MV_PAR07 )
    oQuery:SetString(nParam++, DtoS(MV_PAR03) )
    oQuery:SetString(nParam++, DtoS(MV_PAR04) )

    If !Empty(self:aProdQuery)
        oQuery:SetIn(nParam++, self:aProdQuery)
    EndIf

    If !Empty(self:aGrupQuery)
        oQuery:SetIn(nParam++, self:aGrupQuery)
    EndIf

    If !Empty(self:aTipoQuery)
        oQuery:SetIn(nParam++, self:aTipoQuery)
    EndIf

    If !Empty(self:aClieQuery)
        oQuery:SetIn(nParam++, self:aClieQuery)
    EndIf

    oQuery:SetUnsafe(nParam++, self:cWhereSD2)
    oQuery:SetString(nParam++, ' ' )

    self:cAliasTmpQry := self:cAlias := oQuery:OpenAlias()

    oQuery:Destroy()
	FreeObj(oQuery)

	oTempTableBranch:Delete()
	FreeObj(oTempTableBranch)

	aSize(aFiliais, 0)

Return
