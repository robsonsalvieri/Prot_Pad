#INCLUDE "FATA020.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE 'FWMVCDEF.CH'

PUBLISH MODEL REST NAME FATA020 source FATA020

/*/


Ŀ
Program   FATA020    Autor Eduardo Riera           Data 11.01.2000	
Ĵ
Descrio Cadastro de Concorrentes.                                  	
Ĵ
Retorno   Nenhum                                                      	
Ĵ
ParametrosNenhum                                                      	
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     	
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   	
Ĵ
ٱ


/*/
Function Fata020()

Local oMBrowse  := Nil  
Local oTableAtt := TableAttDef()

PRIVATE cCadastro := STR0001   
PRIVATE aRotina := MenuDef()

oMBrowse := FWMBrowse():New()
oMBrowse:SetAlias('AC3')
oMBrowse:SetDescription(STR0001)//"Concorrentes" - "Contatos"

oMBrowse:SetAttach( .T. ) 				  //Habilita as vises do Browse

If oTableAtt <> Nil
	oMBrowse:SetViewsDefault( oTableAtt:aViews ) 
	oMBrowse:SetChartsDefault( oTableAtt:aCharts ) //Passa o grfico para o Browse
	
	oMBrowse:SetIDChartDefault( "PorEst" )	
	
		//Se no for SIGACRM inibe a exibio do grfico
		If nModulo <> 73
			oMBrowse:SetOpenChart( .F. )
		EndIf
EndIf	

oMBrowse:SetTotalDefault('AC3_FILIAL','COUNT',STR0018) // "Total de Registros"
oMBrowse:Activate()

Return(.T.)               


//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as vises e grficos.

@sample	TableAttDef()

@param		Nenhum

@return	ExpO - Objetos com as Visoes e Grficos.

@author	Cristiane Nishizaka
@since		28/04/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt 	:= FWTableAtt():New()
// Vises  
Local oAtivos		:= Nil // Parceiros Ativos
Local oInativos	:= Nil // Parceiros Inativos
// Grficos
Local oPorEst		:= Nil // Colunas: Concorrentes Por Estado

oTableAtt:SetAlias("AC3")

//----------
// Vises
//---------- 
	
// Concorrentes Ativos
oAtivos := FWDSView():New()
oAtivos:SetName(STR0015) // "Concorrentes Ativos"
oAtivos:SetID("Ativos") 
oAtivos:SetOrder(1) // AC3_FILIAL+AC3_CODCON
oAtivos:SetCollumns({"AC3_CODCON", "AC3_NOME", "AC3_NREDUZ", "AC3_END", "AC3_MUN",;
					 	"AC3_EST", "AC3_BAIRRO", "AC3_DDD", "AC3_TEL", "AC3_CONTAT", "AC3_HPAGE"})
oAtivos:SetPublic( .T. )
oAtivos:AddFilter(STR0015, "AC3_MSBLQL == '2'") // "Concorrentes Ativos"

oTableAtt:AddView(oAtivos)

// Concorrentes Inativos
oInativos := FWDSView():New()
oInativos:SetName(STR0016) // "Concorrentes Inativos"
oInativos:SetID("Inativos") 
oInativos:SetOrder(1) // AC3_FILIAL+AC3_CODCON
oInativos:SetCollumns({"AC3_CODCON", "AC3_NOME", "AC3_NREDUZ", "AC3_END", "AC3_MUN",;
					 	"AC3_EST", "AC3_BAIRRO", "AC3_DDD", "AC3_TEL", "AC3_CONTAT", "AC3_HPAGE"})
oInativos:SetPublic( .T. )
oInativos:AddFilter(STR0016, "AC3_MSBLQL == '1'") // "Concorrentes Inativos"

oTableAtt:AddView(oInativos)

//------------
// Grficos
//------------
	
// Colunas: Concorrentes Por Estado
oPorEst := FWDSChart():New()
oPorEst:SetName(STR0017) // "Concorrentes Por Estado"
oPorEst:setTitle(STR0017) // "Concorrentes Por Estado"
oPorEst:SetID("PorEst") 
oPorEst:SetType("BARCOMPCHART")
oPorEst:SetSeries({ {"AC3", "AC3_CODCON", "COUNT"} })
oPorEst:SetCategory( { {"AC3", "AC3_EST"} } )
oPorEst:SetPublic( .T. )
oPorEst:SetLegend( CONTROL_ALIGN_BOTTOM ) //Inferior
oPorEst:SetTitleAlign( CONTROL_ALIGN_CENTER ) 	

oTableAtt:AddChart(oPorEst)

Return (oTableAtt)


/*


ͻ
Programa  ModelDef  Autor  Vendas CRM           Data   17/09/10   
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       FATA020                                                     
ͼ


*/
Static Function ModelDef()

Local oModel
Local oStruAC3 := FWFormStruct(1,'AC3', /*bAvalCampo*/,/*lViewUsado*/ )

Local bCommit		:= {|oMdl|FATA020Cmt(oMdl)}		// Gravacao dos dados
Local bPosValidacao	:= {|oMdl|FA20VerCon(oMdl)}		// Bloco de validacao posterior 

Local aMemoAC3 := { { 'AC3_CODMEM' , 'AC3_MEMO' } }

oModel := MPFormModel():New('FATA020', /*bPreValidacao*/,bPosValidacao,bCommit,/*bCancel*/ )
FWMemoVirtual( oStruAC3, aMemoAC3)

oModel:AddFields('AC3MASTER',/*cOwner*/,oStruAC3, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )

oModel:SetDescription(STR0001)

Return oModel

/*


ͻ
Programa  ViewDef   Autor  Vendas CRM           Data   17/09/10   
͹
Desc.     Define a interface para cadastro de concorrentes em MVC.    
                                                                      
͹
Uso       FATA020                                                     
ͼ


*/
Static Function ViewDef()   

Local oView    := Nil
Local oModel   := FWLoadModel('FATA020')
Local oStruAC3 := FWFormStruct( 2,'AC3')     


oStruAC3:RemoveField("AC3_CODMEM")

oView := FWFormView():New()
oView:SetContinuousForm()
oView:SetModel(oModel)
oView:AddField('VIEW_AC3',oStruAC3,'AC3MASTER')

oView := CRMXAddAct("AC3",oView) //Adcionar Rotinas no 'Aes relacionadas' do Formulrio 

oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_AC3','TELA') 
  
Return oView

/*/


Ŀ
Funo    |MenuDef    Autor Vendas CRM              Data 08/12/06  
Ĵ
Descrio  Funcao de definio do aRotina                             
Ĵ
Retorno    aRotina   retorna a array com lista de aRotina             
Ĵ
Uso        SIGAFAT                                                    
ٱ


/*/
Static Function MenuDef() 

Local nPos       := 0

Local aRotina    := {}
Local aEntRelac  := {}
Local aAtiv       := {}
Local aAnotac     := {}


ADD OPTION aRotina TITLE STR0004 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0//pesquisar
ADD OPTION aRotina TITLE STR0005 ACTION 'VIEWDEF.FATA020'	OPERATION 2	ACCESS 0//visualizar
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.FATA020'	OPERATION 3	ACCESS 0//incluir
ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.FATA020'	OPERATION 4	ACCESS 0//alterar
ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.FATA020'	OPERATION 5	ACCESS 0//excluir

ADD OPTION aEntRelac  TITLE STR0002  ACTION "FtContato('AC3', AC3->(Recno()), 4)" OPERATION  4 ACCESS 0 //"Contatos"

If nModulo == 73
	
	aEntRelac := CRMXINCROT( "AC3", aEntRelac )
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA190Con()",Nil) })
	If nPos > 0 
		ADD OPTION aRotina TITLE aEntRelac[nPos][1] ACTION aEntRelac[nPos][2] OPERATION 8  ACCESS 0//"Conectar"
		Adel(aEntRelac,nPos)
		Asize(aEntRelac,Len(aEntRelac)-1)
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x|  IIF(ValType(x[2]) == "C", x[2] == "CRMA180()", Nil) })
	If nPos > 0
		ADD OPTION aAtiv   TITLE STR0013 ACTION "CRMA180(,,,3,,)" OPERATION 3  ACCESS 0 //"Nova Atividade" 
		ADD OPTION aAtiv   TITLE STR0014 ACTION "CRMA180()" OPERATION 8  ACCESS 0 //"Todas as ATividades"
		aEntRelac[nPos][2] := aAtiv
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA090()", Nil)})
	If nPos > 0
		ADD OPTION aAnotac   TITLE STR0011 ACTION "CRMA090(3)" OPERATION 3  ACCESS 0 //"Nova Anotao"
		ADD OPTION aAnotac   TITLE STR0012 ACTION "CRMA090()" OPERATION 8  ACCESS 0 //"Todas as Anotaes" 
		aEntRelac[nPos][2] := aAnotac
	EndIf
	
EndIf

ADD OPTION aEntRelac  TITLE STR0003  ACTION "MsDocument('AC3',AC3->(Recno()),4)" OPERATION  9 ACCESS 0 //"Conhecimento"

Asort(aEntRelac,,,{ | x,y | y[1] > x[1] } )
ADD OPTION aRotina TITLE STR0010 ACTION aEntRelac OPERATION 8  ACCESS 0//"Relacionadas"

Return(aRotina)

/*


ͻ
Programa  FATA020CmtAutor  Vendas CRM           Data   21/09/10   
͹
Desc.     Bloco executado na gravacao dos dados do formulario, substi-
          tuindo a gravacao padrao do MVC.                            
͹
Uso       FATA020                                                     
ͼ


*/
Static Function FATA020Cmt(oMdl)

Local aArea			:= GetArea()
Local nOperation	:= oMdl:GetOperation()

Local lFTBTTS	:= ExistBlock("FTBTTS")	// P.E executado durante a gravao   

If lFTBTTS
	bTTS := {||ExecBlock( "FTBTTS", .F., .F. )}
EndIf 

If nOperation == MODEL_OPERATION_DELETE
	Ft020DEL()
EndIf

RestArea( aArea )

FWFormCommit( oMdl )

Return .T.

/*/


Ŀ
Funcao    Ft020Del   Autor Eduardo Riera           Data 11.01.2000
Ĵ
Descrio Funcao de Tratamento da Exclusao do Concorrente             
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft020DEL()

Local lExclui := .T.

//Ŀ
// Exclui a amarracao de contatos                                         
//
FtContato( Alias(), Recno(), 2, , 3 ) 

//Ŀ
// Exclui a amarracao de conhecimento                                     
//
If !Empty( Select( "AC9" ) ) 
	MsDocument( Alias(), Recno(), 2, , 3 ) 
EndIf 

//Ŀ
// Exclusao de Anotaes    
//		                                                                        
If nModulo == 73
	CRMA090Del( "SA1", SA1->( Recno() ) )
EndIf	

Return(lExclui)         

/*


Ŀ
Funao    FA20VerCon  Autor  Marco Aurelio - Mano     Data 18/05/11  
Ĵ
Descriao Verifica se existe vinculo do concorrente a ser excluido no    
          cadastro de oportunidades.                                     
Ĵ
Sintaxe   FA20VerOp(ExpO1)                                               
Ĵ
ParametrosExpO1 = Objeto da FormModel                                    
Ĵ
Uso       FATA020                                                        
ٱ


*/             
Static Function FA20VerCon(oModel)
Local nOpc      := (oModel:GetOperation())	// Numero da operacao executada     
Local cAliasTRB := GetNextAlias()			// Nome do arquivo temporario
Local cQuery    := ""						// Auxiliar para execucao de query
Local aArea     := GetArea()				// Salva area atual para posterior restauracao
Local lRet      := .T.						// Conteudo de retorno

//Ŀ
//Valida possibilidade de exclusao do concorrente 
//
If nOpc == MODEL_OPERATION_DELETE

	//Ŀ
	//Query para pesquisar o cliente na tabela de Oportunidaes(AD1) 
	//
	cQuery := "SELECT * "
	
	//Ŀ
	//Tabela base do filtro 
	//
	cQuery += "FROM "  
	cQuery += RetSqlName("AD3")+" AD3 "	
	
	//Ŀ
	//Condicoes de filtro 
	//
	cQuery += "WHERE "
	cQuery += "AD3_FILIAL = '"+ xFilial("AD3")  +"' AND "
	cQuery += "AD3_CODCON = '"+ AC3->AC3_CODCON +"' AND "
	cQuery += "AD3.D_E_L_E_T_  <> '*'"
	
	cQuery := ChangeQuery(cQuery)
	
	DbUseArea(.T.,'TOPCONN',TCGENQRY(,,cQuery),cAliasTRB,.T.,.T.)
	
	If !(cAliasTRB)->(Eof())
	
		Help(" ",1,"CONNODEL") // Exclusao do concorrente nao permitida pois existe vinculo do mesmo no cadastro de oportunidades. 
		lRet := .F.
		
	EndIf
	
	(cAliasTRB)->(DbCloseArea())

EndIf

RestArea(aArea)

Return(lRet)
