#INCLUDE "MATA560.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Mata560  ³ Autor ³ Elizabeth A. Eguni    ³ Data ³ 19/09/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Atualizacao mensal de vendas de produtos (Consumo).        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Arq.Util. ³ SA1/SB1/SD1/SD2/SJ3                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Viviani       ³05/02/99³19742A³Inclusao do Mv_CfoFat                   ³±±
±±³Aline C. Vale ³14.04.99³Proth ³Retirada de macros para Protheus        ³±±
±±³Fernando Joly ³24.05.99³XXXXXX³Revis„o na Declara‡„o de Variaveis Loc. ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mata560()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local oDlg
Local cSavMenuh  := ''
Local nOpca      := 0
Local GetList    := {}
Local aCRA       := { "Confirma","Redigita","Abandona" }
Local aAC        := { "Abandona","Confirma" }
Local cDatCalc   := ''
Local bProcesso  := { |oSelf| MA560Process(oSelf,,cDatCalc)}
Local oTProces
      
Private dAtualiz := CtoD('  /  /  ')
Private lVersao101	:= GetRpoRelease()=="R1.1"


cMens := OemToAnsi(STR0001)+chr(13)		//"Esta  rotina  exige  que  os   arquivos"
cMens += OemToAnsi(STR0002)+chr(13)		//"associados a ela n†o estejam em uso por"
cMens += OemToAnsi(STR0003)+chr(13)		//"outras esta‡”es."
cMens += OemToAnsi(STR0004)+chr(13)		//"Fa‡a com que os outros usu rios saiam do"
cMens += OemToAnsi(STR0005)+chr(13)		//"sistema."

IF !MsgYesNo(cMens,OemToAnsi(STR0006))	//"ATEN€ŽO"
	Return
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // Inclui Dev.Compras ?                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		

If lVersao101
	cDatCalc := StrZero(Month(dDataBase),2) + "/"
	cDatCalc += Substr(StrZero(Year(dDataBase),4),3,2)
	oTProces := tNewProcess():New( "MATA560" , STR0007 , bProcesso , STR0007 , "MTA560",,,,,.T.,.T.  ) 
	oTProces:SaveLog(OemToAnsi(STR0016))
Else
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
	Pergunte("MTA560",.T.)	
	cDatCalc := StrZero(Month(dDataBase),2) + "/"
	cDatCalc += Substr(StrZero(Year(dDataBase),4),3,2)
	nOpca    := 0
	DEFINE MSDIALOG oDlg FROM  103,   9 TO 350,592 TITLE OemToAnsi(STR0007) PIXEL		//"Atualiza‡„o Mensal de Produtos"
	@ 30, 6 TO 86, 287 LABEL "" OF oDlg  PIXEL
	@ 43, 15 SAY OemToAnsi(STR0008) SIZE 268, 8 OF oDlg PIXEL		//"Este programa far  o c lculo mensal de produtos (consumo mensal).                            "
	@ 52, 15 SAY OemToAnsi(STR0009) SIZE 268, 8 OF oDlg PIXEL		//"Calcula com base no mˆs, ano informados,somente para vendas.NÆo calcula consumos (saidas)    "
	@ 62, 15 SAY OemToAnsi(STR0010) SIZE 268, 8 OF oDlg PIXEL		//"para transferˆncia ou produ‡Æo . "
	@ 64, 98 MsGet cDatCalc Valid !Empty(cDatCalc) Picture "99/99" OF oDlg PIXEL
	DEFINE SBUTTON FROM 100, 224 TYPE 1 ACTION (oDlg:End(),nOpca:=1) ENABLE OF oDlg
	DEFINE SBUTTON FROM 100, 252 TYPE 2 ACTION oDlg:End() ENABLE OF oDlg
	ACTIVATE MSDIALOG oDlg
		If nOpca == 1
			Processa({|lEnd| MA560Process(,@lEnd,cDatCalc)},OemToAnsi(STR0007),OemToAnsi(STR0011),.F.) 		//"Atualiza‡„o Mensal de Produtos"###"Efetuando Atualiza‡„o Mensal de Produtos..."
		EndIf
	EndIf
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MA560Process³ Autor ³                       ³ Data ³        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao que executa o processamento                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA560                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MA560Process(oSelf,lEnd,cDatCalc)
LOCAL lMta560    := (existblock("MTA560G"))
Local cInd       := ''
Local cIndex     := ''
Local nIndex     := 0
Local nTotRegs   := 0
Local cKey       := ''
Local cCliente   := ''
Local cLojaCli   := ''
Local cProduto   := ''
Local nValProd   := ''
Local nQtdProd   := ''
Local cAnoCalc   := ''
Local cCondicao  := ''
Local l560SD1    := ExistBlock("M560SD1")
Local l560SD2    := ExistBlock("M560SD2")
Local cCFODEV	 := SuperGetMV("MV_CFODEV",.F.,"")

While .T.
	dAtualiz := Ctod("01/" + cDatCalc,"ddmmyy")

	dbSelectArea("SD2")
	nTotRegs := RecCount()
	ProcRegua(nTotRegs)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Abre todos os arquivos de forma exclusiva                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ! ( MA280FLock("SA1") .And. MA280FLock("SB1") .And. MA280FLock("SD1") ;
			.And. MA280FLock("SD2") .And. MA280FLock("SJ3") )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Fecha todos os arquivos e reabre-os de forma compartilhada   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Exit
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Abre indices dos arquivos que foram abertos de forma exclusivo ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	OpenIndx("SA1")
	OpenIndx("SB1")
	OpenIndx("SD1")
	OpenIndx("SD2")
	OpenIndx("SJ3")

	dbSelectArea("SJ3")
	dbSetOrder(3)   && J3_FILIAL+J3_REGVEND+J3_CLIENTE+J3_LOJA+J3_PRODUTO+J3_ANO

	dbSelectArea("SD2")
	dbSetOrder(1)        && D2_FILIAL + D2_COD +D2_LOCAL + D2_NUMSEQ

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Criacao de indice condicional temporario.                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cIndex := CriaTrab(Nil,.F.)
	dbSelectArea("SD2")
	cKey   := "D2_CLIENTE + D2_LOJA + D2_COD"
	IndRegua("SD2",cIndex,cKey,,MA560Cond2(),OemToAnsi(STR0012))		//"Selecionando Registros..."
	nIndex := RetIndex("SD2")
	dbSelectArea("SD2")

	dbSetOrder(nIndex+1)
	dbGoTop()

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava N.F. de vendas (SD2) no SJ3 (arq. de acumulados mensais).  ³
	//³ Somente produtos cuja Classificacao Fiscal seja x.11 ou x.12.    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While SD2->(!Eof())

		cCliente := SD2->D2_CLIENTE
		cLojaCli := SD2->D2_LOJA
		cProduto := SD2->D2_COD
		nValProd := 0
		nQtdProd := 0

		While cCliente+cLojaCli+cProduto==SD2->D2_CLIENTE+SD2->D2_LOJA+SD2->D2_COD .And. SD2->(!Eof())
			dbSelectArea("SA1")
			dbSetOrder(1)
			dbSeek( xFilial("SA1") + cCliente + cLojaCli )

			If l560Sd2
				nValProd += Execblock("M560SD2",.f.,.f.)
			Else
				nValProd += SD2->D2_TOTAL
			Endif

			nQtdProd += SD2->D2_QUANT

			IncProc()
			dbSelectArea("SD2")
			dbSkip()
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava no SJ3 somente produtos/clientes com vendas efetuadas.   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nValProd + nQtdProd > 0
			cAnoCalc := StrZero(Year(dAtualiz),4)
			cMesCalc := StrZero(Month(dAtualiz),2)

			dbSelectArea("SJ3")
			dbSeek( xFilial("SJ3") + SA1->A1_REGIAO + cCliente + cLojaCli + cProduto + cAnoCalc )
			If Eof()
				Reclock("SJ3",.T.)
				Replace	J3_FILIAL  With xFilial("SJ3"), J3_PRODUTO 	With cProduto,;
						J3_REGVEND With SA1->A1_REGIAO, J3_ANO     	With cAnoCalc,;
						J3_CLIENTE With cCliente,       J3_LOJA		With cLojaCli
			Endif

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gravacao no mes correspondente ao movimento.                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FieldPut(FieldPos("J3_QUAR"+cMesCalc),nQtdProd)
			FieldPut(FieldPos("J3_VALR"+cMesCalc),nValProd)

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada para eventual grava‡„o adicional no SJ3     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMta560
				Execblock("MTA560G",.F.,.F.)
			Endif
		Endif
		dbSelectArea("SD2")
	End

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravacao de N.F. de devolucao no SJ3 (acumulados mensais).       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SD1")
	dbSetOrder(1)        && D1_FILIAL+D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA+D1_COD

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Criacao de indice condicional temporario.                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cIndex := CriaTrab(Nil,.F.)
	dbSelectArea("SD1")
	cKey   := "D1_FORNECE + D1_LOJA + D1_COD"
	cCondicao := 'D1_FILIAL == "' + xFilial("SD1") + ;
		'" .And. Substr(DTOS(D1_DTDIGIT),5,2) == "' + StrZero(Month(dAtualiz),2) + ;
		'" .And. Substr(DTOS(D1_DTDIGIT),1,4) == "' + StrZero(Year(dAtualiz),4)
	cCondicao += '" .And. D1_TIPO == "D" '
	
	IndRegua("SD1",cIndex,cKey,,cCondicao,OemToAnsi(STR0012))	//"Selecionando Registros..."
	nIndex := RetIndex("SD1")
	dbSelectArea("SD1")
	
	dbSetOrder(nIndex+1)
	dbGoTop()
	IncProc()

	dbSelectArea("SD1")
	nTotRegs := RecCount()
	ProcRegua(nTotRegs)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Grava N.F. de devolucao (SD1) no SJ3 (acumulados mensais).       ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	While SD1->(!Eof())
		cCliente := SD1->D1_FORNECE
		cLojaCli := SD1->D1_LOJA
		cProduto := SD1->D1_COD
		nValProd := 0
		nQtdProd := 0
		While cCliente+cLojaCli+cProduto == SD1->D1_FORNECE+SD1->D1_LOJA+SD1->D1_COD .And. SD1->(!Eof())
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Movimentacao do cursor.                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IncProc()
			dbSelectArea("SF4")
			dbSeek( xFilial("SF4") + SD1->D1_TES )
			If AllTrim(SD1->D1_CF) $ cCFODEV
				If SF4->F4_ESTOQUE == "S" .And. SF4->(!Eof())
			 		If l560Sd1
			 			nValProd += Execblock("M560SD1",.f.,.f.)
			 		Else
			 			nValProd += SD1->D1_TOTAL
			 		Endif
			 		nQtdProd += SD1->D1_QUANT
		   		Endif
			EndIf

			dbSelectArea("SD1")
			dbSkip()
		End

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Grava no SJ3 somente produtos/clientes com devolucoes.         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If nValProd + nQtdProd > 0
			dbSelectArea("SA1")
			dbSeek( xFilial("SA1") + cCliente + cLojaCli )

			cAnoCalc := StrZero(Year(dAtualiz),4)
			cMesCalc := StrZero(Month(dAtualiz),2)

			dbSelectArea("SJ3")
			dbSeek( xFilial("SJ3") + SA1->A1_REGIAO + cCliente + cLojaCli + cProduto + cAnoCalc )
			cInd := "SJ3->J3_QUAR"+cMesCalc
			If Eof()
				Reclock("SJ3",.T.)
				Replace  J3_FILIAL  With xFilial("SJ3"), J3_PRODUTO	With cProduto ,;
						 J3_REGVEND With SA1->A1_REGIAO, J3_ANO		With cAnoCalc ,;
						 J3_CLIENTE With cCliente,       J3_LOJA	With cLojaCli
			ElseIf &cInd < 0
				FieldPut(FieldPos("J3_QUAR"+cMesCalc),0)
				FieldPut(FieldPos("J3_VALR"+cMesCalc),0)
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Gravacao no mes correspondente ao movimento.                 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			FieldPut(FieldPos("J3_QUAR"+cMesCalc),&("J3_QUAR"+cMesCalc) - nQtdProd)
			FieldPut(FieldPos("J3_VALR"+cMesCalc),&("J3_VALR"+cMesCalc) - nValProd)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de entrada para eventual grava‡„o adicional no SJ3     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lMta560
				Execblock("MTA560G",.F.,.F.)
			Endif
		Endif
		dbSelectArea("SD1")
	End
	Exit
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha todos os arquivos e reabre-os de forma compartilhada   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

DbCloseAll()
__DRIVER := ""
OpenFile(SubStr(cNumEmp,1,2))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Retorna ordens originais dos arquivos.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

dbSelectArea("SD2")
RetIndex("SD2")

dbSelectArea("SD2")
Set Filter To

RetIndex("SD1")

dbSelectArea("SD1")
Set Filter To

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA560Cond2³ Autor ³ Elizabeth A. Eguni    ³ Data ³ 25/10/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Retorna Expresao para Indice Condicional ( SD2 )           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Ma560Cond2()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Gen‚rico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MA560Cond2()
Local cCondicao	:= ""
Local cCfo  	:= GetMv("MV_CFOFAT")
Local cMesA 	:= StrZero(Year(dAtualiz),4)+StrZero(Month(dAtualiz),2)
Local cMA560Con := ''

cCondicao   := 'D2_FILIAL == "' + xFilial("SD2") + '" .And. '
cCondicao   += 'DTOS(D2_EMISSAO) >= "'+cMesA+'00" .And. DTOS(D2_EMISSAO) <= "'+cMesA+'99" .and. '
cCondicao   += 'D2_CF $ "'+cCfo+'"'

//Verifica se Considera ou Nao as Notas de Devolucao de Compras
If mv_par01 == 2
	cCondicao +='.And.D2_TIPO<>"D"'
EndIf
If ExistBlock('MA560Con')
	cMA560Con := ExecBlock("MA560Con",,,{cCondicao})
	cCondicao := cCondicao + " .And. " + cMA560Con
EndIf

Return cCondicao