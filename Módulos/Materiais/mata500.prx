#INCLUDE "MATA500.CH"
#INCLUDE "PROTHEUS.CH"

Static lFATTRVSA1   := ExistBlock("FATTRVSA1")

/*/


Ŀ
Funo     Mata500   Autor  Eduardo Riera          Data  14.10.99 
Ĵ
Descrio  Programa de Eliminacao de Residuos                         
Ĵ
Sintaxe e  Void Mata500(void)                                         
Ĵ
 Uso       Generico                                                   
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   
Ĵ
                                                                    
ٱ


/*/
FUNCTION Mata500
//Ŀ
// Define Variaveis                                         
//
LOCAL aIndexSC6	:= {}
LOCAL cQuery	:= ""
LOCAL cFilMarkb := ""
Local lListaPre	:= SuperGetMV("MV_LJLSPRE",,.F.) .AND. SC6->( FieldPos("C6_CODLPRE") ) > 0	//Define que foi aplicada a melhoria da Lista de Presentes
Local cLjPvLib	:= SuperGetMV("MV_LJPVLIB",,"1")
Local cC6SpacRes:= Space(Len(SC6->C6_RESERVA)) 
Local lTopConn	:= .F.

PRIVATE bFiltraSC6
PRIVATE cCadastro := OemToAnsi(STR0001)	//"Eliminao de Resduos"

//Ŀ
// Define Array contendo as Rotinas a executar do programa  
// ----------- Elementos contidos por dimensao ------------ 
// 1. Nome a aparecer no cabecalho                          
// 2. Nome da Rotina associada                              
// 3. Usado pela rotina                                     
// 4. Tipo de Transao a ser efetuada                      
//    1 - Pesquisa e Posiciona em um Banco de Dados         
//    2 - Simplesmente Mostra os Campos                     
//    3 - Inclui registros no Bancos de Dados               
//    4 - Altera o registro corrente                        
//    5 - Remove o registro corrente do Banco de Dados      
//    6 - Altera determinados campos sem incluir novos Regs 
//
PRIVATE aRotina := {	{ STR0002 ,"MA500Pesq" , 0 , 0},;		// "Pesquisar"
	{ STR0003 ,"MA500Resid", 0 , 0}}	// "Processa"
//Ŀ
// Define parametros abertos                                
//                                                          
//MV_PAR01: Percentual Maximo                               
//MV_PAR02: Emissao de ?                                    
//MV_PAR03: Emissao ate?                                    
//MV_PAR04: Pedido de  ?                                    
//MV_PAR05: Pedido Ate ?                                    
//MV_PAR06: Cons.Totalmente Faturados? Sim / Nao             //Sem Funcao -> Eduardo
//MV_PAR07: Produto de ?                                    
//MV_PAR08: Produto Ate?                                    
//MV_PAR09: Cons. PV com Bloqueidos  ? Sim / Nao            
//

If ( Pergunte("MTA500",.T.) )

	lTopConn := .T.

	//Ŀ
	//Realiza a Filtragem                                                     
	//
	cQuery		:= "C6_FILIAL='"+xFilial("SC6")+"' AND "
	cQuery		+= "C6_NUM>='"+MV_PAR04+"' AND "
	cQuery		+= "C6_NUM<='"+MV_PAR05+"' AND "
	cQuery		+= "C6_PRODUTO>='"+MV_PAR07+"' AND "
	cQuery		+= "C6_PRODUTO<='"+MV_PAR08+"' AND "
	cQuery		+= "C6_ENTREG BETWEEN '"+Dtos(MV_PAR10)+"' AND '"+Dtos(MV_PAR11)+"' AND "
	cQuery		+= "C6_BLQ<>'R ' AND C6_BLQ<>'S ' AND "
	cQuery		+= "(C6_QTDVEN-C6_QTDENT)>0 AND "
	If cLjPvLib = "2"
		cQuery 		+= "(C6_RESERVA='"+Space(Len(SC6->C6_RESERVA))+"' OR EXISTS(SELECT C5_ORCRES FROM " + RetSQLName("SC5") ;
					+ " WHERE C5_FILIAL=C6_FILIAL AND C5_NUM=C6_NUM AND C5_ORCRES<>'"+Space(Len(SC5->C5_ORCRES))+"' AND D_E_L_E_T_ = ' '"+")  )"
	Else
		cQuery 		+= "C6_RESERVA='"+Space(Len(SC6->C6_RESERVA))+"'"
	EndIf

	//A rotina de eliminacao de residuo nao pode tratar pedidos de Lista de Presentes, sendo necessario utilizar a rotina LOJA854
	If lListaPre .And. lTopConn
		cQuery 		+= " AND C6_CODLPRE='"+Space(Len(SC6->C6_CODLPRE))+"'"
	EndIf
	cFilMarkb	:= "C6_FILIAL=='"+xFilial("SC6")+"'.And. "
	cFilMarkb	+= "C6_NUM >= '"+MV_PAR04+"'.And."
	cFilMarkb	+= "C6_NUM <= '"+MV_PAR05+"'.And."
	cFilMarkb	+= "C6_PRODUTO >= '"+MV_PAR07+"'.And."
	cFilMarkb	+= "C6_PRODUTO <= '"+MV_PAR08+"'.And."
	cFilMarkb	+= "Dtos(C6_ENTREG)  >= '"+Dtos(MV_PAR10)+"' .And. "
	cFilMarkb	+= "Dtos(C6_ENTREG)  <= '"+Dtos(MV_PAR11)+"' .And. "
	cFilMarkb	+= "C6_BLQ<>'R '.And.C6_BLQ<>'S '.And."
	cFilMarkb	+= "(C6_QTDVEN-C6_QTDENT)>0 .AND. "
	cFilMarkb	+= "C6_RESERVA=='"+cC6SpacRes+"'"

	//A rotina de eliminacao de residuo nao pode tratar pedidos de Lista de Presentes, sendo necessario utilizar a rotina LOJA854
	If lListaPre .And. !lTopConn
		cFilMarkb	+= " .And. C6_CODLPRE=='"+Space(Len(SC6->C6_CODLPRE))+"'"
	EndIf
	If !lTopConn .And. ExistBlock("MTA500FTR")
		cFilMarkb += ".And."+ExecBlock("MTA500FTR",.F.,.F.,)
	EndIf

	If lTopConn .And. ExistBlock("MTA500QRY")
		cQuery    += " AND "+ExecBlock("MTA500QRY",.F.,.F.,)
	EndIf

	bFiltraSC6 := {|x| If(x==Nil,FilBrowse("SC6",@aIndexSC6,@cFilMarkb),If(x==1,cFilMarkb,cQuery)) }
	If !lTopConn
		Eval(bFiltraSC6)
	EndIf

	//Ŀ
	// Endereca a funcao de BROWSE                              
	//
	MarkBrow("SC6","C6_OK",,,,GetMark(,"SC6","C6_OK"),,,,,,,IIf(lTopConn,cQuery,Nil))
	//Ŀ
	// Retorna indices do SC6                                       
	//
	DbSelectArea("SC6")
	RetIndex("SC6")
	dbClearFilter()
	aEval(aIndexSC6,{|x| Ferase(x[1]+OrdBagExt())})
EndIf
Return(.T.)

/*/


Ŀ
Funcao    Ma500Pesq  Autor Eduardo Riera           Data 14.10.1999
Ĵ
Descrio Tratamento do Filtro na Pesquisa da MarkBrowse              
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ma500Pesq()

AxPesqui()

Eval(bFiltraSC6)

Return(.T.)

/*/


Ŀ
Funcao    Ma500Resid Autor Eduardo Riera           Data 14.10.1999
Ĵ
Descrio Processamento da Rotina de Eliminacao de Residuos.          
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ma500Resid()
Local nOpcA := 0

If !ctbValiDt( Nil, dDataBase, .T., Nil, Nil, { "FAT001" }, Nil )
	Return .F.
EndIf

If ExistFunc("LjMa500Chk") .AND. !LjMa500Chk()		//Funo do Loja - Rotina para validar ao cliente se Loja e com C6_RESERVA preenchido
	Return .F.
EndIf

FormBatch(OemToAnsi(STR0001),;		//"Eliminao de Resduos"
	{OemToAnsi(STR0004),;		//"  Este programa  tem  como  objetivo  eliminar automaticamente os resduos    "
	OemToAnsi(STR0005)},;		//"  de pedidos de venda, baseado em informaes da opes de parametros.        "
	{	{5,.F.,{|o| o:oWnd:End()}           },;
	{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}  },;
	{2,.T.,{|o| o:oWnd:End() }}         })

If ( nOpcA == 1 )
	Processa({|| Ma500Proc()},STR0001) //"Eliminao de Resduos"
EndIf
Return(.T.)

/*/


Ŀ
Funcao    Ma500Proc  Autor Eduardo Riera           Data 14.10.1999
Ĵ
Descrio Processamento da Rotina de Eliminacao de Residuos.          
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ma500Proc()

Local aArea		   := GetArea()
Local cAlias	   := "SC6"
Local lMT500NCC    := ExistBlock("MT500NCC")
Local lMT500GRNCC  := ExistBlock("MT500GRNCC")
Local nResid	   := 0
Local nVlrDep	   := 0
Local nVlrNCC	   := 0
Local cFiltro	   := ""
Local nRegSC6	   := 0
Local nRegSC5	   := 0
Local aRegSC6      := {}
Local lRetorno	   := .T.
Local lContinua	   := .T.
Local cMsg         := ""
Local cMsgErro	   := ""
Local nX           := 0             
Local lGeraNCC     := .T.
Local lIntTMK	   := .T.
Local lMvNCCResi   := SuperGetMv("MV_NCCRESI",,.F.)		//indica se gera NCC com o valor do resduo eliminado
Local lPedLoja     := .F.								//indica se o pedido foi originado no SIGALOJA 
Local lUseOffBal   := FindFunction( "RskIsActive" ) .And. RskIsActive()
Local lRskClrPB	   := FindFunction( "RskClrPedBalance" )
Local cQuery	   := ""
Local cWhere	   := Eval(bFiltraSC6,2)
Local cPedBalance  := ""
Local cLjPvLib	   := SuperGetMV("MV_LJPVLIB",,"1")

If ExistBlock("MTA500PROC")
	ExecBlock("MTA500PROC",.F.,.F.)
Endif

If ExistBlock("MTA500FIL")
	cFiltro := ExecBlock("MTA500FIL",.F.,.F.)
EndIf

cQuery := "SELECT C6_FILIAL,C6_NUM,C6_ITEM,SC6.R_E_C_N_O_ REGSC6,SC5.R_E_C_N_O_ REGSC5 "
cQuery += "FROM "+RetSqlName("SC6")+" SC6, "+RetSqlName("SC5")+" SC5 "
cQuery += "WHERE "
cQuery += (cWhere + " AND ")
If ( ThisInv() )
	cQuery += "SC6.C6_OK<>'"+ThisMark()+"' AND "
Else
	cQuery += "SC6.C6_OK='"+ThisMark()+"' AND "
EndIf
cQuery += "SC6.D_E_L_E_T_ = ' ' AND "
cQuery += "SC5.C5_FILIAL='"+xFilial("SC5")+"' AND "
cQuery += "SC5.C5_NUM=SC6.C6_NUM AND "
cQuery += "SC5.C5_EMISSAO>='"+DTOS(MV_PAR02)+"' AND "
cQuery += "SC5.C5_EMISSAO<='"+DTOS(MV_PAR03)+"' AND "
cQuery += "SC5.D_E_L_E_T_ = ' ' AND "
cQuery += "SC5.C5_ORIGEM  = ' ' AND "//Pedidos de integracao devem ser feito individualmente.
cQuery += "((SC6.C6_QTDVEN=0 AND SC5.C5_NOTA<>'"+Space(Len(SC5->C5_NOTA))+"') OR "
cQuery += "(100-((SC6.C6_QTDENT+SC6.C6_QTDEMP)/SC6.C6_QTDVEN*100)<="+Str(MV_PAR01,6,2)+")) "
cQuery += If(! Empty(cFiltro)," AND ("+cFiltro+") ", "")
cQuery += "ORDER BY C6_FILIAL,C6_NUM,C6_ITEM"

cAlias := GetNextAlias()
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)

ProcRegua(SC6->(LastRec()))

dbSelectArea(cAlias)

While ( !Eof() .And. xFilial("SC6")==C6_FILIAL )

	nRegSC5 := REGSC5
	nRegSC6 := REGSC6
	SC6->(MsGoto(nREGSC6))
	SC5->(MsGoto(nREGSC5))

	//indica que  um pedido originado pelo SIGALOJA
	lPedLoja := !Empty(SC5->C5_ORCRES)

	If lContinua
		If ( SC6->(IsMark("C6_OK",ThisMark(),ThisInv())) )
			If ( SC5->C5_EMISSAO>= MV_PAR02 .And. SC5->C5_EMISSAO<= MV_PAR03 )
				If ( (SC6->C6_QTDEMP == 0 .Or. MV_PAR09 == 1) .And. ;
						(Empty(SC6->C6_RESERVA) .Or.;
						 (cLjPvLib = "2" .And. !Empty(SC6->C6_RESERVA) .And. !Empty(SC5->C5_ORCRES))) .And.;
						 !SC6->C6_BLQ$"R #S " )
								
					If ( SC6->C6_QTDVEN > 0 )
						nResid := 100 - ( (SC6->C6_QTDENT+SC6->C6_QTDEMP) / SC6->C6_QTDVEN * 100 )
						nResid := NoRound(nResid,2)
					Else
						nResid := If(!Empty(SC5->C5_NOTA),0,100)
					EndIf
					If ( nResid <= MV_PAR01  )
						//Ŀ
						// Este ponto de entrada permite validar dados especificos
						// do usuario antes da eliminacao do residuo.             
						//
						If ExistBlock('MT500ANT')
							lRetorno := ExecBlock('MT500ANT', .F., .F.)
						Endif

						If lRetorno .And. !Empty(SC5->C5_PEDEXP) .And. SuperGetMv("MV_EECFAT") // Integracao SIGAEEC
							If FindFunction("EECZeraSaldo")
								lRetorno := EECZeraSaldo(,SC5->C5_PEDEXP,,.T.,SC5->C5_NUM)
							Else
								lRetorno := EECCancelPed(,SC5->C5_PEDEXP,,.T.,SC5->C5_NUM)
							EndIf
						EndIf
						If lRetorno
							aAdd(aRegSC6, SC6->(RecNo()))
						EndIf
					EndIf
				ElseIf lPedLoja
					//se for pedido originado pelo SIGALOJA, exibe a mensagem que nao conseguiu eliminar resduo de algum item
					cMsg := STR0014 + SC6->C6_ITEM + STR0015 + SC6->C6_NUM	//#"Operao no realizada para o item " #"do pedido"
					cMsg += CRLF + "Os itens selecionados sero restaurados."
					lRetorno := .F.
				EndIf
			EndIf
		EndIf
	EndIf
	dbSelectArea(cAlias)
	IncProc(OemToAnsi(STR0006)+": "+C6_NUM+"/"+C6_ITEM)
	
	//Ŀ
	//Enquanto for verdadeiro avalia ate o ultimo item do pedido
	//
	lIntTMK := IIF(lIntTMK,!Empty(SC6->C6_PEDCLI) .And. "TMK" $ upper(SC6->C6_PEDCLI),lIntTMK)

	dbSkip()
	
	If ( SC5->C5_NUM != C6_NUM )
		Begin Transaction
			// Elimina residuo de itens e atualiza cabecalho do pedido.
			nVlrNCC := 0
			
			For nX := 1 to len(aRegSC6)
				If lRetorno
					SC6->(dbGoTo(aRegSC6[nX]))				 

					//Se loja, e se reserva ativa, dever eliminar a reserva primeiro
					//antes de processar o MaResDoFat()
					If cLjPvLib = "2" .AND. !Empty(SC6->C6_RESERVA) .AND. !Empty(SC5->C5_ORCRES) .AND. ExistFunc("LjMa500CRs")
						lRetorno := LjMa500CRS(@cMsgErro)
						If !lRetorno
							cMsg := STR0014 + SC6->C6_ITEM + STR0015 + SC6->C6_NUM	//#"Operao no realizada para o item " #"do pedido"
							cMsg += CRLF + "Funo LjMa500Crs :" + "Os itens selecionados sero restaurados."
							cMsg += CRLF + cMsgErro
							DisarmTransaction()
						EndIf
					EndIf

					If lRetorno
						If lPedLoja
							nResid := 100 - ( (SC6->C6_QTDENT+SC6->C6_QTDEMP) / SC6->C6_QTDVEN * 100 )
							nResid := NoRound(nResid,2)
						EndIf
						If !MaResDoFat(nil, MV_PAR09==1, .F., @nVlrDep, MV_PAR12 == 1, MV_PAR13 == 1)
							//se for pedido originado pelo SIGALOJA, exibe a mensagem que nao conseguiu eliminar resduo de algum item
							If lPedLoja						
								cMsg := STR0014 + SC6->C6_ITEM + STR0015 + SC6->C6_NUM	//#"Operao no realizada para o item " #"do pedido"
								cMsg += CRLF + "Os itens selecionados sero restaurados."
								lRetorno := .F.
								Exit
							EndIf
						EndIf

						nVlrNCC += nVlrDep

						If lPedLoja  // Pedido Originado no Sigaloja
							// Retorna o valor de IPI do item ja entregues para somar no valor da NCC (lojxfunf.prw)
							If FindFunction("LjIpiPedEnt")
								nVlrNCC += LjIpiPedEnt(SC5->C5_ORCRES, SC6->C6_ITEM, SC6->C6_QTDEMP, SC6->C6_QTDENT, SC6->C6_QTDVEN)  
							Endif
						Endif
					EndIf
				EndIf
			Next nX

			MaLiberOk({ SC5->C5_NUM }, .T.)
			                         
			// Gera NCC   
			If lRetorno .AND. lMvNCCResi .AND. !Empty(SC5->C5_ORCRES)
							
				// Verifica a necessidade da geracao de uma NCC para o cliente.			
				If lMT500GRNCC
	               lGeraNCC := ExecBlock('MT500GRNCC', .F., .F.)
				Endif    
				
				lRetorno := .T.
				If nVlrNCC > 0 .and. lGeraNCC
					lRetorno := Ma500NCC(SC5->C5_NUM, nVlrNCC, @cMsg)
					
					// Ponto de entrada apos a geracao da NCC.
					If lMT500NCC .and. lRetorno
						lRetorno := ExecBlock('MT500NCC', .F., .F.)
						Default lRetorno := .F.
						If !lRetorno
							ConOut("- Erro no ponto de entrada MT500NCC.")
						Endif
					Endif
				Endif
			Endif

			If lIntTMK
				ValAtTMK(SC5->C5_NUM)
			EndIf			

			/* Integrao RISK - TOTVS Mais Negcios
			 Se a integrao com o TOTVS MAis Negcios estiver habilitada,
			 tenta limpar o saldo do ticket de crdito. */
			If lRetorno .And. lUseOffBal .And. lRskClrPB
				cPedBalance := RskClrPedBalance( SC5->C5_NUM )
				If cPedBalance == "2"
					lRetorno := .F.  
				EndIf
			EndIf

			// Se retornou falso, desfaz toda a transacao.
			If !lRetorno
				DisarmTransaction()
			Endif
			
			// Zera a variaveis para o proximo looping.
			aRegSC6 := {}
		End Transaction
		
		If !lRetorno .and. !empty(cMsg)
			MsgAlert(cMsg, "Ateno")
		Endif

		lIntTMK := .T. //Ao trocar o pedido reinicializa a variavel
	EndIf
EndDo

dbSelectArea(cAlias)
dbCloseArea()
dbSelectArea("SC9")

RestArea(aArea)
Return(.T.)

/*/


Ŀ
Funcao    MaResDoFat Autor Eduardo Riera           Data 15.10.1999
Ĵ
Descrio Funcao de Eliminacao de Residuo por item de Pedido          
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosExpN1: Numero do Registro do SC6.                           
          ExpL2: Estorna Itens Bloqueados.                            
          ExpL3: Avalia o Cabecalho do Pedido.                        
          ExpN4: Parametro por referencia, que contera o valor depu-  
                 rado do item do pedido.                              
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function MaResDoFat(nRegSc6, lBloqueado, lPV, nVlrRes, lContabili, lMostraCtb)

Local aArea		:= GetArea()
Local aAreaSC6  := SC6->(GetArea())
Local aAreaSC5  := SC5->(GetArea())
Local aAreaSB2  := SB2->(GetArea())
Local aAreaSA1  := SA1->(GetArea())
Local cQry		:= ""

Local nQtdLibSC9:= 0
Local lResiduo  := .F.
Local nMCusto   := 0
Local nRecnoB2  := 0
Local nVlrDep   := 0  
Local cPadrao  	:= "614"
Local cVDBAcc	:= TCGetBuild() 	// Traz informaes do DBAccess 
Local lPadrao   := VerPadrao(cPadrao)
Local cQuery	:= ""
Local lTrvSA1   := .T.

Default nVlrRes		:= 0
Default lContabili	:= .F.
Default lMostraCtb	:= .F.
 
lPv			:= If(lPv==Nil,.T.,lPv)
lBloqueado	:= If(lBloqueado==Nil,.T.,lBloqueado)
nVlrRes     := 0

dbSelectArea("SC6")
If ( nRegSc6 != Nil )
	MsGoto(nRegSC6)
EndIf

dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial("SF4")+SC6->C6_TES)

dbSelectArea("SC5")
dbSetOrder(1)
MsSeek(xFilial("SC5")+SC6->C6_NUM)

If ( Empty(SC6->C6_RESERVA) .And. !SC6->C6_BLQ$'R #S ' .And. ( SC6->C6_QTDEMP==0 .Or. lBloqueados ) ) .And.;
		( SF4->F4_PODER3 == "N" .Or. SF4->F4_PODER3 == "R" )

	If ( lBloqueados )
	
		cQuery := "SELECT SUM(SC9.C9_QTDLIB) C9_QTDLIB FROM "
		cQuery += RetSqlName("SC9")+" SC9 "
		cQuery += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
		cQuery += "SC9.C9_PEDIDO='"+SC6->C6_NUM+"' AND "
		cQuery += "SC9.C9_ITEM='"+SC6->C6_ITEM+"' AND "
		cQuery += "SC9.C9_PRODUTO='"+SC6->C6_PRODUTO+"' AND "
		cQuery += "SC9.C9_BLCRED<>'10' AND "
		cQuery += "SC9.C9_BLEST<>'10' AND "
		cQuery += "(SC9.C9_BLEST<>'  ' OR SC9.C9_BLCRED<>'  ') AND "
		cQuery += "SC9.D_E_L_E_T_=' '"

		cQuery := ChangeQuery(cQuery)
		cQry := GetNextAlias()
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cQry,.T.,.T.)
		TcSetField(cQry,"C9_QTDLIB","N",TamSX3("C9_QTDLIB")[1],TamSX3("C9_QTDLIB")[2])
		nQtdLibSC9 := C9_QTDLIB
		dbCloseArea()
		dbSelectArea("SC5")

		If ( nQtdLibSC9 == SC6->C6_QTDEMP .or. nQtdLibSC9 == SC6->C6_QTDLIB )

			cQuery := "SELECT C9_FILIAL,C9_PEDIDO,C9_PRODUTO,C9_ITEM,C9_BLCRED,C9_BLEST,SC9.R_E_C_N_O_ RECNOSC9 FROM "
			cQuery += RetSqlName("SC9")+" SC9 "
			cQuery += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
			cQuery += 		"SC9.C9_PEDIDO='"+SC6->C6_NUM+"' AND "
			cQuery += 		"SC9.C9_ITEM='"+SC6->C6_ITEM+"' AND "
			cQuery += 		"SC9.C9_PRODUTO='"+SC6->C6_PRODUTO+"' AND "
			cQuery += "SC9.C9_BLCRED<>'10' AND "
			cQuery += "SC9.C9_BLEST<>'10' AND "
			cQuery += "(SC9.C9_BLEST<>'  ' OR SC9.C9_BLCRED<>'  ') AND "
			cQuery +=		"SC9.D_E_L_E_T_=' '"
			
			cQuery := ChangeQuery(cQuery)
			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cQry,.T.,.T.)
			dbSelectArea(cQry)

			While ( !Eof() .And.C9_FILIAL == xFilial("SC9") .And.;
					C9_PEDIDO == SC6->C6_NUM .And.;
					C9_ITEM == SC6->C6_ITEM)
				If ( C9_BLCRED != '10' .And. C9_BLEST != '10' .And.; // (C9_BLCRED != '  ' .Or. C9_BLEST != '  ') .And.;
						C9_PRODUTO == SC6->C6_PRODUTO)
					If ( cQry!="SC9" )
						SC9->(MsGoto((cQry)->RECNOSC9))
					EndIf
					SC9->(A460Estorna())
				EndIf
				dbSelectArea(cQry)
				dbSkip()
			EndDo
			If ( cQry != "SC9" )
				dbSelectArea(cQry)
				dbCloseArea()
				dbSelectArea("SC9")
			EndIf
		EndIf
	EndIf
	If ( SC6->C6_QTDEMP == 0 )
		If ( SF4->F4_ESTOQUE=="S" )
			dbSelectArea("SB2")
			dbSetOrder(1)
			MsSeek(xFilial("SB2")+SC6->C6_PRODUTO+SC6->C6_LOCAL)
			RecLock("SB2")
			SB2->B2_QPEDVEN -= Max(SC6->C6_QTDVEN-SC6->C6_QTDEMP-SC6->C6_QTDENT,0)
			SB2->B2_QPEDVE2 -= ConvUM(SB2->B2_COD, Max(SC6->C6_QTDVEN-SC6->C6_QTDEMP-SC6->C6_QTDENT,0), 0, 2)
			MsUnLock()
			If ( SC6->C6_OP$"01#03#05#06#08" )
				nRecnoB2 := SB2->(Recno())
				If !Empty(SC6->(C6_NUMOP+C6_ITEMOP))
					SC2->(dbSetOrder(1))
					SC2->(dbSeek(xFilial("SC2")+SC6->(C6_NUMOP+C6_ITEMOP)))
					If SB2->B2_LOCAL # SC2->C2_LOCAL .And. !SB2->(dbSeek(xFilial("SB2")+SC2->(C2_PRODUTO+C2_LOCAL)))
						CriaSB2(SC2->C2_PRODUTO,SC2->C2_LOCAL)
					EndIf
					A650AtEmpN(SC6->(C6_QTDVEN-C6_QTDENT-C6_QTDRESE) - If(FindFunction("A650QLibPV"),A650QLibPV(),0),"-",SC2->C2_TPOP)
				ElseIf SC6->(FieldPos("C6_NUMSC")) > 0 .And. !Empty(SC6->(C6_NUMSC+C6_ITEMSC))
					SC1->(dbSetOrder(1))
					SC1->(dbSeek(xFilial("SC1")+SC6->(C6_NUMSC+C6_ITEMSC)))
					If SB2->B2_LOCAL # SC1->C1_LOCAL .And. !SB2->(dbSeek(xFilial("SB2")+SC1->(C1_PRODUTO+C1_LOCAL)))
						CriaSB2(SC1->C1_PRODUTO,SC1->C1_LOCAL)
					EndIf
					A650AtEmpN(SC6->(C6_QTDVEN-C6_QTDENT-C6_QTDRESE) - If(FindFunction("A650QLibPV"),A650QLibPV(),0),"-",SC1->C1_TPOP)
				Else
					SB2->(dbSeek(xFilial("SB2")+SC6->(C6_PRODUTO+C6_LOCAL)))
					A650AtEmpN(SC6->(C6_QTDVEN-C6_QTDENT-C6_QTDRESE) - If(FindFunction("A650QLibPV"),A650QLibPV(),0),"-",SC6->C6_TPOP)				
				EndIf
			Endif
			SB2->(dbGoTo(nRecnoB2))
		EndIf
		nVlrDep := Max(SC6->C6_QTDVEN-SC6->C6_QTDEMP-SC6->C6_QTDENT,0)*SC6->C6_PRCVEN
		If ( SF4->F4_DUPLIC=="S" .And. !SC5->C5_TIPO$'DB' )
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+SC6->C6_CLI+SC6->C6_LOJA)
			lTrvSA1 := IIf(cPaisLoc == "BRA" .And. lFATTRVSA1, ExecBlock("FATTRVSA1", .F., .F., {xFilial("SA1"), SA1->A1_COD, SA1->A1_LOJA}), .T.)
			If lTrvSA1
				RecLock("SA1")
				nMCusto := If(SA1->A1_MOEDALC > 0, SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
				SA1->A1_SALPED -= xMoeda(nVlrDep, SC5->C5_MOEDA, nMCusto, SC5->C5_EMISSAO)
				SA1->(MsUnLock())
			EndIf
		EndIf
		
		//Ŀ
		//Se eliminar o resduo do item, elimina tambm a OS gerada pelo mesmo.
		//Somente se no forem vendidos todos os itens.                        
		//
		If SC6->C6_QTDENT == 0
			If FindFunction("Lj7HasDtEM") .And. Lj7HasDtEM()
				Lj7EliProdPed( SC6->C6_NUM, SC6->C6_PRODUTO )
			EndIf
		EndIf
		
		// Acumula valor para gerar NCC.
		nVlrRes += nVlrDep
		
		RecLock("SC6")
		SC6->C6_BLQ := "R"
		MsUnLock()
		lResiduo := .T.
	EndIf
	If ( lPv )
		MaLiberOk({ SC5->C5_NUM } , .T. )
	EndIf
	If ( lPadrao .And. lContabili .And. cVDBAcc >= "20170202")
		MA500ElRes(lMostraCtb)
	EndIf	
	//Ŀ
	// Ponto de entrada do usuario apos a eliminacao do residuo.
	//
	
	If ExistBlock('MT500APO')
		ExecBlock('MT500APO',.F.,.F.,{lResiduo})
	EndIf
EndIf
RestArea(aAreaSC6)
RestArea(aAreaSC5)
RestArea(aAreaSB2)
RestArea(aAreaSA1)
RestArea(aArea)
Return(lResiduo)

/*/


Ŀ
Funcao    Ma500NCC   Autor  Felipe Raposo          Data 23.06.2008
Ĵ
Descrio Gera NCC para pedidos gerados a partir do SigaLoja.         
Ĵ
Retorno   Logico, indicando se a rotina foi executada com sucesso.    
Ĵ
ParametrosExpC1: Numero do pedido de venda                            
          ExpN2: Valor de NCC a ser gerado para esse pedido.          
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Static Function Ma500NCC(cPedido, nVlrNCC, cMsg)

//Ŀ
// Declaracao de variaveis.                                            
//
Local lRetorno := .T.
Local aRegAuto[0]
Local nOpc := 0
Local aAreas[0]          
Local cParcela := "A"
Private lMsHelpAuto := .F.
Private lMsErroAuto := .F.

// Armazena as condicoes das tabelas.
aAdd(aAreas, SL1->(GetArea()))
aAdd(aAreas, SC5->(GetArea()))
aAdd(aAreas, SE1->(GetArea()))
aAdd(aAreas, GetArea())

SE1->(dbSetOrder(1))  // E1_FILIAL + E1_PREFIXO + E1_NUM + E1_PARCELA + E1_TIPO.

// Posiciona o pedido de venda.
SC5->(dbSetOrder(1))  // C5_FILIAL + C5_NUM.
If SC5->(dbSeek(xFilial() + cPedido, .F.))    
    
    If SE1->(DbSeek(xFilial("SE1") +;
             PadR("LOJ",TamSX3("E1_PREFIXO")[1] ) +;
             PadR(cPedido, TamSX3("E1_NUM")[1] ) +;
             PadR(cParcela, TamSX3("E1_PARCELA")[1] ) +;
             PadR("NCC", TamSX3("E1_TIPO")[1] )))                          
                                           
             While !SE1->(Eof()) .and.;
                    SE1->E1_FILIAL  == xFilial("SE1") .and.;
                    SE1->E1_PREFIXO == PadR("LOJ",TamSX3("E1_PREFIXO")[1] ) .and.;
                    SE1->E1_NUM     == PadR(cPedido, TamSX3("E1_NUM")[1] ) .and.;
                    SE1->E1_TIPO    == PadR("NCC", TamSX3("E1_TIPO")[1] )
                    
                cParcela := Soma1(SE1->E1_PARCELA)
                
                SE1->(DbSkip())
            End    
    Endif
    
	// Monta o array com as informacoes para a gravacao do titulo
	aAdd(aRegAuto, {"E1_FILIAL",	xFilial("SE1"), nil})
	aAdd(aRegAuto, {"E1_PREFIXO",	"LOJ", nil})
	aAdd(aRegAuto, {"E1_NUM",		cPedido, nil})
	aAdd(aRegAuto, {"E1_PARCELA",	cParcela, nil})
	aAdd(aRegAuto, {"E1_TIPO",		"NCC", nil})
	aAdd(aRegAuto, {"E1_NATUREZ",	&(SuperGetMV("MV_NATNCC")), nil})
	aAdd(aRegAuto, {"E1_CLIENTE",	SC5->C5_CLIENTE, nil})
	aAdd(aRegAuto, {"E1_LOJA",		SC5->C5_LOJACLI, nil})
	aAdd(aRegAuto, {"E1_NOMCLI" ,	Posicione("SA1", 1, xFilial("SA1") + SC5->(C5_CLIENTE + C5_LOJACLI), "A1_NOME"), nil})
	aAdd(aRegAuto, {"E1_EMISSAO",	dDataBase, nil})
	aAdd(aRegAuto, {"E1_VENCTO",	dDataBase, nil})
	aAdd(aRegAuto, {"E1_VENCREA",	DataValida(dDataBase), nil})
	aAdd(aRegAuto, {"E1_VALOR",		nVlrNCC, nil})
	aAdd(aRegAuto, {"E1_HIST",		"CANC. DE ORCAM. " + SC5->C5_ORCRES, nil})
	aAdd(aRegAuto, {"E1_ORIGEM",	"FINA040", nil})
	aAdd(aRegAuto, {"E1_VEND1",		SC5->C5_VEND1, nil})
	
	If ExistBlock('MA550NCI')
		aRegAuto := ExecBlock("MA550NCI",,,{aRegAuto})
		nVlrNCC := aRegAuto[Ascan(aRegAuto,{|x| x[1] == 'E1_VALOR'})][2]
    EndIf

	nOpc := 3 // 3-Incluir.
	msExecAuto({|x, y| FINA040(x, y)}, aRegAuto, nOpc)

	If lMsErroAuto
		MostraErro()
	EndIf

Else
	lMsErroAuto := .T.
Endif

// Verifica se houve erro.
If lMsErroAuto
	lRetorno := .F.
	cMsg := "Erro na geracao da NCC! Valor: " + AllTrim(str(nVlrNCC))
	ConOut(cMsg)      
	MsgStop(cMsg)
Else
	If ExistFunc("LJMA500NCC")
		LjMa500NCC(aRegAuto,cPedido,cParcela,nVlrNCC)
	EndIf
	cMsg := "NCC gerada! Ttulo: LOJ " + cPedido + " " + cParcela + " - Valor: " + AllTrim(str(nVlrNCC))
	ConOut(cMsg)
	MsgInfo(cMsg)
Endif

// Restaura as condicoes anteriores das tabelas.
aEval(aAreas, {|aArea| RestArea(aArea)})

Return lRetorno    

/*


ͻ
Programa  ValAtTMK  Autor  Vendas CRM           Data   09/22/09   
͹
Desc.     Verifica se todos os itens dos pedido foram eliminados por  
          residuos                                                    
͹
Uso       MATA500                                                     
ͼ


*/  
Static Function ValAtTMK(cPedTMK)

Local aAreaSC6 := SC6->(GetArea())

SC6->(dbSetOrder(1))

If !SC6->(dbSeek(xFilial("SC6")+cPedTMK))
	TkAtuTlv( cPedTMK , 4 )
ElseIf SC5->C5_LIBEROK == "S" .And. "X" $ SC5->C5_NOTA
	//Se o pedido for eliminado por completo e no tiver nenhum item j faturado, ser feito o 
	//cancelamento do atendimento, caso contrario, continuar com o status de NF emitida.
	SUA->(dbSetOrder(8))	//UA_FILIAL+UA_NUMSC5
	If SUA->(MsSeek(xFilial("SUA")+cPedTMK)) 
		TkAtuTlv(cPedTMK,IIf(Empty(SUA->UA_DOC),4,3),,,,.T.)
	EndIf
EndIf

RestArea(aAreaSC6)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} MA500ElRes

Contabiliza a eliminacao de residuo do PV

@param Nil
@return Nil

@author SQUAD CRM
@since 09.08.2017
@version 1.0
/*/
//-------------------------------------------------------------------

Function MA500ElRes(lMostraCtb)

Local aArea     	:= GetArea()
Local cPadrao  		:= "614"  
Local cArquivo 		:= " "                     
Local nHdlPrv   	:= 0
Local nTotal    	:= 0

Private cLote	  	:= ""

LoteCont("FAT")

//Ŀ
//Lancamento Contabil
//
nHdlPrv :=HeadProva(cLote,"MATA500",cUserName,@cArquivo)
nTotal  += DetProva(nHdlPrv,cPadrao,"MATA500",cLote)
RodaProva(nHdlPrv,nTotal)
cA100Incl(cArquivo,nHdlPrv,3,cLote,lMostraCtb,.F.)

RestArea(aArea)
Return Nil
