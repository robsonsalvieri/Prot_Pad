#INCLUDE "Protheus.ch"
#INCLUDE "TECA110.CH"

//Situacoes de contrato
#DEFINE DEF_SCANC "01" //Cancelado
#DEFINE DEF_SELAB "02" //Em Elaboracao
#DEFINE DEF_SEMIT "03" //Emitido
#DEFINE DEF_SAPRO "04" //Em Aprovacao
#DEFINE DEF_SVIGE "05" //Vigente
#DEFINE DEF_SPARA "06" //Paralisado
#DEFINE DEF_SSPAR "07" //Sol Fina.
#DEFINE DEF_SFINA "08" //Finalizado
#DEFINE DEF_SREVS "09" //Revisao
#DEFINE DEF_SREVD "10" //Revisado

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ TECA110  ³ Autor ³ Joao Goncalves de Oliveira ³ Data ³ 10/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Localização Física da Base Instalada                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe	 ³ TECA110()                    				          		   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Retorno  ³ Nenhum                       						           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECA110()

Private cCadastro := OemToAnsi(STR0006) //Localizações Físicas
Private aRotina   := MenuDef()

MBrowse(6,1,22,75,"AGW")

Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ AT110Cad ³ Autor ³ Joao Goncalves de Oliveira ³ Data ³ 10/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Localização Física            				       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AT110Cad(ExpC1,ExpN2,ExpN3)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                        ³±±
±±³          ³ ExpN2 = Numero do registro                                      ³±±
±±³          ³ ExpN3 = Numero da opcao selecionada                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA110	                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT110Cad(cAliaEnch,nReg,nOpc)
Return AT110Man(cAliaEnch,nReg,nOpc)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³ AT110Man ³ Autor ³ Joao Goncalves de Oliveira ³ Data ³ 10/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Cadastro de Localização Física            				       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AT110Man(ExpN1,ExpA2)	                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                        ³±±
±±³          ³ ExpN2 = Numero do registro                                      ³±±
±±³          ³ ExpN3 = Numero da opcao selecionada                             ³±±
±±³          ³ ExpA4 = Vetor com dados do contrato para registro               ³±± 
±±³          ³ ExpL5 = Determina se registra os dados no arquivo               ³±±
±±³          ³ ExpA6 = aHeader da CNB em edição								   ³±± 
±±³          ³ ExpA7 = aCols da CNB em edição								   ³±± 
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA110	                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT110Man(cAliaEnch,nReg,nOpc,aDadosCtr,cPlanil,aHeadCNB,aColsCNB,oModCNB)
Local lOK       := .F.
Local aAlter    := {}
Local aParmInc  := {{|| aEval(aDadosCtr,{|x| M->&(x[1]) := x[2]})},{|| .T.},{|| .T.},{|| .T.}}
Local aDadosPE  := {}
Local lAT110AGW := ExistBlock("AT110AGW")
Local lGCT		:= IsInCAllStack("TECLOCFIS")
Local aRotBKP := aRotina

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Default aDadosCtr := {}
Default aHeadCNB := {}
Default aColsCNB := {}
Default oModCNB := Nil

If lGCT
	cCadastro := OemToAnsi(STR0006)
EndIf

//-- Ponto de entrada para manipulacao dos dados da localizacao fisica
If ExistBlock("AT110DLF")
	aDadosPE := ExecBlock("AT110DLF",.F.,.F.,{aDadosCtr,aHeadCNB,aColsCNB})
	If ValType(aDadosPE) == "A"
		aDadosCtr := aClone(aDadosPE)
	EndIf
EndIf

//-- Tratamento para bloquear campos oriundos do GCT
If !Empty(aDadosCtr)	
	aAlter := {}
	aStruField := FWSX3Util():GetAllFields("AGW")
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField)
			cField := AllTrim(aStruField[nC])
			If X3Uso(GetSX3Cache(cField,"X3_USADO")) .And. cNivel >= GetSX3Cache(cField,"X3_NIVEL") .And.;
			IF(lGCT, !(cField $ ('AGW_CONTRA|AGW_PLANIL|AGW_ITEM')),.T.) .And. ;
			aScan(aDadosCtr,{|x| x[1] == AllTrim(cField) .And. If(nOpc == 3,Len(x) > 2 .And. !x[3],.F.)}) == 0
				aAdd(aAlter,cField)
			EndIf
		Next nC
	EndIf
EndIf

aRotina := NIl
If nOpc == 3
	If lGCT

		lOk := AxInclui("AGW",nReg,nOpc,,,aAlter,"AT110Tudok()",,,,aParmInc) == 1
	Else
		lOk := AxInclui("AGW",nReg,nOpc,,,,"AT110Tudok()",,,,aParmInc) == 1
	EndIf 
ElseIf nOpc == 4
	//Rotina em MVC modificar o nOpc para alteracao
	If lGCT
		nOpc := 2
		lOk := AxAltera("AGW",nReg,nOpc,,aAlter,,"AT110Tudok()") == 1	 
	Else
		lOk := AxAltera("AGW",nReg,nOpc,,,,"AT110Tudok()") == 1	 
	EndIf
ElseIf nOpc == 5
	lOk := AxDeleta("AGW",nReg,nOpc,,,,{{|| .T.},{|| AT110Tudok()},{|| .T.},{|| .T.}}) == 1
Else
	lOk := AxVisual("AGW",nReg,nOpc)
EndIf

If lAT110AGW .And. lOk
	ExecBlock("AT110AGW",.F.,.F.,{nOpc})
EndIf

aRotina := aRotBKP

Return lOk

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AT110TudokºAutor  ³Microsiga           º Data ³  08/11/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Validacao da interface                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TECA110                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT110Tudok()
Local lRet    := .T.
Local cRevAtu := ""

CN9->(dbSetOrder(1))
CNB->(dbSetOrder(1))

If !INCLUI .And. !ALTERA
	cRevAtu := Posicione("CN9",1,xFilial("CN9")+AGW->AGW_CONTRA,"CN9_REVATU")
	
	//-- Valida se contrato esta em etapa que permite a exclusao
	If CN9->(dbSeek(xfilial("CN9")+AGW->AGW_CONTRA+cRevAtu)) .And.;
							!(CN9->CN9_SITUAC $ DEF_SELAB +"/" +DEF_SCANC +"/" +DEF_SFINA +"/" +DEF_SREVS) .And.;
						CNB->(dbSeek(xFilial("CNB")+CN9->(CN9_NUMERO+CN9_REVISA)+AGW->(AGW_PLANIL+AGW_ITEM))) .And. CNB->CNB_SLDMED > 0
		Aviso(STR0007,STR0008,{"OK"}) //O contrato encontra-se em situação que não permite a exclusão.
		lRet := .F.
	EndIf
	
	//-- Valida se item do contrato tem saldo e ja gerou base instalada
	If lRet .And. CNB->(dbSeek(xFilial("CNB")+AGW->(AGW_CONTRA+cRevAtu+AGW_PLANIL+AGW_ITEM))) .And. CNB->CNB_SLDMED > 0 .And. CNB->CNB_GERBIN == "1"
		Aviso(STR0007,STR0015,{"OK"}) //Este item do contrato já gerou base instalada.
		lRet := .F.
	EndIf
ElseIf INCLUI
	If Empty(M->AGW_CONTRA)
		AA3->(dbSetOrder(1))
		If !AA3->(dbSeek(xFilial("AA3")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT+M->AGW_NUMSER))
			Aviso(STR0007,STR0009,{"OK"}) //Os dados da informados não conferem com as bases instaladas.
			lRet := .F.
		EndIf
	Else
		If Empty(M->AGW_PLANIL) .Or. Empty(M->AGW_ITEM)
			Aviso(STR0007,STR0010,{"OK"}) //Quando localização física de item de contrato, planilha e item deverão ser informados.
			lRet := .F.
		EndIf
		
		AGW->(dbSetOrder(2))
		If lRet .And. AGW->(dbSeek(xFilial("AGW")+M->AGW_CONTRA+M->AGW_PLANIL+M->AGW_ITEM))
			Aviso(STR0007,STR0011,{"OK"}) //Este item do contrato já possui localização física cadastrada.
			lRet := .F.
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a Primary Key Cliente/Loja/Produto/Serie  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRet
		AGW->(dbSetOrder(3))
		If AGW->(dbSeek(xFilial("AGW")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT+M->AGW_NUMSER))
			Help(" ",1,"AT040INC01")
			lRet := .F.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Caso o eqto esteja em outro contrato ativo, nao permite  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			AGW->(dbSetOrder(4))
			If AGW->(dbSeek(xFilial("AGW")+M->AGW_CODFAB+M->AGW_LOJAFA+M->AGW_PRODUT+M->AGW_NUMSER))
				cRevAtu := GetLastRev(AGW->AGW_CONTRA)

				If CN9->(dbSeek(xfilial("CN9")+AGW->AGW_CONTRA+cRevAtu)) .And. !(CN9->CN9_SITUAC $ DEF_SCANC +"/" +DEF_SFINA) .And.;
						CNB->(dbSeek(xFilial("CNB")+CN9->(CN9_NUMERO+CN9_REVISA)+AGW->(AGW_PLANIL+AGW_ITEM))) .And. CNB->CNB_SLDMED > 0
					Help(" ",1,"AT110_01")
					lRet := .F.
				ElseIf MsgYesNo(STR0016,STR0007) //Este produto/eqto estava registrado em outra localização física. Confirma a mudança de local?
					RecLock("AGW",.F.)
					AGW->(dbDelete())
					AGW->(MsUnLock())
				Else
					lRet := .F.
				EndIf
			EndIf
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a Primary Key Fabric./Loja/Produto/Serie  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	AGW->(dbSetOrder(4))
	If lRet .And. Empty(M->AGW_CONTRA) .And. AGW->(dbSeek(xFilial("AGW")+M->AGW_CODFAB+M->AGW_LOJAFA+M->AGW_PRODUT+M->AGW_NUMSER))
		Help(" ",1,"AT040INC02")
		lRet := .F.
	EndIf
EndIf

Return lRet

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao	 ³AT110VlSer³ Autor ³ Joao Goncalves de Oliveira ³ Data ³ 15/02/11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validação no campo número de série       				       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AT110Vld(ExpC1,ExpC2)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Numero de série digitado                                ³±± 
±±³          ³ ExpC2 = Numero de Contrato                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECA110	                                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function AT110VlSer()
Local lRet := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Caso seja inclusao a partir da assistencia tecnica, somente ³
//³permite a inclusao do endereço quando a base instalada ja   ³
//³estiver cadastrada.                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(M->AGW_NUMSER)
	If Empty(M->AGW_CONTRA)
		If Empty(M->AGW_PRODUT) .And. Empty(M->AGW_CLIENT) .And. Empty(M->AGW_LOJA)
			AA3->(dbSetOrder(6))
			If AA3->(dbSeek(xFilial("AA3")+M->AGW_NUMSER))
				M->AGW_CLIENT := AA3->AA3_CODCLI
				M->AGW_LOJA   := AA3->AA3_LOJA
				M->AGW_PRODUT := AA3->AA3_CODPRO
				M->AGW_DESCRI := Posicione("SB1",1,xFilial("SB1")+AA3->AA3_CODPRO,"B1_DESC")
				M->AGW_CODFAB := AA3->AA3_CODFAB
				M->AGW_LOJAFA := AA3->AA3_LOJAFA	
			Else
				Help(" ",1,"REGNOIS")
				lRet := .F.
			EndIf
		Else
			AA3->(dbSetOrder(1))
			If AA3->(dbSeek(xFilial("AA3")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT+M->AGW_NUMSER))
				M->AGW_CLIENT := AA3->AA3_CODCLI
				M->AGW_LOJA   := AA3->AA3_LOJA
				M->AGW_PRODUT := AA3->AA3_CODPRO
				M->AGW_DESCRI := Posicione("SB1",1,xFilial("SB1")+AA3->AA3_CODPRO,"B1_DESC")
				M->AGW_CODFAB := AA3->AA3_CODFAB
				M->AGW_LOJAFA := AA3->AA3_LOJAFA	
			Else
				Help(" ",1,"REGNOIS")
				lRet := .F.
			EndIf
		EndIf

	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AT110VlCtrºAutor  ³ Andre Anjos		 º Data ³  15/08/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida digitacao do contrato.                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TECA110                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT110VlCtr()
Local lRet := .T.

AA3->(dbSetOrder(1))
If Empty(M->AGW_CONTRA)
	M->AGW_PLANIL := CriaVar("AGW_PLANIL",.F.)
	M->AGW_ITEM   := CriaVar("AGW_ITEM",.F.)
	
	AA3->(dbSetOrder(1))
	If !AA3->(dbSeek(xFilial("AA3")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT+M->AGW_NUMSER))
		M->AGW_NUMSER := CriaVar("AGW_NUMSER",.F.)
		M->AGW_PRODUT := CriaVar("AGW_PRODUT",.F.)
		M->AGW_DESCRI := CriaVar("AGW_DESCRI",.F.)
		M->AGW_CLIENT := CriaVar("AGW_CLIENT",.F.)
		M->AGW_LOJA   := CriaVar("AGW_LOJA",.F.)
		M->AGW_CODFAB := CriaVar("AGW_CODFAB",.F.)
		M->AGW_LOJAFA := CriaVar("AGW_LOJAFA",.F.)
	Else
		M->AGW_PRODUT := AA3->AA3_CODPRO
		M->AGW_DESCRI := Posicione("SB1",1,xFilial("SB1")+AA3->AA3_CODPRO,"B1_DESC")
		M->AGW_CLIENT := AA3->AA3_CODCLI
		M->AGW_LOJA   := AA3->AA3_LOJA
		M->AGW_CODFAB := AA3->AA3_CODFAB
		M->AGW_LOJAFA := AA3->AA3_LOJAFA
	EndIf
Else
	lRet := ExistCpo("CN9")
	
	//-- Valida se contrato esta em etapa que permite o cadastro	
	CN9->(dbSetOrder(1))
	If CN9->(dbSeek(xfilial("CN9")+M->AGW_CONTRA+GetLastRev(M->AGW_CONTRA))) .And.;
				!(CN9->CN9_SITUAC $ DEF_SELAB +"/" +DEF_SREVS)
		Aviso(STR0007,STR0012,{"OK"}) //Contrato em andamento, utilização não permitida
		lRet := .F.
	EndIf
	
	If lRet
		M->AGW_CLIENT := CN9->CN9_CLIENT
		M->AGW_LOJA   := CN9->CN9_LOJACL
		M->AGW_PLANIL := CriaVar("AGW_PLANIL",.F.)
		M->AGW_ITEM   := CriaVar("AGW_ITEM",.F.)
		M->AGW_PRODUT := CriaVar("AGW_PRODUT",.F.)
		M->AGW_DESCRI := CriaVar("AGW_DESCRI",.F.)
		If AA3->(dbSeek(xFilial("AA3")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT+M->AGW_NUMSER))
			M->AGW_NUMSER := CriaVar("AGW_NUMSER",.F.)
		EndIf
	EndIf
EndIf

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AT110VlPlaºAutor  ³ Andre Anjos        º Data ³  08/15/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida digitacao da planilha.                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TECA110                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT110VlPla()
Local lRet    := .T.
Local cItemPl := If("AGW_ITEM" $ ReadVar(),M->AGW_ITEM,If(CNB->CNB_NUMERO == M->AGW_PLANIL,CNB->CNB_ITEM,""))

CNB->(dbSetOrder(1))
If CNB->(dbSeek(xFilial("CNB")+M->AGW_CONTRA+GetLastRev(M->AGW_CONTRA)+M->AGW_PLANIL+cItemPl))
	If CNB->CNB_BASINS # '1'
		Aviso(STR0007,STR0013,{"OK"}) // Produto inválido para a geração de base instalada
		lRet := .F.
	Else
		M->AGW_ITEM   := CNB->CNB_ITEM
		M->AGW_PRODUT := CNB->CNB_PRODUT
		M->AGW_DESCRI := Posicione("SB1",1,xFilial("SB1")+CNB->CNB_PRODUT,"B1_DESC")
	EndIf
ElseIf Empty(M->AGW_PLANIL)
	M->AGW_ITEM   := CriaVar("AGW_ITEM",.F.)
	M->AGW_PRODUT := CriaVar("AGW_PRODUT",.F.)
	M->AGW_DESCRI := CriaVar("AGW_DESCRI",.F.)
Else
	Help(" ",1,"REGNOIS")
	lRet := .F.
EndIf

CNA->(dbSetOrder(1))
If lRet .And. CNA->(dbSeek(xFilial("CNA")+M->AGW_CONTRA+GetLastRev(M->AGW_CONTRA)+M->AGW_PLANIL+cItemPl))
	M->AGW_CLIENT := CNA->CNA_CLIENT
	M->AGW_LOJA := CNA->CNA_LOJACL
EndIf

Return lRet


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³AT110VlPrdºAutor  ³ Andre Anjos		 º Data ³  15/08/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Valida a digitacao do produto.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TECA110												      º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function AT110VlPrd()
Local lRet := .T.

If !Empty(M->AGW_PRODUT)
	lRet := ExistCpo("SB1")

	//Pre valida a combinacao cliente x produto
	AA3->(dbSetOrder(1))
	If AA3->(dbSeek(xFilial("AA3")+M->AGW_CLIENT+M->AGW_LOJA+M->AGW_PRODUT)) .Or. Empty(M->AGW_CLIENT)
		M->AGW_DESCRI := Posicione("SB1",1,xFilial("SB1")+M->AGW_PRODUT,"B1_DESC")
		M->AGW_NUMSER := AA3->AA3_NUMSER
	Else
		Aviso(STR0007,STR0014,{"OK"}) //Este cliente não possui o produto/eqto informado
		lRet := .F.
	EndIf
Else
	M->AGW_DESCRI := CriaVar("AGW_DESCRI",.F.)
EndIf

Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Fabio Alves Silva     ³ Data ³18/10/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³		1 - Pesquisa e Posiciona em um Banco de Dados         ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()

Private aRotina	:= 	{	{STR0001,"AxPesqui",0,1,0,.F.},;	//"Pesquisar"
						{STR0002,"AT110Cad",0,2,0,nil},;	//"Visualizar"
						{STR0003,"AT110Cad",0,3,0,nil},; 	//"Incluir"
						{STR0004,"AT110Cad",0,4,0,nil},; 	//"Alterar"
						{STR0005,"AT110Cad",0,5,0,nil}	} 	//"Excluir"

Return(aRotina)                                            


/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³GetLastRevºAutor  ³ Andre Anjos		 º Data ³  31/05/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Retorna a revisao atual do contrato.                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ TECA110                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GetLastRev(cContra)
Local cRet := ""
Local aArea := CN9->(GetArea())

CN9->(dbSetOrder(1))
CN9->(dbSeek(xFilial("CN9")+cContra))
While !CN9->(EOF()) .And. CN9->(CN9_FILIAL+CN9_NUMERO) == xFilial("CN9")+cContra
	cRet := CN9->CN9_REVISA
	CN9->(dbSkip())
End

CN9->(RestArea(aArea))
Return cRet
