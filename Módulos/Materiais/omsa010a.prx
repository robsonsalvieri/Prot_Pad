#include "Protheus.ch"
#include "OMSA010.CH"
#INCLUDE "FWMVCDEF.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ViewDef     ³ Autor ³Vendas & CRM           ³ Data ³ 28/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao da View                                          	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oView                                                       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ViewDef()

Local oModel	:= FWLoadModel( 'OMSA010A' )	// Cria um objeto de Modelo de dados baseado no ModelDef do fonte informado
Local oStruDA1	:= FWFormStruct( 2, 'DA1' )		// Cria as estruturas a serem usadas na View
Local aUsrMemo  := If( ExistBlock( "OM010MEM" ), ExecBlock( "OM010MEM", .F.,.F. ), {} ) 
Local aNoFields	:= {}
Local oView									// Interface de visualização construída
Local nLoop	 	:= 1 
Local nI		:= 0
Local oStrMdlDA1:=oModel:GetModel("DA0MASTER"):GetStruct()
Local oStru2DA1 :=FWFormViewStruct():New()

oStru2DA1:AddField("DA1_CODPRO","00",oStrMdlDA1:GetProperty("DA1_CODPRO",MODEL_FIELD_TITULO),oStrMdlDA1:GetProperty("DA1_CODPRO",MODEL_FIELD_TOOLTIP), /*aHelp*/ ,"C" ,"" /*cPicture*/,/*bPictVar*/,/*cLookUp*/,/*lCanChange*/,/*cFolder*/,"001"/*cGroup*/,/*aComboValues*/,/*nMaxLenCombo*/,/*cIniBrow*/)
oStru2DA1:AddField("DA1_DESCRI","01",oStrMdlDA1:GetProperty("DA1_DESCRI",MODEL_FIELD_TITULO),oStrMdlDA1:GetProperty("DA1_DESCRI",MODEL_FIELD_TOOLTIP), /*aHelp*/ ,"C" ,"" /*cPicture*/,/*bPictVar*/,/*cLookUp*/,/*lCanChange*/,/*cFolder*/,"001"/*cGroup*/,/*aComboValues*/,/*nMaxLenCombo*/,/*cIniBrow*/)

aNoFields := {"DA1_CODPRO","DA1_DESCRI","DA1_GRUPO","DA1_REFGRD","DA1_INDLOT"}

For nI := 1 To Len(aNoFields)
	oStruDA1:RemoveField(aNoFields[nI])
Next nI

oView := FWFormView():New()					// Cria o objeto de View
oView:SetModel( oModel )					// Define qual Modelo de dados será utilizado				

//Retira campo de Codigo da SYP da View
If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "DA1"
			oStruDA1:RemoveField(aUsrMemo[ nLoop, 2 ])
		EndIf
	Next nLoop
EndIf

oView:AddField( 'VIEW_DA0', oStru2DA1,'DA0MASTER' )// Adiciona no nosso View um controle do tipo formulário (antiga Enchoice)
oView:AddGrid(  'VIEW_DA1', oStruDA1, 'DA1DETAIL' )	// Adiciona no nosso View um controle do tipo Grid (antiga Getdados)

oView:CreateHorizontalBox( 'SUPERIOR'  , 10 )
oView:CreateHorizontalBox( 'INFERIOR'  , 90 )

If ExistBlock("OS010BTN")
	aButtonUsr := ExecBlock("OS010BTN",.F.,.F.)
	If ValType(aButtonUsr) == "A"
		For nI   := 1  To  Len(aButtonUsr)			
			oView:AddUserButton( aButtonUsr[nI][3], aButtonUsr[nI][1], aButtonUsr[nI][2],NIL,NIL)
		Next nI
	EndIf
EndIf 

// Relaciona o identificador (ID) da View com o "box" para exibição
oView:SetOwnerView( 'VIEW_DA0', 'SUPERIOR' )		
oView:SetOwnerView( 'VIEW_DA1', 'INFERIOR' )

Return oView

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ModelDef    ³ Autor ³Vendas & CRM           ³ Data ³ 28/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Definicao do Model                                         	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ oModel                                                      	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ModelDef()
Local oStruDA1	:= FWFormStruct( 1, 'DA1' )	// Cria as estruturas a serem usadas no Modelo de Dados
Local oStru2Da1	:=FWFormModelStruct():New()	// Cria as estruturas a serem usadas no Modelo de Dados
Local aUsrMemo  := If( ExistBlock( "OM010MEM" ), ExecBlock( "OM010MEM", .F.,.F. ), {} ) 
Local aMemoDA0 	:= {}						// Memo de Usuario em DA0	
Local aMemoDA1 	:= {}						// Memo de Usuario em DA1
Local aAux 		:= {}						// Auxiliar para criar a trigger
Local nLoop		:= 1						
Local oModel 							

aAux := FwStruTrigger(;
'DA1_CODTAB',;
'DA1_DESTAB',;
'DA0->DA0_DESCRI',;
.T.,;
'DA0',;
1,;
'xFilial("DA0")+FwFldGet("DA1_CODTAB")')
oStruDA1:AddTrigger( ;
aAux[1] , ; 	// [01] identificador (ID) do campo de origem
aAux[2] , ; 	// [02] identificador (ID) do campo de destino
aAux[3] , ; 	// [03] Bloco de código de validação da execução do gatilho
aAux[4] ) 		// [04] Bloco de código de execução do gatilho


oStruDA1:SetProperty("DA1_PRCVEN",MODEL_FIELD_OBRIGAT,.F.)
oStruDA1:SetProperty("DA1_DESTAB",MODEL_FIELD_INIT,FwBuildFeature(STRUCT_FEATURE_INIPAD,'Oms010aDes()'))

oStru2Da1:AddTable( "DA1" , {"DA1_FILIAL","DA1_CODTAB","DA1_CODPRO"} , "Tabela de Preço" )
dbSelectArea("SX3")
dbSetOrder(2)
dbSeek("DA1_CODPRO")
oStru2Da1:AddField(X3Titulo(),X3Titulo(),"DA1_CODPRO" ,"C",TamSX3("DA1_CODPRO")[1] , TamSX3("DA1_CODPRO")[2] , {|| .T. } , /*bWhen*/,/*aValues*/ , /*lObrigat*/,{|| Pergunte("OMS010",.F.),mv_par02}/*bInit*/, /*lKey*/, /*lNoUpd*/,.T.)
dbSeek("DA1_DESCRI")
oStru2Da1:AddField(X3Titulo(),X3Titulo(),"DA1_DESCRI" ,"C",TamSX3("DA1_DESCRI")[1] , TamSX3("DA1_DESCRI")[2] , {|| .T. } , /*bWhen*/,/*aValues*/ , /*lObrigat*/,{|| Pergunte("OMS010",.F.),Posicione("SB1",1,xFilial("SB1")+mv_par02,"B1_DESC") }/*bInit*/, /*lKey*/, /*lNoUpd*/,.T.)

// Cria o objeto do Modelo de Dados
oModel := MPFormModel():New( 'OMSA010A' , /*Pre*/, {|oMdl| OMS010TudOk(oMdl)}, {|oMdl| OMS010Com(oMdl) })

If ValType( aUsrMemo ) == "A" .And. Len( aUsrMemo ) > 0
	For nLoop := 1 to Len( aUsrMemo )
		If aUsrMemo[ nLoop, 1 ] == "DA1"
			AAdd( aMemoDA1, { aUsrMemo[ nLoop, 2 ], aUsrMemo[ nLoop, 3 ] } )
			Oms010AddC(oStruDA1,aUsrMemo[ nLoop, 2 ])
		EndIf
	Next nLoop
EndIf

FWMemoVirtual( oStruDA1, aMemoDA1)

oStru2DA1:SetProperty( 'DA1_CODPRO' , MODEL_FIELD_WHEN, {|oMdl| (Empty(oMdl:GetValue('DA1_CODPRO')))} )
oStru2DA1:SetProperty( 'DA1_DESCRI' , MODEL_FIELD_WHEN, {|oMdl| (Empty(oMdl:GetValue('DA1_DESCRI')))} )

oStruDA1:SetProperty( 'DA1_CODPRO' , MODEL_FIELD_OBRIGAT, .F.)
// Adiciona ao modelo um componente de formulário
oModel:AddFields( 'DA0MASTER', /*cOwner*/, oStru2DA1 )	

// Adiciona ao modelo componentes de gridm
oModel:AddGrid( 'DA1DETAIL', 'DA0MASTER', oStruDA1 , /*LinPre*/, {|oMdl| OMS010LinOk(oMdl) },{|oMdl| OMS010GdPr(oMdl)},/*Pos*/,/*Load*/)

//Model de alteração por Produto opcional eh opcional
oModel:GetModel('DA1DETAIL'):SetOptional(.T.)

// Linha unica do grid.
oModel:GetModel( 'DA1DETAIL' ):SetUniqueLine( { 'DA1_QTDLOT','DA1_TPOPER','DA1_ESTADO','DA1_DATVIG','DA1_CODTAB','DA1_REFGRD' } )

//Validacao de Ativacao
oModel:SetVldActivate( { |oModel| OMS010VlAc( oModel ) } )

// Faz relacionamento entre os componentes do model
oModel:SetRelation( 'DA1DETAIL', { { 'DA1_FILIAL', 'xFilial( "DA1" )' }, { 'DA1_CODPRO', 'DA1_CODPRO' } }, DA1->( IndexKey( 2 ) ) )
oModel:SetPrimaryKey({'DA1_FILIAL','DA1_CODPRO','DA1_CODTAB'})

// Retorna o Modelo de dados
Return oModel

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³MenuDef     ³ Autor ³Vendas & CRM           ³ Data ³ 04/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Menudef                                                    	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ aRotina                                                     	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
Return FWMVCMENU('OMSA010A')

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Oms010aDes  ³ Autor ³Vendas & CRM           ³ Data ³ 05/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descrição ³ Inicializador Padrao                                       	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Descricao da Tabela de Preco                                	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum														³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Oms010aDes()
Local cRet		:= ""
Local oModel 	:= FwModelActive()
Local oMdlDA1	:= oModel:GetModel("DA1DETAIL")

If oModel:GetOperation() != MODEL_OPERATION_INSERT
	If oModel:IsActive() .AND. oMdlDA1:GetLine() > 0
		If !oMdlDA1:lInsertLine
			cRet := Posicione("DA0",1,xFilial("DA0")+FwFldGet("DA1_CODTAB"),"DA0_DESCRI")
		EndIf
	Else		
		cRet := Posicione("DA0",1,xFilial("DA0")+DA1->DA1_CODTAB,"DA0_DESCRI")
	EndIf
EndIf

Return cRet