#INCLUDE "TECC010.CH" 
#INCLUDE "PROTHEUS.CH"
#DEFINE nGetAlt  009 // Altura do Get 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Program   ³TECC010   ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao Historico de Clientes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function TECC010()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro := STR0001 //"Hist¢rico de Clientes"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                       PARAMETROS                             ³
//³                                                              ³
//³ MV_PAR01 : Emissao de                                        ³
//³ MV_PAR02 : Emissao Ate                                       ³
//³                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

Private aRotina := MenuDef()

If Pergunte("ATC010",.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Endereca a funcao de BROWSE                                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	mBrowse( 6, 1,22,75,"SA1")
EndIf 	
	
dbSelectArea("SA1")
dbSetOrder(1)
Return

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Função    ³ MenuDef  ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 11.12.06 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Definição do aRotina (Menu funcional)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ MenuDef()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ TECC010                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MenuDef()
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa      ³
//³ ----------- Elementos contidos por dimensao ------------     ³
//³ 1. Nome a aparecer no cabecalho                              ³
//³ 2. Nome da Rotina associada                                  ³
//³ 3. Usado pela rotina                                         ³
//³ 4. Tipo de Transa‡„o a ser efetuada                          ³
//³    1 - Pesquisa e Posiciona em um Banco de Dados             ³
//³    2 - Simplesmente Mostra os Campos                         ³
//³    3 - Inclui registros no Bancos de Dados                   ³
//³    4 - Altera o registro corrente                            ³
//³    5 - Remove o registro corrente do Banco de Dados          ³
//³    6 - Alteracao sem inclusao de registro                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aRotina := {{ STR0002, "AxPesqui",  0, 1, 0, .F.},;	//"Pesquisar"
				    { STR0003, "AtC010con", 0, 2, 0, .T.} }	//"Consulta"
Return(aRotina)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtC010con ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Consulta ao Historico de Clientes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias                                              ³±±
±±³          ³ ExpN1 : Registro                                           ³±±
±±³          ³ ExpN2 : Opcao                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³30/01/2007³ Conrado Q.    ³ BOPS 115755: Adaptação para utilização em  ³±±
±±³          ³               ³ conjunto com a FillGetDb.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function AtC010con(cAlias,nReg,nOpc)

Local aArea		:= { Alias() , IndexOrd() , RecNo() }
Local aAreaSA1	:= { SA1->(IndexOrd()) , SA1->(RecNo()) }
Local oDlg			:= Nil
Local cCadastro	:= STR0001 //"Hist¢rico de Clientes"
Local cMoeda		:= ""
Local aFerase		:= {}
Local nMcusto		:= 	IIf(SA1->A1_MOEDALC > 0, SA1->A1_MOEDALC, Val(SuperGetMv("MV_MCUSTO")))
Local aCols		:= {}
Local aHeader		:= {}
Local nCntFor		:= 0
Local oGsTmpTable	:= NIL
Local oPanel1		:= Nil
Local aSavHeaders	:= {}

Private INCLUI  := .F.
Private ALTERA  := .F.
Private aRotina := {	{ STR0002,"AxPesqui"  , 0, 1},;     //"Pesquisar"
						{ STR0003,"AtC010con" , 0, 2} }    //"Consulta"
	
dbSelectArea(cAlias)
dbGoto(nReg)

cMoeda  		:=  SubStr(SuperGetMv("MV_SIMB"+Alltrim(STR(nMCusto)))+Space(4),1,4) 
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Carrega as Perguntas                                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("ATC010",.F.)

aHeader	:= {STR0050, STR0049, STR0048+AllTrim(cMoeda), " ", STR0050, STR0001}  

//Limite de credito - Primeira compra
Aadd(aCols,{STR0013,TRansform(xMoeda(SA1->A1_LC, nMCusto, 1,dDataBase),PesqPict("SA1","A1_LC",14,1)),;
				TRansform(SA1->A1_LC,PesqPict("SA1","A1_LC",14,nMCusto))," ",;
	            STR0014,SPACE(07)+DtoC(SA1->A1_PRICOM)}) // Limite credito ## Primeira Compra

//Saldo Limite de credito - Ultima compra
Aadd(aCols,{STR0009,TRansform(SA1->A1_SALDUP,PesqPict("SA1","A1_SALDUP",14,1) ),;
            TRansform(SA1->A1_SALDUPM,PesqPict("SA1","A1_SALDUPM",14,nMCusto))," ",;
			STR0015,SPACE(07)+DtoC(SA1->A1_ULTCOM)}) // Saldo Atual ## Ultima Compra

//Limite de credito secundario - Maior atraso
Aadd(aCols,{RetTitle("A1_LCFIN"),TRansform(xMoeda(SA1->A1_LCFIN,nMCusto,1,dDatabase,MsDecimais(1)),;
            PesqPict("SA1","A1_LCFIN",14,1)),TRansform(SA1->A1_LCFIN,PesqPict("SA1","A1_LCFIN",14,nMCusto))," ",;
            STR0016,Transform(SA1->A1_MATR,PesqPict("SA1","A1_MATR",14))}) //  Maior Atraso    

//Saldo Limite de credito secundario - Media de atraso
Aadd(aCols,{RetTitle("A1_SALFIN"),TRansform(SA1->A1_SALFIN,PesqPict("SA1","A1_SALFIN",14,1)),;
            TRansform(SA1->A1_SALFINM,PesqPict("SA1","A1_SALFINM",14,nMCusto))," ",STR0017,;
            Transform(SA1->A1_METR,PesqPict("SA1","A1_METR",14))}) // Media de Atraso

//Maior Compra - Grau de risco
Aadd(aCols,{STR0010,;
				TRansform(xMoeda(SA1->A1_MCOMPRA, nMCusto ,1, dDataBase,MsDecimais(1) ),PesqPict("SA1","A1_MCOMPRA",14,1) ) ,;
				TRansform(SA1->A1_MCOMPRA,PesqPict("SA1","A1_MCOMPRA",14,nMCusto))," ",;
            STR0018,SPACE(25)+SA1->A1_RISCO}) // Maior Compra ## Grau de Risco

//Maior Saldo
Aadd(aCols,{STR0011,;
			TRansform(xMoeda(SA1->A1_MSALDO, nMCusto ,1, dDataBase,MsDecimais(1) ),PesqPict("SA1","A1_MSALDO",14,1)),;
            TRansform(SA1->A1_MSALDO,PesqPict("SA1","A1_MSALDO",14,nMCusto)),,,,}) //Maior Saldo
            
DEFINE MSDIALOG oDlg FROM	07,0 TO 28,80 TITLE cCadastro OF oMainWnd

@ 002,005 SAY STR0004 SIZE 021,007 OF oDlg  PIXEL //"C¢digo"
@ 011,004 MSGET SA1->A1_COD When .F. SIZE 021,nGetAlt OF oDlg  PIXEL

@ 002,033 SAY STR0005 SIZE 016,007 OF oDlg  PIXEL //"Loja"
@ 011,033 MSGET SA1->A1_LOJA When .F. SIZE 016,nGetAlt OF oDlg  PIXEL

@ 002,052 SAY STR0006 SIZE 017,007 OF oDlg  PIXEL //"Nome"
@ 011,052 MSGET SA1->A1_NOME When .F. SIZE 107,nGetAlt OF oDlg  PIXEL

@ 002,161 SAY RetTitle("A1_CGC") SIZE 025,007 OF oDlg  PIXEL 
@ 011,161 MSGET SA1->A1_CGC When .F. SIZE 053,nGetAlt OF oDlg  PIXEL

@ 002,216 SAY STR0008 SIZE 021,007 OF oDlg  PIXEL //"Telefone"
@ 011,216 MSGET SA1->A1_TEL When .F. SIZE 050,nGetAlt OF oDlg  PIXEL

oLbx := RDListBox(1.9, .42, 310, 70, aCols, aHeader,{38,51,51,11,50,63})   

@ 099,006 SAY STR0019 SIZE 061,007 OF oDlg  PIXEL //"Cheques Devolvidos"

@ 107,006 SAY STR0020 SIZE 034,007 OF oDlg  PIXEL //"Quantidade"
@ 114,006 MSGET SA1->A1_CHQDEVO When .F. SIZE 024,nGetAlt OF oDlg  PIXEL

@ 107,045 SAY STR0021 SIZE 066,007 OF oDlg  PIXEL //"Ultimo Devolvido"
@ 114,045 MSGET SA1->A1_DTULCHQ When .F. SIZE 034,nGetAlt OF oDlg  PIXEL

@ 127,006 SAY STR0022 SIZE 061,007 OF oDlg  PIXEL //"Titulos Protestados"

@ 136,006 SAY STR0023 SIZE 034,007 OF oDlg  PIXEL //"Quantidade"
@ 143,006 MSGET SA1->A1_TITPROT When .F. SIZE 024,nGetAlt OF oDlg  PIXEL

@ 135,045 SAY STR0024 SIZE 066,007 OF oDlg  PIXEL //"Ultimo Protesto"
@ 143,045 MSGET SA1->A1_DTULTIT When .F. SIZE 034,nGetAlt OF oDlg  PIXEL

@ 101, 125 MSPANEL oPanel1 PROMPT "" SIZE 188, 052 OF oDlg LOWERED 

@  106, 132 BUTTON STR0025 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(1,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Garantia"
@  106, 177 BUTTON STR0026 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(2,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Contratos"
@  106, 222 BUTTON STR0027 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(3,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Pendentes"
@  106, 267 BUTTON STR0028 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(4,  @aFerase, @aSavHeaders,@oGsTmpTable) OF oDlg PIXEL  //"Chamados"
@  122, 132 BUTTON STR0029 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(5,  @aFerase, @aSavHeaders,@oGsTmpTable) OF oDlg PIXEL  //"Or‡amentos"
@  122, 177 BUTTON STR0030 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(6,  @aFerase, @aSavHeaders,@oGsTmpTable) OF oDlg PIXEL  //"O.Servi‡o"
@  122, 222 BUTTON STR0031 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(7,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Hist¢ricos"
@  122, 267 BUTTON STR0032 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(8,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Garantia/OS"
@  138, 132 BUTTON STR0042 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(9,  @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Manutencao"
@  138, 177 BUTTON STR0043 SIZE 040,011 FONT oDlg:oFont ACTION AtcMostra(10, @aFerase, @aSavHeaders)              OF oDlg PIXEL  //"Pendencia"
@  138, 267 BUTTON STR0033 SIZE 040,010 FONT oDlg:oFont ACTION oDlg:End()                                         OF oDlg PIXEL  //"Sair"

ACTIVATE MSDIALOG oDlg

If ( Select("QUERY1")<> 0 )
	dbSelectArea("QUERY1")
	dbCloseArea()
EndIf
If ( Select("QUERY2")<> 0 )
	dbSelectArea("QUERY2")
	dbCloseArea()
EndIf
If ( Select("QUERY3")<> 0 )
	dbSelectArea("QUERY3")
	dbCloseArea()
EndIf
If ( Select("QUERY4")<> 0 )
	dbSelectArea("QUERY4")
	dbCloseArea()
EndIf
If ( Select("QUERY5")<> 0 )
	dbSelectArea("QUERY5")
	dbCloseArea()
EndIf
If ( Select("QUERY6")<> 0 )
	dbSelectArea("QUERY6")
	dbCloseArea()
EndIf
If ( Select("QUERY7")<> 0 )
	dbSelectArea("QUERY7")
	dbCloseArea()
EndIf
If ( Select("QUERY8")<> 0 )
	dbSelectArea("QUERY8")
	dbCloseArea()
EndIf

If ( Select("QUERY9")<> 0 )
	dbSelectArea("QUERY9")
	dbCloseArea()
EndIf

If ( Select("QUERY10")<> 0 )
	dbSelectArea("QUERY10")
	dbCloseArea()
EndIf

If ( Select("QUERY11")<> 0 )
	dbSelectArea("QUERY11")
	dbCloseArea()
EndIf

If Valtype(oGsTmpTable) <> 'U'
	oGsTmpTable:Close()
EndIf

dbSelectArea("SA1")
dbSetOrder(aAreaSA1[1])
dbGoto(aAreaSA1[2])

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcMostra ³ Autor ³ Eduardo Riera         ³ Data ³ 24.09.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Demonstra o Historico de Clientes                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Query a ser Montada                                ³±±
±±³          ³ ExpA1 : Arquivos para serem deletados                      ³±±
±±³          ³ ExpA2 : aHeaders já criados                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³17/08/2006³ Cleber M.     ³Bops 102420: Correcao na query de chamados  ³±±
±±³          ³               ³tecnicos.                                   ³±±
±±³30/01/2007³ Conrado Q.    ³Bops 155755: Montagem do aCols e aHeader    ³±±
±±³          ³               ³através da rotina FillGetDb.                ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function AtcMostra(nCaso, aFerase, aSavHeaders, oGsTmpTable)

Local oDlg				:= Nil
Local oBtn				:= Nil
Local oGetDb			:= Nil
Local cCadastro		:= ""
Local cArqQuery		:= ""
Local cAlias			:= "QUERY" + AllTrim( Str( nCaso, 2 ) )
Local aRecnos			:= {}
Local aArea			:= { Alias() , IndexOrd() , RecNo() }
Local aAreaAA3		:= { AA3->(IndexOrd()) , AA3->(RecNo()) }
Local bVisual			:= Nil
Local aObjects		:= {} 
Local aPosObj			:= {} 
Local aSizeAut		:= MsAdvSize( , .T. ) // Janela padrao Siga 
Local cSeek			:= ""			// Procura inicial da FillGetDb
Local cWhile			:= ""			// Condição da FillGetDb
Local bCond			:= {||}		// Condição para a adição de registros
Local cQuery			:= Nil			// Caso for TOP, é a query para a busca dos registros
Local bMountFile		:= Nil			// Se for um caso especial, utiliza o esse bloco para montar o arquivo de trabalho
Local nIdxOrd			:= 0			// Ordem da FillGetDb
Local bAfterHeader	:= {||}		// Bloco executado após a criação do aHeader e do aEstruQuery na FillGetDb

Default oGsTmpTable	:= Nil

Private aHeader := {}

Do Case
	Case nCaso 	== 1
		cCadastro := STR0034 //"Equipamentos em Garantia"
		bVisual   := {|| AtcVisual(1,oGetDb)}
	Case nCaso 	== 2
		cCadastro := STR0035 //"Equipamentos em Contrato de Manuten‡„o"
		bVisual   := {|| AtcVisual(2,oGetDb)}
	Case nCaso	== 3
		cCadastro := STR0036 //"Equipamentos Pendentes"
		bVisual   := {|| AtcVisual(3,oGetDb)}
	Case nCaso	== 4
		cCadastro := STR0037 //"Chamados T‚cnicos"
		bVisual   := {|| AtcVisual(4,oGetDb)}
	Case nCaso 	== 5
		cCadastro := STR0038 //"Or‡amentos"
		bVisual   := {|| AtcVisual(5,oGetDb)}
	Case nCaso	== 6
		cCadastro := STR0039 //"Ordens de Servi‡o"
		bVisual   := {|| AtcVisual(6,oGetDb)}
	Case nCaso  == 7
		cCadastro := STR0040 //"Hist¢rico"
	Case nCaso  == 8
		cCadastro := STR0041 //"Garantia/OS"
		bVisual   := {|| AtcVisual(8,oGetDb)}
	Case nCaso  == 9
		cCadastro := STR0042 //"Manutencao"
		bVisual   := {|| AtcVisual(9,oGetDb)}
	Case nCaso  == 10
		cCadastro := STR0043 //"Pendencias"
		bVisual   := {|| AtcVisual(10,oGetDb)}
EndCase

Do Case
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamentos em Garantia                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 1 )
	
		cSeek		:= xFilial("AA3")+SA1->A1_COD+SA1->A1_LOJA
		cWhile		:= "AA3->AA3_FILIAL+AA3->AA3_CODCLI+AA3->AA3_LOJA"
		bCond		:= {|| AA3->AA3_DTGAR >= dDataBase }
		cQuery		:= Nil
		bMountFile	:= Nil		
		nIdxOrd	:= 1
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AA3", {"AA3_CONTRT"}, aHeader, aStruQuery ) }
		
		AtcMonta(nCaso,   aSavHeaders, "AA3",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)	
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamento em Contrato de Manutencao                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 2 )
		cSeek		:= xFilial("AA3")+SA1->A1_COD+SA1->A1_LOJA
		cWhile		:= "AA3->AA3_FILIAL+AA3->AA3_CODCLI+AA3->AA3_LOJA"
		bCond		:= {|| AtcbCond(nCaso) }
		cQuery		:= Nil
		bMountFile	:= Nil		
		nIdxOrd	:= 1		
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AA3", {"AA3_CONTRT"}, aHeader, aStruQuery ) }
		                                 	
		AtcMonta(nCaso,   aSavHeaders, "AA3",  cSeek,; 
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
		
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamento sem Contrato e sem Garantia                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 3 )
		cSeek		:= xFilial("AA3")+SA1->A1_COD+SA1->A1_LOJA
		cWhile		:= "AA3->AA3_FILIAL+AA3->AA3_CODCLI+AA3->AA3_LOJA"
		bCond		:= {|| ( Empty(AA3->AA3_CONTRT) .And. AA3->AA3_DTGAR < dDataBase ) }
		cQuery		:= Nil
		bMountFile	:= Nil		
		nIdxOrd	:= 1		
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AA3", {"AA3_CONTRT"}, aHeader, aStruQuery ) }
		
		AtcMonta(nCaso,   aSavHeaders, "AA3",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Chamados Tecnicos                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 4 )
		cSeek		:= ""
		cWhile		:= ""
		bCond		:= {|| .T. }
		bMountFile	:= {|| ATCbMountFile( nCaso, aFErase, @oGsTmpTable ) }
		nIdxOrd	:= 1
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB2", { "AB2_STATUS", "AB2_NUMORC", "AB2_NUMOS", "AB2_NRCHAM" }, aHeader, aStruQuery ) }

		AtcMonta(nCaso,   aSavHeaders, "AB2",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Orcamentos                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 5 )
		cSeek		:= ""
		cWhile		:= ""
		bCond		:= {|| .T. }
		bMountFile	:= {|| ATCbMountFile( nCaso, aFErase,@oGsTmpTable ) }
		nIdxOrd	:= 1
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB4", { "AB4_NUMOS", "AB4_NRCHAM", "AB4_NUMORC" }, aHeader, aStruQuery ) }

		AtcMonta(nCaso,   aSavHeaders, "AB4",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
		
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordem de Servico                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 6 )
		cSeek		:= ""
		cWhile		:= ""
		bCond		:= {|| .T. }
		bMountFile	:= {|| ATCbMountFile( nCaso, aFErase,@oGsTmpTable ) }
		nIdxOrd	:= 1
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB7", { "AB7_NUMORC", "AB7_NRCHAM", "AB7_NUMOS" }, aHeader, aStruQuery ) }

		AtcMonta(nCaso,   aSavHeaders, "AB7",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)	
					
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Historico                                                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 7 )
		cSeek		:= xFilial("AAF")+SA1->A1_COD+SA1->A1_LOJA
		cWhile		:= "AAF->AAF_FILIAL+AAF->AAF_CODCLI+AAF->AAF_LOJA"
		bCond		:= {||	AAF->AAF_DTINI >= MV_PAR01 .And. ;
							AAF->AAF_DTFIM <= MV_PAR02 }
		cQuery		:= Nil
		bMountFile	:= Nil
		nIdxOrd	:= 1
		bAfterHeader:= {||}		
		
		AtcMonta(nCaso,   aSavHeaders, "AAF",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)
			
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³OS em Garantia                                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 8 )
		cSeek		:= xFilial("AB8")+SA1->A1_COD+SA1->A1_LOJA
		cWhile		:= "AB8->AB8_FILIAL+AB8->AB8_CODCLI+AB8->AB8_LOJA"
		bCond		:= {||	AB8->AB8_DTGAR >= dDataBase .AND. AB8->AB8_TIPO $ "24" }
		cQuery		:= Nil
		bMountFile	:= Nil
		nIdxOrd	:= 3
		bAfterHeader:= {| aHeader,aStruQuery | ATCbAfterHeader( "AB8", { "AB8_CODPRD", "AB8_NUMSER", "AB8_NUMOS" }, aHeader, aStruQuery ) }
							
		cQuery	:= "SELECT AB8.*"
		cQuery	+= " FROM "+RetSqlName("AB8")+" AB8 "
		cQuery	+= " WHERE AB8.AB8_FILIAL='"+xFilial("AB8")+"' AND "
		cQuery	+= 	 	"  AB8.AB8_CODCLI='"+SA1->A1_COD+"' AND "
		cQuery	+= 		"  AB8.AB8_LOJA='"+SA1->A1_LOJA+"' AND "
		cQuery	+= 		"  AB8.AB8_DTGAR>='"+Dtos(dDataBase)+"' AND "
		cQuery	+=		"  AB8.AB8_TIPO IN('2','4') AND "
		cQuery	+=		"  AB8.D_E_L_E_T_=' '"
		cQuery	:= ChangeQuery(cQuery)
		cSeek	:= Nil
		cWhile	:= Nil			
		bCond	:= {|| .T. }
		
		AtcMonta(nCaso,   aSavHeaders, "AB8",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader	)	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Plano de Manutencao                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 9 )
		cSeek		:= xFilial( "AA3" ) + SA1->A1_COD + SA1->A1_LOJA
		cWhile		:= "AA3->AA3_FILIAL+AA3->AA3_CODCLI+AA3->AA3_LOJA"
		bCond		:= {||.T.}
		cQuery		:= Nil
		bMountFile	:= Nil
		nIdxOrd		:= 1
		bAfterHeader:= {||}
							
		cQuery	:= "SELECT AA3.*"
		cQuery	+= " FROM " + RetSqlName("AA3")+" AA3 "
		cQuery	+= " WHERE AA3_FILIAL='" + xFilial("AA3") + "' AND "
		cQuery	+= "AA3_CODCLI='"        + SA1->A1_COD    + "' AND "
		cQuery	+= "AA3_LOJA='"          + SA1->A1_LOJA   + "' AND "
		cQuery	+= " D_E_L_E_T_=' ' ORDER BY " + SqlOrder( AA3->( IndexKey() ) )
		cQuery	:= ChangeQuery(cQuery)
		cSeek	:= Nil
		cWhile	:= Nil			
		bCond	:= {|| .T. }
		
		AtcMonta(nCaso,   aSavHeaders, "AA3",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader	)	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Pendências                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 10 )
		cSeek		:= xFilial( "AA3" ) + SA1->A1_COD + SA1->A1_LOJA
		cWhile		:= "AA3->AA3_FILIAL+AA3->AA3_CODCLI+AA3->AA3_LOJA"
		bCond		:= {||.T.}
		cQuery		:= Nil
		bMountFile	:= Nil
		nIdxOrd		:= 1
		bAfterHeader:= {||}
							
		cQuery := "SELECT AA3.*"
		cQuery += " FROM " + RetSqlName("AA3")+" AA3 "
		cQuery += " WHERE AA3_FILIAL='" + xFilial("AA3") + "' AND "
		cQuery += "AA3_CODCLI='"        + SA1->A1_COD    + "' AND "
		cQuery += "AA3_LOJA='"          + SA1->A1_LOJA   + "' AND "
		cQuery += " D_E_L_E_T_=' ' ORDER BY " + SqlOrder( AA3->( IndexKey() ) )
		cSeek	:= Nil
		cWhile	:= Nil			
		bCond	:= {|| .T. }
		
		AtcMonta(nCaso,   aSavHeaders, "AA3",  cSeek,;
		         cWhile,  bCond,       cQuery, bMountFile,;
		         nIdxOrd, bAfterHeader)	

EndCase

If	Select( cAlias ) > 0
	dbSelectArea( cAlias )
	(cAlias)->( dbGoTop() )
	If ! (cAlias)->( Eof() )

		aObjects := {}
		AAdd( aObjects, { 100,  18, .T., .F. } )
		AAdd( aObjects, { 100, 100, .T., .T. } )
	
		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }

		aPosObj := MsObjSize( aInfo, aObjects )

		DEFINE MSDIALOG oDlg FROM	aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] TITLE cCadastro OF oMainWnd PIXEL
		@ 001,004 TO 031,267 OF oDlg	PIXEL

		@ 007,007 SAY STR0004 SIZE 021,007 OF oDlg PIXEL //"C¢digo"
		@ 016,006 MSGET SA1->A1_COD When .F. SIZE 021,nGetAlt OF oDlg PIXEL

		@ 007,035 SAY STR0005 SIZE 016,007 OF oDlg PIXEL //"Loja"
		@ 016,035 MSGET SA1->A1_LOJA When .F. SIZE 016,nGetAlt OF oDlg PIXEL

		@ 007,054 SAY STR0006 SIZE 017,007 OF oDlg PIXEL //"Nome"
		@ 016,054 MSGET SA1->A1_NOME When .F. SIZE 107,nGetAlt OF oDlg PIXEL
	                                        
		dbSelectArea( cAlias )
		oGetDb := MsGetDB():New(aPosObj[2,1], aPosObj[2,2], aPosObj[2,3], aPosObj[2,4], 1, , , , .F., , , .F., , cAlias, , , , oDlg)

		DEFINE SBUTTON 		FROM 005,280 TYPE 1  ENABLE OF oDlg ACTION ( oDlg:End() )
		DEFINE SBUTTON oBtn	FROM 020,280 TYPE 15 ENABLE OF oDlg
		oBtn:lAutDisable := .F.
		If ( bVisual <> Nil )
			oBtn:bAction	:= bVisual
		Else
			oBtn:SetDisable(.T.)
		EndIf
		ACTIVATE MSDIALOG oDlg
	Else
		Help(" ",1,"REGNOIS")
	EndIf
Else
	Help(" ",1,"REGNOIS")
EndIf

dbSelectArea("AA3")
dbSetOrder(aAreaAA3[1])
dbGoto(aAreaAA3[2])

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcVisual ³ Autor ³ Eduardo Riera         ³ Data ³ 28.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Efetuar a consulta generica ( AxVisual ) aos itens envol-  ³±±
±±³          ³ vidos.                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhuma                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Consulta a ser Montada                             ³±±
±±³          ³ ExpO2 : Objeto da GetDb                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³30/01/2007³ Conrado Q.    ³ BOPS 115755: Adaptação para utilização em  ³±±
±±³          ³               ³ conjunto com a FillGetDb.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AtcVisual(nCaso,oGetdb)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao de Variaveis                                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aArea 		:= { Alias() , IndexOrd() , RecNo() }
Local aSavHeader	:= aClone(aHeader)
oGetdb:oBrowse:lDisablePaint := .T.

Do Case
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamentos em Garantia                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 1 )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AA3")+QUERY1->AA3_CODCLI+;
											QUERY1->AA3_LOJA+;
											QUERY1->AA3_CODPRO+;
											QUERY1->AA3_NUMSER) )
			At040Visua("AA3",AA3->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamento em Contrato de Manutencao                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 2 )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AA3")+QUERY2->AA3_CODCLI+;
											QUERY2->AA3_LOJA+;
											QUERY2->AA3_CODPRO+;
											QUERY2->AA3_NUMSER) )
			At040Visua("AA3",AA3->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Equipamento sem Contrato e sem Garantia                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 3 )
		dbSelectArea("AA3")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AA3")+QUERY3->AA3_CODCLI+;
											QUERY3->AA3_LOJA+;
											QUERY3->AA3_CODPRO+;
											QUERY3->AA3_NUMSER) )
			At040Visua("AA3",AA3->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Chamados Tecnicos                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 4 )
		dbSelectArea("AB1")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AB1")+QUERY4->AB2_NRCHAM) )
			At300Visua("AB1",AB1->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Orcamentos                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 5 )
		dbSelectArea("AB3")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AB3")+QUERY5->AB4_NUMORC) )
			At400Visua("AB3",AB3->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ordem de Servico                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 6 )
		dbSelectArea("AB6")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AB6")+QUERY6->AB7_NUMOS) )
			At450Visua("AB6",AB6->(RecNo()),2)
		EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³OS em Garantia                                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 8 )
		dbSelectArea("AB6")
		dbSetOrder(1)
		If ( dbSeek(xFilial("AB6")+QUERY8->AB8_NUMOS) )
			At450Visua("AB6",AB6->(RecNo()),2)
		EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Movimentos de Manutencao preventiva                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Case ( nCaso == 9  )
		AtcManut( aSavHeaders )
	Case ( nCaso == 10 )
		AtcPenden( aSavHeaders )

EndCase
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Restaura a Rotina                                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := aClone(aSavHeader)
oGetdb:oBrowse:lDisablePaint := .F.

dbSelectArea(aArea[1])
dbSetOrder(aArea[2])
dbGoto(aArea[3])

Return(Nil)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ AtcManut ³ Autor ³ Sergio Silveira       ³ Data ³ 12/11/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibir os movimentos de manutencao do equipamento            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtcManut()                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³30/01/2007³ Conrado Q.    ³Bops 155755: Montagem do aCols e aHeader      ³±±
±±³          ³               ³através da rotina FillGetDb.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtcManut( aSavHeaders )

Local aFErase			:= {}
Local bVisual			:= {|| AtcButton( oGetDB2 )}
Local cAlias			:= "QUERY11"
Local oGetDB2			:= Nil
Local oDlg2			:= Nil
Local oBtn 			:= Nil
Local cSeek			:= ""		// Procura inicial da FillGetDb
Local cWhile			:= ""		// Condição da FillGetDb
Local bCond			:= {||}	// Condição para a adição de registros
Local cQuery			:= Nil		// Caso for TOP, é a query para a busca dos registros
Local bMountFile		:= Nil		// Se for um caso especial, utiliza o esse bloco para montar o arquivo de trabalho
Local nIdxOrd			:= 0		// Ordem da FillGetDb
Local bAfterHeader	:= {||}	// Bloco executado após a criação do aHeader e do aEstruQuery na FillGetDb

Private aHeader  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Plano de Manutencao                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cSeek			:=	xFilial("ABE") + QUERY9->AA3_CODFAB + QUERY9->AA3_LOJAFA + QUERY9->AA3_CODPRO	+ QUERY9->AA3_NUMSER
cWhile			:= "ABE->ABE_FILIAL + ABE->ABE_CODFAB + ABE->ABE_LOJAFA + ABE->ABE_CODPRO + ABE->ABE_NUMSER"
bCond			:= {||.T.}
cQuery			:= Nil
bMountFile		:= Nil
nIdxOrd		:= 1
bAfterHeader	:= {||}
					
cQuery	:= "SELECT ABE.*"
cQuery	+= " FROM " + RetSqlName( "ABE" ) + " ABE "
cQuery	+= " WHERE "
cQuery	+= " ABE_FILIAL = '" + xFilial("ABE")     + "' AND "
cQuery	+= " ABE_CODFAB = '" + QUERY9->AA3_CODFAB + "' AND "
cQuery	+= " ABE_LOJAFA = '" + QUERY9->AA3_LOJAFA + "' AND "
cQuery	+= " ABE_CODPRO = '" + QUERY9->AA3_CODPRO + "' AND "
cQuery	+= " ABE_NUMSER = '" + QUERY9->AA3_NUMSER + "' AND "
cQuery	+= " D_E_L_E_T_=' ' ORDER BY " + SqlOrder( ABE->( IndexKey() ) )
cQuery	:= ChangeQuery( cQuery )	
cSeek	:= ""
cWhile	:= ""

AtcMonta(	11, aSavHeaders, "ABE", cSeek,;
			cWhile, bCond, cQuery, bMountFile,; 
			nIdxOrd , bAfterHeader	)	

dbSelectArea( "QUERY11" )
QUERY11->( dbGotop() )

cCadastro := STR0045 // "Movimentos de Planos de Manutencao"

If ( !Eof() )
	DEFINE MSDIALOG oDlg2 FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd
	@ 001,002 TO 031,267 OF oDlg2	PIXEL

	@ 007,005 SAY STR0004 SIZE 021,007 OF oDlg2 PIXEL //"C¢digo"
	@ 016,004 MSGET SA1->A1_COD When .F. SIZE 021,011 OF oDlg2 PIXEL

	@ 007,027 SAY STR0005 SIZE 016,007 OF oDlg2 PIXEL //"Loja"
	@ 016,027 MSGET SA1->A1_LOJA When .F. SIZE 016,011 OF oDlg2 PIXEL

	@ 007,046 SAY STR0006 SIZE 017,007 OF oDlg2 PIXEL //"Nome"
	@ 016,046 MSGET SA1->A1_NOME When .F. SIZE 107,011 OF oDlg2 PIXEL

	@ 007,157 SAY STR0047 			SIZE 065,007 OF oDlg2 PIXEL //"Produtos/Eqto"
	@ 016,157 MSGET QUERY9->AA3_CODPRO When .F. 	SIZE 065,011 OF oDlg2 PIXEL

	oGetDB2:=MsGetDB():New(035,002,134,315,2,,,,.T.,,,.F.,,cAlias)
	DEFINE SBUTTON 		FROM 005,280 TYPE 1  ENABLE OF oDlg2 ACTION ( oDlg2:End() )
	DEFINE SBUTTON oBtn 	FROM 020,280 TYPE 15 ENABLE OF oDlg2
	oBtn:lAutDisable := .F.
	If ( bVisual <> Nil )
		oBtn:bAction              := bVisual
	Else
		oBtn:SetDisable(.T.)
	EndIf
	ACTIVATE MSDIALOG oDlg2
Else
	Help(" ",1,"REGNOIS")
EndIf

dbSelectArea( "QUERY11" )
dbCloseArea()

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcPenden ³ Autor ³ Sergio Silveira       ³ Data ³ 16/11/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Exibir as pendencias para o equipamento                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtcPenden()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1: Array com aHeaders reutilizaveis                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³30/01/2007³ Conrado Q.    ³Bops 155755: Montagem do aCols e aHeader      ³±±
±±³          ³               ³através da rotina FillGetDb.                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtcPenden( aSavHeaders )

Local aFErase			:= {}
Local aRecnos			:= {}
Local bVisual			:= { || AtcButPen( @oGetDB2, aRecnos ) }
Local cAlias			:= "QUERY12"
Local oGetDB2			:= Nil
Local oDlg2			:= Nil
Local oBtn				:= Nil
Local cSeek			:= ""		// Procura inicial da FillGetDb
Local cWhile			:= ""		// Condição da FillGetDb
Local bCond			:= {||}	// Condição para a adição de registros
Local cQuery			:= Nil		// Caso for TOP, é a query para a busca dos registros
Local bMountFile		:= Nil		// Se for um caso especial, utiliza o esse bloco para montar o arquivo de trabalho
Local nIdxOrd			:= 0		// Ordem da FillGetDb
Local bAfterHeader	:= {||}	// Bloco executado após a criação do aHeader e do aEstruQuery na FillGetDb

Private aHeader  := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Plano de Manutencao                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cSeek		:=	xFilial("ABD") + QUERY10->AA3_CODFAB + QUERY10->AA3_LOJAFA + ;
              QUERY10->AA3_CODPRO + QUERY10->AA3_NUMSER
cWhile		:= "ABD->ABD_FILIAL + ABD->ABD_CODFAB + ABD->ABD_LOJAFA + ABD->ABD_CODPRO + ABD->ABD_NUMSER"
bCond		:= {|| ( aAdd( aRecnos, ABD->( Recno() ) ), .T. ) }
cQuery		:= Nil
bMountFile	:= Nil
nIdxOrd	:= 1

AtcMonta(12,     aSavHeaders, "ABD",  cSeek,;
         cWhile, bCond,       cQuery, bMountFile,;
         nIdxOrd )

dbSelectArea( "QUERY12" )
QUERY12->( dbGotop() )

cCadastro := STR0046 // "Pendencias do Equipamento"

If ( !Eof() )
	DEFINE MSDIALOG oDlg2 FROM	09,0 TO 28,80 TITLE cCadastro OF oMainWnd
	@ 001,002 TO 031,267 OF oDlg2	PIXEL

	@ 007,005 SAY STR0004 SIZE 021,007 OF oDlg2 PIXEL //"C¢digo"
	@ 016,004 MSGET SA1->A1_COD When .F. SIZE 021,011 OF oDlg2 PIXEL

	@ 007,027 SAY STR0005 SIZE 016,007 OF oDlg2 PIXEL //"Loja"
	@ 016,027 MSGET SA1->A1_LOJA When .F. SIZE 016,011 OF oDlg2 PIXEL

	@ 007,046 SAY STR0006 SIZE 017,007 OF oDlg2 PIXEL //"Nome"
	@ 016,046 MSGET SA1->A1_NOME When .F. SIZE 107,011 OF oDlg2 PIXEL

	@ 007,157 SAY STR0047 SIZE 065,007 OF oDlg2 PIXEL //"Produtos/Eqto"
	@ 016,157 MSGET QUERY10->AA3_CODPRO When .F. 	SIZE 065,011 OF oDlg2 PIXEL

	oGetDB2:=MsGetDB():New(035,002,134,315,2,,,,.T.,,,.F.,,cAlias)
	DEFINE SBUTTON 		FROM 005,280 TYPE 1  ENABLE OF oDlg2 ACTION ( oDlg2:End() )
	DEFINE SBUTTON oBtn 	FROM 020,280 TYPE 15 ENABLE OF oDlg2
	oBtn:lAutDisable := .F.
	If ( bVisual <> Nil )
		oBtn:bAction := bVisual
	Else
		oBtn:SetDisable(.T.)
	EndIf
	ACTIVATE MSDIALOG oDlg2
Else
	Help(" ",1,"REGNOIS")
EndIf

dbSelectArea( "QUERY12" )
dbCloseArea()

Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcButton ³ Autor ³ Sergio Silveira       ³ Data ³ 12/11/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Exibir o item do plano de manutencao selecionado              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³AtcButton( ExpO1 )                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 : Objeto GetDB                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtcButton( oGetDB2 )

Local cCadBak

AA9->( dbSetOrder( 1 ) )
If AA9->( dbSeek( xFilial("AA9") + QUERY11->ABE_PLANO + QUERY11->ABE_ITEM ) )
	cCadBak   := cCadastro
	cCadastro := STR0044 // Item do Plano de Manutencao
	AxVisual( "AA9", AA9->( Recno() ), 1 )
	cCadastro := cCadBak
	oGetDB2:oBrowse:Refresh()
EndIf
Return( Nil )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AtcButPen ³ Autor ³ Sergio Silveira       ³ Data ³ 17/11/1999 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Exibir o item do plano de manutencao selecionado             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ AtcButPen( ExpO1, ExpA2 )                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1 : Objeto GetDB                                         ³±±
±±³          ³ ExpA2 : Array contendo os recnos da tabela ABD               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function AtcButPen( oGetDB2, aRecnosABD )

Local cCadBak := ""

ABD->( MsGoto( aRecnosABD[ QUERY12->( Recno() ) ] ) )

cCadBak   := cCadastro
cCadastro := STR0051 // PENDENCIA
AxVisual( "ABD", ABD->( Recno() ), 1 )
cCadastro := cCadBak

oGetDB2:oBrowse:Refresh()
Return( Nil )
                         
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCMonta ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 23/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Montar aHeader e o Arq. Trab. através da FillGetDb           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCMonta( ExpN1, ExpA2,  ExpC3, ExpC4, ExpC5, ExpB6, ExpC7   ³±±
±±³          ³           ExpB8, ExpN9, ExpB10 )                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Caso da consulta                                     ³±±
±±³          ³ ExpA2 : aHeaders reutilizáveis                               ³±±
±±³          ³ ExpC3 : Alias da tabela principal                            ³±±
±±³          ³ ExpC4 : Expressão utilizada no seek do FillGetDb             ³±±
±±³          ³ ExpC5 : Expressão utilizada no while do FillGetDb            ³±±
±±³          ³ ExpB6 : Bloco com a condição para a montagem do item         ³±±
±±³          ³ ExpC7 : Query quando é TOP Connect                           ³±±
±±³          ³ ExpB8 : Bloco de código alternativo para a montagem do arq.  ³±±
±±³          ³         trab.                                                ³±±
±±³          ³ ExpN9 : Ordem do cAlias                                      ³±±
±±³          ³ ExpB10: Bloco executado após a montagem do aHeader           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCMonta(nCaso,   aSavHeaders, cAlias, cSeek,;
                         cWhile,  bCond,       cQuery, bMountFile,;
                         nIdxOrd, bAfterHeader)
Local bAction1	:= {||.T.}
Local bAction2	:= {||.F.}
Local aNoFields	:= Nil
Local aCpoNCopia	:= Nil
Local aCpoVirtual	:= Nil
Local aNoHeader  := {}
Local cTempAlias := "QUERY" + Alltrim(Str(nCaso))
Local aStruField := {}
Local cField     := ""
Local nC         := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se utilizar query para filtrar os registros eh necessário retirar todos³
//³os campos do tipo MEMO do aHeader devido a funcção FillGetDb           ³
//³ingnorar a query quando existe algum campo memo na estrutura da        ³
//³tabela.                                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Type("aSavHeaders") == "U" .And. !Empty(cQuery) .And. !Empty(cAlias)
	aStruField := FWSX3Util():GetAllFields(cAlias)
	If Len(aStruField) > 0
		For nC := 1 to Len(aStruField )
			cField := aStruField[nC]
			If GetSx3Cache(cField,"X3_TIPO") == "M"
				aadd(aNoHeader,AllTrim(cField))
			EndIf
		Next nC
	EndIf
EndIf
		
If ( Select( cTempAlias )==0 )
	FillGetDb(2,           cAlias,         cTempAlias,    /*cKeyInd*/,;
	          nIdxOrd,     cSeek,          {|| &cWhile},  {{bCond,bAction1,bAction2}},;
	          aNoFields,   /*aYesFields*/, /*lOnlyYes*/,  cQuery,;
	          bMountFile,  aNoHeader,      .F.,           aCpoNCopia,;
	          aCpoVirtual, bAfterHeader,   /*bAfterRec*/, /*bBeforeRec*/)
	aAdd( aSavHeaders, { cTempAlias, aClone(aHeader) } )
Else
	If aScan( aSavHeaders, { |x| x[1]==cTempAlias } ) > 0
		aHeader := aClone( aSavHeaders[ aScan( aSavHeaders, { |x| x[1]==cTempAlias } ) ][2] )
	EndIf
Endif
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCbCond ³ Autor ³ Conrado Q. Gomes      ³ Data ³ 23/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Função com a condição de adição do item na FillGetDb         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCbCond( ExpN1 )                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Caso da consulta                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCbCond( nCaso )
Local lRet		:= .F.
Local cAlias	:= ""

Do Case
	Case nCaso == 2
		If ( !Empty(AA3->AA3_CONTRT) )
			dbSelectArea("AAH")
			dbSetOrder(1)
			If ( dbSeek(xFilial("AAH")+AA3->AA3_CONTRT) )
				If ( dDataBase >= AAH->AAH_INIVLD )
					lRet := .T.
				EndIf
			EndIf
		EndIf
		dbSelectArea("AA3")
	Case nCaso == 8
		cAlias := Alias()
		If ( (cAlias)->AB8_DTGAR >= dDataBase .AND. (cAlias)->AB8_TIPO $ "24" ) //Gerou PV ou Atendido
			lRet := .T.
		EndIf
EndCase

Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCbMountFile ³ Autor ³ Conrado Q. Gomes ³ Data ³ 23/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Função para montagem do arquivo de trabalho em casos espec.  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCbMountFile( ExpN1, ExpA2 )                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpN1 : Caso da consulta                                     ³±±
±±³          ³ ExpA2 : Arquivos que devem ser apagados posteriormente       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCbMountFile(nCaso, aFerase, oGsTmpTable)

Local lRet			:= .F.	// Retorno da função
Local lTmpOK		:= .F.	// Define se houve problemas na utilização da tabela temporária
Local nCntFor		:= 0	// Contador
Local cArqQuery	:= ""	// Nome do arquivo de trabalho
Local aStruQuery	:= {}	// Estrutura do arquivo de trabalho
Local aIdx			:= {}	// Índices p/ a tabela temporária
Local aAuxIdx		:= {}
Local aInsertTmp	:= {}	// Conteúdo a ser preparado para a gravação na tabela temporária
Local nStpCmmIns	:= 500	// Quantidade do lote de registros a serem gravados na tabela temporária a cada INSERT do objeto GsTmpTable 
Local cQuery		:= ""	// Query de consulta
Local cAliasTemp	:= ""
Local cAlias_WT	:= ""
Local cAuxQuery	:= ""
Local cFldFlag	:= ""

Default oGsTmpTable := Nil

If	ValType(oGsTmpTable) == "O"
	oGsTmpTable:Close()
	TecDestroy(oGsTmpTable)
EndIf

If	nCaso == 4
	cAuxQuery	:= ",AB2."
	cFldFlag	:= "AB2_FLAG"
	aAuxIdx	:= {"AB2_NRCHAM","AB2_ITEM"}
	cAlias_WT	:= "AB2"
	cAliasTemp	:= "QUERY4"

ElseIf nCaso == 5
	cAuxQuery	:= ",AB4."
	cFldFlag	:= "AB4_FLAG"
	aAuxIdx	:= {"AB4_NUMORC","AB4_ITEM"}
	cAlias_WT	:= "AB4"
	cAliasTemp	:= "QUERY5"

ElseIf nCaso == 6
	cAuxQuery	:= ",AB7."
	cFldFlag	:= "AB7_FLAG"
	aAuxIdx	:= {"AB7_NUMOS","AB7_ITEM"}
	cAlias_WT	:= "AB7"
	cAliasTemp	:= "QUERY6"

EndIf

For nCntFor := 1 To Len( aHeader )
	If ( aHeader[nCntFor][10] <> "V" )
		aAdd(aStruQuery, {aHeader[nCntFor][2],;
		                  aHeader[nCntFor][8],;
		                  aHeader[nCntFor][4],;
		                  aHeader[nCntFor][5] } )
		If !IsHeadAlias( aHeader[nCntFor][2] ) .AND. !IsHeadRec( aHeader[nCntFor][2] )
			cQuery += cAuxQuery + aHeader[nCntFor][2]
		Endif
	EndIf
Next nCntFor
aAdd(aStruQuery, {cFldFlag, "L", 1, 0})

If	nCaso == 4
	cQuery	:= "SELECT "+SubStr(cQuery,2)
	cQuery	+=  " FROM "+RetSqlName("AB2")+" AB2, "
	cQuery	+=           RetSqlName("AB1")+" AB1 "
	cQuery	+= " WHERE AB1.AB1_FILIAL = '"+xFilial("AB1")+"' "
	cQuery	+=    "AND AB1.AB1_CODCLI = '"+SA1->A1_COD+"' "
	cQuery	+=    "AND AB1.AB1_LOJA   = '"+SA1->A1_LOJA+"' "
	cQuery	+=    "AND AB1.AB1_EMISSA >= '"+Dtos(MV_PAR01)+"' "
	cQuery	+=    "AND AB1.AB1_EMISSA <= '"+Dtos(MV_PAR02)+"' "
	cQuery	+=    "AND AB1.D_E_L_E_T_ = ' ' "
	cQuery	+=    "AND AB2.AB2_FILIAL = '"+xFilial("AB2")+"' "
	cQuery	+=    "AND AB2.AB2_NRCHAM = AB1.AB1_NRCHAM "
	cQuery	+=    "AND AB2.D_E_L_E_T_ = ' '"

ElseIf	nCaso == 5
	AB3->( dbSetOrder( 1 ) )
	cQuery := "SELECT "+SubStr(cQuery,2)
	cQuery +=  " FROM "+RetSqlName("AB3")+" AB3, "
	cQuery +=           RetSqlName("AB4")+" AB4 "
	cQuery += " WHERE AB3.AB3_FILIAL = '"+xFilial('AB3')+"' "
	cQuery +=    "AND AB3.AB3_EMISSA >= '"+Dtos(MV_PAR01)+"' "
	cQuery +=    "AND AB3.AB3_EMISSA <= '"+Dtos(MV_PAR02)+"' "
	cQuery +=    "AND AB3.AB3_CODCLI = '"+SA1->A1_COD+"' "
	cQuery +=    "AND AB3.AB3_LOJA   = '"+SA1->A1_LOJA+"' "
	cQuery +=    "AND AB3.D_E_L_E_T_ = ' ' "
	cQuery +=    "AND AB4.AB4_FILIAL = '"+xFilial('AB4')+"' "
	cQuery +=    "AND AB4.AB4_NUMORC = AB3.AB3_NUMORC "
	cQuery +=    "AND AB4.D_E_L_E_T_ = ' ' "
	cQuery += " ORDER BY "+StrTran(AB3->(IndexKey()),"+",",")

ElseIf	nCaso == 6
	cQuery := "SELECT "+SubStr(cQuery,2)
	cQuery +=  " FROM "+RetSqlName("AB6")+" AB6, "
	cQuery +=           RetSqlName("AB7")+" AB7 "
	cQuery += " WHERE AB6.AB6_FILIAL = '"+xFilial("AB6")+"' "
	cQuery +=    "AND AB6.AB6_CODCLI = '"+SA1->A1_COD+"' "
	cQuery +=    "AND AB6.AB6_LOJA   = '"+SA1->A1_LOJA+"' "
	cQuery +=    "AND AB6.AB6_EMISSA >= '"+Dtos(MV_PAR01)+"' "
	cQuery +=    "AND AB6.AB6_EMISSA <= '"+Dtos(MV_PAR02)+"' "
	cQuery +=    "AND AB6.D_E_L_E_T_ = ' ' "
	cQuery +=    "AND AB7.AB7_FILIAL = '"+xFilial("AB7")+"' "
	cQuery +=    "AND AB7.AB7_NUMOS  = AB6.AB6_NUMOS "
	cQuery +=    "AND AB7.D_E_L_E_T_ = ' '"

EndIf

cQuery := ChangeQuery(cQuery)
dbUseArea( .T., "TOPCONN", TCGENQRY(,,cQuery),"TOPQRY", .F., .T.)

// Acerta os tipos de dados da Query
For nCntFor := 1 To Len( aStruQuery )
	If aStruQuery[nCntFor,2] <> "C" .And. AllTrim(aStruQuery[nCntFor,1]) <> cFldFlag
		TCSetField("TOPQRY", AllTrim(aStruQuery[nCntFor,1]), aStruQuery[nCntFor,2], aStruQuery[nCntFor,3], aStruQuery[nCntFor,4])
	EndIf
Next nCntFor

aIdx			:= {{"INDICE1", aAuxIdx}}
oGsTmpTable	:= GSTmpTable():New(cAliasTemp /*cArea*/,;
                                  aStruQuery /*aStruct*/,;
                                  aIdx /*aIndex*/,;
                                  {} /*aInitPad*/,;
                                  nStpCmmIns /*nStepCommitInsert*/)
If	( lTmpOK := oGsTmpTable:CreateTMPTable() )
	While ! TOPQRY->( Eof() )
		aInsertTmp	:= {}
		For nCntFor := 1 To Len(aStruQuery)
			If IsHeadAlias( aStruQuery[nCntFor][1] )
				aAdd(aInsertTmp, {aStruQuery[nCntFor][1], cAlias_WT})
			ElseIf IsHeadRec( aStruQuery[nCntFor][1] )
				aAdd(aInsertTmp, {aStruQuery[nCntFor][1], TOPQRY->(Recno())})
			ElseIf TOPQRY->(FieldPos(aStruQuery[nCntFor][1])) <> 0
				aAdd(aInsertTmp, {aStruQuery[nCntFor][1], TOPQRY->(FieldGet(FieldPos(aStruQuery[nCntFor][1])))})
			EndIf
		Next nCntFor
		If !( lTmpOK := (oGsTmpTable:Insert(aInsertTmp) .And. oGsTmpTable:Commit()))
			lRet		:= .F.
			EXIT
		EndIf
		TOPQRY->(dBSkip())
	EndDo
EndIf

TOPQRY->(dBCloseArea())

If	ValType(oGsTmpTable) == "O"
	If	oGsTmpTable:GetProp("ERROR")
		oGsTmpTable:ShowErro()
	EndIf
EndIf
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³ ATCbAfterHeader ³ Autor ³ Conrado Q.     ³ Data ³ 30/01/2007 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Função para adicionar campos no aHeader da FillGetDb         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ATCbAfterHeader( ExpC1, ExpA2, ExpA3, ExpA4 )                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nil                                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 : Alias da tabela                                      ³±±
±±³          ³ ExpA2 : Campos que serão adicionados ao final da aHeader     ³±±
±±³          ³ ExpA3 : aHeader da FillGetDb                                 ³±±
±±³          ³ ExpA4 : aStruQuery da FillGetDb                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                              ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ATCbAfterHeader( cAlias, aCampos, aHeader, aStruQuery )
Local aArea      := GetArea()	// Pega a área atual
Local nScan      := 0			// Posição do campo na array aCampos
Local aRet       := {}
Local aStruField := {}
Local cField     := ""
Local nC         := 0

aStruField := FWSX3Util():GetAllFields(cAlias)
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField )
		cField := aStruField[nC]
		aRet := FwTamSx3(cField)
		// Procura no aCampos algum campo com o mesmo título que o do registro atual
		If ( (nScan := aScan( aCampos, { |x| x == Alltrim(cField) } ) ) > 0 )
			// Procura no aStruQuery se o campo encontrado já existe
			If ( aScan( aStruQuery, { |x| Alltrim(x[1]) == aCampos[nScan] } ) == 0 )
				aadd(aStruQuery,{cField,;
								aRet[3],;
								aRet[1],;
								aRet[2]})
			EndIf
			// Procura no aHeader se o campo encontrado já existe
			If ( aScan( aHeader, { |x| Alltrim(x[2]) == aCampos[nScan] } ) == 0 )
					aadd(aHeader,{AllTrim(GetSx3Cache(cField, "X3_TITULO")),;
						cField,;
						AllTrim(GetSx3Cache(cField, "X3_PICTURE")),;
						aRet[1],;
						aRet[2],;
						GetSx3Cache(cField, "X3_VALID"),;
						GetSx3Cache(cField, "X3_USADO"),;
						aRet[3],;
						cAlias,;
						GetSx3Cache(cField, "X3_CONTEXT")})
				

			EndIf
		EndIf
	Next nC
EndIf

RestArea( aArea )
Return( .T. )
