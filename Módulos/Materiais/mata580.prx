#INCLUDE "MATA580.CH"
#INCLUDE "PROTHEUS.CH"

STATIC __oSC0Tmp := NIL
STATIC __oSC9Tmp := NIL
STATIC __oAIDTmp := NIL
STATIC __oSC5Tmp := NIL
STATIC __oSC6Tmp := NIL
STATIC __oSF2Tmp := NIL
STATIC __oSD2Tmp := NIL
STATIC __oSCJTmp := NIL
STATIC __oSCKTmp := NIL
STATIC __oSCLTmp := NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Mata580  ³ Autor ³ Eduardo Riera         ³ Data ³06.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Limpeza de Arquivos do modulo de Faturamento               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Mata580()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFAT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Mata580()

Local nOpcA 	:= 0
Local dLimite   := dDataBase
Local cDiretorio:= ""
Local lExclusivo:= .F.
Local lContinua := .T.
Local lNFS		:= .F.
Local lLPV		:= .F.
Local lPV       := .F.
Local lOrc      := .F.
      
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³                     Parametros                               ³
//³                                                              ³
//³MV_PAR01: Exclui Notas Fiscais? SF2/SD2                       ³
//³MV_PAR02: Exclui Itens de Liberacao do PV? SC9                ³
//³MV_PAR03: Exclui Pedidos de Venda? SC5/SC6                    ³
//³MV_PAR04: Abre aquivos exclusivos ?                           ³
//³MV_PAR05: Data Limite para Limpeza                            ³
//³MV_PAR06: Diretorio dos arquivos de backup                    ³
//³MV_PAR07: Exclui Orçamentos de Venda                          ³
//³                                                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Pergunte("MTA580",.F.)

FormBatch(OemToAnsi(STR0001),; //"Limpeza Mensal"
							{OemToAnsi(STR0002),;	//"Este programa tem o objetivo de efetuar a limpeza de arquivos do m¢dulo SIGAFAT."
							OemToAnsi(STR0003),;	//"Dever  ser informada a data limite para limpeza.  Os registros deletados ser„o"
							OemToAnsi(STR0004),;	//"armazenados em um jogo de arquivos a serem gerados pelo programa."
							OemToAnsi(STR0005)},;	//"Fa‡a um BACKUP antes de executar esta rotina"
							{{5,.T.,{|o| Pergunte("MTA580",.T.)}},;
							{1,.T.,{|o| nOpcA:=1,o:oWnd:End()}},;
							{2,.T.,{|o| o:oWnd:End()}}})							
If ( nOpcA == 1 )
	Ma580Param(lNFS,lLPV,lPV,lOrc,lExclusivo,dLimite,cDiretorio,lContinua)
EndIf
									

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580Proc ³ Autor ³Eduardo Riera          ³ Data ³06.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Gauge de Processamento da Limpeza Mensal                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica se havera processamento das SF2/SD2           ³±±
±±³          ³ExpL2: Indica se havera processamento do SC9                ³±±
±±³          ³ExpL3: Indica se havera processamento do SC5/SC6/SC9        ³±±
±±³          ³ExpL4: Indica se havera processamento do SCJ/SCK/SCL        ³±±
±±³          ³ExpL5: Indica o modo de Abertura                            ³±±
±±³          ³ExpD6: Data Limite para limpeza                             ³±±
±±³          ³ExpC7: Diretorio onde os arquivo serao gravados             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580Proc(oSelf,lNFS,lLPV,lPV,lOrc,lExclusivo,DLimite,cDiretorio,lContinua)

TcInternal(5,"*OFF")	

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo morto e deleta os registros SC0       			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Ma580SC0(lExclusivo,DLimite,cDiretorio,"01/05")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo morto e deleta os registros SC9       			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lLPV .Or. lPV)
	Ma580SC9(lExclusivo,DLimite,cDiretorio,"02/05")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo morto e deleta os registros AID/SC5/SC6/SC9     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lPV )
	Ma580SC5(lExclusivo,DLimite,cDiretorio,"03/05")
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo morto e deleta os registros SF2/SD2/SCN         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lNFS )
	Ma580SF2(lExclusivo,DLimite,cDiretorio,"04/05")
	If cPaisLoc!="BRA"
	   Ma580SCN(lExclusivo,DLimite,cDiretorio,"")
	Endif   
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gera arquivo morto e deleta os registros SCJ/SCK/SCL         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lOrc )
	Ma580SCJ(lExclusivo,DLimite,cDiretorio,"05/05")
Endif

TcInternal(5,"*ON")	

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SC0  ³ Autor ³Eduardo Riera          ³ Data ³06.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza das Reservas para Faturamento.                      ³±±
±±³          ³A Limpeza das reservas deve ser efetuada com base na Data   ³±±
±±³          ³de Validade da mesma, ou seja, somente as vencidas podem    ³±±
±±³          ³ser apagadas.                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SC0(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea		:= GetArea()
Local aStru     := SC0->(DBStruct())
Local cNome    	:= "C0"+SubStr(Dtos(dLimite),3)+".csv"
Local cCampo   	:= ""
Local cTempTable:= GetNextAlias()
Local cAliasQry	:= "MA580SC0"
Local cQuery   	:= ""
Local cFile		:= ""
Local nX       	:= 0
Local nY       	:= 0
Local aIndexSC0	:= {}
Local cRealName := ""

ProcRegua(SC0->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If __oSC0Tmp <> NIL
	cTempTable := __oSC0Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTempTable) == 0
		__oSC0Tmp:Delete()
		FreeObj(__oSC0Tmp)
	EndIf
EndIf

If __oSC0Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSC0Tmp := FWTemporaryTable():New( cTempTable )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSC0Tmp:SetFields( aStru )
	SC0->( DBSetOrder(1) )  
	aIndexSC0 := StrTokArr( SC0->( SqlOrder( IndexKey() ) ) , "," )  
	__oSC0Tmp:AddIndex("1",aIndexSC0)

	//------------------
	//Criação da tabela
	//------------------
	__oSC0Tmp:Create()
Else
	cRealName := __oSC0Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStru := __oSC0Tmp:GetStruct():GetFields()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := "SELECT SC0.*,SC0.R_E_C_N_O_ SC0RECNO "
cQuery += "FROM "+RetSqlName("SC0")+" SC0 "
cQuery += "WHERE SC0.C0_FILIAL='"+xFilial("SC0")+"' AND "
cQuery += "SC0.C0_VALIDA<='"+Dtos(dLimite)+"' AND "
cQuery += "SC0.C0_QTDPED<=0 AND "
cQuery += "SC0.D_E_L_E_T_ = ' ' "

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)

For nX := 1 To Len(aStru)
	If ( aStru[nX][2]!="C" )
		TcSetField(cAliasQry,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
	EndIf
Next nX

While ( (cAliasQry)->( !Eof() ) .And. xFilial("SC0")==(cAliasQry)->C0_FILIAL )

	If ( (cAliasQry)->C0_VALIDA <= dLimite .And. (cAliasQry)->C0_QTDPED<=0 )
	
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o Arquivo Morto                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SC0->(MsGoto((cAliasQry)->SC0RECNO))
		RecLock(cTempTable,.T.)
		For nX := 1 To FCount()
			cCampo := FieldName(nX)
			FieldPut(nX,SC0->(FieldGet(FieldPos(cCampo))))
		Next nX
		MsUnLock()
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Exclui a Reserva                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
		a430Reserva({3,(cAliasQry)->C0_TIPO,(cAliasQry)->C0_DOCRES,(cAliasQry)->C0_SOLICIT,(cAliasQry)->C0_FILRES},;
			(cAliasQry)->C0_NUM,;
			(cAliasQry)->C0_PRODUTO,;
			(cAliasQry)->C0_LOCAL,;
			(cAliasQry)->C0_QUANT,;
			{(cAliasQry)->C0_NUMLOTE,;
			(cAliasQry)->C0_LOTECTL,;
			(cAliasQry)->C0_LOCALIZ,;
			(cAliasQry)->C0_NUMSERI})

	EndIf
	
	IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"
	
	(cAliasQry)->( DBSkip() )
		
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasQry)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------
DBSelectArea(cTempTable)

(cTempTable)->( DBGoTop() )

If (cTempTable)->( !Eof() )
	IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNome
	Copy To &cFile Delimited
EndIf

DBSelectArea("SC0")

If lExclusivo 

	IncProc(STR0007+" - SC0",cTexto) //"Otimizando"

	nX := 0
	nY := SC0->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SC0")+" "
	cQuery += "WHERE C0_FILIAL='"+xFilial("SC0")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo		

EndIf

RestArea( aArea )

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SC9  ³ Autor ³Eduardo Riera          ³ Data ³06.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza da Liberacao do Pedido de Venda.                    ³±±
±±³          ³A Limpeza da Liberacao do Pedido de Venda deve ser efetuada ³±±
±±³          ³somente dos itens ja faturados.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SC9(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea     	:= GetArea()
Local aStru     	:= SC9->(DBStruct())
Local cNome     	:= "C9"+SubStr(Dtos(dLimite),3)+".csv"
Local cCampo    	:= ""
Local cAliasQry		:= "MA580SC9"
Local cTempTable	:= GetNextAlias()
Local cQuery    	:= ""
Local cFile			:= ""
Local lDeleta   	:= .F.
Local nX        	:= 0
Local nY       		:= 0
Local aIndexSC9		:= {}
Local cRealName 	:= ""

ProcRegua(SC9->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If __oSC9Tmp <> NIL
	cTempTable := __oSC9Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTempTable) == 0
		__oSC9Tmp:Delete()
		FreeObj(__oSC9Tmp)
	EndIf
EndIf

If __oSC9Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSC9Tmp := FWTemporaryTable():New( cTempTable )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSC9Tmp:SetFields( aStru )
	SC9->( DBSetOrder(1) )  
	aIndexSC9 := StrTokArr( SC9->( SqlOrder( IndexKey() ) ) , "," )  
	__oSC9Tmp:AddIndex("1",aIndexSC9)

	//------------------
	//Criação da tabela
	//------------------
	__oSC9Tmp:Create()
Else
	cRealName := __oSC9Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStru := __oSC9Tmp:GetStruct():GetFields()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := "SELECT SC9.*,SC9.R_E_C_N_O_ SC9RECNO "
cQuery += "FROM "+RetSqlName("SC9")+" SC9, "+RetSqlName("SC5")+" SC5 "
cQuery += "WHERE SC9.C9_FILIAL='"+xFilial("SC9")+"' AND "
cQuery += "SC9.D_E_L_E_T_ = ' ' AND "
cQuery += "SC5.C5_FILIAL='"+xFilial("SC5")+"' AND "
cQuery += "SC5.C5_NUM=SC9.C9_PEDIDO AND "
cQuery += "SC5.C5_EMISSAO<='"+Dtos(dLimite)+"' AND "
cQuery += "SC5.D_E_L_E_T_ = ' '"

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)

For nX := 1 To Len(aStru)
	If ( aStru[nX][2]!="C" )
		TcSetField(cAliasQry,aStru[nX][1],aStru[nX][2],aStru[nX][3],aStru[nX][4])
	EndIf
Next nX
		
SC5->( DBSetOrder(1) ) 

While ( (cAliasQry)->( !Eof() ) .And. xFilial("SC9")==(cAliasQry)->C9_FILIAL )

	If ( !Empty((cAliasQry)->C9_BLCRED) .Or. !Empty((cAliasQry)->C9_BLEST) )
	
		lDeleta := .T.
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Atualiza o Arquivo Morto                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SC9->(MsGoto((cAliasQry)->SC9RECNO))	
		If SC5->( MsSeek(xFilial("SC5")+(cAliasQry)->C9_PEDIDO) )
			If ( SC5->C5_EMISSAO > dLimite )
				lDeleta := .F.
				Exit
			EndIf
		EndIf
	
		If lDeleta
			RecLock(cTempTable,.T.)
			For nX := 1 To FCount()
				cCampo := FieldName(nX)
				FieldPut(nX,SC9->(FieldGet(FieldPos(cCampo))))
			Next nX
			MsUnLock()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Exclui a Liberacao do Pedido de Venda                         ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ	
			Begin Transaction
				A460Estorna()
			End Transaction
		EndIf
		
	EndIf
	
	IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"

	(cAliasQry)->( DBSkip() )
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasQry)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------
DBSelectArea(cTempTable)

(cTempTable)->( DBGoTop() )

If (cTempTable)->( !Eof() )
	IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNome
	Copy To &cFile Delimited
EndIf
	
DBSelectArea("SC9")

If lExclusivo 
 
	IncProc(STR0007+" - SC9",cTexto) //"Otimizando"
	
	nX := 0
	nY := SC9->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SC9")+" "
	cQuery += "WHERE C9_FILIAL='"+xFilial("SC9")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo		
	
EndIf

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SC5  ³ Autor ³Eduardo Riera          ³ Data ³07.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza dos Pedidos de Venda.                               ³±±
±±³          ³A Limpeza dos Pedidos de Venda somente podera ser efetuada  ³±±
±±³          ³se o mesmo estiver eliminado por residuo ou totalmente fatu-³±±
±±³          ³do.                                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SC5(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea     	:= GetArea()
Local aStruAID  	:= AID->(DBStruct())
Local aStruSC5  	:= SC5->(DBStruct())
Local aStruSC6  	:= SC6->(DBStruct())
Local cNomeAID  	:= "AID"+SubStr(Dtos(dLimite),3)+".csv"
Local cNomeSC5  	:= "C5" +SubStr(Dtos(dLimite),3)+".csv"
Local cNomeSC6  	:= "C6" +SubStr(Dtos(dLimite),3)+".csv"
Local cAliasSC5 	:= "MA580SC5"
Local cCampo    	:= ""
Local cQuery    	:= ""
Local cFile			:= ""
Local lDeleta   	:= .F.
Local nX        	:= 0
Local nY        	:= 0
Local cTmpTblAID	:= GetNextAlias()
Local aIndexAID		:= {}
Local cTmpTblSC5	:= GetNextAlias()
Local aIndexSC5		:= {}
Local cTmpTblSC6	:= GetNextAlias()
Local aIndexSC6		:= {}
Local cRealName 	:= ""

ProcRegua(SC5->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If __oAIDTmp <> NIL
	cTmpTblAID := __oAIDTmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblAID) == 0
		__oAIDTmp:Delete()
		FreeObj(__oAIDTmp)
	EndIf
EndIf

If __oAIDTmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oAIDTmp := FWTemporaryTable():New( cTmpTblAID )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oAIDTmp:SetFields( aStruAID )
	AID->( DBSetOrder(1) )  
	aIndexAID := StrTokArr( AID->( SqlOrder( IndexKey() ) ) , "," )  
	__oAIDTmp:AddIndex("1",aIndexAID)

	//------------------
	//Criação da tabela
	//------------------
	__oAIDTmp:Create()
Else
	cRealName := __oAIDTmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruAID := __oAIDTmp:GetStruct():GetFields()
EndIf

If __oSC5Tmp <> NIL
	cTmpTblSC5 := __oSC5Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSC5) == 0
		__oSC5Tmp:Delete()
		FreeObj(__oSC5Tmp)
	EndIf
EndIf

If __oSC5Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSC5Tmp := FWTemporaryTable():New( cTmpTblSC5 )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSC5Tmp:SetFields( aStruSC5 )
	SC5->( DBSetOrder(1) )  
	aIndexSC5 := StrTokArr( SC5->( SqlOrder( IndexKey() ) ) , "," )  
	__oSC5Tmp:AddIndex("1",aIndexSC5)

	//------------------
	//Criação da tabela
	//------------------
	__oSC5Tmp:Create()
Else
	cRealName := __oSC5Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSC5 := __oSC5Tmp:GetStruct():GetFields()
EndIf

If __oSC6Tmp <> NIL
	cTmpTblSC6 := __oSC6Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSC6) == 0
		__oSC6Tmp:Delete()
		FreeObj(__oSC6Tmp)
	EndIf
EndIf

If __oSC6Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSC6Tmp := FWTemporaryTable():New( cTmpTblSC6 )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSC6Tmp:SetFields( aStruSC6 )
	SC6->( DBSetOrder(1) )  
	aIndexSC6 := StrTokArr( SC6->( SqlOrder( IndexKey() ) ) , "," )  
	__oSC6Tmp:AddIndex("1",aIndexSC6)

	//------------------
	//Criação da tabela
	//------------------
	__oSC6Tmp:Create()
Else
	cRealName := __oSC6Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSC6 := __oSC6Tmp:GetStruct():GetFields()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery    := "SELECT SC5.*,SC5.R_E_C_N_O_ SC5RECNO "
cQuery    += "FROM "+RetSqlName("SC5")+" SC5,"
cQuery    += RetSqlName("SC6")+" SC6 "
cQuery    += "WHERE SC5.C5_FILIAL='"+xFilial("SC5")+"' AND "
cQuery    += "SC5.C5_EMISSAO<='"+Dtos(dLimite)+"' AND "
cQuery    += "SC5.D_E_L_E_T_ = ' ' AND "
cQuery    += "SC6.C6_FILIAL='"+xFilial("SC6")+"' AND "
cQuery    += "SC6.C6_NUM=SC5.C5_NUM AND "
cQuery    += "(SC6.C6_QTDENT>=SC6.C6_QTDVEN OR SC6.C6_BLQ='R ' ) AND "
cQuery    += "SC6.D_E_L_E_T_ = ' ' "
cQuery    += "ORDER BY "+SqlOrder(SC5->(IndexKey()))

cQuery    := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSC5,.T.,.T.)

For nX := 1 To Len(aStruSC5)
	If ( aStruSC5[nX][2]!="C" )
		TcSetField(cAliasSC5,aStruSC5[nX][1],aStruSC5[nX][2],aStruSC5[nX][3],aStruSC5[nX][4])
	EndIf
Next nX

//Seta Ordens das tabelas utilizadas no loop
SC6->( DBSetOrder(1) )
(cTmpTblSC5)->( DBSetOrder(1) )
AID->( DBSetOrder(1) )
		
While ( (cAliasSC5)->( !Eof() ) .And. xFilial("SC5")==(cAliasSC5)->C5_FILIAL .And.;
		(cAliasSC5)->C5_EMISSAO <= dLimite )
	
	lDeleta := .T.
	
	If ( lDeleta )
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Exclui o Pedido de Venda                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Begin Transaction
			
			SC5->( MsGoto( (cAliasSC5)->SC5RECNO ) )
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza o Arquivo Morto                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If (cTmpTblSC5)->( !MsSeek(xFilial("SC5")+(cAliasSC5)->C5_NUM) ) 
				RecLock(cTmpTblSC5,.T.)
				For nX := 1 To FCount()		
					cCampo := FieldName(nX)
					FieldPut(nX,SC5->(FieldGet(FieldPos(cCampo))))
				Next nX
				MsUnLock()
			EndIf
			
			SC6->( MsSeek(xFilial("SC6")+(cAliasSC5)->C5_NUM) )
			
			While ( SC6->( !Eof() ) .And. xFilial("SC6") == SC6->C6_FILIAL .And.;
					(cAliasSC5)->C5_NUM == SC6->C6_NUM )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza o Arquivo Morto                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock(cTmpTblSC6,.T.)
				For nX := 1 To FCount()
					cCampo := FieldName(nX)
					FieldPut(nX,SC6->(FieldGet(FieldPos(cCampo))))
				Next nX
				MsUnLock()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Exclui o SC6                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("SC6")
				SC6->( DBDelete() )
				MsUnLock()
				SC6->( DBSkip() )
			EndDo
			
			AID->( MsSeek(xFilial("AID")+(cAliasSC5)->C5_NUM) )
			
			While ( AID->( !Eof() ) .And. xFilial("AID") == AID->AID_FILIAL .And.;
					(cAliasSC5)->C5_NUM == AID->AID_NUMPV )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza o Arquivo Morto                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock(cTmpTblAID,.T.)
				For nX := 1 To FCount()
					cCampo := FieldName(nX)
					FieldPut(nX,AID->(FieldGet(FieldPos(cCampo))))
				Next nX
				MsUnLock()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Exclui o AID                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("AID")          
				AID->( DBDelete() )
				MsUnLock()
				AID->( DBSkip() )
			EndDo
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Exclui o SC5                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			RecLock("SC5")
			SC5->( DBDelete() )
			MsUnLock()
		End Transaction
		
	EndIf
	
	IncProc(STR0006+" "+cNomeSC5,cTexto) //"Atualizando Arquivo Morto"
	
	(cAliasSC5)->( DBSkip() )
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasSC5)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------
DBSelectArea(cTmpTblAID)

(cTmpTblAID)->( DBGoTop() )

If (cTmpTblAID)->( !Eof() )
	IncProc(STR0006+" "+cNomeAID,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeAID
	Copy To &cFile Delimited
EndIf

DBSelectArea(cTmpTblSC5)

(cTmpTblSC5)->( DBGoTop() )

If (cTmpTblSC5)->( !Eof() )
	IncProc(STR0006+" "+cNomeSC5,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSC5
	Copy To &cFile Delimited
EndIf

DBSelectArea(cTmpTblSC6)

(cTmpTblSC6)->( DBGoTop() )

If (cTmpTblSC6)->( !Eof() )
	IncProc(STR0006+" "+cNomeSC6,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSC6
	Copy To &cFile Delimited
EndIf

DBSelectArea("SC5")

If lExclusivo 
	
	IncProc(STR0007+" - AID",cTexto) //"Otimizando"
	nX := 0
	nY := AID->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("AID")+" "
	cQuery += "WHERE AID_FILIAL='"+xFilial("AID")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	IncProc(STR0007+" - SC5",cTexto) //"Otimizando"
	nX := 0
	nY := SC5->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SC5")+" "
	cQuery += "WHERE C5_FILIAL='"+xFilial("SC5")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	IncProc(STR0007+" - SC6",cTexto) //"Otimizando"
	nX := 0
	nY := SC6->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SC6")+" "
	cQuery += "WHERE C6_FILIAL='"+xFilial("SC6")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo

EndIf

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SF2  ³ Autor ³Eduardo Riera          ³ Data ³07.07.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza das Notas Fiscal de Venda                           ³±±
±±³          ³A Limpeza das notas somente podera ser efetuada se nao      ³±±
±±³          ³houver poder de terceiro nao estiver em aberto.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SF2(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea     	:= GetArea()
Local aStruSF2  	:= SF2->(DBStruct())
Local aStruSD2  	:= SD2->(DBStruct())
Local cNomeSF2  	:= "F2"+SubStr(Dtos(dLimite),3)+".csv"
Local cNomeSD2  	:= "D2"+SubStr(Dtos(dLimite),3)+".csv"
Local cAliasSF2 	:= "MA580SF2"
Local cAliasSD2 	:= "MTA580SD2"
Local cCampo    	:= "" 
Local cQuery    	:= ""
Local cFile			:= ""
Local lDeleta   	:= .F.
Local nX        	:= 0
Local nY        	:= 0
Local lMa580F2V		:= ExistBlock("MA580F2V")
Local cTmpTblSF2	:= GetNextAlias()
Local aIndexSF2		:= {}
Local cTmpTblSD2	:= GetNextAlias()
Local aIndexSD2		:= {}
Local cRealName 	:= ""

ProcRegua(SF2->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If __oSF2Tmp <> NIL
	cTmpTblSF2 := __oSF2Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSF2) == 0
		__oSF2Tmp:Delete()
		FreeObj(__oSF2Tmp)
	EndIf
EndIf

If __oSF2Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSF2Tmp := FWTemporaryTable():New( cTmpTblSF2 )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSF2Tmp:SetFields( aStruSF2 )
	SF2->( DBSetOrder(1) )  
	aIndexSF2 := StrTokArr( SF2->( SqlOrder( IndexKey() ) ) , "," )  
	__oSF2Tmp:AddIndex("1",aIndexSF2)

	//------------------
	//Criação da tabela
	//------------------
	__oSF2Tmp:Create()
Else
	cRealName := __oSF2Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSF2 := __oSF2Tmp:GetStruct():GetFields()
EndIf

If __oSD2Tmp <> NIL
	cTmpTblSD2 := __oSD2Tmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSD2) == 0
		__oSD2Tmp:Delete()
		FreeObj(__oSD2Tmp)
	EndIf
EndIf

If __oSD2Tmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSD2Tmp := FWTemporaryTable():New( cTmpTblSD2 )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSD2Tmp:SetFields( aStruSD2 )
	SD2->( DBSetOrder(1) )  
	aIndexSD2 := StrTokArr( SD2->( SqlOrder( IndexKey() ) ) , "," )  
	__oSD2Tmp:AddIndex("1",aIndexSD2)

	//------------------
	//Criação da tabela
	//------------------
	__oSD2Tmp:Create()
Else
	cRealName := __oSD2Tmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSD2 := __oSD2Tmp:GetStruct():GetFields()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery    := "SELECT SF2.*,SF2.R_E_C_N_O_ SF2RECNO "
cQuery    += "FROM "+RetSqlName("SF2")+" SF2 "
cQuery    += "WHERE SF2.F2_FILIAL='"+xFilial("SF2")+"' AND "
cQuery    += "SF2.F2_EMISSAO<='"+Dtos(dLimite)+"' AND "
cQuery    += "SF2.D_E_L_E_T_ = ' ' "

cQuery    := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSF2,.T.,.T.)

For nX := 1 To Len(aStruSF2)
	If ( aStruSF2[nX][2]!="C" )
		TcSetField(cAliasSF2,aStruSF2[nX][1],aStruSF2[nX][2],aStruSF2[nX][3],aStruSF2[nX][4])
	EndIf
Next nX

SD2->( DBSetOrder( 3 ) )
		
While ( (cAliasSF2)->( !Eof() ) .And. xFilial("SF2")==(cAliasSF2)->F2_FILIAL )
	If ( (cAliasSF2)->F2_EMISSAO <= dLimite )
		
		lDeleta := .T.
		
		cQuery := "SELECT * FROM "+RetSqlName("SD2")+" SD2 "
		cQuery += "WHERE SD2.D2_FILIAL='"+xFilial("SD2")+"' AND "
		cQuery += "SD2.D2_DOC='"+(cAliasSF2)->F2_DOC+"' AND "
		cQuery += "SD2.D2_SERIE='"+(cAliasSF2)->F2_SERIE+"' AND "
		cQuery += "SD2.D2_CLIENTE='"+(cAliasSF2)->F2_CLIENTE+"' AND "
		cQuery += "SD2.D2_LOJA='"+(cAliasSF2)->F2_LOJA+"' AND "
		cQuery += "SD2.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY "+SqlOrder(SD2->(IndexKey()))
		
		cQuery := ChangeQuery(cQuery)
		
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSD2,.T.,.T.)
		
		For nX := 1 To Len(aStruSD2)
			If ( aStruSD2[nX][2]!="C" )
				TcSetField(cAliasSD2,aStruSD2[nX][1],aStruSD2[nX][2],aStruSD2[nX][3],aStruSD2[nX][4])
			EndIf
		Next nX
		
		While ( !( cAliasSD2 )->( Eof() ) .And. xFilial("SD2") == (cAliasSD2)->D2_FILIAL .And.;
				(cAliasSF2)->F2_DOC == (cAliasSD2)->D2_DOC .And.; 
				(cAliasSF2)->F2_SERIE == (cAliasSD2)->D2_SERIE .And.; 
				(cAliasSF2)->F2_CLIENTE == (cAliasSD2)->D2_CLIENTE .And.; 
				(cAliasSF2)->F2_LOJA == (cAliasSD2)->D2_LOJA )
			If ( !Empty((cAliasSD2)->D2_IDENTB6) )
				If ( CalcTerc((cAliasSD2)->D2_COD,(cAliasSD2)->D2_CLIENTE,(cAliasSD2)->D2_LOJA,(cAliasSD2)->D2_IDENTB6,( cAliasSD2 )->D2_TES)[1] > 0 )
					lDeleta := .F.
					Exit
				EndIf
			EndIf
			( cAliasSD2 )->( 	DBSkip() ) 
		EndDo
	
		(cAliasSD2)->( DBCloseArea() )
		
		If lDeleta .And. lMa580F2V
			lDeleta := ExecBlock("MA580F2V",.F.,.F.,{(cAliasSF2)->F2_FILIAL, (cAliasSF2)->F2_CLIENTE, (cAliasSF2)->F2_LOJA, (cAliasSF2)->F2_DOC,;
				(cAliasSF2)->F2_SERIE, (cAliasSF2)->F2_TIPO, (cAliasSF2)->F2_ESPECIE})
		EndIf
		
		If ( lDeleta )
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Exclui o Pedido de Venda                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Begin Transaction
				
				SF2->( MsGoto( (cAliasSF2)->SF2RECNO ) )
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza o Arquivo Morto                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock(cTmpTblSF2,.T.)
				For nX := 1 To FCount()
					cCampo := FieldName(nX)
					FieldPut(nX,SF2->(FieldGet(FieldPos(cCampo))))
				Next nX
				MsUnLock()
				
				SD2->( MsSeek(xFilial("SD2")+(cAliasSF2)->F2_DOC+(cAliasSF2)->F2_SERIE+(cAliasSF2)->F2_CLIENTE+(cAliasSF2)->F2_LOJA) )
				
				While ( (cAliasSF2)->( !Eof() ) .And. xFilial("SD2") == SD2->D2_FILIAL .And.;
						 (cAliasSF2)->F2_DOC == SD2->D2_DOC .And.; 
						 (cAliasSF2)->F2_SERIE == SD2->D2_SERIE .And.; 
						 (cAliasSF2)->F2_CLIENTE == SD2->D2_CLIENTE .And.; 
						 (cAliasSF2)->F2_LOJA == SD2->D2_LOJA )
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Atualiza o Arquivo Morto                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RecLock(cTmpTblSD2,.T.)
					For nX := 1 To FCount()
						cCampo := FieldName(nX)
						FieldPut(nX,SD2->(FieldGet(FieldPos(cCampo))))
					Next nX
					MsUnLock()
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Exclui o SD2                                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RecLock("SD2")
					SD2->( DBDelete() )
					MsUnLock()
					SD2->( DBSkip() )
					
				EndDo
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Exclui o SF2                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
				RecLock("SF2")
				SF2->( DBDelete() )
				MsUnLock()
				
			End Transaction
		
		EndIf
		
	EndIf
	
	IncProc(STR0006+" "+cNomeSF2,cTexto) //"Atualizando Arquivo Morto"
	
	(cAliasSF2)->( DBSkip() )
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasSF2)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------

DBSelectArea(cTmpTblSF2)

(cTmpTblSF2)->( DBGoTop() )

If (cTmpTblSF2)->( !Eof() )
	IncProc(STR0006+" "+cNomeSF2,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSF2
	Copy To &cFile Delimited
EndIf


DBSelectArea(cTmpTblSD2)

(cTmpTblSD2)->( DBGoTop() )

If (cTmpTblSD2)->( !Eof() )
	IncProc(STR0006+" "+cNomeSD2,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSD2
	Copy To &cFile Delimited
EndIf

dbSelectArea("SF2")

If lExclusivo  

	IncProc(STR0007+" - SF2",cTexto) //"Otimizando"	
	nX := 0
	nY := SF2->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SF2")+" "
	cQuery += "WHERE F2_FILIAL='"+xFilial("SF2")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	IncProc(STR0007+" - SD2",cTexto) //"Otimizando"	
	nX := 0
	nY := SD2->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SD2")+" "
	cQuery += "WHERE D2_FILIAL='"+xFilial("SD2")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	
EndIf

RestArea(aArea)

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SCN  ³ Autor ³Marcello               ³ Data ³05.08.2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza do arquivo de remision.                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SCN(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea			:= GetArea()
Local aStruSCN		:= SCN->(DBStruct())
Local cNome			:= "CN"+SubStr(Dtos(dLimite),3)+".csv"
Local cCampo		:= ""
Local cAliasQry		:= "MA580SCN"
Local cQuery		:= ""
Local cFile			:= "" 
Local nX			:= 0
Local oTmpTblSCN	:= Nil
Local cTmpTblSCN	:= GetNextAlias()
Local aIndexSCN		:= {}
Local cFilSCN		:= xFilial("SCN")

ProcRegua(SCN->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//-------------------------------------------------------------------
// Criação do objeto  
//-------------------------------------------------------------------
oTmpTblSCN	:= FWTemporaryTable():New( cTmpTblSCN )

//-------------------------------------------------------------------
// Atribui campos e índices.  
//-------------------------------------------------------------------
oTmpTblSCN:SetFields( aStruSCN )
SCN->( DBSetOrder(1) )  
aIndexSCN := StrTokArr( SCN->( SqlOrder( IndexKey() ) ) , "," )  
oTmpTblSCN:AddIndex("1",aIndexSCN)

//------------------
//Criação da tabela
//------------------
oTmpTblSCN:Create()
	
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := "SELECT SCN.*,SCN.R_E_C_N_O_ SCNRECNO "
cQuery += "FROM "+RetSqlName("SCN")+" SCN "
cQuery += "WHERE SCN.CN_FILIAL = '"+cFilSCN+"' AND "
cQuery += "SCN.CN_EMISSAO <= '"+Dtos(dLimite)+"' AND "
cQuery += "SCN.D_E_L_E_T_ = ' ' "
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasQry,.T.,.T.)

For nX := 1 To Len(aStruSCN)
	If ( aStruSCN[nX][2]!="C" )
		TcSetField(cAliasQry,aStruSCN[nX][1],aStruSCN[nX][2],aStruSCN[nX][3],aStruSCN[nX][4])
	EndIf
Next nX
	
Begin Transaction
	While (cAliasQry)->( !Eof() )
      	
      	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	 	//³Atualiza o Arquivo Morto                                      ³
	   	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
		SCN->(MsGoto((cAliasQry)->SCNRECNO))

    	RecLock(cTmpTblSCN,.T.)
    	For nX := 1 To FCount()
			cCampo := FieldName(nX)
			FieldPut(nX,SCN->(FieldGet(FieldPos(cCampo))))
 	  	Next nX
      	MsUnLock()
    	
	    RecLock("SCN",.F.)
	    SCN->( DBDelete() )
	    MsUnlock()
 		
 		IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"
 		
    	(cAliasQry)->( DBSkip() )
    	
	EndDo
End Transaction

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasQry)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------
DBSelectArea(cTmpTblSCN)
(cTmpTblSCN)->( DBGoTop() )
If (cTmpTblSCN)->( !Eof() )
	IncProc(STR0006+" "+cNome,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNome
	Copy To &cFile Delimited
EndIf

//------------------------------
//Deleta as tabelas temporaria
//------------------------------ 
oTmpTblSCN:Delete()

DBSelectArea("SCN")
If lExclusivo 
	IncProc(STR0007+" - SCN",cTexto) //"Otimizando"
	cQuery := "DELETE FROM "+RetSqlName("SCN")+" "
	cQuery += "WHERE CN_FILIAL = '"+cFilSCN+"' AND "
	cQuery += "CN_EMISSAO <= '"+Dtos(dLimite)+"' AND "
	cQuery += "D_E_L_E_T_ = '*'"
	TcSqlExec(cQuery)
EndIf

RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma580SCJ  ³ Autor ³Eduardo Riera          ³ Data ³31.10.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Limpeza dos Orçamentos de Venda.                            ³±±
±±³          ³A Limpeza dos Orcamentos de venda somente podera ser efetua-³±±
±±³          ³da se o mesmo não estiver com os status A ou D e caso esteja³±±
±±³          ³efetivado o mesmo será excluido se não houver mais o PV.    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1: Indica o modo de Abertura                            ³±±
±±³          ³ExpD2: Data Limite para limpeza                             ³±±
±±³          ³ExpC3: Diretorio onde os arquivo serao gravados             ³±±
±±³          ³ExpC4: Texto da Mensagem                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580SCJ(lExclusivo,DLimite,cDiretorio,cTexto)

Local aArea     	:= GetArea()
Local aStruSCJ  	:= SCJ->(DBStruct())
Local aStruSCK  	:= SCK->(DBStruct())
Local aStruSCL  	:= SCL->(DBStruct())
Local cNomeSCJ  	:= "CJ"+SubStr(Dtos(dLimite),3)+".csv"
Local cNomeSCK  	:= "CK"+SubStr(Dtos(dLimite),3)+".csv"
Local cNomeSCL  	:= "CL"+SubStr(Dtos(dLimite),3)+".csv"
Local cAliasSCJ 	:= "MA580SCJ"
Local cCampo    	:= ""
Local cQuery    	:= ""
Local cFile			:= ""
Local lDeleta   	:= .F.
Local nX        	:= 0
Local nY        	:= 0
Local cTmpTblSCJ	:= GetNextAlias()
Local aIndexSCJ		:= {}
Local cTmpTblSCK	:= GetNextAlias()
Local aIndexSCK		:= {}
Local cTmpTblSCL	:= GetNextAlias()
Local aIndexSCL		:= {}
Local cRealName 	:= ""

ProcRegua(SCJ->(LastRec()))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Criando arquivo morto para as reservas do Faturamento         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If __oSCJTmp <> NIL
	cTmpTblSCJ := __oSCJTmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSCJ) == 0
		__oSCJTmp:Delete()
		FreeObj(__oSCJTmp)
	EndIf
EndIf

If __oSCJTmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSCJTmp := FWTemporaryTable():New( cTmpTblSCJ )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSCJTmp:SetFields( aStruSCJ )
	SCJ->( DBSetOrder(1) )  
	aIndexSCJ := StrTokArr( SCJ->( SqlOrder( IndexKey() ) ) , "," )  
	__oSCJTmp:AddIndex("1",aIndexSCJ)

	//------------------
	//Criação da tabela
	//------------------
	__oSCJTmp:Create()
Else
	cRealName := __oSCJTmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSCJ := __oSCJTmp:GetStruct():GetFields()
EndIf

If __oSCKTmp <> NIL
	cTmpTblSCK := __oSCKTmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSCK) == 0
		__oSCKTmp:Delete()
		FreeObj(__oSCKTmp)
	EndIf
EndIf

If __oSCKTmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSCKTmp := FWTemporaryTable():New( cTmpTblSCK )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSCKTmp:SetFields( aStruSCK )
	SCK->( DBSetOrder(1) )  
	aIndexSCK := StrTokArr( SCK->( SqlOrder( IndexKey() ) ) , "," )  
	__oSCKTmp:AddIndex("1",aIndexSCK)

	//------------------
	//Criação da tabela
	//------------------
	__oSCKTmp:Create()
Else
	cRealName := __oSCKTmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSCK := __oSCKTmp:GetStruct():GetFields()
EndIf

If __oSCLTmp <> NIL
	cTmpTblSCL := __oSCLTmp:GetAlias()
	//Se o alias não existir mais, exclui a tabela temporária para ser criada uma nova.
	If Select(cTmpTblSCL) == 0
		__oSCLTmp:Delete()
		FreeObj(__oSCLTmp)
	EndIf
EndIf

If __oSCLTmp == NIL
	//-------------------------------------------------------------------
	// Criação do objeto  
	//-------------------------------------------------------------------
	__oSCLTmp := FWTemporaryTable():New( cTmpTblSCL )

	//-------------------------------------------------------------------
	// Atribui campos e índices.  
	//-------------------------------------------------------------------
	__oSCLTmp:SetFields( aStruSCL )
	SCL->( DBSetOrder(1) )  
	aIndexSCL := StrTokArr( SCL->( SqlOrder( IndexKey() ) ) , "," )  
	__oSCLTmp:AddIndex("1",aIndexSCL)

	//------------------
	//Criação da tabela
	//------------------
	__oSCLTmp:Create()	
Else
	cRealName := __oSCLTmp:GetRealName()
	TcSqlExec("TRUNCATE TABLE " + cRealName)
	aStruSCL := __oSCLTmp:GetStruct():GetFields()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Selecionando o escopo para limpeza das reservas de faturamento³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery    := "SELECT SCJ.CJ_FILIAL,SCJ.CJ_NUM,SCJ.CJ_EMISSAO,SCJ.R_E_C_N_O_ SCJRECNO "
cQuery    += "FROM "+RetSqlName("SCJ")+" SCJ "
cQuery    += "WHERE SCJ.CJ_FILIAL='"+xFilial("SCJ")+"' AND "
cQuery    += "SCJ.CJ_EMISSAO<='"+Dtos(dLimite)+"' AND "
cQuery    += "SCJ.D_E_L_E_T_ = ' ' AND "
cQuery    += "(SCJ.CJ_STATUS NOT IN('A','D','B') OR "
cQuery    += "(CJ_STATUS = 'B' AND "
cQuery    += "NOT EXISTS (SELECT count(C6_FILIAL) "
cQuery    += "FROM "+RetSqlName("SC6")+" SC6,"+RetSqlName("SCK")+" SCK "
cQuery    += "WHERE "
cQuery    += "SCK.CK_FILIAL = '"+xFilial("SCK")+"' AND "
cQuery    += "SCK.CK_NUM = SCJ.CJ_NUM AND "
cQuery    += "SCK.D_E_L_E_T_ = ' ' AND "
cQuery    += "C6_FILIAL = '"+xFilial("SC6")+"' AND "
cQuery    += "C6_NUM=SCK.CK_NUMPV AND "
cQuery    += "SC6.D_E_L_E_T_ = ' ' ))) "
cQuery    += "ORDER BY "+SqlOrder(SCJ->(IndexKey()))

cQuery    := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCJ,.T.,.T.)

TcSetField(cAliasSCJ,"CJ_EMISSAO","D",8,0)

SCK->( DBSetOrder(1) )
SCL->( DBSetOrder(1) )

While ( (cAliasSCJ)->( !Eof() ) .And. xFilial("SCJ")==(cAliasSCJ)->CJ_FILIAL .And. 	(cAliasSCJ)->CJ_EMISSAO <= dLimite )
	
	lDeleta := .T.
	
	If ( lDeleta )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Exclui o Pedido de Venda                                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Begin Transaction
			
			SCJ->(MsGoto((cAliasSCJ)->SCJRECNO))
		
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Atualiza o Arquivo Morto                                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			RecLock(cTmpTblSCJ,.T.)
			For nX := 1 To FCount()
				cCampo := FieldName(nX)
				FieldPut(nX,SCJ->(FieldGet(FieldPos(cCampo))))
			Next nX
			MsUnLock()
			
			SCK->( MsSeek(xFilial("SCK")+(cAliasSCJ)->CJ_NUM) )
			
			While ( SCK->( !Eof() ) .And. xFilial("SCK") == SCK->CK_FILIAL .And.;
					(cAliasSCJ)->CJ_NUM == SCK->CK_NUM )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Atualiza o Arquivo Morto                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock(cTmpTblSCK,.T.)
				For nX := 1 To FCount()
					cCampo := FieldName(nX)
					FieldPut(nX,SCK->(FieldGet(FieldPos(cCampo))))
				Next nX
				MsUnLock()
					
				SCL->( MsSeek(xFilial("SCL")+(cAliasSCJ)->CJ_NUM+SCK->CK_ITEM) )
				
				While ( SCL->( !Eof() ) .And. xFilial("SCL") == SCL->CL_FILIAL .And.;
						(cAliasSCJ)->CJ_NUM == SCL->CL_NUM .And.;
						SCK->CK_ITEM == SCL->CL_ITEMORC )
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Atualiza o Arquivo Morto                                      ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RecLock(cTmpTblSCL,.T.)
					For nX := 1 To FCount()
						cCampo := FieldName(nX)
						FieldPut(nX,SCL->(FieldGet(FieldPos(cCampo))))
					Next nX
					MsUnLock()
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³Exclui o SCL                                                  ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					RecLock("SCL")
					SCL->( DBDelete() )
					MsUnLock()
					SCL->( DBSkip() )
				EndDo
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Exclui o SCK                                                  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("SCK")
				SCK->( DBDelete() )
				MsUnLock()
				SCK->( DBSkip() )
			EndDo
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Exclui o SCJ                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ			
			RecLock("SCJ")
			SCJ->( DBDelete() )
			MsUnLock()
		End Transaction
	EndIf
	
	IncProc(STR0006+" "+cNomeSCJ,cTexto) //"Atualizando Arquivo Morto"

	(cAliasSCJ)->( DBSkip() )
	
EndDo

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a query e o arquivo temporario.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
(cAliasSCJ)->( DBCloseArea() )

//----------------------------------------
//Faz a copia dos dados no formato CSV
//----------------------------------------
DBSelectArea(cTmpTblSCJ)

(cTmpTblSCJ)->( DBGoTop() )

If (cTmpTblSCJ)->( !Eof() )
	IncProc(STR0006+" "+cNomeSCJ,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSCJ
	Copy To &cFile Delimited
EndIf

DBSelectArea(cTmpTblSCK)

(cTmpTblSCK)->( DBGoTop() )

If (cTmpTblSCK)->( !Eof() )
	IncProc(STR0006+" "+cNomeSCK,cTexto) //"Atualizando Arquivo Morto"
	cFile := cDiretorio+"\"+cNomeSCK
	Copy To &cFile Delimited
EndIf

DBSelectArea(cTmpTblSCL)

(cTmpTblSCL)->( DBGoTop() )

If (cTmpTblSCL)->( !Eof() )
	cFile := cDiretorio+"\"+cNomeSCL
	Copy To &cFile Delimited
EndIf

DBSelectArea("SCJ")

If lExclusivo 
	
	IncProc(STR0007+" - SCJ",cTexto) //"Otimizando"
	nX := 0
	nY := SCJ->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SCJ")+" "
	cQuery += "WHERE CJ_FILIAL='"+xFilial("SCJ")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	
	IncProc(STR0007+" - SCK",cTexto) //"Otimizando"
	nX := 0
	nY := SCK->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SCK")+" "
	cQuery += "WHERE CK_FILIAL='"+xFilial("SCK")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo
	
	IncProc(STR0007+" - SCL",cTexto) //"Otimizando"
	nX := 0
	nY := SCL->(LastRec())
	cQuery := "DELETE FROM "+RetSqlName("SCL")+" "
	cQuery += "WHERE CL_FILIAL='"+xFilial("SCL")+"' AND "
	cQuery += "D_E_L_E_T_='*' AND "		
	While ( nX < nY )
		TcSqlExec(cQuery+"R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+"")
		nX += 1024
	EndDo		

EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Encerra a rotina renomeando o arquivo morto.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

RestArea(aArea)
Return(.T.) 

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ma580Param³ Autor ³ Rodrigo Zatt          ³ Data ³17.12.2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Verificacao dos MV_PAR e condicoes                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Mata580()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFAT                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ PROGRAMADOR  ³ DATA   ³ BOPS ³  MOTIVO DA ALTERACAO                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma580Param(lNFS,lLPV,lPV,lOrc,lExclusivo,dLimite,cDiretorio,lContinua)

lNFS        := MV_PAR01==1
lLPV        := MV_PAR02==1
lPV         := MV_PAR03==1
lOrc        := MV_PAR07==1
lExclusivo	:= MV_PAR04==1
dLimite		:= MV_PAR05
cDiretorio  := AllTrim(MV_PAR06)
If ( IsFileServer(cDiretorio) )
	If ( MakeDir(cDiretorio)>=0 )
	
		If ( lExclusivo )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Verifica se os arquivos foram abertos de forma exclusiva 	 ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SC0")
		    dbCloseArea()
		    If ( lContinua .And. !ChkFile("SC0",.T.) )
		    	lContinua := .F.
		    EndIf
		    If ( lPV )
   				dbSelectArea("AID")
			    dbCloseArea()
		    	If ( lContinua .And. !ChkFile("AID",.T.) )
			    	lContinua := .F.
			    EndIf
   				dbSelectArea("SC5")
			    dbCloseArea()
		    	If ( lContinua .And. !ChkFile("SC5",.T.) )
			    	lContinua := .F.
			    EndIf
   					dbSelectArea("SC6")
			  		dbCloseArea()
  		    		If ( lContinua .And. !ChkFile("SC6",.T.) )
		    		lContinua := .F.
			    EndIf
			EndIf
			If ( lLPV )
   				dbSelectArea("SC9")
			    dbCloseArea()
		    	If ( lContinua .And. !ChkFile("SC9",.T.) )
			    	lContinua := .F.
		    	EndIf
		 	EndIf
		 	If ( lNFS )
   				dbSelectArea("SD2")
			    dbCloseArea()
   			    	If ( lContinua .And. !ChkFile("SD2",.T.) )
			    	lContinua := .F.
		    	EndIf
   				dbSelectArea("SF2")
			    dbCloseArea()
		    	If ( lContinua .And. !ChkFile("SF2",.T.) )
			    	lContinua := .F.
		    	EndIf
		    	If cPaisLoc<>"BRA" .And. lContinua
  	   				    dbSelectArea("SCN")
			        dbCloseArea()
		    	    If !ChkFile("SCN",.T.)
			    	   lContinua := .F.
		    	    EndIf
		    	Endif    
			EndIf
		    If ( lOrc ) 
   				dbSelectArea("SCJ")
			    dbCloseArea()
		    	If ( lContinua .And. !ChkFile("SCJ",.T.) )
			    	lContinua := .F.
			    EndIf
   					dbSelectArea("SCK")
			    dbCloseArea()
  		    		If ( lContinua .And. !ChkFile("SCK",.T.) )
		    		lContinua := .F.
			    EndIf
   					dbSelectArea("SCL")
			    dbCloseArea()
  		    		If ( lContinua .And. !ChkFile("SCL",.T.) )
		    		lContinua := .F.
			    EndIf
			EndIf				
		EndIf
		
		If ( lContinua ) 
			Processa( {|lEnd| MA580Proc(,lNFS,lLPV,lPV,lOrc,lExclusivo,dLimite,cDiretorio) } )
		Else
			Help(" ",1,"EXCLUSIVO")
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Reabre os arquivos                                       	 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( Select("SC0")!=0 )
			dbSelectArea("SC0")
			dbCloseArea()
		EndIf
		If ( Select("AID")!=0 )
			dbSelectArea("AID")
			dbCloseArea()
		EndIf
		If ( Select("SC5")!=0 )
			dbSelectArea("SC5")
			dbCloseArea()
		EndIf
		If ( Select("SC6")!=0 )
			dbSelectArea("SC6")
			dbCloseArea()
		EndIf
		If ( Select("SC9")!=0 )
			dbSelectArea("SC9")
			dbCloseArea()
		EndIf
		If ( Select("SD2")!=0 )
			dbSelectArea("SD2")
			dbCloseArea()
		EndIf
		If ( Select("SF2")!=0 )
			dbSelectArea("SF2")
			dbCloseArea()
		EndIf			
		If ( Select("SCN")!=0 )
			dbSelectArea("SCN")
			dbCloseArea()
		EndIf			
		ChkFile("SC0",.F.)
		ChkFile("AID",.F.)			
		ChkFile("SC5",.F.)
		ChkFile("SC6",.F.)
		ChkFile("SC9",.F.)
		ChkFile("SF2",.F.)
		ChkFile("SD2",.F.)
		If cPaisLoc<>"BRA"
	       ChkFile("SCN",.F.)
	    Endif
	Else
		Help(" ",1,"NODIR")
	EndIf
Else
	Help(" ",1,"NODIR")
EndIf

Return
