#INCLUDE "PROTHEUS.CH"
#INCLUDE "TECA460.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"

#DEFINE CADASTRO STR0006 //"Atendimento da OS"
#DEFINE NUMITENS 300

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё TECA460  Ё Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Programa de Atendimento da OS.                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё Generico                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function TECA460(xAutoCab,xAutoItens,nOpcAuto, aAutoCmd)
Local cAt460Fil	:= ""		// Filtro fornecido pelo ponto de entrada
Local bFiltraBrw:= {||}		// Bloco de cСdigo para execuГЦo do filtro
Local aIndexAB9	:= {}		// Indice do AB1
Local cFiltraAB9:= ""		// Filtro utilizado no bloco de cСdigo executado para ativar o filtro

Private aRotina := MenuDef()
Private cCadastro := CADASTRO
Private l460Auto  := xAutoCab <> NIL  .And. xAutoItens <> NIL

If  ( Type("l460Auto") <> "U" .And. l460Auto )
	aAutoCab := xAutoCab
	aAutoItens := xAutoItens
	MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"AB9")
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica a existencia de Filtros na mBrowse                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AB9->(dbSetOrder(1))
	If (ExistBlock("AT460FIL"))
		cAt460Fil := ExecBlock("AT460FIL",.F.,.F.)
		If ( ValType(cAt460Fil) == "C" ) .And. !Empty(cAt460Fil)
			cFiltraAB9 := cAt460Fil
		EndIf
		bFiltraBrw 	:= {|| FilBrowse("AB9",@aIndexAB9,@cFiltraAB9) }
		Eval(bFiltraBrw)
	EndIf
	mBrowse( 6, 1,22,75,"AB9")
EndIf
dbSelectArea("AB9")
dbSetOrder(1)
Return .T.

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFunГЦo    ЁMenuDef   Ё Autor Ё Vendas e CRM          Ё Data Ё 08.12.06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё DefiniГЦo do aRotina (Menu funcional)                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё MenuDef()                                                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function MenuDef()
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Define Array contendo as Rotinas a executar do programa      Ё
	//Ё ----------- Elementos contidos por dimensao ------------     Ё
	//Ё 1. Nome a aparecer no cabecalho                              Ё
	//Ё 2. Nome da Rotina associada                                  Ё
	//Ё 3. Usado pela rotina                                         Ё
	//Ё 4. Tipo de Transa┤└o a ser efetuada                          Ё
	//Ё    1 - Pesquisa e Posiciona em um Banco de Dados             Ё
	//Ё    2 - Simplesmente Mostra os Campos                         Ё
	//Ё    3 - Inclui registros no Bancos de Dados                   Ё
	//Ё    4 - Altera o registro corrente                            Ё
	//Ё    5 - Remove o registro corrente do Banco de Dados          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Local aRotina := {	{ STR0001	,"AxPesqui"		,0	,1	,0	,.F.	}	,;  //"Pesquisar"
						{ STR0002	,"At460Visua"	,0	,2	,0	,.T.	}	,;	//"Visualizar"
						{ STR0003	,"At460Inclu"	,0	,3	,0	,.T.	}	,;	//"Incluir"
						{ STR0004	,"At460Alter"	,0	,4	,0	,.T.	}	,;	//"Alterar"
						{ STR0005	,"At460Exclu" 	,0	,5	,0	,.T.	}	}	//"Excluir"
	Local aRotAdic := {}	// Rotinas adicionais vindas de ponto de entrada

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Adiciona rotinas ao aRotina                          Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ExistBlock( "AT460ROT" )
		aRotAdic := ExecBlock( "AT460ROT", .F., .F. )
		If ValType( aRotAdic ) == "A"
			AEval( aRotAdic, { |x| AAdd( aRotina, x ) } )
		EndIf
	EndIf
Return(aRotina)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAt460IncluЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de Inclusao do Atendimento ao Chamado               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460Inclu(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460Inclu(cAlias,nReg,nOpc)

Local oDlg		:= Nil
Local nOpcA		:= 0
Local oGetD		:= Nil
Local nTotDesp	:= 0
Local nTotMat	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0
Local nTotGer	:= 0
Local oSay		:= Nil
Local aObjects	:= {}
Local aPosObj	:= {}
Local aSizeAut	:= {}
Local aPosGet	:= {}
Local nSaveSX8	:= GetSX8Len()
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis usadas na Integracao com Controle de Nao-Conformidades Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local lIntQNC	:= AAG->(FieldPos("AAG_GERFNC")) <> 0 			// Define a Integracao entre o ambiente Controle de Nao-Conformidades e o ambiente Field Service
Local aCamposFNC:= {} 											// Array com os campos da FNC

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT460INC")
	Execblock("AT460INC",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aCols		:= {}
Private aColsAnt	:= {}
Private aHeaderABC	:= {}
Private aColsABC	:= {}
Private aActions	:= {	{ || oGetD:oBrowse:Refresh()	}	,;
							{ || oGetD:ForceRefresh() 		}	}
Private oGetDUs		:= Nil

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Variaveis de Memoria da Enchoice                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("AB9")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		M->&(cField) := CriaVar(cField)
	Next nC
EndIf

//зддддддддддддддддддддддд©
//ЁMontagem aHeader, aColsЁ
//юддддддддддддддддддддддды
FillGetDados(	nOpc			,"ABA"		,1				,/*cSeek*/		,;
				/*{||cWhile}*/	,{|| .T. }	,/*aNoFields*/	,/*aYesFields*/	,;
				/*lOnlyYes*/	,/*cQuery*/	,/*bMontCols*/	,.T.			)

aCols[1][GdFieldPos("ABA_ITEM")] := "01"

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDefine o SetKey                                                         Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Type("l460Auto") == "U" .Or. !l460Auto )
	SetKey(VK_F4,{|a,b,c| At460F4(a,b,c)})
	SetKey(VK_F6,{|| At460F6()})
	SetKey(VK_F8,{|| At460F8()})


	aSizeAut := MsAdvSize()

	aObjects := {}
	AAdd( aObjects, { 315,  70, .T., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100,  14, .t., .f. } )

	aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }

	aPosObj := MsObjSize( aInfo, aObjects )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta tela de entrada                                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

	EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 )
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At460LinOk","At460TudOk","+ABA_ITEM",.T.,,,,NUMITENS)

	nLinIni := aPosObj[3,1]

	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
		{{005,065,105,165,205,265}} )

	@ nLinIni,aPosGet[1,1] SAY STR0007 SIZE 60,06 OF oDlg PIXEL //"Total: "
	@ nLinIni,aPosGet[1,2] SAY oSay PROMPT nTotGer  PICTURE TM(nTotGer,14,2)  SIZE 40,06 OF oDlg PIXEL
	oSay:Cargo := "nTotGer"
	@ nLinIni,aPosGet[1,3] SAY STR0008 SIZE 60,06 OF oDlg PIXEL //"Total do Cliente: "
	@ nLinIni,aPosGet[1,4] SAY oSay PROMPT nTotCli  PICTURE TM(nTotCli,14,2)  SIZE 40,06 OF oDlg PIXEL
	oSay:Cargo := "nTotCli"
	@ nLinIni,aPosGet[1,5] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ nLinIni,aPosGet[1,6] SAY oSay PROMPT nTotFab  PICTURE TM(nTotFab,14,2)  SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotFab"
	@ nLinIni+8,aPosGet[1,1] SAY STR0010 SIZE 60,09 OF oDlg PIXEL  //"Apontamentos: "
	@ nLinIni+8,aPosGet[1,2] SAY oSay PROMPT nTotMat  PICTURE TM(nTotMat,14,2)  SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotMat"
	@ nLinIni+8,aPosGet[1,3] SAY STR0011 SIZE 60,09 OF oDlg PIXEL  //"Despesas:     "
	@ nLinIni+8,aPosGet[1,4] SAY oSay PROMPT nTotDesp PICTURE TM(nTotDesp,14,2) SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotDesp"
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁInicializa as Despesas                                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	At460Desp(nOpc,oGetD,.F.)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os Totais da Tela de Apontamentos                              Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	At460TotAp(oDlg)

	oGetDUs := oGetD

	ACTIVATE MSDIALOG oDlg ON INIT At460Bar(oDlg,{||nOpcA:=1,If(Obrigatorio(aGets,aTela).And.oGetd:TudoOk(),oDlg:End(),nOpcA:=0)},{||nOpcA:=0,At460nConf(1,oDlg,lIntQNC,@aCamposFNC)},nOpc,oGetD,@aCamposFNC)
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁRetorna o SetKey Original                                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	SetKey(VK_F4,)
	SetKey(VK_F6,)
	SetKey(VK_F8,)
Else
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Variaveis utilizadas pela rotina de inclusao automatica     Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND. MsGetDAuto(aAutoItens,"At460LinOk",{|| At460TudOk()},aAutoCab)
		nOpcA := 1
	EndIf
EndIf

If ( nOpcA == 1 )
	Begin Transaction
		If ( aT460Grava(1, aCamposFNC) )
			EvalTrigger()
			While ( GetSX8Len() > nSaveSx8 )
				ConfirmSx8()
			End

			DbSelectArea('TEW')
			TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS

			If TEW->( DbSeek( xFilial('TEW')+AB7->( AB7_NUMOS + AB7_ITEM ) ) )

				If AB7->AB7_TIPO $ '2#4#5'
					// Atualiza o fechamento da OS no movimento de locaГЦo
					If !At800FechOs( .F. /*lExclusao*/ )
						DisarmTransaction()
					EndIf
				ElseIf TEW->TEW_FECHOS <> CTOD('')
					// Atualiza o fechamento da OS no movimento de locaГЦo
					If !At800FechOs( .T. /*lExclusao*/ )
						DisarmTransaction()
					EndIf
				EndIf

			EndIf

		EndIf
	End Transaction
EndIf

While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
End

dbSelectArea( cAlias )

Return .T.

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAt460VisuaЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de Visualizacao do Atendimento da OS                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460Visua(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460Visua(cAlias,nReg,nOpc)

Local oDlg		:= Nil
Local nOpcA		:= 0
Local aArea		:= GetArea()
Local oGetD		:= Nil
Local nTotDesp	:= 0
Local nTotMat	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0
Local nTotGer	:= 0
Local oSay		:= Nil
Local aObjects	:= {}
Local aPosObj	:= {}
Local aSizeAut	:= {}
Local aPosGet	:= {}
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader

Local aStruField := {}
Local cField     := ""
Local nC         := 0

Iif(Type("cCadastro") == "U", cCadastro := STR0006,)

If ExistBlock("AT460VIS")
	Execblock("AT460VIS",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aCols		:= {}
Private aHeaderABC	:= {}
Private aColsABC	:= {}

If aRotina == Nil
	aRotina := MenuDef()
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Variaveis de Memoria da Enchoice                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("AB9")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AB9->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//зддддддддддддддддддддддд©
//ЁMontagem aHeader, aColsЁ
//юддддддддддддддддддддддды
cSeek	:= xFilial("ABA")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ
cWhile	:= "ABA->ABA_FILIAL+ABA->ABA_NUMOS+ABA->ABA_CODTEC+ABA->ABA_SEQ"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	FillGetDados(	nOpc			,"ABA"			,1				,cSeek			,;
	 				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,;
					/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/		,;
					/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/)
Endif

If Empty(aCols[1][GdFieldPos("ABA_ITEM")])
	aCols[1][GdFieldPos("ABA_ITEM")] := "01"
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁMonta tela de entrada                                                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Type("l460Auto") == "U" .Or. !l460Auto )

	aSizeAut := MsAdvSize()
	aObjects := {}
	AAdd( aObjects, { 315,  70, .T., .t. } )
	AAdd( aObjects, { 100, 100, .t., .t. } )
	AAdd( aObjects, { 100,  14, .t., .f. } )

	aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }

	aPosObj := MsObjSize( aInfo, aObjects )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta tela de entrada                                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

	EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
	oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At460LinOk","At460TudOk","+ABA_ITEM",.F.)

	nLinIni := aPosObj[3,1]

	aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
	{{005,065,105,165,205,265}} )

	@ nLinIni,aPosGet[1,1] SAY STR0007 SIZE 60,06 OF oDlg PIXEL //"Total: "
	@ nLinIni,aPosGet[1,2] SAY oSay PROMPT nTotGer  PICTURE TM(nTotGer,14,2)  SIZE 40,06 OF oDlg PIXEL
	oSay:Cargo := "nTotGer"
	@ nLinIni,aPosGet[1,3] SAY STR0008 SIZE 60,06 OF oDlg PIXEL //"Total do Cliente: "
	@ nLinIni,aPosGet[1,4] SAY oSay PROMPT nTotCli  PICTURE TM(nTotCli,14,2)  SIZE 40,06 OF oDlg PIXEL
	oSay:Cargo := "nTotCli"
	@ nLinIni,aPosGet[1,5] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
	@ nLinIni,aPosGet[1,6] SAY oSay PROMPT nTotFab  PICTURE TM(nTotFab,14,2)  SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotFab"
	@ nLinIni+8,aPosGet[1,1] SAY STR0010 SIZE 60,09 OF oDlg PIXEL  //"Apontamentos: "
	@ nLinIni+8,aPosGet[1,2] SAY oSay PROMPT nTotMat  PICTURE TM(nTotMat,14,2)  SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotMat"
	@ nLinIni+8,aPosGet[1,3] SAY STR0011 SIZE 60,09 OF oDlg PIXEL  //"Despesas:     "
	@ nLinIni+8,aPosGet[1,4] SAY oSay PROMPT nTotDesp PICTURE TM(nTotDesp,14,2) SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotDesp"
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa as Despesas                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
At460Desp(nOpc,oGetD,.F.)
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAtualiza os Totais da Tela de Apontamentos                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Type("l460Auto") == "U" .Or. !l460Auto )
	At460TotAp(oDlg)
	ACTIVATE MSDIALOG oDlg ON INIT At460Bar(oDlg,{||oDlg:End()},{||oDlg:End()},nOpc,oGetD)
EndIf
RestArea(aArea)
Return .T.

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAt460AlterЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de Alteracao dos atendimentos da OS                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460Alter(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460Alter(cAlias,nReg,nOpc)

Local oDlg		:= Nil
Local nOpcA		:= 0
Local lTravas	:= .T.
Local lAltera	:= .T.
Local oGetD		:= Nil
Local nTotDesp	:= 0
Local nTotMat	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0
Local nTotGer	:= 0
Local oSay		:= Nil
Local aObjects	:= {}
Local aPosObj	:= {}
Local aSizeAut	:= {}
Local aPosGet	:= {}
Local nSaveSX8	:= GetSX8Len()
Local aCamposFNC:= {} 			// Arrar com os  dados da FNC
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader
Local aTravas	:= {}			// Array com os registros lockados
Local nC 		:= 0
Local lCheckEnc	:= SuperGetMv("MV_GSOSENC",.F.,.F.)

Local aStruField := {}
Local cField     := ""

If ExistBlock("AT460ALT")
	Execblock("AT460ALT",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aCols		:= {}
Private aColsAnt	:= {}
Private aHeaderABC	:= {}
Private aColsABC	:= {}
Private aActions	:= {	{ || oGetD:oBrowse:Refresh()	}	,;
						{ || oGetD:ForceRefresh() 		}	}
Private oGetDUs		:= Nil

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁTrava os registros necessarios                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !SoftLock("AB9") )
	lTravas := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Variaveis de Memoria da Enchoice                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("AB9")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AB9->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//зддддддддддддддддддддддд©
//ЁMontagem aHeader, aColsЁ
//юддддддддддддддддддддддды
cSeek	:= xFilial("ABA")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ
cWhile	:= "ABA->ABA_FILIAL+ABA->ABA_NUMOS+ABA->ABA_CODTEC+ABA->ABA_SEQ"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	If !FillGetDados(	nOpc			,"ABA"			,1				,cSeek			,;
	   					{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,;
						/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/		,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| IIf(!ABA->(Eof()), AtTravaReg("ABA", aTravas), .T.) } )
		lTravas := .F.
	Endif
Endif

If Len(aCols) > 0
	If Empty(aCols[1][GdFieldPos("ABA_ITEM")])
		aCols[1][GdFieldPos("ABA_ITEM")] := "01"
	EndIf
EndIf

aColsAnt := AClone( aCols )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se a Ordem de Servico nao eh Pedido de Venda                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("AB7")
dbSetOrder(1)
If ( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )
	If ( AB7->AB7_TIPO=="2" )
		lAltera := .F.
		Help(" ",1,"AT460ALT01")
	ElseIf lCheckEnc .And. AB7->AB7_TIPO=="5"
		lAltera := .F.
		Help( ,, 'AT460ENC01',, STR0082, 1, 0,,,,,,{STR0083} )//"NЦo И permitido a alteraГЦo de Ordens de ServiГos Encerrada"##"Altere o status da ordem de serviГo para O.S(AB7_TIPO = 1), se for necessario a alteraГЦo do atendimento"
	EndIf

	#IFDEF TOP
		If At800HasMov( .T., AB7->( Recno() ) )
			lAltera := .F.
			Help(" ",1,"NODELOSLOC",,STR0078,1,0) // 'Essa O.S. nЦo pode mais sofrer alteraГЦo pois o equipamento jА foi re-utilizado'
		EndIf
	#ENDIF

EndIf

dbSelectArea("AB4")
dbSetOrder(1)
If ( DbSeek(xFilial("AB4")+M->AB9_NUMORC) )
	If ( AB4->AB4_TIPO<>"1" )
		lAltera := .F.
		Help(" ",1,"AT460ALT02")
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa as Despesas                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lAltera
	lAltera := At460Desp(nOpc,oGetD,.F.)
EndIf

If ( lTravas .And. lAltera)
	If ( Type("l460Auto") == "U" .Or. !l460Auto )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁDefine o SetKey                                                         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SetKey(VK_F4,{|a,b,c| At460F4(a,b,c)})
		SetKey(VK_F6,{|| At460F6()})
		SetKey(VK_F8,{|| At460F8()})

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMonta tela de entrada                                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		aSizeAut := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 315,  70, .T., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100,  14, .t., .f. } )

		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }

		aPosObj := MsObjSize( aInfo, aObjects )

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMonta tela de entrada                                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

		EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At460LinOk","At460TudOk","+ABA_ITEM",.T.,,,,NUMITENS)

		nLinIni := aPosObj[3,1]

		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
			{{005,065,105,165,205,265}} )

		@ nLinIni,aPosGet[1,1] SAY STR0007 SIZE 60,06 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay PROMPT nTotGer  PICTURE TM(nTotGer,14,2)  SIZE 40,06 OF oDlg PIXEL
		oSay:Cargo := "nTotGer"
		@ nLinIni,aPosGet[1,3] SAY STR0008 SIZE 60,06 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay PROMPT nTotCli  PICTURE TM(nTotCli,14,2)  SIZE 40,06 OF oDlg PIXEL
		oSay:Cargo := "nTotCli"
		@ nLinIni,aPosGet[1,5] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay PROMPT nTotFab  PICTURE TM(nTotFab,14,2)  SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotFab"
		@ nLinIni+8,aPosGet[1,1] SAY STR0010 SIZE 60,09 OF oDlg PIXEL  //"Apontamentos: "
		@ nLinIni+8,aPosGet[1,2] SAY oSay PROMPT nTotMat  PICTURE TM(nTotMat,14,2)  SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotMat"
		@ nLinIni+8,aPosGet[1,3] SAY STR0011 SIZE 60,09 OF oDlg PIXEL  //"Despesas:     "
		@ nLinIni+8,aPosGet[1,4] SAY oSay PROMPT nTotDesp PICTURE TM(nTotDesp,14,2) SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotDesp"
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza os Totais da Tela de Apontamentos                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		At460TotAp(oDlg)

		oGetDUs := oGetD

		ACTIVATE MSDIALOG oDlg ON INIT At460Bar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||At460nConf(2,oDlg)},nOpc,oGetD,@aCamposFNC)
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁRetorna o SetKey Original                                               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		SetKey(VK_F4,)
		SetKey(VK_F6,)
		SetKey(VK_F8,)
	Else
		// Execucao de funГУes da automacao de testes
		If Type("aAutoCmd") == "A" .AND. Len(aAutoCmd) > 0
			For nC := 1 to Len(aAutoCmd)
				(&(aAutoCmd[nC]))
			Next nC
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Variaveis utilizadas pela rotina de inclusao automatica     Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .AND. MsGetDAuto(aAutoItens,"At460LinOk",{|| At460TudOk()},aAutoCab)
			nOpcA := 1
		EndIf
	EndIf

	If ( nOpcA == 1 )
		Begin Transaction
			If ( aT460Grava(2,aCamposFNC) )
				EvalTrigger()
				While ( GetSX8Len() > nSaveSx8 )
					ConfirmSx8()
				End


				DbSelectArea('TEW')
				TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS

				If TEW->( DbSeek( xFilial('TEW')+AB7->( AB7_NUMOS + AB7_ITEM ) ) )

					If AB7->AB7_TIPO $ '2#4#5' // Atendido ou Encerrado
						// Atualiza o fechamento da OS no movimento de locaГЦo
						//  inclui a data do fechamento da OS
						If !At800FechOs( .F. /*lExclusao*/ )
							DisarmTransaction()
						EndIf
					ElseIf TEW->TEW_FECHOS <> CTOD('')
						// Atualiza o fechamento da OS no movimento de locaГЦo
						//  remove a informaГЦo do fechamento da OS
						If !At800FechOs( .T. /*lExclusao*/ )
							DisarmTransaction()
						EndIf
					EndIf

				EndIf

			EndIf
		End Transaction
	EndIf
EndIf

While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
End
MsUnLockAll()
dbSelectArea( cAlias )
dbSetOrder(1)
Return .T.

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAt460ExcluЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Rotina de Exclusao dos atendimentos da OS                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460Exclu(ExpC1,ExpN1,ExpN2)                              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = Alias do arquivo                                   Ё╠╠
╠╠Ё          Ё ExpN1 = Numero do registro                                 Ё╠╠
╠╠Ё          Ё ExpN2 = Numero da opcao selecionada                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460Exclu(cAlias,nReg,nOpc)

Local oDlg		:= Nil
Local nOpcA		:= 0
Local lTravas	:= .T.
Local lExclui	:= .T.
Local oGetD		:= Nil
Local nTotDesp	:= 0
Local nTotMat	:= 0
Local nTotCli	:= 0
Local nTotFab	:= 0
Local nTotGer	:= 0
Local oSay		:= Nil
Local aObjects	:= {}
Local aPosObj	:= {}
Local aSizeAut	:= {}
Local aPosGet	:= {}
Local nSaveSX8	:= GetSX8Len()
Local cSeek		:= ""			// Seek para montagem da aCols
Local cWhile	:= ""			// While para montagem da aHeader
Local aTravas	:= {}			// Array com os reg. lockados
Local lRet		:= .T.
Local lCheckEnc	:= SuperGetMv("MV_GSOSENC",.F.,.F.)

Local aStruField := {}
Local cField     := ""
Local nC         := 0

If ExistBlock("AT460EXC")
	Execblock("AT460EXC",.F.,.F.)
Endif

Private aTela		:= {}
Private aGets		:= {}
Private aHeader		:= {}
Private aCols		:= {}
Private aHeaderABC	:= {}
Private aColsABC	:= {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁTrava os registros necessarios                                          Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( !SoftLock("AB9") )
	lTravas := .F.
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria Variaveis de Memoria da Enchoice                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aStruField := FWSX3Util():GetAllFields("AB9")
If Len(aStruField) > 0
	For nC := 1 to Len(aStruField)
		cField := aStruField[nC]
		If GetSx3Cache(cField,"X3_CONTEXT")=="V"
			M->&(cField) := CriaVar(cField)
		Else
			M->&(cField) := AB9->(FieldGet(FieldPos(cField)))
		EndIf
	Next nC
EndIf

//зддддддддддддддддддддддд©
//ЁMontagem aHeader, aColsЁ
//юддддддддддддддддддддддды
cSeek	:= xFilial("ABA")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ
cWhile	:= "ABA->ABA_FILIAL+ABA->ABA_NUMOS+ABA->ABA_CODTEC+ABA->ABA_SEQ"

If Len(aHeader) == 0 .AND. Len(aCols) == 0
	If !FillGetDados(	nOpc			,"ABA"			,1				,cSeek			,;
	   					{|| &cWhile }	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,;
						/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,/*lEmpty*/		,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,{|| IIf(!ABA->(Eof()), AtTravaReg("ABA", aTravas), .T.) } )
		lTravas := .F.
	Endif
Endif

If Len(aCols) > 0
	If Empty(aCols[1][GdFieldPos("ABA_ITEM")])
		aCols[1][GdFieldPos("ABA_ITEM")] := "01"
	Endif
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se a Ordem de Servico nao eh Pedido de Venda                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("AB7")
dbSetOrder(1)
If ( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )
	If ( AB7->AB7_TIPO=="2" )
		lExclui := .F.
		Help(" ",1,"AT460EXL01")
	ElseIf lCheckEnc .And. AB7->AB7_TIPO=="5"
		lExclui := .F.
		Help( ,, 'AT460ENC01',, STR0084, 1, 0,,,,,,{STR0085} )//"NЦo И permitido a exclusЦo de Ordens de ServiГos Encerrada"##"Altere o status da ordem de serviГo para O.S(AB7_TIPO = 1), se for necessario a exclusЦo do atendimento"
	EndIf
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se o equipamento jА foi alocado novamente                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If TEW->(!EOF()) .And. At800HasMov( .T., AB7->( Recno() ) )
	lExclui := .F.
	Help(" ",1,"NODELOSLOC",,STR0078,1,0) // 'Essa O.S. nЦo pode mais sofrer alteraГЦo pois o equipamento jА foi re-utilizado'
EndIf

dbSelectArea("AB4")
dbSetOrder(1)
If ( DbSeek(xFilial("AB4")+M->AB9_NUMORC) )
	If ( AB4->AB4_TIPO<>"1" .And. lExclui)
		lExclui := .F.
		Help(" ",1,"AT460EXL02")
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁInicializa as Despesas                                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lExclui
	lExclui := At460Desp(nOpc,oGetD,.F.)
EndIf

If ( lTravas .And. lExclui)
	If ( Type("l460Auto") == "U" .Or. !l460Auto )

		aSizeAut := MsAdvSize()
		aObjects := {}
		AAdd( aObjects, { 315,  70, .T., .t. } )
		AAdd( aObjects, { 100, 100, .t., .t. } )
		AAdd( aObjects, { 100,  14, .t., .f. } )

		aInfo := { aSizeAut[ 1 ], aSizeAut[ 2 ], aSizeAut[ 3 ], aSizeAut[ 4 ], 3, 3 }

		aPosObj := MsObjSize( aInfo, aObjects )

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMonta tela de entrada                                                   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DEFINE MSDIALOG oDlg TITLE cCadastro FROM aSizeAut[7],0 TO aSizeAut[6],aSizeAut[5] OF oMainWnd PIXEL STYLE WS_DLGFRAME

		EnChoice( cAlias, nReg, nOpc, , , , , aPosObj[1], , 3 , , , , , , .T. )
		oGetd:=MsGetDados():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpc,"At460LinOk","At460TudOk","+ABA_ITEM",.F.)

		nLinIni := aPosObj[3,1]

		aPosGet := MsObjGetPos(aSizeAut[3]-aSizeAut[1],310,;
			{{005,065,105,165,205,265}} )

		@ nLinIni,aPosGet[1,1] SAY STR0007 SIZE 60,06 OF oDlg PIXEL //"Total: "
		@ nLinIni,aPosGet[1,2] SAY oSay PROMPT nTotGer  PICTURE TM(nTotGer,14,2)  SIZE 40,06 OF oDlg PIXEL
		oSay:Cargo := "nTotGer"
		@ nLinIni,aPosGet[1,3] SAY STR0008 SIZE 60,06 OF oDlg PIXEL //"Total do Cliente: "
		@ nLinIni,aPosGet[1,4] SAY oSay PROMPT nTotCli  PICTURE TM(nTotCli,14,2)  SIZE 40,06 OF oDlg PIXEL
		oSay:Cargo := "nTotCli"
		@ nLinIni,aPosGet[1,5] SAY STR0009 SIZE 60,09 OF oDlg PIXEL //"Total do Fabricante: "
		@ nLinIni,aPosGet[1,6] SAY oSay PROMPT nTotFab  PICTURE TM(nTotFab,14,2)  SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotFab"
		@ nLinIni+8,aPosGet[1,1] SAY STR0010 SIZE 60,09 OF oDlg PIXEL  //"Apontamentos: "
		@ nLinIni+8,aPosGet[1,2] SAY oSay PROMPT nTotMat  PICTURE TM(nTotMat,14,2)  SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotMat"
		@ nLinIni+8,aPosGet[1,3] SAY STR0011 SIZE 60,09 OF oDlg PIXEL  //"Despesas:     "
		@ nLinIni+8,aPosGet[1,4] SAY oSay PROMPT nTotDesp PICTURE TM(nTotDesp,14,2) SIZE 40,09 OF oDlg PIXEL
		oSay:Cargo := "nTotDesp"
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁAtualiza os Totais da Tela de Apontamentos                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		At460TotAp(oDlg)
		ACTIVATE MSDIALOG oDlg ON INIT At460Bar(oDlg,{||nOpcA:=1,if(oGetd:TudoOk(),If(!obrigatorio(aGets,aTela),nOpcA := 0,oDlg:End()),nOpcA := 0)},{||At460nConf(3,oDlg)},nOpc,oGetD)
		If ExistBlock("AT460DOK")
			If !ExecBlock("AT460DOK",.F.,.F.)
				nOpcA := 0
			EndIf
		EndIf
	Else
		nOpcA := 1
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
			lRet := aT460Grava(3)
			EvalTrigger()

			DbSelectArea('TEW')
			TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS

			If TEW->( DbSeek( xFilial('TEW')+AB7->( AB7_NUMOS + AB7_ITEM ) ) )
				// Atualiza o fechamento da OS no movimento de locaГЦo
				//  remove a informaГЦo do fechamento da OS
				If !At800FechOs( .T. /*lExclusao*/ )
					DisarmTransaction()
				EndIf

			EndIf

		End Transaction
		While ( GetSX8Len() > nSaveSx8 )
			ConfirmSx8()
		End
	EndIf
EndIf
While ( GetSX8Len() > nSaveSx8 )
	RollBackSx8()
End

MsUnLockAll()
dbSelectArea( cAlias )
dbSetOrder(1)

Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460LinOkЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da LinhaOk                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460LinOk(oGetD)

Local lRetorno     := .T.
Local nUsado       := Len(aHeader)
Local nPosProd     := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODPRO"})
Local nPosQuant    := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QUANT"})
Local nPosSer      := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODSER"})
Local nPosLocal    := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCAL"})
Local nPosLocaLi   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCALI"})
Local nPosNumSer   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_NUMSER"})
Local nPosLoteCt   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOTECT"})
Local nPosNumLote  := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_NUMLOT"})

Local nSaldoSBF    := 0
Local nSaldoSb2    := 0
Local nQtdAtu      := 0
Local nQtdAnt      := 0
Local nQtdEstoque  := 0
Local nQtdNecess   := 0

Local nCntFor      := 0

Local nPosItemRC   := GDFieldPos( "ABA_ITEMRC" )
Local nPosSeqRC    := GDFieldPos( "ABA_SEQRC" )

Local cItemRC
Local cSeqRC
Local cLoteCtl     := ""

Local bCmpLoteCtl  := { || .T. }
Local bCmpNumLote  := { || .T. }
Local bCmpLocali   := { || .T. }

Local bCmpLtCtlAnt := { || .T. }
Local bCmpNuLtAnt  := { || .T. }
Local bCmpLoczAnt  := { || .T. }

Local lLocali      := Localiza( aCols[ n, nPosProd ] )
Local lRastroLote  := .F.
Local lRastroSub   := .F.
Local lEstoqueAnt  := .F.
Local oDlg

If  ( Type("l460Auto") <> "U" .And. !l460Auto )
    oDlg := oGetD:oWnd
EndIf

If ( !aCols[n][nUsado+1] .And. ( Len(aCols) > 1.Or.!Empty(aCols[n][nPosProd])) )
	If (  Empty(aCols[n][nPosProd]) .Or.;
			Empty(aCols[n][nPosLocal]) .Or.;
			Empty(aCols[n][nPosQuant]) .Or.;
			Empty(aCols[n][nPosSer]) )
			Help(" ",1,"AT460LINOK")
		lRetorno := .F.
	EndIf
	If ( lRetorno )

		nQtdAtu := 0
		nQtdAnt := 0
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se atualiza estoque ( acols atual )                           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("AA5")
		dbSetOrder(1)
		DbSeek(xFilial("AA5")+aCols[n][nPosSer])

		dbSelectArea("SF4")
		dbSetOrder(1)
		DbSeek(xFilial("SF4")+AA5->AA5_TES)

		dbSelectArea("SB2")
		dbSetOrder(1)
		If ( SF4->F4_ESTOQUE=="S" .Or. AA5->AA5_ATUEST=="S" )
			If (!DbSeek(xFilial("SB2")+aCols[n][nPosProd]+aCols[n][nPosLocal]) ) 
				lRetorno := .F.
				If IsInCallStack("At900IncAt")
					Help(" ",1,"AT460LIN02",,STR0081, 1, 0)//NЦo foi possМvel gerar o atendimento, pois o item nЦo possui saldo em estoque. Para desligar a integraГЦo com o estoque, И necessАrio alterar o campo F4_ESTOQUE = N, no cadastro de TES e o campo AA5_ATUEST = N, no cadastro de ServiГo.
				Else 
					Help(" ",1,"AT460LIN01")
				EndIf
			Else

				If Rastro( aCols[n,nPosProd], "L" )

					lRastroLote := .T.

					If Empty( nPosLoteCt )
						lRetorno := .F.
						Help( " ", 1, "AT460LOTUS" ) // Caso seja utilizado produto com rastreabilidade lote de controle, o campo lote de controle deve estar em uso
					EndIf

					If lRetorno

						If AA5->AA5_ATUEST == "S"
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Se o servico atualiza estoque, o lote deve ser obrigatoriamente ser preenchido     Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If Empty( aCols[n,nPosLoteCt ] )
								lRetorno := .F.
								Help( " ", 1, "AT460LOTCT" ) // "Caso seja utilizado produto com rastreabilidade lote de controle, o lote de controle deve ser informado
							EndIf
						Else
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Se o servico nao atualiza estoque, o lote deve ser obrigatoriamente em branco      Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If !Empty( aCols[n,nPosLoteCt ] )
								lRetorno := .F.
								Help( " ", 1, "AT460LOTNU" )
							EndIf
						EndIf
					EndIf

					If lRetorno
						bCmpLoteCtl  := { || aCols[n,nPosLoteCt]  == aCols[nCntFor,nPosLoteCt] }
						bCmpLtCtlAnt := { || aCols[n,nPosLoteCt]  == aColsAnt[nCntFor,nPosLoteCt] }
					EndIf

				ElseIf Rastro( aCols[n,nPosProd], "S" )

					lRastroSub := .T.

					If Empty( nPosNumLote )
						lRetorno := .F.
						Help( " ", 1, "SUBUS" ) // Caso seja utilizado produto com rastreabilidade sublote, o campo sublote deve estar em uso
					EndIf

					If lRetorno

						If AA5->AA5_ATUEST == "S"
							//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Se o servico atualiza estoque, o sublote deve obrigatoriamente ser preenchido     Ё
							//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If Empty( aCols[n,nPosNumLote ] )
								lRetorno := .F.
								Help( " ", 1, "AT460SUBLT" ) // Caso seja utilizado produto com rastreabilidade sublote, o subLote deve ser informado
							EndIf
						Else
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Se o servico nao atualiza estoque, o sublote deve obrigatoriamente estar em branco Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							If !Empty( aCols[n,nPosNumLote ] )
								lRetorno := .F.
								Help( " ", 1, "AT460SUBNU") // Caso seja utilizado produto com rastreabilidade sublote e codigo de servico nao atualiza estoque, nao e permitido informar o sublote !
							EndIf
						EndIf

					EndIf

					If lRetorno
						bCmpNumLote := { || aCols[n,nPosNumLote] == aCols[nCntFor,nPosNumLote] }
						bCmpNuLtAnt := { || aCols[n,nPosNumLote] == aColsAnt[nCntFor,nPosNumLote] }
					EndIf

				EndIf

				If lRetorno
					If lLocali

						If AA5->AA5_ATUEST == "S"
							If Empty( aCols[n,nPosLocali] )
								lRetorno := .F.
								Help( " ", 1, "AT460LOCLZ" ) // "Caso seja utilizado produto com enderecamento, o campo endereco deve estar preenchido
							EndIf
						Else
							If !Empty( aCols[n,nPosLocali] )
								lRetorno := .F.
								Help( " ", 1, "AT460LOCNU" ) // Caso seja utilizado produto com controle de localizacao fisica e codigo de servico nao atualiza estoque, nao e permitido informar a localizacao !
							EndIf
						EndIf

						If lRetorno
							bCmpLocali  := { || aCols[n,nPosLocali] == aCols[nCntFor,nPosLocali] .And. ;
								aCols[n,nPosNumSer] == aCols[nCntFor,nPosNumSer] }

							bCmpLoczAnt := { || aCols[n,nPosLocali] == aColsAnt[nCntFor,nPosLocali] .And. ;
								aCols[n,nPosNumSer] == aColsAnt[nCntFor,nPosNumSer] }

						EndIf
					EndIf
				EndIf

				If lRetorno

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁVarre o acols em busca das quantidades ja utilizadas                    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					For nCntFor := 1 To Len(aCols)
						If ( !aCols[nCntFor][nUsado+1] )
							If ( aCols[n,nPosProd]  == aCols[nCntFor,nPosProd] .And.;
									aCols[n,nPosLocal]== aCols[nCntFor,nPosLocal] .And. Eval( bCmpLoteCtl ) ;
									.And. Eval( bCmpLocali ) .And. Eval( bCmpNumLote ) )
								If AA5->( DbSeek( xFilial( "AA5" ) + ;
										aCols[ nCntFor, nPosSer ] ) )
									SF4->( dbSetOrder( 1 ) )
									SF4->( DbSeek(xFilial("SF4")+AA5->AA5_TES) )

									If ( SF4->F4_ESTOQUE=="S" .Or. AA5->AA5_ATUEST=="S" )
										nQtdAtu += aCols[nCntFor,nPosQuant]
									EndIf
								EndIf

							EndIf
						EndIf
					Next nCntFor

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//ЁVarre o aColsAnt em busca das quantidades utilizadas anteriormente      Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					For nCntFor := 1 To Len(aColsAnt)
						If ( aCols[n,nPosProd]  == aColsAnt[nCntFor,nPosProd] .And.;
								aCols[n,nPosLocal]== aColsAnt[nCntFor,nPosLocal] .And. Eval( bCmpLtCtlAnt );
								.And. Eval( bCmpLoczAnt ) .And. Eval( bCmpNuLtAnt ) )
							If AA5->( DbSeek( xFilial( "AA5" ) + ;
									aColsAnt[ nCntFor, nPosSer ] ) )
								SF4->( dbSetOrder( 1 ) )
								SF4->( DbSeek(xFilial("SF4")+AA5->AA5_TES) )

								If AA5->AA5_ATUEST=="S"
									nQtdAnt += aColsAnt[nCntFor,nPosQuant]
								EndIf

							EndIf
						EndIf
					Next nCntFor

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Define a necessidade de estoque                                        Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					nQtdNecess := ( nQtdAtu - nQtdAnt )

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Verifica se existe saldo para atender a solicitacao                    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If lLocali

						cLoteCtl := ""

						If lRastroLote
							cLoteCtl := aCols[n,nPosLoteCt]
						ElseIf lRastroSub
							SB8->( dbSetOrder( 2 ) )
							cLoteCtl := ""
							If SB8->( DbSeek( xFilial( "SB8" ) + aCols[n,nPosNumLote] ) )
								cLoteCtl := SB8->B8_LOTECTL
							EndIf
						EndIf

						nQtdEstoque := SaldoSBF(aCols[n,nPosLocal],	aCols[n,nPosLocali],;
							aCols[n,nPosProd],	aCols[n,nPosNumSer],cLoteCtl,;
							If( lRastroSub , aCols[n,nPosNumLote], Nil ) )

					ElseIf lRastroLote .Or. lRastroSub
						If lRastroLote
							cLoteCtl := aCols[n,nPosLoteCt]
						Else
							SB8->( dbSetOrder( 2 ) )
							cLoteCtl := ""
							If SB8->( DbSeek( xFilial( "SB8" ) + aCols[n,nPosNumLote] ) )
								cLoteCtl := SB8->B8_LOTECTL
							EndIf
						EndIf

						nQtdEstoque := SldAtuEst(aCols[n,nPosProd],aCols[n,nPosLocal],nQtdNecess,cLoteCtl,If( lRastroSub , aCols[n,nPosNumLote], Nil ) )

					Else
						nQtdEstoque := SaldoSb2()
					EndIf

					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Subtrai a quantidade em estoque da necessidade                         Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					nSaldoEst := nQtdEstoque - nQtdNecess

					If ( nSaldoEst < 0 )
						Help(" ",1,"AT460LINO2") //Hedit2
						lRetorno := .F.
					EndIf

				EndIf

				If lRetorno .and. (AA5->AA5_ATUEST == "S")

					//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Se o servico atualiza estoque, o produto nao pode estar bloqueado para inventario Ё
					//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					If BlqInvent(aCols[n,nPosProd],aCols[n][nPosLocal])
						lRetorno := .F.
						Help(" ",1,"BLQINVENT")
					EndIf

				EndIf

			EndIf

		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se existem itens de solicitacao duplicados                    Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		cItemRC := aCols[ n, nPosItemRC ]
		cSeqRC  := aCols[ n, nPosSeqRC  ]

		If lRetorno

			If !Empty( cItemRC ) .And. !Empty( cSeqRC )
				For nCntFor := 1 To Len( aCols )
					If aCols[ nCntFor, nPosItemRC ] == cItemRC .And. ;
							aCols[ nCntFor, nPosSeqRC ] == cSeqRC .And. ;
							!GDDeleted( nCntFor ) .And. nCntFor <> n
						lRetorno := .F.
						Help( " ", 1, "AT460RCDUP" )
						Exit
					EndIf
				Next nCntFor
			EndIf
		EndIf

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se o produto И o mesmo e se as quantidades dos itens solicitados estao corretas        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		If lRetorno
			If !Empty( cItemRC ) .And. !Empty( cSeqRC )
				ABG->( dbSetOrder( 1 ) )
				If ABG->(DbSeek(xFilial("ABG") + M->AB9_NUMOS + ;
						aCols[n,nPosSeqRC] + aCols[n,nPosItemRC] ) )

					SCP->( dbSetOrder( 1 ) )
					If SCP->( DbSeek( xFilial( "SCP" ) + ABG->ABG_NUMSA + ;
							ABG->ABG_ITEMSA ) )
						If aCols[n,nPosQuant] > SCP->CP_QUJE
							lRetorno := .F.
							Help( " ", 1, "AT460QTRC")
						EndIf
						If aCols[n,nPosProd] <> SCP->CP_PRODUTO
							lRetorno := .F.
							Help( " ", 1, "TECA460",,i18n(STR0079,{alltrim(str(n))}),1,0) //"O produto na linha #1[Numero da Linha da Grid]# informado difere do informado na SolicitaГЦo ao Armazem."
						EndIf
					Else
						lRetorno := .F.
						Help( " ", 1, "REGNOIS")
					EndIf

				Else
					lRetorno := .F.
					Help( " ", 1, "REGNOIS")
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

If lRetorno
	If ExistBlock( "AT460LOK" )
	   lRetorno := ExecBlock( "AT460LOK", .F., .F. )
	EndIf
EndIf

If  ( Type("l460Auto") <> "U" .And. !l460Auto )
	At460TotAp(oDlg)
EndIf

Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460TudOkЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valicao da TudoOk                                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460TudOk(oGetD)
Local nPosValor	:= 0
Local nValor	:= 0
Local nLoop		:= 0
Local nUsado	:= Len(aHeader)
Local lRetorno	:= .T.
Local nBkp		:= n
Local nPosProd	:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODPRO"})
Local nPosQuant	:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QUANT"})
Local lAloc		:= SuperGetMv("MV_ATALOC",,.F.) //MV_ATALOC - Parametro ativo nЦo permite atendimento de O.S de atendentes indisponМveis

If lAloc .AND. !At460Disp(M->AB9_CODTEC)
	lRetorno := .F.
	Help(" ", 1, "AT460TUDOK", , STR0086, 1, 0,,,,,,{STR0087}) // ##"Atendente indisponМvel para uso!" ##"Selecione outro atendente."
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Refaz as validacoes da LinOk                                           Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lRetorno	
	For nLoop := 1 To Len( aCols )
		n := nLoop
		If !( lRetorno := At460LinOk(oGetD) )
			Aviso( STR0035, STR0042 + GDFieldGet( "ABA_ITEM", nLoop ) + STR0043, { STR0037 } )  //"Atencao !"###"O item "###" nao foi validado !"###"Ok"
			Exit
		EndIf

		If ( Len( aCols[nLoop] ) == nUsado .Or. !aCols[nLoop][nUsado+1] )
			nValor	+= At450Tab( aCols[nLoop][nPosProd], AB6->AB6_TABELA, aCols[nLoop][nPosQuant], AB6->AB6_CODCLI, AB6->AB6_LOJA, AB6->AB6_MOEDA ) * aCols[nLoop][nPosQuant]
		EndIf
	Next nLoop
EndIf

If lRetorno

	lRetorno := AT460MonTe(AB7->AB7_CODCLI,AB7->AB7_LOJA,AB7->AB7_CODPRO,AB7->AB7_NUMSER,AA3->AA3_CONTRT)

	// Valor Maximo para Apontamentos do Contrato
	If lRetorno .AND. ValType( aHeaderABC ) == "A" .AND. ValType( aColsABC ) == "A"
		nPosValor	:= aScan( aHeaderABC,{ |x| AllTrim( x[2] ) == "ABC_VALOR" } )
		If nPosValor > 0
			// Soma o valor do atendimento atual e verifica se excedeu o valor maximo
			aEval( aColsABC, { |x| nValor += x[nPosValor] } )

			lRetorno := AT460VlMax( M->AB9_NUMOS, M->AB9_DTINI, nValor )
		EndIf
	EndIf

EndIf

If lRetorno .And. ExistBlock( "AT460TOK" )
	lRetorno := ExecBlock( "AT460TOK", .F., .F. )
EndIf

n := nBkp

If !l460Auto
	oGetD:Refresh()
EndIf

Return(lRetorno)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460GravaЁ Autor Ё Vendas e CRM          Ё Data Ё 23.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Efetua a Gravacao do Atendimento da OSs                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ nOpcao    : [3] Exclusao [1] Inclusao [2] Alteracao        Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ Relacao do AB8 com AB9,ABA,ABC                             Ё╠╠
╠╠Ё          Ё AB8->AB9 : Todo AB9 gera um AB8 com Linha "000000", assim  Ё╠╠
╠╠Ё          Ё        deve-se efetuar um controle de saldo e quando ha    Ё╠╠
╠╠Ё          Ё            este controle pode haver item "000000" zerado.  Ё╠╠
╠╠Ё          Ё AB8->ABA : Todo ABA gera um AB8 com controle de item dife- Ё╠╠
╠╠Ё          Ё            renciado, que eh armazenado no campo ABA_SUBOS  Ё╠╠
╠╠Ё          Ё AB8->ABC : Idem ao anterior -> campo armazenado   ABC_SUBOSЁ╠╠
╠╠Ё          Ё AB8->ABA : Ha controle de saldo quando houver aglutinacao  Ё╠╠
╠╠Ё          Ё AB8->ABC : Ha controle de saldo quando houver aglutinacao  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460Grava(nOpcao, aCamposFNC)

Local aStats       := {}

Local bCampo       := {|x| FieldName(x) }

Local cSubItem     := StrZero(0,TamSX3("AB8_SUBITE")[1])
Local cProdMod := SuperGetMv("MV_MODPROD", .F., "")
Local cServMod := SuperGetMv("MV_MODSERV", .F., "")
Local cProdDes := SuperGetMv("MV_DESPROD", .F., "")
Local cAgluPrd := SuperGetMv("MV_AGLPROD", .F., "")

Local cServCob     := ""
Local cDesPro      := ""
Local cNumOrc      := ""
Local cNumOS       := ""
Local cItemOrc     := "00"
Local cABASeqRC    := ""
Local cSeekABG     := ""
Local cQuery       := ""
Local cCampo 		 := "ABA_CODPRO"
Local cCmpQtd		 := "ABA_QUANT"

Local lInclui      := .F.

Local lFalhaMTBF   := .F.
Local lIncluiFalha := .F.
Local lDecMTBF     := .F.
Local lStatOnLine  := SuperGetMv( "MV_ATSTATI" )
Local lGravaSD3    := .T.
Local lFoundAB8    := .F.
Local lAtHrPr      := SuperGetMv( "MV_ATHRPR" )

Local nUsadoABC    := Len(aHeaderABC)
Local nVlHoraEst   := 0
Local nVlHoraAtu   := 0
Local nItemOrc     := 0
Local nPItemOrc    := 0
Local nQtRepAnt    := 0
Local nHorRpAnt    := 0
Local nQtFalAnt    := 0
Local nCntFor      := 0
Local nCntFor2     := 0
Local nPosProd     := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_CODPRO" })
Local nPosItem     := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_ITEM" })
Local nPosSer      := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODSER"})
Local nPosAHead    := 0
Local nUsado       := Len(aHeader)
Local ucampo       := ""
Local cOrcamento   := ""
Local cCodOs	   := ""
Local cItemOs	   := ""
Local cCodTFI	   := ""
Local cProd		   := ""	
Local cCodTFL	   := ""		 
Local cArmazen	   := ""

Local lDelFNC      := .T. 								// Indica se a FNC pode ser deletada
Local lAjusta	   := .F.									// Se deverА ser feito o ajuste nos parБmetros que tem aspas a mais no conteЗdo

Local dDtReal 	:= cToD("")							//Cria a variavel de data vazia	
Local lStatusAlt   := .F.								//Se o status foi alterado de 1-Encerrado para 2-Aberto
Local cMV_FORMGAR := SuperGetMv( "MV_FORMGAR" ) 
Local lAb7AppSt	  := AB7->( ColumnPos('AB7_APPSTA') ) > 0

Default aCamposFNC := {} 								// Indica a Integracao com o ambiente Controle de Nao-Conformidades

Private aColsAB4   := {}
Private aColsAB5   := {{}}
Private aHeaderAB4 := {}
Private aHeaderAB5 := {}

//здддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁVerifica se alguma ajuste em parБmetro И necessАrio.Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддды
If Alltrim(cProdMod) == '""' .OR. Alltrim(cProdMod) == "''"
	cProdMod := ""
	lAjusta := .T.
EndIf
If Alltrim(cServMod) == '""' .OR. Alltrim(cServMod) == "''"
	cServMod := ""
	lAjusta := .T.
EndIf
If Alltrim(cProdDes) == '""' .OR. Alltrim(cProdDes) == "''"
	cProdDes := ""
	lAjusta := .T.
EndIf
If Alltrim(cAgluPrd) == '""' .OR. Alltrim(cAgluPrd) == "''"
	cAgluPrd := ""
	lAjusta := .T.
EndIf

//Verifica se o atendimento gerou marcaГУes automaticas, nЦo permite a alteraГЦo ou exclusЦo.
If (nOpcao == 2 .OR. nOpcao == 3) .AND. M->AB9_MPONTO
	Help(" ",1,"AT460EXL03")
	Return(.F.)
EndIf

If Left(cProdMod, 1) = "'" .or. Left(cProdMod, 1) = '"'
	cProdMod := &(cProdMod)
EndIf

cProdMod := PadR(Alltrim(cProdMod), Tamsx3("B1_COD")[1])

If Left(cServMod, 1) = "'" .or. Left(cServMod, 1) = '"'
	cServMod := &(cServMod)
EndIf

cServMod := PadR(Alltrim(cServMod), Tamsx3("AA5_CODSER")[1])

If Left(cProdDes, 1) = "'" .or. Left(cProdDes, 1) = '"'
	cProdDes := &(cProdDes)
EndIf

cProdDes := PadR(Alltrim(cProdDes), Tamsx3("B1_DESC")[1])

If Left(cAgluPrd, 1) = "'" .or. Left(cAgluPrd, 1) = '"'
	cAgluPrd := &(cAgluPrd)
EndIf

cAgluPrd := PadR(Alltrim(cAgluPrd), Tamsx3("B1_COD")[1])

Do Case
Case nOpcao <> 3

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Obtem os valores anteriores para este item de OS                Ё
	//Ё Atencao !!! O AB7 DEVE ESTAR POSICIONADO !!!                    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If lStatOnLine
		AB7->( dbSetOrder( 1 ) )
		If AB7->( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )
			aStats := AtStaItOS()

			nQtRepAnt := aStats[ 1 ]
			nHorRpAnt := aStats[ 2 ]
			nQtFalAnt := aStats[ 3 ]

		EndIf
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁValidaГЦo de integraГЦo com a lista de contatos do SIGATMK (Call Center).Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DbSelectArea( "AB9" )
	DbSetOrder( 1 )
	If DbSeek( xFilial("AB9") + M->AB9_NUMOS + M->AB9_CODTEC + M->AB9_SEQ )
		// Se o campo de lista de contato existe e o mesmo estА preenchido
		If !Empty( M->AB9_TMKLST )
			// Se o usuАrio estА alterando de encerrado para aberto
			If AB9->AB9_TIPO == "1" .And. M->AB9_TIPO == "2"
				// Verifica se a lista jА foi executada
				DbSelectArea( "SU4" )
				DbSetOrder( 1 )
				If DbSeek( xFilial( "SU4" ) + M->AB9_TMKLST )
					// Se a lista de contato estiver em aberto, elimina o contato desse item apontado da lista de atendimento
					If SU4->U4_STATUS == "1"
						// Localiza o item da ordem de serviГo referente ao apontamento
						DbSelectArea( "AB7" )
						DbSetOrder( 1 )
						If DbSeek( xFilial( "AB7" ) + M->AB9_NUMOS )
							// Pega o cСdigo do contato desse item e localiza o contato nos itens da lista de contato
							DbSelectArea( "SU6" )
							DbSetOrder( 1 )
							DbSeek( xFilial( "SU6" ) + SU4->U4_LISTA )
							While	xFilial( "SU6" ) + SU4->U4_LISTA == SU6->U6_FILIAL + SU6->U6_LISTA .And.;
									SU6->(!EOF())
								// Encontrou o contato do item do apontamento da OS que estА sendo alterado
								If SU6->U6_CONTATO == AB7->AB7_CODCON
									// Apaga o contato da lista
									RecLock( "SU6", .F. )
									SU6->(DbDelete())
									SU6->(MsUnLock())

									// Apaga o relacionamento do apontamento com a lista de contato
									M->AB9_TMKLST := ""
								EndIf
								SU6->(DbSkip())
							End
						EndIf

					Else
						Alert( STR0060 ) // "O apontamento da OS nЦo poderА ser reaberto pois a lista de contato que esse apontamento gerou jА estА sendo executada, ou jА foi encerrada."
						Return .F.
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava arquivo AB9 (Cabecalho do Atendimento )         Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("AB9")
	dbSetOrder(1)
	DbSeek(xFilial("AB9")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ)

	//Verifica se o usuАrio estА alterando o status de encerrado para aberto na alteraГЦo do atendimento.
	If (nOpcao == 2 .And. AB9->AB9_TIPO == "1" .And. M->AB9_TIPO == "2")
		lStatusAlt := .T.
	EndIf
	
	RecLock("AB9",!Found())

	nTotFatAnt := AB9->AB9_TOTFAT

	For nCntFor := 1 To FCount()
		If ( "FILIAL"$Field(nCntFor) )
			AB9->(FieldPut(nCntFor,xFilial("AB9")))
		Else
			AB9->(FieldPut(nCntFor,M->&(EVAL(bCampo,nCntFor))))
		EndIf
	Next nCntFor

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava o contrato de manutencao e o grupo de cobertura    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	dbSelectArea("AB7")
	AB7->(dbSetOrder(1))
	AB7->(DbSeek(xFilial("AB7")+AB9->AB9_NUMOS))

	dbSelectArea("AB6")
	AB6->(dbSetOrder(1))
	AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))

	AB9->AB9_CODCLI := AB6->AB6_CODCLI
	AB9->AB9_LOJA   := AB6->AB6_LOJA
	AB9->AB9_CODPRO := AB7->AB7_CODPRO

    DbSelectArea("AA3")
    AA3->(DbSetOrder(6))
    AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))

    DbSelectArea("AAH")
    AAH->(DbSetOrder(1))
    AAH->(DbSeek(xFilial("AAH")+AA3->AA3_CONTRT))


    AB9->AB9_CONTRT := AAH->AAH_CONTRT
	AB9->AB9_CODGRP := AAH->AAH_CODGRP


	//зддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Gravo os  dados  da FNC  gravada  para esta  OS Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty(aCamposFNC) .AND. Empty(AB9->AB9_CODFNC)
		If Len(aCamposFNC[1]) >= 2
			AB9->AB9_CODFNC := aCamposFNC[1][2]
		EndIf
		If Len(aCamposFNC[1]) >= 3
			AB9->AB9_FNCREV := aCamposFNC[1][3]
		EndIf
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGrava o custo para o metodo de calculo custo historicoЁ
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	AA1->( dbSetOrder( 1 ) )
	If AA1->( DbSeek( xFilial( "AA1" ) + AB9->AB9_CODTEC ) )
		AB9->AB9_CUSTO := (HoraToInt(AB9->AB9_TOTFAT,3)*AA1->AA1_CUSTO)
	EndIf
	
	
	//-- Gera o registro de custo
	AB6->(dbSetOrder(1))
	AB6->(dbSeek(xFilial("AB6")+AB9->AB9_NUMOS))
	
	If AB9->AB9_CUSTO > 0
		cOrcamento := AB6->AB6_ORCAME		
		If !Empty(cOrcamento)
		
			cCodOs := SubStr(AB9->AB9_NUMOS,1,Len(AB9->AB9_NUMOS)-2)
			cItemOs := SubStr(AB9->AB9_NUMOS,Len(AB9->AB9_NUMOS)-1,2) 
			
			DbSelectArea('TEW')
			TEW->( DbSetOrder( 9 ) ) // TEW_FILIAL+TEW_NUMOS+TEW_ITEMOS
			If TEW->( DbSeek( xFilial('TEW')+cCodOs+cItemOs ) )
				cCodTFI := TEW->TEW_CODEQU		
				cProd   := TEW->TEW_PRODUT
				cCodTFL := Posicione( "TFI", 1, xFilial("TFI")+cCodTFI,"TFI_CODPAI" ) 
			EndIf
			
			If nOpcao == 1
				cCusto := At995Custo(cOrcamento,cCodTFI,cCodTFL,cProd,'4',AB9->AB9_CUSTO,'TECA460',.T.)
			ElseIf nOpcao == 2
				If !Empty(AB9->AB9_CODTWZ)
					At995ExcC(cOrcamento,AB9->AB9_CODTWZ,.T.)
				EndIf
				cCusto := At995Custo(cOrcamento,cCodTFI,cCodTFL,cProd,'4',AB9->AB9_CUSTO,'TECA460',.T.)
			EndIf
			
			If !Empty(cCusto)
				AB9->AB9_CODTWZ := cCusto					  
			EndIf
		EndIf	
	EndIf	
	MSMM(AB9->AB9_MEMO1,,,M->AB9_MEMO2,1,,,"AB9","AB9_MEMO1")

	AB9->( FKCommit() )
		
	//здддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Posiciona a base instalada                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддды
	AA3->( dbSetOrder( 4 ) )
	AA3->( DbSeek(xFilial("AA3")+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+;
		AB7->AB7_NUMSER))

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o Acumulador do Produto                      Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( AB9->AB9_ACUMUL > 0 )
		If ( AA3->(Found()) )
			RecLock("AA3")
			AA3->AA3_ACUMUL := Max(AB9->AB9_ACUMUL,AA3->AA3_ACUMUL)
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza as Preventivas atrasadas                                       Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( M->AB9_ATUPRE == "S" )
		dbSelectArea("AAJ")
		AAJ->(dbSetOrder(1))
		AAJ->(DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE))
		While ( !Eof() .And. xFilial("AAJ")    == AAJ->AAJ_FILIAL .And.;
				AA3->AA3_CTAPRE   == AAJ->AAJ_CTAPRE )
			If Empty(AAJ->AAJ_DTREAL) .And. ( AAJ->AAJ_DTPREV <= AB9->AB9_DTINI )
				RecLock("AAJ")
				AAJ->AAJ_DTREAL := AB9->AB9_DTINI
				AAJ->(MsUnLock())
			EndIf
			dbSelectArea("AAJ")
			AAJ->(dbSkip())
		End
	Else
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Limpa a data da realizaГЦo da preventiva, caso o usuАrio esteja    Ё
		//Ё alterando o atendimento de encerrado para aberto.				   Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lStatusAlt
			DbSelectArea("AAJ")
			DbSetOrder(1)
			If AAJ->(DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE))
				While ( !Eof() .And. xFilial("AAJ")    == AAJ->AAJ_FILIAL .And.;
					AA3->AA3_CTAPRE   == AAJ->AAJ_CTAPRE )
					If !Empty(AAJ->AAJ_DTREAL) .And. ( AAJ->AAJ_DTPREV == AB9->AB9_DTINI )
						RecLock("AAJ", .F.)
						AAJ->AAJ_DTREAL := cToD("")
						AAJ->(MsUnLock())
					EndIf
					DbSelectArea("AAJ")
					AAJ->(DbSkip())
				End
			EndIf
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAtualiza os itens em obsolesc┬ncia                                      Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( M->AB9_ATUOBS == "S" )
		dbSelectArea("AAK")
		AAK->(dbSetOrder(1))
		AAK->(DbSeek(xFilial("AAK")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO))
		While ( !Eof() .And. xFilial("AAK") == AAK->AAK_FILIAL .And.;
				AA3->AA3_CODFAB== AAK->AAK_CODFAB .And.;
				AA3->AA3_LOJAFA== AAK->AAK_LOJAFA .And.;
				AA3->AA3_CODPRO== AAK->AAK_CODPRO )
			If ( ItIsObsol() )
				dbSelectArea("AAL")
				AAL->(dbSetOrder(1))
				AAL->(DbSeek(xFilial("AAL")+AA3->AA3_CODFAB+AA3->AA3_LOJAFA+AA3->AA3_CODPRO+AA3->AA3_NUMSER+AAK->AAK_CODCOM))
				RecLock("AAL",!Found())
				AAL->AAL_FILIAL := xFilial("AAL")
				AAL->AAL_CODFAB := AA3->AA3_CODFAB
				AAL->AAL_LOJAFA := AA3->AA3_LOJAFA
				AAL->AAL_CODPRO := AA3->AA3_CODPRO
				AAL->AAL_NUMSER := AA3->AA3_NUMSER
				AAL->AAL_CODCOM := AAK->AAK_CODCOM
				AAL->AAL_ACUMUL := AA3->AA3_ACUMUL
				AAL->AAL_ACUTMP := M->AB9_DTINI
				AAL->AAL_DATA   := dDataBase
			EndIf
			dbSelectArea("AAK")
			dbSkip()
		End
	EndIf


	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se eh inclusao                                                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("AB9")
	AA9->(dbSetOrder(1))
	If ( DbSeek(xFilial("AB9")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ) )
		lInclui := .F.

		cNumOrc := AB9->AB9_NUMORC

	Else
		lInclui := .T.
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁTratamento para Orcamento                                               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	At400Monta()
	nPItemOrc := aScan(aHeaderAB5,{|x| AllTrim(x[2])=="AB5_SUBITE"})

	If !Empty( cNumOrc )

		dbSelectArea("AB5")
		AB5->(dbSetOrder(1))
		If ( DbSeek(xFilial("AB5")+cNumOrc+"01") )
			While ( !Eof() .And. xFilial("AB5") == AB5->AB5_FILIAL .And.;
					cNumOrc        == AB5->AB5_NUMORC .And.;
					"01"           == AB5->AB5_ITEM )
				aadd(aColsAB5[1],Array(Len(aHeaderAB5)+1))
				For nCntFor := 1 To Len(aHeaderAB5)
					If IsHeadRec(aHeaderAB5[nCntFor][2])
						aColsAB5[1][Len(aColsAB5[1])][nCntFor] := AB5->(RecNo())
					Elseif IsHeadAlias(aHeaderAB5[nCntFor][2])
						aColsAB5[1][Len(aColsAB5[1])][nCntFor] := "AB5"
					ElseIf ( aHeaderAB5[nCntFor][10] <> "V" )
						aColsAB5[1][Len(aColsAB5[1])][nCntFor] := FieldGet(FieldPos(aHeaderAB5[nCntFor][2]))
					Else
						aColsAB5[1][Len(aColsAB5[1])][nCntFor] := CriaVar(aHeaderAB5[nCntFor][2])
					EndIf
				Next nCntFor
				aColsAB5[1][Len(aColsAB5[1])][Len(aHeaderAB5)+1] := .F.
				dbSelectArea("AB5")
				dbSkip()
			End
		EndIf
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMarca todos os itens do Orcamento como deletados                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	For nCntFor := 1 To Len(aColsAB5[1])
		aColsAB5[1][nCntFor][Len(aHeaderAB5)+1] := .T.
	Next nCntFor

	//зддддддддддддддддддддддддддддддддддддддддддд©
	//ЁEfetua a Troca do Numero de Serie          Ё
	//юддддддддддддддддддддддддддддддддддддддддддды
	If ( !Empty(M->AB9_NUMSER) )
		dbSelectArea("AB7")
		AB7->(dbSetOrder(1))
		AB7->(DbSeek(xFilial("AB7")+M->AB9_NUMOS))
		RecLock("AB7")
		AB7->AB7_NUMSER := M->AB9_NUMSER
		dbSelectArea("AB8")
		AB8->(dbSetOrder(1))
		AB8->(DbSeek(xFilial("AB8")+M->AB9_NUMOS))
		While ( !Eof() .And. xFilial("AB8") == AB8_FILIAL .And.;
				M->AB9_NUMOS   == AB8->AB8_NUMOS+AB8->AB8_ITEM )
			RecLock("AB8")
			AB8->AB8_NUMSER := M->AB9_NUMSER
			dbSkip()
		End
		If ( !Empty(AB7->AB7_NUMORC) )
			dbSelectArea("AB4")
			AB4->(dbSetOrder(1))
			If ( DbSeek(xFilial("AB4")+AB7->AB7_NUMORC) )
				RecLock("AB4")
				AB4->AB4_NUMSER := M->AB9_NUMSER
				If ( !Empty(AB4->AB4_NRCHAM) )
					dbSelectArea("AB2")
					AB2->(dbSetOrder(1))
					If ( DbSeek(xFilial("AB2")+AB4->AB4_NRCHAM) )
						RecLock("AB2")
						AB2->AB2_NUMSER := M->AB9_NUMSER
					EndIf
				EndIf
			EndIf
		EndIf
		If ( !Empty(AB7->AB7_NRCHAM) )
			dbSelectArea("AB2")
			AB2->(dbSetOrder(1))
			If ( DbSeek(xFilial("AB2")+AB7->AB7_NRCHAM) )
				RecLock("AB2")
				AB2->AB2_NUMSER := M->AB9_NUMSER
			EndIf
		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁAjusta o aCols para gravar na tabela de saldos do grupo de cobertura  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	At460AjAco(AA3->AA3_CONTRT,M->AB9_NUMOS,M->AB9_CODTEC,M->AB9_SEQ)

	//здддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Insere os registros na tabela de saldo AAR Ё
	//юдддддддддддддддддддддддддддддддддддддддддддды
	AAQ->(DbSetOrder(1))
	AAR->(DbSetOrder(1))
	For nCntFor := 1 To Len(aCols)
		If Empty(aCols[nCntFor,GDFieldPos("ABA_PRDORI")]) .AND. Empty(aCols[nCntFor,GDFieldPos("ABA_QNTORI")])
			cCampo := "ABA_CODPRO"
			cCmpQtd := "ABA_QUANT"
		Else
			cCampo := "ABA_PRDORI"
			cCmpQtd := "ABA_QNTORI"
		EndIf
		If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]))
			AAR->(DbSetOrder(1))
			If AAQ->AAQ_MODLIB == "1"
				If AAR->(!dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And.;
					aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
					RecLock("AAR", .T.)
					AAR->AAR_FILIAL := xFilial("AAR")
					AAR->AAR_CONTRT := AAQ->AAQ_CONTRT
					AAR->AAR_CODPRO := AAQ->AAQ_CODPRO
					AAR->AAR_SALDO  := AAQ->AAQ_QTDLIB
					AAR->(MsUnLock())
				EndIf
			ElseIf AAQ->AAQ_MODLIB == "2"
				If AAR->(!dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6))) .And.;
					aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
					RecLock("AAR", .T.)
					AAR->AAR_FILIAL := xFilial("AAR")
					AAR->AAR_CONTRT := AAQ->AAQ_CONTRT
					AAR->AAR_CODPRO := AAQ->AAQ_CODPRO
					AAR->AAR_SALDO  := AAQ->AAQ_QTDLIB
					AAR->AAR_PERIOD := LEFT(DTOS(AB9->AB9_DTFIM),6)
					AAR->(MsUnLock())
				EndIf
			EndIf
		EndIf
	Next nCntFor
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGrava o saldo das quantidades de cada item do contrato                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	For nCntFor := 1 To Len(aColsAB5[1])
		aColsAB5[1][nCntFor][Len(aHeaderAB5)+1] := .T.
	Next nCntFor
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGrava os Itens do Atendimento da OS                                     Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("ABA")
	For nCntFor := 1 To Len(aCols)

		lGravaSD3	:= .T.
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPosiciona Registro                                                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("AB7")
		AB7->(dbSetOrder(1))
		AB7->(DbSeek(xFilial("AB7")+M->AB9_NUMOS))

		dbSelectArea("AB6")
		AB6->(dbSetOrder(1))
		AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))

		If ( !aCols[nCntFor][nUsado+1] .And. !Empty(aCols[nCntFor][nPosProd]) )

			dbSelectArea("ABA")
			ABA->(dbSetOrder(1))

			If ( DbSeek(xFilial("ABA")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ+aCols[nCntFor][nPosItem]) )

				If !Inclui

					If !Empty( ABA->ABA_SEQRC )
						//здддддддддддддддддддддддддддддддддд©
						//Ё Se possuia solicitacao anterior  Ё
						//юдддддддддддддддддддддддддддддддддды
						ABG->( dbSetOrder( 1 ) )
						If ABG->( dbSeek( xFilial("ABG")+M->AB9_NUMOS+ABA->ABA_SEQRC+;
								ABA->ABA_ITEMRC ) )
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Exclui a associacao da sequencia de atendimento com a solicitacao    Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							RecLock( "ABG", .F. )
							ABG->ABG_SEQ    := CriaVar( "ABG_SEQ",    .F. )
							ABG->ABG_CODTEC := CriaVar( "ABG_CODTEC", .F. )
							ABG->( MsUnLock() )
							ABG->( FKCommit() )
						EndIf
					EndIf
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁEstorna os movimentos de estoque                                        Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("AA5")
				AA5->(dbSetOrder(1))
				AA5->(DbSeek(xFilial("AA5")+ABA->ABA_CODSER))
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁMovimenta o Estoque pelo SD3                                            Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If Len(aColsAnt) >= nCntFor
					lGravaSD3 := !fCompArray(aCols[nCntFor],aColsAnt[nCntFor])
					If lGravaSD3
						At460Trans(.t.)
					EndIf
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁEstorna movimento gerado pela Ordem de Servico                          Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( !Empty(ABA->ABA_SUBOS) )
					dbSelectArea("AB8")
					AB8->(dbSetOrder(1))
					If ( DbSeek(xFilial("AB8")+ABA->ABA_NUMOS+ABA->ABA_SUBOS) )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Estorna os Valores - Somente necessario quando aglutinacao             Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If !Empty( cAgluPrd )
							RecLock("AB8")
							AB8->AB8_TOTAL  -= At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*ABA->ABA_QUANT
							AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
							AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
						EndIf
					EndIf
				EndIf
				RecLock("ABA",.F.)
			Else

				dbSelectArea("AA5")
				AA5->(dbSetOrder(1))

				AA5->(DbSeek(xFilial("AA5") + GDFieldGet( "ABA_CODSER", nCntFor ) ))

				RecLock("ABA",.T.)

			EndIf

			ABA->(FKCommit())
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Atualiza o saldo dos produtos que fazem parte do grupo de cobertura no contrato|
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды


			If Empty(aCols[nCntFor,GDFieldPos("ABA_PRDORI")]) .AND. Empty(aCols[nCntFor,GDFieldPos("ABA_QNTORI")])
				cCampo := "ABA_CODPRO"
				cCmpQtd := "ABA_QUANT"
			Else
				cCampo := "ABA_PRDORI"
				cCmpQtd := "ABA_QNTORI"
			EndIf

			AAQ->(DbSetOrder(1))
			AAR->(DbSetOrder(1))
			If Altera .And. Len(aColsAnt) >= nCntFor
				If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aColsAnt[nCntFor,GDFieldPos(cCampo)])) .And. !(aCols[nCntFor][nUsado+1])
					If AAQ->AAQ_MODLIB == "1"
						If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAnt[nCntFor,GDFieldPos(cCampo)])) .And.;
							aColsAnt[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
							RecLock("AAR",.F.)
							AAR->AAR_SALDO += aColsAnt[nCntFor,GDFieldPos(cCmpQtd)]
							AAR->(MsUnlock())
						EndIf
					ElseIf AAQ->AAQ_MODLIB == "2"
						If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aColsAnt[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6))) .And.;
							aColsAnt[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
							RecLock("AAR",.F.)
							AAR->AAR_SALDO += aColsAnt[nCntFor,GDFieldPos(cCmpQtd)]
							AAR->(MsUnlock())
						EndIf
					Endif
				Endif
				If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And.;
					AtBuscServ(AA3->AA3_CONTRT,.T.) == aCols[nCntFor,GDFieldPos("ABA_CODSER")]
					If AAQ->AAQ_MODLIB == "1"
						If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]))
							RecLock("AAR",.F.)
							AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aCols[nCntFor,GDFieldPos(cCmpQtd)])
							AAR->(MsUnlock())
							AAR->(FKCommit())
						EndIf
					ElseIf AAQ->AAQ_MODLIB == "2"
						If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6)))
							RecLock("AAR",.F.)
							AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aCols[nCntFor,GDFieldPos(cCmpQtd)])
							AAR->(MsUnlock())
							AAR->(FKCommit())
						EndIf
					EndIf
				EndIf
			ElseIf Inclui .Or. (Altera .And. Len(aColsAnt) < nCntFor)
					If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And.;
						AtBuscServ(AA3->AA3_CONTRT,.T.) == aCols[nCntFor,GDFieldPos("ABA_CODSER")]
						If AAQ->AAQ_MODLIB == "1"
							If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]))
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += ABA->ABA_QUANT
								AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aCols[nCntFor,GDFieldPos(cCmpQtd)])
								AAR->(MsUnlock())
								AAR->(FKCommit())
							EndIf
						ElseIf AAQ->AAQ_MODLIB == "2"
							If AAR->(dbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6)))
								RecLock("AAR",.F.)
								AAR->AAR_SALDO += ABA->ABA_QUANT
								AAR->AAR_SALDO := ABS(AAR->AAR_SALDO - aCols[nCntFor,GDFieldPos(cCmpQtd)])
								AAR->(MsUnlock())
								AAR->(FKCommit())
							EndIf
						EndIf
					EndIf
	   		EndIf

			For nCntFor2 := 1 To nUsado
				If ( aHeader[nCntFor2][10] <> "V" )
					ABA->(FieldPut(FieldPos(aHeader[nCntFor2][2]),aCols[nCntFor][nCntFor2]))
				EndIf
			Next nCntFor2
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava os campos fixos                                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			ABA->ABA_FILIAL   := xFilial("ABA")
			ABA->ABA_NUMOS    := M->AB9_NUMOS
			ABA->ABA_CODTEC   := M->AB9_CODTEC
			ABA->ABA_SEQ      := M->AB9_SEQ

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava o contrato de manutencao e o grupo de cobertura    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			dbSelectArea("AB7")
			AB7->(dbSetOrder(1))
			AB7->(DbSeek(xFilial("AB7")+ABA->ABA_NUMOS))

			DbSelectArea("AA3")
   			AA3->(DbSetOrder(6))
    		AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))

    		DbSelectArea("AAH")
    		AAH->(DbSetOrder(1))
    		AAH->(DbSeek(xFilial("AAH")+AA3->AA3_CONTRT))


   			ABA->ABA_CONTRT := AAH->AAH_CONTRT
    		ABA->ABA_CODGRP := AAH->AAH_CODGRP


			If ( AA5->AA5_ATUEST == "S" )

				/*
				//здддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁGrava dados de Localizacao Destino - Pecas RuinsЁ
				//юдддддддддддддддддддддддддддддддддддддддддддддддды
				*/

				ABA->ABA_LOCALD := AA1->AA1_LOCAL
				ABA->ABA_LOCLZD := AA1->AA1_LOCLZR

			ElseIf ( SF4->F4_ESTOQUE == "S" )

				/*
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁGrava dados de Localizacao Destino - Pecas FaturamentoЁ
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
				*/

				ABA->ABA_LOCALD := AA1->AA1_LOCAL
				ABA->ABA_LOCLZD := AA1->AA1_LOCLZF

			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁGrava o custo para o metodo de calculo custo historicoЁ
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SB1->( dbSetOrder( 1 ) )
			If SB1->( DbSeek( xFilial( "SB1" ) + aCols[nCntFor][nPosProd]) )
				nMoeda         := If(Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))==0,1,Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")))
				ABA->ABA_CUSTO := (ABA->ABA_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),nMoeda,1))
			EndIf

		    cSeqRC := GDFieldGet( "ABA_SEQRC", nCntFor )

			If !Empty( cSeqRC )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Associa a sequencia de atendimento com a solicitacao ao almoxarifado   Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				ABG->( dbSetOrder( 1 ) )
				If ABG->( dbSeek( xFilial( "ABF" ) + M->AB9_NUMOS + cSeqRC + ;
						GDFieldGet( "ABA_ITEMRC", nCntFor ) ) )
					RecLock( "ABG", .F. )
					ABG->ABG_CODTEC := M->AB9_CODTEC
					ABG->ABG_SEQ    := M->AB9_SEQ
					ABG->( MsUnLock() )
					ABG->( FKCommit() )
				EndIf
			EndIf

			dbSelectArea("AA5")
			AA5->(dbSetOrder(1))
			AA5->(DbSeek(xFilial("AA5")+ABA->ABA_CODSER))

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁCria o Almoxarifado se nao exitir.                                      Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			dbSelectArea("SF4")
			SF4->(dbSetOrder(1))
			If ( DbSeek(xFilial("SF4")+AA5->AA5_TES) .And. SF4->F4_ESTOQUE=="S" )
				CriaSB2(ABA->ABA_CODPRO,ABA->ABA_LOCAL)
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁMovimenta o Estoque pelo SD3                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			If lGravaSD3
				At460Trans(.F.)
			EndIf

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAtualiza o Numero de Serie dos Acessorios                 Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( !Empty(ABA->ABA_CODPRO) .And. !Empty(ABA->ABA_NUMSER) )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Pesquisa o Codigo Anterior para ser substituido pelo novoЁ
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("AA4")
				AA4->(dbSetOrder(4))
				If ( DbSeek(xFilial("AA4")+ABA->ABA_FABANT+ABA->ABA_LOJANT+ABA->ABA_ANTPRO+ABA->ABA_ANTSER) )
					RecLock("AA4")
				Else
					dbSelectArea("AA4")
					AA4->(dbSetOrder(4))
					If ( DbSeek(xFilial("AA4")+ABA->ABA_CODFAB+ABA->ABA_LOJAFA+ABA->ABA_CODPRO+ABA->ABA_NUMSER) )
						RecLock("AA4",.F.)
					Else
						RecLock("AA4",.T.)
					EndIf
				EndIf
				AA4->AA4_FILIAL := xFilial("AA4")
				AA4->AA4_CODCLI := AB6->AB6_CODCLI
				AA4->AA4_LOJA   := AB6->AB6_LOJA
				AA4->AA4_CODPRO := AB7->AB7_CODPRO
				AA4->AA4_NUMSER := AB7->AB7_NUMSER
				AA4->AA4_PRODAC := ABA->ABA_CODPRO
				AA4->AA4_NSERAC := ABA->ABA_NUMSER
				AA4->AA4_CODFAB := ABA->ABA_CODFAB
				AA4->AA4_LOJAFA := ABA->ABA_LOJAFA
				AA4->AA4_DTINST := dDataBase
				AA4->AA4_DTGAR  := &( cMV_FORMGAR )
			EndIf
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAtualiza os sub-itens da Ordem de Servico                               Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( AA5->AA5_ATUOS == "S" )
				If ( Empty(cAgluPrd) )
					dbSelectArea("SB1")
					dbSetOrder(1)
					DbSeek(xFilial("SB1")+ABA->ABA_CODPRO)
					cDesPro := ABA->ABA_DESCRI
					dbSelectArea("AB8")
					dbSetOrder(1)
					If ( !DbSeek(xFilial("AB8")+ABA->ABA_NUMOS+ABA->ABA_SUBOS) )
						cSubItem := cSubItem := At460SubIt(M->AB9_NUMOS)
						RecLock("AB8",.T.)
					Else
						cSubItem := AB8->AB8_SUBITE
						RecLock("AB8",.F.)
					EndIf
				Else
					dbSelectArea("SB1")
					SB1->(dbSetOrder(1))
					SB1->(DbSeek(xFilial("SB1")+cAgluPrd))
					cDesPro := SB1->B1_DESC
					
					dbSelectArea("AB8")
					AB8->(dbSetOrder(1))
					If ( !DbSeek(xFilial("AB8")+ABA->ABA_NUMOS+ABA->ABA_SUBOS) )

						lFoundAB8 := .F.

						AB8->( dbSeek(xFilial("AB8")+ABA->ABA_NUMOS ) )
						While !AB8->( Eof() ) .And. AB8->AB8_FILIAL + AB8->AB8_NUMOS + ;
							AB8->AB8_ITEM == xFilial("AB8")+ABA->ABA_NUMOS

							If ABA->ABA_CODSER + cAgluPrd == AB8->AB8_CODSER + AB8->AB8_CODPRO
								lFoundAB8 := .T.
								Exit
							EndIf
							AB8->( dbSkip() )

						End

						If lFoundAB8
							cSubItem := AB8->AB8_SUBITE
							RecLock("AB8",.F.)
						Else
							cSubItem := At460SubIt(M->AB9_NUMOS)
							RecLock("AB8",.T.)
						EndIf

					Else
						cSubItem := AB8->AB8_SUBITE
						RecLock("AB8",.F.)
					EndIf
				EndIf
				AB8->AB8_FILIAL := xFilial("AB8")
				AB8->AB8_NUMOS  := AB7->AB7_NUMOS
				AB8->AB8_ITEM   := AB7->AB7_ITEM
				AB8->AB8_SUBITE := cSubItem
				AB8->AB8_CODPRO := SB1->B1_COD
				AB8->AB8_LOCAL  := If( Empty( ABA->ABA_LOCALD ),ABA->ABA_LOCAL, ABA->ABA_LOCALD )
				AB8->AB8_LOCALI := ABA->ABA_LOCALI
				AB8->AB8_DESPRO := cDesPro
				AB8->AB8_CODSER := ABA->ABA_CODSER
				AB8->AB8_ENTREG := M->AB9_DTFIM
				If ( Empty(cAgluPrd) )

					AB8->AB8_PRCLIS := At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
					AB8->AB8_QUANT  := ABA->ABA_QUANT
					If Empty( AB8->AB8_VUNIT ) .OR. Altera
						AB8->AB8_VUNIT := AB8->AB8_PRCLIS
					EndIf
					AB8->AB8_TOTAL  := ABA->ABA_QUANT*AB8->AB8_VUNIT

				Else
					AB8->AB8_TOTAL  += At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*ABA->ABA_QUANT
					AB8->AB8_QUANT  := 1
					AB8->AB8_PRCLIS := AB8->AB8_TOTAL
					AB8->AB8_VUNIT  := AB8->AB8_TOTAL
				EndIf
				AB8->AB8_CODCLI := AB6->AB6_CODCLI
				AB8->AB8_LOJA   := AB6->AB6_LOJA
				AB8->AB8_CODPRD := AB7->AB7_CODPRO
				AB8->AB8_NUMSER := AB7->AB7_NUMSER
				AB8->AB8_DTGAR  := &(cMV_FORMGAR)

				AB8->( FKCommit() )

				//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza Amarracao                                Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддды
				RecLock("ABA")
				ABA->ABA_SUBOS := AB8->AB8_SUBITE
			EndIf

			If ( AA5->AA5_ATUORC=="S" )
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Orcamento                                           Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
				cItemOrc := Soma1(cItemOrc,Len(AB5->AB5_SUBITE))
				nItemOrc := aScan(aColsAB5[1],{|x| x[nPItemOrc]==cItemOrc})
				If ( nItemOrc == 0 )
					aadd(aColsAB5[1],Array(Len(aHeaderAB5)+1))
					nItemOrc := Len(aColsAB5[1])
				EndIf
				For nCntFor2 := 1 To Len(aHeaderAB5)
					Do Case
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_SUBITE"
						aColsAB5[1][nItemOrc][nCntFor2] := cItemOrc
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := ABA->ABA_CODPRO
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_DESPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := ABA->ABA_DESCRI
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODSER"
						aColsAB5[1][nItemOrc][nCntFor2] := ABA->ABA_CODSER
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_QUANT"
						aColsAB5[1][nItemOrc][nCntFor2] := ABA->ABA_QUANT
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_VUNIT"
						aColsAB5[1][nItemOrc][nCntFor2] := At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_PRCLIS"
						aColsAB5[1][nItemOrc][nCntFor2] := At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_TOTAL"
						aColsAB5[1][nItemOrc][nCntFor2] := At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*ABA->ABA_QUANT
					OtherWise
						If !IsHeadRec(aHeaderAb5[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb5[nCntFor2][2])
							aColsAB5[1][nItemOrc][nCntFor2] := CriaVar(aHeaderAb5[nCntFor2][2])
						Endif
					EndCase
				Next nCntFor2
				aColsAB5[1][nItemOrc][Len(aHeaderAB5)+1] := .F.
			EndIf

		Else
			dbSelectArea("ABA")
			ABA->(dbSetOrder(1))
			If ( DbSeek(xFilial("ABA")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ+aCols[nCntFor][nPosItem]) )

				If !Empty( ABA->ABA_SEQRC )
					//здддддддддддддддддддддддддддддддддд©
					//Ё Se possuia solicitacao anterior  Ё
					//юдддддддддддддддддддддддддддддддддды
					ABG->( dbSetOrder( 1 ) )
					If ABG->( dbSeek( xFilial("ABG")+M->AB9_NUMOS+ABA->ABA_SEQRC+;
							ABA->ABA_ITEMRC ) )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Exlclui a associacao da sequencia de atendimento com a solicitacao   Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						RecLock( "ABG", .F. )
						ABG->ABG_SEQ    := CriaVar( "ABG_SEQ",    .F. )
						ABG->ABG_CODTEC := CriaVar( "ABG_CODTEC", .F. )
						ABG->( MsUnLock() )
						ABG->( FKCommit() )
					EndIf
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁEstorna os movimentos de estoque                                        Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("AA5")
				AA5->(dbSetOrder(1))
				AA5->(DbSeek(xFilial("AA5")+ABA->ABA_CODSER))
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁMovimenta o Estoque pelo SD3                                            Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

				At460Trans(.t.)

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁEstorna movimento gerado pela Ordem de Servico                          Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				If ( !Empty(ABA->ABA_SUBOS) )
					dbSelectArea("AB8")
					AB8->(dbSetOrder(1))
					If ( DbSeek(xFilial("AB8")+ABA->ABA_NUMOS+ABA->ABA_SUBOS) )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Estorna os Valores - Somente necessario quando aglutinacao             Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						RecLock("AB8")

						If Empty( cAgluPrd )
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Nao utiliza aglutinacao de produtos                                    Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							AB8->AB8_QUANT  -= ABA->ABA_QUANT
							AB8->AB8_TOTAL  := AB8->AB8_QUANT * AB8->AB8_VUNIT
						Else
							AB8->AB8_TOTAL  -= At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*ABA->ABA_QUANT
							AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
							AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
						EndIf

						If ( AB8->AB8_TOTAL <= 0 )
							AB8->( dbDelete() )
						EndIf

						AB8->( FkCommit() )

					EndIf
				EndIf

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Estorna as quantidade dos produtos para tabela de saldos Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

				AAQ->(DbSetOrder(1))
				AAR->(DbSetOrder(1))
				If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And. aCols[nCntFor][nUsado+1]
					If AAQ->AAQ_MODLIB == "1"
						If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And.;
							aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
							RecLock("AAR",.F.)
							AAR->AAR_SALDO += aCols[nCntFor,GDFieldPos(cCmpQtd)]
							AAR->(MsUnlock())
						EndIf
					ElseIf AAQ->AAQ_MODLIB == "2"
						If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6))) .And.;
							aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
							RecLock("AAR",.F.)
							AAR->AAR_SALDO += aCols[nCntFor,GDFieldPos(cCmpQtd)]
							AAR->(MsUnlock())
						EndIf
					EndIf
				EndIf
				RecLock("ABA")
				ABA->( dbDelete() )
			EndIf
		EndIf

		ABA->( FKCommit() )

	Next nCntFor

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Se existir codigo de servico no grupo de cobertura utiliza este        Ё
	//Ё caso contrario mantem o codigo de servico do parametro                 Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	cServCob := AtBaseServ(AB7->AB7_CODFAB,AB7->AB7_LOJAFA,AB7->AB7_CODPRO,AB7->AB7_NUMSER,cProdMod)

	cServMod := If( Empty( cServCob ), cServMod, cServCob )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁEfetua a Gravacao das Horas apontadas                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( !Empty(cProdMod) .And. !Empty(cServMod) )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPosiciona Registro                                                      Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("AB7")
		AB7->(dbSetOrder(1))
		AB7->(DbSeek(xFilial("AB7")+M->AB9_NUMOS))

		dbSelectArea("AB6")
		AB6->(dbSetOrder(1))
		AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))

		dbSelectArea("AA5")
		AA5->(dbSetOrder(1))
		AA5->(DbSeek(xFilial("AA5")+cServMod))

		dbSelectArea("AA1")
		AA1->(dbSetOrder(1))
		AA1->(DbSeek(xFilial("AA1")+M->AB9_CODTEC))

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Considera o valor hora pelo valor do tecnico ou pela tabela de precos  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If lAtHrPr
			nVlHoraEst := MaTabPrVen("1", AB7->AB7_CODPRO, HoraToInt(nTotFatAnt),AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
			nVlHoraAtu := MaTabPrVen("1", AB7->AB7_CODPRO, HoraToInt(AB9->AB9_TOTFAT),AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
		Else
			nVlHoraEst := xMoeda(AA1->AA1_VALOR,1,AB6->AB6_MOEDA)
			nVlHoraAtu := xMoeda(AA1->AA1_VALOR,1,AB6->AB6_MOEDA)
		EndIf

		dbSelectArea("SB1")
		SB1->(dbSetOrder(1))
		SB1->(DbSeek(xFilial("SB1")+cProdMod))
		cDesPro  := SB1->B1_DESC
		cArmazen := SB1->B1_LOCPAD
		If ( AA5->(Found()) .And. SB1->(Found()) .And. !Empty(nVlHoraAtu) )

			If ( AA5->AA5_ATUOS=="S" )
				AB8->(dbSelectArea("AB8"))
				AB8->(dbSetOrder(1))
				If AB8->( DbSeek(xFilial("AB8")+M->AB9_NUMOS+StrZero(0,TamSX3("AB8_SUBITE")[1])) )
					RecLock("AB8",.F.)
					If ( !lInclui )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//ЁEstorna o Valores lancados anteriormente                                Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						AB8->AB8_QUANT    -= HoraToInt(nTotFatAnt)
						AB8->AB8_TOTAL    -= NoRound(HoraToInt(nTotFatAnt)*nVlHoraEst,2)

						AB8->AB8_PRCLIS   := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
						AB8->AB8_VUNIT    := AB8->AB8_PRCLIS
					EndIf								
				Else
					RecLock("AB8",.T.)				
				EndIf									
					AB8->AB8_FILIAL   := xFilial("AB8")
					AB8->AB8_NUMOS    := AB6->AB6_NUMOS
					AB8->AB8_ITEM     := AB7->AB7_ITEM
					AB8->AB8_SUBITE := StrZero(0,TamSX3("AB8_SUBITE")[1])
					AB8->AB8_CODPRO   := cProdMod
					AB8->AB8_LOCAL    := cArmazen
					AB8->AB8_CODSER   := cServMod
					AB8->AB8_DESPRO   := cDesPro 
					AB8->AB8_QUANT    += HoraToInt(M->AB9_TOTFAT,3)
					AB8->AB8_TOTAL    += NoRound(HoraToInt(M->AB9_TOTFAT,3)*nVlHoraAtu,2)
					AB8->AB8_PRCLIS   := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
					AB8->AB8_VUNIT    := AB8->AB8_PRCLIS
					AB8->AB8_CODCLI   := AB6->AB6_CODCLI
					AB8->AB8_LOJA     := AB6->AB6_LOJA
					AB8->AB8_CODPRD   := AB7->AB7_CODPRO
					AB8->AB8_NUMSER   := AB7->AB7_NUMSER
					AB8->AB8_LOCALI   := ABA->ABA_LOCALI
					AB8->AB8_DTGAR    := &(SuperGetMv("MV_FORMGAR"))		
			EndIf

			If ( AA5->AA5_ATUORC=="S" )
				cItemOrc := "00"
				nItemOrc := aScan(aColsAB5[1],{|x| x[nPItemOrc]==cItemOrc})
				If ( nItemOrc == 0 )
					aadd(aColsAB5[1],Array(Len(aHeaderAB5)+1))
					nItemOrc := Len(aColsAB5[1])
				EndIf
				For nCntFor2 := 1 To Len(aHeaderAB5)
					Do Case
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_SUBITE"
						aColsAB5[1][nItemOrc][nCntFor2] := "00"
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := cProdMod
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_DESPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := SB1->B1_DESC
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODSER"
						aColsAB5[1][nItemOrc][nCntFor2] := cServMod
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_QUANT"
						aColsAB5[1][nItemOrc][nCntFor2] := HoraToInt(M->AB9_TOTFAT,3)
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_VUNIT"
						aColsAB5[1][nItemOrc][nCntFor2] := nVlHoraAtu
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_PRCLIS"
						aColsAB5[1][nItemOrc][nCntFor2] := nVlHoraAtu
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_TOTAL"
						aColsAB5[1][nItemOrc][nCntFor2] := NoRound(HoraToInt(M->AB9_TOTFAT,3)*nVlHoraAtu,2)
					OtherWise
						If !IsHeadRec(aHeaderAB5[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAB5[nCntFor2][2])
							aColsAB5[1][nItemOrc][nCntFor2] := CriaVar(aHeaderAb5[nCntFor2][2])
						Endif
					EndCase
				Next nCntFor2
				aColsAB5[1][nItemOrc][Len(aHeaderAB5)+1] := .F.
			EndIf

		EndIf
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGrava a Tabela ABC (Despesas do Atendimento)                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("ABC")
	nPosProd := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODPRO"})
	nPosItem := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_ITEM"})
	nUsado   := Len(aHeaderABC)
	For nCntFor := 1 To Len(aColsABC)
		If ( !aColsABC[nCntFor][nUsado+1] .And. !Empty(aColsABC[nCntFor][nPosProd]) )
			dbSelectArea("ABC")
			ABC->(dbSetOrder(1))
			If ( DbSeek(xFilial("ABC")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ+aColsABC[nCntFor][nPosItem]) )
				If ( !Empty(ABC->ABC_SUBOS) )
					dbSelectArea("AB8")
					AB8->(dbSetOrder(1))
					If ( DbSeek(xFilial("AB8")+ABC->ABC_NUMOS+ABC->ABC_SUBOS) )
						RecLock("AB8")

						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Caso utilize aglutinador, estorna os valores                           Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						If !Empty( cProdDes )
							AB8->AB8_TOTAL  -= ABC->ABC_VALOR
							AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
							AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
						EndIf
					EndIf
				EndIf
				RecLock("ABC",.F.)
			Else
				RecLock("ABC",.T.)
			EndIf
			For nCntFor2 := 1 To nUsado
				If ( aHeaderABC[nCntFor2][10] <> "V" )
					ABC->(FieldPut(FieldPos(aHeaderABC[nCntFor2][2]),aColsABC[nCntFor][nCntFor2]))
				EndIf
			Next nCntFor2
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava os campos fixos                                    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			ABC->ABC_FILIAL   := xFilial("ABC")
			ABC->ABC_NUMOS    := M->AB9_NUMOS
			ABC->ABC_CODTEC   := M->AB9_CODTEC
			ABC->ABC_SEQ      := M->AB9_SEQ

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Grava o contrato de manutencao e o grupo de cobertura    Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			dbSelectArea("AB7")
			AB7->(dbSetOrder(1))
			AB7->(DbSeek(xFilial("AB7")+ABC->ABC_NUMOS))

			DbSelectArea("AA3")
   			AA3->(DbSetOrder(6))
    		AA3->(DbSeek(xFilial("AA3")+AB7->AB7_NUMSER))

    		DbSelectArea("AAH")
    		AAH->(DbSetOrder(1))
    		AAH->(DbSeek(xFilial("AAH")+AA3->AA3_CONTRT))


   			ABC->ABC_CONTRT := AAH->AAH_CONTRT
    		ABC->ABC_CODGRP := AAH->AAH_CODGRP


			//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁGrava o custo para o metodo de calculo custo historicoЁ
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
			SB1->( dbSetOrder( 1 ) )
			If SB1->( DbSeek( xFilial( "SB1" ) + aColsABC[nCntFor][nPosProd]) )
				nMoeda := If(Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD"))==0,1,Val(RetFldProd(SB1->B1_COD,"B1_MCUSTD")))

				If Empty( RetFldProd(SB1->B1_COD,"B1_CUSTD") )
					ABC->ABC_CUSTO := xMoeda(ABC->ABC_VALOR,AB6->AB6_MOEDA,1)
				Else
					ABC->ABC_CUSTO := (ABC->ABC_QUANT*xMoeda(RetFldProd(SB1->B1_COD,"B1_CUSTD"),nMoeda,1))
				EndIf

			EndIf

			dbSelectArea("AA5")
			AA5->(dbSetOrder(1))
			AA5->(DbSeek(xFilial("AA5")+ABC->ABC_CODSER))
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁAtualiza os sub-itens da Ordem de Servico                               Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( AA5->AA5_ATUOS == "S" )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//ЁPosiciona Registro                                                      Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dbSelectArea("AB7")
				AB7->(dbSetOrder(1))
				AB7->(DbSeek(xFilial("AB7")+M->AB9_NUMOS))

				dbSelectArea("AB6")
				AB6->(dbSetOrder(1))
				AB6->(DbSeek(xFilial("AB6")+AB7->AB7_NUMOS))

				If ( Empty(cProdDes) )
					dbSelectArea("SB1")
					SB1->(dbSetOrder(1))
					SB1->(DbSeek(xFilial("SB1")+ABC->ABC_CODPRO))
					cDesPro := SB1->B1_DESC
					dbSelectArea("AB8")
					AB8->(dbSetOrder(1))
					If ( !DbSeek(xFilial("AB8")+ABC->ABC_NUMOS+ABC->ABC_SUBOS) )
						cSubItem := At460SubIt(M->AB9_NUMOS)
						RecLock("AB8",.T.)
					Else
						cSubItem := AB8->AB8_SUBITE
						RecLock("AB8",.F.)
					EndIf
				Else
					dbSelectArea("SB1")
					dbSetOrder(1)
					DbSeek(xFilial("SB1")+cProdDes)
					cDesPro := SB1->B1_DESC
					dbSelectArea("AB8")
					dbSetOrder(1)
					If ( !DbSeek(xFilial("AB8")+ABC->ABC_NUMOS+ABC->ABC_SUBOS) )

						lFoundAB8 := .F.

						AB8->( dbSeek(xFilial("AB8")+ABC->ABC_NUMOS ) )
						While !AB8->( Eof() ) .And. AB8->AB8_FILIAL + AB8->AB8_NUMOS + ;
							AB8->AB8_ITEM == xFilial("AB8")+ABC->ABC_NUMOS

							If ABC->ABC_CODSER + cProdDes == AB8->AB8_CODSER + AB8->AB8_CODPRO
								lFoundAB8 := .T.
								Exit
							EndIf
					    	AB8->( dbSkip() )

						End

						If lFoundAB8
							cSubItem := AB8->AB8_SUBITE
							RecLock("AB8",.F.)
						Else
							cSubItem := At460SubIt(M->AB9_NUMOS)
							RecLock("AB8",.T.)
						EndIf

					Else
						cSubItem := AB8->AB8_SUBITE
						RecLock("AB8",.F.)
					EndIf
				EndIf
				AB8->AB8_FILIAL := xFilial("AB8")
				AB8->AB8_NUMOS  := AB7->AB7_NUMOS
				AB8->AB8_ITEM   := AB7->AB7_ITEM
				AB8->AB8_SUBITE := cSubItem
				AB8->AB8_CODPRO := SB1->B1_COD
				AB8->AB8_LOCAL  := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
				AB8->AB8_DESPRO := cDesPro
				AB8->AB8_CODSER := ABC->ABC_CODSER
				AB8->AB8_ENTREG := M->AB9_DTFIM
				If ( Empty(cProdDes) )
					AB8->AB8_PRCLIS := ABC->ABC_VLUNIT
					AB8->AB8_VUNIT  := ABC->ABC_VLUNIT
					AB8->AB8_TOTAL  := ABC->ABC_QUANT*ABC->ABC_VLUNIT
					AB8->AB8_QUANT  := ABC->ABC_QUANT
				Else
					AB8->AB8_QUANT  := 1
					AB8->AB8_PRCLIS += ABC->ABC_VALOR
					AB8->AB8_VUNIT  += ABC->ABC_VALOR
					AB8->AB8_TOTAL  += ABC->ABC_VALOR
				EndIf
				AB8->AB8_CODCLI := AB6->AB6_CODCLI
				AB8->AB8_LOJA   := AB6->AB6_LOJA
				AB8->AB8_CODPRD := AB7->AB7_CODPRO
				AB8->AB8_NUMSER := AB7->AB7_NUMSER
				AB8->AB8_LOCALI := ABA->ABA_LOCALI

				//зддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Atualiza Amarracao                                Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддды
				RecLock("ABC")
				ABC->ABC_SUBOS := AB8->AB8_SUBITE
			EndIf

			//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Orcamento                                           Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( AA5->AA5_ATUORC=="S" )
				cItemOrc := Soma1(cItemOrc,Len(AB5->AB5_SUBITE))
				nItemOrc := aScan(aColsAB5[1],{|x| x[nPItemOrc]==cItemOrc})
				If ( nItemOrc == 0 )
					aadd(aColsAB5[1],Array(Len(aHeaderAB5)+1))
					nItemOrc := Len(aColsAB5[1])
				EndIf
				For nCntFor2 := 1 To Len(aHeaderAB5)
					Do Case
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_SUBITE"
						aColsAB5[1][nItemOrc][nCntFor2] := cItemOrc
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_CODPRO
					Case Alltrim(aHeaderAb5[nCntFor2][2])=="AB5_DESPRO"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_DESCRI
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_CODSER"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_CODSER
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_QUANT"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_QUANT
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_VUNIT"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_VLUNIT
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_TOTAL"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_VALOR
					Case AllTrim(aHeaderAb5[nCntFor2][2])=="AB5_PRCLIS"
						aColsAB5[1][nItemOrc][nCntFor2] := ABC->ABC_VLUNIT
					OtherWise
						If !IsHeadRec(aHeaderAb5[nCntFor2][2]) .AND. !IsHeadAlias(aHeaderAb5[nCntFor2][2])
							aColsAB5[1][nItemOrc][nCntFor2] := CriaVar(aHeaderAb5[nCntFor2][2])
						Endif
					EndCase
				Next nCntFor2
				aColsAB5[1][nItemOrc][Len(aHeaderAB5)+1] := .F.
			EndIf

		Else
			dbSelectArea("ABC")
			dbSetOrder(1)
			If ( DbSeek(xFilial("ABC")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ+aColsABC[nCntFor][nPosItem]) )
				If ( !Empty(ABC->ABC_SUBOS) )
					dbSelectArea("AB8")
					dbSetOrder(1)
					If ( DbSeek(xFilial("AB8")+ABC->ABC_NUMOS+ABC->ABC_SUBOS) )
						RecLock("AB8")

						If Empty( cProdDes )
							//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
							//Ё Nao utiliza aglutinacao de produtos                                    Ё
							//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
							AB8->AB8_QUANT  -= ABC->ABC_QUANT
							AB8->AB8_TOTAL  := AB8->AB8_QUANT * AB8->AB8_VUNIT
						Else
							AB8->AB8_TOTAL  -= ABC->ABC_VALOR
							AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
							AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
						EndIf

						If ( AB8->AB8_TOTAL <= 0 )
							AB8->( dbDelete() )
						EndIf
					EndIf
				EndIf
				RecLock("ABC")
				ABC->( dbDelete() )
			EndIf
		EndIf
	Next nCntFor

	ABC->( FKCommit() )

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o status da tarefa                           Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !Empty( M->AB9_TAREFA )
		ABJ->( dbSetOrder( 1 ) )
		If ABJ->( dbSeek( xFilial( "ABJ" ) + M->AB9_TAREFA ) )
			RecLock( "ABJ", .F. )
			ABJ->ABJ_SITUAC := M->AB9_STATAR
			ABJ->( MsUnLock() )
		EndIf
	EndIf

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Atualiza o Situacao da Ordem de Servico               Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	RecLock("AB7",.F.)

	AB7->AB7_TIPO := AtTipoAB7()

	If lAb7AppSt
		AB7->AB7_APPSTA := atStOS(AB7->AB7_TIPO,'AB7')
	EndIf
	
	AB7->( MsUnLock() )
	AtEqStatus("TECA450")

  	//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Grava o mesmo status para os subitens da OS         Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддды

	#IFDEF TOP

		cQuery := "UPDATE " + RetSqlName( "AB8" ) + " "
		cQuery += "SET AB8_TIPO='" + AB7->AB7_TIPO + "' "
		cQuery += "WHERE "
		cQuery += "AB8_FILIAL='"   + xFilial( "AB8" ) + "' AND "
		cQuery += "AB8_NUMOS='"    + AB7->AB7_NUMOS   + "' AND "
		cQuery += "AB8_ITEM='"     + AB7->AB7_ITEM    + "' AND "
		cQuery += "D_E_L_E_T_=' '"

		TcSqlExec( cQuery )

		AB8->( MsGoto( 0 ) )

	#ELSE

		dbSelectArea("AB8")
		dbSetOrder(1)
		DbSeek(xFilial("AB8")+AB7->AB7_NUMOS+AB7->AB7_ITEM)
		While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
				AB7->AB7_NUMOS == AB8->AB8_NUMOS .And.;
				AB7->AB7_ITEM  == AB8->AB8_ITEM )
			RecLock("AB8")
			AB8->AB8_TIPO := AB7->AB7_TIPO
			dbSelectArea("AB8")
			dbSkip()
		End

	#ENDIF

	dbSelectArea("AB6")
	dbSetOrder(1)
	DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)
	RecLock("AB6")
	AB6->AB6_STATUS := AtOsStatus(AB6->AB6_NUMOS)
	IF AB6->( ColumnPos('AB6_APPSTA') ) > 0
		AB6->AB6_APPSTA := 	atStOS(AB6->AB6_STATUS,'AB6')
	EndIf
	AB6->( MsUnLock() )

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁGera Orcamento se Solicitado                                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	If ( Len(aColsAB5[1])>0 )
		If ( Empty(cNumOrc) )
			dbSelectArea("AB3")
			For nCntFor := 1 To FCount()
				uCampo := FieldName(nCntFor)
				M->&(uCampo) := CriaVar(uCampo,.T.)
			Next nCntFor
			M->AB3_CODCLI := AB6->AB6_CODCLI
			M->AB3_LOJA   := AB6->AB6_LOJA
		Else
			dbSelectArea("AB3")
			dbSetOrder(1)
			DbSeek(xFilial("AB3")+cNumOrc)
			For nCntFor := 1 To FCount()
				uCampo := FieldName(nCntFor)
				M->&(uCampo) := AB3->(FieldGet(nCntFor))
			Next nCntFor
		EndIf
		If ( Empty(M->AB3_CONPAG) )
			dbSelectArea("SA1")
			dbSetOrder(1)
			DbSeek(xFilial("SA1")+M->AB3_CODCLI+M->AB3_LOJA)
			M->AB3_CONPAG := SA1->A1_COND
		EndIf
		aadd(aColsAb4,Array(Len(aHeaderAB4)+1))
		For nCntFor := 1 To Len(aHeaderAB4)
			If ( AllTrim(aHeaderAB4[nCntFor][2])<>"AB4_TIPO" .And. AB7->(FieldPos("AB7"+SubStr(AllTrim(aHeaderAB4[nCntFor][2]),4)))<>0 )
				aColsAb4[Len(aColsAb4)][nCntFor] := AB7->(FieldGet(FieldPos("AB7"+SubStr(AllTrim(aHeaderAB4[nCntFor][2]),4))))
			Else
				If !IsHeadRec(aHeaderAB4[nCntFor][2]) .AND. !IsHeadAlias(aHeaderAB4[nCntFor][2])
					aColsAb4[Len(aColsAb4)][nCntFor] := CriaVar(aHeaderAB4[nCntFor][2])
				Endif
			EndIf
		Next nCntFor
		If ( aScan(aColsAb5[1],{|x| !x[Len(x)]})<>0 )
			aColsAb4[Len(aColsAb4)][Len(aHeaderAB4)+1] := .F.
		Else
			aColsAb4[Len(aColsAb4)][Len(aHeaderAB4)+1] := .T.
		EndIf
		dbSelectArea("AB3")
		dbSetOrder(1)
		DbSeek(xFilial("AB3")+M->AB3_NUMORC)
		aHeader  := aClone(aHeaderAB4)
		aCols    := aClone(aColsAB4)
		At400Grava(nOpcao,,{ M->AB9_NUMOS + M->AB9_CODTEC + M->AB9_SEQ })
	EndIf

OtherWise

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Obtem os valores anteriores para este item de OS                Ё
	//Ё Atencao !!! O AB7 DEVE ESTAR POSICIONADO !!!                    Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	
	

	If lStatOnLine
		AB7->( DbSetOrder( 1 ) )
		If AB7->( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )

			aStats := AtStaItOS()

			nQtRepAnt := aStats[ 1 ]
			nHorRpAnt := aStats[ 2 ]
			nQtFalAnt := aStats[ 3 ]

		EndIf
	EndIf

 	lDelFNC := .T.
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁIdentifico se existe alguma FNC que impessa da delecao do Chamado Tecnico   Ё
	//Ёem caso afirmativo nЦo deleto o Cadastro.                                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DbSelectArea("AB9")
	DbSetOrder(1)
	If DbSeek(xFilial("AB9")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ)
		If !EMPTY(AB9->AB9_CODFNC)                        .AND.; 	// Tem FNC
			QNCVERI(AB9->AB9_CODFNC,AB9->AB9_FNCREV,"TEC") 			// Tem Plano de Acao ou esta baixada
			lDelFNC := .F.       									// Impede a exclusao da FNCs e do Chadmado Tecnico
		EndIf
	EndIF
	If lDelFNC

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Deletar FNCs                                        Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды

		At460DFNC()


		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁExclui o Orcamento Tecnico                                              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DbSelectArea("AB3")
		DbSetOrder(1)
		If DbSeek(xFilial("AB3")+AB9->AB9_NUMORC)
			AB9->AB9_NUMORC := ""
			AB9->( FKCommit() )
			At400Grava(3)
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁPosiciona Registros                                                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DbSelectArea("AB7")
		DbSetOrder(1)
		DbSeek(xFilial("AB7")+M->AB9_NUMOS)

		DbSelectArea("AB6")
		DbSetOrder(1)
		DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)

		DbSelectArea("AA1")
		DbSetOrder(1)
		DbSeek(xFilial("AA1")+M->AB9_CODTEC)

		DbSelectArea("AB9")
		DbSetOrder(1)
		DbSeek(xFilial("AB9")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ)

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Considera o valor hora pelo valor do tecnico ou pela tabela de precos  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		If lAtHrPr
			nVlHoraEst := MaTabPrVen("1", AB7->AB7_CODPRO, HoraToInt(AB9->AB9_TOTFAT),AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
		Else
			nVlHoraEst := xMoeda(AA1->AA1_VALOR,1,AB6->AB6_MOEDA)
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Posiciona a base instalada                     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		AA3->( dbSetOrder( 4 ) )
		AA3->( DbSeek(xFilial("AA3")+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+;
			AB7->AB7_NUMSER))

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Volta a tarefa para aberta caso o atendimento excluido estivesse encerrado Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If M->AB9_STATAR == "1"
			ABJ->( dbSetOrder( 1 ) )
			If ABJ->( dbSeek( xFilial( "ABJ" ) + M->AB9_TAREFA ) )
				RecLock( "ABJ", .F. )
				ABJ->ABJ_SITUAC := "2"
				ABJ->( MsUnlock() )
			EndIf
		EndIf

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁDeleta as pecas substituidas ou utilizadas.                             Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("ABA")
		dbSetOrder(1)
		DbSeek(xFilial("ABA")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)
		While ( !Eof() .And. xFilial("ABA") == ABA->ABA_FILIAL .And.;
				AB9->AB9_NUMOS   == ABA->ABA_NUMOS  .And.;
				AB9->AB9_CODTEC  == ABA->ABA_CODTEC .And.;
				AB9->AB9_SEQ     == ABA->ABA_SEQ )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁEstorna os movimentos de estoque                                        Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			dbSelectArea("AA5")
			dbSetOrder(1)
			DbSeek(xFilial("AA5")+ABA->ABA_CODSER)
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁMovimenta o Estoque pelo SD3                                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			At460Trans(.t.)

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//ЁEstorna movimento gerado pela Ordem de Servico                          Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( !Empty(ABA->ABA_SUBOS) )
				dbSelectArea("AB8")
				dbSetOrder(1)
				If ( DbSeek(xFilial("AB8")+ABA->ABA_NUMOS+ABA->ABA_SUBOS) )
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Estorna os Valores - Somente necessario quando aglutinacao             Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					RecLock("AB8")

					If Empty( cAgluPrd )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Nao utiliza aglutinacao de produtos                                    Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						AB8->AB8_QUANT  -= ABA->ABA_QUANT
						AB8->AB8_TOTAL  := AB8->AB8_QUANT * AB8->AB8_VUNIT
					Else
						AB8->AB8_TOTAL  -= At450Tab(ABA->ABA_CODPRO,AB6->AB6_TABELA,ABA->ABA_QUANT,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*ABA->ABA_QUANT
						AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
						AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
					EndIf

					If ( AB8->AB8_TOTAL <= 0 )
						AB8->( dbDelete() )
					EndIf
				EndIf
			EndIf

	     	cABASeqRC := ABA->ABA_SEQRC
	     	cSeekABG  := xFilial("ABG")+M->AB9_NUMOS+ABA->ABA_SEQRC+;
						ABA->ABA_ITEMRC

			RecLock("ABA")
			ABA->( dbDelete() )

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Tratamento da solicitacao ao almoxarifado                            Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If !Empty( cABASeqRC )
				//здддддддддддддддддддддддддддддддддд©
				//Ё Se possuia solicitacao anterior  Ё
				//юдддддддддддддддддддддддддддддддддды
				ABG->( dbSetOrder( 1 ) )
				If ABG->( dbSeek( cSeekABG ) )
					ABA->( FKCommit() )
					//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
					//Ё Exclui a associacao da sequencia de atendimento com a solicitacao    Ё
					//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
					RecLock( "ABG", .F. )
					ABG->ABG_SEQ    := CriaVar( "ABG_SEQ", .F. )
					ABG->ABG_CODTEC := CriaVar( "ABG_CODTEC", .F. )
					ABG->( MsUnLock() )
					ABG->( FKCommit() )
				EndIf
			EndIf

			dbSelectArea("ABA")
			dbSkip()
		End

		ABA->( FKCommit() )
		
		//Estorna as Preventivas atualizadas pelo atendimento
		If ( AB9->AB9_ATUPRE == "S" )
			DbSelectArea("AAJ")
			DbSetOrder(1)
			AAJ->(DbSeek(xFilial("AAJ")+AA3->AA3_CTAPRE))
			While ( !Eof() .And. xFilial("AAJ")    == AAJ->AAJ_FILIAL .And.;
				AA3->AA3_CTAPRE   == AAJ->AAJ_CTAPRE )
				If !Empty(AAJ->AAJ_DTREAL) .And. ( AAJ->AAJ_DTPREV == AB9->AB9_DTINI )
					RecLock("AAJ", .F.)
					AAJ->AAJ_DTREAL := dDtReal
				EndIf
				DbSelectArea("AAJ")
				AAJ->(DbSkip())
			End
			AAJ->( FKCommit() )
		EndIf
		
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁDeleta as despesas de atendimento                                       Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("ABC")
		dbSetOrder(1)
		DbSeek(xFilial("ABC")+AB9->AB9_NUMOS+AB9->AB9_CODTEC+AB9->AB9_SEQ)
		While ( !Eof() .And. xFilial("ABC")    == ABC->ABC_FILIAL .And.;
				AB9->AB9_NUMOS    == ABC->ABC_NUMOS  .And.;
				AB9->AB9_CODTEC   == ABC->ABC_CODTEC .And.;
				AB9->AB9_SEQ      == ABC->ABC_SEQ )
			If ( !Empty(ABC->ABC_SUBOS) )
				dbSelectArea("AB8")
				dbSetOrder(1)
				If ( DbSeek(xFilial("AB8")+ABC->ABC_NUMOS+ABC->ABC_SUBOS) )
					RecLock("AB8")

					If Empty( cProdDes )
						//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
						//Ё Nao utiliza aglutinacao de produtos                                    Ё
						//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
						AB8->AB8_QUANT  -= ABC->ABC_QUANT
						AB8->AB8_TOTAL  := AB8->AB8_QUANT * AB8->AB8_VUNIT
					Else
						AB8->AB8_TOTAL  -= ABC->ABC_VALOR
						AB8->AB8_PRCLIS := NoRound(AB8->AB8_TOTAL/AB8->AB8_QUANT,TamSx3("AB8_PRCLIS")[2])
						AB8->AB8_VUNIT  := AB8->AB8_PRCLIS
					EndIf

					If ( AB8->AB8_TOTAL <= 0 )
						AB8->( dbDelete() )
					EndIf
				EndIf
			EndIf
			RecLock("ABC")

			ABC->( dbDelete() )
			dbSelectArea("ABC")
			dbSkip()
		End

		ABC->( FKCommit() )
		
		dbSelectArea("AB6")
		dbSetOrder(1)

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Deleta Atendimento da OS                            Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		MSMM(AB9->AB9_MEMO1,,,,2)
		dbSelectArea("AB9")
		RecLock("AB9")

		cNumOs := AB9->AB9_NUMOS

		AB9->( dbDelete() )

		AB9->( FKCommit() )

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava o novo status para o item da OS               Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		dbSelectArea("AB7")
		dbSetOrder(1)	
		DbSeek(xFilial("AB7")+cNumOs)
		RecLock("AB7")
		AB7->AB7_TIPO := AtTipoAB7()
		AtEqStatus("TECA450")

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava o mesmo status para os subitens da OS         Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды

		#IFDEF TOP

			cQuery := "UPDATE " + RetSqlName( "AB8" ) + " "
			cQuery += "SET AB8_TIPO='" + AB7->AB7_TIPO + "' "
			cQuery += "WHERE "
			cQuery += "AB8_FILIAL='"   + xFilial( "AB8" ) + "' AND "
			cQuery += "AB8_NUMOS='"    + AB7->AB7_NUMOS   + "' AND "
			cQuery += "AB8_ITEM='"     + AB7->AB7_ITEM    + "' AND "
			cQuery += "D_E_L_E_T_=' '"

			TcSqlExec( cQuery )

			AB8->( MsGoto( 0 ) )

		#ELSE

			dbSelectArea("AB8")
			dbSetOrder(1)
			DbSeek(xFilial("AB8")+AB7->AB7_NUMOS+AB7->AB7_ITEM)
			While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
					AB7->AB7_NUMOS == AB8->AB8_NUMOS .And.;
					AB7->AB7_ITEM  == AB8->AB8_ITEM )
				RecLock("AB8")
				AB8->AB8_TIPO := AB7->AB7_TIPO
				dbSelectArea("AB8")
				dbSkip()
			End

		#ENDIF

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava o novo status para a OS                       Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)
		RecLock("AB6")
		AB6->AB6_STATUS := AtOsStatus(AB6->AB6_NUMOS)

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Estorna tabela de saldo quando o atendimento for excluido  Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

		AAQ->(DbSetOrder(1))
		AAR->(DbSetOrder(1))
		For nCntFor := 1 To Len(aCols)
			If Empty(aCols[nCntFor,GDFieldPos("ABA_PRDORI")]) .AND. Empty(aCols[nCntFor,GDFieldPos("ABA_QNTORI")])
				cCampo := "ABA_CODPRO"
				cCmpQtd := "ABA_QUANT"
			Else
				cCampo := "ABA_PRDORI"
				cCmpQtd := "ABA_QNTORI"
			EndIf
			If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]))
				If AAQ->AAQ_MODLIB == "1"
					If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)])) .And.;
						aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
						RecLock("AAR",.F.)
						AAR->AAR_SALDO += aCols[nCntFor,GDFieldPos(cCmpQtd)]
						AAR->(MsUnlock())
					EndIf
				ElseIf AAQ->AAQ_MODLIB == "2"
					If AAR->(DbSeek(xFilial("AAR")+AA3->AA3_CONTRT+aCols[nCntFor,GDFieldPos(cCampo)]+LEFT(DTOS(AB9->AB9_DTFIM),6))) .And.;
						aCols[nCntFor,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
						RecLock("AAR",.F.)
						AAR->AAR_SALDO += aCols[nCntFor,GDFieldPos(cCmpQtd)]
						AAR->(MsUnlock())
					EndIf
				EndIf
			EndIf
		Next nCntFor
    Else
    	Help( " ", 1, "AT460DELFN" ) // NЦo foi possМvel excluir o registro, pois existe uma FNC para o Chamado TИcnico relacionado. Encerre a FNC
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Atualiza o Status de Atendimento do Agendamento da O.S
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	If (AB9->(FieldPos("AB9_MPONTO"))> 0 .AND. !M->AB9_MPONTO) .AND. !EMPTY("M->AB9_ATAUT")
		DbSelectArea("ABB")
		DbSetOrder(8) //ABB_FILIAL+ABB_CODIGO
		If ABB->(DbSeek(xFilial("ABB") + AB9->AB9_ATAUT))
			RecLock("ABB", .F.)

			REPLACE ABB_CHEGOU	WITH "N"	//Compareceu "S" - Sim ; "N" - NЦo
			REPLACE ABB_ATENDE	WITH "2"	//Atendeu 	"1" - Sim ; "2" - NЦo

			ABB->( MsUnLock() )
		EndIf
	EndIf
	
	AB6->(dbSetOrder(1))
	AB6->(dbSeek(xFilial("AB6")+AB9->AB9_NUMOS))
	If !Empty(AB6->AB6_ORCAME) .And. !Empty(AB9->AB9_CODTWZ) 		
		At995ExcC(AB6->AB6_ORCAME,AB9->AB9_CODTWZ,.T.)
	EndIf	
EndCase

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Atualiza as estatisticas apenas se o parametro estiver definido Ё
//Ё Atencao !!! O AB7 DEVE ESTAR POSICIONADO !!!                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If lStatOnLine
	AA3->(dbSetOrder(4))
	If AA3->( DbSeek(xFilial("AA3")+AB7->AB7_CODFAB+AB7->AB7_LOJAFA+AB7->AB7_CODPRO+AB7->AB7_NUMSER) )

		aStats := AtStaItOS()

		RecLock( "AA3", .F. )

		AA3->AA3_QTREP += aStats[ 1 ] - nQtRepAnt
		AA3->AA3_HORRP += aStats[ 2 ] - nHorRpAnt
		AA3->AA3_QTFAL += aStats[ 3 ] - nQtFalAnt

		AA3->AA3_MTBF  := AtCalcMTBF()
		AA3->AA3_MTTR  := AtCalcMTTR()

		AA3->( MsUnLock() )

	EndIf
EndIf

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Executa ponto apos a gravacao                                   Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "AT460GRV" )
	ExecBlock( "AT460GRV", .F., .F., { nOpcao } )
EndIf

dbSelectArea("AB9")
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460SubItЁ Autor Ё Vendas e CRM          Ё Data Ё 27.01.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Calcula o Proximo item do Apontamento da OS ( AB8 )        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 : Proximo Item                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 : Numero da Ordem de Servico + Item ( AB7 )          Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function At460SubIt(cNumOs)
Local aAreaAB8	:= AB8->(GetArea())
Local cSubItem := StrZero(0,TamSX3("AB8_SUBITE")[1])
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica o Ultimo Sub-Item do apontamento da OS.               Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
dbSelectArea("AB8")
dbSetOrder(1)
DbSeek(xFilial("AB8")+cNumOs)
While ( !Eof() .And. xFilial("AB8") == AB8->AB8_FILIAL .And.;
		AB8->AB8_NUMOS+AB8->AB8_ITEM == cNumOs )
	cSubItem := AB8->AB8_SUBITE
	dbSelectArea("AB8")
	dbSkip()
End
cSubItem := Soma1(RTRIM(cSubItem),2)
RestArea(aAreaAB8)
Return(cSubItem)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAT460OS   Ё Autor Ё Vendas e CRM          Ё Data Ё 27.10.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Verifica se a OS pode ser atendida                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 : Logico                                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function At460OS()

Local lRetorno := .F.
Local cNumOS   := &(ReadVar())

dbSelectArea("AB7")
dbSetOrder(1)
If ( DbSeek(xFilial("AB7")+cNumOs) )
	If ( AB7->AB7_TIPO $ "13" )
		lRetorno := .T.
	Else
		Help(" ",1,"AT460OS")
	EndIf
EndIf


Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460SkSerЁ Autor Ё Vendas e CRM          Ё Data Ё09.11.98  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valida o Numero de Serie informado.                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠Ё          Ё                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Function At460SkSer()

Local aArea    := GetArea()
Local lRetorno := .F.

dbSelectArea("AB7")
dbSetOrder(1)
DbSeek(xFilial("AB7")+M->AB9_NUMOS)

dbSelectArea("AB6")
dbSetOrder(1)
DbSeek(xFilial("AB6")+AB7->AB7_NUMOS)

dbSelectArea("AA3")
dbSetOrder(1)
If ( DbSeek(xFilial("AA3")+AB6->AB6_CODCLI+AB6->AB6_LOJA+AB7->AB7_CODPRO+M->AB9_NUMSER) )
	lRetorno := .T.
Else
	Help(" ",1,"AT460SKSER")
EndIf

If lRetorno
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a OS foi originada de projeto ( nao pode alterar o numero de serie ) Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABI->( dbSetOrder( 2 ) )
	If ABI->( dbSeek( xFilial( "ABI" ) + AB7->AB7_NUMOS ) )
		lRetorno := .F.
		Help( " ", 1,"AT460PRSER",, STR0047, 3,1 ) // "Nao e possivel alterar o numero de serie, pois a OS foi criada a partir de um projeto"
	EndIf
EndIf

If lRetorno
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a OS foi originada de planos de manutencao Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	ABE->( dbSetOrder( 5 ) )
	If ABE->( dbSeek( xFilial( "ABE" ) + AB6->AB6_NUMOS ) )
		lRetorno := .F.
		Help( " ", 1,"AT460PLSER",, STR0048, 3,1 ) // "Nao e possivel alterar o numero de serie, pois a OS foi criada a partir de um plano de manutencao"
	EndIf
EndIf

RestArea(aArea)

Return(lRetorno)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460Desp Ё Autor Ё Vendas e CRM          Ё Data Ё06.01.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁApontamento das Despesas de Atendimento.                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁnOpcx: Opcao selecionada                                    Ё╠╠
╠╠Ё          ЁoGetD: Objeto da GetDados                                   Ё╠╠
╠╠Ё          ЁlExibe: Se Ativa a Exibicao das Despesas                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function At460Desp(nOpcx,oGetD,lExibe)

Local oDlg		:= Nil
Local oGetDesp	:= Nil
Local nOpcA		:= 0
Local aArea		:= GetArea()
Local aSavAcols	:= {}
Local aSavAHead	:= {}
Local nSavN		:= 0
Local cCadastro	:= STR0012 //"Despesas do Apontamento da Ordem Servico"
Local nUsado	:= 0
Local oSay		:= Nil
Local nTotDesp	:= 0
Local nTotDCli	:= 0
Local nTotDFab	:= 0
Local nPosSer	:= 0
Local nPosVlr	:= 0
Local lRet		:= .T.
Local nOper		:= aRotina[ nOpcx, 4 ]
Local nCntFor	:= 0
Local nAutoPos	:= 0

If  Type("l460Auto") <> "U" .AND. l460Auto .AND. (Type( "aAutoItens" ) == "A"  .AND. Len(aAutoItens) > 0 )
	nAutoPos := aScan(aAutoItens[1], { |x| x[1] == "AUTO" })
	If nAutoPos > 0 
		aCols := aAutoItens[1][nAutoPos]
	EndIf
EndIf

If Len(aHeaderABC) == 0 .AND. Len(aColsABC) == 0

	//зддддддддддддддддддддддд©
	//ЁMontagem aHeader, aColsЁ
	//юддддддддддддддддддддддды
	If (!INCLUI)
		cSeek	:= xFilial("ABC")+M->AB9_NUMOS+M->AB9_CODTEC+M->AB9_SEQ
		cWhile	:= "ABC->ABC_FILIAL+ABC->ABC_NUMOS+ABC->ABC_CODTEC+ABC->ABC_SEQ"

		aSavaHeader	:= aClone(aHeader)
		aSavaCols	:= aClone(aCols)
		aHeader		:= {}
		aCols		:= {}

		If !FillGetDados(	nOpcx			,"ABC"			,1			  		 											,cSeek				,;
			  				{|| &cWhile }	,{|| .T. }		,/*aNoFields*/		 											,/*aYesFields*/		,;
							/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/ 		 											,/*lEmpty*/			,;
							/*aHeaderAux*/	,/*aColsAux*/	,{|| At460AfterCols(@nTotDesp, @nTotDCli, @nTotDFab, nOper) }	,/*bBeforeCols*/	)
			lRet := .F.
		Endif

		If Empty(aCols[1][GdFieldPos("ABC_ITEM")])
			aCols[1][GdFieldPos("ABC_ITEM")] := "01"
		Endif

		aHeaderABC	:= aClone(aHeader)
		aColsABC	:= aClone(aCols)
		aHeader		:= aClone(aSavaHeader)
		aCols		:= aClone(aSavaCols)
	Else
		aSavaHeader	:= aClone(aHeader)
		aSavaCols	:= aClone(aCols)
		aHeader		:= {}
		aCols		:= {}

		FillGetDados(	nOpcx			,"ABC"			,1				,/*cSeek*/		,;
						/*{||cWhile}*/	,{|| .T. }		,/*aNoFields*/	,/*aYesFields*/	,;
						/*lOnlyYes*/	,/*cQuery*/		,/*bMontCols*/	,.T.			,;
						/*aHeaderAux*/	,/*aColsAux*/	,/*bAfterCols*/	,/*bBeforeCols*/)
		If Empty(aCols[1][GdFieldPos("ABC_ITEM")])
			aCols[1][GdFieldPos("ABC_ITEM")] := "01"
		Endif

		aHeaderABC	:= aClone(aHeader)
		aColsABC	:= aClone(aCols)
		aHeader		:= aClone(aSavaHeader)
		aCols		:= aClone(aSavaCols)
	EndIf
Else
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSoma todas as Despesas e atualiza os dados da Janela.                   Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	nPosVlr  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_VALOR"})
	nPosSer  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODSER"})
	nUsado   := Len(aHeaderABC)
	For nCntFor := 1 To Len(aColsABC)
		If ( !aColsABC[nCntFor][nUsado+1] )
			nTotDesp += aColsABC[nCntFor][nPosVlr]
			nTotDCli += AtVlrPagto(aColsABC[nCntFor][nPosSer],aColsABC[nCntFor][nPosVlr],"C")
			nTotDFab += AtVlrPagto(aColsABC[nCntFor][nPosSer],aColsABC[nCntFor][nPosVlr],"F")
		EndIf
	Next nCntFor
Endif

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para manipular o aColsABC                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "AT460DSP" )
	ExecBlock( "AT460DSP", .F.,.F., { INCLUI } )
EndIf

If ( lExibe )
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁSalva o aCols   de Entrada e Desabilita o Paint da Primeira GetDados    Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	oGetD:oBrowse:lDisablePaint := .T.
	aSavAcols:= aClone(aCols)
	aSavAhead:= aClone(aHeader)
	aHeader  := aClone(aHeaderABC)
	aCols    := aClone(aColsABC)
	nSavN    := N
	N        := 1

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁMonta a GetDados                                                        Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	DEFINE MSDIALOG oDlg TITLE cCadastro From 10,0 to 30,80
	@ 035,005 SAY RetTitle("ABC_NUMOS")+": "+M->AB9_NUMOS OF oDlg PIXEL
	@ 045,005 SAY RetTitle("ABC_CODTEC")+": "+M->AB9_CODTEC+" - "+Posicione("AA1",1,xFilial("AA1")+M->AB9_CODTEC,"AA1_NOMTEC") OF oDlg PIXEL
	@ 045,200 SAY RetTitle("ABC_SEQ")+": "+M->AB9_SEQ OF oDlg PIXEL
	@ 125,005 SAY STR0013 SIZE 60,09 OF oDlg PIXEL  //"Total: "
	@ 125,105 SAY STR0014 SIZE 60,09 OF oDlg PIXEL  //"Total do Cliente: "
	@ 125,205 SAY STR0015 SIZE 60,09 OF oDlg PIXEL  //"Total do Fabricante: "
	@ 125,065 SAY oSay PROMPT nTotDesp PICTURE Tm(nTotDesp,14,2) SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotDesp"
	@ 125,165 SAY oSay PROMPT nTotDCli PICTURE Tm(nTotDCli,14,2) SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotDCli"
	@ 125,265 SAY oSay PROMPT nTotDFab PICTURE Tm(nTotDFab,14,2) SIZE 40,09 OF oDlg PIXEL
	oSay:Cargo := "nTotDFab"
	oGetDesp := MsGetDados():New(055,001,120,316,nOpcx,"AT460DesOk","AllwaysTrue","+ABC_ITEM", ( nOper == 4 .Or. nOper == 3 ) )
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpca:=1,if(oGetDesp:TudoOk(),oDlg:End(),nOpca:=0)},{||oDlg:End()})
	If ( nOpcA == 1 )
		aColsABC := aClone(aCols)
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁRetorna ao Estado de Entrada                                            Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	aHeader  := aClone(aSavAHead)
	aCols    := aClone(aSavACols)
	N := nSavN
	oGetD:oBrowse:lDisablePaint := .F.
	At460TotAp(oGetD:oWnd)
EndIf

RestArea(aArea)

Return(lRet)

/*/
ээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAt460AfterCols   Ё Autor Ё Vendas e CRM        Ё Data Ё 17.01.07 Ё╠╠
╠╠цддддддддддедддддддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё ValidaГЦo da montagem do FillGetDados                           Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical At460AfterCols(Void)                                    Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1: Total de despesas                                        Ё╠╠
╠╠Ё          Ё ExpN2: Total do cliente                                         Ё╠╠
╠╠Ё          Ё ExpN3: Total do fabricante                                      Ё╠╠
╠╠Ё          Ё ExpN4: OperaГЦo da rotina                                       Ё╠╠
╠╠цддддддддддеддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                         Ё╠╠
╠╠юддддддддддаддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function At460AfterCols(nTotDesp, nTotDCli, nTotDFab, nOper)
	Local lRet		:= .T.

	nTotDesp += ABC->ABC_VALOR
	nTotDCli += AtVlrPagto(ABC->ABC_CODSER,ABC->ABC_VALOR,"C")
	nTotDFab += AtVlrPagto(ABC->ABC_CODSER,ABC->ABC_VALOR,"F")
	If !Empty(ABC->ABC_NUMERO) .And. nOper <> 2
		lRet := .F.
		Help(" ",1,"AT460DESP")
	EndIf
Return lRet

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT460DESOk  Ё Autor Ё Vendas e CRM        Ё Data Ё 06.01.99 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da Linha na GetDados 2                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё Logical AT460DESOk(Void)                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AT460DesOk(oGetDad)

Local lRetorno := .T.
Local nUsado   := Len(aHeaderABC)
Local nPosPrd  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODPRO"})
Local nPosQtd  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_QUANT"})
Local nPosUnit := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_VLUNIT"})
Local nPosVlr  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_VALOR"})
Local nPosSer  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODSER"})
Local oDlg     := oGetDad:oWnd
Local aControl := oDlg:aControls
Local nTotDesp := 0
Local nTotDCli := 0
Local nTotDFab := 0
Local nCntFor  := 0

If ( !aCols[n][nUsado+1] )
	If (  Empty(aCols[n][nPosPrd]) .Or.;
			Empty(aCols[n][nPosSer]) .Or.;
			Empty(aCols[n][nPosQtd]) .Or.;
			Empty(aCols[n][nPosUnit]) .Or.;
			Empty(aCols[n][nPosVlr]) ) ;
			.And. ( Len(aCols) > 1 .Or. !Empty(aCols[n][nPosPrd]))
		lRetorno := .F.
		Help(" ",1,"AT460DES01")
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica o Valor Total                                                  Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( NoRound(aCols[n][nPosQtd]*aCols[n][nPosUnit]) <> NoRound(aCols[n][nPosVlr]) )
		Help(" ",1,"AT460DES02")
		lRetorno := .F.
	EndIf
EndIf
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁSoma todas as Despesas e atualiza os dados da Janela.                   Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nCntFor := 1 To Len(aCols)
	If ( !aCols[nCntFor][nUsado+1] )
		nTotDesp += aCols[nCntFor][nPosVlr]
		nTotDCli += AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosVlr],"C")
		nTotDFab += AtVlrPagto(aCols[nCntFor][nPosSer],aCols[nCntFor][nPosVlr],"F")
	EndIf
Next nCntFor
For nCntFor := 1 To Len(aControl)

	If ValType( aControl[nCntFor]:Cargo ) == "C"

		If ( !Empty(aControl[nCntFor]:Cargo) )
			Do Case
			Case ( aControl[nCntFor]:Cargo $ "nTotDesp" )
				aControl[nCntFor]:SetText(nTotDesp)
			Case ( aControl[nCntFor]:Cargo $ "nTotDCli" )
				aControl[nCntFor]:SetText(nTotDCli)
			Case ( aControl[nCntFor]:Cargo $ "nTotDFab" )
				aControl[nCntFor]:SetText(nTotDFab)
			EndCase
		EndIf

	EndIf

Next nCntFor
Return(lRetorno)

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁAT460Bar  Ё Autor Ё Vendas e CRM          Ё Data Ё 21.09.98 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Mostra a EnchoiceBar na tela                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function AT460Bar(oDlg,bOk,bCancel,nOpc,oGetD,aCamposFNC)

Local aButtons   := {}
Local aUsButtons := {}
//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Variaveis usadas na Integracao com Controle de Nao-Conformidades Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

Default aCamposFNC := {} 									// Array com os dados da FNC

Aadd(aButtons,{"BUDGET" , {|| AT460DESP(nOpc,oGetD,.T.)} , STR0023, STR0023 }) //"Despesas"

Aadd(aButtons,{"PENDENTE" , {|| AT460PEND( oGetD ) } , STR0026, STR0050 })    //"Pendencias"

If Altera .Or. Inclui
	Aadd(aButtons,{"EDIT"  , {|| AT460Solic( ) } , STR0033, STR0051 }) // "Requisicoes"
	Aadd(aButtons,{"SALDOA",{|| At460F8()},STR0070,STR0071}) //Saldos Alternativos
EndIf

If nOpc == 2
	Aadd(aButtons,{"bmpord1",{|| At460Track()}, STR0049, STR0052 }) // "System Tracker"
EndIf

// Este Botao so devera aparecer quando houver  integraГЦo entes os ambientes QNC e TEC
If (Altera .OR. Inclui)
	Aadd(aButtons,{"CRITICA",{|| AT460FNC(oDlg , oGetD, @aCamposFNC, Nil)}, STR0054, STR0053 }) //"Ficha de Nao-Conformidade"###"FNC"
Else
	Aadd(aButtons,{"CRITICA",{|| AT460FNC(oDlg , oGetD, Nil, .T.)}, STR0054, STR0053 }) //"Ficha de Nao-Conformidade"###"FNC"
EndIf

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Adiciona botoes do usuario na EnchoiceBar                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

If ExistBlock( "AT460BUT" )
	aUsButtons := ExecBlock( "AT460BUT", .F., .F. )
	AEval( aUsButtons, { |x| AAdd( aButtons, x ) } )
EndIf

Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460TotApЁ Autor Ё Vendas e CRM          Ё Data Ё07.01.98  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁAtualiza os Totais da Janela de Apontamentos                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁoDlg: Objeto da Janela                                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/

Static Function At460TotAp(oDlg)

Local nUsado   := Len(aHeader)
Local nPosProd := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODPRO"})
Local nPosQuant:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QUANT"})
Local nPosSer  := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODSER"})
Local nPosValor:= aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_VALOR"})
Local nTotDesp := 0
Local nTotMat  := 0
Local nTotCli  := 0
Local nTotFab  := 0
Local nTotGer  := 0
Local nCntFor  := 0
Local nValor   := 0
Local aControl := oDlg:aControls

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁPosiciona Registro                                                      Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
AB7->( dbSetOrder(1) )
AB7->( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )

AB6->( dbSetOrder(1) )
AB6->( DbSeek(xFilial("AB6")+AB7->AB7_NUMOS) )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAjusta os Totais da Janela.                                             Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
For nCntFor := 1 To Len(aCols)
	If ( Len(aCols[nCntFor])==nUsado .Or. !aCols[nCntFor][nUsado+1] )

		nValor   := At450Tab(aCols[nCntFor][nPosProd], AB6->AB6_TABELA, aCols[nCntFor][nPosQuant],AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)*aCols[nCntFor][nPosQuant]

		nTotMat  += nValor
		nTotGer  += nValor
		nTotCli  += AtVlrPagto(aCols[nCntFor][nPosSer],nValor,"C")
		nTotFab  += AtVlrPagto(aCols[nCntFor][nPosSer],nValor,"F")
	EndIf
Next nCntFor
nPosSer  := aScan(aHeaderABC,{|x| AllTrim(x[2])=="ABC_CODSER"})
nUsado   := Len(aHeaderABC)
For nCntFor := 1 To Len(aColsABC)
	If ( !aColsABC[nCntFor][nUsado+1] )
		nValor   := aColsABC[nCntFor][nPosValor]
		nTotDesp += nValor
		nTotGer  += nValor
		nTotCli  += AtVlrPagto(aColsABC[nCntFor][nPosSer],nValor,"C")
		nTotFab  += AtVlrPagto(aColsABC[nCntFor][nPosSer],nValor,"F")
	EndIf
Next nCntFor
For nCntFor := 1 To Len(aControl)

	If  aControl[nCntFor] <> NIL .And. ValType( aControl[nCntFor]:Cargo ) == "C"

		If ( !Empty(aControl[nCntFor]:Cargo) )
			Do Case
			Case ( aControl[nCntFor]:Cargo $ "nTotGer" )
				aControl[nCntFor]:SetText(nTotGer)
			Case ( aControl[nCntFor]:Cargo $ "nTotCli" )
				aControl[nCntFor]:SetText(nTotCli)
			Case ( aControl[nCntFor]:Cargo $ "nTotFab" )
				aControl[nCntFor]:SetText(nTotFab)
			Case ( aControl[nCntFor]:Cargo $ "nTotMat" )
				aControl[nCntFor]:SetText(nTotMat)
			Case ( aControl[nCntFor]:Cargo $ "nTotDesp" )
				aControl[nCntFor]:SetText(nTotDesp)
			EndCase
		EndIf

	EndIf

Next nCntFor
Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAtuEstoqueЁ Autor Ё Vendas e CRM          Ё Data Ё 01.02.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Atualizar a Movimentacao de Estoque via SD3, caso o produtoЁ╠╠
╠╠Ё          Ё utilize Rastro, as movimentacoes serao feitas por FIFO.    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Array com dados referentes ao SD3                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1: Codigo do Produto                                   Ё╠╠
╠╠Ё          Ё ExpC2: Local Padrao                                        Ё╠╠
╠╠Ё          Ё ExpC3: Lote de Controle                                    Ё╠╠
╠╠Ё          Ё ExpC4: Sub-Lote                                            Ё╠╠
╠╠Ё          Ё ExpC5: Localizacao                                         Ё╠╠
╠╠Ё          Ё ExpC6: Numero de Serie                                     Ё╠╠
╠╠Ё          Ё ExpN7: Quantidade                                          Ё╠╠
╠╠Ё          Ё ExpC8: Tipo de Movimentacao                                Ё╠╠
╠╠Ё          Ё ExpA9: Array com os Campos e valores complementares        Ё╠╠
╠╠Ё          Ё        [1] Campo [2] Conteudo a ser gravado.               Ё╠╠
╠╠Ё          Ё ExpNA: Operacao [1]Requisicao [2]Devolucao                 Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AtuEstoque(cCodPro,cLocal,cLoteCtl,cNumLote,cLocaliz,cNumSer,nQtd,cTm,aCampos,nOpcao,cOSItem)

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁDefine as variaveis a serem utilizadas                                  Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
Local nHdlPrv     := 0
Local nTotal      := 0
Local nCntFor     := 0
Local nCampoSB3   := 0
Local lGravou     := .F.
Local cApropri    := "0"
Local cLoteEst    := ""
Local cArquivo    := ""
Local cMes        := ""
Local aCM         := {}
Local aCusto      := {}
Local cLocProc    := SuperGetMv("MV_LOCPROC")
Local bBloco      := { |nV,nX| Trim(nV)+Str(nX,1) }
Local aLocaliz    := {}
Local aLote       := {}
Local aSD3        := {}
Local nSd3        := 0
Local lDigita     := .F.
Local dValidLote  := CToD( "" )
Local lAglutina   := .F.
Local aTravas     := {}
Local lAT460SD3   := ExistBlock( "AT460SD3" )

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//ЁAvalia os dados de Entrada                                              Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ( Empty(cTm) )
	cTm   := If(nOpcao==1,"999","499")
EndIf
aCampos := If(aCampos==Nil,Array(0),aCampos)

cOSItem := If(ValType(cOSItem)=="C",cOSItem,cOSItem:="")

dbSelectArea("SB2")
dbSetOrder(1)
DbSeek(xFilial("SB2")+cCodPro+cLocal)

If ( SB2->( Found() ) )

	lGravou := .T.
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se o custo medio e' calculado On-Line               Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If ( SuperGetMv("MV_CUSMED") == "O" )
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Se necessario cria o cabecalho do arquivo de prova           Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		nHdlPrv := HeadProva(cLoteEst,"MATA240",Substr(cUserName,1,6),@cArquivo)
	EndIf
	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Apropriacao Indireta e' identificada com B1_APROPRI = "I" e o  Ё
	//Ё numero da OP (D3_OP) em branco.                                Ё
	//Ё Apropriacao Direta e' identificada com B1_APROPRI <> "I" ou comЁ
	//Ё o numero da OP (D3_OP) preenchido.                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	dbSelectArea("SB1")
	dbSetOrder(1)
	DbSeek(xFilial("SB1")+cCodPro)

	dbSelectArea("SF5")
	dbSetOrder(1)
	DbSeek(xFilial("SF5")+cTm)

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//ЁVerifica se o Produto possui ratreabilidade                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	If ( Rastro(cCodPro) .Or. Localiza(cCodPro) )

		If nOpcao == 2
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Devolucao                                                              Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If Rastro( cCodPro, "L" )

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Rastro lote                                                            Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				dValidLote := CToD( "  /  /  " )

				SB8->( dbSetOrder( 3 ) )
				If SB8->( dbSeek( xFilial( "SB8" ) + cCodPro + cLocal + cLoteCtl ) )
					dValidLote := SB8->B8_DTVALID
				EndIf
				aSD3 := { { cLoteCtl, "" , cLocaliz, cNumSer, nQtd, 0, dValidLote } }

			ElseIf Rastro( cCodPro, "S" )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Rastro sublote                                                         Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

				dValidLote := CToD( "  /  /  " )

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Pesquisa o sublote                                                     Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				SB8->( dbSetOrder( 2 ) )
				If 	SB8->( dbSeek( xFilial( "SB8" ) + cNumLote ) )
					If cCodPro == SB8->B8_PRODUTO
						cLoteCtl   := SB8->B8_LOTECTL
						dValidLote := SB8->B8_DTVALID
					EndIf
				EndIf

				aSD3 := { { cLoteCtl, cNumLote, cLocaliz, cNumSer, nQtd, 0,  dValidLote } }
			ElseIf Localiza( cCodPro )
				aSD3 := { { "" , "" , cLocaliz, cNumSer, nQtd, 0,  CToD( "  /  /  " ) } }
			EndIf

		Else

			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Requisicao                                                             Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			If Rastro( cCodPro, "S" )
				//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Rastro sublote                                                         Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

				dValidLote := CToD( "  /  /  " )

				SB8->( dbSetOrder( 2 ) )
				If SB8->( dbSeek( xFilial( "SB8" ) + cNumLote ) )
					If cCodPro == SB8->B8_PRODUTO
						cLoteCtl := SB8->B8_LOTECTL
					EndIf
				EndIf
			EndIf

			aSD3 := SldPorLote(cCodPro,cLocal,nQtd,Nil,cLoteCtl,cNumLote,cLocaliz,cNumSer,@aTravas)
		EndIf

		If ( Empty(aSD3) .And. nOpcao==2 )
			aSD3 := { { "" , "" , "" , "" , nQtd, 0,  CToD( "  /  /  " ) } }
		EndIf

	Else
		aSD3 := { { "" , "" , "" , "" , nQtd, 0, CToD( "  /  /  " ) } }
	EndIf
	For nSD3 := 1 To Len(aSD3 )
		RecLock("SD3",.T.)
		aadd(aTravas, { Alias() , RecNo() })
		SD3->D3_FILIAL := xFilial("SD3")
		SD3->D3_TM     := cTM
		SD3->D3_COD    := cCodPro
		SD3->D3_LOCAL  := cLocal
		SD3->D3_UM     := SB1->B1_UM
		SD3->D3_SEGUM  := SB1->B1_SEGUM
		SD3->D3_GRUPO  := SB1->B1_GRUPO
		SD3->D3_TIPO   := SB1->B1_TIPO
		SD3->D3_EMISSAO:= dDataBase
		SD3->D3_LOTECTL:= aSD3[nSD3,1]
		SD3->D3_NUMLOTE:= aSD3[nSD3,2]
		SD3->D3_LOCALIZ:= aSD3[nSd3,3]
		SD3->D3_NUMSERI:= aSD3[nSd3,4]
		SD3->D3_QUANT  := aSD3[nSd3,5]
		SD3->D3_DTVALID:= aSD3[nSd3,7]
		SD3->D3_QTSEGUM:= ConvUm(SD3->D3_COD,SD3->D3_QUANT,0,2)
		SD3->D3_OSTEC  := cOSItem
		For nCntFor := 1 To Len(aCampos)
			SD3->(FieldPut(FieldPos(aCampos[nCntFor,1]),aCampos[nCntFor,2]))
		Next nCntFor

		If ( SB1->B1_APROPRI=="I".And.Empty(SD3->D3_OP)).And.SF5->F5_APROPR<>"S"
			cApropri := "3"
		Else
			cApropri := "0"
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁVerifica o Tipo de Apropriacao a ser efetuada no movimento corrente     Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( cApropri == "0" .And. SF5->F5_VAL == "S" )
			cApropri := "6"
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Variavel para gravar demanda no mes corretoЁ
		//юдддддддддддддддддддддддддддддддддддддддддддды
		cMes := "B3_Q"+StrZero(Month(SD3->D3_EMISSAO),2)
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava a classe do movimento RE0,RE3,DE0 ou DE3 Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		If ( SD3->D3_TM <= "500" )
			SD3->D3_CF := "DE"+cApropri
		Else
			SD3->D3_CF := "RE"+cApropri
		EndIf
		SD3->D3_USUARIO := cUserName
		//здддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava o numero sequencial do movimento         Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддды
		SD3->D3_NUMSEQ := ProxNum()
		SD3->D3_CHAVE  := SubStr(SD3->D3_CF,2,1)+IIF(SD3->D3_CF=="DE4","9","0")

		//зддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Ponto de entrada na gravacao do movimento Ё
		//юддддддддддддддддддддддддддддддддддддддддддды
		If lAT460SD3
			ExecBlock( "AT460SD3" )
		EndIf

		If ( cApropri <> "6" )
			If ( cApropri == "3" .And. SD3->D3_TM <= "500" )
				//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Pega os 5 custos medios atuais do almoxarifado de processo. Ё
				//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
				aCM := PegaCMAtu(SD3->D3_COD, cLocProc)
			Else
				//здддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Pega os 5 custos medios atuais             Ё
				//юдддддддддддддддддддддддддддддддддддддддддддды
				aCM := PegaCMAtu(SD3->D3_COD,SD3->D3_LOCAL)
			EndIf
		Else
			aCM := Array(5)
			For nCntFor := 1 To 5
				aCM[nCntFor] := IIf(&(Eval(bBloco,"SD3->D3_CUSTO",nI))=0,0,&(Eval(bBloco,"SD3->D3_CUSTO",nCntFor))/SD3->D3_QUANT)
			Next nCntFor
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Grava o custo da movimentacao              Ё
		//юдддддддддддддддддддддддддддддддддддддддддддды
		aCusto := GravaCusD3(aCM)

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se o movimento e' referente a material indireto        Ё
		//Ё e atualiza o saldo atual (VATU) do processo com os dados do SD3 Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( cApropri == "3" )
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Atualiza o saldo atual (VATU) com os dados do SD3     Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			
			If !Empty(cLocProc) .AND. (cLocProc <> cLocal)
				B2AtuComD3(aCusto,cLocProc)
			EndIf

			B2AtuComD3(aCusto)
			
		Else
			//зддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Atualiza o saldo atual (VATU) com os dados do SD3     Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддддддддды
			B2AtuComD3(aCusto)
		EndIf
		//зддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Atualiza os saldos atuais (VATU) das OP's Ё
		//юддддддддддддддддддддддддддддддддддддддддддды
		If ( !Empty(SD3->D3_OP) )
			C2AtuComD3(aCusto)
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Atualiza arquivo de demandas se for movimento de material direto Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( cApropri == "0" )
			dbSelectArea("SB3")
			dbSetOrder(1)
			If (!DbSeek(xFilial("SB3")+SD3->D3_COD) )
				RecLock("SB3",.T.)
				SB3->B3_FILIAL := xFilial("SB3")
				SB3->B3_COD    := SD3->D3_COD
			Else
				RecLock("SB3",.F.)
			EndIf
			aadd(aTravas, { Alias() , RecNo() })
			nCampoSB3 := SB3->(FieldPos(cMes))
			If ( SD3->D3_TM <= "500" )
				FieldPut(nCampoSb3,FieldGet(nCampoSb3)-SD3->D3_QUANT)
			Else
				FieldPut(nCampoSb3,FieldGet(nCampoSb3)+SD3->D3_QUANT)
			EndIf
		EndIf
		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se o custo medio e' calculado On-Line               Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If ( nHdlPrv <> 0 )
			//зддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Gera o lancamento no arquivo de prova           Ё
			//юддддддддддддддддддддддддддддддддддддддддддддддддды
			If ( SD3->D3_TM <= "500" )
				nTotal+=DetProva(nHdlPrv,"668","MATA240",cLoteEst)
			Else
				nTotal+=DetProva(nHdlPrv,"666","MATA240",cLoteEst)
			EndIf
		EndIf
	Next nSD3
	If ( nHdlPrv <> 0 )
		RodaProva(nHdlPrv,nTotal)
		cA100Incl(cArquivo,nHdlPrv,3,cLoteEst,lDigita,lAglutina)
	EndIf
EndIf
For nCntFor := 1 To Len(aTravas)
	dbSelectArea(aTravas[nCntFor,1])
	dbGoto(aTravas[nCntFor,2])
	MsUnLock()
Next nCntFor

Return(aSD3)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460F4   Ё Autor Ё Eduardo Riera         Ё Data Ё02.02.99  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁHot Key para Localizacao Fisica                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁNenhum                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁa,b,c passados pela Setkey                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function At460F4(a,b,c)

Local bSetKey     := SetKey(VK_F4)
Local nPosProd    := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODPRO"})
Local nPosQtd     := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QUANT"})
Local nPosLocal   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCAL"})

If ( "ABA_LOCALI"$ReadVar() )
	F4Localiz(a,b,c,"ATEC",aCols[n][nPosProd],aCols[n][nPosLocal],aCols[n][nPosQtd],ReadVar())
EndIf

If ( "ABA_LOTECT"$ReadVar() .Or. "ABA_NUMLOT"$ReadVar() )
	F4Lote(a,b,c,"AT460",aCols[n][nPosProd],aCols[n][nPosLocal])
EndIf

If ( "ABA_CODPRO"$ReadVar() )
	MaViewSB2(M->ABA_CODPRO)
EndIf

SetKey(VK_F4,bSetKey)

Return(.T.)

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAtLocalizaЁ Autor Ё Vendas e CRM          Ё Data Ё 02.02.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Localizacao Fisica                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁLogico                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AtLocaliza()
Local aArea 		:= {}
Local aAreaSBE 	:= {}
Local aAreaSX3 	:= {}
Local nPosLocal   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCAL"})
Local cLocaliz    := &(ReadVar())
Local lRetorno	:= .T.

	If !Empty(cLocaliz)
		If nPosLocal>0 // se existe a coluna Local

			aArea := GetArea()
			If Empty(aCols[n][nPosLocal])
				dbSelectArea("SX3")
				aAreaSX3 := SX3->(GetArea())
				dbSetOrder(2)
				dbSeek("ABA_LOCAL")
				Help( " ", 1,"AtLocaliza",,i18n(STR0080,{AllTrim(X3TITULO())}),1,0) //"O #Armazem# nЦo foi informado. Verifique."
				RestArea(aAreaSX3)

			Else
				dbSelectArea("SBE")
				aAreaSBE := SBE->(GetArea())
				dbSetOrder(1)
				If !SBE->(dbSeek(xFilial("SBE")+aCols[n][nPosLocal]+cLocaliz))
					Help( " ", 1, "REGNOIS")
					lRetorno := .F.
				EndIf
				RestArea(aAreaSBE)
			EndIf
			RestArea(aArea)
		EndIf
	EndIf

Return(lRetorno)


/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460LocTeЁ Autor Ё Vendas e CRM          Ё Data Ё 07.12.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁValidacao da Localiz. Fisica do Tecnico - Valid AB9_CODTEC  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁLogico                                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁNenhum                                                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460LocTe()

Local lRetorno := .t.

Local nLoop
Local nPosLocali := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCALI"})
Local nPosProd   := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODPRO"})
Local nPosLocal  := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCAL"})
Local nPosSer    := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODSER"})

dbSelectArea("AA1")
dbSetOrder(1)
dbSeek(xFILIAL("AA1") + M->AB9_CODTEC)

Help(" ",1,"ATULOCATEC")

For nLoop := 1 To Len(aCols)

	If ( !aCols[nLoop][Len(aHeader) + 1 ] )

		dbSelectArea("AA5")
		dbSetOrder(1)
		DbSeek(xFilial("AA5")+aCols[nLoop][nPosSer])

		dbSelectArea("SB1")
		dbSetOrder(1)

		dbSeek(xFILIAL("SB1") + aCols[nLoop][nPosProd])

		If AA5->AA5_ATUEST=="S"
			aCols[nLoop][nPosLocal]  := AA1->AA1_LOCAL
			aCols[nLoop][nPosLocali] := AA1->AA1_LOCLZB
		EndIf

	EndIf

Next nLoop

Return( lRetorno )




/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460GetSeЁ Autor Ё Vendas e CRM          Ё Data Ё 08.12.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁRetorna codigos de Local/Localizacao conforme status do     Ё╠╠
╠╠Ё          Ёcampo AA5_ATUOS apos selecionar o Codigo do Servico         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁcRetorno = Codigo de Local ou Localizacao                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁlLocal = Aponta se a o conteudo retornado sera atribuido ao Ё╠╠
╠╠Ё          Ё         campo ABA_Local ou ABA_LOCALIZ                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460GetSe(lLocal)


Local nPosSer  := aScan(aHeader,{|x| Alltrim(x[2])=="ABA_CODSER"}),;
	  cAlias   := Alias()
Local cRetorno := ""

cRetorno := If(lLocal,RetFldProd(SB1->B1_COD,"B1_LOCPAD"),"")

dbSelectArea("AA5")
dbSetOrder(1)
DbSeek(xFilial("AA5")+aCols[n][nPosSer])

dbSelectArea("AA1")
dbSetOrder(1)
DbSeek(xFILIAL("AA1") + M->AB9_CODTEC)

dbSelectArea("SF4")
dbSetOrder(1)
DbSeek(xFilial("SF4")+AA5->AA5_TES)

If lLocal
	cRetorno := If(AA5->AA5_ATUEST=="S" .OR. SF4->F4_ESTOQUE=="S",AA1->AA1_LOCAL,Space(Len(AA1->AA1_LOCAL)))
Else
	cRetorno := If(AA5->AA5_ATUEST=="S" .OR. SF4->F4_ESTOQUE=="S",AA1->AA1_LOCLZB,Space(Len(AA1->AA1_LOCLZB)))
EndIf

dbSelectArea(cAlias)

Return( cRetorno )

/*/
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460TransЁ Autor Ё Vendas e CRM          Ё Data Ё 29.12.99 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁTransferencia de estoque entre localizacoes do tecnico      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   ЁcRetorno = Codigo de Local ou Localizacao                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁlEstorno  = Aponta transferencia como estorno quando .T.    Ё╠╠
╠╠Ё          ЁcLoclzDst = Localizacao Destino                             Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function At460Trans(lEstorno)

Local lRetorno := .T.,;
	aSD3Req  := {} ,;
	aCampos  := {}

Local nLoop
Local xLoteCtl := If( Empty( ABA->( FieldPos( "ABA_LOTECT" ) ) ), "", ABA->ABA_LOTECT )
Local xNumLote := If( Empty( ABA->( FieldPos( "ABA_NUMLOT" ) ) ), "", ABA->ABA_NUMLOT )

If !Empty(ABA->ABA_LOCAL) .AND. !Empty(ABA->ABA_LOCALI) .AND. !Empty(ABA->ABA_LOCLZD)

	If lEstorno

		/*
		//зддддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMovimenta estoque Localizacao Ruim - RequisicaoЁ
		//юддддддддддддддддддддддддддддддддддддддддддддддды
		*/

		AtuEstoque(ABA->ABA_CODPRO,ABA->ABA_LOCALD,xLoteCtl,xNumLote,ABA->ABA_LOCLZD,ABA->ABA_NUMSER,ABA->ABA_QUANT,,,1,ABA->ABA_NUMOS)

		/*
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMovimenta estoque Localizacao Boa - Devolucao Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		*/

		AtuEstoque(ABA->ABA_CODPRO,ABA->ABA_LOCAL,xLoteCtl,xNumLote,ABA->ABA_LOCALI,ABA->ABA_NUMSER,ABA->ABA_QUANT,,,2,ABA->ABA_NUMOS)

	Else

		/*
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMovimenta estoque Localizacao Boa - RequisicaoЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		*/

		aSD3Req := AtuEstoque(ABA->ABA_CODPRO,ABA->ABA_LOCAL,xLoteCtl,xNumLote,ABA->ABA_LOCALI,ABA->ABA_NUMSER,ABA->ABA_QUANT,,,1,ABA->ABA_NUMOS)

		/*
		//здддддддддддддддддддддддддддддддддддддддддддддд©
		//ЁMovimenta estoque Localizacao Ruim - DevolucaoЁ
		//юдддддддддддддддддддддддддддддддддддддддддддддды
		*/

		For nLoop := 1 To Len(aSD3Req)

			Aadd(aCampos,{ "D3_LOTECTL", aSD3Req[nLoop][1] })
			Aadd(aCampos,{ "D3_NUMLOTE", aSD3Req[nLoop][2] })
			Aadd(aCampos,{ "D3_LOCALIZ",  ABA->ABA_LOCLZD    })
			Aadd(aCampos,{ "D3_NUMSERI", aSD3Req[nLoop][4] })
			Aadd(aCampos,{ "D3_QUANT"  , aSD3Req[nLoop][5] })

			AtuEstoque(ABA->ABA_CODPRO,ABA->ABA_LOCALD,"",,,,aSD3Req[nLoop][5],,aCampos,2,ABA->ABA_NUMOS)

			aCampos := {}

		Next nLoop

	EndIf

Else
	If ( AA5->AA5_ATUEST == "S" )
		AtuEstoque(ABA->ABA_CODPRO,ABA->ABA_LOCAL,xLoteCtl,xNumLote,ABA->ABA_LOCALI,ABA->ABA_NUMSER,ABA->ABA_QUANT,,,If(lEstorno,2,1),ABA->ABA_NUMOS)
	EndIf
EndIf

Return(lRetorno)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460Pend Ё Autor Ё Vendas e CRM        Ё Data Ё 19/01/2000 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz a chamada da rotina de manutencao de pendencias        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460Pend( ExpO1 )                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpO1 -> Objeto GetDados                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460Pend( oGetD )

Local aHeadBack
Local aColsBack

Local cBaseInst

Local nRowPos
Local nAt
Local nBack
Local lSavInclui := INCLUI

AB7->( dbSetOrder( 1 ) )

If AB7->( DbSeek( xFilial( "AB7" ) + M->AB9_NUMOS ) )

	//зддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Pesquisa pelo equipamento na base instalada Ё
	//юддддддддддддддддддддддддддддддддддддддддддддды
	AA3->( dbSetOrder( 4 ) )

	cBaseInst := AB7->AB7_CODFAB + AB7->AB7_LOJAFA + AB7->AB7_CODPRO + ;
		AB7->AB7_NUMSER

	If AA3->( DbSeek( xFilial( "AA3" ) + cBaseInst ) )

		nBack   := n
		nRowPos := oGetD:oBrowse:nRowPos
		nAt     := oGetD:oBrowse:nAt

		aHeadBack := AClone( aHeader )
		aColsBack := AClone( aCols )

		oGetD:oBrowse:lDisablePaint := .T.

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Faz a chamada da rotina de manutencao de pendencias Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддды
		TECA660( "At660Pend" )

		CursorArrow()

		aHeader   := AClone( aHeadBack )
		aCols     := AClone( aColsBack )

		oGetD:oBrowse:lDisablePaint := .F.

		oGetD:oBrowse:nRowPos := nRowPos
		oGetD:oBrowse:nAt     := nAt

		n := nBack

		oGetD:oBrowse:Refresh()

	EndIf

EndIf

INCLUI := lSavInclui

Return( .T. )


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460LjFabЁ Autor Ё Vendas e CRM        Ё Data Ё 10/04/2000 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Valida a loja do fabricante atual e anterior / atendimento Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460LjFab()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460LjFab()

Local aAreaSA1  := SA1->( GetArea() )
Local cVar      := ReadVar()
Local cConteudo := &( cVar )
Local lRet      := .F.

Do Case
Case "ABA_LOJAFA" $ cVar
	SA1->( dbSetOrder( 1 ) )
	lRet := SA1->( dbSeek( xFilial( "SA1") + GDFieldGet( "ABA_CODFAB" ) + cConteudo ) )
Case "ABA_LOJANT" $ cVar
	SA1->( dbSetOrder( 1 ) )
	lRet := SA1->( dbSeek( xFilial( "SA1") + GDFieldGet( "ABA_FABANT" ) + cConteudo ) )
EndCase

RestArea( aAreaSA1 )

Return( lRet )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460SolicЁ Autor Ё Vendas e CRM        Ё Data Ё01/06/2000  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Chamada da solicitacao ao almoxarifado                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460Solic()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460Solic()

Local cSequencia := CriaVar( "ABF_SEQRC", .F. )

Local nOpca := 0

Local oGet1
Local oBut1
Local oBut2
Local oDlgSeq

DEFINE MSDIALOG oDlgSeq TITLE STR0027 From ;
	9,0 To 20, 40 OF oMainWnd // "Solicitacao ao almoxarifado"

@   0, 0 BITMAP oBmp RESNAME "LOGIN" oF oDlgSeq SIZE 30, 120 NOBORDER WHEN .F. PIXEL

@ 03, 40 SAY STR0034 PIXEL //
@ 11 ,30 TO 13 ,400 LABEL '' OF oDlgSeq PIXEL

@ 35, 40 SAY STR0028 SIZE 28, 08 PIXEL // "Sequencia "
@ 35, 80 MSGET cSequencia F3 "ABF" SIZE 25, 08 of oDlgSeq PIXEL VALID At460VlSeq( cSequencia, oBut1 )

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Este GET foi criado para receber o foco do get acima. Nao retirar !!! Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

@ 1000, 1000 MSGET oGet1 VAR cSequencia SIZE 25, 08 of oDlgSeq PIXEL
oGet1:bGotFocus := { || oBut1:SetFocus() }

DEFINE SBUTTON oBut1 FROM 45, 125 TYPE 1 ACTION ( nOpca := 1,;
	oDlgSeq:End() ) ENABLE of oDlgSeq

DEFINE SBUTTON oBut2 FROM 65, 125 TYPE 2 ACTION ( nOpca := 0,;
	oDlgSeq:End() ) ENABLE of oDlgSeq

oBut1:Disable()

ACTIVATE MSDIALOG oDlgSeq CENTERED

If nOpca == 1
	At460BrSeq( ABF->ABF_NUMOS, ABF->ABF_ITEMOS, ABF->ABF_SEQRC )
EndIf

CursorArrow()

Return( .T. )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460VlSeqЁ Autor Ё Vendas e CRM        Ё Data Ё30/05/2000  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da sequencia de solicitacao ao almoxarifado      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460VlSeq( [ExpC1], [ExpO1] )                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Sequencia / ExpO1 -> Botao confirma               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function At460VlSeq( cSequencia, oBut1 )

Local cSeqRC
Local lRet     := .F.

cSeqRC := If( ValType( cSequencia ) == "C", cSequencia, &( ReadVar() ) )

ABF->( dbSetOrder( 1 ) )
If ABF->( dbSeek( xFilial("ABF") + M->AB9_NUMOS + cSeqRC ) )
	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a sequencia de solicitacao nao esta associada Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	lRet := .T.
Else
	Help( " ", 1, "REGNOIS")
	lRet := .F.
EndIf

If ValType( oBut1 ) == "O" .And. lRet
	oBut1:Enable()
EndIf

Return( lRet )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460BrSeqЁ Autor Ё Vendas e CRM        Ё Data Ё31/05/2000  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Alimenta o browse com os dados da solicitacao selecionada  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460BrSeq( ExpC1, ExpC2, ExpC3 )                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Numero da OS / ExpC2 -> Item da OS                Ё╠╠
╠╠Ё          Ё ExpC3 -> Sequencia da solicitacao / repair center          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460BrSeq( cNumOs, cItemOs, cSeqRC )

Local aColsLine := {}
Local aColsBack := AClone( aCols )

Local cItem
Local cSeekABG

Local lRet     := .T.
Local lAddLine := .F.

Local nPItem   := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_ITEM"})
Local nPCodPro := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_CODPRO"})
Local nPQuant  := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_QUANT"})
Local nPLocal  := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_LOCAL"})
Local nPCodSer := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_CODSER"})
Local nPDescri := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_DESCRI"})
Local nPItemRC := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_ITEMRC"})
Local nPSeqRC  := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_SEQRC"})
Local nPNumSer  := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_NUMSER"})
Local nPLocali  := aScan(aHeader,{|x| AllTrim(x[2])=="ABA_LOCALI"})

Local nQuant   := 0
Local nQuje    := 0
Local nLoop

Local cFilialSCQ := xFilial("SCQ")
Local cFilialSD3 := xFilial("SD3")
Local cFilialSCP := xFilial("SCP")
Local cFilialSB1 := xFilial("SB1")

Local aArea := {}
Local aAreaABG := {}
Local aAreaSCP := {}
Local aAreaSB1 := {}
Local aAreaSCQ := {}
Local aAreaSD3 := {}
If ( Len( aCols ) == 1 .And. Empty( GDFieldGet( "ABA_CODPRO" ) ) ) .Or. ;
		Empty( aCols )
	aCols := {}
	cItem := "00"
Else
	//здддддддддддддддддддддддддддддддддддд©
	//Ё Exclui os itens desta solicitacao  Ё
	//юдддддддддддддддддддддддддддддддддддды
	For nLoop := 1 to Len( aCols )
		If aCols[ nLoop, nPSeqRC ] == cSeqRC
			aCols[ nLoop, Len( aHeader ) + 1 ] := .T.
		EndIf
	Next nLoop

	cItem := ACols[ Len( aCols ), nPItem ]

EndIf


//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Cria uma linha em branco para o aCols                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
aColsLine := {}
AEval( aHeader, { |x| IIF( x[2] == "ABA_REC_WT", AAdd( aColsLine, 0 ), IIF( x[2] == "ABA_ALI_WT", AAdd( aColsLine, 'ABA' ), AAdd( aColsLine, CriaVar( x[2] ) ))) } )
AAdd( aColsLine, .F. )

cSeekABG := xFilial("ABG") + cNumOS + cItemOS + cSeqRC

aArea := GetArea()

dbSelectArea("ABG")
aAreaABG := ABG->(GetArea())
dbSetOrder( 1 )
If ABG->( dbSeek( cSeekABG ) )

	nQuje   := 0
	nQuant  := 0

	dbSelectArea("SD3")
	aAreaSD3 := SD3->(GetArea())
	dbSelectArea("SCQ")
	aAreaSCQ := SCQ->(GetArea())
	dbSelectArea("SB1")
	aAreaSB1 := SB1->(GetArea())
	dbSelectArea("SCP")
	aAreaSCP := SCP->(GetArea())

	While ( !ABG->( Eof() ) .And. cSeekABG == ABG->ABG_FILIAL + ABG->ABG_NUMOS + ;
			ABG->ABG_ITEMOS + ABG->ABG_SEQRC )

		//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Verifica se este item ja esta associado a um atendimento Ё
		//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		If  ( Empty( ABG->ABG_SEQ ) .And. Empty( ABG->ABG_CODTEC )) .Or. ;
				( ABG->ABG_SEQ == M->AB9_SEQ .And. ABG->ABG_CODTEC == M->AB9_CODTEC )
			//здддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
			//Ё Verifica se a solicitacao ao almoxarifado foi atendida Ё
			//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддды

			SCP->( dbSetOrder( 1 ) )
			If SCP->( dbSeek( cFilialSCP + ABG->ABG_NUMSA + ABG->ABG_ITEMSA ) )

				nQuje   += SCP->CP_QUJE
				nQuant  += SCP->CP_QUANT

				//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
				//Ё Se pelo menos uma unidade foi atendida               Ё
				//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды

				If !Empty( SCP->CP_QUJE )

					dbSelectArea("SB1")
					dbSetOrder(1)
					dbSeek(cFilialSB1+ABG->ABG_CODPRO)

					AAdd( aCols, AClone( aColsLine ) )

					cItem := SomaIt( cItem )

					aCols[ Len(aCols), nPItem   ] := cItem
					aCols[ Len(aCols), nPCodPro ] := ABG->ABG_CODPRO
					aCols[ Len(aCols), nPQuant  ] := SCP->CP_QUJE
					aCols[ Len(aCols), nPLocal  ] := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
					aCols[ Len(aCols), nPCodSer ] := ABG->ABG_CODSER
					aCols[ Len(aCols), nPDescri ] := SB1->B1_DESC
					aCols[ Len(aCols), nPItemRC ] := ABG->ABG_ITEM
					aCols[ Len(aCols), nPSeqRC ]  := ABG->ABG_SEQRC

					// Busca na pre-requisicao para conseguir achar
					// qual movimento interno foi gerado com o produto para a OS
					dbSelectArea("SCQ")
					dbSetOrder(1)
					If dbSeek(cFilialSCQ+SCP->CP_NUM+SCP->CP_ITEM)
						If !Empty(CQ_NUMREQ)
							SD3->(dbSetOrder(4))
							If SD3->(dbSeek(cFilialSD3+SCQ->CQ_NUMREQ+"E0"+SCQ->CQ_PRODUTO))
								If SD3->D3_ESTORNO == " " .AND. SD3->D3_OSTEC==ABG->(ABG_NUMOS+ABG_ITEMOS)
									aCols[ Len(aCols), nPNumSer ] := SD3->D3_NUMSERI
									aCols[ Len(aCols), nPLocali ]  := SD3->D3_LOCALIZ
								EndIf
							EndIf
						EndIf
					EndIf

					lAddLine := .T.

				EndIf
			EndIf
		EndIf

		ABG->( dbskip() )

	End

	RestArea(aAreaSD3)
	RestArea(aAreaSCQ)
	RestArea(aAreaSB1)
	RestArea(aAreaSCP)

	If  QtdComp( nQuje ) <> QtdComp( nQuant )
		If Empty( aCols )
			lRet := .F.
			Help( " ", 1, "AT460SNVAL" ) // A solicitacao selecionada nao possui nenhum item baixado !
		Else
			lRet := ( Aviso( STR0029, STR0030, { STR0031, STR0032 } ) == 1 )
			// "Atencao !", "A solicitacao ao almoxarifado selecionada nao foi totalmente atendida.", "Prossegue", "Abandona"
		EndIf
	EndIf

	If !lAddLine
		Help( " ", 1, "AT460RCIT" ) // Nao foi possivel trazer nenhum item desta solicitacao
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Retorna o aCols anterior                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	If !lRet .Or. Empty( aCols )
		aCols := aClone( aColsBack )
	EndIf

	//здддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Efetua um ForceRefresh()                             Ё
	//юдддддддддддддддддддддддддддддддддддддддддддддддддддддды
	Eval( aActions[ 2 ] )

EndIf

RestArea(aAreaABG)
RestArea(aArea)

Return( lRet )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFuncao    ЁAt460RcFilЁ Autor Ё Vendas e CRM        Ё Data Ё01/06/2000  Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Filtro para a consulta do Repair Center                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := At460RcFil()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> String de filtro                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function At460RcFil()

Return( xFilial( "ABF" ) + M->AB9_NUMOS )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460VlTrfЁ Autor Ё Vendas e CRM          Ё Data Ё05/07/2000Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao da digitacao da tarefa no atendimento            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460VlTrf()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Validacao                                         Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460VlTrf()

Local cTarefa := &( ReadVar() )
Local lRet    := .F.
Local nTamOS  := Len( CriaVar( "AB7_NUMOS", .F. ) + CriaVar( "AB7_ITEM", .F. ) )

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Verifica se o atendimento da OS se referencia ao mesmo projeto da     Ё
//Ё tarefa selecionada                                                    Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Pesquisa pela tarefa selecionada                                      Ё
//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

ABJ->( dbSetOrder( 1 ) )
If ABJ->( dbSeek( xFilial( "ABJ" ) + cTarefa ) )

	//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
	//Ё Verifica se a tarefa esta encerrada ou cancelada                      Ё
	//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды

	If ABJ->ABJ_SITUAC == "2" .And. ABJ->ABJ_SITATU == "1"

		//зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
		//Ё Pesquisa pela etapa para verificar se a mesma esta associada esta OS  Ё
		//юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		ABI->( dbSetOrder( 1 ) )
		If ABI->( dbSeek( xFilial( "ABI" ) + Left( cTarefa, 8 ) ) )

			If ABI->ABI_SITATU == "1"
				If Left( M->AB9_NUMOS, nTamOS ) == ABI->ABI_NUMOS
					lRet := .T.
				Else
					Help( " ", 1, "AT460OSETA" ) // A etapa nao pertence a esta OS
					lRet := .F.
				EndIf
			Else
				Help( " ", 1, "AT460ETCANC" ) // A etapa esta cancelada
				lRet := .F.
			EndIf
		Else
			Help( " ", 1, "REGNOIS" ) // Registro nao encontrado
			lRet := .F.
		EndIf

	Else
		Help( " ", 1, "AT460TFCANC" ) // Esta tarefa esta cancelada ou encerrada
	EndIf

Else
	Help( " ", 1, "REGNOIS" )
	lRet := .F.
EndIf

Return( lRet )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT460Tab    Ё Autor Ё Vendas e CRM        Ё Data Ё 26/06/01 Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao de Calculo do Preco de Tabela                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpN1 := AT460Tab(ExpC1,ExpN2)                             Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Produto                                           Ё╠╠
╠╠Ё          Ё ExpN2 -> Quantidade                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpN1 := Preco de tabela                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function AT460Tab(cProduto,nQtde)

Local aArea   := GetArea()
Local nPrcVen := 0

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Posiciona o AB6                                                        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
AB7->( dbSetOrder(1) )
AB7->( DbSeek(xFilial("AB7")+M->AB9_NUMOS) )

AB6->( dbSetOrder(1) )
AB6->( DbSeek(xFilial("AB6")+AB7->AB7_NUMOS) )

dbSelectArea("SB1")
dbSetOrder(1)
If ( !Empty(cProduto) .And. DbSeek(xFilial("SB1")+cProduto,.F.) )
	nPrcVen := MaTabPrVen( AB6->AB6_TABELA,cProduto,nQtde,AB6->AB6_CODCLI,AB6->AB6_LOJA,AB6->AB6_MOEDA)
EndIf

RestArea( aArea )

Return (nPrcVen)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддбдддддддбдддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    ЁAT460VlLot  Ё Autor Ё Vendas e CRM        Ё Data Ё02/08/2001Ё╠╠
╠╠цддддддддддеддддддддддддадддддддадддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao para validacao do lote / sublote                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := AT460VlLot()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ                                                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 := .T.                                               Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё TECA460                                                    Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function AT460VlLot()

Return( .T. )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤└o    ЁAt460VlApoЁ Autor Ё Vendas e CRM          Ё Data Ё05/09/2001Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Verifica se o perido de data / hora inicio / fim esta      Ё╠╠
╠╠Ё          Ё contido no periodo de chegada / saida no atendimento da OS Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460VlApo()                                      Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 := Validacao                                         Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё SIGATEC                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁObservacaoЁ                                                            Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460VlApo()

Local cHrVazio := "  :  |" + Space( 5 )
Local lRetorno := .T.

lRetorno := .T.

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Valida apenas se todos os itens estiverem preenchidos        Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If !Empty( M->AB9_DTCHEG ) .And. !Empty( M->AB9_DTSAID ) .And. ;
		!( M->AB9_HRCHEG $ cHrVazio ) .And. !( M->AB9_HRSAID $ cHrVazio ) .And. ;
		!Empty( M->AB9_DTINI ) .And. !Empty( M->AB9_DTFIM ) .And. ;
		!( M->AB9_HRINI $ cHrVazio ) .And. !( M->AB9_HRFIM $ cHrVazio )

	lRetorno := ( DToS( M->AB9_DTINI ) + Str( HoraToInt( M->AB9_HRINI ), 5, 2 ) ) >= ;
		( DToS( M->AB9_DTCHEG ) + Str( HoraToInt( M->AB9_HRCHEG ), 5, 2 ) ) .And. ;
		( DToS( M->AB9_DTSAID ) + Str( HoraToInt( M->AB9_HRSAID ), 5, 2 ) ) >= ;
		( DToS( M->AB9_DTFIM ) + Str( HoraToInt( M->AB9_HRFIM ), 5, 2 ) )

EndIf

Return( lRetorno )


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460TrackЁ Autor Ё Vendas e CRM          Ё Data Ё09/01/2002Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Faz o tratamento da chamada do System Tracker              Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё .T.                                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ Nenhum                                                     Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At460Track()

Local aEnt     := {}
Local cChave   := M->AB9_NUMOS + M->AB9_CODTEC + M->AB9_SEQ

//зддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Carrega os itens para rastreamento          Ё
//юддддддддддддддддддддддддддддддддддддддддддддды
AAdd( aEnt, { "AB9", cChave } )

MaTrkShow( aEnt )

Return( .T. )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460nConfЁ Autor Ё Vendas e CRM          Ё Data Ё17/12/2002Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o ЁFuncao a executar quando da nao-confirmacao da janela       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := At460nConf( ExpN1, ExpO1 )                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> Indica se valida a nao-confirmacao                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpN1 -> Operacao : 1-Inclusao / 2-Alteracao / 3-Exclusao  Ё╠╠
╠╠Ё          Ё ExpO1 -> Objeto da dialog                                  Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function At460nConf( nOper, oDlg, lIntQNC, aCamposFNC )

Local lClose := .T.
Local cCompl := STR0055 	// " Motivo: OS Cancelada pelo usuario durante a inclusao"
Default lIntQNC := .F. 		// Define a IntegraГЦo com o ambiente Controle de Nao-Conformidades

//здддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
//Ё Ponto de entrada para a nao-confirmacao                                Ё
//юдддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
If ExistBlock( "AT460NCF" )
	lClose := ExecBlock( "AT460NCF", .F., .F., { nOper } )
EndIf


If lIntQNC // Se houver a integracao entre o QNC e o TEC
	// Se o array dos campos da FNC estiver uma coluna isto indica que ha FNC
	If !Empty(aCamposFNC) .AND. Len(aCamposFNC) == 1 .AND. Len(aCamposFNC[1]) == 3 // garanto que a estrutura do array tem uma linha e 3 colunas
	    //Baixa a FNC quando for  pressionado o Botao Cancela
		QNCBAIXA(aCamposFNC[1][2], aCamposFNC[1][3],"TEC", cCompl)
	EndIf
EndIf

If lClose
	oDlg:End()
EndIf

Return( Nil )

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460nConfЁ Autor Ё Vendas e CRM          Ё Data Ё20/01/2006Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Funcao que  chama Tela da FNC Preenchida                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function AT460FNC(oDlg , oGetD, aCamposFNC, lVisualiza)
Local cResMat	:= AllTrim(SuperGetMv("MV_QNCMRES",.F.,"1234567890"))  			// Usuario Responsavel
Default aCamposFNC := {}  													// Arrar com os  dados da FNC
If lVisualiza
    QI2->(DbSetOrder(2))
    If QI2->(DbSeek(xFilial("QI2")+AB9->AB9_CODFNC+AB9->AB9_FNCREV))
    	QI2->(Recno())
		QNC040ALT("QI2",QI2->(Recno()),2)
	EndIf
Else
	If ALTERA .AND. !Empty(AB9->AB9_CODFNC)   // Caso haja FNC gerada eu visualizo ela
	    QI2->(DbSetOrder(2))
	    If QI2->(DbSeek(xFilial("QI2")+AB9->AB9_CODFNC+AB9->AB9_FNCREV))
	    	QI2->(Recno())
			QNC040ALT("QI2",QI2->(Recno()),2)
		EndIf
	ElseIf !Empty(M->AB9_NUMOS)               // Inclusao da FNC
		Aadd(aCamposFNC,{"QI2_TPFIC" 	,"1"})
		Aadd(aCamposFNC,{"QI2_ORIGEM"	,"TEC"})
		Aadd(aCamposFNC,{"QI2_DESCR"	,STR0056+SUBSTR(M->AB9_NUMOS,1,6)+"/"+SUBSTR(M->AB9_NUMOS,7,2)}) //"OS: "
		Aadd(aCamposFNC,{"QI2_MEMO1"  , M->AB9_MEMO2})
		Aadd(aCamposFNC,{"QI2_MATRES"  , cResMat})
		//Campos especificos da integracao
		Aadd(aCamposFNC,{"QI2_NUMOS"  , SUBSTR(M->AB9_NUMOS,1,6)})   // Numero da OS
		Aadd(aCamposFNC,{"QI2_ITEMOS"  , SUBSTR(M->AB9_NUMOS,7,2)})  // Item OS

		QNC040ALT("QI2",0,3,.T., @aCamposFNC)
	EndIf
EndIf

Return


/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460DFNC Ё Autor Ё Vendas e CRM                    Ё Data Ё 19/01/06 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Deleta as FNC criadas                                                Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё FIELD SERVICE - ExclusЦo de Chamado Tecnico                          Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function At460DFNC()
Local aArea := GetArea() 	// Salva area atual

DbSelectArea("AB9")
DbSetOrder(1)
If DbSeek(xFilial("AB9")+M->AB9_NUMOS)
	DbSelectArea("QI2")
	DbSetOrder(2)
    If DbSeek(xFilial("QI2")+AB9->AB9_CODFNC+AB9->AB9_FNCREV)
   		DbSelectArea("QI2")
        // Prevencao Auditorias
		RecLock("QI2",.F.)
		cVar := ""
		If Empty(QI2->QI2_COMEN)	// Inclusao
			MSMM(,TamSx3("QI2_MEMO2")[1],,STR0057+UPPER(QA_AmbDet( Nil, "SIGATEC", 4 ))+STR0058,1,,,"QI2","QI2_COMEN") // " ** FICHA DE NAO CONFORMIDADE CANCELADA PELO"###" ** - Motivo: OS Excluida"
		Else						// Alteracao
			MSMM(QI2->QI2_COMEN,TamSx3("QI2_MEMO2")[1],,STR0057+UPPER(QA_AmbDet( Nil, "SIGATEC", 4 ))+STR0058,1,,,"QI2","QI2_COMEN") // " ** FICHA DE NAO CONFORMIDADE CANCELADA PELO "###" ** - Motivo: OS Excluida"
		Endif
		MsUnLock()
	EndIf
	If DbSeek(xFilial("QI2")+AB9->AB9_CODFNC+AB9->AB9_FNCREV)
	   	Begin Transaction
			DbSelectArea("QI2")
		    // Deleto o Registro
			RecLock("QI2",.F.,.T.)
			DbDelete()
			MsUnLock()
		End Transaction
	EndIf
EndIf
RestArea(aArea)
Return Nil

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё AT460FilABJ Ё Autor Ё Vendas e CRM                 Ё Data Ё 25/05/07 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Filtra os registros da consulta padrЦo ABJ                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё FIELD SERVICE - InclusЦo de Chamado Tecnico                          Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Function AT460FilABJ()
Local aArea		:= GetArea()	// Salva a Аrea atual
Local lFiltra	:= .F.			// Se filtra o registro ou nЦo

DbSelectArea("ABI")
DbSetOrder(2)
If DbSeek(xFilial("ABI")+M->AB9_NUMOS)
	If ABJ->ABJ_PROJET==ABI->ABI_PROJET
		lFiltra := .T.
	EndIf
EndIf

RestArea( aArea )
Return lFiltra

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAT460MonTeЁ Autor Ё Rodrigo Toledo        Ё Data Ё 11.04.11 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Monta a tela para os produtos que excederam no limite da   Ё╠╠
╠╠Ё          Ё quantidade digitada no contrato.	  						  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё lRet = valor logico                                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё AT460MonTe(ExpC1,ExpC2,ExpC3,ExpC4)				  		  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 = codigo do cliente								  Ё╠╠
╠╠Ё          Ё ExpC2 = codigo da loja							  		  Ё╠╠
╠╠Ё          Ё ExpC3 = codigo do produto				  		  		  Ё╠╠
╠╠Ё          Ё ExpC4 = Numero de serie   				  		  		  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠Ё Uso      Ё AT460MonTe                                                 Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function AT460MonTe(cCodCli,cLojaCli,cCodPro,cNumSer)
Local aAreaAA3 := GetArea()
Local aAreaAAQ := GetArea()
Local aAreaAAR := GetArea()
Local nLoop    := 0
Local nOpc     := 0
Local aColsSal := {}
Local lRet	   := .T.

AA3->(DbSetOrder(1))
AAQ->(DbSetOrder(1))
If AA3->(DbSeek(xFilial("AA3")+cCodCli+cLojaCli+cCodPro+cNumSer))
	For nLoop := 1 To Len(aCols)
		If AAQ->(DbSeek(xFilial("AAQ")+AA3->AA3_CONTRT+aCols[nLoop,GDFieldPos("ABA_CODPRO")]))
			If aCols[nLoop,GDFieldPos("ABA_QUANT")] > AAQ->AAQ_QTDLIB .And. !(aCols[nLoop,Len(aHeader)+1]) .And.;
			   aCols[nLoop,GDFieldPos("ABA_CODSER")] == AtBuscServ(AA3->AA3_CONTRT,.T.)
			   aAdd(aColsSal,{AAQ->AAQ_CODPRO,;          	   				 								  //1- Codigo do Produto
					 aCols[nLoop,GDFieldPos("ABA_DESCRI")],;  				 								  //2- Descricao do Produto
					 AAQ->AAQ_QTDLIB,;						   				 								  //3- Quantidade liberada
				     aCols[nLoop,GDFieldPos("ABA_QUANT")],;   				 								  //4- Quantidade digitada
					 aCols[nLoop,GDFieldPos("ABA_QUANT")] - AAQ->AAQ_QTDLIB,TamSx3("ABA_QUANT")[2]})   	  //5- Quantidade excedida
			EndIf
		EndIf
	Next nLoop
	If ( Type("l460Auto") == "U" .Or. !l460Auto )
		If Len(aColsSal) > 0
			If (MsgYesNo(STR0061,STR0029)) //Existem produtos com quantidades superadas pelo limite estipulado no contrato. Deseja prosseguir com o atendimento da OS?
				DEFINE DIALOG oDlg TITLE STR0062 FROM 180,180 TO 550,800 PIXEL STYLE WS_DLGFRAME //Produtos com superaГЦo do limite estipulado no contrato

				aBrowse	:= aClone(aColsSal)
				oBrowse := TCBrowse():New( 01 , 01, 320, 160,,;
										{STR0063,STR0064,STR0065,STR0066,STR0067},{20,130,35,35,35},oDlg,,,,,{||},,,,,,,.F.,,.T.,,.F.,,, ) //Codigo#Descricao#Qtde Liberada#Qtde Digitada#Qtde Excedida
				oBrowse:SetArray(aBrowse)
				oBrowse:bLine := {||{ aBrowse[oBrowse:nAt,01],;
									aBrowse[oBrowse:nAt,02],;
									Transform(aBrowse[oBrowse:nAt,03],'@E 99,999,999,999.99'),;
									Transform(aBrowse[oBrowse:nAt,04],'@E 99,999,999,999.99'),;
									Transform(aBrowse[oBrowse:nAT,05],'@E 99,999,999,999.99')}}

				TButton():New( 170, 228, STR0068, oDlg,{|| nOpc:=1,oDlg:End()},40,010,,,.F.,.T.,.F.,,.F.,,,.F. ) //Confirmar
				TButton():New( 170, 270, STR0069, oDlg,{|| nOpc:=2,oDlg:End()},40,010,,,.F.,.T.,.F.,,.F.,,,.F. ) //Cancelar
				ACTIVATE DIALOG oDlg CENTERED
			Else
				lRet :=	.F.
			EndIf
		EndIf
	EndIf
EndIf

lRet := If((nOpc) == 2,.F.,lRet)
RestArea(aAreaAA3)
RestArea(aAreaAAQ)

Return(lRet)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤└o    ЁAtBuscServЁ Autor Ё Rodrigo Toledo        Ё Data Ё 11/04/2011 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Retorna o codigo do servico do grupo de cobertura.		    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := AtBuscServ(ExpC1)                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Codigo do contrato                                	Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Codigo do servico						            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460                                                      Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Function AtBuscServ(cContrat,lItGrpCob)
Local aAreaAAH  := AAH->(GetArea())
Local aAreaAAA  := AAA->(GetArea())
Local aAreaAAB  := AAB->(GetArea())
Local cCodServ	:= ""


AAH->(DbSetOrder(1))
If AAH->(DbSeek(xFilial("AAH")+cContrat))
	If !lItGrpCob
		AAA->(DbSetOrder(1))
		If AAA->(DbSeek(xFilial("AAA")+AAH->AAH_CODGRP))
			cCodServ := AAA->AAA_CODSER
		EndIf
	Else
		AAB->(DbSetOrder(1))
		If AAB->(DbSeek(xFilial("AAB")+AAH->AAH_CODGRP))
			cCodServ := AAB->AAB_CODSER
		EndIf
	EndIf
EndIf

RestArea(aAreaAAH)
RestArea(aAreaAAA)
RestArea(aAreaAAB)
Return(cCodServ)

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддддд©╠╠
╠╠ЁFun┤└o    ЁAt460AjAcoЁ Autor Ё Rodrigo Toledo        Ё Data Ё 11/04/2011 Ё╠╠
╠╠цддддддддддеддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддддд╢╠╠
╠╠ЁDescri┤└o Ё Ajusta o acols para gravar o conteudo das informacoes nas 	Ё╠╠
╠╠Ё			 Ё tabelas ABA e AAR.											Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё At460AjAco(ExpC1,ExpC2,ExpC3,ExpC4)                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Codigo do contrato                                	Ё╠╠
╠╠Ё			 Ё ExpC2 -> Numero da OS                                		Ё╠╠
╠╠Ё			 Ё ExpC3 -> Codigo Tecnico                                		Ё╠╠
╠╠Ё			 Ё ExpC4 -> Sequencia                                			Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Nil						            						Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё At460AjAco                                                   Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/

Static Function At460AjAco(cContrato,cNumOs,cCodTec,cSeq)
Local aAreaAAR := AAR->(GetArea())
Local aAreaAAQ := AAQ->(GetArea())
Local nPos	   := 0
Local nNovoSal := 0
Local nSaldo   := 0
Local nLoop	   := 0
Local nX	   := 0
Local aProd    := {}
Local cMaxItem := ""

AAQ->(DbSetOrder(1))
AAR->(DbSetOrder(1))
ABA->(DbSetOrder(1))

For nLoop := 1 To Len(aCols)
	If (nPos := aScan(aProd,{|x| x[1] == aCols[nLoop,GDFieldPos("ABA_CODPRO")]})) == 0
		aAdd(aProd,{aCols[nLoop,GDFieldPos("ABA_CODPRO")],Array(2)})
		nPos := Len(aProd)
	EndIf
	If AtBuscServ(cContrato,.T.) == aCols[nLoop,GDFieldPos("ABA_CODSER")]
		aProd[nPos,2,1] := nLoop
	ElseIf AtBuscServ(cContrato,.F.) == aCols[nLoop,GDFieldPos("ABA_CODSER")]
		aProd[nPos,2,2] := nLoop
	EndIf
Next nLoop

For nLoop := 1 To Len(aProd)
	If Empty(aProd[nLoop,2,1]) .Or. !AAQ->(DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]))
		Loop
	EndIf
	ABA->(DbSeek(xFilial("ABA")+cNumOs+cCodTec+cSeq+aCols[nLoop,GDFieldPos("ABA_ITEM")]))
	If AAQ->AAQ_MODLIB == "1"
		If AAR->(!DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]))
			nNovoSal := AAQ->AAQ_QTDLIB - aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")]
		Else
			nSaldo 	 := ABS(AAR->AAR_SALDO + ABA->ABA_QUANT)
			nNovoSal := nSaldo - aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")]
		EndIf
	ElseIf AAQ->AAQ_MODLIB == "2"
		If AAR->(!DbSeek(xFilial("AAR")+cContrato+aProd[nLoop,1]+LEFT(DTOS(AB9->AB9_DTFIM),6)))
			nNovoSal := AAQ->AAQ_QTDLIB - aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")]
		Else
			nSaldo 	 := ABS(AAR->AAR_SALDO + ABA->ABA_QUANT)
			nNovoSal := nSaldo - aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")]
		EndIf
	EndIf

	If nNovoSal >= 0
		If !Empty(aProd[nLoop,2,2])
			aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")] += Min(nNovoSal,aCols[aProd[nLoop,2,2],GDFieldPos("ABA_QUANT")])
			If nNovoSal >= aCols[aProd[nLoop,2,2],GDFieldPos("ABA_QUANT")]
				nNovoSal -= aCols[aProd[nLoop,2,2],GDFieldPos("ABA_QUANT")]
				aCols[aProd[nLoop,2,2],Len(aHeader)+1]	:= .T.
			Else
				aCols[aProd[nLoop,2,2],GDFieldPos("ABA_QUANT")] -= nNovoSal
			EndIf
		EndIf
	ElseIf nNovoSal < 0
		aCols[aProd[nLoop,2,1],GDFieldPos("ABA_QUANT")] += nNovoSal
		If !Empty(aProd[nLoop,2,2])
			aCols[aProd[nLoop,2,2],GDFieldPos("ABA_QUANT")] += ABS(nNovoSal)
		Else
			aEval(aCols,{|x| If(x[GDFieldPos("ABA_ITEM")] > cMaxItem,cMaxItem := x[GDFieldPos("ABA_ITEM")],NIL)})
			aAdd(aCols,Array(Len(aHeader)+1))
			For nX := 1 To Len(aHeader)
				If IsHeadRec(aHeader[nx,2])
					ATAIL(aCols)[nX] := 0
				ElseIf IsHeadAlias(aHeader[nx,2])
					ATAIL(aCols)[nX] := "ABA"
				Else
					ATAIL(aCols)[nX] := CriaVar(aHeader[nX,2],.T.)
				EndIf
			Next nX
			ATAIL(aCols)[GDFieldPos("ABA_ITEM")]   := Soma1(cMaxItem)
		 	ATAIL(aCols)[GDFieldPos("ABA_CODPRO")] := aProd[nLoop,1]
		 	ATAIL(aCols)[GDFieldPos("ABA_DESCRI")] := Posicione("SB1",1,xFilial("SB1")+aProd[nLoop,1],"B1_DESC")
		  	ATAIL(aCols)[GDFieldPos("ABA_CODSER")] := AtBuscServ(AA3->AA3_CONTRT,.F.)
		  	ATAIL(aCols)[GDFieldPos("ABA_QUANT")]  := ABS(nNovoSal)
		  	ATAIL(aCols)[GDFieldPos("ABA_LOCAL")]  := RetFldProd(aProd[nLoop,1],"B1_LOCPAD")
			ATAIL(aCols)[Len(aHeader)+1] := .F.
		Endif
	EndIf
Next nLoop

RestArea(aAreaAAR)
RestArea(aAreaAAQ)
Return()

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё AT460VlMax  Ё Autor Ё Totvs                        Ё Data Ё 12/04/11 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Valida o Vlr. Maximo para Apontamento do Contrato.                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpL1 := AT460VlMax( ExpC1, ExpD1, ExpN1 )                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Codigo da OS e Item                                         Ё╠╠
╠╠Ё          Ё ExpD1 -> Data do Atendimento da OS                                   Ё╠╠
╠╠Ё          Ё ExpN1 -> Valor da OS                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpL1 -> true se passou na validacao                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё FIELD SERVICE                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function AT460VlMax( cChave, dData, nTotal )
Local aArea		:= GetArea()
Local aAreaAA3	:= AA3->( GetArea() )
Local aAreaAAH	:= AAH->( GetArea() )
Local aAreaAB7	:= AB7->( GetArea() )
Local cEventID	:= "049"
Local lRet		:= .T.
Local nTotAtend := nTotal
Local lMV_TECBLAT := SuperGetMV( "MV_TECBLAT" )
Local lNt460Aut := ( Type( "l460Auto" ) <> "U" .AND. !l460Auto )

DbSelectArea( "AB7" )
AB7->( DbSetOrder( 1 ) )
AB7->( DbSeek( xFilial( "AB7" ) + cChave ) )
While AB7->( !Eof() ) .AND. AB7->( AB7_FILIAL + AB7_NUMOS + AB7_ITEM ) == xFilial( "AB7" ) + cChave

	// Localiza a base instalada
	DbSelectArea( "AA3" )
	AA3->( DbSetOrder( 1 ) )
	If AA3->( DbSeek( xFilial( "AA3" ) + AB7->( AB7_CODCLI + AB7_LOJA + AB7_CODPRO + AB7_NUMSER ) ) )

		// Localiza o contrato para verificar data de validade e valor maximo
		DbSelectArea( "AAH" )
		AAH->( DbSetOrder( 1 ) )
		If AAH->( DbSeek( xFilial( "AAH" ) + AA3->AA3_CONTRT ) )
			// Verifica todos os atendimentos realizados para este contrato e nome no valor
			nTotAtend += AT460TotAtend( AA3->AA3_CONTRT, AB7->( AB7_NUMOS + AB7_ITEM ) )

			// Verifica a validade do contato
			If dData >= AAH->AAH_INIVLD .AND. dData <= AAH->AAH_FIMVLD
				// Verifica o valor maximo do contrato
				If AAH->( FieldPos( "AAH_VALMAX" ) ) > 0
					If AAH->AAH_VALMAX > 0 .AND. nTotAtend > AAH->AAH_VALMAX
						EventInsert( FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID, FW_EV_LEVEL_INFO, " ", STR0061, STR0062 + " - " + STR0006 + ": " + cChave ) // "Valor Limite de Cotrato" ## "Ao incluir este atendimento, o valor limite de apontamentos deste contrato serА superado!"

						If lMV_TECBLAT .AND. lNt460Aut
							Help( " ", 1,"AT460VLMAX",, STR0062 + " - " + STR0006 + ": " + cChave, 3,1 )
							lRet := .F.
						EndIf
					EndIf
				EndIf
			EndIf

		EndIf
	EndIf

	AB7->( DbSkip() )
End


RestArea( aAreaAB7 )
RestArea( aAreaAAH )
RestArea( aAreaAA3 )
RestArea( aArea )

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} AT460TotAtend

Localiza o valor dos atendimentos com o mesmo contrato.

@param cContrt, caracter, numero do contrato
@param cOSItem, caracter, numero da Ordem de Servico e o Item da Ordem de Servico

@return numerico, Valor total apontado no contrato

@author (desconhecido)
@since 12/04/11
@version 1.0
/*/
//-------------------------------------------------------------------
Static Function AT460TotAtend( cContrt, cOSItem )
Local aArea		:= GetArea()
Local aAreaAA3	:= AA3->( GetArea() )
Local aAreaAAC	:= AAC->( GetArea() )
Local aAreaAB6	:= AB6->( GetArea() )
Local aAreaAB7	:= AB7->( GetArea() )
Local aAreaABA	:= ABA->( GetArea() )
Local aTam			:= {}
Local nRet			:= 0
Local cFilialAA3	:= xFilial("AA3")
Local cFilialAB7	:= xFilial("AB7")
Local cFilialABA	:= xFilial("ABA")
Local cFilialABC	:= xFilial("ABC")
Local cQuery		:= ""
Local cAliasTmp	:= ""
Local cCharConcat	:= ""

	If Upper( TcGetDb() ) $ "ORACLE*POSTGRES*DB2*INFORMIX"
		cCharConcat := "||"
	Else
		cCharConcat	:= "+"
	EndIf

	cQuery	:= " SELECT AB7_NUMOS, AB7_ITEM, "
	cQuery	+= " AB6_TABELA, AB6_CODCLI, AB6_LOJA, AB6_MOEDA, "
	cQuery	+= " ABA_CODPRO, ABA_QUANT "
	cQuery	+= " FROM "+RetSqlName("AA3") + " AA3 "
	cQuery	+= " INNER JOIN "+RetSqlName("AB7") + " AB7 ON "
	cQuery	+= " AB7.AB7_FILIAL = '"+cFilialAB7+"' "
	cQuery	+= " AND AB7.AB7_CODCLI = AA3.AA3_CODCLI "
	cQuery	+= " AND AB7.AB7_LOJA = AA3.AA3_LOJA "
	cQuery	+= " AND AB7.AB7_CODPRO = AA3.AA3_CODPRO "
	cQuery	+= " AND AB7.AB7_NUMSER = AA3.AA3_NUMSER "
	cQuery	+= " AND AB7.D_E_L_E_T_ = ' ' "
	cQuery	+= " INNER JOIN "+RetSqlName("ABA") + " ABA ON "
	cQuery	+= " ABA.ABA_FILIAL = '"+cFilialABA+"' "
	cQuery	+= " AND ABA.ABA_NUMOS = AB7.AB7_NUMOS"+cCharConcat+"AB7.AB7_ITEM "
	cQuery	+= " AND ABA.D_E_L_E_T_ = ' ' "
	cQuery	+= " LEFT JOIN "+RetSqlName("AB6") + " AB6 ON "
	cQuery	+= " AB6.AB6_FILIAL = '"+xFilial( "AB6" )+"' "
	cQuery	+= " AND AB6.AB6_NUMOS = AB7.AB7_NUMOS "
	cQuery	+= " AND AB6.D_E_L_E_T_ = ' ' "
	cQuery	+= " WHERE "
	cQuery	+= " AA3.AA3_FILIAL = '"+cFilialAA3+"' AND "
	cQuery	+= " AA3.AA3_CONTRT = '"+cContrt+"' AND "
	cQuery	+= " ( AB7.AB7_NUMOS <> '"+Left(cOSItem,Len(AB7->AB7_NUMOS))+ "' OR AB7.AB7_ITEM <> '"+RIGHT(cOSItem,Len(AB7->AB7_ITEM))+ "') "
	cQuery	+= " AND AA3.D_E_L_E_T_ = ' ' "

	cQuery := ChangeQuery(cQuery)
	cAliasTmp := GetNextAlias()
	dbUseArea(.T., "TOPCONN", TcGenQry(,,cQuery), cAliasTmp, .T., .T.)

	aTam := TamSX3("ABA_QUANT")
	TCSetField(cAliasTmp,"ABA_QUANT","N",aTam[1],aTam[2])

	While (cAliasTmp)->(!Eof())

		nRet += AT450Tab( (cAliasTmp)->ABA_CODPRO, (cAliasTmp)->AB6_TABELA, (cAliasTmp)->ABA_QUANT, (cAliasTmp)->AB6_CODCLI, (cAliasTmp)->AB6_LOJA, (cAliasTmp)->AB6_MOEDA ) * (cAliasTmp)->ABA_QUANT

		// Despesas
		ABC->( DbSetOrder( 1 ) )
		ABC->( DbSeek( cFilialABC + (cAliasTmp)->AB7_NUMOS ) )
		While ABC->( !Eof() ) .AND. ABC->( ABC_FILIAL + ABC_NUMOS ) == cFilialABC + (cAliasTmp)->( AB7_NUMOS + AB7_ITEM )
			If ABC->( ABC_NUMOS + ABC_SEQ ) <> M->( AB9_NUMOS + AB9_SEQ )
				nRet += ABC->ABC_VALOR
			EndIf
			ABC->( DbSkip() )
		End
		(cAliasTmp)->( DbSkip() )
	EndDo
	(cAliasTmp)->(dbCloseArea())

RestArea( aAreaAB6 )
RestArea( aAreaAB7 )
RestArea( aAreaABA )
RestArea( aAreaAAC )
RestArea( aAreaAA3 )
RestArea( aArea )

Return nRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460F6Ё       Autor Ё Bruno                        Ё Data Ё 11/07/13 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verifica o Saldo em estoque do produto selecionado.                  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё AT460F6( )   		                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460                                                              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460F6()

Local aArea     	:= GetArea()
Local cProduto		:= ""
Local cLocal		:= ""
Local nPosLocal	:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_LOCAL"})
Local nPProduto	:= aScan(aHeader,{|x| AllTrim(x[2])=="ABA_CODPRO"})
Local bSetKey		:= SetKey(VK_F6)

cProduto := aCols[n][nPProduto]
cLocal 	:= aCols[n][nPProduto]

If ("ABA_QUANT"$ReadVar())
	MaViewSB2(cProduto,,cLocal)
EndIf

SetKey(VK_F6,bSetKey)
RestArea(aArea)

Return .T.

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    ЁAt460F8Ё       Autor Ё Bruno                        Ё Data Ё 15/07/13 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Verifica o Saldo dos produtos alternativos.								Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё AT460F8( )   		                                                 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460                                                              Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460F8()

Local bSeek 		:= SetKey(VK_F8,Nil)

Local nPosProd		:= aScan(aHeader,{|x| AllTrim(x[2])=="ABA_CODPRO"})
Local nPosQtd		:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QUANT"})
Local nPosDesc		:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_DESCRI"})
Local nPosPrdOri	:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_PRDORI"})
Local nPosQtdOri	:= aScan(aHeader,{|x| Alltrim(x[2])=="ABA_QNTORI"})

Local aBrowse		:= {}
Local cProd		:= ""
Local nQtd			:= 0
Local cProdAlt		:= ""
Local nQtdAlt		:= 0
Local cDescAlt		:= ""
Local nY			:= 0
Local aColum		:= {}
Local lAtualiza	:= .F.
Local aCmpBr		:= {"GI_PRODALT","B1_DESC","B1_UM","ABA_QUANT","B2_QATU"}

Local oDlg			:= Nil
Local oLayer		:= Nil
Local oPanTop		:= Nil
Local oPanBrw		:= Nil
Local oPanRodp		:= Nil
Local oTButtonOk	:= Nil
Local oTButtonSair := Nil

cProd 	:= aCols[n][nPosProd]
nQtd	:= aCols[n][nPosQtd]

aBrowse   := At460BrwGI(cProd,nQtd)

For nY := 1 To Len(aCmpBr)
	aAdd( aColum , At460Field("{|| aBrowse[oBrowse:nAt,"+cValToChar(nY)+"] }",aBrowse,aCmpBr[nY]) )
Next nY

If Empty(aBrowse)

	Help(" ",1,"At460F8", ,STR0072,1)	//"Quantidade nЦo digitada ou NЦo existem produtos alternativos para o item selecionado!"

Else

	DEFINE DIALOG oDlg TITLE STR0073 FROM 180,180 TO 550,700 PIXEL STYLE WS_DLGFRAME 	 //"Saldo Alternativo"

		oLayer := FWLayer():new()

		oLayer:init(oDlg,.F.)

		oLayer:addLine("Lin01",25,.F.)
		oLayer:addCollumn('Col01',100,.F.,"Lin01")
		oLayer:addWindow('Col01','C1_Win01',STR0074,100,.F.,.F.,,"Lin01") //Produto Original

		oLayer:addLine("Lin02",65,.F.)
		oLayer:addCollumn('Col01',100,.F.,"Lin02")
		oLayer:addWindow('Col01','C1_Win02',STR0071,100,.F.,.T.,,"Lin02") //Alternativos

		oLayer:addLine("Lin03",10,.F.)
		oLayer:addCollumn('Col01',100,.F.,"Lin03")

		oPanTop	:= oLayer:getWinPanel('Col01','C1_Win01',"Lin01")
		oPanBrw	:= oLayer:getWinPanel('Col01','C1_Win02',"Lin02")
		oPanRodp	:= oLayer:getColPanel('Col01',"Lin03")

		@ 02,05 SAY Alltrim(cProd) + "-" + POSICIONE("SB1",1,xFilial("SB1")+cProd,"B1_DESC") SIZE 140, 7 OF oPanTop PIXEL
		@ 12,05 SAY "Qtd.:" SIZE 31,07 OF oPanTop PIXEL
		@ 12,30 SAY nQtd SIZE 140, 7 OF oPanTop PIXEL

		oBrowse := FwBrowse():New()

		bLine := {||{aBrowse[oBrowse:nAt,01],aBrowse[oBrowse:nAt,02],;
					   aBrowse[oBrowse:nAt,03],aBrowse[oBrowse:nAt,04],aBrowse[oBrowse:nAt,05]}}

		oBrowse:SetDataArray()			//Define que a utilizacao И por array
		oBrowse:AddStatusColumns({||ATStatus(aBrowse[oBrowse:nAt,06])},{||ATLegend()})	//Define os status
		oBrowse:SetColumns(aColum) 		//Define as colunas preestabelecidas
		oBrowse:SetArray(aBrowse)		//Define o array a ser utilizado
		oBrowse:SetOwner(oPanBrw)		//Define o objeto pai
		oBrowse:SetChange(bLine)			//Define o bloco de codigo a ser executado na troca de linha
		oBrowse:DisableReport()			//Desabilita botao de impressao
		oBrowse:DisableConfig()			//Desabilita botao de configuracao
		oBrowse:Activate()

		oTButtonOk	:= TButton():Create( oPanRodp,03,165,STR0068,{||cProdAlt:=aBrowse[oBrowse:nAt,01],;
									nQtdAlt:=aBrowse[oBrowse:nAt,04],cDescAlt:=aBrowse[oBrowse:nAt,02],;
									lAtualiza := .T.,oDlg:End(),},40,12,,,,.T.,,,,,,) //Confirmar
		oTButtonSair := TButton():Create( oPanRodp,03,215,STR0075,{||lAtualiza := .F.,oDlg:End(),},40,12,,,,.T.,,,,,,) //Voltar

	ACTIVATE DIALOG oDlg CENTERED

	If lAtualiza
		aCols[n][nPosProd]		:= cProdAlt
		aCols[n][nPosQtd]		:= nQtdAlt
		aCols[n][nPosDesc]		:= cDescAlt
		aCols[n][nPosPrdOri]	:= cProd
		aCols[n][nPosQtdOri]	:= nQtd
	EndIf

EndIf

SetKey(VK_F8,bSeek)

Return

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At460BrwGI  Ё Autor Ё Bruno                        Ё Data Ё 15/07/13 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Busca os produtos alternativos pertencentes ao produto selecionado.  Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpA1 := At460BrwGI( ExpC1, ExpN1 )		                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> Codigo do produto                                           Ё╠╠
╠╠Ё          Ё ExpN1 -> Quantidade do produto                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpA1 -> Array com os produtos alternativos                          Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460		                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460BrwGI(cProdOrig,nQtdOrig)

Local aRet			:= {}
Local lSaldo		:= .F.
Local nX			:= 0
Local aProdAlt		:= {}
Local nTotQtd		:= 0
Local cDesc		:= ""
Local cLocProc		:= SuperGetMv("Mv_LOCPROC",.F.,"99")

SGI->(DbSelectArea("SGI"))
SGI->(DbSetOrder(1)) //GI_FILIAL+GI_PRODORI
SGI->(DbSeek(xFilial("SGI")+cProdOrig))


While SGI->(!Eof()) .And. nQtdOrig > 0 .And. xFilial("SGI") == SGI->GI_FILIAL .AND. cProdOrig == SGI->GI_PRODORI

		If SGI->GI_TIPOCON == "M"
			nTotQtd := nQtdOrig * SGI->GI_FATOR
		Else
			nTotQtd := nQtdOrig / SGI->GI_FATOR
		EndIf

 		aAdd(aProdAlt,{SGI->GI_ORDEM,SGI->GI_PRODALT,nTotQtd})

SGI->(DbSkip())
End

For nX := 1 To Len(aProdAlt)

	DbSelectArea("SB1")
	DbSetorder(1)
	If DbSeek(xFilial("SB1")+aProdAlt[nX][2])
		dbSelectArea("SB2")
		dbSetOrder(1)
		SB2->(dbSeek(xFilial("SB2")+aProdAlt[nX][2]+If(SB1->B1_APROPRI=="I",cLocProc,RetFldProd(aProdAlt[nX][2],"B1_LOCPAD"))))
		If EOF()
			CriaSB2(aProdAlt[nX][2],If(SB1->B1_APROPRI=="I",cLocProc,RetFldProd(aProdAlt[nX][2],"B1_LOCPAD")))
		EndIf
		nSldDisp := SaldoSB2(.T., , ,.F.,.T.,,,)+SB2->B2_SALPEDI-SB2->B2_QEMPN+AvalQtdPre("SB2",2)
		If nSldDisp > 0
			lSaldo := .T.
		EndIf
		cUM := SB1->B1_UM
		cDesc := Alltrim(POSICIONE("SB1",1,xFilial("SB1")+aProdAlt[nX][2],"B1_DESC"))
	EndIf

	aAdd(aRet,{aProdAlt[nX][2],cDesc,cUM,aProdAlt[nX][3],nSldDisp,lSaldo})

Next

Return aRet

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё At460Field  Ё Autor Ё Bruno                        Ё Data Ё 16/07/13 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Constroi a estrutura do Browse dos produtos alternativos atraves do  Ё╠╠
╠╠Ё Metodo FWBRWColumn. 																		Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpA1 := At460Field( ExpC1, ExpA1 )		                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpC1 -> PosiГЦo de cada linha referente aos produtos alternativos   Ё╠╠
╠╠Ё          Ё ExpA1 -> Produtos alternativos                                       Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё Objeto da Estrutura das colunas				                        Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460		                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function At460Field(cDados,aBrowse,cField)

	Local oCol
	Local aRet := {}

	aRet := FwTamSx3(cField)
	oCol := FWBrwColumn():New()//Cria objeto
	oCol:SetTitle( AllTrim(FWX3Titulo(cField)) )//Define titulo
	oCol:SetData( &(cDados))//Define valor
	oCol:SetType( aRet[3] )//Define tipo
	oCol:SetPicture( AllTrim(X3Picture(cField)) ) //Define picture
	oCol:SetAlign( If(aRet[3] == "N",CONTROL_ALIGN_RIGHT,CONTROL_ALIGN_LEFT) )//Define alinhamento
	oCol:SetSize( aRet[1] + aRet[2] )//Define tamanho
	oCol:SetEdit( .F. )//Indica se И editavel

Return oCol

/*
эээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээээ
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбдддддддддддддбдддддддбддддддддддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFuncao    Ё ATStatus    Ё Autor Ё Bruno                        Ё Data Ё 16/07/13 Ё╠╠
╠╠цддддддддддедддддддддддддадддддддаддддддддддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescricao Ё Identifica o status dos itens dos Alternativos.  						Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ExpC1 := ATStatus( ExpL1 )					                            Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁParametrosЁ ExpL1 -> True se tem saldo                                           Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁRetorno   Ё ExpC1 -> Status do produto alternativo									Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁUso       Ё TECA460		                                                        Ё╠╠
╠╠юддддддддддадддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
*/
Static Function ATStatus(lSaldo)

Local cStatus := ""

If lSaldo
	cStatus :='BR_VERDE'
Else
	cStatus := 'BR_VERMELHO'
EndIf

Return cStatus

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё ATLegend     Ё Autor ЁBruno		                  Ё 16/07/13 Ё╠╠
╠╠цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Consulta a Legenda.													 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё ATLegend( )                                                   Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Static Function ATLegend()

Local oLegend  :=  FWLegend():New()

oLegend:Add('','BR_VERDE'	,STR0076 )	//'Com Saldo'
oLegend:Add('','BR_VERMELHO',STR0077 )	//'Sem Saldo'

oLegend:Activate()
oLegend:View()
oLegend:DeActivate()

Return

/*/
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
╠╠зддддддддддбддддддддддддддбдддддддбдддддддддддддддддддддддбддддддбдддддддддд©╠╠
╠╠ЁFun┤┘o    Ё AtRetCmp     Ё Autor ЁBruno		                  Ё 16/07/13 Ё╠╠
╠╠цддддддддддеддддддддддддддадддддддадддддддддддддддддддддддаддддддадддддддддд╢╠╠
╠╠ЁDescri┤┘o Ё Validacao do codigo do produto nos apontamentos deixando os	 Ё╠╠
╠╠Ё campos Quantidadade, Prd.Original e Qtd.Original em branco. (Gatilhos)	 Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠ЁSintaxe   Ё AtRetCmp( )                                                    Ё╠╠
╠╠цддддддддддедддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд╢╠╠
╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠╠
ъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъъ
/*/
Function AtRetCmp(cDomin)

Local xRet
Local cCampo   := ReadVar()

If ABA->(FieldPos(cDomin) > 0)
	If "M->ABA_CODPRO" == cCampo
		Do Case
		Case cDomin == "ABA_PRDORI"
			xRet := " "
		Case cDomin == "ABA_QNTORI"
			xRet := 0
		Case Alltrim(cDomin) == "ABA_QUANT"
			xRet := 0
		EndCase
	EndIf
EndIf

Return xRet

//------------------------------------------------------------------------------
/*/{Protheus.doc} At460Disp
	FunГЦo que verifica se o atendente esta disponМvel para alocaГЦo
	@param	cCodTec = cСdigo do tИcnico
	@return	Boolen (.T. se atendente disponМvel)
	@since	05/10/2022      
	@Jack Junior
/*/
//------------------------------------------------------------------------------

Static Function At460Disp(cCodTec) 
Local lRet	:= .T.

If Posicione("AA1",1,xFilial("AA1")+cCodTec,"AA1_ALOCA") == "2"
	lRet := .F.
EndIf

Return lRet
