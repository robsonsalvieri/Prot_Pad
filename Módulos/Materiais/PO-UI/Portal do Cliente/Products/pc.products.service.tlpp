#include "tlpp-core.th"

namespace pc.products

static __instance as object

//------------------------------------------------------------------------------
/*/{Protheus.doc} ProductsService
	Disponibiliza um serviço de Produtos
	@type		Class
	@sample		ProductsService
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Class ProductsService
	Public Method New() as Object
	Public Method getInstance() as Object
	Public Method getProducts() as Json
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} New
	Disponibiliza uma nova intancia do serviço Produtos.
	@type		method
	@sample		ProductsService:New()
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method New() as Object Class ProductsService
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetInstance
	Retorna uma instancia do serviço
	@type		method
	@sample		ProductsService:GetInstance()
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method getInstance() as Object Class ProductsService
	If (__instance == Nil)
		__instance := ProductsService():New()
	EndIf
Return __instance

//------------------------------------------------------------------------------
/*/{Protheus.doc} getProducts
	Efetua a consulta de Produtos
	@type		method
	@sample		oService:getProducts(1, 10, {}, 'code, description', '-code', Nil)
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por página
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param		cPriceListId, Character	, Código da Tabela de Preço
	@param		cCustomerId	, Character	, Código do Cliente
	@param		cStoreId	, Character	, Loja do Cliente
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		28/11/2023
	@version	12.1.2310
/*/
//------------------------------------------------------------------------------
Method getProducts(nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character,;
 cSort as Character, cPriceListId as Character, cCustomerId as Character, cStoreId as Character) as Json Class ProductsService
	Local oProducts := ProductsDataFactory():getData()
	Local jResponse as Json	

	oProducts:Get(nPage, nPageSize, aURLFilter, cFields, cSort, cPriceListId, cCustomerId, cStoreId )

	jResponse := oProducts:getJSONResponse()

	oProducts:DeActivate()
	FreeObj(oProducts)
Return jResponse
