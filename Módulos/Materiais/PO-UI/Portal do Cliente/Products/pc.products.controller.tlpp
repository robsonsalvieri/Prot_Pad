#include "tlpp-core.th"
#include "pc.products.controller.ch"

namespace pc.products
using namespace pc.util
//------------------------------------------------------------------------------
/*/{Protheus.doc} pcProducts
	API para consulta de produtos (SB1) utilizada no TOTVS Gestão de Vendas
	@type		class
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
/*/
//------------------------------------------------------------------------------
Class pcProducts
	Data jResponse  as Json
	Data nPage      as Numeric
	Data nPageSize  as Numeric
	Data oService   as Object

	Public Method new() as Object

	@Get("/api/pc/v1/products/")
	Public Method getProducts() as Logical

	@Get("/api/pc/v1/products/:priceListId/:customerId/:storeId")
	Public Method getProductsWithPriceList() as Logical

endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcProducts::new() as Object
	Obtém uma nova instancia da classe pcProducts
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@return		object, retorna uma nova instancia da classe.
/*/
//------------------------------------------------------------------------------
Method new() as Object Class pcProducts
	::jResponse := JsonObject():new()
	::nPage     := 1
	::nPageSize := 10
	::oService  := ProductsService():getInstance()
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcProducts::getProducts() as Logical
	Obtem uma Lista de Produtos, possibilitando filtrar (QueryParam - FILTER) 
	os produtos e permitindo retornar todos	os campos ou somente os campos 
	especificados no QueryParam FIELDS.
    Observação: Ao utilizar o getProducts o campo DELETED não será retornado. 
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		27/11/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getProducts() as Logical Class pcProducts
	Local aItems		                                                      as Array
	Local aURLFilter	  := getFilterParam()                                 as Array
	Local cFields		  := getQueryParam('FIELDS')                          as Character
	Local cSort			  := getQueryParam('SORT')                            as Character

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getProducts(::nPage, ::nPageSize, aURLFilter, cFields, cSort))
	aItems := ::jResponse:GetJsonObject( "items" )

	If Empty( aURLFilter ) .Or. ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
		aSize(aItems, 0)
	Else
		SetRestFault(404, STR0001 ) //"Produto nao localizado"
	Endif

	aSize(aURLFilter, 0)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcProducts::getProductsWithPriceList() as Logical
	Obtem uma Lista de Produtos já com o tratamento de preço, possibilitando 
	filtrar (QueryParam - FILTER) os produtos e permitindo retornar todos 
	os campos ou somente os campos especificados no QueryParam FIELDS.
    Observação: Ao utilizar o getProducts o campo DELETED não será retornado. 
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		28/11/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getProductsWithPriceList() as Logical Class pcProducts
	Local aItems		as Array
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:fromJson(::oService:getProducts(::nPage, ::nPageSize, aURLFilter, cFields, cSort,;
	GetPathParams("priceListId"), GetPathParams("customerId"), GetPathParams("storeId")))
	aItems := ::jResponse:GetJsonObject( "items" )

	If Empty( aURLFilter ) .Or. ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
		aSize(aItems, 0)
	Else
		SetRestFault(404, STR0001 ) //"Produto nao localizado"
	Endif

	aSize(aURLFilter, 0)
return .T.
