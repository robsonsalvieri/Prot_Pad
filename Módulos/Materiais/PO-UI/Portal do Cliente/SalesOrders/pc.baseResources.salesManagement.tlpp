#include "tlpp-core.th"
#include "tlpp-object.th"
#include 'fwlibversion.ch'

using namespace pc.util

#DEFINE INCLUI 3
#DEFINE PROPERTY 1 
#DEFINE FIELD 2

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement
	Disponibiliza uma classe base utilizada para auxiliar outras classes que 
	efetuem	as operações de Inclusão de APIs do Portal do Cliente.
	@type class
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@see pcSalesOrders
/*/
//------------------------------------------------------------------------------
Class pcbaseResourcesSalesManagement
	public data aHeader				as array
	public data aItems				as array
    public data cBranch				as character
	public data cExternalId			as character
    public data nOperation			as numeric
    public data oHashFieldsHeader	as object
    public data oHashFieldsItem		as object
    public data oStatement			as object

    protected data cError as character

    public method new() as object
	public method destroy()
	public method clean()
	public method getErrorMessage() as character
    public method setErrorMessage()
    public method setOperation() as logical

	protected method addMapFields()
	protected method getSalesId() as Character
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::new(cTable as Character, nOperation as Numeric) as Object
	Obtem uma nova instancia da clase pcbaseResourcesSalesManagement.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param cTable, character, Alias principal
	@param nOperation, numeric, Informe o número de operação (3 - Inclui)
	@return Object, pcbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
method new(cTable as Character, nOperation as Numeric) as Object Class pcbaseResourcesSalesManagement
	::cBranch := FwXFilial( cTable )
    ::nOperation := nOperation
return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::destroy
	Elimina os dados da memoria do objeto pcbaseResourcesSalesManagement
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
/*/
//------------------------------------------------------------------------------
method destroy() Class pcbaseResourcesSalesManagement
	::cError := ""
	
	If ::oHashFieldsHeader <> Nil 
		::oHashFieldsHeader:Clean()
		FreeObj(::oHashFieldsHeader)
	Endif

	If ::oHashFieldsItem <> Nil 
		::oHashFieldsItem:Clean()
		FreeObj(::oHashFieldsItem)
	Endif

	If ::oStatement <> Nil 
		::oStatement:Destroy()
		FreeObj(::oStatement)
	Endif
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::clean
	Efetua a Limpeza dos dados utilizados pelo ExecAuto da rotina.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
/*/
//------------------------------------------------------------------------------
method clean() Class pcbaseResourcesSalesManagement
	::aHeader := {}
	::aItems := {}
	::cError := ""
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::setErrorMessage
	Define uma mensagem de erro.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param cError, character, Mensagem de erro
	@param lIsForce, logical, Força a atualização do erro
/*/
//------------------------------------------------------------------------------
method setErrorMessage(cError as Character, lIsForce as Logical) Class pcbaseResourcesSalesManagement
	If Empty(::cError) .or. lIsForce
		::cError := cError
	Else
		::cError += cError
	Endif
return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::getErrorMessage() as Character
	Obtem a mensagem de erro definada no objeto.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return character, Mensagem de error
/*/
//------------------------------------------------------------------------------
method getErrorMessage() as Character Class pcbaseResourcesSalesManagement
return ::cError

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::setOperation(nOperation as Numeric) as Logical
	Define a operação que sera utilizada no commit dos dados.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@param nOperation, numeric, Informe o número de operação (3 - Inclui)
	@return logical, Caso o tipo informado estiver correto retorna verdadeiro.
/*/
//------------------------------------------------------------------------------
method setOperation(nOperation as Numeric) as Logical Class pcbaseResourcesSalesManagement
	Local lreturn := .T. as Logical
	::nOperation := nOperation
return lreturn

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::addMapFields
	Adiciona no objeto os HashMaps utilizado para convertar os dados do json.	
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
/*/
//------------------------------------------------------------------------------
method addMapFields() Class pcbaseResourcesSalesManagement
	If (::oHashFieldsHeader == Nil)
		::addMapFieldsHeader()
	Endif

	If (::oHashFieldsItem == Nil)
		::addMapFieldsItem()
	Endif

return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcbaseResourcesSalesManagement::getSalesId() as Character
	Obtem o código do Pedido, utilizando o Número Externo do Pedido
	como parametro.
	@type method
	@version 12.1.2310
	@author Lucas Panão / Squad CRM & Faturamento
	@since 28/11/2023
	@return character, Código do Pedido
	@obs O Número externo deve ser informado anteriormente no atributo cExternalId
	do objeto.
	@see protected
/*/
//------------------------------------------------------------------------------
method getSalesId() as Character Class pcbaseResourcesSalesManagement
	Local aArea		:= FwGetArea() as Array
	Local cAliasQry	:= GetNextAlias() as Character
	Local cSalesId	as Character

	::oStatement:SetString(2, ::cExternalId)
	::oStatement:OpenAlias(cAliasQry)
	
	While (cAliasQry)->(!(EoF()))
		cSalesId := (cAliasQry)->SALESID
		(cAliasQry)->(DbSkip())
	EndDo

	(cAliasQry)->(DbCloseArea())

	FwrestArea( aArea )
	aSize(aArea, 0)
return cSalesId
