#include 'tlpp-core.th'
#include 'pc.salesorders.controller.ch'

namespace pc.salesOrders
using namespace pc.util

Static __lHasGetHeaderRequest := FindFunction("pc.util.getHeaderRequest") as logical

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders
	API para integração dos Pedidos de vendas com o TOTVS Gestão de Vendas.
	@type		class
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
/*/
//------------------------------------------------------------------------------
Class pcSalesOrders
	Public Data nPage      as Numeric
	Public Data nPageSize  as Numeric
	Public Data jResponse  as Json
	Public Data oService   as Object

	Public Method new() as Object

	@Post("/api/pc/v1/salesorders/list")
	Public Method getHeaders() as Logical

	@Post("/api/pc/v1/salesorders/list/:orderId")
	Public Method getHeadersByOrderId() as Logical

	@Post("/api/pc/v1/salesorders/list/:orderId/items")
	public Method getItemsByOrderId() as Logical

	@Post("/api/pc/v1/salesorders/list/:orderId/items/:itemId")
	public Method getItemsByOrderItemId() as Logical

	@Get("/api/pc/v1/salesorders/total/:orderId")
	public Method getTotalSalesOrder() as Logical

	@Post("/api/pc/v1/salesorders/")
	Public Method postSalesOrder() as Logical

EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::new() as Object
	Cria uma nova instacia do objeto pcSalesOrders.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		object , nova instacia do objeto pcSalesOrders
/*/
//------------------------------------------------------------------------------
Method new() as Object Class pcSalesOrders
	::nPage         := 1
	::nPageSize     := 10
	::jResponse     := JsonObject():new()
	::oService      := SalesOrdersService():GetInstance()
Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::getHeaders() as Logical
	Obtem todos os Pedidos de Vendas, respeitando os paramentros informados na
	Requisição.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getHeaders() as Logical Class pcSalesOrders
	Local aItems		:= {} as Array
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character
	Local jBody         := GetRequestBody() as Json
	Local cCustomer 	:= jBody:toJSON() as Character
	Local cUser         := "" as Character

	cUser := Iif(__lHasGetHeaderRequest, getHeaderRequest("x-pc-user"), "")

	oRest:setKeyHeaderResponse('Content-Type','application/json')

	getPageAndPageSize(@::nPage, @::nPageSize)

	::jResponse:fromJson(::oService:getHeaders(::nPage, ::nPageSize, /*cOrderId*/,aURLFilter, cFields, cSort, cCustomer, cUser))
	aItems := ::jResponse:GetJsonObject( "items" )

	If Empty( aURLFilter ) .Or. ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0002) ) //"Pedido de Venda não localizado"
	EndIf

	FwFreeObj(jBody)
	aSize(aItems, 0)
	aSize(aURLFilter, 0)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::getHeadersByOrderId() as Logical
	Obtem um Pedido de Venda Especifico
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getHeadersByOrderId() as Logical Class pcSalesOrders
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character
	Local jBody         := GetRequestBody() as Json
	Local cCustomer 	:= jBody:toJSON() as Character
	Local cUser         := "" as Character

	cUser := Iif(__lHasGetHeaderRequest, getHeaderRequest("x-pc-user"), "")

	oRest:setKeyHeaderResponse('Content-Type','application/json')

	::jResponse:fromJson(::oService:getHeaders(1, 1, GetPathParams("orderId"), aURLFilter, cFields, cSort, cCustomer, cUser))

	If Empty(::jResponse["orderid"])
		SetRestFault(404, STR0002 ) //"Pedido de Venda não localizado"
	Else
		oRest:setResponse(::jResponse)
	Endif

	FwFreeObj(jBody)
	aSize(aURLFilter, 0)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::getItemsByOrderId() as Logical
	Retorna os items do Pedido de Venda especificado na URL.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getItemsByOrderId() as Logical Class pcSalesOrders
	Local aItems		:= {} as Array
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character
	Local jBody         := GetRequestBody() as Json
	Local cCustomer 	:= jBody:toJSON() as Character
	Local cUser         := "" as Character

	cUser := Iif(__lHasGetHeaderRequest, getHeaderRequest("x-pc-user"), "")

	oRest:setKeyHeaderResponse('Content-Type','application/json')

	getPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:FromJson(::oService:GetItems(::nPage, ::nPageSize, GetPathParams("orderId"), /*cItemId*/, aURLFilter, cFields, cSort, cCustomer, cUser))
	aItems := ::jResponse:GetJsonObject( "items" )

	If Empty( aURLFilter ) .Or. ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0003) ) //"Item do Pedido de Venda não localizado"
	EndIf

	FwFreeObj(jBody)
	aSize(aItems, 0)
	aSize(aURLFilter, 0)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::getItemsByOrderItemId() as Logical
	Retorna os items do Pedido de Venda especificado na URL.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getItemsByOrderItemId() as Logical Class pcSalesOrders
	Local aURLFilter	:= getFilterParam() as Array
	Local cFields		:= getQueryParam('FIELDS') as Character
	Local cSort			:= getQueryParam('SORT') as Character
	Local jBody         := GetRequestBody() as Json
	Local cCustomer 	:= jBody:toJSON() as Character
	Local cUser         := "" as Character

	cUser := Iif(__lHasGetHeaderRequest, getHeaderRequest("x-pc-user"), "")
	
	oRest:setKeyHeaderResponse('Content-Type','application/json')

	::jResponse:FromJson(::oService:GetItems(1, 1, GetPathParams("orderId"), GetPathParams("itemId"), aURLFilter, cFields, cSort, cCustomer, cUser))

	If Empty(::jResponse["orderid"])
		SetRestFault(404, STR0003 ) //"Item do Pedido de Venda não localizado"
	Else
		oRest:setResponse(::jResponse)
	Endif

	FwFreeObj(jBody)
	aSize(aURLFilter, 0)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::getTotalSalesOrder() as Logical
	Retorna o total do Pedido de Venda especificado na URI.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		17/10/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method getTotalSalesOrder() as Logical Class pcSalesOrders
	Local cOrderId := GetPathParams("orderId")
	::jResponse:FromJson(::oService:getTotalSalesOrder( cOrderId ))
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pcSalesOrders::postSalesOrder() as Logical
	Efetua a inclusão de um Pedido de venda.
	@type		method
	@version	12.1.2310
	@author		Lucas Panão / Squad CRM & Faturamento
	@since		14/11/2023
	@return		logical, Verdadeiro
/*/
//------------------------------------------------------------------------------
Method postSalesOrder() as Logical Class pcSalesOrders
	Local jBody := GetRequestBody() as Json

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If jBody <> Nil
		::oService:saveSalesOrders(jBody)
		FreeObj(jBody)
	Endif
Return .T.
