#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'tlpp-rest.th'
#include 'FWMVCDEF.CH'
//#include 'pgv.pdfcustombase.ch'

#DEFINE FIELD 1
#DEFINE VALUE 2
#DEFINE FILTER_ISDELETED "AUTDELETA"
#DEFINE FIELDS_SECTION "FIELDSOFSECTION"

namespace totvs.protheus.backoffice.pgvPDFCustomBase
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase
    Classe responsável por ler o JSON oriundo da sua respectiva API REST e 
    realizar a Inclusão, Alteração ou Exclusão de uma Configuração de PDF
    Customizado no Portal de Vendas.
    @type class
	@version 12.1.2410
	@author Squad CRM & Faturamento - Inovação
	@since 21/11/2024
    @see tgvbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------

class pgvpdfcustombase from tgvbaseResourcesSalesManagement
    public data cPDFCustomId	    as character
 
    public method new()                   as object      
    public method fromExecAuto()          as logical    
    public method commitData()            as logical 

    private method setHeader()
    private method setItemsSection()
    private method setItemsField()
	private method addLineMVC()
	private method deleteLineMVC()
    private method setItemSection()       as array
    private method setItemField()         as array
	private method setValuesHeaderMVC()   as logical
	private method checkAddLineMVC()      as logical
	private method setValuesItemMVC()     as logical

    protected method addMapFieldsHeader()
    protected method addMapFieldsItem()
    protected method addMapFieldsItem2()
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:New() as object
	Cria uma nova instancia da classe pgvpdfcustombase
	@type method
	@version 12.1.2410
	@author Squad CRM & Faturamento - Inovação
	@since 21/11/2024
	@return object, objeto da classe pgvpdfcustomBase utilizando a herança da
                    classe tgvbaseResourcesSalesManagement
/*/
//------------------------------------------------------------------------------
method New() as Object class pgvpdfcustombase
    _Super:New("AQ6", MODEL_OPERATION_INSERT)
return self
//----------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:fromExecAuto(jPDFCustomConfig as json) as logical
	Cria a estrutura utilizada na gravação do filtro
	@type method
	@version 12.1.2410
	@author Squad CRM & Faturamento - Inovação
	@since 21/11/2024
    @param jPDFCustomConfig, Json, Objeto JSON com os dados da Configuração de PDF
	@return logical, Indica se a preparação do execauto foi realizada com sucesso.
/*/
//----------------------------------------------------------------------------------
method fromExecAuto(jPDFCustomConfig as json) as Logical class pgvpdfcustombase
    Local aArea := FwGetArea() as Array
    Local lContinue := .F. as logical
    ::clean()

    If (::nOperation == MODEL_OPERATION_DELETE)
		If !Empty(::cPDFCustomId)
			lContinue := .T.
			aAdd(::aHeader, {"AQ6_ID", ::cPDFCustomId, Nil})
		EndIf
	Else
		If !(jPDFCustomConfig == Nil)
			lContinue := .T.
			::addMapFields()
			::setHeader(jPDFCustomConfig)
			::setItemsSection(jPDFCustomConfig["sections"])
		EndIf
	Endif

    FwrestArea( aArea )
	FWFreeObj(aArea)
return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase::addMapFieldsHeader
	Adiciona no Hashmap todos os campos utilizados pelo TOTVS Gestão
	de Vendas no Cabeçalho (AQ6) do Cadastro Configurações de PDF.
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 22/11/2024
/*/
//------------------------------------------------------------------------------

method addMapFieldsHeader() class pgvpdfcustombase
    ::oHashFieldsHeader := tHashMap():New()

    ::oHashFieldsHeader:set("id"           ,"AQ6_ID")       // ID
    ::oHashFieldsHeader:set("title"        ,"AQ6_TITULO")   // Título
    ::oHashFieldsHeader:set("description"  ,"AQ6_DESCRI")   // Descrição
    ::oHashFieldsHeader:set("type"         ,"AQ6_TIPO")     // Tipo
    ::oHashFieldsHeader:set("url_image"    ,"AQ6_URLIMG")   // URL da Imagem
    return nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase::addMapFieldsItem
	Adiciona no Hashmap de Items todos os campos utilizados pelo TOTVS Gestão de
	Vendas nas Seções de uma configuração de PDF (AQ7).
	@type method
	@version 12.1.2410
	@author Squad CRM & Faturamento - Inovação
	@since 22/11/2024
/*/
//------------------------------------------------------------------------------
method addMapFieldsItem() class pgvpdfcustombase
    ::oHashFieldsItem := tHashMap():New()
    ::oHashFieldsItem:set("id"           ,"AQ7_ID")       // ID da Seção
    ::oHashFieldsItem:set("title"        ,"AQ7_TITULO")   // Título
    ::oHashFieldsItem:set("description"  ,"AQ7_DESCRI")   // Descrição
    ::oHashFieldsItem:set("type"         ,"AQ7_TIPO")     // Tipo
    ::oHashFieldsItem:set("order"        ,"AQ7_ORDEM")    // Ordem
	::oHashFieldsItem:set("isDeleted"    ,"isDeleted")    // Deletado?
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:addMapFieldsItem2
	Adiciona no Hashmap de Items todos os campos utilizados pelo TOTVS Gestão de
	Vendas nos Campos das Seções de uma configuração de PDF (AQ8).
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 22/11/2024
/*/
//------------------------------------------------------------------------------
    method addMapFieldsItem2() class pgvpdfcustombase
    ::oHashFieldsItem2 := tHashMap():New()
    ::oHashFieldsItem2:set("id"                 ,"AQ8_ID")       // ID do Campo
    ::oHashFieldsItem2:set("property"           ,"AQ8_PROPRI")   // Propriedade
    ::oHashFieldsItem2:set("type"               ,"AQ8_TIPO")     // Tipo
    ::oHashFieldsItem2:set("title"              ,"AQ8_TITULO")   // Título
    ::oHashFieldsItem2:set("format"             ,"AQ8_FORMAT")   // Formato
    ::oHashFieldsItem2:set("decimal"            ,"AQ8_DECIMA")   // Decimal
    ::oHashFieldsItem2:set("order"              ,"AQ8_ORDEM")    // Ordem
    ::oHashFieldsItem2:set("thousandsSeparator" ,"AQ8_SEPMIL")   // Separador de Milhar
	::oHashFieldsItem2:set("isDeleted"          ,"isDeleted")    // Deletado?
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:setHeader
	Efetua a carga do array da propriedade aHeader para popular a tabela AQ6
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 22/11/2024
	@param jpdfcustom, Json, JSON contendo os dados do PDF Customizado
/*/
//------------------------------------------------------------------------------
method setHeader(jpdfcustom as json) class pgvpdfcustombase
    Local aFieldsHeader         as array
    Local cField                as character
    Local nField                as numeric
    Local xValue                as variant

    aFieldsHeader := jpdfcustom:getNames()

    For nField := 1 To Len(aFieldsHeader)
        If ::oHashFieldsHeader:Get(aFieldsHeader[nField], @cField)
            xValue := ConvertValue(jpdfcustom:GetJsonObject(aFieldsHeader[nField]), GetSx3Cache(cField, "X3_TIPO"))
            
            If cField == "AQ6_ID"
				If Empty(xValue)
					::SetOperation(MODEL_OPERATION_INSERT)
					Loop
				Else
					::cPDFCustomId := PadR(xValue, GetSx3Cache(cField, "X3_TAMANHO"))
					xValue := PadR(xValue, GetSx3Cache(cField, "X3_TAMANHO"))
				EndIf
			EndIf

			aAdd(::aHeader, {cField, xValue, Nil})
        EndIf
    Next

    // Adicionar a Filial da tabela de acordo com a Filial logada no momento
    aAdd(::aHeader, {"AQ6_FILIAL", FWXFilial("AQ6"), Nil})

    If Empty(::cPDFCustomId)
		::SetOperation(MODEL_OPERATION_INSERT)
	Endif

    ::aHeader := FwVetByDic(::aHeader, "AQ6", .F.)
    
    FwFreeObj(aFieldsHeader)
return nil

//--------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:setItemsSection
	Efetua a conversão dos campos informados no json do Cadastro de Configuração em uma
	estrutura de Items da Seção da Configuração de PDF Customizado utilizado no MVC da rotina PGVA006.
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param aSections, Array, Array de JSON contendo as Seções do Cadastro de Configurações de PDF
/*/
//--------------------------------------------------------------------------------------------------------
method setItemsSection(aSections as Array) Class pgvpdfcustombase
	Local aItems		                  as Array
	Local aItem		                      as Array
	Local aItemsClone                     as Array
	Local cAutDeleta	                  as Character
	Local nAutdeleta	                  as Numeric
	Local nItem			                  as Integer
	Local cBranchAQ7  := FwXFilial("AQ7") as Character
	Local nAFields                          as Numeric
	Local aFieldsOfSection                  as Array
	
	aItems := aSections

	For nItem := 1 To Len(aItems)
		aItem := ::setItemSection(aItems[nItem], cBranchAQ7)
		aAdd(::aItems, aClone(aItem))
		FwFreeObj(aItem)
	Next

	// Ordenação do array conforme dicionário
	aItemsClone := aClone(::aItems)
	::aItems := FwVetByDic(::aItems, "AQ7", .T.)

	// Voltar a informação sobre a deleção do item, pois a função FWVetByDic retira do array
	For nItem := 1 to Len(::aItems)
		If ::nOperation == MODEL_OPERATION_UPDATE
			nAutdeleta := aScan(aItemsClone[nItem], {|x| x[1] == "AUTDELETA"})
			If nAutdeleta > 0
				cAutDeleta := aItemsClone[nItem, nAutdeleta, 2]
				aAdd(::aItems[nItem], {"AUTDELETA"	, cAutDeleta, NIL})
			EndIf	
		EndIf
		
		nAFields := aScan(aItemsClone[nItem], {|x| x[1] == FIELDS_SECTION})
		If nAFields > 0
			aFieldsOfSection := aClone(aItemsClone[nItem, nAFields, 2])
			aAdd(::aItems[nItem], {FIELDS_SECTION	, aFieldsOfSection, NIL})
		EndIf
	Next
	
	FwFreeObj(aItemsClone)
	FwFreeObj(aItems)
return Nil

//----------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:setItemSection
	Efetua a conversão dos campos informados no json de Seções do Cadastro de Configurações de PDF em
	um array de um item de Seção das Configurações do PDF.
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@jSection, Json, JSON com os dados da Seção das Configurações do PDF Customizado
	@cBranchAQ7, Character, Filial da tabela AQ7
	@return array, Item da Seção do Cadastro de Configurações do PDF para gravação por meio do MVC
/*/
//----------------------------------------------------------------------------------------------------------
method setItemSection(jSection as Json, cBranchAQ7 as Character) as Array Class pgvpdfcustombase
	Local aFieldsItem 	                          as Array
	Local cSectionId    	                      as Character
	Local cField		                          as Character
	Local lNewItem		                          as Logical
	Local nField		                          as Integer
	Local xValue		                          as variant	
	Local nTamCodigo                              as Numeric
	Local aArea			    := FwGetArea()        as Array
	Local aAreaAQ7		    := AQ7->(FwGetArea()) as Array
	Local lAutdeleta	    := .F.                as Logical
	Local aItem			    := {}                 as Array

	aFieldsItem := jSection:GetNames()
	nTamCodigo := GetSX3Cache('AQ7_ID', 'X3_TAMANHO')

	For nField := 1 To Len(aFieldsItem)
		If ::oHashFieldsItem:Get( aFieldsItem[nField] , @cField )
			If cField == "AQ7_ID"
				lNewItem := .T.
				cSectionId := PadR(jSection:GetJsonObject(aFieldsItem[nField]), nTamCodigo)
				If ::nOperation == MODEL_OPERATION_UPDATE
					DbSelectArea("AQ7")
					AQ7->( DbSetOrder(1) ) // AQ7_FILIAL+AQ7_IDCFG+AQ7_ID                                                                                                                                     
					If AQ7->(DbSeek(cBranchAQ7 + ::cPDFCustomId + cSectionId ))
						lNewItem := .F.
					Endif
				Endif

				aAdd(aItem, {"AQ7_ID", cSectionId, Nil})
                aAdd(aItem, {"AQ7_IDCFG", ::cPDFCustomId, Nil})
			ElseIf cField == "isDeleted" .And. ::nOperation == MODEL_OPERATION_UPDATE
				lAutdeleta := jSection:GetJsonObject(aFieldsItem[nField])
				lNewItem := .F.
			Else
				xValue := ConvertValue(jSection:GetJsonObject(aFieldsItem[nField]), GetSx3Cache(cField, "X3_TIPO"))
				aAdd(aItem, {cField, xValue, Nil})
			Endif
		Endif
	Next

	If ::nOperation == MODEL_OPERATION_UPDATE .And. !lNewItem
		If lAutdeleta
			aAdd(aItem, {"AUTDELETA", "S", Nil})
		Else
			aAdd(aItem, {"AUTDELETA", "N", Nil})
		EndIf
	EndIf

    If aScan(aFieldsItem, {|cField| cField == "fields"}) > 0
		aAdd(aItem, {FIELDS_SECTION, {}, Nil})
        ::setItemsField(jSection["fields"], cSectionId, aTail(aItem)[VALUE])
    EndIf

	FwRestArea(aAreaAQ7)
	FwRestArea(aArea)

	FWFreeObj(aFieldsItem)
	FWFreeObj(aArea)
	FWFreeObj(aAreaAQ7)
return aItem

//----------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:setItemsField
	Efetua a conversão dos campos informados no json do Cadastro de Configuração em uma
	estrutura de Items de Canois de uma Seção utilizado no MVC da rotina PGVA006.
	@type method
	@version 12.1.2510
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param aFields   , Array    , Array de JSON contendo os campos de uma Seção do Cadastro de 
                                  Configurações de PDF
	@param cSectionId, Character, Id da seção dos campos informados nos parâmetros
	@param aItems2   , Array    , Array para ser populado com os campos referentes a seção informada
/*/
//----------------------------------------------------------------------------------------------------
method setItemsField(aFields as Array, cSectionId as Character, aItems2 as Array ) Class pgvpdfcustombase
	Local aItems		                    as Array
	Local aItem			                    as Array
	Local aItemsClone                       as Array
	Local cAutDeleta	                    as Character
	Local nAutdeleta	                    as Numeric
	Local nItem			                    as Integer
	Local cBranchAQ8	:= FwXFilial("AQ8") as Character
	
	aItems := aFields

	For nItem := 1 To Len(aItems)
		aItem := ::setItemField(aItems[nItem], cSectionId, cBranchAQ8)
		aAdd(aItems2, aClone(aItem))
		FwFreeObj(aItem)
	Next

	// Ordenação do array conforme dicionário
	aItemsClone := aClone(aItems2)
	aItems2 := FwVetByDic(aItems2, "AQ8", .T.)

	// Voltar a informação sobre a deleção do item, pois a função FWVetByDic retira do array
	If ::nOperation == MODEL_OPERATION_UPDATE
		For nItem := 1 to Len(aItems2)
			nAutdeleta := aScan(aItemsClone[nItem], {|x| x[1] == "AUTDELETA"})
			If nAutdeleta > 0
				cAutDeleta := aItemsClone[nItem, nAutdeleta, 2]
				aAdd(aItems2[nItem], {"AUTDELETA"	, cAutDeleta, NIL})
			EndIf
		Next
	EndIf

	FwFreeObj(aItemsClone)
	FwFreeObj(aItems)
return Nil

//----------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:setItemField
	Efetua a conversão dos campos informados no json de Campo de uma Seção do Cadastro de Configurações de 
    PDF em um array de um item de Campo de uma Seção das Configurações do PDF.
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@jField    , Json     , JSON com os dados de um campo de uma Seção das Configurações do PDF Customizado
    @cSectionId, Character, Id da Seção a que o Campo pertence
	@cBranchAQ8, Character, Filial da tabela AQ8
	@return array, Item de Campo de uma Seção do Cadastro de Configurações do PDF
/*/
//----------------------------------------------------------------------------------------------------------------------------
method setItemField(jField as Json, cSectionId as Character, cBranchAQ8 as Character) as Array Class pgvpdfcustombase
	Local aFieldsItem 	                          as Array
	Local cFieldId      	                      as Character
	Local cField		                          as Character
	Local lNewItem		                          as Logical
	Local nField		                          as Integer
	Local xValue		                          as variant	
	Local nTamCodigo                              as Numeric
	Local aArea			    := FwGetArea()        as Array
	Local aAreaAQ8		    := AQ8->(FwGetArea()) as Array
	Local aItem			    := {}                 as Array
	Local lAutdeleta	    := .F.                as Logical

	aFieldsItem := jField:GetNames()
	nTamCodigo := GetSX3Cache('AQ8_ID', 'X3_TAMANHO')

	For nField := 1 To Len(aFieldsItem)
		If ::oHashFieldsItem2:Get( aFieldsItem[nField] , @cField )
			If cField == "AQ8_ID"
				lNewItem := .T.
				cFieldId := PadR(jField:GetJsonObject(aFieldsItem[nField]), nTamCodigo)
				If ::nOperation == MODEL_OPERATION_UPDATE
					DbSelectArea("AQ8")
					AQ8->( DbSetOrder(1) ) // AQ8_FILIAL+AQ8_IDCFG+AQ8_IDSECT+AQ8_ID                                                                                                                                 
					If AQ8->(DbSeek(cBranchAQ8 + ::cPDFCustomId + cSectionId + cFieldId ))
						lNewItem := .F.
					Endif
				Endif

                aAdd(aItem, {"AQ8_ID", cFieldId, Nil})
                aAdd(aItem, {"AQ8_IDCFG", ::cPDFCustomId, Nil})
				aAdd(aItem, {"AQ8_IDSECT", cSectionId, Nil})
			ElseIf cField == "isDeleted" .And. ::nOperation == MODEL_OPERATION_UPDATE
				lAutdeleta := jField:GetJsonObject(aFieldsItem[nField])
				lNewItem := .F.
			Else
				xValue := ConvertValue(jField:GetJsonObject(aFieldsItem[nField]), GetSx3Cache(cField, "X3_TIPO"))
				aAdd(aItem, {cField, xValue, Nil})
			Endif
		Endif
	Next

	If ::nOperation == MODEL_OPERATION_UPDATE .And. !lNewItem
		If lAutdeleta
			aAdd(aItem, {"AUTDELETA", "S", Nil})
		Else
			aAdd(aItem, {"AUTDELETA", "N", Nil})
		EndIf
	EndIf

	FwRestArea(aAreaAQ8)
	FwRestArea(aArea)

	FWFreeObj(aFieldsItem)
	FWFreeObj(aArea)
	FWFreeObj(aAreaAQ8)
return aItem


//-------------------------------------------------------------------------------------
/*/{Protheus.doc} pgvpdfcustombase:commitData
	Efetua a gravação da Configuração de PDF Customizado nas tabelas AQ6, AQ7 e AQ8
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@return logical, se gravou a Configuração Customizada de PDF retorna verdadeiro
/*/
//-------------------------------------------------------------------------------------
method commitData() as logical class pgvpdfcustombase
    local aMsgErro     := {} as array
    local cError       := '' as character
	local cIDAQ7       := '' as character
	local cIDAQ8       := '' as character
	local lContinue    := .f. as logical
    local nItem        as integer
	local nItem2       as integer
	Local oModel	   := FWLoadModel('PGVA006') as object
	Local oModelAQ6    as object
	Local oModelAQ7    as object
	Local oModelAQ8    as object
	Local isDeleted    := .f. as logical
	Local isDeletedAQ8 := .f. as logical
	Local isAddLine    := .f. as logical
	Local isAddLineAQ8 := .f. as logical
	Local lSeekLine    := .f. as logical

	oModel:SetOperation( ::nOperation )
	If oModel:Activate()
		If oModel:IsActive()
            lContinue := .t.
			oModelAQ6:= oModel:GetModel('AQ6MASTER')
			oModelAQ7:= oModel:GetModel('AQ7DETAIL')
			oModelAQ8:= oModel:GetModel('AQ8DETAIL')

			If oModel:GetOperation() <> MODEL_OPERATION_DELETE
				lContinue := ::setValuesHeaderMVC(@oModelAQ6)

                If lContinue
                    For nItem := 1 To Len(::aItems)
						isAddLine := .f.
						isDeleted := getValueByProperty(::aItems[nItem], FILTER_ISDELETED, 'N') == 'S'
						cIDAQ7 := getValueByProperty(::aItems[nItem], 'AQ7_ID', '')
						lSeekLine := oModelAQ7:SeekLine({{"AQ7_ID", cIDAQ7}})
						isAddLine := ::checkAddLineMVC(lSeekLine, isDeleted, oModel:GetOperation())

						If !isDeleted
							::addLineMVC(isAddLine, @oModelAQ7)
							lContinue := ::setValuesItemMVC(@oModelAQ7, ::aItems[nItem])

							// Atualizacao da AQ8
							If lContinue
								::aItems2 := ::aItems[nItem][Len(::aItems[nItem])][VALUE]
								For nItem2 := 1 To Len(::aItems2)
									isAddLineAQ8  := .f.
									isDeletedAQ8  := getValueByProperty(::aItems2[nItem2], FILTER_ISDELETED, 'N') == 'S'
									cIDAQ8 := IIf(!Empty(getValueByProperty(::aItems2[nItem2], "AQ8_ID", "")), getValueByProperty(::aItems2[nItem2], "AQ8_ID", ""), "")
									
									IIf(!Empty(cIDAQ8), lSeekLine := oModelAQ8:SeekLine({{"AQ8_ID", cIDAQ8}}), lSeekLine := .F.)

									If !isDeletedAQ8
										isAddLineAQ8 := ::checkAddLineMVC(lSeekLine, isDeletedAQ8, oModel:GetOperation())
										::addLineMVC(isAddLineAQ8, @oModelAQ8)
										lContinue := ::setValuesItemMVC(@oModelAQ8, ::aItems2[nItem2])
									Else
										::deleteLineMVC(lSeekLine, @oModelAQ8)
									Endif

									If !lContinue
										Exit
									Endif
								Next
							Endif
							// Atualizacao da AQ8
						Else
							::deleteLineMVC(lSeekLine, @oModelAQ7)
						Endif

                        If !lContinue
                            Exit
                        Endif
                    Next
			    Endif
			EndIF

			If !lContinue .Or. !( oModel:VldData() .And. oModel:CommitData() )
                lContinue := .F.
                cError := ""
                aMsgErro := oModel:GetErrorMessage()

                For nItem := 1 To Len(aMsgErro)
                    If ValType(aMsgErro[nItem]) == "C" .and. !Empty(aMsgErro[nItem])
                        cError := convertStringToLine(StrTran( StrTran( aMsgErro[nItem], "<", "" ), "-", "" ) + (" "), .f.)
                		::setErrorMessage(cError, .f.)
                    EndIf
                Next

                FWFreeObj(aMsgErro)
			Else
				If oModel:GetOperation() <> MODEL_OPERATION_DELETE
					::cPDFCustomId := AQ6->AQ6_ID
				Endif
            Endif
        Endif

		oModel:Deactivate()
		oModel:Destroy()

		FwFreeObj(oModelAQ6)
		FwFreeObj(oModelAQ7)
		FwFreeObj(oModelAQ8)
    Endif
	FwFreeObj(oModel)
return lContinue

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} setValuesHeaderMVC
	Efetua o preenchimento do modelo da tabela AQ6 - Cabeçalho
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param oModelAQ6, object, objeto do modelo MVC da tabela AQ6
	@return logical, se populou o modelo com sucesso ou nao
/*/
//-------------------------------------------------------------------------------------
method setValuesHeaderMVC(oModelAQ6 as object) as logical class pgvpdfcustombase
	local lContinue    := .t. as logical
	local nField       as integer

	For nField := 1 To Len(::aHeader)
		If !oModelAQ6:SetValue(::aHeader[nField, FIELD], ::aHeader[nField, VALUE])
			lContinue := .F.
			Exit
		EndIf
	Next
return lContinue

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} setValuesItemMVC
	Efetua o preenchimento do modelo da tabela AQ7 e AQ8 - Items
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param oModel , object, objeto do modelo MVC das tabelas AQ6 e AQ7
	@param aItem  , array , item da tabela AQ6 e AQ7 a ser populado no model
	@return logical, se populou o modelo com sucesso ou nao
/*/
//-------------------------------------------------------------------------------------
method setValuesItemMVC(oModel as object, aItem as array) as logical class pgvpdfcustombase
	local lContinue    := .t. as logical
	local nField       as integer

	For nField := 1 To Len(aItem)
		If (aItem[nField, FIELD] <> FILTER_ISDELETED .AND. aItem[nField, FIELD] <> FIELDS_SECTION) .AND. !oModel:SetValue(aItem[nField, FIELD], aItem[nField, VALUE])
			lContinue := .F.
			Exit
		EndIf
	Next
return lContinue

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} checkAddLineMVC
	Checa se é necessário adicionar uma nova linha no modelo MVC ou não
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param lSeekLine , logical , indica se a linha foi encontrada na base de dados
	@param isDeleted , logical , indica se a linha deve ser deletada ou não
	@param nOperation, numeric , operacao do modelo MVC  
	@return logical, se populou o modelo com sucesso ou nao
/*/
//-------------------------------------------------------------------------------------
method checkAddLineMVC(lSeekLine as logical, isDeleted as logical, nOperation as numeric) as logical class pgvpdfcustombase
	Local isAddLine    := .f. as logical

	If !lSeekLine 
		If !isDeleted .or. nOperation == MODEL_OPERATION_INSERT
			isAddLine := .t.
		Endif
	Endif
return isAddLine

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} addLineMVC
	Adicionar uma nova linha no modelo MVC quando confirmado no parametro isAddLine
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param isAddLine, logical, indica se a linha deve ser adicionada ou nao
	@param oModel   , object , modelo MVC que deve ter a linha adicionada
/*/
//-------------------------------------------------------------------------------------
method addLineMVC(isAddLine as logical, oModel as object) class pgvpdfcustombase
	If isAddLine
		oModel:AddLine()
	Endif
return nil

//-----------------------------------------------------------------------------------------
/*/{Protheus.doc} deleteLineMVC
	Exclui uma linha no modelo MVC quando confirmado no parametro lSeekLine
	@type method
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param lSeekLine, logical, indica se a linha foi localizada para ser excluida ou nao
	@param oModel   , object , modelo MVC que deve ter a linha excluida
/*/
//----------------------------------------------------------------------------------------
method deleteLineMVC(lSeekLine as logical, oModel as object) class pgvpdfcustombase
	If lSeekLine
		oModel:DeleteLine()
	Endif
return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} getValueByProperty
	Obter o valor de um propriedade especificado por parâmetro
	@type function
	@version 12.1.2410 ou Superior
	@author Squad CRM & Faturamento - Inovação
	@since 26/11/2024
	@param aItems, array, Array que será avaliado
	@param cProperty, character, Propriedade que será utilizada na busca
	@param defaultValue, variant, conteudo padrão caso a propriedade não seja localizada
	@return variant, valor da propriedade
/*/
//------------------------------------------------------------------------------
static function getValueByProperty(aItems as array, cProperty as character, defaultValue as variant) as variant
	Local nPosProperty := aScan(aItems, {|x| allTrim(x[1]) == cProperty}) as integer
return IIF (nPosProperty > 0, aItems[nPosProperty, 2], defaultValue )
