#include 'tlpp-core.th'
#include 'tlpp-object.th'
#include 'FWMVCDEF.CH'

namespace totvs.protheus.backoffice.tgvProspects
using namespace tgv.util

#DEFINE FIELD 1
#DEFINE VALUE 2

Static __cPgvBlql   := SuperGetMv("MV_PGVBLQL", .F., "")

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase
	Classe responsavel pela gravação do Prospect utilizando o padrão de
	dados do Portal Gestão de vendas
	@type class
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
/*/
//------------------------------------------------------------------------------

class pgvProspectsBase from tgvbaseResourcesSalesManagement

    public data cProspectId	    as character
    public data cProspectStore	as character

    public method new() as object
    public method fromExecAuto() as logical
    public method commitData() as logical
    private method setHeader()
    protected method addMapFieldsHeader()

endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase::new() as object
	Cria uma nova instancia da classe pgvProspectsBase
	@type method
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
	@return object
/*/
//------------------------------------------------------------------------------

method new() as object class pgvProspectsBase

    _Super:New("SUS", MODEL_OPERATION_INSERT)

return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase::fromExecAuto(jProspect as json) as logical
	Cria a estrutura utilizada na gravação do filtro
	@type method
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
	@param jProspect
	@return logical
/*/
//------------------------------------------------------------------------------

method fromExecAuto(jProspect as json) as logical class pgvProspectsBase

    Local lContinue := .T. as logical
    ::clean()

    If ::nOperation <> MODEL_OPERATION_DELETE
        IIF(jProspect == Nil,lContinue := .F.,(::addMapFields(),::setHeader(jProspect)))
    EndIf

return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase::addMapFieldsHeader
	Adiciona um Mapa de campos utilizando os dados da (SUS)
	@type method
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
/*/
//------------------------------------------------------------------------------

method addMapFieldsHeader() class pgvProspectsBase

	::oHashFieldsHeader := tHashMap():New()
    ::oHashFieldsHeader:set("code"                      ,"US_COD") 
    ::oHashFieldsHeader:set("store"                     ,"US_LOJA") 
    ::oHashFieldsHeader:set("name"                      ,"US_NOME") 
    ::oHashFieldsHeader:set("fantasy"                   ,"US_NREDUZ") 
    ::oHashFieldsHeader:set("estadualregistration"      ,"US_INSCR") 
    ::oHashFieldsHeader:set("prospecttype"              ,"US_TIPO") 
    ::oHashFieldsHeader:set("persontype"                ,"US_PESSOA") 
    ::oHashFieldsHeader:set("cgc"                       ,"US_CGC") 
    ::oHashFieldsHeader:set("city"                      ,"US_MUN") 
    ::oHashFieldsHeader:set("citycode"                  ,"US_COD_MUN") 
    ::oHashFieldsHeader:set("neighborhood"              ,"US_BAIRRO") 
    ::oHashFieldsHeader:set("address"                   ,"US_END") 
    ::oHashFieldsHeader:set("zipcode"                   ,"US_CEP") 
    ::oHashFieldsHeader:set("uf"                        ,"US_EST") 
    ::oHashFieldsHeader:set("phone"                     ,"US_TEL") 
    ::oHashFieldsHeader:set("email"                     ,"US_EMAIL") 

return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase::setHeader(jProspect as json) 
	Define um Cabeçalho considerando os campos mapeados
	@type method
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
	@param jProspect, json, json com os dados do Prospect
/*/
//------------------------------------------------------------------------------

method setHeader(jProspect as json) class pgvProspectsBase

    Local aFieldsHeader as array
    Local jHeader as json
    Local cField  as character
    Local nField as numeric
    Local xValue as variant

    jHeader :=jProspect:getJsonObject("header")
    aFieldsHeader := jHeader:getNames()

    For nField := 1 To Len( aFieldsHeader )
        If ::oHashFieldsHeader:Get(aFieldsHeader[nField] , @cField)
            xValue := ConvertValue(jHeader:GetJsonObject(aFieldsHeader[nField]), GetSx3Cache(cField, "X3_TIPO"))
            
            If ::nOperation == MODEL_OPERATION_UPDATE 
                If cField == "US_COD" .OR. cField == "US_LOJA"
                    Loop
                EndIf
            Else
                If cField == "US_COD"
                    xValue := PadR(xValue, getSX3Cache("US_COD","X3_TAMANHO"))
                    ::cProspectId := xValue
                EndIf
                If cField == "US_LOJA"
                    xValue := PadR(xValue, getSX3Cache("US_LOJA","X3_TAMANHO"))
                    ::cProspectStore := xValue
                EndIf
            EndIf

            aAdd(::aHeader, {cField, xValue, Nil})
        EndIf
    Next

    If ::nOperation == MODEL_OPERATION_INSERT                                                                                                                                 
        If __cPgvBlql == '2' .OR. __cPgvBlql == '3'
            aAdd(::aHeader, {"US_MSBLQL", '1', Nil})
        Else 
            aAdd(::aHeader, {"US_MSBLQL", '2', Nil})
        EndIf
    EndIf 

    If ::nOperation == MODEL_OPERATION_INSERT
        aAdd(::aHeader, {"US_VEND", getSellerFromFilter(.F., .F.), Nil})
    EndIf
    
    ::aHeader := FwVetByDic(::aHeader, "SUS", .F.)
	aSize(aFieldsHeader, 0)
	FwFreeObj(jHeader)

return nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvProspectsBase::commitData() as logical
	Efetua a gravação do Prospect
	@type method
	@version 12.1.2210
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/07/2023
	@return lContinue, logical, retorna .T. em caso de sucesso
/*/
//------------------------------------------------------------------------------

method commitData() as logical class pgvProspectsBase

    Local aMsgErro   := {}   as array
    Local cError     := ''   as character
    Local lContinue  := .T.  as logical
    Local nField     := 1    as numeric
    Local oModel     := FWLoadModel('TMKA260') as object
    Local oModelSUS  as object
    Local nOperation as numeric

    oModel:SetOperation( ::nOperation )
	If oModel:Activate()
		If oModel:IsActive()
            lContinue := .T.
            oModelSUS := oModel:GetModel("SUSMASTER")
            nOperation := oModel:getOperation()
            If nOperation <> MODEL_OPERATION_DELETE
                For nField := 1 To Len(::aHeader)
                    If nOperation == MODEL_OPERATION_INSERT .And. EMPTY(::aHeader[nField, VALUE])
                        Loop
                    EndIf

                    If !oModelSUS:SetValue(::aHeader[nField, FIELD], ::aHeader[nField, VALUE])
                        lContinue := .F.
                        Exit
                    EndIf
                Next
            EndIf

            If !lContinue .Or. !( oModel:VldData() .And. oModel:CommitData() )
                lContinue := .F.
                cError := ""
                aMsgErro := oModel:GetErrorMessage()
                For nField := 1 To Len(aMsgErro)
                    If ValType(aMsgErro[nField]) == "C" .and. !Empty(aMsgErro[nField])
                        cError := convertStringToLine(StrTran( StrTran( aMsgErro[nField], "<", "" ), "-", "" ) + (" "), .f.)
                		::setErrorMessage(cError, .f.)
				        lContinue := .F.
                    EndIf
                Next
                aSize(aMsgErro, 0)
            Endif
        EndIf
        oModel:DeActivate()
		oModel:Destroy()
		FwFreeObj(oModelSUS)
    Endif
	FwFreeObj(oModel)

return lContinue
