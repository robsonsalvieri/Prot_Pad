#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'tgv.prospect.controller.ch'

namespace totvs.protheus.backoffice.tgvProspects
using namespace tgv.util

Static lExistIsSellerFilterInDB := FindFunction("tgv.util.isSellerFilterInDB")
Static lExistCheckDbUseArea     := FindFunction("tgv.util.checkDbUseArea")
Static lExistFindMethod     	:= FindFunction("tgv.util.findMethod")
Static lExistSaveProspect
Static lExistGetNextStore

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects
	API para integração de Prospects - Utilizada no Portal Gestão de Vendas
	@type class
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 03/03/2021
/*/
//------------------------------------------------------------------------------
class tgvProspects
	data jResponse  as json
	data nPage      as numeric
	data nPageSize  as numeric
	data oService   as object

	public method new() as object

	@Get("/api/tgv/prospects/sync/")
	public method getAllSync() as logical

	@Get("/api/tgv/prospects/sync/diff/:dateSync")
	public method getDiffSync() as logical

	@Get("/api/tgv/prospects/")
	public method getAll() as logical

	@Get("/api/tgv/prospects/:prospectid/:prospectstore")
	public method getById() as logical

	@Post("/api/tgv/prospects/")
	Public Method postProspect() as Logical

	@Put("/api/tgv/prospects/:prospectid/:prospectstore")
	Public Method putProspect() as Logical

	@Delete("/api/tgv/prospects/:prospectid/:prospectstore")
	Public Method deleteProspect() as Logical

	@Get("/api/tgv/prospects/nextstore/:prospectid")
	Public Method getNextStore() as Logical

endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects::New() as Object
	Obtem uma nova instancia da classe tgvProspects
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 03/03/2021
	@return object, retorna uma nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as object class tgvProspects

	::nPage         := 1
	::nPageSize     := 10
	::oService      := ProspectService():getInstance()
	::jResponse     := jsonObject():New()
	
	If ( Empty( lExistSaveProspect) )
		lExistSaveProspect := IIF(lExistFindMethod, findMethod(::oService, "saveProspect"), .F.)
	EndIf	

	If ( Empty( lExistGetNextStore) )
		lExistGetNextStore := IIF(lExistGetNextStore, findMethod(::oService, "getNextStore"), .F.)
	EndIf	

Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects::getAllSync() as logical
	Obtem uma lista de prospects, utilizando o mesmo padrão do PO-SYNC
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 03/03/2021
	@return logical, este metódo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getAllSync() as logical class tgvProspects
	getPageAndPageSize(@::nPage, @::nPageSize)
	If lExistIsSellerFilterInDB .And. isSellerFilterInDB()
		::jResponse:fromJson(::oService:getProspectsSync(::nPage, ::nPageSize, /*cDateSync*/))
	Else
		::jResponse:fromJson('{"items": [], "hasNext": false}')
	EndIf
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects::getDiffSync() as logical
	Retorna somente os Prospects alterados, conforme data informado no parametro DateSync
	@type method
	@version 12.1.3
	@author Danilo Salve / Squad CRM & Faturamento
	@since 03/03/2021
	@return logical, este metódo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getDiffSync() as logical class tgvProspects
	getPageAndPageSize(@::nPage, @::nPageSize)
	If lExistIsSellerFilterInDB .And. isSellerFilterInDB()
		::jResponse:fromJson(::oService:getProspectsSync(::nPage, ::nPageSize, GetSyncDate()))
	Else
		::jResponse:fromJson('{"items": [], "hasNext": false}')
	EndIf
	AnswerRest(::jResponse)
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects::getAll() as logical
	Obtem uma Lista de prospects (Utilizado no PGV Online)
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
	@return logical, este metódo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getAll() as logical class tgvProspects
	Local aURLFilter	:= getFilterParam()        as array
	Local cFields		:= getQueryParam('FIELDS') as character
	Local cSort			:= getQueryParam('SORT')   as character
	Local cQuickFilter  := ""                      as Character

	GetPageAndPageSize(@::nPage, @::nPageSize)
	cQuickFilter := getQueryParam('QUICKFILTER')
	::jResponse:FromJson(::oService:getProspects(::nPage, ::nPageSize, aURLFilter, cFields, cSort, /*cCode*/, /*cStore*/, cQuickFilter))
	AnswerRest(::jResponse)

	aSize(aURLFilter, 0)
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspects::getById() as logical
	Obtem um Prospect utilizando o código e loja como identificador
	@type method
	@version 12.1.33
	@author Danilo Salve / Squad CRM & Faturamento
	@since 19/01/2022
	@return logical, este metódo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getById() as logical class tgvProspects

	Local cFields := getQueryParam('FIELDS') as character

	oRest:setKeyHeaderResponse('Content-Type','application/json')
	::jResponse:FromJson(::oService:getProspects(1, 1, {}, cFields, /*cSort*/, getPathParams('prospectid'), getPathParams('prospectstore')))

	If len(::jResponse:GetNames()) > 0
		oRest:setResponse(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0001) ) //"Prospect não localizado"
	EndIf
	
return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspect::postProspect() as Logical
	Efetua a inclusão de um Prospect.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		19/07/2023
	@return		logical
/*/
//------------------------------------------------------------------------------

Method postProspect() as Logical Class tgvProspects	
	
	Local jBody := GetRequestBody() as Json
	Local lCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If lCheckDbUseArea .And. lExistSaveProspect
		If jBody <> Nil
			::oService:saveProspect(jBody, 3)
			FreeObj(jBody)
		Endif
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspect::putProspect() as Logical
	Efetua a alteração de um Prospect.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		19/07/2023
	@return		logical
/*/
//------------------------------------------------------------------------------

Method putProspect() as Logical Class tgvProspects

	Local jBody := GetRequestBody() as Json
	Local lCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)

	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If lCheckDbUseArea .And. lExistSaveProspect
		If jBody <> Nil
			::oService:saveProspect(jBody, 4,  GetPathParams("prospectid"),  GetPathParams("prospectstore"))
			FreeObj(jBody)
		Endif
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} tgvProspect::deleteProspect() as Logical
	Efetua a exclusão de um Prospect.
	@type		method
	@version	12.1.2210
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		19/07/2023
	@return		logical
/*/
//------------------------------------------------------------------------------

Method deleteProspect() as Logical Class tgvProspects

	Local lCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If lCheckDbUseArea .And. lExistSaveProspect
		::oService:saveProspect(/*jBody*/, 5,  GetPathParams("prospectid"),  GetPathParams("prospectstore"))
	EndIf

Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc}  tgvProspect::getNextStore() as Logical
	Deve obter a próxima loja para o prospect.	
	@type		method
	@sample     GET /api/tgv/prospect/nextstore/:prospectid
	@author     Gabriel Oliveira dos Santos
	@since      19/07/2023
	@version    12.1.2210
/*/
//------------------------------------------------------------------------------
Method getNextStore() as Logical Class tgvProspects

	Local lCheckDbUseArea := IIF(lExistCheckDbUseArea, checkDbUseArea(), .T.)
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If lCheckDbUseArea .And. lExistSaveProspect
		::jResponse := ::oService:getNextStore(getPathParams('prospectid'))
	EndIf

	oRest:setResponse(::jResponse:ToJson())

Return .T.
