#include "PROTHEUS.CH"
#include "tlpp-core.th"
#include "tgv.contacts.data.protheus.ch"

namespace tgv.contacts
using namespace tgv.util
using namespace totvs.protheus.backoffice.pgvContacts

#DEFINE INCLUI 3
#DEFINE ALTERA 4
#DEFINE EXCLUI 5
#DEFINE SOURCE "TMKA070"

Static lExistPGVContactsBase

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:ContactsProtheusData
	Efetua a consulta de Contatos (SU5) Protheus.

	@author	Squad CRM & Faturamento
	@since	01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Class ContactsProtheusData from FWAdapterBaseV2
	Public Method New() as Object
	Public Method Get()
	Public Method getContactsAddress() 
	Public Method getContactsPhone()
	Public Method saveContact() as logical
	Public Method getlExistPGVContactsBase() as Logical
	Public Method getAllContacts() 
EndClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:New
	Cria uma nova instancia do Objeto ContactsProtheusData.

	@sample		ContactsProtheusData():New()
	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Method New() as Object Class ContactsProtheusData
	_Super:New("GET", .T.)
Return Self

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:Get
	Obtem uma Lista de Contatos retornando um JSON respeitando a Quantidade de 
	registro por pagina, especificado no paramentro PageSize.

	@sample		oData:Get( 1, 10, {}, Nil, '-id' )
	@type		method
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão do FWAdapterBaseV2
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param		cSort		, Character	, Ordernação do Response
	@param		cContactId	, Character	, Codigo do contato
	@author		Danilo Salve
	@since		20/01/2021
	@version	12.1.33
/*/
//------------------------------------------------------------------------------
Method get(nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, cSort as Character, cContactId as Character) CLASS ContactsProtheusData
	Local aArea		:= GetArea()
	
	AddMapFieldsContacts( self, .T. )
	
	::setPage( nPage )
	::setPageSize( nPageSize )
	::SetQuery( GetQueryContacts(.T.) )	

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif
	
	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	If !Empty( cSort )
		::SetOrderQuery( cSort )
	EndIf

	::SetWhere( GetFilterContacts( cContactId, /*cDateSync*/ ) )
	::SetOrder( "AC8_FILIAL,AC8_CODCON,AC8_ENTIDA,AC8_FILENT,AC8_CODENT" )

	If ::Execute()
		::FillGetResponse()
	EndIf	

	RestArea(aArea)
	aSize(aArea, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:getContactsAddress
	Obtem uma Lista de endereços de um contato

	@type		method
	@param		cContactId	, Character	, Codigo do contato
	@author		Gabriel Oliveira dos Santos
	@since		28/09/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Method getContactsAddress(	nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, ;
							cContactId as Character) CLASS ContactsProtheusData
	Local aArea		:= GetArea()

	AddMapFieldsContactsAddress(self)

	::setPage( nPage )
	::setPageSize( nPageSize )

	::SetQuery(GetQueryAddress())

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	::SetWhere(GetFilterContactsAddress( cContactId ))
	::SetOrder("AGA_FILIAL,AGA_CODIGO,AGA_ENTIDA,AGA_CODENT")

	If ::Execute()
		::FillGetResponse()
	EndIf

	RestArea(aArea)
	aSize(aArea, 0)
Return Nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:getContactSPhone
	Obtem uma Lista de telefones de um contato

	@type		method
	@param		cContactId	, Character	, Codigo do contato
	@author		Gabriel Oliveira dos Santos
	@since		28/09/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Method getContactsPhone(nPage as Numeric, nPageSize as Numeric, aURLFilter as Array, cFields as Character, ;
						cContactId as Character) CLASS ContactsProtheusData
	Local aArea		:= GetArea()
	
	AddMapFieldsContactsPhone(self)
	
	::setPage( nPage )
	::setPageSize( nPageSize )

	::SetQuery(GetQueryPhone())

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	::SetWhere(GetFilterContactsPhone( cContactId ))
	::SetOrder("AGB_FILIAL,AGB_CODIGO,AGB_ENTIDA,AGB_CODENT")

	If ::Execute()
		::FillGetResponse()
	EndIf	

	RestArea(aArea)
	aSize(aArea, 0)
Return Nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:AddMapFieldsContacts
	Cria o Mapa de campos Protheus x API para os Contatos.

	@param		oSelf	, Object	, Objeto com herança da Classe FWAdapterBaseV2
	@param		isOnline, Logical	, Adicionar campos no padrão OnLine ou Sync
	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Function AddMapFieldsContacts(oSelf as Object, isOnline as Logical)
	Local aInternalId   := GetInternalId( { "U5_FILIAL", "A1_CGC", "U5_CODCONT" } )
	Local aContactId	:= GetInternalId( { "U5_FILIAL", "U5_CODCONT" } )
	Local lFixedId		:= !oSelf:oJsonObj:lList

	If isOnline
		oSelf:AddMapFields("internalid" , "internalid", .T., .F., { "internalid", 'C', aInternalId[1]			, 0 }, aInternalId[2])
		oSelf:AddMapFields("contactid"  , "contactid" , .T., lFixedId, { "contactid"	, 'C', aContactId[1]	, 0 }, aContactId[2])
	else
		oSelf:AddMapFields("internalid" , "internalid", .T., .F., { "internalid", 'C', aInternalId[1]			, 0 }, "' '")
		oSelf:AddMapFields("contactid"  , "contactid" , .T., .F., { "contactid"	, 'C', aContactId[1]			, 0 }, "' '")
		oSelf:AddMapFields("deleted"    , "deleted"   , .T., .F., { "deleted"	, 'C', 1, 0 }, "SU5.D_E_L_E_T_")
	Endif

	oSelf:AddMapFields("branch"     , "U5_FILIAL" , .T., .F., { "U5_FILIAL"	, 'C', GetFieldLength("U5_FILIAL")	, 0 }, "SU5.U5_FILIAL")
	oSelf:AddMapFields("code"       , "U5_CODCONT", .T., .F., { "U5_CODCONT", 'C', GetFieldLength("U5_CODCONT")	, 0 }, "SU5.U5_CODCONT")
	oSelf:AddMapFields("cgcentity"  , "A1_CGC"	  , .T., .F., { "A1_CGC"	, 'C', GetFieldLength("A1_CGC")		, 0 }, "SA1.A1_CGC")
	oSelf:AddMapFields("name"       , "U5_CONTAT" , .T., .F., { "U5_CONTAT"	, 'C', GetFieldLength("U5_CONTAT")	, 0 })
	oSelf:AddMapFields("email"      , "U5_EMAIL"  , .T., .F., { "U5_EMAIL"	, 'C', GetFieldLength("U5_EMAIL")	, 0 })

	aSize(aContactId, 0)
	aSize(aInternalId, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:AddMapFieldsContactsAddress
	Cria o Mapa de campos Protheus x API para os endereços dos Contatos.

	@param		oSelf	, Object	, Objeto com herança da Classe FWAdapterBaseV2
	@author		Squad CRM & Faturamento
	@since		27/09/2023
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Function AddMapFieldsContactsAddress(oSelf as Object)
	Local aInternalId   := GetInternalId( { "AGA_FILIAL", "AGA_CODIGO", "AGA_ENTIDA", "AGA_CODENT" } )
	Local aContactId	:= GetInternalId( { "AGA_FILIAL", "AGA_CODIGO" } )
	Local lFixedId		:= !oSelf:oJsonObj:lList

	oSelf:AddMapFields("internalid" , "internalid", .T., .F., { "internalid", 'C', aInternalId[1]			, 0 }, aInternalId[2])
	oSelf:AddMapFields("contactid"  , "contactid" , .T., lFixedId, { "contactid"	, 'C', aContactId[1]	, 0 }, aContactId[2])
	oSelf:AddMapFields("branch"      , "AGA_FILIAL", .T., .F., { "AGA_FILIAL", 'C', GetFieldLength("AGA_FILIAL"), 0 }, "AGA.AGA_FILIAL")
	oSelf:AddMapFields("code"        , "AGA_CODIGO", .T., .F., { "AGA_CODIGO", 'C', GetFieldLength("AGA_CODIGO"), 0 }, "AGA.AGA_CODIGO")
	oSelf:AddMapFields("addresstype" , "AGA_TIPO"  , .T., .F., { "AGA_TIPO"	 , 'C', GetFieldLength("AGA_TIPO")	, 0 }, "AGA.AGA_TIPO")
	oSelf:AddMapFields("address"     , "AGA_END"   , .T., .F., { "AGA_END"	 , 'C', GetFieldLength("AGA_END")	, 0 })
	oSelf:AddMapFields("zipcode"     , "AGA_CEP"   , .T., .F., { "AGA_CEP"	 , 'C', GetFieldLength("AGA_CEP")	, 0 })
	oSelf:AddMapFields("neighborhood", "AGA_BAIRRO", .T., .F., { "AGA_BAIRRO", 'C', GetFieldLength("AGA_BAIRRO"), 0 })
	oSelf:AddMapFields("citycode"    , "AGA_MUN"   , .T., .F., { "AGA_MUN"	 , 'C', GetFieldLength("AGA_MUN")	 , 0 })
	oSelf:AddMapFields("city"   	 , "AGA_MUNDES", .T., .F., { "AGA_MUNDES", 'C', GetFieldLength("AGA_MUNDES"), 0 })
	oSelf:AddMapFields("complement"  , "AGA_COMP"  , .T., .F., { "AGA_COMP"	 , 'C', GetFieldLength("AGA_COMP")	, 0 })
	oSelf:AddMapFields("state"  	 , "AGA_EST"   , .T., .F., { "AGA_EST"	 , 'C', GetFieldLength("AGA_EST")	, 0 })

	aSize(aContactId, 0)
	aSize(aInternalId, 0)
Return Nil
//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:AddMapFieldsContactsPhone
	Cria o Mapa de campos Protheus x API para os telefones dos Contatos.

	@param		oSelf	, Object	, Objeto com herança da Classe FWAdapterBaseV2
	@author		Squad CRM & Faturamento
	@since		27/09/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Function AddMapFieldsContactsPhone(oSelf as Object)
	Local aInternalId   := GetInternalId( { "AGB_FILIAL", "AGB_CODIGO", "AGB_ENTIDA" ,"AGB_CODENT"} ) //AGB_FILIAL+AGB_CODIGO+AGB_ENTIDA+AGB_CODENT                                                                                                                                                                                                               
	Local aContactId	:= GetInternalId( { "AGB_FILIAL", "AGB_CODIGO" } )
	Local lFixedId		:= !oSelf:oJsonObj:lList

	oSelf:AddMapFields("internalid" , "internalid", .T., .F., { "internalid", 'C', aInternalId[1]			, 0 }, aInternalId[2])
	oSelf:AddMapFields("contactid"  , "contactid" , .T., lFixedId, { "contactid"	, 'C', aContactId[1]	, 0 }, aContactId[2])
	oSelf:AddMapFields("branch"     , "AGB_FILIAL", .T., .F., { "AGB_FILIAL", 'C', GetFieldLength("AGB_FILIAL")	, 0 }, "AGB.AGB_FILIAL")
	oSelf:AddMapFields("code"       , "AGB_CODIGO", .T., .F., { "AGB_CODIGO", 'C', GetFieldLength("AGB_CODIGO")	, 0 }, "AGB.AGB_CODIGO")
	oSelf:AddMapFields("phonetype"  , "AGB_TIPO"  , .T., .F., { "AGB_TIPO"	, 'C', GetFieldLength("AGB_TIPO")	, 0 })
	oSelf:AddMapFields("ddd"       	, "AGB_DDD"   , .T., .F., { "AGB_DDD"	, 'C', GetFieldLength("AGB_DDD")	, 0 })
	oSelf:AddMapFields("ddi"      	, "AGB_DDI"   , .T., .F., { "AGB_DDI"	, 'C', GetFieldLength("AGB_DDI")	, 0 })
	oSelf:AddMapFields("phone"      , "AGB_TELEFO", .T., .F., { "AGB_TELEFO", 'C', GetFieldLength("AGB_TELEFO")	, 0 })
	oSelf:AddMapFields("complement" , "AGB_COMP"  , .T., .F., { "AGB_COMP"	, 'C', GetFieldLength("AGB_COMP")	, 0 })

	aSize(aContactId, 0)
	aSize(aInternalId, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
	Monta a expressão SQL para consulta dos Contatos.

	@author		Squad CRM & Faturamento
	@since		01/10/2020
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Function GetQueryContacts(lCanFilterExcluded as Logical)
	Local cQuery AS CHARACTER
	Local cConcat :=  "||"
	Local cSubstr := "SUBSTR"
	LocaL nTamCod := GetFieldLength("A1_COD")
	Local nTamLoj := GetFieldLength("A1_LOJA")

	If AllTrim( Upper( TcGetDb() ) ) == "MSSQL"
		cConcat := "+"
		cSubstr := "SUBSTRING"
	Endif

	cQuery := " SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("AC8") + " AC8 "
	cQuery += " INNER JOIN " + RetSqlName("SU5") + " SU5 "
	cQuery += " ON SU5.U5_CODCONT = AC8.AC8_CODCON "
	cQuery += " AND SU5.U5_FILIAL = '" + FwxFilial("SU5") + "' "

	If lCanFilterExcluded
		cQuery += "AND SU5.D_E_L_E_T_ = ' ' "
	Endif

	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 "
	cQuery += " ON SA1.A1_FILIAL = '" + FwXFilial("SA1") + "' "
	cQuery += " AND SA1.A1_COD = " + cSubstr + "(AC8.AC8_CODENT, 1," + CValToChar(nTamCod) + ") "
	cQuery += " AND SA1.A1_LOJA = " + cSubstr + "(AC8.AC8_CODENT," + CValToChar(nTamCod + 1) + ", " + CValToChar(nTamLoj + 1) + ") "

	If lCanFilterExcluded
		cQuery += "AND SA1.D_E_L_E_T_ = ' ' "
	Endif

	cQuery += " WHERE #QueryWhere#"

Return cQuery

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQueryAddress
	Monta a expressão SQL para consulta dos endereços dos Contatos.

	@author		Squad CRM & Faturamento
	@since		28/09/2023
	@version	12.1.2210
	@return		Character	, Expressão com a consulta a ser utilizada
/*/
//------------------------------------------------------------------------------
Static Function GetQueryAddress()
	Local cQuery AS CHARACTER

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + RetSqlName("AGA") + " AGA "
	cQuery += " WHERE #QueryWhere# "

Return cQuery
//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQueryPhone
	Monta a expressão SQL para consulta telefones dos Contatos.

	@author		Squad CRM & Faturamento
	@since		28/09/2023
	@version	12.1.2210
	@return		Character	, Expressão com a consulta a ser utilizada
/*/
//------------------------------------------------------------------------------
Static Function GetQueryPhone()
	Local cQuery AS CHARACTER
	
	cQuery := " SELECT #QueryFields#"
	cQuery += " FROM " + RetSqlName("AGB") + " AGB "
	cQuery += " WHERE #QueryWhere#"

Return cQuery
//------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilterContacts
	Obtem o filtro de contatos

	@sample		GetFilterContacts("TGVR01", .T.)
	@param		cContactId	, Character	, Codigo do contato
	@param		cDateSync	, Character	, Data de Sincronismo
	@return		Character	, Expressão utilizada na consulta
	@author		Danilo Salve
	@since		27/07/2021
	@version	12.1.27
/*/
//------------------------------------------------------------------------------
Function GetFilterContacts(cContactId as Character, cDateSync as Character) as Character
	Local cWhere as Character

	cWhere := " AC8.AC8_FILIAL = '" + FwXFilial("AC8") + "' AND AC8.AC8_ENTIDA = 'SA1' "

	If !Empty(cDateSync) .And. SubStr(cDateSync,1,1) != '-'
		cWhere += " AND AC8.S_T_A_M_P_ > " + ConvertTimeStamp( cDateSync, "value", "timestamp" )
	else
		cWhere += " AND AC8.D_E_L_E_T_ = ' ' "
	EndIf
	
	If !Empty( cContactId )
		cWhere += " AND SU5.U5_CODCONT = '" + cContactId + "'"
	Endif

Return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilterContactsPhone
	Obtem o filtro de telefones dos contatos

	@sample		GetFilterContactsPhone("TGVR01")
	@param		cContactId	, Character	, Codigo do contato
	@return		Character	, Expressão utilizada na consulta
	@author		Rafael Mota Previdi
	@since		29/09/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Static Function GetFilterContactsPhone( cContactId as Character) as Character
	Local cWhere as Character

	Default cContactId := ""

	cWhere := " AGB.AGB_FILIAL = '" + FwXFilial("AGB") +"'"
	cWhere += " AND AGB.D_E_L_E_T_ = ' ' "
	
	If !Empty( cContactId )
		cWhere += " AND AGB.AGB_ENTIDA = 'SU5'"
		cWhere += " AND AGB.AGB_CODENT = '" + cContactId + "'"
	Endif
Return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilterContactsAddress
	Obtem o filtro de endereços dos contatos

	@sample		GetFilterContactsAddress("TGVR01")
	@param		cContactId	, Character	, Codigo do contato
	@return		Character	, Expressão utilizada na consulta
	@author		Rafael Mota Previdi
	@since		29/09/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Static Function GetFilterContactsAddress(cContactId as Character) as Character
	Local cWhere as Character

	Default cContactId := ""

	cWhere := " AGA.AGA_FILIAL = '" + FwXFilial("AGA") + "'"
	cWhere += " AND AGA.D_E_L_E_T_ = ' ' "
	
	If !Empty( cContactId )
		cWhere += " AND AGA.AGA_ENTIDA = 'SU5'"
		cWhere += " AND AGA.AGA_CODENT = '" + cContactId + "'"
	Endif
Return cWhere

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactProtheusData:saveContact(jContact as Json, nOperation as Numeric,cContactId as Character, cContactStore as Character) as Logical
	Efetua a inclusão, altera ou exclusão de um Contato
	@type		method
	@version	12.1.33
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		25/09/2023
	@param		jContact		, json		, json do Contato
	@param		nOperation		, Numeric	, Operação (3 - Inclusão, 4 - Alteração ou 5 - Exclusão)
	@param		cContactId		, character	, U5_CODCONT
	@return		logical, Se obteve sucesso na transação retorna verdadeiro.
/*/
//------------------------------------------------------------------------------
Method saveContact(jContact as Json, nOperation as Numeric, cContactId as Character) as Logical Class ContactsProtheusData
	Local aArea			:= FwGetArea() as Array
	Local aAreaSU5		:= SU5->(FwGetArea()) as Array
	Local oContact		as Object
	Local lContinue		:= .T. as Logical
	Local nStatusCode	:= 201 as Numeric

	If ::getlExistPGVContactsBase()
		oContact := pgvContactsBase():new()
		oContact:cContactId	 := PadR(cContactId, getSX3Cache("U5_CODCONT","X3_TAMANHO"))
		oContact:cSellerId 	 := getSellerFromFilter(FWIsAdmin(), .F.)

		If oContact:setOperation( nOperation )
			If nOperation <> INCLUI
				DbSelectArea("SU5")
				SU5->(DbSetOrder(1)) 
				If !SU5->( DbSeek( FwXFilial('SU5') + oContact:cContactId ))
					oContact:setErrorMessage( STR0001, .T. ) // Contato não encontrado
					nStatusCode := 404
					lContinue := .F.			
				Endif
			EndIf

    	    If lContinue .And. oContact:fromExecAuto(jContact)
				nStatusCode := 200
    	        If oContact:commitData()
					oRest:setStatusCode( nStatusCode )
					oRest:setResponse( getResponseContacts( oContact:nOperation, oContact:cContactId) )
					NotifySeller(oContact:cContactId, oContact:cSellerId, oContact:nOperation, /*cError*/, /*cExternalId*/, .F., SOURCE)
				Else
					nStatusCode := 400
					NotifySeller(oContact:cContactId, oContact:cSellerId, nOperation, oContact:getErrorMessage(), /*cExternalId*/, .F., SOURCE)
					SetRestFault(nStatusCode, FWHttpEncode(oContact:getErrorMessage()) )
					lContinue := .F.
    	        Endif
    	    Else
				nStatusCode := IIF(nStatusCode == 404, 404, 400)
				NotifySeller(oContact:cContactId, oContact:cSellerId, nOperation, oContact:getErrorMessage(), /*cExternalId*/, .F., SOURCE)
    	        SetRestFault(nStatusCode, FWHttpEncode(oContact:getErrorMessage()) )
				lContinue := .F.
    	    Endif
    	Endif
		oContact:Destroy()
		FreeObj(oContact)
	Else
		lContinue := .F.
		nStatusCode := 501
		SetRestFault(nStatusCode, FWHttpEncode(STR0002) ) //Falha ao registrar o Contato, atualize o ambiente com a última expedição continua do Faturamento
	EndIf

	FwRestArea(aAreaSU5)
	FwRestArea(aArea)
	aSize(aAreaSU5, 0)
	aSize(aArea, 0)
Return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactProtheusData:getlExistPGVContactsBase(lExistPGVContactsBaseNew as Logical) as Logical
	Preenche a variavel lExistPGVContactsBase
	@type		method
	@version	12.1.2210
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@since		05/10/2023
	@param		lExistPGVContactsBaseNew		, Logical		, Valor a ser atribuido na variavel lExistPGVContactsBase
	@return		logical, Novo valor da variável lExistPGVContactsBase
/*/
//------------------------------------------------------------------------------
Method getlExistPGVContactsBase(lExistPGVContactsBaseNew as Logical) as Logical Class ContactsProtheusData

	If !(lExistPGVContactsBaseNew == Nil)
		lExistPGVContactsBase := .F.
	EndIf

	If (lExistPGVContactsBase == Nil)
		lExistPGVContactsBase := FindClass("pgvContactsBase")
	EndIf

Return lExistPGVContactsBase

//------------------------------------------------------------------------------
/*/{Protheus.doc} getResponseContacts
	Obtem uma resposta de sucesso de acordo com a operação informada.
	@type		function
	@version	12.1.2210 ou superior 
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@since		29/09/2023
	@param		nOperation	, numeric, Operação (3, 4 ou 5)
	@param		cContactId	, character, Id do contato processado
	@return 	json, resposta de sucesso da requisição
/*/
//------------------------------------------------------------------------------
static function getResponseContacts(nOperation as Numeric, cContactId as Character) as json
	Local jResponse		:= JsonObject():New() as json
	Local jContact		:= JsonObject():New() as json
	Local aContact		:= {} as Array

	If nOperation == INCLUI
		jResponse['messageSuccess'] := FwHttpEncode(STR0003)	//#"Contato incluído com sucesso"
	Elseif nOperation == ALTERA
		jResponse['messageSuccess'] := FwHttpEncode(STR0004)	//#"Contato alterado com sucesso"
	Elseif nOperation == EXCLUI
		jResponse['messageSuccess'] := FwHttpEncode(STR0005)	//#"Contato excluído com sucesso"
	Endif

	jResponse['status'] := "OK"

	If nOperation <> EXCLUI
		jContact['code'] := cContactId
		aAdd(aContact, jContact)
		jResponse['items'] := aClone(aContact)
	Endif

	FreeObj(jContact)
	aSize(aContact, 0)
Return jResponse

//MENON

//----------------------------------------------------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:AddMapFieldsAllContacts
	Cria o Mapa de campos Protheus x API para os Contatos.

	@param		oSelf	     , Object	, Objeto com herança da Classe FWAdapterBaseV2
	@param		lIsForRelation, Logical	, Adicionar campo que indica que o contato esta relacionado com a entidade informada
	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
/*/
//----------------------------------------------------------------------------------------------------------------------------
Function AddMapFieldsAllContacts(oSelf as Object, lIsForRelation as Logical)
	Local aInternalId   := GetInternalId( { "U5_FILIAL", "U5_CODCONT", "U5_EMAIL"} )
	Local aContactId	:= GetInternalId( { "U5_FILIAL", "U5_CODCONT" } )
	Local lFixedId		:= !oSelf:oJsonObj:lList

	oSelf:AddMapFields("internalid" , "internalid", .T., .F., { "internalid", 'C', aInternalId[1]			, 0 }, aInternalId[2])
	oSelf:AddMapFields("contactid"  , "contactid" , .T., lFixedId, { "contactid"	, 'C', aContactId[1]	, 0 }, aContactId[2])
	oSelf:AddMapFields("branch"     , "U5_FILIAL" , .T., .F., { "U5_FILIAL"	, 'C', GetFieldLength("U5_FILIAL")	, 0 }, "SU5.U5_FILIAL")
	oSelf:AddMapFields("code"       , "U5_CODCONT", .T., .F., { "U5_CODCONT", 'C', GetFieldLength("U5_CODCONT")	, 0 }, "SU5.U5_CODCONT")
	oSelf:AddMapFields("name"       , "U5_CONTAT" , .T., .F., { "U5_CONTAT"	, 'C', GetFieldLength("U5_CONTAT")	, 0 })
	oSelf:AddMapFields("email"      , "U5_EMAIL"  , .T., .F., { "U5_EMAIL"	, 'C', GetFieldLength("U5_EMAIL")	, 0 })
	If lIsForRelation
		oSelf:AddMapFields("isChecked"  , "isChecked" , .T., .F., { "isChecked"	, 'L', 1                            , 0 }, "CASE WHEN AC8_CODCON IS NULL THEN 'F' ELSE 'T' END " )
	EndIf

	aSize(aContactId, 0)
	aSize(aInternalId, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactsProtheusData:getAllContacts
	Obtem uma Lista de todos os contatos

	@type		method
	@param		nPage		, Numeric	, Número da página que será retornada
	@param		nPageSize	, Numeric	, Quantidade de registros por pagina
	@param		aURLFilter	, Array		, Lista de Filtros no padrão
	@param		cFields		, Character	, Lista de campos que devem ser retornados
	@param      cEntBranch  , Character , Filial da entidade a ser utilizada para verificar os contatos relacionados
	@param      cEntity     , Character , Entidade a ser utilizada para verificar os contatos relacionados
	@param      cCodEntity  , Character , Código da entidade a ser utilizada para verificar os contatos relacionados	
	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Method getAllContacts(	nPage      as Numeric,   ;
						nPageSize  as Numeric,   ;
						aURLFilter as Array,     ;
						cFields    as Character, ;
						cEntBranch as Character, ;
						cEntity    as Character, ;
						cCodEntity as Character) CLASS ContactsProtheusData
	Local aArea	        := GetArea() as Array
	Local lIsForRelation             as Logical

	Default cFields    := ""
	Default cEntBranch := ""
	Default cCodEntity := ""

	lIsForRelation := !Empty(cCodEntity)

	AddMapFieldsAllContacts( self, lIsForRelation )

	::setPage( nPage )
	::setPageSize( nPageSize )

	::SetQuery(GetQueryAllContacts(lIsForRelation, cEntBranch, cEntity, cCodEntity))

	If !Empty( aURLFilter ) .And. Len( aURLFilter ) > 0 
		::SetUrlFilter( aURLFilter )
	Endif

	If !Empty( cFields )
		::SetFields( cFields )
	Endif

	::SetWhere(GetFilterAllContacts(lIsForRelation))
	If !lIsForRelation
		::SetOrder("U5_FILIAL,U5_CODCONT")
	Else
		::SetOrder("CASE WHEN AC8_CODCON IS NULL THEN '1' ELSE '0' END,U5_FILIAL,U5_CODCONT")
	EndIf

	If ::Execute()
		::FillGetResponse()
	EndIf

	RestArea(aArea)
	aSize(aArea, 0)
Return Nil

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetQueryAllContacts
	Monta a expressão SQL para consulta de todos os Contatos.

	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
	@param		lIsForRelation, Logical	, Adicionar campo que indica que o contato esta relacionado com a entidade informada
	@param      cEntBranch  , Character , Filial da entidade a ser utilizada para verificar os contatos relacionados
	@param      cEntity     , Character , Entidade a ser utilizada para verificar os contatos relacionados
	@param      cCodEntity  , Character , Código da entidade a ser utilizada para verificar os contatos relacionados
	@return		Character	 , Expressão com a consulta a ser utilizada
/*/
//------------------------------------------------------------------------------
Static Function GetQueryAllContacts(	lIsForRelation	as Logical  , ;
										cEntBranch 		as Character, ;
										cEntity    		as Character, ;
										cCodEntity 		as Character)
	Local cQuery AS CHARACTER

	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + RetSqlName("SU5") + " SU5 "
	If lIsForRelation
		cQuery += "	LEFT JOIN " + RetSqlName("AC8") + " AC8 "
		cQuery += "	ON SU5.U5_CODCONT = AC8.AC8_CODCON  AND "
		cQuery += "	AC8.AC8_FILENT = '" + PADR(cEntBranch , GetSX3Cache("AC8_FILENT", "X3_TAMANHO")) + "' AND "
		cQuery += "	AC8.AC8_ENTIDA = '" + PADR(cEntity    , GetSX3Cache("AC8_ENTIDA", "X3_TAMANHO")) + "' AND "
		cQuery += "	AC8.AC8_CODENT = '" + PADR(cCodEntity , GetSX3Cache("AC8_CODENT", "X3_TAMANHO")) + "'     "
	EndIf
	cQuery += " WHERE #QueryWhere# "

Return cQuery

//------------------------------------------------------------------------------
/*/{Protheus.doc} GetFilterAllContacts
	Obtem o filtro de todos os contatos

	@sample		GetFilterAllContacts("TGVR01")
	@param		lIsForRelation, Logical	, Adicionar campo que indica que o contato esta relacionado com a entidade informada
	@return		Character	, Expressão utilizada na consulta
	@author		Squad CRM & Faturamento
	@since		26/10/2023
	@version	12.1.2210
/*/
//------------------------------------------------------------------------------
Static Function GetFilterAllContacts(lIsForRelation) as Character
	Local cWhere as Character

	cWhere := " SU5.U5_FILIAL = '" + FwXFilial("SU5") + "'"
	cWhere += " AND SU5.D_E_L_E_T_ = ' ' "
	If lIsForRelation
		cWhere += " AND (AC8.AC8_FILIAL = '" + FwXFilial("AC8") + "' OR AC8.AC8_FILIAL IS NULL) "
		cWhere += " AND (AC8.D_E_L_E_T_ = ' ' OR AC8.D_E_L_E_T_ IS NULL)"
	EndIf

Return cWhere
