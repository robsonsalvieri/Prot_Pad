#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'PGV.SALESGOALS.CONTROLLER.CH'

namespace totvs.protheus.backoffice.pgvsalesgoals
using namespace tgv.util

Static lExistFindMethod     	:= FindFunction("tgv.util.findMethod")
Static lExistSavesalesgoal

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvsalesgoals
	API para integracao de salesgoals - Utilizada no Portal Gestao de Vendas
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 03/03/2021
/*/
//------------------------------------------------------------------------------
class pgvsalesgoals
	data jResponse  as json
	data nPage      as numeric
	data nPageSize  as numeric
	data oService   as object

	public method new() as object

	@Get("/api/pgv/salesgoals/:datade/:dataate")
	Public Method getSalesGoalsbyDate()

	@Get("/api/pgv/salesgoals/")
	Public Method getSalesGoals()

endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvsalesgoals::New() as Object
	Obtem uma nova instancia da classe pgvsalesgoals
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 03/03/2021
	@return object, retorna uma nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as object class pgvsalesgoals

	::nPage         := 1
	::nPageSize     := 10
	::oService      := salesgoalService():getInstance()
	::jResponse     := jsonObject():New()

	If ( Empty( lExistSavesalesgoal) )
		lExistSavesalesgoal := IIF(lExistFindMethod, findMethod(::oService, "savesalesgoal"), .F.)
	EndIf

Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvsalesgoals::getSalesGoals() as logical
	Obtem uma Lista de salesgoals (Utilizado no PGV Online)
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/01/2022
	@return logical, este metodo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getSalesGoals()  class pgvsalesgoals
	Local aURLFilter:= getFilterParam()        as array
	Local cFields	:= getQueryParam('FIELDS') as character
	Local cSort		:= getQueryParam('SORT')   as character
	Local aItems    := {}                      as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:FromJson(::oService:getsalesgoals(::nPage, ::nPageSize,aURLFilter, cFields, cSort))
	
	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		AnswerRest(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0001) ) //Nenhum dado encontrado
	EndIf

	aSize(aURLFilter, 0)
	aSize(aItems, 0)

return .T.
//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvsalesgoals::getSalesGoalsbyDate() as logical
	Obtem uma Lista de salesgoals (Utilizado no PGV Online)
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 19/01/2022
	@return logical, este metodo sempre retorna verdadeiro
/*/
//------------------------------------------------------------------------------
method getSalesGoalsbyDate()  class pgvsalesgoals
	Local aURLFilter:= getFilterParam()                 as array
	Local cFields	:= getQueryParam('FIELDS')          as character
	Local cSort		:= getQueryParam('SORT')            as character
	Local dDataDe   := cTod(getPathParams("datade"))   	as Date
	Local dDataAte  := cTod(getPathParams("dataate"))  	as Date
	Local aItems    := {}                               as Array

	GetPageAndPageSize(@::nPage, @::nPageSize)
	::jResponse:FromJson(::oService:getsalesgoalsbydate(::nPage, ::nPageSize, aURLFilter, cFields, cSort,dDataDe,dDataAte))

	aItems := ::jResponse:GetJsonObject( "items" )

	aItems := IIF( !Empty( aItems ), aItems, {})

	If ( !Empty( aItems ) .And. Len( aItems ) > 0 )
		AnswerRest(::jResponse)
	Else
		SetRestFault(404, FWhttpEncode(STR0001) ) //Nenhum dado encontrado
	EndIf

	aSize(aURLFilter, 0)
	aSize(aItems, 0)
return .T.





