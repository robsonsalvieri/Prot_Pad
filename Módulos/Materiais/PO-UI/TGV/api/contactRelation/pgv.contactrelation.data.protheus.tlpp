#INCLUDE 'tlpp-core.th'
#INCLUDE "FWLIBVERSION.CH"
#INCLUDE "PGV.ContactRelation.DATA.PROTHEUS.CH"

#DEFINE INCLUI 3
#DEFINE ALTERA 4
#DEFINE EXCLUI 5
#DEFINE SOURCE "CRMA060"

namespace totvs.protheus.backoffice.contactrelation
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactRelationProtheusData
	Classe para a consulta dos contactsrelation no Protheus.
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 16/11/2023
/*/
//------------------------------------------------------------------------------
class ContactRelationProtheusData from FWAdapterBaseV2
	public method new() 		        as object
	public method saveContactRelation()	as logical
endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactRelationProtheusData::new() as Object
	obtem uma nova instancia da classe ContactRelationProtheusData
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 16/11/2023
	@return object, nova instancia da classe ContactRelationProtheusData
/*/
//------------------------------------------------------------------------------
method new() as object class ContactRelationProtheusData
	_Super:New( "POST", .T. )
return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} ContactRelationProtheusData::saveContactRelation(jContactRelation as Json, nOperation as Numeric,ccontactreId as Character, ccontactreStore as Character) as Logical
	Efetua a inclusão, altera ou exclusão de um Relação de Contato
	@type		method
	@version	12.1.2310
	@author		Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since		01/12/2023
	@param		jContactRelation	, json		, json do Relação de Contato
	@param		nOperation			, numeric
	@param		cContactReCodEnt	, character	
	@param		cContactReBran		, character	
	@param		cContactReEnt		, character	
	@return		logical, Se obteve sucesso na transação retorna verdadeiro.
/*/
//------------------------------------------------------------------------------
Method saveContactRelation(jContactRelation as Json,nOperation as Numeric,cContactReBran as Character,cContactReBranEnt as Character,cContactReEnt as Character,cContactReCodEnt as Character) as Logical Class ContactRelationProtheusData
	Local 	aArea			 	  := FwGetArea() as Array
	Local 	oContactRelation	  := pgvContactRelationsBase():new() as Object
	Local 	lContinue		      := .T. as Logical
	Local 	nStatusCode	    	  := 201 as Numeric
	Local   cContactRelationError := ""  as Character
	Default	nOperation			  := 3
	
	cContactReBran   := PadR(cContactReBran, getSX3Cache("AC8_FILIAL","X3_TAMANHO"))
	cContactReBranEnt:= PadR(cContactReBranEnt, getSX3Cache("AC8_FILENT","X3_TAMANHO"))
	cContactReEnt    := PadR(cContactReEnt, getSX3Cache("AC8_ENTIDA","X3_TAMANHO"))
    cContactReCodEnt := PadR(cContactReCodEnt, getSX3Cache("AC8_CODENT","X3_TAMANHO"))
	oContactRelation:cSellerId := getSellerFromFilter(FWIsAdmin(), .F.)


	If oContactRelation:setOperation( nOperation )

		If nOperation == EXCLUI
			oContactRelation:cBranch       := cContactReBran
			oContactRelation:cBranchEntity := cContactReBranEnt
			oContactRelation:cEntity       := cContactReEnt
			oContactRelation:cCodeEntity   := cContactReCodEnt
		EndIf

		If  oContactRelation:fromExecAuto(jContactRelation)
			If nOperation <> INCLUI
				nStatusCode := 200
			EndIf
			If oContactRelation:commitData()
				oRest:setStatusCode( nStatusCode )
				oRest:setResponse( getResponseContactRelation( nOperation,cContactReBran,cContactReBranEnt,cContactReEnt,cContactReCodEnt ) )
				NotifySeller(AllTrim(cContactReEnt + "/" + cContactReCodEnt), oContactRelation:cSellerId, nOperation, /*cError*/, /*cExternalId*/, .F., SOURCE)
			Else
				nStatusCode := 400
				
				If oContactRelation:nCodeError == 2	// Registro inexitente na tentativa de exclusão ou alteração do registro
					nStatusCode           := 404
				EndIf

				cContactRelationError := oContactRelation:getDescriptionError()

				NotifySeller(	AllTrim(cContactReEnt + "/" + cContactReCodEnt), ;
								oContactRelation:cSellerId, ;
								nOperation, ;
								IIf(!Empty(cContactRelationError), cContactRelationError, "") + oContactRelation:getErrorMessage(), ;
								/*cExternalId*/, ;
								.F., ;
								SOURCE)
				SetRestFault(nStatusCode, FWHttpEncode(oContactRelation:getErrorMessage()) )
				lContinue := .F.
			Endif
		Else

			cContactRelationError := oContactRelation:getDescriptionError()

			nStatusCode := IIF(nStatusCode == 404, 404, 400)
			NotifySeller(	AllTrim(cContactReEnt + "/" + cContactReCodEnt), ;
							oContactRelation:cSellerId, ;
							nOperation, ;
							IIf(!Empty(cContactRelationError), cContactRelationError, "") + oContactRelation:getErrorMessage(), ;
							/*cExternalId*/, ;
							.F., ;
							SOURCE)
			SetRestFault(nStatusCode, FWHttpEncode(oContactRelation:getErrorMessage()) )
			lContinue := .F.
		Endif
	Endif

    oContactRelation:Destroy()
    FreeObj(oContactRelation)
	FwRestArea( aArea )
	aSize( aArea, 0)
Return lContinue

//------------------------------------------------------------------------------
/*/{Protheus.doc} getResponseContactRelation
	Obtem uma resposta de sucesso de acordo com a operação informada.
	@type		function
	@version	12.1.2310
	@author		Gabriel Olvieira dos Santos
	@Since 		04/12/2023
	@param		nOperation	, numeric, Operação (3, 4 ou 5)
	@param		jContactRelation	, json		, json do Relação de Contato
	@param		cContactReCodEnt	, character	
	@param		cContactReBran		, character	
	@param		cContactReEnt		, character	
	@return 	json, resposta de sucesso da requisição
/*/
//------------------------------------------------------------------------------
static function getResponseContactRelation(nOperation as Numeric,cContactReBran as Character,cContactReBranEnt as Character,cContactReEnt as Character,cContactReCodEnt as Character) as json
	Local jResponse			:= JsonObject():New() as json
	Local jContactRelation	:= JsonObject():New() as json
	Local aContactRelation	:= {} as Array

	Do Case
		Case nOperation == INCLUI
			jResponse['messageSuccess'] := FwHttpEncode(STR0001) //"Relação de Contato incluído com sucesso"
		Case nOperation == ALTERA
			jResponse['messageSuccess'] := FwHttpEncode(STR0002) //"Relação de Contato alterado com sucesso"
		Case nOperation == EXCLUI
			jResponse['messageSuccess'] := FwHttpEncode(STR0003) //"Relação de Contato excluído com sucesso"
	EndCase

	jResponse['status'] := "OK"

	If nOperation <> EXCLUI
		jContactRelation['branch']  	:= allTrim(cContactReBran)
		jContactRelation['branchentity']:= allTrim(cContactReBranEnt)
		jContactRelation['entity']		:= allTrim(cContactReEnt)
		jContactRelation['codeentity'] 	:= allTrim(cContactReCodEnt)
		aAdd(aContactRelation, jContactRelation)
		jResponse['header'] := aClone(aContactRelation)
	Endif

	FreeObj(jContactRelation)
	aSize(aContactRelation, 0)
Return jResponse


