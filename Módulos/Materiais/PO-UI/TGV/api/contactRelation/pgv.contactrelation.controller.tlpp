#include 'tlpp-core.th'
#include 'tlpp-rest.th'

#DEFINE INCLUI 3
#DEFINE ALTERA 4
#DEFINE DELETA 5

namespace totvs.protheus.backoffice.contactrelation
using namespace tgv.util

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelation
	API para integracao de contactsrelation - Utilizada no Portal Gestao de Vendas
	@type class
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 29/11/2023
/*/
//------------------------------------------------------------------------------
class pgvContactRelation
	data jResponse  as json
	data nPage      as numeric
	data nPageSize  as numeric
	data oService   as object

	public method new() as object

	@Post("/api/pgv/ContactRelation/")
	Public Method postContactRelation() as Logical

	@Put("/api/pgv/ContactRelation/")
	Public Method putContactRelation() as Logical

	@Delete("/api/pgv/ContactRelation/:filialEntity/:entity/:codEntity")
	Public Method deleteContactRelation() as Logical

endClass

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelation::New() as Object
	Obtem uma nova instancia da classe pgvContactRelation
	@type method
	@version 12.1.2310
	@author Gabriel Oliveira dos Santos / Squad CRM & Faturamento
	@since 29/11/2023
	@return object, retorna uma nova instancia da classe
/*/
//------------------------------------------------------------------------------
Method new() as object class pgvContactRelation

	::nPage         := 1
	::nPageSize     := 10
	::oService      := ContactRelationservice():getInstance()
	::jResponse     := jsonObject():New()

Return self

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelation::postcontactsrelation() as Logical
	Efetua a inclusão de uma Relação de contato.
	@type		method
	@version	12.1.2310
	@author		Gabriel Olvieira dos Santos / Squad CRM & Faturamento
	@since		01/12/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method postContactRelation() as Logical Class pgvContactRelation
	Local jBody := GetRequestBody() as Json
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If jBody <> Nil
		::oService:saveContactRelation(jBody)
		FreeObj(jBody)
	Endif
	
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelation::putcontactsrelation() as Logical
	Efetua a alteração de uma Relação de contato.
	@type		method
	@version	12.1.2310
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@since		12/12/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method putContactRelation() as Logical Class pgvContactRelation
	Local jBody       := GetRequestBody() as Json
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	If jBody <> Nil
		::oService:saveContactRelation(jBody, ALTERA)
		FreeObj(jBody)
	Endif
Return .T.

//------------------------------------------------------------------------------
/*/{Protheus.doc} pgvContactRelation::deleteContactRelation() as Logical
	Efetua a exclusão de uma Relação de contato.
	@type		method
	@version	12.1.2310
	@author		Rafael Mota Previdi / Squad CRM & Faturamento
	@since		12/12/2023
	@return		logical
/*/
//------------------------------------------------------------------------------
Method deleteContactRelation() as Logical Class pgvContactRelation
	Local cFilEntity  := GetPathParams("filialEntity")
	Local cEntity     := GetPathParams("entity")
	Local cCodCont    := GetPathParams("codEntity")
	
	oRest:setKeyHeaderResponse('Content-Type','application/json; charset=utf-8')
	::oService:saveContactRelation(/*jBody*/, DELETA, cFilEntity, cEntity, cCodCont)

Return .T.
