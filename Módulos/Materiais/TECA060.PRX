#Include 'Protheus.ch'
#INCLUDE "TECA060.CH"
#INCLUDE 'FWMVCDEF.CH'

/*/


Ŀ
Program   TECA060    Autor  Vendas e CRM           Data  27/02/12 
Ĵ
Descricao  Programa de Manutencao do Cadastro de Obsolescencia        
Ĵ
Retorno    Nenhum                                                     
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function TECA060()

Local oBrowse
Local cCadastro:= STR0001	//"Cadastro de Obsolescencia"
Local cRotDel 	:= "AT060VlDel()" // Rotina para delecao
Local cTudOk  	:= "At060SkFab()"

oBrowse := FWMBrowse():New()
oBrowse:SetAlias('AAK')
oBrowse:SetDescription(STR0001) // Cadastro de Obsolencia
oBrowse:DisableDetails()
oBrowse:Activate()

Return

/*/


Ŀ
Funcao    MenuDef    Autor  Vendas CRM		       Data  27/02/2012
Ĵ
Descricao Funcao de definicao do aRotina                               
Ĵ
Sintaxe   aRotina   retorna a array com lista de aRotina               
Ĵ
Uso       TECA060                                                      
Ĵ


/*/

Static Function MenuDef()

Return FWMVCMenu( 'TECA060' )

/*


ͻ
Programa  ModelDef   Autor  Vendas CRM          Data  27/02/2012  
͹
Desc.     Define o modelo de dados (MVC)                              
͹
Uso       TECA060                                                     
ͼ


*/
 
Static Function ModelDef()
Local oModel
Local oStruAAK		:= FWFormStruct(1,'AAK',/*bAvalCampo*/,/*lViewUsado*/)
Local bCommit			:= {|oMdl|At060DelMV(oMdl)}		//Funcao a ser executada no momento da exclusao
Local bPosValidacao	:= {|oModel|At060PosVl(oModel)}    //Substitui a funcao TudoOK

oModel := MPFormModel():New('TECA060',/*bPreValidacao*/,bPosValidacao,bCommit,/*bCancel*/)
oModel:AddFields('AAKMASTER',/*cOwner*/,oStruAAK,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)
oModel:SetDescription(STR0001)

Return(oModel)

/*


ͻ
Programa  ViewDef    Autor  Vendas CRM          Data   27/02/2012 
͹
Desc.     Define a interface para cadastro em MVC.                    
                                                                      
͹
Uso       TECA060                                                     
ͼ


*/

Static Function ViewDef()

Local oView
Local oModel   := FWLoadModel('TECA060')
Local oStruAAK := FWFormStruct(2,'AAK')

oView := FWFormView():New()
oView:SetModel(oModel)
oView:AddField('VIEW_AAK', oStruAAK,'AAKMASTER')
oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_AAK','TELA')

Return(oView)


/*/


Ŀ
Funcao    At060VlDel Autor  Eduardo Riera          Data  05.11.98 
Ĵ
Descricao  Validacao da Exclusao do Produto em Obsolescencia          
Ĵ
Retorno    ExpL1: Logico                                              
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At060VlDel()

Local lRetorno := .T.

//Ŀ
//Verifica nos Itens em Obsolescencia                                     
//
dbSelectArea("AAL")
dbSetOrder(2)
If ( dbSeek(xFilial("AAL")+AAK->AAK_CODFAB+AAK->AAK_LOJAFA+AAK->AAK_CODPRO+AAK->AAK_CODCOM) )
	lRetorno := .F.
	Help(" ",1,"AT060DEL01")
EndIf
Return(lRetorno)


/*/


Ŀ
Funcao    At060SkFab Autor  Kleber Dias Gomes      Data  21/01/04 
Ĵ
Descricao  Busca Fabricante                                           
Ĵ
Sintaxe    Void At040SkFab()                                          
Ĵ
 Uso       TECA060                                                    
ٱ


/*/
Function At060SkFab()

Local lRet    := .T.

If (Empty(M->AAK_CODFAB) .And. !Empty(M->AAK_LOJAFA)) .Or.;
	(!Empty(M->AAK_CODFAB) .And. Empty(M->AAK_LOJAFA))
	Help(" ",1,"OBRIGAT")	
	lRet := .F.
EndIf      

lRet := ExistChav("AAK",M->AAK_CODFAB+M->AAK_LOJAFA+M->AAK_CODPRO+M->AAK_CODCOM)

Return(lRet)

/*/


Ŀ
Funcao    At060DelMV Autor  Vendas e CRM           Data  27/02/12 
Ĵ
Descricao  Validacao da Exclusao do Produto em Obsolescencia          
Ĵ
Retorno    ExpL1: Logico                                              
Ĵ
Parametros Nenhum                                                     
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/

Function At060DelMV(oMdl)

Local nOpc := oMdl:GetOperation() 
Local lRetorno := .T.

FWFormCommit( oMdl )

If nOpc == MODEL_OPERATION_DELETE 	
	//Ŀ
	//Verifica nos Itens em Obsolescencia                                     
	//
	dbSelectArea("AAL")
	dbSetOrder(2)
	If ( dbSeek(xFilial("AAL")+AAK->AAK_CODFAB+AAK->AAK_LOJAFA+AAK->AAK_CODPRO+AAK->AAK_CODCOM) )
		lRetorno := .F.
		Help(" ",1,"AT060DEL01")
	EndIf
EndIf

Return(lRetorno)


/*/


Ŀ
Funcao    At060PosVl Autor Vendas e CRM            Data  21/01/04 
Ĵ
Descricao  Busca Fabricante                                           
Ĵ
Sintaxe    Void At060PosVl()                                          
Ĵ
 Uso       TECA060                                                    
ٱ


/*/
Function At060PosVl(oModel)

Local lRet    := .T.
Local oModelAAK := oModel:GetModel( 'AAKMASTER' )
Local nOpc	:= oModel:GetOperation()

If nOpc == MODEL_OPERATION_INSERT .OR. nOpc == MODEL_OPERATION_UPDATE 				
	If (Empty (oModelAAK:GetValue("AAK_CODFAB")) .AND. !Empty(oModelAAK:GetValue("AAK_LOJAFA"))) .Or.;
		(!Empty(oModelAAK:GetValue("AAK_CODFAB")) .AND. Empty(oModelAAK:GetValue("AAK_LOJAFA")))
		Help(" ",1,"OBRIGAT")	
		lRet := .F.
	EndIf   
EndIf

lRet := ExistChav("AAK",oModelAAK:GetValue("AAK_CODFAB")+oModelAAK:GetValue("AAK_LOJAFA")+oModelAAK:GetValue("AAK_CODPRO")+oModelAAK:GetValue("AAK_CODCOM"))

Return (lRet)