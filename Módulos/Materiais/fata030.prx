#INCLUDE "PROTHEUS.CH"
#INCLUDE "FATA030.CH" 
#INCLUDE 'FWMVCDEF.CH'

PUBLISH MODEL REST NAME FATA030 source FATA030

/*/


Ŀ
Program   FATA030    Autor Eduardo Riera           Data 11.01.2000	
Ĵ
Descrio Cadastro de Parceiros.                                     	
Ĵ
Retorno   Nenhum                                                      	
Ĵ
ParametrosNenhum                                                      	
Ĵ
 ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     	
Ĵ
 PROGRAMADOR   DATA    BOPS   MOTIVO DA ALTERACAO                   	
Ĵ
Armando Tess  21/05/0215933 Validacao na exclusao do contato com movi 
                            mentacao no telemarketing.              	
ٱ


/*/
Function Fata030()

Local oBrowse := Nil 
Local oTableAtt := TableAttDef()

Private cCadastro := STR0001   
PRIVATE aRotina 	:= MenuDef()

oBrowse := FWMBrowse():New()
oBrowse:SetAlias('AC4')
oBrowse:SetDescription(STR0001)//"Parceiros" - "Contatos" 

oBrowse:SetAttach( .T. ) 				  //Habilita as vises do Browse

If oTableAtt <> Nil
	oBrowse:SetViewsDefault( oTableAtt:aViews ) 
	oBrowse:SetChartsDefault( oTableAtt:aCharts ) //Passa o grfico para o Browse
	
	oBrowse:SetIDChartDefault( "PorEst" )
EndIf

	
//Se no for SIGACRM inibe a exibio do grfico
If nModulo <> 73
	oBrowse:SetOpenChart( .F. )
EndIf

oBrowse:SetTotalDefault('AC4_FILIAL','COUNT',STR0022) // 'Total de Registros'
oBrowse:Activate()

Return(.T.)


//------------------------------------------------------------------------------
/*/	{Protheus.doc} TableAttDef

Cria as vises e grficos.

@sample	TableAttDef()

@param		Nenhum

@return	ExpO - Objetos com as Visoes e Grficos.

@author	Cristiane Nishizaka
@since		28/04/2014
@version	12
/*/
//------------------------------------------------------------------------------
Static Function TableAttDef()

Local oTableAtt 	:= FWTableAtt():New()
// Vises  
Local oAtivos		:= Nil // Parceiros Ativos
Local oInativos	:= Nil // Parceiros Inativos
// Grficos
Local oPorEst		:= Nil // Colunas: Parceiros Por Estado

oTableAtt:SetAlias("AC4")

//----------
// Vises
//---------- 
	
// Parceiros Ativos
oAtivos := FWDSView():New()
oAtivos:SetName(STR0019) // "Parceiros Ativos"
oAtivos:SetID("Ativos") 
oAtivos:SetOrder(1) // AC4_FILIAL+AC4_PARTNE 
oAtivos:SetCollumns({"AC4_PARTNE", "AC4_NOME", "AC4_NREDUZ", "AC4_END", "AC4_MUN",;
						"AC4_BAIRRO", "AC4_EST", "AC4_DDD", "AC4_TEL", "AC4_CONTAT", "AC4_HPAGE"})
oAtivos:SetPublic( .T. )
oAtivos:AddFilter(STR0019, "AC4_MSBLQL == '2'") // "Parceiros Ativos"

oTableAtt:AddView(oAtivos)

// Parceiros Inativos
oInativos := FWDSView():New()
oInativos:SetName(STR0020) // "Parceiros Inativos"
oInativos:SetID("Inativos") 
oInativos:SetOrder(1) // AC4_FILIAL+AC4_PARTNE
oInativos:SetCollumns({"AC4_PARTNE", "AC4_NOME", "AC4_NREDUZ", "AC4_END", "AC4_MUN",;
						"AC4_BAIRRO", "AC4_EST", "AC4_DDD", "AC4_TEL", "AC4_CONTAT", "AC4_HPAGE"})
oInativos:SetPublic( .T. )
oInativos:AddFilter(STR0020, "AC4_MSBLQL == '1'") // "Parceiros Inativos"

oTableAtt:AddView(oInativos)

//------------
// Grficos
//------------
	
// Colunas: Parceiros Por Estado
oPorEst := FWDSChart():New()
oPorEst:SetName(STR0021) // "Parceiros Por Estado"
oPorEst:setTitle(STR0021) // "Parceiros Por Estado"
oPorEst:SetID("PorEst") 
oPorEst:SetType("BARCOMPCHART")
oPorEst:SetSeries({ {"AC4", "AC4_PARTNE", "COUNT"} })
oPorEst:SetCategory( { {"AC4", "AC4_EST"} } )
oPorEst:SetPublic( .T. )
oPorEst:SetLegend( CONTROL_ALIGN_BOTTOM ) //Inferior
oPorEst:SetTitleAlign( CONTROL_ALIGN_CENTER ) 			

oTableAtt:AddChart(oPorEst)

Return (oTableAtt)

/*


ͻ
Programa  ModelDef  Autor  Vendas CRM           Data   17/09/10   
͹
Desc.     Define o modelo de dados do (MVC)                           
͹
Uso       FATA030                                                     
ͼ


*/
Static Function ModelDef()

Local oModel
Local oStruAC4 := FWFormStruct(1,'AC4', /*bAvalCampo*/,/*lViewUsado*/ )

Local bCommit		:= {|oMdl|FATA030Cmt(oMdl)}		//Gravacao dos dados
Local bPosValid		:= {|oMdl|FATA030Pos(oMdl)}		//Pos validacao dos dados

oModel := MPFormModel():New('FATA030', /*bPreValidacao*/, bPosValid, bCommit, /*bCancel*/)
oModel:AddFields('AC4MASTER',/*cOwner*/,oStruAC4, /*bPreValidacao*/, /*bPosValidacao*/, /*bCarga*/ )
oModel:SetDescription(STR0001)

Return oModel

/*


ͻ
Programa  ViewDef   Autor  Vendas CRM           Data   17/09/10   
͹
Desc.     Define a interface para cadastro de Prceiros em MVC.        
                                                                      
͹
Uso       FATA030                                                     
ͼ


*/
Static Function ViewDef()   

Local oView  
Local oModel   := FWLoadModel('FATA030')
Local oStruAC4 := FWFormStruct( 2,'AC4')  

oView := FWFormView():New()
oView:SetContinuousForm()
oView:SetModel(oModel)
oView:AddField('VIEW_AC4',oStruAC4,'AC4MASTER')

oView := CRMXAddAct("AC4",oView) //Adcionar Rotinas no 'Aes relacionadas' do Formulrio 

oView:CreateHorizontalBox('TELA',100)
oView:SetOwnerView('VIEW_AC4','TELA') 
  
Return oView

/*/


Ŀ
Funo    |MenuDef    Autor  Fernando Amorim        Data 08/12/06  
Ĵ
Descrio  Funcao de definio do aRotina                             
Ĵ
Retorno    aRotina   retorna a array com lista de aRotina             
Ĵ
Uso        SIGAFAT                                                    
ٱ


/*/
Static Function MenuDef()    

Local nPos        := 0 
Local aRotina     := {}
Local aEntRelac   := {}
Local aAtiv       := {}
Local aAnotac     := {}


ADD OPTION aRotina TITLE STR0005 ACTION 'PesqBrw' 			OPERATION 1	ACCESS 0
ADD OPTION aRotina TITLE STR0006 ACTION 'VIEWDEF.FATA030'	OPERATION 2	ACCESS 0
ADD OPTION aRotina TITLE STR0007 ACTION 'VIEWDEF.FATA030'	OPERATION 3	ACCESS 0
ADD OPTION aRotina TITLE STR0008 ACTION 'VIEWDEF.FATA030'	OPERATION 4	ACCESS 0
ADD OPTION aRotina TITLE STR0009 ACTION 'VIEWDEF.FATA030'	OPERATION 5	ACCESS 0

ADD OPTION aEntRelac  TITLE STR0002  ACTION "FtContato('AC4', AC4->(Recno()), 4)" OPERATION  4 ACCESS 0 //"Contato"

If !Empty( Select( "AC9" ) )
	ADD OPTION aEntRelac  TITLE STR0003  ACTION "MsDocument('AC4',AC4->(Recno()),4)" OPERATION  9 ACCESS 0 //"Conhecimento"
End if 

If nModulo == 73

	aEntRelac := CRMXINCROT("AC4",aEntRelac)
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA190Con()",Nil) })
	If nPos > 0 
		ADD OPTION aRotina TITLE aEntRelac[nPos][1] ACTION aEntRelac[nPos][2] OPERATION 8  ACCESS 0//"Conectar"
		Adel(aEntRelac,nPos)
		Asize(aEntRelac,Len(aEntRelac)-1)
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x|  IIF(ValType(x[2]) == "C", x[2] == "CRMA180()", Nil) })
	If nPos > 0
		ADD OPTION aAtiv   TITLE STR0017 ACTION "CRMA180(,,,3,,)" OPERATION 3  ACCESS 0 //"Nova Atividade" 
		ADD OPTION aAtiv   TITLE STR0018 ACTION "CRMA180()" OPERATION 8  ACCESS 0 //"Todas as ATividades"
		aEntRelac[nPos][2] := aAtiv
	EndIf
	
	nPos := ASCAN(aEntRelac, { |x| IIF(ValType(x[2]) == "C", x[2] == "CRMA090()", Nil)})
	If nPos > 0
		ADD OPTION aAnotac   TITLE STR0015 ACTION "CRMA090(3)" OPERATION 3  ACCESS 0 //"Nova Anotao"
		ADD OPTION aAnotac   TITLE STR0016 ACTION "CRMA090()" OPERATION 8  ACCESS 0 //"Todas as Anotaes" 
		aEntRelac[nPos][2] := aAnotac
	EndIf
	
EndIf

Asort(aEntRelac,,,{ | x,y | y[1] > x[1] } )
ADD OPTION aRotina TITLE STR0013 ACTION aEntRelac OPERATION 8  ACCESS 0//"Relacionadas"

Return(aRotina)

/*


ͻ
Programa  FATA030CmtAutor  Vendas CRM           Data   21/09/10   
͹
Desc.     Bloco executado na gravacao dos dados do formulario, substi-
          tuindo a gravacao padrao do MVC.                            
͹
Uso       FATA030                                                     
ͼ


*/
Static Function FATA030Cmt(oMdl)

Local aArea			:= GetArea()
Local nOperation	:= oMdl:GetOperation()

If nOperation == 5  
	Ft030DEL() 
End if

FWModelActive( oMdl )
FWFormCommit( oMdl )
 
RestArea( aArea )

Return (.T.)

/*/


Ŀ
Funcao    Ft030Del   Autor Eduardo Riera           Data 11.01.2000
Ĵ
Descrio Funcao de Tratamento da Exclusao dos Parceiros              
Ĵ
Retorno   Nenhum                                                      
Ĵ
ParametrosNenhum                                                      
                                                                      
Ĵ
   DATA    Programador   Manutencao Efetuada                         
Ĵ
                                                                     
ٱ


/*/
Function Ft030DEL()

Local lExclui       := .T.  
Local nBusca
Local nRecnoSM0     := SM0->(RecNo())
Local aSM0CodFil    := {}


//Ŀ
// Exclui a amarracao de Contatos                                         
//
FtContato( Alias(), RecNo(), 2, , 3 )     

//Ŀ
// Exclui a amarracao de             
//
If !Empty( Select( "AC9" ) ) 
	MsDocument( Alias(), Recno(), 2, , 3 ) 
EndIf 	

//--------------------------
// Exclusao de Anotaes   
//--------------------------
If nModulo == 73 
	CRMA090Del( "AC4", AC4->( Recno() ) )
EndIf	

// Preenche um array com as filiais
DBSelectArea("SM0")
DBGoTop()
Do While ! Eof()
	If SM0->M0_CODIGO == cEmpAnt
		Aadd(aSM0CodFil, FWGETCODFILIAL )
	Endif
	DBSkip()
EndDo
DBGoTo(nRecnoSM0)

//Ŀ
// Verifica se existe algum movimento desta entidade no telemarketing 
//
DBSelectArea("SUC")
DBSetOrder(2)
aFiliais := If(FWModeAccess("SUC",3) == "E" .and. FWModeAccess("AC4",3) == "C", aClone(aSM0CodFil), {xFilial()})
For nBusca := 1 to Len(aFiliais)
   	If DBSeek(aFiliais[nBusca]+'AC4'+AC4->AC4_PARTNE)
   	   MsgStop(OemToAnsi(STR0004)) // 'Este parceiro possui movimento de telemarketing e nao podera ser excluido'
   	   lExclui := .F.
    EndIf
Next
DBSelectArea("AC4")

Return(lExclui)

//-------------------------------------------------------------------
/*/ {Protheus.doc} Fata030Pos
	
Funcao que realiza a pos validacao do modelo de dados.
		 
@sample	Fata030Pos(oMdl)
			
@param		oModel	Objeto do modelo referente a entidade de Parceiros
@return     lValido Indica se o modelo esta valido ou nao para persistencia

@author     Rafael Mota Previdi
@since		25/06/2019
@version	P12 
/*/
//-------------------------------------------------------------------
Function Fata030Pos(oModel)

	Local lValido       := .T.
	Local lPodeApagar   := .F.
	Local oMdl          := Nil
	Local nOperation    := 0

	oMdl       := FwModelActive()
	nOperation := oMdl:GetOperation()

	If (nOperation == MODEL_OPERATION_DELETE)
		// Verifica se existe Atendimento no Teleatendimento ADE
		If FindFunction ("Tk510TAxEn")
			lPodeApagar := Tk510TAxEn("AC4", AC4->AC4_FILIAL, AC4->AC4_PARTNE)
			If !lPodeApagar
				lValido := .F.
			EndIf
		EndIf
	EndIf

Return lValido
