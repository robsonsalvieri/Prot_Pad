#include "protheus.ch"
#include "fwmvcdef.ch"
#include "BACKOFFICE.FAT.DOCUMENTO.TPINVXSERIE.CH"

Static __lNewInvo	:= Nil
Static __RpoRelease := GetRPORelease()

//-----------------------------------------------------------------------------------------------------
//- esse parâmetro não deve ser documentado, pois 
//- é para a situação de uso em projetos pilotos
//-----------------------------------------------------------------------------------------------------
Static lNumInvo := SuperGetMV('MV_NUMINVO',.F.,.F.)

Namespace backoffice.fat.documento	

//------------------------------------------------------------------------------
//-  TpInvXSerie()
//- 	Função de cadastro de Espécies x Séries  
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//------------------------------------------------------------------------------
Function TpInvXSerie()

Local oBrowse as object

oBrowse := FWLoadBrw("backoffice.fat.documento.TpInvXSerie")
oBrowse:Activate()
oBrowse:DeActivate()
oBrowse:Destroy()
FreeObj(oBrowse)
oBrowse := nil

Return nil

//------------------------------------------------------------------------------
//-  BrowseDef()
//- 	Retorna o browse da rotina 
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@return		oBrowse Objeto do tipo FWMBrowse
//------------------------------------------------------------------------------
Function BrowseDef() as object
Local oBrowse as object

oBrowse := FWMBrowse():New()
oBrowse:SetAlias("AZZ")
oBrowse:SetDescription(STR0001) //Manutenção de Tipos de Documentos e Séries

Return oBrowse

//------------------------------------------------------------------------------
//-  ModelDef()
//- 	Retorna o modelo de dados para o cadastro de séreis, alias AZZ
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@return		oModel Objeto do tipo FwFormModel
//------------------------------------------------------------------------------
Function ModelDef() as object

Local oModel as object
Local oStruct as object

oModel := MPFormModel():New( "MODEL_AZZ", /*bPre*/ , {|oModel| TypeInvTOk()}, /*bCommit*/, /*bCancel*/ )
oStruct := FwFormStruct( 1, "AZZ", /*bFiltro*/ )
oModel:AddFields( "FIELDS_AZZ", /*Owner*/ , oStruct, /*bPre*/ , /*bPos*/ , /*bLoad*/ )
oModel:SetPrimaryKey( { 'AZZ_ESPECI','AZZ_SERIE'} )

Return oModel

//------------------------------------------------------------------------------
//-  ViewDef()
//- 	Retorna a view para o cadastro de séries, alias AZZ
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@return		oView Objeto do tipo FwFormView
//------------------------------------------------------------------------------
Function ViewDef() as object
local oView as object
local oStruct as object
local oModel as object

oView := FwFormView():New()
oModel := FwLoadModel("backoffice.fat.documento.TpInvXSerie")
oStruct := FwFormStruct( 2, "AZZ", /*bFiltro*/ )

oView:SetModel(oModel)
oView:AddField( "VIEW_AZZ", oStruct, "FIELDS_AZZ" )

return oView

//------------------------------------------------------------------------------
//-  MenuDef()
//- 	Retorna o array com a opções presentes na rotina
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@return		aRotina Array contendo as opções da rotina
//------------------------------------------------------------------------------
Function MenuDef() as array

Local aRotina := {} as array

ADD OPTION aRotina TITLE STR0002 	ACTION "PesqBrw" 							                    OPERATION 1							ACCESS 0 //"Pesquisar"
ADD OPTION aRotina TITLE STR0003 	ACTION "VIEWDEF.backoffice.fat.documento.TypeInvoice"	OPERATION MODEL_OPERATION_VIEW		ACCESS 0 //'Visualizar'
ADD OPTION aRotina TITLE STR0004 	ACTION "VIEWDEF.backoffice.fat.documento.TypeInvoice"	OPERATION MODEL_OPERATION_INSERT	ACCESS 0 //'Incluir'
ADD OPTION aRotina TITLE STR0005 	ACTION "VIEWDEF.backoffice.fat.documento.TypeInvoice"	OPERATION MODEL_OPERATION_UPDATE	ACCESS 0 //'Alterar'
ADD OPTION aRotina TITLE STR0006 	ACTION "VIEWDEF.backoffice.fat.documento.TypeInvoice"	OPERATION MODEL_OPERATION_DELETE	ACCESS 0 //'Excluir'

Return aRotina

//------------------------------------------------------------------------------
//-  TypeInvTOk()
//- 	Validação TUDOOK na gravação do registro
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@oMdlAZZ	Modelo a tabela AZZ
//-		@return		lRet Lógico indicando a validade da gravação
//------------------------------------------------------------------------------
Static Function TypeInvTOk(oMdlAZZ as Object) as logical

Local lRet As Logical

Default oMdlAZZ := Nil

lRet	:= .T.
If INCLUI
    DbSelectArea("AZZ")
    AZZ->(DbSetOrder(3)) //AZZ_FILIAL+AZZ_DEFAUL+AZZ_SERIE+AZZ_PDV
    If AZZ->(DbSeek(FwXFilial("AZZ")+'1'+FwFldGet("AZZ_SERIE"))) .Or. AZZ->(DbSeek(FwXFilial("AZZ")+'2'+FwFldGet("AZZ_SERIE")))
        Help(" ",1,STR0007,,STR0008,1,0) // Não é possível cadastrar a uma série já existente.
        lRet := .F. 
    EndIf
EndIf

Return lRet

//------------------------------------------------------------------------------
//-  TypeInvVal()
//- 	Valid do campo AZZ_ESPECI
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@cType		Caracter indicando a espécie selecionada
//-		@return		lRet Lógico indicando se a espécie existe na RetTypeDoc()
//------------------------------------------------------------------------------
Function TypeInvVal(cType as character) as logical

Local lRet      As Logical
Local cEspecies As Character

Default cType := ""

lRet        := .F.
cEspecies   := backoffice.fiscal.escrita.documentos.RetTypeDoc(,4)

If Alltrim(cType) $ cEspecies
    lRet := .T.
EndIf

Return lRet
                                                        
//------------------------------------------------------------------------------
//-  UsaNewInvoice()
//- 	Função validadora do ambiente para o uso do novo 
//-		controle de numeração de notas
//- 	@type 		Function
//- 	@author		William Pianheri
//- 	@since 		20/12/2024
//-		@return		__lNewInvo Variável logica e estatica indicando 
//-					se o ambiente esta devidamente configurado
//------------------------------------------------------------------------------
Function UsaNewInvoice() as logical

If __lNewInvo == Nil
	
	If (__RpoRelease == '12.1.2510' .And. lNumInvo) .Or. __RpoRelease > '12.1.2510'
	
		If !(AliasIndic('AZZ') .And. ; //- Verifica a existência das tabelas, campos e fontes necessarios
			SD9->(ColumnPos('D9_KEYNF')) > 0 .And. ;
			SD9->(ColumnPos('D9_STATUS')) > 0 .And. ;
			SD9->(ColumnPos('D9_MODELO')) > 0) .And. ;
			!tlpp.ffunc("backoffice.fiscal.escrita.documentos.RetTypeDoc") .And. ;
			!tlpp.ffunc("protheus.backoffice.fiscal.NewSerieNFd") .And. ;
			!tlpp.ffunc("backoffice.fat.documento.NumberInvoice")
			
			UserException(STR0009 + " " + STR0010) //"O uso do novo controle de numeração para documentos não está devidamente aplicado. Verifique se esta totalmente atualizado conforme documentação."
		
		EndIf
	
		__lNewInvo := .T.

	Else
	
		__lNewInvo := .F.
	
	EndIf
EndIf

Return(__lNewInvo)
