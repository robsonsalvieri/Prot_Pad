#include "msobject.ch"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tecsv005.ch"

Static lMV_PAR11 := .T.

namespace custom.fichaPresenca

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATEC", tables="AA1,ABB,ABN,ABQ,ABR,ABS,SRA,TFF,TGY,TGZ", name="Relatório de Ficha de Presença - GS", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc} FichaPresencaReportBusinessObject
Classe para geração do relatório de Ficha de Presenca - GS

@author Julia
@since 27/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
class FichaPresencaReportBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDisplayName() as character
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data lExistPergunte as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new 
Método de inicializacao da classe

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class FichaPresencaReportBusinessObject
_Super:new()

self:appendArea(STR0001)//"Gestão de serviços"  

// Campos que serão retornados e exibidos
self:aFields := {"AA1_CDFUNC", "AA1_NOMTEC", "AA1_CODTEC"}

self:lExistPergunte := self:setPergunte("TECR920")

if !self:lExistPergunte
     self:setErrorStatus(400, STR0002, STR0003) //"Pergunte não encontrado." ## "Grupo de perguntas TECR920 não cadastrado na base. Contate o administrador do sistema." 
endif

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Retorna o nome de exibição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDisplayName() as character class FichaPresencaReportBusinessObject
    return STR0004 //"Ficha de Presença"

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Retorna a descrição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class FichaPresencaReportBusinessObject
return STR0005 //"Relatório de Ficha de Presença na Mesa Operacional"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Metodo que retorna as dados do objeto de negócio

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class FichaPresencaReportBusinessObject

    local jItems     as json
    local nX         as numeric
    local cId        as character
    local cRealName  as character
    local jParams    as json
	Local oTemptable as object //Tabela temporária Afastamentos QRY2
	Local oTemptabl2 as object //Tabela temporária Atendentes QRY

    If !self:lExistPergunte
        return self:oData
    Else
        //Método para retorno do json dos parâmetros:
        jParams := oFilter:getParameters()
        MV_PAR01 := TecConvData(jParams["MV_PAR01"][1])
        MV_PAR02 := TecConvData(jParams["MV_PAR02"][1])
        MV_PAR03 := jParams["MV_PAR03"][1]
        MV_PAR04 := jParams["MV_PAR04"][1]
        MV_PAR05 := jParams["MV_PAR05"][1]
        MV_PAR06 := jParams["MV_PAR06"][1]
        MV_PAR07 := jParams["MV_PAR07"][1]
        MV_PAR08 := jParams["MV_PAR08"][1]
        MV_PAR09 := jParams["MV_PAR09"][1]
        MV_PAR10 := jParams["MV_PAR10"][1]
        MV_PAR11 := jParams["MV_PAR11"][1]

        lMV_PAR11 := Iif(Valtype(MV_PAR11) == "N", MV_PAR11 == 1, MV_PAR11 == "1")

        // Aplicação de filtros do SmartView 
        If oFilter:hasFilter()
            // oStatement:SetUnsafe(nOrder++, oFilter:getSQLExpression())
            cFilter := oFilter:getSQLExpression()
        EndIf

        // Criar Tabelas Temporarias
        At920CriaTab(@oTemptable, @oTemptabl2)

        //Pega os campos da estrutura:
        aAllFields := self:getStructFields()
        QRY3 := "QRY3"
        While !(QRY3)->(Eof())
            jItems := JsonObject():new()

            For nx := 1 To Len(aAllFields)
                cId := aAllFields[nX]:getName()
                cRealName := aAllFields[nX]:getRealName()
                jItems[cId] := (QRY3)->&(cRealName)
            Next nX

            FormatNestedJson(@jItems, (QRY3)->ATEND)

            self:appendData(jItems)

            (QRY3)->(DBSkip())
        EndDo

        self:setHasNext(!(QRY3)->(Eof())) 

        (QRY3)->(DBCloseArea())
        // oStatement:Destroy()
        // oStatement := nil

		// Excluir tabelas temporarias
		If Valtype(oTempTable) <> NIL
			oTempTable:Delete()
			FreeObj(oTempTable)
		EndIf
		If Valtype(oTempTabl2) <> NIL
			oTempTabl2:Delete()
			FreeObj(oTempTabl2)
		EndIf
    EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class FichaPresencaReportBusinessObject
Local aFldDiaSemana as array

	// self:aliasToSchema("AA1", self:aFields)
self:addProperty("FILIAL", "Filial do Atendente", "string", "Filial Atend.", "FILIAL")
self:addProperty("ATEND", "Codigo do Atendente", "string", "Cod. Atend.", "ATEND")
self:addProperty("AA1_CDFUNC", "Codigo do Funcionario", "string", "Cod. Func.", "AA1_CDFUNC")
self:addProperty("AA1_NOMTEC", "Nome Atendente", "string", "Nome Atend.", "AA1_NOMTEC")
self:addProperty("AA1_FUNFIL", "Filial do Funcionario", "string", "Filial Func.", "AA1_FUNFIL")
self:addProperty("RA_DEMISSA", "Data Demissão", "string", "Data Demissão", "RA_DEMISSA")

aFldDiaSemana := Array(0)
//Posições do array = 1-Id         | 2-Description           | 3-Type  | 4-DisplayName
aAdd(aFldDiaSemana, {"TDV_DTREF"   , "Data Ref"     , "date"  , "Data Ref"     })
aAdd(aFldDiaSemana, {"DIASEM"      , "Dia Semana"   , "string", "Dia Semana"   })
aAdd(aFldDiaSemana, {"ABB_HRINI"   , "Hora Inicial" , "string", "Hora Inicial" })
aAdd(aFldDiaSemana, {"ABB_HRFIM"   , "Hora Final"   , "string", "Hora Final"   })
aAdd(aFldDiaSemana, {"ABS_LOCAL"   , "Codigo"       , "string", "Codigo"       })
aAdd(aFldDiaSemana, {"ABS_DESCRI"  , "Descrição"    , "string", "Descrição"    })
aAdd(aFldDiaSemana, {"ABN_DESC"    , "Manutenção"   , "string", "Manutenção"   })
aAdd(aFldDiaSemana, {"HORASTRAB"   , "Total Manut." , "number", "Total Manut." })
aAdd(aFldDiaSemana, {"DIATRAB"     , "Total"        , "number", "Total"        })

self:addNestedProperty("DiaSemanaDetalhes", "DiaSemanaDetalhes", "DiaSemanaDetalhes",, aFldDiaSemana)

Return self:oSchema

Static Function FormatNestedJson(jItems as json, cAtend as character)

Local cCodTecAnt as character
Local dDiaAnt    as character
Local nHrTrab    as numeric
Local QRY        as character

    cCodTecAnt := ""
    dDiaAnt := SToD("")
	jItems["DiaSemanaDetalhes"] := {}
	QRY := "QRY"
    (QRY)->(DbGotop())
	While !(QRY)->(Eof())
        If (QRY)->ABB_CODTEC == cAtend
            nHrTrab := SubtHoras(QRY->ABB_DTINI,QRY->ABB_HRINI,QRY->ABB_DTFIM,QRY->ABB_HRFIM)
            aAdd(jItems["DiaSemanaDetalhes"], { "ABB_FILIAL": Iif(lMV_PAR11,QRY->ABB_FILIAL,""),;
                                                "TDV_DTREF": totvs.framework.treports.date.stringToTimeStamp(dToS(QRY->TDV_DTREF)),;
                                                "DIASEM": TECCdow(Dow(QRY->TDV_DTREF)),;
                                                "ABB_HRINI": QRY->ABB_HRINI,;
                                                "ABB_HRFIM": QRY->ABB_HRFIM,;
                                                "ABS_LOCAL": QRY->ABS_LOCAL,;
                                                "ABS_DESCRI": QRY->ABS_DESCRI,;
                                                "ABN_DESC": QRY->ABN_DESC,;
                                                "HORASTRAB": nHrTrab,;
                                                "ABR_TEMPO": Iif(QRY->ABN_TIPO $ '01#05',IntToHora(nHrTrab), QRY->ABR_TEMPO ),;
                                                "DIATRAB": At920DiaTr(@cCodTecAnt, @dDiaAnt, QRY->TDV_DTREF, QRY->ABB_CODTEC, QRY->ABN_TIPO)})
        EndIf
        (QRY)->(DBSkip())
	EndDo

Return

//-------------------------------------------------------------------------------------
/*/{Protheus.doc} At920DiaTr
Retorna se o dia é trabalhado ou não
@author serviços
@since 15/05/2019
@version P12.1.25
@param 	cAlias - Tabela temporária aonde serão inseridos os afastamentos
		cCodTecAnt - Atendente Processado Anteriormente
		dDataAnt - Data Anterior
		dData - Data Atual
		cCodTec - Atendente Atual
		cTipoDia - Tipo da Manutenção do dia
@return  nDia - 0 - Dia já computado como trabalhado, 1 - Dia não computado como trabalhado
/*/
//-------------------------------------------------------------------------------------

Static Function At920DiaTr(cCodTecAnt, dDataAnt, dData, cCodTec, ;
							cTipoDia)
Local nDia := 0 //Dia Trabalhado contabiliado

If !(cTipoDia $ '01#05')
	If cCodTecAnt == cCodTec
		If dDataAnt != dData
			nDia := 1
			dDataAnt := dData
		EndIf
 	Else
 		nDia := 1
 		cCodTecAnt := cCodTec 
 		dDataAnt := dData
 	EndIf
EndIf

Return nDia
