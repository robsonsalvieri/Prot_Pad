#include "msobject.ch"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tecsv004.ch"

namespace custom.checkinout

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGATEC", tables="AA1,SRA,ABB,ABS,TDV", name="Relatório Check-In/Out - GS", country="BRA", initialRelease="12.1.2310")

//-------------------------------------------------------------------
/*{Protheus.doc} CheckInOutReportBusinessObject
Classe para geração do relatório de Check-In / Check-Out de Atendentes - GS

@author Julia
@since 27/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
class CheckInOutReportBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getDisplayName() as character
    public method getDescription() as character
    public method getData() as object
    public method getSchema() as object

    protected data aFields as array
    protected data lExistPergunte as logical
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new 
Método de inicializacao da classe

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method new() as object class CheckInOutReportBusinessObject
_Super:new()

self:appendArea(STR0001)//"Gestão de serviços"  

// Campos que serão retornados e exibidos
self:aFields := {"RA_MAT", "AA1_CODTEC", "AA1_NOMTEC", "ABS_DESCRI", "ABB_DTINI", "ABB_HRINI", "ABB_HRCHIN", "ABB_DTFIM", "ABB_HRFIM", "ABB_HRCOUT"}

self:lExistPergunte := self:setPergunte("TECR021")

if !self:lExistPergunte
     self:setErrorStatus(400, STR0002, STR0003) //"Pergunte não encontrado." ## "Grupo de perguntas TECR021 não cadastrado na base. Contate o administrador do sistema."
endif

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Retorna o nome de exibição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDisplayName() as character class CheckInOutReportBusinessObject
    return STR0004//"Check-In / Check-Out" 

//-------------------------------------------------------------------
/*{Protheus.doc} getDisplayName
Retorna o nome de exibição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getDescription() as character class CheckInOutReportBusinessObject
return STR0005//"Relatório Check-In / Check-Out na Mesa Operacional"

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Retorna a descrição do relatório

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class CheckInOutReportBusinessObject
    local cQuery     as character
    local cAliasABB  as character
    local jItems     as json
    local nX         as numeric
    local cId        as character
    local cRealName  as character
    local jParams    as json
    Local nOrder     as numeric
    Local oStatement as object

    If !self:lExistPergunte
        return self:oData
    Else
        nOrder := 1
        cQuery := " SELECT AA1.AA1_CDFUNC, "
        cQuery +=        " AA1.AA1_CODTEC, "
        cQuery +=        " AA1.AA1_NOMTEC, "
        cQuery +=        " ABS.ABS_DESCRI, "
        cQuery +=        " ABB.ABB_DTINI, "
        cQuery +=        " ABB.ABB_HRINI, "
        cQuery +=        " ABB.ABB_HRCHIN, "
        cQuery +=        " ABB.ABB_DTFIM, "
        cQuery +=        " ABB.ABB_HRFIM, "
        cQuery +=        " ABB.ABB_HRCOUT "
        cQuery += " FROM ? ABB "
        cQuery += " INNER JOIN ? AA1 "
        cQuery +=        " ON ABB.ABB_CODTEC = AA1.AA1_CODTEC "
        cQuery +=        " AND AA1.AA1_FILIAL = ? "
        cQuery +=        " AND AA1.D_E_L_E_T_ = ' ' "
        cQuery += " INNER JOIN ? ABS ON ABS.ABS_LOCAL = ABB.ABB_LOCAL "
        cQuery +=        " AND ABS.ABS_FILIAL = ? "
        cQuery +=        " AND ABS.D_E_L_E_T_ = ' ' "
        cQuery += " INNER JOIN ? TDV ON TDV.TDV_CODABB = ABB.ABB_CODIGO "
        cQuery +=        " AND TDV.TDV_FILIAL = ABB.ABB_FILIAL "
        cQuery +=        " AND TDV.D_E_L_E_T_ = ' ' "
        cQuery += " WHERE ABB.ABB_FILIAL = ? "
        cQuery +=        " AND TDV.TDV_DTREF BETWEEN ? AND ? "
        cQuery +=        " AND AA1.AA1_CODTEC BETWEEN ? AND ? "
        cQuery +=        " AND AA1.AA1_CC BETWEEN ? AND ? "
        cQuery +=        " AND ABS.ABS_LOCAL BETWEEN ? AND ? "
        cQuery +=        " AND ABB.D_E_L_E_T_ = ' ' "
        cQuery += " ORDER BY "
        cQuery +=        " AA1.AA1_CDFUNC, "
        cQuery +=        " TDV.TDV_DTREF, " 
        cQuery +=        " ABB.ABB_DTINI, "
        cQuery +=        " ABB.ABB_DTFIM, "
        cQuery +=        " ABB.ABB_HRINI, "
        cQuery +=        " ABB.ABB_HRFIM "

        //Método para retorno do json dos parâmetros:
        jParams := oFilter:getParameters()
        dPar01 := TecConvData(jParams["MV_PAR01"][1])
        dPar02 := TecConvData(jParams["MV_PAR02"][1])

        //Aplicação de filtros do SmartView:
        If oFilter:hasFilter()
            cQuery += " AND ?"
        EndIf

        //Arruma a query para o banco conectado:
        cQuery := ChangeQuery(cQuery)

        //Execucao da query:
        oStatement := FwExecStatement():new(cQuery)

        //Seta as 'interrogações' ? do cQuery
        oStatement:SetUnsafe(nOrder++, RetSQLName("ABB"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("AA1"))
        oStatement:SetString(nOrder++, FWxFilial("AA1"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("ABS"))
        oStatement:SetString(nOrder++, FWxFilial("ABS"))
        oStatement:SetUnsafe(nOrder++, RetSQLName("TDV"))
        oStatement:SetString(nOrder++, FWxFilial("ABB"))
        oStatement:SetDate(nOrder++, dPar01)
        oStatement:SetDate(nOrder++, dPar02)
        oStatement:SetString(nOrder++, jParams["MV_PAR03"][1])
        oStatement:SetString(nOrder++, jParams["MV_PAR04"][1])
        oStatement:SetString(nOrder++, jParams["MV_PAR05"][1])
        oStatement:SetString(nOrder++, jParams["MV_PAR06"][1])
        oStatement:SetString(nOrder++, jParams["MV_PAR07"][1])
        oStatement:SetString(nOrder++, jParams["MV_PAR08"][1])

        // Aplicação de filtros do SmartView 
        If oFilter:hasFilter()
            oStatement:SetUnsafe(nOrder++, oFilter:getSQLExpression())
        EndIf

        //Abre o alias
        cAliasABB := oStatement:OpenAlias()

        //Pega os campos da estrutura:
        aAllFields := self:getStructFields()
        
        While !(cAliasABB)->(Eof())
            jItems := JsonObject():new()

            For nx := 1 To Len(aAllFields)
                cId := aAllFields[nX]:getName()
                cRealName := aAllFields[nX]:getRealName()
                If aAllFields[nX]:getRealName() $ "ABB_DTINI|ABB_DTFIM"
                    jItems[cId] := DTOC(STOD((cAliasABB)->&(cRealName)))
                Else
                    jItems[cId] := (cAliasABB)->&(cRealName)
                EndIf
            Next nX

            self:appendData(jItems)

            (cAliasABB)->(DBSkip())
        EndDo

        self:setHasNext(!(cAliasABB)->(Eof())) 

        (cAliasABB)->(DBCloseArea())
        oStatement:Destroy()
        oStatement := nil
    EndIf

Return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos

@author Julia
@since 29/01/2025
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class CheckInOutReportBusinessObject
self:addProperty("AA1_CDFUNC", "Codigo do Funcionario", "string", "Cod. Func.", "AA1_CDFUNC")
self:addProperty("AA1_CODTEC", "Código do Atendente", "string", "Atendente", "AA1_CODTEC")
self:addProperty("AA1_NOMTEC", "Nome Atendente", "string", "Nome Atend.", "AA1_NOMTEC")
self:addProperty("ABS_DESCRI", "Descrição do Local", "string", "Descrição", "ABS_DESCRI")
self:addProperty("ABB_DTINI", "Data Inicial", "string", "Data Inicial", "ABB_DTINI")
self:addProperty("ABB_HRINI", "Hora Inicial", "string", "Hora Inicial", "ABB_HRINI")
self:addProperty("ABB_HRCHIN", "Hr check-in", "string", "Hr check-in", "ABB_HRCHIN")
self:addProperty("ABB_DTFIM", "Data Final", "string", "Data Final", "ABB_DTFIM")
self:addProperty("ABB_HRFIM", "Hora Final", "string", "Hora Final", "ABB_HRFIM")
self:addProperty("ABB_HRCOUT", "Hr checkout", "string", "Hr checkout", "ABB_HRCOUT")

Return self:oSchema


