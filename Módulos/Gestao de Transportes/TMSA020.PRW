#INCLUDE "TMSA020.ch"
#Include "FWMVCDEF.CH"
#Include "Protheus.ch"

Static cOcoAnt   := ""

/*


ͻ
 Programa    TMSA020    Autor   Richard Anderson   Data  01/10/01 
͹
 Programa    Tabela de Ocorrencias                                    
͹
 Sintaxe    TMSA020()                                                 
͹
 Parametros                                        			          
͹
 Retorno    NIL                                                       
͹
 Uso        TMS - Gestao de Transportes 		            	      
͹
          Atualizacoes efetuadas desde a codificacao inicial           
͹
Programador      Data     BOPS  Motivo da Alteracao                 
͹
Mauro Paladini  11/10/13        Conversao para padrao MVC           
Mauro Paladini  06/12/13        Ajustes para funcionamento do Mile  
ͼ


*/

Function TMSA020(aRotAuto,nOpcAuto)

Local aAutoCab	:= {}
Local oBrowse 	:= Nil

Private aRotina := MenuDef()
Private l020Auto := (Valtype(aRotAuto) == "A")

If l020Auto
	Private lMsHelpAuto := .T.
EndIf

If l020Auto
	aAutoCab   := Aclone( aRotAuto )
	FwMvcRotAuto( ModelDef(), 'DT2', nOpcAuto, { { 'MdFieldDT2', aAutoCab } } )  
Else
	oBrowse:= FWMBrowse():New()
	oBrowse:SetAlias("DT2")
	oBrowse:SetDescription(STR0001) //"Tabela de Ocorrencias"
	oBrowse:Activate()
EndIf

Return Nil

/*


Ŀ
Funcao     ModelDef  Autor  Mauro Paladini         Data 11.10.2013
Ĵ
Descrio  Modelo de dados                                            
Ĵ
Parametros                                                            
Ĵ
Retorno    oModel Objeto do Modelo                                    
ٱ

*/

Static Function ModelDef()

Local oModel	:= Nil
Local oStruDT2	:= FWFormStruct(1,"DT2")

Local bPreValid	:= Nil
Local bPosValid := { |oMdl| PosVldMdl(oMdl) }
Local bCommit 	:= Nil
Local bCancel	:= Nil

oModel:= MpFormMOdel():New("TMSA020",  /*bPreValid*/ , bPosValid , {|oModel| TMA020Cmt(oModel)} ,/*bCancel*/ )
oModel:AddFields("MdFieldDT2",Nil,oStruDT2,/*prevalid*/,,/*bCarga*/)
oModel:SetDescription(STR0001) 	// "Tabela de Ocorrencias"
oModel:GetModel("MdFieldDT2"):SetDescription(STR0001) //"Tabela de Ocorrencias"
oModel:SetPrimaryKey({ "DT2_FILIAL","DT2_CODOCO" })

Return ( oModel )

/*


Ŀ
Funcao     ViewDef   Autor  Mauro Paladini         Data 11.10.2013
Ĵ
Descrio  Exibe browse de acordo com a estrutura                     
Ĵ
Parametros                                                            
Ĵ
Retorno    oView do objeto oView                                      
ٱ

*/

Static Function ViewDef()

Local oModel 	:= FwLoadModel("TMSA020")
Local oStruDT2	:= FWFormStruct(2,"DT2")
Local oView 	:= Nil

oView := FwFormView():New()
oView:SetModel(oModel)
oView:AddField('VwFieldDT2', oStruDT2 , 'MdFieldDT2') 
oView:CreateHorizontalBox("TELA",100)
oView:SetOwnerView("VwFieldDT2","TELA")

Return(oView)

/*


Ŀ
Funcao     MenuDef   Autor  Mauro Paladini         Data 11.10.2013
Ĵ
Descrio  MenuDef com as rotinas do Browse                           
Ĵ
Parametros                                                            
Ĵ
Retorno    aRotina array com as rotina do MenuDef                     
ٱ


*/
Static Function MenuDef()

Local aRotina := {}

ADD OPTION aRotina TITLE STR0002 	ACTION "PesqBrw"         OPERATION 1 ACCESS 0  //"Pesquisar"
ADD OPTION aRotina TITLE STR0003 	ACTION "VIEWDEF.TMSA020" OPERATION 2 ACCESS 0  //"Visualizar"
ADD OPTION aRotina TITLE STR0004 	ACTION "VIEWDEF.TMSA020" OPERATION 3 ACCESS 0  //"Incluir"
ADD OPTION aRotina TITLE STR0005 	ACTION "VIEWDEF.TMSA020" OPERATION 4 ACCESS 0  //"Alterar"
ADD OPTION aRotina TITLE STR0006 	ACTION "VIEWDEF.TMSA020" OPERATION 5 ACCESS 0  //"Excluir"

Return ( aRotina )

    

/*


Ŀ
Funcao    PosVldMdl  Autor  Mauro Paladini         Data 11.10.2013
Ĵ
Descrio Funcao de validacao do modelo MVC                           
Ĵ
Parametros                                                            
Ĵ
Retorno   EXPL1 - Verdadeiro ou Falso                                 
ٱ


*/

Static Function PosVldMdl(oMdl)

Local lRet    := .T.

If oMdl <> Nil .And. oMdl:GetOperation() == MODEL_OPERATION_DELETE

	lRet := TMSA020DelOk()
	
Elseif oMdl <> Nil .And. ( oMdl:GetOperation() == MODEL_OPERATION_INSERT .Or. oMdl:GetOperation() == MODEL_OPERATION_UPDATE )

	lRet := TMSA020TudOk()

Endif

Return lRet 


/*/


Ŀ
Funcao    TMSA020Tud Autor  Patricia A. Salomao    Data  07.06.02 
Ĵ
Descricao  Validacao da Tela                                          
Ĵ
Sintaxe    TMSA020TudOk()                                             
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    Logico                                                     
Ĵ
Uso        TMSA020                                                    
ٱ

/*/
Function TMSA020TudOk()
Local aTipOco 	:= {}

AAdd( aTipOco, StrZero( 6, Len( DT2->DT2_TIPOCO ) ) ) // Gera Pendencia
AAdd( aTipOco, StrZero( 9, Len( DT2->DT2_TIPOCO ) ) ) // Gera Indenizacao

//Somente aceitar tipo de ocorrencia "4 - Retrabalho" para servico de 
//Transporte "1 - Coleta" e "3 - Entrega".
If M->DT2_TIPOCO == StrZero( 4, Len( DT2->DT2_TIPOCO ) ) .And.  ( M->DT2_SERTMS <> StrZero( 1, Len( DT2->DT2_SERTMS ) ) .And. M->DT2_SERTMS <> StrZero( 3, Len( DT2->DT2_SERTMS ) ) )
  	If !Empty(M->DT2_SERTMS)
  		Help('',1,'TMSA02001') //O tipo de ocorrncia "4-Retrabalho" deve ser utilizado somente para servios de  transporte "1- Coleta" ou "3-Entrega".
		Return( .F. )
	Endif	
EndIf

// O Cancelamento so' e' permitido para "Entrega" ou "Coleta"
If M->DT2_TIPOCO == StrZero( 12, Len( DT2->DT2_TIPOCO ) ) .And. ( M->DT2_SERTMS <> StrZero( 1, Len(DT2->DT2_SERTMS)) .And. M->DT2_SERTMS <> StrZero( 3, Len( DT2->DT2_SERTMS ) ) )
	If !Empty(M->DT2_SERTMS)
	  	Help('',1,'TMSA02005') //-- O Tipo de Ocorrencia "12 - Cancelamento", so' e' permitido para Servicos de Transp. de "Coleta" ou "Entrega" 
		Return( .F. )
	Endif	
EndIf

If M->DT2_TIPOCO == StrZero( 6, Len( DT2->DT2_TIPOCO ) ) .And. Empty( M->DT2_COMSEG )
  	Help('',1,'TMSA02002') //-- Obrigatoria a digitao do campo Ramo de Seguro
	Return( .F. )
EndIf

If M->DT2_TIPOCO == StrZero( 6, Len( DT2->DT2_TIPOCO ) ) .And. Empty( M->DT2_TIPPND )
  	Help('',1,'TMSA02003') //-- Obrigatoria a digitao do campo Tipo de Pendencia
	Return( .F. )
EndIf

//-- Obrigatoria a digitacao do tipo do seguro quando o tipo da ocorrencia for igual a 09-Gera Indenizacao
If M->DT2_TIPOCO == StrZero( 9, Len( DT2->DT2_TIPOCO ) ) .And. Empty( M->DT2_COMSEG )
  	Help('',1,'TMSA02002') //-- Obrigatoria a digitao do campo Ramo de Seguro
	Return( .F. )
EndIf

If M->DT2_SERTMS == StrZero( 1, Len( DT2->DT2_SERTMS ) ) .And. Ascan( aTipOco, M->DT2_TIPOCO ) > 0
	Help('',1,'TMSA02004') //-- Tipo invalido para servico de coleta
	Return( .F. )
EndIf

//Somente aceitar tipo de ocorrencia "06 - Gera pendencia + Tipo Pnd 04 - Retorno de Documento 
// para Transporte "3 - Entrega".
If M->DT2_TIPOCO == StrZero( 6, Len( DT2->DT2_TIPOCO ) ) .And. M->DT2_TIPPND == StrZero( 4, Len( DT2->DT2_TIPOCO ) ) .And. M->DT2_SERTMS <> StrZero( 3, Len( DT2->DT2_SERTMS ) )
  	If !Empty(M->DT2_SERTMS) 
	  	Help('',1,'TMSA02008') //O tipo de ocorrncia "06-Gera Pendencia"  e Tipo Pendencia "04 - "Retorno Dc. Cliente" deve ser utilizado somente para servios de  transporte "3-Entrega".
		Return( .F. )
	Endif	
EndIf

If DT2->(ColumnPos("DT2_PRZENT")) > 0 
	If M->DT2_LIBAUT == StrZero(1, Len(DT2->DT2_LIBAUT)) 
		If Empty(M->DT2_PRZENT)
			If M->DT2_RESOCO == StrZero(2, Len(DT2->DT2_RESOCO)) .And. ;
	   			(M->DT2_TIPOCO == StrZero(4, Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero(5, Len(DT2->DT2_TIPOCO)) )
					Help('',1,"OBRIGAT2",,RetTitle('DT2_PRZENT'),04,01) 
					Return( .F. )
			EndIf	
		EndIf	
	EndIf
EndIf

Return  .T. 

/*


Ŀ
Funo    TMSA020Whe Autor  Alex Egydio            Data 09.08.2002
Ĵ
Descrio  Validacoes antes de editar o campo                         
Ĵ
Sintaxe    TMSA020Whe()                                               
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    Logico                                                     
Ĵ
 Uso       DT2_COMSEG                                                 
ٱ
*/
Function TmsA020Whe()

Local lRet		:= .T.
Local aTipOco	:= {}
Local cCampo	:= AllTrim( ReadVar() )
Local oModel	:= FWModelActive()
Local lTmsToGfe	:= SuperGetMV("MV_TMS2GFE", .F., .F. ) //-- Integrao de Ocorrncias entre SIGAGFE e SIGATMS
Local lTipIns	:= DT2->( ColumnPos("DT2_TIPINS") ) > 0

AAdd( aTipOco, StrZero( 6, Len( DT2->DT2_TIPOCO ) ) ) //-- Gera Pendencia
AAdd( aTipOco, StrZero( 9, Len( DT2->DT2_TIPOCO ) ) ) //-- Gera Indenizacao

If cCampo == 'M->DT2_TIPOCO'

	If M->DT2_TIPOCO == StrZero(16, Len(DT2->DT2_TIPOCO)) //-- Receita
		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASD" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASD" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_CODDES" , "" ) //-- Esvazia Contedo - Cd.Despesa
	EndIf

	If M->DT2_TIPOCO == StrZero(17, Len(DT2->DT2_TIPOCO)) //-- Despesa
		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASR" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASR" , "" ) //-- Esvazia Contedo
	EndIf

	If M->DT2_TIPOCO <> StrZero(16, Len(DT2->DT2_TIPOCO)) .And.;
	   M->DT2_TIPOCO <> StrZero(17, Len(DT2->DT2_TIPOCO)) .And.;
	   M->DT2_TIPOCO <> StrZero(18, Len(DT2->DT2_TIPOCO))
		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASD" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASD" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASR" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASR" , "" ) //-- Esvazia Contedo
	EndIf

	If lTipIns .AND. M->DT2_TIPOCO <> StrZero(4, Len(DT2->DT2_TIPOCO)) .And. !Empty( M->DT2_TIPINS )
		oModel:LoadValue( "MdFieldDT2" , "DT2_TIPINS" , " " ) //-- Esvazia Contedo
	EndIf

ElseIf cCampo == 'M->DT2_COMSEG' .Or. cCampo == 'M->DT2_TIPPND' 

	lRet := ( Ascan( aTipOco, M->DT2_TIPOCO ) > 0 )

ElseIf cCampo == "M->DT2_CDPASR" //-- Componente Receita

	If 	M->DT2_TIPOCO <> StrZero( 16 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO))			//-- Receita/Despesa

		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASR" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASR" , "" ) //-- Esvazia Contedo
		lRet := .f.

	EndIf
ElseIf cCampo == "M->DT2_CDPASD" //-- Componente Despesa

	If 	( M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Despesa
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO))	) .Or. ;	//-- Receita/Despesa
		!Empty(M->DT2_CODAED)								  

		oModel:LoadValue( "MdFieldDT2" , "DT2_CDPASD" , "" ) //-- Esvazia Contedo
		oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASD" , "" ) //-- Esvazia Contedo
		lRet := .f.

	EndIf
ElseIf cCampo == "M->DT2_CODDES" //-- Cdigo Despesa Transporte

		If 	( M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Despesa
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO)) ) .Or. ;	//-- Receita/Despesa
		!Empty(M->DT2_CODAED) .Or. !Empty(M->DT2_CDTIPO)	

		oModel:LoadValue( "MdFieldDT2" , "DT2_DESDSP" , "" ) //-- Esvazia Contedo
		lRet := .f.

	EndIf
ElseIf cCampo == "M->DT2_CODNAT" //-- Cdigo Da Natureza Financeira

	If 	M->DT2_TIPOCO =  StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .Or. ;	//-- Despesa
		M->DT2_TIPOCO =  StrZero( 18 ,Len(DT2->DT2_TIPOCO))	.Or. ;	//-- Receita/Despesa
		!Empty(M->DT2_CODAED) .Or. !Empty(M->DT2_CDTIPO)	

		If !Empty(M->DT2_CDTIPO)
			lRet := .f.
		ElseIf DDI->(ColumnPos('DDI_TPREEM'))> 0  .And. !Empty(M->DT2_CODAED) .And. ;
				Posicione('DDI',1,FwxFilial('DDI')+(M->DT2_CODAED),'DDI_TPREEM') == STR(1,TamSX3('DDI_TPREEM')[1])	
			lRet := .F.
		EndIf
	
	Else		
		lRet := .f.
	EndIf
	
	//-- Esvazia Contedo
	If !(lRet)
		oModel:LoadValue( "MdFieldDT2" , "DT2_CODNAT" , "" ) //-- Esvazia Contedo
	EndIf	

ElseIf cCampo == "M->DT2_MOTDT6" //-- Herda Motivo Da Ocorrncia

	If 	M->DT2_TIPOCO <> StrZero( 16 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita/Despesa
		M->DT2_TIPOCO <> StrZero( 19 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Reentrega
		M->DT2_TIPOCO <> StrZero( 20 ,Len(DT2->DT2_TIPOCO))			//-- Devoluo

		lRet := .f.
		oModel:LoadValue( "MdFieldDT2" , "DT2_MOTDT6" , CriaVar("DT2_MOTDT6") ) //-- Altera Contedo Para o Inicilizador Padro Do Campo

	EndIf

ElseIf cCampo == "M->DT2_ALTVLR" //-- Altera Valor Na Liberao?

	If 	M->DT2_TIPOCO <> StrZero( 16 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita
		M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Despesa
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita/Despesa
		M->DT2_TIPOCO <> StrZero( 19 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Reentrega
		M->DT2_TIPOCO <> StrZero( 20 ,Len(DT2->DT2_TIPOCO))	//-- Devoluo
		
		lRet := .f.
		oModel:LoadValue( "MdFieldDT2" , "DT2_ALTVLR" , CriaVar("DT2_ALTVLR") ) //-- Altera Contedo Para o Inicilizador Padro Do Campo

	EndIf

ElseIf cCampo == "M->DT2_CDTIPO" //-- Tipo Da Ocorrncia GFE   

	lRet := .F.
	If lTmsToGfe
		If M->DT2_TIPOCO == StrZero(18,Len(DT2_TIPOCO))
			lRet	:= .F. 
		ElseIf  M->DT2_TIPOCO == StrZero(16,Len(DT2_TIPOCO)) 
			lRet	:= .T.
		Else 
			lRet	:= .T. 
			
			If !Empty(M->DT2_CODAED) .Or. !Empty(M->DT2_CDPASD) .Or. !Empty(M->DT2_CDPASR)
				lRet	:= .F. 
			EndIf
			
		EndIf
	EndIf
	
	//-- Esvazia Contedo
	If !(lRet)
		oModel:LoadValue( "MdFieldDT2" , "DT2_CDTIPO" , "" ) //-- Esvazia Contedo
	EndIf

ElseIf cCampo == "M->DT2_CMPAUT"

	lRet	:= .T. 
	
	If 	M->DT2_TIPOCO <> StrZero( 16 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita
		M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Despesa
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita/Despesa
		M->DT2_TIPOCO <> StrZero( 19 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Reentrega
		M->DT2_TIPOCO <> StrZero( 20 ,Len(DT2->DT2_TIPOCO))			//-- Devoluo
		
		lRet	:= .F. 
		oModel:LoadValue( "MdFieldDT2" , "DT2_CMPAUT" , CriaVar("DT2_CMPAUT") ) //-- Altera Contedo Para o Inicilizador Padro Do Campo
		
	EndIf
	

ElseIf cCampo == "M->DT2_LIBAUT"

	lRet	:= .T. 
	   
	If 	M->DT2_TIPOCO <> StrZero( 16 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita
		M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Despesa
		M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Receita/Despesa
		M->DT2_TIPOCO <> StrZero( 19 ,Len(DT2->DT2_TIPOCO)) .And.;	//-- Reentrega
		M->DT2_TIPOCO <> StrZero( 20 ,Len(DT2->DT2_TIPOCO))		//-- Devoluo
	
		lRet	:= .F. 
	EndIf

	//--- Prazo de Entrega
	If DT2->(ColumnPos("DT2_PRZENT")) > 0 .And. M->DT2_RESOCO == StrZero( 2 ,Len(DT2->DT2_RESOCO))  .And. ;
    	(M->DT2_TIPOCO == StrZero( 4 ,Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero( 5 ,Len(DT2->DT2_TIPOCO)) )
		lRet:= .T.
	EndIf

	If !lRet
		oModel:LoadValue( "MdFieldDT2" , "DT2_LIBAUT" , CriaVar("DT2_LIBAUT") ) //-- Altera Contedo Para o Inicilizador Padro Do Campo
	EndIf
	

ElseIf cCampo == "M->DT2_CODAED"

	lRet	:= .T.

	If 	M->DT2_TIPOCO <> StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .And. M->DT2_TIPOCO <> StrZero( 18 ,Len(DT2->DT2_TIPOCO))
		lRet	:= .F. 
		oModel:LoadValue( "MdFieldDT2" , "DT2_CODAED" , "" ) 
	Else
		
		If !Empty(M->DT2_CDTIPO)
			lRet	:= .F. 
			oModel:LoadValue( "MdFieldDT2" , "DT2_CODAED" , "" ) 
		EndIf
		
	EndIf

ElseIf cCampo == "M->DT2_PRZENT"
	lRet	:= .F. 
	If Empty(M->DT2_SERTMS) .Or. M->DT2_SERTMS == StrZero( 3 ,Len(DT2->DT2_SERTMS))   //Entrega
		// Poder ser preenchido se a Responsabilidade for 2-Cliente e se o Tipo for  04-Retorna documento ou 05-Informativa		
		If (M->DT2_TIPOCO == StrZero( 4 ,Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero( 5 ,Len(DT2->DT2_TIPOCO)) )  .And. M->DT2_RESOCO == StrZero( 2 ,Len(DT2->DT2_RESOCO))  
			lRet:= .T.
		EndIf
	EndIf	

EndIf

Return( lRet )

/*


Ŀ
Funo    TMSA020Vld Autor  Alex Egydio            Data 09.08.2002
Ĵ
Descrio  Validacoes do sistema                                      
Ĵ
Sintaxe    TMSA020Vld()                                               
Ĵ
Parametros Nenhum                                                     
Ĵ
Retorno    Logico                                                     
Ĵ
 Uso       DT2_TIPOCO                                                 
ٱ
*/
Function TmsA020Vld()

Local lRet     := .T.
Local cCampo   := AllTrim( ReadVar() )
Local oModel   := FWModelActive()

Local aArea     := GetArea()
Local cQuery    := ""
Local cAliasDT2 := ""
Local nMotGFE	:= 0
Local lPrzEnt   := DT2->(ColumnPos("DT2_PRZENT")) > 0

If	cCampo == 'M->DT2_TIPOCO' 

	M->DT2_COMSEG := Space(Len(DT2->DT2_COMSEG))
	M->DT2_DESSEG := Space(TamSX3('DT2_DESSEG')[1])
	M->DT2_TIPPND := Space(Len(DT2->DT2_TIPPND))
	M->DT2_DESPND := Space(TamSX3('DT2_DESPND')[1])

	If	M->DT2_TIPOCO == StrZero(14,Len(DT2->DT2_TIPOCO)) //-- Ajuste Previsao de Chegada
		M->DT2_CATOCO := StrZero( 2,Len(DT2->DT2_CATOCO)) //-- Por Viagem
	EndIf

	If lPrzEnt .And. M->DT2_PRZENT == StrZero(1, Len(DT2->DT2_PRZENT)) 
		If M->DT2_RESOCO == StrZero(2, Len(DT2->DT2_RESOCO)) .And. ;
		   (M->DT2_TIPOCO <> StrZero(4, Len(DT2->DT2_TIPOCO)) .And. M->DT2_TIPOCO <> StrZero(5, Len(DT2->DT2_TIPOCO)) )
			//-- "O Prazo de Entrega (DT2_PRZENT), s poder ser preenchido com contedo "SIM" se a Responsabilidade (DT2_RESOCO) 
			//-- for do  Cliente e se o Tipo da Ocorrncia (DT2_TIPOCO) for  04-Retorna documento ou 05-Informativa.
			Help("", 1, 'TMSA02019')
			lRet := .F.
		EndIf
	EndIf	
	

ElseIf cCampo == 'M->DT2_CATOCO'

	If	M->DT2_TIPOCO == StrZero(14,Len(DT2->DT2_TIPOCO)) .And. ; //-- Ajuste Previsao de Chegada
		M->DT2_CATOCO == StrZero( 1,Len(DT2->DT2_CATOCO)) //-- Por Documento
		Help("",1,"TMSA02006") //-- "Para o tipo 'Ajuste Previsao de Chegada' a categoria dever ser por viagem !!!"
		lRet := .F.
	EndIf

ElseIf cCampo == 'M->DT2_CDPASR'

	//-- Posiciona No Componente Do Frete RECEITA
	DbSelectArea("DT3")
	DbSetOrder(1) //-- DT3_FILIAL+DT3_CODPAS
	If MsSeek( FWxFilial("DT3") + M->DT2_CDPASR , .F. )
	
		If DT3->DT3_TIPFAI >= '50'
			Help("",1,"TMSA02009") //-- "Permitido Apenas Componentes De Frete Com Tipo De Faixa (DT3_TIPFAI) Inferior a 50 !"
			lRet := .F.
		Else
			//-- Gatilha Descrio Para Campo Virtual
			oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASR" , DT3->DT3_DESCRI )
		EndIf
	EndIf

ElseIf cCampo == 'M->DT2_CDPASD'

	//-- Posiciona No Componente Do Frete DESPESA
	DbSelectArea("DT3")
	DbSetOrder(1) //-- DT3_FILIAL+DT3_CODPAS
	If MsSeek( FWxFilial("DT3") + M->DT2_CDPASD , .F. )
	
		If DT3->DT3_TIPFAI < '50'
			Help("",1,"TMSA02010") //-- "Permitido Apenas Componentes De Frete Com Tipo De Faixa (DT3_TIPFAI) Superior a 49 !"
			lRet := .F.
		Else
			//-- Gatilha Descrio Para Campo Virtual
			oModel:LoadValue( "MdFieldDT2" , "DT2_DEPASD" , DT3->DT3_DESCRI )
		EndIf
	EndIf

ElseIf cCampo == 'M->DT2_CODDES'

	//-- Posiciona No Cadastro De Despesas De Transporte
	DbSelectArea("DT7")
	DbSetOrder(1) //-- DT7_FILIAL+DT7_CODDES
	If MsSeek( FWxFilial("DT7") + M->DT2_CODDES , .F. )
	
		//-- No Pode Movimentar Banco Nem Estoque
		If DT7->DT7_MOVBCO == '1' .Or. DT7->DT7_CONEST == '1'
			Help("",1,"TMSA02011") //-- "No  Permitido Cdigos De Despesa Que Movimentem Financeiro (DT7_MOVBCO) Ou Estoque (DT7_CONEST) !"
			lRet := .F.
		Else
			//-- Gatilha Descrio Para Campo Virtual
			oModel:LoadValue( "MdFieldDT2" , "DT2_DESDSP" , DT7->DT7_DESCRI )
		EndIf
	EndIf	

ElseIf cCampo == 'M->DT2_LIBAUT'

	//--------------------------------------------------------------------------------------------- 
	//-- Validao na funo TmsA020Vld() consistindo configurao entre 
	//-- o campo de aprovao automtica TMS e o campo de aprovao automtica GFE 
	//-- (DT2_LIBAUT='1' .AND. GU6_APRAUT='1' .or DT2_LIBAUT='2' .AND. GU6_APRAUT='2') - 
	//-- DT2_CDTIPO<>'' .AND. (GU5_CDTIPO=DT2_CDTIPO; GU4_CDTIPO=GU5_CDTIPO; GU6_CDMOT=GU4_CDMOT). 
	//-- Alertar o operador sobre o motivo da inconsistencia.
	//---------------------------------------------------------------------------------------------
	If M->DT2_LIBAUT == '1' .And. lRet
	
		//-- Tratamento Para Campo DT2_CDTIPO No Informado.
		If !Empty(M->DT2_CDTIPO)

			//--Help("",1,"TMSA02012") //-- "Informe Primeiro o Tipo Da Ocorrncia GFE (DT2_CDTIPO) !"
			//--lRet := .F.

			//-- Posiciona No "Tipo Da Ocorrencia" (GFE)
			DbSelectArea("GU5")
			DbSetOrder(1) //-- GU5_FILIAL+GU5_CDTIPO
			If MsSeek( FWxFilial("GU5") + M->DT2_CDTIPO , .F. )
			
				//-- Tipo X Motivo Ocorrencia
				DbSelectArea("GU4")
				DbSetOrder(2) //-- GU4_FILIAL+GU4_CDTIPO
				DbSeek(xFilial("GU4") + GU5->GU5_CDTIPO , .F.)
				
				While GU4->(!Eof()) .And. (GU4->GU4_CDTIPO == GU5->GU5_CDTIPO)  
					
					//-- Motivo Da Ocorrencia
					DbSelectArea("GU6")
					DbSetOrder(1) //-- GU6_FILIAL+GU6_CDMOT
					If DbSeek( FWxFilial("GU6") + GU4->GU4_CDMOT , .F. )
						
						If M->DT2_LIBAUT <> GU6->GU6_APRAUT
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'DT2_LIBAUT' ) + " / " + TmsReTitle( 'GU6_APRAUT' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(M->DT2_CDTIPO) .And. (GU5->GU5_CDTIPO <> M->DT2_CDTIPO) 		 
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'DT2_CDTIPO' ) + " / " + TmsReTitle( 'GU5_CDTIPO' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(M->DT2_CDTIPO) .And. (GU4->GU4_CDTIPO <> GU5->GU5_CDTIPO) 		 
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'GU4_CDTIPO' ) + " / " + TmsReTitle( 'GU5_CDTIPO' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(M->DT2_CDTIPO) .And. (GU6->GU6_CDMOT <> GU4->GU4_CDMOT) 		 
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'GU6_CDMOT' ) + " / " + TmsReTitle( 'GU4_CDMOT' ) , 1, 0)
							lRet := .F.
						EndIf								
				
					EndIf
					
					GU4->(DbSkip())
				EndDo	
	
			EndIf	
		EndIf

		//--- Prazo de Entrega
		If lRet .And. lPrzEnt
			If M->DT2_RESOCO == StrZero( 2 ,Len(DT2->DT2_RESOCO)) .And. (M->DT2_TIPOCO == StrZero( 4 ,Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero( 5 ,Len(DT2->DT2_TIPOCO)) )
				If M->DT2_PRZENT == StrZero( 2 ,Len(DT2->DT2_PRZENT))  
					//Se o Prazo de Entrega estiver preenchido como 'No', a Liberao Automtica tambm dever ser preenchida como 'No'.
					Help("", 1, 'TMSA02020')  
					lRet:= .F.
				EndIf
			EndIf	
		EndIf

	EndIf

ElseIf cCampo == 'M->DT2_CDTIPO'

	If ! Empty(M->DT2_CDTIPO) //-- Somente se for informada

		//-- Posiciona No "Tipo Da Ocorrencia" (GFE)
		DbSelectArea("GU5")
		DbSetOrder(1) //-- GU5_FILIAL+GU5_CDTIPO
		If MsSeek( FWxFilial("GU5") + M->DT2_CDTIPO , .F. )
			
			//-- Tipo X Motivo Ocorrencia
			DbSelectArea("GU4")
			DbSetOrder(2) //-- GU4_FILIAL+GU4_CDTIPO
			DbSeek(xFilial("GU4") + GU5->GU5_CDTIPO , .F.)
				
			While GU4->(!Eof()) .And. (GU4->GU4_CDTIPO == GU5->GU5_CDTIPO) .And. lRet
				
				//-- Motivo Da Ocorrencia
				DbSelectArea("GU6")
				DbSetOrder(1) //-- GU6_FILIAL+GU6_CDMOT
				If DbSeek( FWxFilial("GU6") + GU4->GU4_CDMOT , .F. )
					
					//-- Contador de quantos motivos o GFE possui
					nMotGFE++
					
					//-- Tratamento DT2_TIPOCO = 17 Ou 18
					If	M->DT2_TIPOCO == StrZero( 17 ,Len(DT2->DT2_TIPOCO)) .Or. ;	//-- Despesa
						M->DT2_TIPOCO == StrZero( 18 ,Len(DT2->DT2_TIPOCO))			//-- Receita/Despesa
							
						If M->DT2_LIBAUT <> GU6->GU6_APRAUT
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'DT2_LIBAUT' ) + " / " + TmsReTitle( 'GU6_APRAUT' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(M->DT2_CDTIPO) .And. (GU4->GU4_CDTIPO <> GU5->GU5_CDTIPO)
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'GU4_CDTIPO' ) + " / " + TmsReTitle( 'GU5_CDTIPO' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(M->DT2_CDTIPO) .And. (GU6->GU6_CDMOT  <> GU4->GU4_CDMOT )
							//-- "Configuraes De Liberao Automtica Diferente Entre TMS e GFE !", "Verifique O Contedo Dos Campos "
							Help(Nil,Nil,'HELP',Nil, STR0007 + Chr(13) + Chr(10) + STR0008 + TmsReTitle( 'GU6_CDMOT' ) + " / " + TmsReTitle( 'GU4_CDMOT' ) , 1, 0)
							lRet := .F.
						ElseIf !Empty(GU5->GU5_OCOTMS)
							//-- "No  Permitido Utilizar Ocorrncia do GFE Vinculada ao TMS !"
							Help(Nil,Nil,'HELP',Nil, STR0011 , 1, 0)
							lRet := .F.
						EndIf
					EndIf

					//-- Tratamento DT2_TIPOCO = 21
					If M->DT2_TIPOCO == StrZero( 21 ,Len(DT2->DT2_TIPOCO))
						
						If GU5->GU5_EVENTO <> StrZero( 04 ,Len(GU5->GU5_EVENTO))
							Help("",1,"TMSA02014",Nil,Nil ) //-- "Campo De Evento (GU5_EVENTO) No GFE Diferente de '4'"
							lRet := .F.
							Exit
						EndIf
						
					ElseIf M->DT2_TIPOCO == StrZero( 17 ,Len(DT2->DT2_TIPOCO))
					
						If GU5->GU5_EVENTO <> StrZero( 1 , Len(GU5->GU5_EVENTO)) .And. GU5->GU5_TPCALC <> StrZero(3,Len(GU5->GU5_TPCALC) )
							Help("",1,"TMSA02018",Nil,Nil) //-- Tip.Ocor.GFE informado invlido.Para ocorrencias do tipo 17 = Despesa as Ocorrncias do SIGAGFE devem possuir o Evento de Ocorrencia igual a 1=Clculo, e o campo Tipo do Calculo Adicional igual a 3 = Servio.
							lRet	:= .F. 
							Exit
						EndIf
						
					EndIf
				
				EndIf
				GU4->(DbSkip())
			EndDo

			If lRet .And. nMotGFE > 1			
				Help(" ", 1, 'TMSA02017') //-- "Tip.Ocor.GFE informado invlido, pois h mais de um Motivo vinculado no cadastro deste Tipo da Ocorrncia GFE."
				lRet := .F.
			EndIf
		EndIf

		//-- Verifica se a ocorrncia j est sendo utilizada
		If lRet
			cAliasDT2 := GetNextAlias()
			cQuery := "SELECT DT2_CODOCO "	
			cQuery += "  FROM " + RetSqlName("DT2") + " DT2 "
			cQuery += " WHERE DT2_FILIAL = '" + FwxFilial("DT2") + "' "
			cQuery += "   AND DT2_CDTIPO = '" + M->DT2_CDTIPO + "' "
			cQuery += "   AND DT2.D_E_L_E_T_ = ' ' "
			cQuery := ChangeQuery(cQuery)
			DbUseArea(.T.,'TOPCONN',TCGENQRY(,,cQuery),cAliasDT2,.T.,.T.)
			If (cAliasDT2)->(!Eof())
				Help(Nil,Nil,'HELP',Nil, STR0009 + "( " + (cAliasDT2)->DT2_CODOCO + " )" , 1, 0) //-- "Ocorrncia do GFE j vinculada na ocorrncia do TMS " ###
				lRet := .F.
			EndIf
		EndIf
	EndIf
ElseIf cCampo == 'M->DT2_RESOCO'
	If lPrzEnt .And. M->DT2_PRZENT == StrZero(1, Len(DT2->DT2_PRZENT))  
		If M->DT2_RESOCO <> StrZero(2, Len(DT2->DT2_RESOCO)) .And. ;
		   (M->DT2_TIPOCO == StrZero(4, Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero(5, Len(DT2->DT2_TIPOCO)) )
			//-- "O Prazo de Entrega (DT2_PRZENT), s poder ser preenchido com contedo "SIM" se a Responsabilidade (DT2_RESOCO) 
			//-- for do  Cliente e se o Tipo da Ocorrncia (DT2_TIPOCO) for  04-Retorna documento ou 05-Informativa.
			Help("", 1, 'TMSA02019')
			lRet := .F.
		EndIf
	EndIf	

ElseIf cCampo == 'M->DT2_PRZENT'
	If M->DT2_LIBAUT == StrZero(1, Len(DT2->DT2_LIBAUT)) 
		If M->DT2_PRZENT == StrZero(2, Len(DT2->DT2_PRZENT)) 
			If M->DT2_RESOCO == StrZero(2, Len(DT2->DT2_RESOCO)) .And. ;
		   		(M->DT2_TIPOCO == StrZero(4, Len(DT2->DT2_TIPOCO)) .Or. M->DT2_TIPOCO == StrZero(5, Len(DT2->DT2_TIPOCO)) )
                //Se o Prazo de Entrega estiver preenchido como 'No', a Liberao Automtica tambm dever ser preenchida como 'No'.
				Help("", 1, 'TMSA02020')
				lRet := .F.
			EndIf	
		EndIf	
	EndIf		

EndIf

RestArea(aArea)

Return( lRet )

/*

Ŀ
Funcao    TMSA020Inc Autor  Patricia A. Salomao    Data  02.12.02 
Ĵ
Descrio  Funcao de Manutencao do Arquivo DT2 (Cadastro de Operacoes)
           Disparada por rotina automatica                            
Ĵ
Sintaxe    TMSA020Inc(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do Arquivo.                                  
           ExpC2 = Registro.                                          
           ExpC3 = Opcao Selecionada.                                 
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        TMSA020                                                    
ٱ

/*/
Function TMSA020Inc(cAlias,nReg,nOpc)
Local aArea := GetArea()

SAVEINTER()

FWExecView(STR0004,"VIEWDEF.TMSA020", MODEL_OPERATION_INSERT,, { || .T. } ,,  /*nPerReducTela*/ ) //'Incluir'

RestArea(aArea)

Return NIL

/*


ͻ
Programa TMSA020DelOkAutor  Adalberto S.M        Data   08/10/09   
͹
Desc.     Manutencao TMSA020.                                         
                                                                      
ͼ


*/
Function TMSA020DelOk()

	Local cAliasDWU	:= GetNextAlias()
	Local cQuery	:= ""
	Local lRet		:= .T.
	
	Local cAliasDUA := ""
	cQuery := "SELECT (COUNT(*)) DWU_COUNT "
	cQuery += "  FROM " + RetSqlName("DWU")+" DWU "
	cQuery += " WHERE DWU.DWU_FILIAL = '" + xFilial("DWU") + "'"
	cQuery += "   AND DWU.DWU_CODOCO = '" + DT2->DT2_CODOCO + "'"
	cQuery += "   AND DWU.D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery(cQuery)
	DbUseArea(.T.,'TOPCONN',TCGENQRY(,,cQuery),cAliasDWU,.T.,.T.)
	
	If (cAliasDWU)->DWU_COUNT > 0
		Help('',1,"TMSA02007") //-- Nao eh possivel excluir ocorrencia. Verifique o cadastro de Ocorrencias x Usuarios.
		lRet := .F.
	EndIf
	
	(cAliasDWU)->(DbCloseArea())

	//-- Verifica se existe registro de ocorrncia
	cAliasDUA	:= GetNextAlias()
	cQuery := "SELECT COUNT(DUA_CODOCO) QUANTIDADE "
	cQuery += "  FROM " + RetSqlName("DUA")+" DUA "
	cQuery += " WHERE DUA_FILIAL = '" + xFilial("DUA") + "'"
	cQuery += "   AND DUA_CODOCO = '" + DT2->DT2_CODOCO + "'"
	cQuery += "   AND DUA.D_E_L_E_T_ = ' '"
	cQuery := ChangeQuery(cQuery)
	DbUseArea(.T.,'TOPCONN',TCGENQRY(,,cQuery),cAliasDUA,.T.,.T.)
	
	If (cAliasDUA)->QUANTIDADE > 0
		Help('',1,"TMSA02015") //-- Existem apontamentos para a ocorrncia.
		lRet := .F.
	EndIf
	
	(cAliasDUA)->(DbCloseArea())

Return (lRet)
/*


Ŀ
Funcao     TMA020Cmt   Autor  Valdemar Roberto     Data  23.02.17 
Ĵ
Descrio  Grava registro                                             
Ĵ
Sintaxe    TMA020Cmt(oExp01)                                         
Ĵ
Parametros oExp01 = Modelo                                            
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        TMSA020                                                    
ٱ


*/

Static Function TMA020Cmt(oModel)

Local lTmsToGfe := SuperGetMV("MV_TMS2GFE",.f.,.f.) //-- Integrao de Ocorrncias entre SIGAGFE e SIGATMS

If lTmsToGfe
	cOcoAnt := DT2->DT2_CDTIPO
EndIf

FwFormCommit(oModel,/*bBefore*/,{|oModel,cID,cAlias,lNewRecord| TMA020CCmt(oModel,cID,cAlias,lNewRecord)},/*bAfterSTTS*/)
		
Return .T.

/*


Ŀ
Funcao     TMA020CCmt  Autor  Valdemar Roberto     Data  23.02.17 
Ĵ
Descrio  Complementa gravao do registro                           
Ĵ
Sintaxe    TMA020CCmt()                                               
Ĵ
Parametros                                                            
Ĵ
Retorno    Nil                                                        
Ĵ
Uso        TMSA020                                                    
ٱ


*/

Static Function TMA020CCmt(oModel,cId,cAlias,lNewRecord)

Local aAreas	:= { GU5->(GetArea()), GetArea() }
Local lTmsToGfe	:= SuperGetMV( "MV_TMS2GFE", .f., .f. ) //-- Integrao de Ocorrncias entre SIGAGFE e SIGATMS

If lTmsToGfe
	If oModel:GetOperation() == MODEL_OPERATION_UPDATE .Or. oModel:GetOperation() == MODEL_OPERATION_INSERT
		GU5->(DbSetOrder(1))
		If GU5->(DbSeek(xFilial("GU5") + M->DT2_CDTIPO))
			RecLock("GU5",.F.)
			GU5->GU5_OCOTMS := DT2->DT2_CODOCO
			GU5->(MsUnlock())
		EndIf
		If GU5->(DbSeek(xFilial("GU5") + cOcoAnt))
			RecLock("GU5",.F.)
			GU5->GU5_OCOTMS := Space(TamSX3("GU5_OCOTMS")[1])
			GU5->(MsUnlock())
		EndIf
	ElseIf oModel:GetOperation() == MODEL_OPERATION_DELETE
		If GU5->(DbSeek(xFilial("GU5") + cOcoAnt))
			RecLock("GU5",.F.)
			GU5->GU5_OCOTMS := Space(TamSX3("GU5_OCOTMS")[1])
			GU5->(MsUnlock())
		EndIf
	EndIf
EndIf

aEval(aAreas,{|xArea| RestArea(xArea)})

Return .T.

