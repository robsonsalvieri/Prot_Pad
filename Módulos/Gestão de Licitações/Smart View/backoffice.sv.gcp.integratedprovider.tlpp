#include 'totvs.ch'
#include 'msobject.ch'
#include 'tlpp-rest.th'
#include 'tlpp-core.th'
#include 'fwlibversion.ch'
#include 'backoffice.sv.gcp.integratedprovider.ch'
#include 'totvs.framework.treports.integratedprovider.th'

namespace totvs.protheus.backoffice.gcp.smartView.integratedProvider

//-------------------------------------------------------------------
/*/{Protheus.doc} GCPIntegratedProvider
    Classe de Integrated Provider que incorpora as principais soluções  de compras.
    Será a Super classe dos objetos de negócio do módulo.
    @author 
    @since 
    @version 12.1.2210
*/
//-------------------------------------------------------------------

class GCPIntegratedProvider from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new() as object
    public method getData() as object

    protected data aStruct              as array

    protected data cAlias               as character
    protected data cMinimumLibLabel     as character
	protected data cMinimumLibLookUp	as character
    protected data cSX1Group            as character
    protected data cSelectComplement    as character
    protected data cWhereComplement     as character

    protected data lPergunteOK          as logical
    protected data lLibOK               as logical
    protected data lLibLookUpOK	    	as logical

    protected data nDecimals            as numeric
    protected data nCurrentPage         as numeric
    
    protected data jData                as json
	protected data jCustomPar			as json 
	protected data jStatement			as json

    protected data oCurrentFilter       as object
    protected data oLGPD                as object
    
    protected method enableFilterByBranch()
    protected method validEnvironment()
    protected method setPergunte() as logical
    protected method setMVPAR()
    protected method setFilter()
	protected method setFilterOnWhere()
    protected method setErrorInvalidLib()
    protected method setErrorInvalidPergunte()    
    protected method setContentToData()
    protected method setCustomInfoToData()
    protected method setAliasToData()
    protected method setEmptyContent()

    protected method addToData()
    protected method varToTimeStamp() as variant
    
    protected method getAllFieldsToSQL() as character
    protected method getCustomFieldsToSQL() as character
    protected method getCustomParam()
    protected method getStructCustom()
    protected method getInfoEmp()
    protected method addLGPDCalcField()
    protected method loadLGPDFields()
    
    protected method processAndAppend()
    protected method processCustomFields()
    protected method processSQLStatement()
    protected method gcpAddProperty()
    
    private method setValueMVPAR()

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method New() as object class GCPIntegratedProvider
    
	_Super:new()

    self:appendArea(STR0001) // "Gestão de Licitações"  

	self:aStruct			:= {}
	
	self:cAlias				:= ""
	self:cMinimumLibLabel	:= "20240226"
    self:cMinimumLibLookUp	:= "20231121"
	self:cSelectComplement	:= ""
	self:cWhereComplement	:= ""
	
	self:nCurrentPage		:= 1
	self:nDecimals			:= TamSx3("M2_MOEDA2")[2] + 1
	
	self:jCustomPar			:= JsonObject():New()
	self:jData				:= JsonObject():new()
	self:jStatement			:= JsonObject():New()

	self:lLibOK				:= FwLibVersion() >= self:cMinimumLibLabel
    self:lLibLookUpOK		:= FwLibVersion() >= self:cMinimumLibLookUp
	
	self:oLGPD				:= SmartViewGCPLGPD():New()

return self

//-------------------------------------------------------------------
/*/{Protheus.doc} setPergunte
    Utiliza polimorfismo para guardar o grupo de perguntas que o 
    objeto de negócio tentou usar, e guardar o resultado do setPergunte original, 
    sendo usado depois na validEnvironment.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

method setPergunte(cPergunte as character) as logical class GCPIntegratedProvider
    
    self:cSX1Group := cPergunte
    self:lPergunteOK := _Super:setPergunte(cPergunte)

return self:lPergunteOK

//-------------------------------------------------------------------
/*/{Protheus.doc} setMVPAR
    Atualiza as variáveis públicas MV_PAR de acordo com os parâmetros
    informados pelo usuário na interface do Smart View.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method setMVPAR(oFilter as object) class GCPIntegratedProvider    
    
default oFilter := self:oCurrentFilter

    self:setValueMVPAR(oFilter:getParameters(), self:cPergunte)
   
return

//-------------------------------------------------------------------
/*/{Protheus.doc} processAndAppend
    Faz o processamento dos dados do JData e o append no objeto oData.
    Chama os métodos responsáveis pelo complemento dos dados no MI,
    no objeto extendido do compras, o ofuscamento de dados protegidos/sendiveis
    conforme LGPD e qualquer outro tratamento que deve ser feito nos dados,
    antes de entregar para a classe do Framework montar o JSON que será entregue
    ao Smart View.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method processAndAppend() class GCPIntegratedProvider

    self:processData()         // Manipula jData antes do append, criado pelo Framework para uso do MI
    self:processCustomFields() // Manipula jData antes do append, incluindo o conteúdo dos campos personalizados
    self:loadLGPDFields()
    self:jData := self:oLGPD:handleDataWithLGPD(self:jData)
    self:oData:appendData(self:jData)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} varToTimeStamp
    Chama a função do frame que faz o tratamento de campos tipo data
    para o formato adequado à API. Campos vazios devem preencher com NULO 
    o JSON.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

method varToTimeStamp(xDate as variant) as variant class GCPIntegratedProvider

local cRet as character

    cRet := if( valType(xDate) == "D", totvs.framework.treports.date.dateToTimeStamp( xDate )   ,;
            if( valType(xDate) == "C", totvs.framework.treports.date.stringToTimeStamp( xDate ) , Nil ))

return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} loadLGPDFields
    Usa os campos do esquema para analisar quais campos serão ofuscados
    no objeto de negócio, dependendo do usuário ativo.
    Os campos calculados, adicionados pelo método addLGPDCalcFields também
    serão analisados aqui.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method loadLGPDFields() class GCPIntegratedProvider
    self:oLGPD:loadFields(self:oSchema:getProperties())
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addLGPDCalcField
    Adiciona campos calculados à classe de tratamento LGPD, pois esses
    campos precisam ser vinculados a algum campo do dicionário de dados
    para que o sistema saiba se deve ofuscar ou não.
    @author SQUAD SUPRIMENTOS
    @since 01/09/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------

method addLGPDCalcField(cCalcFieldName as character, cRealName as character) class GCPIntegratedProvider
    self:oLGPD:addCalcField(cCalcFieldName, cRealName)
return

/*/{Protheus.doc} setValueMVPAR()
    Seta os valores para os MV_PAR publicas.
    @author Francisco Oliveira
    @since 20/04/2023
    @version 1.0
    @return Nil
/*/
method setValueMVPAR(oJsonMvPar As Json, cPergunte as Character) class GCPIntegratedProvider

local oJson     as json
local nY        as numeric
local nJson     as numeric
local cTipoPar  as Character

	Pergunte(cPergunte,.F.)

	oJson := oJsonMvPar:GetNames()
	nJson := len(oJson)

	for nY := 1 To nJson
		cTipoPar := TYPE(UPPER( oJson[nY] ) )
		do case
			case cTipoPar == "D"
				&( UPPER( oJson[nY] ) ) := StoD( SubStr(StrTran(oJsonMvPar[ oJson[nY] ][1],"-",""),1,8) )
			case cTipoPar $ "N|C"
				&( UPPER( oJson[nY] ) ) := oJsonMvPar[ oJson[nY] ][1]
			otherwise
				self:jCustomPar[UPPER(oJson[nY])] := oJsonMvPar[oJson[nY]]
		endcase
	next nY

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getCustomFieldsToSQL
    Retorna string de personalização de campos pronta para ser adicionada à query.
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getCustomFieldsToSQL() as character class GCPIntegratedProvider

local cCustomFields := "" as character
local lContactTableName := .T.
local aTables := NIL
local lOnlyCustomFields := .T.

    cCustomFields := self:getSQLFields(lContactTableName, aTables, lOnlyCustomFields)
    if !Empty(cCustomFields)
        cCustomFields := ", " + cCustomFields
    endIf

return cCustomFields

//-------------------------------------------------------------------
/*/{Protheus.doc} processCustomFields
    Complementa o JSON jData com o conteúdo dos campos personalizados.
    @author SQUAD SUPRIMENTOS
    @since 19/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method processCustomFields() class GCPIntegratedProvider

local aCustomFields := {} as array
local nX := 0 as numeric
local cFieldName := "" as character
local cType := "" as character
local cPropertyId := "" as character

    aCustomFields := self:getCustomFields()

    for nX := 1 To Len(aCustomFields)
        cPropertyId := cFieldName := aCustomFields[nX][1]
        cType := aCustomFields[nX][3]        

        self:addToData(cPropertyId, cType, cFieldName)
    next nX
return
//-------------------------------------------------------------------
/*/{Protheus.doc} addToData
    Adiciona algum conteúdo no jData
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method addToData(cPropertyId as character, cType as character, cFieldName as character) class GCPIntegratedProvider

local cComboContent := "" as character

    if cType == "date"
        self:jData[cPropertyId] := self:varToTimeStamp((self:cAlias)->&(cFieldName))
    elseif cType == "string" .and. !Empty(cComboContent := x3Combo(cFieldName, (self:cAlias)->&(cFieldName)))
        self:jData[cPropertyId] := cComboContent
    else
        self:jData[cPropertyId] := (self:cAlias)->&(cFieldName)
    endIf
return


//-------------------------------------------------------------------
/*/{Protheus.doc} setFilter
    Adiciona algum conteúdo no jData
    @author SQUAD SUPRIMENTOS
    @since 20/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setFilter(oFilter as object) class GCPIntegratedProvider
    self:oCurrentFilter := oFilter
return


//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Método getData padrão para todos os objetos de negócio.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class GCPIntegratedProvider

    if self:validEnvironment()

        self:setFilter(oFilter)

        if oFilter <> NIL
            self:setMVPAR(oFilter)
        endIf
        
		self:jStatement := JsonObject():New()
        self:cAlias := MPSysOpenQuery(self:getQuery(), NIL, self:aStruct)

        self:setAliasToData()
    
        (self:cAlias)->( DBCloseArea() )
    
    endif

return self:oData


//-------------------------------------------------------------------
/*/{Protheus.doc} setAliasToData
    Método responsável em carregar o self:jData com os atributos padrões
    do Alias. Caso o ON tenha conteúdos específicos ou condicionais, o mesmo
    deve utilizar o método setCustomInfoToData.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setAliasToData() class GCPIntegratedProvider

    self:aStruct := self:getStructFields()

	(self:cAlias)->(dbGoTop())

    if (self:cAlias)->(Eof())
        self:setEmptyContent()
		self:processAndAppend()
    endif

	while !(self:cAlias)->(Eof())
        self:setContentToData()
        self:setCustomInfoToData()
        self:processAndAppend()
        (self:cAlias)->(DbSkip())
    enddo

return


//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
    Método definido para não ocorrer errorLog nos processamentos dos 
    ON's, quando o mesmo não conter este método definido em sua classe.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setCustomInfoToData() class GCPIntegratedProvider
return


//-------------------------------------------------------------------
/*/{Protheus.doc} setCustomInfoToData
    Método definido para não ocorrer errorLog nos processamentos dos 
    ON's, quando o mesmo não conter este método definido em sua classe.
    @author SQUAD SUPRIMENTOS
    @since 24/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setEmptyContent() class GCPIntegratedProvider
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllFieldsToSQL
    Retorna todos os campos usados no objeto de negócio para compor
    a query. Inclui todos os campos do schema padrão, da personalização de campos
    e da internacionalização.
    É bom dar preferência para esse método em vez de montar os campos da query manualmente
    no objeto de negócio porque se houver algum campo na consulta que não existe no schema,
    será permitido ao usuário incluir esse campo no objeto de negócio e ele ficaria duplicado
    na query, correndo um risco alto de gerar erros em tempo de execução.
    @author SQUAD SUPRIMENTOS
    @since 26/10/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getAllFieldsToSQL() as character class GCPIntegratedProvider

    local cFields := "" as character    
    local lContactTableName := .T. as logical

    cFields := self:getSQLFields(lContactTableName) + " "
    cFields += self:getCustomFieldsToSQL() + " "
    cFields += self:cSelectComplement + " "
    
return cFields

/*/{Protheus.doc} setFilterOnWhere
	Tratamento do filtro do objeto de negócio para tratamento no statement da query
@author Everton Fregonezi Diniz
@since 13/12/2023
@version version
@return cRet, character, retorna script SQL concatenado
/*/
method setFilterOnWhere() class GCPIntegratedProvider
return iif( self:oCurrentFilter:hasFilter(), ' AND ' + self:oCurrentFilter:getSQLExpression(), '' )


/*/{Protheus.doc} getCustomParam
	Em caso de parâmetro customizado este método retornará o valor do parâmetro desejado
@author SQUAD SUPRIMENTOS
@since 15/12/2023
@version version
@return xRet, character, retorna o valor do parâmetro fornecido pelo ON.
/*/
method getCustomParam(cParam as character, lIsBranch as logical, cTable as character) class GCPIntegratedProvider

local xRet  := Nil

default cTable      := ""
default cParam      := ""
default lIsBranch   := .F.

    if !empty(cParam)
		if lIsBranch .and. ( !(self:lLibLookUpOK) .Or. empty(self:jCustomPar["SV_MULTBRANCH"]) )
			xRet := FWxFilial(cTable)
		else
			xRet := self:jCustomPar[cParam]
		endif
    endif

return xRet



//-------------------------------------------------------------------
/*/{Protheus.doc} comAddProperty
    Método para a criação de propriedades/campos custom no ON.
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//------------------------------------------------------------------
method gcpAddProperty(cPropertyName as character, cDescription as character, cType as character, cRealNameForLGPD as character, cRenameField as character, lCanFilter as logical) class GCPIntegratedProvider

default cRealNameForLGPD    := ""
default cRenameField        := ""
default lCanFilter          := .F.
    
	if !empty(cRealNameForLGPD)
		self:addProperty(cPropertyName, cDescription, cType, cDescription, cRealNameForLGPD, , , cRenameField, lCanFilter)
		self:addLGPDCalcField(cPropertyName, cRealNameForLGPD)
	else
		self:addProperty(cPropertyName, cDescription, cType, cDescription, cPropertyName, , , cRenameField, lCanFilter)
	endif
	
	self:loadLGPDFields()

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getInfoEmp
    Método para retornar informações/atributos do cadastro de empresa
        - Existência do grupo de perguntas (SX1)
        - Versão mínima da LIB
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method getInfoEmp(cFldSM0 as character, cCodFil as character) class GCPIntegratedProvider

local aAux          as array
local cRet := ""    as character
local nX            as numeric

	aAux := FWSM0Util():GetSM0Data(, cCodFil,)
	nX	 := aScan(aAux,{|x| alltrim(x[1]) == upper(alltrim(cFldSM0)) })

	if nX > 0 .and. upper(alltrim(cFldSM0)) == 'M0_CGC' .and. len(alltrim(aAux[nX][2])) == 14
		cRet := transform(alltrim(aAux[nX][2]), "@!R NN.NNN.NNN/NNNN-99")
	elseif nX > 0
		cRet := alltrim(aAux[nX][2])
	endif

return cRet

/*/{Protheus.doc} processSQLStatement
    
	Método para processamento da classe FWPreparedStatement para todos os
	ON's pertinentes ao compras, com o objeto de centralizar a utilização dessa classe
	numa possível manutenção/troca, conforme definição do Framework.

    @author SQUAD SUPRIMENTOS
    @since 10/01/2024
    @version version
/*/
method processSQLStatement(cQuery as character) class GCPIntegratedProvider

local cTipoPar			as character
local cRetQry	:= ''	as character
local nX				as numeric
local oJson				as json
local oQuery			as object

default cQuery	:= ''

	if !empty(cQuery)
		oJson := self:jStatement:GetNames()
		oQuery := FWPreparedStatement():New(cQuery)
		for nX := 1 To len(oJson)
			cTipoPar := valtype( self:jStatement[oJson[nX]] )
			do case
				case cTipoPar == "C"
					oQuery:setString(nX, self:jStatement[oJson[nX]])
				case cTipoPar == "D"
					oQuery:setDate(nX, self:jStatement[oJson[nX]])
				case cTipoPar == "N"
					oQuery:setNumeric(nX, self:jStatement[oJson[nX]])
				case cTipoPar == "A"
					oQuery:setIn(nX, self:jStatement[oJson[nX]])
			endcase
		next nX

		cRetQry := oQuery:GetFixQuery()
	endif
	
return cRetQry


//-------------------------------------------------------------------
/*/{Protheus.doc} setContentToData
	Método para carregar o jData antes do append
	@since 19/01/2024
	@version 12.1.2310
*/
//-------------------------------------------------------------------
method setContentToData() class GCPIntegratedProvider

local cProperty	as character
local cRealName	as character
local cField    as character
local lCombo	as logical
local nX		as numeric

    self:jData := JSonObject():New()

	for nX := 1 to len(self:aStruct)
        
        cProperty := self:aStruct[nX]:getName()
        cRealName := self:aStruct[nX]:getRealName()

        cField := if( cProperty == cRealName, cProperty, cRealName )

        if (self:cAlias)->(fieldPos(cProperty)) > 0
			lCombo := At('_', cField) > 0 .and. !empty( x3Combo(cField, (self:cAlias)->&(cField)) )
			do case	
				case self:aStruct[nX]:getType() == "date" 
					self:jData[cProperty] := self:varToTimeStamp((self:cAlias)->&(cField))
                case lCombo
					self:jData[cProperty] := x3Combo(cField, (self:cAlias)->&(cField))
				otherwise
					self:jData[cProperty] := (self:cAlias)->&(cField)
			endcase
		endif
	next nX

return


//-------------------------------------------------------------------
/*/{Protheus.doc} validEnvironment
    Método para validação do Ambiente.
        - Existência do grupo de perguntas (SX1)
        - Versão mínima da LIB
    @author SQUAD SUPRIMENTOS
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method validEnvironment() class GCPIntegratedProvider

	if !(self:lLibOK)
		self:setErrorInvalidLib()
	endif

    if !(self:lPergunteOK)
		self:setErrorInvalidPergunte()
	endif

return self:lLibOK .and. self:lPergunteOK


//-------------------------------------------------------------------
/*/{Protheus.doc} setErrorInvalidLib
    Método para 'setar' o erro 400 HTTP
        - Notificar o usuário sobre a versão mínima da LIB
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setErrorInvalidLib() class GCPIntegratedProvider
    
	self:setErrorStatus(400, STR0005, STR0006 + self:cMinimumLibLabel)  //"Versão de Lib inválida", "Necessário Lib Label igual ou superior a "
	FwLogMsg("WARN",,STR0005,,,,STR0006 + self:cMinimumLibLabel,,,)     //"Versão de Lib inválida", "Necessário Lib Label igual ou superior a " 

return


//-------------------------------------------------------------------
/*/{Protheus.doc} setErrorInvalidPergunte
    Método para 'setar' o erro 400 HTTP
        - Notificar o usuário sobre a ausência do grupo de 
        perguntas no dicionário (SX1)
    @since 16/01/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method setErrorInvalidPergunte() class GCPIntegratedProvider
    
	self:setErrorStatus(400, STR0002, STR0003 + self:cSX1Group )    //"Sem grupo de perguntas", "Grupo de perguntas não encontrado: "
	FwLogMsg("WARN",, STR0004,,, , STR0003 + self:cSX1Group , , ,)  //"Smart View, "Grupo de perguntas não encontrado: ""

return

/*/{Protheus.doc} enableFilterByBranch
		Método utilizado para habilitar a utilização de filtro por Filial
		sem a necessidade de criação do parâmetro no dicionário de perguntas (SX1)
	@since 23/10/2024
	@version 12.1.2310
	/*/
method enableFilterByBranch(cAlsBase as character) class GCPIntegratedProvider

default cAlsBase := ""

    if self:lLibLookUpOK
        self:oSchema:addParameter("SV_MULTBRANCH", STR0007, "string", .T.)	// "Qual Filial/Filiais?"
        self:setCustomURL("SV_MULTBRANCH","/api/gcp/smartview/v1/options/getBranches/" + cAlsBase,2)
    endif

return
