#INCLUDE "Protheus.ch"
#INCLUDE "CTBXMI.ch"

//-------------------------------------------------------------------
/*/{Protheus.doc}CT102LOK
Valida existencia de NIT en comprobantes contables Colombia
Funcion que se utiliza en fuente CTBA102 en func Ctba102Lan
Especifico da colombia (Usado en CT105TOk )
@return lRet - .T. si encuentra datos de cuenta
@author Totvs
@since  25/09/2018
@version 12
@return
/*/
Function CT102LOK()
Local aArea     := GetArea()
Local lRet		:= .T.
Local cLinha    := ""
Local cTipoDC   := ""

nRecTMP := TMP->(Recno())
TMP->(dbGoTop())

While !TMP->(Eof()) .And. lRet
	If !TMP->CT2_FLAG
		cLinha    := TMP->CT2_LINHA
		cTipoDC   := TMP->CT2_DC

		If cTipoDC=="1" 	//D?bito
			lRet := VldColE05(TMP->CT2_DEBITO,TMP->CT2_EC05DB,"CT2_EC05DB",cTipoDC,cLinha)
		ElseIf cTipoDC=="2" //Cr?dito
			lRet := VldColE05(TMP->CT2_CREDIT,TMP->CT2_EC05CR,"CT2_EC05CR",cTipoDC,cLinha)
		ElseIf cTipoDC=='3'	//Partida Dobrada
			lRet := (VldColE05(TMP->CT2_DEBITO,TMP->CT2_EC05DB,"CT2_EC05DB","1",cLinha) .And.;
					 VldColE05(TMP->CT2_CREDIT,TMP->CT2_EC05CR,"CT2_EC05CR","2",cLinha))
		EndIf
	EndIf
	TMP->(dbSkip())
EndDo

TMP->(dbGoTo(nRecTMP))

RestArea(aArea)

Return(lRet)

//-------------------------------------------------------------------
/*/{Protheus.doc}VldColE05
Espec?fico para Col?mbia.
Valida??o de NIT (entidade 5) de acordo com a parametriza??o da conta
no Plano de Contas CT1
@author Totvs
@since  29/06/2018
@version 12
/*/
Static Function VldColE05(cConAux,cEnti05,cCpoAux,cTipoDC,cLinAux)
Local lRet 		:= .T.
Local lVldEnt   := .T.

Default cConAux := ""
Default cEnti05 := ""
Default cCpoAux := ""
Default cTipoDC := ""
Default cLinAux := ""

CT1->(dbSetORder(1))
If CT1->(dbSeek(xFilial("CT1")+AllTrim(cConAux)))
	If CT1->(FieldPos("CT1_05OBRG"))>0
		lVldEnt := (CT1->CT1_05OBRG=="1") //Se o campo CT1_05OBRG Existir, s? valido o preenchimento se a entidade 05 foi parametrizada como obrigat?ria.
	EndIf

	If !Empty(cEnti05)
	   	CV0->(dbSetOrder(1))
	   	If !CV0->(dbSeek(xFilial("CV0")+"01"+AllTrim(cEnti05)))
			MsgInfo(OemToAnsi(STR0001) + cEnti05 + OemToAnsi(IIf(cTipoDC=="1",STR0002,STR0007)) + cLinAux + OemToAnsi(STR0003) )//El NIT /  en debito de la linea / no existe
			lRet := .F.
		EndIf
	ElseIf lVldEnt
		MsgInfo(OemToAnsi(IIf(cTipoDC=="1",STR0004,STR0008)) + RTrim(FWX3Titulo(cCpoAux)) + " - " +  cCpoAux + OemToAnsi(STR0005) + cLinAux + OemToAnsi(STR0006))//El Campo NIT de Debito( / ) de la linea /  esta vac?o
		lRet := .F.
   EndIf
Else
   MsgInfo(OemToAnsi(STR0009)+AllTrim(cConAux)+OemToAnsi(STR0010)+cLinAux+".")
   lRet := .F.
EndIf

Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc}CtxMod22Pt
Executa visao para utilizacao no arquivo modelo22.ini para gerar
declaracao modelo 22 para Portugal
@author Totvs
@since  12/08/11
@version 12
/*/
Function CtxMod22Pt(aParam, aVisao, cAliasQ12, cAlias396, cAlias397)
Local aQuadro12 := {}
Local aParQdo12 := { 0,0.00 }
Local bAddQ12   := {|oLstBox|	CtbQ12Add(NIL, aParQdo12, oLstBox, aQuadro12) }
Local bDelQ12   := {|oLstBox|	CtbQ12Del(oLstBox, aQuadro12) }

Local aQ9_396    := {}
Local aParQ9396  := { 0.00,0 }
Local bAddQ9_396 := {|oLstBox|	CtQ9396Add(NIL, aParQ9396, oLstBox, aQ9_396) }
Local bDelQ9_396 := {|oLstBox|	CtQ9396Del(oLstBox, aQ9_396) }

Local aQ9_397    := {}
Local aParQ9397  := { 0.00,0 }
Local bAddQ9_397 := {|oLstBox|	CtQ9397Add(NIL, aParQ9397, oLstBox, aQ9_397) }
Local bDelQ9_397 := {|oLstBox|	CtQ9397Del(oLstBox, aQ9_397) }
Local aStruct

	aVisao := GetSldPlGer( aParam[10][1]/*cSetOfBook*/, STOD(aParam[1][1])/*dDataIni*/, STOD(aParam[1][2])/*dDataFim*/, "01"/*cMoeda*/, 4000/*nTamArray*/, .T./*lAbs*/, .T./*lTdsRgs*/, "1"/*cSaldos*/, /*lNImpMov*/,;
                      /*cHeader*/, /*lImpAntLP*/, /*dDataLP*/, /*lVlrZerado*/, /*cMoedaDesc*/, /*lMovPeriodo*/, /*lPlGerSint*/, /*lConsSaldo*/)

    //criacao dos arquivos temporarios
	//Quadro 12
	aStruct := {}
	aAdd(aStruct,{"LINHA",			"N",	5,	0})
	aAdd(aStruct,{"NIPC",			"N",	9,	0})
	aAdd(aStruct,{"RETFTE",			"N",	13,	2})

    oTmpTableQ12 := FWTemporaryTable():New(cAliasQ12,aStruct)
    oTmpTableQ12:Create()

	//Quadro 09 - Campo 396
	aStruct := {}
	aAdd(aStruct,{"LINHA",			"N",	5,	0})
	aAdd(aStruct,{"PREJUIZOS",		"N",	13,	2})
	aAdd(aStruct,{"NIF",			"N",	9,	0})

    oTmpTable396 := FWTemporaryTable():New(cAlias396,aStruct)
    oTmpTable396:Create()

	//Quadro 09 - Campo 397
	aStruct := {}
	aAdd(aStruct,{"LINHA",			"N",	5,	0})
	aAdd(aStruct,{"VLRUTILIZ",		"N",	13,	2})
	aAdd(aStruct,{"NIF",			"N",	9,	0})

    oTmpTable397 := FWTemporaryTable():New(cAlias397,aStruct)
    oTmpTable397:Create()

	If aParam[12][1]=="T"
		If File(Alltrim(mv_par05)+Alltrim(mv_par04))
			CtbCarrega(aQuadro12, "23")
		EndIf
		If Len(aQuadro12) > 0
			CtbLstBM22( /*oDlg*/, STR0025+"12",{STR0026,STR0027,STR0028}, aQuadro12, bAddQ12, bDelQ12) //Quadro ##"Linha" ##"Num.Identificacao" ##"Retencao na Fonte"
		Else
			CtbLstBM22( /*oDlg*/, STR0025+"12",{STR0026,STR0027,STR0028}, {{0,0,0}}, bAddQ12, bDelQ12)//Quadro ##"Linha" ##"Num.Identificacao" ##"Retencao na Fonte"
		EndIf
		CtbPopTrb(cAliasQ12, aQuadro12)
	EndIf

	If aParam[12][2]=="T"
		If File(Alltrim(mv_par05)+Alltrim(mv_par04))
			CtbCarrega(aQ9_396, "24")
		EndIf
		If Len(aQ9_396) > 0
			CtbLstBM22( /*oDlg*/,STR0025 + "09 - "+STR0030+"396",{STR0026,STR0029,"NIF"}, aQ9_396, bAddQ9_396, bDelQ9_396) //Quadro ##Campo ##Linha ##Prejuizos
		Else
			CtbLstBM22( /*oDlg*/,STR0025 + "09 - "+STR0030+"396",{STR0026,STR0029,"NIF"}, {{0,0,0}}, bAddQ9_396, bDelQ9_396) //Quadro ##Campo ##Linha ##Prejuizos
		EndIf
		CtbPopTrb(cAlias396, aQ9_396)
	EndIf

	If aParam[12][3]=="T"
		If File(Alltrim(mv_par05)+Alltrim(mv_par04))
			CtbCarrega(aQ9_397, "25")
		EndIf
		If Len(aQ9_397) > 0
			CtbLstBM22( /*oDlg*/,STR0025 + "09 - "+STR0030+"397",{STR0026,STR0031,"NIF"}, aQ9_397, bAddQ9_397, bDelQ9_397) //Quadro ##Campo ##Linha ##"Valor Utilizado en el Periodo"
		Else
			CtbLstBM22( /*oDlg*/,STR0025 + "09 - "+STR0030+"397",{STR0026,STR0031,"NIF"}, {{0,0.00,0}}, bAddQ9_397, bDelQ9_397) //Quadro ##Campo ##Linha ##"Valor Utilizado en el Periodo"
		EndIf
		CtbPopTrb(cAlias397, aQ9_397)
	EndIf

Return


Static Function CtbPopTrb(cAlias, aDados)
	Local nX, nY
	Local aArea := GetArea()

	dbSelectArea(cAlias)

	For nX := 1 TO Len(aDados)
		RecLock(cAlias,.T.)
		For nY := 1 TO Len(aDados[nX])
			FieldPut(nY, aDados[nX, nY])
		Next
		MsUnLock()
		FkCommit()
	Next

	RestArea(aArea)

Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtbQ12Add ?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Adiciona linha para Quadro 12                               ???
???          ?                                                            ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtbQ12Add(oDlg, aDados, oLstBox, aQuadro12)
	Local aDadosAux
	Local aParam := {	{ 1 ,STR0027, aDados[1] ,"@E 999999999",""  ,"" ,".T." ,65 ,.T. }, ; //"Num.Identificacao"
						{ 1 ,STR0028, aDados[2] ,"@E 9,999,999,999.99" 	 ,""  ,"" ,".T." ,65 ,.T. } } //"Retencao na Fonte"

	ParamBox(aParam ,"Quadro12", aDados,,,.F.,120,3, oDlg,,.F.,.F.)

	aDadosAux := { 0, aDados[1], aDados[2] }
	aDados := {0,0}
	If Len(aQuadro12) == 1 .And. aQuadro12[1,1]=0
		aQuadro12 := {}
	EndIf
	aAdd(aQuadro12, aClone(aDadosAux) )
	aQuadro12[Len(aQuadro12), 1] := Len(aQuadro12)

	oLstBox:SetArray(aQuadro12)
	oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
	oLstBox:Refresh()

Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtbQ12Del ?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Exclui linha do Quadro 12                                   ???
???          ?                                                            ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtbQ12Del(oLstBox, aQuadro12)
	Local nPosAt := oLstBox:nAT
	Local nX

	If Len(aQuadro12) > 1
		aDel(aQuadro12, nPosAt)
		aSize(aQuadro12, Len(aQuadro12)-1)
		For nX := 1 TO Len(aQuadro12)
			aQuadro12[nX, 1] := nX
		Next
		oLstBox:SetArray(aQuadro12)
		oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
		oLstBox:Refresh()
	EndIf

Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtQ9396Add?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Adiciona linha para quadro referente campo 396              ???
???          ?                                                            ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtQ9396Add(oDlg, aDados, oLstBox, aQ9_396)
	Local aDadosAux
	Local aParam := {	{ 1 ,STR0029, aDados[2] ,"@E 9,999,999,999.99" 	 ,""  ,"" ,".T." ,65 ,.T. }, ; //"Prejuizos"
						{ 1 ,"NIF", aDados[1] ,"@E 999999999",""  ,"" ,".T." ,65 ,.T. } }

	ParamBox(aParam ,STR0025 + "09 - "+STR0030+"396", aDados,,,.F.,120,3, oDlg,,.F.,.F.) //"Cuadro " ##"Campo "
	aDadosAux := { 0, aDados[1], aDados[2] }
	aDados := {0,0}
	If Len(aQ9_396) == 1 .And. aQ9_396[1,1]=0
		aQ9_396 := {}
	EndIf
	aAdd(aQ9_396, aClone(aDadosAux) )
	aQ9_396[Len(aQ9_396), 1] := Len(aQ9_396)

	oLstBox:SetArray(aQ9_396)
	oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
	oLstBox:Refresh()

Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtQ9396Del?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Exclui linha do quadro referente ao campo 396               ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtQ9396Del(oLstBox, aQ9_396)
	Local nPosAt := oLstBox:nAT
	Local nX

	If Len(aQ9_396) > 1
		aDel(aQ9_396, nPosAt)
		aSize(aQ9_396, Len(aQ9_396)-1)
		For nX := 1 TO Len(aQ9_396)
			aQ9_396[nX, 1] := nX
		Next
		oLstBox:SetArray(aQ9_396)
		oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
		oLstBox:Refresh()
	EndIf

Return

/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtQ9397Add?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Adiciona linha para quadro campo 397                        ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtQ9397Add(oDlg, aDados, oLstBox, aQ9_397)
	Local aDadosAux

	Local aParam := {{ 1 ,STR0031, aDados[2] ,"@E 9,999,999,999.99" 	 ,""  ,"" ,".T." ,65 ,.T. }, ; //"Valor Utilizado no Periodo"
					{  1 ,"NIF", aDados[1] ,"@E 999999999",""  ,"" ,".T." ,65 ,.T. } }

	ParamBox(aParam,STR0025 + "09 - "+STR0030+"397" ,aDados,,,.F.,120,3,oDlg,,.F.,.F.) //"Cuadro " ##"Campo "

	aDadosAux := { 0, aDados[1], aDados[2] }
	aDados := {0.00,0.00}
	If Len(aQ9_397) == 1 .And. aQ9_397[1,1]=0
		aQ9_397 := {}
	EndIf
	aAdd(aQ9_397, aClone(aDadosAux) )
	aQ9_397[Len(aQ9_397), 1] := Len(aQ9_397)

	oLstBox:SetArray(aQ9_397)
	oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
	oLstBox:Refresh()

Return

/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtQ9397Del?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Excluir linha do quadro para o campo 397                    ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtQ9397Del(oLstBox, aQ9_397)
	Local nPosAt := oLstBox:nAT
	Local nX

	If Len(aQ9_397) > 1
		aDel(aQ9_397, nPosAt)
		aSize(aQ9_397, Len(aQ9_397)-1)
		For nX := 1 TO Len(aQ9_397)
			aQ9_397[nX, 1] := nX
		Next
		oLstBox:SetArray(aQ9_397)
		oLstBox:bLine := { ||{oLstBox:aArray[oLstBox:nAT][1],oLstBox:aArray[oLstBox:nAT][2],oLstBox:aArray[oLstBox:nAT][3]}}
		oLstBox:Refresh()
	EndIf
Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtbCarrega?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Carrega os dados para registros 23/24/25                    ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtbCarrega(aDados, cRegistro)
	Local cArqTxt := Alltrim(mv_par05)+Alltrim(mv_par04)
	Local nHandle
	Local cLinha
	Local aTxt := {}
	Local aAux := {}
	Local nX

	If (nHandle := FT_FUse(cArqTxt)) == -1
		Help(" ",1,"NOFILEIMPOR")
		Return
	EndIf

	FT_FGOTOP()
	cLinha := FT_FREADLN()
	While !FT_FEOF()
		cLinha := FT_FREADLN()
		AADD(aTxt,cLinha)
		FT_FSKIP()
	EndDo
	FT_FUSE()

	For nX := 1 TO Len(aTxt)
		If Subs(aTxt[nX],1,2) == cRegistro
			If cRegistro == "23"
				aAux := {}
				aAdd(aAux, Val(Subs(aTxt[nX],3,5)))
				aAdd(aAux, Val(Subs(aTxt[nX],8,9)))
				aAdd(aAux, Val(Subs(aTxt[nX],17,12))/100)
			ElseIf cRegistro == "24"
				aAux := {}
				aAdd(aAux, Val(Subs(aTxt[nX],3,5)))
				aAdd(aAux, Val(Subs(aTxt[nX],8,12))/100)
				aAdd(aAux, Val(Subs(aTxt[nX],20,9)))
			ElseIf cRegistro == "25"
				aAux := {}
				aAdd(aAux, Val(Subs(aTxt[nX],3,5)))
				aAdd(aAux, Val(Subs(aTxt[nX],8,12))/100)
				aAdd(aAux, Val(Subs(aTxt[nX],20,9)))
			EndIf
			aAdd(aDados, aClone(aAux))
		EndIf
	Next

Return


/*
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
???Programa  ?CtbLstBM22?Autor  ?Microsiga           ? Data ?  12/08/11   ???
?????????????????????????????????????????????????????????????????????????????
???Desc.     ?Funcao para montar list box com informacoes quadro 12 /     ???
???          ?campo 396 / campo 397                                       ???
?????????????????????????????????????????????????????????????????????????????
???Uso       ? AP                                                         ???
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
?????????????????????????????????????????????????????????????????????????????
*/
Static Function CtbLstBM22(oDlg, cTitulo, aColunas, aLstRet, bAddLin, bDelLin, bConfirma)
	Local oPanel, oBmp, oListBox, oAdiciona, oCanc, oGrava
	Local lDlg := .T.
	Local lOk  := .F.

	Default oDlg      := NIL
	Default aColunas  := {{"",""}}
	Default aLstRet   := {{"",""}}
	Default bAddLin   := {||.T.}
	Default bDelLin   := {||.T.}
	Default bConfirma := {||.T.}

	lDlg := lDlg .And. (oDlg == NIL)
	lDlg := lDlg .And. ( ValType(oDlg)!="O" )

	If lDlg
		DEFINE MSDIALOG oDlg FROM 00,00 TO 290,490 PIXEL TITLE OemToAnsi(cTitulo)
	EndIf

	oPanel       := TPanel():New(1,1,'',oDlg,oDlg:oFont, .T., .T.,,,235,125,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_ALLCLIENT

	@ 0,0 BITMAP oBmp RESNAME "PROJETOAP" Of oDlg SIZE 100,300 NOBORDER When .F. PIXEL
	oListBox := TWBrowse():New( 20,05,235,125,,aColunas,,oPanel,,,,,,,,,,,,.F.,,.T.,,.F.,,,)
	oListBox:SetArray(aLstRet)
	oListBox:bLine := { ||{aLstRet[oListBox:nAT][1],aLstRet[oListBox:nAT][2], aLstRet[oListBox:nAT][3]}}
	oListBox:bLDblClick := { ||Eval(oAdiciona:bAction)}

	@ 05,05 BUTTON oAdiciona Prompt STR0032  SIZE 45 ,10  FONT oDlg:oFont ACTION Eval(bAddLin,oListBox)            OF oPanel PIXEL //"Adiciona Linha"
	@ 05,75 BUTTON oCanc     Prompt STR0033  SIZE 45 ,10  FONT oDlg:oFont ACTION Eval(bDelLin,oListBox)            OF oPanel PIXEL //"Exclui Linha"
	@ 05,175 BUTTON oGrava   Prompt STR0034  SIZE 65 ,10  FONT oDlg:oFont ACTION (lOk:=Eval(bConfirma),oDlg:End()) OF oPanel PIXEL //"Confirma os Dados"

	If lDlg
		ACTIVATE MSDIALOG oDlg CENTERED
	EndIf

Return(aLstRet)

/*/{Protheus.doc} CTR051QCol
	Colombia: CTBR051 - Balance parcial x Entidad
	Define parte de query para discriminar saldos anteriores de las cuentas de resultados.
	@type    Function
	@author  ARodriguez
	@since   10/09/2022
	@version 1.0
	@param   cQryInterna, string, sentencia SQL parcial
	@return  lRet, logical, ejecutó tratamiento?
	/*/
Function CTR051QCol(nColProc, nQtdValues, cAlias, cQryInterna)
Local cSubString	:= ""
Local cCtasResult	:= ""
Local cFecResult	:= ""
Local cDeb			:= ""
Local cCred			:= ""
Local nX			:= 0
Local lRet			:= .F.

/* Preguntas adicionales en el grupo de preguntas CTR051 del informe para CTBR051:
 * MV_PAR11 ¿Ignora Sld Ant.Ings/Gtos ? 1-Si, 2-No
 * MV_PAR12 ¿Grupos Ingresos/Gastos ? "4,5,6"
 * MV_PAR13 ¿Fch Sld Ant. Ingresos/Gast. ? 31/12/aaaa
*/

If MV_PAR11 == 1 .And. !Empty(MV_PAR12) .And. !Empty(MV_PAR13)
	/* Para uso de la función Substring() en otras BD diferentes a SQL Server, considerar enviar por parámetro el objeto Ctb_Exec_Cube() desde CTBXCUB
		Case Self:cDBType == "MSSQL" .Or. Self:lPostgres
			cString := "SUBSTRING(CVX.CVX_NIV01,1,1)"
		Case Self:lOracle .Or. Self:lDB2
			cString := "SUBSTR(CVX.CVX_NIV01,1,1)"
		Case Self:lInformix
			cString := "SUBSTRING(CVX.CVX_NIV01 FROM 1 FOR 1)"
	*/
	cSubString := "SUBSTRING(CVX.CVX_NIV01,1,1)"
	cCtasResult := "'" + Strtran(Trim(MV_PAR12), ",", "','") + "'"
	cFecResult := DTOS(MV_PAR13)
	cDeb := cAlias + "." + cAlias + "_SLDDEB"
	cCred := cAlias + "." + cAlias + "_SLDCRD"

	For nX := 1 TO nQtdValues
		cQryInterna += IIf( nX == nColProc, ", SUM(CASE WHEN " + cSubString + " IN (" + cCtasResult + ") AND CVX.CVX_DATA <= '" + cFecResult + "' THEN 0 ELSE " + cDeb + " END)", ", 0") + " CVX_SLDB" + StrZero(nX,2)
		cQryInterna += IIf( nX == nColProc, ", SUM(CASE WHEN " + cSubString + " IN (" + cCtasResult + ") AND CVX.CVX_DATA <= '" + cFecResult + "' THEN 0 ELSE " + cCred + " END)", ", 0") + " CVX_SLCR" + StrZero(nX,2)
		cQryInterna += IIf( nX == nColProc, ", SUM(CASE WHEN " + cSubString + " IN (" + cCtasResult + ") AND CVX.CVX_DATA <= '" + cFecResult + "' THEN 0 ELSE " + cCred + "-" + cDeb + " END)", ", 0") + " CVX_SALD" + StrZero(nX,2)
	Next

	lRet := .T.
EndIf

Return lRet

/*/{Protheus.doc} Ct211ValE5
	Valida si el NIT de cálculo para PyG está registrado
	@type    Function
	@author  ARodriguez
	@since   23/01/2023
	@version 1.0
	@param   cEnt05, caracter, Código Entidad 05
	@param   cEnt05LP, caracter, Código Entidad 05 PyG; pasado por referencia
	@param   lEnt05Ok, lógica, Código válido?; pasado por referencia
	@param   lCadastro, lógica, Usa código registrado en catálog?
	@return  Nil
/*/
Function Ct211ValE5(cEnt05,cEnt05LP,lEnt05Ok,lCadastro)
Local aSaveArea	:= GetArea()
Local cPlano	:= "01"
Local cAliasEnt	:= "CV0"
Local cEntLP	:= ""

dbSelectArea("CT0")
dbSetOrder(1)
If dbSeek(xFilial("CT0")+"05")
	cPlano    := CT0->CT0_ENTIDA
	cAliasEnt := CT0->CT0_ALIAS
EndIf

dbSelectArea(cAliasEnt)
dbsetOrder(1)
msSeek(xFilial(cAliasEnt)+IIf(cAliasEnt=="CV0",cPlano,"")+cEnt05)
cEntLP := (cAliasEnt)->&(cAliasEnt+"_LUCPER")

If lCadastro//Se utiliza Entidad 05 LP do Cadastro
	If Found()	// Si no existe o no tiene entidad LP, asigna código origen
		If Empty(cEntLP)
			lEnt05Ok 	:= .T.
			cEnt05LP	:= cEnt05
		Else
			lEnt05Ok 	:= .T.
			cEnt05LP	:= cEntLP
		Endif
	Else
		lEnt05Ok 	:= .T.
		cEnt05LP	:= cEnt05
	Endif
Else
	If Subs(cEntLP,1,1) = "*"
		lEnt05Ok	:= .F.
	EndIf
	msSeek(xFilial(cAliasEnt)+IIf(cAliasEnt=="CV0",cPlano,"")+cEnt05LP)
	If lEnt05Ok .And. !Found()
		lEnt05Ok	:= .F.
	EndIf
EndIf

RestArea(aSaveArea)

Return

/*/{Protheus.doc} Ct215Resul
	Verifica Lote y Sublote en asientos contables (CT2)
	Determina si son de un Cálculo de Resultado o de Cierre de cuentas de Balance
	@type    Function
	@author  ARodriguez
	@since   01/02/2023
	@version 1.0
	@param   lResultado, lógica, Asigna .T. = Cálculo de resultado / .F. = Cierre de Cuentas de Balance
	@param   dDataLP, fecha, Fecha de cálculo
	@param   cLote, caracter, Número de Lote
	@param   cSublote, caracter, Número de Sublote
	@param   cDoc, caracter, Número de Documento
	@param   cDocFin, caracter, Número de Documento final
	@return  lRet, lógica, .T.=  Continuar / .F. = Cancelar
	/*/
Function Ct215Resul(lResultado, dDataLP, cLote, cSublote, cDoc, cDocFin)
Local aSaveArea		:= GetArea()
Local cDebitoIni	:= ""
Local cCreditIni	:= ""
Local cDebitoFin	:= ""
Local cCreditFin	:= ""
Local cContaIni		:= ""
Local cContaFim		:= ""
Local cDocBusc		:= ""
Local lContinua		:= .F.
Local lRet			:= .T.

If cDoc <= cDocFin
	dbSelectArea("CT2")
	dbSetOrder(1)	// CT2_FILIAL+DTOS(CT2_DATA)+CT2_LOTE+CT2_SBLOTE+CT2_DOC+CT2_LINHA+CT2_TPSALD+CT2_EMPORI+CT2_FILORI+CT2_MOEDLC
	// Busca primer asiento del Lote + Sublote + Documento
	cDocBusc := cDoc
	msSeek(xFilial("CT2")+DtoS(dDataLP)+cLote+cSublote+cDoc, .T.)

	If CT2->(CT2_DATA == dDataLP .And. CT2_LOTE == cLote .And. CT2_SBLOTE == cSublote .And. CT2_DOC == cDoc)
		cDebitoIni := CT2->CT2_DEBITO
		cCreditIni := CT2->CT2_CREDIT

		// Busca última línea del Lote + Sublote + Documento final
		cDocBusc := Soma1(cDocFin)
		msSeek(xFilial("CT2")+DtoS(dDataLP)+cLote+cSublote+cDocBusc, .T.)
		CT2->(dbSkip(-1))
		cDocBusc := cDocFin
		If CT2->(CT2_DATA == dDataLP .And. CT2_LOTE == cLote .And. CT2_SBLOTE == cSublote .And. CT2_DOC == cDocFin)
			lContinua := .T.
		Else
			msSeek(xFilial("CT2")+DtoS(dDataLP)+cLote+cSublote+cDocFin, .T.)
			lContinua := (CT2->(CT2_DATA == dDataLP .And. CT2_LOTE == cLote .And. CT2_SBLOTE == cSublote .And. CT2_DOC == cDocFin))
		EndIf

		If lContinua
			cDebitoFin := CT2->CT2_DEBITO
			cCreditFin := CT2->CT2_CREDIT
		EndIf
	EndIf

	If !lContinua
		Help(NIL, NIL, STR0035, NIL, STR0037 + cLote + "/" + cSublote + "/" + cDocBusc + STR0038, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0039})	//"¡ATENCION !"##"El Lote / Sublote / Documento "##" no se encontraron en asientos contables."##"Configure correctamente los parámetros del proceso."
		ProcLogAtu("ERRO","Parámetros",STR0037 + cLote + "/" + cSublote + "/" + cDocBusc + STR0038)	//"El Lote / Sublote / Documento "##" no se encontraron en la tabla de asientos contables."
		lRet := .F.
	EndIf

Else
	Help(NIL, NIL, STR0035, NIL, STR0036, 1, 0, NIL, NIL, NIL, NIL, NIL, {STR0039})	//"¡ATENCION !"##"El número de documento final debe ser mayor o igual al documento inicial."##"Configure correctamente los parámetros del proceso."
	ProcLogAtu("ERRO","Parámetros",STR0036) // "El número de documento final debe ser mayor o igual al documento inicial."
	lRet := .F.

EndIf

If lContinua
	// Determina cuenta inicial y final descartando la de cálculo (común en el primer y último asiento)
	If cDebitoIni == cDebitoFin
		cContaIni := cCreditIni
		cContaFim := cCreditFin
	ElseIf cDebitoIni == cCreditFin
		cContaIni := cCreditIni
		cContaFim := cDebitoFin
	ElseIf cCreditIni == cCreditFin
		cContaIni := cDebitoIni
		cContaFim := cDebitoFin
	ElseIf cCreditIni == cDebitoFin
		cContaIni := cDebitoIni
		cContaFim := cCreditFin
	Else
		// Todas la cuentas son diferentes, continuar con proceso estándar
		lContinua := .F.
	EndIf

	If lContinua
		// Determina si los rangos de cuentas son de resultados
		lResultado := ( cContaIni >= "4" .And. cContaFim < "8" )
	EndIf
EndIf

RestArea(aSaveArea)

Return lRet
