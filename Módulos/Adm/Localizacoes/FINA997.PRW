#INCLUDE "FINA997.ch"
#INCLUDE "Protheus.ch"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัออออออออออออออออปฑฑ
ฑฑบPrograma    ณ FINA997  บAutor  ณ	Bruno Schmidt    บ Data ณ 20/02/2013     บฑฑ
ฑฑฬออออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯออออออออออออออออนฑฑ
ฑฑบDesc.       ณLe o arquivo de retorno 			                              บฑฑ
ฑฑฬออออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso         ณSigaFis                                                        บฑฑ
ฑฑณProgramador ณ Data   ณ   BOPS   ณ           Motivo da Alteracao             ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณgsantacruz  ณ09/06/17ณ  MMI-5915ณSe replica el fuente para 12.1.14 ya se    ณฑฑ
ฑฑณ            ณ        ณ          ณmodifica el uso de tablas temporales CTREE ณฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/      
Function FINA997()                     


Local aCarga	:= {}

Local cTitulo	:= ""
Local cErro		:= ""
Local cSolucao	:= ""

Local lCarga 	:= .F.
Private oTmpTable		:= Nil

If FINA997Wiz()	   
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณCarrega no arquivo temporario as informacoes do arquivo de retornoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		Processa({|| aCarga := FINA997Le()})
		lCarga := aCarga[01]   
		If lCarga
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณAtualiza tabelas com as informacoes do retornoณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			Processa({|| FINA997Atu(aCarga[02])})
			 
		Else
			cTitulo 	:= STR0024		//"Importa็ใo nใo realizada"	
			cErro		:= STR0025		//"A importa็ใo do arquivo de retorno nใo foi realizada "
			cErro		+= STR0026		//"por nใo existirem informa็๕es de retorno no arquivo "
			cErro		+= STR0027		//"texto informado. "
			cSolucao	:= STR0028		//"Verifique se o arquivo de retorno informado nas "
			cSolucao	+= STR0029		//"perguntas da rotina ้ o enviado pela Reproweb "
			cSolucao	+= STR0030		//"e processe esta rotina novamente."
			xMagHelpFis(cTitulo,cErro,cSolucao)
		Endif
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณExcluindo o arquivo temporario criadoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If oTmpTable <> Nil 
				oTmpTable:Delete()
				oTmpTable := Nil
		EndIf
		
Else
	cTitulo 	:= STR0018 				//"Implementa็ใo nใo efetuada"	
	cErro		:= STR0019 				//"A implementa็ใo do processo de atualiza็ใo"
	cErro		+= STR0020 				//"Reproweb nใo foi efetuada corretamente."
	cSolucao	:= STR0021 				//"verifique a documenta็ใo que acompanha a rotina e "
	cSolucao	+= STR0022 				//"execute todos os procedimentos indicados e processe "
	cSolucao	+= STR0023 				//"esta rotina novamente."
	xMagHelpFis(cTitulo,cErro,cSolucao)
Endif

Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFINA997Wiz  บAutor  ณ   Bruno Schmidt    บ Data ณ 20/02/2013  บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณMonta a wizard com as perguntas a rotina de importacao        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณReproweb                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function FINA997Wiz()

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณDeclaracao das variaveisณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
Local aTxtPre 		:= {}
Local aPaineis 		:= {}

Local cTitObj1		:= ""                
Local cMask			:= Replicate("X",245)
	
Local nPos			:= 0
	
Local lRet			:= 0
Local cCab			:= STR0001 //"Importa็ใo do Arquivo Reproweb"
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณMonta wizard com as perguntas necessariasณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aAdd(aTxtPre,STR0001) //"Importa็ใo do Arquivo "
aAdd(aTxtPre,STR0002) //"Aten็ใo"
aAdd(aTxtPre,STR0003) //"Preencha corretamente as informa็๕es solicitadas."
aAdd(aTxtPre,Alltrim(STR0004)+" "+cCab+Alltrim(STR0005)+" "+Alltrim(STR0006))	
										//"Esta rotina ira importar o arquivo de retorno disponibilizado pela Reproweb       "
										//", contendo informa็๕es sobre fornecedores     "
										//"em um deternimado perํodo."

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPainel 1 - Informacoes da Empresa    ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
aAdd(aPaineis,{})
nPos :=	Len(aPaineis)
aAdd(aPaineis[nPos],STR0007) //"Assistente de parametriza็ใo" 
aAdd(aPaineis[nPos],STR0008) //"Informa็๕es sobre o arquivo de retorno: "
aAdd(aPaineis[nPos],{})

cTitObj1 :=	STR0009 //"Arquivo de retorno: "
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{1,cTitObj1,,,,,,})
aAdd(aPaineis[nPos][3],{2,"",cMask,1,,,,245,,.T.})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})
aAdd(aPaineis[nPos][3],{0,"",,,,,,})

lRet :=	xMagWizard(aTxtPre,aPaineis,"FINA997")
	
Return(lRet)   

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFINA997Tmp  บAutor  ณ   Bruno Schmidt    บ Data ณ 20/02/2013  บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณCria o arquivo temporario para importacao                     บฑฑ
ฑฑบ          ณ                                                              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณReproweb                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function FINA997Tmp()

Local aArqPRO   := {}                                 


AADD(aArqPRO,{"CUIT"    ,"C",11 ,0})
AADD(aArqPRO,{"NUMCERT"	,"C",13 ,0})
AADD(aArqPRO,{"BRN"	    ,"C",10 ,0})
AADD(aArqPRO,{"PORC"	,"C",03 ,0})
AADD(aArqPRO,{"RG"      ,"C",04 ,0})
AADD(aArqPRO,{"INN"     ,"C",04 ,0})
AADD(aArqPRO,{"DTEMIS"  ,"C",10 ,0})
AADD(aArqPRO,{"DTPUBLI" ,"C",10 ,0})

AADD(aArqPRO,{"IMPORT"	,"C",01,0}) 
AADD(aArqPRO,{"ERRO"	,"C",35,0}) 

	aOrdem := {"CUIT" }
	oTmpTable := FWTemporaryTable():New("PRO")
	oTmpTable:SetFields(aArqPRO)
	oTmpTable:AddIndex("I1", aOrdem)
	oTmpTable:Create()
	
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ FINA997Le  บAutor  ณ   Bruno Schmidt    บ Data ณ 20/02/2013  บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณLe arquivo de retorno e carrega o arquivo temporario          บฑฑ
ฑฑบ          ณpara atualizar cadastro de Fornecedor.                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณReproweb                                                      บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function FINA997Le

Local aWizard		:= {}
Local aInforma      := {}
	
Local lRet			:= !xMagLeWiz("FINA997",@aWizard,.T.)
	
Local cArqProc		:= Alltrim(aWizard[01][01])
Local cTitulo		:= ""
Local cErro			:= ""
Local cSolucao		:= ""
Local cLinha		:= ""
Local cChar			:= ""

Local lCarga		:= .F.   
Local lArqValido	:= .F.

Local nCont			:= 0
Local nX            := 0 
//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณCria o arquivo temporario para a importacaoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
 FINA997TMP()                                  

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณVerifica se o arquivo existe no diretorio indicadoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If File(cArqProc) .And. !lRet

	nHandle	:=	FOpen(cArqProc)
	nTam	:=	FSeek(nHandle,0,2)
	FSeek(nHandle,0,0)
	ProcRegua(nTam)

	FT_FUse(cArqProc)
	FT_FGotop()

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณVerifica se o arquivo aberto e um arquivo valido com o mesmo CGC do SM0ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	lArqValido 	:= .T.	
	cChar  		:= "."       
	nCont	    := 0
	
	cLinha := ""
  	cLinha := FT_FREADLN()	
	
	If lArqValido 
		FT_FGotop()
		FSeek(nHandle,0,0)
		cChar  := ","		
		cLinha := ""		
		
		While (!FT_FEof()) 		
			IncProc()
			cLinha := ""        
			aDadosSA := {}
		    aInforma := {}	            
   			cLinha := FT_FREADLN()
        	RecLock("PRO",.T.)
        			For nX := 1 to 9
        				If nX == 9
        			   	 	AADD (aInforma,Alltrim(SubStr(cLinha,1,(nPos-1))))
        				Else
	        	        	nPos := at(";",cLinha)
	        	        	AADD (aInforma,Alltrim(SubStr(cLinha,1,(nPos-1))))
	        	        	cLinha := SubStr(cLinha,nPos+1,Len(cLinha))
	        	        EndIf	
        	        nEXT
        	  		PRO->CUIT 	 := aInforma [1]
					PRO->NUMCERT := aInforma [2]
					PRO->BRN	 := aInforma [3]
					PRO->PORC	 := aInforma [5]
					PRO->RG  	 := aInforma [6]
					PRO->INN 	 := aInforma [7]
					PRO->DTEMIS	 := aInforma [8]
					PRO->DTPUBLI := aInforma [9]			
					
			FT_FSkip()
		Enddo 
		lCarga := .T.        
	Else
		lCarga := .F.
	Endif	
	FT_FUse()
	FClose(nHandle)
Else 
	cTitulo 	:= STR0011 						//"Arquivo de importa็ใo nใo localizado"	
	cErro		:= STR0012 + cArqProc			//"Nใo foi localizado no diret๓rio "
	cErro		+= STR0013 + STR0014 	        //" o arquivo "," indicado nas perguntas "
	cErro		+= STR0015 						//"da rotina."
	cSolucao	:= STR0016 						//"Informe o diret๓rio e o nome do arquivo "
	cSolucao	+= STR0017 						//"corretamente e processe a rotina novamente."
	xMagHelpFis(cTitulo,cErro,cSolucao)
	lCarga := .F.
Endif

Return({lCarga,cArqProc})

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ FINA997Atu บAutor  ณ	  Bruno Schmidt    บ Data ณ 20/02/2013  บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณAtualiza tabela do Protheus com as informacoes retornadas     บฑฑ
ฑฑบ          ณpela tabela de Fornecedores                        	        บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/                                                                                                    
Static Function FINA997Atu(cCarga)

ProcRegua(PRO->(LastRec()))
SA2->(dbSetOrder(3))

PRO->(dbGoTop())
While !(PRO->(Eof()))

	IncProc()
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณAtualizando tabelas posicionadasณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If SA2->(MsSeek(xFilial("SA2")+Alltrim(PRO->CUIT)))
		While !SA2->(EOF()) .And. xFilial("SA2") == SA2->A2_FILIAL .And. Alltrim(PRO->CUIT) == Alltrim(SA2->A2_CGC) .and. SA2->A2_CONCSUS $"1246" 
		  	Begin Transaction 
		  	
				RecLock("SA2",.F.) 
				
				SA2->A2_PORSUS	:= 100 - Val(PRO->PORC)
				SA2->A2_DTICALS	:= TrsfData(PRO->DTEMIS)
				SA2->A2_DTFCALS := TrsfData(PRO->DTPUBLI)	
				MsUnLock()
			End Transaction
			SA2->(dbSkip())
		EndDo
		
 		RecLock("PRO",.F.)
 		PRO->ERRO :=STR0041 //"Importado"
		MsUnLock()
 	Else
    	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe houve algum erro na atualizacao das tabelasณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		RecLock("PRO",.F.)
		PRO->IMPORT	:= "1"
		PRO->ERRO	:= STR0031 //"Fornecedor nใo encontrado"
		MsUnLock() 	
	EndIf
	PRO->(dbSkip())
Enddo

Return(.T.)	

/*
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ TransfData บAutor  ณ	  Bruno Schmidt    บ Data ณ 20/02/2013  บฑฑ
ฑฑฬออออออออออุออออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณTransforna a a data recebi do Arquivo						    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿*/ 
Static Function TrsfData(cData)
Local cMes := ""
Local cDia := ""
Local cAno := ""   


cDia	:= Substr(cData,1,2) 
cMes	:= Substr(cData,4,3) 
cAno	:= Substr(cData,8,2) 
cAnoVig := Str(Year(Date()),4,0)
cAnoVig := SubStr(cAnoVig,1,2)
	
Do case
	Case cMes == "JAN"
		cMes:= "01"
	Case cMes == "FEB"
		cMes:= "02"
	Case cMes == "MAR"
		cMes:= "03"
	Case cMes == "APR"
		cMes:= "04"
	Case cMes == "MAY"
		cMes:= "05"
	Case cMes == "JUN"
		cMes:= "06"
	Case cMes == "JUL"
		cMes:= "07"
	Case cMes $ "AGO|AUG"
		cMes:= "08"
	Case cMes $ "SET|SEP"
		cMes:= "09"
	Case cMes == "OCT"
		cMes:= "10"
	Case cMes == "NOV"
		cMes:= "11"
	Case cMes $ "DIC|DEC"
		cMes:= "12"
EndCase


cData:= cDia+"/"+cMes+"/"+cAnoVig+cAno

Return Ctod(cData)

