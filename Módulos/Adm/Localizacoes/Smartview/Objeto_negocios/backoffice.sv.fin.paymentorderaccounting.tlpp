#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.paymentorder.accounting.ch'

namespace custom.financeiro.paymentorderaccounting.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fat.salesinvoice.accounting
@description Classe para creación del Objeto de Negocio de Ordenes de pago Conciliadas para TReports
@author Marcelo Hruschka
@since 28/10/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SA2,SEK', name='Ordenes de pago Conciliadas', country='ALL', initialRelease='12.1.2410', customTables='SA2,SEK,CT2' )
class paymentorderaccountingTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecnoSEK as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class paymentorderaccountingTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Ordenes de pago Conciliadas'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Ordenes de pago Conciliadas'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financieiro'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV509' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	aAdd( aCpos, { 'M0_CGC'	    , STR0007 , 'string'	, STR0007 } ) // 'RFC Emisor'
	aAdd( aCpos, { 'M0_NOMECOM'	, STR0008 , 'string'	, STR0008 } ) // 'Nombre Emisor'
	aAdd( aCpos, { 'EK_FORPGOP'	, STR0009 , 'string'	, STR0009 } ) // 'Forma de Pago'
	aAdd( aCpos, { 'EK_BANCOP'  , STR0010 , 'string'	, STR0010 } ) // 'Banco Pago'
	aAdd( aCpos, { 'CT2_DOCLSL' , STR0011 , 'string'	, STR0011 } ) // 'Doc/Lote/Sublote'
	aAdd( aCpos, { 'CT2_DTACTB' , STR0012 , 'date'		, STR0012 } ) // 'Fecha de Asiento'
	aAdd( aCpos, { 'CT2_TPSLD'  , STR0013 , 'string'	, STR0013 } ) // 'Tipo Saldo'
	aAdd( aCpos, { 'CT2_HISTO'  , STR0014 , 'string'	, STR0014 } ) // 'Historial'

	// demais campos
	self:aFields := {'M0_CGC','M0_NOMECOM','A2_CGC','A2_NOME','EK_EMISSAO','EK_VALOR','EK_VLMOED1','EK_MOEDA','EK_TXMOE02','EK_ORDPAGO','EK_FORNEPG','EK_LOJAPG','EK_FORNECE','EK_LOJA','EK_NUM','EK_PARCELA','EK_TIPO','EK_FILIAL','EK_BANCOP','EK_FORPGOP','CT2_DOCLSL','CT2_DTACTB','CT2_TPSLD','CT2_HISTO'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )
	
//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class paymentorderaccountingTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character
	Local cExp 			as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local oExecB        as object
	Local cAliasA       as character
	Local cAliasB       as character
	Local cChaveCTL     as character
	Local cFilialCTL    as character
	Local cFilialCV3    as character
	
	cFilialCTL := xFilial("CTL")
	cFilialCV3 := xFilial("CV3")

	cChaveCTL := AllTrim(POSICIONE("CTL",1,xFilial("CTL")+"570","CTL_KEY"))
	cChaveCTL := IIF("SQL" $ TcGetDB(),cChaveCTL,StrTran(cChaveCTL,"+","||"))

	cFiltro 			:= ''
	cExp 				:= ''
	cAliasA			    := ''
	cAliasB			    := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA2 := getCpoUser( aCustomFields, 'SA2', 'A2', ',' )
	cCpoSEK := getCpoUser( aCustomFields, 'SEK', 'EK', ',' )
	cCpoCT2 := getCpoUser( aCustomFields, 'CT2', 'CT2', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT
	cQuery += "SM0.M0_CGC,SM0.M0_NOMECOM,SA2.A2_CGC,SA2.A2_NOME,SEK.EK_EMISSAO,?"
	cQuery += "SEK.EK_VALOR,SEK.EK_VLMOED1,?"
	cQuery += "SEK.EK_MOEDA,SEK.EK_TXMOE02,SEK.EK_ORDPAGO,SEK.EK_FORNEPG,SEK.EK_LOJAPG,"
	cQuery += "SEK.EK_FORNECE,SEK.EK_LOJA,SEK.EK_NUM,SEK.EK_PARCELA,SEK.EK_TIPO,SEK.EK_FILIAL,"
	cQuery += "SEKP.EK_TIPODOC EK_FORPGOP,"
	cQuery += "SEKP.EK_BANCO||' / '||SEKP.EK_AGENCIA||' / '||SEKP.EK_CONTA  EK_BANCOP,"
	cQuery += " ?  CHVE, SEK.R_E_C_N_O_  REGSEK"
	cQuery += " FROM " + RetSqlName("SEK") + " SEK "
	cQuery += " INNER JOIN SA2T10 SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = SEK.EK_FORNECE AND SA2.A2_LOJA = SEK.EK_LOJA AND SA2.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN SYS_COMPANY SM0  ON SM0.M0_CODIGO = ? AND SM0.M0_CODFIL = ? AND SM0.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN SEKT10 SEKP ON SEKP.EK_FILIAL = ? AND SEKP.EK_ORDPAGO = SEK.EK_ORDPAGO AND SEKP.EK_TIPODOC = ? AND SEKP.D_E_L_E_T_ = ?"
	cQuery += " WHERE SEK.EK_FILIAL= ?"
	cQuery += "   AND SEK.EK_EMISSAO BETWEEN ? AND ?"
	cQuery += "   AND SEK.EK_FORNECE BETWEEN ? AND ?"
	cQuery += "   AND SEK.EK_CANCEL =  ?"
	cQuery += "   AND SEK.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next
	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSA2)
	oExecA:SetUnSafe( nSeq += 1, cCpoSEK)
	oExecA:SetUnSafe( nSeq += 1, StrTran(cChaveCTL,"EK_","SEK.EK_"))
	oExecA:SetString( nSeq += 1, xFilial("SA2") )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, cEmpAnt )
	oExecA:SetString( nSeq += 1, cFilAnt )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SEK") )
	oExecA:SetString( nSeq += 1, 'CP' )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, xFilial("SEK") )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )
	oExecA:SetString( nSeq += 1, 'F' )
	oExecA:SetString( nSeq += 1, ' ' )
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	//ConOut(oExecA:getFixQuery( ))
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecnoSEK := ( cAliasA )->REGSEK

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf LEFT(self:aStruct[nx][5],4) = "CT2_"

				cQuery := "SELECT CT2_LOTE,CT2_SBLOTE,CT2_DOC, CT2_DATA CT2_DTACTB, ?" 
				cQuery += " CT2_TPSALD  CT2_TPSLD, CT2_HIST CT2_HISTO"
				cQuery += " FROM " + RetSqlName("CT2") + " CT2"
				cQuery += " INNER JOIN " + RetSqlName("CTL") + " CTL ON CTL_FILIAL = ? AND CTL_LP = ? AND CTL.D_E_L_E_T_ = ?"
				cQuery += " INNER JOIN " + RetSqlName("CV3") + " CV3 ON CV3_FILIAL = ? AND CV3_KEY = ? AND CV3.D_E_L_E_T_ = ? "
				cQuery += " WHERE CV3.CV3_RECDES <> ? "
				cQuery += "   AND CT2.R_E_C_N_O_ = CAST(CV3.CV3_RECDES AS ? )"   
				cQuery += "   AND CT2.D_E_L_E_T_ = ?"
				cQuery := ChangeQuery(cQuery)
				oExecB := FwExecStatement():New( cQuery )
				oExecB:SetUnSafe( 01, cCpoCT2)
				oExecB:SetString( 02, cFilialCTL )
				oExecB:SetString( 03, "620" )
				oExecB:SetString( 04, ' ' )
				oExecB:SetString( 05, cFilialCV3 )
				oExecB:SetString( 06, ( cAliasA )->CHVE )
				oExecB:SetString( 07, ' ' )
				oExecB:SetString( 08, ' ' )
				oExecB:SetUnsafe( 09, IIF("ORACLE" $ TcGetDB(),"INT","BIGINT"))
				oExecB:SetString( 10, ' ' )

				// Executa a QUERY e cria uma tabela temporaria com os dados retornados
				cAliasB := oExecB:OpenAlias( )
				If ( self:aStruct[nX][3] == 'date' )
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasB )->&( self:aStruct[nX][5] ) ) )
				ElseiF self:aStruct[nX][5] == "CT2_DOCLSL"
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->CT2_LOTE+"/"+( cAliasB )->CT2_SBLOTE+"/"+( cAliasB )->CT2_DOC
				Else
					self:jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
				Endif

				// fecha tabela
				( cAliasB )->( DbCloseArea( ) )
				oExecB:Destroy( )
				oExecB := Nil

			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 28/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class paymentorderaccountingTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
