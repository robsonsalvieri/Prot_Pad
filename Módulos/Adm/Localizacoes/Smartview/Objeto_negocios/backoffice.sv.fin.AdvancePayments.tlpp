#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.AdvancePayments.ch'

namespace custom.financiero.AdvancePayments.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.AdvancePayments
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 10/02/2025
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SA2,SE2,SEK', name='Anticipos a proveedores', country='ALL', initialRelease='12.1.2410', customTables='SA2,SE2,SEK' )
class AdvancePaymentsTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 10/02/2025
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class AdvancePaymentsTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Anticipos a proveedores'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Anticipos a proveedores'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financiero'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV511' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos customizados
	aAdd( aCpos, { 'EK_TXMOEDA'	, STR0007 , 'number'	, STR0007 } ) // 'TASA MONEDA'
	aAdd( aCpos, { 'E2_XSALDO'	, 'Saldo antic último pago' , 'number'	, 'Saldo antic último pago' } ) // 'TASA MONEDA'


	// demais campos
	//self:aFields := {'E2_ORDPAGO','E2_EMISSAO','E2_FORNECE','E2_LOJA','A2_NOME', 'E2_VALOR', 'E2_MOEDA', 'E2_TXMOEDA', 'E2_VLCRUZ', 'E2_SALDO', 'E2_VALLIQ', 'EK_NUM', 'EK_PREFIXO', 'EK_FORNECE', 'EK_LOJA', 'EK_VALOR', 'EK_MOEDA', 'EK_TXMOEDA', 'EK_ORDPAGO', 'EK_EMISSAO'}
	self:aFields := {'E2_ORDPAGO','E2_FORNECE','E2_LOJA','A2_NOME','E2_EMISSAO','E2_VALOR','E2_MOEDA','E2_TXMOEDA','E2_XSALDO','EK_NUM','EK_PREFIXO','EK_FORNECE','EK_LOJA','EK_ORDPAGO','EK_EMISSAO','EK_VALOR','EK_MOEDA','EK_TXMOEDA'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class AdvancePaymentsTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character
	Local nSeq          as numeric

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA2 := getCpoUser( aCustomFields, 'SA2', 'A2', ',' )
	cCpoSE2 := getCpoUser( aCustomFields, 'SE2', 'E2', ',' )
	cCpoSEK := getCpoUser( aCustomFields, 'SEK', 'EK', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SE2.E2_ORDPAGO, SE2.E2_EMISSAO, SE2.E2_FORNECE, SE2.E2_LOJA, SA2.A2_NOME, SE2.E2_VALOR, SE2.E2_MOEDA, SE2.E2_TXMOEDA, SE2.E2_VLCRUZ, SE2.E2_SALDO E2_XSALDO, SE2.E2_VALLIQ, "
	cQuery += "  SEK.EK_NUM, SEK.EK_PREFIXO, SEK.EK_FORNECE, SEK.EK_LOJA, SEK.EK_VALOR, SEK.EK_MOEDA, SEK.EK_TXMOE02, SEK.EK_TXMOE03, SEK.EK_TXMOE04, SEK.EK_TXMOE05, SEK.EK_ORDPAGO, SEK.EK_EMISSAO, "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " 0 EK_TXMOEDA"
	cQuery += " FROM " + RetSqlName("SE2") + " SE2"
	cQuery += " INNER JOIN " + RetSqlName("SA2") + " SA2 ON SA2.A2_FILIAL = ? AND SA2.A2_COD = SE2.E2_FORNECE AND SA2.A2_LOJA = SE2.E2_LOJA AND SA2.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN  " + RetSqlName("SEK") + " SEK ON SEK.EK_FILIAL = ? AND SEK.EK_ORDPAGO = SE2.E2_ORDPAGO AND SEK.EK_TIPO NOT IN ('PA','TF') AND SEK.D_E_L_E_T_ = ?"
	cQuery += " WHERE SE2.E2_FILIAL = ?"
	cQuery += "   AND SE2.E2_FORNECE BETWEEN ? AND ?"
	cQuery += "   AND SE2.E2_LOJA    BETWEEN ? AND ?"
	cQuery += "   AND SE2.E2_EMISSAO BETWEEN ? AND ?"
	cQuery += "   AND SE2.E2_TIPO = ?"
	cQuery += "   AND SE2.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSA2 )
	oExecA:SetUnSafe( nSeq += 1, cCpoSE2 )
	oExecA:SetUnSafe( nSeq += 1, cCpoSEK )
	oExecA:SetString( nSeq += 1, xFilial("SA2") )			// A2_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SA2.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SEK") )			// EK_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SEK.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SE2") )			// E2_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// E2_FORNECE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// E2_FORNECE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )	// E2_LOJA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )	// E2_LOJA
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )	// EL_DTENTRA
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )	// EL_DTENTRA
	oExecA:SetString( nSeq += 1, 'PA' )					// E2_TIPO
	oExecA:SetString( nSeq += 1, ' ' )						// SE2.D_E_L_E_T_

	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	//ConOut(oExecA:getFixQuery( ))
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nx][1] == 'EK_TXMOEDA' )
				If ( cAliasA )->EK_MOEDA == "2"
					self:jItems[self:aStruct[nX][1]] := ( cAliasA )->EK_TXMOE02
				ElseIf ( cAliasA )->EK_MOEDA == "3"
					self:jItems[self:aStruct[nX][1]] := ( cAliasA )->EK_TXMOE03
				ElseIf ( cAliasA )->EK_MOEDA == "4"
					self:jItems[self:aStruct[nX][1]] := ( cAliasA )->EK_TXMOE04
				ElseIf ( cAliasA )->EK_MOEDA == "5"
					self:jItems[self:aStruct[nX][1]] := ( cAliasA )->EK_TXMOE05
				Else
					self:jItems[self:aStruct[nX][1]] := 1
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class AdvancePaymentsTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
