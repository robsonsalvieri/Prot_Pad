#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.PaymentInCash.ch'

namespace custom.financiero.PaymentInCash.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fin.PaymentInCash
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 09/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SA1,SE5', name='Clientes con pagos en efectivo', country='ALL', initialRelease='12.1.2410', customTables='SA1,SE5' )
class PaymentInCashTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class PaymentInCashTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Clientes con pagos en efectivo'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Clientes con pagos en efectivo'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financiero'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV516' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// adiciona campos customizados
	aAdd( aCpos, { 'QTDPAGO' , STR0007 , 'number', STR0007 } ) // 'Qtd Pago'

	// demais campos
	self:aFields := {'E5_FILIAL','E5_CLIFOR','E5_LOJA','A1_NOME','A1_CGC','A1_END','A1_MUN','A1_EST','E5_MOEDA','QTDPAGO','E5_VALOR'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class PaymentInCashTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )
	cCpoFK1 := getCpoUser( aCustomFields, 'FK1', 'FK1', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT FK1_FILIAL E5_FILIAL, FK7_CLIFOR E5_CLIFOR, FK7_LOJA E5_LOJA, A1_NOME, A1_CGC, A1_END, A1_MUN, A1_EST, FK1_MOEDA E5_MOEDA, COUNT(*) AS QTDPAGO,"
	cQuery += " ? "
	cQuery += " ? "
	cQuery += "  SUM(FK1_VALOR) E5_VALOR"
	cQuery += " FROM " + RetSqlName("FK1") + " FK1"
	cQuery += " INNER JOIN " + RetSqlName("FK7") + " FK7 ON FK7.FK7_FILIAL = ? AND FK7.FK7_IDDOC = FK1.FK1_IDDOC AND FK7.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = FK7.FK7_CLIFOR AND SA1.A1_LOJA = FK7.FK7_LOJA AND SA1.D_E_L_E_T_ = ?"
	cQuery += " WHERE FK1.FK1_FILIAL = ?"
	cQuery += "	  AND FK1.FK1_DTDIGI BETWEEN ? AND ?"
	cQuery += "   AND FK1.FK1_MOEDA = ?"
	cQuery += "	  AND FK1.FK1_TPDOC = ?"
	cQuery += "	  AND FK1.FK1_RECPAG = ?"
	cQuery += "	  AND FK7.FK7_TIPO IN ( ? )
	cQuery += "   AND FK1.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next
	cQuery += " GROUP BY ? ? FK1_FILIAL, FK7_CLIFOR, FK7_LOJA, A1_NOME, A1_CGC, A1_END, A1_MUN, A1_EST, FK1_MOEDA"

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
    oExecA:SetUnSafe( nSeq += 1, cCpoSA1)
    oExecA:SetUnSafe( nSeq += 1, cCpoFK1)
	oExecA:SetString( nSeq += 1, xFilial("FK7") )			// FK7_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// FK7.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SA1") )			// A1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SA1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("FK1") )			// FK1_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, StrZero(jParams['MV_PAR03'][1],2) )	// FK1.FK1_MOEDA
	oExecA:SetString( nSeq += 1, 'VL' )						// FK1.FK1_TPDOC
	oExecA:SetString( nSeq += 1, 'R' )						// FK1.FK1_RECPAG
	oExecA:SetIn( nSeq += 1, {'EF','TF'} )					// FK7.FK7_TIPO
	oExecA:SetString( nSeq += 1, ' ' )						// FK1.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
    oExecA:SetUnSafe( nSeq += 1, cCpoSA1)
    oExecA:SetUnSafe( nSeq += 1, cCpoFK1)

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class PaymentInCashTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
