#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.customerAaccountStatement.ch'

namespace custom.financiero.customerAaccountStatement.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.customerAaccountStatement
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 09/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SA1,SE1,SEL', name='Estados de cuenta de Clientes', country='ALL', initialRelease='12.1.2310', customTables='SA1,SEL,SA1' )
class customerAaccountStaTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class customerAaccountStaTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Estados de cuenta de Clientes'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Estados de cuenta de Clientes'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financiero'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV505' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// Ambas visões de dados
	aAdd( aCpos, {'E1_2SALDO' , STR0007 , 'number', STR0008 } ) // 'Saldo por cobrar al ultimo pago' / Saldo al Ultimo pago

	// demais campos
	self:aFields := {'E1_CLIENTE','E1_LOJA','A1_NOME','E1_TIPO','E1_EMISSAO','E1_NUM','E1_PREFIXO','E1_PARCELA','E1_VALOR','E1_MOEDA','E1_TXMOEDA','E1_VENCTO','E1_2SALDO','EL_RECIBO','EL_SERIE','EL_EMISSAO','EL_NUMERO','EL_PREFIXO','EL_TIPO','EL_VALOR','EL_MOEDA','EL_TXMOEDA','EL_VLMOED1','EL_DTENTRA'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class customerAaccountStaTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )
	cCpoSE1 := getCpoUser( aCustomFields, 'SE1', 'E1', ',' )


	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SE1.E1_CLIENTE, SE1.E1_LOJA, SA1.A1_NOME, SEL.EL_RECIBO, SEL.EL_SERIE, SE1.E1_PREFIXO, SE1.E1_NUM, SE1.E1_PARCELA, "
	cQuery += "  SE1.E1_TIPO, SE1.E1_VALOR, SE1.E1_SALDO E1_2SALDO, SEL2.EL_DTENTRA, SEL2.EL_PREFIXO, SEL2.EL_NUMERO, SEL2.EL_TIPO, SEL2.EL_VALOR, SEL2.EL_MOEDA, "
	cQuery += cCpoSA1
	cQuery += cCpoSE1
	cQuery += "  SEL2.EL_TXMOEDA, SEL2.EL_VLMOED1, SE1.E1_EMISSAO, SE1.E1_MOEDA, SE1.E1_TXMOEDA, SE1.E1_VENCTO, SEL2.EL_EMISSAO, SEL2.EL_VLMOED1 "
	cQuery += " FROM " + RetSqlName("SE1") + " SE1 "
	cQuery += " INNER JOIN " + RetSqlName("SEL") + " SEL  ON SEL.EL_FILIAL  = ? AND SEL.EL_PREFIXO = SE1.E1_PREFIXO AND SEL.EL_NUMERO = SE1.E1_NUM AND SEL.EL_PARCELA = SE1.E1_PARCELA AND SEL.EL_TIPO = SE1.E1_TIPO AND SEL.EL_CLIENTE = SE1.E1_CLIENTE AND SEL.EL_LOJA = SE1.E1_LOJA AND SEL.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1  ON SA1.A1_FILIAL  = ? AND SA1.A1_COD = SE1.E1_CLIENTE AND SA1.A1_LOJA = SE1.E1_LOJA AND SA1.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SEL") + " SEL2 ON SEL2.EL_FILIAL = ? AND SEL2.EL_RECIBO = SEL.EL_RECIBO AND SEL2.EL_EMISSAO = SE1.E1_EMISSAO AND SEL2.EL_EMISSAO BETWEEN ? AND ? AND SEL2.D_E_L_E_T_ = ? "
	cQuery += " WHERE SE1.E1_FILIAL = ? "
	cQuery += "   AND SE1.E1_CLIENTE BETWEEN ? AND ? "
	cQuery += "   AND SE1.E1_LOJA    BETWEEN ? AND ? "
	cQuery += "   AND SEL.EL_DTENTRA = ? "
	cQuery += "   AND SE1.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	oExecA:SetString( 01, xFilial("SEL") )			// EL_FILIAL
	oExecA:SetString( 02, ' ' )						// SEL.D_E_L_E_T_
	oExecA:SetString( 03, xFilial("SA1") )			// A1_FILIAL
	oExecA:SetString( 04, ' ' )						// SA1.D_E_L_E_T_
	oExecA:SetString( 05, xFilial("SEL") )			// EL_FILIAL
	oExecA:SetString( 06, DtoS( FwDateTimeToLocal( jParams['MV_PAR05'][1] )[1] ) )	// EL_DTENTRA
	oExecA:SetString( 07, DtoS( FwDateTimeToLocal( jParams['MV_PAR06'][1] )[1] ) )	// EL_DTENTRA
	oExecA:SetString( 08, ' ' )						// SEL.D_E_L_E_T_
	oExecA:SetString( 09, xFilial("SE1") )			// E1_FILIAL
	oExecA:SetString( 10, jParams['MV_PAR01'][1] )	// E1_CLIENTE
	oExecA:SetString( 11, jParams['MV_PAR02'][1] )	// E1_CLIENTE
	oExecA:SetString( 12, jParams['MV_PAR03'][1] )	// E1_LOJA
	oExecA:SetString( 13, jParams['MV_PAR04'][1] )	// E1_LOJA
	oExecA:SetString( 14, ' ' )						// EL_DTENTRA
	oExecA:SetString( 15, ' ' )						// SA1.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	//ConOut(oExecA:getFixQuery( ))
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class customerAaccountStaTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
