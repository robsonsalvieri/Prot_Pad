#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.commissionpayment.ch'

namespace custom.financiero.commissionpayment.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.commissionpayment
@description Classe para creación del Objeto de Negocio de Recibos pendientes por cobrador para TReports
@author Marcelo Hruschka
@since 19/11/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SAQ,SEX,SA1', name='Comisiones del cobrador', country='ALL', initialRelease='12.1.2410', customTables='SAQ,SEX,SA1' )
class commissionpaymentTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class commissionpaymentTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Comisiones del cobrador'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Comisiones del cobrador'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financiero'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV897' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	//Cheques emitidos e cheques recebidos
	aAdd( aCpos, { 'SITUACION'	, STR0007 , 'string' , STR0007 } ) // 'SITUACIÓN'
	If SAQ->(ColumnPos("AQ_STATUS")) <= 0
		aAdd( aCpos, { 'AQ_STATUS'	, 'Estatus' , 'string' , 'Estatus' } ) // 'Estatus'
	Endif

	// demais campos
	self:aFields := {'EX_FILIAL','EX_COBRAD','AQ_NOME','AQ_STATUS','EX_NUM','EX_EMISSAO','EX_CODCLI','EX_LOJA','A1_NOME','EX_TIPODOC','EX_TITULO','EX_MOEDA','EX_BASE','EX_PORC','EX_COMIS','EX_VENCTO','EX_DATA','SITUACION','EX_AJUSTE'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class commissionpaymentTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSA1 := getCpoUser( aCustomFields, 'SA1', 'A1', ',' )
	cCpoSAQ := getCpoUser( aCustomFields, 'SAQ', 'AQ', ',' )
	cCpoSEX := getCpoUser( aCustomFields, 'SEX', 'EX', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT SEX.EX_FILIAL, SEX.EX_COBRAD, SAQ.AQ_NOME, SEX.EX_NUM, SEX.EX_EMISSAO, SEX.EX_CODCLI, SEX.EX_LOJA, SA1.A1_NOME, "
	cQuery += " SEX.EX_TIPODOC, SEX.EX_TITULO, SEX.EX_MOEDA, SEX.EX_BASE, SEX.EX_PORC, SEX.EX_COMIS, SEX.EX_VENCTO, SEX.EX_DATA, "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " ? "
	If SAQ->(ColumnPos("AQ_STATUS")) > 0
		cQuery += "SAQ.AQ_STATUS, "
	Else
		cQuery += "' ' AQ_STATUS, "
	Endif
	cQuery += " SEX.EX_AJUSTE, SEX.R_E_C_N_O_  REG "
	cQuery += " FROM " + RetSqlName("SEX") + " SEX "
	cQuery += " INNER JOIN " + RetSqlName("SAQ") + " SAQ ON SAQ.AQ_FILIAL = ? AND SAQ.AQ_COD = SEX.EX_COBRAD AND SAQ.D_E_L_E_T_ = ? "
	cQuery += " INNER JOIN " + RetSqlName("SA1") + " SA1 ON SA1.A1_FILIAL = ? AND SA1.A1_COD = SEX.EX_CODCLI AND SA1.A1_LOJA = SEX.EX_LOJA AND SA1.D_E_L_E_T_ = ? "
	cQuery += " WHERE SEX.EX_FILIAL = ?"
	cQuery += "	  AND SEX.EX_EMISSAO BETWEEN ? AND ? "
	cQuery += "	  AND SEX.EX_COBRAD  BETWEEN ? AND ? "
	cQuery += "   AND SEX.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSA1 )
	oExecA:SetUnSafe( nSeq += 1, cCpoSAQ )
	oExecA:SetUnSafe( nSeq += 1, cCpoSEX )
	oExecA:SetString( nSeq += 1, xFilial("SAQ") )			// AQ_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SAQ.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SA1") )			// A1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SA1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SEX") )			// EX_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )	// EX_EMISSAO
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )	// EX_EMISSAO
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )	// EX_COBRAD
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )	// EX_COBRAD
	oExecA:SetString( nSeq += 1, ' ' )						// SEX.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] $ "SITUACION" )
				self:jItems[self:aStruct[nX][1]] := ""
				If Empty(( cAliasA )->&( EX_DATA ))
					self:jItems[self:aStruct[nX][1]] := STR0008 // A PAGAR
				Else
					self:jItems[self:aStruct[nX][1]] := STR0009 // PAGADA
				Endif
			ElseIf ( self:aStruct[nX][5] == "AQ_STATUS" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0012 // "Activo"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0013 // "Inactivo"
				Endif
			ElseIf ( self:aStruct[nX][5] $ "EX_AJUSTE" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0010 // "Sí"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0011 // "No"
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class commissionpaymentTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
