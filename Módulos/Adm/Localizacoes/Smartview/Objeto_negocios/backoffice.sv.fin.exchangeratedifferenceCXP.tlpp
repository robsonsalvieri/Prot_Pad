#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.exchangeratedifferenceCXP.ch'

namespace custom.financeiro.exchangeratedifferenceCXP.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.fin.exchangeratedifferenceCXP
@description Classe para creación del Objeto de Negocio de Diferencia en cambio Cuentas por Pagar para SMARTVIEW
@author Leonardo Pereira
@since 10/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SE2,SFR,SA2', name='Diferencia en cambio Cuentas por Pagar', country='ALL', initialRelease='12.1.2410', customTables='SE2,SA2' )
class exchangeratedifferenceCXPTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class exchangeratedifferenceCXPTReportsBusinessObject

	Local aCpos := { } as array

	_Super:new( )

	// Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Diferencia en cambio Cuentas por Pagar'

	// Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Diferencia en cambio Cuentas por Pagar'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financeiro'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV508' )
		IIf( !self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'' ) // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// campos que no existen en SX3
	//aAdd( aCpos, { 'CU_DESMOT' , STR0007, 'string', STR0007 } ) // 'Descripción del motivo'

	// demais campos
	self:aFields := { 'A2_COD', 'A2_LOJA', 'E2_VENCREA', 'E2_EMISSAO', 'E2_EMIS1', 'E2_NATUREZ', 'E2_PREFIXO', 'E2_NUM', 'E2_PARCELA', 'E2_TIPO', 'E2_VLCRUZ', 'E2_VALOR', 'E2_MOEDA', 'E2_TXMOEDA', 'E2_HIST', 'FR_DATADI', 'FR_CHAVDE', 'FR_CHAVOR', 'FR_VALOR', 'FR_MOEDA', 'FR_TXORI', 'FR_TXATU', 'FR_CORANT' }

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class exchangeratedifferenceCXPTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		  as character
	Local cFiltro 		  as character
	Local cExp 			  as character
	Local cCpoSE2		  as character
	Local cCpoSA2 		  as character
	Local cAliasA       as character
	Local cAliasB       as character

	Local nX			     as numeric
	Local n1			     as numeric
	Local nTamChav 	  as numeric
	Local nSeq			  as numeric

	Local jParams 		  as json

	Local aPDFields	  as array
	Local aFiltro		  as array
	Local aCustomFields as array

	Local lObfuscated	  as logical

	Local oExecA		  as object
	Local oExecB		  as object

	Local cFilialSFR      as character

	cFilialSFR := FWxFilial( "SFR" )

	cFiltro 		:= ''
	cExp 			:= ''
	cAliasA			:= ''
	cAliasB			:= ''

	nX 				:= 0
	n1				   := 0
	nTamChav  		:= TamSX3( 'FR_CHAVOR' )[1]

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSE2 := getCpoUser( aCustomFields, 'SE2', 'E2', ',' )
	cCpoSA2 := getCpoUser( aCustomFields, 'SA2', 'A2', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { | x, y | x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT "

	cQuery += " SA2.A2_COD, SA2.A2_LOJA, SA2.A2_NOME, " + cCpoSA2 + cCpoSE2 + "SE2.E2_VENCREA, SE2.E2_EMISSAO, SE2.E2_EMIS1, SE2.E2_NATUREZ, SE2.E2_PREFIXO, SE2.E2_NUM, SE2.E2_PARCELA, SE2.E2_TIPO, SE2.E2_VLCRUZ, SE2.E2_VALOR, SE2.E2_MOEDA, SE2.E2_TXMOEDA "
	cQuery += " FROM " + RetSqlName( 'SE2' ) + " SE2 "
	cQuery += " INNER JOIN " + RetSqlName( "SA2" ) + " SA2 "
	cQuery += "   ON SA2.A2_FILIAL = ? "
	cQuery += "      AND SA2.A2_COD = SE2.E2_FORNECE "
	cQuery += "      AND SA2.A2_LOJA = SE2.E2_LOJA "
	cQuery += "      AND SA2.D_E_L_E_T_ = ? "
	cQuery += " WHERE SE2.E2_FILIAL = ? "
	cQuery += "   AND SE2.E2_PREFIXO BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_NUM BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_FORNECE BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_NATUREZ BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_EMISSAO BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_VENCREA BETWEEN ? AND ? "
	cQuery += "   AND SE2.E2_EMIS1 BETWEEN ? AND ? "

	If ( jParams['MV_PAR15'][1] == 1 )
		cQuery += "   AND SE2.E2_MOEDA > ?  "
	Else
		cQuery += "   AND SE2.E2_MOEDA <= ?  "
		cQuery += "   AND SE2.E2_CONVERT = ? "
	EndIf

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += "   AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery += "   AND SE2.D_E_L_E_T_ = ? "
	cQuery += " ORDER BY ? "

	cQuery := ChangeQuery( cQuery )

	oExecA := FwExecStatement( ):New( cQuery )

	nSeq := 0

	oExecA:SetString( nSeq += 1, FWxFilial( "SA2" ) )
	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetString( nSeq += 1, FWxFilial( "SE2" ) )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR05'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )
	oExecA:SetString( nSeq += 1, jParams['MV_PAR08'][1] )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR09'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR10'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR11'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR12'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR13'][1] )[1] ) )
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR14'][1] )[1] ) )

	If ( jParams['MV_PAR15'][1] == 1 )
		oExecA:SetNumeric( nSeq += 1, 1 )
	Else
		oExecA:SetNumeric( nSeq += 1, 1 )
		oExecA:SetString( nSeq += 1, 'N' )
	EndIf

	oExecA:SetString( nSeq += 1, ' ' )
	oExecA:SetUnsafe( nSeq += 1, 'SE2.E2_FILIAL, SE2.E2_PREFIXO, SE2.E2_NUM, SE2.E2_PARCELA' )

	cQuery := oExecA:getFixQuery( )

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )
		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If ( SubStr( self:aStruct[nx][5], 1, 2 ) $ "A2,E2" )
				If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
					self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
				ElseIf ( self:aStruct[nX][3] == 'date' )
					self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
				Else
					If ( self:aStruct[nx][5] == 'E2_HIST' )
						If ( jParams['MV_PAR15'][1] == 1 )
							self:jItems[self:aStruct[nX][1]] := STR0007
						Else
							self:jItems[self:aStruct[nX][1]] := STR0008
						Endif
					Else
						self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
					Endif
				Endif
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		self:oData:appendData( self:jItems )

		// Realiza a montagem da QUERY que será enviada para o banco de dados
		cQuery := "SELECT "

		cQuery += "SFR.FR_DATADI, SFR.FR_CHAVDE, SFR.FR_CHAVOR, SFR.FR_VALOR, SFR.FR_MOEDA, SFR.FR_TXORI, SFR.FR_TXATU, SFR.FR_CORANT "

		cQuery += "FROM " + RetSQLName( 'SFR' ) + " SFR "

		cQuery += "WHERE SFR.FR_FILIAL = ? "
		cQuery += "   AND SFR.FR_CARTEI = ? "

		If ( jParams['MV_PAR15'][1] == 1 )
			cQuery += "   AND SFR.FR_CHAVOR = ? "
		Else
			cQuery += "   AND SFR.FR_CHAVDE = ? "
		EndIf

		cQuery	+=	"   AND SFR.D_E_L_E_T_ = ? "

		cQuery := ChangeQuery( cQuery )

		oExecB := FwExecStatement( ):New( cQuery )

		oExecB:SetString( 1, cFilialSFR )
		oExecB:SetString( 2, '2' )
		oExecB:SetString( 3, SubStr( ( cAliasA )->E2_PREFIXO + ( cAliasA )->E2_NUM + ( cAliasA )->E2_PARCELA + ( cAliasA )->E2_TIPO + ( cAliasA )->A2_COD + ( cAliasA )->A2_LOJA, 1, nTamChav ) )
		oExecB:SetString( 4, ' ' )

		cQuery := oExecB:getFixQuery( )

		// Executa a QUERY e cria uma tabela temporaria com os dados retornados
		cAliasB := oExecB:OpenAlias( )

		// Alimenta o objeto de dados da classe para retornar ao SmartView
		While !( cAliasB )->( EOF( ) )
			self:jItems := JsonObject():new( )

			For nX := 1 To Len( self:aStruct )
				If ( SubStr( self:aStruct[nx][5], 1, 2 ) $ "FR" )
					If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
						self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasB )->&( self:aStruct[nX][5] ) )
					ElseIf ( self:aStruct[nX][3] == 'date' )
						self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasB )->&( self:aStruct[nX][5] ) ) )
					Else
						self:jItems[self:aStruct[nX][1]] := ( cAliasB )->&( self:aStruct[nX][5] )
					Endif
				Endif
			Next

			// Inclui os dados no objeto paea retorno ao SmartView
			self:oData:appendData( self:jItems )

			( cAliasB )->( DbSkip( ) )
		End
		( cAliasB )->( DbCloseArea( ) )

		( cAliasA )->( DbSkip( ) )
	End

	( cAliasA )->( DbCloseArea( ) )

	If oExecA <> Nil
		oExecA:Destroy( )
		oExecA := Nil
	Endif
	If oExecB <> Nil
		oExecB:Destroy( )
		oExecB := Nil
	Endif

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Leonardo Pereira
@since 31/10/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class exchangeratedifferenceCXPTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
