#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.ctb.generaljournalbook.ch'

namespace custom.contabil.generaljournalbook.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.generaljournalbook
@description Classe para creación del Objeto de Negocio de Recibos pendientes por cobrador para TReports
@author Marcelo Hruschka
@since 26/11/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACTB', tables='CT1,CT2', name='Libro diario general', country='ALL', initialRelease='12.1.2410', customTables='CT1,CT2' )
class generaljournalbookTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 26/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class generaljournalbookTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Libro diario general'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Libro diario general'

	// Define a Área
	self:appendArea( STR0002 ) // 'Contabilid de Gestión'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CTBSV115' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	self:aFields := {'CT2_FILIAL','CT2_DATA','CT2_LOTE','CT2_SBLOTE','CT2_DOC','CT2_LINHA','CT2_MOEDLC','CT2_TPSALD','CT2_DC','CT2_DEBITO','CT2_CREDIT','CT2_VALOR','CT2_HIST','CT2_CCD','CT2_CCC','CT2_ITEMD','CT2_ITEMC','CT2_CLVLDB','CT2_CLVLCR'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 26/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class generaljournalbookTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoCT1 := getCpoUser( aCustomFields, 'CT1', 'CT1', ',' )
	cCpoCT2 := getCpoUser( aCustomFields, 'CT2', 'CT2', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT CT2.CT2_FILIAL, CT2.CT2_DATA, CT2.CT2_LOTE, CT2.CT2_SBLOTE, CT2.CT2_DOC, CT2.CT2_LINHA, CTO_DESC AS CT2_MOEDLC,"
	cQuery += " CT2.CT2_TPSALD, CT2.CT2_DC, "
	cQuery += " CT2.CT2_DEBITO||' '||CT1_DEB.CT1_DESC01 CT2_DEBITO,"
	cQuery += " CT2.CT2_CREDIT||' '||CT1_CRE.CT1_DESC01 CT2_CREDIT,"
	cQuery += " CT2.CT2_VALOR, CT2.CT2_HIST,"
	cQuery += " CT2.CT2_CCD||' '||CTT_DEB.CTT_DESC01 AS CT2_CCD,"
	cQuery += " CT2.CT2_CCC||' '||CTT_CRE.CTT_DESC01 AS CT2_CCC,"
	cQuery += " CT2.CT2_ITEMD||' '||CTD_DEB.CTD_DESC01 AS CT2_ITEMD,"
	cQuery += " CT2.CT2_ITEMC||' '||CTD_CRE.CTD_DESC01 AS CT2_ITEMC,"
	cQuery += " CT2.CT2_CLVLDB||' '||CTH_DEB.CTH_CLVL AS CT2_CLVLDB,"
	cQuery += " CT2.CT2_CLVLCR||' '||CTH_CRE.CTH_CLVL AS CT2_CLVLCR,"
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " CT2.R_E_C_N_O_ AS REG"
	cQuery += " FROM " + RetSqlName("CT2") + " CT2"
	cQuery += " INNER JOIN " + RetSqlName("CTO") + " CTO ON CTO.CTO_FILIAL = ? AND CTO.CTO_MOEDA = CT2.CT2_MOEDLC AND CTO.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CT1") + " CT1_DEB ON CT1_DEB.CT1_FILIAL = ? AND CT1_DEB.CT1_CONTA = CT2.CT2_DEBITO AND CT1_DEB.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CT1") + " CT1_CRE ON CT1_CRE.CT1_FILIAL = ? AND CT1_CRE.CT1_CONTA = CT2.CT2_CREDIT AND CT1_CRE.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTT") + " CTT_DEB ON CTT_DEB.CTT_FILIAL = ? AND CTT_DEB.CTT_CUSTO = CT2.CT2_CCD AND CTT_DEB.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTT") + " CTT_CRE ON CTT_CRE.CTT_FILIAL = ? AND CTT_CRE.CTT_CUSTO = CT2.CT2_CCC AND CTT_CRE.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTD") + " CTD_DEB ON CTD_DEB.CTD_FILIAL = ? AND CTD_DEB.CTD_ITEM = CT2.CT2_ITEMD AND CTD_DEB.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTD") + " CTD_CRE ON CTD_CRE.CTD_FILIAL = ? AND CTD_CRE.CTD_ITEM = CT2.CT2_ITEMC AND CTD_CRE.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTH") + " CTH_DEB ON CTH_DEB.CTH_FILIAL = ? AND CTH_DEB.CTH_CLVL = CT2.CT2_CLVLDB AND CTH_DEB.D_E_L_E_T_ = ?"
	cQuery += "  LEFT JOIN " + RetSqlName("CTH") + " CTH_CRE ON CTH_CRE.CTH_FILIAL = ? AND CTH_CRE.CTH_CLVL = CT2.CT2_CLVLCR AND CTH_CRE.D_E_L_E_T_ = ?"
	cQuery += " WHERE CT2.CT2_FILIAL = ?"
	cQuery += "   AND CT2.CT2_DATA BETWEEN ? AND ?"
	cQuery += "   AND CT2.CT2_MOEDLC = ?"
	cQuery += "   AND CT2.CT2_TPSALD = ?"
	cQuery += "   AND CT2.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	oExecA:SetUnSafe( 01, cCpoCT1 )
	oExecA:SetUnSafe( 02, cCpoCT2 )
	oExecA:SetString( 03, xFilial("CTO") )			// CTO_FILIAL
	oExecA:SetString( 04, '' 			 )			// CTO.D_E_L_E_T_
	oExecA:SetString( 05, xFilial("CT1") )			// CT1_FILIAL
	oExecA:SetString( 06, '' 			 )			// CT1.D_E_L_E_T_
	oExecA:SetString( 07, xFilial("CT1") )			// CT1_FILIAL
	oExecA:SetString( 08, '' 			 )			// CT1.D_E_L_E_T_
	oExecA:SetString( 09, xFilial("CTT") )			// CTT_FILIAL
	oExecA:SetString( 10, '' 			 )			// CTT.D_E_L_E_T_
	oExecA:SetString( 11, xFilial("CTT") )			// CTT_FILIAL
	oExecA:SetString( 12, '' 			 )			// CTT.D_E_L_E_T_
	oExecA:SetString( 13, xFilial("CTD") )			// CTD_FILIAL
	oExecA:SetString( 14, '' 			 )			// CTD.D_E_L_E_T_
	oExecA:SetString( 15, xFilial("CTD") )			// CTD_FILIAL
	oExecA:SetString( 16, '' 			 )			// CTD.D_E_L_E_T_
	oExecA:SetString( 17, xFilial("CTH") )			// CTH_FILIAL
	oExecA:SetString( 18, '' 			 )			// CTH.D_E_L_E_T_
	oExecA:SetString( 19, xFilial("CTH") )			// CTH_FILIAL
	oExecA:SetString( 20, '' 			 )			// CTH.D_E_L_E_T_
	oExecA:SetString( 21, xFilial("CT2") )			// CT2_FILIAL
	oExecA:SetString( 22, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] ) )			// CT2_DATA
	oExecA:SetString( 23, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] ) )			// CT2_DATA
	oExecA:SetString( 24, jParams['MV_PAR03'][1] )	// CT2_MOEDLC
	oExecA:SetString( 25, jParams['MV_PAR04'][1] )	// CT2_TPSALD
	oExecA:SetString( 26, '' 			 )			// CT2.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )


		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] == "CT2_DC" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0007 // 1=Debito
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0008 // 2=Credito
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "3"
					self:jItems[self:aStruct[nX][1]] := STR0009 // 3=Partida Doble
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "4"
					self:jItems[self:aStruct[nX][1]] := STR0010 // 4=Cont.Hist
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "5"
					self:jItems[self:aStruct[nX][1]] := STR0011 // 5=Prorrateo
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "6"
					self:jItems[self:aStruct[nX][1]] := STR0012 //6=Asto Estandar 
				Endif
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 26/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class generaljournalbookTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
