#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.ctb.balancedeterceros.ch'

namespace custom.contabil.balancedeterceros.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.balancedeterceros
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 17/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACTB', tables='CT1', name='Balance de terceros', country='COL', initialRelease='12.1.2410', customTables='CT1' )
class balancedetercerosTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class balancedetercerosTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Balance de terceros'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Balance de terceros'

	// Define a Área
	self:appendArea( STR0002 ) // 'Contabilid de Gestión'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CTBSV820' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	aAdd( aCpos, { 'ECX'		, STR0007 , 'string', STR0007	} ) // 'CUENTA'
	aAdd( aCpos, { 'ECXDESC' 	, STR0008 , 'string', STR0008	} ) // 'DESCRIPCION'
	aAdd( aCpos, { 'ECY' 		, STR0009 , 'string', STR0009	} ) // 'NIT'
	aAdd( aCpos, { 'ECYDESC'	, STR0010 , 'string', STR0010	} ) // 'DESCRIPCION NIT'
	aAdd( aCpos, { 'SALDOANT'	, STR0011 , 'number', STR0011	} ) // 'SALDO ANTERIOR'
	aAdd( aCpos, { 'DCANT' 		, STR0016 , 'string', STR0016	} ) // 'DC'
	aAdd( aCpos, { 'SALDODEB' 	, STR0012 , 'number', STR0012	} ) // 'DEBITO'
	aAdd( aCpos, { 'SALDOCRD' 	, STR0013 , 'number', STR0013	} ) // 'CREDITO'
	aAdd( aCpos, { 'MOVIMENTO' 	, STR0014 , 'number', STR0014	} ) // 'MOVIMIENTO'
	aAdd( aCpos, { 'SALDOATU'	, STR0015 , 'number', STR0015	} ) // 'SALDO ACTUAL'
	aAdd( aCpos, { 'DCATU' 		, STR0016 , 'string', STR0016	} ) // 'DC'

	// demais campos
	self:aFields := {'ECX','ECXDESC','ECY','ECYDESC','SALDOANT','DCANT','SALDODEB','SALDOCRD','MOVIMENTO','SALDOATU','DCATU','CT1_NORMAL','CT1_CLASSE','CT1_RES','CT1_CTASUP'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class balancedetercerosTReportsBusinessObject

	// Declaracao de variaveis
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character
	Local lImpAntLP     as logical
	Local lSaldoZero	as logical
	Local dDataLP		as date
	Local cDescMoed		as character
	Local cQuery        as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams    := oFilter:getParameters( )
	lImpAntLP  := (jParams['MV_PAR11'][1]==1)
	dDataLP    := IIf(lImpAntLP,FwDateTimeToLocal( jParams['MV_PAR12'][1] )[1],CtoD(""))
	lSaldoZero := (jParams['MV_PAR05'][1]==1)


	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoCT1 := getCpoUser( aCustomFields, 'CT1', 'CT1', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// descricao
	cDescMoed := 'CT1_DESC01'
	If !Empty(jParams['MV_PAR08'][1])
		cDescMoed := 'CT1_DESC' + jParams['MV_PAR08'][1]
		If CT1->(ColumnPos(cDescMoed)) <= 0
			cDescMoed := 'CT1_DESC01'
		Endif
	Endif

	// executa rotina para criar tabela temporaria
	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT QL7.QL7_CONTA ECX, QL7.QL7_ENT05 ECY, CT1.CT1_NORMAL, CT1.CT1_CLASSE, CT1.CT1_RES, CT1.CT1_CTASUP, CT1.? ECXDESC, ?"
	cQuery += " ( SELECT TOP 1 CV0_DESC FROM " + RetSqlName("CV0") + " CV0 WHERE CV0.CV0_FILIAL = ? AND CV0.CV0_CODIGO  = QL7.QL7_ENT05 AND CV0.D_E_L_E_T_ = ? ) AS ECYDESC,"

	cQuery += " 	 (SELECT SUM(QL7DT.QL7_DEBITO)"
	cQuery += " 	  FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA < ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += " 	  ) SALDOANTDB,"

	cQuery += " 	 (SELECT SUM(QL7DT.QL7_DEBITO)"
	cQuery += " 	  FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA <= ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += " 	  ) SALDOATUDB,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_DEBITO)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA < ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += "		  AND (QL7DT.QL7_LP = ? AND ((QL7DT.QL7_DTLP <> ? AND QL7DT.QL7_DTLP >= ?) OR (QL7DT.QL7_DTLP = ? AND QL7DT.QL7_DATA >= ?)))"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SLDLPANTDB,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_CREDIT)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA < ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SALDOANTCR,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_CREDIT)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA <= ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SALDOATUCR,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_CREDIT)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA < ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += "		  AND (QL7DT.QL7_LP = ? AND ((QL7DT.QL7_DTLP <> ? AND QL7DT.QL7_DTLP >= ?) OR (QL7DT.QL7_DTLP = ? AND QL7DT.QL7_DATA >= ?)))"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SLDLPANTCR,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_DEBITO)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA BETWEEN ? AND ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SALDODEB,"

	cQuery += "		 (SELECT SUM(QL7DT.QL7_CREDIT)"
	cQuery += "		 FROM " + RetSqlName("QL7") + " QL7DT"
	cQuery += " 	  WHERE QL7DT.QL7_FILIAL = ?"
	cQuery += " 	  AND QL7DT.QL7_DATA BETWEEN ? AND ?"
	cQuery += " 	  AND QL7DT.QL7_ENT05 = QL7.QL7_ENT05"
	cQuery += " 	  AND QL7DT.QL7_CONTA = QL7.QL7_CONTA"
	cQuery += " 	  AND QL7DT.QL7_MOEDA = ?"
	cQuery += " 	  AND QL7DT.QL7_TPSALD = ?"
	cQuery += " 	  AND QL7DT.D_E_L_E_T_ = ?"
	cQuery += "		 ) SALDOCRD"

	cQuery += " FROM " + RetSqlName("CT1") + " CT1"
	cQuery += " INNER JOIN " + RetSqlName("QL7") + " QL7 ON QL7.QL7_CONTA = CT1.CT1_CONTA AND CT1.D_E_L_E_T_ = ?"
	cQuery += " WHERE QL7.QL7_FILIAL = ?"
	cQuery += "  AND QL7.QL7_CONTA BETWEEN ? AND ?"
	cQuery += "  AND QL7.QL7_ENT05 BETWEEN ? AND ?"
	cQuery += "  AND QL7.QL7_MOEDA = ? "
	cQuery += "  AND QL7.QL7_TPSALD = ? "
	cQuery += "  AND CT1.CT1_CLASSE = ?"
	cQuery += "  AND (SELECT COUNT(CV0.CV0_CODIGO) FROM " + RetSqlName("CV0") + " CV0 WHERE CV0.CV0_CODIGO = QL7.QL7_ENT05 AND CV0.CV0_CLASSE = ? AND CV0.D_E_L_E_T_ = ?) > ?"
	cQuery += "  AND QL7.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery += " GROUP BY QL7_CONTA,QL7_ENT05,CT1_NORMAL,CT1_CLASSE,CT1_RES,CT1_CTASUP,? ?"

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	nSeq := 0

	oExecA:SetUnSafe( nSeq += 1, cDescMoed )
	oExecA:SetUnSafe( nSeq += 1, cCpoCT1 )
	oExecA:SetString( nSeq += 1, xFilial("CV0") ) 	// CV0_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, 'Z' )				// QL7_LP
	oExecA:SetString( nSeq += 1, DTOS( dDataLP ) )				// QL7_DTLP
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR12'][1] )[1] )) // QL7_DTLP
	oExecA:SetString( nSeq += 1, ' ' )				// QL7_DTLP
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, 'Z' )				// QL7_LP
	oExecA:SetString( nSeq += 1, DTOS( dDataLP ) )				// QL7_DTLP
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR12'][1] )[1] )) // QL7_DTLP
	oExecA:SetString( nSeq += 1, ' ' )				// QL7_DTLP
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR01'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, DtoS( FwDateTimeToLocal( jParams['MV_PAR02'][1] )[1] )) // QL7_DATA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("QL7") )	// QL7_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR03'][1] )	// QL7_CONTA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR04'][1] )	// QL7_CONTA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR09'][1] )	// QL7_ENT05
	oExecA:SetString( nSeq += 1, jParams['MV_PAR10'][1] )	// QL7_ENT05
	oExecA:SetString( nSeq += 1, jParams['MV_PAR06'][1] )	// QL7_MOEDA
	oExecA:SetString( nSeq += 1, jParams['MV_PAR07'][1] )	// QL7_TPSALD
	oExecA:SetString( nSeq += 1, '2' )				// CT1_CLASSE = '2'
	oExecA:SetString( nSeq += 1, '2' )				// CV0_CLASSE = '2'
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	oExecA:SetUnSafe( nSeq += 1, '0' )
	oExecA:SetString( nSeq += 1, ' ' )				// D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next
	oExecA:SetUnSafe( nSeq += 1, cCpoCT1 )
	oExecA:SetUnSafe( nSeq += 1, cDescMoed )

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] == "SALDOANT" )
				cDcAnt := ""
				IF ( cAliasA )->CT1_NORMAL == "1"
					If lImpAntLP
						nSaldoAnt := ( cAliasA )->SLDLPANTDB-( cAliasA )->SLDLPANTCR
					Else
						nSaldoAnt := ( cAliasA )->SALDOANTDB-( cAliasA )->SALDOANTCR
					Endif
					If nSaldoAnt > 0
						cDcAnt := "D"
					Else
						nSaldoAnt := nSaldoAnt * (-1)
						cDcAnt := "C"
					Endif
				Else
					If lImpAntLP
						nSaldoAnt := ( cAliasA )->SLDLPANTCR-( cAliasA )->SLDLPANTDB
					Else
						nSaldoAnt := ( cAliasA )->SALDOANTCR-( cAliasA )->SALDOANTDB
					Endif
					If nSaldoAnt > 0
						cDcAnt := "C"
					Else
						nSaldoAnt := nSaldoAnt * (-1)
						cDcAnt := "D"
					Endif
				Endif
				self:jItems[self:aStruct[nX][1]] := nSaldoAnt
			ElseIf ( self:aStruct[nX][5] == "DCANT" )
				self:jItems[self:aStruct[nX][1]] := cDcAnt
			ElseIf ( self:aStruct[nX][5] == "SALDOATU" )
				cDcAtu := ""
				IF ( cAliasA )->CT1_NORMAL == "1"
					nSaldoAtu := ( cAliasA )->SALDOATUDB-( cAliasA )->SALDOATUCR
					If nSaldoAtu > 0
						cDcAtu := "D"
					Else
						nSaldoAtu := nSaldoAtu * (-1)
						cDcAtu := "C"
					Endif
				Else
					nSaldoAtu := ( cAliasA )->SALDOATUCR-( cAliasA )->SALDOATUDB
					If nSaldoAtu > 0
						cDcAtu := "C"
					Else
						nSaldoAtu := nSaldoAtu * (-1)
						cDcAtu := "D"
					Endif
				Endif
				self:jItems[self:aStruct[nX][1]] := nSaldoAtu
			ElseIf ( self:aStruct[nX][5] == "DCATU" )
				self:jItems[self:aStruct[nX][1]] := cDcAtu
			ElseIf ( self:aStruct[nX][5] == "MOVIMENTO" )
				nMovimento := 0
				IF ( cAliasA )->CT1_NORMAL == "1"
					nMovimento := ( cAliasA )->SALDODEB -  ( cAliasA )->SALDOCRD
				Else
					nMovimento := ( cAliasA )->SALDOCRD -  ( cAliasA )->SALDODEB
				Endif
				self:jItems[self:aStruct[nX][1]] := nMovimento
			ElseIf ( self:aStruct[nX][5] == "CT1_NORMAL" )
				self:jItems[self:aStruct[nX][1]] := IIF(( cAliasA )->CT1_NORMAL == "1",STR0017,STR0018)	// "Deudora" / "Acreedora"
			ElseIf ( self:aStruct[nX][5] == "CT1_CLASSE" )
				self:jItems[self:aStruct[nX][1]] := IIF(( cAliasA )->CT1_CLASSE == "1",STR0019,STR0020)	// "Sintetica" / "Analitica"
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		If ( !lSaldoZero .And. nSaldoAtu <> 0 ) .Or. lSaldoZero
			self:oData:appendData( self:jItems )
		Endif

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 17/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class balancedetercerosTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
