#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.ctb.groupeddiarybook.ch'

namespace custom.contabil.groupeddiarybook.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.groupeddiarybook
@description Classe para creación del Objeto de Negocio de Recibos pendientes por cobrador para TReports
@author Marcelo Hruschka
@since 27/11/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGACTB', tables='CT1,CWS,CWT', name='Libro diario por Agrupadores', country='ALL', initialRelease='12.1.2410', customTables='CT1,CWS,CWT' )
class groupeddiarybookTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 27/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class groupeddiarybookTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Libro diario por Agrupadores'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Libro diario por Agrupadores'

	// Define a Área
	self:appendArea( STR0002 ) // 'Contabilid de Gestión'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'CTBSV810' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	self:aFields := {'CWS_FILIAL','CWS_LANC','CWS_DTLANC','CWS_DSCGRP','CWT_CONTA','CT1_DESC01','CWT_DEBITO','CWT_CREDIT'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 27/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class groupeddiarybookTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoCT1 := getCpoUser( aCustomFields, 'CT1', 'CT1', ',' )
	cCpoCWS := getCpoUser( aCustomFields, 'CWS', 'CWS', ',' )
	cCpoCWT := getCpoUser( aCustomFields, 'CWT', 'CWT', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT CWS.CWS_FILIAL, CWS.CWS_LANC, CWS.CWS_DTLANC, CWS.CWS_DSCGRP, CWT.CWT_CONTA, CT1.CT1_DESC01, CWT.CWT_DEBITO, CWT.CWT_CREDIT, "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " ? "
	cQuery += " CWT.R_E_C_N_O_ AS REG"
	cQuery += " FROM " + RetSqlName("CWS") + " CWS"
	cQuery += " INNER JOIN " + RetSqlName("CWT") + " CWT ON CWT.CWT_FILIAL = ? AND CWT.CWT_LANC = CWS.CWS_LANC  AND CWT.D_E_L_E_T_ = ?"
	cQuery += " INNER JOIN " + RetSqlName("CT1") + " CT1 ON CT1.CT1_FILIAL = ? AND CT1.CT1_CONTA = CWT.CWT_CONTA AND CT1.D_E_L_E_T_ = ?"
	cQuery += " WHERE CWS.CWS_FILIAL = ?"
	cQuery += "   AND CWS.CWS_LANC BETWEEN ? AND ? "
	cQuery += "   AND CWS.CWS_DTLANC BETWEEN ? AND ? "
	cQuery += "   AND CWS.D_E_L_E_T_ = ? "

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND " + aFiltro[nX, 1] + ' ' + aFiltro[nX, 2] + " " + aFiltro[nX, 3] + " "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )

	oExecA:SetUnSafe( 01, cCpoCT1 )
	oExecA:SetUnSafe( 02, cCpoCWS )
	oExecA:SetUnSafe( 03, cCpoCWT )
	oExecA:SetString( 04, xFilial("CWT") )			// CWT_FILIAL
	oExecA:SetString( 05, '' 			 )			// CWT.D_E_L_E_T_
	oExecA:SetString( 06, xFilial("CT1") )			// CT1_FILIAL
	oExecA:SetString( 07, '' 			 )			// CT1.D_E_L_E_T_
	oExecA:SetString( 08, xFilial("CWS") )			// CWS_FILIAL
	oExecA:SetString( 09, jParams['MV_PAR01'][1] )	// CWS_LANC
	oExecA:SetString( 10, jParams['MV_PAR02'][1] )	// CWS_LANC
	oExecA:SetString( 11, DtoS( FwDateTimeToLocal( jParams['MV_PAR03'][1] )[1] ) )			// CWS_DTLANC
	oExecA:SetString( 12, DtoS( FwDateTimeToLocal( jParams['MV_PAR04'][1] )[1] ) )			// CWS_DTLANC
	oExecA:SetString( 13, '' 			 )			// CWS.D_E_L_E_T_

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 27/11/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class groupeddiarybookTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )

