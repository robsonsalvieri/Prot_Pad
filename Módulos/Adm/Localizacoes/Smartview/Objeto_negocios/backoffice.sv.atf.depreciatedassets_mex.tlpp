#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.atf.depreciatedassets_mex.ch'

namespace custom.activofijo.depreciatedassets_mex.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.depreciatedassets_mex
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 09/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAATF', tables='SN1,SN3,CT1,CTT', name="Depreciación actualizada activo fijo", country='MEX', initialRelease='12.1.2310', customTables='SN1,SN3,CT1,CTT' )
class depreciatedassets_mexTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class depreciatedassets_mexTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // "Depreciación actualizada activo fijo"

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // "Depreciación actualizada activo fijo"

	// Define a Área
	self:appendArea( STR0002 ) // 'Activo Fijo'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'ATFSV500' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// define campos com descrição mexico
	aAdd( aCpos, { 'N1_DESCRIC'		, STR0007 , 'string', STR0007	} ) // "ACTIVO FIJO"
	aAdd( aCpos, { 'N1_AQUISIC'		, STR0008 , 'date' , STR0008	} ) // "FECHA ADQUISICIÓN"
	aAdd( aCpos, { 'N3_VORIG1'		, STR0009 , 'number', STR0009	} ) // "MOI"
	aAdd( aCpos, { 'N3_TXDEPR1'		, STR0010 , 'number', STR0010	} ) // "TASA DE DEPREC."
	aAdd( aCpos, { 'MESES'		    , STR0011 , 'number', STR0011	} ) // "MESES DE USO"
	aAdd( aCpos, { 'N3_VRDMES1'		, STR0012 , 'number', STR0012	} ) // "DEPRECIACIÓN MENSUAL"
	aAdd( aCpos, { 'N3_VRDBAL1'		, STR0013 , 'number', STR0013	} ) // "DEPREC. DEL EJERCICIO"
	aAdd( aCpos, { 'INPCP'		    , STR0014 , 'number', STR0014	} ) // "INPC MEDIO UTILIZADO"
	aAdd( aCpos, { 'INPCDATAAQ'		, STR0015 , 'number', STR0015	} ) // "INPC FECHA ADQUISICIÓN"
	aAdd( aCpos, { 'FATORATU'		, STR0016 , 'number', STR0016	} ) // "FACTOR ACTUALIZACIÓN"
	aAdd( aCpos, { 'DPATUAL'		, STR0017 , 'number', STR0017	} ) // "DEPREC. ACTUALIZADA"
	aAdd( aCpos, { 'N3_VRDACM1'		, STR0018 , 'number', STR0018	} ) // "DEPREC. ACUMULADA"
	aAdd( aCpos, { 'SALDORED'		, STR0019 , 'number', STR0019	} ) // "SALDO X REDIMIR"
	aAdd( aCpos, { 'SALDOREDA'		, STR0020 , 'number', STR0020	} ) // "REDIMIR ACTUALIZADO"

	// demais campos
	self:aFields := {'N1_DESCRIC','N1_AQUISIC','N3_VORIG1','N3_TXDEPR1','MESES','N3_VRDMES1','N3_VRDBAL1','INPCP','INPCDATAAQ','FATORATU','DPATUAL','N3_VRDACM1','SALDORED','SALDOREDA','N3_CCONTAB','N3_CCUSTO','CT1_DESC01'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class depreciatedassets_mexTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX		 	as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	Local nValor         as numeric
	Local nTxMed         as numeric
	Local nMesIni        as numeric
	Local nMesMed        as numeric
	Local nTxAqui        as numeric
	Local nTxAtua        as numeric
	Local nSldAct        as numeric
	Local nMesAquis      as numeric
	Local nMesUso        as numeric


	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSN1 := getCpoUser( aCustomFields, 'SN1', 'N1' , ',' )
	cCpoSN3 := getCpoUser( aCustomFields, 'SN3', 'N3' , ',' )
	cCpoCT1 := getCpoUser( aCustomFields, 'CT1', 'CT1', ',' )
	cCpoCTT := getCpoUser( aCustomFields, 'CTT', 'CTT', ',' )

	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT
	cQuery += " N3_CBASE, N3_ITEM, N3_TIPO, N3_CCUSTO, N3_CCONTAB, N3_VORIG1, N3_AMPLIA1, N3_VRCACM1, N3_VRDACM1, N3_VRCDA1,"
	cQuery += " N3_VORIG2, N3_AMPLIA2, N3_VRDACM2, N3_VORIG3, N3_AMPLIA3, N3_VRDACM3, N3_VORIG4, N3_AMPLIA4, N3_VRDACM4,"
	cQuery += " N3_VORIG5, N3_AMPLIA5, N3_VRDACM5, N3_CDEPREC, N3_CCDEPR, N1_DESCRIC, CTT_DESC01, CT1_DESC01, N3_TXDEPR1,"
	cQuery += " ? " // SN1
	cQuery += " ? " // SN3
	cQuery += " ? " // CT1
	cQuery += " ? " // CTT
	cQuery += " N3_VORIG1,N1_AQUISIC,N3_DINDEPR,N3_VRDMES1,N3_VRDBAL1,N3_VRDMES1,N3_AQUISIC,N3_FIMDEPR,N3_BAIXA,N3_DTBAIXA"
	cQuery += " FROM " + RetSqlName("SN3") + " SN3"
	cQuery += " JOIN " + RetSqlName("SN1") + " SN1 ON SN1.N1_FILIAL =  ? AND SN1.N1_CBASE = SN3.N3_CBASE AND SN1.N1_ITEM = SN3.N3_ITEM AND SN1.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("CT1") + " CT1 ON CT1.CT1_FILIAL =  ? AND CT1.CT1_CONTA = SN3.N3_CCONTAB AND CT1.D_E_L_E_T_ = ?"
	cQuery += " LEFT JOIN " + RetSqlName("CTT") + " CTT ON CTT.CTT_FILIAL =  ? AND CTT.CTT_CUSTO = SN3.N3_CCUSTO AND CTT.D_E_L_E_T_ = ?"
	cQuery += " WHERE "
	cQuery += " SN3.N3_FILIAL = ? AND "
	cQuery += " SN3.N3_CBASE BETWEEN ? AND ? AND"
	cQuery += " SN3.N3_BAIXA = ? AND"
	cQuery += " SN3.N3_TXDEPR1 <> ? AND"
	cQuery += " (SN3.N3_CDEPREC <> ? OR  SN3.N3_CDESP <> ? OR SN3.N3_CCDEPR <> ? ) AND"
	cQuery += " SN3.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSN1 )
	oExecA:SetUnSafe( nSeq += 1, cCpoSN3 )
	oExecA:SetUnSafe( nSeq += 1, cCpoCT1 )
	oExecA:SetUnSafe( nSeq += 1, cCpoCTT )
	oExecA:SetString( nSeq += 1, xFilial("SN1") )			// N1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// SN1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CT1") )			// CT1_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// CT1.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("CTT") )			// CTT_FILIAL
	oExecA:SetString( nSeq += 1, ' ' )						// CTT.D_E_L_E_T_
	oExecA:SetString( nSeq += 1, xFilial("SN3") )			// N3_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// N3_CBASE
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// N3_CBASE
	oExecA:SetString( nSeq += 1, '0' )						// N3_BAIXA
	oExecA:SetUnSafe( nSeq += 1, '0' )		 		    	// N3_TXDEPR1
	oExecA:SetString( nSeq += 1, ' ' )						// SN3.N3_CDEPREC
	oExecA:SetString( nSeq += 1, ' ' )						// SN3.N3_CDESP
	oExecA:SetString( nSeq += 1, ' ' )						// SN3.N3_CCDEPR
	oExecA:SetString( nSeq += 1, ' ' )						// SN3.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] == "MESES" )
				self:jItems[self:aStruct[nX][1]] := Atf051Dt(STOD((cAliasA)->N3_FIMDEPR),STOD((cAliasA)->N3_AQUISIC),(cAliasA)->N3_BAIXA,STOD((cAliasA)->N3_DTBAIXA))
			ElseIf ( self:aStruct[nX][5] $ "INPCP|INPCDATAAQ|FATORATU|DPATUAL|SALDORED|SALDOREDA" )

				nTxMed    :=0
				nMesIni   :=0
				nMesMed   :=0
				nTxAqui   :=0
				nTxAtua   :=0
				nSldAct   :=0
				nMesAquis :=0
				nMesUso   := Atf051Dt(STOD((cAliasA)->N3_FIMDEPR),STOD((cAliasA)->N3_AQUISIC),(cAliasA)->N3_BAIXA,STOD((cAliasA)->N3_DTBAIXA))

				If Year(STOD((cAliasA)->N3_AQUISIC)) == Year(dDataBase)
					nMesAquis := Month(STOD((cAliasA)->N3_AQUISIC))
				Endif

				nMesMed:=Int((nMesUso/2)+nMesAquis)
				cMesMed:=Strzero(nMesMed,2)

				DbSelectArea("SIE")
				DbSetOrder(1)
				If DbSeek( xFilial("SIE")+Str(Year(dDataBase),4)+cMesMed)
					nTxMed:=SIE->IE_INDICE
				EndIf
				dDatInDep := STOD((cAliasA)->N3_AQUISIC)
				If DbSeek( xFilial("SIE")+Str(Year(dDatInDep),4)+STRzero(Month(dDatInDep),2))
					nTxAqui:=SIE->IE_INDICE
				EndIf
				nTxAtua:=nTxMed/nTxAqui
				nSldAct:=(cAliasA)->N3_VORIG1-(cAliasA)->N3_VRDACM1

				If ( self:aStruct[nX][5] $ "INPCP")
					self:jItems[self:aStruct[nX][1]] := nTxMed
				ElseIf ( self:aStruct[nX][5] $ "INPCDATAAQ")
					self:jItems[self:aStruct[nX][1]] := nTxAqui
				ElseIf ( self:aStruct[nX][5] $ "FATORATU")
					self:jItems[self:aStruct[nX][1]] := nTxAtua
				ElseIf ( self:aStruct[nX][5] $ "DPATUAL")
					self:jItems[self:aStruct[nX][1]] := (cAliasA)->N3_VRDBAL1*nTxAtua
				ElseIf ( self:aStruct[nX][5] $ "SALDORED")
					self:jItems[self:aStruct[nX][1]] := nSldAct
				ElseIf ( self:aStruct[nX][5] $ "SALDOREDA")
					self:jItems[self:aStruct[nX][1]] := nSldAct*nTxAtua
				Endif

			ElseIf ( self:aStruct[nX][5] == "N3_VRDMES1" )
				nValor := 0
				If (cAliasA)->N3_VRDMES1 == 0 .and. !Empty((cAliasA)->N3_FIMDEPR)
					DbSelectArea("SN4")
					DbSetOrder(1)
					If DbSeek(xFilial("SN4")+(cAliasA)->N3_CBASE+(cAliasA)->N3_ITEM+(cAliasA)->N3_TIPO+(cAliasA)->N3_FIMDEPR+"06")
						nValor := SN4->N4_VLROC1
					Endif
					//senao atribui o valor da depreciacao mensal no SN3
				Else
					nValor := (cAliasA)->N3_VRDMES1
				Endif
				self:jItems[self:aStruct[nX][1]] := nValor
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class depreciatedassets_mexTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
