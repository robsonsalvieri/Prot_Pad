#INCLUDE 'msobject.ch'
#INCLUDE 'totvs.framework.treports.integratedprovider.th'
#INCLUDE 'backoffice.sv.fin.collectors.ch'

namespace custom.financiero.collectors.integratedprovider

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.com.collectors
@description Classe para creación del Objeto de Negocio para TReports
@author Marcelo Hruschka
@since 09/12/2024
@version 1.1
*/
//-------------------------------------------------------------------------------
@totvsFrameworkTReportsIntegratedProvider( active=.T., team='SIGAFIN', tables='SAQ', name='Cobradores Datos Generales', country='ALL', initialRelease='12.1.2410', customTables='SAQ' )
class collectorsTReportsBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public Method new() as object
	public Method getData() as object
	public Method getSchema() as object

	protected data aFields as array
	protected data aStruct as array
	protected data nRecno as numeric
	protected data jItems as json

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@Return object: self
@author Marcelo Hruschka
@since 19/11/2024
@version 1.0
*/
//-------------------------------------------------------------------
Method new() class collectorsTReportsBusinessObject

	Local aCpos := {} as array

	_Super:new()

	//Define o nome do Objeto de Negócio
	self:setDisplayName( STR0001 )  // 'Cobradores Datos Generales'

	//Define a descrição do Objeto de Negócio
	self:setDescription( STR0001 )  // 'Cobradores Datos Generales'

	// Define a Área
	self:appendArea( STR0002 ) // 'Financiero'

	// Define se as perguntas terao lookup
	self:setIsLookUp( .T. )

	// Indica o pergunte que será utilizado no relatório
	If !self:setPergunte( 'FINSV512' )
		IIf(!self:setErrorStatus( 400, STR0003, STR0004 ),FwLogMsg( 'WARN',, 'Smart View',,,, STR0005,,, ),'') // 'Sin Preguntas' // '¡Verifique el grupo de preguntas dado!' // 'Código de error no válido, solo acepte códigos de error 4xx'
		FwLogMsg( 'WARN',, 'Smart View',,,, STR0006,,, ) // 'Grupo de preguntas no encontrado!'
	EndIf

	// demais campos
	If SAQ->(ColumnPos("AQ_STATUS")) <= 0
		aAdd( aCpos, { 'AQ_STATUS'	, 'Estatus' , 'string' , 'Estatus' } ) // 'Estatus'
	Endif
	self:aFields := {'AQ_FILIAL','AQ_COD','AQ_NOME','AQ_END','AQ_BAIRRO','AQ_MUN','AQ_EST','AQ_CEP','AQ_TEL','AQ_FAX','AQ_EMAIL','AQ_TIPOREC','AQ_CGC','AQ_FORNECE','AQ_LOJA','AQ_GERASE2','AQ_REGIAO','AQ_COMIS','AQ_DIA','AQ_DDD','AQ_DOCTRAN','AQ_STATUS'}

	self:aStruct := getStrutObj( self:aFields, aCpos )

Return( self )

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna los datos del objeto de negócio
@param nPage, numérico, indica la pagina actual del relatório
@param oFilter, objeto, contiene el filtro del TReports
@return object: self:oData
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getData( nPage as numeric, oFilter as object) as object class collectorsTReportsBusinessObject

	// Declaracao de variaveis
	Local cQuery		as character
	Local cFiltro 		as character

	Local nX			as numeric
	Local n1			as numeric
	Local nPosMon		as numeric
	Local nPosExp		as numeric
	Local nDecs 		as numeric

	Local jParams 		as json

	Local aPDFields		as array
	Local aFiltro		as array
	Local aCustomFields as array

	Local lObfuscated	as logical

	Local oExecA		as object
	Local cAliasA       as character
	Local cExp          as character

	cFiltro 			:= ''
	cAliasA			    := ''
	cAliasB			    := ''
	cExp                := ''

	nPosMon   		:= 0
	nPosExp 		:= 0
	nDecs 			:= 0
	nX 				:= 0
	n1				:= 0

	aPDFields		:= { }
	aFiltro			:= { }
	aCustomFields	:= { }

	// Verifica se existem campos sensiveis na lista de campos a serem retornados
	aPDFields := FwProtectedDataUtil():UsrAccessPDField( __cUserID, self:aFields )
	lObfuscated := Len( aPDFields ) != Len( self:aFields )

	// Coleta os dados dos parametros
	jParams := oFilter:getParameters( )

	// Coleta os campos personalizados pelo usuário
	aCustomFields := self:getCustomFields( )
	cCpoSAQ := getCpoUser( aCustomFields, 'SAQ', 'AQ', ',' )


	// Adiciona campo customizado na estrutura de campos
	For n1 := 1 To Len( aCustomFields )
		aAdd( self:aStruct, { aCustomFields[n1, 1], aCustomFields[n1, 4], aCustomFields[n1, 3], aCustomFields[n1, 2], aCustomFields[n1, 1] } )
		aAdd( self:aFields, aCustomFields[n1, 1] )
	Next

	If oFilter:hasFilter( )
		// Tratamento para conversão para moeda selecionada
		cFiltro := oFilter:getSQLExpression( )
		aFiltro := getParamToArr( cFiltro )
		// Ordena os parametros
		aSort( aFiltro,,, { |x, y| x[1] + x[3] < y[1] + y[3] } )
	EndIf

	// Realiza a montagem da QUERY que será enviada para o banco de dados
	cQuery := "SELECT AQ_FILIAL, AQ_COD, AQ_NOME, AQ_END, AQ_BAIRRO, AQ_MUN, AQ_EST, AQ_CEP, AQ_TEL, AQ_FAX, "
	cQuery += " AQ_EMAIL, AQ_TIPOREC, AQ_CGC, AQ_FORNECE, AQ_LOJA, AQ_GERASE2, AQ_REGIAO, AQ_COMIS, AQ_DIA, AQ_DDD, AQ_DOCTRAN,"
	cQuery += " ? "
	If SAQ->(ColumnPos("AQ_STATUS")) > 0
	   cQuery += "SAQ.AQ_STATUS, "
	Else
	   cQuery += "' ' AQ_STATUS, "
	Endif
	cQuery += " SAQ.R_E_C_N_O_ REG"
	cQuery += " FROM " + RetSqlName("SAQ") + " SAQ"
	cQuery += " WHERE SAQ.AQ_FILIAL = ?"
	cQuery += "	  AND SAQ.AQ_COD  BETWEEN ? AND ?"
	cQuery += "   AND SAQ.D_E_L_E_T_ = ?"

	// Agrega os filtros na QUERY
	For nX := 1 To Len( aFiltro )
		cQuery += " AND ? " + ' ' + " ? ? "
	Next

	cQuery := ChangeQuery(cQuery)
	oExecA := FwExecStatement():New( cQuery )
	nSeq := 0
	oExecA:SetUnSafe( nSeq += 1, cCpoSAQ )
	oExecA:SetString( nSeq += 1, xFilial("SAQ") )			// AQ_FILIAL
	oExecA:SetString( nSeq += 1, jParams['MV_PAR01'][1] )	// AQ_COD
	oExecA:SetString( nSeq += 1, jParams['MV_PAR02'][1] )	// AQ_COD
	oExecA:SetString( nSeq += 1, ' ' )						// SAQ.D_E_L_E_T_
	For nX := 1 To Len( aFiltro )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 1] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 2] )
		oExecA:SetUnSafe ( nSeq += 1, aFiltro[nX, 3] )
	Next	

	// Executa a QUERY e cria uma tabela temporaria com os dados retornados
	cAliasA := oExecA:OpenAlias( )

	// Alimenta o objeto de dados da classe para retornar ao SmartView
	While !( cAliasA )->( EOF( ) )

		self:jItems := JsonObject():new( )
		self:nRecno := ( cAliasA )->REG

		For nX := 1 To Len( self:aStruct )
			If lObfuscated .And. ( aScan( aPDFields, self:aStruct[nX][5] ) == 0 )
				self:jItems[self:aStruct[nX][1]] := FwProtectedDataUtil():ValueAsteriskToAnonymize( ( cAliasA )->&( self:aStruct[nX][5] ) )
			ElseIf ( self:aStruct[nX][3] == 'date' )
				self:jItems[self:aStruct[nX][1]] := totvs.framework.treports.date.dateToTimeStamp( StoD( ( cAliasA )->&( self:aStruct[nX][5] ) ) )
			ElseIf ( self:aStruct[nX][5] == "AQ_TIPOREC" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0007 // "1=Oficial"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0008 // "2=Provisional"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "3"
					self:jItems[self:aStruct[nX][1]] := STR0009 // "3=Ninguno"
				Endif
			ElseIf ( self:aStruct[nX][5] == "AQ_GERASE2" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0010 // "Si"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0011 // "No"
				Endif
			ElseIf ( self:aStruct[nX][5] == "AQ_DDD" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "F"
					self:jItems[self:aStruct[nX][1]] := STR0012 // "Fuera Mes"
				Endif
			ElseIf ( self:aStruct[nX][5] == "AQ_DOCTRAN" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0010 // "Si"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0011 // "No"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "3"
					self:jItems[self:aStruct[nX][1]] := STR0013 // "Indiferente"
				Endif
			ElseIf ( self:aStruct[nX][5] == "AQ_STATUS" )
				self:jItems[self:aStruct[nX][1]] := ""
				If ( cAliasA )->&( self:aStruct[nX][5] ) == "1"
					self:jItems[self:aStruct[nX][1]] := STR0014 // "Activo"
				ElseIf ( cAliasA )->&( self:aStruct[nX][5] ) == "2"
					self:jItems[self:aStruct[nX][1]] := STR0015 // "Inactivo"
				Endif				
			Else
				self:jItems[self:aStruct[nX][1]] := ( cAliasA )->&( self:aStruct[nX][5] )
			Endif
		Next

		// Inclui os dados no objeto paea retorno ao SmartView
		//self:processData()
		self:oData:appendData( self:jItems )

		( cAliasA )->( DbSkip( ) )

	End
	( cAliasA )->( DbCloseArea( ) )

	oExecA:Destroy( )
	oExecA := Nil

Return( self:oData )

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
@description Retorna la estructura de los campos
@return object: self:oSchema
@author Marcelo Hruschka
@since 09/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
Method getSchema() as object class collectorsTReportsBusinessObject

	Local nX as numeric

	// Adiciona as propriedades dos campos que serão retornados para o SMARTView
	For nX := 1 To Len( self:aStruct )
		self:addProperty( self:aStruct[nX][1], self:aStruct[nX][2], self:aStruct[nX][3], self:aStruct[nX][4], self:aStruct[nX][5] )
	Next

Return( self:oSchema )
