#include 'tlpp-core.th'
#include "FWMVCDEF.CH"
#INCLUDE "PAYMENTFORMSERVICE.CH"
namespace tr.paymentForm

/*/{Protheus.doc} paymentFormService
Class paymentFormService
@type class
@version  1
@author José González
@since 18/03/2021
/*/
Class paymentFormService
	Public Method new()
	Public Method getpaymentForm()
	Public Method checkReaderService()
	Public Method serviceGetCGC()
	Public Method getpostValidCpo()
	Public Method getpostValidHdr()
	Public Method getpaymentView()
	Private Method getError()
	Private Method setError()
	Private Method getJson()
	Private Method saveHeader()
	Private Method setValues()
EndClass

/*/{Protheus.doc} paymentFormService::new
Constructor de la clase
@type method
@version  1
@author José González
@since 18/03/2021
/*/
Method new() Class paymentFormService
Return

/*/{Protheus.doc} paymentFormService::getpaymentForm
Obtiene el contenido de los campos de la tabla SEL Usados
@type method
@version  1
@author José González
@since 18/03/2021
/*/
Method getpaymentForm (cParams) Class paymentFormService
	Local oStruSEL1	:= FWFormStruct( 2,'SEL',,.F.) //2-View
	Local oJParams	:= JsonObject():New()
	Local oModel	:= FwLoadModel("FINA887")	
	Local aFields	:= oModel:GetModel('SEL_DETAIL'):oFormModelStruct:GetFields()
	Local oMdlSEL	:= oModel:GetModel('SEL_DETAIL'):oFormModelStruct
	Local lExistDB	:= .F.
	Local cMask		:= ""
	Local cCliLoja	:= "" As Character
	Local oResponse := JsonObject():New()
	Local oPaymentForm := JsonObject():new()
	Local aOptions 	:= {} As Array
	Local aTemp		:= {} As Array
	Local aJson 	:= {} As Array
	Local aAux 		:= {} As Array
	Local aCposNo 	:= {} As Array
	Local nItens 	:= 1 As Numeric
	Local nI 		:= 1 As Numeric
	Local nJ 		:= 1 As Numeric
	Local nOrden 	:= 1  As Numeric
	Local nTempOrd	As Numeric
	Local nOptions 	:= 0  As Numeric
	Local cCampo 	:= '' As Character
	Local cTipo 	:= '' As Character
	Local cThousand	:= ''
	Local cTamanho 	:= ''
	Local nAux		:= 0
	Local cCposNo 	:= ''	As Character //Indica los campos que deben ser bloqueados y no permitir edición por el usuario, pero el front y disparadores si pueden asiganar datos a estos campos aun estando bloqueados.
	Local cTitulo 	:= '' As Character
	Local cSegOfi	:= ''	As Character
	Local cTrigger 	:= "EL_VALBASE|EL_ALIQ" //Campos virtuales que contienen triggers
	Local cFormatd := ""
	Local cFordat := UPPER(GetPvProfString(GetEnvServer(), "DateFormat", "undefined", GetAdv97()))
	Local cHelp	 As Character
	Local dEmissa As Date
	
	conout( "inicia getpaymentForm "  + Time())
	SetFunName("FINA998")

	cSegOfi	:= SuperGetMv("MV_SEGOFI",.T.,0) //Indica el tipo de control secuencial y único que se utilizará en la contabilidad.
	IF cPaisloc == "ARG"
		cCposNo += "EL_VALBASE|EL_ALIQ|EL_AGREGAN|EL_CONCSUS|EL_CFO|EL_RET_MUN|EL_SIRECER|EL_SIRESEG"
		AAdd(aCposNo,"EL_TPCRED")
	ELSEIF cPaisloc == "PER"
		cCposNo += "EL_SERRET"
	ELSEIF cPaisloc == "PAR"
		cCposNo += "EL_VALIMP1|EL_ALQIMP1"
	ENDIF

	//Campos del encabezado del recibo y no deben mostrarse en la forma de pago
	AAdd(aCposNo,"EL_FILIAL")
	AAdd(aCposNo,"EL_SERIE")
	AAdd(aCposNo,"EL_RECIBO")
	AAdd(aCposNo,"EL_DTDIGIT")
	AAdd(aCposNo,"EL_CANCEL")
	AAdd(aCposNo,"EL_PREREC")
	AAdd(aCposNo,"EL_CLIENTE")
	AAdd(aCposNo,"EL_LOJA")
	AAdd(aCposNo,"EL_VLMOED1")
	AAdd(aCposNo,"EL_COBRAD")
	AAdd(aCposNo,"EL_RECPROV")
	AAdd(aCposNo,"EL_CLIORIG")
	AAdd(aCposNo,"EL_LOJORIG")
	AAdd(aCposNo,"EL_TIPAGRO")
	AAdd(aCposNo,"EL_RETGAN")
	AAdd(aCposNo,"EL_SERSUS")
	AAdd(aCposNo,"EL_RECSUS")
	AAdd(aCposNo,"EL_FACTOR")
	AAdd(aCposNo,"EL_HORA") 
	If Alltrim(GetSX3Cache("EL_TIPO","X3_RELACAO")) == ''
		AAdd(aCposNo,"EL_TIPO")
	EndIf

	//Obtenermos el numero de campos
	nItens := LEN(aFields)
	oModel:SetOperation( MODEL_OPERATION_INSERT )
	oModel:ACTIVATE()
conout( "getpaymentForm  Finaliza carga "  + Time())
	//Obtenemos el encabezado del recibo para guardarlo en el modelo
	oJParams:fromJson(cParams)
	oModel:LoadValue('FJT_MASTER', "FJT_CLIENT",oJParams['header']['client'])	
	aHeader := oJParams['header']:GetNames()
	self:saveHeader(aHeader,@oModel,@oJParams,"header")
conout( "getpaymentForm   Inicia nItens "  + Time())
	FOR nI := 1 to nItens
		//El campo existe en diccionario o se genero por medio del modelo (.T. si esta en diccionario y .F. si es por modelo)
		lExistDB := SEL->(ColumnPos(aFields[nI][3])) > 0 
		nTempOrd := 0
		cTipo		:= 	""
		cTamanho	:= 	""
		cTitulo 	:= 	""
		aOptions 	:=	{}
		aAux 		:= 	{}
		cCampo 		:= 	""
		nOptions 	:= 	0
		cMask		:= 	""
		cCampo 		:= 	aFields[nI][MODEL_FIELD_IDFIELD]
		cFormatd	:=""
		nAux		:= 0
		cThousand	:= ''
		aColums		:= {}
		

		IF lExistDB
			//Si el campo esta como no usado no continua y pasa al siguiente
			IF !X3Uso(GetSx3Cache(cCampo,"X3_USADO"))
				LOOP
			ENDIF

			If cCampo =="EL_BANCO" //--bancos
                Aadd(aJson,JsonObject():new())
                nPos := Len(aJson)
                aJson[nPos]['trigger'] := .T.
                aJson[nPos]['searchService' ]  := "/api/v1/totvsRecibos/catconpag/" +Alltrim(Getsx3Cache( cCampo,'X3_F3' ))
				aJson[nPos]["columns"] 	:= SetColProps({{"cod",STR0002},{"agencia",STR0003},{"numcon",STR0004},{"nome",STR0005}}, .F.)
                aJson[nPos]['maxLength'] := oMdlSEL:GetProperty("EL_BANCOS",MODEL_FIELD_TAMANHO) 
                aJson[nPos]['decimalsLength'] :=0
                aJson[nPos]['thousandMaxlength'] := oMdlSEL:GetProperty("EL_BANCOS",MODEL_FIELD_TAMANHO)
                aJson[nPos]['label'] := AllTrim(oMdlSEL:GetProperty("EL_BANCOS",MODEL_FIELD_TITULO))
                aJson[nPos]['valid'] :=""
                aJson[nPos]['campo'] :="EL_BANCOS"
                aJson[nPos]['property'] :="bancos"
                aJson[nPos]['required'] :=.F.
                aJson[nPos]['gridColumns'] := 12
                aJson[nPos]['options'] := {}
                aJson[nPos]['value'] :=""
                aJson[nPos]['mask'] :=""
                aJson[nPos]['order'] :=nOrden
                nOrden++
                aJson[nPos]['type'] :="string"
                aJson[nPos]['disabled']:= .F.
                aJson[nPos]['visible'] :=.T.
				aJson[nPos]['infiniteScroll'] := .F.
				aJson[nPos]['optionsMulti'] := .F.
				aJson[nPos]['advancedFilters'] := SetColProps({{"codigo",STR0002},{"agencia",STR0003},{"cuenta",STR0004},{"descripcion",STR0005}},.T.)
            EndIf
            If cCampo =="EL_BCOCHQ" //--bancos
                Aadd(aJson,JsonObject():new())
                nPos := Len(aJson)
                aJson[nPos]['trigger'] := .T.
                aJson[nPos]['searchService' ]  := "/api/v1/totvsRecibos/catconpag/" +Alltrim(Getsx3Cache( cCampo,'X3_F3' ))
				IF cPaisloc $ "ARG|PAR"
					aJson[nPos]['columns'] := SetColProps({{"cod",STR0006},{"agenci",STR0003},{"postal",STR0007},{"nomege",STR0005}},.F.)
				Else
					aJson[nPos]['columns'] := SetColProps({{"cod",STR0002},{"agencia",STR0003},{"numcon",STR0004},{"nome",STR0005}},.F.)
				EndIf
                aJson[nPos]['maxLength'] := oMdlSEL:GetProperty("EL_BCOCHQS",MODEL_FIELD_TAMANHO)
                aJson[nPos]['decimalsLength'] :=0
                aJson[nPos]['thousandMaxlength'] := oMdlSEL:GetProperty("EL_BCOCHQS",MODEL_FIELD_TAMANHO)
                aJson[nPos]['label'] := AllTrim(oMdlSEL:GetProperty("EL_BCOCHQS",MODEL_FIELD_TITULO))
                aJson[nPos]['valid'] :=""
                aJson[nPos]['campo'] :="EL_BCOCHQS"
                aJson[nPos]['property'] :="bcochqs"
                aJson[nPos]['required'] :=.F.
                aJson[nPos]['gridColumns'] :=12
                aJson[nPos]['options'] := {}
                aJson[nPos]['value'] :=""
                aJson[nPos]['mask'] :=""
                aJson[nPos]['order'] :=nOrden
                nOrden++
                aJson[nPos]['type'] :="string"
                aJson[nPos]['disabled']:= .F.
                aJson[nPos]['visible'] :=.T.
				aJson[nPos]['infiniteScroll'] := .F.
				aJson[nPos]['optionsMulti'] := .F.
				IF cPaisloc $ "ARG|PAR"
					aJson[nPos]['advancedFilters'] := SetColProps({{"codigo",STR0006},{"agencia",STR0003},{"postal",STR0007},{"descripcion",STR0005}},.T.)
				Else
					aJson[nPos]['advancedFilters'] := SetColProps({{"codigo",STR0002},{"agencia",STR0003},{"postal",STR0004},{"descripcion",STR0005}},.T.)
				EndIf
            EndIf

			Aadd(aJson,JsonObject():new())
			nPos := Len(aJson)

			//Se obtiene el tipo y/o tamaño del campo
			IF Getsx3Cache( cCampo,'X3_TIPO' ) == 'C'
				cTipo 		:= 'string'
				cTamanho 	:= Getsx3Cache( cCampo,'X3_TAMANHO' )
			ELSEIF Getsx3Cache( cCampo,'X3_TIPO' ) == 'D'
				cTipo 		:= 'date'
				IF cFordat == "AMERICAN"
					cFormatd := 'mm/dd/yyyy' 
				EndIF
				IF cFordat == "DEFAULT"
					cFormatd := 'dd/mm/yyyy'
				EndIF
			ELSEIF Getsx3Cache( cCampo,'X3_TIPO' ) == 'N'
				cTipo 		:= 'currency'
				cTamanho 	:= ''
				nAux		:= Getsx3Cache( cCampo,"X3_TAMANHO" ) - (Getsx3Cache( cCampo,"X3_DECIMAL")+1) 
				cThousand 	:= IIF(Getsx3Cache( cCampo,"X3_DECIMAL") != 0, nAux ,Getsx3Cache( cCampo,"X3_TAMANHO")) 
			ELSEIF Getsx3Cache( cCampo,'X3_TIPO' ) == 'M'
				cTipo 		:= 'string'
				cTamanho 	:= 500
			ENDIF

			//Se obtiene el titulo del campo
			cTitulo := oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TITULO)

			//Se obtiene la mascara de un campo
			IF AllTrim(cCampo) == 'EL_CGC'
				DO CASE 
					CASE Substr(Alltrim(GetSX3Cache(cCampo,"X3_PICTURE")),1,2) == '@R'
						cMask := Substr(Alltrim(Replace(GetSX3Cache(cCampo,"X3_PICTURE")," ","")),3)
					OTHERWISE
						cMask := ""
				ENDCASE
			ENDIF
			aJson[nPos]['maskFormatModel' ]     :=  .F.	
			IF AllTrim(cCampo) $ 'EL_HRPAGO|EL_HORA'
				cMask := Alltrim(GetSX3Cache(cCampo,"X3_PICTURE"))
				aJson[nPos]['maskFormatModel' ]      :=  .T.
			ENDIF

			//Si el campo tiene diferentes opciones
			nOptions := len(oStruSEL1:GetProperty(cCampo,MVC_VIEW_COMBOBOX))
			aAux := {}
			IF nOptions > 1
				FOR nJ:=1 to nOptions
					IF Alltrim(oStruSEL1:GetProperty(cCampo,MVC_VIEW_COMBOBOX)[nJ])<>''
						Aadd(aOptions,JsonObject():new())
						aadd( aAux ,separa(oStruSEL1:GetProperty(cCampo,MVC_VIEW_COMBOBOX)[nJ], "=") )
						aOptions[nJ]['value'] :=  Alltrim(aAux[nJ][1])
						aOptions[nJ]['label']  := aAux[nJ][2]
					ENDIF
				NEXT
			ENDIF

			cHelp := GetHlpSoluc(cCampo)[1]
			conout( "Estructura del campo "  + cCampo + "  " +Time())
			//Se construye el json de respuesta con todas las propiedades del campo
			aJson[nPos]['trigger' ]  			:=  ExistTrigger(cCampo) 
			aJson[nPos]["columns"] :={}
			aJson[nPos]['optionsService' ]      :=  ''
			aJson[nPos]['searchService' ]      	:=  ''
			aJson[nPos]['maxLength' ] 			:= 	cTamanho
			aJson[nPos]['decimalsLength' ] 		:= 	oMdlSEL:GetProperty(cCampo,MODEL_FIELD_DECIMAL)
			aJson[nPos]['thousandMaxlength' ] 	:= 	cThousand
			aJson[nPos]['label' ] 				:=	cTitulo
			aJson[nPos]['campo' ] 				:=  cCampo
			aJson[nPos]['property' ] 			:=	lower(Alltrim(substr(cCampo,at("_",cCampo)+1)))
			aJson[nPos]['required' ] 			:=	IIF(AScan(aCposNo,Alltrim(cCampo)) != 0,.F.,X3Obrigat(cCampo))
			aJson[nPos]['gridColumns' ] 		:=  12
			aJson[nPos]['options' ]				:=  aOptions
			aJson[nPos]['forceOptionsComponentType'] := IIF(LEN(aOptions)>0 .AND. LEN(aOptions)<4,"select","")
			aJson[nPos]['value' ] 			  	:= 	''
			aJson[nPos]['mask' ] 				:=  cMask
			aJson[nPos]['format' ] 			 	:= cFormatd
			aJson[nPos]['minValue']				:= ""
			aJson[nPos]['showRequired' ] 		:= IIF(aJson[nPos]['required'],.T.,.F.)
			aJson[nPos]['hasHelp']				:= IIF(!VAZIO(cHelp),STRTRAN(cHelp,Chr(13)+Chr(10)," "),"")//Funcao de apoio para retirar CR/LF/TAB de strings
			aJson[nPos]['infiniteScroll'] 		:= .F.
			aJson[nPos]['optionsMulti']			:= .F.
			aJson[nPos]['advancedFilters'] := {}
			cHelp:= ""
			//Campo exclusivo de Perú
			IF(cCampo=="EL_SERRET")
				IF !VAZIO(SuperGetMv('MV_CRSERIE'))
					aJson[nPos]['value' ] := SuperGetMv('MV_CRSERIE')
				ENDIF
			END

			//nOrden reservados para EL_VALBASE|EL_VALIMP1 y EL_ALIQ|EL_ALQIMP1 para que aparezcan antes de EL_VALOR ya que son campos creados por modelo
			IF (cPaisloc $ "ARG|PAR" .AND. cCampo=="EL_VALOR")
				AADD(aTemp,nOrden++)
				AADD(aTemp,nOrden++)
			ENDIF

			//Informa el orden a exhibir el campo, si es EL_VALIMP1 y EL_ALQIMP1 se colocan antes del campo EL_VALOR
			If cPaisloc $ "PAR"  .AND. cCampo $ 'EL_VALIMP1' .AND. LEN(aTemp) > 0
				aJson[nPos]['order' ] := aTemp[1]
			ELSEIF cPaisloc $ "PAR" .AND. cCampo $ 'EL_ALQIMP1' .AND. LEN(aTemp) > 1
				aJson[nPos]['order' ] := aTemp[2]
			ELSE
				//Informa el orden a exhibir el campo.
				aJson[nPos]['order' ] := nOrden
				nOrden++
			ENDIF

			IF oStruSEL1:GetProperty(cCampo,6) == "COMBO"
				aJson[nPos]['value' ] 	:= alltrim(IIF(VAZIO(GetSX3Cache(cCampo,"X3_RELACAO")),"",InitPad(GetSX3Cache(cCampo,"X3_RELACAO"))))
			ELSE
				aJson[nPos]['type' ] 	:= cTipo
			ENDIF

			IF Alltrim(GetSX3Cache(cCampo,"X3_RELACAO")) <> '' .and. oStruSEL1:GetProperty(cCampo,6) <> "COMBO"
				IF cTipo == 'date'
					aJson[nPos]['value'] := dtos(InitPad(GetSX3Cache(cCampo,"X3_RELACAO")))
					IF cCampo == "EL_DTVCTO"
						dEmissa := oModel:GetValue('FJT_MASTER', "FJT_EMISSA")
						aJson[nPos]['minValue'] := StrZero(Year(dEmissa),4) + "-" + StrZero(Month(dEmissa),2)  + "-" + StrZero(Day(dEmissa),2)
					ENDIF
				ELSE
					IF !(cCampo $ "EL_RECIBO|EL_DIACTB")
						aJson[nPos]['value'] := Alltrim(InitPad(GetSX3Cache(cCampo,"X3_RELACAO")))
					ELSEIF cCampo == "EL_TIPO"
						aJson[nPos]['value'] := ""
					ENDIF
				ENDIF
			ENDIF

			//URL de catalogos
			IF Alltrim(Getsx3Cache( cCampo,'X3_F3' )) <> '' .and. !(cCampo$("EL_BANCO|EL_BCOCHQ|EL_CFO"))
				aJson[nPos]['optionsService' ]  := "/api/v1/totvsRecibos/catalogs/" +Alltrim(Getsx3Cache( cCampo,'X3_F3' ))
			END
			If cCampo $ ("EL_BANCO|EL_BCOCHQ")
				aJson[nPos]['searchService' ]  := ""
				aJson[nPos]['optionsService' ]  := ""
			EndIf

			IF cCampo == "EL_CFO"
				aJson[nPos]['searchService' ]  := ""
				aJson[nPos]['infiniteScroll'] := .T.
				aJson[nPos]['optionsService' ]  := "/api/v1/totvsRecibos/catconpag/" +Alltrim(Getsx3Cache( cCampo,'X3_F3' ))
			EndIf

			//El campo sera desabilitado o habilitado 
			IF Alltrim(Getsx3Cache( cCampo,"X3_VISUAL" ) ) == "V" .OR. cCampo$(cCposNo)
				aJson[nPos]['disabled' ]:= 	.T.
			Else
				aJson[nPos]['disabled' ]:= !oModel:GetModel('SEL_DETAIL'):CansetValue(cCampo)
				IF aJson[nPos]['disabled' ]
					aJson[nPos]['required' ] := .F.
				ENDIF
			EndIF

			//El campo se mostrara en la forma de pago
			IF  AScan(aCposNo,Alltrim(cCampo)) == 0
				aJson[nPos]['visible' ] := .T.
			ELSE
				aJson[nPos]['visible' ] := .F.
				aJson[nPos]['value'] :=""
			ENDIF
		
			IF cCampo == "EL_DIACTB"
				IF ( cSegOfi$("5|8")  ) .Or. (cSegOfi == "7" .And. cPaisloc == "PER" ) 
					aJson[nPos]['disabled' ]:= 	.F.
				ELSE 
					aJson[nPos]['disabled' ]:= 	.T.
				END
			END
		ELSEIF !cCampo $ ("EL_BANCOS|EL_BCOCHQS")
			//Array donde se guardan las propiedades de cada campo
			Aadd(aJson,JsonObject():new())
			nPos := Len(aJson)

			//Se obtiene el tipo y/o tamaño del campo
			IF oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TIPO) == 'C'
				cTipo 		:= 'string'
				cTamanho 	:= oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TAMANHO) 
			ELSEIF oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TIPO) == 'D'
				cTipo 		:= 'date'
			ELSEIF oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TIPO) == 'N' 
				cTipo 		:= 'currency'
				cTamanho 	:= ''
				nAux		:= oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TAMANHO) - (oMdlSEL:GetProperty(cCampo,MODEL_FIELD_DECIMAL)+1)
				cThousand 	:= IIF(oMdlSEL:GetProperty(cCampo,MODEL_FIELD_DECIMAL) != 0,nAux,Getsx3Cache( cCampo,"X3_TAMANHO"))
			ELSEIF oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TIPO) == 'M'
				cTipo 		:= 'string'
				cTamanho 	:= 500
			ENDIF

			//Se obtiene el titulo del campo
			cTitulo := oMdlSEL:GetProperty(cCampo,MODEL_FIELD_TITULO)

		    //Se construye el json de respuesta con todas las propiedades del campo
			aJson[nPos]['trigger' ]  			:=  cCampo$cTrigger
			aJson[nPos]["columns"] :={}
			aJson[nPos]['optionsService' ]		:=  ''
			aJson[nPos]['searchService' ]       :=  ''
			aJson[nPos]['maxLength' ] 		  	:= 	cTamanho
			aJson[nPos]['decimalsLength' ] 	  	:= 	oMdlSEL:GetProperty(cCampo,MODEL_FIELD_DECIMAL)
			aJson[nPos]['thousandMaxlength' ]   := 	cThousand
			aJson[nPos]['label' ] 			  	:=	ALLTRIM(cTitulo)
			aJson[nPos]['campo' ] 			  	:=  cCampo
			aJson[nPos]['property' ] 		 	:=	lower(Alltrim(substr(cCampo,at("_",cCampo)+1)))
			aJson[nPos]['gridColumns' ] 		:= 	12
			aJson[nPos]['options' ]			  	:=  aOptions
			aJson[nPos]['forceOptionsComponentType'] := IIF(LEN(aOptions)>0 .AND. LEN(aOptions)<4,"select","")
			aJson[nPos]['required' ] 			:=	oMdlSEL:GetProperty(cCampo,MODEL_FIELD_OBRIGAT)
			aJson[nPos]['value' ] 			  	:= 	''
			aJson[nPos]['mask' ] 				:= 	''
			aJson[nPos]['type' ] 				:=  cTipo
			aJson[nPos]['visible' ] 			:= .T.
			aJson[nPos]['showRequired' ] 		:= IIF(aJson[nPos]['required'],.T.,.F.)
			aJson[nPos]['minValue']				:= ""
			aJson[nPos]['hasHelp']				:= ""
			aJson[nPos]['optionsMulti']			:= .F.
			aJson[nPos]['maskFormatModel' ]     := .F.

			IF cCampo$(cCposNo)
				aJson[nPos]['disabled'] := .T.
			ELSE 
				aJson[nPos]['disabled' ]:= !oModel:GetModel('SEL_DETAIL'):CansetValue(cCampo)
			ENDIF

			//Informa el orden a exhibir el campo, si es EL_VALBASE y EL_ALIQ se colocan antes del campo EL_VALOR
			If cPaisloc $ "ARG" .AND. cCampo $ 'EL_VALBASE' .AND. LEN(aTemp) > 0
				aJson[nPos]['order' ] := aTemp[1]
			ELSEIF cPaisloc $ "ARG" .AND. cCampo $ 'EL_ALIQ' .AND. LEN(aTemp) > 1
				aJson[nPos]['order' ] := aTemp[2]
			ELSE
				aJson[nPos]['order' ] := nOrden
				nOrden++
			ENDIF

			//Consultas para campos de ARG
			IF cPaisloc == "ARG"
				IF cCampo == 'EL_AGREGAN' 
					aJson[nPos]['optionsService' ]  := "/api/v1/totvsRecibos/catalogs/" +Alltrim(Getsx3Cache( 'A2_AGREGAN','X3_F3' ))
				ELSEIF cCampo == 'EL_CONCSUS' 
					aJson[nPos]['optionsService' ]  := "/api/v1/totvsRecibos/catalogs/" +Alltrim(Getsx3Cache( 'A2_CONCSUS','X3_F3' ))
				ENDIF
			ENDIF
		ENDIF
	NEXT
conout( "getpaymentForm  Finaliza nItens "  + Time())
	oModel:DeActivate()
	oPaymentForm:set(aJson)
	oResponse['result'] := .T.
	oResponse[ 'response' ] := oPaymentForm
conout( "Finaliza getpaymentForm"  + Time())	
Return oResponse

/*/{Protheus.doc} paymentFormService::checkReaderService
Metodo encargado para la lectora de cheques
@type method
@version  1
@author luis.aboytes
@since 22/7/2022
/*/
Method checkReaderService() Class paymentFormService
Local aCheque 	As Array
Local jResponse As Object
Local nPos 		As Numeric
Local oResponse As Object

oResponse := JsonObject():New()
jResponse:=JsonObject():new()
nPos := 0
aCheque := FinCmc7Tc()

	If Len(aCheque) > 0
		//Verifica e cadastra as entidades bancárias se for ao caso
		FINEntBc(aCheque[1],aCheque[2],aCheque[3])

		jResponse['bcochq' ] 	:= aCheque[1]
		jResponse['agechq' ] 	:= aCheque[2]
		jResponse['postal' ] 	:= aCheque[3]
		jResponse['numero' ] 	:= aCheque[4]
		jResponse['ctachq' ] 	:= aCheque[5]
	EndIf

	oResponse['result'] := .T.
	oResponse['response'] := jResponse	

Return oResponse

/*/{Protheus.doc} paymentFormService::serviceGetCGC
Servicio encargado de llamar al data para obtener el CGC del cliente seleccionado en el encabezado de nuevo recibo
@type method
@author luis.aboytes
@since 7/9/2022
/*/
Method serviceGetCGC(jBody) Class paymentFormService
Local jResponse := JsonObject():New()	As Object
Local oResponse := JsonObject():New()   As Object
Local cCGC	:= '' As Character 
Local oPaymentFormData 	:= paymentFormAdapter():new('GET') As Object

	cCGC := oPaymentFormData:dataGetCGC(jBody)

	IIf(!Empty(cCGC),jResponse['cgc']:=cCGC,jResponse['cgc']:="")	

	oResponse['result'] := .T.
	oResponse['response'] := jResponse	

Return oResponse

/*/{Protheus.doc} getpostValidCpo
Metodo que ejecuta la funcion que retorna el campo a editar y su contenido
@type Function
@version  1
@author José González
@since 01/10/2022
/*/
Method getpostValidCpo(oJParams)  Class paymentFormService
Local jResponse := JsonObject():New() 
Local oResponse := JsonObject():New() 
Local oError	:= JsonObject():New()
Local aPayForm	:= {}	As Array
Local cCampo	:= ""
Local i			:= 1
Local oMdlTab  	As Object 
Local aTriger 	:= {}
Local xValue 	:= ""
Local aError 	:= {}
Local aPay 		:= {}
Local aHeader 	:= {}
Local aHelp		:=	{}
Local cHelp		:= ""
Local cSaltLn	:= ""
conout( "Inicia getpostValidCpo "  + Time())
aPayForm := oJParams['formFields']:getnames() //Obtenemos todos los campos de la forma de pago que estan disponibles para el cliente a partir del modelo

SetFunName("FINA998")
oMdlTab  :=  FwLoadModel("FINA887")   
conout( "getpostValidCpo  Termina FwLoadModel "  + Time())
cCampo := "EL_"+UPPER(oJParams['property']) //Campo que ha sido modificado
IF oJParams:HasProperty('value') //Obtenemos el valor de la propiedad modificada
	IF oJParams['value']:HasProperty(oJParams['property'])
		xValue := oJParams['value'][oJParams['property']]
	ENDIF
ENDIF

IF ALLTRIM(cCampo) <> ""
	oMdlTab:SetOperation(MODEL_OPERATION_INSERT)
	oMdlTab:Activate()
	
	//Obtenemos las monedas registradas para guardarlas en el modelo
	FOR i := 1 to LEN(oJParams['coins'])
		IF LEN(oJParams['coins']) > 1 .and. i > 1
			oMdlTab:GetModel('MOE_DETAIL' ):AddLine()
		ENDIF
		oMdlTab:SetValue('MOE_DETAIL',"MOEDA" 		, ALLTRIM(STR(oJParams['coins'][i]['moneda'] )))
		oMdlTab:SetValue('MOE_DETAIL',"TASA" 		, oJParams['coins'][i]['tasa'] )
		oMdlTab:SetValue('MOE_DETAIL',"RECIBIDO"	, oJParams['coins'][i]['received'] )
		oMdlTab:SetValue('MOE_DETAIL',"SALDO" 		, oJParams['coins'][i]['balance'] )
	Next
										
	//Obtenemos el encabezado del recibo para guardarlo en el modelo
conout( "getpostValidCpo  Header del recibo "  + Time())
	aHeader := oJParams['header']:GetNames()
	self:saveHeader(aHeader,@oMdlTab,@oJParams,"header")

    //Obtenemos las propiedades de la forma de pago de oJParams['value'] para guardarlas en el modelo SIN detonar triggers, reglas de dependencia, when y validaciones
conout( "getpostValidCpo  GetNames de la forma de pago " +  Time())  
   aPay := oJParams['value']:GetNames() 
    FOR i := 1 to LEN(aPay)
        IF oMdlTab:GetModel('SEL_DETAIL'):HasField("EL_"+UPPER(aPay[i]))
            IF !VAZIO(oJParams['value'][aPay[i]]) .and. (oJParams["edit"] .or. !(cCampo == "EL_"+UPPER(aPay[i])))
                IF oMdlTab:GetModel('SEL_DETAIL'):GetStruct():GetProperty("EL_"+UPPER(aPay[i]),MODEL_FIELD_TIPO ) == 'D'
                    oMdlTab:LoadValue('SEL_DETAIL', "EL_"+UPPER(aPay[i]),STOD(StrTran(oJParams['value'][aPay[i]],"-","")))
                ELSE
                    oMdlTab:LoadValue('SEL_DETAIL', "EL_"+UPPER(aPay[i]),oJParams['value'][aPay[i]])
                ENDIF
			ENDIF
        ENDIF
    NEXT

	jResponse["value"] := JsonObject():New() //Objeto donde se retornaran los valores de los campos (Actualizados por triggers y/o when)

	If oJParams["edit"]
		cCampo := ""
		xValue := ""
	EndIf
	IF !oJParams["edit"] //Bandera habilitada solo cuando se abre la forma de pago ya sea por opcion nueva forma de pago o edición de recibo
		conout( "getpostValidCpo  GetTriggers " + Time())  
		aTriger := oMdlTab:GetModel('SEL_DETAIL'):GetStruct():GetTriggers() //Obtenemos los triggers
		IF cCampo == "EL_BANCOS" .and. xValue==""
			oMdlTab:LoadValue('SEL_DETAIL',"EL_BANCO"  , "") 
			oMdlTab:LoadValue('SEL_DETAIL',"EL_AGENCIA", "")
			oMdlTab:LoadValue('SEL_DETAIL',"EL_CONTA"  , "") 
		ENDIF 
		IF cCampo == "EL_BCOCHQS" .and. xValue==""
			oMdlTab:LoadValue('SEL_DETAIL',"EL_BCOCHQ"  , "") 
			oMdlTab:LoadValue('SEL_DETAIL',"EL_AGECHQ", "")
			IF oMdlTab:GetModel('SEL_DETAIL'):hasfield("EL_POSTAL")
				oMdlTab:LoadValue('SEL_DETAIL',"EL_POSTAL"  , "")
			ELSE
				oMdlTab:LoadValue('SEL_DETAIL',"EL_CTACHQ"  , "")
			ENDIF
		ENDIF 
		IF oMdlTab:GetModel('SEL_DETAIL'):GetStruct():GetProperty(cCampo,MODEL_FIELD_TIPO ) == 'D'
			xValue := STOD(StrTran(xValue,"-",""))
		ELSEIF oMdlTab:GetModel('SEL_DETAIL'):GetStruct():GetProperty(cCampo,MODEL_FIELD_TIPO ) == "N" .AND. VAZIO(oJParams['value'][oJParams['property']])
			xValue := 0
		ENDIF	

		IF cCampo $ "EL_VALOR|EL_ALIQ|EL_VALBASE|EL_ALQIMP1|EL_VALIMP1"
			LdAlqBseVl(oMdlTab, cCampo, xValue)
		ENDIF
	ENDIF

	jResponse["fields"] := {}
	conout( "getpostValidCpo  setValues " + cCampo+" "+ Time()) 
	self:setValues('SEL_DETAIL',aTriger,cCampo,xValue,oMdlTab,@jResponse,4,aPayForm,"EL_",,oJParams["edit"])

	aError := oMdlTab:GetErrorMessage()
	cHelp:=""

	IF !Empty(aError[6]) 
		cSaltLn := chr(13)+chr(10) //Se evitan retorno de carro y salto de linea de la cadena obtenida
		aHelp := GetHlpSoluc(aError[4]) 
		cHelp += IIF(VAZIO(aHelp[2]),STR0011+" "+REPLACE(aHelp[1],chr(13)+chr(10)," "),STR0011+" "+REPLACE(aHelp[1],chr(13)+chr(10)," ")+ cSaltLn + STR0012+" "+aError[7]) //STR0011 - Problema o Información:  STR0012 - Solución:
		
		REPLACE(aHelp[1], chr(13)+chr(10)," ")
	ENDIF

	oError["type"] 				:= "warning"
	oError["message"] 			:= IIF(!VAZIO(aError[6]),aError[6],"") //Se define el encabezado del error
	oError["detailedMessage"] 	:= IIF(!VAZIO(cHelp),cHelp,"") //Se muestra el problema situacion y solución
		
	jResponse["_messages"] 	:= oError

ENDIF

oMdlTab:DeActivate() 
oResponse['result'] := .T.
oResponse['response'] := jResponse	
conout( "getpostValidCpo  Finaliza getpostValidCpo  " +  Time()) 
Return oResponse

/*/{Protheus.doc} IsRequired
Metodo creado para obtener los campos obligatorios (No se puede por MVC ya que MODEL_FIELD_OBRIGAT no permite un bloque de código)
@type function
@version  1
@author luis.aboytes
@since 28/2/2023
/*/
Function IsRequired( oMdlTab,cCanCampo )
Local lRequired := .F. As Logical

	IF cPaisloc == "ARG"
		IF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RI|RB|RG|RM|RS' .AND. cCanCampo $ "EL_VALBASE|EL_ALIQ"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RB|RM' .AND. cCanCampo $ "EL_PROVIN"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RB|RI|RG|RM' .AND. cCanCampo $ "EL_CFO"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RG' .AND. cCanCampo $ "EL_AGREGAN"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RM' .AND. cCanCampo $ "EL_RET_MUN"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'RS' .AND. cCanCampo $ "EL_SIRECER|EL_SIRESEG|EL_CONCSUS"
			lRequired := .T.
		ELSEIF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPODOC")) $ 'DC' .AND. cCanCampo $ "EL_CGC"
			lRequired := .T.
		ENDIF
	ELSEIF cPaisloc == "PER"
		IF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue('EL_TIPODOC')) $ 'RI' .AND. cCanCampo $ "EL_SERRET|EL_DOCTO"
			lRequired := .T.
		ENDIF
	ELSEIF cPaisloc == "PAR"
		IF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue('EL_TIPODOC')) $ 'RI|RR' .AND. cCanCampo $ "EL_VALIMP1|EL_ALQIMP1|EL_DOCTO"
			lRequired := .T.
		EndIf
	ENDIF 
	
Return lRequired

/*/{Protheus.doc} IsDisabled
Metodo creado para desabilitar campos pero que necesita ser asignado un valor
@type function
@version  1
@author luis.aboytes
@since 1/3/2023
/*/
Function IsDisabled( oMdlTab,cCanCampo,cCampo )
Local lDisabled := .F. As Logical

	IF cPaisloc == "ARG" 
		IF cCampo $ "EL_TIPODOC|EL_TIPO" .AND. cCanCampo == 'EL_PREFIXO' .AND. (ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPODOC")) $ 'DC' .OR. (ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPO")) $ 'PGR' .AND. ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue("EL_TIPODOC")) $ 'DC')) 
			lDisabled := .T.
		ENDIF
	ELSEIF cPaisloc == "PAR" .AND.  cCampo == "EL_TIPODOC"
		IF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue(cCampo)) $ 'RI|RR' .AND. cCanCampo $ "EL_SDOCTO"
			lDisabled := .T.
		EndIf
	ELSEIF cPaisloc == "PER" .AND.  cCampo == "EL_TIPODOC"
		IF ALLTRIM(oMdlTab:GetModel('SEL_DETAIL'):GetValue(cCampo)) $ 'RI' .AND. cCanCampo $ "EL_SDOCTO|EL_PARDOC"
			lDisabled := .T.
		ENDIF  
	ENDIF
	
Return lDisabled

/*/{Protheus.doc} TriggerFormData
Funcion que ejecuta la regla de la tabla SX7
@type Function
@version  1
@author José González
@since 01/10/2022
/*/
Function TriggerFormData(cCampo,cValor)

Local cdestino
local cConten
Local aTrigg :={}	
local aChave :={}

DbSelectArea("SX7")
If MSSeek(cCampo)
While SX7->(!EOF()) .And. Alltrim(SX7->X7_CAMPO) == Alltrim(cCampo)
	

	aChave:= Separa(SX7->X7_CDOMIN,"_")
	cdestino := lower(Alltrim(aChave[2]))
	IF ALLTRIM(SX7->X7_CONDIC) == "LSERVICE" .and. &(SX7->X7_CONDIC)
		
		If SX7->X7_Tipo ==	"P"
		
			cConten := &(SX7->X7_REGRA) 
			Aadd(aTrigg,{cdestino,cConten})
			
		ElseIf SX7->X7_TIPO == "X"

			DbSelectArea(SX7->X7_ALIAS)
			DbSetOrder(SX7->X7_ORDEM)
			DbSeek(&(SX7->X7_CHAVE))
			cConten := &(SX7->X7_REGRA) 
			Aadd(aTrigg,{cdestino,cConten})

		EndIf
	EndIf

	SX7->(DbSkip())
End
EndIF
Return aTrigg

/*/{Protheus.doc} paymentFormService::setValues
Metodo encargado de realizar un setValue a los campos de la forma de pago, el setValue ejecuta Reglas de dependencia, When, disparadores y validaciones
@type method
@version  1
@author luis.aboytes
@since 21/9/2023
/*/
Method setValues(cSubModel, aTriger, cCampo, xValue,oMdlTab,jResponse,nTamanio,aForm,cAlias,cCpoDisable,lEdit,oJParams) Class paymentFormService
Local i As Numeric
Local cDestino  := "" 	As Character
Local cReset 	:= ""	As Character
Local cConten	:= ""	As Character
Local aResp		:= {} 	As Array 
Local jAfields	:= {}
Local nPos,nCont:= 0	As Numeric
Local cCanCampo	:= ""	As Character
Local xValAux
Default cCpoDisable := ""	 			//String que contiene los campos que no deben ser desbloqueado ni modificados en su valor al momento de dar click en "Aplicar filtros" y se cambie un valor
Default lEdit := .F.
Default oJParams	:= JsonObject():New()

	If !lEdit
		//Desarrollo de disparadores 
		IF oMdlTab:SetValue(cSubModel,cCampo, xValue) //Se valida Reglas de dependencia, When y validaciones
			If cSubModel == "FJT_MASTER" .And. cCampo =="FJT_CLIENT"
				oMdlTab:LoadValue(cSubModel,"FJT_LOJA", "")
				oMdlTab:SetValue(cSubModel,"FJT_LOJA", oJParams["value"]["loja"])
			EndIF
			FOR i :=1 to LEN(aTriger)
				IF !(aTriger[i][2] == cCampo) 
					cDestino := lower( subst(aTriger[i][2],nTamanio))
					cConten :=	oMdlTab:GetValue(cSubModel, aTriger[i][2])
					Aadd(aresp,{cDestino,cConten,aTriger[i][2],aTriger[i][1]}) 
				ENDIF
			NEXT
			FOR i:=1 to LEN(aResp) //Asignamos al objeto json los valores que se actualizaron por medio de los disparadores
				IF oMdlTab:GetModel(cSubModel):IsFieldUpdated(aResp[i][3]) .or. (cCampo == aResp[i][4] .and. !Empty(aResp[i][2])) .and. !cCampo$cCpoDisable //Si se actualizo el valor de un campo a raíz de un trigger
					IF VALTYPE(aResp[i][2]) == "D"
						aResp[i][2] := StrZero(Year(aResp[i][2]),4) + "-" + StrZero(Month(aResp[i][2]),2)  + "-" + StrZero(Day(aResp[i][2]),2)
					ELSEIF  VALTYPE(aResp[i][2]) == "N"
						jResponse["value"][ aResp[i][1] ] := aResp[i][2]
					ELSE
						jResponse["value"][ aResp[i][1] ] := ALLTRIM(aResp[i][2]) 
					ENDIF
				ENDIF
			Next 
		ELSE
			cReset := LOWER( SUBST(cCampo,nTamanio))
			IF TYPE(cCampo) == "D"
				jResponse["value"][cReset] := CTOD("")
			ELSEIF  TYPE(cCampo) == "N"
				jResponse["value"][cReset] := 0
			ELSE
				jResponse["value"][cReset] := ""
			ENDIF
			RETURN
		ENDIF
	EndIf

	//Desarrollo de when, reglas de dependencia, validaciones
	FOR nCont := 1 TO len(aForm) 
		cCanCampo := cAlias+UPPER(aForm[nCont]) //Obtenemos cada campo de la forma de pago que este visible para el cliente
		IF !cCanCampo$cCpoDisable .and. !(cCanCampo == cCampo)  .and. oMdlTab:CanSetValue(cSubModel,cCanCampo) //Comparamos cada campo de la forma de pago y verificamos si es diferente del campo al que le inserto un dato el cliente y si puede ser editado el campo (El CanSetValue verifica, reglas de dependencia, when y validaciones)
			Aadd(jAfields,JsonObject():new())
			nPos := Len(jAfields)
			jAfields[nPos]["property"] 		:= LOWER(SUBSTR(cCanCampo,nTamanio)) //Campo a modificar
			jAfields[nPos]["required"] 		:= IsRequired(oMdlTab,cCanCampo) .OR. IIF(X3Obrigat(cCanCampo),.T.,.F.) .OR. oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_OBRIGAT )
			jAfields[nPos]["showRequired"]	:= IIF(jAfields[nPos]["required"]  ,.T.,.F.) //Si es true se mostrara como "Requerido" a vista del cliente
			IF SEL->(ColumnPos(cCanCampo)) > 0 .OR. FJT->(ColumnPos(cCanCampo)) > 0 .OR. Alltrim(Getsx3Cache( cCanCampo,"X3_VISUAL" )) == "V" //Verifica si el campo existe en el diccionario, para checar el campo X3_VISUAL y respetar si el campo es visual o editable
				jAfields[nPos]["disabled"] 		:=  IsDisabled(oMdlTab,cCanCampo,cCampo) .OR. Alltrim(Getsx3Cache( cCanCampo,"X3_VISUAL" )) == "V" .OR. !oMdlTab:GetModel(cSubModel):CansetValue(cCanCampo)//Campos existentes en diccionario
			ELSE
				jAfields[nPos]["disabled"] 		:=  IsDisabled(oMdlTab,cCanCampo,cCampo) //Para campos creados por modelo
			ENDIF

			If !lEdit
				IF oMdlTab:GetModel(cSubModel):IsFieldUpdated(cCanCampo)//Si fue actualizado el valor de la propiedad
					xValAux := oMdlTab:GetModel(cSubModel):GetValue(cCanCampo)//Se obtiene el valor
					IF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "D" //Obtenermos el valor si es tipo date
						xValAux := StrZero(Year(xValAux),4) + "-" + StrZero(Month(xValAux),2)  + "-" + StrZero(Day(xValAux),2) 
					ELSEIF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "N" //Obtenermos el valor si es tipo numerico
						jResponse["value"][aForm[nCont]] := IIF(VAZIO(xValAux),0,xValAux)
					ELSE
						jResponse["value"][aForm[nCont]] := IIF(VAZIO(xValAux),"",RTRIM(xValAux)) //Obtenermos el valor si es tipo caracter o diferente a los anteriores
					ENDIF
				ENDIF	
			EndIf
		ELSEIF !cCanCampo$cCpoDisable .and. !(cCanCampo == cCampo)
			Aadd(jAfields,JsonObject():new()) 
			nPos := Len(jAfields)
			jAfields[nPos]["property"] 		:= LOWER(SUBSTR(cCanCampo,nTamanio)) //Campo a modificar
			jAfields[nPos]["required"] 		:= .F.
			jAfields[nPos]["showRequired"]	:= .F.
			jAfields[nPos]["disabled"] 		:= .T.
			IF !lEdit .And. oMdlTab:GetModel(cSubModel):IsFieldUpdated(cCanCampo)//Si fue actualizado el valor de la propiedad 
				IF GetSX3Cache(cCanCampo,"X3_PROPRI") == "U"  .OR. !oMdlTab:CanSetValue(cSubModel,cCanCampo) //Si es un campo de usuario o no se puede asignar un valor
					IF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "D" //Seteamos un valor de tipo fecha
						xValAux := STOD("") 
					ELSEIF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "N" //seteamos un valor de tipo numerico
						xValAux := 0
					ELSE
						xValAux := "" 
					ENDIF
				ELSE
					xValAux := oMdlTab:GetModel(cSubModel):GetValue(cCanCampo)
				ENDIF
				IF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "D"
					xValAux := IIF(!VAZIO(xValAux),StrZero(Year(xValAux),4) + "-" + StrZero(Month(xValAux),2)  + "-" + StrZero(Day(xValAux),2),xValAux)
					jResponse["value"][aForm[nCont]] := xValAux		//Actualizamos valor de tipo fecha
				ELSEIF oMdlTab:GetModel(cSubModel):GetStruct():GetProperty(cCanCampo,MODEL_FIELD_TIPO ) == "N"
					jResponse["value"][aForm[nCont]] := IIF(VAZIO(xValAux),0,xValAux) //Actualizamos valor de tipo numerico
				ELSE
					jResponse["value"][aForm[nCont]] := IIF(VAZIO(xValAux),"",ALLTRIM(xValAux))
				ENDIF
				oMdlTab:LoadValue(cSubModel, cCanCampo,xValAux)	 //Actualizamos el modelo con el valor actualizado.
			ENDIF
		ENDIF
	NEXT
	//Si se cambia la fecha de emisión se tiene que cambiar la propiedad minValue de la propiedad EL_DTVCTO para que el usuario no pueda digitar una fecha menor en fecha de vencimiento a la fecha de emicion
	IF cCampo == "EL_EMISSAO"
		Aadd(jAfields,JsonObject():new())
		nPos := Len(jAfields)
		jAfields[nPos]["property"] := "dtvcto"
		jAfields[nPos]["required"] := .F.
		jAfields[nPos]["showRequired"]:= .F.
		jAfields[nPos]["disabled"] := .F.
		jAfields[nPos]["minValue"] := oMdlTab:GetModel(cSubModel):GetValue(cCampo)
	ENDIF
	jResponse["fields"] := jAfields
RETURN 


/*/{Protheus.doc} paymentFormService::saveHeader
Realiza el guardado en el objeto FJT_MASTER
@type method
@version  1
@author luis.aboytes
@since 21/9/2023
/*/
Method saveHeader(aHeader,oMdlTab,oJParams,cType,cCampo) Class paymentFormService
	Local i As Numeric
	Default cCampo := ""

	FOR i := 1 to len(aHeader)
		IF !VAZIO(oJParams[cType][aHeader[i]]) .and. oMdlTab:GetModel('FJT_MASTER'):HasField("FJT_"+UPPER(aHeader[i])) .and. cCampo != UPPER(aHeader[i])
			IF oMdlTab:GetModel('FJT_MASTER'):GetStruct():GetProperty("FJT_"+UPPER(aHeader[i]),MODEL_FIELD_TIPO ) == 'D'
				oMdlTab:LoadValue('FJT_MASTER', "FJT_"+UPPER(aHeader[i]),STOD(StrTran(oJParams[cType][aHeader[i]],"-","")))
			ELSE
				oMdlTab:LoadValue('FJT_MASTER', "FJT_"+UPPER(aHeader[i]),oJParams[cType][aHeader[i]])	
			ENDIF
		EndIf
	NEXT
Return

/*{Protheus.doc} paymentFormService::getpostValidHdr
Obtiene el contenido de los campos del encabezado y verifica si tiene triggers, when o reglas de dependencia
@type method
@version 1 
@author luis.aboytes
@since 14/9/2023
*/
Method getpostValidHdr(oJParams) Class paymentFormService
Local jResponse := JsonObject():New() 
Local oResponse := JsonObject():New() 
Local oError	:= JsonObject():New()
Local oParam	:= JsonObject():New()
Local oAnswer	:= JsonObject():New()
Local oCollector:= JsonObject():New()
Local aAux		:= {} 	As Array 
Local oMdlTab  	As Object 	
Local aHeadForm	:= {}	As Array
Local aError 	:= {}
Local xValue	:= ""
Local aHeader 		:= {} As Array
Local aTriger 		:= {} As Array
Local i				:= 0  As Numeric
Local cCampo		:= '' As Character
Local cCpoDisable	:= '' As Character

aHeadForm := oJParams['headerForm']:getnames() //Obtenemos todos los campos de la forma de pago que estan disponibles para el cliente a partir del modelo

SetFunName("FINA998")
oMdlTab  :=  FwLoadModel("FINA887")   

cCampo := "FJT_"+UPPER(oJParams['property']) //Campo que ha sido modificado
IF oJParams:HasProperty('value') 
	IF oJParams['value']:HasProperty(oJParams['property'])
		xValue := oJParams['value'][oJParams['property']] //Obtenemos el valor de la propiedad modificada
	ENDIF
ENDIF

IF oJParams['appliedFilter']
	FOR i=1 To LEN(oJParams['disabledFields']:getnames())
		IF i=1
			cCpoDisable:= "FJT_"+UPPER(oJParams['disabledFields']:getnames()[i])
		ELSE
			cCpoDisable+= "|FJT_"+UPPER(oJParams['disabledFields']:getnames()[i])
		ENDIF
	NEXT
	
ENDIF

IF ALLTRIM(cCampo) <> ""
	oMdlTab:SetOperation(MODEL_OPERATION_INSERT)
	oMdlTab:Activate()

	jResponse["value"] := JsonObject():New() //Objeto donde se retornaran los valores de los campos (Actualizados por triggers y/o when)


	//Obtenemos las monedas registradas para guardarlas en el modelo
	FOR i := 1 to LEN(oJParams['coins'])
		IF LEN(oJParams['coins']) > 1 .and. i > 1
			oMdlTab:GetModel('MOE_DETAIL' ):AddLine()
		ENDIF
		oMdlTab:SetValue('MOE_DETAIL',"MOEDA" 		, ALLTRIM(STR(oJParams['coins'][i]['moneda'] )))
		oMdlTab:SetValue('MOE_DETAIL',"TASA" 		, oJParams['coins'][i]['tasa'] )
		oMdlTab:SetValue('MOE_DETAIL',"RECIBIDO"	, oJParams['coins'][i]['received'] )
		oMdlTab:SetValue('MOE_DETAIL',"SALDO" 		, oJParams['coins'][i]['balance'] )
	Next

	aHeader := oJParams['value']:GetNames()
	self:saveHeader(aHeader,@oMdlTab,@oJParams,'value',SubStr(cCampo,5))

	aTriger := oMdlTab:GetModel('FJT_MASTER'):GetStruct():GetTriggers() //Obtenemos los triggers
	
	IF oMdlTab:GetModel('FJT_MASTER'):GetStruct():GetProperty(cCampo,MODEL_FIELD_TIPO ) == 'D'
		xValue := STOD(StrTran(xValue,"-",""))
	ELSEIF oMdlTab:GetModel('FJT_MASTER'):GetStruct():GetProperty(cCampo,MODEL_FIELD_TIPO ) == "N" .AND. VAZIO(oJParams['value'][oJParams['property']])
		xValue := 0
	ENDIF	

	jResponse["fields"] := {}

	
	IF !VAZIO(aError)
		jResponse["_messages"] 	:= oError

		oMdlTab:DeActivate()
		oResponse['result'] := .T.
		oResponse['response'] := jResponse	

		Return oResponse
	ENDIF
	
	self:setValues("FJT_MASTER", aTriger,cCampo,xValue,@oMdlTab,@jResponse,5,aHeadForm,"FJT_",cCpoDisable,,oJParams)
ENDIF

aError := oMdlTab:GetErrorMessage()

//Codición para evitar que muestre helps QUE NO PERTENECEN A CAMPOS DEL ENCABEZADO DE RECIBO (FJT), evita mostrar helps del grupo de preguntas FIN998
IF !VAZIO(aError) .AND. '.FIN998' $ aError[5]
	aError[6]:=""
ENDIF

self:getError(aError,@oError)
	
jResponse["_messages"] 	:= oError

If cCampo == "FJT_EMISSA" .And. oJParams:HasProperty('hasPaymentItems') .And. oJParams['hasPaymentItems']
	oError["type"] 				:= "warning"	//success(verde) , warning (naranja) , information (gris) y error(rojo)
	//STR0018 - "Aviso"
	//STR0019 - "Verifique que las fecha de emisión y vencimiento de las formas de pago que ya registró, sean correctas de acuerdo a este cambio."
	oError["message"] 			:= STR0018 //Se define el encabezado del error
	oError["detailedMessage"] 	:= STR0019 //Se muestra el problema situacion y solución
EndIf 

oMdlTab:DeActivate()
oResponse['result'] := .T.
oResponse['response'] := jResponse	

Return oResponse

/*{Protheus.doc} paymentFormService::getpaymentView
Monta el grid de formas de pago en ADVPL
@type method
@version 1 
@author Jose.gonzalez
@since 21/02/2024 
*/
Method getpaymentView(cParams) Class paymentFormService
Local oModel  	As Object 
local bCommit := {  |oModel| FIN887FORP(oModel,.T.)}
local bCancel := {  |oModel| FIN887FORP(oModel,.F.)}
Local oResponse := JsonObject():New() 
local oExecView 
private oJParams	:= JsonObject():New()
private oJPaymetFo	:= JsonObject():New()
oJParams:fromJson(cParams)
oExecView := FWViewExec():New()

oModel  :=  FwLoadModel("FOPA887")
oModel:SetCommit(bCommit)
oModel:SetCancel(bCancel)
oExecView:setTitle(STR0015)
oExecView:setSource("FOPA887")
oExecView:setModal(.T.)               
oExecView:setOperation(MODEL_OPERATION_INSERT)
oExecView:setModel(oModel)
oExecView:setButtons({{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.T.,STR0016},{.T.,STR0017},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil},{.F.,Nil}})	  
oExecView:setReduction(60)
oExecView:openView(.T.) 

oResponse['result'] := .T.
oResponse['response'] := oJPaymetFo	
 
Return oResponse

/*/{Protheus.doc} paymentForm::getJson
Metodo que recibe un array bidimensional con dos valores (nombre de objeto json y su respectivo valor) para retornar un json
@type method
@version 1 
@author luis.aboytes
@since 10/11/2023
/*/
Method getJson(aAux As Array,cHelp,oError) class paymentFormService
Local json          As json
Local nX    := 0    As numeric

    json := JsonObject():New()

	For nX := 1 To Len(aAux)
		json[aAux[nX][1]] := IIF(VAZIO(aAux[nX][2]),"",aAux[nX][2])
    Next

Return Json

/*/{Protheus.doc} paymentFormService::getError
Metodo que retorna la estructura en formato json si hay un error
@type method
@version  1
@author luis.aboytes
@since 10/11/2023
/*/
Method getError(aError,oError) class paymentFormService
Local cSaltLn	:= "" As Character
Local cHelp		:= "" As Character
Local aHelp		:= {} As Array

IF !Empty(aError[6]) 
	cSaltLn := chr(13)+chr(10) //Se evitan retorno de carro y salto de linea de la cadena obtenida
	IF !VAZIO(aError[4])
		aHelp := GetHlpSoluc(aError[4]) 
		cHelp += IIF(VAZIO(aHelp[2]),STR0011+" "+REPLACE(aHelp[1],chr(13)+chr(10)," "),STR0011+" "+REPLACE(aHelp[1],chr(13)+chr(10)," ")+ cSaltLn + STR0012+" "+aError[7]) //STR0011 - Problema o Información:  STR0012 - Solución:
		REPLACE(aHelp[1], chr(13)+chr(10)," ")
	ELSE
		cHelp += IIF(!VAZIO(aError[7]),STR0012+" "+aError[7],"") //STR0012 - Solución:
	ENDIF	
ENDIF

oError["type"] 				:= "warning"	//success(verde) , warning (naranja) , information (gris) y error(rojo)
oError["message"] 			:= IIF(!VAZIO(aError[6]),aError[6],"") //Se define el encabezado del error
oError["detailedMessage"] 	:= IIF(!VAZIO(cHelp),cHelp,"") //Se muestra el problema situacion y solución

Return 

/*/{Protheus.doc} paymentFormService::setError
Metodo que a traves de los parametros retorna un array de errores como el metodo GETERRORMESSAGE()
@type method
@version 1 
@author luis.aboytes
@since 10/11/2023
[1] ExpC: Id do submodelo de origem
[2] ExpC: Id do campo de origem
[3] ExpC: Id do submodelo de erro
[4] ExpC: Id do campo de erro
[5] ExpC: Id do erro
[6] ExpC: mensagem do erro
[7] ExpC: mensagem da solução
[8] ExpX: Valor atribuido
[9] ExpX: Valor anterior
/*/
Method setError(IdSubMO,IdCpoO,IdSubME,IdCpoE,IdError,MsjError,MsjSolu,xValS,xValA) class paymentFormService
Local aError := {} AS Array

	AADD( aError, IdSubMO )
	AADD( aError, IdCpoO )
	AADD( aError, IdSubME )
	AADD( aError, IdCpoE )
	AADD( aError, IdError )
	AADD( aError, MsjError )
	AADD( aError, MsjSolu )
	AADD( aError, xValS )
	AADD( aError, xValA )

Return aError


/*/{Protheus.doc} LdAlqBseVl
Metodo que se ejecuta para calcular y actualizar el valor, base y/o alicuota de la forma de pago automáticamente
@type function
@version  1
@author carlos.espinoza
@since 11/12/2023
/*/
Function LdAlqBseVl(oMdlTab, cCampo, xValue)
Local nAliq As Numeric 
Local nBase	As Numeric
Local nValor As Numeric

IF oMdlTab:GetModel('SEL_DETAIL'):HasField("EL_ALIQ") .And. oMdlTab:GetModel('SEL_DETAIL'):HasField("EL_VALBASE")
	nAliq 	:= oMdlTab:GetValue("SEL_DETAIL","EL_ALIQ") 	
	nBase 	:= oMdlTab:GetValue("SEL_DETAIL","EL_VALBASE")	
	nValor 	:= oMdlTab:GetValue("SEL_DETAIL","EL_VALOR")
	IF cCampo $ "EL_VALBASE"
		IF xValue == 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALIQ"  , 0)
		ELSEIF nValor > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALIQ"  , (nValor/xValue)*100)
		ELSEIF nAliq > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , (xValue*nAliq)/100)
		ENDIF
	ENDIF

	IF cCampo $ "EL_ALIQ"
		IF xValue == 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , 0)
		ELSEIF nBase > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , (nBase*xValue)/100)
		ELSEIF nValor > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALBASE"  , (nValor*100)/xValue)
		ENDIF
	ENDIF		

	IF cCampo $ "EL_VALOR"
		IF nBase > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALIQ"  , (xValue/nBase)*100)
		ELSEIF nAliq > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALBASE"  , (xValue*100)/nAliq)
		ENDIF
	ENDIF 
ELSEIF oMdlTab:GetModel('SEL_DETAIL'):HasField("EL_ALQIMP1") .And. oMdlTab:GetModel('SEL_DETAIL'):HasField("EL_VALIMP1") 
	nAliq 	:= oMdlTab:GetValue("SEL_DETAIL","EL_ALQIMP1") 	
	nBase 	:= oMdlTab:GetValue("SEL_DETAIL","EL_VALIMP1")	
	nValor 	:= oMdlTab:GetValue("SEL_DETAIL","EL_VALOR")
	IF cCampo $ "EL_VALIMP1"
		IF xValue == 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALQIMP1"  , 0)
		ELSEIF nValor > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALQIMP1"  , (nValor/xValue)*100)
		ELSEIF nAliq > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , (xValue*nAliq)/100)
		ENDIF
	ENDIF

	IF cCampo $ "EL_ALQIMP1"
		IF xValue == 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , 0)
		ELSEIF nBase > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALOR"  , (nBase*xValue)/100)
		ELSEIF nValor > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALIMP1"  , (nValor*100)/xValue)
		ENDIF
	ENDIF

	IF cCampo $ "EL_VALOR"
		IF nBase > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_ALQIMP1"  , (xValue/nBase)*100)
		ELSEIF nAliq > 0 .And. xValue > 0
			oMdlTab:LoadValue('SEL_DETAIL',"EL_VALIMP1"  , (xValue*100)/nAliq)
		ENDIF
	ENDIF
ENDIF

Return Nil

/*/{Protheus.doc} SetColProps
Metodo que alimenta las propiedades para asignar el nombre de las columnas y sus propiedades de la busqueda y busqueda avanzada
De los campos que tienen activado la busqueda avanzada
@type function
@version  1
@author carlos.espinoza
@since 29/01/2024
/*/
Function SetColProps(aColumns, lAdvFil)

Local nF := 0
Local aResult := {}
Default aColumns := {}
Default lAdvFil := .F.

	
	For nF := 1 To Len(aColumns)
		Aadd(aResult,JsonObject():new())	
		aResult[nF]['property'] := aColumns[nF][1]
		aResult[nF]['label'] := aColumns[nF][2]
		If lAdvFil
			aResult[nF]['optional'] := .T.
			aResult[nF]['gridColumns'] := 6
		EndIf
	Next 
	

Return aResult
