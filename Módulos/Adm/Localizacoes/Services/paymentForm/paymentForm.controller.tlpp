#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace tr.paymentForm
using namespace tr.util 
using namespace tr.paymentForm

/*/{Protheus.doc} paymentFormController
Clase que obtendra campos de la tabla SEL
@type class
@version  1
@author luis.aboytes
@since 6/9/2021
/*/
class paymentFormController
	public method New()

	@POST('/api/v1/totvsRecibos/paymentForm')
	public method paymentFormVisible()
	@GET('/api/v1/totvsRecibos/checkreader')
	public method checkReaderController()
	@POST('/api/v1/totvsRecibos/getCGC')
	public method controllerGetCGC()
	@Post('/api/v1/totvsRecibos/campos/valid')
    public method postValidCpo()
	@Post('/api/v1/totvsRecibos/campos/validHeader')
	Public method postValidHdr()
	@POST('/api/v1/totvsRecibos/paymentView')
	public method paymentFormView()
endclass

/*/{Protheus.doc} paymentFormController::new
Inicializador de clase
@type method
@version  1
@author luis.aboytes
@since 6/9/2021
/*/
Method new() class paymentFormController
return 

/*/{Protheus.doc} paymentFormVisible
EndPoint para obtener los campos de la tabla SEL Usados
@type function
@version  1
@author José González
@since 18/03/2021
/*/
Method paymentFormVisible(cContent,jResponse) class paymentFormController
	Local jResponse := JsonObject():New()
	Local paymentFormService As Object
	Local lAuto     := isblind()

	paymentFormService := paymentFormService():new()  
	jResponse := paymentFormService:getpaymentForm(iif(lAuto,oRest:GetBodyRequest(),cContent))
	If lAuto
		AnswerRest(jResponse)
		RETURN .T.
	Endif
Return Nil

/*/{Protheus.doc} paymentFormController::checkReader
Endpoint para el tratado de las lectoras de cheques
@type method
@version  1
@author luis.aboytes
/*/
Method checkReaderController(jResponse) class paymentFormController
	Local jResponse := JsonObject():New()
	Local paymentFormService As Object

	paymentFormService := paymentFormService():new()
	jResponse := paymentFormService:checkReaderService()
Return Nil

/*/{Protheus.doc} paymentFormController::controllerGetCGC
Endpoint encargado de obtener el CGC del cliente seleccionado en el encabezado de nuevo recibo (SA1->A1_CGC)
@type method
@version  1
@author luis.aboytes
@since 7/9/2022
/*/
Method controllerGetCGC() class paymentFormController
	Local jBody 		As Character
	Local jResponse 	As Object
	Local oPaymentoForm	As Object

	jBody := JsonObject():New()
	jBody:fromJson(oRest:GetBodyRequest())

	oPaymentoForm := paymentFormService():new()
	jResponse := oPaymentoForm:serviceGetCGC(jBody)
	AnswerRest(jResponse)
Return

/*/{Protheus.doc} paymentFormController::checkReader
Endpoint para la verificacion de campos y ejecutar el disparador del campo
@type method
@version  1
@author Jose.Gonzalez
@since 01/10/2022
/*/
Method postValidCpo() class paymentFormController
	Local jResponse := JsonObject():New() 
	Local jBody 	:= JsonObject():New()
	Local paymentFormService As Object

	jBody:FromJson(oRest:GetBodyRequest())
	paymentFormService := paymentFormService():new()
	jResponse := paymentFormService:getpostValidCpo(jBody)
	AnswerRest(jResponse)
Return

/*/{Protheus.doc} paymentFormController::postValidHdr
Endpoint para la verificacion de campos del encabezado
@type method
@version 1 
@author luis.aboytes
@since 14/9/2023
/*/
Method postValidHdr() class paymentFormController
	Local jResponse := JsonObject():New() 
	Local jBody 	:= JsonObject():New()
	Local paymentFormService As Object

	jBody:FromJson(DecodeUTF8(oRest:GetBodyRequest()))
	paymentFormService := paymentFormService():new()
	jResponse := paymentFormService:getpostValidHdr(jBody)
	AnswerRest(jResponse)
Return

/*/{Protheus.doc} paymentFormController::paymentFormView
Endpoint para la cargar la vista de forma de pago en ADVPL
@type method
@version 1 
@author jose.gonzalez	
@since 01/03/2024
/*/
Method paymentFormView(cContent,jResponse) class paymentFormController
	Local jResponse := JsonObject():New()
	Local paymentFormService As Object

	paymentFormService := paymentFormService():new()  
	jResponse := paymentFormService:getpaymentView(cContent) 
Return
