#include 'tlpp-core.th'

namespace tr.coins

/*/{Protheus.doc} coinsService
Class coinsService
@type class
@version  1
@author José González
@since 04/03/2021
/*/
Class coinsService
	Public Method new()
	Public Method getcoins()
	Public Method getLocalCurrency()
EndClass

/*/{Protheus.doc} coinsService::new
Inicializador de la clase
@type method
@version  1
@author luis.aboytes
@since 04/03/2021
/*/
Method new() Class coinsService
Return

/*/{Protheus.doc} coinsService::getcoins
Obtiene el contenido de la tasa de las monedas configuradas en el sistema
@type method
@version  1
@author luis.aboytes
@since 19/02/2021
/*/
Method getcoins (pathParams) Class coinsService
	Local response := JsonObject():New()
	Local aJson := {}
	Local coins := JsonObject():new()
	Local nI := 1
	Local nQtMoedas	:= Moedfin()
	Local cMoneda := ""
	Local cM2Cpo  := ""
    
	For nI := 1 to nQtMoedas 
        Aadd(aJson,JsonObject():new())
        nPos := Len(aJson)
		cMoneda := lower(Alltrim(superGetMV("MV_MOEDA"+Alltrim(STR(nI)))))
		cM2Cpo  := "M2_MOEDA"+Alltrim(STR(nI))
        aJson[nPos]['property' ] := cMoneda
        aJson[nPos]['label' ] := cMoneda
        aJson[nPos]['type' ] := 'currency'
        aJson[nPos]['value' ] := RecMoeda(pathParams['datebase'],nI,.T.)
		aJson[nPos]['fieldValue' ] := cMoneda
		aJson[nPos]['gridColumns' ] := 7
		aJson[nPos]['decimalsLength' ] := TamSx3(cM2Cpo)[2]
		aJson[nPos]['thousandMaxlength' ] :=GetSX3Cache(cM2Cpo,"X3_TAMANHO" ) - ( GetSX3Cache(cM2Cpo,"X3_DECIMAL" ) +1)
		aJson[nPos]['moneda' ] := nI
    Next
                             
	coins:set(aJson)     
	response["result"] := .T.
	response[ "response" ] := coins
	
Return response

/*/{Protheus.doc} coinsService::getLocalCurrency
Obtiene la moneda local en plural del pais para ser mostrada en la ventan principal del aplicativo de totvs recibo
@type method
@version  1
@author luis.aboytes
@since 24/10/2022
/*/
Method getLocalCurrency (pathParams) Class coinsService
	Local response 	:= JsonObject():New()
	Local jResponse	:= JsonObject():New()
	Local cMoeda1	:=	lower(Alltrim(superGetMV("MV_MOEDAP1")))

	jResponse['localCurrency'] := ALLTRIM(cMoeda1)
	jResponse['cents'] := MsDecimais(1)

	response["result"] := .T.
	response[ "response" ] := jResponse
Return response
