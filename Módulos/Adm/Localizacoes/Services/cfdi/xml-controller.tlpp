#include 'tlpp-core.th'
#include 'tlpp-rest.th'
#include 'xmlservicecontroller.ch'

namespace tr.xml
using namespace tr.util

/*/{Protheus.doc} validCDF
Endpoint exclusivo para Mexico que tiene la tarea de realizar las validaciones por recibo o un grupo de recibos para generar su CFDI
@type function
@version  1
@author luis.aboytes
@since 29/4/2021
@imput JsonObject, {
  "items": [
    {
      "values": {
        "receipt": "007   ",
        "serie": "   ",
        "hour": "12:00:00"
      },
      "files": [
        {
          "series": "A",
          "actualduedt": "20210526",
          "unit": "01",
          "fine": 0,
          "issuedt": "20210512",
          "prefix": "A",
          "currency": 1,
          "custname": "CLIENTE GENERICO FIS",
          "branch": "D MG 01",
          "customer": "CI0808",
          "class": "COBRO",
          "discount": 0,
          "type": "NF",
          "write-offne": 386.67,
          "interest": 0,
          "billnumber": "00000000000000000070",
          "installment": "A",
          "billvalue": 386.67,
          "balance": 0,
          "origin": "MATA467N",
          "valueBx": 500,
          "tasaBx": 1,
          "clientBx": "CI0808"
        }
      ]
    }
  ]
}
@return JsonObject, [
    {
        "haserror": false,
        "error": {},
        "receipt": "007   ",
        "success": true,
        "process": "VALIDACION CFDI"
    },
    {
        "hasError": false,
        "values": [
            {
                "message": "Archivo XML Generado",
                "success": true
            },
            {
                "message": "Archivo PDF y Timbrado Generado",
                "success": true
            }
        ],
        "receipt": "007   ",
        "error": [],
        "process": "XML TIMBRADO Y GENERACION PDF"
    }
]
/*/
@POST('/api/v1/totvsRecibos/xml/validCFDI')
Function validCDF()
	Local oXmlService 	
	Local jBody 		   
	Local jResponse 	 
	Local jResponseCFD 
	Local jResponseXML 
	Local jTemp			   
	Local aValues 		  

	jBody 			  := JsonObject():New()
	jResponse 		:= JsonObject():New()
	jResponseCFD 	:= JsonObject():New()
	jResponseXML	:= JsonObject():New()
	jTemp			    := JsonObject():New()
	cBodyContent 	:= jBody:fromJson(oRest:GetBodyRequest())
	aValues 		  :={}

	//Validamos si recibio un Json
	If ( cBodyContent <> nil )
		oRest:setStatusCode ( 400 )
		oRest:setResponse( STR0001 + cBodyContent )//STR0001 "Objeto Json invalido."
	Else
		oXmlService := xmlService():getInstance()
		//Obtenemos las validaciones del CFDI exclusivas para México
		jResponseCFD := oXmlService:validCDFService(jBody)

		//Se manda a imprimir el Json
		AnswerRest(jResponseCFD)
	EndIf
Return

/*/{Protheus.doc} getCopyarch
Funcion creada para copiar archivos de la carpeta system a una carpeta local
@type function
@version  1
@author luis.aboytes
@since 15/11/2023
/*/
Function getCopyarch(jParams,oResponse)
Local oCopyarch as Object
Local oJson := JsonObject():New()

  oJson:FromJson(jParams)
  oCopyarch := xmlService():new()
  oResponse := oCopyarch:CopyarchService(oJson)
Return .T.
