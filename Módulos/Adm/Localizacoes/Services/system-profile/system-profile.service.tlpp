#include 'tlpp-core.th'

namespace tr.systemProfile


/*/{Protheus.doc} systemProfileService
Clase donde se definen los metodos y las reglas de negocio de la tabla system-profile
@type class
@version  1
@author luis.aboytes
@since 15/4/2021
/*/
Class systemProfileService
	Public Method new()
	
	Public Method getParametersFIN998()
	Public Method postFinancialTitlesFIN998()
	Public MEthod getFinancialTitlesFIN998()
EndClass

/*/{Protheus.doc} systemProfileService::new
Metodo donde se inicializan las variables 
@type method
@version  1
@author luis.aboytes
@since 15/4/2021
/*/
Method new() Class systemProfileService
Return 

/*/{Protheus.doc} systemProfileService::getInstance
Metodo donde se genera uns instancia de la clase para poder acceder a los metodos de esta clase
@type method
@version  1
@author luis.aboytes
@since 15/4/2021

Method getInstance() Class systemProfileService
	If(__instance == Nil)
		__instance := systemProfileService():new()
	EndIf
Return __instance
/*/
/*/{Protheus.doc} systemProfileService::getParametersFIN998
Metodo donde se obtienen los parametros de la FINA87A
@type method
@version  1
@author luis.aboytes
@since 15/4/2021
@return JsonObject, {
    				"FIN998": [
        						{
            						"number": "mv_par01",
            						"parameter": "¿Muestra  Asientos ?",
            						"value": 2
        						},
        						{
            						"number": "mv_par02",
            						"parameter": "¿Agrupa Asientos ?",
            						"value": 2
        						},
/*/
Method getParametersFIN998() Class systemProfileService
	Local oResponse As Object
	Local oSystemProfileAdapter As Object

	oSystemProfileAdapter := systemProfileAdapter():new()
	oResponse := JsonObject():new()
	oResponse["result"] := .T.
	oResponse['response']:= oSystemProfileAdapter:getParameters('FIN998','FIN998')

Return oResponse

/*/{Protheus.doc} systemProfileService::postFinancialTitlesFIN998(jBody As JsonObject) As JsonObject
Metodo encargado de crear o modificar un registro en la tabla de system-profile
@type method
@version  1
@author luis.aboytes
@since 16/4/2021
@return JsonObject, [
						{
						    "parameter": "",
						    "success": true
						},
						{
						    "parameter": "",
						    "success": true
						}
					]
/*/
Method postFinancialTitlesFIN998(jBody) Class systemProfileService
	Local oResponse As Object
	Local oSystemProfileAdapter As Object
	Local nValue As Numeric
	Local cNumerParam As Character
	Local nCont As Numeric
	Local cType As Character
	Local aValues As Array
	Local nPos As Numeric
	Local jTemp  := JsonObject():New()

	aValues := {}
	oSystemProfileAdapter := systemProfileAdapter():new()
	oResponse := JsonObject():new()

	For nCont := 1 to LEN(jBody)
		
		cNumerParam := jBody[nCont]['number']
		nValue := jBody[nCont]['value']
		cType := jBody[nCont]['type']
		IF !UPPER(cNumerParam) $ "MV_PAR06|MV_PAR07"
			oSystemProfileAdapter:addProfileSystem(RetCodUsr(),"FIN998",cNumerParam,cType,{nValue})
		ENDIF
		AADD(aValues, JsonObject():new())
		nPos := LEN(aValues)
		aValues[nPos]["parameter"] := cNumerParam
		aValues[nPos]["success"] := .T. 
	
	Next
	jTemp:set(aValues)
	oResponse["result"] := .T.
	oResponse["response"] := jTemp
Return oResponse

/*/{Protheus.doc} systemProfileService::getFinancialTitlesFIN998(jBody As JsonObject) As JsonObject
Metodo que obtendra las configuraciones de los parametros actuales registradas en profile-system de la FINA87A
@type method
@version  1
@author luis.aboytes
@since 16/4/2021
@return JsonObject, [
  {
    "number": "",
    "type": ""
  }
]
/*/
Method getFinancialTitlesFIN998(jBody )Class systemProfileService
	Local oResponse As Object
	Local oSystemProfileAdapter As Object
	Local cNumerParam As Character
	Local nCont As Numeric
	Local cType As Character
	Local aValues As Array
	Local aRetValues As Array
	Local jTemp := JsonObject():new()

	aValues := {}
	oSystemProfileAdapter := systemProfileAdapter():new()
	oResponse := JsonObject():new()

	For nCont := 1 to LEN(jBody)
		cNumerParam := jBody[nCont]['number']
		cType := jBody[nCont]['type']
		
		IF !UPPER(cNumerParam) $ "MV_PAR06|MV_PAR07"
			aRetValues := oSystemProfileAdapter:getProfileSystem(RetCodUsr(),"FIN998",cNumerParam,cType)
		ENDIF

		AADD(aValues, JsonObject():new())
		nPos := LEN(aValues)
		If UPPER(aRetValues[1][2]) == "MV_PAR04" .OR. UPPER(aRetValues[1][2]) == "MV_PAR05"
			aValues[nPos]["value"] :=""
		else
			aValues[nPos]["value"] := aRetValues[1][1]
		EndIf
		aValues[nPos]["number"] := aRetValues[1][2]
		aValues[nPos]["success"] := aRetValues[1][3]
	Next
	jTemp:set(aValues)
	oResponse["result"] := .T.
	oResponse["response"] := jTemp
Return oResponse
