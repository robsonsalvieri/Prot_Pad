#include 'tlpp-core.th'

namespace tr.Clients
//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clientes
Class ClientsData

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Class ClientsAdapter From FWAdapterBaseV2
	Data cTableNick As Character

	Public Method new()
	Public Method Clients1Adapter()
	Public Method Clients2Adapter()
	Public Method ClientsTotalsAdapter()
EndClass

Method new(cResMethod As Character) Class ClientsAdapter
	_Super:new(cResMethod)
Return


Static Function AddMapFields(oSelf As Object, cfield As Character)
	Local cFieldNick := cfield
	
	oSelf:AddMapFields("code"	,cFieldNick+'_COD'	,.T.,.T.,{cFieldNick + '_COD'	, 'C',GetSx3Cache("A1_COD"	,"X3_TAMANHO"),0})
Return 

Static Function getQuery(cTableNick As Character) As Character
	Local cQuery As Character
	cQuery := " SELECT #QueryFields# "
	cQuery += " FROM " + RetSqlName(cTableNick)
	cQuery += " WHERE #QueryWhere#"
	cQuery += " GROUP BY #QueryFields# "
Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clientes

	Prepara y retorna el detalle de los Clientes Paginados

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Method Clients1Adapter( cTableNick As Character, cfil As Character, cFilSelec As Character, lCanFil As Logical , jQryParam As Json, lClient As Logical) Class ClientsAdapter
	Local cQueryFields As Character
	Local cQueryWhere As Character
	Local aActivedArea As Array
	Local afilter := IF(lClient,separa(cfil,"|"),{})
	Default cTableNick 	:= ""
	Default cfil		:= ""
	Default cFilSelec	:= ""
	Default lCanFil		:= .F.
	Default jQryParam	:= Nil
	Default lClient		:= .F.

	//Asignamos el nombre de la tabla a utilizar
	::cTableNick := cTableNick

	aActivedArea   := FwGetArea()

	//Agregamos los campos Json/ResultSet
	AddMapFields(self,"A1")

	//Informamos el query a utilizar en la API
	::SetQuery(getQuery(cTableNick))

	cQueryWhere := " A1_MSBLQL <> '1' AND D_E_L_E_T_ = ' ' "
	cQueryFields := "A1_COD"
	
	If !Empty(cFilSelec) .And. lCanFil
		cQueryWhere += "AND A1_FILIAL = '" + xFilial("SA1",cFilSelec) + "'"
	ElseIf !lCanFil
		cQueryWhere += "AND A1_FILIAL = '" + xFilial("SA1") + "'"
	EndIf
	
	if  cfil != '' .And. len(afilter) > 1
		cQueryWhere += " AND  A1_COD = '"+ afilter[1]+ "' AND ("
		cQueryWhere += " A1_LOJA	= '"+	afilter[2]+ "' OR A1_NOME = '"+ afilter[2]+"')"
	ElseIf cfil != ''
		cQueryWhere += " AND ( A1_COD LIKE '%"	+cfil+	"%'  "
		cQueryWhere += " OR A1_NOME LIKE '%"	+cfil+	"%'  )"
	EndIf

	IF jQryParam != Nil 
		If !Empty(jQryParam['code']) 
			cQueryWhere += " AND  A1_COD LIKE '%"+ALLTRIM(jQryParam['code'])+"%'  "
		EndIf

		If !Empty(jQryParam['branch']) 
			cQueryWhere += " AND  A1_LOJA LIKE '%"+ALLTRIM(jQryParam['branch'])+"%'  "
		EndIf

		If !Empty(jQryParam['name']) 
			cQueryWhere += " AND  A1_NOME LIKE '%"+ALLTRIM(jQryParam['name'])+"%'  "
		EndIf
	ENDIF

	//Configuramos los campos para el query
	::SetWhere(cQueryWhere)
	::SetFields(cQueryFields)

	//Informamos el ordenamiento a ser utilizado en la Query
	::SetOrder("A1_COD")

	::setUseSpaces(.T.)

	//Ejecuta la consulta, retorna .T. si todo ocurre conforme a lo esperado
	If ::Execute()
		//Genera un archivo Json
		::FillGetResponse()
	EndIf

	FwrestArea(aActivedArea)
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clientes

	Prepara y retorna el detalle de los Clientes

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Method Clients2Adapter( cFil ) Class ClientsAdapter 

	Local cQuery As Character
	Local oClients As Object
	Local aClientes := {}
	Local aClient 	:= {}
	Local cAlias  	:= ""
	Local nX 		:= 0
	Local oExec   	:= Nil

	Default cFil := ""

	cQuery := " SELECT DISTINCT "
	cQuery += " A1_COD "
	cQuery += " FROM " + RetSqlName("SA1")
	cQuery += " WHERE A1_MSBLQL <> ? AND D_E_L_E_T_ = ? "
	If !Empty(cFil)
		cQuery += " AND A1_FILIAL = ? "
	EndIf
	cQuery := ChangeQuery(cQuery)
	oExec := FwExecStatement():New(cQuery)
	oExec:SetString(1,"1")
	oExec:SetString(2," ")
	If !Empty(cFil)
		cFil := xFilial("SA1",cFil)
		oExec:SetString(3,cFil)
	Else
		cFil := xFilial("SA1")
	EndIf

	cAlias := oExec:OpenAlias()
	
	IF (cAlias)->(!EOF())
		WHILE (cAlias)->(!EOF())
			AAdd(aClient,(cAlias)->A1_COD)
			(cAlias)->(DbSkip())
		END
	EndIf

	(cAlias)->(DbCloseArea())

	oExec:Destroy()
	oExec := Nil 
	
	dbSelectArea("SA1")
	dbSetOrder(1)//A1_FILIAL+A1_COD+A1_LOJA
	For nX:= 1 to Len(aClient)
		IF SA1->(MsSeek(cFil + aClient[nX]))     // Sucursal: 01 /
			oClientes := JsonObject():New()
			oClientes['code'] 		:= aClient[nX]
			oClientes['branch']		:= SA1->A1_LOJA
			oClientes['name'] 		:= Alltrim(SA1->A1_NOME)	
			AAdd(aClientes,oClientes)
		EndIf	
	Next
	SA1->(DbCloseArea())

	oClients	:= JsonObject():New()
	oClients['items']	:= aClientes

Return oClients

//-------------------------------------------------------------------
/*/{Protheus.doc} totvsRecibos/Clientes

	Retorna el total de los Clientes

@author José González
@since 30/06/2021
/*/
//-------------------------------------------------------------------
Method ClientsTotalsAdapter(lSelectFil As Logical) Class ClientsAdapter

	Local cQuery As Character
	Local oClients As Object

	Local cAlias

	cAlias := GetNextAlias()
	
	cQuery := " SELECT  COUNT(A1_COD)  AS TOTALES"
	cQuery += " FROM " + RetSqlName("SA1")
	cQuery += " Where D_E_L_E_T_ = ' ' AND A1_MSBLQL = '2' "
	cQuery += " AND A1_FILIAL = '" + xFilial("SA1") + "' "                     
	cQuery := ChangeQuery(cQuery)
	MPSysOpenQuery(cQuery, cAlias)
 	oClientes := JsonObject():New()

	IF (cAlias)->(!EOF())
		If (cAlias)->TOTALES <= 200
			oClientes['combo'] 		:= .T.
		Else 
			oClientes['combo'] 		:= .F.
		EndIF
	EndIf

	(cAlias)->(DbCloseArea())
	
Return oClientes
