#INCLUDE "CTBR510.CH"
#INCLUDE "PROTHEUS.CH"
 
/*/


Ŀ
Funo	  Ctbr510A	 Autor  Wagner Mobile Costa	  Data  15.10.01 
Ĵ
Descrio  Demonstracao de Resultados                 			  	   
Ĵ
Retorno	  Nenhum       											   
Ĵ
Parametros Nenhum													   
ٱ


/*/
Function CtbR510A()          

Private dFinalA
Private dFinal  
Private dPeriodo0
Private aSelFil	 	:= {}
Private aFieldSM0 := {"M0_NOME", "M0_FILIAL", "M0_CGC"}
Private aDatosEmp := IIf (cVersao <> "11" ,FWSM0Util():GetSM0Data(cEmpAnt, cFilAnt, aFieldSM0),"")
Private cRUC	  := Trim(IIf (cVersao <> "11" ,aDatosEmp[3][2],SM0->M0_CGC))

If TRepInUse() 
	CTBR510R4A()
EndIf

CTBGerClean()

Return

/*/


Ŀ
Funo	  CTBR510R4  Autor Daniel Sakavicius		 Data  17/08/06 
Ĵ
Descrio  Demostrativo de balancos patrimoniais - R4		          
Ĵ
Sintaxe	  CTBR115R4												  
Ĵ
 Uso		  SIGACTB                                    				  
ٱ


/*/
Function CTBR510R4A()                           

PRIVATE cPerg	   	:= "CTR510"        

//Ŀ
//Interface de impressao                                                  
//            

Pergunte( cPerg, .T. )

// faz a validao do livro
if ! VdSetOfBook( mv_par02 , .T. )
   return .F.
endif

oReport := ReportDef()      

If VALTYPE( oReport ) == "O"
	oReport :PrintDialog()      
EndIf

oReport := nil

Return                                

/*/


Ŀ
Programa  ReportDef  Autor  Daniel Sakavicius		 Data  17/08/06 
Ĵ
Descrio Esta funcao tem como objetivo definir as secoes, celulas,   
          totalizadores do relatorio que poderao ser configurados     
          pelo relatorio.                                             
Ĵ
 Uso		  SIGACTB                                    				  
ٱ

    
/*/          
Static Function ReportDef()     

Local aSetOfBook	:= CTBSetOf(mv_par02)
Local aCtbMoeda		:= {}
Local cDescMoeda 	:= ""
Local CREPORT		:= "CTBR510A"
Local CTITULO		:= OemToAnsi(STR0001)				// DEMONSTRACAO DE RESULTADOS
Local CDESC			:= OemToAnsi(STR0014) + ; 			//"Este programa ir imprimir a Demonstrao de Resultados, "
	   					OemToAnsi(STR0015) 				//"de acordo com os parmetros informados pelo usurio."
Local aTamDesc		:= TAMSX3("CTS_DESCCG")
Local aTamVal		:= TAMSX3("CT2_VALOR")                       
                 
aCtbMoeda := CtbMoeda(mv_par03, aSetOfBook[9])
cDescMoeda 	:= AllTrim(aCtbMoeda[3])

If Empty(aCtbMoeda[1])                       
	Help(" ",1,"NOMOEDA")
    Return .F.
Endif

//Filtra Filiais
If mv_par19 == 1 .And. Len( aSelFil ) <= 0
	aSelFil := AdmGetFil()
EndIf 

//Ŀ
// Verifica se usa Set Of Books + Plano Gerencial (Se usar Plano
// Gerencial -> montagem especifica para impressao)				  
//
If !ct040Valid(mv_par02)
	Return
EndIf	
             
lMovPeriodo	:= (mv_par13 == 1)

If mv_par09 == 1												/// SE DEVE CONSIDERAR TODO O CALENDARIO
	CTG->(DbSeek(xFilial() + mv_par01))
	
	If Empty(mv_par08)
		While CTG->CTG_FILIAL = xFilial("CTG") .And. CTG->CTG_CALEND = mv_par01
			dFinal	:= CTG->CTG_DTFIM
			CTG->(DbSkip())
		EndDo
	Else
		dFinal	:= mv_par08
	EndIf
	
	//Data do periodo anterior
	If !Empty(MV_PAR20)
		If CTG->(DbSeek(xFilial() + mv_par01))
			dFinalA		:= MV_PAR20
		EndIf         
	Else	
		dFinalA   	:= Ctod(Left(Dtoc(dFinal), 6) + Str(Year(dFinal) - 1, 4))
		If Empty ( dFinalA )
			If MONTH(dFinal) == 2
				If Day(dFinal) > 28 .and. Day(dFinal) == 29
					dFinalA := Ctod(Left( STRTRAN ( Dtoc(dFinal) , "29" , "28" ), 6) + Str(Year(dFinal) - 1, 4))
				EndIf
			EndIf
		EndIf	
	EndIf
	
	mv_par01    := dFinal
	If lMovPeriodo
		dPeriodo0 	:= Ctod(Left(Dtoc(dFinal), 6) + Str(Year(dFinal) - 2, 4)) + 1
	EndIf
Else															/// SE DEVE CONSIDERAR O PERIODO CONTABIL
	If Empty(mv_par08)
		MsgInfo(STR0008,STR0009)//" necessrio informar a data de referncia !"#"Parametro Considera igual a Periodo."
		Return
	Endif
    
	dFinal		:= mv_par08
	dFinalA		:= CTOD("  /  /  ")
	dbSelectArea("CTG")
	dbSetOrder(1)

	//Data do periodo anterior
	If !Empty(MV_PAR20)
		If MsSeek(xFilial("CTG")+mv_par01)
			dFinalA		:= MV_PAR20
		EndIf         
	Else	
		MsSeek(xFilial("CTG")+mv_par01,.T.)
		While CTG->CTG_FILIAL == xFilial("CTG") .And. CTG->CTG_CALEND == mv_par01	
			If dFinal >= CTG->CTG_DTINI .and. dFinal <= CTG->CTG_DTFIM
				dFinalA		:= CTG->CTG_DTINI	
				If lMovPeriodo
					nMes			:= Month(dFinalA)
					nAno			:= Year(dFinalA)
					dPeriodo0	:= CtoD(	StrZero(Day(dFinalA),2)							+ "/" +;
												StrZero( If(nMes==1,12		,nMes-1	),2 )	+ "/" +;
												StrZero( If(nMes==1,nAno-1,nAno		),4 ) )
					dFinalA		:= dFinalA - 1
				EndIf
				Exit
			Endif
			CTG->(DbSkip())
		EndDo
	EndIf
    
	If Empty(dFinalA)
		MsgInfo(STR0010,STR0011)//"Data fora do calendrio !"#"Data de referncia."
		Return
	Endif
Endif

CTITULO		:= If(! Empty(aSetOfBook[10]), aSetOfBook[10], CTITULO)		// Titulo definido SetOfBook
If Valtype(mv_par16)=="N" .And. (mv_par16 == 1)
	CTITULO := CTBNomeVis( aSetOfBook[5] )
EndIf
//Ŀ
//Criacao do componente de impressao                                      
//                                                                        
//TReport():New                                                           
//ExpC1 : Nome do relatorio                                               
//ExpC2 : Titulo                                                          
//ExpC3 : Pergunte                                                        
//ExpB4 : Bloco de codigo que sera executado na confirmacao da impressao  
//ExpC5 : Descricao                                                       
//
oReport	:= TReport():New( CREPORT,CTITULO,cPerg, { |oReport| ReportPrint( oReport ) }, CDESC ) 
oReport:SetCustomText( {|| CtCGCCabTR(,,,,,dDataBase,CTITULO,,,,,oReport,,,,,,,,,,mv_par08) } )                                        
oReport:ParamReadOnly()

IF GETNEWPAR("MV_CTBPOFF",.T.)
	oReport:SetEdit(.F.)
ENDIF	

oReport:nFontBody := 6
//Ŀ
//Criacao da secao utilizada pelo relatorio                               
//                                                                        
//TRSection():New                                                         
//ExpO1 : Objeto TReport que a secao pertence                             
//ExpC2 : Descricao da seao                                              
//ExpA3 : Array com as tabelas utilizadas pela secao. A primeira tabela   
//        sera considerada como principal para a seo.                   
//ExpA4 : Array com as Ordens do relatrio                                
//ExpL5 : Carrega campos do SX3 como celulas                              
//        Default : False                                                 
//ExpL6 : Carrega ordens do Sindex                                        
//        Default : False                                                 
//                                                                        
//           

oSection1  := TRSection():New( oReport, STR0012, {"cArqTmp"},, .F., .F. )        //"Contas/Saldos"

TRCell():New( oSection1, "ATIVO"	,"",STR0013+cDescMoeda+")"	/*Titulo*/,/*Picture*/,aTamDesc[1]+50	/*Tamanho*/,/*lPixel*/,/*CodeBlock*//*,,,,,,.T.*/)	//"(Em "
TRCell():New( oSection1, "SALDOATU"	,"",						/*Titulo*/,/*Picture*/,aTamVal[1]+25	/*Tamanho*/,/*lPixel*/,/*CodeBlock*/,"RIGHT",,"RIGHT"/*,,,.T.*/)
TRCell():New( oSection1, "SALDOANT"	,"",						/*Titulo*/,/*Picture*/,aTamVal[1]+25   /*Tamanho*/,/*lPixel*/,/*CodeBlock*/,"RIGHT",,"RIGHT"/*,,,.T.*/)

oSection1:SetTotalInLine(.F.) 

Return(oReport)

/*/


Ŀ
Programa  ReportPrint Autor  Daniel Sakavicius	 Data  17/08/06 
Ĵ
Descrio Imprime o relatorio definido pelo usuario de acordo com as  
          secoes/celulas criadas na funcao ReportDef definida acima.  
          Nesta funcao deve ser criada a query das secoes se SQL ou   
          definido o relacionamento e filtros das tabelas em CodeBase.
Ĵ
Sintaxe    ReportPrint(oReport)                                       
Ĵ
Retorno   EXPO1: Objeto do relatrio                                  
ٱ


*/
Static Function ReportPrint( oReport )  

Local oSection1 	:= oReport:Section(1) 
Local aSetOfBook	:= CTBSetOf(mv_par02)
Local aCtbMoeda		:= {}
Local cArqTmp		:= ""
Local cTpValor		:= GetMV("MV_TPVALOR")
Local cPicture		:= ""
Local cDescMoeda	:= ""
Local lImpTrmAux	:= Iif(mv_par10 == 1,.T.,.F.)
Local cArqTrm		:= ""
Local lVlrZerado	:= Iif(mv_par12==1,.T.,.F.)
Local aTamVal		:= TAMSX3("CT2_VALOR")
Local cMoedaDesc	:= iif( empty( mv_par14 ) , mv_par03 , mv_par14 )
Local lPeriodoAnt 	:= (mv_par06 == 1)
Local cSaldos     	:= CT510TRTSL() 
Local lAutomato     := FWIsInCallStack("UTSTARTRPT") .And. cPerg == "CTR510"
Local aColunas		:= { }

Private lMovPeriodo := .F.
aCtbMoeda := CtbMoeda(mv_par03, aSetOfBook[9])
If Empty(aCtbMoeda[1])                       
	Help(" ",1,"NOMOEDA")
    Return .F.
Endif

cDescMoeda 	:= AllTrim(aCtbMoeda[3])
nDecimais 	:= DecimalCTB(aSetOfBook,mv_par03)
cPicture 	:= aSetOfBook[4]

If ! Empty(cPicture) .And. Len(Trans(0, cPicture)) > 17
	cPicture := ""
Endif

lMovPeriodo	:= (mv_par13 == 1)

//Ŀ
// Monta Arquivo Temporario para Impressao					     
//
MsgMeter({|	oMeter, oText, oDlg, lEnd | ;
			CTGerPlan(	oMeter, oText, oDlg, @lEnd, @cArqTmp, dFinalA+1, dFinal;
					  , "", "", "", Repl( "Z", Len( CT1->CT1_CONTA )), ""; 
					  , Repl( "Z", Len(CTT->CTT_CUSTO)), "", Repl("Z", Len(CTD->CTD_ITEM));
					  , "", Repl("Z", Len(CTH->CTH_CLVL)), mv_par03, /*MV_PAR15*/cSaldos, aSetOfBook, Space(2);
					  , Space(20), Repl("Z", 20), Space(30),,,,, mv_par04=1, mv_par05;
					  , ,lVlrZerado,,,,,,,,,,,,,,,,,,,,,,,,,cMoedaDesc,lMovPeriodo,aSelFil,,.T.,MV_PAR17==1,,,,,,,,,,!Empty(MV_PAR20),dFinalA)};
			,STR0006, STR0001) //"Criando Arquivo Temporario..."

dbSelectArea("cArqTmp")           
dbGoTop()

oReport:SetPageNumber(mv_par07) //mv_par07 - Pagina Inicial

oSection1:Cell("ATIVO"   ):lHeaderSize := .F.
oSection1:Cell("SALDOANT"):lHeaderSize := .F.
oSection1:Cell("SALDOATU"):lHeaderSize := .F.
 		
oSection1:Init()
While ! Eof()

	//Ŀ
	//indica se a entidade gerencial sera impressa/visualizada em 
	//um relatorio ou consulta apos o processamento da visao      
	//
	If cArqTmp->VISENT == "2"
		cArqTmp->( DbSkip() )
		Loop
	EndIf

    //Imprime cabealho saldo atual e anterior
	oSection1:Cell("SALDOATU"     ):SetTitle(Dtoc(dFinal)) 
	If lPeriodoAnt
		oSection1:Cell("SALDOANT" ):SetTitle(Dtoc(dFinalA))
	Else
		oSection1:Cell("SALDOANT" ):Disable()
	EndIf

	oSection1:Cell("ATIVO"):SetBlock( { || Iif(cArqTmp->COLUNA<2,Iif(cArqTmp->TIPOCONTA=="2",cArqTmp->DESCCTA,cArqTmp->DESCCTA),AllTrim(cArqTmp->DESCCTA)+AllTrim(Posicione("CTS",1,xFilial("CTS")+aSetOfBook[5]+cArqTmp->ORDEM,"CTS_DETHCG")))} )		

  	//Imprime Saldo para as contas diferentes de Linha sem Valor
  	If cArqTmp->IDENTIFI < "5"
		oSection1:Cell("SALDOATU"     ):SetBlock( { || ValorCTB( If(lMovPeriodo,cArqTmp->(SALDOATU-SALDOANT),cArqTmp->SALDOATU),,,aTamVal[1],nDecimais,.T.,cPicture,;
    	                                                 cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,,.F. ) } )
		If lPeriodoAnt
			oSection1:Cell("SALDOANT" ):SetBlock( { || ValorCTB( If(lMovPeriodo,cArqTmp->MOVPERANT,cArqTmp->SALDOANT),,,aTamVal[1],nDecimais,.T.,cPicture,;
														 cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,,.F. ) } )
		EndIf
	//Somente para Linha Sem Valor
	ElseIf cArqTmp->IDENTIFI == "5"
		oSection1:Cell("SALDOATU"     ):SetBlock( { || " " } )

		If lPeriodoAnt
			oSection1:Cell("SALDOANT" ):SetBlock( { || " " } )
		EndIf	
	EndIf    
	
	Aadd(aColunas, { cArqTmp->CONTA,cArqTmp->IDENTIFI })	
	oSection1:PrintLine()
	dbSkip()
EndDo             
     
If MV_PAR21 == 1 
	If MV_PAR06 == 2 .and. MV_PAR13 == 1
		If MSGYESNO(STR0016) // "Generar archivo TXT?"
			Processa({|| GerArq(AllTrim(MV_PAR22),aColunas,aTamVal,nDecimais,cTpValor)},,STR0021) // "Generando archivo ..."
		EndIf	
	Else
		If MSGYESNO(STR0016)  //"Generar archivo TXT?"
			Alert(STR0018)   //"Para impresin del archivo TXT, las preguntas deben tener las siguientes configuraciones: Dem. Periodo Anterior = No; Considerar? = Mov. Perodo"
		EndIf	
	EndIf	
EndIf				  
oSection1:Finish()

If !lAutomato

	If lImpTrmAux
		cArqTrm 	:= mv_par11

		If !File(cArqTrm)
			aSavSet:=__SetSets()
			cArqTrm := CFGX024(cArqTrm,STR0007) // "Responsveis..."
			__SetSets(aSavSet)
			Set(24,Set(24),.t.)
		Endif
	
		If cArqTrm#NIL
			ImpTerm2(cArqTrm,,,,,oReport)
		Endif	 
	
	Endif
EndIf
DbSelectArea("cArqTmp")
Set Filter To
dbCloseArea()
 
If Select("cArqTmp") == 0
	FErase(cArqTmp+GetDBExtension())
	FErase(cArqTmp+OrdBagExt())
EndIF	

Return


/*/


Ŀ
Funo	  fTrataSlds Autor Elton da Cunha Santana        13.10.09 
Ĵ
Descrio  Tratamento do retorno do parametro                         
Ĵ
Sintaxe	  CT510TRTSL                                                 
Ĵ
 Uso		  SIGACTB                                                    
ٱ


/*/
Static Function CT510TRTSL()

Local cRet := ""

If MV_PAR17 == 1
	cRet := MV_PAR18
Else
	cRet := MV_PAR15
EndIf

Return(cRet)


/*/


Ŀ
 Funcao      GerArq    Autor  V Flores             Data  15.04.2020 
Ĵ
 Descricao   Gera o arquivo magntico                                   
Ĵ
 Parametros  cDir      - Diretorio donde se creara el archivo.          
             aColunas  - Array con los Cdigos del Edo Financiero.      
             aTamVal   - Array con el Tamao del Valor.                 
             nDecimais - Nmero de Decimales.                           
             cTpValor  - Valor del Parametro MV_TPVALOR.                
Ĵ
 Retorno     Nulo                                                       
Ĵ
 Uso         3.18 LIBRO DE INVENTARIOS Y BALANCES - ESTADO DE FLUJOS     
ٱ


/*/
Static Function GerArq(cDir,aColunas,aTamVal,nDecimais,cTpValor)

Local nHdl    := 0
Local cLin    := ""
Local cSep    := "|"
Local cArq    := ""
Local nCont   := 0
Local nReg	  := 0
Local nMes 	  := Month(dFinal)

cArq += "LE"                     		      // Fijo 'LE'
cArq += cRUC				         		  // Ruc
cArq +=  DTOS(dFinal)						  // Fecha
cArq += "031800"                              // Fijo '031800'
                           
//Cdigo de Oportunidad
If nMes == 12
	cArq += "01"	
ElseIf nMes == 1
	cArq += "02"
ElseIf nMes == 6
	cArq += "04"
Else
	cArq += "07"
EndIf                         
cArq += "1"
cArq += "1"
cArq += "1"
cArq += "1"
cArq += ".TXT" // Extensin

FOR nCont:=LEN(ALLTRIM(cDir)) TO 1 STEP -1
   IF SUBSTR(cDir,nCont,1)=='\' 
      cDir:=Substr(cDir,1,nCont)
      EXIT
   ENDIF   
NEXT 

nHdl := fCreate(cDir+cArq)
If nHdl <= 0
	ApMsgStop(STR0019)  // "Error al crear archivo TXT"
Else
	
	dbSelectArea("cArqTmp")
	cArqTmp ->(dbGoTop())
	For nReg := 1 To Len(aColunas)
		cLin := ""
		If  Trim(aColunas[nReg][2]) != "5" 
			
			//01 - Periodo
			cLin += DTOS(dFinal)
			cLin += cSep
			
			//02 - Cdigo del catlogo
			cLin += "09"
			cLin += cSep
			
			//03 - Cdigo del Rubro del Estado Financiero
			cLin += Trim(aColunas[nReg][1])
			cLin += cSep
			
			//04 - Saldo del Rubro Contable
			cLin += AllTrim(ValorCTB( If(lMovPeriodo,cArqTmp->(SALDOATU-SALDOANT),cArqTmp->SALDOATU),,,aTamVal[1],nDecimais,.T.,"@E 999999999999.99",cArqTmp->NORMAL,cArqTmp->CONTA,,,cTpValor,,,.F.))
		    cLin += cSep
		    
		    //05 - Indica el estado de la operacin
		    cLin += "1"
		    cLin += cSep
		    			
			cLin += chr(13)+chr(10)	
			fWrite(nHdl,cLin)	
		EndIf
		cArqTmp->(dbSkip())
	Next nReg

	fClose(nHdl)
	MsgAlert(STR0020) // "Archivo TXT generado con xito."
EndIf
Return Nil