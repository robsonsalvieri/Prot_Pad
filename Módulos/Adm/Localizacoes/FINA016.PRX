#include "FINA016.CH"
#include "PROTHEUS.CH"
/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINA016    ³ Autor ³ Paulo Augusto         ³ Data ³ 12.08.02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Recalculo de Comissoes                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FINA016                                                   	³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFIN                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß

*/
Function FINA016(lAutomato)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local   nOpca := 0
Local lCpoEst   := SAQ->(ColumnPos("AQ_STATUS")) == 0
LOCAL oDlg
Local aSays:={}, aButtons:={}

Local lPanelFin := IsPanelFin()
Private cCadastro := OemToAnsi(STR0003) //"Recalculo de Comissoes"
Default lAutomato := .F. // Tratamiento para scripts de automatización
Pergunte("AFI016",.f.)

AADD(aSays,OemToAnsi( STR0004 ) ) //"  Este programa tem como objetivo executar os c lculos das comiss”es dos"
AADD(aSays,OemToAnsi( STR0005 ) ) //"cobradores, conforme os parƒmetros definidos pelo usu rio.              "

If lPanelFin  //Chamado pelo Painel Financeiro
	aButtonTxt := {}
	AADD(aButtonTxt,{"Parametros","Parametros", {||Pergunte("AFI016",.T. )}}) // Parametros
	If !lAutomato
		FaMyFormBatch(aSays,aButtonTxt,{||nOpca:= 1},{||nOpca:=0})
    Else
        nOpca:= 1
    Endif
Else
	AADD(aButtons, { 5,.T.,{|| Pergunte("AFI016",.T. ) } } )
	AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
	AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
	If !lAutomato
		FormBatch( cCadastro, aSays, aButtons )
	Else
        nOpca:= 1
    Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Parƒmetros:                       ³
//³ mv_par01 = Data Inicial       ?  ³
//³ mv_par02 = Data Final         ?  ³
//³ mv_par03 = Do Cobrador        ?  ³
//³ mv_par04 = At‚ o Cobrador     ?  ³
//³ mv_par05 = Com status	      ?  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF lCpoEst
	If !MsgYesNo(OemToAnsi(STR0010),OemToAnsi(STR0009))  //"El campo de Estatus (AQ_STATUS) no existe, se realizará el recálculo para todos los cobradores. ¿Desea continuar?" //Atención
		Return
	EndIf
EndIf
If ( nOpcA == 1)
	Processa({|lEnd| fa016DelEX(MV_PAR08)},STR0007)  //"Excluindo Comiss”es n„o pagas"
	Processa({|lEnd| fa016Proc()},STR0008) //"Calculando Comiss”es "

EndIf

dbSelectArea("SEX")
dbSetOrder(1)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fa016DelEX   ³ Autor ³ Paulo Augusto         ³ Data ³12/08/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Zera as comissoes do periodo                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA016                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fa016DelEX(nTipo)

Local aArea		:= GetArea()
Local cChave    := ""
Local cArqInd   := ""
Local cQuery	:= ""
Local nIndex    := 0
Local nX		:= 0
Local nMax		:= 0
Local nMin		:= 0
Local lDelFisico:=	GetNewPar('MV_FIN016D',.T.)

nX:=(nMax:=(nMin:=0))
dbSelectArea("SEX")
dbSetOrder(1)
ProcRegua(RecCount())
#IFDEF TOP
	If ( TcSrvType()!="AS/400" ) .and. lDelFisico
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica qual eh o maior e o menor Recno que satisfaca a selecao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cQuery := "SELECT MIN(R_E_C_N_O_) MINRECNO,"
		cQuery +=	 "MAX(R_E_C_N_O_) MAXRECNO "
		cQuery += "FROM "+RetSqlName("SEX")+" SEX "
		cQuery += "WHERE SEX.EX_FILIAL='"+xFilial("SEX")+"' AND "
		cQuery += 	"SEX.EX_EMISSAO>='"+Dtos(mv_par01)+"' AND "
		cQuery += 	"SEX.EX_EMISSAO<='"+Dtos(mv_par02)+"' AND "
		cQuery += 	"SEX.EX_COBRAD>='"+mv_par03+"' AND "
		cQuery += 	"SEX.EX_COBRAD<='"+mv_par04+"' AND "
		cQuery += 	"SEX.EX_DATA='"+Dtos(Ctod(""))+"' AND "
		cQuery += 	"SEX.EX_AJUSTE <>'" + '"2"' + "' AND "
		cQuery += 	"SEX.D_E_L_E_T_ = '' "
		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),"fa016DelEX")

		nMax := fa016DelEX->MAXRECNO
		nMin := fa016DelEX->MINRECNO
		dbCloseArea()
		dbSelectArea("SEX")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Monta a string de execucao no banco³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cQuery := "DELETE FROM "+RetSqlName("SEX")+" "
		cQuery += "WHERE EX_FILIAL='"+xFilial("SEX")+"' AND "
		cQuery += "EX_EMISSAO>='"+Dtos(mv_par01)+"' AND "
		cQuery += "EX_EMISSAO<='"+Dtos(mv_par02)+"' AND "
		cQuery += "EX_COBRAD>='"+mv_par03+"' AND "
		cQuery += "EX_COBRAD<='"+mv_par04+"' AND "
		cQuery += "EX_DATA='"+Dtos(Ctod(""))+"' AND "
		cQuery += "EX_AJUSTE <>'" + '"2"' + "' AND "

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Executa a string de execucao no banco para os proximos 1024 registro a fim de nao estourar o log do SGBD³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		For nX := nMin To nMax STEP 1024
			cChave := "R_E_C_N_O_>="+Str(nX,10,0)+" AND R_E_C_N_O_<="+Str(nX+1023,10,0)+""
			TcSqlExec(cQuery+cChave)
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³A tabela eh fechada para restaurar o buffer da aplicacao³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SEX")
		dbCloseArea()
		ChkFile("SEX",.F.)
	EndIf
#ENDIF
	cQuery := 'EX_FILIAL=="'+xFilial("SEX")+'".And.'
	cQuery += 'Dtos(EX_EMISSAO)>="'+Dtos(mv_par01)+'".And.'
	cQuery += 'Dtos(EX_EMISSAO)<="'+Dtos(mv_par02)+'".And.'
	cQuery += 'EX_COBRAD>="'+mv_par03+'".And.'
	cQuery += 'EX_COBRAD<="'+mv_par04+'".And.'
	cQuery += 'Dtos(EX_DATA)=="'+Dtos(cTod(""))+'".And.'
	cQuery += 'EX_AJUSTE="1"'
	cArqInd  := CriaTrab(,.F.)
	cChave   := IndexKey()
	IndRegua("SEX",cArqInd,cChave,,cQuery,OemToAnsi(STR0006))  //"Selecionando Registros..."
	nIndex := RetIndex("SEX")
	dbSelectArea("SEX")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	dbSelectArea("SEX")
	dbSetOrder(nIndex+1)
	MsSeek(xFilial("SEX"),.T.)

	While ( ! Eof() .And. xFilial("SEX") == SEX->EX_FILIAL )
		RecLock("SEX",.F.)
		dbDelete()
		MsUnlock()
		dbSelectArea("SEX")
		dbSkip()
		IncProc()
	Enddo
	dbSelectArea("SEX")
	RetIndex("SEX")
	dbClearFilter()
	FErase(cArqInd+OrdBagExt())
	#IFDEF TOP
	#ENDIF
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³fa016Proc    ³ Autor ³ Paulo Augusto         ³ Data ³12/08/02³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Processa o calculo das comissoes                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA016                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function fa016Proc()
Local cAlias	:= "SEL"
Local cChave	:= ""
Local cQuery	:= ""
Local cArqInd	:= ""
Local nIndex   := 0
Local lQuery	:= .F.
Local nPerc		:= 0
Local aCpoSEX	:= {}
Local lGera		:= .f.
Local nSinal	:= 1
Local oObj      := Nil //Objeto de Grupo de Preguntas
Local aX1Pre    := {}	
Local lStatCob  := .F.
Local lCpoEst   := SAQ->(ColumnPos("AQ_STATUS")) > 0
Local oExec     := Nil

Private cCobrador :=" "

oObj := FWSX1Util():New()
oObj:AddGroup("AFI016")
oObj:SearchGroup()
aX1Pre := oObj:GetGroup("AFI016")

If Len(aX1Pre[2]) >= 5 //Si existe pregunta de ESTATUS COBRADOR
	lStatCob := .T.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Selecionando Registros           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SEL")
SEL->(dbSetOrder(6)) //EL_FILIAL+EL_COBRAD+Dtos(EL_DTDIGIT)
ProcRegua(RecCount())
#IFDEF TOP
	If ( TcSrvType()!="AS/400" )
		lQuery := .T.
		cAlias := "fa016Proc"
		cQuery := "SELECT SEL.R_E_C_N_O_ SELRECNO,SEL.EL_TIPODOC,SEL.EL_COBRAD,SEL.EL_SERIE,SEL.EL_RECIBO,SEL.EL_DTDIGIT,SEL.EL_CLIORIG,SEL.EL_LOJORIG,SEL.EL_VALOR,SEL.EL_MOEDA,SEL.EL_NUMERO "
		cQuery += "FROM "+RetSqlName("SEL")+" SEL "
		cQuery += " INNER JOIN SAQT10 SAQ ON SAQ.AQ_COD = SEL.EL_COBRAD "
		cQuery += "WHERE SEL.EL_FILIAL = ? AND "
		cQuery += "SAQ.AQ_FILIAL = ? AND "
		cQuery += "SEL.EL_EMISSAO >= ? AND "
		cQuery += "SEL.EL_EMISSAO <= ? AND "
		cQuery += "SEL.EL_COBRAD >= ? AND "
		cQuery += "SEL.EL_COBRAD <= ? AND "
		If cPaisLoc=="CHI"
			cQuery += "EL_TIPODOC <>'TB ' AND EL_TIPODOC <>'RA ' AND "
		else
			cQuery += "EL_TIPODOC <>'TB' AND EL_TIPODOC <>'RA' AND "
		endif
		If lStatCob .And. lCpoEst
			If MV_PAR05 == 1 //Activos
				cQuery += "SAQ.AQ_STATUS = ? Or SAQ.AQ_STATUS = ' ' AND "
			ElseIf MV_PAR05 == 2 //Inactivos
				cQuery += "SAQ.AQ_STATUS = ? AND "
			EndIf
		EndIf
		cQuery += "SAQ.D_E_L_E_T_ = ' ' AND "
		cQuery += "SEL.D_E_L_E_T_ = ' ' "
		cQuery += "ORDER BY EL_FILIAL,EL_COBRAD,EL_DTDIGIT
		cQuery := ChangeQuery(cQuery)
		oExec := FwExecStatement():New(cQuery)
		oExec:SetString(1,xFilial("SEL"))
		oExec:SetString(2,xFilial("SAQ"))
		oExec:SetString(3,DTOS(MV_PAR01))
		oExec:SetString(4,DTOS(MV_PAR02))
		oExec:SetString(5,MV_PAR03)
		oExec:SetString(6,MV_PAR04)
		If lStatCob .And. lCpoEst
			If MV_PAR05 == 1 //Activos
				oExec:SetString(7,'1')
			ElseIf MV_PAR05 == 2 //Inactivos
				oExec:SetString(7,'2')
			EndIf
		EndIf

		cAlias := oExec:OpenAlias()
	Else
#ENDIF

	cQuery	:= 'EL_FILIAL=="'+xFilial("SEL")+'".And.'
	cQuery	+= 'DTOS(EL_EMISSAO)>="'+DTOS(mv_par01)+'".And.'
	cQuery	+= 'DTOS(EL_EMISSAO)<="'+DTOS(mv_par02)+'".And.'
	cQuery	+= 'EL_COBRAD>="'+mv_par03+'".And.'
	cQuery	+= 'EL_COBRAD<="'+mv_par04+'".And.'

	cQuery	+= '!alltrim(EL_TIPODOC) $ "TB|RA"'

	cArqInd  := CriaTrab(,.F.)
	cChave   := IndexKey()
	IndRegua("SEL",cArqInd,cChave,,cQuery,OemToAnsi(STR0006))  //"Selecionando Registros..."
	nIndex := RetIndex("SEL")
	dbSelectArea("SEL")
	#IFNDEF TOP
		dbSetIndex(cArqInd+OrdBagExt())
	#ENDIF
	dbSetOrder(nIndex+1)
	MsSeek(xFilial("SEL"),.T.)
	#IFDEF TOP
	EndIf
	#ENDIF
While ( ! Eof() )

	If !(alltrim((cAlias)->EL_TIPODOC) $ "TB|RA")
	  	aCpoSEX	:= {}
		If cCobrador <> (cAlias)->EL_COBRAD
			SAQ->(DbSetOrder(1))
			SAQ->(Dbseek(xFilial("SAQ")+(cAlias)->EL_COBRAD))
			nPerc:=SAQ->AQ_COMIS
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calculo da Comissao ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SAQ->AQ_GERASE2=="1"
			dbSelectArea("SEL")
			MsGoto(If(lQuery,(cAlias)->(SELRECNO),RecNo()))
			SEX->(DbSetOrder(2))
		  	If SEL->EL_CANCEL
				If SEX->(Dbseek(xfilial("SEX")+ SEL->EL_COBRAD + SEL->EL_RECIBO + SEL->EL_TIPODOC+SEL->EL_NUMERO+SEL->EL_SERIE))
				  		cNumant:=xFilial("SEX")+ SEL->EL_COBRAD + SEL->EL_RECIBO + SEL->EL_TIPODOC+SEL->EL_NUMERO+SEL->EL_SERIE
					While cNumant==SEX->EX_FILIAL + SEX->EX_COBRAD+SEX->EX_NUM + SEX->EX_TIPODOC+SEX->EX_TITULO+SEX->EX_SERREC .and.  SEX->(!EOF())
						If SEX->EX_BASE >0 .and. dtoc(SEX->EX_DATA)<>"  /  /  "
							nSinal	:= -1
							lGera		:=.t.
						ElseIf SEX->EX_BASE < 0 .and. dtoc(SEX->EX_DATA)<>"  /  /  "
							lGera		:=.f.
						EndIf
						SEX->(dbSkip())
					Enddo
				EndIf
			Else
				If !SEX->(Dbseek(xfilial("SEX")+ SEL->EL_COBRAD + SubStr(SEL->EL_RECIBO,1,TamSX3("EX_NUM")[1]) + SEL->EL_TIPODOC+SEL->EL_NUMERO+SEL->EL_SERIE))
					nSinal	:= +1
					lGera		:=.t.
				EndIf
			EndIf
		Else
			lGera		:=.t.
     	EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua os calculos para cada vendedor. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lGera
		 	//SE MUDAR ALGUMA POSICAO DO ARRAY ABAIXO, PRECISA CORRIGIR TB NOS FONTES FINA016/FINA87A/FINA088.
			AADD(aCpoSEX,SEL->EL_COBRAD )
			AADD(aCpoSEX,SEL->EL_SERIE )
			AADD(aCpoSEX,SEL->EL_RECIBO )
			AADD(aCpoSEX,SEL->EL_DTDIGIT )
			AADD(aCpoSEX,SEL->EL_CLIORIG)
			AADD(aCpoSEX,SEL->EL_LOJORIG )
			AADD(aCpoSEX,SEL->EL_VALOR * nSinal )
			AADD(aCpoSEX, nPerc)
			AADD(aCpoSEX,Val(SEL->EL_MOEDA))
			AADD(aCpoSEX,(SEL->EL_VALOR * nSinal) *(nPerc/100) )
			AADD(aCpoSEX,SEL->EL_TIPODOC)
			AADD(aCpoSEX,SEL->EL_NUMERO)
			Fa016Calc(aCpoSEX)
			lGera		:=.f.
		EndIf
		cCobrador := (cAlias)->EL_COBRAD
	EndIf
	dbSelectArea(cAlias)
	dbSkip()
	IncProc(STR0008+":"+SEL->&(SerieNfId("SEL",3,"EL_SERIE"))+SEL->EL_RECIBO+SEL->EL_PREFIXO+SEL->EL_NUMERO+SEL->EL_PARCELA) //"Titulo"
EndDo

If ( lQuery )
	dbSelectArea(cAlias)
	(cAlias)->(DbCloseArea())
	oExec:Destroy()
	oExec := nil 
Else
	dbSelectArea("SEL")
	RetIndex("SEL")
	dbClearFilter()
	FErase(cArqInd+OrdBagExt())
EndIf
dbSelectArea("SEL")
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Fa016Calc ³ Autor ³ Paulo Augusto         ³ Data ³ 12/08/02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Efetua c lculo das comissoes pela emissao                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Perc. de Comissaoo									        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Fina016                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/


Function Fa016Calc(aCpoSEX)
Local aArea 	:= GetArea()
Local nDia		:= 0
Local nMes		:= 0
Local nAno		:= 0
Local dVencto	:= Ctod("")

//dbSelectArea("SEX")    // Para Abrir se precisar
//dbSelectArea (aArea[1])

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua os c lculos para cada vendedor. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If aCpoSEX[7]> 0   // Verifica se o vendedor tem % de comissao
   DbSelectArea("SEX")
	RecLock("SEX",.T.)
	SEX->EX_FILIAL  := xFilial("SEX") // Filial
	SEX->EX_COBRAD  :=	aCpoSEX[1]		// C¢d. Vendedor
	//SEX->EX_SERREC  :=	aCpoSEX[2]		// Serie do Recibo
	SerieNfId("SEX",1,"EX_SERREC",,,,aCpoSEX[2]) // Serie do Recibo
	SEX->EX_NUM     :=	aCpoSEX[3]		// No. do Recibo
	SEX->EX_EMISSAO :=	aCpoSEX[4]		// Data da emissÆo do t¡tulo
	SEX->EX_CODCLI  :=	aCpoSEX[5]		// C¢d. Cliente
	SEX->EX_LOJA    :=	aCpoSEX[6]		// Loja
	SEX->EX_BASE    :=	Round(aCpoSEX[7],MsDecimais(aCpoSEX[9]))		// Valor Base da Comissao
	SEX->EX_PORC    :=	aCpoSEX[8]		// % de Comissao
	SEX->EX_MOEDA	:=	aCpoSEX[9]		// Moeda do Titulo
	SEX->EX_COMIS   :=	Round(aCpoSEX[10],MsDecimais(aCpoSEX[9]))		// Valor a Pagar
	SEX->EX_TIPODOC	:=	aCpoSEX[11]		// Prefixo do T¡tulo
	SEX->EX_TITULO	:=	aCpoSEX[12]		// Numero do Titulo
	SEX->EX_AJUSTE	:="1"                 

	If Empty( SAQ->AQ_DIA )
		dVencto := SEL->EL_DTDIGIT
	Else
		dVencto := Ctod( strzero(SAQ->AQ_DIA,2)+"/"+;
		strzero(month(SEL->EL_DTDIGIT),2)+"/"+;
		strzero( year(SEL->EL_DTDIGIT),4),"ddmmyy")
		nDia := SAQ->AQ_DIA
		While empty( dVencto)
			nDia -= 1
			dVencto := CtoD(strzero(nDia,2)+"/"+;
			strzero(month(SEL->EL_DTDIGIT),2)+"/"+;
			strzero( year(SEL->EL_DTDIGIT),4),"ddmmyy")
		endDo

		If SAQ->AQ_DDD == "F" .or. dVencto < SEL->EL_DTDIGIT		//Fora o mes
			nDia := SAQ->AQ_DIA
			nMes := month(dVencto) + 1
			nAno := year (dVencto)
			If nMes == 13
				nMes := 01
				nAno := nAno + 1
			Endif
			nDia	  := strzero(nDia,2)
			nMes	  := strzero(nMes,2)
			nAno	  := substr(lTrim(str(nAno)),3,2)
			dVencto := CtoD(nDia+"/"+nMes+"/"+nAno,"ddmmyy")
		Else
			nDia	  := strzero(day(dVencto),2)
			nMes	  := strzero(month(dVencto),2)
			nAno	  := substr(lTrim(str(Year(dVencto))),3,2)
		Endif

		While empty( dVencto)
			nDia := if(Valtype(nDia)=="C",Val(nDia),nDia)
			nDia -= 1
			dVencto := CtoD(strzero(nDia,2)+"/"+nMes+"/"+nAno,"ddmmyy")
			If !empty( dVencto )
				If dVencto < SEL->EL_DTDIGIT
					dVencto += 2
				EndIf
			EndIf
		Enddo
	EndIf
	SEX->EX_VENCTO  := dVencto

	//
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada apos gravar os dados da comissao e antes de  ³
	//³destravar o arquivo													     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If ExistBlock("MSEX016")
		ExecBlock("MSEX016",.F.,.F.)
	EndIf

	MsUnlock()
EndIf
RestArea(aArea)
Return(.T.)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³FINA016T   ³ Autor ³ Marcelo Celi Marques ³ Data ³ 05.05.08 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada semi-automatica utilizado pelo gestor financeiro   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA016                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function FINA016T(aParam)

	cRotinaExec := "FINA016"
	ReCreateBrow("SEX",FinWindow)
	FINA016()
	ReCreateBrow("SEX",FinWindow)
	dbSelectArea("SEX")
	INCLUI := .F.
	ALTERA := .F.

Return .T.
