#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.fin.bills.related

//-------------------------------------------------------------------
/*/{Protheus.doc} BillRelated
	@author @rodrigo.oliveira
	@since 15/05/2025
	@type class
	@version 12.1.2410
    @see 
/*/
//-------------------------------------------------------------------
class BillRelated

	public method new(lForceUseRelatedBill as logical) as object
	public method clear()
	public method getRelatedBills(cAlias as character, lIsBillSon as logical, nRecno as numeric) as json
	public method getRelatedBillKey(cAlias as character, nRecno as numeric) as character
	public method getIsChild(cIdDoc as character, cAlias as character, nRecno as numeric) as logical
	public method canUseRelatedBill() as logical
	public method setAliasOrig(cAliasOrig as character)

	private method setIsChild(lIsChild as logical)
	private method getHasProperty() as logical 
	private method getBillRelatedStatement() as object
	private method getIdDocFK7() as character
	private method setIdDoc(cIdDoc as character)
	private method setAlias(cAlias as character)
	private method aliasToData() as json

	private data jGetPost as json

	protected data oBillRelated as object
	protected data lIsBillSon as logical
	protected data lcanUseRelatedBill as logical
	protected data lUseRelatedBill as logical
	protected data jRet as json
	protected data cKeyTit as character
	protected data cIdDoc as character
	protected data cIdPai as character
	protected data cAlias as character
	protected data cAliasOrig as character
	protected data cAliasStatement as character
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method new(lForceUseRelatedBill as logical) as object class BillRelated
	default lForceUseRelatedBill := .F.
	self:clear()
	self:lUseRelatedBill := lForceUseRelatedBill
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method clear() class BillRelated
	If self:oBillRelated != Nil
		FreeObj(self:oBillRelated)
	EndIf
	self:lIsBillSon := .T.
	self:lcanUseRelatedBill := .F.
	self:cKeyTit := ""
	self:cIdDoc	:= ""
	self:cIdPai := ""
	self:cAlias := ""
	self:cAliasOrig := ""
	self:cAliasStatement := ""
	self:jGetPost := JsonObject():New()
	self:jRet := JsonObject():New()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} canUseRelatedBill

	Verifica se o ambiente está apto a utilizar a Classe

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method canUseRelatedBill() as logical class BillRelated
	self:lcanUseRelatedBill := ( GetRpoRelease() >= "12.1.2610" ) .Or. self:lUseRelatedBill
Return self:lcanUseRelatedBill

//-------------------------------------------------------------------
/*/{Protheus.doc} getRelatedBills

	Retorna o json com os dados do título (principal ou filho),
	de acordo com o solicitado no método.

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	@param cAlias, character, Alias indicando a carteira a ser pesquisada (SE1 / SE2)
	@param lIsBillSon, logical, define se o título é filho ou não para retornar o título relacionado
	@param nRecno, numeric, utilizado quando o título de partida não está ponteirado

	@return jResult, json, json contendo os dados do(s) título(s) relacionados na seguinte estrutura:
		Estrutura do jResult:
			jResult['document']
				jResult['FK7_FILIAL'] 		// Filial do título relacionado
				jResult['FK7_PREFIX']		// Prefixo do título relacionado
				jResult['FK7_NUM']			// Número do título relacionado
				jResult['FK7_PARCEL']		// Parcela do título relacionado
				jResult['FK7_TIPO']			// Tipo do título relacionado
				jResult['FK7_CLIFOR']	    // Cliente / Fornecedor do título relacionado
				jResult['FK7_LOJA']			// Loja do título relacionado
				jResult[Alias+'NATUREZ']	// Natureza do título relacionado (SE1 / SE2)
				jResult[Alias+'EMISSAO']	// Emissão do título relacionado (SE1 / SE2)
				jResult[Alias+'VENCTO']	    // Vencimento do título relacionado (SE1 / SE2)
				jResult[Alias+'VENCREA']	// Vencimento real do título relacionado (SE1 / SE2)
				jResult[Alias+'VLCRUZ']	    // Valor (R$) do título relacionado (SE1 / SE2)
				jResult[Alias+'BAIXA']	    // Data da baixa do título relacionado (SE1 / SE2)
				jResult[Alias+'VENCORI']	// Vencimento original do título relacionado (SE1 / SE2)
				jResult['SERECNO']		    // Recno do título relacionado (SE1 / SE2)
				jResult['FK7_IDDOC']	    // Tipo do título relacionado
				jResult['FK7_IDPAI']	    // Tipo do título relacionado
				jResult['FK7_ALIAS']	    // Tipo do título relacionado
		 
/*/
//-------------------------------------------------------------------
method getRelatedBills(cAlias as character, lIsBillSon as logical, nRecno as numeric) as json class BillRelated
	local jResult 	:= JsonObject():new() as json
	local cIdDoc	:= '' as character
	local aArea     := {} as array
	local aAreaAtu  := {} as array

	default cAlias    := "SE1"
	default lIsBillSon    := .T.
	default nRecno  := 0

	if self:canUseRelatedBill()
		self:setAlias(cAlias)
		if !Empty(nRecno)
			aArea       := FwGetArea()
			aAreaAtu    := (self:cAlias)->(FwGetArea())
			(self:cAlias)->(DbGoTo(nRecno))
		endIf
		if Empty(self:cIdDoc)
			cIdDoc	:= self:getIdDocFK7()
			self:setIdDoc(cIdDoc)
		endIf
		self:setIsChild(lIsBillSon)
		self:cAliasStatement 	:= self:getBillRelatedStatement():openAlias()
		jResult := self:aliasToData()
		(self:cAliasStatement)->(dbCloseArea())
		if !Empty(nRecno)
			RestArea(aAreaAtu)
			RestArea(aArea)
			FwFreeArray(aAreaAtu)
			FwFreeArray(aArea)        
		endIf
	endIf

return jResult

//-------------------------------------------------------------------
/*/{Protheus.doc} setIdDoc

	Seta o IDDOC e o Alias do título pai para ser usado na query

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	@param cIdDoc, character, IDDOC do título de partida (principal ou título relacionado) ponteirado
/*/
//-------------------------------------------------------------------
Method setIdDoc(cIdDoc as character) Class BillRelated
	default cIdDoc  := ""
	
	self:cIdDoc    := cIdDoc
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setAlias

	Seta o Alias do título pai para ser usado na query

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	@param cAlias, character, Alias do título para que seja consultado de acordo com a carteira (SE1 / SE2)
/*/
//-------------------------------------------------------------------
Method setAlias(cAlias as character) Class BillRelated
	default cAlias    := ""
	
    self:cAlias      := cAlias
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} setAliasOrig

	Permite ao operador Setar o Alias do título pai para ser usado
	na query (cenário de autoretenção do IR)

	@author @rodrigo.oliveira
	@since 26/09/2025
	@type method
	@version 12.1.2410
	@param cAlias, character, Alias do título para que seja consultado de acordo com a carteira (SE1 / SE2)
/*/
//-------------------------------------------------------------------
Method setAliasOrig(cAliasOrig as character) Class BillRelated
	default cAliasOrig    := ""
	
    self:cAliasOrig      := cAliasOrig
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getHasProperty

	Seta o IDDOC do título pai para ser usado na query

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410

	@param cChaveJson, character, Chave contendo informações do último título pesquisado na Classe

	@return lRet, logical, retorno positivo caso a chave pesquisada seja encontrada
/*/
//-------------------------------------------------------------------
Method getHasProperty() as logical Class BillRelated
Return 	self:jGetPost:HasProperty(self:cIdDoc + self:cAlias + cValToChar(self:lIsBillSon))

//-------------------------------------------------------------------
/*/{Protheus.doc} setIsChild

	Seta o controle pra saber se o título a ser procurado é o título
    principal (pai) ou os relacionados (filhos)
    
	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	@param lIsChild, logical, Recebe verdadeiro se o título posicionado é
		um relacionado (procura o pai) e falso caso seja uma busca pelos
		títulos relacionados
/*/
//-------------------------------------------------------------------
Method setIsChild(lIsChild as logical) Class BillRelated
	default lIsChild := .T.
	
	self:lIsBillSon    := lIsChild
	If lIsChild
		If Empty(self:cKeyTit)
			self:getIdDocFK7()
		EndIf
		self:cIdPai	:= GetAdvFVal("FK7","FK7_IDPAI", self:cAlias + self:cKeyTit, 4)
	EndIf
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getIsChild

	Retorna um lógico indicando se o título posicionado (imposto ou
    abatimento) é filho do ID passado no parâmetro

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	@param cIdDoc, character, IDDOC do título principal (pai), pesquisado previamente
	@param cAlias, character, Alias do título poscionado definindo a carteira a ser pesquisada (SE1 / SE2)
	@param nRecno, numeric, Recno do título passado para o caso do título pesquisado não estar ponteirado

	@return lRet, logical, retorno positivo caso o título posicionado seja filho do título principal (IDDOC)	
/*/
//-------------------------------------------------------------------
method getIsChild(cIdDoc as character, cAlias as character, nRecno as numeric) as logical class BillRelated
	local lRet      := .F. as logical
	local aAreaAtu  := {} as array
	local aArea     := {} as array

	default cIdDoc  := ""
	default cAlias	:= "SE1"
	default nRecno  := 0

	if self:canUseRelatedBill()
		self:setAlias(cAlias)
		if !Empty(nRecno)
			aAreaAtu    := FwGetArea()
			aArea       := (self:cAlias)->(FwGetArea())
			(self:cAlias)->(DbGoTo(nRecno))
		endIf

		if !Empty(cIdDoc)
			if !self:getHasProperty()
				self:clear()
				self:setAlias(cAlias)
				self:setIdDoc(cIdDoc)
				self:setIsChild()
				self:jRet := self:getRelatedBills(self:cAlias)
			endIf
			lRet := !Empty(self:jRet['document']) .And. self:jRet['document'][1]:hasProperty('FK7_IDDOC') .And. self:jRet['document'][1]['FK7_IDDOC'] == self:cIdPai
		endIf

		if !Empty(nRecno)
			FwRestArea(aArea)
			FwRestArea(aAreaAtu)
			FwFreeArray(aArea)
			FwFreeArray(aAreaAtu)
		endIf
	endIf
return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getRelatedBillKey
    Recebe o Alias do título e retorna os dados do registro 
	(antigo TITPAI) 

	@author @rodrigo.oliveira
	@since 28/05/2025
	@type method
	@version 12.1.2410

	@param cAlias, character, Alias do título poscionado definindo a 
		carteira a ser pesquisada (SE1 / SE2)
	@param nRecno, numeric, Recno do título passado para o caso do
		título pesquisado não estar ponteirado

	@return cDocumentKey, Character, Chave do título principal (antigo TITPAI)
/*/
//-------------------------------------------------------------------
method getRelatedBillKey(cAlias as character, nRecno as numeric) as character class BillRelated
	local cDocumentKey	:= '' as character
	local cIdDoc 		:= '' as character
	local aArea     	:= {} as array
	local aAreaAtu  	:= {} as array

	default cAlias := "SE1"
	default nRecno := 0
	
	if self:canUseRelatedBill()
		self:setAlias(cAlias)
		if !Empty(nRecno)
			aArea       := FwGetArea()
			aAreaAtu    := (self:cAlias)->(FwGetArea())
			(self:cAlias)->(DbGoTo(nRecno))
		endIf
		cIdDoc	:= self:getIdDocFK7()
		self:setIdDoc(cIdDoc)
		self:setIsChild()
		self:cAliasStatement 	:= self:getBillRelatedStatement():openAlias()
		cDocumentKey := (self:cAliasStatement)->FK7_PREFIX + (self:cAliasStatement)->FK7_NUM + (self:cAliasStatement)->FK7_PARCEL +;
			 (self:cAliasStatement)->FK7_TIPO + (self:cAliasStatement)->FK7_CLIFOR + (self:cAliasStatement)->FK7_LOJA
		(self:cAliasStatement)->(dbCloseArea())
		if !Empty(nRecno)
			RestArea(aAreaAtu)
			RestArea(aArea)
			FwFreeArray(aAreaAtu)
			FwFreeArray(aArea)        
		endIf
	endIf

return cDocumentKey

//-------------------------------------------------------------------
/*/{Protheus.doc} getIdDocFK7

	Pesquisa pelo IDDOC do título posicionado

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	
	@return cRet, character, IDDOC do título pesquisado conforme gravado previamente na FK7
/*/
//-------------------------------------------------------------------
method getIdDocFK7() as Character class BillRelated
	local cRet := "" as character

	if self:cAlias == 'SE1'
		cRet    	:= FinBuscaFK7(SE1->E1_FILIAL + "|" + SE1->E1_PREFIXO + "|" + SE1->E1_NUM + "|" + SE1->E1_PARCELA + "|" + SE1->E1_TIPO + "|" + SE1->E1_CLIENTE + "|" + SE1->E1_LOJA , self:cAlias)
		self:cKeyTit	:= SE1->E1_FILIAL + SE1->E1_PREFIXO + SE1->E1_NUM + SE1->E1_PARCELA + SE1->E1_TIPO + SE1->E1_CLIENTE + SE1->E1_LOJA
	elseIf self:cAlias == 'SE2'
		cRet    	:= FinBuscaFK7(SE2->E2_FILIAL + "|" + SE2->E2_PREFIXO + "|" + SE2->E2_NUM + "|" + SE2->E2_PARCELA + "|" + SE2->E2_TIPO + "|" + SE2->E2_FORNECE + "|" + SE2->E2_LOJA , self:cAlias)
		self:cKeyTit	:= SE2->E2_FILIAL + SE2->E2_PREFIXO + SE2->E2_NUM + SE2->E2_PARCELA + SE2->E2_TIPO + SE2->E2_FORNECE + SE2->E2_LOJA
	endIf
return cRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getBillRelatedStatement

	Retorna a query dos métodos da classe BillRelated

	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
/*/
//-------------------------------------------------------------------
method getBillRelatedStatement() as object class BillRelated
	local cQuery := "" as character
	local nParam := 1 as numeric
	local cChaveJson := "" as character
	local cAliasAux := "" as character

	cAliasAux := self:cAlias
	if !empty(self:cAliasOrig)
		cAliasAux := self:cAliasOrig
	endIf

	cChaveJson	:= cAliasAux + cValToChar(self:lIsBillSon)
	if self:oBillRelated == NIL .Or. !(self:jGetPost:HasProperty(cChaveJson))
		//Remove a propriedade anterior do JSON e insere a atual
		If Len(self:jGetPost:GetNames()) > 0
			self:jGetPost:DelName(self:jGetPost:GetNames()[1])
		EndIf
		self:jGetPost[cChaveJson] := .T.

		cQuery	:= "Select FK7.FK7_FILIAL, FK7.FK7_IDDOC IDDOC, FK7.FK7_IDPAI IDPAI, FK7.FK7_ALIAS, "
		cQuery	+= "FK7.FK7_FILTIT, FK7.FK7_PREFIX, FK7.FK7_NUM, FK7.FK7_PARCEL, FK7.FK7_TIPO, FK7.FK7_CLIFOR, FK7.FK7_LOJA, "

		If cAliasAux == 'SE2'
			cQuery 	+= "SE2.E2_NATUREZ NATUREZ, SE2.E2_NOMFOR NOMCLIFOR, SE2.E2_EMISSAO DTEMISSAO, SE2.E2_VENCTO DTVENCTO, SE2.E2_VENCREA DTVENCREA, "
			cQuery 	+= "SE2.E2_VLCRUZ VALOR, SE2.E2_BAIXA DTBAIXA, SE2.E2_VENCORI DTVENCORI, SE2.R_E_C_N_O_ AS SERECNO "
		Else
			cQuery 	+= "SE1.E1_NATUREZ NATUREZ, SE1.E1_NOMCLI NOMCLIFOR, SE1.E1_EMISSAO DTEMISSAO, SE1.E1_VENCTO DTVENCTO, SE1.E1_VENCREA DTVENCREA, "
			cQuery 	+= "SE1.E1_VLCRUZ VALOR, SE1.E1_BAIXA DTBAIXA, SE1.E1_VENCORI DTVENCORI, SE1.R_E_C_N_O_ AS SERECNO "
		EndIf

		cQuery 	+= "From " + RetSqlName('FK7') + " FK7 "

		If cAliasAux == 'SE2'
			cQuery 	+= "Inner Join " + RetSqlName('SE2') + " SE2 "
		Else
			cQuery 	+= "Inner Join " + RetSqlName('SE1') + " SE1 "
		EndIf
			
		cQuery 	+= "On FK7.FK7_ALIAS = ? " // #1
		cQuery	+= " And FK7_FILTIT = ? " //#2
		cQuery 	+= " And FK7.FK7_PREFIX = ? " //#3
		cQuery 	+= " And FK7.FK7_NUM = ? " //#4
		cQuery 	+= " And FK7.FK7_PARCEL = ? " //#5
		cQuery 	+= " And FK7.FK7_TIPO = ? " //#6
		cQuery 	+= " And FK7.FK7_CLIFOR = ? " //#7
		cQuery 	+= " And FK7.FK7_LOJA = ? " //#8

		If cAliasAux == 'SE2'
			cQuery 	+= " AND SE2.D_E_L_E_T_ = ? " //#9
		Else
			cQuery 	+= " AND SE1.D_E_L_E_T_ = ? " //#9
		EndIf
			
		If self:lIsBillSon
			cQuery += " WHERE FK7.FK7_IDDOC = ? " //#10
		Else
			cQuery += " WHERE FK7.FK7_IDPAI = ? " //#10			
		EndIf
		cQuery 	+= "And FK7.D_E_L_E_T_ = ? " //#11

		cQuery	+= " ORDER BY FK7.FK7_PARCEL, FK7.FK7_TIPO"

		cQuery	:= ChangeQuery(cQuery)
		self:oBillRelated := FWExecStatement():new(cQuery)  

	endIf

	self:oBillRelated:SetString(nParam++, cAliasAux) //#1
	if cAliasAux == 'SE2'
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_FILIAL') //#2
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_PREFIXO') //#3
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_NUM') //#4
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_PARCELA') //#5
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_TIPO') //#6
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_FORNECE') //#7
		self:oBillRelated:SetUnsafe(nParam++, 'SE2.E2_LOJA') //#8
	else
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_FILIAL') //#2
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_PREFIXO') //#3
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_NUM') //#4
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_PARCELA') //#5
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_TIPO') //#6
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_CLIENTE') //#7
		self:oBillRelated:SetUnsafe(nParam++, 'SE1.E1_LOJA') //#8
	endIf
	self:oBillRelated:SetString(nParam++, ' ') //#9
	If self:lIsBillSon
		self:oBillRelated:SetString(nParam++, self:cIdPai) //#10
	Else
		self:oBillRelated:SetString(nParam++, self:cIdDoc) //#10
	EndIf
	self:oBillRelated:SetString(nParam++, ' ') //#11

return self:oBillRelated

//-------------------------------------------------------------------
/*/{Protheus.doc} aliasToData
	@author @rodrigo.oliveira
	@since 16/05/2025
	@type method
	@version 12.1.2410
	
	@return jRet, Json, Dados da query no formato de array
/*/
//-------------------------------------------------------------------
method aliasToData() as json class BillRelated
	Local jAux  as json
	Local nI	:= 0 as numeric
	Local cCpo  := 'E1_' as character

	self:jRet['document'] := {}

	If self:cAlias == 'SE2'
		if empty(self:cAliasOrig) .Or. ( !empty(self:cAliasOrig) .And. self:cAliasOrig == "SE2" )
			cCpo    := 'E2_'
		endIf
	EndIf

	(self:cAliasStatement)->(DbGoTop())
	While !(self:cAliasStatement)->(EoF())
		nI++
		
		jAux	:= JsonObject():New()
			
		jAux['FK7_FILTIT'] 		:= (self:cAliasStatement)->FK7_FILTIT
		jAux['FK7_PREFIX']		:= (self:cAliasStatement)->FK7_PREFIX
		jAux['FK7_NUM']			:= (self:cAliasStatement)->FK7_NUM
		jAux['FK7_PARCEL']		:= (self:cAliasStatement)->FK7_PARCEL
		jAux['FK7_TIPO']		:= (self:cAliasStatement)->FK7_TIPO
		jAux['FK7_CLIFOR']	    := (self:cAliasStatement)->FK7_CLIFOR
		jAux['FK7_LOJA']		:= (self:cAliasStatement)->FK7_LOJA
		jAux[cCpo+'NATUREZ']	:= (self:cAliasStatement)->NATUREZ
		jAux[cCpo+'EMISSAO']	:= (self:cAliasStatement)->DTEMISSAO
		jAux[cCpo+'VENCTO']	    := (self:cAliasStatement)->DTVENCTO
		jAux[cCpo+'VENCREA']	:= (self:cAliasStatement)->DTVENCREA
		jAux[cCpo+'VLCRUZ']	    := (self:cAliasStatement)->VALOR
		jAux[cCpo+'BAIXA']	    := (self:cAliasStatement)->DTBAIXA
		jAux[cCpo+'VENCORI']	:= (self:cAliasStatement)->DTVENCORI
		jAux['SERECNO']		    := (self:cAliasStatement)->SERECNO
		jAux['FK7_IDDOC']	    := (self:cAliasStatement)->IDDOC
		jAux['FK7_IDPAI']	    := (self:cAliasStatement)->IDPAI
		jAux['FK7_ALIAS']	    := (self:cAliasStatement)->FK7_ALIAS
		aAdd(self:jRet['document'], jAux)
		FreeObj(jAux)
		(self:cAliasStatement)->(DbSkip())
	EndDo

return self:jRet
