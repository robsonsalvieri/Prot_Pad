#INCLUDE 'tlpp-core.th'

NAMESPACE totvs.protheus.backoffice.fin.general

static _oMockDataInstance as object

class MockData
	static method getInstance()

	public method new() constructor    
	public method clear() 
	public method getMockData() as json
	public method setMockData()
	public method hasMockData() as logical

	protected method setInstance()

	protected data jMockData as json
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	Classe para controle de dados que simulam a resposta de APIs externas, ou forçam determinadas
	condições para automação de testes.
	Implementa o padrão de projeto Singleton.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new(jMockData as json) Class MockData	
	default jMockData := JsonObject():new()
	self:clear()

	self:setMockData(self:jMockData)

	self:setInstance()
return Self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	Limpa o controle de estado da classe.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method clear() class MockData
	self:jMockData := JsonObject():New()
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getInstance
	Retorna a instância única da classe, criando-a, se necessário.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getInstance(jMockData as json) class MockData
	if _oMockDataInstance != NIL
		_oMockDataInstance:setMockData(jMockData)
		return _oMockDataInstance
	endIf	
return MockData():new(jMockData)

//-------------------------------------------------------------------
/*/{Protheus.doc} setInstance
	Guarda a referência da classe única na variável estática, que posteriormente será recuperada pelo método getInstance.
	Implementa o design patter Singleton.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setInstance() class MockData
	_oMockDataInstance := self
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getMockData
	Retorna o Json com os dados simulados.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getMockData() as json class MockData
return self:jMockData

//-------------------------------------------------------------------
/*/{Protheus.doc} setMockData
	Classe para controle de dados que simulam a resposta de APIs externas, ou forçam determinadas
	condições para automação de testes.
	Implementa o design patter Singleton.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setMockData(jMockData as json) class MockData
	if jMockData != NIL
		self:jMockData := jMockData
	endIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} hasMockData
	Retorna verdadeiro quando o json de dados simulados possui ao menos uma
	propriedade.
	@author guilherme.sordi@totvs.com.br
	@since 08/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method hasMockData() as logical class MockData	
return !empty(self:jMockData:getNames())
