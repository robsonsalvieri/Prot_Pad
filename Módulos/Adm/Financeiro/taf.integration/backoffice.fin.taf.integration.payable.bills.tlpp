#include 'protheus.ch'
#include 'tlpp-core.th'
#include 'tlpp-rest.th'

namespace totvs.protheus.backoffice.fin.taf.integration

//-------------------------------------------------------------------
/*/{Protheus.doc} TafIntegration
    @description Classe responsável por retornar os dados dos títulos a pagar
    para a integração com o TAF.
    Centralizar os principais métodos para adequar os dados à forma como o TAF espera
    receber, por isso a classe PayableWriteOff é uma extensão.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type class
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
class PayableBillsData from totvs.protheus.backoffice.fin.abstract.DefaultGetModel
    public method new() as object 
    public method setParameters()
    public method clear() as json

    // override methods ==========================
    protected method loadMapFields()
    protected method getQuery() as character
    protected method getOrder() as character
    protected method setCalcData()
    // ===========================================

    protected method getCalcNatureOfIncome() as array
    protected method getCalcBillNumber() as character
    protected method getCalcBillOriginType() as character
    protected method getCalcBillValue() as numeric
    protected method getCalcBillDate() as date
    protected method getCalcParticipatingCode() as character
    protected method hasIndividualIncomeTaxAlocation() as logical
    protected method getSFTData() as json
    protected method getFilterFKW() as character
    protected method getIgnoreTypes() as array
    protected method getCodeRentOrRoyalties() as array
    protected method getNatureOfIncomeFromFKW() as array
    protected method isValidNatureOfIncomeFKW() as logical
    protected method getFKWIndividualIncomeTaxAlocationFilter() as character

    
    protected data oMainExec as object    
    protected data jParameters as json
    protected data aMandatoryParameters as array
    protected data cDataType as character
    protected data jResult as json

    protected data lIrAtWriteOff as logical
    protected data lPCCAtWriteOff as logical
    protected data lIssAtWriteOff as logical
    protected data aIgnoreTypes as array
    protected data aCodeRentOrRoyalties as array
    protected data nLenInstallment as numeric
    protected data nLenSeries as numeric
    protected data nLenSequence as numeric
    protected data lOriginIsInvoice as logical //Título oriundo de nota fiscal
    protected data jSFTData as json
    protected data oExecNatureOfIncomeFromFKW as object
    protected data lHasGrossValueField as logical
endClass

//-------------------------------------------------------------------
/*/{Protheus.doc} new
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method new() as object class PayableBillsData
    _Super:new()
    self:clear()

    self:lPCCAtWriteOff := SuperGetMv("MV_BX10925", .T., "2") == "1"
    self:lIssAtWriteOff := SuperGetMv("MV_MRETISS", .T., "1") == "2"
    self:lIrAtWriteOff := .F.
    self:nLenInstallment := TamSx3( "E2_PARCELA" )[1]
    self:nLenSeries := TamSx3( "FT_SERIE" )[1]
    self:nLenSequence := TamSx3( "FK2_SEQ" )[1]
    self:aIgnoreTypes := self:getIgnoreTypes()
    self:aCodeRentOrRoyalties := self:getCodeRentOrRoyalties()
    self:lHasGrossValueField := GetRPORelease() > '12.1.2410' .or. ( FKW->(FieldPos("FKW_VLBRUT") > 0) .and. FKY->(FieldPos("FKY_VLBRUT") > 0) )
return self

//-------------------------------------------------------------------
/*/{Protheus.doc} clear
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method clear() class PayableBillsData
    _Super:clear()
    self:setPageSize(100)
    self:setIsCaseSensitive(.T.)
    self:setUseSpaces(.T.)
    self:lOriginIsInvoice := .F.
return

//-------------------------------------------------------------------
/*/{Protheus.doc} setParameters
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setParameters(jParameters as json) class PayableBillsData
    self:jParameters := jParameters
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getOrder
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getOrder() class PayableBillsData    
return SqlOrder(SE2->(IndexKey( 1 )))

//-------------------------------------------------------------------
/*/{Protheus.doc} loadMapFields
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method loadMapFields() class PayableBillsData
    self:addMapFields('branch', 'E2_FILORIG', .T.)
    self:addMapFields('E2_FILIAL', 'E2_FILIAL', .F.)
    self:addMapFields('billPrefix', 'E2_PREFIXO', .T.)
    self:addMapFields('E2_NUM', 'E2_NUM', .F.)
    self:addMapFields('billInstallment', 'E2_PARCELA', .T.)
    self:addMapFields('E2_TIPO', 'E2_TIPO', .F.)
    self:addMapFields('E2_FORNECE', 'E2_FORNECE', .F.)
    self:addMapFields('E2_LOJA', 'E2_LOJA', .F.)

    self:addMapFields('E2_ORIGEM', 'E2_ORIGEM', .F.)
    self:addMapFields('E2_EMIS1', "E2_EMIS1", .F.)
    self:addMapFields('E2_EMISSAO', "E2_EMISSAO", .F.)
    self:addMapFields('E2_VLCRUZ', 'E2_VLCRUZ', .F.)
    self:addMapFields('E2_INSS', 'E2_INSS', .F.)
    self:addMapFields('E2_SEST', 'E2_SEST', .F.)
    self:addMapFields('E2_PIS', 'E2_PIS', .F.)
    self:addMapFields('E2_COFINS', 'E2_COFINS', .F.)
    self:addMapFields('E2_CSLL', 'E2_CSLL', .F.)
    self:addMapFields('E2_ISS', 'E2_ISS', .F.)
    self:addMapFields('E2_IRRF', 'E2_IRRF', .F.)
    
    self:addMapFields('FK7_IDDOC', 'FK7_IDDOC', .F.)

    self:addMapFields('A2_CALCIRF', 'A2_CALCIRF', .F.)
    self:addMapFields('SE2RECNO', 'SE2RECNO', .F., .F., { 'SE2RECNO', 'N', 14, 0 })
    self:addMapFields('CPF', 'CPF', .F., .F., { 'CPF', 'C', TamSX3('FKJ_CPF')[1], 0 })

    self:addMapFields('FKF_REINF', 'FKF_REINF ', .F.)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQuery() as character class PayableBillsData
    local cQuery := "" as character
    local cDateField := "" as character
    local nParam := 1 as numeric
            
    if self:jParameters["typeDatePayable"] == '1'
        cDateField := "E2_EMIS1"
    else
        cDateField := "E2_EMISSAO"
    endIf

    if self:oMainExec == NIL

        cQuery := " SELECT ? FROM "
        cQuery += " ( SELECT ? "
        cQuery += " , COALESCE(FKJ_CPF, '') CPF, SE2.R_E_C_N_O_ SE2RECNO "
        cQuery += " FROM ? SE2 "
        
        cQuery += " JOIN ? FK7  "
        cQuery += " ON FK7.FK7_FILTIT = SE2.E2_FILIAL "
        cQuery += " AND FK7.FK7_PREFIX = SE2.E2_PREFIXO "
        cQuery += " AND FK7.FK7_NUM = SE2.E2_NUM "
        cQuery += " AND FK7.FK7_PARCEL = SE2.E2_PARCELA "
        cQuery += " AND FK7.FK7_TIPO = SE2.E2_TIPO "
        cQuery += " AND FK7.FK7_CLIFOR = SE2.E2_FORNECE "
        cQuery += " AND FK7.FK7_LOJA = SE2.E2_LOJA "
        cQuery += " AND FK7.FK7_ALIAS = 'SE2' "
        cQuery += " AND FK7.D_E_L_E_T_ = ' ' "
        
        cQuery += " JOIN ? FKF "
        cQuery += " ON FK7.FK7_IDDOC = FKF.FKF_IDDOC "
        cQuery += " AND FKF.D_E_L_E_T_ = ' ' "
        
        cQuery += " JOIN ? SA2 "
        cQuery += " ON ? "
        cQuery += " AND SE2.E2_FORNECE = SA2.A2_COD "
        cQuery += " AND SE2.E2_LOJA = SA2.A2_LOJA "
        cQuery += " AND SA2.D_E_L_E_T_ = ' ' "

        cQuery += " LEFT JOIN ? FKJ "
        cQuery += " ON SA2.A2_COD = FKJ_COD "
        cQuery += " AND SA2.A2_LOJA = FKJ.FKJ_LOJA "
        cQuery += " AND ? "
        cQuery += " AND SE2.E2_CODRET IN ( ? ) "
        cQuery += " AND FKJ.D_E_L_E_T_ = ' ' "
        
        cQuery += " WHERE SE2.E2_FILORIG = ?  "
        cQuery += " AND ? BETWEEN ? AND ? "
        cQuery += " AND SE2.E2_TIPO NOT IN ( ? ) "
        cQuery += " AND SE2.D_E_L_E_T_ = ' ' "
        cQuery += " ? ) TEMPTABLE "

        cQuery := changeQuery(cQuery)

        self:oMainExec := FWExecStatement():new(cQuery)

    endIf

    self:oMainExec:setUnsafe(nParam++, "#QueryFields#")
    self:oMainExec:setUnsafe(nParam++, self:getMapFieldsToString({"CPF", "SE2RECNO"}))
    self:oMainExec:setUnsafe(nParam++, retSqlName("SE2"))
    self:oMainExec:setUnsafe(nParam++, retSqlName("FK7"))
    self:oMainExec:setUnsafe(nParam++, retSqlName("FKF"))
    self:oMainExec:setUnsafe(nParam++, retSqlName("SA2"))
    self:oMainExec:setUnsafe(nParam++, FWJoinFilial("SE2", "SA2"))
    self:oMainExec:setUnsafe(nParam++, retSqlName("FKJ"))
    self:oMainExec:setUnsafe(nParam++, FWJoinFilial("SA2", "FKJ"))
    self:oMainExec:setIn(nParam++, self:aCodeRentOrRoyalties)
    self:oMainExec:setString(nParam++, self:jParameters['branch'])
    self:oMainExec:setUnsafe(nParam++, cDateField)
    self:oMainExec:setDate(nParam++, self:jParameters['initialDate'])
    self:oMainExec:setDate(nParam++, self:jParameters['finalDate'])
    self:oMainExec:setIn(nParam++, self:aIgnoreTypes)
    self:oMainExec:setUnsafe(nParam++, "#QueryWhere#")

    cQuery := self:oMainExec:getFixQuery()
return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getNatureOfIncomeFromFKW
    @description Obs.: Esse método é mais complexo na classe WriteOffData.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcNatureOfIncome() as array class PayableBillsData

return self:getNatureOfIncomeFromFKW()

//-------------------------------------------------------------------
/*/{Protheus.doc} getNatureOfIncomeFromFKW
    @description Retorna a lista de naturezas de rendimento geradas na tabela FKW.
    Não deve trazer natureza de rendimento sem impostos, exceto quando for uma natureza
    de rendimento do grupo 20 ou PA. Para mais detalhes, veja o método isValidNatureOfIncomeFKW().

    Tributes
    Iniciamos o desenvolvimento do preenchimento do objeto "tributes", que relaciona todos os 
    tributos da FKW e da FKY, mas esbarramos em alguns complicadores e decidimos liberar no futuro.
    Por exemplo, em alguns casos, devemos agrupar determinados tipos de impostos (ver função AgregTrib).

    Tributes - taxRate
    Na concepção do projeto foi mapeada uma propriedade chamada taxRate para o objeto tributo.
    A ideia seria preencher com a alíquota do tributo, para alimentar um campo do TAF. Porém, 
    esse campo não é obrigatório no layout do REINF e o extrator atual (FINA989) não envia esse campo.
    Na verdade, sequer existe um campo referente a essa informação na FKW ou na FKW. Por isso não
    enviamos essa propriedade nesse componente.

    O campo FKW_VLBRUT é recente, então não podemos considerá-lo se não existir ou for igual a zero.
    O registro deve possuir FKW_BASETR ou FKW_BASENR maior que zero. Se ambos estiverem zerados, devemos
    desconsiderar o registro (provavelmente a existência desse registro é consequência de algum erro do passado).

	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
    @parameters nRatio, numeric, Razão do valor da baixa sobre o título: Quando o PCC está 
    na emissão, ele fica gravado na FKW. Mas, segundo a legislação atual, o PCC é enviado 
    no T158 com as baixas. Então é necessário calcular o valor do imposto proporcional a
    cada baixa. Essa razão/proporção é enviada no parâmetro nRatio.
/*/
//-------------------------------------------------------------------
method getNatureOfIncomeFromFKW(nRatio as numeric) as array class PayableBillsData
    local jNatureOfIncome as json
    local jTribute as json
    local aList := {} as array

    local cQuery := "" as character
    local cFields := "" as character
    local nParam := 1 as numeric
    local cAlias := "" as character
    local cLifeTime := "600" as character
    local cTimeOut := "120" as character
    local nNaturePosition := 0 as numeric
    local nGrossValue := 0 as numeric

    default nRatio := 1

    cFields := " FKW_IDFKW, FKW_NATREN, FKW_TPIMP, FKW_BASETR, FKW_BASENR, FKW_VLIMP, FK7_TIPO "
    if self:lHasGrossValueField
        cFields += ", FKW_VLBRUT "
    endIf

    if self:oExecNatureOfIncomeFromFKW == NIL
           
        cQuery := " SELECT ? "
        cQuery += " FROM ? FKW "
        cQuery += " JOIN ? FK7 ON FKW_IDDOC = FK7_IDDOC "
        cQuery += " WHERE FKW.D_E_L_E_T_ = ? "
        cQuery += " AND FKW_FILIAL = ? "
        cQuery += " AND FKW_IDDOC = ? "
        cQuery += " AND (FKW_TPIMP <> ? OR SUBSTRING(FKW_NATREN,1,2) = ? OR FK7_TIPO = ? ) "
        cQuery += " AND (FKW_BASETR <> ? OR FKW_BASENR <> ?) "
        cQuery += " ? "
        cQuery += " ? "

        cQuery := changeQuery(cQuery)

        self:oExecNatureOfIncomeFromFKW := FWExecStatement():new(cQuery)

    endIf

    self:oExecNatureOfIncomeFromFKW:setUnsafe(nParam++, cFields)
    self:oExecNatureOfIncomeFromFKW:setUnsafe(nParam++, retSqlName("FKW"))
    self:oExecNatureOfIncomeFromFKW:setUnsafe(nParam++, retSqlName("FK7"))
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, '')
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, xFilial("FKW", self:jParameters['branch']))
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, (self:cAlias)->FK7_IDDOC)
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, 'SEMIMP')
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, '20')
    self:oExecNatureOfIncomeFromFKW:setString(nParam++, MVPAGANT)
    self:oExecNatureOfIncomeFromFKW:setNumeric(nParam++, 0)
    self:oExecNatureOfIncomeFromFKW:setNumeric(nParam++, 0)
    self:oExecNatureOfIncomeFromFKW:setUnsafe(nParam++, self:getFilterFKW())
    self:oExecNatureOfIncomeFromFKW:setUnsafe(nParam++, self:getFKWIndividualIncomeTaxAlocationFilter())

    cAlias := self:oExecNatureOfIncomeFromFKW:openAlias(nil, cLifeTime, cTimeOut)

    while !(cAlias)->(eof())
        if self:lHasGrossValueField .and. (cAlias)->FKW_VLBRUT > 0
            nGrossValue := (cAlias)->FKW_VLBRUT * nRatio
        else
            nGrossValue := ((cAlias)->FKW_BASETR + (cAlias)->FKW_BASENR) * nRatio
        endIf

        if self:isValidNatureOfIncomeFKW(cAlias)
            jTribute := JsonObject():new()
            jTribute['id'] := (cAlias)->FKW_IDFKW
            jTribute['taxCode'] := (cAlias)->FKW_TPIMP
            jTribute['baseValue'] := (cAlias)->FKW_BASETR * nRatio
            jTribute['nonWithheldBaseValue'] := (cAlias)->FKW_BASENR * nRatio
            jTribute['taxValue'] := (cAlias)->FKW_VLIMP * nRatio

            nNaturePosition := aScan(aList, {|jNatureOfIncome| jNatureOfIncome['code'] == (cAlias)->FKW_NATREN} )
            if nNaturePosition == 0
                jNatureOfIncome := JsonObject():new()
                jNatureOfIncome['code'] := (cAlias)->FKW_NATREN
                jNatureOfIncome['grossValue'] := nGrossValue
                // jNatureOfIncome['tributes'] := { jTribute }
                aAdd(aList, jNatureOfIncome)
            else
                // aAdd(jNatureOfIncome['tributes'], jTribute)
            endIf
        endIf
        
        (cAlias)->(dbSkip())
    endDo

    (cAlias)->(dbCloseArea())

return aList

//-------------------------------------------------------------------
/*/{Protheus.doc} getFKWIndividualIncomeTaxAlocationFilter
    Quando o título posicionado possui retenção de IR com rateio por CPF, 
    retorna uma condição a mais para a query da FKW considerar apenas as retenções 
    para o CPF posicionado. Como se trata de uma exceção e a inclusão de um filtro simples,
    entendo que não é necessário criar um objeto statement exclusivo para esse cenário, 
    diferente do que foi feito no PayableWriteoff, porque lá a query muda bastante, passando
    a olhar FK3 e FK4.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getFKWIndividualIncomeTaxAlocationFilter() as character class PayableBillsData
    local cFilter := "" as character
    if self:hasIndividualIncomeTaxAlocation()
        cFilter := " AND FKW_CGC = '"+ (self:cAlias)->CPF +"' "
    endIf
return cFilter

//-------------------------------------------------------------------
/*/{Protheus.doc} setCalcData
    Método herdado da classe totvs.protheus.backoffice.fin.abstract.DefaultGetModel
    Define o conteúdo dos campos calculados para cada registro retornado na query principal do
    FwAdapterBaseV2.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method setCalcData() class PayableBillsData
    local aServices := {} as array

    self:lIrAtWriteOff := (self:cAlias)->A2_CALCIRF == "2"

    self:jSFTData := self:getSFTData()

    self:jCalcData['billOriginType'] := self:getCalcBillOriginType()
    self:lOriginIsInvoice := self:jCalcData['billOriginType'] == '1'
    self:jCalcData['billNumber'] := self:getCalcBillNumber()
    self:jCalcData['billDate'] := dToC(self:getCalcBillDate())
    self:jCalcData['participatingCode'] := self:getCalcParticipatingCode()
    self:jCalcData['operationType'] := '0' //Carteira: 0=A Pagar; 1=A Receber //Pareado com a NF
    
    if self:lOriginIsInvoice
        self:jCalcData['modelIdentificationCode'] := self:jSFTData['cModelIdentificationCode']
        self:jCalcData['documentNumber'] := self:jSFTData['cInvoiceNumber']
        self:jCalcData['documentSeries'] := self:jSFTData['cInvoiceSeries']
        self:jCalcData['documentValue'] := self:jSFTData['nInvoiceValue']
    else 
        self:jCalcData['modelIdentificationCode'] := ''
        self:jCalcData['documentNumber'] := ''
        self:jCalcData['documentSeries'] := ''
        self:jCalcData['documentValue'] := self:getCalcBillValue()        
    endIf
    
    self:jCalcData['natureOfIncome'] := self:getCalcNatureOfIncome()
    self:jCalcData['services'] := aServices
    self:jCalcData['finSentTaf'] := (self:cAlias)->FKF_REINF
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalcBillOriginType
    @description Verifica se é um título avulso ou se tem origem em uma nota fiscal.
    Quando tem rateio de IR por CPF, deve ser tratado como título avulso, mesmo que tenha nota.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcBillOriginType() as character class PayableBillsData
    local cFromInvoice := "1" as character //Origem = Nota fiscal
    local cSeparatedBill := "3" as character //Origem = Título avulso do financeiro
    local cBillOriginType := cSeparatedBill as character

    if Alltrim((self:cAlias)->E2_ORIGEM) $ "MATA461|MATA460|MATA103|MATA100" ;
        .And. !self:hasIndividualIncomeTaxAlocation() .and. self:jSFTData['lHasSFT']

        cBillOriginType := cFromInvoice
    endIf
return cBillOriginType

//-------------------------------------------------------------------
/*/{Protheus.doc} hasIndividualIncomeTaxAlocation
    Retorna se o fornecedor do título posicionado no self:cAlias está configurado para
    rateio de IR por CPF.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method hasIndividualIncomeTaxAlocation() as logical class PayableBillsData
return !Empty((self:cAlias)->CPF)

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalcBillNumber
    Retorna o número do título no padrão do extrator.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcBillNumber() as character class PayableBillsData
    local cNumber := allTrim((self:cAlias)->E2_NUM) as character
    local cInstallment := padr(Alltrim((self:cAlias)->E2_PARCELA), self:nLenInstallment) as character

    if self:lOriginIsInvoice .or. Empty(cInstallment)
        return cNumber
    endIf

return cNumber + '-' + cInstallment

//-------------------------------------------------------------------
/*/{Protheus.doc} getSFTData
    Retorna os principais dados da nota fiscal de origem, conforme tabela SFT.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getSFTData() as json class PayableBillsData
    local jData := JsonObject():new() as json
    local nInvoiceValue := 0 as numeric    
    local lHasSFT := .F. as logical

    local nPortifolio := 1 as numeric//Pagar
    local cInvoiceNumber := (self:cAlias)->E2_NUM as character
    local cInvoiceSeries := (self:cAlias)->E2_PREFIXO as character
    local cCustomerOrSupplier := (self:cAlias)->E2_FORNECE as character
    local cUnit := (self:cAlias)->E2_LOJA as character

    lHasSFT := FTemSFT(nPortifolio, cInvoiceNumber, @cInvoiceSeries, cCustomerOrSupplier, cUnit, self:nLenSeries) //Também posiciona a tabela SFT e troca Prefixo por F1_SERIE se precisar
    cModelIdentificationCode := AModNot(SFT->FT_ESPECIE)

    While SFT->(!Eof()) .and. SFT->(FT_FILIAL+FT_TIPOMOV+FT_SERIE+FT_NFISCAL+FT_CLIEFOR+FT_LOJA) ==  (xFilial("SFT",self:jParameters['branch']) + "E" +  cInvoiceSeries + cInvoiceNumber + cCustomerOrSupplier + cUnit)
        nInvoiceValue += SFT->FT_VALCONT
        SFT->(DBSkip())
    EndDo

    jData['lHasSFT'] := lHasSFT
    jData['nInvoiceValue'] := nInvoiceValue
    jData['cInvoiceNumber'] := cInvoiceNumber
    jData['cInvoiceSeries'] := cInvoiceSeries
    jData['cModelIdentificationCode'] := cModelIdentificationCode
return jData

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalcParticipatingCode
    Retorna o código do participante (cliente ou fornecedor) no padrão do extrator.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcParticipatingCode() as character class PayableBillsData
    local cType := "F" as character //F = Fornecedor, C = Cliente    
    local cParticipatingCode := "" as character
    if self:hasIndividualIncomeTaxAlocation()
        cParticipatingCode := cType + AllTrim((self:cAlias)->CPF)
    else
        cParticipatingCode := cType + (self:cAlias)->E2_FORNECE + (self:cAlias)->E2_LOJA
    endIf
return cParticipatingCode

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalcBillValue
    Calcula o valor bruto do título, recompondo com os impostos criados na emissão.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcBillValue() as numeric class PayableBillsData
    local nValue := 0 as numeric

    nValue := (self:cAlias)->E2_VLCRUZ + (self:cAlias)->E2_INSS + (self:cAlias)->E2_SEST

    If !self:lPCCAtWriteOff
        nValue += (self:cAlias)->E2_PIS + (self:cAlias)->E2_COFINS + (self:cAlias)->E2_CSLL
    EndIf

    If !self:lIssAtWriteOff
        nValue += (self:cAlias)->E2_ISS
    EndIf

    If !self:lIrAtWriteOff
        nValue += (self:cAlias)->E2_IRRF
    EndIf
return nValue

//-------------------------------------------------------------------
/*/{Protheus.doc} getCalcBillDate
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCalcBillDate() as date class PayableBillsData
    local dBillDate := cToD("  /  /    ") as date

    if !self:lEmpty        
        if self:jParameters["typeDatePayable"] == '1'
            dBillDate := (self:cAlias)->E2_EMIS1
        else
            dBillDate := (self:cAlias)->E2_EMISSAO
        endIf
    endIf
return dBillDate

//-------------------------------------------------------------------
/*/{Protheus.doc} getFilterFKW
    @description Define um filtro padrão para a consulta na tabela FKW.
    No caso da busca de títulos, não deve trazer PCC, porque para o REINF
    o fato gerador do PCC é sempre a baixa.
    No caso da busca de baixas, deve buscar as retenções de PCC na FKW quando
    o parâmetro MV_BX10925 estiver na emissão, por isso a classe
    PayableWriteOff vai sobrepor esse método com o filtro oposto, isto é, vai buscar na FKW
    exclusivamente os impostos PIS, COFINS e CSLL quando estiverem sendo gerados pelo Protheus na emissão.
    Excelto PA, porque no PA a busca de baixas deve retronar tudo o que foi gerado na FKW, independetemente do 
    TPIMP.
	@author guilherme.sordi@totvs.com.br
	@since 22/03/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getFilterFKW() as character class PayableBillsData
return " AND FKW_TPIMP NOT IN ('PIS', 'COF', 'CSL') "

//-------------------------------------------------------------------
/*/{Protheus.doc} isValidNatureOfIncomeFKW
    @description Quando não tem imposto (TPIMP = 'SEMIMP'), a natureza de rendimento será
    informada apenas no T158, a partir da FKY. 
    Exceções à regra:
    1) Naturezas de rendimento do grupo 20 (ex.: publicidade). Nesse caso,
    ela será informada no T154 ou no T158 dependendo de como estiver a configuração do fornecedor. 
    Quando configurado com IR na emissão, a natureza de rendimento será informada no T154 e quando configurado
    com IR na baixa será informado no T158.
    2) Pagamento antecipado. Nesse caso, o T158 apresentará a natureza de rendimento a partir da FKW
    e não da FKY, porque PA só gera FKW.

    Esse método deve se preocupar apenas com cenários sem imposto e IRRF, porque o PCC já é tratado
    na query. Veja o método getFilterFKW() tanto no PayableBillsData quanto no WriteOffBillsData.

    Portanto, os cenários em que devemos apresentar as naturezas de rendimento da FKW, desconsiderando PCC, são:
    1) TPIMP = sem imposto, natureza do grupo 20 e fornecedor com IR na emissão
    2) TPIMP = IRRF, independente da natureza e fornecedor com IR na emissão
    3) Pagamento antecipado, independente do TPIMP, independente na natureza e independente do cadastro do fornecedor

	@author guilherme.sordi@totvs.com.br
	@since 18/04/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method isValidNatureOfIncomeFKW(cAliasNatureOfIncome as character) as logical class PayableBillsData
    local lIsValid := .T. as logical
    local cCode := (cAliasNatureOfIncome)->FKW_NATREN as character
    local lGroup20 := Substr(cCode,1,2) == "20" as logical
    local lAdvancePayment := (cAliasNatureOfIncome)->FK7_TIPO == MVPAGANT as logical
    local lNoTax := (cAliasNatureOfIncome)->FKW_TPIMP == 'SEMIMP' as logical

    lIsValid := (lNoTax .and. lGroup20 .and. !self:lIrAtWriteOff) ;
        .or. (!lNoTax .and. !self:lIrAtWriteOff) ;
        .or. lAdvancePayment

return lIsValid

//-------------------------------------------------------------------
/*/{Protheus.doc} getIgnoreTypes
    @description Retorna array de E2_TIPO que devem ser desprezados na consulta
    de títulos.
	@author guilherme.sordi@totvs.com.br
	@since 24/04/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getIgnoreTypes() as array class PayableBillsData
    local aIgnore := {} as array
    local cIgnore := MV_CPNEG + "|" + MVPAGANT
    aIgnore := strTokArr(cIgnore, "|")
return aIgnore

//-------------------------------------------------------------------
/*/{Protheus.doc} getCodeRentOrRoyalties
    @description Retorna array de código de retenção relacionado a
    aluguel ou royalties.
	@author guilherme.sordi@totvs.com.br
	@since 06/05/2024
	@type method
	@version 12.1.2310
/*/
//-------------------------------------------------------------------
method getCodeRentOrRoyalties() as array class PayableBillsData
    local aCodes := {} as array
    local cCode := superGetMv("MV_RETIRRT",.F.,"3208")
    local nCodeLength := tamSx3("E2_CODRET")[1]

    aCodes := strTokArr(cCode, ";")
    aEval(aCodes, {|x| x := padR(x, nCodeLength)})
return aCodes
