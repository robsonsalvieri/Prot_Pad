/*/


Ŀ
Funo     FINA030   Autor  Wagner Xavier          Data  22/05/92 
Ĵ
Descrio  Programa de atualizao de Saldos Bancrios                
Ĵ
 Uso       Generico                                                   
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL.          	  
Ĵ
Programador Data    BOPS   Motivo da Alteracao                  	  
Ĵ
 Julio W.  22.11.99META   Reviso do fonte para Protheus            
                                                                    
ٱ


/*/

#INCLUDE "FINA030.CH"
#INCLUDE "PROTHEUS.CH"

FUNCTION FINA030(nPosArotina, lAutomato)

Local lPanelFin := IsPanelFin()
Local lFinBc    := ExistBlock("F030BROWSE") // Ponto de Entrada 
Private aRotina := MenuDef()  

//Ŀ
//                    MEMORIA DE CALCULOS                       
//Quando for necessario transmitir mais de um campo da consulta 
//via F3 para um browse, e' preciso definir um array privado    
//que contem os elementos de Banco/Agencia/Conta do Browse.     
//

PRIVATE aRetBan:= {"E8_BANCO","E8_AGENCIA","E8_CONTA"}  // Campos de Destino

//Ŀ
// Define o cabecalho da tela de atualizacoes                   
//
PRIVATE cCadastro := OemToAnsi(STR0004) // "Atualizao de Saldos Bancrios"  

//Ŀ
// Ponto de entrada para ser utilizado antes do Browse          
//    


	If lFinBc
		ExecBlock("F030BROWSE",.F.,.F.)
	EndIf


//Ŀ
// Endereca a funcao de BROWSE                                  
//

DEFAULT nPosArotina := 0
DEFAULT lAutomato	:= .F.

If nPosArotina > 0 // Sera executada uma opcao diretamento de aRotina, sem passar pela mBrowse
	dbSelectArea("SE8")
	bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
	Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina,lAutomato)
Else
	mBrowse( 6, 1,22,75,"SE8")
Endif

RETURN

/*/

Ŀ
Funo    FA030Delet Autor  Wagner Xavier          Data  22/05/92 
Ĵ
Descrio  Programa de exclusao de Saldos Bancarios                   
Ĵ
Sintaxe    FA030Delet(ExpC1,ExpN1,ExpN2)                              
Ĵ
Parametros ExpC1 = Alias do arquivo                                   
           ExpN1 = Numero do registro                                 
           ExpN2 = Numero da opcao selecionada                        
Ĵ
 Uso       FINA030                                                    
ٱ


/*/
FUNCTION FA030DELET(cAlias,nReg,nOpc,lAutomato)

Local lPanelFin := IsPanelFin()
Local nOpcA
Local oDlg                                                       
LOCAL i,bCampo                                                   
Local lDtMovFin := .T.
Local dDataBase := Date() 
Local dDataFin  := SuperGetMV("MV_DATAFIN")
                                                            
//Ŀ
// Monta a entrada de dados do arquivo                          
//
Private aTELA[0][0],aGETS[0]

//Ŀ
// Verifica se o arquivo est realmente vazio ou se       
// est posicionado em outra filial.                      
//
If EOF() .or. SE8->E8_FILIAL # xFilial("SE8")
   HELP(" " , 1 , "ARQVAZIO")
   Return Nil
Endif 

//Ŀ
// Verifica se a excluso podera ser realizada            
// considerando o parametro MV_DATAFIN.                   
//
If dDataBase < dDataFin
	Help(" ",1,"DATAFIN",,STR0009+" "+; //"Nao  permitido excluso de saldos bancarios"                                                                                                                                                                                                                                                                                                                                                                                                                                                                      
						  STR0010,1,0) //"de data anterior a data contida no parametro MV_DATAFIN"
	Return Nil
Endif

If !CtbValiDt(,dDataBase,,,,{"FIN001","FIN002"},)
	Return Nil	
Endif

 //
 // Ponto de Entrada 
 //
 If ExistBlock( "FA030DEL" )

    lDtMovFin := Execblock( "FA030DEL", .f., .f. )

    If !lDtMovFin
       Return
    EndIf

 EndIf


While .T.
    nOpcA := 1
    dbSelectArea( cAlias )
	If ! SoftLock( cAlias )
		Return
	EndIf

	If !lAutomato

		//Ŀ
		// Envia para processamento dos Gets          
		//
		dbSelectArea( cAlias )
		bCampo := {|nCPO| Field(nCPO) }
		FOR i := 1 TO FCount()
			M->&(EVAL(bCampo,i)) := FieldGet(i)
		NEXT i
		nOpcA := 1
		
		oSize := FwDefSize():New(.T.)

		oSize:lLateral := .F.
		oSize:lProp	:= .T. // Proporcional

		oSize:AddObject( "1STROW" ,  100, 100, .T., .T. ) // Totalmente dimensionavel
		
		oSize:Process() // Dispara os calculos
		
		nTop    := oSize:aWindSize[1]
		nLeft   := oSize:aWindSize[2]
		nBottom := oSize:aWindSize[3]
		nRight  := oSize:aWindSize[4]

		If lPanelFin  //Chamado pelo Painel Financeiro
			oPanelDados := FinWindow:GetVisPanel()
			oPanelDados:FreeChildren()
			aDim := DLGinPANEL(oPanelDados)
			DEFINE MSDIALOG oDlg OF oPanelDados:oWnd FROM 0,0 To 0,0 PIXEL STYLE nOR( WS_VISIBLE, WS_POPUP )
			
			a1stRow := {,,(oDlg:nClientHeight - 4)/2,}  // ocupa todo o  espao da janela
			
			oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,, a1stRow,,,,,,,,,.t.,,,,,,,,)
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT								
		Else		
			DEFINE MSDIALOG oDlg TITLE cCadastro From nTop, nLeft to nBottom, nRight OF oMainWnd PIXEL
			oDlg:lMaximized := .T.
			
			a1stRow := {	oSize:GetDimension("1STROW","LININI"),;
							oSize:GetDimension("1STROW","COLINI"),;
							oSize:GetDimension("1STROW","LINEND"),;
							oSize:GetDimension("1STROW","COLEND")}
			
			oEnc01:= MsMGet():New( cAlias, nReg, nOpc,,,,, a1stRow)
			oEnc01:oBox:align := CONTROL_ALIGN_ALLCLIENT
		Endif
		
		If lPanelFin  //Chamado pelo Painel Financeiro					
			// define dimeno da dialog
			oDlg:nWidth := aDim[4]-aDim[2]

			ACTIVATE MSDIALOG oDlg ON INIT ( FaMyBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()}),	oDlg:Move(aDim[1],aDim[2],aDim[4]-aDim[2], aDim[3]-aDim[1]))
		Else
			ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 2,oDlg:End()},{|| nOpca := 1,oDlg:End()})
		Endif
	Endif
	
	If nOpcA == 2 .or. lAutomato
		//Ŀ
		// Inicio da protecao via TTS                 
		//
		BEGIN TRANSACTION
			dbSelectArea(cAlias)
			RecLock(cAlias,.F.,.T.)
            dbDelete()
			//Ŀ
			// Final  da protecao via TTS                 
			//
		END TRANSACTION
	Else
		MsUnlock()
	Endif
    Exit
EndDo
dbSelectArea( cAlias )
If lPanelFin  //Chamado pelo Painel Financeiro				
	dbSelectArea(FinWindow:cAliasFile)					
	FinVisual(FinWindow:cAliasFile,FinWindow,(FinWindow:cAliasFile)->(Recno()),.T.)
Endif
Return .t.

/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 17/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
Local aRotina := { {OemToAnsi(STR0001) ,"AxPesqui"  , 0 , 1,,.F. },; // "Pesquisar"
                      {OemToAnsi(STR0002) ,"AxVisual"  , 0 , 2 },; // "Visualizar"
                      {OemToAnsi(STR0003) ,"FA030Delet", 0 , 5 } } // "Excluir"  
Return(aRotina)

/*/

Ŀ
Funo    FinA030T    Autor  Marcelo Celi Marques  Data  04.04.08 
Ĵ
Descrio  Chamada semi-automatica utilizado pelo gestor financeiro   
Ĵ
 Uso       FINA700 FINA702                                            
ٱ


/*/
Function FinA030T(aParam)	
	cRotinaExec := "FINA030"
	ReCreateBrow("SE8",FinWindow)      		
	FinA030(aParam[1])
	ReCreateBrow("SE8",FinWindow)      	
	dbSelectArea("SE8")
	
	INCLUI := .F.
	ALTERA := .F.
	
Return .T.	
