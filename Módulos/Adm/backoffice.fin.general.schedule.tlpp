#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#include 'fwlibversion.ch'
#include 'backoffice.fin.general.schedule.ch'

namespace totvs.protheus.backoffice.fin.general

//-------------------------------------------------------------------
/*/{Protheus.doc} Schedule
    Classe de utilitários para manipulação de tarefas e agendamentos do Schedule.

    @author Fábio Henrique Andrade
    @since 04/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
class Schedule
	public method new() as object
	static method createTask() as character
    static method isReleaseOK() as logical 
endClass


//-------------------------------------------------------------------
/*{Protheus.doc} new
    Construtor
    @return object: self
    @author Fábio Henrique Andrade
    @since 04/07/2023
    @version 12.1.2210
*/
//-------------------------------------------------------------------
method new() as object class Schedule
return self


/*/{Protheus.doc} createTask()
    Encapsula a criação de task executando uma pergunta se deve ou não executar em segundo plano.
    @author fabioh.andrade@totvs.com.br
    @since 22/03/2024
    @version 12.1.2310
/*/
method createTask(cRoutine as character, cSX1Group as character, lInterfaceParams as logical, lShowMessages as logical ) as character class Schedule
	local lCanCreateTask       := .F. as logical
    local nModule           := 6 as numeric
    local cUserCode         := RetCodUsr() as character
    local oTask as object
    local lReleaseOK          := .F. as logical 
    local lIsScheduleEnabled  := .F. as logical 
    local cProcessCode        := ""  as character
    
    Default lInterfaceParams  := .T.
    Default lShowMessages     := .T.

    lReleaseOK          := totvs.protheus.backoffice.fin.general.Schedule():isReleaseOK()
    lIsScheduleEnabled  := totvs.framework.smartschedule.startSchedule.smartSchedIsEnabled()
	
    if !IsBlind() .And. lReleaseOK
        if lIsScheduleEnabled

            if lShowMessages
                lCanCreateTask := MSGYESNO( STR0001, STR0002 ) //"Deseja executar em segundo plano?" # "AGENDADOR"
            else 
                lCanCreateTask := .T.
            endIf

            if lCanCreateTask
                oTask := totvs.framework.schedule.utils.createTask( /*cEnvironment*/,; 
                                                        /*cEmpAnt*/,; 
                                                        /*cFilAnt*/,; 
                                                        cRoutine,;
                                                        nModule,; 
                                                        cUserCode,; 
                                                        cSX1Group,; 
                                                        /*aMVParams*/,; 
                                                        /*lReuse*/,; 
                                                        lInterfaceParams )
                if !Empty(oTask)
                    if lShowMessages
                        MSGINFO( STR0003, STR0004 ) //"A tarefa foi cadastrada com sucesso. Você consegue acompanhar a evolução através da central de notificações." # "EXECUÇÃO EM SEGUNDO PLANO"
                    endIf
                    cProcessCode := oTask:cID
                endIf
                
            endIf
        endIf
    endIf
	
return cProcessCode

/*/{Protheus.doc} isReleaseOK()
    Valida se o ambiente está na release mínima para execução do smart schedule.
    @author fabioh.andrade@totvs.com.br
    @since 20/06/2024
    @version 12.1.2410
/*/
method isReleaseOK() as logical class Schedule
    local cMinimumRelease  := "12.1.2410" as character
return ( GetRpoRelease() >= cMinimumRelease )
