#INCLUDE "PCOC350.ch"
#include "protheus.ch"
#include "msgraphi.ch"

/*/
_F_U_N_C_
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑฺฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤยฤฤฤฤฤฤยฤฤฤฤฤฤฤฤฤฤฤฤฟฑฑ
ฑฑณFUNCAO    ณ PCOC350  ณ AUTOR ณ Edson Maricate        ณ DATA ณ 26.11.2003 ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณDESCRICAO ณ Programa de Consulta ao arquivo de saldos mensais dos Cubos  ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ USO      ณ SIGAPCO                                                      ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ_DOCUMEN_ ณ PCOC350                                                      ณฑฑ
ฑฑณ_DESCRI_  ณ Programa de Consulta ao arquivo de saldos mensair dos Cubos  ณฑฑ
ฑฑณ_FUNC_    ณ Esta funcao podera ser utilizada com a sua chamada normal    ณฑฑ
ฑฑณ          ณ partir do Menu ou a partir de uma funcao pulando assim o     ณฑฑ
ฑฑณ          ณ browse principal e executando a chamada direta da rotina     ณฑฑ
ฑฑณ          ณ selecionada.                                                 ณฑฑ
ฑฑณ          ณ Exemplo: PCOC350(2) - Executa a chamada da funcao de visua-  ณฑฑ
ฑฑณ          ณ                       zacao da rotina.                       ณฑฑ
ฑฑรฤฤฤฤฤฤฤฤฤฤลฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤดฑฑ
ฑฑณ_PARAMETR_ณ ExpN1 : Chamada direta sem passar pela mBrowse               ณฑฑ
ฑฑภฤฤฤฤฤฤฤฤฤฤมฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤูฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PCOC350(nCallOpcx)

Local bBlock
Local nPos
Private cCadastro	:= STR0001 //"Consulta Saldos na Data - Visoes"
Private aRotina := MenuDef()

	If nCallOpcx <> Nil
		nPos := Ascan(aRotina,{|x| x[4]== nCallOpcx})
		If ( nPos # 0 )
			bBlock := &( "{ |x,y,z,k,w,a,b,c,d,e,f,g| " + aRotina[ nPos,2 ] + "(x,y,z,k,w,a,b,c,d,e,f,g) }" )
			Eval( bBlock,Alias(),AKN->(Recno()),nPos)
		EndIf
	Else
		If SuperGetMV("MV_PCOCNIV",.F., .F.)
			PCOC351(nCallOpcx)
		Else
			mBrowse(6,1,22,75,"AKN")
		EndIf
	EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPco350ViewบAutor  ณEdson Maricate      บ Data ณ  24/05/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณfuncao que solicita parametros para utilizacao na montagem  บฑฑ
ฑฑบ          ณda grade e grafico ref. saldo gerencial do pco              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function Pco350View(cAlias,nRecno,nOpcx)
Local aProcessa
Local nDetalhe
Local aCfgAuxCube := {}
Local aAuxCube := {} 
Local lContinua := .T.
Local dData	 := dDataBase
Local nDirAcesso 	:= 0
Local aListArq := {}
Local aVarPriv := {}
Local cPicture := PadR("@E 999,999,999,999.99",25)

Private aConfig := {}

If SuperGetMV("MV_PCO_AKN",.F.,"2")!="1"  //1-Verifica acesso por entidade
	lContinua := .T.                        // 2-Nao verifica o acesso por entidade
Else
	nDirAcesso := PcoDirEnt_User("AKN", AKN->AKN_CODIGO, __cUserID, .F.)
    If nDirAcesso == 0 //0=bloqueado
		Aviso(STR0026,STR0037,{STR0028},2)//"Aten็ใo"###"Usuario sem acesso a esta configura็ใo de visao gerencial. "###"Fechar"
		lContinua := .F.
	Else
	    lContinua := .T. 
	EndIf
EndIf

If lContinua
	If ParamBox({ { 1 ,STR0019,Space(LEN(AL3->AL3_CODIGO))		  ,"@!" 	 ,""  ,"AL3" ,"" ,25 ,.F. },; //"Config Cubo"
						{3,STR0020,1,{STR0021,STR0022},40,,.F.},; //"Exibe Configura็๕es"###"Sim"###"Nao"
						{ 1 ,STR0023,dData,"" 	 ,""  ,""    ,"" ,50 ,.F. },; //"Saldo em"
						{2,STR0038,2,{"1="+STR0021,"2="+STR0022},80,"",.F.},;//"Detalhar Cubos"###"Sim"###"Nao"
						{3,STR0042,2,{STR0043,STR0044,STR0045},40,,.F.},; //"Mostrar valores"##"Unidade"##"Milhar"##"Milhao"
						{1,IIf(cPaisLoc$"RUS",STR0052,"Picture"),cPicture,"@!" 	 ,""  ,"" ,"" ,75 ,.F. };
						},STR0025,aConfig,,,,,,,"PCOC350",,.T.) //"Tipo do Grafico"###"Parametros"
	
		nDetalhe  := If(ValType(aConfig[4])=="N", aConfig[4], Val(aConfig[4]))

		aVarPriv := {}
		aAdd(aVarPriv, {"aConfig", aClone(aConfig)})                
		                                                             
		aProcessa := PcoCubeVis(AKN->AKN_CODIGO,1,"Pcoc350Sld",aConfig[1],aConfig[2], nDetalhe,.T.,aClone(aVarPriv))

		aAdd(aCfgAuxCube, aClone(aAuxCube))
	
		If !Empty(aProcessa)			
			PCOC350PFI(aProcessa,aCfgAuxCube,If(nDetalhe==1,STR0039,STR0040),aConfig[5],,aListArq,aConfig[6])//"Conta Gerencial / Detalhes"###"Conta Gerencial"
		Else
			Aviso(STR0026,STR0027,{STR0028},2) //"Aten็ใo"###"Nใo existem valores a serem visualizados na configura็ใo selecionada. Verifique as configura็๕es da consulta."###"Fechar"
		EndIf
						
	EndIf
EndIf
	
Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPcoC350SldบAutor  ณEdson Maricate      บ Data ณ  24/05/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณfuncao utilizada na rotina de processamento do cubo         บฑฑ
ฑฑบ          ณgerencial do pco                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Function PcoC350Sld(cConfig,cChave)
Local aRetFim
Local nCrdFim := 0
Local nDebFim := 0
Local nSldFim := 0

aRetFim := PcoRetSld(cConfig,cChave,aConfig[3])
nCrdFim := aRetFim[1, 1]
nDebFim := aRetFim[2, 1]

nSldFim := nCrdFim-nDebFim

Return {nSldFim}

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณPCOC350PFIบAutor  ณEdson Maricate      บ Data ณ  24/05/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณfuncao que processa o cubo gerencial do pco e exibe uma     บฑฑ
ฑฑบ          ณgrade com o grafico                                         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP                                                         บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function PCOC350PFI(aProcessa,aCfgAuxCube,cDescri,nCasas, lShowGraph, aListArq, cPicture)

Local oDlg,oPanel,oPanel1,oPanel2
Local oView
Local oGraphic
Local aArea    := GetArea()
Local cAlias
Local nRecView
Local nStep
Local dx
Local nSerie
Local cTexto
Local aSize    := {}
Local aPosObj  := {}
Local aObjects := {}
Local aInfo    := {}
Local aView    := {}
Local nNivCub  := 0
Local nx
Local aButtons := {}
Local bEncerra := {|| If(Aviso(STR0029,STR0030, {STR0021, STR0022},2)==1, ( PcoArqSave(aListArq), oDlg:End() ), NIL) } //"Atencao"###"Deseja abandonar a consulta ?"###"Sim"###"Nao"
Local aTabMail :=	{}
Local cFiltro  := 'DTOS(AKD->AKD_DATA) <= "' +DTOS(aConfig[3])+'"'
Local nDivisor :=	1
Local aTitle     := {cDescri,STR0035,STR0036}
Local lPCOC3502 := ExistBlock("PCOC3502")
Local lPCO350GRF := ExistBlock("PCO350GRF") 
Local lRetPe  := .T.

DEFAULT lShowGraph := .T.
DEFAULT aListArq := {}
DEFAULT cPicture := PadR("@E 999,999,999,999.99",25)

nCasas    := Iif(nCasas==1,0,IIf(nCasas==2,-3,-6))
nDivisor  := 10**(Abs(nCasas))
//cPicture  := If(nCasas==-6,"@E 999,999,999,999.99","@E 999,999,999,999")
cCadastro += IIf(nCasas==0,"" ,IIf(nCasas==-3,STR0046,STR0047))//" - (Valores em milhares)"##" - (Valores em milhoes)"

aButtons := { {"GRAF2D"	, {|| HideShowGraph(oPanel2, oPanel1, @lShowGraph) },STR0048,STR0049 } } //"Exibir/Esconder Grafico"###"Grafico"

aadd(aTabMail, aClone(aTitle) )

For nx := 1 to Len(aProcessa)
	aAdd(aView,{Substr(aProcessa[nx,1],1),aProcessa[nx,6],TransForm(Round(aProcessa[nx,2,1],2)/nDivisor* If(aProcessa[nx, 18] == "1",1,-1),cPicture)})
	aadd(aTabMail,{Substr(aProcessa[nx,1],1),aProcessa[nx,6],Alltrim(Transform(Round(aProcessa[nx,2,1],2)/nDivisor, cPicture) ) } )
Next


If !Empty(aView) 

	If lPCOC3502 
		ExecBlock( "PCOC3502",.F.,.F.,{ aView, aTabMail, aProcessa }) 
	EndIf 
	
	aSize := MsAdvSize(,.F.,400)
	aObjects := {}
	
	AAdd( aObjects, { 100, 100 , .T., .T. } )
	
	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 2, 2 }
	aPosObj := MsObjSize( aInfo, aObjects, .T. )
	
	DEFINE FONT oBold NAME "Arial" SIZE 0, -11 BOLD
	DEFINE FONT oFont NAME "Arial" SIZE 0, -10 
	DEFINE MSDIALOG oDlg TITLE cCadastro  From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
	oDlg:lMaximized := .T.
	
	oPanel := TPanel():New(0,0,'',oDlg, , .T., .T.,, ,10,22,.T.,.T. )
	oPanel:Align := CONTROL_ALIGN_TOP

	oPanel1 := TPanel():New(0,0,'',oDlg, , .T., .T.,, ,40,40,.T.,.T. )
	oPanel1:Align := CONTROL_ALIGN_ALLCLIENT

	oPanel2 := TPanel():New(0,0,'',oDlg, , .T., .T.,, ,40,120,.T.,.T. )
	oPanel2:Align := CONTROL_ALIGN_BOTTOM
	If !lShowGraph
		oPanel2:Hide()
	EndIf	

	@ 2,4 SAY AKN->AKN_DESCRI  of oPanel SIZE 120,9 PIXEL FONT oBold COLOR RGB(80,80,80)
	@ 3,3 BITMAP oBar RESNAME "MYBAR" Of oPanel SIZE BrwSize(@oDlg,0)/2,8 NOBORDER When .F. PIXEL ADJUST

	@ 12,2 SAY STR0041+DTOC(mv_par03) + IIf(nCasas==0,"" ,IIf(nCasas==-3,STR0046,STR0047)) Of oPanel PIXEL SIZE 640 ,79 FONT oBold //"Saldo em : "

	oGraphic := FWChartFactory():New()
	oGraphic:SetOwner(oPanel2)

	oGraphic:setPicture(cPicture)
	oGraphic:setMask("R$ *@*")
	oGraphic:SetLegend(CONTROL_ALIGN_TOP)
	oGraphic:setTitle("", CONTROL_ALIGN_CENTER)
	oGraphic:SetAlignSerieLabel(CONTROL_ALIGN_RIGHT)
	oGraphic:EnableMenu(.T.)

	oGraphic:SetChartDefault(COLUMNCHART)

	For nx := 1 to Len(aProcessa)
		If ExistBlock("PCO350GRF")
	   		lRetPe :=	ExecBlock("PCO350GRF",.F.,.F.,{aProcessa,nX})
   		EndIf	
   		If lRetPe
			oGraphic:addSerie(Substr(aProcessa[nx,1],1), aProcessa[nx,2,1])
   		EndIf
	Next 

	oGraphic:Activate()

	oView	:= TWBrowse():New( 2,2,aPosObj[1,4]-6,aPosObj[1,3]-aPosObj[1,1]-16,,aTitle,,oPanel1,,,,,,,oFont,,,,,.F.,,.T.,,.F.,,,) //"Descricao"###"Valor"
	oView:Align := CONTROL_ALIGN_ALLCLIENT
	oView:SetArray(aView)
	oView:bLine := { || aView[oView:nAT]}

	aButtons := aClone(AddToExcel(aButtons,{ {"ARRAY",STR0041+DTOC(mv_par03)+ IIf(nCasas==0,"" ,IIf(nCasas==-3,STR0046,STR0047)),{cDescri,STR0035,STR0036},aView} } ))//"Saldo em : "

	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| Eval(bEncerra)},{||Eval(bEncerra)},, aButtons)

EndIf

RestArea(aArea)

Return

Static Function MenuDef()
Local aRotina 	:= {	{ STR0002,		"AxPesqui" , 0 , 1},; //"Pesquisar"
							{ STR0003, 	"Pco350View" , 0 , 2} }  //"Visualizar"
						
If AMIIn(57) // AMIIn do modulo SIGAPCO ( 57 )
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณ Adiciona botoes do usuario no Browse                                   ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If ExistBlock( "PCOC3501" )
		//P_Eฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//P_Eณ Ponto de entrada utilizado para inclusao de funcoes de usuarios no     ณ
		//P_Eณ browse da tela de Centros Orcamentarios                                            ณ
		//P_Eณ Parametros : Nenhum                                                    ณ
		//P_Eณ Retorno    : Array contendo as rotinas a serem adicionados na enchoice ณ
		//P_Eณ               Ex. :  User Function PCOC3501                            ณ
		//P_Eณ                      Return {{"Titulo", {|| U_Teste() } }}             ณ
		//P_Eภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If ValType( aUsRotina := ExecBlock( "PCOC3501", .F., .F. ) ) == "A"
			AEval( aUsRotina, { |x| AAdd( aRotina, x ) } )
		EndIf
	EndIf
EndIf
Return(aRotina)
