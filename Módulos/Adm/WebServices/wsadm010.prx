#INCLUDE "WSADM010.ch"
#INCLUDE "APWEBSRV.CH"
#INCLUDE "PROTHEUS.CH"
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSADM010  ³ Autor ³Eduardo Riera          ³ Data ³18.03.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Financeiro para o portal  ³±±
±±³          ³ de cliente                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE FinanceCustomerBill DESCRIPTION STR0001 NAMESPACE "http://webservices.microsiga.com.br/financecustomerbill.apw" //"Serviço de consulta as informações financeiras de clientes ( <b>Restrição de cliente</b> )"
WSDATA UserCode                 As String
WSDATA CustomerID               As String
WSDATA DateFrom                 As Date OPTIONAL
WSDATA DateTo                   As Date OPTIONAL
WSDATA Header                   As Array Of BrwHeader
WSDATA HeaderType               As String
WSDATA ListOfBill               As Array Of BillView
WSDATA Bill                     As BillView
WSDATA PaidBill                 AS PaidBillCollection
WSDATA QueryAddWhere            As String OPTIONAL
WSDATA IndexKey                 As String OPTIONAL
WSDATA Prefix                   As String
WSDATA BillNumber               As String
WSDATA Installment              As String
WSDATA BillType                 As String

WSMETHOD GetHeader		DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do Serviço"
WSMETHOD BrwBill         DESCRIPTION STR0003 //"Método de listagem dos títulos financeiros em aberto"
WSMETHOD GetBill         DESCRIPTION STR0004 //"Método de consulta ao título financeiro em aberto"
WSMETHOD BrwCreditNote   DESCRIPTION STR0005 //"Método de listagem das notas de credito financeiras em aberto"
WSMETHOD BrwPaidBill     DESCRIPTION STR0006 //"Método de listagem dos títulos financeiros baixados"
WSMETHOD GetPaidBill     DESCRIPTION STR0007 //"Método de consulta ao título financeiro baixado"
ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao do header de titulos financeiros       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da Estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE FinanceCustomerBill

::Header := FinHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwBill   ³Autor  ³ Eduardo Riera         ³ Data ³04.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos financeiros de um cliente  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC4: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os titulos financeiro em aberto de um   ³±±
±±³          ³cliente                                                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwBill WSRECEIVE UserCode,CustomerID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceCustomerBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local lQuery    := .F.

Local nY        := 0
Local cAliasSE1 := "SE1"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local nX        := 0
	Local aStruSE1  := {}
	Local cQuery    := ""
#ENDIF

DEFAULT ::IndexKey := SE1->(IndexKey())

If PrtChkUser(::UserCode,"FinanceCustomerBill","BrwBill","SA1",::CustomerID)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			cAliasSE1 := "FINBRWBILL"

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_SDDECRE,E1_SDACRES,E1_TXMOEDA,E1_VALLIQ,E1_ACRESC,E1_MULTA"
			cQuery += GetUserField("SE1")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_SALDO > 0 AND "
			cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
			cQuery += "SE1.D_E_L_E_T_=' ' "

			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)	

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF
		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO > 0 .And. !(cAliasSE1)->E1_TIPO$MVABATIM .And. !(cAliasSE1)->E1_TIPO$MV_CRNEG+"/"+MVRECANT
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
			EndIf		
			dbSelectArea(cAliasSE1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetBill   ³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao de titulo financeiro de um cliente     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Prefixo                                               ³±±
±±³          ³ExpC4: Numero                                                ³±±
±±³          ³ExpC3: Parcela                                               ³±±
±±³          ³ExpC4: Tipo                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera um titulo  financeiro em aberto de um   ³±±
±±³          ³cliente                                                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetBill WSRECEIVE UserCode,CustomerID,Prefix,BillNumber,Installment,BillType WSSEND Bill WSSERVICE FinanceCustomerBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)

If PrtChkUser(::UserCode,"FinanceCustomerBill","GetBill","SA1",::CustomerID)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		If MsSeek(xFilial("SE1")+cCliente+cLoja+::Prefix+::BillNumber+::Installment+::BillType)
			GetCRBill(@::Bill,"SE1")
		Else
			lRetorno := .F.
			SetSoapFault("GETBILL",STR0009) //"Titulo nao encontrado"
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwCredit ³Autor  ³ Eduardo Riera         ³ Data ³15.05.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao das notas de credito de um cliente     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC4: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera as notas de credito de um cliente       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwCreditNote WSRECEIVE UserCode,CustomerID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceCustomerBill

Local aArea     := GetArea()

Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE1 := "SE1"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local aStruSE1  := {}
	Local cQuery    := ""
	Local nX        := 0
#ENDIF

DEFAULT ::IndexKey := SE1->(IndexKey())

If PrtChkUser(::UserCode,"FINANCECUSTOMERBILL","BRWCREDITNOTE","SA1",::CustomerID)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			cAliasSE1 := "FINGETBILL"

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_SDDECRE,E1_SDACRES,E1_TXMOEDA,E1_VALLIQ "
			cQuery += GetUserField("SE1")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_SALDO > 0 AND "
			cQuery += "SE1.E1_TIPO IN "+FormatIn(MV_CRNEG+"/"+MVRECANT,"/")+" AND "
			cQuery += "SE1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF

		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO > 0 .And. (cAliasSE1)->E1_TIPO$MV_CRNEG
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
			EndIf
			dbSelectArea(cAliasSE1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWCREDITNOTE",STR0008) //"Cliente invalido"
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwPaidBil³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos pagos de um cliente        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpD3: Pagamento de                                          ³±±
±±³          ³ExpD4: Pagamento ate                                         ³±±
±±³          ³ExpC5: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC6: Expressa de ordem na query                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera uma colecao de titulos pagos jutamente  ³±±
±±³          ³com seus respectivos pagamentos                              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwPaidBill WSRECEIVE UserCode,CustomerID,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceCustomerBill

Local aArea     := GetArea()
Local aChave    := {}
Local lRetorno  := .F.
Local lQuery    := .F.
Local lSkip     := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSE1 := "SE1"
Local cAliasSE5 := "SE5"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local dDataIni  := ::DateFrom
Local dDataFim  := ::DateTo
#IFDEF TOP
	Local aStruSE1  := {}
	Local cQuery    := ""
#ENDIF

DEFAULT ::IndexKey := SE1->(IndexKey())
dDataIni := IIF(Empty(dDataIni),Ctod("01/01/1980"),dDataIni)
dDataFim := IIF(Empty(dDataFim),dDataBase,dDataFim)

If PrtChkUser(::UserCode,"FINANCECUSTOMERBILL","BRWPAIDBILL","SA1",::CustomerID)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			aStruSE5  := SE5->(dbStruct())

			cAliasSE1 := "FINGETBILL"
			cAliasSE5 := "FINGETBILL"			

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,E1_TXMOEDA,E1_VALLIQ, "
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_TXMOEDA,E1_VALLIQ,"
			cQuery += "E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
			cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
			cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC,E1_SDDECRE,E1_SDACRES"
			cQuery += GetUserField("SE1")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1,"
			cQuery += RetSqlName("SE5")+" SE5 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
			cQuery += "SE1.E1_SALDO <> E1_VALOR AND "
			cQuery += "SE1.D_E_L_E_T_=' ' AND "
			cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "SE5.E5_NATUREZ=SE1.E1_NATUREZ AND "
			cQuery += "SE5.E5_PREFIXO=SE1.E1_PREFIXO AND "
			cQuery += "SE5.E5_NUMERO=SE1.E1_NUM AND "
			cQuery += "SE5.E5_PARCELA=SE1.E1_PARCELA AND "
			cQuery += "SE5.E5_TIPO=SE1.E1_TIPO AND "
			cQuery += "SE5.E5_CLIFOR=SE1.E1_CLIENTE AND "
			cQuery += "SE5.E5_LOJA=SE1.E1_LOJA AND "
			cQuery += "SE5.E5_RECPAG='R' AND "
			cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
			cQuery += "SE5.E5_SITUACA<>'C' AND "
			cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
			cQuery += "SELECT A.E5_NUMERO "
			cQuery += "FROM "+RetSqlName("SE5")+" A "
			cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
			cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
			cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
			cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
			cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
			cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
			cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
			cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
			cQuery += "A.E5_TIPODOC='ES' AND "
			cQuery += "A.E5_RECPAG<>'R' AND "
			cQuery += "A.D_E_L_E_T_ = ' ') "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
			For nX := 1 To Len(aStruSE5)
				If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
					TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
				EndIf
			Next nX			
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF

		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO < (cAliasSE1)->E1_VALOR .And. !(cAliasSE1)->E1_TIPO $ MVABATIM

				If !lQuery
					dbSelectArea("SE5")
					dbSetOrder(7)
					MsSeek(xFilial("SE5")+(cAliasSE1)->E1_PREFIXO+(cAliasSE1)->E1_NUM+(cAliasSE1)->E1_PARCELA+(cAliasSE1)->E1_TIPO+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
				EndIf

				nX    := 0
				lSkip := .F.
				aChave := {	(cAliasSE1)->E1_PREFIXO ,;
					(cAliasSE1)->E1_NUM ,;
					(cAliasSE1)->E1_PARCELA ,;
					(cAliasSE1)->E1_TIPO ,;
					(cAliasSE1)->E1_CLIENTE,;
					(cAliasSE1)->E1_LOJA}


				While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
						aChave[1] == (cAliasSE5)->E5_PREFIXO .And.;
						aChave[2] == (cAliasSE5)->E5_NUMERO .And.;
						aChave[3] == (cAliasSE5)->E5_PARCELA .And.;
						aChave[4] == (cAliasSE5)->E5_TIPO .And.;
						aChave[5] == (cAliasSE5)->E5_CLIFOR .And.;
						aChave[6] == (cAliasSE5)->E5_LOJA

					If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
							(cAliasSE5)->E5_DATA >= dDataIni .And. (cAliasSE5)->E5_DATA <= dDataFim .And.;
							(cAliasSE5)->E5_SITUACA <> "C" .And.;
							(cAliasSE5)->E5_RECPAG == "R" .And.;
							IIf(lQuery,.T.,!TemBxCanc())
						nX++
						If nX == 1
							aadd(::ListOfBill,WSClassNew("BillView"))
							nY++
							GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
						EndIf
					EndIf
					dbSelectArea(cAliasSE5)
					dbSkip()
					lSkip := .T.
				EndDo
			EndIf
			If !lQuery .Or. !lSkip
				dbSelectArea(cAliasSE1)
				dbSkip()
			EndIf
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWPAIDBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPaidBil³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos pagamentos de um titulo financeiro ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Prefixo                                               ³±±
±±³          ³ExpC4: Numero                                                ³±±
±±³          ³ExpC3: Parcela                                               ³±±
±±³          ³ExpC4: Tipo                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os pagamentos referentes a um titulo    ³±±
±±³          ³financeiro                                                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetPaidBill WSRECEIVE UserCode,CustomerID,Prefix,BillNumber,Installment,BillType WSSEND PaidBill WSSERVICE FinanceCustomerBill

Local aArea     := GetArea()

Local lRetorno  := .F.
Local lQuery    := .F.
Local lSkip     := .F.
Local nX        := 0
Local cAliasSE5 := "SE5"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local aStruSE5  := {}
	Local cQuery    := ""
#ENDIF

If PrtChkUser(::UserCode,"FinanceCustomerBill","GetPaidBill","SA1",::CustomerID)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		If MsSeek(xFilial("SE1")+cCliente+cLoja+::Prefix+::BillNumber+::Installment+::BillType)

			dbSelectArea("SE5")
			dbSetOrder(7)		
			#IFDEF TOP
				lQuery    := .T.
				aStruSE5  := SE5->(dbStruct())

				cAliasSE5 := "FINGETBILL"			

				cQuery := "SELECT E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
				cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
				cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC"
				cQuery += GetUserField("SE5")
				cQuery += "FROM "+RetSqlName("SE5")+" SE5 "
				cQuery += "WHERE "
				cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
				cQuery += "SE5.E5_NATUREZ='"+SE1->E1_NATUREZ+"' AND "
				cQuery += "SE5.E5_PREFIXO='"+SE1->E1_PREFIXO+"' AND "
				cQuery += "SE5.E5_NUMERO='"+SE1->E1_NUM+"' AND "
				cQuery += "SE5.E5_PARCELA='"+SE1->E1_PARCELA+"' AND "
				cQuery += "SE5.E5_TIPO='"+SE1->E1_TIPO+"' AND "
				cQuery += "SE5.E5_CLIFOR='"+SE1->E1_CLIENTE+"' AND "
				cQuery += "SE5.E5_LOJA='"+SE1->E1_LOJA+"' AND "
				cQuery += "SE5.E5_RECPAG='R' AND "
				cQuery += "SE5.E5_SITUACA<>'C' AND "
				cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
				cQuery += "SELECT A.E5_NUMERO "
				cQuery += "FROM "+RetSqlName("SE5")+" A "
				cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
				cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
				cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
				cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
				cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
				cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
				cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
				cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
				cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
				cQuery += "A.E5_TIPODOC='ES' AND "
				cQuery += "A.E5_RECPAG<>'R' AND "
				cQuery += "A.D_E_L_E_T_ = ' ') "
				cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

				cQuery := ChangeQuery(cQuery)

				dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE5)

				For nX := 1 To Len(aStruSE5)
					If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
						TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
					EndIf
				Next nX	
			#ELSE
				MsSeek(xFilial("SE5")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA)									
			#ENDIF

			nX    := 0
			lSkip := .F.

			While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
					SE1->E1_PREFIXO == (cAliasSE5)->E5_PREFIXO .And.;
					SE1->E1_NUM == (cAliasSE5)->E5_NUMERO .And.;
					SE1->E1_PARCELA == (cAliasSE5)->E5_PARCELA .And.;
					SE1->E1_TIPO == (cAliasSE5)->E5_TIPO .And.;
					SE1->E1_CLIENTE == (cAliasSE5)->E5_CLIFOR .And.;
					SE1->E1_LOJA == (cAliasSE5)->E5_LOJA

				If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
						(cAliasSE5)->E5_SITUACA <> "C" .And.;
						(cAliasSE5)->E5_RECPAG == "R" .And.;
						IIf(lQuery,.T.,!TemBxCanc())
					nX++					
					If nX == 1
						::PaidBill:Bill := WSClassNew("BillView")
						::PaidBill:Payment := {}
						GetCRBill(@::PaidBill:Bill,"SE1")
					EndIf
					aadd(::PaidBill:PayMent,WSClassNew("PaidBillView"))
					GetCRPaidBill(@::PaidBill:PayMent[nX],cAliasSE5)
				EndIf
				dbSelectArea(cAliasSE5)
				dbSkip()
				lSkip := .T.
			EndDo
			If lQuery
				dbSelectArea(cAliasSE5)
				dbCloseArea()
				dbSelectArea("SE5")
			EndIf
		Else
			lRetorno := .F.
			SetSoapFault("GETPAIDBILL",STR0009) //"Titulo nao encontrado"
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETPAIDBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³WSADM010  ³ Autor ³Eduardo Riera          ³ Data ³18.03.2003  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Web Service das funcionalidades do Financeiro para o portal  ³±±
±±³          ³ do vendedor                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Programador  ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³              ³        ³      ³                                          ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Definicao do Web Service de Controle do Usuario                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
WSSERVICE FinanceSellerBill        DESCRIPTION STR0010 NAMESPACE "http://webservices.microsiga.com.br/financesellerbill.apw" //"Serviço de consulta as informações financeiras de clientes ( <b>Restrição de representante comercial</b> )"
WSDATA UserCode                 AS String
WSDATA SellerCode               AS String
WSDATA CustomerID               AS String OPTIONAL
WSDATA DateFrom                 AS Date OPTIONAL
WSDATA DateTo                   AS Date OPTIONAL
WSDATA RegisterDateFrom         AS Date OPTIONAL
WSDATA RegisterDateTo           AS Date OPTIONAL
WSDATA RealDueDateFrom          AS Date OPTIONAL
WSDATA RealDueDateTo            AS Date OPTIONAL
WSDATA Header                   AS Array Of BrwHeader
WSDATA HeaderType               AS String
WSDATA ListOfBill               AS Array Of BillView
WSDATA Bill                     AS BillView
WSDATA PaidBill                 AS PaidBillCollection
WSDATA QueryAddWhere            AS String OPTIONAL
WSDATA IndexKey                 AS String OPTIONAL
WSDATA Prefix                   As String
WSDATA BillNumber               As String
WSDATA Installment              As String
WSDATA BillType                 As String
WSDATA PageLen                  As Integer
WSDATA PageFirst                As Integer
WSDATA QtdeTitulos              As Integer

WSMETHOD GetHeader		       DESCRIPTION STR0002 //"Método que descreve as estruturas de retorno do Serviço"
WSMETHOD GetBill                DESCRIPTION STR0004 //"Método de consulta ao título financeiro em aberto"
WSMETHOD BrwCustomerBill        DESCRIPTION STR0011 //"Método de listagem aos títulos financeiros em aberto de um cliente"
WSMETHOD BrwCustomerCreditNote  DESCRIPTION STR0012 //"Método de listagem das notas de credito financeiras em aberto de um cliente"
WSMETHOD BrwCustomerPaidBill    DESCRIPTION STR0013 //"Método de listagem dos títulos financeiros baixados de um cliente"
WSMETHOD BrwBill                DESCRIPTION STR0003 //"Método de listagem dos títulos financeiros em aberto"
WSMETHOD BrwCreditNote          DESCRIPTION STR0005 //"Método de listagem das notas de credito financeiras em aberto"
WSMETHOD BrwPaidBill            DESCRIPTION STR0006 //"Método de listagem dos títulos financeiros baixados"
WSMETHOD GetPaidBill            DESCRIPTION STR0007 //"Método de consulta ao título financeiro baixado"
WSMETHOD BrwPaidBillQTDE		DESCRIPTION STR0019	//"Método que consulta a quantidade de títulos baixados do financeiro"

ENDWSSERVICE

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetHeader ³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao do header de titulos financeiros       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Nome da Estrutura                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método devolve o header de uma estrutura                ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetHeader WSRECEIVE HeaderType WSSEND Header WSSERVICE FinanceSellerBill

::Header := FinHeader(::HeaderType)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetBill   ³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos dados de um titulo financeiro      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Prefixo                                               ³±±
±±³          ³ExpC4: Numero                                                ³±±
±±³          ³ExpC3: Parcela                                               ³±±
±±³          ³ExpC4: Tipo                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os titulos financeiro em aberto de um   ³±±
±±³          ³cliente                                                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetBill WSRECEIVE UserCode,SellerCode,Prefix,BillNumber,Installment,BillType WSSEND Bill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()
Local lRetorno  := .F.

If PrtChkUser(::UserCode,"FinanceSellerBill","GetBill","SA3",::SellerCode)


	lRetorno := .T.

	dbSelectArea("SE1")
	dbSetOrder(1)
	If MsSeek(xFilial("SE1")+::Prefix+::BillNumber+::Installment+::BillType)
		GetCRBill(@::Bill,"SE1")
	Else
		lRetorno := .F.
		SetSoapFault("GETBILL",STR0009) //"Titulo nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwCustome³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos em aberto de um cliente    ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Cliente                                               ³±±
±±³          ³ExpC4: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC5: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os titulos financeiros em aberto de um  ³±±
±±³          ³cliente                                                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwCustomerBill WSRECEIVE UserCode,SellerCode,CustomerID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()

Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE1 := "SE1"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local aStruSE1  := {}
	Local cQuery    := ""
	Local nX        := 0
#ENDIF

DEFAULT ::IndexKey := SE1->(IndexKey())

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwCustomerBill","SA3",::SellerCode)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			cAliasSE1 := "FINGETBILL"

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_TXMOEDA,E1_VALLIQ "
			cQuery += GetUserField("SE1")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_SALDO > 0 AND "
			cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
			cQuery += "SE1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF

		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO > 0 .And. !(cAliasSE1)->E1_TIPO$MVABATIM .And. !(cAliasSE1)->E1_TIPO$MV_CRNEG+"/"+MVRECANT
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
			EndIf		
			dbSelectArea(cAliasSE1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWCUSTOMERBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwCustome³Autor  ³ Eduardo Riera         ³ Data ³15.05.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao das notas de credito em aberto de um   ³±±
±±³          ³cliente                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Cliente                                               ³±±
±±³          ³ExpC4: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC5: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera as notas de credito em aberto de um cli-³±±
±±³          ³ente                                                         ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwCustomerCreditNote WSRECEIVE UserCode,SellerCode,CustomerID,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()

Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE1 := "SE1"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
#IFDEF TOP
	Local aStruSE1  := {}
	Local cQuery    := ""
	Local nX        := 0
#ENDIF
DEFAULT ::IndexKey := SE1->(IndexKey())

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwCustomerCreditNote","SA3",::SellerCode)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			cAliasSE1 := "FINGETBILL"

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_TXMOEDA,E1_VALLIQ"
			cQuery += GetUserField("SE1")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_SALDO > 0 AND "
			cQuery += "SE1.E1_TIPO IN "+FormatIn(MV_CRNEG+"/"+MVRECANT,"/")+" AND "
			cQuery += "SE1.D_E_L_E_T_=' ' "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF

		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO > 0 .And. (cAliasSE1)->E1_TIPO$MV_CRNEG

				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++				
				GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)

			EndIf		
			dbSelectArea(cAliasSE1)
			dbSkip()
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETBILL",STR0008) //"Cliente invalido"
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwPaidBil³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos financeiros pagos de um    ³±±
±±³          ³cliente                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Cliente                                               ³±±
±±³          ³ExpD4: Pagamento de                                          ³±±
±±³          ³ExpD5: Pagamento ate                                         ³±±
±±³          ³ExpC6: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC7: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera uma colecao de titulos pagos jutamente  ³±±
±±³          ³com seus respectivos pagamentos                              ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwCustomerPaidBill WSRECEIVE UserCode,SellerCode,CustomerID,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()
Local aChave    := {}
Local lRetorno  := .F.
Local lQuery    := .F.
Local lSkip     := .F.
Local nX        := 0
Local nY        := 0
Local cAliasSE1 := "SE1"
Local cAliasSE5 := "SE5"
Local cCliente  := SubStr(::CustomerID,1,Len(SA1->A1_COD))
Local cLoja     := SubStr(::CustomerID,Len(SA1->A1_COD)+1)
Local dDataIni  := ::DateFrom
Local dDataFim  := ::DateTo
#IFDEF TOP
	Local aStruSE1  := {}
	Local aStruSE5  := {}
	Local cQuery    := ""
#ENDIF

DEFAULT ::IndexKey := SE1->(IndexKey())
dDataIni := IIF(Empty(dDataIni),Ctod("01/01/1980"),dDataIni)
dDataFim := IIF(Empty(dDataFim),dDataBase,dDataFim)

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwCustomerPaidBill","SA3",::SellerCode)

	If !Empty(cCliente) .And. !Empty(cLoja)

		lRetorno := .T.

		dbSelectArea("SE1")
		dbSetOrder(2)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE1  := SE1->(dbStruct())
			aStruSE5  := SE5->(dbStruct())

			cAliasSE1 := "FINGETBILL"
			cAliasSE5 := "FINGETBILL"			

			cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
			cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
			cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_TXMOEDA,E1_VALLIQ,"
			cQuery += "E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
			cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
			cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC"
			cQuery += GetUserField("SE1")
			cQuery += GetUserField("SE5")
			cQuery += "FROM "+RetSqlName("SE1")+" SE1,"
			cQuery += RetSqlName("SE5")+" SE5 "
			cQuery += "WHERE "
			cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
			cQuery += "SE1.E1_CLIENTE='"+cCliente+"' AND "
			cQuery += "SE1.E1_LOJA='"+cLoja+"' AND "
			cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
			cQuery += "SE1.E1_SALDO <> E1_VALOR AND "
			cQuery += "SE1.D_E_L_E_T_=' ' AND "
			cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "SE5.E5_NATUREZ=SE1.E1_NATUREZ AND "
			cQuery += "SE5.E5_PREFIXO=SE1.E1_PREFIXO AND "
			cQuery += "SE5.E5_NUMERO=SE1.E1_NUM AND "
			cQuery += "SE5.E5_PARCELA=SE1.E1_PARCELA AND "
			cQuery += "SE5.E5_TIPO=SE1.E1_TIPO AND "
			cQuery += "SE5.E5_CLIFOR=SE1.E1_CLIENTE AND "
			cQuery += "SE5.E5_LOJA=SE1.E1_LOJA AND "
			cQuery += "SE5.E5_RECPAG='R' AND "
			cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
			cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
			cQuery += "SE5.E5_SITUACA<>'C' AND "
			cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
			cQuery += "SELECT A.E5_NUMERO "
			cQuery += "FROM "+RetSqlName("SE5")+" A "
			cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
			cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
			cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
			cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
			cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
			cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
			cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
			cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
			cQuery += "A.E5_TIPODOC='ES' AND "
			cQuery += "A.E5_RECPAG<>'R' AND "
			cQuery += "A.D_E_L_E_T_ = ' ') "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
			cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

			For nX := 1 To Len(aStruSE1)
				If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
					TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
				EndIf
			Next nX
			For nX := 1 To Len(aStruSE5)
				If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
					TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
				EndIf
			Next nX			
		#ELSE
			MsSeek(xFilial("SE1")+::CustomerID)
		#ENDIF

		While !Eof() .And. xFilial("SE1") == (cAliasSE1)->E1_FILIAL .And.;
				cCliente == (cAliasSE1)->E1_CLIENTE .And.;
				cLoja == (cAliasSE1)->E1_LOJA		
			If (cAliasSE1)->E1_SALDO < (cAliasSE1)->E1_VALOR .And. !(cAliasSE1)->E1_TIPO $ MVABATIM

				If !lQuery
					dbSelectArea("SE5")
					dbSetOrder(7)
					MsSeek(xFilial("SE5")+(cAliasSE1)->E1_PREFIXO+(cAliasSE1)->E1_NUM+(cAliasSE1)->E1_PARCELA+(cAliasSE1)->E1_TIPO+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
				EndIf

				nX    := 0
				lSkip := .F.
				aChave := {	(cAliasSE1)->E1_PREFIXO ,;
					(cAliasSE1)->E1_NUM ,;
					(cAliasSE1)->E1_PARCELA ,;
					(cAliasSE1)->E1_TIPO ,;
					(cAliasSE1)->E1_CLIENTE,;
					(cAliasSE1)->E1_LOJA}


				While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
						aChave[1] == (cAliasSE5)->E5_PREFIXO .And.;
						aChave[2] == (cAliasSE5)->E5_NUMERO .And.;
						aChave[3] == (cAliasSE5)->E5_PARCELA .And.;
						aChave[4] == (cAliasSE5)->E5_TIPO .And.;
						aChave[5] == (cAliasSE5)->E5_CLIFOR .And.;
						aChave[6] == (cAliasSE5)->E5_LOJA

					If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
							(cAliasSE5)->E5_DATA >= dDataIni .And. (cAliasSE5)->E5_DATA <= dDataFim .And.;
							(cAliasSE5)->E5_SITUACA <> "C" .And.;
							(cAliasSE5)->E5_RECPAG == "R" .And.;
							IIf(lQuery,.T.,!TemBxCanc())
						nX++					
						If nX == 1
							aadd(::ListOfBill,WSClassNew("BillView"))
							nY++
							GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
						EndIf
					EndIf
					dbSelectArea(cAliasSE5)
					dbSkip()
					lSkip := .T.
				EndDo
			EndIf
			If !lQuery .Or. !lSkip
				dbSelectArea(cAliasSE1)
				dbSkip()
			EndIf
		EndDo
		If lQuery
			dbSelectArea(cAliasSE1)
			dbCloseArea()
			dbSelectArea("SE1")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("BRWCUSTOMERPAIDBILL",STR0008) //"Cliente invalido"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwBill   ³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos financeiros em abertos de  ³±±
±±³          ³um periodo                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpC3: Cliente                                               ³±±
±±³          ³ExpD4: Emissao de                                            ³±±
±±³          ³ExpD5: Emissao ate                                           ³±±
±±³          ³ExpD4: Vencto  de                                            ³±±
±±³          ³ExpD5: Vencto  ate                                           ³±±
±±³          ³ExpC6: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC7: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os titulos financeiro em aberto de um   ³±±
±±³          ³cliente                                                      ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÃ±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Analista     ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³11/04/06³096504³- Tratamento para posicionar corretamen- ³±±
±±³              ³        ³      ³te no cod. do vend. retornado na query.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwBill WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,RealDueDateFrom,RealDueDateTo,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()	//Guarda a area atual
Local lRetorno  := .F.			//Indica o retorno da funcao
Local lQuery    := .F.			//Indica se ira executar query (Top)
Local cVend 	:= ""			//Cod. vendedor no cad. de clientes
Local nY        := 0			//Contador de titulos encontrados
Local cAliasSE1 := "SE1"		//Alias da tabela de titulos SE1
Local cAliasSA1 := "SA1"		//Alias da tabela de clientes SA1
#IFDEF TOP
	Local aStruSE1  := {}		//Array com a estrutura do SE1
	Local cQuery    := ""		//Query a ser executada
	Local nX        := 0		//Usada em lacos For...Next
#ENDIF

DEFAULT ::IndexKey        := SE1->(IndexKey())
DEFAULT ::RegisterDateTo  := LastDay(dDataBase)
DEFAULT ::RegisterDateFrom:= FirstDay(dDataBase)
DEFAULT ::RealDueDateTo   := LastDay(dDataBase)
DEFAULT ::RealDueDateFrom := FirstDay(dDataBase)

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwBill","SA3",::SellerCode)

	lRetorno := .T.

	dbSelectArea("SE1")
	dbSetOrder(6)
	#IFDEF TOP
		lQuery    := .T.
		aStruSE1  := SE1->(dbStruct())
		cAliasSE1 := "FINGETBILL"

		cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
		cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
        cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,A1_VEND,E1_SDDECRE,E1_SDACRES,E1_TXMOEDA,E1_VALLIQ "
		cQuery += GetUserField("SE1")
		cQuery += "FROM "+RetSqlName("SE1")+" SE1, "
		cQuery += RetSqlName("SA1")+" SA1 "
		cQuery += "WHERE "
		cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
		cQuery += "SE1.E1_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
		cQuery += "SE1.E1_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
		cQuery += "SE1.E1_VENCREA>='"+Dtos(::RealDueDateFrom)+"' AND "
		cQuery += "SE1.E1_VENCREA<='"+Dtos(::RealDueDateTo)+"' AND "
		cQuery += "SE1.E1_SALDO > 0 AND "
		cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
		cQuery += "SE1.D_E_L_E_T_=' ' AND "
		cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
		cQuery += "SA1.A1_COD = SE1.E1_CLIENTE AND "
		cQuery += "SA1.A1_LOJA = SE1.E1_LOJA AND "
		cQuery += "(SA1.A1_VEND = '"+::SellerCode+"' OR SA1.A1_VEND = '"+Space(Len(SA1->A1_VEND))+"') AND "
		cQuery += "SA1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

		For nX := 1 To Len(aStruSE1)
			If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
				TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
			EndIf
		Next nX
	#ELSE
		MsSeek(xFilial("SE1")+Dtos(::RegisterDateTo),.T.)
	#ENDIF

	While !Eof() .And. (cAliasSE1)->E1_FILIAL == xFilial("SE1") .And.;
			(cAliasSE1)->E1_EMISSAO >= ::RegisterDateFrom  .And.;
			(cAliasSE1)->E1_EMISSAO <= ::RegisterDateTo

		If (cAliasSE1)->E1_SALDO > 0 .And. !(cAliasSE1)->E1_TIPO$MVABATIM .And. !(cAliasSE1)->E1_TIPO$MV_CRNEG+"/"+MVRECANT 			
			If !lQuery
				dbSelectArea("SA1")
				dbSetOrder(1)
				MsSeek(xFilial("SA1")+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
				cVend := (cAliasSA1)->A1_VEND
			Else
				cVend := (cAliasSE1)->A1_VEND
			EndIf
			If cVend == ::SellerCode .Or. Empty( cVend )
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				GetCRBill(@ListOfBill[nY],cAliasSE1,.T.)
			EndIf
		EndIf
		dbSelectArea(cAliasSE1)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSE1)
		dbCloseArea()
		dbSelectArea("SE1")
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³FinGetCred³Autor  ³ Eduardo Riera         ³ Data ³15.05.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao das notas de credito em aberto de um   ³±±
±±³          ³periodo                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpD4: Emissao de                                            ³±±
±±³          ³ExpD5: Emissao ate                                           ³±±
±±³          ³ExpD4: Vencto  de                                            ³±±
±±³          ³ExpD5: Vencto  ate                                           ³±±
±±³          ³ExpC6: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpC7: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera as notas de credito de um periodo       ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwCreditNote WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,RealDueDateTo,RealDueDateFrom,QueryAddWhere,IndexKey WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local lQuery    := .F.
Local nY        := 0
Local cAliasSE1 := "SE1"
#IFDEF TOP
	Local aStruSE1  := {}
	Local cQuery    := ""
	Local nX        := 0
#ENDIF

DEFAULT ::IndexKey        := SE1->(IndexKey())
DEFAULT ::RegisterDateTo  := LastDay(dDataBase)
DEFAULT ::RegisterDateFrom:= FirstDay(dDataBase)
DEFAULT ::RealDueDateTo   := LastDay(dDataBase)
DEFAULT ::RealDueDateFrom := FirstDay(dDataBase)

If PrtChkUser(::UserCode,"FinanceSellerBill","GetCreditNote","SA3",::SellerCode)

	lRetorno := .T.

	dbSelectArea("SE1")
	dbSetOrder(6)
	#IFDEF TOP
		lQuery    := .T.
		aStruSE1  := SE1->(dbStruct())
		cAliasSE1 := "FINGETBILL"

		cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
		cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
		cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,A1_VEND,E1_SDDECRE,E1_SDACRES,E1_TXMOEDA,E1_VALLIQ "
		cQuery += GetUserField("SE1")
		cQuery += "FROM "+RetSqlName("SE1")+" SE1, "
		cQuery += RetSqlName("SA1")+" SA1 "
		cQuery += "WHERE "
		cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
		cQuery += "SE1.E1_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
		cQuery += "SE1.E1_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
		cQuery += "SE1.E1_VENCREA>='"+Dtos(::RealDueDateFrom)+"' AND "
		cQuery += "SE1.E1_VENCREA<='"+Dtos(::RealDueDateTo)+"' AND "
		cQuery += "SE1.E1_SALDO > 0 AND "
		cQuery += "SE1.E1_TIPO IN "+FormatIn(MV_CRNEG+"/"+MVRECANT,"/")+" AND "
		cQuery += "SE1.D_E_L_E_T_=' ' AND "
		cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
		cQuery += "SA1.A1_COD = SE1.E1_CLIENTE AND "
		cQuery += "SA1.A1_LOJA = SE1.E1_LOJA AND "
		cQuery += "(SA1.A1_VEND = '"+::SellerCode+"' OR SA1.A1_VEND = '"+Space(Len(SA1->A1_VEND))+"') AND "
		cQuery += "SA1.D_E_L_E_T_=' ' "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

		For nX := 1 To Len(aStruSE1)
			If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
				TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
			EndIf
		Next nX
	#ELSE
		MsSeek(xFilial("SE1")+Dtos(::RegisterDateFrom),.T.)
	#ENDIF

	While !Eof() .And. (cAliasSE1)->E1_FILIAL == xFilial("SE1") .And.;
			(cAliasSE1)->E1_EMISSAO >= ::RegisterDateFrom  .And.;
			(cAliasSE1)->E1_EMISSAO <= ::RegisterDateTo
		If !lQuery
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
		EndIf
		If (cAliasSE1)->A1_VEND == ::SellerCode .Or. Empty((cAliasSE1)->A1_VEND)
			If (cAliasSE1)->E1_SALDO > 0 .And. (cAliasSE1)->E1_TIPO$MV_CRNEG
				aadd(::ListOfBill,WSClassNew("BillView"))
				nY++
				GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)
			EndIf
		EndIf
		dbSelectArea(cAliasSE1)
		dbSkip()
	EndDo
	If lQuery
		dbSelectArea(cAliasSE1)
		dbCloseArea()
		dbSelectArea("SE1")
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwPaidBil³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos titulos financeiros pagos de um    ³±±
±±³          ³periodo                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpD3: Emissao de                                            ³±±
±±³          ³ExpD4: Emissao ate                                           ³±±
±±³          ³ExpD5: Vencimento de                                         ³±±
±±³          ³ExpD6: Vencimento ate                                        ³±±
±±³          ³ExpD7: Pagamento de                                          ³±±
±±³          ³ExpD8: Pagamento ate                                         ³±±
±±³          ³ExpC9: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpCA: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera uma colecao de titulos pagos de um peri-³±±
±±³          ³odo                                                          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÃ±±
±±³ Atualizacoes sofridas desde a Construcao Inicial.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Analista     ³ Data   ³ BOPS ³  Motivo da Alteracao                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Cleber M.    ³11/04/06³096456³- Tratamento para posicionar corretamen- ³±±
±±³              ³        ³      ³te no cod. do vend. retornado na query.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwPaidBill WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,RealDueDateTo,RealDueDateFrom,DateFrom,DateTo,QueryAddWhere,IndexKey,PageLen,PageFirst WSSEND ListOfBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()	//Guarda a area atual
Local aChave    := {}			//Array com os campos chaves do SE1 
Local lRetorno  := .F.			//Indica retorno da funcao
Local lQuery    := .F.			//Indica se esta usando query (Top)
Local lSkip     := .F.			//Indica se deve procurar pelo prox. registro
Local nX        := 0			//Usado em lacos For...Next
Local nY        := 0			//Contador de titulos encontrados
Local cAliasSA1 := "SA1"		//Alias da tabela de clientes - SA1
Local cAliasSE1 := "SE1"		//Alias da tabela de titulos - SE1
Local cAliasSE5 := "SE5"		//Alias da tabela de mov. bancario - SE5
Local dDataIni  := ::DateFrom	//Parametro para busca Data de
Local dDataFim  := ::DateTo		//Parametro para busca Data ate
Local cVend		:= ""			//Cod. do vendedor no cad. de clientes
#IFDEF TOP
	Local aStruSE1  := {}		//Array com a estrutura do SE1
	Local aStruSE5  := {}		//Array com a estrutura do SE5
	Local cQuery    := ""		//Query a ser executada
#ENDIF
DEFAULT ::IndexKey        := SE1->(IndexKey())
DEFAULT ::RegisterDateFrom:= FirstDay(dDataBase)
DEFAULT ::RegisterDateTo  := LastDay(dDataBase)
DEFAULT ::RealDueDateTo   := LastDay(dDataBase)
DEFAULT ::RealDueDateFrom := FirstDay(dDataBase)

dDataIni := IIF(Empty(dDataIni),Ctod("01/01/1980"),dDataIni)
dDataFim := IIF(Empty(dDataFim),dDataBase,dDataFim)

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwPaidBill","SA3",::SellerCode)

	lRetorno := .T.

	dbSelectArea("SE1")
	dbSetOrder(6)
	#IFDEF TOP
		lQuery    := .T.
		aStruSE1  := SE1->(dbStruct())
		aStruSE5  := SE5->(dbStruct())

		cAliasSE1 := "FINGETBILL"
		cAliasSE5 := "FINGETBILL"			

		cQuery := "SELECT E1_FILIAL,E1_CLIENTE,E1_LOJA,E1_SALDO,E1_TIPO,E1_PREFIXO,E1_NUM,E1_PARCELA,E1_VALOR,"
		cQuery += "E1_NATUREZ,E1_EMISSAO,E1_VENCTO,E1_VENCREA,E1_MOEDA,E1_PORTADO,E1_HIST,E1_PORCJUR,E1_LIDESCF,"
		cQuery += "E1_VALJUR,E1_DESCFIN,E1_DIADESC,E1_DTVARIA,E1_BAIXA,E1_TIPODES,E1_TXMOEDA,E1_VALLIQ,"
		cQuery += "E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
		cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
		cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC,A1_VEND,E1_SDDECRE,E1_SDACRES"
		cQuery += GetUserField("SE1")
		cQuery += GetUserField("SE5")
		cQuery += "FROM "+RetSqlName("SE1")+" SE1,"
		cQuery += RetSqlName("SA1")+" SA1, "
		cQuery += RetSqlName("SE5")+" SE5 "
		cQuery += "WHERE "
		cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
		cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
		cQuery += "SE1.E1_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
		cQuery += "SE1.E1_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
		cQuery += "SE1.E1_VENCREA>='"+Dtos(::RealDueDateFrom)+"' AND "
		cQuery += "SE1.E1_VENCREA<='"+Dtos(::RealDueDateTo)+"' AND "		
		cQuery += "SE1.E1_SALDO <> E1_VALOR AND "
		cQuery += "SE1.D_E_L_E_T_=' ' AND "
		cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
		cQuery += "SA1.A1_COD = SE1.E1_CLIENTE AND "
		cQuery += "SA1.A1_LOJA = SE1.E1_LOJA AND "
		cQuery += "(SA1.A1_VEND = '"+::SellerCode+"' OR SA1.A1_VEND = '"+Space(Len(SA1->A1_VEND))+"') AND "
		cQuery += "SA1.D_E_L_E_T_=' ' AND "
		cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
		cQuery += "SE5.E5_NATUREZ=SE1.E1_NATUREZ AND "
		cQuery += "SE5.E5_PREFIXO=SE1.E1_PREFIXO AND "
		cQuery += "SE5.E5_NUMERO=SE1.E1_NUM AND "
		cQuery += "SE5.E5_PARCELA=SE1.E1_PARCELA AND "
		cQuery += "SE5.E5_TIPO=SE1.E1_TIPO AND "
		cQuery += "SE5.E5_CLIFOR=SE1.E1_CLIENTE AND "
		cQuery += "SE5.E5_LOJA=SE1.E1_LOJA AND "
		cQuery += "SE5.E5_RECPAG='R' AND "
		cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
		cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
		cQuery += "SE5.E5_SITUACA<>'C' AND "
		cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
		cQuery += "SELECT A.E5_NUMERO "
		cQuery += "FROM "+RetSqlName("SE5")+" A "
		cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
		cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
		cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
		cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
		cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
		cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
		cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
		cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
		cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
		cQuery += "A.E5_TIPODOC='ES' AND "
		cQuery += "A.E5_RECPAG<>'R' AND "
		cQuery += "A.D_E_L_E_T_ = ' ') "
		cQuery := WsQueryAdd(cQuery,::QueryAddWhere)
		cQuery += "ORDER BY "+WsSqlOrder(::IndexKey)

		cQuery := ChangeQuery(cQuery)

		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)

		For nX := 1 To Len(aStruSE1)
			If aStruSE1[nX][2]<>"C" .And. FieldPos(aStruSE1[nX][1])<>0
				TcSetField(cAliasSE1,aStruSE1[nX][1],aStruSE1[nX][2],aStruSE1[nX][3],aStruSE1[nX][4])
			EndIf
		Next nX
		For nX := 1 To Len(aStruSE5)
			If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
				TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
			EndIf
		Next nX			
	#ELSE
		MsSeek(xFilial("SE1")+Dtos(::RegisterDateFrom),.T.)
	#ENDIF
	nX    := 0
	While !Eof() .And. (cAliasSE1)->E1_FILIAL == xFilial("SE1") .And.;
			(cAliasSE1)->E1_EMISSAO >= ::RegisterDateFrom .And.;
			(cAliasSE1)->E1_EMISSAO <= ::RegisterDateTo

		If !lQuery
			dbSelectArea("SA1")
			dbSetOrder(1)
			MsSeek(xFilial("SA1")+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
			cVend := (cAliasSA1)->A1_VEND
		Else
			cVend := (cAliasSE1)->A1_VEND
		EndIf
		If cVend == ::SellerCode .Or. Empty( cVend )
			If (cAliasSE1)->E1_SALDO < (cAliasSE1)->E1_VALOR .And. !(cAliasSE1)->E1_TIPO $ MVABATIM

				If !lQuery
					dbSelectArea("SE5")
					dbSetOrder(7)
					MsSeek(xFilial("SE5")+(cAliasSE1)->E1_PREFIXO+(cAliasSE1)->E1_NUM+(cAliasSE1)->E1_PARCELA+(cAliasSE1)->E1_TIPO+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
				EndIf

				
				lSkip := .F.
				aChave := {	(cAliasSE1)->E1_PREFIXO ,;
					(cAliasSE1)->E1_NUM ,;
					(cAliasSE1)->E1_PARCELA ,;
					(cAliasSE1)->E1_TIPO ,;
					(cAliasSE1)->E1_CLIENTE,;
					(cAliasSE1)->E1_LOJA}

				While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
						aChave[1] == (cAliasSE5)->E5_PREFIXO .And.;
						aChave[2] == (cAliasSE5)->E5_NUMERO .And.;
						aChave[3] == (cAliasSE5)->E5_PARCELA .And.;
						aChave[4] == (cAliasSE5)->E5_TIPO .And.;
						aChave[5] == (cAliasSE5)->E5_CLIFOR .And.;
						aChave[6] == (cAliasSE5)->E5_LOJA

					If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
							(cAliasSE5)->E5_DATA >= dDataIni .And. (cAliasSE5)->E5_DATA <= dDataFim .And.;
							(cAliasSE5)->E5_SITUACA <> "C" .And.;
							(cAliasSE5)->E5_RECPAG == "R" .And.;
							IIf(lQuery,.T.,!TemBxCanc())
						nX++
						If ::PageFirst==0 .Or. nX >= ::PageFirst .And. nY < ::PageLen																
								aadd(::ListOfBill,WSClassNew("BillView"))
								nY++
								GetCRBill(@::ListOfBill[nY],cAliasSE1,.T.)							
						EndIf
					EndIf
					dbSelectArea(cAliasSE5)
					dbSkip()
					lSkip := .T.
				EndDo
			EndIf
		EndIf
		If !lQuery .Or. !lSkip
			dbSelectArea(cAliasSE1)
			dbSkip()
		EndIf
		If nY >= ::PageLen .And. ::PageLen <> 0
			Exit
		EndIf
	EndDo
	If lQuery
		dbSelectArea(cAliasSE1)
		dbCloseArea()
		dbSelectArea("SE1")
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPaidBil³Autor  ³ Eduardo Riera         ³ Data ³21.03.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método de recuperacao dos pagamentos de um titulo financeiro ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Customer ID                                           ³±±
±±³          ³ExpC3: Prefixo                                               ³±±
±±³          ³ExpC4: Numero                                                ³±±
±±³          ³ExpC3: Parcela                                               ³±±
±±³          ³ExpC4: Tipo                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Este Método recupera os pagamentos referentes a um titulo    ³±±
±±³          ³financeiro                                                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD GetPaidBill WSRECEIVE UserCode,CustomerID,Prefix,BillNumber,Installment,BillType WSSEND PaidBill WSSERVICE FinanceSellerBill

Local aArea     := GetArea()
Local lRetorno  := .F.
Local lQuery    := .F.
Local lSkip     := .F.
Local nX        := 0
Local cAliasSE5 := "SE5"
#IFDEF TOP
	Local aStruSE5  := {}
	Local cQuery    := ""
#ENDIF

If PrtChkUser(::UserCode,"FinanceCustomerBill","GetPaidBill","SA1",::CustomerID)

	lRetorno := .T.

	dbSelectArea("SE1")
	dbSetOrder(1)
	If MsSeek(xFilial("SE1")+::Prefix+::BillNumber+::Installment+::BillType)		

		dbSelectArea("SE5")
		dbSetOrder(7)
		#IFDEF TOP
			lQuery    := .T.
			aStruSE5  := SE5->(dbStruct())

			cAliasSE5 := "FINGETBILL"			

			cQuery := "SELECT E5_FILIAL,E5_PREFIXO,E5_NUMERO,E5_PARCELA,E5_TIPO,E5_CLIFOR,E5_LOJA,E5_TIPODOC,E5_DATA,"
			cQuery += "E5_SITUACA,E5_RECPAG,E5_MOTBX,E5_BANCO,E5_AGENCIA,E5_CONTA,E5_DATA,E5_DTDISPO,E5_VALOR,"
			cQuery += "E5_HISTOR,E5_ORDREC,E5_SERREC"
			cQuery += GetUserField("SE5")
			cQuery += "FROM "+RetSqlName("SE5")+" SE5 "
			cQuery += "WHERE "
			cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "SE5.E5_NATUREZ='"+SE1->E1_NATUREZ+"' AND "
			cQuery += "SE5.E5_PREFIXO='"+SE1->E1_PREFIXO+"' AND "
			cQuery += "SE5.E5_NUMERO='"+SE1->E1_NUM+"' AND "
			cQuery += "SE5.E5_PARCELA='"+SE1->E1_PARCELA+"' AND "
			cQuery += "SE5.E5_TIPO='"+SE1->E1_TIPO+"' AND "
			cQuery += "SE5.E5_CLIFOR='"+SE1->E1_CLIENTE+"' AND "
			cQuery += "SE5.E5_LOJA='"+SE1->E1_LOJA+"' AND "
			cQuery += "SE5.E5_RECPAG='R' AND "
			cQuery += "SE5.E5_SITUACA<>'C' AND "
			cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
			cQuery += "SELECT A.E5_NUMERO "
			cQuery += "FROM "+RetSqlName("SE5")+" A "
			cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
			cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
			cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
			cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
			cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
			cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
			cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
			cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
			cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
			cQuery += "A.E5_TIPODOC='ES' AND "
			cQuery += "A.E5_RECPAG<>'R' AND "
			cQuery += "A.D_E_L_E_T_ = ' ') "
			cQuery := WsQueryAdd(cQuery,::QueryAddWhere)

			cQuery := ChangeQuery(cQuery)

			dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE5)

			For nX := 1 To Len(aStruSE5)
				If aStruSE5[nX][2]<>"C" .And. FieldPos(aStruSE5[nX][1])<>0
					TcSetField(cAliasSE5,aStruSE5[nX][1],aStruSE5[nX][2],aStruSE5[nX][3],aStruSE5[nX][4])
				EndIf
			Next nX			
		#ELSE
			MsSeek(xFilial("SE5")+(cAliasSE1)->E1_PREFIXO+(cAliasSE1)->E1_NUM+(cAliasSE1)->E1_PARCELA+(cAliasSE1)->E1_TIPO+(cAliasSE1)->E1_CLIENTE+(cAliasSE1)->E1_LOJA)
		#ENDIF
		nX    := 0
		lSkip := .F.

		While !Eof() .And. xFilial("SE5") == (cAliasSE5)->E5_FILIAL .And.;
				SE1->E1_PREFIXO == (cAliasSE5)->E5_PREFIXO .And.;
				SE1->E1_NUM == (cAliasSE5)->E5_NUMERO .And.;
				SE1->E1_PARCELA == (cAliasSE5)->E5_PARCELA .And.;
				SE1->E1_TIPO == (cAliasSE5)->E5_TIPO .And.;
				SE1->E1_CLIENTE == (cAliasSE5)->E5_CLIFOR .And.;
				SE1->E1_LOJA == (cAliasSE5)->E5_LOJA

			If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ#DC#D2#MT#M2#JR#J2#CM#C2#CX" .And.;
					(cAliasSE5)->E5_SITUACA <> "C" .And.;
					(cAliasSE5)->E5_RECPAG == "R" .And.;
					IIf(lQuery,.T.,!TemBxCanc())
				nX++					
				If nX == 1
					::PaidBill:Bill := WSClassNew("BillView")
					::PaidBill:Payment := {}
					GetCRBill(@::PaidBill:Bill,"SE1")
				EndIf
				aadd(::PaidBill:PayMent,WSClassNew("PaidBillView"))
				GetCRPaidBill(@::PaidBill:PayMent[nX],cAliasSE5)
			EndIf
			dbSelectArea(cAliasSE5)
			dbSkip()
			lSkip := .T.
		EndDo
		If lQuery
			dbSelectArea(cAliasSE5)
			dbCloseArea()
			dbSelectArea("SE5")
		EndIf
	Else
		lRetorno := .F.
		SetSoapFault("GETPAIDBILL",STR0009) //"Titulo nao encontrado"
	EndIf
Else
	lRetorno := .F.
EndIf
RestArea(aArea)
Return(lRetorno)


/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetCrBill ³Autor  ³ Eduardo Riera         ³ Data ³04.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Montagem da estrutura de titulos financeiros                 ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto da estrutura de titulos                        ³±±
±±³          ³ExpC2: Alias do SE1                                          ³±±
±±³          ³ExpL3: Indica se a otimizacao deve ser utilizada             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta funcao atualiza a estrutura Bill com os dados do titulo ³±±
±±³          ³financeiro                                                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetCRBill(oObjeto,cAliasSE1,lFast)

Local aValores 		:= {}					//Valores retornados pela funcao FaVlAtuCR

DEFAULT lFast := .F.
aValores := FaVlAtuCR(cAliasSE1,dDataBase,"NOR",lFast)
If lFast
	aValores[11] := Nil
	aValores[12] := Nil
EndIF

oObjeto:Prefix                := (cAliasSE1)->E1_PREFIXO
oObjeto:BillNumber            := (cAliasSE1)->E1_NUM
oObjeto:Installment           := (cAliasSE1)->E1_PARCELA	
oObjeto:BillType              := (cAliasSE1)->E1_TIPO
oObjeto:CustomerCode          := (cAliasSE1)->E1_CLIENTE
oObjeto:UnitCustomerCode      := (cAliasSE1)->E1_LOJA

SA1->( DbSetOrder( 1 ) )

If SA1->( MsSeek( xFilial( "SA1" ) + ( cAliasSE1 )->E1_CLIENTE + ( cAliasSE1 )->E1_LOJA ) )
	oObjeto:CustomerName := SA1->A1_NOME
EndIf

oObjeto:ClassCode             := (cAliasSE1)->E1_NATUREZ
oObjeto:IssueDate             := (cAliasSE1)->E1_EMISSAO
oObjeto:OriginalDueDate       := (cAliasSE1)->E1_VENCTO
oObjeto:RealDueDate           := (cAliasSE1)->E1_VENCREA
oObjeto:Currency              := (cAliasSE1)->E1_MOEDA
oObjeto:CurrencySimbol        := SuperGetMV("MV_SIMB"+AllTrim(Str((cAliasSE1)->E1_MOEDA,2)))
oObjeto:OriginalValue         := aValores[1]
oObjeto:OriginalCurrencyValue := xMoeda(aValores[01],(cAliasSE1)->E1_MOEDA,1)
oObjeto:DeductionsValue       := aValores[02]
oObjeto:IncreaseValue         := aValores[05]
oObjeto:DecreaseValue         := aValores[04]
oObjeto:BalanceValue          := aValores[11]
oObjeto:BalanceCurrencyValue  := aValores[12]
oObjeto:Bank                  := (cAliasSE1)->E1_PORTADO
oObjeto:History               := (cAliasSE1)->E1_HIST
oObjeto:DelayInDays           := IIf((cAliasSE1)->E1_VENCREA>dDatabase,dDataBase-(cAliasSE1)->E1_VENCTO,0)
oObjeto:DaylyInterestRate     := (cAliasSE1)->E1_PORCJUR
oObjeto:DaylyInterestValue    := (cAliasSE1)->E1_VALJUR
oObjeto:FinancialDiscount     := (cAliasSE1)->E1_DESCFIN
oObjeto:DaysForDiscount       := (cAliasSE1)->E1_DIADESC
UserFields("SE1",@oObjeto:UserFields,cAliasSE1)

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³GetPaidBil³Autor  ³ Eduardo Riera         ³ Data ³04.12.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Montagem da estrutura de pagamentos dos titulos financeiros  ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto da estrutura de titulos                        ³±±
±±³          ³ExpC2: Alias do SE5                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta funcao atualiza a estrutura Bill com os dados do titulo ³±±
±±³          ³financeiro                                                   ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function GetCRPaidBill(oObjeto,cAliasSE5)

oObjeto:ReasonOfWriteOff    := (cAliasSE5)->E5_MOTBX
oObjeto:Bank                := (cAliasSE5)->E5_BANCO
oObjeto:BankBranch          := (cAliasSE5)->E5_AGENCIA
oObjeto:BankAccount         := (cAliasSE5)->E5_CONTA
oObjeto:PaidDate            := (cAliasSE5)->E5_DATA
oObjeto:CreditDate          := (cAliasSE5)->E5_DTDISPO
oObjeto:Currency            := 1
oObjeto:CurrencySimbol      := SuperGetMv("MV_SIMB1")
oObjeto:ValuePaid           := (cAliasSE5)->E5_VALOR
oObjeto:CurrencyValuePaid   := (cAliasSE5)->E5_VALOR
oObjeto:History             := (cAliasSE5)->E5_HISTOR
oObjeto:ReceiptOrPaymentOrder := (cAliasSE5)->E5_ORDREC
oObjeto:SerialReceipt       := (cAliasSE5)->E5_SERREC
If (cAliasSE5)->E5_TIPODOC $ "VL#BA#V2#CP#LJ"
	oObjeto:WriteOffType        :=  "1"
	oObjeto:TypeDescription  :=  STR0014 //"Principal"
EndIf
If ( (cAliasSE5)->E5_TIPODOC$"DC#D2" )
	oObjeto:WriteOffType        :=  "2"
	oObjeto:TypeDescription  :=  STR0015 //"Desconto"
Endif
If (cAliasSE5)->E5_TIPODOC$"MT#M2"
	oObjeto:WriteOffType        :=  "3"
	oObjeto:TypeDescription  :=  STR0016 //"Multa"
EndIf
If (cAliasSE5)->E5_TIPODOC$"JR#J2"
	oObjeto:WriteOffType        :=  "4"
	oObjeto:TypeDescription  :=  STR0017 //"Juros"
EndIf
If (cAliasSE5)->E5_TIPODOC$"CM#C2#CX"
	oObjeto:WriteOffType        :=  "5"
	oObjeto:TypeDescription  :=  STR0018 //"Correcao Monetaria"
EndIf
UserFields("SE5",@oObjeto:UserFields,cAliasSE5)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³BrwPaidBillQTDE   |                       ³ Data ³           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Método decontagem dos titulos financeiros pagos de um        ³±±
±±³          ³periodo                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do usuario                                     ³±±
±±³          ³ExpC2: Vendedor                                              ³±±
±±³          ³ExpD3: Emissao de                                            ³±±
±±³          ³ExpD4: Emissao ate                                           ³±±
±±³          ³ExpD5: Vencimento de                                         ³±±
±±³          ³ExpD6: Vencimento ate                                        ³±±
±±³          ³ExpD7: Pagamento de                                          ³±±
±±³          ³ExpD8: Pagamento ate                                         ³±±
±±³          ³ExpC9: Expressao a ser adicionada na query                   ³±±
±±³          ³ExpCA: Expressao de ordenacao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Indica que o Método foi avaliado com sucesso          ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ CRM/Materiais/Portais                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÃ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
WSMETHOD BrwPaidBillQTDE WSRECEIVE UserCode,SellerCode,RegisterDateFrom,RegisterDateTo,RealDueDateTo,RealDueDateFrom,DateFrom,DateTo,QueryAddWhere,IndexKey WSSEND QtdeTitulos WSSERVICE FinanceSellerBill

Local aArea     := GetArea()			
Local lRetorno  := .F.					
Local cAliasSE1 := "SE1"		
Local dDataIni  := ::DateFrom	
Local dDataFim  := ::DateTo					
Local cQuery    := ""			

DEFAULT ::IndexKey        := SE1->(IndexKey())
DEFAULT ::RegisterDateFrom:= FirstDay(dDataBase)
DEFAULT ::RegisterDateTo  := LastDay(dDataBase)
DEFAULT ::RealDueDateTo   := LastDay(dDataBase)
DEFAULT ::RealDueDateFrom := FirstDay(dDataBase)

dDataIni := IIF(Empty(dDataIni),Ctod("01/01/1980"),dDataIni)
dDataFim := IIF(Empty(dDataFim),dDataBase,dDataFim)

If PrtChkUser(::UserCode,"FinanceSellerBill","BrwPaidBill","SA3",::SellerCode)

	lRetorno := .T.
	
	cAliasSE1 := "QTDETITULO"			

	cQuery := "SELECT COUNT(*) AS QTDETITULOS "
	cQuery += "FROM "+RetSqlName("SE1")+" SE1,"
	cQuery += RetSqlName("SA1")+" SA1, "
	cQuery += RetSqlName("SE5")+" SE5 "
	cQuery += "WHERE "
	cQuery += "SE1.E1_FILIAL = '"+xFilial("SE1")+"' AND "
	cQuery += "SE1.E1_TIPO NOT IN "+FormatIn(MV_CRNEG+"/"+MVRECANT+"/"+MVABATIM,"/")+" AND "
	cQuery += "SE1.E1_EMISSAO>='"+Dtos(::RegisterDateFrom)+"' AND "
	cQuery += "SE1.E1_EMISSAO<='"+Dtos(::RegisterDateTo)+"' AND "
	cQuery += "SE1.E1_VENCREA>='"+Dtos(::RealDueDateFrom)+"' AND "
	cQuery += "SE1.E1_VENCREA<='"+Dtos(::RealDueDateTo)+"' AND "		
	cQuery += "SE1.E1_SALDO <> E1_VALOR AND "
	cQuery += "SE1.D_E_L_E_T_=' ' AND "
	cQuery += "SA1.A1_FILIAL = '"+xFilial("SA1")+"' AND "
	cQuery += "SA1.A1_COD = SE1.E1_CLIENTE AND "
	cQuery += "SA1.A1_LOJA = SE1.E1_LOJA AND "
	cQuery += "(SA1.A1_VEND = '"+::SellerCode+"' OR SA1.A1_VEND = '"+Space(Len(SA1->A1_VEND))+"') AND "
	cQuery += "SA1.D_E_L_E_T_=' ' AND "
	cQuery += "SE5.E5_FILIAL='"+xFilial("SE5")+"' AND "
	cQuery += "SE5.E5_NATUREZ=SE1.E1_NATUREZ AND "
	cQuery += "SE5.E5_PREFIXO=SE1.E1_PREFIXO AND "
	cQuery += "SE5.E5_NUMERO=SE1.E1_NUM AND "
	cQuery += "SE5.E5_PARCELA=SE1.E1_PARCELA AND "
	cQuery += "SE5.E5_TIPO=SE1.E1_TIPO AND "
	cQuery += "SE5.E5_CLIFOR=SE1.E1_CLIENTE AND "
	cQuery += "SE5.E5_LOJA=SE1.E1_LOJA AND "
	cQuery += "SE5.E5_RECPAG='R' AND "
	cQuery += "SE5.E5_DATA>='"+Dtos(dDataIni)+"' AND "
	cQuery += "SE5.E5_DATA<='"+Dtos(dDataFim )+"' AND "
	cQuery += "SE5.E5_SITUACA<>'C' AND "
	cQuery += "SE5.D_E_L_E_T_=' ' AND NOT EXISTS ("
	cQuery += "SELECT A.E5_NUMERO "
	cQuery += "FROM "+RetSqlName("SE5")+" A "
	cQuery += "WHERE A.E5_FILIAL='"+xFilial("SE5")+"' AND "
	cQuery += "A.E5_NATUREZ=SE5.E5_NATUREZ AND "
	cQuery += "A.E5_PREFIXO=SE5.E5_PREFIXO AND "
	cQuery += "A.E5_NUMERO=SE5.E5_NUMERO AND "
	cQuery += "A.E5_PARCELA=SE5.E5_PARCELA AND "
	cQuery += "A.E5_TIPO=SE5.E5_TIPO AND "
	cQuery += "A.E5_CLIFOR=SE5.E5_CLIFOR AND "
	cQuery += "A.E5_LOJA=SE5.E5_LOJA AND "
	cQuery += "A.E5_SEQ=SE5.E5_SEQ AND "
	cQuery += "A.E5_TIPODOC='ES' AND "
	cQuery += "A.E5_RECPAG<>'R' AND "
	cQuery += "A.D_E_L_E_T_ = ' ') "
	cQuery := WsQueryAdd(cQuery,::QueryAddWhere)	

	cQuery := ChangeQuery(cQuery)
	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSE1)
	::QtdeTitulos := (cAliasSE1)->QTDETITULOS
	
	dbSelectArea(cAliasSE1)
	dbCloseArea()
	
EndIf

RestArea(aArea)

Return(lRetorno)
