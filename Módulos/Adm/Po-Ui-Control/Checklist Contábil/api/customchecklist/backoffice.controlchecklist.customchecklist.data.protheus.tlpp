#include "tlpp-core.th"

namespace totvs.protheus.backoffice.controlchecklist.customchecklist
//-------------------------------------------------------------------
/*/{Protheus.doc} CustomChecklistProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class CustomChecklistProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    Public method getDataCustom()
    Public method getNewDataCustom()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} CustomChecklistProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class CustomChecklistProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} CustomChecklistProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class CustomChecklistProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := CustomChecklistProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca dos gerais do checklist

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cOrigem as Character) class CustomChecklistProtheusData
    Local oSettings := CustomChecklistProtheusData():getData() As Object
    Local cReturn   := "" as Character

    Default cOrigem := ""

    If cOrigem == "CTBA961"
        cReturn := oSettings:getNewDataCustom()
    Else
        cReturn := oSettings:getDataCustom()
    EndIf

return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} getDataCustom
Metodo responsável pela busca dos dados do server do checklist

@author Totvs
/*/
//-------------------------------------------------------------------
method getDataCustom() class CustomChecklistProtheusData
	Local lPECache := ExistBlock("CTBTXCACHE") as Logical
	Local lPECab   := ExistBlock("ECDCHVCAB") as Logical
	Local lPEMov   := ExistBlock("ECDCHVMOV") as Logical
	Local lUsaProc := SuperGetMV("MV_CTBSPRC" ,.F., .T.) as Logical
    Local oReturn  := JsonObject():New() as Json

    oReturn['procedures'] := lUsaProc
    oReturn['ctbtxcache'] := lPECache
    oReturn['ecdchvcab' ] := lPECab
    oReturn['ecdchvmov' ] := lPEMov

return oReturn:toJson()

method getNewDataCustom() class CustomChecklistProtheusData
    Local oReturn  := JsonObject():New() as Json

    oReturn['mv_ctbjob'] := SuperGetMV("MV_CTBJOB",.F.,"2")
    oReturn['mv_atusal'] := SuperGetMv("MV_ATUSAL")

return oReturn:toJson()
