#include "tlpp-core.th"

namespace totvs.protheus.backoffice.controlchecklist.rpochecklist
using namespace totvs.protheus.backoffice.controlchecklist.util

//-------------------------------------------------------------------
/*/{Protheus.doc} RpoChecklistProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class RpoChecklistProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    Public method getDataRpo()
    static method getData() As object
    static method getFileList()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} RpoChecklistProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class RpoChecklistProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} RpoChecklistProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class RpoChecklistProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := RpoChecklistProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca dos gerais do checklist

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cOrigem as Character) class RpoChecklistProtheusData

    Local oSettings := RpoChecklistProtheusData():getData() As Object
    Local cReturn   := "" as Character

    Default cOrigem   := ""

    cReturn := oSettings:getDataRpo(cOrigem)

return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} GetDadosFontes
Metodo responsável pela busca dos dados dos fontes validados no checklist

@author Totvs
/*/
//-------------------------------------------------------------------
method getDataRpo(cOrigem as Character) class RpoChecklistProtheusData
	Local aFontes     := {} as Array
    Local aFonteInfo  := {} as Array
    Local aJson       := {} as Array
    Local oReturn     := JsonObject():New() as Json
    Local cReturn     := "" as Character
    Local nI          := 0  as Numeric

    Default cOrigem   := ""

    ::getFileList(aFontes,cOrigem)

    For nI := 1 to Len(aFontes)
        aFonteInfo := GetAPOInfo(aFontes[nI])
        If Len(aFonteInfo) > 0
            Aadd(aJson,JsonObject():new())
            aJson[nI]['fonte'] := aFonteInfo[1]
            aJson[nI]['data']  := aFonteInfo[4]
            aJson[nI]['hora']  := aFonteInfo[5]
        EndIf
        aFonteInfo := {}
    Next

    If Len(aJson) > 0
        oReturn['items'] := aJson
        cReturn := oReturn:toJson()
    EndIf

    FwFreeArray(aFontes)
    FwFreeArray(aFonteInfo)
    FwFreeArray(aJson)
    FreeObj(oReturn)

return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} GetDadosFontes
Metodo responsável por montar o array aFontes p/ getDataRpo

@author Totvs
/*/
//-------------------------------------------------------------------
method getFileLIst(aFiles as Array,cOrigem as Character) class RpoChecklistProtheusData
    DEFAULT aFiles := {}

    If cOrigem=='CTBA961'
        aAdd(aFiles, "CTBA190.prw")
        aAdd(aFiles, "CTBA192.prw")
        aAdd(aFiles, "CTBA193.prw")
    Else
        aAdd(aFiles, "CTBS001.prw")
        aAdd(aFiles, "CTBS002.prw")
        aAdd(aFiles, "CTBS010.prw")
        aAdd(aFiles, "CTBS011.prw")
        aAdd(aFiles, "CTBS012.prw")
        aAdd(aFiles, "CTBS013.prw")
        aAdd(aFiles, "CTBS020.prw")
        aAdd(aFiles, "CTBS030.prw")
        aAdd(aFiles, "CTBS040.prw")
        aAdd(aFiles, "CTBS050.prw")
        aAdd(aFiles, "CTBS100.prw")
        aAdd(aFiles, "CTBS101.prw")
        aAdd(aFiles, "CTBS102.prw")
        aAdd(aFiles, "CTBS103.prw")
        aAdd(aFiles, "CTBA211.prw")
        aAdd(aFiles, "CTBS300.prw")
        aAdd(aFiles, "CTBS301.prw")
        aAdd(aFiles, "CTBSFUN.prw")
        aAdd(aFiles, "CTBSFUNA.prw")
        aAdd(aFiles, "CTBSTXT.prw")
        aAdd(aFiles, "CTBSVLD.prw")
        aAdd(aFiles, "CTBXATU.prw")
        aAdd(aFiles, "CTBXSAL.prw")
    EndIf

return
