#Include "BACKOFFICE.RECONCILIATION.DELETEREMATCHSETTING.DATA.PROTHEUS.CH"
#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.DeleteAssetGroup
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} DeleteAssetGroupProtheusData
Classe responsavel pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class DeleteAssetGroupProtheusData
Public  method new()
Public  method postByBody()
Public  method getData() as Object
Private method delHeaders()
Private method execDelete()
endclass

/*/{Protheus.doc} DeleteAssetGroupProtheusData
Metodo construtor

@author Totvs
/*/
method new() class DeleteAssetGroupProtheusData
return

/*/{Protheus.doc} getData
Metodo para chamada e validacao do tratamento de dados

@author Totvs
/*/
method getData() class DeleteAssetGroupProtheusData as Object
static __oActiveData as Object

If ValType(__oActiveData) == "U"
    __oActiveData := DeleteAssetGroupProtheusData():new()
EndIf
return __oActiveData

/*/{Protheus.doc} postByBody
Metodo responsavel pela busca e gravacao das delecoes de matches

@author Totvs
/*/
method postByBody(jBody as Json) class DeleteAssetGroupProtheusData
Local aResponse := {} as Array
Local lContinue := .T. as Logical
Local cError    := "" as Character

lContinue := ::delHeaders(jBody,@cError)

If lContinue
    aAdd(aResponse, lContinue)
    aAdd(aResponse, '{}')
Else
    aAdd(aResponse, lContinue)
    aAdd(aResponse, cError)
EndIf

return aResponse

/*/{Protheus.doc} getMatchsetting
Metodo responsavel pela busca dos Matchsetting

@author Totvs
/*/
method delHeaders(jBody as Json, cError as Character) class DeleteAssetGroupProtheusData
Local cCodAssets    as Character
Local lContinue := .T. as Logical

If ValType(jBody["codassets"]) == "C"
    cCodAssets := jBody["codassets"]

    lContinue := ::execDelete(cCodAssets, @cError)
Else
    cError := STR0001 //"Conteúdo do corpo inválido."
    lContinue := .F.
EndIf

return lContinue

/*/{Protheus.doc} execDelete
    Executa os updates para deletar o agrupador de ativos (FM3 e FM4)
    @author user
/*/
Method execDelete(cCodAssets as Character, cError as Character) class DeleteAssetGroupProtheusData
Local cQryUpd as Character
Local cQryUpd2 as Character
Local lContinue := .T. as Logical

cQryUpd := "UPDATE "+RetSQLName("FM3")+" SET D_E_L_E_T_ = '*', R_E_C_D_E_L_ = R_E_C_N_O_ WHERE FM3_FILIAL = '"+xFilial("FM3")+"' AND FM3_CODIGO = '"+cCodAssets+"' AND D_E_L_E_T_ = ' ' "

If TcSqlExec(cQryUpd) <> 0    
    cError += STR0002+"FM3" //"Tabela: "
    lContinue := .F.
EndIf

If lContinue
    cQryUpd2 := "UPDATE "+RetSQLName("FM4")+" SET D_E_L_E_T_ = '*', R_E_C_D_E_L_ = R_E_C_N_O_ WHERE FM4_FILIAL = '"+xFilial("FM4")+"' AND FM4_CODIGO = '"+cCodAssets+"' AND D_E_L_E_T_ = ' ' "
    If TcSqlExec(cQryUpd2) <> 0        
        cError += STR0002+"FM4" //"Tabela: "
        lContinue := .F.
    EndIf
EndIf

If !lContinue
    cError += STR0003+" "+cCodAssets+" "+STR0004  //"DeleteAssetGroupProtheusData Erro: Nao foi possivel deletar a configuração:"
EndIf

Return lContinue
