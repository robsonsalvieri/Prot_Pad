#include "tlpp-core.th"
#include "backoffice.revitalization.sendmail.data.ch"
#include "TOTVS.CH"

namespace totvs.protheus.backoffice.revitalization.sendmail
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMailProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class SendMailProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
    static method SendMailFC()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMailProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class SendMailProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMailProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class SendMailProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := SendMailProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável por chamar o método de envio de email

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(jBody as Json) class SendMailProtheusData
Local cReturn := "" as Character

    cReturn := ::SendMailFC(jBody)

Return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMailFC
Envia email de notificação de fechamento de contabilidade

@author Totvs
/*/
//-------------------------------------------------------------------
method SendMailFC(jBody as Json) class SendMailProtheusData

Local oMail			:= Nil
Local oMessage 		:= Nil
Local cReturn		:= "true"
Local cCC			:= ""
Local cFrom			:= Alltrim(GETMV("MV_RELFROM",.F.,""))
Local cSMTPServer	:= Alltrim(GETMV("MV_RELSERV",.F.,"")) // Indica o endereço ou alias do servidor de e-mail SMTP.
Local cSMTPUser 	:= Alltrim(GETMV("MV_RELACNT",.F.,"")) // Indica a conta de e-mail do usuário no servidor de e-mail.
Local cSMTPPass 	:= Alltrim(GETMV("MV_RELPSW",.F.,"")) // Indica a senha do usuário no servidor de e-mail.
Local lUseTLSMail 	:= SuperGetMv("MV_RELTLS",,.F.)
Local lUseSSLMail 	:= SuperGetMv("MV_RELSSL",,.F.)
Local nSMTPPort		:= GETMV("MV_PORSMTP") // Indica o Indica a porta de comunicação para conexão SMTP (Padrão 25).
Local cMailServer   := "" // Indica o endereço ou alias do servidor de e-mail IMAP/POP/MAPI.
Local nErro			:= 0
Local lRelAuth 		:= GetMv("MV_RELAUTH",.F., .F.)
Local cMensagem	    := jBody["message"]
Local cTO           := jBody["emails"]
Local cAssunto	    := EncodeUTF8(STR0001) //"Agrupador Patrimonial - Novo agrupador cadastrado"
Local cEntry        := GetImageFC() //Verifica imagem para envio de e-mail

Private cError	:= ""
Private lSendOk	:=	.T.

//Valida os parâmetros informados
If Empty(cSMTPServer) .OR. Empty(cSMTPUser) .OR. Empty(cSMTPPass)
    cReturn := STR0002 //"Por favor, Verifique os parametros: MV_RELSERV, MV_RELACNT e MV_RELPSW!"
EndIf

// Caso o endereço do server tenha a porta informada, separo os campos
If(At(":",cSMTPServer) > 0)
    nSMTPPort := Val(Substr(cSMTPServer,At(":",cSMTPServer)+1,Len(cSMTPServer)))
    cSMTPServer := Substr(cSMTPServer,0,At(":",cSMTPServer)-1)
EndIf

oMail := TMailManager():New() //Iniciando conexão com o servidor de e-mails
If(lUseSSLMail)
    oMail:SetUseSSL(lUseSSLMail)
EndIf
If(lUseTLSMail)
    oMail:SetUseTLS(lUseTLSMail)
EndIf
oMail:Init(cMailServer, cSMTPServer , cSMTPUser, cSMTPPass, 0, nSMTPPort)
oMail:SetSmtpTimeOut(120)
nErro := oMail:SmtpConnect()

If lRelAuth //Autenticando o usuário no servidor de e-mails
    nErro := oMail:SmtpAuth(cSMTPUser ,cSMTPPass)
    If nErro <> 0
        // Recupera erro
        cMAilError := oMail:GetErrorString(nErro)
        DEFAULT cMailError := '***UNKNOW***'
        oMail:SMTPDisconnect()
        cReturn := "false"
    Endif
EndIf

cMensagem := StrTran(DecodeUTF8(cMensagem),CHR(10),"<br/>")

If cReturn == "true"
    //Criando o objeto da mensagem do e-mail
    cMsgEmail :='<html>'
    cMsgEmail +=  '<head>'
    cMsgEmail +=    '<meta charset="utf-8" />'
    cMsgEmail +=    '<style>'
    cMsgEmail +=    '.img-container {'
    cMsgEmail +=    'text-align: center;'
    cMsgEmail +=    'display: block;'
    cMsgEmail +=    '}'
    cMsgEmail +=    '</style>'
    cMsgEmail +=  '</head>'
    cMsgEmail +=    '<body>'
    If !Empty(cEntry) //Se encontrou imagem
        cMsgEmail +=        '<span class="img-container">'
        cMsgEmail +=            "<img src='cid:logo_1' alt='LOGO_DEFAULT' height='170' width='800'>"
        cMsgEmail +=        '</span>'
    EndIf
    cMsgEmail +=            '<div style="border: 10px solid transparent; padding: 10px;">'
    cMsgEmail +=                  '</h2>'+cMensagem+'</h2>'
    cMsgEmail +=            '</div>'
    cMsgEmail +=    '</body>'
    cMsgEmail += '</html>'

    oMessage := TMailMessage():New()
    oMessage:Clear()
    oMessage:cFrom		:= cFrom
    oMessage:cTo		:= cTO
    oMessage:cBcc		:= cCC
    oMessage:cSubject	:= cAssunto
    oMessage:cBody      := cMsgEmail
    If !Empty(cEntry) //Se encontrou imagem
        oMessage:MsgBodyType("text/html")
        oMessage:AttachFile(cEntry)
        oMessage:AddAttHTag("Content-ID: <logo_1>")
        oMessage:MsgBodyType("html")
    EndIf

    oMessage:Send( oMail )    
    oMail:SMTPDisconnect()
EndIf

cReturn := '{"message": '+cReturn+'}'

return cReturn

/*/{Protheus.doc} GetImageFC
Verifica a existencia de imagem para e-mail

@author Totvs
@since 01/08/2023
@return cRet - Caminho da imagem na pasta system
/system/fechamento+empresa.bmp
/*/
Static Function GetImageFC()
Local cPathImage := GetSrvProfString("Startpath", "") as character
Local cImage     := Lower(AllTrim("agrupador"+cEmpant+".bmp")) as character //Imagem .BMP
Local cRet       := "" as character

//Verifica se imagem existe na pasta
If File(cPathImage+cImage)
    cRet := cPathImage+cImage
EndIf
Return cRet
