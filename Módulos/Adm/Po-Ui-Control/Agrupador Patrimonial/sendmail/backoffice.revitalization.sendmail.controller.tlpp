#include "tlpp-core.th"
#include "backoffice.accountingclosing.sendmail.controller.ch"

namespace totvs.protheus.backoffice.revitalization.sendmail
using namespace totvs.protheus.backoffice.reconciliation.util
//-------------------------------------------------------------------
/*/{Protheus.doc} SendMail
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class SendMail
    public method new()

    @POST("/api/rt/sendmail/")
    public method getAllSendMail()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} SendMail
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class SendMail
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllSendMail
Metodo que inicia a chamada do envio de email para os destinatários
definidos no fechamento contábil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllSendMail() class SendMail
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cBody     As Character
    Local jBody     As Json
    Local cSMTPServer	:= Alltrim(GETMV("MV_RELSERV",.F.,"")) // Indica o endereço ou alias do servidor de e-mail SMTP.

    //Inicilaiza variaveis
    oService  := SendMailService():getInstance()
    jBody     := JsonObject():new()
    cBody 	  := oRest:GetBodyRequest()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."

    If cSMTPServer == ""
        cResponse := STR0002 //"MV_RELSERV não informado, por favor, configure o servidor de emails via Configurador Protheus."
    Else

        If ValType(cBody) == "C"
            uRet := jBody:fromJson( cBody )
            If ValType(uRet) == "U"
                If ValType(jBody["message"]) == "U"
                    cResponse := STR0003 //"Parâmetro message não informado"
                ElseIf ValType(jBody["emails"]) == "U"
                    cResponse := STR0004 //"Parâmetro emails não informado"
                Else
                    cReturn := oService:getAllSendMail(jBody)
                    lResponse := !Empty(cReturn)

                    If lResponse
                        cChaveRes := "data"
                        cResponse := cReturn
                    EndIf
                EndIf
                //Restaura empresa e filial
                cFilAnt := cFilBkp
            Else
                cResponse := STR0005 // "Conteúdo do corpo inválido"
            EndIf
        EndIf
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse, lResponse)

    //Limpa array
	FWFreeArray()
return
