#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.ctb.balance.model1.controller.ch"

namespace totvs.protheus.backoffice.ctb.balance.model1
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} BalanceModel1
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class BalanceModel1
    public method new()
    @Post("/api/ctb/balance/model1")

    public method getAllGetBalanceModel1()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} BalanceModel1
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class BalanceModel1
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGetBalanceModel1
Metodo que retorna balancete mensal no formato requerido

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllGetBalanceModel1() class BalanceModel1
    Local ojResposta as json
    Local oService  as object
    Local oJBody    as json
    Local lResponse as logical
    Local cChaveRes as character
    Local cResponse as character	
    Local aUrlFilter as array
    Local oJcheckParams as json
    Local cFilBkp   as character
    Local cParamFil as character
    local aFils     as array
    Local cFiliais  as character
    Local nI        as numeric

    aFils   := {}
    cFilBkp := cFilant

    oJBody     := JsonObject():new()
    ojResposta := JsonObject():new()

    aUrlFilter:= tcGetFilters() 

    oService  := GetBalanceModel1Service():getInstance()
      
    oJBody:fromJson(oRest:getBodyRequest())
    cChaveRes := STR0005            // mensagem
    cResponse := STR0006 + STR0002  // Resposta inválida.
                
    oJcheckParams :=  BalanVldPrms(ojBody, aFils)
    If oJcheckParams["isok"] == .F.
        cFilAnt := cFilBkp

       
        If oJBody["INDICADORES"]== 1 
            /*For nI := 1 To Len(aFils)
                cFiliais += aFils[nI]
                
                // Adiciona o separador, exceto no último elemento
                If nI < Len(aFils)
                    cFiliais += "; "
                EndIf
            Next*/
            oJcheckParams['data_inicial']:= oJBody["DATA_INICIAL"]
            oJcheckParams['data_final']:= oJBody["DATA_FINAL"]
            oJcheckParams['branch']:= Iif(empty(cFiliais),oJBody["CNPJ"],cFiliais)
        EndIf
        tcAnswerRest( oJcheckParams, .T.)  
    Else
        // Para acessar outro grupo empresa, deve ser enviado via tenantId
        if !Vazio(oJBody["CNPJ"])
            cParamFil := aFils[1]
        else
            cParamFil := separa( ojBody["FILIAL"], ";" )[1] 
        endIf    

        // Para acessar outro grupo empresa, deve ser enviado via tenantId
        if cParamFil <> cFilAnt .And. FWFilExist(cEmpAnt, cParamFil)
            cFilAnt := cParamFil
        endIf
        

        ojResposta:fromJson( ;
            oService:getAllGetBalanceModel1(oJBody, aUrlFilter, aFils) )

        If oJBody["INDICADORES"]== 1 
            For nI := 1 To Len(aFils)
                cFiliais += aFils[nI]
                
                // Adiciona o separador, exceto no último elemento
                If nI < Len(aFils)
                    cFiliais += "; "
                EndIf
            Next
            ojResposta['data_inicial']:= oJBody["DATA_INICIAL"]
            ojResposta['data_final']:= oJBody["DATA_FINAL"]
            ojResposta['branch']:= Iif(empty(cFiliais),oJBody["CNPJ"],cFiliais)
        EndIf
        
        
        IIf(ValType(ojResposta:GetJsonText("items")) <> "U", ; 
            tcAnswerRest(ojResposta, .T.), ;    
            tcSetResponse(lResponse, cChaveRes, cResponse, .F.) )
    EndIf

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(aUrlFilter)
return
