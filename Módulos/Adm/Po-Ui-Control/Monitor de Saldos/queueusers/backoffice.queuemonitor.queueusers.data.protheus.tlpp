#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.queueusers

//-------------------------------------------------------------------
/*/{Protheus.doc} QueueUsersProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class QueueUsersProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} QueueUsersProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class QueueUsersProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} QueueUsersProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class QueueUsersProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := QueueUsersProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das configuracoes do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(nPage as Numeric, nPageSize as Numeric) class QueueUsersProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := "" As Character
    
    Default nPage     := 1
    Default nPageSize := 30

    addMapFields(self)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery())

    cWhere += "QLG.D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder("QLG.R_E_C_N_O_")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)
    
    oSelf:addMapFields("branch"     , "branch"      , .T., .F., {"branch"       , "C", TamSx3("QLG_FILIAL")[1], 0}, "QLG.QLG_FILIAL")
    oSelf:addMapFields("source"     , "source"      , .T., .F., {"source"       , "C", TamSx3("QLG_ROTINA")[1], 0}, "QLG.QLG_ROTINA")
    oSelf:addMapFields("branchFrom" , "branchFrom"  , .T., .F., {"branchFrom"   , "C", TamSx3("QLG_FILDE" )[1], 0}, "QLG.QLG_FILDE" )  
    oSelf:addMapFields("branchTo"   , "branchTo"    , .T., .F., {"branchTo"     , "C", TamSx3("QLG_FILATE")[1], 0}, "QLG.QLG_FILATE") 
    oSelf:addMapFields("dateFrom"   , "dateFrom"    , .T., .F., {"dateFrom"     , "C", TamSx3("QLG_DATADE")[1], 0}, "QLG.QLG_DATADE")  
    oSelf:addMapFields("dateTo"     , "dateTo"      , .T., .F., {"dateTo"       , "C", TamSx3("QLG_DATATE")[1], 0}, "QLG.QLG_DATATE")  
    oSelf:addMapFields("processDate", "processDate" , .T., .F., {"processDate"  , "C", TamSx3("QLG_DATAP" )[1], 0}, "QLG.QLG_DATAP" )  
    oSelf:addMapFields("processHour", "processHour" , .T., .F., {"processHour"  , "C", TamSx3("QLG_HORA"  )[1], 0}, "QLG.QLG_HORA"  )  
    oSelf:addMapFields("processUser", "processUser" , .T., .F., {"processUser"  , "C", TamSx3("QLG_USER"  )[1], 0}, "QLG.QLG_USER"  )      
    
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("QLG") + " QLG "
    cQuery += " WHERE #QueryWhere#"
Return cQuery
