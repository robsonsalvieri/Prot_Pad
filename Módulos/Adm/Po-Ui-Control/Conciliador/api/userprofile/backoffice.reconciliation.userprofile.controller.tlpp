#include "tlpp-core.th"
#include "backoffice.reconciliation.userprofile.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.userprofile
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} UserProfile
Classe principal de controle, onde e inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class UserProfile
	public method new()

	@Post("/api/tc/userprofile/")
	public method postUserProfile()

	@Get("/api/tc/userprofile/")
	public method getUserProfile()

	@Post("/api/tc/deleteuserprofile/")
	public method deleteUserProfile()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} UserProfile
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class UserProfile
return

//-------------------------------------------------------------------
/*/{Protheus.doc} deleteUserProfile
Metodo que inicia a chamada para exclusao de profile dos usuarios

@author Totvs
/*/
//-------------------------------------------------------------------
method deleteUserProfile() class UserProfile
	::postUserProfile(.T.)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} postUserProfile
Metodo que inicia a chamada para criacao de profile das filiais de 
usuário do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method postUserProfile(lDelete as logical) class UserProfile
	Local oService  := UserProfileService():getInstance() as Object
	Local cBody     := oRest:getBodyRequest() as Object
	Local jBody     := JsonObject():new() as Json
	Local jResponse := JsonObject():new() as Json
	Local aResponse := {} as Array
	Local cFilBkp   := cFilAnt as Character
	Local uRet      := Nil
	Local lPadrao   := .F. as Logical
	Local lResponse := .F. as Logical
	Local cChaveRes := "message" as Character
	Local cResponse := STR0001 as Character //"Conteúdo do corpo inválido."
	Local cOrigem   := oRest:getQueryRequest():GetJsonText("origin")

//Identificador de qual app está chamando esta API
//Se não for enviado a origem, o app que chamou foi o Conciliador (CTBA940)

	If cOrigem == "null"
		cOrigem := "CTBA940"
	EndIf

	Default lDelete := .F.

	If ValType(cBody) == "C"
		uRet := jBody:fromJson(cBody)
		If ValType(uRet) == "U"
			tcGetHeaders() //Posiciona na filial passada pelo tenantId
			aResponse := oService:postUserProfileByBody(jBody, lDelete, cOrigem)

			If ValType(aResponse) == "A" .And. Len(aResponse) > 1
				If aResponse[1]
					If cOrigem == "CTBA961"
						jResponse["config"] := aResponse[2]
					Else
						jResponse["branches"] := aResponse[2]
					EndIf
					oRest:setKeyHeaderResponse("Content-Type", "application/json")
					oRest:setResponse(jResponse)
				EndIf
			EndIf
		Else
			tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)
		EndIf
	EndIf

	FwFreeArray(aResponse)
	FreeObj(oService)

    //Restaura empresa e filial
	cFilAnt := cFilBkp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getUserProfile
Metodo que inicia a chamada das filiais de usuÃ¡rio do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getUserProfile() class UserProfile
	Local oService  := UserProfileService():getInstance() as Object
	Local aResponse := {} as Array
	Local cFilBkp   := cFilAnt as Character
	Local lResponse := .T. as Logical
	Local cChaveRes := "message" as Character
	Local cResponse := STR0002 as Character //"Conteúdo do corpo inválido."
	Local jResponse := JsonObject():new() As Json
	Local cOrigem   := oRest:getQueryRequest():GetJsonText("origin")
	Local cParamType := oRest:getQueryRequest():GetJsonText("codcfg") as Character
    Local cFontSize := oRest:getQueryRequest():GetJsonText("fontsize") as Character

	//Identificador de qual app está chamando esta API 
	//Se não for enviado a origem, o app que chamou foi o Conciliador (CTBA940)
	If cOrigem == "null"
		cOrigem := "CTBA940"
	EndIf

	if cParamType == "null"
		cParamType := ""
	endif

    if cFontSize == "null"
		cFontSize := ""
	else
		cParamType := cFontSize
	endif

	tcGetHeaders() //Posiciona na filial passada pelo tenantId
	aResponse := oService:getUserProfile( cParamType, cOrigem,cFontSize)

	If ValType(aResponse) == "A" .And. Len(aResponse) > 1

		if !Empty(cParamType) .And. aResponse[1]
			jResponse:FromJson(aResponse[2][1])
			oRest:setKeyHeaderResponse("Content-Type", "application/json")
			oRest:setResponse(jResponse)
		elseif aResponse[1]
			If cOrigem == "CTBA961"
				jResponse["config"] := aClone(aResponse[2])
			Else
				jResponse["branches"] := aClone(aResponse[2])
			EndIf
			oRest:setKeyHeaderResponse("Content-Type", "application/json")
			oRest:setResponse(jResponse)
		Else
			cResponse := aResponse[2]
			tcSetResponse(lResponse, cChaveRes, cResponse, .F.)
		EndIf
	EndIf

	FwFreeArray(aResponse)
	FreeObj(oService)

	//Restaura empresa e filial
	cFilAnt := cFilBkp
return
