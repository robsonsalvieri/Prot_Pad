#include "tlpp-core.th"
#include "backoffice.reconciliation.reconciliationheader.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.reconciliationheader
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ReconciliationHeader
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class ReconciliationHeader
    public method new()

    @Get("/api/tc/reconciliationheader/")
    public method getAllReconciliationHeaders()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ReconciliationHeader
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ReconciliationHeader
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllReconciliationHeaders
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllReconciliationHeaders() class ReconciliationHeader
    Local oService  As Object
    Local oField    As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local cFilters  As Character 
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local aFilters  As Array
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	
    Local nI := 0   As Numeric
    Local cNmTabOri As Character
    Local cNmTabDes As Character
    Local cBranchesCondition  As Character 
    Local lRet      As Logical 

    //Inicilaiza variaveis
    oService  := ReconciliationHeaderService():getInstance()
    oField    := tcTreatsFilter():new()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    cFilters  := ""
    aFilters  := {}
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse := "Resposta inválida."
    lRet      := .T.
    cBranchesCondition := GetBranchiesForConditional("QLC")
    
    If cBranchesCondition == ""
        cResponse := STR0001+;//"Não há filiais validas na seleção de filiais que podem ser acessadas desta filial, de acordo com seu compartilhamento de tabelas."+;
                        STR0002//"Acesse a rotina por outra filial ou altere sua seleção de filiais"
        lRet := .F.
    EndIf
    
    If lRet

        tcGetHeaders()

        tcGetPageAndPageSize(@nPage, @nPageSize)

        jResposta:fromJson(oService:getAllReconciliationHeader(nPage, nPageSize, cBranchesCondition))

        If ValType(jResposta:GetJsonText("items")) <> "U" 

            If Len(jResposta["items"]) > 0
                For nI := 1 to Len(jResposta["items"]) 
                    cNmTabOri := FwSX2Util():GetX2Name( jResposta["items"][nI]["tabori"]) 
                    cNmTabDes := FwSX2Util():GetX2Name( jResposta["items"][nI]["tabdes"]) 
                    jResposta["items"][nI]["codusr"] += "-"+ PswChave(jResposta["items"][nI]["codusr"])
                    jResposta["items"][nI]["tabori"] += "-"+cNmTabOri
                    jResposta["items"][nI]["tabdes"] += "-"+cNmTabDes
                Next nI
            EndIf

            lResponse := .T.                    
        EndIf

        IIf(lResponse,; 
            tcAnswerRest(jResposta, .T.),; 
            tcSetResponse(lResponse, cChaveRes, cResponse, .F.))
    Else
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.)
    EndIf

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(aFilters)
return
