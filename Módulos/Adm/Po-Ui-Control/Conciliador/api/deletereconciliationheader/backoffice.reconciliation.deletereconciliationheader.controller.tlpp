#include "tlpp-core.th"
#include "backoffice.reconciliation.deletereconciliationheader.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.deletereconciliationheader
using namespace totvs.protheus.backoffice.reconciliation.util


//-------------------------------------------------------------------
/*/{Protheus.doc} DeleteReconciliationHeader
Classe principal de controle, onde e inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class DeleteReconciliationHeader
public method new()

@Post("/api/tc/deletereconciliationheader/")
public method getDeleteReconciliationHeader()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} DeleteReconciliationHeader
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class DeleteReconciliationHeader
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getDeleteReconciliationHeader
Metodo que inicia a chamada das configuracoes de DeleteReconciliationHeader do 
conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getDeleteReconciliationHeader() class DeleteReconciliationHeader
Local oService  := DeleteReconciliationHeaderService():getInstance() as Object
Local cBody     := oRest:getBodyRequest() as Object
Local jBody     := JsonObject():new() as Json
Local aResponse := {} as Array
Local cFilBkp   := cFilAnt as Character
Local uRet      := Nil
Local lPadrao   := .F. as Logical
Local lResponse := .F. as Logical
Local cChaveRes := STR0001 as Character //"message"
Local cResponse := STR0002 as Character //"Conteúdo do corpo inválido."

If ValType(cBody) == "C"
    uRet := jBody:fromJson(cBody)
    If ValType(uRet) == "U"            
        tcGetHeaders() //Posiciona na filial passada pelo tenantId            
        aResponse := oService:DeleteReconciliationHeaderByBody(jBody)            
        
        If ValType(aResponse) == "A" .And. Len(aResponse) > 1
            If aResponse[1]
                lPadrao   := .T.
                lResponse := aResponse[1]
                cResponse := aResponse[2]                    
            Else
                cResponse := aResponse[2]
            EndIf             
        EndIf        
    EndIf    
EndIf

tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)
FwFreeArray(aResponse)
FreeObj(oService)                

//Restaura empresa e filial
cFilAnt := cFilBkp
return
