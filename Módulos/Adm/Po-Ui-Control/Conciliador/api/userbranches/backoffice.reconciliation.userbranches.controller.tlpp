#include "tlpp-core.th"
#include "totvs.ch"

namespace totvs.protheus.backoffice.reconciliation.userbranches
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} UserBranches
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class UserBranches
    public method new()

    @Get("/api/tc/userbranches/")
    public method getUserBranchess()
    private method getFieldsStrut()
    private method setFieldsStrut()
endclass


//-------------------------------------------------------------------
/*/{Protheus.doc} UserBranches
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class UserBranches
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllUserBranchess
Metodo que inicia a chamada das configuracoes de match do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getUserBranchess() class UserBranches

  Local jResposta := JsonObject():New() As Json

  //Identificador de qual app está chamando esta API
  //Se não for enviado a origem, o app que chamou foi o Conciliador (CTBA940)
  Local cOrigem := oRest:getQueryRequest():GetJsonText("origin")
  
  If cOrigem == "null"
    cOrigem := "CTBA940"
  EndIf
  
  jResposta := userBranches(cOrigem)
  jResposta["fields"] := JsonObject():New()
  jResposta["fields"] := ::getFieldsStrut()

  If ValType(jResposta:GetJsonText("items")) <> "U" .And. jResposta:GetJsonText("items") <> "null"
      tcAnswerRest(jResposta, .T.)  
  EndIf

  FreeObj(jResposta)

return

//-------------------------------------------------------------------
/*/{Protheus.doc} getFieldsStrut
Metodo para retornar o array com a estrutura das colunas

@author Totvs
/*/
//-------------------------------------------------------------------
method getFieldsStrut() class UserBranches
  Local aRetStrut := {}

  ::setFieldsStrut("01","group" ,"Grupo Empresa","C","@!",@aRetStrut)
  ::setFieldsStrut("02","branch","Filial"       ,"C","@!",@aRetStrut)
  ::setFieldsStrut("03","label" ,"Descrição"    ,"C","@!",@aRetStrut)

return aRetStrut

//-------------------------------------------------------------------
/*/{Protheus.doc} getFieldsStrut
Metodo para montar o array com a estrutura das colunas

@author Totvs
/*/
//-------------------------------------------------------------------
method setFieldsStrut(cOrder as Character, cField as Character, cTitle as Character,;
       cType as Character, cPicture as Character, aRetStrut as Array) class UserBranches
  
  DEFAULT cOrder    := ""
  DEFAULT cField    := ""
  DEFAULT cTitle    := ""
  DEFAULT cType     := ""  
  DEFAULT cPicture  := ""
  DEFAULT aRetStrut := {}

  aAdd(aRetStrut, JsonObject():New())
  aRetStrut[Len(aRetStrut)]["order"]   := cOrder
  aRetStrut[Len(aRetStrut)]["field"]   := cField
  aRetStrut[Len(aRetStrut)]["title"]   := cTitle
  aRetStrut[Len(aRetStrut)]["type"]    := cType
  aRetStrut[Len(aRetStrut)]["picture"] := cPicture

return 

