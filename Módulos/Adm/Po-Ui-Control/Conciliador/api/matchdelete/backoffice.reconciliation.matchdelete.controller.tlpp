#include "tlpp-core.th"
#include "backoffice.reconciliation.matchdelete.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.matchdelete
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchDelete
Classe principal de controle, onde e inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class MatchDelete
public method new()

@Post("/api/tc/matchdelete/")
public method getMatchDelete()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} MatchDelete
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class MatchDelete
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getMatchDelete
Metodo que inicia a chamada das configuracoes de matchdelete do 
conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getMatchDelete() class MatchDelete
Local oService  := MatchDeleteService():getInstance() as Object
Local cBody     := oRest:getBodyRequest() as Object
Local jBody     := JsonObject():new() as Json
Local aResponse := {} as Array
Local cFilBkp   := cFilAnt as Character
Local uRet      := Nil
Local lPadrao   := .F. as Logical
Local lResponse := .F. as Logical
Local cChaveRes := "message" as Character
Local cResponse := STR0001 as Character //"Conteúdo do corpo inválido."

If ValType(cBody) == "C"
    uRet := jBody:fromJson(cBody)
    If ValType(uRet) == "U"            
        tcGetHeaders() //Posiciona na filial passada pelo tenantId            
        aResponse := oService:matchDeleteByBody(jBody)            
        
        If ValType(aResponse) == "A" .And. Len(aResponse) > 1
            If aResponse[1]
                lPadrao   := .T.
                lResponse := aResponse[1]
                cResponse := aResponse[2]                    
            Else
                cResponse := aResponse[2]
            EndIf             
        EndIf        
    EndIf    
EndIf

tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)
FwFreeArray(aResponse)
FreeObj(oService)                

//Restaura empresa e filial
cFilAnt := cFilBkp
return
