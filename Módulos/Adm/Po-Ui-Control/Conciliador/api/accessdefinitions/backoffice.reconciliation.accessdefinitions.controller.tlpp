#include "tlpp-core.th"
#include "backoffice.reconciliation.accessdefinitions.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.accessdefinitions
using namespace totvs.protheus.backoffice.reconciliation.util

/*/{Protheus.doc} AccessDefinitions
    Classe principal de controle, onde é inicializado o EndPoint
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
    /*/
Class AccessDefinitions
    public method new()

    @Post("/api/tc/saveaccessdefinitions/")
    public method postSaveAccessDefinitions()

    @Delete("/api/tc/delaccessdefinitions/:code")
    public method deleteAccessDefinitions()
   
    @Get("/api/tc/detailaccessdefinitions/")
    public method getAccessDefinitions()

    private method ValidStructJson() as Logical

EndClass

/*/{Protheus.doc} new
    Metodo construtor
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
/*/
Method new() class AccessDefinitions
Return 

/*/{Protheus.doc} postSaveAccessDefinitions
    Metodo que inicia a chamanda das gravações matchsetting do conciliador
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
/*/
Method postSaveAccessDefinitions() class AccessDefinitions
    Local oService      := AccessDefinitionsSettingService():getInstance() as Object
    Local aResponse     := {} as Array
    Local lResponse     := .F. as Logical
    Local cChaveRes     := "message" as Character
    Local cResponse     := STR0001 as Character // "Json invalido"
    Local oBody         := oRest:GetBodyRequest() as Object
    Local jBody         := JsonObject():new() as Json
    Local uRet          := Nil
    Local cBodyTreated  := "" as Character

    If ValType(oBody) == "C"
        uRet := jBody:fromJson(oBody)
        If ValType(uRet) == "U"
            
            If ::ValidStructJson(jBody, @cResponse) // Valida estrutura    
                
                /* Tratamento para converter o encoding */
                cBodyTreated := jBody:ToJSON()
                cBodyTreated := DecodeUTF8(cBodyTreated, "cp1252")
                uRet := jBody:FromJSON(cBodyTreated)

                If ValType(uRet) == "U"
                    aResponse := oService:postSaveAccessDefinitions(jBody)
                    If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                        lResponse := aResponse[1]
                        cChaveRes := If(lResponse, "code", "message")
                        cResponse := aResponse[2]
                    EndIf
                EndIf
            EndIf
        EndIf
    EndIf
    tcSetResponse(lResponse, cChaveRes, cResponse)
    FwFreeArray(aResponse)
    FreeObj(oService)
Return 

/*/{Protheus.doc} deleteAccessDefinitions
    Metodo que inicia a chamanda das gravações matchsetting do conciliador
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
/*/
Method deleteAccessDefinitions() class AccessDefinitions
    Local oService      := AccessDefinitionsSettingService():getInstance() as Object
    Local aResponse     := {} as Array
    Local lResponse     := .F. as Logical
    Local cChaveRes     := "message" as Character
    Local cResponse     := STR0001 as Character //"Json invalido"
    Local cCode         := oRest:getPathParamsRequest()["code"] as Character
                
        aResponse := oService:deleteAccessDefinitions(cCode)
        If ValType(aResponse) == "A" .And. Len(aResponse) > 1
            lResponse := aResponse[1]
            cChaveRes := If(lResponse, "code", "message")
            cResponse := aResponse[2]
        EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse)
    FwFreeArray(aResponse)
    FreeObj(oService)
Return 

/*/{Protheus.doc} getAccessDefinitions
    Metodo que inicia a chamanda das gravações matchsetting do conciliador
    @author Totvs
    @since 04/04/2022
    @version 12.1.33
/*/
Method getAccessDefinitions() class AccessDefinitions
    Local oService      := AccessDefinitionsSettingService():getInstance() as Object
    Local aResponse     := {} as Array
    Local lResponse     := .F. as Logical
    Local cChaveRes     := "message" as Character
    Local cResponse     := STR0001 as Character  // "Json invalido"
    Local cCode         := tcGetQryParams("code")
                
    aResponse := oService:getAccessDefinitions(cCode)

    If ValType(aResponse) == "A" .And. Len(aResponse) > 1
        lResponse := aResponse[1]
        cChaveRes := If(lResponse, "", "message")
        cResponse := aResponse[2]
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse, lResponse)
    FwFreeArray(aResponse)
    FreeObj(oService)
Return 


/*/{Protheus.doc} ValidStructJson
    Valida estrutura de JSON
    @author user
    @since 11/04/2022
    @version 12.1.33
    @param jBody
    @return lRet
    /*/
Method ValidStructJson(jBody, cResponse) class AccessDefinitions
    Local lRet := .F. as Logical

    If (jBody:hasProperty("descri") .And. jBody:hasProperty("type") .And. jBody:hasProperty("status") .And. ;
        jBody:hasProperty("program") .And. jBody:hasProperty("usersgroups") .And. jBody:hasProperty("save"))
        lRet := .T.
    Else
        cResponse := STR0002 //"Não foram enviadas todas as propriedades esperadas para a API"
    EndIf
Return lRet

