#include "tlpp-core.th"

namespace totvs.protheus.backoffice.reconciliation.actions
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ActionsProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class ActionsProtheusData
    Public  method new()    
    Public  method postByBody() As Array
    Public  method getData() As Object
    Private method tcSetActions()
    Private method tcEffectMovement()
endclass

/*/{Protheus.doc} ActionsProtheusData
Metodo construtor

@author Totvs
/*/
method new() class ActionsProtheusData
return

/*/{Protheus.doc} getData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
method getData() class ActionsProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := ActionsProtheusData():new()
    EndIf
return __oActiveData

/*/{Protheus.doc} postByBody
Metodo responsável pela busca e gravacao dos matches manuais

@author Totvs
/*/
method postByBody(jBody) class ActionsProtheusData
Local aResponse := {} As Array

    aResponse := ::tcSetActions(jBody)

return aResponse

/*/{Protheus.doc} tcSetActions
Metodo responsável pela atualizacao dos matches manuais
nas temporarias e json de retorno

@author Totvs
/*/
method tcSetActions(jBody As Json) class ActionsProtheusData

Local aResponse := {} as Array
Local aUUID     := {} as Array
Local cCodCfg   := "" as Character
Local cEfetAct  := "" as Character
Local cError    := "" as Character
Local cIdMatch  := ""  as Character
Local cOriDes   := ""  as Character
Local cTableDes := ""  as Character
Local cTableOri := ""  as Character
Local lContinue := .F. as Logical
Local oData     := JsonObject():New() as Json

If Valtype(jBody) == "J" 
    If ValType(jBody:GetJsonText("tabori")) == "C"
        cTableOri := jBody:GetJsonText("tabori")        //TRB Origem
    Endif

    If ValType(jBody:GetJsonText("tabdes")) == "C"
        cTableDes := jBody:GetJsonText("tabdes")        //TRB Destino
    Endif

    //Verifica se temporarios existem
    If TCCanOpen(cTableOri) .And. TCCanOpen(cTableDes)    
        If ValType(jBody:GetJsonText("action")) == "C"
            cEfetAct := jBody:GetJsonText("action")        //Função da Ação
        Endif

        If ValType(jBody:GetJsonText("type")) == "C"
            cOriDes := jBody:GetJsonText("type")        //Origem ou Destino
        Endif

        If jBody:hasProperty("uuid")
            aUUID := jBody['uuid']                      //Ids dos registros selecionados
        Endif

        If ValType(jBody:GetJsonText("idmatch")) == "C"
            cIdMatch := jBody:GetJsonText("idmatch")        //Origem ou Destino
        Endif

        If ValType(jBody:GetJsonText("codcfg")) == "C"
            cCodCfg := jBody:GetJsonText("codcfg")        //Codigo da configuração
        Endif

        If ValType(jBody:GetJsonText("data")) == "C"        
            oData:FromJson(jBody:GetJsonText("data"))     //Dados complementares
        Endif

        lContinue := ::tcEffectMovement(cEfetAct , aUUID, cTableOri, cTableDes, cOriDes, @cError, cIdMatch , cCodCfg, oData)

        TcRefresh(cTableOri) // Resetar tempo de timeout
        TcRefresh(cTableDes) // Resetar tempo de timeout
    EndIf
EndIf    

//Retorno
aAdd(aResponse, lContinue)
aAdd(aResponse, cError)

return aResponse


//-------------------------------------------------------------------
/*/{Protheus.doc} tcEffectMovement
    Executar a ação definida pelo usuário para a efetivação 
    @author pequim
    @since 01/02/2023
    @version version
    @param cCodConc, Character, Código do processo de conciliação
/*/
//-------------------------------------------------------------------
Method tcEffectMovement(cEfetAct as Character, aUUID as Array, cTableOri as Character, cTableDes as Character, ;
                         cOriDes as Character, cErro as Character, cIdMatch as Character, cCodCfg as Character, oData as Json) class ActionsProtheusData   //aqui kco
    
    Local bBlock As Codeblock
    Local lRet := .F. As Logical
    
    If FindFunction(cEfetAct)
        bBlock := ErrorBlock( { |e| ErroEfMvt(e,@cErro) } )
        BEGIN SEQUENCE
            cEfetAct := cEfetAct + "(aUUID,cTableOri,cTableDes,cOriDes,cIdMatch,cCodCfg,oData,@cErro)"
            lRet := &cEfetAct
        END SEQUENCE
        ErrorBlock(bBlock)
    Endif

Return lRet

//-------------------------------------------------------------------
 /*/{Protheus.doc} ErroEfMvt
    Obtém descrição do erro ocorrido

    @type  Function
    @author pequim
    @since 14/02/2023
    @version version

    @param bBlock, Block, Bloco do tratamento de erro
/*/
//-------------------------------------------------------------------
Function ErroEfMvt(oError as Object, cErro as Character )
    cErro := oError:description
Return 
