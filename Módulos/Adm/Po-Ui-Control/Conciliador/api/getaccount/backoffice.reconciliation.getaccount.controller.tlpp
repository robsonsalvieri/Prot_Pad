#include "tlpp-core.th"
#include "backoffice.reconciliation.getaccount.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.getaccount
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccount
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class GetAccount
    public method new()

    @Get("/api/tc/getaccount/")
    public method getAllGetAccount()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} GetAccount
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class GetAccount
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllGetAccounts
Metodo que retorna as contas contabeis (Consulta padrão)

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllGetAccount() class GetAccount
    Local oService  As Object
    Local oField    As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local cFilters  As Character 
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local aFilters  As Array
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cPlano	As Character	
	Local cCodEntity As Character
    Local cValue As Character

    //Inicilaiza variaveis
    oService  := GetAccountService():getInstance()
    oField    := tcTreatsFilter():new()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    cFilters  := ""
    cValue    := ""
    aFilters  := {}
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse :=  STR0001 //"Resposta inválida."
    
    tcGetHeaders()
    aFilters := tcGetFilters()
    
    If ValType(aFilters) == "A" .And. Len(aFilters) > 0
        nAt := At("=", aFilters[1][2])
        cFilters := AllTrim(SubStr(aFilters[1][2], nAt+1, Len(aFilters[1][2])))
    EndIf

    tcGetPageAndPageSize(@nPage, @nPageSize)

    If (oRest:getQueryRequest():GetJsonText("cCodEntity") != "null")
        cCodEntity := oRest:getQueryRequest():GetJsonText("cCodEntity")
    Else
        cCodEntity := 'CT1'
    EndIf
    If (oRest:getQueryRequest():GetJsonText("cPlano") != "null")
        cPlano := oRest:getQueryRequest():GetJsonText("cPlano")
    Else
        cPlano := ''
    EndIf
    If (oRest:getQueryRequest():GetJsonText("value") != "null")
        cValue := oRest:getQueryRequest():GetJsonText("value")
    EndIf

    jResposta:fromJson(oService:getAllGetAccount(cCodEntity,cPlano,cValue, cFilters, nPage, nPageSize))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        tcAnswerRest(jResposta, .T.),;    
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray(aFilters)
return
