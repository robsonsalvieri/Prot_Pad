#include "tlpp-core.th"
#include "backoffice.reconciliation.matchdeletetemp.controller.ch"

namespace totvs.protheus.backoffice.reconciliation.matchdeletetemp
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} matchdeletetemp
Classe principal de controle, onde e inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class matchdeletetemp
public method new()

@Post("/api/tc/matchdeletetemp/")
public method postMatchDeleteTemp()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} matchdeletetemp
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class MatchDeleteTemp
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getmatchdeletetemp
Metodo que inicia a chamada das configuracoes de matchdeletetemp do 
conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method postMatchDeleteTemp() class MatchDeleteTemp
Local oService  := matchDeleteTempService():getInstance() as Object
Local cBody     := oRest:getBodyRequest() as Object
Local jBody     := JsonObject():new() as Json
Local aResponse := {} as Array
Local cFilBkp   := cFilAnt as Character
Local uRet      := Nil
Local lPadrao   := .F. as Logical
Local lResponse := .F. as Logical
Local cChaveRes := "mensagem" as Character
Local cResponse := STR0001 as Character //"Conteúdo do corpo inválido."

If ValType(cBody) == "C"
    uRet := jBody:fromJson(cBody)
    If ValType(uRet) == "U"            
        tcGetHeaders() //Posiciona na filial passada pelo tenantId            
        aResponse := oService:matchDeleteTempByBody(jBody)            
        
        If ValType(aResponse) == "A" .And. Len(aResponse) > 1
            If aResponse[1]
                lPadrao   := .T.
                lResponse := aResponse[1]
                cResponse := aResponse[2]                    
            Else
                cResponse := aResponse[2]
            EndIf             
        EndIf        
    EndIf    
EndIf

tcSetResponse(lResponse, cChaveRes, cResponse, lPadrao)
FwFreeArray(aResponse)
FreeObj(oService)                

//Restaura empresa e filial
cFilAnt := cFilBkp
return
