#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.closingdefinitions
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingDefinitionsProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class ClosingDefinitionsProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingDefinitionsProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class ClosingDefinitionsProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingDefinitionsProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class ClosingDefinitionsProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := ClosingDefinitionsProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das definições do fechamento contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(nPage as Numeric, nPageSize as Numeric) class ClosingDefinitionsProtheusData
    Local aArea  := GetArea() As Array
    Local cWhere := GetBranchiesForConditional("QLF", "CTBA960") As Character
    
    Default nPage     := 1
    Default nPageSize := 10

    addMapFields(self)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery())

    cWhere += " AND QLF.D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder("QLF.QLF_FILIAL, QLF.QLF_MODULE")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)

    oSelf:addMapFields("branch", "branch", .T., .F., {"branch", "C", TamSx3("QLF_FILIAL")[1], 0}, "QLF.QLF_FILIAL")
    oSelf:addMapFields("module", "module", .T., .F., {"module", "C", TamSx3("QLF_MODULE")[1], 0}, "QLF.QLF_MODULE")
    oSelf:addMapFields("items" , "items" , .T., .F., {"items" , "M", TamSx3("QLF_ITENS")[1], 0}, "QLF.QLF_ITENS") 
    oSelf:addMapFields("message", "message", .T., .F., {"message", "M", TamSx3("QLF_MESSAG")[1], 0}, "QLF.QLF_MESSAG")      
    oSelf:addMapFields("tables", "tables", .T., .F., {"tables", "M", TamSx3("QLF_TABLES")[1], 0}, "QLF.QLF_TABLES")  

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("QLF") + " QLF "
    cQuery += " WHERE #QueryWhere#"
Return cQuery
