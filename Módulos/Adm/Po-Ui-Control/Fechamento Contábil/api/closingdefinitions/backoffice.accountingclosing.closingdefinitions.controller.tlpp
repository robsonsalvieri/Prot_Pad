#include "tlpp-core.th"
#include "backoffice.accountingclosing.closingdefinitions.controller.ch"

namespace totvs.protheus.backoffice.accountingclosing.closingdefinitions
using namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingDefinitions
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class ClosingDefinitions
    public method new()

    @Get("/api/fc/closingdefinitions/")
    public method getAllClosingDefinitions()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} ClosingDefinitions
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class ClosingDefinitions
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllClosingDefinitions
Metodo que inicia a chamada das configuracoes das definições de fechamento
do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllClosingDefinitions() class ClosingDefinitions
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local cFilters  As Character 
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local nAt       As Numeric
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	

    //Inicilaiza variaveis
    oService  := ClosingDefinitionsService():getInstance()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    cFilters  := ""
    nPage     := 1
    nPageSize := 10
    nAt       := 0
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."
    
    fcGetPageAndPageSize(@nPage, @nPageSize)

    jResposta:fromJson(oService:getAllClosingDefinitions(nPage, nPageSize))

    IIf(ValType(jResposta:GetJsonText("items")) <> "U",; 
        fcAnswerRest(jResposta, .T.),;    
        fcSetResponse(lResponse, cChaveRes, cResponse, .F.))

    //Restaura empresa e filial
    cFilAnt := cFilBkp

    //Limpa array
	FWFreeArray()
return
