#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.pendingitems
using namespace totvs.protheus.backoffice.reconciliation.util

static _lPostgres
static _lOracle

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingItemsProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class PendingItemsProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
    static method getType3() 
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingItemsProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class PendingItemsProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingItemsProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class PendingItemsProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := PendingItemsProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das definições do fechamento contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(jBody as Json) class PendingItemsProtheusData
Local cReturn := "" as Character

    cReturn := ::getType3(jBody)

Return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} getType3
Get type = 3 - Busca os registros pela rotina de contabilização

@author Totvs
/*/
//-------------------------------------------------------------------
method getType3(jBody as Json) class PendingItemsProtheusData

    Local cSQL     := "" as Character
    Local cReturn  := "" as Character
    Local cBranches:= "" as Character
    Local cDataDe  := jBody["datefrom"]
    Local cDataAte := jBody["dateto"]
    Local cTable   := jBody["table"]["alias"]
    Local fieldList := { jBody["table"]["flagfield"],;
                         jBody["table"]["uuidfield"],;
                         jBody["table"]["datefield"],;
                         jBody["table"]["branchfield"],;
                         jBody["table"]["aditionalcondition"]}

    If _lPostgres == Nil
        _lPostgres := Upper(Alltrim(TCGetDB())) $ "POSTGRES"
    EndIf

    If _lOracle == Nil
        _lOracle := Upper(Alltrim(TCGetDB())) $ "ORACLE"
    EndIf
        
    cBranches := getBranchiesForConditional(cTable, "CTBA960")
    
    // Monta o select com base nos dados recebidos
    cSQL := " SELECT "
    cSQL += "   SUM(CASE WHEN "+fieldList[1]+" <> ' ' AND QLI_FILIAL IS NULL THEN 1 ELSE 0 END) AS INTEGRADOS, "
    cSQL += "   SUM(CASE WHEN "+fieldList[1]+" = ' ' AND QLI_FILIAL IS NULL THEN 1 ELSE 0 END) AS PENDENTES, "
    cSQL += "   SUM(CASE WHEN QLI_TABELA = '"+cTable+"' THEN 1 ELSE 0 END) AS DESPREZADOS "
    cSQL += " FROM "+RetSQLName(cTable)+" "+cTable+" " 
    cSQL += " LEFT JOIN "+RetSQLName("QLI")+" QLI ON "            
	cSQL += "   QLI_FILIAL = '"+xFilial("QLI")+"' AND "
	cSQL += "   QLI_TABELA = '"+cTable+"' AND "
    
    If _lPostgres 
        cSQL += "   QLI_UUID = CAST( "+fieldList[2]+" AS VARCHAR ) AND "
    ElseIf _lOracle 
        cSQL += "	RTRIM(QLI_UUID) = "+fieldList[2]+" AND "
    Else
        cSQL += "	QLI_UUID = "+fieldList[2]+" AND "
    EndIf
	
	cSQL += "	QLI.D_E_L_E_T_ = ' ' "
    cSQL += " WHERE "+cBranches+" " 
    cSQL += "   AND "+fieldList[3]+" BETWEEN '"+cDataDe+"' AND '"+cDataAte+"' "
    cSQL += " "+fieldList[5]+" "  // Condição where adicional
    cSQL += " AND "+cTable+".D_E_L_E_T_ = ' ' "
    cReturn := ProcessaQuery(cSQL)
    
return cReturn

//-------------------------------------------------------------------
/*/{Protheus.doc} ProcessaQuery

@author Totvs
/*/
//-------------------------------------------------------------------

Static Function ProcessaQuery(cQuery)
Local cSQL      := "" as Character
Local cReturn   := '{"integrated": 0, "notintegrated": 0}' as Character
Local cAliasQLI := "" as Character
Local nTotal    := 0 as Numeric
Local nPercInt  := 0 as Numeric
Local nPercPen  := 0 as Numeric
Local nPercDes  := 0 as Numeric

    cSQL := " SELECT SUM(INTEGRADOS) INTEGRADOS, SUM(PENDENTES) PENDENTES, SUM(DESPREZADOS) DESPREZADOS "+;
            " FROM ("+cQuery+") TRB "

    cAliasQLI := GetNextAlias()
    dbUseArea( .T., "TOPCONN", TcGenQry(,,cSQL), cAliasQLI, .T., .T. )   

    If !(cAliasQLI)->(Eof())
        nTotal := (cAliasQLI)->INTEGRADOS + (cAliasQLI)->PENDENTES        
        nPercInt := (cAliasQLI)->INTEGRADOS / nTotal * 100
        nPercPen := (cAliasQLI)->PENDENTES  / nTotal * 100               
        nPercDes := (cAliasQLI)->DESPREZADOS  / nTotal * 100  
        cReturn := '{"integrated": '+cValToChar(Round(nPercInt,1))+',"notintegrated": '+cValToChar(Round(nPercPen,1))+',"despised": '+cValToChar(Round(nPercDes,1))+',"tottintegrated": '+cValToChar((cAliasQLI)->INTEGRADOS)+',"totnotintegrated": '+cValToChar((cAliasQLI)->PENDENTES)+',"totdespised": '+cValToChar((cAliasQLI)->DESPREZADOS)+'}'        
    EndIf
    (cAliasQLI)->(dbCloseArea())

Return cReturn
