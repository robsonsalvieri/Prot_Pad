#include "tlpp-core.th"
#include "BACKOFFICE.ACCOUNTINGCLOSING.PENDINGITEMS.CONTROLLER.CH"

namespace totvs.protheus.backoffice.accountingclosing.pendingitems
using namespace totvs.protheus.backoffice.accountingclosing.util
//-------------------------------------------------------------------
/*/{Protheus.doc} PendingItems
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class PendingItems
    public method new()

    @POST("/api/fc/pendingitems/")
    public method getAllPendingItems()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} PendingItems
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class PendingItems
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllPendingItems
Metodo que inicia a chamada das configuracoes das definições de fechamento
do Fechamento Contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllPendingItems() class PendingItems
    Local oService  As Object
    Local jResposta As Json
    Local cFilBkp   As Character
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character	
    Local cBody     As Character
    Local jBody     As Json

    //Inicilaiza variaveis
    oService  := PendingItemsService():getInstance()
    jBody     := JsonObject():new()
    cBody 	  := oRest:GetBodyRequest()
    jResposta := JsonObject():new()
    cFilBkp   := cFilAnt
    lResponse := .F. 
    cChaveRes := "message"
    cResponse := "Resposta inválida."

    If ValType(cBody) == "C"
        uRet := jBody:fromJson( cBody )
        If ValType(uRet) == "U"
            If ValType(jBody["table"]) == "U"
                cResponse := STR0001//"parâmetro table não informado"                
            ElseIf ValType(jBody["type"]) == "U"
                cResponse := STR0002//"parâmetro type não informado"            
            Else
                If fcTableHasMSUIDT(jBody)
                    cReturn := oService:getAllPendingItems(jBody)                
                    lResponse := !Empty(cReturn) 
                
                    If lResponse
                        cChaveRes := "data"
                        cResponse := cReturn
                    EndIf
                Else
                    cResponse := STR0003 + jBody["table"]["alias"] + STR0004 //"A tabela " + jBody["table"]["alias"] + " não possui o campo MSUIDT. Inclua o campo na tabela por meio do WIZARDUUID para conferir as movimentações desta tabela"  
                EndIf
            EndIf
            //Restaura empresa e filial
            cFilAnt := cFilBkp
        Else
            cResponse := "Conteúdo do corpo inválido"
        EndIf
    EndIf

    fcSetResponse(lResponse, cChaveRes, cResponse, lResponse)

    //Limpa array
	FWFreeArray()
return

/*/{Protheus.doc} fcTableHasMSUIDT
    Valida se o campo de UUID da tabela existe para realizar as consultas sem erros
    @type  funcion
    @author totvs
    @since 27/12/2023
    @version 12.1.2310
    @param
        cTable, Character, string da tabela que o campo pertence
        cField, Character, string do campo que sera validado
    @return
        lRet, Logical, variavel lógica indicando se encontrou ou não o campo
    /*/
Function fcTableHasMSUIDT(jBody as Json)

    Local cTable 
    Local cField
    Local lRet as Logical
    
    lRet    := .F.
    cTable  := jBody["table"]["alias"]
    cField  := jBody["table"]["uuidfield"]

    DbSelectArea(cTable)
    If FieldPos(cField) > 0
        lRet := .T.
    EndIf

Return lRet
