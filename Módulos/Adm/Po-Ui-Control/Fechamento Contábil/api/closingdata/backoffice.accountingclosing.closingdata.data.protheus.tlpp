#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.accountingclosing.closingdata.data.protheus.ch"

namespace backoffice.accountingclosing.closingdata
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountClosingData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountClosingData from FWAdapterBaseV2
	public method new()
	public method getInstance()
	public method data_post()
	public method data_get()

	static method getData() As object
endclass
//-------------------------------------------------------------------
/*/{Protheus.doc} new
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class AccountClosingData
	Default cVerbo := "GET"
	Default lList  := .T.
	_Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountClosingData
Responsável por retorar a instancia

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class AccountClosingData
    static __instance As Object
    
    If ValType(__instance) == "U"
        __instance := AccountClosingData():new()
    EndIf
return
//-------------------------------------------------------------------
/*/{Protheus.doc} AccountClosingData
Efetua um get na tabela QLK

@author Totvs
/*/
//-------------------------------------------------------------------
method data_get(oJsUriParams as json, nPage as numeric, nPageSize as numeric ) class AccountClosingData
	local cWhere    as character

	self:SetFields(" QLK_FILIAL, QLK_RANGE, QLK_MOEDA, QLK_BLCALE, QLK_BLPROC, QLK_STATUS, QLK_DATA, QLK_HORA, QLK_USUARI, QLK_CODIGO ")

	addMapFields(self)

	self:SetQuery(" SELECT #QueryFields# FROM " + retSqlName("QLK") + " WHERE #QueryWhere# " )

	cWhere +=	" QLK_FILIAL = '" + fwxFilial("QLK") + "' " + ;	
				" AND D_E_L_E_T_ = ' '  "   	  
	::setWhere( cWhere )	
	
	self:setOrder(" QLK_DATA, QLK_HORA ")
			
	self:setPage(nPage)
	self:setPageSize( nPageSize )

	If self:Execute()
		self:FillGetResponse()
	endIf
return 
//-------------------------------------------------------------------
/*/{Protheus.doc} AccountClosingData
Grava uma novo registro na QLK

@author Totvs
/*/
//-------------------------------------------------------------------
method data_post( oJBody as json) class AccountClosingData
	local cCod  as character
	local cFil as character
	local oJsRes as json

	cFil	:= fwxFilial("QLK")
	cCod 	:= " "
	oJsRes	:= JsonObject():new()

	dbSelectArea("QLK")
	QLK->(dbSetOrder(1))

	QLK->(dbSeek(cFil))
	cCod  := GetSXENum("QLK", "QLK_CODIGO", "QLK_CODIGO", 1)
	confirmSx8()

	recLock("QLK", .T.)

	QLK->QLK_FILIAL	:= cFil
	QLK->QLK_CODIGO	:= cCod	
	QLK->QLK_RANGE	:= oJBody["range"]
	QLK->QLK_MOEDA	:= oJBody["currency"]
	QLK->QLK_STATUS := "1"
	QLK->QLK_DATA	:= dDataBase
	QLK->QLK_HORA	:= time()
	QLK->QLK_USUARI	:= RetCodUsr()
	QLK->QLK_BLCALE := DecodeUTF8((oJBody["blkcalendar"]:toJson()) , "cp1252") 
	QLK->QLK_BLPROC := DecodeUTF8((oJBody["blkprocess"]:toJson()) , "cp1252")
	QLK->(msUnlock())
	
	QLK->(dbCloseArea())

	oJsRes["result"] 	:= .T.
	oJsRes["message"] 	:= STR0001 + cCod + STR0002 + STR0003 + cFil // Regitro + cCod + incluido com sucesso + na filial + cFil

	oRest:setStatusCode ( 200 )
	oRest:setResponse( ojsRes )
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
	Metodo responsável por retornar os fields de retorno

	@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as object)
	oSelf:addMapFields("branch", 		"QLK_FILIAL", .T., .T., {"QLK_FILIAL",	"C", tamSx3("QLK_FILIAL")[1], 0} )
	oSelf:addMapFields("range" , 		"QLK_RANGE", .T., .T., 	{"QLK_RANGE",   "C", tamSx3("QLK_RANGE")[1], 0}  )
	oSelf:addMapFields("currency",		"QLK_MOEDA", .T., .T.,	{"QLK_MOEDA", 	"C", tamSx3("QLK_MOEDA")[1], 0} )
	oSelf:addMapFields("blkcalendar",	"QLK_BLCALE", .T., .T., {"QLK_BLCALE", 	"M", tamSx3("QLK_BLCALE")[1], 0} )			
	oSelf:addMapFields("blkprocess",	"QLK_BLPROC", .T., .T., 	{"QLK_BLPROC", 	"M", tamSx3("QLK_BLPROC")[1], 0}   )
	oSelf:addMapFields("status",		"QLK_STATUS", .T., .T., {"QLK_STATUS", 	"C", tamSx3("QLK_STATUS")[1], 0} )
	oSelf:addMapFields("date_close",	"QLK_DATA", .T., .T., 	{"QLK_DATA", 	"D", tamSx3("QLK_DATA")[1], 0}   )
	oSelf:addMapFields("time", 			"QLK_HORA", .T., .T.,	{"QLK_HORA", 	"C", tamSx3("QLK_HORA")[1], 0}   )
	oSelf:addMapFields("user", 			"QLK_USUARI", .T., .T., {"QLK_USUARI",	"C", tamSx3("QLK_USUARI")[1], 0} )
	oSelf:addMapFields("code",			"QLK_CODIGO", .T., .T., {"QLK_CODIGO", 	"C", tamSx3("QLK_CODIGO")[1], 0} )
Return
