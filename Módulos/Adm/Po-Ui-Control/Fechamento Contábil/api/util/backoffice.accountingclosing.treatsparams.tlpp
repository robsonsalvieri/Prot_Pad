#include "tlpp-core.th"
#include "backoffice.reconciliation.treatsparams.ch"

namespace totvs.protheus.backoffice.accountingclosing.util

//-------------------------------------------------------------------
/*/{Protheus.doc} fcSetResponse
Adiciona resposta padrao na api
Quando lPadrao = T, nao trata e utiliza retorno padrao ja enviado 
pela api

@author Totvs
/*/
//-------------------------------------------------------------------
Function fcSetResponse(lResponse As Logical, cChaveRes As Character, cResponse As Character, lPadrao As Logical)
Local cReturn := "" As Character

If ValType(oRest) == "O"            
    If ValType(lResponse) == "L" .And. ValType(cChaveRes) == "C" .And. ValType(cResponse) == "C"                                 
        oRest:setKeyHeaderResponse("Content-Type", "application/json") 
        
        If lPadrao
            cReturn := cResponse
        Else
            cReturn := If(!Empty(cChaveRes) .And. !Empty(cResponse), '{"'+cChaveRes+'": "'+cResponse+'"}', '{}')                               
        EndIf

        If lResponse                        
            oRest:setResponse(cReturn)
        Else                                                          
            oRest:setFault(cReturn)
        EndIf        
    EndIf
EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} fcGetPageAndPageSize
Retorna pagina e tamanho da pagina para a consulta
@param nPage: página
@param nPage: Tamanho da página

@author Totvs 
/*/
//-------------------------------------------------------------------
Function fcGetPageAndPageSize(nPage as Numeric, nPageSize as Numeric)
    If (oRest:getQueryRequest():GetJsonText("pageSize") != "null")
        nPageSize := Val(oRest:getQueryRequest():GetJsonText("pageSize"))
    EndIf

    If (oRest:getQueryRequest():GetJsonText("page") != "null")
        nPage := Val(oRest:getQueryRequest():GetJsonText("page"))
    EndIf
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fcGetHeaders
Retorna as Empresa e Filial utilizadas

@author Totvs
/*/
//-------------------------------------------------------------------
Function fcGetHeaders()
    Local cFilAtu      As Character
    Local aHeadersApi  As Array

    If (oRest:getHeaderRequest():GetJsonText("tenantId") != "null")
        aHeadersApi := StrTokArr(oRest:getHeaderRequest():GetJsonText("tenantId"), ",")
    
        //Altera Empresa e Filial
        If ValType(aHeadersApi) == "A" .And. Len(aHeadersApi) > 1
            cFilAtu := Upper(AllTrim(aHeadersApi[2]))

            If FWFilExist(cEmpAnt, cFilAtu) //Verifica se encontrou Empresa-Filial. O cEmpAnt já virá do protheus-lib-core
                cFilAnt := cFilAtu
            EndIf
        EndIf
    EndIf
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} fcArrayToObject
Retorna um objeto no formato string para o array enviado

@author Totvs
/*/
//-------------------------------------------------------------------
Function fcArrayToObject(cNameObject as Character, aArrayObject as Array)
Local aReturn := {} as Array

aAdd(aReturn, JsonObject():new())
aReturn[Len(aReturn)][cNameObject] := aArrayObject
Return FWJsonSerialize(aReturn[Len(aReturn)])
