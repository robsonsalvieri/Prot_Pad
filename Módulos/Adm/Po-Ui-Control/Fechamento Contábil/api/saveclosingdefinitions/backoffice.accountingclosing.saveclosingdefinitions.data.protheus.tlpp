#include "tlpp-core.th"
#include "backoffice.accountingclosing.saveclosingdefinitions.data.protheus.ch"

namespace totvs.protheus.backoffice.accountingclosing.saveclosingdefinitions

/*/{Protheus.doc} SaveClosingDefinitionsProtheusData
    (long_description)
    @author Totvs
    @since 01/12/2022
    @version 12.1.33
    /*/
class SaveClosingDefinitionsProtheusData
    Public  method new()
    Public  method getData() as Object
    Public  method postSaveClosingDefinitions() as Array
    Private method validClosingDefinitions() as Array
endClass

/*/{Protheus.doc} new
    Método construtor
    @author Totvs
    @since 01/12/2022
    @version version
    /*/
method new() class SaveClosingDefinitionsProtheusData
return

method getData() class SaveClosingDefinitionsProtheusData as Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := SaveClosingDefinitionsProtheusData():new()
    EndIf
return __oActiveData

/*/{Protheus.doc} postSaveClosingDefinitions
    Método que grava o JSON na tabela QLF
    @author Totvs
    @since 01/12/2022
    @version 12.1.2210
    @param jBody
    @return aValid[2], return_type: character, 
    return_description: aValid contém as mensagens de validação negativas, 
    caso esteja vazio a validação está ok
/*/
Method postSaveClosingDefinitions(jBody) class SaveClosingDefinitionsProtheusData 
    Local aValid        := ::validClosingDefinitions(jBody) as Array
    Local cCodModule    := '' as Character
    Local nX            := 0 as Numeric

    If aValid[1]

        for nX := 1 to Len(jBody["items"])

            cCodModule :=  jBody["items"][nX]["module"]["codmodule"]
            
            // Verificar se é uma alteração ou inclusão nova
            
            If QLF->(dbSeek(xFilial("QLF")+cCodModule))
                RecLock("QLF", .F.)
                QLF->QLF_ITENS  := jBody["items"][nX]["module"]:toJson()
                QLF->QLF_MESSAG := jBody["message"]
                QLF->(MsUnlock())

            Else 
                RecLock("QLF", .T.)
                QLF->QLF_FILIAL := xFilial("QLF")
                QLF->QLF_MODULE := cCodModule
                QLF->QLF_ITENS  := jBody["items"][nX]["module"]:toJson() 
                QLF->QLF_MESSAG := jBody["message"]
                QLF->(MsUnlock())
            EndIf
        next nX

        //Exclusão caso haja modulos a excluir
        If Len(jBody["itemsToRemove"]) > 0
            
            for nX := 1 to Len(jBody["itemsToRemove"])
                
                cCodModule :=  jBody["itemsToRemove"][nX]
                If QLF->(dbSeek(xFilial("QLF")+cCodModule))
                    RecLock("QLF", .F.)
                    QLF->QLF_ITENS := ''
                    QLF->QLF_MESSAG := ''
                    QLF->(MsUnlock())
                EndIf

            next nX
        EndIf
    EndIf

        
Return aValid 

/*/{Protheus.doc} postSaveClosingDefinitions
    Método que realiza a validação JSON recebido
    @author Totvs
    @since 01/12/2022
    @version 12.1.2210
    @param jBody
/*/

Method validClosingDefinitions(jBody) class SaveClosingDefinitionsProtheusData as Array
    Local aValid := {} as Array

    aAdd(aValid, .F.)
    aAdd(aValid, STR0001) //"A tabela de Configurações de Conciliação (QLB) não existe no banco de dados"  

    If ChkFile("QLF")         
        aValid[1] := .T.
        aValid[2] := ""
    EndIf                   

Return aValid
