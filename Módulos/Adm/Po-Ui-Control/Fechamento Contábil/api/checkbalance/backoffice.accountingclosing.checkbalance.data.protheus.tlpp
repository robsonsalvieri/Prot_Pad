#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.checkBalance
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} CheckBalanceProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class CheckBalanceProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    Static method getData() As object  
    Static method getCheckBalance()  
    Static method setBranches()
    Static method delBranches()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} CheckBalanceProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class CheckBalanceProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} CheckBalanceProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class CheckBalanceProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := CheckBalanceProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das configuracoes do conciliador

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll(cDatIni as Character, cDatFin as Character, cCurrency as Character, nPage as Numeric, nPageSize as Numeric, cUUIDBalance as Character) class CheckBalanceProtheusData
    Local cCodUUID := IIF(Empty(cUUIDBalance) .And. nPage == 1, FwUUIDV4(), cUUIDBalance) 
    Local cTipoDB  := Alltrim(Upper(TCGetDB()))

    addMapFields(self, cTipoDB, cCodUUID)
    ::setPage(nPage)
    ::setPageSize(nPageSize)

    If nPage == 1
        ::setBranches(cCodUUID)
        //Executa a procedure de verificação de saldos
        CTBA965(cDatIni, cDatFin, cCurrency, cCodUUID)
		::delBranches(cCodUUID)
    EndIf
    
    ::SetQuery(GetQuery())    
    ::SetWhere(" QLJ_UUID = '"+cCodUUID+"'")
    ::SetOrder("QLJ_DATA, QLJ_CONTA")

    If ::Execute()
        ::FillGetResponse()
    EndIf
    
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object, cTipoDB as Character, cCodUUID as Character)
    Local cIsNull := getIsNull(cTipoDB)

    oSelf:addMapFields("originbranch", "originbranch", .T., .F.,    {"originbranch", "C",   TamSx3("QLJ_FILORI")[1], 0},    "QLJ_FILORI")
    oSelf:addMapFields("datediv", "datediv", .T., .F.,              {"datediv", "D",        TamSx3("QLJ_DATA")[1], 0},      "QLJ_DATA")
    oSelf:addMapFields("account", "account", .T., .F.,              {"account", "C",        TamSx3("QLJ_CONTA")[1], 0},     "QLJ_CONTA")
    oSelf:addMapFields("descr", "descr", .T., .F.,                  {"descr", "C",          TamSx3("CT1_DESC01")[1], 0},    cIsNull+"(CT1_DESC01,'')")
    oSelf:addMapFields("movdeb", "movdeb", .T., .F.,                {"movdeb", "N",         TamSx3("QLJ_MOVDEB")[1], 0},    "QLJ_MOVDEB")
    oSelf:addMapFields("slddeb", "slddeb", .T., .F.,                {"slddeb", "N",         TamSx3("QLJ_SLDDEB")[1], 0},    "QLJ_SLDDEB")  
    oSelf:addMapFields("movcred", "movcred", .T., .F.,              {"movcred", "N",        TamSx3("QLJ_MOVCRED")[1], 0},   "QLJ_MOVCRE")  
    oSelf:addMapFields("sldcred", "sldcred", .T., .F.,              {"sldcred", "N",        TamSx3("QLJ_SLDCRED")[1], 0},   "QLJ_SLDCRE")  
    oSelf:addMapFields("currency", "currency", .T., .F.,            {"currency", "N",       TamSx3("QLJ_MOEDA")[1], 0},     "QLJ_MOEDA")  
    oSelf:addMapFields("costcenter", "costcenter", .T., .F.,        {"costcenter", "N",     TamSx3("QLJ_CUSTO")[1], 0},     "QLJ_CUSTO")  
    oSelf:addMapFields("accoutingitem", "accoutingitem", .T., .F.,  {"accoutingitem", "N",  TamSx3("QLJ_ITEM")[1], 0},      "QLJ_ITEM")  
    oSelf:addMapFields("valueclass", "valueclass", .T., .F.,        {"valueclass", "N",     TamSx3("QLJ_CLVL")[1], 0},      "QLJ_CLVL")  
    oSelf:addMapFields("balancetype", "balancetype", .T., .F.,      {"balancetype", "N",    TamSx3("QLJ_TPSALD")[1], 0},    "QLJ_TPSALD")
    oSelf:addMapFields("entity05", "entity05", .T., .T.,            {"entity05", "C",       TamSx3("QLJ_ENT05")[1], 0},     "QLJ_ENT05")
    oSelf:addMapFields("entity06", "entity06", .T., .T.,            {"entity06", "C",       TamSx3("QLJ_ENT06")[1], 0},     "QLJ_ENT06")
    oSelf:addMapFields("entity07", "entity07", .T., .T.,            {"entity07", "C",       TamSx3("QLJ_ENT07")[1], 0},     "QLJ_ENT07")
    oSelf:addMapFields("entity08", "entity08", .T., .T.,            {"entity08", "C",       TamSx3("QLJ_ENT08")[1], 0},     "QLJ_ENT08")
    oSelf:addMapFields("entity09", "entity09", .T., .T.,            {"entity09", "C",       TamSx3("QLJ_ENT09")[1], 0},     "QLJ_ENT09")
    oSelf:addMapFields("uuid", "uuid", .T., .F.,                    {"uuid", "C",           TamSx3("QLJ_UUID")[1], 0},      "'"+cCodUUID+"'")  

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} GetQuery
Retorna a query que sera utilizada no método getAll

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function GetQuery() as Character    
    Local nLenCompany  := Len(FWSM0Layout(, 1))
    Local nLenUnit     := Len(FWSM0Layout(, 2))
    Local nLenBranchId := Len(FWSM0Layout(, 3))
    Local nLenFilCT1   := 0
    Local nLenSpace    := 0
    Local cQuery       := ""  
    Local cGetDB       := Upper(AllTrim(TCGetDB()))
    Local cSubStr      := IIf(cGetDB $ "ORACLE/POSTGRES","SUBSTR","SUBSTRING")
    Local cConcat      := IIf("SQL"$cGetDB,"+","||")    
    
    If FWModeAccess("CT1",1) = "E"
        nLenFilCT1 += nLenCompany
    Else
        nLenSpace += nLenCompany
    EndIf
    
    If FWModeAccess("CT1",2) = "E"
        nLenFilCT1 += nLenUnit
    Else
        nLenSpace += nLenUnit
    EndIf

    If FWModeAccess("CT1",3) = "E"
        nLenFilCT1 += nLenBranchId
    Else
        nLenSpace += nLenBranchId
    EndIf
    
    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("QLJ") + " QLJ " 
    cQuery += " LEFT JOIN " + RetSqlName("CT1") + " CT1 " 
    cQuery += " ON CT1_FILIAL = "+cSubStr+"(QLJ_FILORI,1,"+cValtoChar(nLenFilCT1)+")"
    
    If "ORACLE"$cGetDB // Tratamento específico para Oracle
        cQuery += cConcat+"'"+Space(nLenSpace)+"'"
    EndIf

    cQuery += " AND CT1_CONTA = QLJ_CONTA "
    cQuery += " AND CT1.D_E_L_E_T_ = ' ' "
    cQuery += " WHERE #QueryWhere#"

Return cQuery

//-------------------------------------------------------------------
/*/{Protheus.doc} getIsNull
Retorna a instrução de ISNULL de acordo com o banco de dados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getIsNull(cTipoDB)
    Local cIsNull := ""
    If ("INFORMIX" $ cTipoDB) .Or. ("ORACLE" $ cTipoDB)
        cIsNull  := " NVL"
    ElseIf ("DB2" $ cTipoDB)  .Or. ("POSTGRES" $ cTipoDB)
        cIsNull := " COALESCE"
    Else
        cIsNull := " ISNULL"
    EndIf
Return cIsNull

/*/{Protheus.doc} methodName
    (long_description)
    @author user
    @since 07/01/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
method setBranches(cCodUUID as Character) Class CheckBalanceProtheusData
DEFAULT cCodUUID := ""

C965SetFil("CT2", cCodUUID)
    
Return 

/*/{Protheus.doc} methodName
    (long_description)
    @author user
    @since 07/01/2024
    @version version
    @param param_name, param_type, param_descr
    @return return_var, return_type, return_description
    /*/
method delBranches(cCodUUID as Character) Class CheckBalanceProtheusData
DEFAULT cCodUUID := ""

C965DelFil(cCodUUID)
    
Return 
