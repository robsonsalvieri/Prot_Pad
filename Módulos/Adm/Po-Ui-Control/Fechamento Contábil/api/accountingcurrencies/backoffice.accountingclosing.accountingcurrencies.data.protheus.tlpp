#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accountingclosing.accountingcurrencies
using namespace totvs.protheus.backoffice.accountingclosing.util
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingCurrenciesProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingCurrenciesProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAll()
    static method getData() As object
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingCurrenciesProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as Character, lList as Logical) Class AccountingCurrenciesProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingCurrenciesProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class AccountingCurrenciesProtheusData As Object
    static __oActiveData As Object

    If ValType(__oActiveData) == "U"
        __oActiveData := AccountingCurrenciesProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAll
Metodo responsável pela busca das definições do fechamento contabil

@author Totvs
/*/
//-------------------------------------------------------------------
method getAll() class AccountingCurrenciesProtheusData
    Local aArea  := GetArea() As Array

    addMapFields(self)

    ::SetQuery(getQuery())

    ::SetWhere(getWhere())

    ::SetOrder(" CTO_FILIAL, CTO_MOEDA ")

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serão retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as Object)
    
    oSelf:addMapFields("branch",    "branch",   .T., .F., {"branch",    "C", TamSx3("CTO_FILIAL")[1], 0}, "CTO_FILIAL")
    oSelf:addMapFields("currency",  "currency", .T., .F., {"currency",  "C", TamSx3("CTO_MOEDA ")[1], 0}, "CTO_MOEDA")
    oSelf:addMapFields("description" , "description" ,    .T., .F., {"description" ,     "C", TamSx3("CTO_DESC  " )[1], 0}, "CTO_DESC" ) 
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() As Character
    Local cQuery As Character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM " + RetSqlName("CTO") + " CTO "
    cQuery += " WHERE #QueryWhere#"

Return cQuery


//-------------------------------------------------------------------
/*/{Protheus.doc} getWhere
Função que retorna o where que será utilizado na busca
@param 
    cType, character, Tipo de busca que será utilizado para retornar os calendários 
        "1" para calendários e "2" para processos" 
@return
    cWhere, character, clausula where que será utiizado na busca
@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getWhere()
    Local cWhere AS Character
    
    cWhere +=   " CTO_FILIAL = '"+xFilial("CTO")+"' AND   CTO.D_E_L_E_T_ = ' '"

Return cWhere
