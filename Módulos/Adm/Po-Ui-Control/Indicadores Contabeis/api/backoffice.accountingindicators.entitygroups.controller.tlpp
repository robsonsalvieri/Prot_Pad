#include "tlpp-core.th"
#include "backoffice.accountingindicators.entitygroups.controller.ch"

namespace totvs.protheus.backoffice.accountingindicators.entitygroups
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityGroups
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class EntityGroups

    Data jResposta	as json
    Data Page       as Numeric
	Data PageSize   as Numeric
	Data aFieldsApi as Array
	Data aUrlFilter as Array
    Data jBody      as json
    Data oService   as Object

    private Data nOpcAuto as numeric

    public method new()

    @Get("/api/rt/entitygroups/")
    public method getAllEntityGroups()

    @Post("/api/rt/entitygroups/")
    public method postEntityGroups()

    @Delete("/api/rt/entitygroups/")
    public method deleteEntityGroups()

    @Get("/api/rt/entitygroups/getnextcode/") // pega o proximo contag
    public method getNextCode()

    @Get("/api/rt/entitygroups/verifyexistcode/:id") // verica se já existe o contag informado
    public method verifyExistCode()

    private method postSaveEntityGroups()
    private method ValidStructJson() as Logical

endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityGroups
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class EntityGroups
	::page       := 1
	::pagesize   := 10
	::aFieldsApi := {}
	::aUrlFilter := {}
    ::oService   := EntityGroupsService():getInstance()
	::jResposta  := JsonObject():New()
    ::jBody 	 := JsonObject():New()
return


//-------------------------------------------------------------------
/*/{Protheus.doc} postEntityGroups
    Metodo que inicia a chamada post para gravar as entidades contabeis
    @autor Totvs
/*/
//-------------------------------------------------------------------
method postEntityGroups() class EntityGroups
    ::nOpcAuto := 4 // inclusão / alteração
    ::postSaveEntityGroups()  
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} getAllEntityGroups
Metodo que retorna as contas contabeis (Consulta padrão)
@author Totvs
/*/
//-------------------------------------------------------------------
method getAllEntityGroups() class EntityGroups
    Local oService  As Object
    Local jResposta As Json
    Local nPage     As Numeric
    Local nPageSize As Numeric
    Local lResponse As Logical
    Local cChaveRes As Character
    Local cResponse As Character
    Local cSearch   As Character
    Local cCodPla   As Character
    Local cConTag   As Character
    Local cOrdem    As Character

    //Inicilaiza variaveis
    oService  := EntityGroupsService():getInstance()
    jResposta := JsonObject():new()
    nPage     := 1
    nPageSize := 10
    lResponse := .F.
    cChaveRes := "message"
    cResponse :=  STR0001 //"Resposta inválida."

    cSearch   := oRest:getQueryRequest():GetJsonObject("search")
    cCodPla   := oRest:getQueryRequest():GetJsonObject("codpla") 
    cConTag   := oRest:getQueryRequest():GetJsonObject("contag")
    cOrdem    := oRest:getQueryRequest():GetJsonObject("ordem")

    cSearch := Iif(ValType(cSearch) == "C", cSearch, "")
    cCodPla := Iif(ValType(cCodPla) == "C", cCodPla, "")
    cConTag := Iif(ValType(cConTag) == "C", cConTag, "")
    cOrdem  := Iif(ValType(cOrdem) == "C", cOrdem, "")

    tcGetPageAndPageSize(@nPage, @nPageSize)
    jResposta:fromJson(oService:getEntityGroupsService(cSearch, cCodPla, cConTag, cOrdem, nPage, nPageSize))
    
    IIf(ValType(jResposta:GetJsonObject("items")) <> "U",; 
        tcAnswerRest(jResposta, .T.),;    
        tcSetResponse(lResponse, cChaveRes, cResponse, .F.))
return

//-------------------------------------------------------------------
/*/{Protheus.doc} deleteEntityGroups
Metodo que retorna as contas contabeis (Consulta padrão)
@author Totvs
/*/
//-------------------------------------------------------------------
method deleteEntityGroups() class EntityGroups
    ::nOpcAuto := 5
    ::postSaveEntityGroups()
return


//-------------------------------------------------------------------
/*/{Protheus.doc} ValidStructJson
    Valida estrutura de JSON
    @author Totvs
/*/
//-------------------------------------------------------------------
Method ValidStructJson(jBody, cResponse) class EntityGroups
    Local lRet := .F. as Logical
    If (jBody:hasProperty("entitys") .And. ValType(jBody:GetJsonObject("entitys")) == "A")
        lRet := .T.
    Else
        cResponse := STR0002 //"Não foram enviadas todas as propriedades esperadas para a API."
    EndIf
Return lRet


//-------------------------------------------------------------------
/*/{Protheus.doc} postSaveEntityGroups
    Metodo que inicia a chamanda das gravações das entidades contabeis na CTS
    
    @author Totvs
/*/
//-------------------------------------------------------------------
Method postSaveEntityGroups() class EntityGroups
    Local oService      := EntityGroupsService():getInstance() as Object
    Local aResponse     := {} as Array
    Local lResponse     := .F. as Logical
    Local cChaveRes     := "message" as Character
    Local cResponse     := "" as Character 
    Local oBody         := oRest:GetBodyRequest() as Object
    Local jBody         := JsonObject():new() as Json
    Local uRet          := Nil
    Local cBodyTreated  := "" as Character

    cResponse := STR0003 // "Json invalido"

    If ValType(oBody) == "C"
        uRet := jBody:fromJson(oBody)
        If ValType(uRet) == "U"
            
            If ::ValidStructJson(jBody, @cResponse) // Valida estrutura    
                
                /* Tratamento para converter o encoding */
                cBodyTreated := jBody:ToJSON()
                cBodyTreated := DecodeUTF8(cBodyTreated, "cp1252")
                uRet := jBody:FromJSON(cBodyTreated)

                If ValType(uRet) == "U"
                    aResponse := oService:postSaveEntityGroups(jBody, ::nOpcAuto)
                    If ValType(aResponse) == "A" .And. Len(aResponse) > 1
                        lResponse := aResponse[1]
                        cChaveRes := If(lResponse, "code", "message")
                        cResponse := aResponse[2] + '-' + aResponse[3]
                    EndIf
                EndIf
            EndIf
        EndIf
    EndIf

    tcSetResponse(lResponse, cChaveRes, cResponse)
    
    FwFreeArray(aResponse)
    FreeObj(oService)
Return 


//-------------------------------------------------------------------
/*/{Protheus.doc} getNextCode
    Metodo que retorna o proximo codigo do agrupador contábil e o tamanho do campo CTS_CONTAG, CTS_DESCCG e os tipos de saldos contabeis

    @return JResposta - Json com o proximo codigo do agrupador contábil e o tamanho do campo CTS_CONTAG e CTS_DESCCG
    @author Totvs
/*/
//-------------------------------------------------------------------
method getNextCode() class EntityGroups
    Local jResposta     As Json
    Local oSettings     As Object
    Local aTpSaldos     As Array
    Local aTpSaldosAux  As Array
    Local JTpSaldo      As Json
    Local  nX           As Numeric

    oRest:setKeyHeaderResponse("Content-Type", "application/json")
    oSettings := EntityGroupsProtheusData():getData()
    cCodPla   := oSettings:getCodPla()
    cNextCode := oSettings:getNextCode(cCodPla)

    jResposta := JsonObject():new()
    aTpSaldos := {}
    nX        := 0

    // cContag   := MpSysExecScalar("SELECT MAX(CTS_CONTAG) NUMMAX FROM "+RetSqlName("CTS")+" WHERE CTS_FILIAL = '" + xFilial("CTS") + "' AND CTS_ENTGRP = '1' ", "NUMMAX")
    // Pegando SX5 da SL - Saldos contabeis
    aTpSaldosAux := FWGetSX5("SL")

    for nX := 1 to len(aTpSaldosAux)
        JTpSaldo := JsonObject():new()
        JTpSaldo["label"] := aTpSaldosAux[nX][4]
        JTpSaldo["value"] := AllTrim(aTpSaldosAux[nX][3])

        aAdd(aTpSaldos, JTpSaldo)
    next

    jResposta["contag"]    := cNextCode
    jResposta["tamcontag"] := TamSx3("CTS_CONTAG")[1]
    jResposta["tamdesccg"] := TamSx3("CTS_DESCCG")[1]
    jResposta["tpsaldos"]  := aTpSaldos

    oRest:setResponse(jResposta)
Return


//-------------------------------------------------------------------
/*/{Protheus.doc} verifyExistCode
    Metodo que verifica se o codigo informado já existe em um agrupaodr contábil

    @param cCod - Codigo a ser verificado
    @author Totvs
/*/
//-------------------------------------------------------------------
method verifyExistCode() class EntityGroups
    Local jResposta As Json
    Local oSettings  As Object

    oRest:setKeyHeaderResponse("Content-Type", "application/json")

    oSettings := EntityGroupsProtheusData():getData()
    cCod      := oRest:getPathParamsRequest():GetJsonText('id')
    cCodPla   := oSettings:getCodPla()
    jResposta := JsonObject():new()
    jResposta["exist"] := oSettings:verifyExistCode(cCodPla, cCod)

    oRest:setResponse(jResposta)
Return 




