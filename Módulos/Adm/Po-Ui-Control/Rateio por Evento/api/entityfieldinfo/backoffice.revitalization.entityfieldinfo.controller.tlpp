#include "tlpp-core.th"
#include "backoffice.revitalization.entityfieldinfo.controller.ch"

namespace totvs.protheus.backoffice.revitalization.entityfieldinfo
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityFieldInfo
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class EntityFieldInfo
    public method new()

    @Get("/api/ctb/entityfieldinfo/")
    public method getEntityFieldInfo()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} EntityFieldInfo
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class EntityFieldInfo
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getEntityFieldInfo
Metodo que inicia a chamada dos agrupadores do ativo fixo

@author Totvs
/*/
//-------------------------------------------------------------------
method getEntityFieldInfo() class EntityFieldInfo
    local oService  as object
    local oJcheckParams as object
    local jResposta as json
    local lResponse as logical
    local cChaveRes as character
    local cResponse as character
    local cEntity   as character
    local cField    as character
    local cValue    as character
    local cRange    as character

    oService  := EntityFieldInfoService():getInstance()
    jResposta := JsonObject():new()
    lResponse := .F.
    cChaveRes := "message"
    cResponse := STR0001 //"Resposta inválida."
    cEntity   := ""
    cField    := ""
    cValue    := ""

    If oRest:getQueryRequest():hasProperty("entity")
        cEntity := oRest:getQueryRequest():GetJsonText("entity")
    EndIf

    If oRest:getQueryRequest():hasProperty("field")
        cField := oRest:getQueryRequest():GetJsonText("field")
    EndIf

    If oRest:getQueryRequest():hasProperty("value")
        cValue := oRest:getQueryRequest():GetJsonText("value")
    EndIf

    If oRest:getQueryRequest():hasProperty("range")
        cRange := oRest:getQueryRequest():GetJsonText("range")
    EndIf

	If !Vazio(cValue) .and. !Vazio(cRange) 
        oJcheckParams :=  oService:getEntityValueRangeData(cEntity, cField, cValue, cRange)
    Else
        oJcheckParams :=  oService:checkMandatoryParams(cEntity, cField)
    EndIf
    
    if oJcheckParams["result"] == .F.
       tcAnswerRest( oJcheckParams, .T.)
    else
    	
        If Vazio(cValue) .or. Vazio(cRange) 
            jResposta := oService:getEntityData(cEntity, cField)
        Else
        	jResposta := oService:getEntityValueRangeData(cEntity, cField, cValue, cRange)
        EndIf

        oRest:setResponse(jResposta)
    endIf

return
