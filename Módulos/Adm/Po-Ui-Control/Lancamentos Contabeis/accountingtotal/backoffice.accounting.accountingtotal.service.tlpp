#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accounting.accountingtotal

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingTotalService
Classe de servico, realizada instância e chamada de metodos de servicos

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingTotalService
    public method new()
    public method getInstance()
    public method getTotal()
    
    private method setTotal()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingTotalService
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountingTotalService
return

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingTotalService
Responsavel por instanciar a classe controller

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class AccountingTotalService
    static __instance As Object
    
    If ValType(__instance) == "U"
        __instance := AccountingTotalService():new()
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getTotal
Responsavel por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getTotal() class AccountingTotalService
    Local cIdProces := "" as character
    Local cCurrency := "01" as character //Moeda 01 default
    Local cType     := "" as character    
    Local cAliasTmp := GetNextAlias() as character
    Local aResponse := {} as array
    Local oTotalData := AccountingTotalProtheusData():new()
    
    getParamsTotal(@cIdProces, @cCurrency, @cType)
    oTotalData:getTotal(cAliasTmp, cIdProces, cCurrency, cType)
    aResponse := ::setTotal(cAliasTmp)

    If Select(cAliasTmp) > 0
        (cAliasTmp)->(dbCloseArea())
    EndIf
    
    FreeObj(oTotalData)
return aResponse

/*/{Protheus.doc} getParamsTotal
Alimenta parametros que serao utilizados na api
@since 10/07/2023
@param cIdProces, cCurrency, cType
@author Totvs
@return
/*/
Static Function getParamsTotal(cIdProces as character, cCurrency as character, cType as character)
If oRest:getQueryRequest():hasProperty("idproces")
    cIdProces := oRest:getQueryRequest():GetJsonText("idproces")
EndIf

If oRest:getQueryRequest():hasProperty("type")
    cType := oRest:getQueryRequest():GetJsonText("type") //1 = Inconsistentes | 2 = Sem inconsistencia
EndIf

If oRest:getQueryRequest():hasProperty("currency")
    cCurrency := oRest:getQueryRequest():GetJsonText("currency") //01-02-03-04-05
EndIf
Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} setTotal
Responsavel pela criacao do Json com os totalizadores dos lancamentos
contabeis

@author Totvs
/*/
//-------------------------------------------------------------------
method setTotal(cAliasTmp as character) class AccountingTotalService   
    Local cLote      := "" as character
    Local cSbLote    := "" as character
    Local cDocto     := "" as character
    Local nPosTotal  := 0  as numeric
    Local nPosLote   := 0  as numeric
    Local nPosSbLote := 0  as numeric
    Local nPosDocto  := 0  as numeric
    Local aResponse  := {} as array    

    //Nivel totais gerais
    aAdd(aResponse, JsonObject():new())
    nPosTotal++
    
    //Inicializa totais gerais
    aResponse[nPosTotal]["total debito"]  := 0
    aResponse[nPosTotal]["total credito"] := 0
    aResponse[nPosTotal]["total"] := 0
    aResponse[nPosTotal]["lotes"] := {} //Cria array para nivel de lotes

    While (cAliasTmp)->(!EOF())
        //Alimenta valores totais gerais
        aResponse[nPosTotal]["total debito"]  += (cAliasTmp)->TOTALDEB
        aResponse[nPosTotal]["total credito"] += (cAliasTmp)->TOTALCRED
        aResponse[nPosTotal]["total"] += (cAliasTmp)->TOTAL
        
        //Nivel lotes
        If Empty(cLote) .Or. cLote <> (cAliasTmp)->CT2_LOTE            
            cLote := (cAliasTmp)->CT2_LOTE
            aAdd(aResponse[nPosTotal]["lotes"], JsonObject():new())            
            nPosLote := Len(aResponse[nPosTotal]["lotes"])

            //Inicializa lotes
            aResponse[nPosTotal]["lotes"][nPosLote]["lote"] := (cAliasTmp)->CT2_LOTE
            aResponse[nPosTotal]["lotes"][nPosLote]["total debito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["total credito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["total"] := 0                                            
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"] := {} //Cria o array para o nivel sublotes            
        EndIf
        //Alimenta valores lotes
        aResponse[nPosTotal]["lotes"][nPosLote]["total debito"] += (cAliasTmp)->TOTALDEB
        aResponse[nPosTotal]["lotes"][nPosLote]["total credito"] += (cAliasTmp)->TOTALCRED
        aResponse[nPosTotal]["lotes"][nPosLote]["total"] += (cAliasTmp)->TOTAL
                
        //Nivel sublotes
        If Empty(cSbLote) .Or. cSbLote <> (cAliasTmp)->CT2_LOTE+(cAliasTmp)->CT2_SBLOTE
            cSbLote := (cAliasTmp)->CT2_LOTE+(cAliasTmp)->CT2_SBLOTE
            aAdd(aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"], JsonObject():new())            
            nPosSbLote := Len(aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"])

            //Inicializa sublotes
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["sublote"] := (cAliasTmp)->CT2_SBLOTE
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total debito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total credito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total"] := 0                                    
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"] := {} //Cria array para nivel documentos            
        EndIf
        //Alimenta valores sublotes
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total debito"] += (cAliasTmp)->TOTALDEB
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total credito"] += (cAliasTmp)->TOTALCRED
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["total"] += (cAliasTmp)->TOTAL
        
        //Nivel documentos        
        If Empty(cDocto) .Or. cDocto <> (cAliasTmp)->CT2_LOTE+(cAliasTmp)->CT2_SBLOTE+(cAliasTmp)->CT2_DOC
            cDocto := (cAliasTmp)->CT2_LOTE+(cAliasTmp)->CT2_SBLOTE+(cAliasTmp)->CT2_DOC
            aAdd(aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"], JsonObject():new())            
            nPosDocto := Len(aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"])

            //Inicializa documentos           
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["documento"] := (cAliasTmp)->CT2_DOC
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total debito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total credito"] := 0
            aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total"] := 0
        EndIf
        //Alimenta valores documentos
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total debito"] += (cAliasTmp)->TOTALDEB
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total credito"] += (cAliasTmp)->TOTALCRED
        aResponse[nPosTotal]["lotes"][nPosLote]["sublotes"][nPosSbLote]["documentos"][nPosDocto]["total"] += (cAliasTmp)->TOTAL
        
        (cAliasTmp)->(dbSkip())    
    EndDo
return aResponse
