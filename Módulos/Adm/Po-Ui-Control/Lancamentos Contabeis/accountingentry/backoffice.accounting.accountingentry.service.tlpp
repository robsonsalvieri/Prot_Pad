#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accounting.accountingentry
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryService
Classe de serviço, realizada instância e chamada de métodos de serviços

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingEntryService
    public method new()
    public method getInstance()
    public method getAccountingEntry()    
    private method getAccountingStructure()
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryService
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountingEntryService
return

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryService
Responsável por instanciar a classe controller

@author Totvs
/*/
//-------------------------------------------------------------------
method getInstance() class AccountingEntryService
    static __instance as object
    
    If ValType(__instance) == "U"
        __instance := AccountingEntryService():new()
    EndIf
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingEntry
Responsavel por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingEntry(nOption as numeric, aFieldSX3 as array) class AccountingEntryService
    Local cIdProces := "" as character
    Local cType     := "" as character
    Local cCurrency := "" as character
    Local cFilter   := "" as character
    Local nPage     := 1 as numeric
    Local nPageSize := 10 as numeric    
    Local aFieldsCT2 := {} as array   
    Local aNotFieldsCT2 := {} as array
    Local oSettings := AccountingEntryProtheusData():getData() as object    
    Local jResponse := JsonObject():new() as Json
   
    tcGetHeaders()
    tcGetPageAndPageSize(@nPage, @nPageSize)
    getParamsCT2(@cIdProces, @cType, @cCurrency, @cFilter, aFieldsCT2, aNotFieldsCT2)
    aFieldSX3 := ::getAccountingStructure(aFieldsCT2, aNotFieldsCT2)
    
    If nOption == 1 .Or.  nOption == 2 //Método para recuperar lancamentos contabeis
        oSettings:getAllAccountingEntry(cIdProces, cType, cCurrency, cFilter, aFieldSX3, nPage, nPageSize)               
        jResponse := oSettings:getJSONResponse()
    EndIf
            
    aFieldsCT2    := {}
    aNotFieldsCT2 := {}    
    FwFreeArray(aFieldsCT2)
    FwFreeArray(aNotFieldsCT2)

    oSettings:DeActivate()    
    FreeObj(oSettings)    
return jResponse

/*/{Protheus.doc} getParamsCT2
Alimenta parametros que serao utilizados na api
@since 04/07/2023
@param cIdProces, cType, cCurrency
@author Totvs
@return
/*/
Static Function getParamsCT2(cIdProces as character, cType as character, cCurrency as character,;
                                cFilter as character, aFieldsCT2 as array, aNotFieldsCT2 as array)

If oRest:getQueryRequest():hasProperty("idproces")
    cIdProces := oRest:getQueryRequest():GetJsonText("idproces")
EndIf

If oRest:getQueryRequest():hasProperty("type")
    cType := oRest:getQueryRequest():GetJsonText("type") //1 = Inconsistentes | 2 = Sem inconsistencia
EndIf

If oRest:getQueryRequest():hasProperty("currency")
    cCurrency := oRest:getQueryRequest():GetJsonText("currency") //01-02-03-04-05
EndIf

If oRest:getQueryRequest():hasProperty("filter")
    cFilter := oRest:getQueryRequest():GetJsonText("filter")
EndIf

If oRest:getQueryRequest():hasProperty("fields")
	aFieldsCT2 := StrTokArr(Upper(oRest:getQueryRequest():GetJsonText("fields")), ",")
EndIf

If oRest:getQueryRequest():hasProperty("notfields")
	aNotFieldsCT2 := StrTokArr(Upper(oRest:getQueryRequest():GetJsonText("notfields")), ",")
EndIf

Return Nil

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingStructure
Responsavel por chamar a consulta e parsear o retorno no formato JSON

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingStructure(aFieldsCT2 as array, aNotFieldsCT2 as array) class AccountingEntryService
Local cField     := "" as character
Local cTitulo    := "" as character
Local nI         := 0 as numeric
Local aAllFields := FWSX3Util():GetAllFields("CT2", .F.) as array
Local aStruct    := {} as array
Local aResponse  := {} as array

For nI := 1 To Len(aAllFields)
    cField  := aAllFields[nI]
    aStruct := FWSX3Util():GetFieldStruct(cField, .F.)
        
    If Len(aFieldsCT2) > 0 //Se parametro fields enviado, considera apenas os campos contidos no array aFieldsCT2
        If aScan(aFieldsCT2, cField) == 0
            Loop //Se campo nao encontrado
        EndIf
    EndIf
    If Len(aNotFieldsCT2) > 0 //Se parametro notfields enviado, desconsidera os campos contidos no array aNotFieldsCT2
        If aScan(aNotFieldsCT2, cField) > 0
            Loop //Se campo encontrado
        EndIf
    EndIf

    cTitulo := AllTrim(FWX3Titulo(cField))
    aAdd(aResponse,{GetSx3Cache(cField, "X3_ORDEM"),;
                    AllTrim(Lower(cField)),;
                    AllTrim(If(Empty(cTitulo), Lower(cField), cTitulo)),;
                    aStruct[2],;
                    aStruct[3],;
                    aStruct[4],;
                    AllTrim(X3Picture(cField)),;
                    ""})    
Next nI

aAllFields := {}
aStruct    := {}
FwFreeArray(aAllFields)
FwFreeArray(aStruct)

return aResponse
