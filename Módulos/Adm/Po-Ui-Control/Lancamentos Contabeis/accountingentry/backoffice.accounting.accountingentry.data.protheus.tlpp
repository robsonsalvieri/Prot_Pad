#include "tlpp-core.th"

namespace totvs.protheus.backoffice.accounting.accountingentry
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryProtheusData
Classe responsável pela consulta de dados

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingEntryProtheusData from FWAdapterBaseV2
    Public method new()
    Public method getAllAccountingEntry()
    static method getData() as object    
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryProtheusData
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new(cVerbo as character, lList as Logical) class AccountingEntryProtheusData
    Default cVerbo := "GET"
    Default lList  := .T.
    _Super:New(cVerbo, lList)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntryProtheusData
Método para chamada e validação do tratamento de dados

@author Totvs
/*/
//-------------------------------------------------------------------
method getData() class AccountingEntryProtheusData as object
    static __oActiveData as object

    If ValType(__oActiveData) == "U"
        __oActiveData := AccountingEntryProtheusData():new()
    EndIf
return __oActiveData

//-------------------------------------------------------------------
/*/{Protheus.doc} getAllAccountingEntry
Metodo responsável pela busca dos lancamentos contabeis

@author Totvs
/*/
//-------------------------------------------------------------------
method getAllAccountingEntry(cIdProces as character, cType as character, cCurrency as character, cFilter as character,;
                                aFieldSX3 as array, nPage as numeric, nPageSize) class AccountingEntryProtheusData            
    Local aArea  := GetArea() as Array
    Local cWhere := "" as character    
    
    Default nPage     := 1
    Default nPageSize := 10

    addMapFields(self, aFieldSX3)

    ::setPage(nPage)
    ::setPageSize(nPageSize)
    ::SetQuery(getQuery())
   
    cWhere += "LOWER(CT2_PROCES) = '"+Padr(Lower(cIdProces), TamSx3("CT2_PROCES")[1])+"'"
    If AllTrim(cType) == "1" .Or. AllTrim(cType) == "2" //1 = Inconsistentes | 2 = Sem inconsistencia
        cWhere += " AND CT2_INCONS = '"+AllTrim(cType)+"'"
    EndIf
    If !Empty(cCurrency)
        cWhere += " AND CT2_MOEDLC = '"+AllTrim(cCurrency)+"'"
    EndIf
    cWhere += " AND D_E_L_E_T_ = ' '"

    ::SetWhere(cWhere)
    ::SetOrder("CT2_FILIAL, CT2_DATA, CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_LINHA, CT2_MOEDLC")
    
    If !Empty(cFilter)
        ::SetUrlFilter({{"FILTER", cFilter}})
    EndIf

    If ::Execute()
        ::FillGetResponse()
    EndIf

    RestArea(aArea)
    FwFreeArray(aArea)
return

//-------------------------------------------------------------------
/*/{Protheus.doc} addMapFields
Realiza o mapeamento dos campos que serao retornados

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function addMapFields(oSelf as object, aFieldSX3 as array)
    Local cField    := "" as character
    Local cConcat   := tcRetConcat()+"'|'"+tcRetConcat() as character    
    Local nI        := 0 as numeric
    Local nTamField := TamSx3("CT2_EMPORI")[1]+TamSx3("CT2_FILIAL")[1]+TamSx3("CT2_DATA")[1]+TamSx3("CT2_LOTE")[1]+;
                        TamSx3("CT2_SBLOTE")[1]+TamSx3("CT2_DOC")[1]+TamSx3("CT2_LINHA")[1]+TamSx3("CT2_EMPORI")[1]+;
                        TamSx3("CT2_FILORI")[1]+TamSx3("CT2_MOEDLC")[1]+TamSx3("CT2_SEQIDX")[1] as numeric
        
    //Chave única CT2: CT2_FILIAL+DTOS(CT2_DATA)+CT2_LOTE+CT2_SBLOTE+CT2_DOC+CT2_LINHA+CT2_EMPORI+CT2_FILORI+CT2_MOEDLC+CT2_SEQIDX
    oSelf:addMapFields("value", "value", .T., .F., {"value", "C", nTamField, 0}, "'"+cEmpAnt+"'"+cConcat+"RTRIM(CT2_FILIAL)"+;
                        cConcat+"CT2_DATA"+cConcat+"CT2_LOTE"+cConcat+"CT2_SBLOTE"+cConcat+"CT2_DOC"+cConcat+"CT2_LINHA"+cConcat+;
                        "CT2_EMPORI"+cConcat+"CT2_FILORI"+cConcat+"CT2_MOEDLC"+cConcat+"CT2_SEQIDX")
    
    For nI := 1 To Len(aFieldSX3)
        cField := Upper(aFieldSX3[nI][2])
        oSelf:addMapFields(cField, cField, .T., .F., {cField, aFieldSX3[nI][4], aFieldSX3[nI][5], aFieldSX3[nI][6]}, cField)
    Next nI   
Return

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
Realiza a query para busca de informações

@author Totvs
/*/
//-------------------------------------------------------------------
Static Function getQuery() as character
    Local cQuery as character

    cQuery := " SELECT #QueryFields#"
    cQuery += " FROM "+RetSqlName("CT2")
    cQuery += " WHERE #QueryWhere#"
Return cQuery
