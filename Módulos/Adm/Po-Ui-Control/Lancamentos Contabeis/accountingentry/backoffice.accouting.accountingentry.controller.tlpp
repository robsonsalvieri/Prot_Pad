#include "tlpp-core.th"
#include "backoffice.accouting.accountingentry.controller.ch"

namespace totvs.protheus.backoffice.accounting.accountingentry
using namespace totvs.protheus.backoffice.reconciliation.util

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntry
Classe principal de controle, onde é inicializado o EndPoint

@author Totvs
/*/
//-------------------------------------------------------------------
class AccountingEntry
    public method new()    

    @Get("/api/ctb/accountingentry/")
    public method getAccountingAll()

    @Get("/api/ctb/accountingentry/accountingentries/")
    public method getAccountingEntries()

    @Get("/api/ctb/accountingentry/struct/")
    public method getAccountingStruct()

    private method getAccountingEntry()     
endclass

//-------------------------------------------------------------------
/*/{Protheus.doc} AccountingEntry
Metodo construtor

@author Totvs
/*/
//-------------------------------------------------------------------
method new() class AccountingEntry
return

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingAll
Metodo que inicia a chamada dos lancamentos contabeis e estrutura
dos campos - SX3

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingAll() class AccountingEntry
return ::getAccountingEntry(1)

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingEntries
Metodo que inicia a chamada dos lancamentos contabeis

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingEntries() class AccountingEntry
return ::getAccountingEntry(2)

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingStruct
Metodo que inicia a chamada Metodo que inicia a chamada da estrutura 
dos campos - SX3

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingStruct() class AccountingEntry
return ::getAccountingEntry(3)

//-------------------------------------------------------------------
/*/{Protheus.doc} getAccountingEntry
Metodo que inicia a chamada conforme parametro recebido:
1 - Lancamentos contabeis e estrutura dos campos - SX3
2 - Lancamentos contabeis
3 - Estrutura dos campos - SX3

@author Totvs
/*/
//-------------------------------------------------------------------
method getAccountingEntry(nOption as numeric) class AccountingEntry
    Local oService  := AccountingEntryService():getInstance() as object    
    Local jResponse := Jsonobject():new() as json
    Local cFilBkp   := cFilAnt as character     
    Local cResponse := STR0001 as character //"Inconsistência encontrada na API."
    Local aJsonSX3  := {} as Array
    Local aFieldSX3 := {} as array
    Local oField    := tcTreatsFilter():new() as object
    Local lResponse := .F. as logical
               
    If CT2->(FieldPos("CT2_PROCES")) > 0 .And. CT2->(FieldPos("CT2_INCONS")) > 0
        jResponse:fromJson(oService:getAccountingEntry(nOption, aFieldSX3))
        If ValType(jResponse) == "J"
            //Lancamentos contabeis e estrutura SX3
            If (nOption == 1 .Or. nOption == 2) .And. jResponse:hasProperty("items")
                lResponse := .T.                
            Else
                lResponse := .T.
            EndIf
        EndIf

        //Adiciona informacoes SX3 dos campos no Json de retorno            
        If lResponse
            If (nOption == 1 .And. Len(jResponse["items"]) > 0) .Or. nOption == 3
                aJsonSX3 := oField:tcSetStruct(aFieldSx3)
                If Len(aJsonSX3) > 0                                        
                    If  nOption == 3 //Somente estrutura adiciona items para padronizar retorno
                        jResponse["items"]  := {}
                    EndIf                    
                    jResponse["fields"] := aJsonSX3
                EndIf
            EndIf
        EndIf
    Else
        cResponse := STR0002 //"Campos CT2_PROCES e CT2_INCONS não encontrados na base de dados"
        lResponse := .F.
    EndIf
                        
    If lResponse
        tcAnswerRest(jResponse, .T.)
    Else
        tcSetResponse(.F., "message", cResponse, .F.)
    EndIf
    
    //Restaura empresa e filial
    cFilAnt := cFilBkp

    aJsonSX3  := {}
    aFieldSX3 := {}
    FwFreeArray(aJsonSX3)
    FwFreeArray(aFieldSX3)    
return
