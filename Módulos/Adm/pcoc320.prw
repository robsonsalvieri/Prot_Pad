#include "pcoc320.ch"
#include "protheus.ch"
/*/
_F_U_N_C_

Ŀ
FUNCAO     PCOC320   AUTOR  Edson Maricate         DATA  26.11.2003 
Ĵ
DESCRICAO  Programa de Consulta ao arquivo de saldos mensais dos Cubos  
Ĵ
 USO       SIGAPCO                                                      
Ĵ
_DOCUMEN_  PCOC320                                                      
_DESCRI_   Programa de Consulta ao arquivo de saldos mensair dos Cubos  
_FUNC_     Esta funcao podera ser utilizada com a sua chamada normal    
           partir do Menu ou a partir de uma funcao pulando assim o     
           browse principal e executando a chamada direta da rotina     
           selecionada.                                                 
           Exemplo: PCOC320(2) - Executa a chamada da funcao de visua-  
                                 zacao da rotina.                       
Ĵ
_PARAMETR_ ExpN1 : Chamada direta sem passar pela mBrowse               
ٱ


*/
Function PCOC320(nCallOpcx)

Local bBlock
Local nPos
Private cCadastro	:= STR0001//"Consulta Saldos Mensais de Cubos"
Private aRotina 	:= MenuDef()

	If nCallOpcx <> Nil
		nPos := Ascan(aRotina,{|x| x[4]== nCallOpcx})
		If ( nPos # 0 )
			bBlock := &( "{ |x,y,z,k| " + aRotina[ nPos,2 ] + "(x,y,z,k) }" )
			Eval( bBlock,Alias(),AL4->(Recno()),nPos)
		EndIf
	Else
		mBrowse(6,1,22,75,"AKT")
	EndIf

Return


Function PCOC320ViewChv()

Local aArea := GetArea()
Local aAreaAKW := AKW->(GetArea())
Local cCubo   := M->AKT_CONFIG
Local cSaldo := ""
Local nIni:= 1, nFim := 1
Local aQebra := {}, cAux
Local cTitle

dbSelectArea("AKW")
dbSetOrder(1)
dbSeek(xFilial()+cCubo)

While !Eof() .And. xFilial()+cCubo == AKW->AKW_FILIAL+AKW->AKW_COD
	
	cTitle := AllTrim(AKW->AKW_DESCRI)
	cAux := &(AllTrim(AKW->AKW_CHAVER))
	nFim := Len(cAux)
	cAux := Subs(M->AKT_CHAVE, nIni, nFim)
	cSaldo += cTitle+Space(20-Len(cTitle)*2)+chr(9)+ " :   "+cAux+C320Descri(cAux)+CRLF
	nIni += nFim
	dbSkip()

End

RestArea(aAreaAKW)
RestArea(aArea)

Return(cSaldo)
   

Static Function C320Descri(cAux)

Local aArea := GetArea(cAux)
Local cDescri := ""
dbSelectArea(AKW_ALIAS)
dbSetOrder(1)

If dbSeek(xFilial()+cAux) .And. ;
	AllTrim(AKW->AKW_DESCRE) != AllTrim(AKW->AKW_RELAC)
	cDescri := " - "+&(AllTrim(AKW->AKW_DESCRE))
EndIf	
RestArea(aArea)

Return(cDescri)


/*/


Ŀ
Programa  MenuDef    Autor  Ana Paula N. Silva      Data 29/11/06 
Ĵ
Descrio  Utilizacao de menu Funcional                               
Ĵ
Retorno   Array com opcoes da rotina.                                 
Ĵ
ParametrosParametros do array a Rotina:                               
          1. Nome a aparecer no cabecalho                             
          2. Nome da Rotina associada                                 
          3. Reservado                                                
          4. Tipo de Transao a ser efetuada:                        
          		1 - Pesquisa e Posiciona em um Banco de Dados     	  
              2 - Simplesmente Mostra os Campos                       
              3 - Inclui registros no Bancos de Dados                 
              4 - Altera o registro corrente                          
              5 - Remove o registro corrente do Banco de Dados        
          5. Nivel de acesso                                          
          6. Habilita Menu Funcional                                  
Ĵ
   DATA    Programador   Manutencao efetuada                         
Ĵ
                                                                     
ٱ


/*/

Static Function MenuDef()
Local aRotina 	:= {	{ STR0002,		"AxPesqui" , 0 , 1},; //"Pesquisar"
							{ STR0003, 	"AxVisual" , 0 , 2} } //"Visualizar"
						
If AMIIn(57) // AMIIn do modulo SIGAPCO ( 57 )
	//Ŀ
	// Adiciona botoes do usuario no Browse                                   
	//
	If ExistBlock( "PCOC3201" )
		//P_EĿ
		//P_E Ponto de entrada utilizado para inclusao de funcoes de usuarios no     
		//P_E browse da tela de Centros Orcamentarios                                
		//P_E Parametros : Nenhum                                                    
		//P_E Retorno    : Array contendo as rotinas a serem adicionados na enchoice 
		//P_E               Ex. :  User Function PCOC3201                            
		//P_E                      Return {{"Titulo", {|| U_Teste() } }}             
		//P_E
		If ValType( aUsRotina := ExecBlock( "PCOC3201", .F., .F. ) ) == "A"
			AEval( aUsRotina, { |x| AAdd( aRotina, x ) } )
		EndIf
	EndIf      
EndIf

Return(aRotina)