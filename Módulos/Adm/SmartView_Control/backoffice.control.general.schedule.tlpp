#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "FWMVCDEF.CH"
#include "fwlibversion.ch"
#include "backoffice.control.general.schedule.ch"

namespace totvs.protheus.backoffice.control.general

//-------------------------------------------------------------------
/*/{Protheus.doc} Schedule
    Classe de utilitários para manipulação de tarefas e agendamentos 
    do Schedule.

    @author Renan Leite Gremes
    @since 15/04/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
class Schedule
	public method new()         as object
	public method createTask()

    protected method validEnvironment()

    public data cError	        as character
    public data lSuccess	    as logical    
    public data lCreateTask	    as logical
    public data lMessageSuccess  as logical
endClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
    Método Construtor

    @return object: self

    @author Renan Leite Gremes
    @since 15/04/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method new() as object class Schedule
    self:cError         := ""
    self:lSuccess	    := .F.
    self:lMessageSuccess := .F.
    self:lCreateTask    := .T.
    self:validEnvironment()
return self

//-------------------------------------------------------------------
/*{Protheus.doc} createTask
    Encapsula a criação de task para executar em segundo plano.

    @return object: self

    @author Renan Leite Gremes
    @since 15/04/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method createTask(cRoutine as character, cSX1Group as character, nModule as numeric) class Schedule
    local cUserCode         := RetCodUsr()        as character
    local oParams           := FWSX1Util():new()  as object
    local lSchedule 	    := FWGetRunSchedule() as logical
    local cMessage          := ""          as character
    local cFunction         := ""          as character
    local nX                := 0           as numeric
    local lInterfaceParams  := .T.         as logical
    local aPergs, aMVParams := {}          as array
    local oTask as object

    default cRoutine  := ""
    default cSX1Group := ""
    default nModule   := 1
	
    if self:lCreateTask .and. !empty(cRoutine)
        //Recupera parâmetros da rotina
        aPergs := totvs.framework.schedule.utils.getParamsMV(cRoutine)

        //Se não conseguir recuperar os parâmetros na rotina, pega pelo cSX1Group
        if len(aPergs) < 1 .and. !empty(cSX1Group)
            //Recupera a estrutura dos parâmetros
            if oParams:ExistPergunte(cSX1Group)
                oParams:AddGroup(Alltrim(cSX1Group))
                oParams:SearchGroup()

                aPergs := oParams:GetGroup(Alltrim(cSX1Group))[2]
            endif 
        endif

        aMVParams := Array(Len(aPergs), "")

		for nX := 1 to len(aPergs)
			aMVParams[nX] := &('MV_PAR' + strZero(nX,2) )
		next nX        

        //Cria o agendamento
        oTask := totvs.framework.schedule.utils.createTask( getEnvServer(),; 
                                                cEmpAnt,; 
                                                cFilAnt,; 
                                                cRoutine,;
                                                nModule,; 
                                                cUserCode,; 
                                                /*uParam*/,;
                                                aMVParams,; 
                                                .T.,; 
                                                lInterfaceParams )
        //Monta a mensagem de sucesso
        if !empty(oTask:cID)
            self:lSuccess := .T.
            cFunction := SubStr(oTask:cFunction, 1, At("(", oTask:cFunction)-1)
            cMessage := STR0001 + CRLF + CRLF				// 'Rotina inserida na fila de processamento com sucesso!'
            cMessage += STR0002 + cFunction + CRLF	        // 'Nome da Rotina: '
            cMessage += STR0003 + oTask:cID + CRLF + CRLF	// 'ID Tarefa: '
            cMessage += STR0004								// 'Acompanhe no Visualizador de Eventos a evolução do processamento da sua rotina'
            
            if !lSchedule .And. self:lMessageSuccess
                fwAlertSuccess(cMessage, STR0005)			// 'TOTVS'
            EndIf

            conout("[SUCCESS][SMARTSCHEDULE] " + STR0006 + ' ' + STR0007 + cFunction + STR0008)	// ">> Programa agendado com sucesso" # " " Agendamento da função " " # " cadastrada com sucesso"
        endif
    endif

    FreeObj(oParams)
    FreeObj(oTask)
    FWFreeArray(aPergs)
    FWFreeArray(aMVParams)
	
return

//-------------------------------------------------------------------
/*{Protheus.doc} validEnvironment
    Valida a versão minima da LIB a ser utilizada no ambiente

    @return object: self

    @author Renan Leite Gremes
    @since 15/04/2024
    @version 12.1.2310
*/
//-------------------------------------------------------------------
method validEnvironment(cMinimumLibLabel as character) class Schedule
    local lRunningSmartSched	as logical

    default cMinimumLibLabel := "20240408"
 
	lRunningSmartSched	:= totvs.framework.smartschedule.startSchedule.smartSchedIsRunning()

    //Válida o mínimo da Lib Label
	if FwLibVersion() < cMinimumLibLabel
		self:lCreateTask := .F.
        self:cError := STR0009 + cMinimumLibLabel // "Necessário Lib Label igual ou superior a "
		conout("[WARN][SMARTSCHEDULE] " + self:cError)	
    endif

    //Válida se o smart schedule está habilitado e em execução
	if self:lCreateTask .and. !(lRunningSmartSched)
		self:lCreateTask := .F.
        self:cError := if(!lRunningSmartSched, STR0010, STR0011) // "Necessário que o Smart Schedule esteja em execução em seu ambiente." # "Necessário que o Smart Schedule esteja habilitado em seu ambiente."
		conout("[WARN][SMARTSCHEDULE] " + self:cError)	
	endif

return
