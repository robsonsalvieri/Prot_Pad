#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-rest.th"
#include "tlpp-core.th"
#include 'PROTHEUS.CH'
#include 'FWMVCDEF.CH'

namespace control.sv.functions.utils

//-------------------------------------------------------------------
/*/{Protheus.doc} CTBsetValueMVPAR
    Copia da funcao CtbCfCubo
    Funç?o que determina o range das entidades a serem filtradas na query
    @author     
    @since      
    @version    
/*/
//-------------------------------------------------------------------
Function CTBsetValueMVPAR(oJsonMvPar as json, cPergunte as character)

	Local nY	    := 0  as numeric
	Local nJson	    := Len(oJsonMvPar:GetNames()) as numeric
	Local oJson     := oJsonMvPar:GetNames() as Json
	Local cTipoPar  := "" as character
    Local cGetDB    := Alltrim(UPPER(TcGetDb())) as character

	//Carrega os MV_PAR para a memória
	Pergunte(cPergunte,.F.)

	//Popula com os novos valores
	For nY := 1 To nJson
        If subStr(UPPER(oJson[nY]),1,3) <> "SV_"
		    cTipoPar := VALTYPE(&(UPPER(oJson[nY])))

            If Len(oJsonMvPar[oJson[nY]]) > 0
                If cTipoPar == "D"
                    If !Empty(oJsonMvPar[ oJson[nY] ][1])
                        &(UPPER(oJson[nY])) := StoD(SubStr(StrTran(oJsonMvPar[oJson[nY]][1],"-",""),1,8))
                    Else
                        &(UPPER(oJson[nY])) := StoD("")
                    EndIf
                ElseIf cTipoPar == "N"
                    If VALTYPE(oJsonMvPar[oJson[nY]][1]) == "C"
                        &(UPPER(oJson[nY])) := VAL(oJsonMvPar[oJson[nY]][1])
                    Else
                        &(UPPER(oJson[nY])) := oJsonMvPar[oJson[nY]][1]
                    EndIf
                ElseIf cTipoPar $ "CL"
                    &(UPPER( oJson[nY])) := Iif(Empty(oJsonMvPar[oJson[nY]][1]) .And. "ORACLE" $ cGetDB, space(Len(&(UPPER(oJson[nY])))), oJsonMvPar[oJson[nY]][1])
                EndIf
            EndIf
        EndIf
	Next nY

Return


//-------------------------------------------------------------------
/*/{Protheus.doc} CtbGetTypes
    Copia da funcao CtbGetTypes
    Funç?o que retorna os Tipos
    @author     
    @since      
    @version    
/*/
//-------------------------------------------------------------------

Function CtbGetTypes(cTipo as Character)

    local cTypeVar    as character
    
    cTypeVar    :=  ""
    
    If cTipo == "C"
        cTypeVar := "string"
    Elseif cTipo == "D"
        cTypeVar := "date"
    Elseif cTipo == "L"
        cTypeVar := "boolean"
    Elseif cTipo == "N"
        cTypeVar := "number"
    EndIf                 

Return cTypeVar

//-------------------------------------------------------------------
/*/{Protheus.doc} SvAFGetMtv
    Copia da funcao baseada na AtfGetMtvo
    Funcao que retorna a descricao do N4_MOTIVO conforme tabela SX5 16
    @author Gustavo Campos
    @since  18/12/2023
    @version 1.0
/*/
//-------------------------------------------------------------------

Function SvAFGetMtv(cMotivo)

    Local aMotivos as array
    Local cRetorno as character
    Local nX as numeric

    aMotivos := {}
    cRetorno := ""
    nX       := 0
    aMotivos := FWGetSX5('16')

    For nX := 1 to len(aMotivos)
        If Alltrim(aMotivos[nX][3]) == cMotivo
            cRetorno := aMotivos[nX][4]
        EndIf

    Next nX

Return cRetorno

//-------------------------------------------------------------------
/*/{Protheus.doc} GetDescFor
  Funcao para buscar nome fantasia do fornecedor com base
  no codigo
    @author Gustavo Campos
    @since  18/12/2023
    @version 1.0
/*/
//-------------------------------------------------------------------
Function GetDescFor(cCodigo,cLoja)

    Local cNFantDesc  as character
    Local aArea        as array
    
    cNFantDesc := ""
    aArea := (GetArea())

    DbSelectArea("SA2")
    SA2->(DbSetOrder(1)) 

    If SA2->(DbSeek(xFilial("SA2")+cCodigo+cLoja))
        cNFantDesc := SA2->A2_NREDUZ
    EndIf

    RestArea(aArea)

Return cNFantDesc
