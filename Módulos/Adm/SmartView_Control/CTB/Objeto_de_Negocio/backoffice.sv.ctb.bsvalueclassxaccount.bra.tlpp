#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.bsvalueclassxaccount.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2, CTH", NAME="Balancete Classe de Valor X Conta", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BsValueClassxAccountSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Classe de Valor X Conta
para o TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BsValueClassxAccountSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object
    public method processCustomData()       as json
    
    //usados na internacionalização
    public method preData()                as object
    public method preSchema()              as object
    public method processData()            as json

    protected method RetValorCtb()          as character

    protected data jDados  as json
    protected data nTotCrd as numeric
    protected data nTotDeb as numeric
    protected data nTotMov as numeric
    protected data cClasseAnt    as character
    protected data cValorCtb    as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BsValueClassxAccountSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete Classe de Valor x Contas"
    self:setPergunte("CTR190") //Indica o pergunte que será utilizado no relatório

    self:nTotCrd := 0
    self:nTotDeb := 0
    self:nTotMov := 0
    self:cClasseAnt := ""

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getDescription() as character class BsValueClassxAccountSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre os Balancete Balancete Classe de Valor x Contas"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class BsValueClassxAccountSmartViewBusinessObject

    self:oSchema:addProperty("TOT_MOV"       , STR0003  , "string", STR0003 , "TOT_MOV"      ) //"Total Mov. Acumulado"
    self:oSchema:addProperty("TOT_DEBITO"    , STR0004  , "string", STR0004 , "TOT_DEBITO"   ) //"Total Débito Acumulado"
    self:oSchema:addProperty("TOT_CREDITO"   , STR0005  , "string", STR0005 , "TOT_CREDITO"  ) //"Total Crédito Acumulado"    */
    self:oSchema:addProperty("NomeEmpresa"   , STR0006  , "string", STR0006 , "NomeEmpresa"  ) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    , STR0007  , "string", STR0007 , "NomeFilial"   ) // "Nome Filial"
    
    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0008 , "string", .T.)  // - "Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  
return self:oSchema


/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BsValueClassxAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BsValueClassxAccountSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class BsValueClassxAccountSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BsValueClassxAccountSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR190")    

    self:handleCtgGerPlanParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters()) 
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BsValueClassxAccountSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData



//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Utilizado antes do append no data
 
@return object: self:oData
 
@author Ewerton Franklin
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processCustomData() as json class BsValueClassxAccountSmartViewBusinessObject
    
    If cArqTmp->TIPOCONTA == "2"   
		If ::cClasseAnt == cArqTmp->CLVL         		    
			If cArqTmp->TIPOCLVL == "2"
				::nTotDeb += cArqTmp->SALDODEB	     
				::nTotCrd += cArqTmp->SALDOCRD	     
		    Endif
		Else    	    
    	    If cArqTmp->TIPOCLVL == "2" 
				::nTotDeb += cArqTmp->SALDODEB	     
				::nTotCrd += cArqTmp->SALDOCRD	      
            Endif 
		Endif
	Endif

     self:nTotMov := (self:nTotCrd - self:nTotDeb)
    //Adiciona totais no data
    ::jDados["TOT_DEBITO" ]  := self:RetValorCtb(self:nTotDeb) 
    ::jDados["TOT_CREDITO"]  := self:RetValorCtb(self:nTotCrd) 
    ::jDados["TOT_MOV" ]     := self:RetValorCtb(self:nTotMov)  
   
    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := ::cCompanyName
    ::jDados["NomeFilial" ] := ::cBranchName

    ::cClasseAnt := cArqTmp->CLVL

return ::jDados



//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BsValueClassxAccountSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BsValueClassxAccountSmartViewBusinessObject
   
    local cContaIni         as character
    local cContaFim         as character
    local cClvlIni          as character
    local cClvlFim          as character
    local cMoeda  :=  "01"  as character
    local cTpsald :=  "1"   as character
    local cGrpDesp:=  "1"   as character
    local cFilSegClvl       as character
    local cContIniClvl      as character
    local cContFimClvl      as character
    local cContClvl         as character
    local nDivide           as numeric
    local lImpAntLP         as logical
    local lImpSint          as logical 
    local lPrintZero        as logical
    local lSldZerado        as logical
    local lImpCodCla        as logical
    local lSldRecDes        as logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as date
    local dDataFim          as date
    local dDataLP           as date
    local dDtZeraRD         as date
    local lMov           as logical


    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    ///Parametros do relatorio
    dDataIni     := MV_PAR01     //Data Inicial ?
    dDataFim     := MV_PAR02     //Data Final ?
    cContaIni    := MV_PAR03     //Conta Inicial ?
    cContaFim    := MV_PAR04     //Conta Final ?
    cClvlIni     := MV_PAR05     //Da Classe de Valor ?
    cClvlFim     := MV_PAR06     //Ate Classe de Valor ?
    lImpSint     := iif(MV_PAR07==1 .Or. MV_PAR07 ==3,.T.,.F.) //Imprime Contas ?
    aSetOfBook   := iif(!empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf("")) //Cod. Config. Livros ?
    lSldZerado   := iif(MV_PAR09==1,.T.,.F.) //Saldos Zerados ?
    cMoeda       := iif(!empty(MV_PAR10),MV_PAR10,cMoeda) //Moeda ?
    cTpsald      := Iif(Empty(MV_PAR12),cTpsald,PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]))  //Tipo de Saldo ?
    cFilSeg      := MV_PAR15     //Filtra Segmento No. ?
    cConteudIni  := MV_PAR16     //Conteudo Ini Segmen ?
    cConteudFim  := MV_PAR17     //Conteudo Fim Segmen ?
    cConteudCont := MV_PAR18     //Conteudo Contido em ?
    lMov         := iIf(MV_PAR19==1,.T.,.F.)  
    lPrintZero   := iif(MV_PAR23==1,.T.,.F.) //Imprime Valor 0.00  ?
    nDivide      := MV_PAR25   //Divide por ?
    lImpAntLP    := iif(MV_PAR27 == 1,.T.,.F.)
    dDataLP      := MV_PAR28     //Data Lucros/Perdas ?
    cFilSegClvl  := MV_PAR30     //Filtra Segmento No(Clvl) ?  
    cContIniClvl := MV_PAR31     //Conteudo Ini Segmen(Clvl) ?
    cContFimClvl := MV_PAR32     //Conteudo Fim Segmen(Clvl) ?
    cContClvl    := MV_PAR33     //Conteudo Contido em(Clvl) ?
    lImpCodCla   := iif(MV_PAR34==1 .Or. MV_PAR34==3,.T.,.F.) //Imprime Classe Valor ?
    lSldRecDes   := iif(mv_par35==1,.T.,.F.) //Ignora Sl Ant.Rec/Des. ?
    cGrpDesp     := MV_PAR36     //Grupo Receitas/Desp. ?
    dDtZeraRD    := MV_PAR37     //Data Sld Ant.Rec/Desp. ?

    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CTI" //são obrigatórios
    self:cIdent         := ""    //são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cClvlIni       := cClvlIni
    self:cClVlFim       := cClvlFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := !lMov
    self:lImpConta      := .T.
    self:cHeader        := "CTH"
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:lPrintZero     := lPrintZero
    self:cSegmento      := cFilSegClvl 
    self:cSegIniG       := cContIniClvl
    self:cSegFimG       := cContFimClvl
    self:cFiltSegmG     := cContClvl   
    self:lRecDesp0      := lSldRecDes 
    self:cRecDesp       := cGrpDesp
    self:dDtZeraRD      := dDtZeraRD
    self:aSelFil        := aSelFil
    self:lCttSint       := lImpCodCla
    self:lImpSint       := lImpSint
    
return


//-------------------------------------------------------------------
/*{Protheus.doc} RetValorCtb
Retorna Mascara

@return character

@author Controladoria
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method RetValorCtb(nValCtb as numeric) as character class BsValueClassxAccountSmartViewBusinessObject

Local cPicture as character
Local TAM_VALOR := 17 as numeric
Local nDecimais as numeric
nDecimais := DecimalCTB(::aSetOfBook,mv_par10)
cPicture := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")

DEFAULT nValCtb := 0

::cValorCtb := "0"

If nValCtb <=  0
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
Else
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
EndIf

Return ::cValorCtb
