#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.generaldiarybyfiscaldocument.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2", name="Diario Geral por Documento Fiscal", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} GeneralDiarybyFiscalDocumentSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Diario Geral por Documento Fiscal
para o TReports
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class GeneralDiarybyFiscalDocumentSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json
    
    protected method ImpEntCont()  as character
    protected method getQuery()    as character
    protected method setData()     as object    

    protected data cAlias          as character
    protected data cDataDe         as character
    protected data cDataAte        as character
    protected data cMoeda          as character
    protected data cTipoSaldo      as character    
    protected data nTipoConta      as numeric
    protected data lPrintZero      as logical
    protected data jDados          as json
    protected data aCtbMoeda       as array
    protected data aFields         as array
    protected data aAllFields      as array
    protected data aSetOfBook      as array    
    protected data oQuery          as object

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character 

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Diario Geral por Documento Fiscal"

    self:setPergunte("CTR112")      //Indica o pergunte que será utilizado no relatório

    ::cCompanyName := ""
    ::cBranchName  := ""
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Diario Geral por Documento Fiscal"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
Return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Renan Gremes
@since 04/12/2023
@version 1.0
/*/
//-------------------------------------------------------------------

method preSchema() as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method processData() class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject    
    
    local cQuery as character    

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()

    //Captura o valor dos parâmetros
    ::cDataDe     := MV_PAR01  // Data Inicial ?
    ::cDataAte    := MV_PAR02  // Data Final ?
    ::cMoeda      := iif(!Empty(MV_PAR03), MV_PAR03, "01") // Moeda ?
    ::aSetOfBook  := iif(!Empty(MV_PAR04), CTBSetOf(MV_PAR04), CTBSetOf(""))  // Cod. Config. Livros ?
    ::cTipoSaldo  := iif(!Empty(MV_PAR05), MV_PAR05, "1")  // Tipo de Saldo ?
    ::lPrintZero  := iif(MV_PAR12 == 1, .T., .F.)          // Imprime Valor 0,00 ?
    ::nTipoConta  := iif(!Empty(MV_PAR13), MV_PAR13, 1)    // Imprime Codigo Conta ?
    ::aCtbMoeda	  := CtbMoeda(::cMoeda)

    //Faz a validação do livro
    if !Empty(MV_PAR04)
        if !VdSetOfBook(MV_PAR04, .F.)
            return self:oData
        endif
    endif

    //Valida a moeda	
	if Empty(::aCtbMoeda[1])
		return self:oData
	endif    

    cQuery := self:getQuery(oFilter)

    self:cAlias := MPSysOpenQuery(cQuery)
    self:setData(oFilter)

    //Elimina da memória
    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} setData
    Seta os dados filtrados no objeto oData

    @author Renan Gremes
    @since 04/12/2023
    @version 12.1.2310
*/
//-------------------------------------------------------------------------------
method setData(oFilter as object) as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
    
    local cPicture      as character
    local cMesAnt       as character
    local cSepara       as character
    local cDescMoeda    as character
    local cMascara      as character
    local cValDeb       as character
    local cDebitoM      as character
    local cValCred      as character
    local cCreditoM     as character
    local nValDeb       as numeric
    local nValCred      as numeric
    local nDecimais     as numeric
    local nX      := 0  as numeric
    local aTamVal := TAMSX3("CT2_VALOR") as array

    cDescMoeda 	:= ::aCtbMoeda[2]
    nDecimais 	:= DecimalCTB(::aSetOfBook, ::cMoeda)
    cMascara    := iif(Empty(::aSetOfBook[2]), SuperGetMv("MV_MASCARA"), RetMasCtb(::aSetOfBook[2], @cSepara)) // Mascara da Conta
    
    //Posiciona no registro inicial
    (self:cAlias)->(dbGoTop())

    While (self:cAlias)->(!Eof())
        //Tratamento Picture
        cPicture := ::aSetOfBook[4]
        if Empty(cPicture)
            cPicture := "@E " + TmContab((self:cAlias)->CT2_VALOR,aTamVal[1],nDecimais)
        endif

        //Tratamento valores
        nValDeb  := iif((self:cAlias)->CT2_DC == "1" .Or. (self:cAlias)->CT2_DC == "3", (self:cAlias)->CT2_VALOR, 0)
        nValCred := iif((self:cAlias)->CT2_DC == "2" .Or. (self:cAlias)->CT2_DC == "3", (self:cAlias)->CT2_VALOR, 0)
		
        //Tratamento Mascaras
        cDebitoM  := self:ImpEntCont(cMascara, cSepara, .T.)
        cCreditoM := self:ImpEntCont(cMascara, cSepara, .F.)
        cValDeb   := ValorCTB(nValDeb,,,aTamVal[1],nDecimais,.F.,cPicture,"1",,,,,,::lPrintZero,.F.)
        cValCred  := ValorCTB(nValCred,,,aTamVal[1],nDecimais,.F.,cPicture,"2",,,,,,::lPrintZero,.F.)
        
        //Pega mês da data do registro
        cMesAnt := Month(STOD((self:cAlias)->CT2_DATA))

        ::jDados := JsonObject():new()

        //Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(self:aAllFields[nX]:getRealName())), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

		//Altera valores de campos da query com tratativas
        ::jDados["CT2_VALDEB"]  	:= nValDeb
        ::jDados["CT2_VALCRED"]  	:= nValCred

        //Popula campos customizados
        ::jDados["DEBITO_M"]        := cDebitoM
        ::jDados["CREDITO_M"]       := cCreditoM
        ::jDados["DESCMOEDA"]  	    := cDescMoeda
        ::jDados["VALDEB_M"]  	    := cValDeb
        ::jDados["VALCRED_M"]  	    := cValCred
        ::jDados["NomeEmpresa"]     := self:cCompanyName
        ::jDados["NomeFilial" ]     := self:cBranchName
		
        (self:cAlias)->(dbSkip())

        self:processData()
        self:oData:appendData(::jDados)
	EndDo

    FreeObj(::jDados)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class GeneralDiarybyFiscalDocumentSmartViewBusinessObject

    local lCanFilter := .F. as logical    
    local aFieldsCT2 := {}  as array 

    //Popula o array com os campos do schema   
    aFieldsCT2 := { "CT2_FILIAL", "CT2_DATA",   "CT2_LOTE", "CT2_SBLOTE", "CT2_DOC",;
                    "CT2_LINHA",  "CT2_VALOR",  "CT2_DC",   "CT2_TPSALD", "CT2_MOEDLC",;
                    "CT2_DEBITO", "CT2_CREDIT", "CT2_HIST", "CT2_VALDEB", "CT2_VALCRED" }
    
    self:oSchema:aliasToSchema("CT2", aFieldsCT2)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    self:oSchema:addProperty("DEBITO_M"    , STR0005 , "string" , STR0005 , "DEBITO_M",,,,lCanFilter )      // "Debito Mascara"
    self:oSchema:addProperty("CREDITO_M"   , STR0006 , "string" , STR0006 , "CREDITO_M",,,,lCanFilter )     // "Credito Mascara"
    self:oSchema:addProperty("DESCMOEDA"   , STR0007 , "string" , STR0007 , "DESCMOEDA",,,,lCanFilter )     // "Moeda Descr"
    self:oSchema:addProperty("CT2_VALDEB"  , STR0020 , "number" , STR0020 , "CT2_VALDEB",,,,lCanFilter )    // "Valor Debito"
    self:oSchema:addProperty("VALDEB_M"    , STR0008 , "string" , STR0008 , "VALDEB_M",,,,lCanFilter )      // "Vlr Debito Mascara"
    self:oSchema:addProperty("CT2_VALCRED" , STR0021 , "number" , STR0021 , "CT2_VALCRED",,,,lCanFilter )   // "Valor Credito"
    self:oSchema:addProperty("VALCRED_M"   , STR0009 , "string" , STR0009 , "VALCRED_M",,,,lCanFilter )     // "Vlr Credito Mascara"
    self:oSchema:addProperty("NomeEmpresa" , STR0022 , "string" , STR0022 , "NomeEmpresa",,,,lCanFilter)    // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"  , STR0023 , "string" , STR0023 , "NomeFilial",,,,lCanFilter)     // "Nome Filial"

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} getQuery
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query.

    @author Renan Gremes
    @since  13/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getQuery(oFilter as object) as character class GeneralDiarybyFiscalDocumentSmartViewBusinessObject
    
    local cDelete := space(1)   as character
    local cQuery                as character
    local nParamOrder := 1      as numeric

    cQuery := "SELECT "
    cQuery += self:getSQLFields(.F., , .F.)
    cQuery += " FROM "+RetSqlName("CT2")+" CT2 "
    cQuery += " WHERE "    
    cQuery += " CT2.CT2_FILIAL = ? "
    cQuery += " AND CT2.CT2_DATA BETWEEN ? AND ? "
    cQuery += " AND CT2.CT2_MOEDLC = ? "
    cQuery += " AND CT2.CT2_TPSALD = ? "
    cQuery += " AND CT2.D_E_L_E_T_ = ? "

    //Os filtros serão setados na interface do novo TReports
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")
    
    //Order by
    cQuery += " ORDER BY CT2.CT2_FILIAL, CT2.CT2_DATA, CT2.CT2_LOTE, CT2.CT2_SBLOTE, CT2.CT2_DOC, CT2.CT2_LINHA"

    self:oQuery := FWExecStatement():New(ChangeQuery(cQuery))

    //Popula query
    self:oQuery:SetString(nParamOrder++, FWxFilial("CT2"))    
    self:oQuery:SetDate(nParamOrder++, ::cDataDe)
    self:oQuery:SetDate(nParamOrder++, ::cDataAte)
    self:oQuery:SetString(nParamOrder++, ::cMoeda)
    self:oQuery:SetString(nParamOrder++, ::cTipoSaldo)
    self:oQuery:SetString(nParamOrder++, cDelete)

    cQuery := self:oQuery:GetFixQuery()	

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} ImpEntCont
Faz a impressao do código da Entidade Contabil, conforme a opção 
do parametro mv_par13
 
@param cMascara, caracter, indica a mascara a ser utilizada
@param cSepara, caracter, indica o separador a ser utilizado
@param lDeb, lógico, valida se é débito
@param nTipoConta, numérico, indica o tipo da conta

@return cRet, caracter, retorna a conta formatada
 
@author Renan Gremes
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method ImpEntCont(cMascara as character, cSepara as character, lDeb as logical) as character class GeneralDiarybyFiscalDocumentSmartViewBusinessObject

    local cRet := "" as character

    default cMascara := SuperGetMv("MV_MASCARA")
    default cSepara  := ""

    if lDeb
        if !Empty((self:cAlias)->CT2_DEBITO)					// Se a Conta a Debito estiver preenchida            
            //Atribui o padrão caso não exista plano de contas
            cRet := EntidadeCTB((self:cAlias)->CT2_DEBITO,0,0,20,.F.,cMascara,cSepara,,,,,.F.)

            dbSelectArea("CT1")
            CT1->(dbSetOrder(1))
            if CT1->(MsSeek(xFilial("CT1")+(self:cAlias)->CT2_DEBITO,.F.))	// e existir no plano de contas
                if ::nTipoConta == 2							// Impressao do Codigo Reduzido
                    cRet := EntidadeCTB(CT1->CT1_RES,0 ,00,20,.F.,cMascara,cSepara,,,,,.F.)
                elseif ::nTipoConta == 3 			 	// Impressao do Codigo de Impressao (se o campo existir)
                    cRet := EntidadeCTB(CT1->CT1_CODIMP,0,0,20,.F.,cMascara,cSepara,,,,,.F.)
                else										// Impressao do Codigo Normal
                    cRet := EntidadeCTB((self:cAlias)->CT2_DEBITO,0,0,20,.F.,cMascara,cSepara,,,,,.F.)
                endif                
            endif
            CT1->(dbCloseArea())
        endif
    else
        if !Empty((self:cAlias)->CT2_CREDIT)
            //Atribui o padrão caso não exista plano de contas
            cRet := EntidadeCTB((self:cAlias)->CT2_CREDIT,0,0,20,.F.,cMascara,cSepara,,,,,.F.)

            dbSelectArea("CT1")
            CT1->(dbSetOrder(1))
            if CT1->(MsSeek(xFilial("CT1")+(self:cAlias)->CT2_CREDIT,.F.))
                if ::nTipoConta == 2							// Impressao do Codigo Reduzido
                    cRet := EntidadeCTB(CT1->CT1_RES,0,0,20,.F.,cMascara,cSepara,,,,,.F.)
                elseif ::nTipoConta == 3 						// Impressao do Codigo de Impressao (se o campo existir)
                    cRet := EntidadeCTB(CT1->CT1_CODIMP,0,0,20,.F.,cMascara,cSepara,,,,,.F.)
                else										// Impressao do Codigo Normal
                    cRet := EntidadeCTB((self:cAlias)->CT2_CREDIT,0,0,20,.F.,cMascara,cSepara,,,,,.F.)
                endif                
            endif
            CT1->(dbCloseArea())
        endif
    endif

return cRet
