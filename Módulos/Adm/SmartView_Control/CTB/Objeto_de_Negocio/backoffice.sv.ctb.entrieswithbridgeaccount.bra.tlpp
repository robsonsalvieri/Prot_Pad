#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.entrieswithbridgeaccount.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTZ", name="Lançamento de Conta Ponte", country="ALL", customTables="CTZ,CT1,CTT,CTD,CTH")

//-------------------------------------------------------------------
/*/{Protheus.doc} EntrieswithBridgeAccountSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Lançamento de Conta Ponte
para o TReports

@author Controladoria
@since 15/09/2023
@version 1.0
*/
//-------------------------------------------------------------------

class EntrieswithBridgeAccountSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
    public method getAreas() as array
    public method getDescription() as character

    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 

    protected data jDados 		   as json
	protected data aAllFields      as array
	protected data cAlias          as character
    protected data oQuery1         as object
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe

@return object: self

@author Controladoria
@since 15/09/2023
@version 1.0
*/
//-------------------------------------------------------------------

method new() as object class EntrieswithBridgeAccountSmartViewBusinessObject

	_Super:new()
	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0001) //"Lançamentos de Apuração e Conta Ponte"
	self:setPergunte("CTR600") //Indica o pergunte que será utilizado no relatório

	self:aAllFields := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de descrição

@return character: descrição

@author Controladoria
@since 15/09/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getDescription() as character class EntrieswithBridgeAccountSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre os Lançamentos de Apuração e Conta Ponte"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Controladoria
@since 15/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class EntrieswithBridgeAccountSmartViewBusinessObject
return {STR0003}//"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports

@return object: self:oData

@author Controladoria
@since 15/09/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getData(nPage as numeric, oFilter as object) as object class EntrieswithBridgeAccountSmartViewBusinessObject
    local dDataApur    as date
    local cLoteIni     as character
    local cLoteFim     as character
    local cSbloteIni   as character
    local cSbloteFim   as character
    local cDocIni      as character
    local cDocFim      as character
    local cMoeda       as character
    local cTpSaldo     as character
    local cQuery       as character
	local nX 		  := 0 as numeric
	local nParamOrder := 1 as numeric

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR600")
    
    dDataApur  := MV_PAR01  // Data de apuracao ?
    cLoteIni   := MV_PAR02  // Lote Inicial ?
    cLoteFim   := MV_PAR03  // Lote Final ?
    cSbloteIni := MV_PAR04  // SubLote Inicial ?
    cSbloteFim := MV_PAR05  // SubLote Final ?
    cDocIni    := MV_PAR06  // Documento Inicial ?
    cDocFim    := MV_PAR07  // Documento Final ?
    cMoeda     := MV_PAR08  // Moeda ?
    cTpSaldo   := MV_PAR09  // Tipo de Saldo ?
 
	cQuery := "	SELECT "
	cQuery += self:getSQLFields()
    cQuery += "	FROM "+ RetSQLName("CTZ") +" CTZ "
	cQuery += "	INNER JOIN "+ RetSQLName("CT1") +" CT1 ON "
	cQuery += "		    CT1.CT1_FILIAL = ? "
	cQuery += "		AND CT1.CT1_CONTA = CTZ.CTZ_CONTA"
	cQuery += "		AND CT1.D_E_L_E_T_ = ? "
	cQuery += "	LEFT JOIN "+ RetSQLName("CTT") +" CTT ON "
	cQuery += "		    CTT.CTT_FILIAL = ? "
	cQuery += "		AND CTT.CTT_CUSTO = CTZ.CTZ_CUSTO "
	cQuery += "		AND CTT.D_E_L_E_T_ = ? "
	cQuery += "	LEFT JOIN "+ RetSQLName("CTD") +" CTD ON "
	cQuery += "		    CTD.CTD_FILIAL = ? "
	cQuery += "		AND CTD.CTD_ITEM = CTZ.CTZ_ITEM "
	cQuery += "		AND CTD.D_E_L_E_T_ = ? "
	cQuery += "	LEFT JOIN "+ RetSQLName("CTH") +" CTH ON "
	cQuery += "		    CTH.CTH_FILIAL = ? "
	cQuery += "		AND CTH.CTH_CLVL = CTZ.CTZ_CLVL"
	cQuery += "		AND CTH.D_E_L_E_T_ = ? "
	cQuery += "	WHERE "
	cQuery += "	   CTZ.CTZ_FILIAL	=	? "
	cQuery += "		AND CTZ.CTZ_DATA	=	? "
	cQuery += "		AND CTZ.CTZ_LOTE	>=	? "
	cQuery += "		AND CTZ.CTZ_LOTE	<=	? "
	cQuery += "		AND CTZ.CTZ_SBLOTE	>=	? "
	cQuery += "		AND CTZ.CTZ_SBLOTE	<=	? "
	cQuery += "		AND CTZ.CTZ_DOC		>=	? "
	cQuery += "		AND CTZ.CTZ_DOC		<=	? "
	cQuery += "		AND CTZ.CTZ_MOEDLC	=	? "	
	cQuery += "		AND CTZ.CTZ_TPSALD	=	? "
	cQuery += "		AND CTZ.D_E_L_E_T_ = ? "
	cQuery += "	ORDER BY "
	cQuery += "	    	CTZ.CTZ_FILIAL, CTZ.CTZ_DATA, CTZ.CTZ_LOTE, CTZ.CTZ_SBLOTE, CTZ.CTZ_DOC, CTZ.CTZ_TPSALD, "
	cQuery += "	    	CTZ.CTZ_EMPORI, CTZ.CTZ_FILORI, CTZ.CTZ_MOEDLC, CTZ.CTZ_LINHA, CTZ.CTZ_SEQLIN "

	cQuery := ChangeQuery(cQuery)
	self:oQuery1 := FWPreparedStatement():New(cQuery)

	self:oQuery1:SetString(nParamOrder++,FWxFilial("CT1"))
	self:oQuery1:SetString(nParamOrder++," ")
	self:oQuery1:SetString(nParamOrder++,FWxFilial("CTT"))
	self:oQuery1:SetString(nParamOrder++," ")
	self:oQuery1:SetString(nParamOrder++,FWxFilial("CTD"))
	self:oQuery1:SetString(nParamOrder++," ")
	self:oQuery1:SetString(nParamOrder++,FWxFilial("CTH"))
	self:oQuery1:SetString(nParamOrder++," ")
    self:oQuery1:SetString(nParamOrder++,xFilial("CTZ"))
	self:oQuery1:SetString(nParamOrder++,DTOS(dDataApur))
	self:oQuery1:SetString(nParamOrder++,cLoteIni)
	self:oQuery1:SetString(nParamOrder++,cLoteFim)
	self:oQuery1:SetString(nParamOrder++,cSbloteIni)
	self:oQuery1:SetString(nParamOrder++,cSbloteFim)
	self:oQuery1:SetString(nParamOrder++,cDocIni)
	self:oQuery1:SetString(nParamOrder++,cDocFim)
	self:oQuery1:SetString(nParamOrder++,cMoeda)
	self:oQuery1:SetString(nParamOrder++,cTpSaldo)
	self:oQuery1:SetString(nParamOrder++," ")

	cQuery := self:oQuery1:GetFixQuery()
    cAlias := MPSysOpenQuery(ChangeQuery(cQuery))

    DbSelectArea(cAlias)
    (cAlias)->(DbGoTop())

    While !(cAlias)->(Eof())	

        ::jDados := JsonObject():new()

		//Popula campos da query
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jDados[self:aAllFields[nX]:getName()] := iif(!empty((cAlias)->&(self:aAllFields[nX]:getRealName())), totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(self:aAllFields[nX]:getRealName())), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jDados[self:aAllFields[nX]:getName()] := AllTrim((cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jDados[self:aAllFields[nX]:getName()] := (cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

        self:processData()
        self:oData:appendData(::jDados)
        
    	(cAlias)->(dbSkip())

    EndDo

    (cAlias)->(dbCloseArea())
    
	//Elimina da memória.
    FreeObj(oFilter:getParameters())
	FreeObj(::jDados)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Retorna a estrutura dos campos e conjunto de parametros

@return object: self:oSchema

@author Controladoria
@since 15/09/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getSchema() as object class EntrieswithBridgeAccountSmartViewBusinessObject

    local aFieldsCTZ as array
	local aFieldsCT1 as array
	local aFieldsCTT as array
	local aFieldsCTD as array
	local aFieldsCTH as array

    aFieldsCTZ := {"CTZ_FILIAL", "CTZ_DATA", "CTZ_LOTE", "CTZ_SBLOTE", "CTZ_DOC", "CTZ_TPSALD", "CTZ_EMPORI", "CTZ_FILORI",;
              	   "CTZ_MOEDLC", "CTZ_LINHA", "CTZ_SEQLIN", "CTZ_CONTA", "CTZ_CUSTO", "CTZ_ITEM", "CTZ_CLVL", "CTZ_VLRDEB",;
            	   "CTZ_VLRCRD"}
	aFieldsCT1 := {"CT1_RES"}
	aFieldsCTT := {"CTT_RES"}
	aFieldsCTD := {"CTD_RES"}
	aFieldsCTH := {"CTH_RES"}	

    self:oSchema:aliasToSchema("CTZ",aFieldsCTZ)
	self:oSchema:aliasToSchema("CT1",aFieldsCT1)
	self:oSchema:aliasToSchema("CTT",aFieldsCTT)
	self:oSchema:aliasToSchema("CTD",aFieldsCTD)
	self:oSchema:aliasToSchema("CTH",aFieldsCTH)

	//Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()
        
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    Adiciona novos campos ao schema

    @return object: oSchema

    @author Controladoria
	@since 15/09/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class EntrieswithBridgeAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que ser? consumido pelo M.I.
    Ajusta o Json conforme alteracao no processData e preSchema

    @return object: oData

    @author Controladoria
	@since 15/09/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class EntrieswithBridgeAccountSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.

    @return json: jDados

    @author Controladoria
	@since 15/09/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() class EntrieswithBridgeAccountSmartViewBusinessObject
return ::jDados

