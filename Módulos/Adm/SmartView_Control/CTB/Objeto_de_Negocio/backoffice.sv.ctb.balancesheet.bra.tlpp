#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheet.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2", name="Balancete", country="ALL")

class BalanceSheetSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object

    public method processCustomData()       as json

    public method ExecDBSkipTemp()          as logical

    protected data cPicture  as character
    protected data nDecimais as numeric

endclass

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método de instância da classe
@author author 
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method new() as object class BalanceSheetSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete"
    self:setPergunte("CTR040") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Retorna descrição do objeto
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method getDescription() as character class BalanceSheetSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete"

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Prepara a estrutura dos campos
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method getCtGerPlanSchema() as object class BalanceSheetSmartViewBusinessObject
    
    self:oSchema:addProperty("NomeEmpresa"    ,STR0003 , "string", STR0003 , "NomeEmpresa"  ) //STR0003 - "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"     ,STR0004 , "string", STR0004 , "NomeFilial"     ) //STR0004 - "Nome Filial"
    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0005 , "string", .T.)                                   //STR0005 - "Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método para execução do CTGERPLAN
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método para execução do append
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Carrega lista de parametros para a memória
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(),self:cPergunte)
    self:handleCtgGerPlanParams( oFilter )
    FreeObj(oFilter:getParameters())  
return self:oData

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Define para os atributos conforme parâmetros informados
@author  author
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceSheetSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    local cGrpDesp          as Character
    local cMoedaDsc         as character 
    
    local nIniPage          as Numeric
    Local nDivide := 1      as numeric

    local lMov              as logical
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    Local lQuebraGrp        as Logical
    Local lPulaSint         as Logical
    Local lSldRecDes        as Logical

    local aSetOfBook        as array
    local aSelFil           as array

    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date
    local dDtZeraRD         as Date

    dDataIni    :=  MV_PAR01 //      	Data Inicial ?                
    dDataFim    :=  MV_PAR02 //      	Data Final ?                  
    cContaIni   :=  MV_PAR03 //         Conta Inicial ?               
    cContaFim   :=  MV_PAR04 //         Conta Final ?                 
    lImpSint    := iIf(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.) //      	Imprime Contas ?              
    aSetOfBook  := iIf(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf("")) //      	Cod. Config. Livros ?         
    lSldZerado  := iIf(MV_PAR07==1,.T.,.F.) //Saldos Zerados                            
    cMoeda      := iIf(!empty(MV_PAR08),MV_PAR08,cMoeda) //Moeda                                            
    nIniPage    := MV_PAR09 //Folha Inicial
    cTpsald     := MV_PAR10 //Tipo de Saldo
    lQuebraGrp  := IIF(MV_PAR11==1,.T.,.F.) //Quebra por Grupo
    cFilSeg     := MV_PAR12 //Filtra Segmento No. ?  
    cConteudIni := MV_PAR13 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR14 //Conteudo Fim Segmen ?        
    cConteudCont:= MV_PAR15 //Conteudo Contido em ?
    lMov        := IIF(MV_PAR16==1,.T.,.F.) //Imprime Coluna Movimento ?
    lPulaSint   := Iif(mv_par17==1,.T.,.F.) //Salta Linha Sintet. ?
    lPrintZero  := Iif(mv_par18==1,.T.,.F.) //Imprime Valor 0.00  ?
    lImpCod     := Iif(mv_par19==1,.T.,.F.) //Imprime codigo ?
    nDivide     := iif(empty(MV_PAR20),nDivide,MV_PAR20)
    cImprimeSeg := MV_PAR21    //Imprimir ate o Segmento?
    lImpAntLP   := Iif(MV_PAR22 == 1,.T.,.F.) //Posição Ant. L/P?
    dDataLP     := MV_PAR23 //Data Lucros/Perdas?
    //MV_PAR24       	Imp Quadros Contabeis ?       
    lSldRecDes  := IIf(MV_PAR25 == 1,.T.,.F.)//       	Ignora Sl Ant.Rec/Des ?       
    cGrpDesp    := MV_PAR26       	//Grupos Receitas/Despesas ?    
    dDtZeraRD   := MV_PAR27       	//Data Sld Ant. Receitas/Desp. ?
    //MV_PAR28       	Num.linhas p/ o Balancete ?   
    cMoedaDsc   := iIf(!empty(MV_PAR29),MV_PAR29,cMoeda)//     	Descricao na Moeda ?          

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT7"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := .F.
    self:lImpConta      := .F.
    self:lRecDesp0      := lSldRecDes 
    self:cRecDesp       := cGrpDesp
    self:dDtZeraRD      := dDtZeraRD
    self:cMoedaDsc      := cMoedaDsc

    ::nDecimais := DecimalCTB(::aSetOfBook,::cMoeda)
    ::cPicture  := iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")
    
return 

/*/{Protheus.doc} BalanceSheetSmartViewBusinessObject::preSchema
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preSchema() as object class BalanceSheetSmartViewBusinessObject 
return self:oSchema

/*/{Protheus.doc} BalanceSheetSmartViewBusinessObject::preData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preData() as object class BalanceSheetSmartViewBusinessObject 
return self:oData

/*/{Protheus.doc} BalanceSheetSmartViewBusinessObject::processData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method processData() as json class BalanceSheetSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método para appendar dados no json
@author  loex
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetSmartViewBusinessObject
    local TAM_VALOR as numeric

    TAM_VALOR := 20
    
    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName
    
    //Manipula os valores, pois a classe genérica não esta tendo o comportamento correto para este relatório em questão.
    ::jDados["SALDOANT_M" ] := ValorCTB(cArqTmp->SALDOANT,,,TAM_VALOR+10,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.,,.F.)
    ::jDados["SALDODEB_M" ] := ValorCTB(cArqTmp->SALDODEB,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.T.,,.F.)
    ::jDados["SALDOCRD_M" ] := ValorCTB(cArqTmp->SALDOCRD,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.T.,,.F.)
    ::jDados["SALDOATU_M" ] := ValorCTB(cArqTmp->SALDOATU,,,TAM_VALOR+10,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.,,,.F.) 
    ::jDados["MOVIMENTO_M"] := ValorCTB(cArqTmp->MOVIMENTO,,,TAM_VALOR+5,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.,,,.F.)

return ::jDados

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método para validar se irá pular registro
@author  loex
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) as logical class BalanceSheetSmartViewBusinessObject
   
    Local lRet := .T.
    
    If mv_par05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
            lRet := .F.
		EndIf
	EndIf

return lRet
