#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.accountingitem.bra.ch"

namespace totvs.protheus.ctb.SmartViewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTD", name="Item Contábil", country="ALL",customTables="CTD")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio de Cad Centro de Custo para o TReports
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class AccountingItemSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object

    protected data jItens          as json
    protected data cAlias          as character
    protected data aAllFields      as array
    protected data oQuery1         as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class AccountingItemSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Cadastro do Item Contábil" 
    self:setPergunte("CTR030")   //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getDescription() as character class AccountingItemSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Cadastro de Item Contábil"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method getAreas() as array class AccountingItemSmartViewBusinessObject
Return {STR0003} //"Contabilidade" 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class AccountingItemSmartViewBusinessObject
    
    local cQuery            as character
    local cItemde           as character 
    local cItemAte          as character
    local cMascara          as character
    local cMoeda            as character
    local cCampoMoeda       as character
    local cCompanyName      as character
    local cBranchName       as character
    local cSeparador := ""  as character
    local lBloq             as logical
    local nX                as numeric
    local nParamOrder := 1  as numeric

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR030")

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := self:oIntegratedProvider:getCompanyName()
    cBranchName  := self:oIntegratedProvider:getBranchName() 

    cItemde   := MV_PAR01                             //Imprime do Item ? 
    cItemAte  := MV_PAR02                             //Até o Item ?
    cMoeda    := iif(Empty(MV_PAR04), "01", MV_PAR04) //Impr Desc da Moeda ?
    lBloq     := iif(MV_PAR05 == 2, .T., .F.)         //Impr Bloqueados ? 
    cMascara  := iif(Empty(MV_PAR06), SuperGetMv("MV_MASCARA"), RetMasCtb(MV_PAR06,@cSeparador)) //Mascara

    DBSelectArea("CTD")
    If CTD->(FieldPos("CTD_DESC"+cMoeda)) > 0
        cCampoMoeda := "CTD_DESC"+cMoeda
    Else    
        cCampoMoeda := "CTD_DESC01"
    EndIf

    //Select
    cQuery := "SELECT "+cCampoMoeda+", "
    cQuery += self:getSQLFields(.F., , .F.)
    cQuery += " FROM "+RetSqlName("CTD")+" CTD"
    
    //Where
    cQuery += " WHERE "    
    cQuery += " CTD_FILIAL =  ? "
    cQuery += " AND CTD_ITEM >= ? "
    cQuery += " AND CTD_ITEM <= ? "
    cQuery += " AND CTD.D_E_L_E_T_ = ? "
    If lBloq
	    cQuery += " AND CTD_BLOQ <> ? "
    EndIf

    //Os filtros serão setados na interface do novo SmartView
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

	self:oQuery1 := FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery1:SetString(nParamOrder++, FwXFilial("CTD"))
	self:oQuery1:SetString(nParamOrder++, cItemde)
	self:oQuery1:SetString(nParamOrder++, cItemAte)
    self:oQuery1:SetString(nParamOrder++, " ")

    If lBloq
        self:oQuery1:SetString(nParamOrder++, "1")
    EndIf

	cQuery := self:oQuery1:GetFixQuery()

    cAlias := MPSysOpenQuery(ChangeQuery(cQuery))
    (cAlias)->(dbGoTop())

    While !(cAlias)->(Eof()) 
 
        ::jItens := JsonObject():new()

        For nX := 1 to len(self:aAllFields)
            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()
            If self:aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cRealName)) 
            ElseIf self:aAllFields[nX]:getType() == "string"
                ::jItens[cId] := AllTrim((cAlias)->&(cRealName)) 
            Else
                ::jItens[cId] := (cAlias)->&(cRealName)
            EndIf
        Next nX

        //Popula campos customizados do schema
        ::jItens["ITEM_MASC"   ] := EntidadeCTB((cAlias)->CTD_ITEM,000,000,030,.F.,cMascara,cSeparador,,,.F.,,.F.)
        ::jItens["ITEMSUP_MASC"] := EntidadeCTB((cAlias)->CTD_ITSUP,000,000,030,.F.,cMascara,cSeparador,,,.F.,,.F.)
        ::jItens["DESCMOEDA"   ] := (cAlias)->&(cCampoMoeda)
        ::jItens["NomeEmpresa" ] := cCompanyName
        ::jItens["NomeFilial"  ] := cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (cAlias)->(DbSkip())        
    EndDo

    (cAlias)->(DBCloseArea())
    CTD->(DBCloseArea())
    
    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    FreeObj(self:oIntegratedProvider)
   
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Douglas Rodrigues da Silva
@since 20/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class AccountingItemSmartViewBusinessObject

    Local aFieldsCTD        as array

    aFieldsCTD := {"CTD_ITEM","CTD_ITSUP","CTD_BLOQ","CTD_CLASSE","CTD_NORMAL","CTD_DTEXIS"}
    
    self:oSchema:aliasToSchema("CTD",aFieldsCTD)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    //Adiciona campos customizados ao schema
	self:oSchema:addProperty("ITEM_MASC"    ,STR0004, "string", STR0004, "ITEM_MASC"   ,,,,.F. ) // "Mascara Item"
    self:oSchema:addProperty("ITEMSUP_MASC" ,STR0005, "string", STR0005, "ITEMSUP_MASC",,,,.F. ) // "Mascara Item Superior"
    self:oSchema:addProperty("DESCMOEDA"    ,STR0006, "string", STR0006, "DESCMOEDA"   ,,,,.F. ) // "Denominação"
    self:oSchema:addProperty("NomeEmpresa"  ,STR0007, "string", STR0007, "NomeEmpresa" ,,,,.F. ) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"   ,STR0008, "string", STR0008, "NomeFilial"  ,,,,.F. ) // "Nome Filial"

return self:oSchema
