#include "protheus.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.referenceplanreason.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT2,CVD,CVN",name="Razao do Plano Referencial",country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CostCenterSmartViewBusinessObject
Classe para criação do Objeto de Negócio do Razão do Plano Referencial para o SmartView Reason Reference Plan
Fonte/Perg      CTBR404	/ CTBR404
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
class ReferencePlanReasonSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method Exec_Query()   as character
    protected data jItems           as json
    protected data jDados           as json
    protected data aSelFil          as array
    protected data oIntegratedProvider  as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
@return object: self
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object class ReferencePlanReasonSmartviewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)        //"Razao do Plano Referencial"
    self:setPergunte( "CTBR404" )       //Indica o pergunte que ser? utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Renato July
@since          18/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class ReferencePlanReasonSmartviewBusinessObject
return STR0002  //"Objeto contendo informações sobre o relatório de Razão do Plano Referencial"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAreas() as array class ReferencePlanReasonSmartviewBusinessObject
return {STR0003} //Contabilidade

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ReferencePlanReasonSmartviewBusinessObject

    Local cAlias            as character
    Local cQuery            as character
    Local cCtaRefAnt := ""  as character   
    Local aSaldoAnt         as array     
    Local nSaldoAnt         as numeric     
    Local nSaldoAtu         as numeric
    Local TAM_VALOR := 20   as numeric
    Local lTrocouCta := .F. as logical
    Local lImpSemMov := .T. as logical
    Local jParams           as json

    //Popula variáves das pergutas
    jParams := oFilter:getParameters()
	CTBsetValueMVPAR(jParams, "CTBR404")

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    cPlanoRef	:= MV_PAR01
    cVersao		:= MV_PAR02
    dDataIni	:= MV_PAR03
    dDataFim	:= MV_PAR04
    cMoeda		:= MV_PAR05
    cTpSald		:= PadR(AllTrim(MV_PAR06), TamSX3("CT2_TPSALD")[1])
    lImpSemMov	:= Iif(MV_PAR07 == 1,.T.,.F.)

    //Monta a query
    cQuery := self:Exec_Query(oFilter)

    cAlias := MPSysOpenQuery(cQuery)

    dbSelectArea(cAlias)
    (cAlias)->(DBGoTop())

    While !(cAlias)->(Eof())

        aSaldoAnt   := array(0)
        nSaldoAnt   := 0

        If cCtaRefAnt <> (cAlias)->CTAREF
            aSaldoAnt := SaldoCVNFil(cPlanoRef,cVersao,(cAlias)->CTAREF,dDataIni,cMoeda,cTpSald,.F.,,::aSelFil)
            nSaldoAnt := aSaldoAnt[6]
            nSaldoAtu := nSaldoAnt
            lTrocouCta := .T.
        Else
            lTrocouCta := .F.
        EndIf
        
        If Empty(cCtaRefAnt) .Or. lTrocouCta
            cCtaRefAnt := (cAlias)->CTAREF
            (cAlias)->(DbSkip())
    		Loop
        Endif

        If !lImpSemMov .And. (cAlias)->VALOR == 0
            cCtaRefAnt := (cAlias)->CTAREF
            (cAlias)->(DbSkip())
    		Loop
        EndIf

        nSaldoAtu := nSaldoAtu - (cAlias)->VLRDEB + (cAlias)->VLRCRD

        //Cria registro
        ::jItems := JsonObject():New()

        ::jItems["FILIAL"]		:= (cAlias)->FILIAL
        ::jItems["CTAREF"]		:= (cAlias)->CTAREF
        ::jItems["DSCCTAREF"]	:= (cAlias)->DSCCTAREF
        ::jItems["DATAL"]		:= Iif(!Empty((cAlias)->DATAL), FwTimeStamp(5, SToD((cAlias)->DATAL), "00:00:00"), nil)     // Data do Lancamento
        ::jItems["LOTE"] 		:= (cAlias)->LOTE
        ::jItems["SUBLOTE"] 	:= (cAlias)->SUBLOTE
        ::jItems["DOC"] 		:= (cAlias)->DOC
        ::jItems["LINHA"]		:= (cAlias)->LINHA
        ::jItems["CONTA"]		:= (cAlias)->CONTA
        ::jItems["XPARTIDA"]   	:= (cAlias)->XPARTIDA
        ::jItems["TIPOLAN"]     := Iif((cAlias)->TIPOLAN == "1", "D", "C")
        ::jItems["VALOR"]		:= (cAlias)->VALOR
        ::jItems["VLRDEB"]		:= (cAlias)->VLRDEB
        ::jItems["VLRCRD"]		:= (cAlias)->VLRCRD
        ::jItems["HIST"]     	:= (cAlias)->HIST
        ::jItems["CUSTO"]		:= (cAlias)->CUSTO
        ::jItems["ITEM"]		:= (cAlias)->ITEM
        ::jItems["CLVL"]		:= (cAlias)->CLVL
        ::jItems["SEQLAN"]		:= (cAlias)->SEQLAN
        ::jItems["SEQHIS"]		:= (cAlias)->SEQHIS
        ::jItems["EMPORI"]		:= (cAlias)->EMPORI
        ::jItems["FILORI"]		:= (cAlias)->FILORI
        ::jItems["SLDANT"]		:= nSaldoAnt
        ::jItems["SLDANT_M"]	:= ALLTRIM(ValorCTB(nSaldoAnt,,,TAM_VALOR,,.T.,PesqPict("CT2","CT2_VALOR"),,,,,,,.T.,.F.))
        ::jItems["SLDATU"]		:= nSaldoAtu
        ::jItems["SLDATU_M"]    := ALLTRIM(ValorCTB(nSaldoAtu,,,TAM_VALOR,2,.T.,PesqPict("CT2","CT2_VALOR"),,,,,,,.T.,.F.))

        self:processData()        
        self:oData:appendData( ::jItems )

        cCtaRefAnt := (cAlias)->CTAREF
        (cAlias)->(DbSkip())
          
    EndDo

    (cAlias)->(DBCloseArea())

    //Elimina da memória
    FreeObj(self:jDados)
    FreeObj(jParams)
    FreeObj(oFilter:getParameters())  

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         Douglas Silva
@since          12/04/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class ReferencePlanReasonSmartviewBusinessObject

    self:addProperty( "CTAREF"      , STR0004 , "string"  , STR0004 , "CTAREF"    )  // Codigo da Conta Referencial
    self:addProperty( "DSCCTAREF"   , STR0005 , "string"  , STR0005 , "DSCCTAREF" )  // Descrição Conta Referencial
    self:addProperty( "DATAL"	    , STR0006 , "date"    , STR0006 , "DATAL"     )  // Data do Lancamento
    self:addProperty( "LOTE" 	    , STR0007 , "string"  , STR0007 , "LOTE"      )  // Lote
    self:addProperty( "SUBLOTE" 	, STR0008 , "string"  , STR0008 , "SUBLOTE"   )  // Sub-Lote
    self:addProperty( "DOC" 		, STR0009 , "string"  , STR0009 , "DOC"       )  // Documento
    self:addProperty( "LINHA"	    , STR0010 , "string"  , STR0010 , "LINHA"     )  // Linha
    self:addProperty( "CONTA"	    , STR0011 , "string"  , STR0011 , "CONTA"     )  // Codigo da Conta
    self:addProperty( "XPARTIDA"    , STR0012 , "string"  , STR0012 , "XPARTIDA"  )  // Contra Partida
    self:addProperty( "TIPOLAN"     , STR0013 , "string"  , STR0013 , "TIPOLAN"   )  // Tipo do Registro (Debito/Credito/Continuacao)
    self:addProperty( "VALOR"	    , STR0014 , "number"  , STR0014 , "VALOR"     )  // Valor
    self:addProperty( "HIST"        , STR0015 , "string"  , STR0015 , "HIST"      )  // Historico
    self:addProperty( "CUSTO"	    , STR0016 , "string"  , STR0016 , "CUSTO"     )  // Centro de Custo
    self:addProperty( "ITEM"		, STR0017 , "string"  , STR0017 , "ITEM"      )  // Item Contabil
    self:addProperty( "CLVL"		, STR0018 , "string"  , STR0018 , "CLVL"      )  // Classe de Valor
    self:addProperty( "SEQLAN"	    , STR0019 , "string"  , STR0019 , "SEQLAN"    )  // Sequencia do Lancamento
    self:addProperty( "SEQHIS"	    , STR0020 , "string"  , STR0020 , "SEQHIS"    )  // Seq do Historico
    self:addProperty( "EMPORI"	    , STR0021 , "string"  , STR0021 , "EMPORI"    )  // Empresa Original
    self:addProperty( "FILORI"	    , STR0022 , "string"  , STR0022 , "FILORI"    )  // Filial Original
    self:addProperty( "FILIAL"	    , STR0024 , "string"  , STR0024 , "FILIAL"    )  // Filial do sistema
    self:addProperty( "VLRDEB"	    , STR0025 , "number"  , STR0025 , "VLRDEB"    )  // Valor Debito
    self:addProperty( "VLRCRD"	    , STR0026 , "number"  , STR0026 , "VLRCRD"    )  // Valor Credito
    self:addProperty( "XPARTCC"     , STR0027 , "string"  , STR0027 , "XPARTCC"   )  // C. Part. C.Custo
    self:addProperty( "XPARTIT"     , STR0028 , "string"  , STR0028 , "XPARTIT"   )  // C. Part. Item
    self:addProperty( "XPARTCLVL"   , STR0029 , "string"  , STR0029 , "XPARTCLVL" )  // C. Part. Clvl
    self:addProperty( "SLDANT"	    , STR0030 , "number"  , STR0030 , "SLDANT"    )  // Saldo Anterior
    self:addProperty( "SLDANT_M"	, STR0033 , "string"  , STR0033 , "SLDANT_M"  )  // Saldo Anterior Mascara
    self:addProperty( "SLDATU"	    , STR0031 , "number"  , STR0031 , "SLDATU"    )  // Saldo Atual
    self:addProperty( "SLDATU_M"	, STR0034 , "string"  , STR0034 , "SLDATU_M"  )  // Saldo Atual Mascara

    self:oSchema:addParameter("SV_MULTBRANCH", STR0032, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
@author         Renato July
@since          11/09/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ReferencePlanReasonSmartviewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
@return object: self:oData
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class ReferencePlanReasonSmartviewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
@return object: self:oData
@author         Renato July
@since          11/09/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class ReferencePlanReasonSmartviewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} Exec_Query
Executa a query conforme parametrização
@author         Renato July
@since          11/09/2023
@version        12.1.2210
/*/
//-------------------------------------------------------------------
method Exec_Query( oFilter as object ) as character class ReferencePlanReasonSmartviewBusinessObject

    Local cQuery                as character
    Local cSpace := Space(01)   as character
    Local oStatement            as object
    Local nContaOrder := 1      as numeric

    //Seleção de dados
	cQuery	:= " SELECT * FROM "                                                                                                    
	cQuery	+= " ( "                                                                                                                
	cQuery	+= " SELECT "
	cQuery	+= " COALESCE(CT2_FILIAL, '')  FILIAL , "
    cQuery	+= " CVN_CTAREF                CTAREF , "
    cQuery	+= " CVN_DSCCTA                DSCCTAREF , "
    cQuery	+= " COALESCE(CT2_DEBITO, '')  CONTA , "
    cQuery	+= " COALESCE(CT2_CCD, '')     CUSTO , "
    cQuery	+= " COALESCE(CT2_ITEMD, '')   ITEM , "
    cQuery	+= " COALESCE(CT2_CLVLDB, '')  CLVL , "
    cQuery	+= " COALESCE(CT2_DATA, '')    DATAL , "
    cQuery	+= " COALESCE(CT2_TPSALD, '')  TPSALD , "
	cQuery	+= " COALESCE(CT2_DC, '')      DC , "
    cQuery	+= " COALESCE(CT2_LOTE, '')    LOTE , "
    cQuery	+= " COALESCE(CT2_SBLOTE, '')  SUBLOTE , "
    cQuery	+= " COALESCE(CT2_DOC, '')     DOC , "
    cQuery	+= " COALESCE(CT2_LINHA, '')   LINHA , "
	cQuery	+= " COALESCE(CT2_CREDIT, '')  XPARTIDA , "
    cQuery	+= " COALESCE(CT2_CCC, '')     XPARTCC , "
    cQuery	+= " COALESCE(CT2_ITEMC, '')   XPARTIT , "
    cQuery	+= " COALESCE(CT2_CLVLCR, '')  XPARTCLVL , "
	cQuery	+= " COALESCE(CT2_HIST, '')    HIST , "
    cQuery	+= " COALESCE(CT2_SEQHIS, '')  SEQHIS , "
    cQuery	+= " COALESCE(CT2_SEQLAN, '')  SEQLAN , "
    cQuery	+= " '1'                       TIPOLAN , "
    cQuery	+= " COALESCE(CT2_VALOR, 0)    VALOR , "
	cQuery	+= " COALESCE(CT2_VALOR, 0)    VLRDEB , "
    cQuery	+= " 0                         VLRCRD , "
    cQuery	+= " COALESCE(CT2_EMPORI, '')  EMPORI , "
    cQuery	+= " COALESCE(CT2_FILORI, '')  FILORI "
	cQuery	+= " FROM "+RetSqlName("CVN")+" CVN " 

	cQuery	+= " LEFT JOIN "+RetSqlName("CVD")+" CVD ON CVD.D_E_L_E_T_ = ? " 
    cQuery	+= "  AND CVD_FILIAL   = ? " 
	cQuery	+= "  AND CVD_CODPLA   = CVN_CODPLA "
   	cQuery	+= "  AND CVD_VERSAO   = CVN_VERSAO "
    cQuery	+= "  AND CVD_CTAREF   = CVN_CTAREF "

	cQuery	+= " LEFT JOIN "+RetSqlName("CT2")+" CT2 ON CT2.D_E_L_E_T_ = ? "
    if !empty(::aSelFil)
	    cQuery	+= "  AND CT2.CT2_FILIAL   IN ( ? ) "
    else
        cQuery	+= "  AND CT2.CT2_FILIAL   =  ?  "
    endif
	cQuery	+= "  AND CT2_DATA BETWEEN  ? AND ? "
	cQuery	+= "  AND CT2.CT2_MOEDLC    = ? "
	cQuery	+= "  AND CT2.CT2_TPSALD    = ? "
    cQuery	+= "  AND CT2.CT2_DEBITO    = CVD_CONTA "
	cQuery  += "  AND CT2.CT2_VALOR    <> ? "
	cQuery  += "  AND (CT2.CT2_DC       = ? OR CT2.CT2_DC = ?) "

	cQuery	+= " WHERE CVN.CVN_FILIAL = ? "
	cQuery	+= "   AND CVN.CVN_CODPLA = ? "
	cQuery	+= "   AND CVN.CVN_VERSAO = ? "
	cQuery	+= "   AND CVN.CVN_CLASSE = ?"

    //Os filtros serão setados na interface do novo TReports 
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")    

	cQuery	+= " UNION "

	cQuery	+= " SELECT "
	cQuery	+= " COALESCE(CT2_FILIAL, '')  FILIAL , "
    cQuery	+= " CVN_CTAREF                CTAREF ,"
    cQuery	+= " CVN_DSCCTA                DSCCTAREF ,"
    cQuery	+= " COALESCE(CT2_CREDIT, '')  CONTA ,"
    cQuery	+= " COALESCE(CT2_CCC, '')     CUSTO ,"
    cQuery	+= " COALESCE(CT2_ITEMC, '')   ITEM ,"
    cQuery	+= " COALESCE(CT2_CLVLCR,'')   CLVL ,"
    cQuery	+= " COALESCE(CT2_DATA, '')    DATAL  ,"
    cQuery	+= " COALESCE(CT2_TPSALD, '')  TPSALD  ,"
	cQuery	+= " COALESCE(CT2_DC, '')      DC ,"
    cQuery	+= " COALESCE(CT2_LOTE, '')    LOTE ,"
    cQuery	+= " COALESCE(CT2_SBLOTE, '')  SUBLOTE ,"
    cQuery	+= " COALESCE(CT2_DOC, '')     DOC ,"
    cQuery	+= " COALESCE(CT2_LINHA, '')   LINHA ,"
	cQuery	+= " COALESCE(CT2_DEBITO, '')  XPARTIDA ,"
    cQuery	+= " COALESCE(CT2_CCD, '')     XPARTCC ,"
    cQuery	+= " COALESCE(CT2_ITEMD, '')   XPARTIT ,"
    cQuery	+= " COALESCE(CT2_CLVLDB, '')  XPARTCLVL ,"
	cQuery	+= " COALESCE(CT2_HIST, '')    HIST ,"
    cQuery	+= " COALESCE(CT2_SEQHIS, '')  SEQHIS ,"
    cQuery	+= " COALESCE(CT2_SEQLAN, '')  SEQLAN ,"
    cQuery	+= " '2'                       TIPOLAN ,"
    cQuery	+= " COALESCE(CT2_VALOR, 0)    VALOR ,"
    cQuery	+= " 0                         VLRDEB, 	"
	cQuery	+= " COALESCE(CT2_VALOR, 0)    VLRCRD ,"
    cQuery	+= " COALESCE(CT2_EMPORI, '')  EMPORI ,"
    cQuery	+= " COALESCE(CT2_FILORI, '')  FILORI "
	cQuery	+= " FROM "+RetSqlName("CVN")+" CVN " 

	cQuery	+= " LEFT JOIN "+RetSqlName("CVD")+" CVD ON CVD.D_E_L_E_T_ = ? " 
    cQuery	+= "  AND CVD.CVD_FILIAL   = ? " 
	cQuery	+= "  AND CVD.CVD_CODPLA   = CVN_CODPLA "
    cQuery	+= "  AND CVD.CVD_VERSAO   = CVN_VERSAO "
    cQuery	+= "  AND CVD.CVD_CTAREF   = CVN_CTAREF "

	cQuery	+= " LEFT JOIN "+RetSqlName("CT2")+" CT2 ON CT2.D_E_L_E_T_ = ? "
    if !empty(::aSelFil)
	    cQuery	+= "  AND CT2.CT2_FILIAL   IN ( ? ) "
    else
        cQuery	+= "  AND CT2.CT2_FILIAL   =  ?  "
    endif
	cQuery	+= "  AND CT2.CT2_DATA     BETWEEN ? AND ? "
	cQuery	+= "  AND CT2.CT2_MOEDLC   = ? "
	cQuery	+= "  AND CT2.CT2_TPSALD   = ? "
    cQuery	+= "  AND CT2.CT2_CREDIT   = CVD_CONTA "
	cQuery  += "  AND CT2.CT2_VALOR   <> ? "
	cQuery  += "  AND (CT2.CT2_DC = ? OR CT2.CT2_DC = ?) "

	cQuery	+= " WHERE CVN.CVN_FILIAL = ? "
	cQuery	+= "   AND CVN.CVN_CODPLA = ? "
	cQuery	+= "   AND CVN.CVN_VERSAO = ? "
	cQuery	+= "   AND CVN.CVN_CLASSE = ? "

    //Os filtros serão setados na interface do novo TReports     
    cQuery += IIf(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery	+= " ) CVNLANCTO " 

    //Define o campo de ordenação
	cQuery	+= " ORDER BY CTAREF,DATAL,LOTE,SUBLOTE,DOC,LINHA "

    //Objeto executa o comando SQL inúmeras vezes.
    oStatement := FWExecStatement():New(changequery(cQuery))  
    
    //Popula primeira parte da query
    oStatement:SetString( nContaOrder++ , cSpace            )
    oStatement:SetString( nContaOrder++ , FWxFilial("CVD")  )
    oStatement:SetString( nContaOrder++ , cSpace            )
    if !empty(::aSelFil)
	    oStatement:SetIn( nContaOrder++ , ::aSelFil         )
    else
        oStatement:SetString( nContaOrder++ , FWxFilial("CT2"))
    endif
    oStatement:SetDate(   nContaOrder++ , dDataIni          )
    oStatement:SetDate(   nContaOrder++ , dDataFim          )
    oStatement:SetString( nContaOrder++ , cMoeda            )
    oStatement:SetString( nContaOrder++ , cTpSald           )
    oStatement:SetNumeric( nContaOrder++ , 0                )
    oStatement:SetString( nContaOrder++ , "1"               )
    oStatement:SetString( nContaOrder++ , "3"               )
    oStatement:SetString( nContaOrder++ , FWxFilial("CVN")  )
    oStatement:SetString( nContaOrder++ , cPlanoRef         )
    oStatement:SetString( nContaOrder++ , cVersao           )
    oStatement:SetString( nContaOrder++ , "2"               )

    //Popula segunda parte da query
    oStatement:SetString( nContaOrder++ , cSpace            )
    oStatement:SetString( nContaOrder++ , FWxFilial("CVD")  )
    oStatement:SetString( nContaOrder++ , cSpace            )
    if !empty(::aSelFil)
	    oStatement:SetIn( nContaOrder++ , ::aSelFil         )
    else
        oStatement:SetString( nContaOrder++ , FWxFilial("CT2"))
    endif
    oStatement:SetDate(   nContaOrder++ , dDataIni          )
    oStatement:SetDate(   nContaOrder++ , dDataFim          )
    oStatement:SetString( nContaOrder++ , cMoeda            )
    oStatement:SetString( nContaOrder++ , cTpSald           )
    oStatement:SetNumeric( nContaOrder++ , 0                )
    oStatement:SetString( nContaOrder++ , "2"               )
    oStatement:SetString( nContaOrder++ , "3"               )
    oStatement:SetString( nContaOrder++ , FWxFilial("CVN")  )
    oStatement:SetString( nContaOrder++ , cPlanoRef         )
    oStatement:SetString( nContaOrder++ , cVersao           )
    oStatement:SetString( nContaOrder++ , "2"               )

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := oStatement:GetFixQuery()

return cQuery
