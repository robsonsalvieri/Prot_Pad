#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetitem.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTU, CTD", name="Balancete de Item Contabil", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceCostCenterSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete de Item Contabil
para o TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceSheetItemSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object 
    public method getAreas()                as array
    public method getDescription()          as Character
    public method processCustomData()       as json

    //usados na internacionalizacao
    public method preschema()      		    as object 
    public method preData()        		    as object
    public method processData()    		    as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
  
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object
    
    public method ExecDBSkipTemp()          as logical

    private method f120Soma() as numeric

    protected data nDecimais as numeric
    protected data cPicture  as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceSheetItemSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete de Item Contabil"
    self:setPergunte("CTR120") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceSheetItemSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Item Contabil"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BalanceSheetItemSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class BalanceSheetItemSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceSheetItemSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processData() as json class BalanceSheetItemSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerPlanSchema() as object class BalanceSheetItemSmartViewBusinessObject
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("NomeEmpresa",STR0004, "string", STR0004, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0005, "string", STR0005, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0006, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 

method processCustomData() as json class BalanceSheetItemSmartViewBusinessObject

    local TAM_VALOR := 17 as numeric
    local nTotDeb   as numeric
    local nTotCrd   as numeric
    local nTotMov   as numeric

    nTotDeb := self:f120Soma("D")
    nTotCrd := self:f120Soma("C")

    nTotMov := (nTotCrd - nTotDeb)

    ::jDados["SALDODEB"	  ] := nTotDeb
    ::jDados["SALDODEB_M" ] := ValorCTB(nTotDeb,,,TAM_VALOR-3,::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDOCRD"	  ] := nTotCrd
    ::jDados["SALDOCRD_M" ] := ValorCTB(nTotCrd,,,TAM_VALOR-3,::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["MOVIMENTO"  ] := nTotMov
    ::jDados["MOVIMENTO_M"] := iif(nTotMov < 0, ValorCTB(nTotMov,,,TAM_VALOR-3,::nDecimais,.T.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.),;
			                   iif(nTotMov > 0, ValorCTB(nTotMov,,,TAM_VALOR-3,::nDecimais,.T.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.), nil)) 
	::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial"]  := self:cBranchName
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetItemSmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetItemSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetItemSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBSetValueMVPAR(oFilter:getParameters(), "CTR120")
    self:handleCtgGerPlanParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceSheetItemSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    Local cItemIni          as Character
    Local cItemFim          as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    Local nDivide := 1      as numeric
    local lMov              as logical
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CTU"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CTU"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CTU"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CTU"]
    EndIf

    dDataIni    := MV_PAR01                                             //Data Inicial ?                
    dDataFim    := MV_PAR02                                             //Data Final ?  
    cContaIni   := Space(Len(CriaVar("CT1_CONTA")))                     //Conta Inicial ?               
    cContaFim   := Repl('Z',Len(CriaVar("CT1_CONTA")))                  //Conta Final ?  
    cItemIni    := MV_PAR03                                             //Item Contabil Inicial ?
    cItemFim    := MV_PAR04                                             //Item Contabil Final?   
                
    lImpSint    := iif(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.)            //Imprime Contas ?              
    aSetOfBook  := iif(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf(""))//Cod. Config. Livros ?         
    lSldZerado  := iif(MV_PAR07==1,.T.,.F.)                             //Saldos Zerados                            
    cMoeda      := iif(!empty(MV_PAR08),MV_PAR08,cMoeda)                //Moeda                                            
    cTpsald     := iif(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) //Tipo de Saldo
    cFilSeg     := MV_PAR12                                             //Filtra Segmento No. ?  
    cConteudIni := MV_PAR13                                             //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR14                                             //Conteudo Fim Segmen ?        
    cConteudCont:= MV_PAR15                                             //Conteudo Contido em ?
    lMov        := iif(MV_PAR16 == 1,.F.,.T.)                           //Se imprime saldo movimento do periodo
    lPrintZero  := iif(MV_PAR18==1,.T.,.F.)                             //Imprime Valor 0.00  ?
    lImpCod     := iif(MV_PAR19==1,.T.,.F.)                             //Imprime codigo ?
    nDivide     := iif(empty(MV_PAR20),nDivide,MV_PAR20)
    lImpAntLP   := iif(MV_PAR21 == 1,.T.,.F.)                           //Posição Ant. L/P?
    dDataLP     := MV_PAR22                                             //Data Lucros/Perdas?      
    
    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cItemIni       := cItemIni  
    self:cItemFim       := cItemFim  
    self:cAlias         := "CTU" //são obrigatórios
    self:cIdent         := "CTD" //são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := lMov
    self:lImpConta      := .F.
    self:aGeren         := nil

    ::nDecimais := DecimalCTB(::aSetOfBook, MV_PAR08)
    ::cPicture  := ::aSetOfBook[4]  
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) as logical class BalanceSheetItemSmartViewBusinessObject
   
    local lRet := .T.
    
    If MV_PAR05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOITEM == "2"
			lRet := .F.
		EndIf
	EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} f120Soma
Soma o valor do lançamento
 
@return numérico
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method f120Soma(cTipo as character) as numeric class BalanceSheetItemSmartViewBusinessObject

    local nRetValor	:= 0 as numeric

	If MV_PAR05 == 1 // So imprime Sinteticas - Soma Sinteticas
		If cArqTmp->TIPOITEM == "1" .And. cArqTmp->NIVEL1
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	Else // Soma Analiticas		
		If cArqTmp->TIPOITEM == "2"
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf		
	EndIf                     

return nRetValor
