#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.valueclassreason.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2, CTT, CTD, CTH, CTZ, CTO", name="Razão Classe Valor", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} ReferentialChartofAccountsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Razão Classe Valor
para o TReports
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class ValueClassReasonSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbgerraz 

    public method new()                   as object
    public method getAreas()              as array
    public method getDescription()        as character

    //usados na internacionalizacao
    public method preschema()             as object 
    public method preData()               as object
    public method processData()           as json
    public method processCustomData()     as json

    public method getCtgGerRazData()      as object    
    public method getCtGerRazSchema()     as object
    public method handleCtgGerRazParams()

    protected method getSaldoCTI()

    protected data jDados       as json
    protected data nTotCtaDeb   as numeric
    protected data nTotCtaCred  as numeric
    protected data nTotCtaSld   as numeric
    protected data nSaldoAtu    as numeric
    protected data aSaldo       as array
    protected data aSaldoAnt    as array
    protected data lCusto       as logical
    protected data lItem        as logical

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class ValueClassReasonSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Razao Classe Valor"

    //Indica o pergunte que será utilizado no relatório
    self:setPergunte("CTR490")

    ::nTotCtaDeb  := 0
    ::nTotCtaCred := 0
    ::nTotCtaSld  := 0
    ::nSaldoAtu   := 0
    ::aSaldo      := {}
    ::aSaldoAnt   := {}
    
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class ValueClassReasonSmartViewBusinessObject
return STR0002 // "Objeto contendo informacoes sobre o Razao Classe Valor" 

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class ValueClassReasonSmartViewBusinessObject
return {STR0003} // "Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerRazSchema
Método chamada antes do getSchema do objeto principal
 
@return object: self:oSchema
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerRazSchema() as object class ValueClassReasonSmartViewBusinessObject

    local lCanFilter := .F. as logical
    
    //Adição de novos campos ao schema
    self:oSchema:addProperty("DESCCLVL",    STR0005, "string",  STR0005, "DESCCLVL",,,,lCanFilter)     // Descricao Cl Valor
    self:oSchema:addProperty("TROCACTA",    STR0006, "boolean", STR0006, "TROCACTA",,,,lCanFilter)     // Logico para identificar se trocou a conta
    self:oSchema:addProperty("TOTCTADEB",   STR0007, "number",  STR0007, "TOTCTADEB",,,,lCanFilter)    // Total Conta Debito
    self:oSchema:addProperty("TOTCTACRED",  STR0008, "number",  STR0008, "TOTCTACRED",,,,lCanFilter)   // Total Conta Credito
    self:oSchema:addProperty("TOTCTASLD",   STR0009, "number",  STR0009, "TOTCTASLD",,,,lCanFilter)    // Total Conta Saldo
    self:oSchema:addProperty("NomeEmpresa", STR0010, "string",  STR0010, "NomeEmpresa",,,,lCanFilter)  // Nome Empresa
    self:oSchema:addProperty("NomeFilial",  STR0011, "string",  STR0011, "NomeFilial",,,,lCanFilter)   // Nome Filial

    self:oSchema:addParameter("SV_MULTBRANCH", STR0012, "string", .T.)  //Filiais
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerRazData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerRazData(nPage as numeric, oFilter as object) as object class ValueClassReasonSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR490")

    self:handleCtgGerRazParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerRazParams
Atribui valores aos atributos da classe principal

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerRazParams(oFilter as object)  class ValueClassReasonSmartViewBusinessObject
    
    local cTipo   as character
    local aSelFil as array
    local lJunta  as logical

    //Carrega os valores default
    cTipo       := "4" // Razão por Classe de Valor    
    aSelFil     := {}
    lJunta      := .T.

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Atribui parâmetros do pergunte
    ::cCLVLIni     := MV_PAR01                      // Da Classe de Valor ?
    ::cCLVLFim     := MV_PAR02                      // Ate a Classe de Valor ?
    ::dDataIni     := MV_PAR03                      // Da data ?
    ::dDataFim     := MV_PAR04                      // Ate a data ?
    ::cMoeda       := iif(!empty(MV_PAR05), MV_PAR05, "01") // Moeda ?
    ::cSaldo       := AllTrim(MV_PAR06)             // Tipo de Saldo ?
    ::aSetOfBook   := iif(!empty(MV_PAR07), CTBSetOf(MV_PAR07), CTBSetOf("")) // Cod. Config. Livros ?
    ::lAnalit      := iif(MV_PAR08 == 1, .T., .F.)  // Tipo Relatorio ? - 1-Analitico; 2-Sintetico
    ::lNoMov       := iif(MV_PAR09 == 1, .T., .F.)  // Impr. CLVL S/ Movim ?
    ::nCodConta    := MV_PAR10                      // Impr Cod Conta ?
    ::cContaIni    := MV_PAR12                      // Da Conta ?
    ::cContaFim    := MV_PAR13                      // Ate Conta ?
    ::lCusto	   := iif(MV_PAR14 == 1, .T., .F.)  // Imprime CC ?
    ::cCustoIni    := MV_PAR15                      // Do Centro de Custo ?
    ::cCustoFim    := MV_PAR16                      // Ate o Centro de Custo ?
    ::lItem	       := iif(MV_PAR17 == 1, .T., .F.)  // Imprime Item ?
    ::cItemIni     := MV_PAR18                      // Do Item Contabil ?
    ::cItemFim     := MV_PAR19                      // Ate o Item Contabil ?
    ::lCCRes       := iif(MV_PAR24 == 2, .T., .F.)  // Impr Cod C.Custo ?
    ::lItemRes     := iif(MV_PAR25 == 2, .T., .F.)  // Impr Cod Item ?
    ::lClVlRes     := iif(MV_PAR26 == 2, .T., .F.)  // Impr Cod Cl.Valor ?
    ::lPrintZero   := iif(MV_PAR27 == 1, .T., .F.)  // Imprime Valor 0,00 ?

    //Atribui valores default
    ::cTipo        := cTipo
    ::aSelFil      := aSelFil       // Seleciona Filiais ?
    ::lJunta       := lJunta        // Indica se junta CC ou nao

return

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados adicionais no data
 
@return json: self:jDados
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processCustomData() as json class ValueClassReasonSmartViewBusinessObject
    
    local cMoedaDesc    as character
    local cDescClVl     as character
    local cPicture      as character
    local nDecimais     as numeric
    local TAM_VALOR := 23 as numeric
    local lTrocaCta     as logical
    local lTrocaClVl    as logical
    local lNovoSaldo    as logical

    //Carrega os valores default    
    cMoedaDesc  := ::cMoeda
    nDecimais 	:= DecimalCTB(::aSetOfBook, ::cMoeda)
    cPicture 	:= ::aSetOfBook[4]
    lNovoSaldo  := .F.

    //Desrição da Cl. Valor
    if CTH->(dbSeek(FWxFilial("CTH")+cArqTmp->CLVL))
        cDescClVl := &("CTH->CTH_DESC" + cMoedaDesc)
    endif

    //Verifica se trocou classe de valor
    if !empty(::cClvlAnt) .and. ::cClvlAnt <> cArqTmp->CLVL
        lTrocaClVl := .T.
    endif

    //Verifica se a próxima conta é diferente
    cCtaAnt  := cArqTMP->CONTA
    cArqTMP->(dbSkip())

    if ::cContaAnt <> cArqTMP->CONTA
        lTrocaCta  := .T.
    endif

    //Volta registro atual
    cArqTMP->(dbSkip(-1))

    //Carrega os saldos se trocou a classe de valor ou for a primeira linha
    if lTrocaClVl .or. empty(::aSaldoAnt)
        ::aSaldoAnt := {}
        self:getSaldoCTI()
        lNovoSaldo := .T. //Indica que pegou um novo saldo
    endif
    
    if lNovoSaldo 
        ::nSaldoAtu := ::aSaldoAnt[6]
    endif

    ::nSaldoAtu := ::nSaldoAtu - cArqTmp->LANCDEB + cArqTmp->LANCCRD

    // Totaliza por conta Analitico
    ::nTotCtaDeb  += cArqTMP->LANCDEB
    ::nTotCtaCred += cArqTMP->LANCCRD
    ::nTotCtaSld  += ::nSaldoAtu
    
    //Manipulação de conteúdo do json
    ::jDados["TPSLDANT"]    := ValorCTB(::aSaldoAnt[6],,,TAM_VALOR,nDecimais,.T.,cPicture,,,,,,,,.F.)        //cArqTMP->TPSLDANT                               
    ::jDados["TPSLDATU"]    := ValorCTB(::nSaldoAtu,,,TAM_VALOR,nDecimais,.T.,cPicture,,,,,,,::lPrintZero,.F.) //cArqTMP->TPSLDATU
    ::jDados["DESCCLVL"]    := cDescClVl
    ::jDados["TROCACTA"]    := iif(lTrocaCta, .T., .F.)
    ::jDados["TOTCTADEB"]   := iif(lTrocaCta, ::nTotCtaDeb, 0)
    ::jDados["TOTCTACRED"]  := iif(lTrocaCta, ::nTotCtaCred, 0)
    ::jDados["TOTCTASLD"]   := iif(lTrocaCta, ::nTotCtaSld, 0)
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

    //Se trocou a conta, reseta os totalizadores
    if lTrocaCta
        ::nTotCtaDeb := ::nTotCtaCred := ::nTotCtaSld := 0
    endif

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getSaldoCTI
Calcula os saldos
 
@return
 
@author Renan Gremes
@since 06/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getSaldoCTI() class ValueClassReasonSmartViewBusinessObject

    //Se imprime centro de custo e item, ira considerar o filtro do centro de custo e do item para 
	//calculo do saldo ant.
	if self:lCusto .And. self:lItem
		::aSaldoAnt	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,::cItemIni,::cItemFim,;
							  ::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,;
							  ::dDataIni,::cMoeda,::cSaldo,::aSelFil)
		::aSaldo	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,::cItemIni,::cItemFim,;
							  ::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,;
							  cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
	//Se imprime centro de custo, ira considerar o filtro do centro de custo para 
	//calculo do saldo ant.
	elseif self:lCusto .And. !self:lItem
		::aSaldoAnt	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,Space(Len(CTD->CTD_ITEM)),Repl("Z",Len(CTD->CTD_ITEM)),;
							  ::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,;
							  ::dDataIni,::cMoeda,::cSaldo,::aSelFil)
		::aSaldo	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,Space(Len(CTD->CTD_ITEM)),Repl("Z",Len(CTD->CTD_ITEM)),;
							  ::cCustoIni,::cCustoFim,::cContaIni,::cContaFim,;
							  cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
	//Se imprime item, ira considerar o filtro do item para calculo do saldo anterior.
	elseif !self:lCusto .And. self:lItem
		::aSaldoAnt	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,::cItemIni,::cItemFim,;
							  Space(Len(CTT->CTT_CUSTO)),Repl("Z",Len(CTT->CTT_CUSTO)),::cContaIni,::cContaFim,;
							  ::dDataIni,::cMoeda,::cSaldo,::aSelFil)
		::aSaldo	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,::cItemIni,::cItemFim,;
							  Space(Len(CTT->CTT_CUSTO)),Repl("Z",Len(CTT->CTT_CUSTO)),::cContaIni,::cContaFim,;
							  cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)

	elseif !self:lCusto .And. !self:lItem
		::aSaldoAnt	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,Space(Len(CTD->CTD_ITEM)),Repl("Z",Len(CTD->CTD_ITEM)),;
							  Space(Len(CTT->CTT_CUSTO)),Repl("Z",Len(CTT->CTT_CUSTO)),::cContaIni,::cContaFim,;
							  ::dDataIni,::cMoeda,::cSaldo,::aSelFil)
		::aSaldo	:= SaldTotCTI(cArqTmp->CLVL,cArqTMP->CLVL,Space(Len(CTD->CTD_ITEM)),Repl("Z",Len(CTD->CTD_ITEM)),;
							  Space(Len(CTT->CTT_CUSTO)),Repl("Z",Len(CTT->CTT_CUSTO)),::cContaIni,::cContaFim,;
							  cArqTmp->DATAL,::cMoeda,::cSaldo,::aSelFil)
	endif

return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    Sera usado para manipular o schema do objeto

    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ValueClassReasonSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    Ajusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ValueClassReasonSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    
    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ValueClassReasonSmartViewBusinessObject
return ::jDados
