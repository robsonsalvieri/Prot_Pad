#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.generaldiarybookmod2.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1,CT2", name="Livro Diario Geral Modelo 2", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} GeneralDiaryBookMod2SmartViewBusinessObject
Classe para criação do Objeto de Negócio de Diario Geral Modelo 2 para o TReports
Fonte/Perg      CTBR117	/ CTR117
@author         Gustavo Fernandes Campos
@since          06/12/2023
@version        12.1.2310
@Revitalização  SmartView
*/
//-------------------------------------------------------------------------------
Class GeneralDiaryBookMod2SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json 
    
    protected data jItens		   as json
    protected data oStatement      as object
    protected data cAlias          as character
    protected data oIntegratedProvider as object
    
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
new
@return object: self
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class GeneralDiaryBookMod2SmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName( STR0001 )  //"Diario Geral Modelo 2" 
    self:setPergunte("CTR117")      //Indica o pergunte que será utilizado no relatório
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class GeneralDiaryBookMod2SmartViewBusinessObject
return STR0002 //"Objeto contendo informações do Diario Geral Modelo 2"###

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@author         
@since          
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class GeneralDiaryBookMod2SmartViewBusinessObject
Return { STR0003 } //"Contabilidade"###

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author         
@since          
@version        12.1.2310
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class GeneralDiaryBookMod2SmartViewBusinessObject

    Local cQuery     := ""      as character
    Local cPicture   := ""      as character
    Local cMascara   := ""      as character
    Local cSeparador := ""      as character
    Local cDescMoeda := "01"    as character
    Local cMoeda                as character
    Local cTpSaldo              as character
    Local cFilCt1               as character
    Local cFilCt2               as character
    Local cCompanyName          as character
    Local cBranchName           as character
    Local cTmpCT1Fil            as character
    Local cTmpCT2Fil            as character
    Local nTamConta             as numeric
    Local nDecimais   := 0      as numeric
    Local nParamOrder := 1      as numeric
    Local aTamVal               as array
    Local aSetOfBook	        as array
    Local aSelFil               as array
    Local aDC := {"D","C","P","H"} as array
    Local lPrintZero	        as logical
    Local lColDbCr 		        as logical
    Local lIsRedStor            as logical 
    Local dDataIni              as date
    Local dDataFim              as date
    Local jParams               as json

    //Chama funcao que Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(),self:cPergunte) //"CTR117"

    jParams := oFilter:getParameters()

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := self:oIntegratedProvider:getCompanyName()
    cBranchName  := self:oIntegratedProvider:getBranchName()

    self:oIntegratedProvider:lMultSelectBranches := .T.
    self:oIntegratedProvider:lAllowBranchFilter  := .T.
    self:oIntegratedProvider:setBranchPar(jParams)
    self:oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(self:oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := self:oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := self:oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Carga dos MV_PAR
    dDataIni    := MV_PAR01
    dDataFim    := MV_PAR02
    cMoeda      := iif(Empty(MV_PAR03),"01",MV_PAR03)
	aSetOfBook  := IIf(empty(MV_PAR04),CTBSetOf(""),CTBSetOf(MV_PAR04))
    cTpSaldo    := iif(Empty(MV_PAR05),"1",MV_PAR05)
    lPrintZero	:= Iif(MV_PAR12 == 1,.T.,.F.)

    cFilCt1  := GetRngFil( aSelFil, "CT1", .T., @cTmpCT1Fil )
    cFilCt2  := GetRngFil( aSelFil, "CT2", .T., @cTmpCT2Fil )

    cQuery := " SELECT "  
    cQuery += " CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_SEGOFI, CT2_DATA, CT1_CONTA, CT2_DEBITO, CT2_CREDIT, CT1_DESC01, CT1_RES, CT1_CODIMP, CT2_VALOR, CT2_DC, CT2_LINHA, 'DB' CT2_ID_DC "
    cQuery += " FROM "+RetSqlName("CT2")+" CT2"
    cQuery += " LEFT JOIN "+RetSqlName("CT1")+" CT1 ON (CT2_DEBITO = CT1_CONTA) AND CT1_FILIAL ? AND CT1.D_E_L_E_T_ = ? "
    cQuery += " WHERE CT1_FILIAL ? AND CT2_FILIAL ? "
    cQuery += " AND CT2_DATA BETWEEN ? AND ? "
    cQuery += " AND CT2_MOEDLC = ? "
    cQuery += " AND CT2_TPSALD = ? "
    cQuery += " AND CT2_DC IN ( ? ) "
    cQuery += " AND CT2.D_E_L_E_T_ = ? "
    cQuery += " UNION ALL "
    cQuery += " SELECT "  
    cQuery += " CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_SEGOFI, CT2_DATA, CT1_CONTA, CT2_DEBITO, CT2_CREDIT, CT1_DESC01, CT1_RES, CT1_CODIMP, CT2_VALOR, CT2_DC, CT2_LINHA, 'CR' CT2_ID_DC "
    cQuery += " FROM "+RetSqlName("CT2")+" CT2"
    cQuery += " LEFT JOIN "+RetSqlName("CT1")+" CT1 ON (CT2_CREDIT = CT1_CONTA) AND CT1_FILIAL ? AND CT1.D_E_L_E_T_ = ? "
    cQuery += " WHERE CT1_FILIAL ? AND CT2_FILIAL ? "
    cQuery += " AND CT2_DATA BETWEEN ? AND ? "
    cQuery += " AND CT2_MOEDLC = ? "
    cQuery += " AND CT2_TPSALD = ? "  
    cQuery += " AND CT2_DC IN ( ? ) "
    cQuery += " AND CT2.D_E_L_E_T_ = ? "

    If MV_PAR18 == 1
        cQuery += " ORDER BY CT2_LOTE, CT2_SBLOTE, CT2_SEGOFI, CT2_LINHA, CT2_DATA "
    Else	
        cQuery += " ORDER BY CT2_LOTE, CT2_SBLOTE, CT2_DOC, CT2_LINHA, CT2_DATA "
    EndIf

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)  

    self:oStatement:SetUnsafe(nParamOrder++, cFilCt1         )
    self:oStatement:SetString(nParamOrder++, space(1)        )
    self:oStatement:SetUnsafe(nParamOrder++, cFilCt1         )
    self:oStatement:SetUnsafe(nParamOrder++, cFilCt2         )
    self:oStatement:SetString(nParamOrder++, DTOS( dDataIni ))
    self:oStatement:SetString(nParamOrder++, DTOS( dDataFim ))
    self:oStatement:SetString(nParamOrder++, cMoeda          )
    self:oStatement:SetString(nParamOrder++, cTpSaldo        )
    self:oStatement:SetIn(nParamOrder++,     {"1", "3"}      )
    self:oStatement:SetString(nParamOrder++, space(1)        )
    self:oStatement:SetUnsafe(nParamOrder++, cFilCt1         )
    self:oStatement:SetString(nParamOrder++, space(1)        )
    self:oStatement:SetUnsafe(nParamOrder++, cFilCt1         )
    self:oStatement:SetUnsafe(nParamOrder++, cFilCt2         )
    self:oStatement:SetString(nParamOrder++, DTOS( dDataIni ))
    self:oStatement:SetString(nParamOrder++, DTOS( dDataFim ))
    self:oStatement:SetString(nParamOrder++, cMoeda          )
    self:oStatement:SetString(nParamOrder++, cTpSaldo        )
    self:oStatement:SetIn(nParamOrder++,     {"2", "3"}      )
    self:oStatement:SetString(nParamOrder++, space(1)        )

    self:cAlias := self:oStatement:OpenAlias(GetNextAlias())

    nTamConta   := TAMSX3("CT1_CONTA")[1]
    aTamVal     := TAMSX3("CT2_VALOR")
	aCtbMoeda	:= CtbMoeda(MV_PAR03)
    lIsRedStor  := FindFunction("IsRedStor") .and. IsRedStor() 
    lColDbCr    := lIsRedStor
    nDecimais 	:= DecimalCTB(aSetOfBook,cMoeda)
	
	// seta a moeda
    cDescMoeda 	:= aCtbMoeda[2]

    If Empty(aSetOfBook[2])
	    cMascara := SuperGetMv("MV_MASCARA")
    Else
        cMascara := RetMasCtb(aSetOfBook[2],@cSeparador)
    EndIf

    // mascara do valor
    cPicture 	:= aSetOfBook[4]
    If Empty( cPicture )
        cPicture := "@E " + TmContab((self:cAlias)->CT2_VALOR,aTamVal[1],nDecimais)
    Endif

    While !(self:cAlias)->(Eof())
	        
        //Nova objeto
        ::jItens := JsonObject():new()

        ::jItens["DC"]    := aDC[val((self:cAlias)->CT2_DC)]
        ::jItens["LINHA"] := (self:cAlias)->CT2_LINHA
        
        If MV_PAR18 == 1
            ::jItens["DOCUMENTO"]:= (self:cAlias)->CT2_LOTE + (self:cAlias)->CT2_SBLOTE + "-" + (self:cAlias)->CT2_SEGOFI
        Else
            ::jItens["DOCUMENTO"]:= (self:cAlias)->CT2_LOTE + (self:cAlias)->CT2_SBLOTE + (self:cAlias)->CT2_DOC
        EndIf
        
        ::jItens["DATA"]:= totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->CT2_DATA)

        If MV_PAR13 == 2	    /// Impressao do Codigo Reduzido
            ::jItens["CONTA"]:= EntidadeCTB((self:cAlias)->CT1_RES,0 ,00,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
		ElseIf MV_PAR13 == 3 	/// Impressao do Codigo de Impressao (se o campo existir)
			::jItens["CONTA"]:= EntidadeCTB((self:cAlias)->CT1_CODIMP,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.)
		Else   					/// Impressao do Codigo Normal
			::jItens["CONTA"]:= EntidadeCTB((self:cAlias)->CT1_CONTA,0,0,nTamConta,.F.,cMascara,cSeparador,,,,,.F.) 
		Endif

        ::jItens["DOCTO"   ]:= (self:cAlias)->CT2_SEGOFI
        ::jItens["NUM_LOTE"]:= (self:cAlias)->CT2_LOTE
        ::jItens["SUBLOTE" ]:= (self:cAlias)->CT2_SBLOTE
        ::jItens["DOC"     ]:= (self:cAlias)->CT2_DOC

        ::jItens["DESC_CONTA"]:= (self:cAlias)->CT1_DESC01

        If len(aSetOfBook) > 0  .Or. !Empty(cPicture)
            ::jItens["CVALDEB_M" ]:= ValorCTB( if( (self:cAlias)->CT1_CONTA == (self:cAlias)->CT2_DEBITO,(self:cAlias)->CT2_VALOR , 0 ) ,,,aTamVal[1],nDecimais,.F.,cPicture,"1",,,,,,lPrintZero,.F.,lColDbCr)
            ::jItens["CVALCRED_M"]:= ValorCTB( if( (self:cAlias)->CT1_CONTA == (self:cAlias)->CT2_CREDIT,(self:cAlias)->CT2_VALOR ,0 ) ,,,aTamVal[1],nDecimais,.F.,cPicture,"2",,,,,,lPrintZero,.F.,lColDbCr)       
        EndIF

        ::jItens["CVALDEB"   ]:= IIf( (self:cAlias)->CT1_CONTA == (self:cAlias)->CT2_DEBITO,(self:cAlias)->CT2_VALOR , 0 )
        ::jItens["CVALCRED"  ]:= IIf( (self:cAlias)->CT1_CONTA == (self:cAlias)->CT2_CREDIT,(self:cAlias)->CT2_VALOR , 0 )

        ::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial" ] := cBranchName
        
        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())
    	   
    ENDDO

    dbSelectArea(self:cAlias)
    (self:cAlias)->(DBCloseArea())

    If Select(self:cAlias) == 0
        FErase((self:cAlias)+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf

    CtbTmpErase(cTmpCT1Fil)
    CtbTmpErase(cTmpCT2Fil)

    FreeObj(oFilter:getParameters())
    FreeObj(jParams)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class GeneralDiaryBookMod2SmartViewBusinessObject
    Local lCanFilter := .F. as logical

    self:oSchema:addProperty("DOCTO"       ,  STR0008   ,"string",STR0008  ,"DOCTO"      ,,,,lCanFilter)    // Numero Correlativo
    self:oSchema:addProperty("DOCUMENTO"   ,  STR0009   ,"string",STR0009  ,"DOCUMENTO"  ,,,,lCanFilter)    // Documento
    self:oSchema:addProperty("DC"          ,  STR0010   ,"string",STR0010  ,"DC"         ,,,,lCanFilter)    // Partida Lançamento
    self:oSchema:addProperty("LINHA"       ,  STR0011   ,"string",STR0011  ,"LINHA"      ,,,,lCanFilter)    // Linha
    self:oSchema:addProperty("DATA"        ,  STR0012   ,"date"  ,STR0012  ,"DATA"       ,,,,lCanFilter)    // Data               
    self:oSchema:addProperty("CONTA"       ,  STR0013   ,"string",STR0013  ,"CONTA"      ,,,,lCanFilter)    // Conta               
    self:oSchema:addProperty("DESC_CONTA"  ,  STR0014   ,"string",STR0014  ,"DESC_CONTA" ,,,,lCanFilter)    // Descrição da Conta
    self:oSchema:addProperty("CVALDEB"     ,  STR0015   ,"number",STR0015  ,"CVALDEB"    ,,,,lCanFilter)    // Valor Débito      
    self:oSchema:addProperty("CVALCRED"    ,  STR0016   ,"number",STR0016  ,"CVALCRED"   ,,,,lCanFilter)    // Valor Crédito    
    self:oSchema:addProperty("CVALDEB_M"   ,  STR0017   ,"string",STR0017  ,"CVALDEB_M"  ,,,,lCanFilter)    // Valor Débito Mascara
    self:oSchema:addProperty("CVALCRED_M"  ,  STR0018   ,"string",STR0018  ,"CVALCRED_M" ,,,,lCanFilter)    // Valor Crédito Mascara
    self:oSchema:addProperty("NUM_LOTE"    ,  STR0019   ,"string",STR0019  ,"NUM_LOTE"   ,,,,lCanFilter)    // Numero do lote
    self:oSchema:addProperty("SUBLOTE"     ,  STR0020   ,"string",STR0020  ,"SUBLOTE"    ,,,,lCanFilter)    // Sub-Lote
    self:oSchema:addProperty("DOC"         ,  STR0021   ,"string",STR0021  ,"DOC"        ,,,,lCanFilter)    // Numero de documento
    self:oSchema:addProperty("NomeEmpresa" ,  STR0022   ,"string",STR0022  ,"NomeEmpresa",,,,lCanFilter)    // Nome Empresa
    self:oSchema:addProperty("NomeFilial"  ,  STR0023   ,"string",STR0023  ,"NomeFilial" ,,,,lCanFilter)    // Nome Filial

    self:oSchema:addParameter("SV_MULTBRANCH", STR0024, "string", .T.)  //Filiais
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class GeneralDiaryBookMod2SmartViewBusinessObject

return self:oSchema
//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    AJusta o Json conforme alteracao no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class GeneralDiaryBookMod2SmartViewBusinessObject
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class GeneralDiaryBookMod2SmartViewBusinessObject

return ::JItens
