
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.statementofmonetaryvariation.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT7", name="Demonstrativo de Variação Monetária", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CostCenterSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Demonstrativo de Variação Monetária 
para o SmartView
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class StatementOfMonetaryVariationSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewctbgercmp
    public method new() as object 
    public method getDescription() as Character

    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json

    public method getCtbGerCmpData()   as object  //Carregado na classe pai
    public method getCtbGerCmpSchema() as object // Carregado na classe pai
    
    public method handleCtbGerCmpParams() //carrega os parametros
    public method handleCtbGerCmpAppend()  as object    //Executado na classe pai
    public method handleExeCtbGerCmp()     as object    //Executado na classe pai

    public method VldExectbgercmp() as Logical
    public method execDBSkipTemp() // Pula Registro da tabela cArqTMP

    public method itemGetData()    as json 

    public method SomaTotalizador() as Numeric

    protected data nTotalCol1 as numeric
    protected data nTotalCol2 as numeric
    protected data nTotalCol3 as numeric
    protected data nTotalCol4 as numeric
    protected data cMascara as character

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
new
@return object: self
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class StatementOfMonetaryVariationSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0001) //- STR0001 "Demonstrativo de Variação Monetária"
    self:setPergunte("CTR380") //Indica o pergunte que será utilizado no relatório

    ::nTotalCol1 := 0
    ::nTotalCol2 := 0
    ::nTotalCol3 := 0
    ::nTotalCol4 := 0

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return string
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as Character class StatementOfMonetaryVariationSmartViewBusinessObject
return STR0002   //"Objeto contendo informações sobre o relatório de Demonstrativo de Variação Monetária" 

//-------------------------------------------------------------------
/*{Protheus.doc} itemGetData
Adiciona valores durante While no appendData da classe pai
 
@return json
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method itemGetData() as Json class StatementOfMonetaryVariationSmartViewBusinessObject
    Local cDescMoe1	:=	CtbMoeda("01",::nDivide)[2]       as Character
    Local cDescMoe2	:=	CtbMoeda(mv_par07,::nDivide)[2]   as Character
    Local cGrupoBkp as character

    //Descricao das moedas que serao usadas no Design
    ::jItens["DESCMOEDA01"]     := cDescMoe1
    ::jItens["DESCMOEDAMV_PAR"] := cDescMoe2
    ::jItens["NomeEmpresa"]     := self:cCompanyName
    ::jItens["NomeFilial" ]     := self:cBranchName

    //Totalizadores acumulados
    ::JItens["TOTGERALCOL1"]    :=  ::nTotalCol1
    ::JItens["TOTGERALCOL2"]    :=  ::nTotalCol2
    ::JItens["TOTGERALCOL3"]    :=  ::nTotalCol3
    ::JItens["TOTGERALCOL4"]    :=  ::nTotalCol4

    //Verifica se o próximo grupo é diferente
    cGrupoBkp := cArqTmp->GRUPO
    cArqTmp->(dbSkip())
    
    ::nTotalCol1 := iif(cGrupoBkp != cArqTmp->GRUPO, 0, ::nTotalCol1)
    ::nTotalCol2 := iif(cGrupoBkp != cArqTmp->GRUPO, 0, ::nTotalCol2)
    ::nTotalCol3 := iif(cGrupoBkp != cArqTmp->GRUPO, 0, ::nTotalCol3)
    ::nTotalCol4 := iif(cGrupoBkp != cArqTmp->GRUPO, 0, ::nTotalCol4)

    //Volta registro atual
    cArqTmp->(dbSkip(-1))

Return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} VldExectbgercmp
Valida se executará o relatório
 
@return Lógico
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method VldExectbgercmp() as Logical class StatementOfMonetaryVariationSmartViewBusinessObject
    Local lRet := .T. as Logical

    If !ct040Valid(mv_par05)// mv_par05 - Set Of Books
		lRet := .F.
    EndIf 

    If lRet
		aCtbMoeda	:= CtbMoeda(mv_par07,self:nDivide) //mv_par07 - Moeda?
		If Empty(aCtbMoeda[1])                       
	      lRet := .F.     
	   Endif
	Endif
Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Se pula o registro da  tabela  cArqTemp
 
@return Lógico
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method ExecDBSkipTemp() class StatementOfMonetaryVariationSmartViewBusinessObject
    Local lRet := .F. AS Logical
    Local nValorM1 := 0 as Numeric
    Local nValorM2 := 0 as Numeric
	Local nValorC1 := 0 as Numeric
	Local nValorC2 := 0 as Numeric                   

	If MV_PAR04 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
			lRet := .T.
		EndIf
	ElseIf MV_PAR04 == 2				// So imprime Analiticas
		If cArqTmp->TIPOCONTA == "1"
			lRet := .T.
		EndIf
	EndIf

    If !lRet
        lRet := Iif(MV_PAR19 != 1 .And. cArqTmp->NIVEL1, .T. , .F.)
    EndIf
    
    //Soma somente se não pular a linha
    If !lRet
        nValorM1 := self:SomaTotalizador("M1")
    	nValorM2 := self:SomaTotalizador("M2")
		nValorC1 := self:SomaTotalizador("C1")
		nValorC2 := self:SomaTotalizador("C2")

        ::nTotalCol1  += nValorM1
		::nTotalCol2  += nValorM2
		::nTotalCol3  += nValorC1
		::nTotalCol4  += nValorC2
    EndIf

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} getCtbGerCmpSchema
Adiciona conteúdo ao schema do objeto
 
@return object: self:oSchema
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtbGerCmpSchema() as object class StatementOfMonetaryVariationSmartViewBusinessObject

        self:oSchema:addProperty("DESCMOEDA01"		,STR0005    ,"string" ,STR0005  ,"DESCMOEDA01"  ,,,,.F. )   // "Total Geral Col 1"
        self:oSchema:addProperty("DESCMOEDAMV_PAR"	,STR0006    ,"string" ,STR0006  ,"DESCMOEDAMV_PAR",,,,.F. ) // "Total Geral Col 1"
        self:oSchema:addProperty("TOTGERALCOL1"		,STR0007    ,"number" ,STR0007  ,"TOTGERALCOL1" ,,,,.F. )   // "Total Geral Col 1"
        self:oSchema:addProperty("TOTGERALCOL2"		,STR0008    ,"number" ,STR0008  ,"TOTGERALCOL2" ,,,,.F. )   // "Total Geral Col 2"      
        self:oSchema:addProperty("TOTGERALCOL3"		,STR0009    ,"number" ,STR0009  ,"TOTGERALCOL3" ,,,,.F. )   // "Total Geral Col 3"  
        self:oSchema:addProperty("TOTGERALCOL4"		,STR0010    ,"number" ,STR0010  ,"TOTGERALCOL4" ,,,,.F. )   // "Total Geral Col 4"
        self:oSchema:addProperty("NomeEmpresa"      ,STR0011    ,"string" ,STR0011  ,"NomeEmpresa"  ,,,,.F. )   // "Nome Empresa"
        self:oSchema:addProperty("NomeFilial"       ,STR0012    ,"string" ,STR0012  ,"NomeFilial"   ,,,,.F. )   // "Nome Filial"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtbGerCmpParams
Seta o conteúdo dos parâmetros nos atributos da classe pai
 
@return Nil
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtbGerCmpParams( oFilter as object ) class StatementOfMonetaryVariationSmartViewBusinessObject
    
    //Variaveis --
    Local cAlias      := "CT7"                                 as character
    Local cIdent      := ""                                    as character
    Local cSepara	  := ""                                    as character

    Local dDataIni    := MV_PAR01                              as date
    Local dDataFim    := MV_PAR01                              as date
    Local cContaIni   := MV_PAR02                              as character
    Local cContaFim   := MV_PAR03                              as character
    Local lCt1Sint	  := iif(MV_PAR04 == 2, .F., .T.)          as logical
    Local aSetOfBook  := iif(!empty(MV_PAR05), CTBSetOf(MV_PAR05), CTBSetOf("")) as array
    Local lVariacao0  := iif(MV_PAR06 == 1, .T., .F.)          as logical
    Local cMoeda      := iif(Empty(MV_PAR07), "01", MV_PAR07)  as character 
    Local cTpSld1     := iif(Empty(MV_PAR09), "1", PadR(AllTrim(MV_PAR09), TamSX3("CT2_TPSALD")[1])) as character
    Local cTpSld2     := iif(Empty(MV_PAR09), "1", PadR(AllTrim(MV_PAR09), TamSX3("CT2_TPSALD")[1])) as character
    Local nGrupo      := MV_PAR19                              as numeric
    local cSegAte     := MV_PAR10                              as character
    Local cSegmento   := MV_PAR11                              as character
    Local cSegIni     := MV_PAR12                              as character
    Local cSegFim     := MV_PAR13                              as character
    Local cFiltSegm   := MV_PAR14                              as character
    Local lPrintZero  := iif(MV_PAR16 == 1, .T., .F.)          as logical
    Local nDivide     := iif(Empty(MV_PAR17), 1, MV_PAR17)     as numeric
    Local bVariacao   := { |x,y| Ctbr380Val(x,y) }             as codeblock

    self:cMascara := iif(Empty(aSetOfBook[2]), SuperGetMv("MV_MASCARA"), RetMasCtb(aSetOfBook[2], @cSepara))
 
    self:dDataIni      := dDataIni
    self:dDataFim      := dDataFim
    self:cAlias        := cAlias
    self:cContaIni     := cContaIni
    self:cContaFim     := cContaFim
    self:cMoeda        := cMoeda
    self:cTpSld1       := cTpSld1
    self:cTpSld2       := cTpSld2
    self:aSetOfBook    := aSetOfBook
    self:cSegmento     := cSegmento
    self:cSegIni       := cSegIni
    self:cSegFim       := cSegFim
    self:cFiltSegm     := cFiltSegm
    self:lPrintZero    := lPrintZero
    self:cSegAte       := cSegAte
    self:lVariacao0    := lVariacao0
    self:nDivide       := nDivide
    self:nGrupo        := nGrupo
    self:bVariacao     := bVariacao
    self:cIdent        := cIdent
    self:cString       := lCt1Sint
    self:cFilUSU       := ""
    self:lCtb380       := .T.
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtbGerCmpAppend
Appenda conteúdo adicional no data
 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtbGerCmpAppend(oFilter as object) as object class StatementOfMonetaryVariationSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} SomaTotalizador
Soma totais do relatório
 
@return numérico
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method SomaTotalizador(cTipo as character) as numeric class StatementOfMonetaryVariationSmartViewBusinessObject
    Local nRetValor := 0

	If mv_par04 == 1 .Or. mv_par04 = 3	// So imprime Sinteticas ou ambas - Soma Sinteticas
		If cArqTmp->TIPOCONTA == "1" .And. cArqTmp->NIVEL1
			If cTipo == "M1"
				nRetValor := cArqTmp->MOVIMENTO1
			ElseIf cTipo == "M2"
				nRetValor := cArqTmp->MOVIMENTO2
			ElseIf cTipo == "C1"
				nRetValor := cArqTmp->COLUNA_1
			ElseIf cTipo == "C2"
				nRetValor := cArqTmp->COLUNA_2
			EndIf
		EndIf
	Else         //Se soma as analiticas ou ambas		
		If cArqTmp->TIPOCONTA == "2"
			If cTipo == "M1"
				nRetValor := cArqTmp->MOVIMENTO1
			ElseIf cTipo == "M2"
				nRetValor := cArqTmp->MOVIMENTO2
			ElseIf cTipo == "C1"
				nRetValor := cArqTmp->COLUNA_1
			ElseIf cTipo == "C2"
				nRetValor := cArqTmp->COLUNA_2
			EndIf
		EndIf			
	EndIf         
Return nRetValor

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtbGerCmp
Após chamada  do methodo ctbgercmp
 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 07/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtbGerCmp(oFilter as object) as object class StatementOfMonetaryVariationSmartViewBusinessObject
//execucao da CtbGerCmp
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    Sera usado para manipular o schema do objeto

    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class StatementOfMonetaryVariationSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    Ajusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class StatementOfMonetaryVariationSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    
    @author Renan Gremes
    @since 29/11/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class StatementOfMonetaryVariationSmartViewBusinessObject
return ::jItens
