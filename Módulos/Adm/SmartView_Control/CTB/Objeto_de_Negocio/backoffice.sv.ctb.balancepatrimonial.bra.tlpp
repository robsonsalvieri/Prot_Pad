#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancepatrimonial.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2", NAME="Balanço Patrimonial", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} balancepatrimonialSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balanço Patrimonial
para o TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class balancepatrimonialSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions

	public method new()                     as object
	public method getDescription()          as character
	public method processCustomData()       as json

	//usados na internacionalizacao
    public method preschema()      			as object 
    public method preData()        			as object
    public method processData()    			as json

	public method ExecDBSkipTemp()
	public method vldExeCtgGerPlan()
	public method getCtgGerPlanData()       as object
	public method getCtGerPlanSchema()      as object

	public method handleCtgGerPlanParams()
	public method handleExeCtgGerPlan()     as object
	public method handleCtgGerPlanAppend()  as object

	private method vldCalendario() as logical

	private data dFinal  as date
	private data dFinalA as date

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class balancepatrimonialSmartViewBusinessObject

	_Super:new()
	self:setDisplayName(STR0001) // "Balanço Patrimonial"
	self:setPergunte("CTR500") //Indica o pergunte que será utilizado no relatório

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getDescription() as character class balancepatrimonialSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balanço Patrimonial"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class balancepatrimonialSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class balancepatrimonialSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class balancepatrimonialSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class balancepatrimonialSmartViewBusinessObject
	local lCanFilter := .F. as logical

	//Adição de novos campos ao schema
	self:oSchema:addProperty("SINALMOEDA" ,STR0004, "string", STR0004, "SINALMOEDA" ,,,,lCanFilter) // Conta Sem Movimento
	self:oSchema:addProperty("NomeEmpresa",STR0005, "string", STR0005, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0006, "string", STR0006, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"
	self:oSchema:addProperty("dFinal"     ,STR0013, "date"  , STR0013, "dFinal" 	,,,,lCanFilter) // "Data Final"
	self:oSchema:addProperty("dFinalA"    ,STR0014, "date"  , STR0014, "dFinalA" 	,,,,lCanFilter) // "Data Final Anterior"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0007, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method processCustomData() as json class balancepatrimonialSmartViewBusinessObject

	local aCtbMoeda as array
	local nColuna  as numeric

	nColuna := 1

	if cArqTmp->(FieldPos("COLUNA")) > 0
		if cArqTmp->COLUNA < 2
			nColuna := 1
		else
			nColuna := 2
		endif
	endif

	aCtbMoeda := CtbMoeda(::cMoedaDsc)
	::jDados["SINALMOEDA"]  := aCtbMoeda[3]
	::jDados["COLUNA"]      := nColuna
	::jDados["dFinal"]      := totvs.framework.treports.date.dateToTimeStamp(::dFinal)
	::jDados["dFinalA"]     := totvs.framework.treports.date.dateToTimeStamp(::dFinalA)
	::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial"]  := self:cBranchName
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class balancepatrimonialSmartViewBusinessObject
	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR500")
	self:handleCtgGerPlanParams(oFilter)

	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())  

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class balancepatrimonialSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class balancepatrimonialSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class balancepatrimonialSmartViewBusinessObject

	local cTpSald  	  as character
	local cMoeda   	  as character
	local aSelFil  	  as array
	local dSemestre   as date
	local lPeriodoAnt as logical

	cTpSald := "1"
	cMoeda  := "01"
	lPeriodoAnt := (MV_PAR10 == 1)

	//Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

	//Tratativa para pegar data final, conforme calendário
	If Empty(MV_PAR09)
		dbSelectArea("CTG")
		CTG->(dbSetOrder(1))

		if CTG->(dbSeek(FWxFilial("CTG") + MV_PAR01))
			While CTG->CTG_FILIAL == FWxFilial("CTG") .And. CTG->CTG_CALEND == MV_PAR01
				::dFinal := CTG->CTG_DTFIM
				CTG->(dbSkip())
			EndDo
		endif
		CTG->(dbCloseArea())
	Else
		::dFinal := MV_PAR09
	EndIf  

	//Tratativa período anterior
	If !lPeriodoAnt
		::dFinalA := ::dFinal-1
	ElseIf Substr(dtoc(::dFinal),1,5) == "29/02" // Validacao para anos bissextos
		::dFinalA := Ctod("28/02/" + Str(Year(::dFinal) - 1, 4))
	Else
		::dFinalA := Ctod(Left(Dtoc(::dFinal), 6) + Str(Year(::dFinal) - 1, 4))
	Endif

	//Tratativa semestre
	If MV_PAR04 == 1 .and. Month(::dFinal) > 6
		self:lSemestre := .T.
        dSemestre := Ctod("30/06/" + Str(Year(::dFinal), 4))
    Endif

	::aSetOfBook    := iif(!empty(MV_PAR02),CTBSetOf(MV_PAR02),CTBSetOf(""))
	::cMoeda        := iif(!empty(MV_PAR03),MV_PAR03,cMoeda)
	::lImpAntLP     := .F.
	::lMovPeriodo   := .F.
	::lVlrZerado    := iif(MV_PAR08==1,.T.,.F.)
	::dDataIni      := ::dFinalA+1
	::dDataFim      := ::dFinal
	::cMoedaDsc	    := iif(!empty(MV_PAR11),MV_PAR11,cMoeda)
	::cSaldos       := iif(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), cTpsald)
	::nDivide       := iif(!empty(MV_PAR15),MV_PAR15,1)
	::cContaIni     := Space(Len(CriaVar("CT1_CONTA")))
	::cContaFim     := Repl('Z',Len(CriaVar("CT1_CONTA")))
	::cCCIni        := Space(Len(CriaVar("CTT_CUSTO")))
	::cCCFim        := Repl('Z',Len(CriaVar("CTT_CUSTO")))
	::cItemIni      := Space(Len(CriaVar("CTD_ITEM")))
	::cItemFim      := Repl('Z',Len(CriaVar("CTD_ITEM")))
	::cClvlIni      := Space(Len(CriaVar("CTH_CLVL")))
	::cClvlFim      := Repl('Z',Len(CriaVar("CTH_CLVL")))
	::aSetOfBook[9] := ::nDivide
	::aSelFil		:= aSelFil
	::dSemestre		:= dSemestre

return

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Valida se executa a função CtgGerPlan

@param lRet, lógico, contém o filtro do TReports
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method vldExeCtgGerPlan() class balancepatrimonialSmartViewBusinessObject
    
	local lRet as logical
	local cMsgError as character
	Local aCtbMoeda as array

	lRet := .T.
	cMsgError := ""

	if Empty(mv_par02)
		lRet :=.F.
		FwLogMsg("WARN",, STR0008,,, , STR0009 , , ,) //Smart View; Livro nao informado. Selecione um livro para prosseguir.
        self:setErrorStatus(400, STR0008, STR0009) 
	Elseif !VdSetOfBook( mv_par02 , .F. )
		lRet := .F.
		FwLogMsg("WARN",, STR0008,,, , STR0010 , , ,) //Smart View; Os demonstrativos contabeis obrigatoriamente devem ter um plano gerencial associado ao livro. Verifique a configuracao de livros escolhida!
		self:setErrorStatus(400, STR0008, STR0010) 
	Endif

	//Valida o calendario selecionado
	if lRet
		if self:vldCalendario(@cMsgError)
			lRet := .T.
		else
			lRet := .F.
			FwLogMsg("WARN",, STR0008,,, , cMsgError , , ,) //Smart View; Mensagem retornada pelo metodo
        	self:setErrorStatus(400, STR0008, cMsgError) 
		endif
	endif

	if lRet
		aCtbMoeda := CtbMoeda(::cMoeda, ::aSetOfBook[9])
    	If Empty(aCtbMoeda[1])                       
			lRet := .F.
			FwLogMsg("WARN",, STR0008,,, , STR0015 , , ,) //Smart View; Mensagem retornada pelo metodo
			self:setErrorStatus(400, STR0008, STR0015) 
    	Endif
	EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 15/05/2025
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp(cArqTmp as character) class balancepatrimonialSmartViewBusinessObject
   
    local lRet := .T. as Logical

	lRet := .T.

	//Indica se a entidade gerencial sera impressa/visualizada em
	//um relatorio ou consulta apos o processamento da visao
	if cArqTmp->VISENT == "2"
		lRet := .F.
	endif    
    
return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} vldCalendario
Valida o calendario selecionado com a data de referencia

@param lRet, lógico
 
@author Renan Gremes
@since 15/05/2025
@version 1.0
*/
//-------------------------------------------------------------------
method vldCalendario(cMsgError as character) as logical class balancepatrimonialSmartViewBusinessObject
	local dDatIni as date
	local dDatFin as date
	local lRet 	  as logical

	lRet := .T.

	if !empty(MV_PAR09)
		dbselectArea("CTG")
		CTG->(dbSetOrder(1))

		if CTG->(DbSeek(FWxFilial("CTG") + MV_PAR01))
			dDatIni := CTG->CTG_DTINI

			While CTG->CTG_FILIAL == FWxFilial("CTG") .and. CTG->CTG_CALEND == MV_PAR01
				dDatFin	:= CTG->CTG_DTFIM
				CTG->(dbSkip())
			endDo

			if MV_PAR09 < dDatIni  .or.  MV_PAR09 > dDatFin
				lRet := .F.
				cMsgError := STR0011 //"Data Informada nao pertence ao calendario selecionado."
			endif
		else
			lRet := .F.
			cMsgError := STR0012 //Calendario nao cadastrado. Digite o codigo de um calendario existente.
		endif

		CTG->(dbCloseArea())
	endif

return lRet
