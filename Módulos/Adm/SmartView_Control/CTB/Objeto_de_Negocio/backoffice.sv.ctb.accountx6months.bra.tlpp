
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.accountx6months.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT7", name="Conta X 6 Meses", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} Accountx6MonthsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Conta X 6 Meses para o SmartView
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class Accountx6MonthsSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtGerComp
    public method new() as object 
    public method getDescription() as character
    public method getCtGerCompData() as object  //Carregado na classe pai
    public method getCtGerCompSchema() as object // Carregado na classe pai
    
    public method handleCtGerCompParams() //carrega os parametros
    public method handleCtGerCompAppend()  as object    //Executado na classe pai
    public method handleExeCtGerComp()     as object    //Executado na classe pai
    public method processCustomData()      as json

    public method execDBSkipTemp() // Pula Registro da tabela cArqTMP

    protected data nTotalCol1 as numeric
    protected data nTotalCol2 as numeric
    protected data nTotalCol3 as numeric
    protected data nTotalCol4 as numeric
    protected data nTotalCol5 as numeric
    protected data nTotalCol6 as numeric
    protected data cMascaraMV as character
endclass

//-------------------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
method new() as object class Accountx6MonthsSmartViewBusinessObject

    _Super:new()
    self:setDisplayName(STR0001) //"Conta X 6 Meses"
    self:setPergunte("CTR260") //Indica o pergunte que será utilizado no relatório

    ::nTotalCol1 := 0
    ::nTotalCol2 := 0
    ::nTotalCol3 := 0
    ::nTotalCol4 := 0
    ::nTotalCol5 := 0
    ::nTotalCol6 := 0
    ::cMascaraMV := SuperGetMv("MV_MASCARA")

return self

//-------------------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
method getDescription() as character class Accountx6MonthsSmartViewBusinessObject
return STR0002   //"Objeto contendo informações sobre o relatório de Conta X 6 Meses" 

//-------------------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompData
Retorna os dados do objeto de negócio
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
method getCtGerCompData(nPage as numeric, oFilter as object) as object class Accountx6MonthsSmartViewBusinessObject
    self:handleCtGerCompParams( oFilter )    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar se irá pular registro
 
@return lógico
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method ExecDBSkipTemp() class Accountx6MonthsSmartViewBusinessObject
    Local lRet       := .F.      as logical
    Local lVlrZerado := iif(MV_PAR07 == 1, .T., .F.) as logical
    Local cSegAte    := MV_PAR20 as character
    Local cSeparador := ""       as character
    Local cMascara               as character
    Local cCodmasc               as character    
    Local nDigitAte  := 0        as numeric
    Local nPos       := 0        as numeric
    local nDigitos   := 0        as numeric
    Local bCond                  as CodeBlock     

    bCond := {||Iif(cArqTmp->TIPOCONTA == "1" /*Conta Sintetica*/,;
                If(MV_PAR05 <> 1	/*Analiticas ou ambas*/,;
                .F.,;
                If(cArqTmp->NIVEL1 /*Maior Conta superiora*/,.T.,.F.) ),;
                .T. ) }

    If Empty(::aSetOfBook[2])
	    cMascara    := ::cMascaraMV
	    cCodMasc	:= ""
    Else
        cCodmasc	:= ::aSetOfBook[2]
        cMascara 	:= RetMasCtb(::aSetOfBook[2],@cSeparador)
    EndIf

    // Verifica Se existe filtragem Ate o Segmento
    If !Empty(cSegAte)                
        nDigitAte	:= CtbRelDig(cSegAte,cMascara) 	
    EndIf

    If !Empty(mv_par12)

        dbSelectArea("CTM")
        dbSetOrder(1)
        If MsSeek(xFilial()+cCodMasc)
            While !Eof() .And. CTM->CTM_FILIAL == xFilial() .And. CTM->CTM_CODIGO == cCodMasc
                nPos += Val(CTM->CTM_DIGITO)
                If CTM->CTM_SEGMEN == STRZERO(VAL(mv_par12),2)
                    nPos -= Val(CTM->CTM_DIGITO)
                    nPos ++
                    nDigitos := Val(CTM->CTM_DIGITO)      
                    Exit
                EndIf	
                dbSkip()
            EndDo	
        EndIf	
    EndIf

    If MV_PAR05 == 1 // So imprime Sinteticas
        If cArqTmp->TIPOCONTA  == '2' 
            lRet := .T.   
        else
            If lVlrZerado
                lRet := .F.   
            else
                If (cArqTmp->COLUNA1 <> 0 .OR.  cArqTmp->COLUNA2 <> 0 .OR.  cArqTmp->COLUNA3 <> 0;
                .OR.   cArqTmp->COLUNA4 <> 0 .OR.  cArqTmp->COLUNA5 <> 0 .OR.  cArqTmp->COLUNA6 <> 0)
                    lRet := .F.  
                Else
                    lRet := .T.    
                EndIf                  
            EndIf    
        EndIf
      
    ElseIf MV_PAR05 == 2 // So imprime Analiticas        
            If lVlrZerado
                lRet := .F.   
            else
                If (cArqTmp->COLUNA1 <> 0 .OR.  cArqTmp->COLUNA2 <> 0 .OR.  cArqTmp->COLUNA3 <> 0;
                .OR.   cArqTmp->COLUNA4 <> 0 .OR.  cArqTmp->COLUNA5 <> 0 .OR.  cArqTmp->COLUNA6 <> 0)
                    lRet := .F.  
                Else
                    lRet := .T.    
                EndIf                  
            EndIf    

    ElseIf !lVlrZerado  //se nao traz valor zerado entao verificar se as analiticas estao com algum coluna com valor diferente de zero
  
        If ALLTRIM(cArqTmp->TIPOCONTA) == "1" .OR. ( ALLTRIM(cArqTmp->TIPOCONTA) == "2" .And. ;
            ( cArqTmp->COLUNA1 <> 0 .OR. cArqTmp->COLUNA2 <> 0 .OR. cArqTmp->COLUNA3 <> 0 .OR. ;
            cArqTmp->COLUNA4 <> 0 .OR. cArqTmp->COLUNA5 <> 0 .OR. cArqTmp->COLUNA6 <> 0 )  ) 
            lRet := .F.   
        else
            lRet := .T.
        EndIf  

    EndIf

    // Verifica Se existe filtragem Ate o Segmento
    If ! Empty( cSegAte )
       If CVALTOCHAR(Len(Alltrim(cArqTmp->CONTA))) <= alltrim( Str( nDigitAte ))
            lRet := .T.
       EndIf 
    EndIf	 

    If !lRet //Se não pula o registro
        If Eval(bCond) 
            ::nTotalCol1 += cArqTmp->COLUNA1
            ::nTotalCol2 += cArqTmp->COLUNA2
            ::nTotalCol3 += cArqTmp->COLUNA3
            ::nTotalCol4 += cArqTmp->COLUNA4
            ::nTotalCol5 += cArqTmp->COLUNA5
            ::nTotalCol6 += cArqTmp->COLUNA6
        EndIf
    Endif

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerCompSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getCtGerCompSchema() as object class Accountx6MonthsSmartViewBusinessObject

        self:oSchema:addProperty("TOTGERALCOL1"		,STR0003    ,"number" ,STR0003  ,"TOTGERALCOL1" ,,,,.F. )  // "Total Geral Col 1"
        self:oSchema:addProperty("TOTGERALCOL2"		,STR0004    ,"number" ,STR0004  ,"TOTGERALCOL2" ,,,,.F. )  // "Total Geral Col 2"
        self:oSchema:addProperty("TOTGERALCOL3"		,STR0005    ,"number" ,STR0005  ,"TOTGERALCOL3" ,,,,.F. )  // "Total Geral Col 3"
        self:oSchema:addProperty("TOTGERALCOL4"		,STR0006    ,"number" ,STR0006  ,"TOTGERALCOL4" ,,,,.F. )  // "Total Geral Col 4"
        self:oSchema:addProperty("TOTGERALCOL5"		,STR0007    ,"number" ,STR0007  ,"TOTGERALCOL5" ,,,,.F. )  // "Total Geral Col 5"
        self:oSchema:addProperty("TOTGERALCOL6"		,STR0008    ,"number" ,STR0008  ,"TOTGERALCOL6"	,,,,.F. )  // "Total Geral Col 6"
        self:oSchema:addProperty("NomeEmpresa"      ,STR0009    ,"string" ,STR0009  ,"NomeEmpresa"  ,,,,.F. )  // "Nome Empresa"
        self:oSchema:addProperty("NomeFilial"       ,STR0010    ,"string" ,STR0010  ,"NomeFilial"   ,,,,.F. )  // "Nome Filial"

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompParams
Método para carregar os parâmetros nos atributos
 
@return Nil
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method handleCtGerCompParams( oFilter as object ) class Accountx6MonthsSmartViewBusinessObject
    Local cAlias      := "CT7"                                      as character
    Local cIdent      := ""                                         as character

    Local dDataIni    := MV_PAR01                                   as date
    Local dDataFim    := MV_PAR02                                   as date
    Local cContaIni   := MV_PAR03                                   as character
    Local cContaFim   := MV_PAR04                                   as character
   
    Local lImpSint    := iif(MV_PAR05 == 1 .OR. MV_PAR05 == 3, .T., .F.)         as logical
    Local aSetOfBook  := iif(!empty(MV_PAR06), CTBSetOf(MV_PAR06), CTBSetOf("")) as array
    Local lVlrZerado  := iif(MV_PAR07 == 1, .T., .F.)                            as logical
    Local cMoeda      := iif(empty(MV_PAR08), "01", MV_PAR08)       as character 
    Local cSaldos     := iif(empty(MV_PAR10), "1", MV_PAR10)        as character
    Local nGrupo      := MV_PAR11                                   as numeric
    Local cSegmento   := MV_PAR12                                   as character
    Local cSegIni     := MV_PAR13                                   as character
    Local cSegFim     := MV_PAR14                                   as character
    Local cFiltSegm   := MV_PAR15                                   as character
    Local lPrintZero  := iif(MV_PAR17 == 1, .T., .F.)               as logical
    Local nDivide     := iif(empty(MV_PAR19), 1, MV_PAR19)          as numeric
    Local lImpAntLP   := iif(MV_PAR21 == 1, .T., .F.)               as logical
    Local dDataLP     := MV_PAR22                                   as date
    Local cTpComp     := iif(MV_PAR23 == 1, "M", "S")	            as character

    //Variaveis fixas no relatorio    
    Local cHeader     := ""                                         as character
    Local cString     := "CT1"                                      as character
    Local aFilAux     := {}                                         as array
    Local aMeses      := ::aMeses                                   as array
    Local lNImpMov    := .F.                                        as logical
    Local lMeses      := .T.                                        as logical
    Local lExercicio  :=  .T.                                       as logical
    Local lZeradas    :=  .F.                                       as logical    

    If MV_PAR23 == 2
	    cHeader := "SLD"			/// Indica que deverá obter o saldo na 1ª coluna (Comparativo de Saldo Acumulado)
    Endif

    If Len(aMeses) < 1
        AADD(aMeses,{StrZero(Month(MV_PAR01),2),MV_PAR01,MV_PAR02,MesExtenso(Month(MV_PAR01))})	
    EndIf

    //setar os parametros da classe CtGerComp conforme os MV_PARxx
    self:cAlias      := cAlias
    self:cIdent      := cIdent
    self:dDataIni    := dDataIni
    self:dDataFim    := dDataFim
    self:cContaIni   := cContaIni
    self:cContaFim   := cContaFim
    self:lImpSint    := lImpSint
    self:aSetOfBook  := aSetOfBook
    self:cMoeda      := cMoeda
    self:lVlrZerado  := lVlrZerado
    self:cSaldos     := cSaldos
    self:cSegmento   := cSegmento
    self:cSegIni     := cSegIni
    self:cSegFim     := cSegFim
    self:cFiltSegm   := cFiltSegm
    self:lPrintZero  := lPrintZero
    self:lImpConta   := .F.
    self:nDivide     := nDivide
    self:lImpAntLP   := lImpAntLP
    self:dDataLP     := dDataLP
    self:cTpVlr      := cTpComp
    self:nGrupo      := nGrupo
    self:lMeses      := lMeses
    self:aMeses      := aMeses
    self:cHeader     := cHeader
    self:lNImpMov    := lNImpMov
    self:lImpTotS    := .F.
    self:cString     := cString

    //Utilizadas no metodo CTBPERIODOS para preencher o aMeses, verificar conforme relatorio seu valor
    self:lExercicio :=  lExercicio
    self:lZeradas   :=  lZeradas
    self:lFiliais    := .F. 
    self:aFiliais    := aFilAux

return

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtGerCompAppend
Método para appendar registros ao objeto oData
Só será acionado após a montagem completa do oData,
para appendar no fim do objeto
 
@return Nil
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method handleCtGerCompAppend(oFilter as object) as object class Accountx6MonthsSmartViewBusinessObject
    //Novo objeto
    ::jItens := JsonObject():new()

    ::JItens["TOTGERALCOL1"]   :=  ::nTotalCol1
    ::JItens["TOTGERALCOL2"]   :=  ::nTotalCol2
    ::JItens["TOTGERALCOL3"]   :=  ::nTotalCol3
    ::JItens["TOTGERALCOL4"]   :=  ::nTotalCol4
    ::JItens["TOTGERALCOL5"]   :=  ::nTotalCol5
    ::JItens["TOTGERALCOL6"]   :=  ::nTotalCol6

    self:oData:appendData(::JItens)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Método para appendar dados no json
 
@return object: string
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//-------------------------------------------------------------------
method processCustomData() as json class Accountx6MonthsSmartViewBusinessObject
    ::JItens["NomeEmpresa" ]   :=  ::cCompanyName
    ::JItens["NomeFilial" ]    :=  ::cBranchName
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtGerComp
Método para execução do CtGerComp da classe pai
 
@return Nil
 
@author Bruno Oliveira
@since 04/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method handleExeCtGerComp(oFilter as object) as object class Accountx6MonthsSmartViewBusinessObject
//execucao da CtGerComp
return self:oData

