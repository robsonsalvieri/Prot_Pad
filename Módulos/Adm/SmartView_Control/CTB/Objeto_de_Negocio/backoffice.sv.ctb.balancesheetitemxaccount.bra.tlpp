#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetitemxaccount.bra.ch"

namespace totvs.protheus.ctb.SmartViewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT2, CTD, CT1", name="Balancete Itens x Contas", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetItemxAccountSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Itens x Contas
para o TReports
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetItemxAccountSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    //usados na internacionalizacao
    public method preschema()      		    as object 
    public method preData()        		    as object
    public method processData()    		    as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp()
    public method vldExeCtgGerPlan()

    protected data lTotal    as logical
    protected data nDecimais as numeric
    protected data cPicture  as character
    protected data aTamVal   as array

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetItemxAccountSmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete ITENS X CONTAS"
    self:setPergunte("CTR100") //Indica o pergunte que será utilizado no relatório

    self:lTotal     := .F.
    self:aTamVal    := TAMSX3("CT2_VALOR")
    self:aTamVal[1] := self:aTamVal[1]+4
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Gustavo Campos
@since 26/06/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetItemxAccountSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balancete de Itens x Contas analitico"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetItemxAccountSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetItemxAccountSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetItemxAccountSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Metodo que valida se vai imprimir o relatório
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method vldExeCtgGerPlan() class BalanceSheetItemxAccountSmartViewBusinessObject
    Local lRet      := .T. as Logical
    Local aCtbMoeda := {} as Array

    If !ct040Valid(mv_par08)
        lRet := .F.    
	EndIf
    //Valida se a moeda é valida!
    aCtbMoeda  	:= CtbMoeda(mv_par10,self:nDivide)
    lRet := iif(Empty(aCtbMoeda[1]), .F., lRet)
Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) class BalanceSheetItemxAccountSmartViewBusinessObject
    Local lRet          := .T. as Logical
    Local lFiltra       := .T.
    Local cSegAte 	   	:= mv_par14 as Character
    Local cSegAteIt 	:= mv_par29 as Character
    Local cSepara1		:= "" as Character
    Local cSepara2		:= "" as Character
    Local aSetOfBook    := CTBSetOf(mv_par08) as Array
    Local cMascara1		:= IIF (Empty(aSetOfBook[2]),GetMv("MV_MASCARA"),RetMasCtb(aSetOfBook[2],@cSepara1)) as Character//Mascara da Conta
    Local cMascara2		:= IIF (Empty(aSetOfBook[7]),GetMv("MV_MASCCUS"),RetMasCtb(aSetOfBook[7],@cSepara2)) as Character//Mascara do Centro de Custo
    Local nDigitAte     := 0 as Numeric
    Local nDigiItAte    := 0 as Numeric

    If MV_PAR07 == 1 // Só imprime Conta Sintética
        lRet := cArqTmp->TIPOCONTA <> "2"
        lFiltra := .F. // Já filtrou pela conta, não precisa fazer filtro isolado depois

        If MV_PAR34 == 1 // Só imprime Item Contábil Sintético
            lRet := lRet .And. cArqTmp->TIPOITEM <> "2"
        ElseIf MV_PAR34 == 2 // Só imprime Item Contábil Analítico
            lRet := lRet .And. cArqTmp->TIPOITEM <> "1"
        EndIf

    ElseIf MV_PAR07 == 2 // Só imprime Conta Analítica
        lRet := cArqTmp->TIPOCONTA <> "1"
        lFiltra := .F. // Já filtrou pela conta, não precisa fazer filtro isolado depois

        If MV_PAR34 == 1 // Só imprime Item Contábil Sintético
            lRet := lRet .And. cArqTmp->TIPOITEM <> "2"
        ElseIf MV_PAR34 == 2 // Só imprime Item Contábil Analítico
            lRet := lRet .And. cArqTmp->TIPOITEM <> "1"
        EndIf
    EndIf

    If MV_PAR34 == 1 .And. lFiltra // Só imprime Item Contábil Sintético
        lRet := cArqTmp->TIPOITEM <> "2"
    ElseIf MV_PAR34 == 2 .And. lFiltra // Só imprime Item Contábil Analítico
        lRet := cArqTmp->TIPOITEM <> "1"
    EndIf

    //Filtragem ate o Segmento da Conta( antigo nivel do SIGACON)
    If !Empty(cSegAte)
        nDigitAte := CtbRelDig(cSegAte,cMascara1) 	
        lRet := iif(Len(Alltrim(cArqTmp->CONTA)) > nDigitAte, .T., .F.)
    EndIf

    //Filtragem ate o Segmento do Item( antigo nivel do SIGACON)
    If !Empty(cSegAteIt)
        nDigiItAte := CtbRelDig(cSegAteIt,cMascara2) 	
        lRet := iif(Len(Alltrim(cArqTmp->ITEM)) > nDigiItAte, .T., .F.)        
    EndIf

    //Valida se ira compor os campos de totais gerais
    If MV_PAR07 <> 1
        self:lTotal := IIF( cArqTmp->TIPOCONTA == "2" .And. ( ((MV_PAR34 <> 1 .Or. MV_PAR34 == 3) .And. cArqTmp->TIPOITEM == "2") .Or. ;
                                        ((MV_PAR34 == 1 .Or. MV_PAR34 == 3) .And. cArqTmp->TIPOITEM <> "2") ), .T., .F. )
    Else
        self:lTotal := IIF( (cArqTmp->TIPOCONTA == "1" .And. Empty(cArqTmp->SUPERIOR)) .And. ( ((MV_PAR34 <> 1 .Or. MV_PAR34 == 3) .And. cArqTmp->TIPOITEM == "2") .Or. ;
                                        ((MV_PAR34 == 1 .Or. MV_PAR34 == 3) .And. cArqTmp->TIPOITEM <> "2") ), .T., .F. )
    Endif

Return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetItemxAccountSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR100")
    self:handleCtgGerPlanParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters()) 
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceSheetItemxAccountSmartViewBusinessObject
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("SALDODEBACUM"   , STR0003, "number", STR0003, "SALDODEBACUM"   ,,,,lCanFilter) //"Debito Calculado"
    self:oSchema:addProperty("SALDODEBACUM_M" , STR0004, "string", STR0004, "SALDODEBACUM_M" ,,,,lCanFilter) //"Debito Calculado Mascara"
    self:oSchema:addProperty("SALDOCRDACUM"	  , STR0005, "number", STR0005, "SALDOCRDACUM"   ,,,,lCanFilter) //"Credito Calculado"
    self:oSchema:addProperty("SALDOCRDACUM_M" , STR0006, "string", STR0006, "SALDOCRDACUM_M" ,,,,lCanFilter) //"Credito Calculado Mascara"
    self:oSchema:addProperty("MOVIMENTOACUM"  , STR0007, "number", STR0007, "MOVIMENTOACUM"  ,,,,lCanFilter) //"Mov. do Periodo Calc"
    self:oSchema:addProperty("MOVIMENTOACUM_M", STR0008, "string", STR0008, "MOVIMENTOACUM_M",,,,lCanFilter) //"Mov. do Periodo Calc Mascara"
    self:oSchema:addProperty("NomeEmpresa"    , STR0009, "string", STR0009, "NomeEmpresa"    ,,,,lCanFilter) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"     , STR0010, "string", STR0010, "NomeFilial"     ,,,,lCanFilter) //"Nome Filial"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0011, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetItemxAccountSmartViewBusinessObject

    local nAcumDeb   := 0  as numeric
    local nAcumCrd   := 0  as numeric
    local nAcumMov   := 0  as numeric

    If self:lTotal
        nAcumDeb := cArqTmp->SALDODEB
        nAcumCrd := cArqTmp->SALDOCRD
        nAcumMov := (nAcumCrd - nAcumDeb)
    endIf

    ::jDados["SALDODEBACUM"   ] := nAcumDeb
    ::jDados["SALDODEBACUM_M" ] := ValorCTB(nAcumDeb,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SALDOCRDACUM"	  ] := nAcumCrd
    ::jDados["SALDOCRDACUM_M" ] := ValorCTB(nAcumCrd,,,::aTamVal[1],::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["MOVIMENTOACUM"  ] := nAcumMov
    ::jDados["MOVIMENTOACUM_M"] := ValorCTB(nAcumMov,,,::aTamVal[1],::nDecimais,.T.,::cPicture,iif(nAcumMov < 0,"1","2"),,,,,,::lPrintZero,.F.)
	::jDados["NomeEmpresa"    ] := self:cCompanyName
    ::jDados["NomeFilial"     ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetItemxAccountSmartViewBusinessObject
    local aSelFil as array
    local nDivide := 1 as numeric

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CQ5"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CQ5"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CQ5"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CQ5"]
    EndIf
    
    //setar os parametros conforme os MV_PARxx
    self:cAlias      := "CT4"//são obrigatórios
    self:cIdent      := ""//são obrigatórios   
    
    self:dDataIni    := MV_PAR01
    self:dDataFim    := MV_PAR02
    self:cContaIni   := MV_PAR03
    self:cContaFim   := MV_PAR04
    self:cItemIni    := MV_PAR05
    self:cItemFim    := MV_PAR06
    self:aSetOfBook  := iif(empty(MV_PAR08),CTBSetOf(""),CTBSetOf(MV_PAR08))
    self:lVlrZerado  := iif(MV_PAR09==1,.T.,.F.)
    self:cMoeda      := iif(Empty(MV_PAR10),"01",MV_PAR10)
    self:cSaldos     := iif(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), "1")
    self:cSegIni     := MV_PAR14
    self:cSegmento   := MV_PAR15
    self:cSegIni     := MV_PAR16
    self:cSegFim     := MV_PAR17
    self:cFiltSegm   := MV_PAR18
    self:lNImpMov    := iif(MV_PAR19==1,.F.,.T.)
    self:lPrintZero  := iif(MV_PAR23==1,.T.,.F.)
    self:nDivide     := iif(Empty(MV_PAR25), nDivide, MV_PAR25) 
    self:lImpConta   := .T.
    self:lImpAntLP   := iif(MV_PAR27==1,.T.,.F.)
    self:cHeader     := "CTD"
    self:dDataLP     := MV_PAR28
    self:cSegmentoG  := MV_PAR30
    self:cSegIniG    := MV_PAR31
    self:cSegFimG    := MV_PAR32
    self:cFiltSegmG  := MV_PAR33
    self:lRecDesp0   := iif(MV_PAR35==1,.T.,.F.)
    self:cRecDesp    := MV_PAR36
    self:dDtZeraRD   := MV_PAR37
    self:lCttSint    := iif(MV_PAR34==2,.F.,.T.)
    self:aSelFil     := aSelFil
    self:lCTBR100SV  := .T.

    ::nDecimais := DecimalCTB(::aSetOfBook,::cMoeda)
    ::cPicture  := iif(!Empty(::aSetOfBook[4]),::aSetOfBook[4],"@E 999,999,999,999,999.99")
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetItemxAccountSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/06/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetItemxAccountSmartViewBusinessObject
//execução do Append
return self:oData

