#INCLUDE "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.ClassifiedbyLot.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT2,CTC", name="Lançamentos Classificados por Lote", country="ALL", customTables="CT2,CTC")

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.ClassifiedbyLot.bra
Classe para criação do Objeto de Negócio de contendo informações Lancamento Classificado por Sub Lote
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class ClassifiedbyLotSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()            as object
	public method getDescription() as character
	public method getAreas()       as array
	public method getData()        as object
	public method getSchema()      as object

	protected data jDados 		   as json
	protected data oQuery1 		   as object
	protected data aAllFields      as array
	protected data oIntegratedProvider  as object
EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

Method new() as object Class ClassifiedbyLotSmartViewBusinessObject
	_Super:new()
	self:setDisplayName(STR0001) //"Lançamentos Classificados por SubLote"
	self:setPergunte("CTR080") //Indica o pergunte que será utilizado no relatório
Return self
//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-----------------------------------------------------------------
Method getDescription() as character class ClassifiedbyLotSmartViewBusinessObject
return STR0002 //"Objeto contendo informações do Lançamentos Classificados por SubLote"
//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getAreas() as array class ClassifiedbyLotSmartViewBusinessObject
Return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class ClassifiedbyLotSmartViewBusinessObject

	local cQuery 		as character
	local cAlias 		as character
	local cCompanyName 	as character
	local cBranchName  	as character
	local cMoeda		as character
	local nParamOrder 	 := 1 as numeric
	local nX, nInf, nDig := 0 as numeric
	local aRetCT6 as array

	cAlias   := GetNextAlias()
	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR080")

	::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	cMoeda := iif(Empty(MV_PAR09), "01", MV_PAR09)

	//Select
	cQuery := "SELECT "
	cQuery += self:getSQLFields()
	cQuery += "FROM " + RetSQLName("CT2") + " CT2 "
	cQuery += "INNER JOIN " + RetSQLName("CTC") + "  CTC ON CTC_FILIAL = CT2_FILIAL"
	cQuery += "		AND CTC.CTC_DATA = CT2_DATA"
	cQuery += "		AND CTC.CTC_LOTE = CT2_LOTE"
	cQuery += "		AND CTC.CTC_SBLOTE = CT2_SBLOTE"
	cQuery += "		AND CTC.CTC_DOC = CT2_DOC"
	cQuery += "		AND CTC.CTC_MOEDA = CT2_MOEDLC "
	cQuery += "		AND CTC.CTC_TPSALD = CT2_TPSALD "
	cQuery += "		AND CTC.D_E_L_E_T_ = ? "

	cQuery += " WHERE "
	cQuery += " CT2_FILIAL = ? "
	cQuery += " AND CT2_DATA >= ? "
	cQuery += " AND CT2_DATA <= ? "
	cQuery += " AND CT2_LOTE >= ? "
	cQuery += " AND CT2_LOTE <= ? "
	cQuery += " AND CT2_SBLOTE >= ? "
	cQuery += " AND CT2_SBLOTE <= ? "
	cQuery += " AND CT2_DOC >= ? "
	cQuery += " AND CT2_DOC <= ? "
	cQuery += " AND CT2_TPSALD = ? "

	If MV_PAR09 == "01"
		cQuery +=  "AND CT2_MOEDLC = ? "
	Else
		cQuery +=  "and ((CT2_DC <> ? AND CT2_MOEDLC = ? ) or "
		cQuery +=  "(CT2_DC = ? AND CT2_MOEDLC = ?))"
	EndIf

	cQuery += " AND CT2.D_E_L_E_T_ = ? "

	//Os filtros serão setados na interface do novo TReports
	if oFilter:hasFilter()
		cQuery += " AND " + oFilter:getSQLExpression()
	endif

	cQuery := ChangeQuery(cQuery)
	self:oQuery1:= FWPreparedStatement():New(cQuery)

	self:oQuery1:SetString(nParamOrder++,	" ")
	self:oQuery1:SetString(nParamOrder++,	FwXFilial("CT2"))
	self:oQuery1:SetDate(nParamOrder++,		MV_PAR01)
	self:oQuery1:SetDate(nParamOrder++,		MV_PAR02)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR03)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR04)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR05)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR06)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR07)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR08)
	self:oQuery1:SetString(nParamOrder++,	MV_PAR11)

	If MV_PAR09 == "01"
		self:oQuery1:SetString(nParamOrder++, "01")		
	else
		self:oQuery1:SetString(nParamOrder++, "4")
		self:oQuery1:SetString(nParamOrder++, MV_PAR09)
		self:oQuery1:SetString(nParamOrder++, "4")
		self:oQuery1:SetString(nParamOrder++, "01")
	Endif

	self:oQuery1:SetString(nParamOrder++, " ")	

	cQuery := self:oQuery1:GetFixQuery()

	cAlias := MPSysOpenQuery(ChangeQuery(cQuery))
	
	DbSelectArea(cAlias)
	(cAlias)->(DbGoTop())

	While (cAlias)->(!Eof())

		::jDados := JsonObject():new()

		For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jDados[self:aAllFields[nX]:getName()] := iif(!empty((cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jDados[self:aAllFields[nX]:getName()] := AllTrim((cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jDados[self:aAllFields[nX]:getName()] := (cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
        next nX

		If MV_PAR10 == 2
			::jDados["CT2_DEBITO"] := CodRed(CT2->CT2_DEBITO, "D")
			::jDados["CT2_CREDIT"] := CodRed(CT2->CT2_CREDIT, "C")
		EndIf
		
		If(cAlias)->CT2_DC == "1" .Or. (cAlias)->CT2_DC == "3"
			::jDados["CT2_VALDEB"]  	:= (cAlias)->CT2_VALOR
		Endif

		If(cAlias)->CT2_DC == "2" .Or. (cAlias)->CT2_DC == "3"
			::jDados["CT2_VALCRED"]  	:= (cAlias)->CT2_VALOR
		Endif

		//Trata INFORMADO e DIGITADO se totaliza por lote (MV_PAR13)
		nInf := 0
		nDig := 0
		If MV_PAR14 == 1
			aRetCT6 := CtbSaldoLote((cAlias)->CT2_LOTE, (cAlias)->CT2_SBLOTE, STOD((cAlias)->CT2_DATA), cMoeda)
			nInf := aRetCT6[3]
			nDig := aRetCT6[4]
		EndIf

		::JDados["INFORMADO"] := nInf
		::JDados["DIGITADO" ] := nDig
		::JDados["DIFERENCA"] := nDig-nInf

		::jDados["NomeEmpresa"] := cCompanyName
        ::jDados["NomeFilial"]  := cBranchName

		self:processData()
		self:oData:appendData(::jDados)

		(cAlias)->(dbSkip())

	EndDo

	(cAlias)->(dbCloseArea())

	FreeObj(::jDados)
	FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class ClassifiedbyLotSmartViewBusinessObject

	local aFieldsCT2 as array
	local aFieldsCTC as array

    aFieldsCT2 := { "CT2_FILIAL", "CT2_DATA", 	"CT2_DOC",    "CT2_SBLOTE", "CT2_LOTE",  "CT2_LINHA", "CT2_DC",;
					"CT2_DEBITO", "CT2_CREDIT", "CT2_VALOR",  "CT2_HP",     "CT2_HIST",  "CT2_CCD",   "CT2_CCC",;
					"CT2_ITEMD",  "CT2_ITEMC",  "CT2_CLVLDB", "CT2_CLVLCR" }
	
	aFieldsCTC := { "CTC_DIG", "CTC_INF" }

    //Adiciona campos ao schema
    self:oSchema:aliasToSchema("CT2", aFieldsCT2)
	self:oSchema:aliasToSchema("CTC", aFieldsCTC)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

	self:oSchema:addProperty("CT2_VALDEB" , STR0006, "number", STR0006, "CT2_VALDEB" ,,,,.F.)   //"Valor Débito"
    self:oSchema:addProperty("CT2_VALCRED", STR0007, "number", STR0007, "CT2_VALCRED",,,,.F.)   //"Valor Crédito"
	self:oSchema:addProperty("INFORMADO"  , STR0008, "number", STR0008, "INFORMADO"  ,,,,.F.)   //"Valor Informado por Lote"
    self:oSchema:addProperty("DIGITADO"   , STR0009, "number", STR0009, "DIGITADO"   ,,,,.F.)   //"Valor Digitado por Lote"
	self:oSchema:addProperty("DIFERENCA"  , STR0010, "number", STR0010, "DIFERENCA"  ,,,,.F.)   //"Valor Diferenca por Lote"

	self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,.F.)   //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial" ,,,,.F.)   //"Nome Filial"

return self:oSchema
