#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheet6columns.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CQ1, CT7, CTO, SLD, CTZ, CTN", name="Balancete 6 Colunas", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheet6ColumnsSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete 6 colunas
para o TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceSheet6ColumnsSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    //usados na internacionalização
    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp()          as logical

    private method R170Soma() as numeric
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceSheet6ColumnsSmartViewBusinessObject
    _Super:new()    
    self:setDisplayName(STR0001) // "Balancete 6 Colunas"
    self:setPergunte("CTR170")

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceSheet6ColumnsSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre os Balancete 6 Colunas"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getCtGerPlanSchema() as object class BalanceSheet6ColumnsSmartViewBusinessObject
    local lCanFilter := .F. as logical

    self:oSchema:addProperty("NomeEmpresa",STR0003, "string", STR0003, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" ,STR0004, "string", STR0004, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0005, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method processCustomData() as json class BalanceSheet6ColumnsSmartViewBusinessObject
    local cPicture  as character
    local TAM_VALOR := 17 as numeric
    local nTotDeb   as numeric
    local nTotCrd   as numeric
    local nDecimais as numeric

    nTotDeb := self:R170Soma("D")
	nTotCrd := self:R170Soma("C")

    ::jDados["SALDODEB"	  ] := nTotDeb
    ::jDados["SALDODEB_M" ] := ValorCTB(nTotDeb,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
    ::jDados["SALDOCRD"	  ] := nTotCrd
    ::jDados["SALDOCRD_M" ] := ValorCTB(nTotCrd,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
	::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName
return ::jDados


//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheet6ColumnsSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR170")
    self:handleCtgGerPlanParams( oFilter )
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author Ednilson Queiroz
    @since 08/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceSheet6ColumnsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author Ednilson Queiroz
    @since 08/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceSheet6ColumnsSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author Ednilson Queiroz
    @since 08/02/2024
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheet6ColumnsSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheet6ColumnsSmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheet6ColumnsSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceSheet6ColumnsSmartViewBusinessObject

    local cContaIni     as character
    local cContaFim     as character
    local cMoeda        as character
    local cTpsald       as character
    local nDivide       as numeric
    local lImpAntLP     as logical
    local lImpSint      as logical
    local lPrintZero    as logical
    local lSldZerado    as logical
    local aSetOfBook    as array
    local aSelFil       as array
    local dDataIni      as date
    local dDataFim      as date
    local dDataLP       as date

    //Carrega os valores
    cMoeda  := "01"
    cTpsald := "1"

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    //Atribui parâmetros nas variáveis
    dDataIni     := MV_PAR01 // Data Inicial
    dDataFim     := MV_PAR02 // Data Final
    cContaIni    := MV_PAR03 // Conta Inicial
    cContaFim    := MV_PAR04 // Conta Final
    lImpSint     := IIf(MV_PAR05 == 1 .Or. MV_PAR05 == 3, .T., .F.) // Imprime Contas (Sintetica, Analitica, Ambas)
    aSetOfBook   := IIf(!Empty(MV_PAR06), CTBSetOf(MV_PAR06),   CTBSetOf("")) // Codigo de Configuração dos Livros
    lSldZerado   := IIf(MV_PAR07 == 1, .T., .F.) // Saldos Zerados
    cMoeda       := IIf(!Empty(MV_PAR08), MV_PAR08, cMoeda)  // Moeda
    cTpsald      := IIf(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo
    lPrintZero   := IIf(MV_PAR18 == 1, .T., .F.) // Imprime Valor 0.00  ?

    // Tratamento do parâmetro Divide por ?
    if MV_PAR20 == 1 		// Não se aplica
	    nDivide  := 1
    elseif MV_PAR20 == 2	// Divide por cem
		nDivide  := 100
	elseif MV_PAR20 == 3	// Divide por mil
		nDivide  := 1000
	elseif MV_PAR20 == 4	// Divide por milhao
		nDivide  := 1000000
	endif

    lImpAntLP    := IIf(MV_PAR22 == 1, .T., .F.) // Posição Ant. L/P?
    dDataLP      := MV_PAR23 // Data Lucros/Perdas?  

    //Atribui variáveis nos atributos
    ::dDataIni   := dDataIni
    ::dDataFim   := dDataFim
    ::cAlias     := "CT7" // Alias do Arquivo
    ::cIdent     := ""    // Identificador do arquivo a ser processado
    ::cContaIni  := cContaIni
    ::cContaFim  := cContaFim
    ::lImpSint   := lImpSint
    ::aSetOfBook := aSetOfBook
    ::lVlrZerado := lSldZerado
    ::cMoeda     := cMoeda
    ::cSaldos    := cTpsald
    ::nDivide    := nDivide
    ::lImpAntLP  := lImpAntLP
    ::dDataLP    := dDataLP
    ::aSelFil    := aSelFil
    ::lNImpMov   := .T.
    ::lImpConta  := .F.
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 16/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp As character) as logical class BalanceSheet6ColumnsSmartViewBusinessObject
   
    local lRet := .T.
    
    If MV_PAR05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
			lRet := .F.
		EndIf
	EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} R170Soma
Soma o valor do lançamento
 
@return numérico
 
@author Renan Gremes
@since 16/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method R170Soma(cTipo as character) as numeric class BalanceSheet6ColumnsSmartViewBusinessObject
    local nRetValor := 0 as numeric

	If MV_PAR05 == 1	// So imprime Sinteticas - Soma Sinteticas
		If cArqTmp->TIPOCONTA == "1" .And. cArqTmp->NIVEL1
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	Else	// Soma Analiticas
		If cArqTmp->TIPOCONTA == "2"
			If cTipo == "D"
				nRetValor := cArqTmp->SALDODEB
			ElseIf cTipo == "C"
				nRetValor := cArqTmp->SALDOCRD
			EndIf
		EndIf
	EndIf

return nRetValor
