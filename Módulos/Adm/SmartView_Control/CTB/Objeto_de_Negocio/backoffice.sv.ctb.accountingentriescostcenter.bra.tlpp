#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.accountingentriescostcenter.bra.ch"

Static cArqtmp as character
Static __oTempTable as object

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT1, CT2", name="Lançamento por Centro de Custo", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AccountingEntriesCostCenterSmartViewBusinessObject
classe para criação do Objeto de Negócio de Lançamento por Centro de Custo 
para o TReports
 
@author wilton.santos
@since 14/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class AccountingEntriesCostCenterSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            as object
    public method getDescription() as character
    public method getAreas()       as array
    public method getData()        as object
    public method getSchema()      as object
    
    //usados na internacionalização
    public method preschema()      as object
    public method preData()        as object
    public method processData()    as json
    
    protected method ctbgerlanc()  //cria temporaria
    protected method ctbgeralan()  //cria temporaria
    protected method ctbgrvlanc()  //cria temporaria
    
    //parametros que serão alimentados por MV_PAR
    protected data dDataIni	      as date
    protected data dDataFim	      as date
    protected data cCustoIni      as character
    protected data cCustoFim      as character
    protected data cContaIni      as character
    protected data cContaFim      as character
    protected data cMoeda	      as character
    protected data cSaldo	      as character
	protected data cDescMoeda	  as character
	protected data cAlias         as character
    protected data jItens         as json
	protected data oIntegratedProvider as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author wilton.santos
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class AccountingEntriesCostCenterSmartViewBusinessObject
	_Super:new()
	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0001) //"Lançamento por Centro de Custo"
	self:setPergunte("CTR095")   //Indica o pergunte que será utilizado no relatório

	//Seta atributos com valores default
	::cSaldo      := "1"
	::cMoeda      := "01"
	::cDescMoeda  := ""
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author wilton.santos
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class AccountingEntriesCostCenterSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Lançamento por Centro de Custo"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author wilton.santos
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getAreas() as array class AccountingEntriesCostCenterSmartViewBusinessObject
Return {STR0003} //"Contabilidade" 

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author wilton.santos
@since 14/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class AccountingEntriesCostCenterSmartViewBusinessObject
	Local aSetOfBook            as array
	Local nDecimais             as numeric
	Local cMascCta              as character
	Local cMascCus              as character
	Local cMoeda       := "01"  as character
	Local cTpSaldo     := "1"   as character
	Local TAM_VALOR    := 19    as numeric
	Local cFiltCTT  			as character
	Local cFiltCT1  			as character
	Local cSepara  			    as character
	Local cDesConta			    as character
	Local cContaRes    := ""	as character
	Local cCustoRes    := ""	as character
	Local cDesCusto			    as character
	Local cDesPartida		    as character
	Local cPartRes			    as character
	Local lCusNormal			as logical
	Local lCtaNormal			as logical
	Local lTotalConta			as logical
	Local cCustoAnt 			as character
	Local cContaAnt 			as character
	Local cComplemento := ""	as character
	Local cCompanyName := ""	as character
	Local cBranchName  := ""	as character
	Local dDataAnt  			as date
	Local nSaldoAntCC  := 0		as numeric
	Local nSldAntCCCta := 0		as numeric
	Local nSaldoAntCta := 0		as numeric
	Local nValLanc     := 0		as numeric
	Local nValLancAcum := 0		as numeric
	Local nValLancTotal:= 0		as numeric
	Local aSaldoCC     := {}	as array
	Local aSldAntCCCta := {}	as array

	CTBsetValueMVPAR(oFilter:getParameters(), "CTR095")

	::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	::dDataIni 		:= MV_PAR01 							 //Data Inicial ?                
	::dDataFim 		:= MV_PAR02 							 //Data Final ?                  
	::cCustoIni    	:= MV_PAR03 							 //Centro de Custo Inicial ?     
	::cCustoFim   	:= MV_PAR04 							 //Centro de Custo Final ?       
	::cContaIni  	:= MV_PAR05 							 //Conta Inicial ?               
	::cContaFim  	:= MV_PAR06 							 //Conta Final ?                 
	::cMoeda	    := Iif(Empty(MV_PAR07),cMoeda,AllTrim(MV_PAR07))  //Moeda ?                       
	::cSaldo	    := Iif(Empty(MV_PAR08),cTpSaldo,AllTrim(MV_PAR08))//Tipo de Saldo ? 
	aSetOfBook      := Iif(!Empty(MV_PAR09),CTBSetOf(MV_PAR09),CTBSetOf("")) 										 //Cod. Config. Livros ?         
	lCusNormal      := Iif(MV_PAR10==1,.T.,.F.)      		 //Imprime Cod. C.Custo ?        
	lCtaNormal      := Iif(MV_PAR11==1,.T.,.F.)      		 //Imprime Cod. Conta ?
	lTotalConta  	:= Iif(MV_PAR12==1,.T.,.F.)      		 //Imprime Tot. Conta ?           

	//Monta o arquivo temporário
	self:ctbgerlanc(oFilter,cFiltCTT,cFiltCT1)

	nDecimais := DecimalCTB(aSetOfBook,self:cMoeda)	

	//coletando as 2 mascaras
	cMascCta   := IIF(Empty(aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(aSetOfBook[2],@cSepara)) //Mascara da Conta
	cMascCus   := IIF(Empty(aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(aSetOfBook[6],@cSepara)) //Mascara do Centro de Custo

	dbSelectArea("cArqTmp")
	cArqTmp->(dbgotop())

	While !cArqTmp->(Eof())
		
		cDesConta := ""
		cDesCusto := ""

		//Coletando descrição e codigo resumido
		dbSelectArea("CT1")
		CT1->(dbSetOrder(1))
		if CT1->(MsSeek(FwXFilial("CT1")+cArqTmp->CONTA,.F.))
			cDesConta := CT1->CT1_DESC01
			cContaRes := CT1->CT1_RES
		endif
		
		if CT1->(MsSeek(FwXFilial("CT1")+cArqTmp->XPARTIDA,.F.))
			cDesPartida := CT1->CT1_DESC01
			cPartRes	:= CT1->CT1_RES
		endif
		
		dbSelectArea("CTT")
		CTT->(dbSetOrder(1))

		if CTT->(MsSeek(FwXFilial("CTT")+cArqTmp->CCUSTO,.F.))
			cDesCusto := CTT->CTT_DESC01
			cCustoRes := CTT->CTT_RES
		endif
		
		//Preencher com as mascaras
		cConta    := EntidadeCTB(cArqTmp->CONTA    ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
		cCusto    := EntidadeCTB(cArqTmp->CCUSTO   ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
		cXpartida := EntidadeCTB(cArqTmp->XPARTIDA ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CT1",,,,.F.)
		
		If cCustoAnt <> cArqTmp->CCUSTO
			//Calcula o saldo anterior do centro de custo atual
			aSaldoCC  := SaldTotCT3(cArqTmp->CCUSTO,cArqTmp->CCUSTO,::cContaIni,::cContaFim,::dDataIni,::cMoeda,::cSaldo)
			nSaldoAntCC  := aSaldoCC[6] //Saldo Anterior
			
			//Salva saldo atual do centro de custo
			nSaldoAtuCC	 := aSaldoCC[6] //Saldo Atual CC		
		EndIf

		//Saldo Anterior CC x Conta
		If lTotalConta .And. (cArqTmp->CCUSTO <> cCustoAnt .Or. cArqTmp->CONTA <> cContaAnt)
			aSldAntCCCta	:= SaldoCT3(cArqTmp->CONTA,cArqTmp->CCUSTO,::dDataIni,::cMoeda,::cSaldo,,.F.)				
			nSldAntCCCta	:= aSldAntCCCta[6]
			
			//Salva o saldo anterior da conta
			nSaldoAntCta 	:= aSldAntCCCta[6]
      	EndIf
		
		//Calculos saldo anterior
		If cArqTmp->TIPO == "1"
			nSaldoAntCC	 -= cArqTmp->VALORLANC
			nSldAntCCCta -= cArqTmp->VALORLANC 
		Else
			nSaldoAntCC	 += cArqTmp->VALORLANC
			nSldAntCCCta += cArqTmp->VALORLANC 	            
		EndIf

		// Procura pelo complemento de historico
		dbSelectArea("CT2")
		CT2->(dbSetOrder(10))

		If CT2->(MsSeek(xFilial()+DTOS(cArqTmp->DATAL)+cArqTmp->LOTE+cArqTmp->SUBLOTE+cArqTmp->DOC+cArqTmp->SEQLAN))		
			CT2->(dbSkip())
			If CT2->CT2_DC == "4"
				While CT2->(!Eof()) .And.;
						CT2->CT2_FILIAL == FwXFilial("CT2") .And.;
						CT2->CT2_LOTE   == cArqTmp->LOTE .And.;
						CT2->CT2_DOC    == cArqTmp->DOC   .And.;
						CT2->CT2_SEQLAN == cArqTmp->SEQLAN .And.;
						CT2->CT2_DC     == "4" .And.;
						DTOS(CT2->CT2_DATA) == DTOS(cArqTmp->DATAL)

					cComplemento += alltrim(CT2->CT2_HIST)
					CT2->(dbSkip())
				EndDo	
			EndIf	
		EndIf	

		//Valor lançamento
		nValLanc := iif(cArqTmp->TIPO == "1", -cArqTmp->VALORLANC, cArqTmp->VALORLANC)
		nValLancAcum  += nValLanc //Acumulado por CC
		nValLancTotal += nValLanc //Totalizador
		
		//Nova objeto
		::jItens := JsonObject():new()

		::jItens["CCUSTO"	      ] := iif(!lCusNormal .and. !empty(cCustoRes),cCustoRes,cArqTmp->CCUSTO)
		::jItens["CCUSTO_M"	      ] := iif(!lCusNormal .and. !empty(cCustoRes),cCustoRes,cCusto)	
		::jItens["CONTA"	      ] := iif(!lCtaNormal .and. !empty(cContaRes),cContaRes,cArqTmp->CONTA)
		::jItens["CONTA_M"	      ] := iif(!lCtaNormal .and. !empty(cContaRes),cContaRes,cConta)	
		::jItens["XPARTIDA"       ] := iif(!lCtaNormal .and. !empty(cPartRes) ,cPartRes ,cArqTmp->XPARTIDA)
		::jItens["XPARTIDA_M"     ] := iif(!lCtaNormal .and. !empty(cPartRes) ,cPartRes ,cXpartida)
		::jItens["TIPO"           ] := cArqTmp->TIPO
		::jItens["VALORLANC"      ] := nValLanc
		::jItens["VALORLANC_M"    ] := ValorCTB(nValLanc,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["SALDOSCR"	      ] := nSaldoAntCC
		::jItens["SALDOSCR_M"     ] := ValorCTB(nSaldoAntCC ,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["SALDOSCRCTA"    ] := nSldAntCCCta
		::jItens["SALDOSCRCTA_M"  ] := ValorCTB(nSldAntCCCta ,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["HISTORICO"      ] := alltrim(cArqTmp->HISTORICO) + alltrim(cComplemento)
		::jItens["DATAL"	      ] := totvs.framework.treports.date.dateToTimeStamp(cArqTmp->DATAL)
		::jItens["LOTE" 	      ] := cArqTmp->LOTE
		::jItens["SUBLOTE" 	      ] := cArqTmp->SUBLOTE
		::jItens["DOC" 		      ] := cArqTmp->DOC
		::jItens["LINHA"	      ] := cArqTmp->LINHA
		::jItens["SEQLAN"	      ] := cArqTmp->SEQLAN
		::jItens["SEQHIST"	      ] := cArqTmp->SEQHIST
		::jItens["EMPORI"	      ] := cArqTmp->EMPORI
		::jItens["FILORI"	      ] := cArqTmp->FILORI
		::jItens["DESCCTA"	      ] := alltrim(cDesConta)
		::jItens["DESCCTT"	      ] := alltrim(cDesCusto)
		::jItens["DESCXPR"	      ] := alltrim(cDesPartida)
		::jItens["SALDOANTCTA"	  ] := nSaldoAntCta
		::jItens["SALDOANTCTA_M"  ] := ValorCTB(nSaldoAntCta,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["NomeEmpresa"    ] := cCompanyName
		::jItens["NomeFilial"     ] := cBranchName
		::jItens["DESCMOEDA"      ] := ::cDescMoeda
		::jItens["SALDOATUCC"	  ] := nSaldoAtuCC
		::jItens["SALDOATUCC_M"	  ] := ValorCTB(nSaldoAtuCC,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["VALORLANCACUM"  ] := nValLancAcum
		::jItens["VALORLANCACUM_M"] := ValorCTB(nValLancAcum,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		::jItens["VALORLANCTOT"   ] := nValLancTotal
		::jItens["VALORLANCTOT_M" ] := ValorCTB(nValLancTotal,,,TAM_VALOR-3,nDecimais,.T.,,/*cTipo*/,,,,,,,.F.)
		
		self:processData()
		self:oData:appendData(::jItens)

		cCustoAnt    := cArqTmp->CCUSTO
		cContaAnt    := cArqTmp->CONTA
		dDataAnt     := cArqTmp->DATAL
		cComplemento := ""

		cArqTmp->(DbSkip())

		If (cArqTmp->CCUSTO <> cCustoAnt .Or. cArqTmp->CONTA <> cContaAnt) // Totaliza tb por Conta
			If lTotalConta // Imprimir total da conta				
            	nSldAntCCCta := 0
			EndIf
			nValLancAcum := 0
		EndIf
			
	EndDo

	CT1->(dbCloseArea())
	CTT->(dbCloseArea())
	CT2->(dbCloseArea())

	FreeObj(::jItens)
	FreeObj(::oIntegratedProvider)

	dbSelectArea("cArqTmp")
	Set Filter To
	cArqTmp->(dbCloseArea())

	If Select(cArqTmp) == 0
		FErase(cArqTmp+GetDBExtension())
		FErase("cArqInd"+OrdBagExt())
	EndIf

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Modela os campos
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method getSchema() as object class AccountingEntriesCostCenterSmartViewBusinessObject

	self:oSchema:addProperty( "CCUSTO"		   , STR0004 , "string" , STR0004 , "CCUSTO"		 ,,,,.F.) // "Centro de Custo"
	self:oSchema:addProperty( "CCUSTO_M"	   , STR0005 , "string" , STR0005 , "CCUSTO_M"	 	 ,,,,.F.) // "Centro de Custo Mascara"
	self:oSchema:addProperty( "CONTA"		   , STR0006 , "string" , STR0006 , "CONTA"		 	 ,,,,.F.) // "Codigo da Conta"
	self:oSchema:addProperty( "CONTA_M"		   , STR0007 , "string" , STR0007 , "CONTA_M"		 ,,,,.F.) // "Codigo da Conta Mascara"
	self:oSchema:addProperty( "XPARTIDA"   	   , STR0008 , "string" , STR0008 , "XPARTIDA"   	 ,,,,.F.) // "Contra Partida"
	self:oSchema:addProperty( "XPARTIDA_M" 	   , STR0009 , "string" , STR0009 , "XPARTIDA_M"   	 ,,,,.F.) // "Contra Partida Mascara"
	self:oSchema:addProperty( "TIPO"       	   , STR0010 , "string" , STR0010 , "TIPO"       	 ,,,,.F.) // "Tipo do Registro"
	self:oSchema:addProperty( "VALORLANC"	   , STR0011 , "number" , STR0011 , "VALORLANC"	 	 ,,,,.F.) // "Vlr.do Lanc."
	self:oSchema:addProperty( "VALORLANC_M"	   , STR0012 , "string" , STR0012 , "VALORLANC_M"	 ,,,,.F.) // "Vlr.do Lanc. Mascara"
	self:oSchema:addProperty( "SALDOSCR"	   , STR0013 , "number" , STR0013 , "SALDOSCR"	 	 ,,,,.F.) // "Saldo Anterior CC"
	self:oSchema:addProperty( "SALDOSCR_M"	   , STR0014 , "string" , STR0014 , "SALDOSCR_M"	 ,,,,.F.) // "Saldo Anterior CC Mascara"
	self:oSchema:addProperty( "HISTORICO"	   , STR0015 , "string" , STR0015 , "HISTORICO"	 	 ,,,,.F.) // "Historico"
	self:oSchema:addProperty( "DATAL"		   , STR0016 , "date"   , STR0016 , "DATAL"		 	 ,,,,.F.) // "Data do Lancamento"
	self:oSchema:addProperty( "LOTE" 		   , STR0017 , "string" , STR0017 , "LOTE" 		 	 ,,,,.F.) // "Lote"
	self:oSchema:addProperty( "SUBLOTE" 	   , STR0018 , "string" , STR0018 , "SUBLOTE" 	 	 ,,,,.F.) // "Sub-Lote"
	self:oSchema:addProperty( "DOC" 		   , STR0019 , "string" , STR0019 , "DOC" 		 	 ,,,,.F.) // "Documento"
	self:oSchema:addProperty( "LINHA"		   , STR0020 , "string" , STR0020 , "LINHA"		 	 ,,,,.F.) // "Linha"
	self:oSchema:addProperty( "SEQLAN"		   , STR0021 , "string" , STR0021 , "SEQLAN"		 ,,,,.F.) // "Seq. Lanc."
	self:oSchema:addProperty( "SEQHIST"		   , STR0022 , "string" , STR0022 , "SEQHIST"		 ,,,,.F.) // "Seq do Historico"
	self:oSchema:addProperty( "EMPORI"		   , STR0023 , "string" , STR0023 , "EMPORI"		 ,,,,.F.) // "Empresa Original"
	self:oSchema:addProperty( "FILORI"		   , STR0024 , "string" , STR0024 , "FILORI"		 ,,,,.F.) // "Filial"
	self:oSchema:addProperty( "DESCCTA"		   , STR0025 , "string" , STR0025 , "DESCCTA"		 ,,,,.F.) // "Desc. Conta"
	self:oSchema:addProperty( "DESCCTT"		   , STR0026 , "string" , STR0026 , "DESCCTT"		 ,,,,.F.) // "Desc. C. Custo"
	self:oSchema:addProperty( "DESCXPR"		   , STR0027 , "string" , STR0027 , "DESCXPR"		 ,,,,.F.) // "Desc. C. Partida"
	self:oSchema:addProperty( "SALDOANTCTA"	   , STR0028 , "number" , STR0028 , "SALDOANTCTA"	 ,,,,.F.) // "Saldo Anterior Conta"
	self:oSchema:addProperty( "NomeEmpresa"	   , STR0029 , "string" , STR0029 , "NomeEmpresa"	 ,,,,.F.) // "Nome Empresa"
	self:oSchema:addProperty( "NomeFilial" 	   , STR0030 , "string" , STR0030 , "NomeFilial"     ,,,,.F.) // "Nome Filial"
	self:oSchema:addProperty( "DESCMOEDA" 	   , STR0031 , "string" , STR0031 , "DESCMOEDA"      ,,,,.F.) // "Descrição Moeda"
	self:oSchema:addProperty( "SALDOSCRCTA"	   , STR0032 , "number" , STR0032 , "SALDOSCRCTA"	 ,,,,.F.) // "Saldo Anterior CC x Conta"
	self:oSchema:addProperty( "SALDOSCRCTA_M"  , STR0033 , "string" , STR0033 , "SALDOSCRCTA_M"  ,,,,.F.) // "Saldo Anterior CC x Conta Mascara"
	self:oSchema:addProperty( "SALDOATUCC"	   , STR0034 , "number" , STR0034 , "SALDOATUCC"	 ,,,,.F.) // "Saldo Atual CC"
	self:oSchema:addProperty( "SALDOATUCC_M"   , STR0035 , "string" , STR0035 , "SALDOATUCC_M"   ,,,,.F.) // "Saldo Atual CC Mascara"
	self:oSchema:addProperty( "SALDOANTCTA_M"  , STR0036 , "string" , STR0036 , "SALDOANTCTA_M"  ,,,,.F.) // "Saldo Anterior Conta Mascara"
	self:oSchema:addProperty( "VALORLANCACUM"  , STR0037 , "number" , STR0037 , "VALORLANCACUM"	 ,,,,.F.) // "Vlr.do Lanc. Acum. CC"
	self:oSchema:addProperty( "VALORLANCACUM_M", STR0038 , "string" , STR0038 , "VALORLANCACUM_M",,,,.F.) // "Vlr.do Lanc. Acum. CC Mascara"
	self:oSchema:addProperty( "VALORLANCTOT"   , STR0039 , "number" , STR0039 , "VALORLANCTOT"	 ,,,,.F.) // "Vlr.do Lanc. Totalizador"
	self:oSchema:addProperty( "VALORLANCTOT_M" , STR0040 , "string" , STR0040 , "VALORLANCTOT_M" ,,,,.F.) // "Vlr.do Lanc. Totalizador Mascara"

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class AccountingEntriesCostCenterSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class AccountingEntriesCostCenterSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class AccountingEntriesCostCenterSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*/{Protheus.doc} ctbgerlanc
    Será usado para criar a temporaria
    
	@author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method ctbgerlanc(oFilter as object, cFiltCTT as character, cFiltCT1 as character) class AccountingEntriesCostCenterSmartViewBusinessObject
	Local aTamConta	 := TAMSX3("CT1_CONTA") as array
	Local aTamCusto	 := TAMSX3("CTT_CUSTO") as array
	Local aTamVal	 := TAMSX3("CT2_VALOR") as array
	Local aSaveArea  := GetArea() as array
	Local aCtbMoeda	 := {} as array
	Local aCampos as array

	Local nTamHist	:= Len(CriaVar("CT2_HIST")) as numeric
	Local nDecimais	:= 0 as numeric

	// Retorna Decimais
	aCtbMoeda    := CTbMoeda(::cMoeda)
	nDecimais    := aCtbMoeda[5]
	::cDescMoeda := Alltrim(aCtbMoeda[2])

	aCampos :={	{ "CCUSTO"		, "C", aTamCusto[1] , 0 },;			// Centro de Custo
				{ "CONTA"		, "C", aTamConta[1] , 0 },;  		// Codigo da Conta
				{ "XPARTIDA"   	, "C", aTamConta[1] , 0 },;			// Contra Partida			
				{ "TIPO"       	, "C", 01		    , 0 },;			// Tipo do Registro (Debito/Credito/Continuacao)
				{ "VALORLANC"	, "N", aTamVal[1]+2 , nDecimais },; // Valor do Lancamento
				{ "SALDOSCR"	, "N", aTamVal[1]+2	, nDecimais },;	// Saldo
				{ "HISTORICO"	, "C", nTamHist   	, 0 },;			// Historico			
				{ "DATAL"		, "D", 10			, 0 },;			// Data do Lancamento
				{ "LOTE" 		, "C", 06			, 0 },;			// Lote
				{ "SUBLOTE" 	, "C", 03			, 0 },;			// Sub-Lote
				{ "DOC" 		, "C", 06			, 0 },;			// Documento
				{ "LINHA"		, "C", LEN(CT2->CT2_LINHA) , 0 },;	// Linha  03
				{ "SEQLAN"		, "C", LEN(CT2->CT2_SEQLAN), 0 },;	// Sequencia do Lancamento  03
				{ "SEQHIST"		, "C", 03			, 0 },;			// Seq do Historico
				{ "EMPORI"		, "C", 02			, 0 },;			// Empresa Original
				{ "FILORI"		, "C", 02			, 0 }}			// Filial Original

	If __oTempTable <> Nil
		__oTempTable:Delete()
		__oTempTable := Nil
	Endif

	__oTempTable := FWTemporaryTable():New("cArqTmp")  
	__oTempTable:SetFields(aCampos) 
	__oTempTable:AddIndex("1", {"CCUSTO","CONTA","DATAL","LOTE","SUBLOTE","DOC","LINHA","EMPORI","FILORI"})
	
	//Criação da tabela temporaria
	__oTempTable:Create()  

	dbSelectArea("cArqTmp")
	dbSetOrder(1)

	//Monta os dados do arquivo temporário
	self:ctbgeralan(oFilter, cFiltCTT, cFiltCT1)        				
				
	RestArea(aSaveArea)

	FreeObj(__oTempTable)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} ctbgeralan
    Transforma os lançamentos de partida dobrada em débitos e créditos
    
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method ctbgeralan(oFilter as object, cFiltCTT as character, cFiltCT1 as character) class AccountingEntriesCostCenterSmartViewBusinessObject
	Local aSaveArea := GetArea() as array

	dbSelectArea("CTT")
	CTT->(dbSetOrder(2))
	
	CTT->(MSSeek(FwXFilial("CTT")+"2"+self:cCustoIni,.T.))
	While CTT->(!Eof()) .And. CTT->CTT_FILIAL == FwXFilial("CTT") .And. CTT->CTT_CUSTO <= self:cCustoFim .And. CTT->CTT_CLASSE == "2"

		dbSelectArea("CT2")
		CT2->(dbSetOrder(4))

		CT2->(MsSeek(FwXFilial("CT2")+CTT->CTT_CUSTO+ DTOS(self:dDataIni),.T.))
		While CT2->(!Eof()) .And. CT2->CT2_FILIAL == FwXFilial("CT2") .And. ;
			CT2->CT2_CCD == CTT->CTT_CUSTO .And.;
			CT2->CT2_DATA >= self:dDataIni .And. CT2->CT2_DATA <= self:dDataFim 

			If CT2->CT2_VALOR == 0 .Or. CT2->CT2_TPSALD != self:cSaldo .Or. ;
				CT2->CT2_MOEDLC <> self:cMoeda		
				CT2->(dbSkip())
				Loop
			EndIf

			If (CT2->CT2_DEBITO < self:cContaIni .Or. CT2->CT2_DEBITO > self:cContaFim) .Or.;
				(CT2->CT2_CCD < self:cCustoIni .Or. CT2->CT2_CCD > self:cCustoFim) 	
				CT2->(dbSkip())
				Loop
			Endif

			self:ctbgrvlanc(self:cMoeda,self:cSaldo,"1")

			dbSelectArea("CT2")
			CT2->(dbSetOrder(4))	        
			CT2->(dbSkip())
		Enddo			

		//Obtém os créditos
		dbSelectArea("CT2")
		CT2->(dbSetOrder(5))

		CT2->(MsSeek(FwXFilial("CT2")+CTT->CTT_CUSTO+ DTOS(self:dDataIni),.T.))
		While CT2->(!Eof()) .and. CT2->CT2_FILIAL == FwXFilial("CT2") .And. ;
			CT2->CT2_CCC == CTT->CTT_CUSTO .And.;
			CT2->CT2_DATA >= self:dDataIni .And. CT2->CT2_DATA <= self:dDataFim 

				If CT2->CT2_VALOR == 0 .Or. CT2->CT2_TPSALD != self:cSaldo .Or. ;
					CT2->CT2_MOEDLC <> self:cMoeda				
					CT2->(dbSkip())
					Loop
				EndIf

				If (CT2->CT2_CREDIT < self:cContaIni .Or. CT2->CT2_CREDIT > self:cContaFim) .Or.;
					(CT2->CT2_CCC < self:cCustoIni .Or. CT2->CT2_CCC > self:cCustoFim)
					CT2->(dbSkip())
					Loop
				Endif

				//Popula o arquivo temporário
				self:ctbgrvlanc(self:cMoeda,self:cSaldo,"2")

				DbSelectArea("CT2")
				CT2->(dbSetOrder(5))
				CT2->(dbSkip())	
			EndDo			

			CTT->(dbSelectArea("CTT"))
			CTT->(dbSetOrder(2))
			CTT->(dbSkip())
		EndDo	

	CT2->(dbCloseArea())
	CTT->(dbCloseArea())

	RestArea(aSaveArea)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} ctbgrvlanc
    
    Grava os dados na tabela temporaria
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method ctbgrvlanc(cMoeda as character, cSaldo as character, cTipo as character) class AccountingEntriesCostCenterSmartViewBusinessObject
	Local cConta  as character
	Local cContra as character
	Local cCusto  as character

	If cTipo == "1"
		cConta 	:= CT2->CT2_DEBITO
		cContra	:= CT2->CT2_CREDIT
		cCusto	:= CT2->CT2_CCD
	EndIf	

	If cTipo == "2"
		cConta 	:= CT2->CT2_CREDIT
		cContra := CT2->CT2_DEBITO
		cCusto	:= CT2->CT2_CCC
	EndIf		           

	dbSelectArea("cArqTmp")
	cArqTmp->(dbSetOrder(1))

	If RecLock("cArqTmp",.T.)
		Replace DATAL		With CT2->CT2_DATA
		Replace TIPO		With cTipo
		Replace LOTE		With CT2->CT2_LOTE
		Replace SUBLOTE		With CT2->CT2_SBLOTE
		Replace DOC			With CT2->CT2_DOC
		Replace LINHA		With CT2->CT2_LINHA
		Replace CONTA		With cConta
		Replace XPARTIDA	With cContra
		Replace CCUSTO		With cCusto
		Replace HISTORICO	With CT2->CT2_HIST
		Replace EMPORI		With CT2->CT2_EMPORI
		Replace FILORI		With CT2->CT2_FILORI
		Replace SEQHIST		With CT2->CT2_SEQHIS
		Replace SEQLAN		With CT2->CT2_SEQLAN
		Replace VALORLANC	With CT2->CT2_VALOR

		If CT2->CT2_DC == "3"
			Replace TIPO	With cTipo
		Else
			Replace TIPO 	With CT2->CT2_DC
		EndIf

		cArqTmp->(MsUnlock())
	EndIf

Return
