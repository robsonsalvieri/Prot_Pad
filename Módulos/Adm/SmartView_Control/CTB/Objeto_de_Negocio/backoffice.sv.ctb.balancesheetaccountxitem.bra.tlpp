#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetaccountxitem.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CT4, CT1", name="Balancete de Contas x Itens", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetAccountxItemSmartViewBusinessObject
Classe para a criação do Objeto de Negócio do relatório Balancete de Contas x Itens
para o TReports
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetAccountxItemSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method ExecDBSkipTemp()          as logical

    private method f140Soma() as numeric

    protected data nDecimais as numeric
    protected data cPicture  as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetAccountxItemSmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete de Item Contabil"
    self:setPergunte("CTR140") //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetAccountxItemSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Contas x Itens"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceSheetAccountxItemSmartViewBusinessObject
    self:oSchema:addProperty("SOMAANT"    , STR0003, "number", STR0003, "SOMAANT"    ,,,,.F.) //"Soma Sld. Ant."
    self:oSchema:addProperty("SOMAANT_M"  , STR0004, "string", STR0004, "SOMAANT_M"  ,,,,.F.) //"Soma Sld. Ant. Mascara"
    self:oSchema:addProperty("SOMADEB"    , STR0005, "number", STR0005, "SOMADEB"    ,,,,.F.) //"Soma Debito"
    self:oSchema:addProperty("SOMADEB_M"  , STR0006, "string", STR0006, "SOMADEB_M"  ,,,,.F.) //"Soma Debito Mascara"
    self:oSchema:addProperty("SOMACRD"    , STR0007, "number", STR0007, "SOMACRD"    ,,,,.F.) //"Soma Credito"
    self:oSchema:addProperty("SOMACRD_M"  , STR0008, "string", STR0008, "SOMACRD_M"  ,,,,.F.) //"Soma Credito Mascara"
    self:oSchema:addProperty("SOMAMOVI"   , STR0009, "number", STR0009, "SOMAMOVI"   ,,,,.F.) //"Soma Mov."
    self:oSchema:addProperty("SOMAMOVI_M" , STR0010, "string", STR0010, "SOMAMOVI_M" ,,,,.F.) //"Soma Mov. Mascara"
    self:oSchema:addProperty("SOMAATU"    , STR0011, "number", STR0011, "SOMAATU"    ,,,,.F.) //"Soma Sld. Atu."
    self:oSchema:addProperty("SOMAATU_M"  , STR0012, "string", STR0012, "SOMAATU_M"  ,,,,.F.) //"Soma Sld. Atu. Mascara"
    self:oSchema:addProperty("NomeEmpresa", STR0013, "string", STR0013, "NomeEmpresa",,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0014, "string", STR0014, "NomeFilial" ,,,,.F.) //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0015, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERCOMP

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetAccountxItemSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetAccountxItemSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetAccountxItemSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR140")
   
    self:handleCtgGerPlanParams( oFilter )    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetAccountxItemSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetAccountxItemSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetAccountxItemSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Bruno Oliveira (BO)
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetAccountxItemSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    Local cItemIni          as Character
    Local cItemFim          as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    Local nDivide := 1      as numeric
    local lMov              as logical
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date

    dDataIni    :=  MV_PAR01    // Data Inicial ?                
    dDataFim    :=  MV_PAR02    // Data Final ?  
    cContaIni   :=  MV_PAR03    // Conta Inicial ?               
    cContaFim   :=  MV_PAR04    // Conta Final ?  
    cItemIni    :=  MV_PAR05    // Item Contabil Inicial ?
    cItemFim    :=  MV_PAR06    // Item Contabil Final?   
                
    lImpSint    := Iif(MV_PAR07==1 .Or. MV_PAR07==3,.T.,.F.) //      	Imprime Contas ?              
    aSetOfBook  := Iif(!Empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf("")) //      	Cod. Config. Livros ?         
    lSldZerado  := Iif(MV_PAR09==1,.T.,.F.) //Saldos Zerados                            
    cMoeda      := Iif(!Empty(MV_PAR10),MV_PAR10,cMoeda) //Moeda                                            
    cTpsald     := IIf(!Empty(MV_PAR12), PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]), cTpsald) //Tipo de Saldo
    cFilSeg     := MV_PAR14 //Filtra Segmento No. ?  
    cConteudIni := MV_PAR15 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR16 //Conteudo Fim Segmen ?        
    cConteudCont:= MV_PAR17 //Conteudo Contido em ?
    lMov        := Iif(MV_PAR18 == 1, .T., .F.) //Imprime Coluna Movimento ?
    lPrintZero  := Iif(MV_PAR22 == 1, .T., .F.) //Imprime Valor 0.00  ?
    nDivide     := Iif(Empty(MV_PAR24), nDivide, MV_PAR24)
    lImpAntLP   := Iif(MV_PAR26 == 1,.T.,.F.) //Posição Ant. L/P?
    dDataLP     := MV_PAR27 //Data Lucros/Perdas?

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf 

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cItemIni       := cItemIni  
    self:cItemFim       := cItemFim  
    self:cAlias         := "CT4"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := !lMov
    self:lImpConta      := .T.
    self:cHeader        := "CT1"
    self:aGeren         := nil
    self:lCTBR140SV     := .T.

    self:nDecimais := DecimalCTB(::aSetOfBook, cMoeda)
    self:cPicture  := ::aSetOfBook[4]
    
return

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method processCustomData() as json class BalanceSheetAccountxItemSmartViewBusinessObject

    local nTotAnt   := 0 as numeric
    local nTotDeb   := 0 as numeric
    local nTotCred  := 0 as numeric
    local nTotAtu   := 0 as numeric
    local nTotMov   := 0 as numeric
    local TAM_VALOR := 20 as numeric
    
    nTotAnt  := self:f140Soma("A")
    nTotDeb  := self:f140Soma("D")
    nTotCred := self:f140Soma("C")
    nTotAtu  := self:f140Soma("T")

    nTotMov := nTotCred - nTotDeb

    ::jDados["SOMAANT"   ] := nTotAnt
    ::jDados["SOMAANT_M" ] := ValorCTB(nTotAnt,,,TAM_VALOR,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
    ::jDados["SOMADEB"   ] := nTotDeb
    ::jDados["SOMADEB_M" ] := ValorCTB(nTotDeb,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"1",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SOMACRD"   ] := nTotCred
    ::jDados["SOMACRD_M" ] := ValorCTB(nTotCred,,,TAM_VALOR,::nDecimais,.F.,::cPicture,"2",,,,,,::lPrintZero,.F.,.F.)
    ::jDados["SOMAMOVI"  ] := nTotMov
    ::jDados["SOMAMOVI_M"] := ValorCTB(nTotMov,,,TAM_VALOR,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
    ::jDados["SOMAATU"   ] := nTotAtu
    ::jDados["SOMAATU_M" ] := ValorCTB(nTotAtu,,,TAM_VALOR,::nDecimais,.T.,::cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp() as logical class BalanceSheetAccountxItemSmartViewBusinessObject
    
    local lRet := .T. as logical

    if MV_PAR07 == 1 // So imprime Sinteticas
		if cArqTmp->TIPOITEM == "2"
			lRet := .F.
		endif
	endif

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} f140Soma
Soma valores do lançamento
 
@return numérico
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method f140Soma(cTipo as character) as numeric class BalanceSheetAccountxItemSmartViewBusinessObject

    local nRetValor := 0 as numeric

    if MV_PAR07 != 1 // Imprime Analiticas ou Ambas
    	if cArqTmp->TIPOITEM == "2"
    		if cArqTmp->TIPOCONTA== "2"
    			if cTipo == "D"
    				nRetValor := cArqTmp->SALDODEB
    			elseif cTipo == "C"
    				nRetValor := cArqTmp->SALDOCRD
    			elseif cTipo == "A"
    				nRetValor := cArqTmp->SALDOANT
    			elseif cTipo == "T"
    				nRetValor := cArqTmp->SALDOATU
    			endif
    		endif
    		if cTipo == "D"
    			nRetValor := cArqTmp->SALDODEB
    		elseif cTipo == "C"
    			nRetValor := cArqTmp->SALDOCRD
    		elseif cTipo == "A"
    			nRetValor := cArqTmp->SALDOANT
    		elseif cTipo == "T"
    			nRetValor := cArqTmp->SALDOATU
    		endif
    	endif
    else
    	if cArqTmp->TIPOITEM == "1" .And. cArqTmp->NIVEL1
    		if cTipo == "D"
    			nRetValor := cArqTmp->SALDODEB
    		elseif cTipo == "C"
    			nRetValor := cArqTmp->SALDOCRD
    		elseif cTipo == "A"
    			nRetValor := cArqTmp->SALDOANT
    		elseif cTipo == "T"
    			nRetValor := cArqTmp->SALDOATU
    		endif
    	endif
    	if cArqTmp->TIPOITEM == "1" .And. Empty(cArqTmp->ITSUP)
    		if cTipo == "D"
    			nRetValor := cArqTmp->SALDODEB
    		elseif cTipo == "C"
    			nRetValor := cArqTmp->SALDOCRD
    		elseif cTipo == "A"
    			nRetValor := cArqTmp->SALDOANT
    		elseif cTipo == "T"
    			nRetValor := cArqTmp->SALDOATU
    		endif
    	endif
    endif

return nRetValor
