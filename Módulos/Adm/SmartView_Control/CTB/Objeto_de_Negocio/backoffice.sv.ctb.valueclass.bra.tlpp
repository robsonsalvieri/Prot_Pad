#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.valueclass.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTH", name="Classe de Valor", country="ALL",customTables="CTH")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CostCenterSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cad Classe de Valor para o SmartView
 
@author Bruno Oliveira (B.O)
@since 08/01/2024
@version 1.0
*/
//-------------------------------------------------------------------------------
class ValueClassSmartviewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new() as object
    public method getData() as object
    public method getSchema() as object
    public method getAreas() as array
    public method getDescription() as character

    //usados na internacionalizacao
    public method preschema()      as object 
    public method preData()        as object
    public method processData()    as json

    protected method getQuery() 

    protected data jItens       as json
    protected data aAllFields   as array
    protected data oStatement   as object
    protected data cAlias       as character
    protected data oIntegratedProvider as object
    
endclass


//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
new
@return object: self
@author  Bruno Oliveira (B.O)
@since 18/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class ValueClassSmartviewBusinessObject

    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Classe de Valor"
    self:setPergunte("CTR090") //Indica o pergunte que ser? utilizado no relat?rio

    self:aAllFields := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira (B.O)
@since 18/047/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getDescription() as character class ValueClassSmartviewBusinessObject
return STR0002 //"Objeto contendo informações sobre a Classe de Valor"


//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@author Bruno Oliveira (B.O)
@since 18/07/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
method getAreas() as array class ValueClassSmartviewBusinessObject
return {STR0003}//"Contabilidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Bruno Oliveira (B.O)
@since 18/07/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ValueClassSmartviewBusinessObject

    Local jParams := Nil    as json
    Local nX := 0           as numeric 
    Local cId,cRealName     as character
    Local cMascara	:= ""   as character
    Local cSeparador:= ""   as character
    Local cCompanyName      as character
    Local cBranchName       as character
 
    jParams := oFilter:getParameters() //metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "CTR090")//Carrega lista de parametros para a memória

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := self:oIntegratedProvider:getCompanyName()
    cBranchName  := self:oIntegratedProvider:getBranchName()
      
    self:getQuery(oFilter)
    self:cAlias := self:oStatement:OpenAlias()

    If Empty(mv_par06)
        cMascara := SuperGetMv("MV_MASCCTH")       
    Else
        cMascara := RetMasCtb(mv_par06,@cSeparador)
    EndIf

    While !(self:cAlias)->(Eof())

        ::jItens := JsonObject():new()

        For nX := 1 to len (self:aAllFields)
            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()

            If cId == "CTH_CLVL" .OR. cId == "CTH_CLSUP"               
                ::jItens[cId] := EntidadeCTB((self:cAlias)->&(cRealName) ,000,000,030,.F.,cMascara,cSeparador,,,.F.,,.F.)
            ElseIf self:aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(cRealName))            
            ElseIf self:aAllFields[nX]:getType() == "string"
                ::jItens[cId] := AllTrim((self:cAlias)->&(cRealName))
            Else
                ::jItens[cId] := (self:cAlias)->&(cRealName)                
            EndIf
        Next nX

        ::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial" ] := cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())

    EndDo

    dbSelectArea(self:cAlias)
    (self:cAlias)->(DBCloseArea())

    self:oStatement:Destroy()
    self:oStatement := nil 

    //Elimina da memória
    FreeObj(jParams)
    FreeObj(self:oStatement)
    FreeObj(self:oIntegratedProvider)
    FwFreeArray(self:aAllFields)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira (B.O)
@since 12/04/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getSchema() as object class ValueClassSmartviewBusinessObject

    Local aFieldsCTH := {} as Array

    aFieldsCTH := {"CTH_FILIAL", "CTH_CLVL",  "CTH_CLASSE", "CTH_NORMAL", "CTH_DESC01", "CTH_DESC02", "CTH_DESC03",;
                   "CTH_DESC04", "CTH_DESC05","CTH_BLOQ",   "CTH_DTEXIS", "CTH_DTEXSF", "CTH_CLSUP",  "CTH_RES" }

    self:oSchema:aliasToSchema("CTH", aFieldsCTH)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

    self:oSchema:addProperty("NomeEmpresa", STR0004, "string",  STR0004, "NomeEmpresa",,,,.F.)  // Nome Empresa
    self:oSchema:addProperty("NomeFilial",  STR0005, "string",  STR0005, "NomeFilial" ,,,,.F.)  // Nome Filial

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Retorna a query a ser executada no método getData

@return character: cQuery

@author Bruno Oliveira (B.O)
@since 12/04/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getQuery(oFilter as Object) class ValueClassSmartviewBusinessObject
    local cQuery        := ""  as character
    local nParamOrder   := 1   as numeric
    local lNBloqueado          as logical

    lNBloqueado := iif(MV_PAR05 == 2, .T., .F.)

    cQuery  := " SELECT " + self:getSQLFields()
    cQuery  += " FROM "+RetSqlName("CTH")+" CTH "
    cQuery  += " WHERE "

    cQuery  += " CTH.CTH_FILIAL =  ? "
    cQuery  += " AND CTH.CTH_CLVL >= ? "
    cQuery  += " AND CTH.CTH_CLVL <= ? "

    if lNBloqueado
        cQuery  += " AND CTH.CTH_BLOQ = ? "
    EndIf

    cQuery  += " AND CTH.D_E_L_E_T_ = ? "

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY CTH_CLVL "

    cQuery := ChangeQuery(cQuery)
    
    self:oStatement := FWExecStatement():New(cQuery)

    self:oStatement:SetString(nParamOrder++,FWxFilial("CTH"))
    self:oStatement:SetString(nParamOrder++,MV_PAR01)
    self:oStatement:SetString(nParamOrder++,MV_PAR02)

    if lNBloqueado
        self:oStatement:SetString(nParamOrder++,"2")
    EndIf

    self:oStatement:SetString(nParamOrder++," ")
    
    self:oStatement:GetFixQuery()

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que sera consumido pelo M.I.
    Sera usado para manipular o schema do objeto

    @author Bruno Oliveira (B.O)
    @since 12/04/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ValueClassSmartviewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que sera consumido pelo M.I.
    Ajusta o Json conforme alteração no processData e preSchema

    @author Bruno Oliveira (B.O)
    @since 12/04/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ValueClassSmartviewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que sera consumido pelo M.I.
    Sera usado para alterar a Query antes do getData.
    
    @author Bruno Oliveira (B.O)
    @since 12/04/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class ValueClassSmartviewBusinessObject
return ::jItens
