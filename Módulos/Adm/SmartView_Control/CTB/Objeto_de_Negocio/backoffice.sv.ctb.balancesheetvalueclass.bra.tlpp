#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetvalueclass.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTU, CTH", name="Balancete de Classe de Valor", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetValueClassSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete de Classe de Valor
para o TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceSheetValueClassSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
    public method new()                     as object
    public method getAreas()                as array
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json

    public method processCustomData()       as json

    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object    

    public method ExecDBSkipTemp()

    protected method RetValorCtb()          as character

    protected data jDados       as json
    protected data nTotCrd      as numeric
    protected data nTotDeb      as numeric
    protected data nTotMov      as numeric
    protected data cValorCtb    as character
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object class BalanceSheetValueClassSmartViewBusinessObject

    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"Balancete de Classe de Valor"
    self:setPergunte("CTR210") //Indica o pergunte que será utilizado no relatório

    self:nTotCrd := 0
    self:nTotDeb := 0
    self:nTotMov := 0

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 

method getDescription() as character class BalanceSheetValueClassSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre o Balancete de Classe de Valor"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BalanceSheetValueClassSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceSheetValueClassSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceSheetValueClassSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.

    @author Renan Gremes
    @since 09/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class BalanceSheetValueClassSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtGerPlanSchema() as object class BalanceSheetValueClassSmartViewBusinessObject

    self:oSchema:addProperty("TOT_MOV"       , STR0004  , "string", STR0004 , "TOT_MOV"      ) //"Total Mov. Acumulado"
    self:oSchema:addProperty("TOT_DEBITO"    , STR0005  , "string", STR0005 , "TOT_DEBITO"   ) //"Total Débito Acumulado"
    self:oSchema:addProperty("TOT_CREDITO"   , STR0006  , "string", STR0006 , "TOT_CREDITO"  ) //"Total Crédito Acumulado"    */
    self:oSchema:addProperty("NomeEmpresa"   , STR0007  , "string", STR0007 , "NomeEmpresa"  ) // STR0003 - "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    , STR0008  , "string", STR0008 , "NomeFilial"   ) //STR0004 - "Nome Filial"
    
    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0009 , "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  
return self:oSchema


//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Utilizado antes do append no data
 
@return object: self:oData
 
@author Ewerton Franklin
@since 11/12/2024
@version 1.0
*/
//-------------------------------------------------------------------

method processCustomData() as json class BalanceSheetValueClassSmartViewBusinessObject
    
    ::nTotDeb += cArqTmp->SALDODEB	     
    ::nTotCrd += cArqTmp->SALDOCRD
    ::nTotMov := (self:nTotCrd - self:nTotDeb)
    //Adiciona totais no data
    ::jDados["TOT_DEBITO" ]  := self:RetValorCtb(self:nTotDeb) 
    ::jDados["TOT_CREDITO"]  := self:RetValorCtb(self:nTotCrd) 
    ::jDados["TOT_MOV" ]     := self:RetValorCtb(self:nTotMov)  
    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := ::cCompanyName
    ::jDados["NomeFilial" ] := ::cBranchName


return ::jDados


//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetValueClassSmartViewBusinessObject
    //execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetValueClassSmartViewBusinessObject
    //execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetValueClassSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR210")
    self:handleCtgGerPlanParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method ExecDBSkipTemp(cArqTmp As character) class BalanceSheetValueClassSmartViewBusinessObject
   
    local lRet := .T.   
    
    If mv_par05 == 1		// Apenas sinteticas
	    lRet := (cArqTmp->TIPOCLVL == "1")
    ElseIf mv_par05 == 2	// Apenas analiticas
        lRet := (cArqTmp->TIPOCLVL == "2")
    EndIf

return lRet

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Renan Gremes
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------

method handleCtgGerPlanParams( oFilter ) class BalanceSheetValueClassSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    
    local nIniPage          as Numeric
    local nDivide := 1      as numeric

    local lMov              as logical
    local lImpAntLP         as logical
    local lImpSint          as Logical 
    local lPrintZero        as Logical
    local lSldZerado        as Logical

    local lPulaSint         as Logical
    local aSetOfBook        as array
    local aSelFil           as array

    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

    dDataIni    := MV_PAR01                                 //Data Inicial ?
    dDataFim    := MV_PAR02                                 //Data Final ?
    cContaIni   := Space(Len(CriaVar("CT1_CONTA")))         //Conta Inicial ?
    cContaFim   := Repl('Z',Len(CriaVar("CT1_CONTA")))      //Conta Final ?
    cClvlIni    := MV_PAR03
    cClVlFim    := MV_PAR04
                
    lImpSint    := iif(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.) //Imprime Contas ?
    aSetOfBook  := iif(!empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf("")) //Cod. Config. Livros ?
    lSldZerado  := iif(MV_PAR07==1,.T.,.F.)                 //Saldos Zerados
    cMoeda      := iif(!empty(MV_PAR08),MV_PAR08,cMoeda)    //Moeda
    nIniPage    := MV_PAR09                                 //Folha Inicial
    cTpsald     := Iif(Empty(MV_PAR10),cTpsald,PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]))                                 //Tipo de Saldo
    cFilSeg     := MV_PAR12                                 //Filtra Segmento No. ?
    cConteudIni := MV_PAR13                                 //Conteudo Ini Segmen ?
    cConteudFim := MV_PAR14                                 //Conteudo Fim Segmen ?
    cConteudCont:= MV_PAR15                                 //Conteudo Contido em ?
    lMov        := !(mv_par16==1)                           //Imprime Coluna Movimento ?
    lPulaSint   := iif(mv_par17==1,.T.,.F.)                 //Salta Linha Sintet. ?
    lPrintZero  := iif(mv_par18==1,.T.,.F.)                 //Imprime Valor 0.00  ?
    lImpCod     := iif(mv_par19==1,.T.,.F.)                 //Imprime codigo ?
    nDivide     := iif(empty(MV_PAR20),nDivide,MV_PAR20)
    lImpAntLP   := iif(MV_PAR21 == 1,.T.,.F.)               //Posição Ant. L/P?
    dDataLP     := MV_PAR22                                 //Data Lucros/Perdas?

    // Atributos com conteúdo fixado estão obedecendo a chamado do relatorio principal.
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cClvlIni       := cClvlIni  
    self:cClVlFim       := cClVlFim  
    self:cAlias         := "CTU" //são obrigatórios
    self:cIdent         := "CTH" //são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim    
    self:lImpSint       := lImpSint
    self:aSetOfBook     := aSetOfBook
    self:lVlrZerado     := lSldZerado
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:nDivide        := nDivide
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:aSelFil        := aSelFil
    self:lNImpMov       := lMov
    self:lImpConta      := .F.
    self:aGeren         := nil
    
return 


//-------------------------------------------------------------------
/*{Protheus.doc} RetValorCtb
Retorna Mascara

@return character

@author Ewerton Franklin
@since 09/11/2023
@version 1.0*/
//-------------------------------------------------------------------

method RetValorCtb(nValCtb as numeric) as character class BalanceSheetValueClassSmartViewBusinessObject

Local cPicture as character
Local TAM_VALOR := 17 as numeric
Local nDecimais as numeric

DEFAULT nValCtb := 0

nDecimais := DecimalCTB(::aSetOfBook,mv_par08)
cPicture := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")


::cValorCtb := "0"

If nValCtb <=  0
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"1",,,,,,::lPrintZero,.F.)
Else
    ::cValorCtb := ValorCTB(nValCtb,,,TAM_VALOR,nDecimais,.T.,cPicture,"2",,,,,,::lPrintZero,.F.)
EndIf

Return ::cValorCtb

