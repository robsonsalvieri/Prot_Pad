#include "msobject.ch"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "totvs.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "backoffice.sv.ctb.smartviewctgercomp.ch"

static cArqtmp  := " " as character
static cNomeTab := " " as character

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.F., team="SIGACTB", tables="CT1, CT2, CT3, CT7, CTD, CTH, CTM, CTN", name="Classe Pai - SmartViewCtGerComp", country="ALL")

class SmartViewCtGerComp from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()                 as object
    public method getData()             as object
    public method getSchema()           as object    
    public method getDescription()      as character
    public method getAreas()            as array
    
    //usados na internacionalizacao
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json
    public method processCustomData()   as json

    public method getCtGerCompSchema()  as object
    public method getCtGerCompData()    as object
    public method getCtGerCompAppend()  as object
    public method handleCtGerCompParams() 
    public method handleCtGerCompAppend()   //popula pra tabela temporaria conforme parametros
    public method handleExeCtGerComp()      //passar pra CtGerComp os atributos carregados
    public method CtbPeriodos()
    public method CompVld()

    public method execDBSkipTemp()
    
    protected method exeCtGerComp()    as object
    protected method setCtGerCompParams()   //carrega os atributos default da CtGerComp

    public method vldExeCtgGerComp() // Valida se executa a função ctgercomp
    
    //atributos - na ORDEM que esta sendo chamado na função
    protected data oMeter         as object
    protected data oText          as object
    protected data oDlg           as object
    protected data lEnd           as logical

    protected data cArqtmp        as character
    protected data dDataIni       as date
    protected data dDataFim       as date
    protected data cAlias         as character
    protected data cIdent         as character
    protected data cContaIni      as character
    protected data cContaFim      as character
    protected data cCCIni         as character
    protected data cCCFim         as character
    protected data cItemIni       as character
    protected data cItemFim       as character
    protected data cClvlIni       as character
    protected data cClVlFim       as character
    protected data cMoeda         as character
    protected data cSaldos        as character
    protected data aSetOfBook     as array
    protected data cSegmento      as character
    protected data cSegIni        as character
    protected data cSegFim        as character
    protected data cFiltSegm      as character
    protected data lNImpMov       as logical
    protected data lImpConta      as logical
    protected data nGrupo         as numeric
    protected data cHeader        as character
    protected data lImpAntLP      as logical
    protected data dDataLP        as character
    protected data nDivide        as numeric
    protected data cTpVlr         as character
    protected data lFiliais       as Logical
    protected data aFiliais       as array
    protected data lMeses         as Logical
    protected data aMeses         as array
    protected data lVlrZerado     as Logical
    protected data lEntid         as Logical
    protected data aEntid         as logical
    protected data cString        as character
    protected data cFilUSU        as character
    protected data lImpTotS       as Logical
    protected data lImp4Ent       as logical
    protected data c1aEnt         as character
    protected data c2aEnt         as character
    protected data c3aEnt         as character
    protected data c4aEnt         as character
    protected data lAtSlBase      as logical
    protected data lValMed        as logical
    protected data lSalAcum       as logical
    protected data aSelFil        as array
    protected data lTodasFil      as logical
    protected data lImpSint       as logical
    protected data lPrintZero     as logical

    protected data lExercicio     as logical
    protected data lZeradas       as logical   
    protected data jItens         as json

    protected data oIntegratedProvider as object
    protected data cCompanyName        as character
    protected data cBranchName         as character

endclass

method new() as object class SmartViewCtGerComp
    _Super:new()
return self
//-------------------------------------------------------------------
/*/{Protheus.doc} getAreas
    Define o nome da Area, sem a necessidade de declarar no fonte que estiver herdando
    @author gustavo.campos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getAreas() as array class SmartViewCtGerComp
return {STR0001} // STR0001 "Contabilidade"
//-------------------------------------------------------------------
/*/{Protheus.doc} getDescription
    Define um descrição padrão
    @author gustavo.campos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getDescription() as character class SmartViewCtGerComp
return STR0002 + STR0003 // STR0002+STR0003 "Objeto contendo informacoes sobre" + "RELATORIO" 
//-------------------------------------------------------------------
/*/{Protheus.doc} getData
    Atualiza o objeto oData ao ser executado
    @author gustavo.campos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class SmartViewCtGerComp

    //Verifica se o oFilter nao esta vindo de outro lugar e pega os parametros preenchidos
    if oFilter <> NIL
        //Carrega lista de parametros para a memória
        CTBsetValueMVPAR(oFilter:getParameters(), self:cPergunte)
    EndIf

    self:oIntegratedProvider := ControlIntegratedProvider():New()
    self:cCompanyName := self:oIntegratedProvider:getCompanyName()
    self:cBranchName  := self:oIntegratedProvider:getBranchName()

    self:setCtGerCompParams()           //Carrega propriedades e valores default
    self:CtbPeriodos(::cMoeda,::dDataIni,::dDataFim,::lExercicio,::lZeradas)    //Preenche variavel aMeses
    self:CompVld(::nDivide)

    self:getCtGerCompData( nPage, oFilter )

    If self:vldExeCtgGerComp()
        self:exeCtGerComp( oFilter )        //Executa ctgercomp com as propriedades passadas
        self:getCtGerCompAppend(oFilter, nPage)    //Adiciona ao objeto do smartview
    EndIf

    //Elimina da memória a instância do objeto
    FreeObj(oFilter:getParameters())
    FreeObj(self:oIntegratedProvider)

return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Define um schema padrao quando for chamado o metodo getCtGerCompSchema() na classe filha
    @author gustavo.campos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class SmartViewCtGerComp

    //Posicoes 2 e 4 sao descricoes dos campos.
    self:oSchema:addProperty("CONTA"    	,STR0004    ,"string" ,STR0004  ,"CONTA")       // Codigo da Conta
    self:oSchema:addProperty("CONTA_M"    	,STR0005    ,"string" ,STR0005  ,"CONTA_M")       // Cod Conta Mascara
    self:oSchema:addProperty("NORMAL"		,STR0006    ,"string" ,STR0006  ,"NORMAL")      // Situacao    
    self:oSchema:addProperty("CTARES"		,STR0007    ,"string" ,STR0007  ,"CTARES")      // Codigo Reduzido da Conta    
    self:oSchema:addProperty("DESCCTA"		,STR0008    ,"string" ,STR0008  ,"DESCCTA")     // Descricao da Conta
    self:oSchema:addProperty("CUSTO"		,STR0009    ,"string" ,STR0009  ,"CUSTO")       // Codigo do Centro de Custo    
    self:oSchema:addProperty("CUSTO_M"		,STR0010    ,"string" ,STR0010  ,"CUSTO")       // Cod. do CCusto Mascara
    self:oSchema:addProperty("CCRES"		,STR0011    ,"string" ,STR0011  ,"CCRES")       // Codigo reduzido do Centro de Custo
    self:oSchema:addProperty("DESCCC"		,STR0012    ,"string" ,STR0012  ,"DESCCC")      // Descrição do Centro de Custo    
    self:oSchema:addProperty("ITEM"			,STR0013    ,"string" ,STR0013  ,"ITEM")        // Codigo do Item Contabil   
    self:oSchema:addProperty("ITEM_M"		,STR0014    ,"string" ,STR0014  ,"ITEM")        // Codigo do Item Mascara   
    self:oSchema:addProperty("ITEMRES"		,STR0015    ,"string" ,STR0015  ,"ITEMRES")     // Codigo Reduzido do Item    
    self:oSchema:addProperty("DESCITEM"		,STR0016    ,"string" ,STR0016  ,"DESCITEM")    // Descricao do Item    
    self:oSchema:addProperty("CLVL"			,STR0017    ,"string" ,STR0017  ,"CLVL")        // Codigo da Classe de Valor    
    self:oSchema:addProperty("CLVL_M"		,STR0018    ,"string" ,STR0018  ,"CLVL_M")      // Codigo da ClVl Mascara   
    self:oSchema:addProperty("CLVLRES"		,STR0019    ,"string" ,STR0019  ,"CLVLRES")     // Cod. Red. Classe de Valor    
    self:oSchema:addProperty("DESCCLVL"		,STR0020    ,"string" ,STR0020  ,"DESCCLVL")    // Descricao da Classe de Valor    
    self:oSchema:addProperty("COLUNA1"		,STR0021    ,"number" ,STR0021  ,"COLUNA1")     // COLUNA Valor Mensal 1    
    self:oSchema:addProperty("COLUNA2"		,STR0022    ,"number" ,STR0022  ,"COLUNA2")     // COLUNA Valor Mensal 2    
    self:oSchema:addProperty("COLUNA3"		,STR0023    ,"number" ,STR0023  ,"COLUNA3")     // COLUNA Valor Mensal 3    
    self:oSchema:addProperty("COLUNA4"		,STR0024    ,"number" ,STR0024  ,"COLUNA4")     // COLUNA Valor Mensal 4    
    self:oSchema:addProperty("COLUNA5"		,STR0025    ,"number" ,STR0025  ,"COLUNA5")     // COLUNA Valor Mensal 5    
    self:oSchema:addProperty("COLUNA6"		,STR0026    ,"number" ,STR0026  ,"COLUNA6")     // COLUNA Valor Mensal 6    
    self:oSchema:addProperty("COLUNA7"		,STR0027    ,"number" ,STR0027  ,"COLUNA7")     // COLUNA Valor Mensal 7    
    self:oSchema:addProperty("COLUNA8"		,STR0028    ,"number" ,STR0028  ,"COLUNA8")     // COLUNA Valor Mensal 8    
    self:oSchema:addProperty("COLUNA9"		,STR0029    ,"number" ,STR0029  ,"COLUNA9")     // COLUNA Valor Mensal 9  
    self:oSchema:addProperty("COLUNA10"		,STR0030    ,"number" ,STR0030  ,"COLUNA10")    // COLUNA Valor Mensal 10   
    self:oSchema:addProperty("COLUNA11"		,STR0031    ,"number" ,STR0031  ,"COLUNA11")    // COLUNA Valor Mensal 11    
    self:oSchema:addProperty("COLUNA12"		,STR0032    ,"number" ,STR0032  ,"COLUNA12")    // COLUNA Valor Mensal 12    

    self:oSchema:addProperty("COLUNA1_M"	,STR0052    ,"string" ,STR0052  ,"COLUNA1_M")   // "Col. Vlr Mensal 1 Mascara"     
    self:oSchema:addProperty("COLUNA2_M"	,STR0053    ,"string" ,STR0053  ,"COLUNA2_M")   // "Col. Vlr Mensal 1 Mascara"     
    self:oSchema:addProperty("COLUNA3_M"	,STR0054    ,"string" ,STR0054  ,"COLUNA3_M")   // "Col. Vlr Mensal 1 Mascara"    
    self:oSchema:addProperty("COLUNA4_M"	,STR0055    ,"string" ,STR0055  ,"COLUNA4_M")   // "Col. Vlr Mensal 1 Mascara"     
    self:oSchema:addProperty("COLUNA5_M"	,STR0056    ,"string" ,STR0056  ,"COLUNA5_M")   // "Col. Vlr Mensal 1 Mascara"    
    self:oSchema:addProperty("COLUNA6_M"	,STR0057    ,"string" ,STR0057  ,"COLUNA6_M")   // "Col. Vlr Mensal 1 Mascara"   
    self:oSchema:addProperty("COLUNA7_M"	,STR0058    ,"string" ,STR0058  ,"COLUNA7_M")   // "Col. Vlr Mensal 1 Mascara"     
    self:oSchema:addProperty("COLUNA8_M"	,STR0059    ,"string" ,STR0059  ,"COLUNA8_M")   // "Col. Vlr Mensal 1 Mascara"    
    self:oSchema:addProperty("COLUNA9_M"	,STR0060    ,"string" ,STR0060  ,"COLUNA9_M")   // "Col. Vlr Mensal 1 Mascara"  
    self:oSchema:addProperty("COLUNA10_M"	,STR0061    ,"string" ,STR0061  ,"COLUNA10_M")  // "Col. Vlr Mensal 1 Mascara"   
    self:oSchema:addProperty("COLUNA11_M"	,STR0062    ,"string" ,STR0062  ,"COLUNA11_M")  // "Col. Vlr Mensal 1 Mascara"    
    self:oSchema:addProperty("COLUNA12_M"	,STR0063    ,"string" ,STR0063  ,"COLUNA12_M")  // "Col. Vlr Mensal 1 Mascara"  

    self:oSchema:addProperty("TIPOCONTA"	,STR0033    ,"string"  ,STR0033 ,"TIPOCONTA")   // Tipo de Conta
    self:oSchema:addProperty("TIPOCC"		,STR0034    ,"string"  ,STR0034 ,"TIPOCC")      // Tipo de Centro de Custo
    self:oSchema:addProperty("TIPOITEM"		,STR0035    ,"string"  ,STR0035 ,"TIPOITEM")    // Tipo de Item Contabil
    self:oSchema:addProperty("TIPOCLVL"		,STR0036    ,"string"  ,STR0036 ,"TIPOCLVL")    // Tipo de Classe de Valor
    self:oSchema:addProperty("CTASUP"		,STR0037    ,"string"  ,STR0037 ,"CTASUP")      // Codigo da Conta Superior     
    self:oSchema:addProperty("CCSUP"		,STR0038    ,"string"  ,STR0038 ,"CCSUP")       // Codigo do Centro de Custo Superior     
    self:oSchema:addProperty("ITSUP"		,STR0039    ,"string"  ,STR0039 ,"ITSUP")       // Codigo do Item Superior    
    self:oSchema:addProperty("CLSUP"		,STR0040    ,"string"  ,STR0040 ,"CLSUP")       // Codigo da Clvl Superior    
    self:oSchema:addProperty("ORDEM"		,STR0041    ,"string"  ,STR0041 ,"ORDEM")       // ORDEM    
    self:oSchema:addProperty("GRUPO"		,STR0042    ,"string"  ,STR0042 ,"GRUPO")       // Grupo Contabil    
    self:oSchema:addProperty("TOTVIS"		,STR0043    ,"string"  ,STR0043 ,"TOTVIS")      // Resultado da Visão    
    self:oSchema:addProperty("SLDENT"		,STR0044    ,"string"  ,STR0044 ,"SLDENT")      // Saldo entidade    
    self:oSchema:addProperty("FATSLD"		,STR0045    ,"string"  ,STR0045 ,"FATSLD")      // Fator Entidade   
    self:oSchema:addProperty("VISENT"		,STR0046    ,"string"  ,STR0046 ,"VISENT")      // Visualiza Entidade    
    self:oSchema:addProperty("IDENTIFI"		,STR0047    ,"string"  ,STR0047 ,"IDENTIFI")    // Identificador
    self:oSchema:addProperty("ESTOUR"		,STR0048    ,"string"  ,STR0048 ,"ESTOUR")      // Conta Estourada    
    self:oSchema:addProperty("NIVEL1"		,STR0049    ,"boolean" ,STR0049 ,"NIVEL1")      // Nivel    
    self:oSchema:addProperty("COLVISAO"		,STR0050    ,"number"  ,STR0050 ,"COLVISAO")    // Valor da Coluna da visão    
    self:oSchema:addProperty("FILIAL"		,STR0051    ,"string"  ,STR0051 ,"FILIAL")      // Filial
    self:oSchema:addProperty("GRUPOORDEM"   ,STR0064    ,"number"  ,STR0064 ,"GRUPOORDEM")  // Ordenação do Grupo

    self:getCtGerCompSchema()

return self:oSchema

/* Se pula o registro da tabela cArqTemp*/
method ExecDBSkipTemp() class SmartViewCtGerComp
    Local lRet := .F. as Logical
Return lRet

method vldExeCtgGerComp() class SmartViewCtGerComp
    Local lRet := .T.
Return lRet

//-------------------------------------------------------------------
/*/{Protheus.doc} getCtGerCompAppend
    Define o dados que serão appendados na temporaria gerada na CtGerComp,
    podendo serem sobrepostos no handleCtGerCompAppend 
    @author gustavo.campos
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getCtGerCompAppend(oFilter as object, nPage as numeric) as object class SmartViewCtGerComp

    local cMascCta           as character
    local cMascCus           as character
    local cMascIte           as character
    local cMascClv           as character
    local cConta             as character
    local cCusto             as character
    local cItem              as character
    local cClvl              as character
    local cPicture           as character
    local cGrupoBkp          as character
    local nDecimais          as numeric
    local nGrupoOrdem := 0   as numeric
    local aArea := {}        as array
    Local aTamVal := TAMSX3("CT2_VALOR") as array
	Local cSepara	:=""     as character

    aArea := getArea()
    nDecimais := DecimalCTB(::aSetOfBook,::cMoeda) //DecimalCTB(aSetOfBook,mv_par08)
    cPicture  := Iif(!Empty(::aSetOfBook[4]),"@E 999,999,999,999.99","")
    
    //coletando cada mascara
    cMascCta   := IIF(Empty(::aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(::aSetOfBook[2],@cSepara))//Mascara da Conta
    cMascCus   := IIF(Empty(::aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(::aSetOfBook[6],@cSepara))//Mascara do Centro de Custo
    cMascIte   := IIF(Empty(::aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(::aSetOfBook[7],@cSepara))//Mascara do Item Contabil    
    cMascClv   := IIF(Empty(::aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(::aSetOfBook[8],@cSepara))//Mascara da Classe Valor
    
    cArqTmp->(DBGoTop())

    While !cArqTmp->(Eof())
        //Salva o grupo
        cGrupoBkp := cArqTMP->GRUPO

        //nao imprime sinteticas
        If !::lImpSint
            If cArqTmp->TIPOCONTA == '1'
                cArqTmp->(DbSkip())
                Loop
            EndIf
        EndIf

        //Tratamento para mascara
        cConta := EntidadeCTB(cArqTMP->CONTA ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.)
        cCusto := EntidadeCTB(cArqTMP->CUSTO ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.)
        cItem  := EntidadeCTB(cArqTMP->ITEM  ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.)
        cClvl  := EntidadeCTB(cArqTMP->CLVL  ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.)
  
        //Metodo para pular determinado registro
        If self:ExecDBSkipTemp(cArqTmp)
            cArqTmp->(DbSkip())
            Loop
        EndIf

        //NovO objeto
	    ::jItens := JsonObject():new()

        ::JItens["CONTA"]       :=  cArqTMP->CONTA
        ::JItens["CONTA_M"]     :=  cConta
        ::JItens["NORMAL"]      :=  cArqTMP->NORMAL
        ::JItens["CTARES"]      :=  cArqTMP->CTARES
        ::JItens["DESCCTA"]     :=  cArqTMP->DESCCTA
        ::JItens["CUSTO"]       :=  cArqTMP->CUSTO
        ::JItens["CUSTO_M"]     :=  cCusto
        ::JItens["CCRES"]       :=  cArqTMP->CCRES
        ::JItens["DESCCC"]      :=  cArqTMP->DESCCC
        ::JItens["ITEM"]        :=  cArqTMP->ITEM
        ::JItens["ITEM_M"]      :=  cItem
        ::JItens["ITEMRES"]     :=  cArqTMP->ITEMRES
        ::JItens["DESCITEM"]    :=  cArqTMP->DESCITEM
        ::JItens["CLVL"]        :=  cArqTMP->CLVL
        ::JItens["CLVL_M"]      :=  cClvl
        ::JItens["CLVLRES"]     :=  cArqTMP->CLVLRES
        ::JItens["DESCCLVL"]    :=  cArqTMP->DESCCLVL
        ::JItens["COLUNA1"]     :=  cArqTMP->COLUNA1
        ::JItens["COLUNA2"]     :=  cArqTMP->COLUNA2
        ::JItens["COLUNA3"]     :=  cArqTMP->COLUNA3
        ::JItens["COLUNA4"]     :=  cArqTMP->COLUNA4
        ::JItens["COLUNA5"]     :=  cArqTMP->COLUNA5
        ::JItens["COLUNA6"]     :=  cArqTMP->COLUNA6
        ::JItens["COLUNA7"]     :=  cArqTMP->COLUNA7
        ::JItens["COLUNA8"]     :=  cArqTMP->COLUNA8
        ::JItens["COLUNA9"]     :=  cArqTMP->COLUNA9
        ::JItens["COLUNA10"]    :=  cArqTMP->COLUNA10
        ::JItens["COLUNA11"]    :=  cArqTMP->COLUNA11
        ::JItens["COLUNA12"]    :=  cArqTMP->COLUNA12
        ::JItens["COLUNA1_M"]   :=  ValorCTB(cArqTmp->COLUNA1,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA2_M"]   :=  ValorCTB(cArqTmp->COLUNA2,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA3_M"]   :=  ValorCTB(cArqTmp->COLUNA3,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA4_M"]   :=  ValorCTB(cArqTmp->COLUNA4,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA5_M"]   :=  ValorCTB(cArqTmp->COLUNA5,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA6_M"]   :=  ValorCTB(cArqTmp->COLUNA6,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA7_M"]   :=  ValorCTB(cArqTmp->COLUNA7,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA8_M"]   :=  ValorCTB(cArqTmp->COLUNA8,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA9_M"]   :=  ValorCTB(cArqTmp->COLUNA9,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA10_M"]  :=  ValorCTB(cArqTmp->COLUNA10,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA11_M"]  :=  ValorCTB(cArqTmp->COLUNA11,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["COLUNA12_M"]  :=  ValorCTB(cArqTmp->COLUNA12,,,aTamVal[1],nDecimais,.T.,cPicture,"", , , , , ,::lPrintZero,.F.)
        ::JItens["TIPOCONTA"]   :=  cArqTMP->TIPOCONTA
        ::JItens["TIPOCC"]      :=  cArqTMP->TIPOCC
        ::JItens["TIPOITEM"]    :=  cArqTMP->TIPOITEM
        ::JItens["TIPOCLVL"]    :=  cArqTMP->TIPOCLVL
        ::JItens["CTASUP"]      :=  cArqTMP->CTASUP
        ::JItens["CCSUP"]       :=  cArqTMP->CCSUP
        ::JItens["ITSUP"]       :=  cArqTMP->ITSUP
        ::JItens["CLSUP"]       :=  cArqTMP->CLSUP
        ::JItens["ORDEM"]       :=  cArqTMP->ORDEM
        ::JItens["GRUPO"]       :=  cArqTMP->GRUPO
        ::JItens["TOTVIS"]      :=  cArqTMP->TOTVIS
        ::JItens["SLDENT"]      :=  cArqTMP->SLDENT
        ::JItens["FATSLD"]      :=  cArqTMP->FATSLD
        ::JItens["VISENT"]      :=  cArqTMP->VISENT
        ::JItens["IDENTIFI"]    :=  cArqTMP->IDENTIFI
        ::JItens["ESTOUR"]      :=  cArqTMP->ESTOUR
        ::JItens["NIVEL1"]      :=  cArqTMP->NIVEL1
        ::JItens["COLVISAO"]    :=  cArqTMP->COLVISAO
        ::JItens["FILIAL"]      :=  cArqTMP->FILIAL
        ::JItens["GRUPOORDEM"]  :=  nGrupoOrdem

        self:processData()
        self:processCustomData()

        self:oData:appendData(::JItens)
        cArqTmp->(DbSkip())
        
        //Regra utilizada para realizar agrupamentos no design
        If cGrupoBkp <> cArqTMP->GRUPO
            nGrupoOrdem++
        EndIf 
    EndDo

    // Se não for o último registro indica que terá próxima página
	self:setHasNext( !cArqTmp->( EOF( ) ) ) 

    self:handleCtGerCompAppend(oFilter)

    //tratamento para deletar a temporaria
    dbSelectArea("cArqTmp")
    Set Filter To
    dbCloseArea()

    If Select("cArqTmp") == 0
        FErase(cArqTmp+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf

    dbselectArea("CT2")

    restarea(aArea)
return self:oData
//-------------------------------------------------------------------
/*/{Protheus.doc} setCtGerCompParams
    Define um valor default para os atributos abaixo
    @author gustavo.campos
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method setCtGerCompParams(oFilter as object) class SmartViewCtGerComp

    //Defaults por tratativas e cenarios extras
    ::aMeses        := {}
    ::aEntid        := {}

    //Default funcao ctgercomp
    ::lImpSint	    := .F.
    ::cMoeda		:= "01
    ::lEntid		:= .F.
    ::lMeses		:= .F.
    ::lImpTotS	    := .F.
    ::lImp4Ent	    := .F.
    ::c1aEnt		:= ""
    ::c2aEnt		:= ""
    ::c3aEnt		:= ""
    ::c4aEnt		:= ""
    ::lAtSlBase	    := .T.
    ::lValMed		:= .F.
    ::lSalAcum	    := .F.
    ::lTodasFil     := .F.
    ::cArqTmp		:= ""
    ::nDivide       := 1
    ::nGrupo        := 2
    ::lFiliais      := .F.
    ::lPrintZero    := .T.

    self:handleCtGerCompParams()

return 
//-------------------------------------------------------------------
/*/{Protheus.doc} exeCtGerComp
    Executa a CtGerComp conforme atributos preenchidos na classe filha
    @author gustavo.campos
    @since 28/08/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method exeCtGerComp(oFilter as object) as object class SmartViewCtGerComp

    CtGerComp( 	    "",;
                    "",;
                    "",;
                    "",;
                    @cArqtmp,;
                    ::dDataIni,;
                    ::dDataFim,;
                    ::cAlias,;
                    ::cIdent,;
                    ::cContaIni,;
                    ::cContaFim,;
                    ::cCCIni,;
                    ::cCCFim,;
                    ::cItemIni,;
                    ::cItemFim,;
                    ::cClvlIni,;
                    ::cClVlFim,;
                    ::cMoeda,;
                    ::cSaldos,;
                    ::aSetOfBook,;
                    ::cSegmento,;
                    ::cSegIni,;
                    ::cSegFim,;
                    ::cFiltSegm,;
                    ::lNImpMov,;
                    ::lImpConta,;
                    ::nGrupo,;
                    ::cHeader,;
                    ::lImpAntLP,;
                    ::dDataLP,;
                    ::nDivide,;
                    ::cTpVlr,;
                    ::lFiliais,;
                    ::aFiliais,;
                    ::lMeses,;
                    ::aMeses,;
                    ::lVlrZerado,;
                    ::lEntid,;
                    ::aEntid,;
                    ::lImpSint,;
                    ::cString,;
                    ::cFilUSU,;
                    ::lImpTotS,;
                    ::lImp4Ent,;
                    ::c1aEnt,;
                    ::c2aEnt,;
                    ::c3aEnt,;
                    ::c4aEnt,;
                    ::lAtSlBase,;
                    ::lValMed,;
                    ::lSalAcum,;
                    ::aSelFil,;
                    ::lTodasFil,;
                    @cNomeTab) 

    self:handleExeCtGerComp()

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} CtbPeriodos
    Ajusta o array de comparativo mensal aMeses conforme envio do relatorio do objeto de negócio
    @author gustavo.campos
    @since 30/08/2023
    @version 12.1.2210
/*/ 
//-------------------------------------------------------------------
method CtbPeriodos(cMoeda as Character, dDtIni as Date, dDtFim as Date, lExercicio as Logical, lZeradas as Logical, cCalend as Character, cCriter as Character ) class SmartViewCtGerComp

    Local   lPrim       :=  .T. as Logical
    Local   nMes        :=  1   as Numeric
    Local   nMeses      :=  1   as Numeric
    Local   nCont       :=  1   as Numeric
    Local   cDescMes    :=  ""  as Character
    Local   aPeriodos   := {}   as Array
    Local   aMeses      := {}   as Array

    //Verifica datas para 
    aPeriodos := ctbPeriodos(cMoeda, dDtIni, dDtFim, lExercicio, lZeradas)
    
    //Ordena array de matriz das datas.
    If AllTrim(UPPER(Self:cPergunte)) <> "CTR285"
        aSort( aPeriodos,,, {|x,y| DTOS(x[1]) < DTOS(y[1]) } )
    EndIf

    For nCont := 1 to len(aPeriodos)       
        //Se a Data do periodo eh maior ou igual a data inicial solicitada no relatorio.
        If lPrim
            nMes:=Month(aPeriodos[nCont][1])
            cDescMes:=MesExtenso(nMes)
            lPrim:=.F.
        Else 
            nMes:=nMes+1
            If nMes> 12
                nMes:=1
            EndIf
            cDescMes:=MesExtenso(nMes)	
        EndIf
        
        If aPeriodos[nCont][1] >= dDtIni .And. aPeriodos[nCont][2] <= dDtFim 
            If nMeses <= 12
                AADD(aMeses,{StrZero(nMeses,2),aPeriodos[nCont][1],aPeriodos[nCont][2],cDescMes})	
                nMeses += 1           					
            EndIf
        EndIf
    Next    

    //Adiciona na propriedade aMeses o array conforme retorno da funcao ctbperiodos.
    ::aMeses    := aMeses

return 

//-------------------------------------------------------------------
/*/{Protheus.doc} CtbPeriodos
    Ajusta o parâmetro nDivide vindo do objeto de negócio
    @author gustavo.campos
    @since 30/08/2023
    @version 12.1.2210
/*/ 
//-------------------------------------------------------------------
method CompVld(nDivide as Numeric) class SmartViewCtGerComp

    If nDivide == 2			// Divide por cem
	   ::nDivide := 100
    ElseIf nDivide == 3		// Divide por mil
	    ::nDivide := 1000
    ElseIf nDivide == 4		// Divide por milhao
	    ::nDivide := 1000000
    EndIf

return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Executa a query conforme parametrizacao
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class SmartViewCtGerComp

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que  consumido pelo M.I.
    Ajusta o Json conforme alteracao no processData e preSchema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class SmartViewCtGerComp
    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que  consumido pelo M.I.
    usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class SmartViewCtGerComp

return ::JItens

//-------------------------------------------------------------------
/*/{Protheus.doc} processCustomData
    Metodo que será consumido pelas classes filhas
    
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processCustomData() as json class SmartViewCtGerComp    
Return ::JItens
