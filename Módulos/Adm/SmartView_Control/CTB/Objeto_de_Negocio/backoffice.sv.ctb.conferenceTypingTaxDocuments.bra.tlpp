#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.conferenceTypingTaxDocuments.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2", NAME="Conferência Digitação Documento Fiscal", country="ALL")

class conferenceTypingTaxDocumentsSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartViewCtbr420Raz

    public method new()                     as object
    public method getDescription()          as character
    public method getCTBR420RazData()       as object  //Carregado na classe pai
    public method getCTBR420RazSchema()     as object // Carregado na classe pai
    public method handleCTBR420RazParams() //carrega os parametros
    public method processCustomData()       as json 
    
    public method preData()                 as object
    public method preSchema()               as object
    public method processData()             as json

    protected data cLoteAnt   as character

endclass

method new() as object class conferenceTypingTaxDocumentsSmartViewBusinessObject

_Super:new()
//Define o nome do Objeto de Negócio
self:setDisplayName(STR0001) // STR0001 "Conta por Documento Fiscal"
self:setPergunte("CTR075") //Indica o pergunte que será utilizado no relatório

::cLoteAnt    :=""

return self

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.

@return object: self:oSchema

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0
/*/
//-------------------------------------------------------------------
method preSchema() as object class conferenceTypingTaxDocumentsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0
*/
//-------------------------------------------------------------------

method preData() as object class conferenceTypingTaxDocumentsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método chamado antes do appendData

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------

method processData() class conferenceTypingTaxDocumentsSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------

method getDescription() as character class conferenceTypingTaxDocumentsSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Conferência Digitação Documento Fiscal"

//-------------------------------------------------------------------
/*{Protheus.doc} getCTBR420RazData

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------
method getCTBR420RazData(nPage as numeric, oFilter as object) as object class conferenceTypingTaxDocumentsSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR075")
    self:handleCTBR420RazParams( oFilter )

    //Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getCTBR420RazSchema

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------

method getCTBR420RazSchema() as object class conferenceTypingTaxDocumentsSmartViewBusinessObject

    Local lCanFilter := .F. as Logical

    self:oSchema:addProperty("INFORMADO"   ,STR0004,'number',STR0004,"INFORMADO")//Codigo da Conta
    self:oSchema:addProperty("DIGITADO"    ,STR0005,'number',STR0005,"DIGITADO")//Codigo da Conta Mascara
    self:oSchema:addProperty("DIFERENCA"   ,STR0006,'number',STR0006,"DIFERENCA")//Codigo Conta Debito
    self:oSchema:addProperty("LtINFORMADO" ,STR0007,'number',STR0007,"LtINFORMADO")//Codigo da Conta
    self:oSchema:addProperty("LtDIGITADO"  ,STR0008,'number',STR0008,"LtDIGITADO")//Codigo da Conta Mascara
    self:oSchema:addProperty("LtDIFERENCA" ,STR0009,'number',STR0009,"LtDIFERENCA")//Codigo Conta Debito

    self:oSchema:addProperty("NomeEmpresa" ,STR0010, "string",  STR0010 , "NomeEmpresa",,,,lCanFilter  ) //  "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"  ,STR0011, "string",  STR0011 , "NomeFilial" ,,,,lCanFilter  ) //  "Nome Filial"
    
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} handleCTBR420RazParams

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------

method handleCTBR420RazParams( oFilter as object) class conferenceTypingTaxDocumentsSmartViewBusinessObject

Local cArqTmp       := ""   AS Character 
Local cContaIni     := ""   AS Character 
Local cContaFim     := ""   AS Character 
Local cMoeda        := ""   AS Character 
Local dDataIni              AS Date 
Local dDataFim              AS Date 
Local aSetOfBook    := {}   AS Array 
Local lNoMov                AS Logical 
Local cSaldo        := ""   AS Character 
Local cTpRel        := "2"  AS Character 
Local lAnalitico    := .T.  AS Logical 
Local cLoteIni      := ""   AS Character 
Local cLoteFim      := ""   AS Character 
Local cSbLoteIni    := ""   AS Character 
Local cSbLoteFim    := ""   AS Character 
Local cDocIni       := ""   AS Character 
Local cDocFim       := ""   AS Character 
Local cFiltCT1      := ""   AS Character 
Local lNaoQuebra    := .F.  AS Logical 
Local lTotal        := .F.  AS Logical
Local nImpConta     := 1    AS numeric
Local aSelFil       := {}   AS Array 

///Parametros utilizados na visão de dados.
dDataIni    := MV_PAR01
dDataFim    := MV_PAR02
cLoteIni    := MV_PAR03
cLoteFim    := MV_PAR04
cDocIni     := MV_PAR05
cDocFim     := MV_PAR06
cMoeda      :=  iif(Empty(MV_PAR07),"01",MV_PAR07)
cSaldo      :=  iif(Empty(MV_PAR08),"1",PadR(AllTrim(MV_PAR08), TamSX3("CT2_TPSALD")[1])) 
lNaoQuebra	:= (mv_par09 == 3)
lTotal		:= (mv_par10 == 1) 
cSbLoteIni  := MV_PAR11
cSbLoteFim  := MV_PAR12
lImpDoc0	:= (mv_par13 == 1)
cContaIni   :=  MV_PAR15
cContaFim   :=  MV_PAR16
nImpConta   := 1
lNoMov      := .F.
aSetOfBook  := {"","",0,"","","","","",1,""}

//Os parametros de mv_par09 em diante serão utilizados quando for feito o design do relatório.
//por enquanto na visão de dados não tem efeito nenhum até o momento serão revistos futuramente.


::cArqTmp       :=  cArqTmp   
::lEnd          := .F.
::cContaIni     :=  cContaIni  
::cContaFim     :=  cContaFim  
::cMoeda        :=  cMoeda     
::dDataIni      :=  dDataIni   
::dDataFim      :=  dDataFim   
::aSetOfBook    :=  aSetOfBook 
::lNoMov        :=  lNoMov
::cSaldo        :=  cSaldo     
::cTpRel        :=  cTpRel     
::lAnalitico    :=  lAnalitico 
::cLoteIni      :=  cLoteIni   
::cLoteFim      :=  cLoteFim   
::cSbLoteIni    :=  cSbLoteIni 
::cSbLoteFim    :=  cSbLoteFim 
::cDocIni       :=  cDocIni    
::cDocFim       :=  cDocFim    
::cFiltCT1      :=  cFiltCT1   
::aSelFil       :=  aSelFil
::lImpDoc0      :=  lImpDoc0 

return 

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData

@return object: self:oData

@author Ednilson Queiroz
@since 11/03/2024
@version 1.0*/
//-------------------------------------------------------------------

method processCustomData() as json class conferenceTypingTaxDocumentsSmartViewBusinessObject
    
    Local aRetCT6 :={}   as array
    Local nTotDocInf as numeric
    Local nTotDocDig as Numeric
    Local nTotDocDif as numeric

   
    If CTC->(MsSeek((::cAlias)->FILIAL+DtoS((::cAlias)->DATAL)+(::cAlias)->LOTE+(::cAlias)->SUBLOTE+(::cAlias)->DOC+::cMoeda))
        ::JItens["INFORMADO"] := CTC->CTC_INF
        ::JItens["DIGITADO"] := CTC->CTC_DIG
        ::JItens["DIFERENCA"] := Abs(CTC->CTC_INF - CTC->CTC_DIG)
        If Empty(::cLoteAnt) .Or. ::cLoteAnt <> (::cAlias)->FILIAL+DtoS((::cAlias)->DATAL)+(::cAlias)->LOTE+(::cAlias)->SUBLOTE
            aRetCT6 := CtbSaldoLote(CTC->CTC_LOTE,CTC->CTC_SBLOTE,CTC->CTC_DATA,::cMoeda,"1",(::cAlias)->FILIAL)
            nTotDocInf := aRetCT6[3]
            nTotDocDig := aRetCT6[4]
            nTotDocDif := Abs(aRetCT6[4]-aRetCT6[3])
            ::JItens["LtINFORMADO"] := nTotDocInf
            ::JItens["LtDIGITADO"] := nTotDocDig
            ::JItens["LtDIFERENCA"] := nTotDocDif
            ::cLoteAnt:=(::cAlias)->FILIAL+DtoS((::cAlias)->DATAL)+(::cAlias)->LOTE+(::cAlias)->SUBLOTE
        Else
            ::JItens["LtINFORMADO"] := 0
            ::JItens["LtDIGITADO"]  := 0
            ::JItens["LtDIFERENCA"] := 0
        Endif
    EndIf

    //Dados empresa e filial logada
    ::JItens["NomeEmpresa"] := ::cCompanyName
    ::JItens["NomeFilial" ] := ::cBranchName
    
return ::JItens




