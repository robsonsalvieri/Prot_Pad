#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetbyentities.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2", NAME="Balancete por Entidade", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} balancesheetbyentitiesSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete por Entidade
para o TReports
 
@author Ednilson Queiroz
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class balancesheetbyentitiesSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()            as object
	public method getDescription() as character
	public method getAreas()       as array
	public method getData()        as object
	public method getSchema()      as object

	//usados na internacionalização
	public method preschema()      as object
	public method preData()        as object
	public method processData()    as json

	protected method execQuery()   as character //executar a procedure dos Saldos
	protected method loadEntity()  as array  	//carrega quantas entidades tem configurada no ambiente
	protected method execProc()      			//executar a procedure dos Saldos
	protected method sCtbCfCubo()  as array     //executar a procedure dos Saldos

	//parametros que serão alimentados por MV_PAR
	protected data dDataIni	      as date
	protected data dDataFim	      as date
	protected data cMoeda	      as character
	protected data cSaldo	      as character
	protected data nMaxLin        as numeric
	protected data cMoedaDsc      as character
	protected data nEntidades     as numeric
	protected data jItens         as json
	protected data cAlias         as character
	protected data aEntidade      as array
	protected data aStruct        as array
	protected data cNomeConf      as character
	protected data aEntidades     as array
	protected data aSelFil        as array
    protected data oIntegratedProvider  as object

	private data oQry as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Ednilson Queiroz
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class balancesheetbyentitiesSmartViewBusinessObject

	_Super:new()
	//Define o nome do Objeto de Negócio
	self:setDisplayName(STR0001) //  "Balancete por Entidade"
	self:setPergunte("CTR051") //Indica o pergunte que será utilizado no relatório

	//seta alguns atributos com valores default
	::cAlias      := ""
	::cSaldo      := "1"
	::cMoeda      := "01"
	::nMaxLin     := 55
	::aStruct     := {}
	::aEntidades  := {}
	::cNomeConf   := 'CTBR051'

return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz
@since 09/11/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class balancesheetbyentitiesSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balancete por Entidade"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz
@since 09/11/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getAreas() as array class balancesheetbyentitiesSmartViewBusinessObject
return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Executa a query conforme parametrização
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preSchema() as object class balancesheetbyentitiesSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method preData() as object class balancesheetbyentitiesSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method processData() as json class balancesheetbyentitiesSmartViewBusinessObject
return ::JItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Ednilson Queiroz
@since 09/11/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class balancesheetbyentitiesSmartViewBusinessObject
	Local cQuery    := ""       as character
	Local aSetOfBook            as array
	Local nDecimais             as numeric
	Local cMascCta              as character
	Local cMascCus              as character
	Local cMascIte              as character
	Local cMascClv              as character
	Local cCompanyName			as character
	Local cBranchName 			as character
	Local TAM_VALOR := 19       as numeric
	Local lPrintZero :=.T.      as logical
	Local jParams as json

	Private lCusto             :=.T. as logical //ajustar
	Private lItem              :=.T. as logical //ajustar
	Private lClVl              :=.T. as logical //ajustar

	//Carrega lista de parametros para a memória
	jParams := oFilter:getParameters()
	CTBsetValueMVPAR(jParams, "CTR051")

	::oIntegratedProvider := ControlIntegratedProvider():New()

	::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        ::aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf

	cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	aSetOfBook := CTBSetOf( "" )
	If empty(MV_PAR10)
		Return self:oData
	Endif
	lEntidade1	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=1,.T.)
	lEntidade2	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=2,.f.)
	lEntidade3	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=3,.f.)
	lEntidade4	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=4,.f.)
	lEntidade5	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=5,.f.)
	lEntidade6	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=6,.f.)
	lEntidade7	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=7,.f.)
	lEntidade8	:= iif(!empty(MV_PAR10),Val(MV_PAR10) >=8,.f.)
	lEntidade9	:= iif(!empty(MV_PAR10),Val(MV_PAR10) ==9,.f.)

	::dDataIni	 := MV_PAR01       								//Data inicial ?
	::dDataFim	 := MV_PAR02       								//Data final ?
	::cMoeda	 := iif(!empty(MV_PAR04),MV_PAR04,::cMoeda)     //Moeda ?
	::cSaldo	 := iif(!Empty(MV_PAR05), PadR(AllTrim(MV_PAR05), TamSX3("CT2_TPSALD")[1]), ::cSaldo) //Tipo de Saldo ?
	::nMaxLin    := iif(!empty(MV_PAR08),MV_PAR08,55)       	//Num.linhas p/ o Balancete ?
	::cMoedaDsc  := iif(!empty(MV_PAR09),MV_PAR09,::cMoeda)     //Descricao na moeda ?
	::nEntidades := iif(!empty(MV_PAR10),Val(MV_PAR10),1)   	//Impr. Ate Entidade ?
	lPrintZero   := iif(MV_PAR07==1,.T.,.F.)

	//preenche a  quantidade de Entidades
	::aEntidade := self:loadEntity(oFilter)

	//executa a query
	cQuery := self:execQuery(oFilter)

	::cAlias := MPSysOpenQuery(cQuery, NIL, ::aStruct)

	nDecimais := DecimalCTB(aSetOfBook,::cMoeda)
	cPicture  := iif(!Empty(aSetOfBook[4]),"@E 9,999,999,999.99","")

	//coletando as 4 mascaras
	cMascCta   := iif(Empty(aSetOfBook[2]),SuperGetMv("MV_MASCARA"),RetMasCtb(aSetOfBook[2],@cSepara)) //Mascara da Conta

	//se nivel 02
	If ( lCusto := ::nEntidades >= 2 )
		cMascCus   := iif(Empty(aSetOfBook[6]),SuperGetMv("MV_MASCCUS"),RetMasCtb(aSetOfBook[6],@cSepara)) //Mascara do Centro de Custo
	EndIf
	//se nivel 03
	If ( lItem:= ::nEntidades >=3 )
		cMascIte   := iif(Empty(aSetOfBook[7]),SuperGetMv("MV_MASCCTD"),RetMasCtb(aSetOfBook[7],@cSepara)) //Mascara do Item Contabil
	EndIf
	//se nivel 04
	If ( lClVl:=::nEntidades >= 4 )
		cMascClv   := iif(Empty(aSetOfBook[8]),SuperGetMv("MV_MASCCTH"),RetMasCtb(aSetOfBook[8],@cSepara)) //Mascara da Classe Valor
	EndIf

	dbSelectArea(::cAlias)
	(::cAlias)->(DBGoTop())
	While !(::cAlias)->(Eof())

		cConta := iif(lEntidade1,EntidadeCTB((::cAlias)->CVX_NIV01 ,0,0,Nil/*Tamanho*/,.F.,cMascCta,""/*Separador*/,"CT1",,,,.F.),"")
		cCusto := iif(lEntidade2,EntidadeCTB((::cAlias)->CVX_NIV02 ,0,0,Nil/*Tamanho*/,.F.,cMascCus,""/*Separador*/,"CTT",,,,.F.),"")
		cItem  := iif(lEntidade3,EntidadeCTB((::cAlias)->CVX_NIV03 ,0,0,Nil/*Tamanho*/,.F.,cMascIte,""/*Separador*/,"CTD",,,,.F.),"")
		cClvl  := iif(lEntidade4,EntidadeCTB((::cAlias)->CVX_NIV04 ,0,0,Nil/*Tamanho*/,.F.,cMascClv,""/*Separador*/,"CTH",,,,.F.),"")

		::jItens:= {;
			"CVX_FILIAL"    :(::cAlias)->CVX_FILIAL,;
			"CVX_CONFIG"    :(::cAlias)->CVX_CONFIG,;
			"CVX_MOEDA"     :(::cAlias)->CVX_MOEDA,;
			"CVX_TPSALD"    :(::cAlias)->CVX_TPSALD,;
			"CVX_PROC"      :(::cAlias)->CVX_PROC,;
			"CVX_NIVEL"     :(::cAlias)->CVX_NIVEL,;
			"CVX_IDPAI"     :(::cAlias)->CVX_IDPAI,;
			"CVX_NIV01"     :iif(lEntidade1,(::cAlias)->CVX_NIV01,""),;
			"NIV01_M"       :cConta,;
			"CVX_NIV02"     :iif(lEntidade2,(::cAlias)->CVX_NIV02,""),;
			"NIV02_M"       :cCusto,;
			"CVX_NIV03"     :iif(lEntidade3,(::cAlias)->CVX_NIV03,""),;
			"NIV03_M"       :cItem,;
			"CVX_NIV04"     :iif(lEntidade4,(::cAlias)->CVX_NIV04,""),;
			"NIV04_M"       :cClvl,;
			"CVX_NIV05"     :iif(lEntidade5,(::cAlias)->CVX_NIV05,""),;
			"CVX_NIV06"     :iif(lEntidade6,(::cAlias)->CVX_NIV06,""),;
			"CVX_NIV07"     :iif(lEntidade7,(::cAlias)->CVX_NIV07,""),;
			"CVX_NIV08"     :iif(lEntidade8,(::cAlias)->CVX_NIV08,""),;
			"CVX_NIV09"     :iif(lEntidade9,(::cAlias)->CVX_NIV09,""),;
			"CVX_SLCR01"    :(::cAlias)->CVX_SLCR01,;
			"CVX_SLDB01"    :(::cAlias)->CVX_SLDB01,;
			"CVX_SALD01"    :(::cAlias)->CVX_SALD01,;
			"CVX_SALD01_M"  :ValorCTB((::cAlias)->CVX_SALD01,,,TAM_VALOR-2,nDecimais,.T.,cPicture,,,,,,,lPrintZero,.F.),;
			"CVX_SLCR02"    :(::cAlias)->CVX_SLCR02,;
			"CVX_SLDB02"    :(::cAlias)->CVX_SLDB02,;
			"CVX_SALD02"    :(::cAlias)->CVX_SALD02,;
			"CVX_SALD02_M"  :ValorCTB((::cAlias)->CVX_SALD02,,,TAM_VALOR-2,nDecimais,.T.,cPicture,,,,,,,lPrintZero,.F.),;
			"CVX_SLDATU"    :(::cAlias)->CVX_SLDATU,;
			"CVX_SLDATU_M"  :ValorCTB((::cAlias)->CVX_SLDATU,,,TAM_VALOR-2,nDecimais,.T.,cPicture,,,,,,,lPrintZero,.F.)}
		
		//Atribui a empres/filial logada
		::jItens["NomeEmpresa" ] := cCompanyName
		::jItens["NomeFilial"  ] := cBranchName
		
		self:processData()
		self:oData:appendData(::jItens)		

		(::cAlias)->(DbSkip())

	EndDo

	(::cAlias)->(dbCloseArea())

	//Elimina da memória
    FreeObj(oFilter:getParameters())
	FreeObj(jParams)
	FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} getSchema
    Monta o schema
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method getSchema() as object class balancesheetbyentitiesSmartViewBusinessObject
	
	self:oSchema:addProperty("CVX_FILIAL"   ,STR0004,  'string',STR0004,  "CVX_FILIAL",,,,.F.)
	self:oSchema:addProperty("CVX_CONFIG"   ,STR0033,  'string',STR0033,  "CVX_CONFIG",,,,.F.)
	self:oSchema:addProperty("CVX_MOEDA"    ,STR0005,  'string',STR0005,   "CVX_MOEDA",,,,.F.)
	self:oSchema:addProperty("CVX_TPSALD"   ,STR0006,  'string',STR0006,  "CVX_TPSALD",,,,.F.)
	self:oSchema:addProperty("CVX_NIV01"    ,STR0007,  'string',STR0007,   "CVX_NIV01",,,,.F.)
	self:oSchema:addProperty("NIV01_M"      ,STR0008,  'string',STR0008,     "NIV01_M",,,,.F.)
	self:oSchema:addProperty("CVX_NIV02"    ,STR0009,  'string',STR0009,   "NIV02"	  ,,,,.F.)
	self:oSchema:addProperty("NIV02_M"      ,STR0010,  'string',STR0010,     "NIV02_M",,,,.F.)
	self:oSchema:addProperty("CVX_NIV03"    ,STR0011,  'string',STR0011,   "CVX_NIV03",,,,.F.)
	self:oSchema:addProperty("NIV03_M"      ,STR0012,  'string',STR0012,     "NIV03_M",,,,.F.)
	self:oSchema:addProperty("CVX_NIV04"    ,STR0013,  'string',STR0013,   "CVX_NIV04",,,,.F.)
	self:oSchema:addProperty("NIV04_M"      ,STR0014,  'string',STR0014,     "NIV04_M",,,,.F.)
	self:oSchema:addProperty("CVX_NIV05"    ,STR0015,  'string',STR0015,   "CVX_NIV05",,,,.F.)
	self:oSchema:addProperty("CVX_NIV06"    ,STR0016,  'string',STR0016,   "CVX_NIV06",,,,.F.)
	self:oSchema:addProperty("CVX_NIV07"    ,STR0017,  'string',STR0017,   "CVX_NIV07",,,,.F.)
	self:oSchema:addProperty("CVX_NIV08"    ,STR0018,  'string',STR0018,   "CVX_NIV08",,,,.F.)
	self:oSchema:addProperty("CVX_NIV09"    ,STR0019,  'string',STR0019,   "CVX_NIV09",,,,.F.)
	self:oSchema:addProperty("CVX_PROC"     ,STR0020,  'string',STR0020,    "CVX_PROC",,,,.F.)
	self:oSchema:addProperty("CVX_NIVEL"    ,STR0021,  'number',STR0021,   "CVX_NIVEL",,,,.F.)
	self:oSchema:addProperty("CVX_IDPAI"    ,STR0022,  'string',STR0022,   "CVX_IDPAI",,,,.F.)
	self:oSchema:addProperty("CVX_SLCR01"   ,STR0023,  'number',STR0023,  "CVX_SLCR01",,,,.F.)
	self:oSchema:addProperty("CVX_SLDB01"   ,STR0024,  'number',STR0024,  "CVX_SLDB01",,,,.F.)
	self:oSchema:addProperty("CVX_SALD01"   ,STR0025,  'number',STR0025,  "CVX_SALD01",,,,.F.)
	self:oSchema:addProperty("CVX_SALD01_M" ,STR0026,  'string',STR0026,"CVX_SALD01_M",,,,.F.)
	self:oSchema:addProperty("CVX_SLCR02"   ,STR0027,  'number',STR0027,  "CVX_SLCR02",,,,.F.)
	self:oSchema:addProperty("CVX_SLDB02"   ,STR0028,  'number',STR0028,  "CVX_SLDB02",,,,.F.)
	self:oSchema:addProperty("CVX_SALD02"   ,STR0029,  'number',STR0029,  "CVX_SALD02",,,,.F.)
	self:oSchema:addProperty("CVX_SALD02_M" ,STR0030,  'string',STR0030,"CVX_SALD02_M",,,,.F.)
	self:oSchema:addProperty("CVX_SLDATU"   ,STR0031,  'number',STR0031,  "CVX_SLDATU",,,,.F.)
	self:oSchema:addProperty("CVX_SLDATU_M" ,STR0032,  'string',STR0032,"CVX_SLDATU_M",,,,.F.)
	self:oSchema:addProperty("NomeEmpresa"  ,STR0034,  'string',STR0034, "NomeEmpresa",,,,.F.) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"   ,STR0035,  'string',STR0035, "NomeFilial" ,,,,.F.) //"Nome Filial"

	self:oSchema:addParameter("SV_MULTBRANCH", STR0036, "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} execQuery
    Executa a query conforme parametrização
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method execQuery(oFilter as object) as character class balancesheetbyentitiesSmartViewBusinessObject
	Local aTam		 := TamSx3('CT2_VALOR') as array
	Local aDataIni	 := {}			as array
	Local aDataFim	 := {}			as array
	Local cArqTemp	 := ""			as character
	Local cQry		 := ""			as character
	Local cQryField  := ""			as character
	Local nX		 := 0			as numeric
	Local nY		 := 0			as numeric
	Local cEntidade	 := iif(!empty(MV_PAR10),MV_PAR10,"1") as character	
	Local lVlrZerado := iif(MV_PAR03==1,.T.,.F.) as logical

	//Variavel data inicial
	AADD(aDataIni,Ctod("01/01/80"))		// data Anterior
	AADD(aDataIni,::dDataIni)			// data Atual

	AADD(aDataFim,::dDataIni-1)			// data Anterior
	AADD(aDataFim,::dDataFim)			// data Atual

	//Cria Classe Ctb_Exec_Cube Objeto --> oObjCubo
	oObjCubo := Ctb_Exec_Cube():New(cEntidade,::cMoeda,::cSaldo,::nEntidades,Len(aDataFim))
	oObjCubo:lZerado := lVlrZerado

	//Cria arquivo temporario no TOP retornando nome do arquivo
	cArqTemp :=  oObjCubo:CtbCriaTemp()
	aParCubo := self:sCtbCfCubo(cValtoChar(::nEntidades),::cNomeConf,,oFilter)

	For nY:=1 To ::nEntidades
		oObjCubo:Set_Level_Cube(nY)

		oObjCubo:oStructCube:Ctb_Set_IniParam(nY, aParCubo[nY][1]) //aqui colocado parametro inicial
		oObjCubo:oStructCube:Ctb_Set_FimParam(nY, aParCubo[nY][2]) //parametro final
		If nY >= 5
			//não filtrar as entidades 5 a 9 em branco
			oObjCubo:oStructCube:aVazio[nY] := .F.
		EndIf
		//Atualiza a propriedade aQueryDim
		oObjCubo:CtbCriaQueryDim()
	Next nY

	oObjCubo:Set_Level_Cube(::nEntidades)

	oObjCubo:Set_aSelFil(::aSelFil)

	//Monta query com a propriedade aQueryDim
	oObjCubo:CtbCriaQry(.T./*lMovimento*/, aDataIni/*aDtIni*/, aDataFim/*aDtFim*/, cArqTemp, .T./*lAllNiveis*/, .F./* lFechamento*/)

	//Popular tabela temporaria
	oObjCubo:CtbPopulaTemp(cArqTemp)

	cQryField := ""
	FOR nX := 1 TO ::nEntidades
		cQryField += "CVX_NIV0"+ AllTrim(Str(nX)) + ", "
	Next

	cQry += "Select CVX_FILIAL, "
	cQry += "       CVX_CONFIG, "
	cQry += "       CVX_MOEDA , "
	cQry += "       CVX_TPSALD, "
	cQry += cQryField
	cQry += "       CVX_PROC ,  "
	cQry += "       CVX_NIVEL,  "
	cQry += "       CVX_IDPAI,  "
	cQry += "       SUM( CVX_SLCR01 ) CVX_SLCR01, "
	cQry += "       SUM( CVX_SLDB01 ) CVX_SLDB01, "
	cQry += "       SUM( CVX_SALD01 ) CVX_SALD01, "
	cQry += "       SUM( CVX_SLCR02 ) CVX_SLCR02, "
	cQry += "       SUM( CVX_SLDB02 ) CVX_SLDB02, "
	cQry += "       SUM( CVX_SALD02 ) CVX_SALD02, "
	cQry += "       SUM( CVX_SALD02 + CVX_SALD01 ) CVX_SLDATU "
	cQry += " FROM "+cArqTemp
	cQry += " GROUP BY CVX_FILIAL, "
	cQry += "          CVX_CONFIG, "
	cQry += "          CVX_MOEDA , "
	cQry += "          CVX_TPSALD, "
	cQry += cQryField
	cQry += "          CVX_PROC , "
	cQry += "          CVX_NIVEL, "
	cQry += "          CVX_IDPAI  "

	If Select("cArqTemp") > 0
		dbSelectArea("cArqTemp")
		dbCloseArea()
	EndIf

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQry),"cArqTemp",.T.,.F.)

	TCSetField("cArqTemp","CVX_SLCR01", "N",aTam[1],aTam[2])
	TCSetField("cArqTemp","CVX_SLDB01", "N",aTam[1],aTam[2])
	TCSetField("cArqTemp","CVX_SALD01", "N",aTam[1],aTam[2])

	TCSetField("cArqTemp","CVX_SLCR02", "N",aTam[1],aTam[2])
	TCSetField("cArqTemp","CVX_SLDB02", "N",aTam[1],aTam[2])
	TCSetField("cArqTemp","CVX_SALD02", "N",aTam[1],aTam[2])
	TCSetField("cArqTemp","CVX_SLDATU", "N",aTam[1],aTam[2])

	DbSelectArea("cArqTemp")
	DbGoTop()

return cQry

//-------------------------------------------------------------------
/*/{Protheus.doc} loadEntity
    Carrega as entidades configurada no ambiente
    Será usado para alterar a Query antes de ser criada a temporária.
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method loadEntity(oFilter as object) as array class balancesheetbyentitiesSmartViewBusinessObject
	Local oQuery 		:= Nil as object
	Local nTam_CPOCHV 	:= 0  as numeric
	Local cQuery 		:= '' as character
	Local cAlias403 	:= '' as character

	Private aEntidades	:= {} as array
	Private aEstrCT0	:= {} as array
	Private aParCubo	:= {} as array

	cQuery := " SELECT CT0_ID, CT0_CPOCHV, CT0_DSCRES, CT0_F3ENTI "
	cQuery += " FROM "+RetSqlName("CT0")
	cQuery += " WHERE CT0_FILIAL = ? "
	cQuery += " AND CT0_CPOCHV != ? "
	cQuery += " AND D_E_L_E_T_ = ? "

	cQuery := ChangeQuery(cQuery)
	oQuery := FWPreparedStatement():New(cQuery)

	oQuery:SetString(1, FwXFilial("CT0"))
	oQuery:SetString(2, " ")
	oQuery:SetString(3, " ")

	cQuery := oQuery:GetFixQuery()
	cAlias403 := MPSysOpenQuery(ChangeQuery(cQuery))
	
	DbSelectArea(cAlias403)
	(cAlias403)->(DbGoTop())

	While (cAlias403)->(!Eof())
		nTam_CPOCHV := TamSx3( (cAlias403)->CT0_CPOCHV)[1]
		AADD( aEstrCT0,{ (cAlias403)->CT0_ID , nTam_CPOCHV , (cAlias403)->CT0_DSCRES , (cAlias403)->CT0_F3ENTI } )
		AADD( ::aEntidades , Space( nTam_CPOCHV ) )	// Parametro entidade inicio
		AADD( ::aEntidades , Space( nTam_CPOCHV ) )	// Parametro entidade fim

		(cAlias403)->(DbSkip())
	EndDo

	(cAlias403)->(dbCloseArea())

	DbSelectArea('CT0')
	DbSetOrder(1)
	CtbParCubo(.T.,::cNomeConf)

return ::aEntidades

//-------------------------------------------------------------------
/*/{Protheus.doc} sCtbCfCubo
    Copia da função CtbCfCubo
    Função que determina o range das entidades a serem filtradas na query
    @author wilton.santos
    @since 26/07/2023
    @version 12.1.2210
/*/
//-------------------------------------------------------------------
method sCtbCfCubo(cCodSel as character ,cArquivo as character,lWhenConta as logical, oFilter as object) as array class balancesheetbyentitiesSmartViewBusinessObject

	Local nX,nY		:= 0 as numeric
	Local nEscolha	:= iif(!empty(MV_PAR10),Val(MV_PAR10),1) as numeric
	Local aEntidRet	:= {} as array

	Default cArquivo := "CTBPARCUBO"
	Default lWhenConta := .t.

	//até tratar entidades adicionais
	nEscolha := iif(!empty(MV_PAR10),Val(MV_PAR10),1)

	nY := 1
	For nX := 1 TO nEscolha
		::aEntidades[nY]    := ""
		::aEntidades[nY+1]  := "zzzzz"
		AADD(aEntidRet,{ ::aEntidades[nY], ::aEntidades[nY+1] })
		nY:= nY+2
	Next nX

Return aEntidRet
