#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetmod3.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT1, CT2, CT7", NAME="Balanço Modelo 3", country="ALL", initialRelease="12.1.2210")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceSheetItemxCostCenterSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete de Itens x Centro de Custo
para o TReports
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
class BalanceSheetMod3SmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method processCustomData()       as json

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object 

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object    

    public method ExecDBSkipTemp()          as logical

    private method somaTotais()             as Numeric 
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceSheetMod3SmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) //"STR0001" "Balanço Modelo 3"
    self:setPergunte("CTR060") //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Gustavo Campos
@since 26/09/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceSheetMod3SmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balanço Modelo 3"

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preData() as object class BalanceSheetMod3SmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class BalanceSheetMod3SmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class BalanceSheetMod3SmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceSheetMod3SmartViewBusinessObject

    self:oSchema:addProperty("NomeEmpresa", STR0003, "string", STR0003, "NomeEmpresa",,,,.F.)  //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0004, "string", STR0004, "NomeFilial" ,,,,.F.)  //"Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH", STR0005, "string", .T.) //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2)  

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetMod3SmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR060")//Carrega lista de parametros para a memória   
    self:handleCtgGerPlanParams( oFilter )
    FreeObj(oFilter:getParameters())          
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetMod3SmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetMod3SmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceSheetMod3SmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    Local cRecDesp          as character
    Local cMoedaDsc         as character
    Local nDivide := 1      as numeric
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    Local lQuebrNat         as Logical
    Local lRecDesp0         as logical   
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date
    Local dDtZeraRD         as Date
    
    ///Parametros do relatorio
    dDataIni    := MV_PAR01                                              // Data Inicial ?                	
    dDataFim    := MV_PAR02                                              // Data Final ?                  	
    cContaIni   := MV_PAR03                                              // Conta Inicial ?               	
    cContaFim   := MV_PAR04                                              // Conta Final ?                 	
    lImpSint    := iIf(MV_PAR05==1 .Or. MV_PAR05==3,.T.,.F.)             // Imprime Contas ?              	
    aSetOfBook  := iIf(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf("")) // Cod. Config. Livros ?
    lSldZerado  := iIf(MV_PAR07==1,.T.,.F.)                              // Saldos Zerados ?
    cMoeda      := iIf(!Empty(MV_PAR08),MV_PAR08,cMoeda)                 // Moeda ?
    cTpsald     := Iif(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo ?
    lQuebrNat   := MV_PAR12                                              // Quebra por Natureza ?         	       
    cFilSeg     := MV_PAR13                                              // Filtra Segmento No. ?
    cConteudIni := MV_PAR14                                              // Conteudo Ini Segmen ?
    cConteudFim := MV_PAR15                                              // Conteudo Fim Segmen ?
    cConteudCont:= MV_PAR16                                              // Conteudo Contido em ?
    lPrintZero  := Iif(MV_PAR19==1,.T.,.F.)                              // Imprime Valor 0.00  ?
    lImpCod     := Iif(MV_PAR20==1,.T.,.F.)                              // Imprime Codigo ?
    nDivide     := Iif(!Empty(MV_PAR21), MV_PAR21, nDivide)              // Divide por ?
    
    //utilizado na seção do antigo R4 mv_par22                            // Imprimir ate o seg. ?
    lImpAntLP   := Iif(MV_PAR23 == 1,.T.,.F.)                             // Posicao Ant. L/P ?
    dDataLP     := MV_PAR24                                               // Data Lucros/Perdas ?
    lRecDesp0	:= Iif(MV_PAR26 == 1,.T.,.F.)	                          // Rec./Desp. Anterior Zeradas?  
    cRecDesp	:= MV_PAR27						                          // Grupo Receitas/Despesas?
    dDtZeraRD	:= MV_PAR28						                          // Data de Zeramento Receita/Despesas?
    cMoedaDsc	:= MV_PAR29

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT7"    //são obrigatórios
    self:cIdent         := ""       //são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := .F.
    self:lImpConta      := .F.
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP
    self:lRecDesp0      := lRecDesp0
    self:cRecDesp       := cRecDesp
    self:dDtZeraRD      := dDtZeraRD
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:aSelFil        := aSelFil
    self:lTodasFil      := .F.
    self:lImpSint       := lImpSint
    self:cMoedaDsc	    := cMoedaDsc
    
return 

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Tratativa de dados customizados no data
 
@return object: string
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method processCustomData() as json class BalanceSheetMod3SmartViewBusinessObject

    Local nTotDeb   := 0 as numeric
    Local nTotCred  := 0 as numeric
    local cPicture  as character
    local TAM_VALOR := 17 as numeric
    local nDecimais as numeric    
    Local nTotMov as numeric
    
    nTotDeb  := ::somaTotais("D")
    nTotCred := ::somaTotais("C")

    nTotMov := nTotCred - nTotDeb

    ::jDados["SALDODEB"   ] := nTotDeb
    ::jDados["SALDODEB_M" ] := ValorCTB(nTotDeb,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)
    ::jDados["SALDOCRD"   ] := nTotCred
    ::jDados["SALDOCRD_M" ] := ValorCTB(nTotCred,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)    

    ::jDados["MOVIMENTO"]   := nTotMov
    ::jDados["MOVIMENTO_M"] := ValorCTB(nTotMov,,,TAM_VALOR-2,nDecimais,.T.,cPicture,cArqTmp->NORMAL,,,,,,::lPrintZero,.F.)    

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} somaTotais
Soma totais do lançamento
 
@return numérico
 
@author Renan Gremes
@since 18/12/2024
@version 1.0
*/
//------------------------------------------------------------------- 
method somaTotais(cTipo as character) as numeric class BalanceSheetMod3SmartViewBusinessObject

    Local   nRet  := 0 as numeric
    Default cTipo := "" 
    
    If mv_par05 == 1 // So imprime Sinteticas - Soma Sinteticas
        If cArqTmp->TIPOCONTA == "1"
            If cArqTmp->NIVEL1
                If cTipo == "D"
                    nRet += cArqTmp->SALDODEB
                ElseIf cTipo == "C"    
                    nRet += cArqTmp->SALDOCRD
                EndIf
            EndIf
        EndIf
    Else			// Soma Analiticas
        If cArqTmp->TIPOCONTA == "2"
            If cTipo == "D"
                nRet += cArqTmp->SALDODEB
            ElseIf cTipo == "C"    
                nRet += cArqTmp->SALDOCRD
            EndIf
        EndIf	
    EndIf    

Return nRet

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para pular linha
 
@param cArqTmp, caracter, indica o alias do arquivo temporário
 
@return lógico
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp() as logical class BalanceSheetMod3SmartViewBusinessObject
    
    Local lRet := .T. as logical

    If MV_PAR05 == 1 // So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
			lRet := .F.
		EndIf
	EndIf

Return lRet
