#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.IncomeStatement.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CLN,CT2", name="Demonstrativo de Renda", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} IncomeStatementSmartViewBusinessObject
backoffice.ctb.razaoanaliticoativo.businessobject.tlpp	    Demonstrativo de Renda / IncomeStatement
Fonte/Perg      CTBR540	/ CTR540
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
class IncomeStatementSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character

    //Associa campos a função CTGerPlan.
    public method preSchema()               as object
    public method preData()                 as object
    public method processData()             as json

    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    //usados na internacionalizacao
    public method handleCtgGerPlanParams() 
    public method handleExeCtgGerPlan()     as object
    public method handleCtgGerPlanAppend()  as object

    //Valida se executa a função ctgerplan
    public method vldExeCtgGerPlan()
    public method processCustomData()       as json

    //atributos para calculo de porcetagem
    protected data lTotalArqTemp            as logical
    protected data aTotalArqTemp            as array
    protected data nTotMes                  as numeric
    protected data nTotAtu                  as numeric 
    protected data dPeriodo0                as date

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
@return object: self
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method new() as object class IncomeStatementSmartViewBusinessObject
    _Super:new()    
    //Define o nome do Objeto de Negócio
    ::setDisplayName( STR0001 )  // "Demonstrativo de Renda"
    ::setPergunte("CTR540")    // Indica o pergunte que será utilizado no relatório

    ::lTotalArqTemp  := .F.
    ::aTotalArqTemp  := {}
    ::nTotMes        := 0
    ::nTotAtu        := 0
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class IncomeStatementSmartViewBusinessObject
return STR0002 //"Objeto contendo informacoes sobre os Demonstrativo de Renda"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
@return array: Array com a estrutura dos campos
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getCtGerPlanSchema() as object class IncomeStatementSmartViewBusinessObject

	Local lCanFilter    := .F.      as logical

	//Adição de novos campos ao schema
    self:oSchema:addProperty("NPERC01"    , STR0004 , "number" , STR0004 , "NPERC01"    ,,,,lCanFilter)
    self:oSchema:addProperty("NPERC02"    , STR0005 , "number" , STR0005 , "NPERC02"    ,,,,lCanFilter)
    self:oSchema:addProperty("NomeEmpresa", STR0006 , "string" , STR0006 , "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0007 , "string" , STR0007 , "NomeFilial" ,,,,lCanFilter) // "Nome Filial"

return ::oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class IncomeStatementSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(),self:cPergunte)
    ::handleCtgGerPlanParams( oFilter )
return ::oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class IncomeStatementSmartViewBusinessObject
return ::oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append
@param oFilter, objeto, contém o filtro do TReports
@return object: ::oData
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class IncomeStatementSmartViewBusinessObject
return ::oData

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Metodo que será consumido pelo M.I.

@return object: self:oData

@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class IncomeStatementSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 09/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method preSchema() as object class IncomeStatementSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Metodo que será consumido pelo M.I.
 
@return object: self:oData
 
@author Renan Gremes
@since 09/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method processData() as json class IncomeStatementSmartViewBusinessObject    
    //Execução antes do append
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} processCustomData
Deverá ser chamado no momento do processamento da query
@return object: self:oData
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processCustomData() as json class IncomeStatementSmartViewBusinessObject

    Local aTotal 		:= {}           as array
    Local nTotal		:= 0            as numeric
    Local nTotAtu		:= 0            as numeric

    If ! ::lTotalArqTemp 
        nRecnoTMP   := cArqTmp->(Recno())
        cArqTmp->(dbGoTop())
        While !cArqTmp->(Eof())
            If ALLTRIM(cArqTmp->IDENTIFI) == "4"
                nTotal := aScan(aTotal, { |x| x[1] = cArqTmp->CONTA })
                If nTotal = 0
                    aAdd(aTotal, { cArqTmp->CONTA, 0, 0 })
                    nTotal := Len(aTotal)
                Endif
                aTotal[nTotal][2] += cArqTmp->SALDOPER
                aTotal[nTotal][3] += cArqTmp->(SALDOATU - SALDOANT)
            Endif
            cArqTmp->(DbSkip())
        EndDo

        ::lTotalArqTemp  := .T.
        ::aTotalArqTemp  := aTotal

        If Len(aTotal) = 0
            ::aTotalArqTemp  := { {"", 0, 0 }}
        Endif

        dbSelectArea("cArqTmp")
        cArqTmp->(dbGoTo(nRecnoTMP))
    EndIf
    
    nTotal := aScan(::aTotalArqTemp, { |x| x[1] = cArqTmp->SUPERIOR })

    If MV_PAR10 == 1	//Se considerar o % do total em relacao a conta de nivel 1
        If Empty(cArqTmp->SUPERIOR)			
            ::nTotMes := cArqTmp->SALDOPER
            ::nTotAtu := cArqTmp->(SALDOATU - SALDOANT)
        EndIf					
    Else	
        If Empty(cArqTmp->SUPERIOR) .Or. cArqTmp->IDENTIFI = "4"
            ::nTotMes := cArqTmp->SALDOPER
            ::nTotAtu := cArqTmp->(SALDOATU - SALDOANT)
        ElseIf nTotal = 0
            ::nTotMes := nTotAtu := 0
        Endif
    EndIF

    ::jDados["NPERC01"] := Round((cArqTmp->SALDOPER / ::nTotMes ) * 100 , 2)
    ::jDados["NPERC02"] := Round((cArqTmp->(SALDOATU - SALDOANT) / ::nTotAtu ) * 100 ,2)
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados
@param oFilter, objeto, contém o filtro do TReports
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class IncomeStatementSmartViewBusinessObject

    Local cContaIni                 as character
    Local cContaFim                 as character
    Local cMoeda                    as character
    Local cTpsald                   as character
    Local dDataIni                  as date
    Local dDataFim                  as date
    Local dInicio                   as date
    Local dFinal                    as date
    Local dPeriodo0                 as date
    Local aSetOfBook                as array
    Local lVlrZerado                as logical

    self:lPeriodo0 := .T. //Passa na validação do dPeriodo0

    //Combos para escolha ainda sem efeito para o Smart View
    cMoeda      := IIf(!Empty(MV_PAR03),MV_PAR03,"01")

    //Define datas e períodos
    If MV_PAR04 == 2 
        dInicio  	:= MV_PAR05
        dFinal		:= MV_PAR06
        dPeriodo0	:= CtbPeriodos(MV_PAR03,dInicio,dFinal,.F.,.F.)[1][2]
    Else
        dInicio  	:= CToD("01/" + Subs(DToC(MV_PAR01), 4))
        dFinal		:= MV_PAR01
        dPeriodo0 	:= CToD(Str(Day(LastDay(MV_PAR01)),2) +"/"+ Subs(DToC(MV_PAR01),4))
    EndIf

    //Validação do livro
    cCodCfgLiv  := MV_PAR02

    //Atribui parâmetros nas variáveis
    dDataIni          := dInicio
    dDataFim          := dFinal
    cAlias            := ""
    cIdent            := ""
    cContaIni         := Replicate(" ",TamSX3("CT1_CONTA")[1])
    cContaFim         := Replicate("Z",TamSX3("CT1_CONTA")[1])
    cCCIni            := Replicate(" ",TamSX3("CTT_CUSTO")[1])
    cCCFim            := Replicate("Z",TamSX3("CTT_CUSTO")[1])
    cItemIni          := Replicate(" ",TamSX3("CTD_ITEM")[1])
    cItemFim          := Replicate("Z",TamSX3("CTD_ITEM")[1])
    cClvlIni          := Replicate(" ",TamSX3("CTH_CLVL")[1])
    cClVlFim          := Replicate("Z",TamSX3("CTH_CLVL")[1])
    cTpsald           := IIf(!Empty(MV_PAR11),MV_PAR11,"1")
    aSetOfBook        := IIf(!Empty(cCodCfgLiv),CTBSetOf(cCodCfgLiv),CTBSetOf(""))
    cSegIni           := Replicate(" ",20)
    cSegFim           := Replicate("Z",20)
    cFiltSegm         := Replicate(" ",30)
    lVlrZerado        := .T.

    //Atribuir variáveis nos atributos da função CTGerPlan do CTBR540.
    ::dDataIni          := dInicio
    ::dDataFim          := dFinal
    ::cAlias            := cAlias
    ::cIdent            := cIdent
    ::cContaIni         := cContaIni
    ::cContaFim         := cContaFim
    ::cCCIni            := cCCIni
    ::cCCFim            := cCCFim
    ::cItemIni          := cItemIni
    ::cItemFim          := cItemFim
    ::cClvlIni          := cClvlIni
    ::cClVlFim          := cClVlFim
    ::aSetOfBook        := aSetOfBook
    ::cMoeda            := cMoeda
    ::cSaldos           := cTpsald
    ::cSegIni           := cSegIni
    ::cSegFim           := cSegFim
    ::lVlrZerado        := lVlrZerado
    ::dPeriodo0         := dPeriodo0

return 
//-------------------------------------------------------------------
/*{Protheus.doc} vldExeCtgGerPlan
Define para os atributos conforme parâmetros informados
@param oFilter, objeto, contém o filtro do TReports
@author         Renato July
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method vldExeCtgGerPlan() class IncomeStatementSmartViewBusinessObject
	Local lRet := .T.

	If Empty(MV_PAR02)
		lRet:=.F.
	ElseIf !VdSetOfBook(MV_PAR02,.F.) .Or. Empty(::aSetOfBook[5])
		lRet := .F.
	Endif
return lRet
