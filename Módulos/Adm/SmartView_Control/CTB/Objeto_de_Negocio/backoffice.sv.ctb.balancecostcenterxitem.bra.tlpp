#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancecostcenterxitem.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT2,CTT,CTD", name="Balancete Centro de Custo x Item", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BalanceCostCenterxItemSmartViewBusinessObject
Classe paara a criação do Objeto de Negócio do Balancete Centro de Custo x Item
para o TReports
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

class BalanceCostCenterxItemSmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object

    public method preschema()               as object
    public method preData()                 as object
    public method processData()             as json    
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object
    
endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method new() as object class BalanceCostCenterxItemSmartViewBusinessObject
    _Super:new()

    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // "Balancete Centro de Custo x Item"
    self:setPergunte("CTR130") //Indica o pergunte que será utilizado no relatório
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Gustavo Campos
@since 26/09/2023
@version 12.1.2310
*/
//------------------------------------------------------------------- 
method getDescription() as character class BalanceCostCenterxItemSmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balancete Centro de Custo x Item"

//-------------------------------------------------------------------
/*{Protheus.doc} getCtGerPlanSchema
Prepara a estrutura dos campos
 
@return array: Array com a estrutura dos campos
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtGerPlanSchema() as object class BalanceCostCenterxItemSmartViewBusinessObject

    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0003 , "string", .T.)  //"Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2) 
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getCtgGerPlanData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceCostCenterxItemSmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "CTR130")    
    self:handleCtgGerPlanParams( oFilter )

    FreeObj(oFilter:getParameters())
    
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleExeCtgGerPlan
Método para execução do CTGERPLAN

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleExeCtgGerPlan(oFilter as object) as object class BalanceCostCenterxItemSmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanAppend
Método para execução do append

@param oFilter, objeto, contém o filtro do TReports
 
@return object: self:oData
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanAppend(oFilter as object) as object class BalanceCostCenterxItemSmartViewBusinessObject
//execução do Append
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} handleCtgGerPlanParams
Define para os atributos conforme parâmetros informados

@param oFilter, objeto, contém o filtro do TReports
 
@author Gustavo Campos
@since 26/09/2023
@version 1.0
*/
//-------------------------------------------------------------------
method handleCtgGerPlanParams( oFilter ) class BalanceCostCenterxItemSmartViewBusinessObject
   
    local cContaIni         as Character
    local cContaFim         as Character
    local cCCIni            as Character
    local cCCFim            as Character
    local cItemIni          as Character
    local cItemFim          as Character
    local cMoeda  :=  "01"  as Character
    local cTpsald :=  "1"   as Character
    Local nDivide := 1      as numeric
    Local nGrupo  := 2      as numeric
    local lImpAntLP         as logical
    Local lImpSint          as Logical 
    Local lImpMov           as Logical 
    Local lPrintZero        as Logical
    Local lSldZerado        as Logical
    Local lImpConta := .F.  as Logical
    
    local aSetOfBook        as array
    local aSelFil           as array

    local dDataIni          as Date
    local dDataFim          as Date
    local dDataLP           as Date
    
    cContaIni	:= Space(Len(CriaVar("CT1_CONTA")))
    cContaFim	:= Repl('Z',Len(CriaVar("CT1_CONTA")))
    dDataIni    := MV_PAR01                                             // Data Inicial ?
    dDataFim    := MV_PAR02                                             // Data Final ?
    cCCIni      := MV_PAR03                                             // Do Centro de Custo ?         
    cCCFim      := MV_PAR04                                             // Ate Centro de Custo ?        
    cItemIni    := MV_PAR05                                             // Do Item Contabil ?           
    cItemFim    := MV_PAR06                                             // Ate Item Contabil ?          
    lImpSint    := Iif(MV_PAR07 == 1 .Or. MV_PAR07 == 3, .T., .F.)      // Imprime Itens ?
    aSetOfBook  := iIf(!Empty(MV_PAR08),CTBSetOf(MV_PAR08),CTBSetOf(""))// Cod. Config. Livros ?
    lSldZerado  := iIf(MV_PAR09==1,.T.,.F.)                             // Saldos Zerados ?
    cMoeda      := iIf(!empty(MV_PAR10),MV_PAR10,cMoeda)                // Moeda ?
    cTpsald     := iif(!Empty(MV_PAR12),PadR(AllTrim(MV_PAR12), TamSX3("CT2_TPSALD")[1]),cTpsald)               // Tipo de Saldo ?        
    cFilSeg     := MV_PAR14                                             // Filtra Segmento No. ?       
    cConteudIni := MV_PAR15                                             // Conteudo Ini Segmen ?       
    cConteudFim := MV_PAR16                                             // Conteudo Fim Segmen ?       
    cConteudCont:= MV_PAR17                                             // Conteudo Contido em ?       
    lImpMov     := Iif(MV_PAR18==1,.T.,.F.)                             // Imprime Coluna Mov. ?
    lPrintZero  := Iif(MV_PAR23==1,.T.,.F.)                             // Imprime Valor 0.00 ?          	MV_PAR23       
    nDivide     := IIf(!Empty(MV_PAR24),MV_PAR24,nDivide)               // Divide por ?
	lImpAntLP   := Iif(MV_PAR25 == 1,.T.,.F.)                           // Posicao Ant. L/P ?
    dDataLP     := MV_PAR26                                             // Data Lucros/Perdas ?

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CTV"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cCCIni         := cCCIni  
    self:cCCFim         := cCCFim  
    self:cItemIni       := cItemIni
    self:cItemFim       := cItemFim 
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := !lImpMov
    self:lImpConta      := lImpConta
    self:nGrupo         := nGrupo
    self:cHeader        := 'CTT'
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:aSelFil        := aSelFil
    self:lTodasFil      := .F.
    self:lImpSint       := lImpSint
    self:cFilUSU        := ""
    
return

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preSchema() as object class BalanceCostCenterxItemSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method preData() as object class BalanceCostCenterxItemSmartViewBusinessObject 
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.

    @author loex
    @since 11/13/2024
    @version 12.1.2210
/*/
//-------------------------------------------------------------------

method processData() as json class BalanceCostCenterxItemSmartViewBusinessObject
return ::jDados
