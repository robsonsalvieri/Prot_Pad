#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.balancesheetmod4.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T.,team="SIGACTB",tables="CT2", name="Balancete Modelo 4", country="ALL")

class BalanceSheetMod4SmartViewBusinessObject from totvs.protheus.ctb.smartviewintegratedprovider.SmartviewCtbFunctions
   
    public method new()                     as object
    public method getDescription()          as character
    public method getCtgGerPlanData()       as object
    public method getCtGerPlanSchema()      as object
    
    public method handleCtgGerPlanParams() 
    public method handleCtgGerPlanAppend()  as object
    public method handleExeCtgGerPlan()     as object

    public method processData()             as json
    public method preSchema()               as object
    public method preData()                 as object

    public method processCustomData()       as json
    public method ExecDBSkipTemp()          as logical
    
endclass

method new() as object class BalanceSheetMod4SmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0001) // "Balancete Modelo 4"
    self:setPergunte("CTR040") //Indica o pergunte que será utilizado no relatório
return self

method getDescription() as character class BalanceSheetMod4SmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre o Balancete Modelo 4"

method getCtGerPlanSchema() as object class BalanceSheetMod4SmartViewBusinessObject
    self:oSchema:addProperty("NomeEmpresa"    ,STR0003 , "string", STR0003 , "NomeEmpresa",,,,.F.) //STR0003 - "Nome Empresa"
    self:oSchema:addProperty("NomeFilial"     ,STR0004 , "string", STR0004 , "NomeFilial" ,,,,.F.) //STR0004 - "Nome Filial"

    self:oSchema:addParameter("SV_MULTBRANCH" ,STR0005 , "string", .T.                     ) //STR0005 - "Filiais"
    self:setCustomURL("SV_MULTBRANCH",  "/api/controladoria/smartview/v1/options/getBranches",2) 
return self:oSchema

method getCtgGerPlanData(nPage as numeric, oFilter as object) as object class BalanceSheetMod4SmartViewBusinessObject
    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(),self:cPergunte)
    self:handleCtgGerPlanParams( oFilter )
    FreeObj(oFilter:getParameters())      
return self:oData

method handleExeCtgGerPlan(oFilter as object) as object class BalanceSheetMod4SmartViewBusinessObject
//execução da CTGERPLAN
return self:oData

method handleCtgGerPlanAppend(oFilter as object) as object class BalanceSheetMod4SmartViewBusinessObject
//execução do Append
return self:oData

method handleCtgGerPlanParams( oFilter ) class BalanceSheetMod4SmartViewBusinessObject
   
    local cContaIni         as character
    local cContaFim         as character
    local cMoeda  :=  "01"  as character
    local cTpsald :=  "1"   as character
    Local nDivide := 1      as numeric
    local lImpAntLP         as logical
    Local lImpSint          as logical 
    Local lPrintZero        as logical
    Local lSldZerado        as logical
    local aSetOfBook        as array
    local aSelFil           as array
    local dDataIni          as date
    local dDataFim          as date
    local dDataLP           as date
    local cFilSeg           as character
    local cConteudIni       as character
    local cConteudFim       as character
    local cConteudCont      as character
    local lRecDesp0         as logical
    local cRecDesp          as character
    local dDtZeraRD         as date
    local cDescMoeda :="01" as character
    
    dDataIni    := MV_PAR01                                                 //     	Data Inicial ?                
    dDataFim    := MV_PAR02                                                 //     	Data Final ?                  
    cContaIni	:= MV_PAR03                                                 //     	Conta Inicial ?               
    cContaFim	:= MV_PAR04                                                 //     	Conta Final ?                 
    lImpSint    := Iif(MV_PAR05==1 .Or. MV_PAR05 ==3,.T.,.F.)               //     	Imprime Contas ?              
    aSetOfBook  := iIf(!Empty(MV_PAR06),CTBSetOf(MV_PAR06),CTBSetOf(""))    //     	Cod. Config. Livros ?         
    lSldZerado  := Iif(MV_PAR07==1,.T.,.F.)                                 //  	Saldos Zerados ?              
    cMoeda      := iIf(!empty(MV_PAR08),MV_PAR08,cMoeda)                    //     	Moeda ?                                    
    cTpsald     := IIf(!Empty(MV_PAR10), PadR(AllTrim(MV_PAR10), TamSX3("CT2_TPSALD")[1]), cTpsald) // Tipo de Saldo ?
    cFilSeg     := MV_PAR12                                                 //    	Filtra Segmento No. ?         
    cConteudIni := MV_PAR13                                                 //    	Conteudo Ini Segmen ?         
    cConteudFim := MV_PAR14                                                 //    	Conteudo Fim Segmen ?         
    cConteudCont:= MV_PAR15                                                 //    	Conteudo Contido em ?
    lPrintZero  := Iif(MV_PAR18==1,.T.,.F.)                                 //    	Imprime Valor 0.00 ?

    If MV_PAR20 == 1//não se aplica                                         //      Divide por ?                                 
	    nDivide := 1
    ElseIf MV_PAR20 == 2// Divide por cem
		nDivide := 100
	ElseIf MV_PAR20 == 3// Divide por mil
		nDivide := 1000
	ElseIf MV_PAR20 == 4// Divide por milhao
		nDivide := 1000000
	EndIf
      
    lImpAntLP   := Iif(MV_PAR22 == 1,.T.,.F.)                               //     	Posicao Ant. L/P ?            
    dDataLP     := MV_PAR23                                                 //   	Data Lucros/Perdas ?
    lRecDesp0   := Iif(MV_PAR25==1,.T.,.F.)                                 //     	Ignora Sl Ant.Rec/Des ?       
    cRecDesp    := MV_PAR26                                                 //     	Grupos Receitas/Despesas ?    
    dDtZeraRD   := MV_PAR27                                                 //     	Data Sld Ant. Receitas/Desp. ?
    cDescMoeda  := iIf(!empty(MV_PAR29),MV_PAR29,cDescMoeda)                //     	Descricao na Moeda ?          

    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(self:jParams)
    ::oIntegratedProvider:loadFilterBranches({"CT2"})

    If Valtype(::oIntegratedProvider:oFilterBranches["CT2"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["CT2"]
    EndIf
    
    //Setando os atributos
    self:dDataIni       := dDataIni
    self:dDataFim       := dDataFim
    self:cAlias         := "CT7"//são obrigatórios
    self:cIdent         := ""//são obrigatórios   
    self:cContaIni      := cContaIni
    self:cContaFim      := cContaFim
    self:cMoeda         := cMoeda
    self:cSaldos        := cTpsald
    self:aSetOfBook     := aSetOfBook
    self:cSegmento      := cFilSeg     
    self:cSegIni        := cConteudIni 
    self:cSegFim        := cConteudFim 
    self:cFiltSegm      := cConteudCont
    self:lNImpMov       := .F.
    self:lImpConta      := .F.
    self:lImpAntLP      := lImpAntLP     
    self:dDataLP        := dDataLP  
    self:nDivide        := nDivide
    self:lVlrZerado     := lSldZerado
    self:lRecDesp0      := lRecDesp0
    self:cRecDesp       := cRecDesp 
    self:dDtZeraRD      := dDtZeraRD
    self:cMoedaDsc      := cDescMoeda
    self:aSelFil        := aSelFil
    self:lImpSint       := lImpSint
    
return 

//-------------------------------------------------------------------
//https://tdn.totvs.com.br/display/tec/ProtheusDOC
/*{Protheus.doc} backoffice.sv.ctb.balancesheet.bra
Método para appendar dados no json
@author  loex
@since   11/12/2024
@version version
*/
//-------------------------------------------------------------------
method processCustomData() as json class BalanceSheetMod4SmartViewBusinessObject

    //Dados empresa e filial logada
    ::jDados["NomeEmpresa"] := self:cCompanyName
    ::jDados["NomeFilial" ] := self:cBranchName

return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} ExecDBSkipTemp
Método para validar se pula linha no append
 
@return logico: lRet
 
@author Renan Gremes
@since 20/12/2024
@version 1.0
*/
//-------------------------------------------------------------------
method ExecDBSkipTemp(cArqTmp as character) as logical class BalanceSheetMod4SmartViewBusinessObject
   
    Local lRet := .T. as logical
    
    If MV_PAR05 == 1					// So imprime Sinteticas
		If cArqTmp->TIPOCONTA == "2"
			lRet := .F.
		EndIf
	EndIf

Return lRet

/*/{Protheus.doc} BalanceSheetMod4SmartViewBusinessObject::preSchema
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preSchema() as object class BalanceSheetMod4SmartViewBusinessObject 
return self:oSchema

/*/{Protheus.doc} BalanceSheetMod4SmartViewBusinessObject::preData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method preData() as object class BalanceSheetMod4SmartViewBusinessObject 
return self:oData

/*/{Protheus.doc} BalanceSheetMod4SmartViewBusinessObject::processData
Metodo que será consumido pelo M.I.
@type method
@version 1.0  
@author loex
@since 11/13/2024
/*/

method processData() as json class BalanceSheetMod4SmartViewBusinessObject
return ::jDados
