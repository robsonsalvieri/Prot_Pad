#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.ctb.listofmanagementvision.bra.ch"

namespace totvs.protheus.ctb.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGACTB", tables="CTS", name="Visão Gerencial", country="ALL", customTables="CTS")

//-------------------------------------------------------------------------------
/*{Protheus.doc} backoffice.sv.ctb.listofmanagementvision.bra
Classe para criação do Objeto de Negócio de contendo informações Lancamento Classificado por Sub Lote
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class listofmanagementvisionSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

	public method new()            as object
	public method getDescription() as character
	public method getAreas()       as array

	//usados na internacionalização
    public method preData()        as object
    public method preSchema()      as object
    public method processData()    as json
	
	public method getData()        as object
	public method getSchema()      as object

	protected data oQuery1 			as object
	protected data cAlias           as character
	protected data jItens  			as json
	protected data aAllFields    	as array
	protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe 
new
@return object: self
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method new() as object Class listofmanagementvisionSmartViewBusinessObject
	_Super:new()
	self:setDisplayName(STR0001) //"Listagem da Visão Gerencial"
	self:setPergunte("CTR065") //Indica o pergunte que será utilizado no relatório

	self:aAllFields := {}
	self:cAlias := ""
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
Method getDescription() as character class listofmanagementvisionSmartViewBusinessObject
return STR0002 //"Objeto contendo informações da Listagem da Visão Gerencial"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class listofmanagementvisionSmartViewBusinessObject
Return {STR0003} //"Contabilidade"

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author Ednilson Queiroz
    @since 24/10/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class listofmanagementvisionSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author Ednilson Queiroz
    @since 24/10/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class listofmanagementvisionSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author Ednilson Queiroz
    @since 24/10/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------
method processData() as json class listofmanagementvisionSmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@return object: self:oData
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class listofmanagementvisionSmartViewBusinessObject
	local cQuery 		as character
	local cCompanyName 	as character
	local cBranchName  	as character
	local nX 		  := 1 as numeric
	local nParamOrder := 1 as numeric

	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "CTR065")

	::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

	cQuery := "SELECT "+ self:getSQLFields(.F., , .F.)
	cQuery += " FROM " + RetSQLName("CTS") 
	cQuery += " WHERE CTS_FILIAL = ? "
	cQuery += " AND CTS_CODPLA >= ? " 
	cQuery += " AND CTS_CODPLA <= ? "
	cQuery += " AND CTS_ORDEM  >= ? "
	cQuery += " AND CTS_ORDEM  <= ? "
	cQuery += " AND CTS_CONTAG >= ? "
	cQuery += " AND CTS_CONTAG <= ? "
	If MV_PAR07 == 1 .OR. MV_PAR07 == 2
		cQuery += " AND CTS_CLASSE = ? "
	EndIf

	//Os filtros serão setados na interface do novo TReports
	if(oFilter:hasFilter(),cQuery += " AND " + oFilter:getSQLExpression(),)

	cQuery += "AND D_E_L_E_T_ = ?"
	cQuery += "ORDER BY CTS_CODPLA"

	cQuery := ChangeQuery(cQuery)
	self:oQuery1:= FWPreparedStatement():New(cQuery)

	self:oQuery1:SetString(nParamOrder++, FwXFilial("CTS")) //Filial
	self:oQuery1:SetString(nParamOrder++, MV_PAR01) //Codigo Visao Inicial ?
	self:oQuery1:SetString(nParamOrder++, MV_PAR02) //Codigo Visao Final ?          
	self:oQuery1:SetString(nParamOrder++, MV_PAR03) //Ordem De ?                 
	self:oQuery1:SetString(nParamOrder++, MV_PAR04) //Ordem Ate ? 
	self:oQuery1:SetString(nParamOrder++, MV_PAR05) //Entidade De ?  
	self:oQuery1:SetString(nParamOrder++, MV_PAR06) //Entidade Ate ?

	If MV_PAR07 == 1 .OR. MV_PAR07 == 2
		self:oQuery1:SetString(nParamOrder++, Alltrim(STR(MV_PAR07))) //Imprime ? 
	Endif

	self:oQuery1:SetString(nParamOrder++, " ") //Delete

	cQuery := self:oQuery1:GetFixQuery()
	self:cAlias := MPSysOpenQuery(cQuery)

	While !(self:cAlias)->(Eof())        
        ::jItens := JsonObject():new()

        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                ::jItens[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                ::jItens[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                ::jItens[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
		Next nX

		::jItens["NomeEmpresa"] := cCompanyName
        ::jItens["NomeFilial" ] := cBranchName

		self:processData()
		self:oData:appendData(::jItens)

        (self:cAlias)->(DbSkip())
	EndDo

	(self:cAlias)->(DBCloseArea())

	FreeObj(oFilter:getParameters())
	FreeObj(::oIntegratedProvider)
	FreeObj(::jItens)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author Ednilson Queiroz de Castro
@since 24/10/2023
@version 1.0
*/
//-------------------------------------------------------------------
method getSchema() as object class listofmanagementvisionSmartViewBusinessObject
	Local aFieldsCTS := {}  as array
	local nCount as numeric
	local cEntidade as character
	local cEntIni as character
	local cEntFim as character

	aFieldsCTS := {"CTS_FILIAL","CTS_CODPLA","CTS_ORDEM","CTS_LINHA","CTS_CONTAG","CTS_CTASUP","CTS_DESCCG","CTS_NORMAL",;
				   "CTS_CLASSE","CTS_CT1INI","CTS_CT1FIM","CTS_CTTINI","CTS_CTTFIM","CTS_IDENT","CTS_CTDINI","CTS_CTDFIM",;
				   "CTS_CTHINI","CTS_CTHFIM","CTS_TPSALD","CTS_FORMUL"}

	//Adiciona campos de entidades adicionais, caso exista na base
	for nCount := 5 to 9
		cEntidade := StrZero(nCount, 2)
		cEntIni := "CTS_E"+cEntidade+"INI"
		cEntFim := "CTS_E"+cEntidade+"FIM"

		if CTS->(FieldPos(cEntIni)) > 0
			aAdd(aFieldsCTS, cEntIni)
		endif

		if CTS->(FieldPos(cEntFim)) > 0
			aAdd(aFieldsCTS, cEntFim)
		endif
	next nCount

	self:oSchema:aliasToSchema("CTS",aFieldsCTS)

	//Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()

	self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,.F.) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial" , STR0005, "string", STR0005, "NomeFilial" ,,,,.F.) // "Nome Filial"

return self:oSchema
