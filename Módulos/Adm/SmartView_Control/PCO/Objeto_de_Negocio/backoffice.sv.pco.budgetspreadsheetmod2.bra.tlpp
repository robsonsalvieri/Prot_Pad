#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.pco.budgetspreadsheetmod2.bra.ch"

namespace totvs.protheus.pco.smartviewsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCO", tables="AK1,AK2,AK3,CTT", name="Planilhas Orçamentárias Modelo 2", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BudgetSpreadsheetMod2SmartViewBusinessObject
Classe para criação do Objeto de Negócio Planilhas Orçamentárias Modelo 2 
para o SmartView
 
@author wilton.santos
@since 05/12/2023
@version 1.0
*/
//-------------------------------------------------------------------------------

Class BudgetSpreadsheetMod2SmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()            	 as object
    public method getDescription() 	 as character
    public method getAreas()       	 as array
    public method getData()        	 as object
    public method getSchema()      	 as object

    //usados na internacionalização
    public method preSchema()      	 as object 
    public method preData()        	 as object
    public method processData()    	 as json 
    
	protected method getPlanilha()   as character
	protected method getTotalCo()    as character
	protected method PcoR045Avalia() as logical

	protected method PcoRetCO()   	 as character

	protected data cPlanilha 	as character
	protected data cVersao   	as character
	protected data dDataIni  	as date
	protected data dDataFim  	as date
	protected data cCtaOrIni 	as character
	protected data cCtaOrFim 	as character
	protected data cClasIni  	as character
	protected data cClasFim  	as character
	protected data cOperIni  	as character
	protected data cOperFim  	as character
	protected data cRevisa	 	as character
	protected data lImpSint  	as logical
	protected data lTotSint  	as logical

	protected data aPeriodo	 	as array
	protected data aTotCO	 	as array

    protected data jItens    	as json
    protected data oQuery    	as object
    protected data oQueryTot 	as object
	protected data cAlias    	as character
	protected data cAliasTot 	as character
	protected data oIntegratedProvider  as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author wilton.santos
@since 05/12/2023
@version 1.0
*/
//-------------------------------------------------------------------

method new() as object Class BudgetSpreadsheetMod2SmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Planilhas Orçamentárias Modelo 2"
    self:setPergunte("PCR015")   //Indica o pergunte que serÃ¡ utilizado no relatÃrio

	::cPlanilha := ""      
	::cVersao   := ""
	::cRevisa	:= ""	
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author wilton.santos
@since 05/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class BudgetSpreadsheetMod2SmartViewBusinessObject
return STR0002 //"Objeto contendo informações sobre a Planilhas Orçamentárias Modelo 2"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author wilton.santos
@since 05/12/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BudgetSpreadsheetMod2SmartViewBusinessObject
return {STR0003} //"Planejamento e Controle Orçamentário"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@return object: self:oData
 
@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class BudgetSpreadsheetMod2SmartViewBusinessObject

	local cQuery     as character   
	local cContaOrc  as character   
	local cCodMasc   as character
	local cDescCla	 as character
	local cDescIte	 as character
	local cDescClV	 as character
	local nPos, nTotCO 	:= 0 as numeric
	local aTotCO		:= {} as array
	local aFilCO		:= {} as array
	local aFilOper  	:= {} as array
	local cCompanyName        as character
	local cBranchName         as character

	cCodMasc := SuperGetMV("MV_PCOMASC",.F.,"") 

	//Carrega lista de parametros para a memória
	CTBsetValueMVPAR(oFilter:getParameters(), "PCR015")

	//Elimina da memória a instância do objeto informado como parâmetro
    FreeObj(oFilter:getParameters())

	::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName    := ::oIntegratedProvider:getCompanyName()
    cBranchName     := ::oIntegratedProvider:getBranchName()

	::cPlanilha := MV_PAR01                     // 	Planilha ?
	::cVersao   := MV_PAR02                     // 	Versao ?
	::dDataIni  := MV_PAR03                     // 	Periodo de ?
	::dDataFim  := MV_PAR04                     // 	Periodo ate ?
	::cCtaOrIni := MV_PAR05                     // 	C.O. de ?
	::cCtaOrFim := MV_PAR06                     // 	C.O. ate ?
	::cClasIni  := MV_PAR07                     // 	Classe de ?
	::cClasFim  := MV_PAR08                     // 	Classe ate ?
	::cOperIni  := MV_PAR09                     // 	Operacao de ?
	::cOperFim  := MV_PAR10                     // 	Operacao ate ?
	::lImpSint  := iif(MV_PAR11==1,.T.,.F.)     // 	Imprime Sinteticas ?
	::lTotSint  := iif(MV_PAR12==1,.T.,.F.)     // 	Totaliza Sinteticas ?

	//se imprime contas sinteticas e totaliza sinteticas por relatorio
	if ::lImpSint .And. ::lTotSint
		aFilCO := { MV_PAR05, MV_PAR06 }
	endif

	//se imprime contas sinteticas e totaliza sinteticas por planilha
	if !self:lTotSint
		aFilOper := { MV_PAR09, MV_PAR10 }
	endif

	//verifico se a revisão informada existe
	if !self:PcoR045Avalia()
	    Return self:oData
	endif

	//Valida Acesso de usuário a planilha
	If !( PcoVerAcessoPlan(2) > 0 )
		Return self:oData
	EndIf
	
	cQuery := self:getPlanilha(oFilter)
	self:cAlias := MPSysOpenQuery(cQuery)

	While !(self:cAlias)->(Eof())
	    //Nova objeto
	    ::jItens := JsonObject():new()

		cContaOrc := self:PcoRetCO((self:cAlias)->AK3_CO,cCodMasc)

		//totaliza contas analiticas e sinteticas            
		aTotCO := PCOTotCO( (self:cAlias)->AK1_CODIGO, ::cRevisa, aFilCO, ::lImpSint, aFilOper )

		//tratamento para pegar o total da conta
		If !(!self:lImpSint .And. (self:cAlias)->AK3_TIPO $ " ;1")
			nPos := aScan( aTotCO, { |x| x[1] == (self:cAlias)->AK3_CO } )
			iif(nPos > 0, nTotCO := aTotCO[nPos,2], nTotCO := 0)
		EndIf

		//tratamento descricoes
		cDescCla := Posicione("AK6", 1, FWxFilial("AK6") + (self:cAlias)->AK2_CLASSE, "AK6_DESCRI")
		cDescIte := Posicione("CTD", 1, FWxFilial("CTD")+(self:cAlias)->AK2_ITCTB, "CTD_DESC01")
		cDescClV := Posicione("CTH", 1, FWxFilial("CTH")+(self:cAlias)->AK2_CLVLR, "CTH_DESC01")

		::jItens["AK1_FILIAL"] := (self:cAlias)->AK1_FILIAL
		::jItens["AK1_CODIGO"] := (self:cAlias)->AK1_CODIGO
		::jItens["AK1_VERSAO"] := (self:cAlias)->AK1_VERSAO
		::jItens["AK1_DESCRI"] := (self:cAlias)->AK1_DESCRI
		::jItens["AK1_TPPERI"] := (self:cAlias)->AK1_TPPERI
		::jItens["AK2_CC"    ] := (self:cAlias)->AK2_CC
		::jItens["AK2_CLASSE"] := (self:cAlias)->AK2_CLASSE
		::jItens["AK2_PERIOD"] := iif(!empty((self:cAlias)->AK2_PERIOD), FwTimeStamp(5, StoD((self:cAlias)->AK2_PERIOD), "00:00:00"), nil) 
		::jItens["AK2_MOEDA" ] := (self:cAlias)->AK2_MOEDA 
		::jItens["AK2_DATAI" ] := iif(!empty((self:cAlias)->AK2_DATAI), FwTimeStamp(5, StoD((self:cAlias)->AK2_DATAI), "00:00:00"), nil) 
		::jItens["AK2_DATAF" ] := iif(!empty((self:cAlias)->AK2_DATAF), FwTimeStamp(5, StoD((self:cAlias)->AK2_DATAF), "00:00:00"), nil)
		::jItens["AK2_VALOR" ] := (self:cAlias)->AK2_VALOR
		::jItens["AK2_OPER"  ] := (self:cAlias)->AK2_OPER
		::jItens["AK2_DESCRI"] := (self:cAlias)->AK2_DESCRI
		::jItens["AK2_CLVLR" ] := (self:cAlias)->AK2_CLVLR
		::jItens["AK3_CO"    ] := cContaOrc
		::jItens["AK3_PAI"   ] := (self:cAlias)->AK3_PAI
		::jItens["AK3_TIPO"  ] := (self:cAlias)->AK3_TIPO
		::jItens["AK3_DESCRI"] := (self:cAlias)->(REPLICATE(".",(VAL(AK3_NIVEL)-1)*3)+AK3_DESCRI)
		::jItens["AK3_NIVEL" ] := (self:cAlias)->AK3_NIVEL		
		::jItens["AK6_DESCRI"] := cDescCla
		::jItens["DESCCTT"]    := (self:cAlias)->CTT_DESC01
		::jItens["DESCITEM"]   := cDescIte
		::jItens["DESCCLV"]    := cDescClV

		//Campos customizados
		::jItens["TIPO_CO"]    := iif((self:cAlias)->AK3_TIPO $ " ;1",STR0004,STR0005) //"Sintetica"###"Analitica"
		::jItens["TOTAL_CO"]   := nTotCO

		::jItens["NomeEmpresa" ] := cCompanyName
        ::jItens["NomeFilial"  ] := cBranchName

		self:processData()
	    self:oData:appendData(::jItens)
		(self:cAlias)->(DbSkip())
	EndDo

	self:oQuery:Destroy()
	FwFreeObj(self:oQuery)    

return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getSchema() as object class BudgetSpreadsheetMod2SmartViewBusinessObject
	
	local aFieldsAK1 := {} as array
	local aFieldsAK2 := {} as array
	local aFieldsAK3 := {} as array
	local aFieldsCTT := {} as array
	local aFieldsCTD := {} as array
	local aFieldsCTH := {} as array
	Local lCanFilter := .F. as logical

	aFieldsAK1 := {"AK1_FILIAL", "AK1_CODIGO", "AK1_VERSAO", "AK1_DESCRI", "AK1_TPPERI"}

	aFieldsAK2 := {"AK2_CC", "AK2_CLASSE", "AK2_PERIOD", "AK2_MOEDA", "AK2_DATAI", "AK2_DATAF", "AK2_VALOR", "AK2_ITCTB", "AK2_OPER", "AK2_DESCRI", "AK2_CLVLR"}

	aFieldsAK3 := {"AK3_CO", "AK3_PAI", "AK3_TIPO", "AK3_NIVEL", "AK3_DESCRI"}
   
	//Adiciona campos ao schema
    self:oSchema:aliasToSchema("AK1", aFieldsAK1)
    self:oSchema:aliasToSchema("AK2", aFieldsAK2)
	self:oSchema:aliasToSchema("AK3", aFieldsAK3)
	self:oSchema:aliasToSchema("CTT", aFieldsCTT)
	self:oSchema:aliasToSchema("CTD", aFieldsCTD)
	self:oSchema:aliasToSchema("CTH", aFieldsCTH)

	//Adiciona campos customizados ao schema
	self:oSchema:addProperty("TIPO_CO"	   ,STR0006   ,"string"   ,STR0006   ,"TIPO_CO"   ,,,,lCanFilter)
    self:oSchema:addProperty("TOTAL_CO"    ,STR0007   ,"number"   ,STR0007   ,"TOTAL_CO"  ,,,,lCanFilter)
	self:oSchema:addProperty("DESCCTT"     ,STR0008   ,"string"   ,STR0008   ,"DESCCTT"  ,,,,lCanFilter) //"Descrição Centro de Custo"
	self:oSchema:addProperty("DESCITEM"    ,STR0009   ,"string"   ,STR0009   ,"DESCITEM"  ,,,,lCanFilter)//"Descrição Item Contábil"
	self:oSchema:addProperty("DESCCLV"     ,STR0010   ,"string"   ,STR0010   ,"DESCCLV"  ,,,,lCanFilter) //"Descrição Classe de Valor"
	self:oSchema:addProperty( "NomeEmpresa",STR0011   ,"string"   ,STR0011   , "NomeEmpresa" ,,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty( "NomeFilial" ,STR0012   ,"string"   ,STR0012   , "NomeFilial"  ,,,,lCanFilter) // Nome Filial

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que será consumido pelo M.I.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BudgetSpreadsheetMod2SmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que será consumido pelo M.I.
    AJusta o Json conforme alteração no processData e preSchema
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BudgetSpreadsheetMod2SmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData() as json class BudgetSpreadsheetMod2SmartViewBusinessObject
return ::jItens

//-------------------------------------------------------------------
/*/{Protheus.doc} getPlanilha
    Metodo que será consumido pelo M.I.
    Será usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 05/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method getPlanilha(oFilter as object) as character class BudgetSpreadsheetMod2SmartViewBusinessObject
	
	local cQuery as character
	local cDelete := space(1) as character	
	local nParamOrder   := 1    as numeric

	cQuery +=" SELECT  AK1_FILIAL,AK1_CODIGO,AK1_VERSAO,AK1_DESCRI,COALESCE(AK2_CC,'') AK2_CC,AK1_TPPERI,AK3_PAI,AK3_CO, AK3_TIPO, AK2_DATAI, AK2_DATAF, CTT_DESC01, "
	cQuery +=" COALESCE(AK2_CLASSE,'') AK2_CLASSE,COALESCE(AK2_PERIOD,'') AK2_PERIOD,COALESCE(AK2_MOEDA,0) AK2_MOEDA,COALESCE(SUM(AK2_VALOR),0) AK2_VALOR, AK3_NIVEL, AK3_DESCRI, AK2_OPER, AK2_ITCTB, AK2_CLVLR, AK2_DESCRI "  
	cQuery +=" FROM "+ RetSqlName("AK3")+ " AK3 "

	cQuery +=" LEFT JOIN "+RetSqlName("AK2")+" AK2  "
	cQuery +=" ON AK2_FILIAL = AK3_FILIAL AND AK2_ORCAME = AK3_ORCAME AND AK2_VERSAO = AK3_VERSAO  "  
	cQuery +=" AND AK2.D_E_L_E_T_ = ?  AND AK2_CO = AK3_CO  "
	cQuery +=" AND AK2_DATAI >= ? AND AK2_DATAF <=  ? "
	cQuery +=" AND AK2_CLASSE >= ? AND AK2_CLASSE <= ? "
	cQuery +=" AND AK2_OPER >= ? AND AK2_OPER <=  ? "

	cQuery +=" LEFT JOIN "+RetSqlName("CTT")+" CTT  "
	cQuery +=" ON CTT_FILIAL = ? AND  CTT_CUSTO = AK2_CC   AND CTT.D_E_L_E_T_ = ? "

	cQuery +=" LEFT JOIN "+RetSqlName("AK1")+" AK1  "
	cQuery +=" ON AK1_FILIAL = AK3_FILIAL AND AK1_CODIGO = AK3_ORCAME  "
	cQuery +=" AND AK1_VERSAO = AK3_VERSAO  AND AK1.D_E_L_E_T_ = ? "
	
	cQuery +=" WHERE AK3_FILIAL = ? " 
	cQuery +=" AND AK3_ORCAME = ? AND AK3_VERSAO = ?   "
	cQuery +=" AND AK3_CO >= ? AND AK3_CO <= ?   "
	cQuery +=" AND AK3.D_E_L_E_T_ = ?   "

	//Os filtros serão setados na interface do novo TReports
    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

	cQuery +=" GROUP BY AK1_FILIAL,AK1_CODIGO,AK1_VERSAO,AK1_DESCRI,AK2_CC,AK1_TPPERI,AK3_PAI,AK3_CO,AK3_TIPO, AK2_CLASSE,AK2_PERIOD,AK2_DATAI, AK2_DATAF, CTT_DESC01 , AK2_MOEDA, AK3_NIVEL, AK3_DESCRI, AK2_OPER, AK2_ITCTB, AK2_CLVLR, AK2_DESCRI "
	
	self:oQuery := FWExecStatement():New(ChangeQuery(cQuery))

	self:oQuery:SetString(nParamOrder++,  cDelete)
	self:oQuery:SetDate(nParamOrder++,    ::dDataIni)
	self:oQuery:SetDate(nParamOrder++,    ::dDataFim)
	self:oQuery:SetString(nParamOrder++,  ::cClasIni)
	self:oQuery:SetString(nParamOrder++,  ::cClasFim)
	self:oQuery:SetString(nParamOrder++,  ::cOperIni)
	self:oQuery:SetString(nParamOrder++,  ::cOperFim)
	self:oQuery:SetString(nParamOrder++, FWxFilial("CTT"))
	self:oQuery:SetString(nParamOrder++, cDelete)
	self:oQuery:SetString(nParamOrder++, cDelete)	
	self:oQuery:SetString(nParamOrder++, FWxFilial("AK3"))
	self:oQuery:SetString(nParamOrder++, ::cPlanilha)
	self:oQuery:SetString(nParamOrder++, ::cVersao)
	self:oQuery:SetString(nParamOrder++, ::cCtaOrIni)
	self:oQuery:SetString(nParamOrder++, ::cCtaOrFim)
	self:oQuery:SetString(nParamOrder++, cDelete)	
	
	cQuery := self:oQuery:GetFixQuery()

return cQuery


//-------------------------------------------------------------------
/*{Protheus.doc} PcoRetCO
Retorna o codigo do CO aplicando-se a configuração de mascara 

@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method PcoRetCO(cCO,cCodMasc) as character class BudgetSpreadsheetMod2SmartViewBusinessObject
	
	local aArea		:= GetArea()		as array
	local aAreaAK5	:= AK5->(GetArea()) as array
	local cCodRet	:= cCO				as character
	local cSeparador:= ""				as character
	local cMascara	:= ""				as character

	If Empty(cCodMasc)
		cCodMasc := SuperGetMV("MV_PCOMASC",.F.,"")
		If cCo == AK5->AK5_CODIGO
			cCodMasc := AK5->AK5_MASC
		Else
			dbSelectArea("AK5")
			If IndexOrd() > 0
				dbSetOrder(1)
				If(dbSeek(xFilial("AK5")+cCo) .And. !Empty(AK5->AK5_MASC))
					cCodMasc := AK5->AK5_MASC
				EndIf
			EndIf
		EndIf
	EndIf

	If !Empty(cCodMasc)
		cMascara:= RetMasCtb(cCodMasc,@cSeparador)
		If !Empty(cMascara)
			cCodRet	:= MascaraCTB(cCO,cMascara,,cSeparador)
		EndIf
	EndIf

	RestArea(aAreaAK5)
	RestArea(aArea)

return cCodRet

//-------------------------------------------------------------------
/*{Protheus.doc} PcoR045Avalia
Metodo para verificar se a revisão informada existe

@author Bruno Oliveira
@since 25/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method PcoR045Avalia() as logical class BudgetSpreadsheetMod2SmartViewBusinessObject

	local lOk 	  := .T. as logical
	local nRecAK1 := 0 	 as numeric
	
	dbSelectArea("AK1")
	dbSetOrder(1)
	If MSSeek(xFilial()+MV_PAR01)
		If !Empty(MV_PAR02)
			dbSelectArea("AKE")
			dbSetOrder(1)
			If ! MSSeek(xFilial()+PadR(MV_PAR01, TamSX3("AKE_ORCAME")[1])+MV_PAR02)
				lOk := .F.
			Else
				::cRevisa := MV_PAR02
			EndIf
			dbSelectArea("AKE")
		Else
			While AK1->(!EoF()) .And. AllTrim(AK1->(AK1_FILIAL+AK1_CODIGO)) == xFilial("AK1")+MV_PAR01
				::cRevisa	:= AK1->AK1_VERSAO
				nRecAK1 := AK1->(Recno())
				AK1->(dbSkip())
			EndDo
			AK1->(dbGoto(nRecAK1))
		EndIf
	EndIf

return lOk
