#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.pco.budgetaccounts.ch"

namespace totvs.protheus.pco.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAPCO", tables="AK5", name="Cadastro de Contas Orçamentárias", country="ALL", customTables="AK5")

//-------------------------------------------------------------------------------
/*{Protheus.doc} BudgetAccountsSmartViewBusinessObject
Classe para criação do Objeto de Negócio de Cadastro Ativos para o SmartView
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//-------------------------------------------------------------------------------
Class BudgetAccountsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getDescription()  as character
    public method getAreas()        as array
    public method getData()         as object
    public method getSchema()       as object

    //usados na internacionalização
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected data jItens     as json
    protected data cAlias     as character
    protected data oStatement as object
    protected data oIntegratedProvider  as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe
 
@return object: self
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method new() as object Class BudgetAccountsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001) //"Cadastro de Contas Orçamentárias"
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getDescription() as character class BudgetAccountsSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Cadastro de Contas Orçamentárias"

//-------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
 
@return object: string
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getAreas() as array class BudgetAccountsSmartViewBusinessObject
return {STR0003} //"Planejamento e Controle Orçamentário"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
 
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
 
@return object: self:oData
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getData(nPage as numeric, oFilter as object) as object class BudgetAccountsSmartViewBusinessObject

    local aAllFields := {}      as array
    local cQuery        := ""   as character
    local nParamOrder   := 1    as numeric
    local nX            := 0    as numeric
    local cId                   as character
    local cRealName             as character
    local cCompanyName      as character

    Private CONTADE as character
    Private CONTAATE as character
    
    aAllFields := self:getStructFields()

    CTBsetValueMVPAR(oFilter:getParameters(), "")   //Carrega lista de parametros para a memória 

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName    := ::oIntegratedProvider:getCompanyName()
    cBranchName     := ::oIntegratedProvider:getBranchName()

    cQuery := " SELECT " + self:getSQLFields(.F., , .F.) 

    cQuery += " FROM "+RetSQLName("AK5")+" AK5 "
    cQuery += " WHERE "
    cQuery += " AK5.AK5_FILIAL = ?  "
    cQuery += " AND AK5.AK5_CODIGO >= ? "
    cQuery += " AND AK5.AK5_CODIGO <= ? "
    cQuery += " AND AK5.D_E_L_E_T_ =  ? "

    cQuery += iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    cQuery += " ORDER BY AK5_CODIGO "

    cQuery := ChangeQuery(cQuery)

    self:oStatement := FWExecStatement():New(cQuery)  

    self:oStatement:SetString(nParamOrder++, FWxFilial("AK5") )
    self:oStatement:SetString(nParamOrder++, CONTADE)  
    self:oStatement:SetString(nParamOrder++, CONTAATE)
    self:oStatement:SetString(nParamOrder++, Space(1))

    self:cAlias	:= self:oStatement:OpenAlias(GetNextAlias())

    While !(self:cAlias)->(Eof()) 

        ::jItens := JsonObject():new()

        For nX := 1 to len (aAllFields)

            cId := aAllFields[nX]:getName()
            cRealName := aAllFields[nX]:getRealName()

            If aAllFields[nX]:getType() == "date"
                ::jItens[cId] := totvs.framework.treports.date.stringToTimeStamp((self:cAlias)->&(cRealName))
            ElseIf SubStr(cId,1,3) == "AK5"
                ::jItens[cId] := (self:cAlias)->&(cRealName)
            EndIf

        Next nX

        ::jItens["DescricaoTipo" ]     := X3Combo("AK5_TIPO"  , (self:cAlias)->AK5_TIPO )
        ::jItens["DescricaoCondicao"]  := X3Combo("AK5_DEBCRE", (self:cAlias)->AK5_DEBCRE)
        ::jItens["DescricaoBloqueio"]  := X3Combo("AK5_MSBLQL", (self:cAlias)->AK5_MSBLQL)

        ::jItens["NomeEmpresa" ] := cCompanyName
        ::jItens["NomeFilial"  ] := cBranchName

        self:processData()
        self:oData:appendData(::jItens)

        (self:cAlias)->(dbSkip())

    EndDo

    dbSelectArea(self:cAlias)
    (self:cAlias)->(dbCloseArea())

    If Select(self:cAlias) == 0
        FErase((self:cAlias)+GetDBExtension())
        FErase("cArqInd"+OrdBagExt())
    EndIf

    //Elimina da memória
    FreeObj(oFilter:getParameters())

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getStruct
Prepara a estrutura dos campos
 
@param aCpos array: Array com os campos do relatório
 
@return array: Array com a estrutura dos campos
 
@author Bruno Oliveira
@since 13/10/2023
@version 1.0
*/
//------------------------------------------------------------------- 

method getSchema() as object class BudgetAccountsSmartViewBusinessObject

    Local lCanFilter := .F. as logical

    self:oSchema:aliasToSchema("AK5")

    //Seta os parametros manuais no SmartView
    self:oSchema:addParameter("CONTADE", STR0004, "string", .F.)	//"Conta Orcamentaria de?"
	self:oSchema:addParameter("CONTAATE", STR0005, "string", .F.)	//"Conta Orcamentaria até?"
    self:oSchema:addProperty( "NomeEmpresa",  STR0006, "string" , STR0006, "NomeEmpresa" ,,,,lCanFilter) // Nome Empresa
    self:oSchema:addProperty( "NomeFilial",   STR0007, "string" , STR0007, "NomeFilial"  ,,,,lCanFilter) // Nome Filial
    self:oSchema:addProperty( "DescricaoTipo"    ,  STR0008, "string" , STR0008, "DescricaoTipo" ,,,,lCanFilter) // "Descrição Tipo"
    self:oSchema:addProperty( "DescricaoCondicao",  STR0009, "string" , STR0009, "DescricaoCondicao"  ,,,,lCanFilter) // "Descrição Condição"
    self:oSchema:addProperty( "DescricaoBloqueio",  STR0010, "string" , STR0010, "DescricaoBloqueio"  ,,,,lCanFilter) // "Descrição Bloqueio"

    self:setCustomURL("CONTADE",  "api/framework/v1/genericLookupService/smartview/AK5", 2)
    self:setCustomURL("CONTAATE",  "api/framework/v1/genericLookupService/smartview/AK5", 2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
    Metodo que ser? consumido pelo M.I.
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preSchema() as object class BudgetAccountsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
    Metodo que ser? consumido pelo M.I.
    AJusta o Json conforme altera??o no processData e preSchema
    @author wilton.santos
    @since 14/11/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method preData() as object class BudgetAccountsSmartViewBusinessObject    
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
    Metodo que ser? consumido pelo M.I.
    Ser? usado para alterar a Query antes do getData.
    @author wilton.santos
    @since 20/12/2023
    @version 12.1.2310
/*/
//-------------------------------------------------------------------

method processData()  class BudgetAccountsSmartViewBusinessObject
return ::jItens
