#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.acquisitions.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SA2,SN1,SN3", name="Aquisições", country="ALL", , customTables="SN1,SN3,SA2")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoTReportsBusinessObject
Classe para criação do Objeto de Negócio de acquisitions para o TReports
Fonte/Perg      ATFR110	/ AFR110
@author         Douglas Rodrigues da Silva
@since          29/05/2023
@version        12.1.2310
@Revitalização  Renato July - OUT/2023
*/
//-------------------------------------------------------------------------------
Class AcquisitionsSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()                 as object
    public method getDescription()      as character
    public method getAreas()            as array
    public method getData()             as object
    public method getSchema()           as object

    //usados na internacionalização
    public method preschema()           as object 
    public method preData()             as object
    public method processData()         as json 
    
    protected method getAdiantamentos() as character
    protected method setAdiantamentos() as object
    
    protected data jDados       as json
    protected data cAlias       as character
    protected data aAllFields   as array
    protected data oQuery       as object
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe new
@return object: self
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class AcquisitionsSmartViewBusinessObject
    _Super:new()
    self:setDisplayName(STR0001)  //"Aquisições" 
    self:setPergunte("AFR110")    //Indica o pergunte que será utilizado no relatório

    ::aAllFields := {}
Return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class AcquisitionsSmartViewBusinessObject
return STR0002  //"Objeto contendo informações Aquisições Ativo Fixo"###

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@author         
@since          
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class AcquisitionsSmartViewBusinessObject
Return { STR0003 } //"Ativo Fixo"###

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class AcquisitionsSmartViewBusinessObject

    Local aFieldsSA2 as array
    Local aFieldsSN1 as array
    Local aFieldsSN3 as array

    aFieldsSA2  := {"A2_NREDUZ"}

    aFieldsSN1  := {"N1_QUANTD", "N1_NFISCAL", "N1_CHAPA", "N1_GRUPO", "N1_LOCAL", "N1_FORNEC", "N1_PATRIM",;
                    "N1_LOJA"  , "N1_DESCRIC"}

    aFieldsSN3  := {"N3_FILIAL" , "N3_CBASE"  , "N3_ITEM"   , "N3_TIPO"   , "N3_TPSALDO", "N3_AQUISIC", "N3_DINDEPR",;
                    "N3_CCUSTO" , "N3_CCONTAB", "N3_CDESP"  , "N3_DTBAIXA", "N3_CCORREC", "N3_CDEPREC", "N3_CCDEPR" ,;
                    "N3_TXDEPR1", "N3_VORIG1" , "N3_VRDACM1", "N3_VRCACM1",;
                    "N3_TXDEPR2", "N3_VORIG2" , "N3_VRDACM2", "N3_VRCACM2",;
                    "N3_TXDEPR3", "N3_VORIG3" , "N3_VRDACM3", "N3_VRCACM3",;
                    "N3_TXDEPR4", "N3_VORIG4" , "N3_VRDACM4", "N3_VRCACM4",;
                    "N3_TXDEPR5", "N3_VORIG5" , "N3_VRDACM5", "N3_VRCACM5"}

    self:oSchema:aliasToSchema("SA2",aFieldsSA2)
    self:oSchema:aliasToSchema("SN1",aFieldsSN1)
    self:oSchema:aliasToSchema("SN3",aFieldsSN3)

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0004, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author         
@since          
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class AcquisitionsSmartViewBusinessObject

    Local cQuery := "" as character    

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR110")

	//Elimina da memória a instância do objeto informado como parâmetro.
    FreeObj(oFilter:getParameters())

    //Carrega o bloco de baixas
    cQuery       := self:getAdiantamentos(oFilter)

    self:cAlias  := MPSysOpenQuery(cQuery)
    self:setAdiantamentos(oFilter)

    (self:cAlias)->(DBCloseArea())

return self:oData

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method preSchema() as object class AcquisitionsSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method preData() as object class AcquisitionsSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na Chamado no momento do processamento da query
@return object: self:oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------
method processData() class AcquisitionsSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAdiantamentos
gera a query de filtragem dos dados 
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method getAdiantamentos(oFilter) as character class AcquisitionsSmartViewBusinessObject

    Local aTipoN3  := {'01','10','12','16','17'}  as array       
    Local cSpace   := space(1) as character
    Local cQuery   as character  
    Local nParamOrder := 1  as numeric
    Local lAtfCusPrv  := AFXAtCsPrv() as logical
    Local aSelFil  as array

    //Seleção de filiais
    If MV_PAR09 == 2
        ::oIntegratedProvider := ControlIntegratedProvider():New()

        ::oIntegratedProvider:lMultSelectBranches := .T.
        ::oIntegratedProvider:lAllowBranchFilter  := .T.
        ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
        ::oIntegratedProvider:loadFilterBranches({"SN3"})

        If Valtype(::oIntegratedProvider:oFilterBranches["SN3"][1]) == "A"
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"][1]
        Else
            aSelFil := ::oIntegratedProvider:oFilterBranches["SN3"]
        EndIf
    EndIf

    //Seleção de dados para processamento
    cQuery := " SELECT "
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += " FROM "+ RetSQLName("SN3") +" SN3 " 

    //Adiciona Filial corrente
    cQuery += " LEFT JOIN "+ RetSQLName("SN1") +" SN1 ON SN1.D_E_L_E_T_  = ? "
    cQuery += "  AND SN1.N1_FILIAL   = SN3.N3_FILIAL "
    cQuery += "  AND SN1.N1_CBASE    = SN3.N3_CBASE "
    cQuery += "  AND SN1.N1_ITEM     = SN3.N3_ITEM "

    //Adiciona fornecedor
    cQuery += " LEFT JOIN "+ RetSQLName("SA2") +" SA2 ON SA2.D_E_L_E_T_  = ? "
    cQuery += "  AND SA2.A2_FILIAL   = ? "
    cQuery += "  AND SA2.A2_COD      = SN1.N1_FORNEC "
    cQuery += "  AND SA2.A2_LOJA     = SN1.N1_LOJA "

    cQuery += " WHERE SN3.D_E_L_E_T_  = ? "
    If MV_PAR09 == 1
        cQuery += "   AND SN3.N3_FILIAL BETWEEN ? AND ?"
	Else
        If !Empty(aSelFil)
            cQuery += "   AND SN3.N3_FILIAL IN ( ? )"
        EndIf
	Endif
    cQuery += "   AND SN3.N3_AQUISIC BETWEEN ? AND ?"
    cQuery += "   AND SN3.N3_CCONTAB BETWEEN ? AND ?"
    cQuery += "   AND SN3.N3_CCUSTO BETWEEN ? AND ?"

	//Faixa de itens - 12,Considera itens ?
	If MV_PAR12 == 1        // Itens Normais
        cQuery += "   AND SN3.N3_TIPO IN ( ? ) "
	ElseIf MV_PAR12 == 2    // Itens Adiantamentos
		cQuery += "   AND SN3.N3_TIPO = ? "
	ElseIf MV_PAR12 == 3    // Itens Normais e Adiantamentos
		cQuery += "   AND ( SN3.N3_TIPO IN ( ? ) OR SN3.N3_TIPO = ? )"
	EndIf
	
	//Se Considera Itens Baixados (S/N)
	If MV_PAR08 == 2
		cQuery += "   AND SN3.N3_BAIXA = ? "
	Endif	

    //Ativo de Custo/Provisão  - 14,Exibir Ativos Real. Provis ?
	If lAtfCusPrv .And. MV_PAR14 == 2
		cQuery += "   AND SN3.N3_ATFCPR <> ? "
	EndIf

    //Os filtros serão setados na interface do novo TReports    
    cQuery += Iif(oFilter:hasFilter(), " AND " + oFilter:getSQLExpression(), "")

    //Define o campo de ordenação
    cQuery += " ORDER BY N3_CBASE "

    //Objeto executa o comando SQL inúmeras vezes.
    self:oQuery := FWPreparedStatement():New(ChangeQuery(cQuery))

    self:oQuery:setString(nParamOrder++, cSpace  )
    self:oQuery:setString(nParamOrder++, cSpace  )
    self:oQuery:setString(nParamOrder++, FwXFilial("SA2"))
    self:oQuery:setString(nParamOrder++, cSpace  )

    If MV_PAR09 == 1
        self:oQuery:setString(nParamOrder++, MV_PAR10)
        self:oQuery:setString(nParamOrder++, MV_PAR11)        
	Else
        If !Empty(aSelFil)
            self:oQuery:setIn(nParamOrder++, aSelFil )
        EndIf        
	Endif
    
    self:oQuery:setDate(nParamOrder++  , MV_PAR01)
    self:oQuery:setDate(nParamOrder++  , MV_PAR02)
    self:oQuery:setString(nParamOrder++, MV_PAR03)
    self:oQuery:setString(nParamOrder++, MV_PAR04)
    self:oQuery:setString(nParamOrder++, MV_PAR05)
    self:oQuery:setString(nParamOrder++, MV_PAR06)

	If MV_PAR12 == 1  // Itens Normais
        self:oQuery:setIn(nParamOrder++, aTipoN3 )
	ElseIf MV_PAR12 == 2 // Itens Adiantamentos
        self:oQuery:setString(nParamOrder++, "03")
	ElseIf MV_PAR12 == 3 // Itens Normais e Adiantamentos
        self:oQuery:setIn(nParamOrder++, aTipoN3 )
        self:oQuery:setString(nParamOrder++, "03")
	EndIf
	
	If MV_PAR08 == 2
        self:oQuery:setString(nParamOrder++, "0" )
	Endif	

	If lAtfCusPrv .And. MV_PAR14 == 2
        self:oQuery:setString(nParamOrder++, "1" )
	EndIf

    //Retorna query com parâmetros tratados e substituídos.
    cQuery := self:oQuery:GetFixQuery()

    FreeObj(::oIntegratedProvider)

return cQuery

//-------------------------------------------------------------------------------
/*{Protheus.doc} setAdiantamentos
seta os dados filtrados no objeto oData
@author         Controladoria
@since          DEZ/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
method setAdiantamentos(oFilter as object) as object class AcquisitionsSmartViewBusinessObject

    Local nX as numeric

    self:aAllFields := self:getStructFields()

    //Posiciona no registro inicial
    (self:cAlias)->(DBGoTop())

    While !(self:cAlias)->(Eof())
        
        self:jDados := JsonObject():new()

        //Popula campos conforme estrutura
        For nX := 1 To Len(self:aAllFields)
            if self:aAllFields[nX]:getType() == "date"
                self:jDados[self:aAllFields[nX]:getName()] := iif(!empty((self:cAlias)->&(self:aAllFields[nX]:getRealName())), FwTimeStamp(5, StoD((self:cAlias)->&(self:aAllFields[nX]:getRealName())), "00:00:00"), nil)
            elseif self:aAllFields[nX]:getType() == "string"
                self:jDados[self:aAllFields[nX]:getName()] := AllTrim((self:cAlias)->&(self:aAllFields[nX]:getRealName()))
            else
                self:jDados[self:aAllFields[nX]:getName()] := (self:cAlias)->&(self:aAllFields[nX]:getRealName())
            endif
        Next nX

        self:processData()
        self:oData:appendData(self:jDados)

        (self:cAlias)->(dbSkip())

    EndDo

return self:oData
