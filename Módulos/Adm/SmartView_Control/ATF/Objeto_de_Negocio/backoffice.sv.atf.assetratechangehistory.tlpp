#include "protheus.ch"
#include "totvs.ch"
#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.assetratechangehistory.ch"
  
namespace totvs.protheus.backoffice.sv.atf.assetratechangehistory.treportsintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils
 
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="FM5, SN1", name="Histórico Alteração de Taxa do Ativo", country="ALL", customTables="FM5, SN1")

//-------------------------------------------------------------------------------
/*{Protheus.doc} AssetRateChangeHistorySmartViewBusinessObject
Classe para criação do Objeto de Negócio Histórico Alteração de Taxa do Ativo 
para o SmartView
 
@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------------------
class AssetRateChangeHistorySmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider
    public method new()         as object
    public method getData()     as object
    public method getSchema()   as object

    //usados na internacionalização
    public method preschema()   as object 
    public method preData()     as object
    public method processData() as json

    protected method getQuery()    as character
    protected method getTPSaldo()  as character

    protected data cCompanyName    as character
    protected data cBranchName     as character
    protected data jDados          as json
    protected data aAllFields      as array
    protected data aSelFil         as array
    protected data oIntegratedProvider as object

endclass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe new

@return object: self

@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//------------------------------------------------------------------- 
method new() class AssetRateChangeHistorySmartViewBusinessObject
    _Super:new()
    
    //Define a Área
    self:appendArea(STR0001) //"Ativo Fixo"
    
    //Define o nome do Objeto de Negócio
    self:setDisplayName(STR0002) //"Histórico Alteração de Taxa do Ativo"
    
    //Define a descrição do Objeto de Negócio
    self:setDescription(STR0003) //"Objeto contendo informações do relatório Histórico Alteração de Taxa do Ativo"

    ::aAllFields := {}
    ::aSelFil    := {}
return self

//-------------------------------------------------------------------------------
/*{Protheus.doc} preSchema
Metodos usado na Internacionalização

@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------------------
method preSchema() as object class AssetRateChangeHistorySmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} preData
Método de instância da classe

@return object: self:oData

@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------
method preData() as object class AssetRateChangeHistorySmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} processData
Método na chamado no momento do processamento da query

@return object: self:oData

@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------
method processData() class AssetRateChangeHistorySmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio

@return object: self:oData
 
@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------
method getData(nPage as numeric, oFilter as object) as object class AssetRateChangeHistorySmartViewBusinessObject
    local cQuery      as character
    local cAlias      as character
    local cId         as character
    local cRealName   as character
    local jParams     as json
    local nX          as numeric

    private CBASEINI  as character
    private CBASEFIM  as character
    private CITEMINI  as character
    private CITEMFIM  as character
    private CTIPOINI  as character
    private CTIPOFIM  as character
    private CMOEDA    as character
    private NTPSALDO  as numeric
    private DDATAINI  as date
    private DDATAFIM  as date

    jParams := Nil
    nX := 0

    CBASEINI   := ""
    CBASEFIM   := ""
    CITEMINI   := ""
    CITEMFIM   := ""
    CTIPOINI   := ""
    CTIPOFIM   := ""
    CMOEDA     := ""
    NTPSALDO   := 1

    jParams := oFilter:getParameters()  //Metodo para retorno do json dos parâmetros
    CTBsetValueMVPAR(jParams, "")       //Carrega lista de parametros para a memória

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    ::cCompanyName := ::oIntegratedProvider:getCompanyName()
    ::cBranchName  := ::oIntegratedProvider:getBranchName()

    ::oIntegratedProvider:lMultSelectBranches := .T.
	::oIntegratedProvider:lAllowBranchFilter  := .F.
	::oIntegratedProvider:setBranchPar(oFilter:getParameters())
	::oIntegratedProvider:loadFilterBranches({"FM5"})

	if Valtype(::oIntegratedProvider:oFilterBranches["FM5"][1]) == "A"
		::aSelFil := ::oIntegratedProvider:oFilterBranches["FM5"][1]
	else
		::aSelFil := ::oIntegratedProvider:oFilterBranches["FM5"]
	endif

    cQuery := self:getQuery(oFilter)
    cAlias := MPSysOpenQuery(cQuery)

    while (cAlias)->(!eof())
        ::jDados := JsonObject():new()

        for nX := 1 to len (self:aAllFields)
            cId := self:aAllFields[nX]:getName()
            cRealName := self:aAllFields[nX]:getRealName()

            if self:aAllFields[nX]:getType() == "date"
                ::jDados[cId] := totvs.framework.treports.date.stringToTimeStamp((cAlias)->&(cRealName))
            elseif self:aAllFields[nX]:getType() == "string"
                ::jDados[cId] := AllTrim((cAlias)->&(cRealName))
            elseif cRealName == "FM5_OBS"
                FM5->(dbGoTo((cAlias)->FM5RECNO))
                ::jDados[cId] := FM5->FM5_OBS
            elseif cRealName <> "FM5RECNO" //Não appenda campo recno. Utilizado somente para posicionar na FM5 p/ pegar campo memo
                ::jDados[cId] := (cAlias)->&(cRealName)
            endif
        next nX

        ::jDados["NomeEmpresa"] := ::cCompanyName
        ::jDados["NomeFilial" ] := ::cBranchName

        self:processData()
    
        self:oData:appendData(::jDados)
        (cAlias)->(dbSkip())
    endDo

    (cAlias)->(dbCloseArea())
    
    //Elimina da memória a instância do objeto informado como parâmetro.
    FwFreeObj(oFilter:getParameters())
    FwFreeObj(::oIntegratedProvider)
    FwFreeObj(jParams)
     
return self:oData
  
//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos

@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//------------------------------------------------------------------- 
method getSchema() as object class AssetRateChangeHistorySmartViewBusinessObject
    local lCanFilter as logical
    local aFieldsSN1 as array

    lCanFilter := .F.
    aFieldsSN1 := {}

    aFieldsSN1 := {"N1_DESCRIC"}  

    self:oSchema:aliasToSchema("FM5")
    self:oSchema:aliasToSchema("SN1", aFieldsSN1)

    //Popula o atributo aAllFields com os campos acima
    self:aAllFields := self:getStructFields()
    
    //Adiciona campos customizados
    self:oSchema:addProperty("NomeEmpresa", STR0004, "string", STR0004, "NomeEmpresa",,,,lCanFilter) // "Nome Empresa"
    self:oSchema:addProperty("NomeFilial",  STR0005, "string", STR0005, "NomeFilial" ,,,,lCanFilter) // "Nome Filial"

    //Adiciona parâmetros
    self:oSchema:addParameter("SV_MULTBRANCH", STR0006, "string", .T.)  //"Filiais"
    self:oSchema:addParameter("CBASEINI",      STR0007, "string", .F.)	//"Do Codigo ?"
    self:oSchema:addParameter("CBASEFIM",      STR0008, "string", .F.)	//"Ate o Codigo ?"
    self:oSchema:addParameter("CITEMINI",      STR0009, "string", .F.)	//"Do Item ?"
    self:oSchema:addParameter("CITEMFIM",      STR0010, "string", .F.)	//"Ate o Item ?"
    self:oSchema:addParameter("CTIPOINI",      STR0011, "string", .F.)	//"Do Tipo ?"
    self:oSchema:addParameter("CTIPOFIM",      STR0012, "string", .F.)	//"Ate o Tipo ?"
    self:oSchema:addParameter("NTPSALDO",      STR0013, "number", .F.)	//"Tipo Saldo ?"
    self:oSchema:addParameter("CMOEDA",        STR0014, "string", .F.)	//"Moeda ?"
    self:oSchema:addParameter("DDATAINI",      STR0015, "date",   .F.)	//"Data Alteracao De ?"
    self:oSchema:addParameter("DDATAFIM",      STR0016, "date",   .F.)	//"Data Alteracao Ate ?"

    //Seta a consulta padrão nos parâmetros manuais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)
    self:setCustomURL("CBASEINI",     "api/framework/v1/genericLookupService/smartview/SN1",2)
    self:setCustomURL("CBASEFIM",     "api/framework/v1/genericLookupService/smartview/SN1",2)
    self:setCustomURL("CTIPOINI",     "api/framework/v1/genericLookupService/smartview/G1" ,2)
    self:setCustomURL("CTIPOFIM",     "api/framework/v1/genericLookupService/smartview/G1" ,2)
    self:setCustomURL("CMOEDA",       "api/framework/v1/genericLookupService/smartview/CTO",2)

    //Seta combo customizado
    self:setCustomURL("NTPSALDO", "/api/controladoria/smartview/v1/options/atf/assetratechangehistory/SVATFCombo006/1", 1)

    //Limpa array
    FwFreeArray(aFieldsSN1)

return self:oSchema

//-------------------------------------------------------------------
/*{Protheus.doc} getQuery
Monta a query para a consulta
 
@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//------------------------------------------------------------------- 
method getQuery(oFilter as Object) as character class AssetRateChangeHistorySmartViewBusinessObject
    local nParamOrder as numeric
    local cSpace      as character
    local cTpSaldo    as character
    local oStatement  as object

    nParamOrder := 1
    cSpace      := Space(1)
    cTpSaldo    := self:getTPSaldo(NTPSALDO)

    cQuery := "SELECT FM5.R_E_C_N_O_ FM5RECNO,"
    cQuery += self:getSQLFields() //Pega campos do schema
    cQuery += " FROM  "+RetSqlName("FM5")+" FM5 "
    cQuery += " JOIN "+RetSqlName("SN1")+" SN1 ON SN1.N1_FILIAL = FM5.FM5_FILIAL AND SN1.N1_CBASE = FM5.FM5_CBASE AND SN1.N1_ITEM = FM5.FM5_ITEM AND SN1.D_E_L_E_T_ = ? "
    cQuery += " WHERE "

    if !empty(self:aSelFil)
        cQuery += " FM5.FM5_FILIAL IN ( ? )"
    else
        cQuery += " FM5.FM5_FILIAL = ? "
    endif

    cQuery += " AND FM5_CBASE BETWEEN ? AND ? "
    cQuery += " AND FM5_ITEM BETWEEN ? AND ? "
    cQuery += " AND FM5_TIPO BETWEEN ? AND ? "
    cQuery += " AND FM5_TPSALD = ? "

    if !empty(CMOEDA)
        cQuery += " AND FM5_MOEDA = ? "
    endif

    cQuery += " AND FM5_DATA BETWEEN ? AND ? "
    cQuery += " AND FM5.D_E_L_E_T_ = ? "
    cQuery += " ORDER BY FM5.R_E_C_N_O_"

    cQuery := ChangeQuery(cQuery)
    
    oStatement := FWExecStatement():New(cQuery)

    oStatement:SetString(nParamOrder++,  cSpace)

    if !empty(self:aSelFil)
        oStatement:SetIn(nParamOrder++, self:aSelFil)
    else
        oStatement:SetString(nParamOrder++, FWXFilial("FM5")) 
    endif

    oStatement:SetString(nParamOrder++,  CBASEINI)
    oStatement:SetString(nParamOrder++,  CBASEFIM)
    oStatement:SetString(nParamOrder++,  CITEMINI)
    oStatement:SetString(nParamOrder++,  CITEMFIM)
    oStatement:SetString(nParamOrder++,  AllTrim(CTIPOINI))
    oStatement:SetString(nParamOrder++,  AllTrim(CTIPOFIM))
    oStatement:SetString(nParamOrder++,  cTpSaldo)

    if !empty(CMOEDA)
        oStatement:SetString(nParamOrder++,  CMOEDA)
    endif

    oStatement:SetDate(nParamOrder++,    DDATAINI)
    oStatement:SetDate(nParamOrder++,    DDATAFIM)
    oStatement:SetString(nParamOrder++,  cSpace)

    cQuery := oStatement:GetFixQuery()

return cQuery

//-------------------------------------------------------------------
/*{Protheus.doc} getTPSaldo
Monta De > Para do tipo do saldo de acordo com combobox
 
@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//------------------------------------------------------------------- 
method getTPSaldo(nTpSaldo as numeric) as character class AssetRateChangeHistorySmartViewBusinessObject
    local cRet        as character
    local cCombo      as character
    local cContent    as character
    local nCount      as numeric
    local nPos        as numeric
    local aCombo      as array
    local aTPSaldo    as array

    aCombo   := {}
    aTPSaldo := {}

    cCombo := AdmCBGener(FWxFilial("SX5"),"SX5","SL","01")
    aCombo := StrTokArr(cCombo, ";")

    for nCount := 1 to len(aCombo)
        cContent := SubStr(aCombo[nCount], At("=",aCombo[nCount])+1, Len(aCombo[nCount]))
        aAdd(aTPSaldo, {nCount, SubStr(aCombo[nCount], 1, At("=",aCombo[nCount])-1)})
    next nCount

    //Tratativa para pegar o tipo saldo correto
    if (nPos := aScan(aTPSaldo,{|x| x[1] == nTpSaldo})) > 0
        cRet := aTPSaldo[nPos][2]
    endif

    cRet := iif(empty(cRet), "1", cRet)
    
return cRet

//-------------------------------------------------------------------
/*{Protheus.doc} SVATFCombo006
Função para retornar combobox de parâmetro
 
@author  Renan Gremes
@since   08/05/2025
@version 12.1.2410
*/
//-------------------------------------------------------------------
function SVATFCombo006(cOpcao)
    local cCombo   as character
    local cContent as character
    local nCount   as numeric
    local aCombo   as array
    local aRet     as array

    aCombo := {}
    aRet   := {}

    cCombo := AdmCBGener(FWxFilial("SX5"),"SX5","SL","01")
    aCombo := StrTokArr(cCombo, ";")

    for nCount := 1 to len(aCombo)
        cContent := SubStr(aCombo[nCount], At("=",aCombo[nCount])+1, Len(aCombo[nCount]))
        if cOpcao == "1"
            aAdd(aRet, cContent)
        endif
    next nCount

return aRet
