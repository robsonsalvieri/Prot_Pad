#include "msobject.ch"
#include "totvs.framework.treports.integratedprovider.th"
#include "tlpp-core.th"
#include "tlpp-rest.th"
#include "backoffice.sv.atf.valuedpositiondate.bra.ch"

//Cria um novo namespace para sua classe
namespace totvs.protheus.atf.smartviewintegratedprovider
using namespace totvs.protheus.backoffice.control.smartView.integratedProvider
using namespace control.sv.functions.utils

//Ativar ou desativar a utilização da classe
@totvsFrameworkTReportsIntegratedProvider(active=.T., team="SIGAATF", tables="SN1,SN3,SN4", name="Posição Valorizada na Data", country="ALL")

//-------------------------------------------------------------------------------
/*{Protheus.doc} CentrodeCustoSmartViewBusinessObject
Classe para criação do Objeto de Negócio da Posição Valorizada na Data para o Smart View
Fonte/Perg      ATFR072 / ATR072
@author         Douglas Silva
@since          NOV/2023
@version        12.1.2310
@Revitalização  Renato July - NOV/2023
*/
//-------------------------------------------------------------------------------
Class ValuedPositionDateSmartViewBusinessObject from totvs.framework.treports.integratedprovider.IntegratedProvider

    public method new()             as object
    public method getData()         as object
    public method getSchema()       as object
    public method getAreas()        as array
    public method getDescription()  as character

    //usados na internacionalizacao
    public method preschema()       as object 
    public method preData()         as object
    public method processData()     as json

    protected method X3ComboATF()   as character

    protected data jDados           as json
    protected data aComboATF        as array
    protected data oIntegratedProvider as object

EndClass

//-------------------------------------------------------------------
/*{Protheus.doc} new
Método de instância da classe new
@return object: self
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method new() as object Class ValuedPositionDateSmartViewBusinessObject
    _Super:new()
    //Define o nome do Objeto de Negócio
    self:setDisplayName( STR0001 )    //"Posicao Valorizada na Data"
    self:setPergunte( "AFR072" )      //Indica o pergunte que será utilizado no relatório

    self:aComboATF := {}
return self

//-------------------------------------------------------------------
/*{Protheus.doc} getDescription
Método de instância da classe
@return object: string
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
Method getDescription() as character class ValuedPositionDateSmartViewBusinessObject
return STR0002 //"Objeto contendo informações Posicao Valorizada na Data"

//-------------------------------------------------------------------------------
/*{Protheus.doc} getAreas
Método de instância da classe
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//-------------------------------------------------------------------------------
Method getAreas() as array class ValuedPositionDateSmartViewBusinessObject
return { STR0003 } // "Ativo Fixo"

//-------------------------------------------------------------------
/*{Protheus.doc} getData
Retorna os dados do objeto de negócio
@param nPage, numérico, indica a página atual do relatório
@param oFilter, objeto, contém o filtro do SmartView
@return object: self:oData
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getData(nPage as numeric, oFilter as object) as object class ValuedPositionDateSmartViewBusinessObject
    
    Local cDescMoeda01 := AllTrim(SuperGetMV("MV_SIMB1")) as character
    Local cDescMoeda02 := AllTrim(SuperGetMV("MV_SIMB2")) as character
    Local cDescMoeda03 := AllTrim(SuperGetMV("MV_SIMB3")) as character
    Local cDescMoeda04 := AllTrim(SuperGetMV("MV_SIMB4")) as character
    Local cDescMoeda05 := AllTrim(SuperGetMV("MV_SIMB5")) as character
    Local cTipoFiscal  := ATFXTpBem(1) as character
    Local cTipoGerenc  := ATFXTpBem(2) as character
    Local cTipoIncent  := ATFXTpBem(3) as character
    Local cAliasTmp	        as character
    Local aSelMoed          as array 
    Local aSelFil           as array
    Local lTodasFil         as logical
    Local aSelClass         as array
    Local lRealProv         as logical 
    Local lAgrupa           as logical 
    Local aTipos            as array
    local jParams := Nil    as json

    Local dDataSLD  	    as date
    Local dAquIni		    as date
    Local dAquFim   	    as date
    Local cBemIni   	    as character
    Local cBemFim   	    as character
    Local cItemIni  	    as character
    Local cItemFim  	    as character
    Local cContaIni 	    as character
    Local cContaFim 	    as character
    Local cCCIni   		    as character
    Local cCCFim   		    as character
    Local cItCtbIni		    as character
    Local cItCtbFim		    as character
    Local cClvlIni		    as character
    Local cClVlFim		    as character
    Local cGrupoIni		    as character
    Local cGrupoFim		    as character
    Local cDescConta        as character
    Local cChaveBem         as character
    Local cBemBkp           as character
    
    Local cTipoSLD          as character
    Local cDFILIAL          as character
    Local cDTPSALDO         as character
    Local cDCLASSIF         as character
    Local cDTIPO            as character
    Local cCompanyName      as character
    Local cBranchName       as character
    Local cChave  := ""     as character
    Local nTipoTotal	    as numeric
    Local nQtdBem := 0      as numeric
    Local lFiscal      := .F. as logical
    Local lGerencial   := .F. as logical
    Local lIncentivada := .F. as logical

    //Carrega lista de parametros para a memória
    CTBsetValueMVPAR(oFilter:getParameters(), "AFR072")

    jParams := oFilter:getParameters()

    ::oIntegratedProvider := ControlIntegratedProvider():New()
    cCompanyName := ::oIntegratedProvider:getCompanyName()
    cBranchName  := ::oIntegratedProvider:getBranchName()

    //Seleção de filiais    
    ::oIntegratedProvider:lMultSelectBranches := .T.
    ::oIntegratedProvider:lAllowBranchFilter  := .T.
    ::oIntegratedProvider:setBranchPar(oFilter:getParameters())
    ::oIntegratedProvider:loadFilterBranches({"SN1"})

    If Valtype(::oIntegratedProvider:oFilterBranches["SN1"][1]) == "A"
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN1"][1]
    Else
        aSelFil := ::oIntegratedProvider:oFilterBranches["SN1"]
    EndIf

    If !Empty(jParams:SV_MOEDAS)
        If Valtype(jParams:SV_MOEDAS) == "A"
            If Valtype(jParams:SV_MOEDAS[1]) == "A"
                aSelMoed := jParams:SV_MOEDAS[1]
            Else
                aSelMoed := iif(!Empty(jParams:SV_MOEDAS), jParams:SV_MOEDAS, {"01"})
            EndIf
        EndIf
    EndIf

    If !Empty(jParams:SV_CLASSPATR)
        If Valtype(jParams:SV_CLASSPATR) == "A"
            If Valtype(jParams:SV_CLASSPATR[1]) == "A"
                aSelClass := jParams:SV_CLASSPATR[1]
            Else
                aSelClass := jParams:SV_CLASSPATR
            EndIf
        EndIf
    EndIf
    
    dDataSLD  := MV_PAR01
    
    If MV_PAR02 == 1        // Analítico
        cTitulo := STR0014 + STR0015
    ElseIf MV_PAR02 == 2    // Sintetico por Conta
        cTitulo := STR0014 + STR0016
    Else                    // Sintetico por Bem
        cTitulo := STR0014 + STR0017
    EndIf

    dAquIni		:= MV_PAR04
    dAquFim   	:= MV_PAR05
    cBemIni   	:= MV_PAR06
    cItemIni  	:= MV_PAR07
    cBemFim   	:= MV_PAR08
    cItemFim  	:= MV_PAR09
    cGrupoIni	:= MV_PAR10
    cGrupoFim	:= MV_PAR11
    cContaIni 	:= MV_PAR12
    cContaFim 	:= MV_PAR13
    cCCIni   	:= MV_PAR14
    cCCFim   	:= MV_PAR15
    cItCtbIni	:= MV_PAR16
    cItCtbFim	:= MV_PAR17
    cClvlIni	:= MV_PAR18
    cClVlFim	:= MV_PAR19
    nTipoTotal	:= MV_PAR24
    cTipoSLD	:= iif(Empty(MV_PAR25), "1", MV_PAR25)
    lRealProv   := MV_PAR27 == 1
    cBaixa      := cValToChar(MV_PAR28)

    lAgrupa     := .T.
    cChave		:= ""
    cTitulo     := ""

    cBaixa  := iif(Empty(cValToChar(MV_PAR28)),"1",cValToChar(MV_PAR28))

    //População de variáveis
    aTipos          := {} 
    If nTipoTotal == 1              // Fiscal
        aTipos := ATFXTpBem(1,.T.)
    ElseIf nTipoTotal == 2          // Gerencial
        aTipos := ATFXTpBem(2,.T.)
    ElseIf nTipoTotal == 3          // Incentivada
        aTipos := ATFXTpBem(3,.T.)
    EndIf

    cAliasTmp   := Criatrab(,.F.)
    //Verifica a existências de arquivos temporários.
    IIf(Select(cAliasTmp)>0,(cAliasTmp)->(DbCloseArea()),MSErase(cAliasTmp))

    //Monta tabela temporaria - Específico.
    ATFGERSLDM(,,,,@cAliasTmp,dAquIni,dAquFim,dDataSLD,cBemIni,cBemFim,cItemIni,cItemFim,cContaIni,cContaFim,;
                cCCIni,cCCFim,cItCtbIni,cItCtbFim,cClvlIni,cClVlFim,cGrupoIni,cGrupoFim,aSelMoed,aSelFil,;
                lTodasFil,cChave,lAgrupa,aTipos,Nil,cBaixa,cTipoSLD,aSelClass,lRealProv,)

    dbSelectArea(cAliasTmp)
    (cAliasTmp)->(dbGoTop())

    While (cAliasTmp)->(!Eof())
        //Salva o código do bem
        cBemBkp := (cAliasTmp)->CBASE
        //Descrição da filial
        cDFILIAL    := AllTrim(GetAdvFval("SM0","M0_FILIAL", cEmpAnt + (cAliasTmp)->FILIAL ))
        //Descrição do Tipos de Ativos
        SX5->(MsSeek(xFilial("SX5")+"G1"+(cAliasTmp)->TIPO ))
        cDTIPO      := Alltrim(SX5->(X5Descri()))
        cDCLASSIF   := self:X3ComboATF("N1_PATRIM",(cAliasTmp)->CLASSIF)
        //Descrição do Metodos de Depreciacao
        cDTPDEPR    := AlltRIM(GetAdvFVal("SN0","N0_DESC01",xFilial("SN0")+"04"+GetAdvFVal("SN3","N3_TPDEPR",(cAliasTmp)->(FILIAL+CBASE+ITEM+TIPO+FLAGBAIXA+SEQ))))

        //Descrição do tipo de Saldos Contábeis
        SX5->(MsSeek(xFilial("SX5")+"SL"+cTipoSLD))
        cDTPSALDO   := Alltrim(SX5->(X5Descri()))
  
        //Pega a descrição da conta
        cDescConta := Posicione("CT1", 1, xFilial("CT1")+(cAliasTmp)->CONTA, "CT1_DESC01")

        nQtdBem := 0
        If AllTrim(cChaveBem) != AllTrim((cAliasTmp)->FILIAL + (cAliasTmp)->CBASE + (cAliasTmp)->ITEM)
			cChaveBem := (cAliasTmp)->FILIAL + (cAliasTmp)->CBASE + (cAliasTmp)->ITEM
			nQtdBem += (cAliasTmp)->QUANTD
		EndIf

        //População de variáveis.
        ::jDados := JsonObject():new()

        ::jDados["FILIAL"]      := (cAliasTmp)->FILIAL
        ::jDados["DFILIAL"]     := cDFILIAL
        ::jDados["CBASE"]       := (cAliasTmp)->CBASE
        ::jDados["ITEM"]        := (cAliasTmp)->ITEM
        ::jDados["TIPO"]        := (cAliasTmp)->TIPO
        ::jDados["DTIPO"]       := cDTIPO
        ::jDados["DTPDEPR"]     := cDTPDEPR
        ::jDados["DCLASSIF"]    := cDCLASSIF
        ::jDados["DESC_SINT"]   := (cAliasTmp)->DESC_SINT
        ::jDados["AQUISIC"]     := IIf(!Empty((cAliasTmp)->AQUISIC),FwTimeStamp(5,(cAliasTmp)->AQUISIC,"00:00:00"),Nil)
        ::jDados["DTSALDO"]     := IIf(!Empty((cAliasTmp)->DTSALDO),FwTimeStamp(5,(cAliasTmp)->DTSALDO,"00:00:00"),Nil)
        ::jDados["TPSALDO"]     := Alltrim((cAliasTmp)->TPSALDO)
        ::jDados["DTPSALDO"]    := cDTPSALDO
        ::jDados["CONTA"]       := Alltrim((cAliasTmp)->CONTA)
        ::jDados["CONTA_M"]     := MascaraCTB(Alltrim((cAliasTmp)->CONTA))
        ::jDados["DESCCONTA"]   := Alltrim(cDescConta)
        ::jDados["CCUSTO"]      := Alltrim((cAliasTmp)->CCUSTO)
        ::jDados["SUBCTA"]      := Alltrim((cAliasTmp)->SUBCTA)
        ::jDados["CLVL"]        := Alltrim((cAliasTmp)->CLVL)
        ::jDados["SEQ"]         := Alltrim((cAliasTmp)->SEQ)
        ::jDados["SEQREAV"]     := Alltrim((cAliasTmp)->SEQREAV)
        ::jDados["CHAPA"]       := Alltrim((cAliasTmp)->CHAPA)
        ::jDados["GRUPO"]       := Alltrim((cAliasTmp)->GRUPO)
        ::jDados["MOEDA"]       := (cAliasTmp)->MOEDA
        ::jDados["QUANTD"]      := (cAliasTmp)->QUANTD
        ::jDados["ORIGINAL"]    := (cAliasTmp)->ORIGINAL
        ::jDados["AMPLIACAO"]   := (cAliasTmp)->AMPLIACAO
        ::jDados["ATUALIZ"]     := (cAliasTmp)->ATUALIZ
        ::jDados["DEPRECACM"]   := (cAliasTmp)->DEPRECACM
        ::jDados["RESIDUAL"]    := (cAliasTmp)->RESIDUAL
        ::jDados["CORRECACM"]   := (cAliasTmp)->CORRECACM
        ::jDados["CORDEPACM"]   := (cAliasTmp)->CORDEPACM
        ::jDados["DTBAIXA"]     := IIf(!Empty((cAliasTmp)->DTBAIXA),FwTimeStamp(5,(cAliasTmp)->DTBAIXA,"00:00:00"),Nil)
        ::jDados["FLAGBAIXA"]   := Alltrim((cAliasTmp)->FLAGBAIXA)

        //Não utilizados no relatorio
        ::jDados["CLASSIF"]     := (cAliasTmp)->CLASSIF
        ::jDados["VLBAIXAS"]    := (cAliasTmp)->VLBAIXAS
        ::jDados["VLVENDA"]     := (cAliasTmp)->VLVENDA

        ::jDados["NomeEmpresa"] := cCompanyName
        ::jDados["NomeFilial"]  := cBranchName
        ::jDados["DMOEDA1"]     := cDescMoeda01
        ::jDados["DMOEDA2"]     := cDescMoeda02
        ::jDados["DMOEDA3"]     := cDescMoeda03
        ::jDados["DMOEDA4"]     := cDescMoeda04
        ::jDados["DMOEDA5"]     := cDescMoeda05
        ::jDados["QTDBEM"]      := nQtdBem

        //Verifica se existe registros: Fiscal, Gerencial e Incentivada
        If !lFiscal .And. ((cAliasTmp)->TIPO $ cTipoFiscal)
            lFiscal := .T.
        EndIf

        If !lGerencial .And. ((cAliasTmp)->TIPO $ cTipoGerenc)
            lGerencial := .T.
        EndIf

        If !lIncentivada .And. ((cAliasTmp)->TIPO $ cTipoIncent)
            lIncentivada := .T.
        EndIf

        //Usado no design
        ::jDados["LFISCAL"]     := lFiscal
        ::jDados["LGERENCIAL"]  := lGerencial
        ::jDados["LINCENTIVADA"]:= lIncentivada

        self:processData()
        self:oData:appendData(::jDados)

        (cAliasTmp)->(dbSkip())

        If cBemBkp <> (cAliasTmp)->CBASE
            lFiscal      := .F.
            lGerencial   := .F.
            lIncentivada := .F.
        EndIf

    EndDo
    
    (cAliasTmp)->(DbCloseArea())

    FreeObj(jParams)
    FreeObj(::jDados)
    FreeObj(::oIntegratedProvider)

return self:oData

//-------------------------------------------------------------------
/*{Protheus.doc} getSchema
Prepara a estrutura dos campos
@param aCpos array: Array com os campos do relatório
@return array: Array com a estrutura dos campos
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
*/
//------------------------------------------------------------------- 
method getSchema() as object class ValuedPositionDateSmartViewBusinessObject

    Local lCanFilter := .F. as logical

    //Estrutura dos campos
    //Linha 1
    self:oSchema:addProperty( "FILIAL"	     , RetTitle("N3_FILIAL")   , "string"      , RetTitle("N3_FILIAL")     , "FILIAL"     ,,,,lCanFilter )
    self:oSchema:addProperty( "DFILIAL"	     , STR0027                 , "string"      , STR0027                   , "DFILIAL"    ,,,,lCanFilter ) //"Descr. Filial Registro"
    self:oSchema:addProperty( "CBASE" 	     , RetTitle("N3_CBASE")    , "string"      , RetTitle("N3_CBASE")      , "CBASE"      ,,,,lCanFilter )
    self:oSchema:addProperty( "ITEM"  	     , RetTitle("N3_ITEM")     , "string"      , RetTitle("N3_ITEM")       , "ITEM"       ,,,,lCanFilter )
    self:oSchema:addProperty( "TIPO"	     , RetTitle("N3_TIPO")     , "string"      , RetTitle("N3_TIPO")       , "TIPO"       ,,,,lCanFilter )
    self:oSchema:addProperty( "DTIPO"	     , STR0028                 , "string"      , STR0028                   , "DTIPO"      ,,,,lCanFilter ) //"Descr. Tipo Ativo"
    self:oSchema:addProperty( "DTPDEPR"	     , STR0029                 , "string"      , STR0029                   , "DTPDEPR"    ,,,,lCanFilter ) //"Descr. Tipo Depr."
    self:oSchema:addProperty( "DCLASSIF"     , RetTitle("N1_PATRIM")   , "string"      , RetTitle("N1_PATRIM")     , "DCLASSIF"   ,,,,lCanFilter )
    self:oSchema:addProperty( "DESC_SINT"    , RetTitle("N1_DESCRIC")  , "string"      , RetTitle("N1_DESCRIC")    , "DESC_SINT"  ,,,,lCanFilter )
    self:oSchema:addProperty( "AQUISIC"      , RetTitle("N1_AQUISIC")  , "date"        , RetTitle("N1_AQUISIC")    , "AQUISIC"    ,,,,lCanFilter )
    self:oSchema:addProperty( "DTSALDO"      , RetTitle("N4_DATA")     , "date"        , RetTitle("N4_DATA")       , "DTSALDO"    ,,,,lCanFilter )
    self:oSchema:addProperty( "TPSALDO"      , RetTitle("N3_TPSALDO")  , "string"      , RetTitle("N3_TPSALDO")    , "TPSALDO"    ,,,,lCanFilter )
    self:oSchema:addProperty( "DTPSALDO"     , STR0030                 , "string"      , STR0030                   , "DTPSALDO"   ,,,,lCanFilter ) //"Descr. Tipo Saldo"
    self:oSchema:addProperty( "GRUPO"	     , STR0031                 , "string"      , STR0031                   , "GRUPO"	  ,,,,lCanFilter ) //"Grupo"
    self:oSchema:addProperty( "CONTA"	     , RetTitle("N3_CCONTAB")  , "string"      , RetTitle("N3_CCONTAB")    , "CONTA"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "CONTA_M"	     , STR0018                 , "string"      , STR0018                   , "CONTA_M"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "DESCCONTA"    , STR0019                 , "string"      , STR0019                   , "DESCCONTA"  ,,,,lCanFilter )
    self:oSchema:addProperty( "CCUSTO"	     , RetTitle("N3_CUSTBEM")  , "string"      , RetTitle("N3_CUSTBEM")    , "CCUSTO"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "SUBCTA"	     , RetTitle("N3_SUBCCON")  , "string"      , RetTitle("N3_SUBCCON")    , "SUBCTA"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "CLVL"	     , RetTitle("N3_CLVLCON")  , "string"      , RetTitle("N3_CLVLCON")    , "CLVL"	      ,,,,lCanFilter )
    self:oSchema:addProperty( "SEQ"          , STR0032                 , "string"      , STR0032                   , "SEQ"	      ,,,,lCanFilter ) //"Sequência"
    self:oSchema:addProperty( "SEQREAV"	     , STR0033                 , "string"      , STR0033                   , "SEQREAV"	  ,,,,lCanFilter ) //"Sequência Reavaliação"
    self:oSchema:addProperty( "CHAPA"	     , RetTitle("N1_CHAPA")    , "string"      , RetTitle("N1_CHAPA")      , "CHAPA"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "DTBAIXA"      , RetTitle("N1_BAIXA")    , "date"        , RetTitle("N1_BAIXA")      , "DTBAIXA"    ,,,,lCanFilter )
    self:oSchema:addProperty( "FLAGBAIXA"    , STR0034                 , "string"      , STR0034                   , "FLAGBAIXA"  ,,,,lCanFilter ) //"Flag da Baixa"
    self:oSchema:addProperty( "MOEDA" 	     , STR0004                 , "string"      , STR0004                   , "MOEDA" 	  ,,,,lCanFilter )
    self:oSchema:addProperty( "QUANTD"	     , RetTitle("N1_QUANTD")   , "number"      , RetTitle("N1_QUANTD")     , "QUANTD"	  ,,,,lCanFilter )
    self:oSchema:addProperty( "ORIGINAL"     , STR0005                 , "number"      , STR0005                   , "ORIGINAL"   ,,,,lCanFilter )
    self:oSchema:addProperty( "AMPLIACAO"    , STR0006                 , "number"      , STR0006                   , "AMPLIACAO"  ,,,,lCanFilter )
    self:oSchema:addProperty( "ATUALIZ"	     , STR0007                 , "number"      , STR0007                   , "ATUALIZ"    ,,,,lCanFilter )
    self:oSchema:addProperty( "DEPRECACM"    , STR0008                 , "number"      , STR0008                   , "DEPRECACM"  ,,,,lCanFilter )
    self:oSchema:addProperty( "RESIDUAL"     , STR0009                 , "number"      , STR0009                   , "RESIDUAL"   ,,,,lCanFilter )
    self:oSchema:addProperty( "CORRECACM"    , STR0010                 , "number"      , STR0010                   , "CORRECACM"  ,,,,lCanFilter )
    self:oSchema:addProperty( "CORDEPACM"    , STR0011                 , "number"      , STR0011                   , "CORDEPACM"  ,,,,lCanFilter )
    
    //Não utilizados no relatorio
    self:oSchema:addProperty( "CLASSIF"      , STR0035                 , "string"      , STR0035                   , "CLASSIF"    ,,,,lCanFilter ) //"Classificação"
    self:oSchema:addProperty( "VLBAIXAS"     , STR0012                 , "number"      , STR0012                   , "VLBAIXAS"   ,,,,lCanFilter )
    self:oSchema:addProperty( "VLVENDA"      , STR0013                 , "number"      , STR0013                   , "VLVENDA"    ,,,,lCanFilter )

    self:oSchema:addProperty("NomeEmpresa"   , STR0020                 , "string"      , STR0020                   , "NomeEmpresa",,,,lCanFilter ) //"Nome Empresa"
    self:oSchema:addProperty("NomeFilial"    , STR0021                 , "string"      , STR0021                   , "NomeFilial" ,,,,lCanFilter ) //"Nome Filial"
    self:oSchema:addProperty("DMOEDA1"       , STR0022                 , "string"      , STR0022                   , "DMOEDA1"    ,,,,lCanFilter ) //"Descrição Moeda 01"
    self:oSchema:addProperty("DMOEDA2"       , STR0023                 , "string"      , STR0023                   , "DMOEDA2"    ,,,,lCanFilter ) //"Descrição Moeda 02"
    self:oSchema:addProperty("DMOEDA3"       , STR0024                 , "string"      , STR0024                   , "DMOEDA3"    ,,,,lCanFilter ) //"Descrição Moeda 03"
    self:oSchema:addProperty("DMOEDA4"       , STR0025                 , "string"      , STR0025                   , "DMOEDA4"    ,,,,lCanFilter ) //"Descrição Moeda 04"
    self:oSchema:addProperty("DMOEDA5"       , STR0026                 , "string"      , STR0026                   , "DMOEDA5"    ,,,,lCanFilter ) //"Descrição Moeda 05"
    
    self:oSchema:addProperty("QTDBEM"        , STR0039                 , "number"      , STR0039                   , "QTDBEM"     ,,,,lCanFilter ) //"Qtd. Bem Agrupada"
    self:oSchema:addProperty("LFISCAL"       , STR0040                 , "boolean"     , STR0040                   , "LFISCAL"    ,,,,lCanFilter ) //"Exibe Fiscal"
    self:oSchema:addProperty("LGERENCIAL"    , STR0041                 , "boolean"     , STR0041                   , "LGERENCIAL"  ,,,,lCanFilter) //"Exibe Gerencial"
    self:oSchema:addProperty("LINCENTIVADA"  , STR0042                 , "boolean"     , STR0042                   , "LINCENTIVADA",,,,lCanFilter) //"Exibe Incentivada"

    //Seleciona filiais
    self:oSchema:addParameter("SV_MULTBRANCH", STR0036, "string", .T.) //Filiais
    self:setCustomURL("SV_MULTBRANCH","/api/controladoria/smartview/v1/options/getBranches",2)

    //Moedas
    self:oSchema:addParameter("SV_MOEDAS", STR0037, "string", .T.) //Moeda ?
    self:setCustomURL("SV_MOEDAS", "/api/controladoria/smartview/v1/options/getCoins", 2)

    //Seleciona Classificação Patrimonial
    self:oSchema:addParameter("SV_CLASSPATR", STR0038, "string", .T.) //Classif. Patrimonial
    self:setCustomURL("SV_CLASSPATR", "/api/controladoria/smartview/v1/options/getClassification", 2)

return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preSchema
Executa a query conforme parametrizacao
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preSchema() as object class ValuedPositionDateSmartViewBusinessObject
return self:oSchema

//-------------------------------------------------------------------
/*/{Protheus.doc} preData
Metodo que consumido pelo M.I. - Ajusta o Json conforme alteracao no processData e preSchema
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method preData() as object class ValuedPositionDateSmartViewBusinessObject
return self:oData

//-------------------------------------------------------------------
/*/{Protheus.doc} processData
Metodo que  consumido pelo M.I. - Usado para alterar a Query antes do getData.
@author         Controladoria
@since          NOV/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method processData() class ValuedPositionDateSmartViewBusinessObject
return ::jDados

//-------------------------------------------------------------------
/*/{Protheus.doc} X3ComboATF
Busca a opção do combo
@author         Renato July
@since          NOV/2023
@version        12.1.2310
/*/
//-------------------------------------------------------------------
method X3ComboATF(cCampo as character, cCombo as character) as character class ValuedPositionDateSmartViewBusinessObject

    Local cDescription	:= "" as character
    Local nPosicao1		:= 0  as numeric

    Default cCampo      := ""
    Default cCombo      := ""

    If Empty(self:aComboATF)
        self:aComboATF := GetComboATF(cCampo, cCombo)
    Endif

    nPosicao1 := aScan(self:aComboATF,{|x| AllTrim(x[1]) == AllTrim(cCombo)})

    If nPosicao1 <> 0
        cDescription := AllTrim(self:aComboATF[nPosicao1][2])
    EndIf

return cDescription
